/*
$Name:               mtmodifyrecip.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции изменения срочного перевода
*/
import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpModifyRecipient( p_opNum, p_subOpNum, p_psId, p_transCode )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;
  private var transCode = p_transCode;

  private var vNeedCloseAccount = 0;
  private var vRecAccRef = 0;
  private var vRecTypeAccount = "";
  private var vRecAccount = "";
  private var vRecCodClient = 0;
  private var vReferenc = 0;

  private var clientForDepAcc = 0;

  private var isNeedCreateDocs = false;

  private var numRecBranch = null;

  private var rmto_trans_new = TRecHandler( "rmto_trans.dbt" );

  if ( opNum == 0 )
    opNum = MTSUBOP_modify_recip;
    subOpNum = 0;
  end;

  // =====================================================================
  macro startInit( )

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

    return true;
  end;

  // =====================================================================
  macro initTransfer( )

    var stat = true;
    var prevState = transfer.rec.state;

    opHistory.rec.transId = transfer.rec.transId;

    transfer.rec.state = "ABORTED___";
    transfer.rec.action = D_UPDATE;
    
    stat = transfer.save( true ); // С созданием нефинансовой операции

    if ( stat )
      transfer.rec.branch = numRecBranch;
      transfer.rec.transId = 0;
      transfer.rec.state = prevState;
      transfer.rec.accRef = vReferenc;

      stat = initTransfer( );
    end;

    return true;
  end;

  // =====================================================================
  macro defNumRecBranch( )

    if ( ( rmto_trans_new.rec.receivePointCode == transfer.rec.receivePointCode ) or ( rmto_trans_new.rec.receivePointCode == "" ) )
      return transfer.rec.Branch;
    else
      return null;
    end;   
  end;

  // =====================================================================
  macro processClientInfo : bool

    var depclnt = TClientList;
    var stat = true;
    var prevRecPartyID = transfer.rec.recPartyID;

    if ( ( rmto_trans_new.rec.recName1               == transfer.rec.recName1            ) and
         ( rmto_trans_new.rec.recName2               == transfer.rec.recName2            ) and
         ( rmto_trans_new.rec.recName3               == transfer.rec.recName3            ) and
         ( rmto_trans_new.rec.recBorn                == transfer.rec.recBorn             ) and
         ( rmto_trans_new.rec.recCitizenship         == transfer.rec.recCitizenship      ) and
         ( rmto_trans_new.rec.recNotResident         == transfer.rec.recNotResident      ) and
         ( rmto_trans_new.rec.recPaperKind           == transfer.rec.recPaperKind        ) and
         ( rmto_trans_new.rec.recPaperSeries         == transfer.rec.recPaperSeries      ) and
         ( rmto_trans_new.rec.recPaperNumber         == transfer.rec.recPaperNumber      ) and
         ( rmto_trans_new.rec.recPaperIssuedDate     == transfer.rec.recPaperIssuedDate  ) and
         ( rmto_trans_new.rec.recPaperIssuer         == transfer.rec.recPaperIssuer      ) and
         ( rmto_trans_new.rec.recPaperIssuerCode     == transfer.rec.recPaperIssuerCode  ) and
         ( rmto_trans_new.rec.recPaperExpiratDate    == transfer.rec.recPaperExpiratDate ) and
         ( rmto_trans_new.rec.recCountry             == transfer.rec.recCountry          ) and
         ( rmto_trans_new.rec.recPostIndex           == transfer.rec.recPostIndex        ) and
         ( rmto_trans_new.rec.recRegion              == transfer.rec.recRegion           ) and
         ( rmto_trans_new.rec.recPlace               == transfer.rec.recPlace            ) and
         ( rmto_trans_new.rec.recAdress              == transfer.rec.recAdress           ) and
         ( rmto_trans_new.rec.recPhoneNumber         == transfer.rec.recPhoneNumber      ) and
         ( rmto_trans_new.rec.recProperties          == transfer.rec.recProperties       ) and
         ( rmto_trans_new.rec.recTypeAccount         == transfer.rec.recTypeAccount      ) and
         ( rmto_trans_new.rec.recAccount             == transfer.rec.recAccount          ) and
         ( rmto_trans_new.rec.recCountryCode         == transfer.rec.recCountryCode      ) and
         ( rmto_trans_new.rec.recPlaceName           == transfer.rec.recPlaceName        ) and
         ( rmto_trans_new.rec.recAddInfo             == transfer.rec.recAddInfo          )    )
      return stat;
    end;  

    transfer.rec.recName1            = rmto_trans_new.rec.recName1;
    transfer.rec.recName2            = rmto_trans_new.rec.recName2;
    transfer.rec.recName3            = rmto_trans_new.rec.recName3;
    transfer.rec.recBorn             = rmto_trans_new.rec.recBorn;
    transfer.rec.recCitizenship      = rmto_trans_new.rec.recCitizenship;
    transfer.rec.recNotResident      = rmto_trans_new.rec.recNotResident;
    transfer.rec.recPaperKind        = rmto_trans_new.rec.recPaperKind;
    transfer.rec.recPaperSeries      = rmto_trans_new.rec.recPaperSeries;
    transfer.rec.recPaperNumber      = rmto_trans_new.rec.recPaperNumber;
    transfer.rec.recPaperIssuedDate  = rmto_trans_new.rec.recPaperIssuedDate;
    transfer.rec.recPaperIssuer      = rmto_trans_new.rec.recPaperIssuer;
    transfer.rec.recPaperIssuerCode  = rmto_trans_new.rec.recPaperIssuerCode;
    transfer.rec.recPaperExpiratDate = rmto_trans_new.rec.recPaperExpiratDate;
    transfer.rec.recCountry          = rmto_trans_new.rec.recCountry;
    transfer.rec.recPostIndex        = rmto_trans_new.rec.recPostIndex;
    transfer.rec.recRegion           = rmto_trans_new.rec.recRegion;
    transfer.rec.recPlace            = rmto_trans_new.rec.recPlace;
    transfer.rec.recAdress           = rmto_trans_new.rec.recAdress;
    transfer.rec.recPhoneNumber      = rmto_trans_new.rec.recPhoneNumber;
    transfer.rec.recProperties       = rmto_trans_new.rec.recProperties;
    transfer.rec.recTypeAccount      = rmto_trans_new.rec.recTypeAccount;
    transfer.rec.recAccount          = rmto_trans_new.rec.recAccount;
    transfer.rec.recCountryCode      = rmto_trans_new.rec.recCountryCode;
    transfer.rec.recPlaceName        = rmto_trans_new.rec.recPlaceName;
    transfer.rec.recAddInfo          = rmto_trans_new.rec.recAddInfo;

    if ( transfer.rec.recPaperNumber != "" )
      transfer.rec.recPartyID = depclnt.FindCodeByPaper( transfer.rec.recPaperKind, transfer.rec.recPaperSeries, transfer.rec.recPaperNumber );
    else
      transfer.rec.recPartyID = 0;
    end;

    // Клиент по документу не найден, заведем нового -------------------
    if ( transfer.rec.recPartyID == 0 )
      depclnt.CurRec.Clear( );
  
      depclnt.CurRec.rec.CodClient = 0;
  
      depclnt.CurRec.rec.Name1 = transfer.rec.recName1;
      depclnt.CurRec.rec.Name2 = transfer.rec.recName2;
      depclnt.CurRec.rec.Name3 = transfer.rec.recName3;
  
      depclnt.CurRec.rec.Born = transfer.rec.recBorn;
  
      if ( transfer.rec.recNotResident != 0 )
        depclnt.CurRec.rec.NotResident = "X";
      end;
      depclnt.CurRec.rec.NRCountry = transfer.rec.recCitizenship;
  
      if ( ( transfer.rec.recPaperSeries == "" ) and ( transfer.rec.recPaperNumber == "" ) )
        depclnt.CurRec.rec.PaperKind = -1;
      else
        depclnt.CurRec.rec.PaperKind       = transfer.rec.recPaperKind;
        depclnt.CurRec.rec.PaperSeries     = transfer.rec.recPaperSeries;
        depclnt.CurRec.rec.PaperNumber     = transfer.rec.recPaperNumber;
        depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
        depclnt.CurRec.rec.PaperIssuer     = transfer.rec.recPaperIssuer;
        depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.recPaperIssuerCode;
        depclnt.CurRec.rec.IsMain          = "X";
      end;
          
      if ( ( transfer.rec.recCountry == "" ) and ( transfer.rec.recPostIndex == "" ) and ( transfer.rec.recAdress == "" ) and ( transfer.rec.recPhoneNumber == "" ) )
        ;
      else
        depclnt.CurRec.rec.AdressType  = 1;
        depclnt.CurRec.rec.Country     = transfer.rec.recCountry;
        depclnt.CurRec.rec.PostIndex   = transfer.rec.recPostIndex;
        depclnt.CurRec.rec.CodeRegion  = transfer.rec.recRegion;
        depclnt.CurRec.rec.Place       = transfer.rec.recPlaceName;
        depclnt.CurRec.rec.lCodePlace  = transfer.rec.recPlace;
        depclnt.CurRec.rec.Address     = transfer.rec.recAdress;
        depclnt.CurRec.rec.PhoneNumber = transfer.rec.recPhoneNumber;
      end;

      depclnt.CurRec.rec.FNcash = transfer.rec.branch;
      
      depclnt.MayInsert = true;

      stat = depclnt.Insert( );

      if ( stat )
        transfer.rec.recPartyID = depclnt.CurRec.rec.CodClient;
      else
        PushErrorMessage( "Ошибка при вставке нового клиента" );
      end;
    end;

    if ( stat and ( transfer.rec.recPartyID != prevRecPartyID ) )
      isNeedCreateDocs = true;
    end;

    return stat;
  end;

  // =====================================================================
  macro check( )

    var cmd;
    var rs;
    var stat = false;

    Copy( rmto_trans_new, transfer.getRec( ) );

    // Найдем перевод --------------------------------------------------
    cmd = RsdCommand( "select rmto_trans.t_branch as branch, " +
                             "rmto_trans.t_transId as transId " +
                      "from drmto_trans_dbt rmto_trans " +
                      "where rmto_trans.t_branch = :branch and " +
                            "rmto_trans.t_psId = :psId and " +
                            "rmto_trans.t_transCode = :transCode and " +
                            "rmto_trans.t_ourFlag = 0 " +
                            "order by rmto_trans.t_sendDate desc" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "branch", RSDBP_IN, NumFNcash( ) );
    cmd.addParam( "psId", RSDBP_IN, psId );
    cmd.addParam( "transCode", RSDBP_IN, transCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      stat = transfer.load( rs.value( "branch" ), rs.value( "transId" ) );
      if ( not stat )
        PushErrorMessage( "Ошибка при загрузке срочного перевода с уникальным идентификатором " + String( rs.value( "transId" ) ) );
      end;
    else
      PushErrorMessage( "Не найден перевод c идентификатором \"" + transCode + "\"" );
    end;

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag != 0 )
        stat = false;
        PushErrorMessage( "Операция для перевода c идентификатором \"" + transfer.rec.transCode + "\" невозможна: перевод должен быть принят в не \"нашем\" банке" );
      end;
    end;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'ONSET_____' or " +
                                "rmtr_state.t_equivalentCode = 'ONSET_____' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Операция для перевода c идентификатором \"" + transfer.rec.transCode + "\" невозможна: перевод должен быть в состоянии \"К выплате\"" );
      end;
    end;

    // Заведем или обновим клиента в базе ------------------------------
    if ( stat )
      stat = processClientInfo( );
    end;

    // Определим номер подразделения -----------------------------------
    if ( stat )
      numRecBranch = defNumRecBranch( );

      if ( ValType( numRecBranch ) == V_UNDEF )
        stat = false;
        PushErrorMessage( "Невозможно определить подразделение для точки получения \"" + rmto_trans_new.rec.receivePointCode + "\"" );
      elif ( numRecBranch != transfer.rec.branch )
        isNeedCreateDocs = true;
      end;
    end;

    // Также может поменяться пароль и ряд другой информации -----------
    if ( stat )
      transfer.rec.password = rmto_trans_new.rec.password;
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 4 );
    var doc;
    var stat = true;
    var saveNumBranch = 0;
    var cmd, rs;

    var vAccount = "";
    var vTypeAccount = "";
    var vNeedOpenAccount = 0;

    if ( not isNeedCreateDocs )
      return stat;
    end;

    vNeedCloseAccount = 0;
    vRecAccRef = transfer.rec.accRef;
    vRecTypeAccount = "";
    vRecAccount = "";
    vRecCodClient = 0;

    clientForDepAcc = defCodeServiceClient( false );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.recPartyID;
    end;

    if ( vRecAccRef != 0 )
      depositr.KeyNum = 8;
      depositr.Clear;
      depositr.rec.Referenc = vRecAccRef;

      stat = depositr.GetEQ( );

      if ( stat )
        vRecAccount = depositr.rec.Account;
        vRecTypeAccount = depositr.rec.Type_Account;
        vRecCodClient = depositr.rec.CodClient;
      else
        PushErrorMessage( "Счет вкладчика с референсом " + String( vRecAccRef ) + " не найден" );
      end;
    else
      stat = false;
      PushErrorMessage( "У перевода c идентификатором \"" + transfer.rec.transCode + "\" нет ссылки на счет вкладчика" );
    end;

    if ( stat )
      vTypeAccount = determineTypeAccount( true, transfer.rec.curCode );
    end;

    if ( stat )
      if ( system.rec.closeAccFlag == "" )
        cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                          "from ddepositr_dbt depositr "
                          "where depositr.t_CodClient = ? and "
                                "depositr.t_FNCash = ? and "
                                "depositr.t_Open_Close = chr( 0 ) and "
                                "depositr.t_Type_Account = ? and "
                                "depositr.t_Code_Currency = ? and "
                                "depositr.t_Action != 2 "
                          "order by depositr.t_CodClient" );
                                    
        cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
        cmd.addParam( "FNCash", RSDBP_IN, numRecBranch );
        cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
        cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

        rs = RsdRecordSet( cmd );

        cmd.Execute( );

        if ( rs.moveNext )
          vAccount = rs.value( "Account" );
          vReferenc = rs.value( "Referenc" );
        else
          vAccount = "";
        end;
      end;
    end;

    if ( stat )
      if ( system.rec.closeAccFlag != "" )
        vNeedOpenAccount = 1;
        vNeedCloseAccount = 1;
      else
        vNeedCloseAccount = 0;

        if ( vAccount != "" )
          vNeedOpenAccount = 0;
        else
          vNeedOpenAccount = 1;
        end;
      end;
    end;
     
    // Если нужно открыть вкладной счет, откроем его -------------------
    if ( stat and ( vNeedOpenAccount == 1 ) )
      saveNumBranch = SetFNcash( numRecBranch );

      depositr.Clear;
      depositr.rec.FNCash        = numRecBranch;
      depositr.rec.Type_Account  = vTypeAccount;
      depositr.rec.Code_Currency = transfer.rec.curCode;
      depositr.rec.CodClient     = clientForDepAcc;
      depositr.rec.Open_Date     = transfer.rec.sendDate;
      if ( depositr.rec.Open_Date == Date( 0, 0, 0 ) )
        depositr.rec.Open_Date = {curdate};
      end;

      if ( НовыйСчетБезДокументов( depositr, 1 ) != 0 )
        stat = false;
        PushErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
      else
        vReferenc = depositr.rec.Referenc;
        vAccount = depositr.rec.Account;
      end;

      SetFNcash( saveNumBranch );
    end;

    // Открытие нового служебного счета-получателя ---------------------
    if ( stat and ( vNeedOpenAccount != 0 ) )
      doc = newDepDoc( 1 );

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = 0;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Срочный перевод. Изменение в точке получения. Код перевода " + transfer.rec.transCode + ". Открытие служебного счета";

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Перевод денег со старого на новый счет --------------------------
    if ( stat )
      doc = newDepDoc( 2, "sbdepdoc.1" );

      doc.rec.Referenc = vRecAccRef;
      doc.rec.Type_Account = vRecTypeAccount;
      doc.rec.Account = vRecAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.ApplType = 0;
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = transfer.rec.sum;
      doc.rec.Action = D_INSERT;                                                            
      doc.rec.Ground = "Срочный перевод. Изменение в точке получения. Код перевода " + transfer.rec.transCode + ". Списание срочного перевода";
      doc.rec.CorrAcc_Referenc = vReferenc;
      doc.rec.FNcash_Receiver = numRecBranch;

      initClientInfoInDepDoc( doc, false, vRecCodClient );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Перевод денег со старого на новый счет - ответный ---------------
    if ( stat )
      doc = newDepDoc( 2, "sbdepdoc.1" );

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.ApplType = 11;
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Срочный перевод. Изменение в точке получения. Код перевода " + transfer.rec.transCode + ". Списание срочного перевода";
      doc.rec.CorrAcc_Referenc = vRecAccRef;
      doc.rec.FNcash_Receiver = transfer.rec.Branch;

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Закрытие старого служебного счета-получателя --------------------
    if ( stat )
      if ( vNeedCloseAccount == 1 )
        ; // Производится в транзакции функцией Выполнение_Операции( ... )
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    record operParm ( "oper_prm.1" );
    record depDoc ( sbdepdoc );

    var stat = true;

    if ( stat and ( vNeedCloseAccount == 1 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vRecAccount;
      operParm.Type_Account = vRecTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vRecAccRef;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vRecAccount;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vRecTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = vRecCodClient;
      depDoc.FlagRezid = StrFor( transfer.rec.recNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      SetBitFlag( depDoc.Flags2, 7, 1 ); // BIT_FLAG2_MONTRANS
      depDoc.Ground = "Срочный перевод. Изменение в точке получения. Код перевода " + transfer.rec.transCode + ". Закрытие служебного счета";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro runPanel( )

    setFlagOperationPanel( false );

    return true;
  end;

  // =====================================================================
  macro errorMessage( )
    ;
  end;

  // =====================================================================
  InitTMtOperation( p_psId );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opModifyRecipient = TMtOpModifyRecipient( opNum, subOpNum, psId, "" ); 

  return opModifyRecipient;
end;
