/*
$Name:             mtcng_oppayout.mac
$Module:           RS-Retail
$Description:      Выдача перевода ПС Contact NG
*/

import mtpayout, mtcntng_webserv, mtcng_service, mtcng_view;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TCNGTransfer ) TReqPayIncoming

  class ( TRequestAttributesBase ) A
    var DOC_ID : integer;
    var USER_ID : integer;
    var LANG : string = "RU";

    InitTRequestAttributesBase( "TMoneyOrderObject", "PayIncoming" ); 
  end;

  InitTCNGTransfer;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespPayIncoming

  class ( TResponseAttributesBase ) A
    ;
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOpPayout ) TMtCngOpPayout( p_opNum, p_subOpNum, p_psId, p_initClient )

  // =====================================================================
  InitTMtOpPayout( p_opNum, p_subOpNum, p_psId, p_initClient );

  // =====================================================================
  macro Configure( )

    var i;
    var j;
    var stat = true;

    // Закрытие всех полей от редактирования
    for ( i, 0, operPanel.numForms - 1 )

      if ( i == MT_TOPAYOUT_PANEL )
        continue;
      end;

      for ( j, 0, operPanel.getForm( i ).numControls - 1 )

        if ((i != MT_RECEIVER_PANEL) or (operPanel.getForm( i ).getControl( j ).name != "agent"))
          if ( operPanel.getForm( i ).getControl( j ).editable )
            operPanel.getForm( i ).getControl( j ).editable = false;
          end;
          operPanel.getForm( i ).getControl( j ).focusable = false;
        end;
      end;
    end;

    // Дополнительные действия для основной панели
    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "recKindCode" ).value = "Код ПС Contact";

      operPanel.getForm( MT_OPER_PANEL ).getControl( "insideTheCountry" ).enabled = false;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "recNPflag" ).enabled = false;
    end;

    // Дополнительные действия для панели плательщика
    if ( operPanel.numForms > MT_PAYER_PANEL )
      operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true;
    end;

    // Дополнительные действия для панели получателя
    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      //operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "agent" ).editable = true;
      //operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "agent" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "born" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "born" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "bornPlace" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "bornPlace" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "notResident" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "residentship" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperKind" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperSeries" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperSeries" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperNumber" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperNumber" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuedDate" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuedDate" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuer" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuer" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuerCode" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuerCode" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperExpirationDate" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperExpirationDate" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "country" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "postIndex" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "postIndex" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "region" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "region" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "place" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "place" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "address" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "address" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "phoneNumber" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "phoneNumber" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "eMail" ).editable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "eMail" ).focusable = true;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).editable = false;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).focusable = true;
    end;

    // Дополнительные действия для дополнительной панели
    if ( operPanel.numForms > MT_AUX_PANEL )
      if ( operPanel.getForm( MT_AUX_PANEL ).numControls > 0 )
        operPanel.getForm( MT_AUX_PANEL ).getControl( 0 ).focusable = true;
      end;
    end;
 
    return stat;
  end;

  // =====================================================================
  macro SelectPayment( )

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 1 );

    var stat = true;
    var id = 0;

    if ( stat )
      id = SelectIncomingPayment( );

      if ( id == 0 )
        stat = false;
      end;
    end;

    if ( stat )
      stat = GetPaymentData( id, transfer );
    end;

    if ( stat )
      cng_banks.KeyNum = 1;
      cng_banks.Clear( );
      cng_banks.rec.Erased = 0;
      cng_banks.rec.PP_CODE = transfer.rec.receivePointCode;

      if ( cng_banks.GetEQ( ) )
        transfer.rec.recBankName = cng_banks.rec.Name;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro createPanel( )

    ;
  end;

  // =====================================================================
  macro startInit( )

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 1 );
    var stat = true;
    var tmpSum;
    var nominal;

    ResetErrorMessage( );

    if ( stat )
      if ( checkPing( true ) != 0 )
        stat = false;
      end;
    end;

    if ( stat )
      handInput = true;
      stat = startInit( );
      handInput = false;
    end;

    // Ввод идентификатора платежа
    if ( stat )
      transfer.rec.recBankCodeKind = 0;

      stat = SelectPayment( );
    end;

    // Определим тип отправителя
    if ( stat )

      transfer.rec.senderNPflag = 1;

      cng_banks.KeyNum = 1;
      cng_banks.Clear( );
      cng_banks.rec.Erased = 0;
      cng_banks.rec.PP_CODE = transfer.rec.sendPointCode;

      if ( cng_banks.GetEQ( ) )
        if ( cng_banks.rec.Contact == 5 )
          transfer.rec.senderNPflag = 0;
        end;
      end;
    end;

    if ( stat )

      if ( operPanel.numForms > MT_TOPAYOUT_PANEL )
        calcRates( );

        tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

        nominal = utils.getMinNominal( transfer.rec.curCode );

        tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

        if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > tmpSum )
          operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
        end;
      end;

      calcConcernedFields( );
    end;

    if ( stat )
      panel = TMtCngOperPanel( this );
      panel.Configure( );

      Configure( );
    end;

    transfer.rec.senderIdentType = defNumIdentClient( );
    transfer.rec.recIdentType = defNumIdentClient( );

    return stat;
  end;

  // =====================================================================
  private macro getPaperName( paperKind )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_name as nameKind "
                      "from dpaprkind_dbt "
                      "where t_paperkind = :paperKind" );

    cmd.addParam( "paperKind", RSDBP_IN, paperKind );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "nameKind" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaper( sr, num )

    return Trim( sr + " " + num );
  end;

  // =====================================================================
  private macro getCodeLat2( inCode )

    var cmd;
    var rs;
    var outCode = inCode;

    if ( StrLen( inCode ) == 3 )
      cmd = RsdCommand( "select t_codelat2 as codeLat2 "
                        "from dcountry_dbt "
                        "where t_codelat3 = :codeLat3" );

      cmd.addParam( "codeLat3", RSDBP_IN, inCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
    
      if ( rs.moveNext( ) )
        outCode = rs.value( "codeLat2" );
      end;
    end;

    return outCode;
  end;

  // =====================================================================
  macro finalInit( )

    var codeClient = 0;
    var depclnt = TClientList;
    var selectCode;
    var stat = true;

    if ( stat )
      handInput = true;
      stat = finalInit( );
      handInput = false;
    end;

    if ( stat )
      while ( true )
        selectCode = SelectUniqClient( codeClient, transfer.rec.recName1, transfer.rec.recName2, null, transfer.rec.recBorn );

        if ( selectCode == 0 )
          transfer.rec.recPartyID = codeClient;
          break;
        elif ( selectCode > 0 )
          depclnt.CurRec.Clear( );

          depclnt.CurRec.rec.CodClient = 0;

          depclnt.CurRec.rec.Name1 = transfer.rec.recName1;
          depclnt.CurRec.rec.Name2 = transfer.rec.recName2;
          depclnt.CurRec.rec.Name3 = transfer.rec.recName3;

          depclnt.CurRec.rec.Born = transfer.rec.recBorn;
          depclnt.CurRec.rec.PlaceBorn = transfer.rec.recBornPlace;

          if ( transfer.rec.recNotResident != 0 )
            depclnt.CurRec.rec.NotResident = "X";
          end;
          depclnt.CurRec.rec.NRCountry = transfer.rec.recCitizenship;

          if ( ( transfer.rec.recPaperSeries == "" ) and ( transfer.rec.recPaperNumber == "" ) )
            depclnt.CurRec.rec.PaperKind = -1;
          else
            depclnt.CurRec.rec.PaperKind       = transfer.rec.recPaperKind;
            depclnt.CurRec.rec.PaperSeries     = transfer.rec.recPaperSeries;
            depclnt.CurRec.rec.PaperNumber     = transfer.rec.recPaperNumber;
            depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
            depclnt.CurRec.rec.PaperIssuer     = transfer.rec.recPaperIssuer;
            depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.recPaperIssuerCode;
            depclnt.CurRec.rec.IsMain          = "X";
          end;
                
          if ( ( transfer.rec.recCountry == "" ) and ( transfer.rec.recPostIndex == "" ) and ( transfer.rec.recAdress == "" ) and ( transfer.rec.recPhoneNumber == "" ) )
            ;
          else
            depclnt.CurRec.rec.AdressType  = 1;
            depclnt.CurRec.rec.Country     = transfer.rec.recCountry;
            depclnt.CurRec.rec.PostIndex   = transfer.rec.recPostIndex;
            depclnt.CurRec.rec.CodeRegion  = transfer.rec.recRegion;
            depclnt.CurRec.rec.Place       = transfer.rec.recPlace;
            depclnt.CurRec.rec.Address     = transfer.rec.recAdress;
            depclnt.CurRec.rec.PhoneNumber = transfer.rec.recPhoneNumber;
          end;

          depclnt.CurRec.rec.FNcash = transfer.rec.branch;
            
          depclnt.MayInsert = true;

          stat = depclnt.Insert( );

          if ( stat )
            transfer.rec.recPartyID = depclnt.CurRec.rec.CodClient;
            break;
          else
            PushErrorMessage( "Ошибка при вставке нового клиента" );
            stat = false;
            break;
          end;
        else
          if ( GetTrue( true, "Выплата платежа без выбора клиента невозможна. Отменить операцию ?" ) )
            stat = false;
            break;
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro OnConfirm( )

    var req = TReqPayIncoming;
    var resp = TRespPayIncoming;
    var retCode = 0;
    var cycle = true;
    var stat = true;

    req.attr.USER_ID = transfer.rec.recPartyID;
    req.attr.DOC_ID = transfer.rec.psTransID;
    req.bName = transfer.rec.recName1;
    req.bLastName = transfer.rec.recName2;
    req.bSurName = transfer.rec.recName3;
    req.bBirthday = transfer.rec.recBorn;
    req.bBirthPlace = transfer.rec.recBornPlace;
    req.bCountryC = getCodeLat2( transfer.rec.recCitizenship );
    req.bCountry = getCodeLat2( transfer.rec.recCountry );
    req.bResidentC = getCodeLat2( transfer.rec.recResidentship );
    req.bCity = transfer.rec.recPlace;
    req.bAddress = transfer.rec.recAdress;
    req.bPhone = transfer.rec.recPhoneNumber;
    req.bIDtype = getPaperName( transfer.rec.recPaperKind );
    req.bIDnumber = getPaper( transfer.rec.recPaperSeries, transfer.rec.recPaperNumber );
    req.bIDdate = transfer.rec.recPaperIssuedDate;
    req.bIDwhom = transfer.rec.recPaperIssuer;
    req.bIDwhomCode = transfer.rec.recPaperIssuerCode;
    req.bEMAIL = transfer.rec.recEMail;
    if ( transfer.rec.recNotResident != 0 )
      req.bResident = 0;
    else
      req.bResident = 1;
    end;

    while ( cycle )
      cycle = false;

      retCode = CNG_Request( req, resp );

      if ( retCode == 0 )
        retCode = resp.attr.RE;
      end;

      if ( ( retCode == 100 ) or ( retCode == 105 ) )
        if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
          cycle = true;
        else
          stat = false;
        end;
      elif ( retCode != 0 )
        if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
          MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
        else
          MsgBox( "Ошибка при запросе в КЦ о изменении состояния на \"Выплачен получателю\"" );
        end;
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )
    var stat = true;

    if ( stat )
      handInput = true;
      stat = createDocuments( );
      handInput = false;
    end;

    return stat;
  end;

  // =====================================================================
  macro RollBack( )

    var stat = true;

    if ( stat )
      if ( not GetTrue( true, "Перевод выдан получателю|Выполнить откат операции без информирования КЦ ?" ) )
        stat = false;
      end;
    end;

    if ( stat )
      RollBack( );
    end;

    return stat;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId, NULL, initClient )

  var opPayout = TMtCngOpPayout( opNum, subOpNum, psId, initClient ); 

  return opPayout;
end;
