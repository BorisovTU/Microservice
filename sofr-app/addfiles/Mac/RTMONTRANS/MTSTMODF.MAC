import DeprIntr, globals, MonTransInter, rsd, sqlconv;

private var transferReport = getCurrentTransfer( );
private var opHistoryReport = getCurrentOpHistory( );

private var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
private var currency = Tbfile( "currency.dbt", "r", 0 );
private var sb_casln = Tbfile( "sb_casln.dbt", "r", 0 );
private var rtnfopr = Tbfile( "rtnfopr.dbt", "r", 1 );

var rmto_trans_new = TRecHandler( "rmto_trans.dbt" );
var rmto_trans_old = TRecHandler( "rmto_trans.dbt" );

private var senderName = "";
private var senderPaper = "";
private var senderAddress = "";
private var transCode = "";
private var nameCurr = "";
private var recNameBefore = "";
private var recBankNameBefore = "";
private var recNameAfter = "";
private var recPaperAfter = "";
private var recBankNameAfter = "";


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefShName( currCode )

  currency.Clear( );
  currency.KeyNum = 0;

  currency.rec.Code_Currency = currCode;

  if ( currency.GetEQ( ) )
    return currency.rec.Short_Name;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefRecBankName( rmto_trans_rec )

  var recPointCode = Int( Trim( rmto_trans_rec.rec.receivePointCode ) );
  var country = "";
  var place = "";
  var cmd;
  var rs;
  var retBankName = "";

  retBankName = Trim( rmto_trans_rec.rec.recBankName );

  if ( ( retBankName == "" ) and ( recPointCode > 0 ) )
    cmd = RsdCommand( "select party.t_name as name " +
                      "from dparty_dbt party " +
                      "where party.t_partyid = :partyid" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "partyid", RSDBP_IN, recPointCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      retBankName = rslString( rs.value( "name" ) );
    end;
  end;

  return retBankName;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro findRtNFOpr

  var mainKind = 0;
  var mainKey  = "";
  var numDprt = 0;
  var recID   = 0;
  var stat = false;
  var flag;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = opHistoryReport.rec.opApplicationKind;
  sb_casln.rec.ApplicationKey2  = opHistoryReport.rec.opApplicationKey;
  sb_casln.rec.RefValue2 = 0;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1 = mainKey;

    flag = sb_casln.GetGE;

    while ( flag )
      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )
        if ( ( sb_casln.rec.ApplicationKind2 == 3001 ) and ( StrLen( sb_casln.rec.ApplicationKey2 ) >= 12 ) )
          numDprt = Int( SubStr( sb_casln.rec.ApplicationKey2, 9, 3 ) );
          recID   = Int( SubStr( sb_casln.rec.ApplicationKey2, 12 ) );

          rtnfopr.KeyNum = 1;
          rtnfopr.Clear( );
          rtnfopr.rec.numDprt = numDprt;
          rtnfopr.rec.recID = recID;

          stat = rtnfopr.GetEQ( );

          if ( stat )
            if ( ( Index( StrUpr( rtnfopr.rec.TableName ), "RMTO_TRANS" ) != 0 ) and ( ( rtnfopr.rec.opCode == 3 ) or ( rtnfopr.rec.opCode == 1005 ) ) ) // OLupdate
              flag = false; // Нашли нужную запись
            else
              stat = false;
            end;
          end;
        end;
        if ( flag )
          flag = sb_casln.Next;
        end;
      else
        flag = false;
      end;
    end;
  end;

  if ( stat )
    rmto_trans_new.SetRecordAddr( rtnfopr, 0, 2, false );
    rmto_trans_old.SetRecordAddr( rtnfopr, 0, 4 + rmto_trans_new.RecSize( ), false );
  end;

  return stat;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro fillOutField ( inField )

  var outField = "";
  var i = 1;

  while ( i <= StrLen( inField ) )

    if ( SubStr( inField, i, 1 ) == " " )
      outField = outField + "_";
    else
      outField = outField + SubStr( inField, i, 1 );
    end;

    i = i + 1;
  end;

  if ( outField == "" )
    outField = "_";
  end;

  return outField;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( ValType( transferReport ) == V_GENOBJ )

    // Определим платежную систему
    rmtr_ps.Clear( );
    rmtr_ps.KeyNum = 0;
    rmtr_ps.rec.psId = transferReport.rec.psId;

    if ( not rmtr_ps.GetEQ( ) )
      rmtr_ps.Clear( );
    end;
    rmtr_ps.rec.psName = fillOutField( rmtr_ps.rec.psName );

    // Определим имя отправителя
    if ( transferReport.rec.senderName1 != "" )
      senderName = Trim( transferReport.rec.senderName1 );
      if ( transferReport.rec.senderName2 != "" )
        senderName = senderName + "_" + Trim( transferReport.rec.senderName2 );
        if ( transferReport.rec.senderName3 != "" )
          senderName = senderName + "_" + Trim( transferReport.rec.senderName3 );
        end;
      end;
    end;
    senderName = fillOutField( senderName );

    // Определим номер документа отправителя
    if ( transferReport.rec.senderPaperNumber != "" )
      senderPaper = Trim( GetNameOfPAPRKIND( transferReport.rec.senderPaperKind ) );

      if ( transferReport.rec.senderPaperSeries != "" )
        senderPaper = senderPaper + " " + Trim( transferReport.rec.senderPaperSeries );
      end;

      senderPaper = senderPaper + " № " + Trim( transferReport.rec.senderPaperNumber );

      if ( transferReport.rec.senderPaperIssuer != "" )
        senderPaper = senderPaper + ", выдан " + Trim( transferReport.rec.senderPaperIssuer );
      end;

      if ( transferReport.rec.senderPaperIssuedDate != Date( 0, 0, 0 ) )
        senderPaper = senderPaper + ", " + String( transferReport.rec.senderPaperIssuedDate:f );
      end;
    end;
    senderPaper = fillOutField( senderPaper );

    // Определим адрес отправителя
    senderAddress = fillOutField( Trim( transferReport.rec.senderAdress ) );

    // Определим номер перевода
    transCode = fillOutField( Trim( transferReport.rec.transCode ) );

    // Определим название валюты
    nameCurr = DefShName( transferReport.rec.curCode );
    nameCurr = fillOutField( Trim( nameCurr ) );

    // Определим имя и банк получателя до и после операции
    if ( findRtNFOpr( ) )    
      if ( rmto_trans_old.rec.recName1 != "" )
        recNameBefore = Trim( rmto_trans_old.rec.recName1 );
        if ( rmto_trans_old.rec.recName2 != "" )
          recNameBefore = recNameBefore + "_" + Trim( rmto_trans_old.rec.recName2 );
          if ( rmto_trans_old.rec.recName3 != "" )
            recNameBefore = recNameBefore + "_" + Trim( rmto_trans_old.rec.recName3 );
          end;
        end;
      end;

      if ( rmto_trans_new.rec.recName1 != "" )
        recNameAfter = Trim( rmto_trans_new.rec.recName1 );
        if ( rmto_trans_new.rec.recName2 != "" )
          recNameAfter = recNameAfter + "_" + Trim( rmto_trans_new.rec.recName2 );
          if ( rmto_trans_new.rec.recName3 != "" )
            recNameAfter = recNameAfter + "_" + Trim( rmto_trans_new.rec.recName3 );
          end;
        end;
      end;

      if ( rmto_trans_new.rec.recPaperNumber != "" )
        recPaperAfter = Trim( GetNameOfPAPRKIND( rmto_trans_new.rec.recPaperKind ) );

        if ( rmto_trans_new.rec.recPaperSeries != "" )
          recPaperAfter = recPaperAfter + " " + Trim( rmto_trans_new.rec.recPaperSeries );
        end;

        recPaperAfter = recPaperAfter + " № " + Trim( rmto_trans_new.rec.recPaperNumber );

        if ( rmto_trans_new.rec.recPaperIssuer != "" )
          recPaperAfter = recPaperAfter + ", выдан " + Trim( rmto_trans_new.rec.recPaperIssuer );
        end;

        if ( rmto_trans_new.rec.recPaperIssuedDate != Date( 0, 0, 0 ) )
          recPaperAfter = recPaperAfter + ", " + String( rmto_trans_new.rec.recPaperIssuedDate:f );
        end;
      end;

      recBankNameBefore = DefRecBankName( rmto_trans_old );
      recBankNameAfter = DefRecBankName( rmto_trans_new );
    end;

    recNameBefore = fillOutField( recNameBefore );
    recBankNameBefore = fillOutField( recBankNameBefore );
    recNameAfter = fillOutField( recNameAfter );
    recPaperAfter = fillOutField( recPaperAfter );
    recBankNameAfter = fillOutField( recBankNameAfter );

    [Платежная система _#_________________________________________
                         (название платежной системы)


                                       ЗАЯВЛЕНИЕ НА ИЗМЕНЕНИЕ ДЕНЕЖНОГО ПЕРЕВОДА

     Я, _#__________________________________________________________________________________________________________________
                                            (Фамилия, Имя, Отчество Отправителя)
     _#_____________________________________________________________________________________________________________________
                                   (документ, удостоверяющий личность (серия, номер, кем и когда выдан)
     _#_____________________________________________________________________________________________________________________
                                                (адрес отправителя)
     отправил денежный перевод со следующими реквизитами:
     	Номер перевода _#_________________________________________________
     	Дата отправления перевода _#______________________________________
     	Сумма и валюта перевода _#_____________________________ _#________
     	Ф.И.О. Получателя _#______________________________________________
     	Банк Получателя _#________________________________________________
     Прошу внести в отправленный перевод следующие изменения:
     	Ф.И.О. Получателя _#______________________________________________
     	Данные Получателя _#________________________________________________________________________________________________
     	Банк Получателя _#________________________________________________
     	
     _#_________________________                                    ________________________________________________________
              (дата)                                                             (подпись Отправителя)                        


     ОТМЕТКИ БАНКА
     +-----------------------------------------------------------+-----------------------------------------------------------+
     |             Заявление принято и проверено                 |  Подтверждение о внесении изменений в денежный перевод    |
     |                                                           |    получено "_______" _______________________ 200___ г.   |
     |                                                           |                                                           |
     |                                                           |  Номер перевода _____________________________             |
     |                                                           |                                                           |
     |                                                           |                                                           |
     |  _______________________________________________________  |  _______________________________________________________  |
     |         (печать, подпись сотрудника Банка)                |              (печать, подпись сотрудника Банка)           |
     +-----------------------------------------------------------+-----------------------------------------------------------+]
     ( rmtr_ps.rec.psName, 
       senderName, senderPaper, senderAddress,
       transCode, String( transferReport.rec.sendDate:f ),
       String( transferReport.rec.sum:0:2:a ), nameCurr,
       recNameBefore, recBankNameBefore,
       recNameAfter, recPaperAfter, recBankNameAfter,
       String( {curdate}:f ) );

  end;

end;
