/*
$Name:             mtcng_refload.mac
$Module:           RS-Retail
$Description:      Обновление справочников ПС Contact NG
*/

import rsd, mtcntng_webserv, ErrHandler, CommonInter;

private const UseLocalFiles = false;  // Признак использования локальных файлов-справочников
private const PathToLocalFiles = "..\\TrakFile\\"; // Путь к локальным файлам-справочникам
private const FLPath = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true);  // Путь к файлам загрузки

private const ServiceTimeOut = 600; // Максимальное время загрузки справочников


private const
  SUBJ_TYPE_COUNTRY = 1,
  SUBJ_TYPE_BANK = 2,
  SUBJ_TYPE_POINT = 3,
  SUBJ_TYPE_CITY = 4,
  SUBJ_TYPE_SCEN = 5;

private FILE DbfFile() dbf;

private var cng_id_type = Tbfile( "cng_id_type.dbt", "r", 0 );
private var typedul_cng_bnk = Tbfile( "typedul_cng_bnk.dbt", "w", 0 );
private var wasAddCngTypeDUL = false;

private var {oper};


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TReqPing

  class ( TRequestAttributesBase ) A
    var USER_ID : integer = 1;

    InitTRequestAttributesBase( "TAbonentObject", "Ping" ); 
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespPing

  class ( TResponseAttributesBase ) A
    var SCHEME : string = "";
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro checkPing ( )

  var req = TReqPing;
  var resp = TRespPing;
  var retCode = 0;
  var counter = 0;

  while ( true )

    counter = counter + 1;

    retCode = CNG_Request( req, resp, true );

    if ( retCode != 0 )
      if ( counter >= 3 )
        break;
      end;
    else
      break;
    end;
  end;

  if ( ( retCode == 100 ) or ( retCode == 105 ) )
    DoError( "Не удалось получить информацию из КЦ при запросе о наличии связи" );
  elif ( retCode != 0 )
    if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
       DoError( "Ошибка " + retCode + " при запросе в КЦ о наличии связи|" + resp.attr.ERR_TEXT );
    else
      DoError( "Ошибка " + retCode + " при запросе в КЦ о наличии связи" );
    end;
  end;

  return retCode;
end;


//-----------------------------------------------------------------------------
private macro ClearTable(name)
  var cmd = RsdCommand("delete from " + name);
  cmd.execute();
end;


//-----------------------------------------------------------------------------
private macro ClearTables(suffix)
  ClearTable("dcng_banks_" + suffix);
  ClearTable("dcng_bankserv_" + suffix);
  ClearTable("dcng_serv_" + suffix);
  ClearTable("dcng_city_" + suffix);
  ClearTable("dcng_country_" + suffix);
  ClearTable("dcng_region_" + suffix);
  ClearTable("dcng_feature_" + suffix);
  ClearTable("dcng_feat_txt_" + suffix);
  ClearTable("dcng_attrlist_" + suffix);
  ClearTable("dcng_min_max_" + suffix);
  ClearTable("dcng_attr_grps_" + suffix);
  ClearTable("dcng_currency_" + suffix);
  ClearTable("dcng_attr_chng_" + suffix);
  ClearTable("dcng_scitem_grp_" + suffix);
  ClearTable("dcng_id_type_" + suffix);
end;


//-----------------------------------------------------------------------------
private macro ClearTempTables
  ClearTables("tmp");
end;


//-----------------------------------------------------------------------------
private macro ClearWorkTables
  ClearTables("dbt");
end;


//-----------------------------------------------------------------------------
private macro SplitString(str, delim)
  var arr = TArray;
  
  var idx;
  while (str != "")
    idx = Index(str, delim);
    if (idx == 0)
      arr[arr.size] = str;
      return arr;
    end;

    arr[arr.size] = substr(str, 1, idx - 1);
    str = substr(str, idx + 1);
  end;

  return arr;
end;


//-----------------------------------------------------------------------------
private class TDataPacket
  var ROWDATA = TArray;
end;


//-------------------------------------------------------------------------------------------------
// Базовый класс для атрибутов объекта-таблицы справочника
//-------------------------------------------------------------------------------------------------
private class TTableObjectAttributes
    var ID:integer = 0;
    var ERASED:integer = 0;
end;


//-----------------------------------------------------------------------------
// Базовый класс объекта-таблицы справочника
//-----------------------------------------------------------------------------
private class TTableObject
  var attr;

  //-----------------------------------------------------------------------------
  private macro GetTableName
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    return GetObjProps(attr);
  end;

  //-----------------------------------------------------------------------------
  private macro GetPropValue(name)
    return GenGetProp(attr, name);
  end;

  //-----------------------------------------------------------------------------
  macro IntToChar(val_int)
    if (val_int == 0)
      return "";
    else
      return "X";
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro GetFieldNames
    return GetPropNames();
  end;
  
  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    return false;
  end;

  //-----------------------------------------------------------------------------
  private macro GetDbfFieldNames
    return GetObjProps(attr);
  end;

  //-----------------------------------------------------------------------------
  macro FillFromDbf(dbf_file)
    var v, i;
    var fields = GetDbfFieldNames();
    var props = GetObjProps(attr);
    for (i, 0, fields.size - 1)
      v = dbf_file(FldIndex(dbf_file, fields[i]));
      if (ValType(v) == V_STRING)
        v = Trim(v);
      end;
      GenSetProp(attr, props[i], v);
    end;
    return int(dbf_file.Version);
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
  end;  

  //-----------------------------------------------------------------------------
  macro Insert
    var into = "";
    var values = "";
    var field, fields = GetFieldNames();
    var props = GetPropNames();
    var delim = "";
    var i = 0;
    for (i, 0, fields.size - 1)
      field = fields[i];
      if (field != null)
        into = into + delim + "t_" + field;
        
        if (IsFlagField(field))
          if (GetPropValue(props[i]))
            values = values + delim + "'X'";
          else
            values = values + delim + "chr(0)"; // не понятно, как передать это значение в параметре RsdCommand
          end;
          props[i] = null;
        else
          values = values + delim + "?";
        end;
        
        delim = ", ";
      end;
    end;

    
    var cmd = RsdCommand("insert into d" + GetTableName() + "_tmp (" + into + ") values (" + values + ")");
    cmd.NullConversion = true;

    var prop;
    for (prop, props)
      if (prop != null)
        cmd.addParam(prop, RSDBP_IN, GetPropValue(prop));
      end;
    end;
    
    cmd.execute();

    if (attr.ERASED == 0)
      InsertChildren();
    end;

    onError( e );
      if ( IsEqClass( "TRsdError", e.err ) ) // Дублирование записи за ошибку не считаем
        [ ];
        i = 0;
        while ( i < e.err.environment.errorcount )
          println( e.err.environment.error( i ).descr );
          i = i + 1;
        end;
      else
        RunError( e );
      end;
  end;

  //-----------------------------------------------------------------------------
  macro defSeqNumber ( subjType, subjCode, groupNum )

    var cmdSeq, rsSeq;
    var seqNumber = 1;
    
    cmdSeq = RsdCommand( "select max( t.t_SeqNumber ) as maxSeqNumber " +
                         "from dcng_attr_grps_tmp t " +
                         "where t.t_Subj_Type = ? and " +
                               "t.t_Subj_Code = ?" );

    cmdSeq.addParam( "", RSDBP_IN, subjType );
    cmdSeq.addParam( "", RSDBP_IN, subjCode );

    cmdSeq.execute;

    rsSeq = RsdRecordset( cmdSeq );

    if ( ( rsSeq.moveNext ) and ( ( ValType( rsSeq.value( "maxSeqNumber" ) ) == V_INTEGER ) or 
                                  ( ValType( rsSeq.value( "maxSeqNumber" ) ) == V_DOUBLE  ) or 
                                  ( ValType( rsSeq.value( "maxSeqNumber" ) ) == V_DOUBLEL )   ) )
      seqNumber = Int( rsSeq.value( "maxSeqNumber" ) ) + 1;
    end;

    return seqNumber;
  end;

  //-----------------------------------------------------------------------------
  macro InsertAttr_Grps(type, grps_str)
    var cmd;   
    var grp, grps = SplitString(grps_str, ",");
    var seqNumber = 0;

    for (grp, grps)

      seqNumber = defSeqNumber( type, attr.ID, int( grp ) );

      cmd = RsdCommand("insert into dcng_attr_grps_tmp (t_Subj_Type, t_Subj_Code, t_GroupNum, t_SeqNumber) values (?, ?, ?, ?)");
      cmd.NullConversion = true;
      cmd.addParam("Subj_Type", RSDBP_IN, type);
      cmd.addParam("Subj_Code", RSDBP_IN, attr.ID);
      cmd.addParam("GroupNum", RSDBP_IN, int(grp));
      cmd.addParam("SeqNumber", RSDBP_IN, seqNumber);

      cmd.execute( );
    end;
  end;  

  //-----------------------------------------------------------------------------
  private macro DeleteChild(table, type)
    var cmd = RsdCommand(
        "delete from d" + table + "_dbt " +
        "where t_Subj_Type = ? " +
        "  and t_Subj_Code in ( "+
        "    select t_Id from d" + GetTableName() + "_tmp " +
        "    where t_Erased = 0 "+
        "  )");
    cmd.addParam("Subj_Type", RSDBP_IN, type);
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  macro DeleteAttr_Grps(type)
    DeleteChild("cng_attr_grps", type);
  end;

  //-----------------------------------------------------------------------------
  macro InsertCurrency(type, curr_str, is_payment)
    var cmd;
    var is_pay;

    if (is_payment)
      is_pay = "'X'";
    else
      is_pay = "chr(0)";
    end;
   
    var curr, currs = SplitString(curr_str, ";");
    for (curr, currs)
      cmd = RsdCommand(
          "insert into dcng_currency_tmp " +
          "(t_Subj_Type, t_Subj_Code, t_Is_Payment, t_Code_Currency) " +
          "values (?, ?, " + is_pay + ", (select t_Code_Currency from dcurrency_dbt where t_Short_Name = ?))");
          
      cmd.NullConversion = true;
      cmd.addParam("Subj_Type", RSDBP_IN, type);
      cmd.addParam("Subj_Code", RSDBP_IN, attr.ID);
      cmd.addParam("Code_Currency", RSDBP_IN, curr);
      cmd.execute();
    end;
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteCurrency(type)
    DeleteChild("cng_currency", type);
  end;

  //-----------------------------------------------------------------------------
  macro InsertAttr_Chng(states_str)
    var cmd;
    
    var state, states = SplitString(states_str, ",");
    for (state, states)
      cmd = RsdCommand("insert into dcng_attr_chng_tmp (t_Attr_Id, t_Status) values (?, ?)");
      cmd.NullConversion = true;
      cmd.addParam("Attr_Id", RSDBP_IN, attr.ID);
      cmd.addParam("Status", RSDBP_IN, int(state));
      cmd.execute();
    end;
  end;  

  //-----------------------------------------------------------------------------
  macro Erase
    var cmd = RsdCommand(
        "update d" + GetTableName() + "_dbt " +
        "set t_Erased = 1 " +
        "where t_Id in ( " +
        "  select t_Id from d" + GetTableName() + "_tmp " +
        "  where t_Erased = 1 " +
        ")");
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  macro DeleteChildren
  end;

  //-----------------------------------------------------------------------------
  macro Merge
    var set = "";
    var into = "";
    var values = "";
    var field, fields = GetFieldNames();
    var delim = "";
    var delim2 = "";
    for (field, fields)
      if (field != null)
        if (field != "ID")
          set = set + delim + "t1.t_" + field + " = t2.t_" + field;
          delim = ", ";
        end;
        into = into + delim2 + "t1.t_" + field;
        values = values + delim2 + "t2.t_" + field;
        delim2 = ", ";
      end;
    end;

    var cmd = RsdCommand(
        "merge into d" + GetTableName() + "_dbt t1 " +
        "using d" + GetTableName() + "_tmp t2 " +
        "on (t1.t_Id = t2.t_Id) " +
        "when matched then " +
        "  update set " + set + " " +
        "when not matched then " +
        "  insert (" + into + ") " + 
        "  values (" + values + ")");
    cmd.execute();
  end;
  
end;



//-----------------------------------------------------------------------------
// Участник ПС
//-----------------------------------------------------------------------------
class (TTableObject) TBanks
  class (TTableObjectAttributes) A
    var NAME_RUS:string = "";   // в таблице - NAME
    var ADDRESS1:string = "";   // в таблице - ADDRESS
    var ADDRESS2:string = "";   // в таблице - WT_WEEK
    var ADDRESS3:string = "";   // в таблице - WT_HOLIDAY
    var DELETED:integer = 0;   // в таблице - IS_BLOCKED
    var NAME:string = "";   // в таблице - NAME_LAT
    var ADDR_LAT:string = "";   // в таблице - ADDRESS_LAT
    var PARENT_ID:integer = 0;
    var PP_CODE:string = "";
    var BIC:string = "";
    var CITY_ID:integer = 0;
    var PHONE:string = "";
    var CONTACT:integer = 0;
    var IS_KFM:integer = 0;
    var IS_ONLINE:integer = 0;
    var CAN_REVOKE:integer = 0;
    var CAN_CHANGE:integer = 0;
    var GET_MONEY:integer = 0;
    var LOGO_ID:integer = 0;
    var SCEN_ID:integer = 0;
    var UNIQUE_TRN:integer = 0;
    var ATTR_GRPS:string = ""; // список, переносится в CNG_ATTR_GRPS
    var SEND_CURR:string = ""; // список, переносится в CNG_CURRENCY
    var REC_CURR:string = ""; // список, переносится в CNG_CURRENCY
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_banks";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    var props = GetObjProps(attr);
    props.size = props.size - 3;    // все поля, кроме ATTR_GRPS, SEND_CURR, REC_CURR
    return props;
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetFieldNames
    var fields = GetPropNames();
    fields[2] = "NAME";   // первые два поля -  ID, ERASED
    fields[3] = "ADDRESS";
    fields[4] = "WT_WEEK";
    fields[5] = "WT_HOLIDAY";
    fields[6] = "IS_BLOCKED";
    fields[7] = "NAME_LAT";
    fields[8] = "ADDRESS_LAT";
    return fields;
  end;

  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    if ((name == "IS_BLOCKED") or (name == "IS_KFM") or (name == "IS_ONLINE") or (name == "CAN_REVOKE") or 
        (name == "CAN_CHANGE") or (name == "GET_MONEY") or (name == "UNIQUE_TRN"))
      return true;
    end;
    return false;
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
    InsertAttr_Grps(SUBJ_TYPE_POINT, attr.ATTR_GRPS);
    InsertCurrency(SUBJ_TYPE_POINT, attr.SEND_CURR, true);
    InsertCurrency(SUBJ_TYPE_POINT, attr.REC_CURR, false);
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteChildren
    DeleteAttr_Grps(SUBJ_TYPE_POINT);
    DeleteCurrency(SUBJ_TYPE_POINT);
  end;

end;



//-----------------------------------------------------------------------------
// Услуги, предоставляемые узлами
//-----------------------------------------------------------------------------
class (TTableObject) TBankserv
  class (TTableObjectAttributes) A
    var BANK_ID:integer = 0;
    var SERV_ID:integer = 0;
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_bankserv";
  end;
  
end;



//-----------------------------------------------------------------------------
// Услуги ПС
//-----------------------------------------------------------------------------
class (TTableObject) TServ
  class (TTableObjectAttributes) A
    var CAPTION_LA:string = "";   // в таблице - CAPTION_LAT
    var COMMENT_LA:string = "";   // в таблице - COMMENT_LAT
    var PARENT_ID:integer = 0;
    var CAPTION:string = "";
    var COMMENT:string = "";
    var CAN_IN:integer = 0;
    var CAN_PAY:integer = 0;
    var CS_IN:string = "";
    var CS_IN_FEE:string = "";
    var CS_PAY:string = "";
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_serv";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetFieldNames
    var fields = GetPropNames();
    fields[2] = "CAPTION_LAT";   // первые два поля -  ID, ERASED
    fields[3] = "COMMENT_LAT";   // первые два поля -  ID, ERASED
    return fields;
  end;

  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    if ((name == "CAN_IN") or (name == "CAN_PAY"))
      return true;
    end;
    return false;
  end;

end;



//-----------------------------------------------------------------------------
// Страна участника ПС
//-----------------------------------------------------------------------------
class (TTableObject) TBnk_City
  class (TTableObjectAttributes) A
    var CITY_HEAD:string = "";   // в таблице - CITY
    var CITY_LAT:string = "";
    var COUNTRY:integer = 0;
    var REGION:integer = 0;
    var PP_CODE:string = "";
    var SEND_CURR:string = ""; // список, переносится в CNG_CURRENCY
    var REC_CURR:string = ""; // список, переносится в CNG_CURRENCY
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_city";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    var props = GetObjProps(attr);
    props.size = props.size - 2;    // все поля, кроме SEND_CURR, REC_CURR
    return props;
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetFieldNames
    var fields = GetPropNames();
    fields[2] = "CITY";   // первые два поля -  ID, ERASED
    return fields;
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
    InsertCurrency(SUBJ_TYPE_CITY, attr.SEND_CURR, true);
    InsertCurrency(SUBJ_TYPE_CITY, attr.REC_CURR, false);
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteChildren
    DeleteCurrency(SUBJ_TYPE_CITY);
  end;

end;



//-----------------------------------------------------------------------------
// Страна участника ПС
//-----------------------------------------------------------------------------
class (TTableObject) TCountry
  class (TTableObjectAttributes) A
    var CODE:string = "";
    var NAME:string = "";
    var NAME_LAT:string = "";
    var IS_FATF:integer = 0;
    var PP_CODE:string = "";
    var SEND_CURR:string = ""; // список, переносится в CNG_CURRENCY
    var REC_CURR:string = ""; // список, переносится в CNG_CURRENCY
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_country";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    var props = GetObjProps(attr);
    props.size = props.size - 2;    // все поля, кроме SEND_CURR, REC_CURR
    return props;
  end;
  
  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    if (name == "IS_FATF")
      return true;
    end;
    return false;
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
    InsertCurrency(SUBJ_TYPE_COUNTRY, attr.SEND_CURR, true);
    InsertCurrency(SUBJ_TYPE_COUNTRY, attr.REC_CURR, false);
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteChildren
    DeleteCurrency(SUBJ_TYPE_COUNTRY);
  end;

end;



//-----------------------------------------------------------------------------
// Регион участника ПС
//-----------------------------------------------------------------------------
class (TTableObject) TRegion
  class (TTableObjectAttributes) A
    var NAME:string = "";
    var NAME_LAT:string = "";
    var NAME_SORT:string = "";
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_region";
  end;
  
end;



//-----------------------------------------------------------------------------
// Особенности обслуживания участников ПС
//-----------------------------------------------------------------------------
class (TTableObject) TFeature
  class (TTableObjectAttributes) A
    var SUBJ_TYPE:integer = 0;
    var SUBJ_CODE:integer = 0;
    var IS_PAYMENT:integer = 0;
    var LANGUAGE:integer = 0;
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_feature";
  end;
  
  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    if (name == "IS_PAYMENT")
      return true;
    end;
    return false;
  end;

end;



//-----------------------------------------------------------------------------
// Описание особенностей обслуживания участников ПС
//-----------------------------------------------------------------------------
class (TTableObject) TFeat_Txt
  class (TTableObjectAttributes) A
    var FEATURE:integer = 0;
    var LINE_NO:integer = 0;
    var LINE_TEXT:string = "";
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_feat_txt";
  end;
  
end;



//-----------------------------------------------------------------------------
// Список полей документа к заполнению
//-----------------------------------------------------------------------------
class (TTableObject) TAttrlist
  class (TTableObjectAttributes) A
    var GRP_NUM:integer = 0;
    var FIELD_GRP:string = "";
    var SORTORDER:integer = 0;
    var IS_COND:integer = 0;
    var MIN_VALUE:money = $0;
    var MAX_VALUE:money = $0;
    var CURR_CODE:string = "";
    var CURR_USE_EQ:integer = 0;
    var S_RESIDENT:integer = 0;
    var FIELD_NAME:string = "";
    var FIELD_CAPTION:string = "";
    var FIELD_CAPTION_LAT:string = "";
    var REG_MASK:string = "";
    var BIC:string = "";
    var IS_REQUIRED:integer = 0;
    var EXAMPLE:string = "";
    var COMMENT:string = "";
    var COMMENT_LAT:string = "";
    var MAX_LEN:integer = 0;
    var SHORT_CAPTION:string = "";
    var SHORT_CAPTION_LAT:string = "";
    var INOUTEDIT:integer = 0;
    var DATA_TYPE:string = "";
    var VISIBLE:integer = 0;
    var LANG:string = "";
    var EDIT_STATES:string = ""; // список, переносится в CNG_ATTR_CHNG
  end;

  attr = A;
  private var DbfFieldNames = null;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_attrlist";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    var props = GetObjProps(attr);
    props.size = props.size - 1;    // все поля, кроме EDIT_STATES
    return props;
  end;
  
  //-----------------------------------------------------------------------------
  private macro IsFlagField(name)
    if ((name == "IS_COND") or (name == "IS_REQUIRED") or (name == "VISIBLE"))
      return true;
    end;
    return false;
  end;

  //-----------------------------------------------------------------------------
  private macro GetPropValue(name)
    var cmd, rs;
    
    var value = GetPropValue(name);
    /*if ((name == "IS_COND") or (name == "IS_REQUIRED"))
      value = IntToChar(value);
    el*/ if (name == "CURR_CODE")
      value = 0;
      cmd = RsdCommand("select t_Code_Currency from dcurrency_dbt where t_Short_Name = ?");
      cmd.addParam("PP_Code", RSDBP_IN, attr.CURR_CODE);
      cmd.NullConversion = true;
      cmd.execute();
      
      rs = RsdRecordset(cmd);
      if (rs.moveNext())
        value = rs.value(0);
      end;
    end;

    return value;
  end;

  //-----------------------------------------------------------------------------
  private macro GetDbfFieldNames
    if (DbfFieldNames != null)
      return DbfFieldNames;
    end;
    
    var i;
    DbfFieldNames = GetObjProps(attr);
    for (i, 0, DbfFieldNames.size - 1)
      if (DbfFieldNames[i] == "CURR_USE_EQ")
        DbfFieldNames[i] = "CURR_USE_E";
      elif (DbfFieldNames[i] == "FIELD_CAPTION")
        DbfFieldNames[i] = "FIELD_CAPT";
      elif (DbfFieldNames[i] == "FIELD_CAPTION_LAT")
        DbfFieldNames[i] = "FIELD_CA_1";
      elif (DbfFieldNames[i] == "IS_REQUIRED")
        DbfFieldNames[i] = "IS_REQUIRE";
      elif (DbfFieldNames[i] == "COMMENT_LAT")
        DbfFieldNames[i] = "COMMENT_LA";
      elif (DbfFieldNames[i] == "SHORT_CAPTION")
        DbfFieldNames[i] = "SHORT_CAPT";
      elif (DbfFieldNames[i] == "SHORT_CAPTION_LAT")
        DbfFieldNames[i] = "SHORT_CA_1";
      elif (DbfFieldNames[i] == "EDIT_STATES")
        DbfFieldNames[i] = "EDIT_STATE";
      end;
    end;

    return DbfFieldNames;
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
    InsertAttr_Chng(attr.EDIT_STATES);
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteChildren
    var cmd = RsdCommand(
        "delete from dcng_attr_chng_dbt " +
        "where t_Attr_Id in ( "+
        "    select t_Id from dcng_attrlist_tmp " +
        "    where t_Erased = 0 "+
        "  )");
    cmd.execute();
  end;

end;



//-----------------------------------------------------------------------------
// Ограничения по суммам
//-----------------------------------------------------------------------------
class (TTableObject) TMin_Max
  class (TTableObjectAttributes) A
    var BANK_ID:integer = 0;
    var CURR_CODE:string = "";
    var MIN_SUM:money = $0;
    var MAX_SUM:money = $0;
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_min_max";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropValue(name)
    if (name != "CURR_CODE")
      return GetPropValue(name);
    end;

    var value = 0;
    var cmd = RsdCommand("select t_Code_Currency from dcurrency_dbt where t_Short_Name = ?");
    cmd.addParam("PP_Code", RSDBP_IN, attr.CURR_CODE);
    cmd.NullConversion = true;
    cmd.execute();
    
    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      value = rs.value(0);
    end;

    return value;
  end;

end;



//-----------------------------------------------------------------------------
// Группы полей, заполняемых на шаге
//-----------------------------------------------------------------------------
class (TTableObject) TScen_Item
  class (TTableObjectAttributes) A
    var SCEN_ID:integer = 0;
    var STEP:integer = 0;
    var OBJECT_CLASS:string = "";
    var OBJECT_ACTION:string = "";
    var LIST_SRC:string = "";
    var ATTR_GRPS:string = ""; // список, переносится в CNG_ATTR_GRPS
  end;

  attr = A;
  private var DbfFieldNames = null;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_scitem_grp";
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetPropNames
    var props = GetObjProps(attr);
    props.size = props.size - 1;    // все поля, кроме ATTR_GRPS
    return props;
  end;
  
  //-----------------------------------------------------------------------------
  private macro GetDbfFieldNames
    if (DbfFieldNames != null)
      return DbfFieldNames;
    end;
    
    var i;
    DbfFieldNames = GetObjProps(attr);
    for (i, 0, DbfFieldNames.size - 1)
      if (DbfFieldNames[i] == "OBJECT_CLASS")
        DbfFieldNames[i] = "OBJECT_CLA";
      elif (DbfFieldNames[i] == "OBJECT_ACTION")
        DbfFieldNames[i] = "OBJECT_ACT";
      end;
    end;

    return DbfFieldNames;
  end;

  //-----------------------------------------------------------------------------
  private macro InsertChildren
    InsertAttr_Grps(SUBJ_TYPE_SCEN, attr.ATTR_GRPS);
  end;  

  //-----------------------------------------------------------------------------
  macro DeleteChildren
    DeleteAttr_Grps(SUBJ_TYPE_SCEN);
  end;

end;



//-----------------------------------------------------------------------------
// Типы ДУЛ
//-----------------------------------------------------------------------------
class (TTableObject) TId_Type
  class (TTableObjectAttributes) A
    var TYPE_CODE:string = "";
    var NAME_LAT:string = "";
    var NAME_RUS:string = "";
    var COUNTRY:integer = 0;
  end;

  attr = A;

  //-----------------------------------------------------------------------------
  private macro GetTableName
    return "cng_id_type";
  end;

end;



//-----------------------------------------------------------------------------
// Статусы документа, в котором возможно изменение атрибута
//-----------------------------------------------------------------------------
class TCng_Attr_Chng

  //-----------------------------------------------------------------------------
  macro Merge
    var cmd = RsdCommand(
        "insert into dcng_attr_chng_dbt " +
        "  (t_Attr_Id, t_Status) " +
        "  select t_Attr_Id, t_Status " +
        "  from dcng_attr_chng_tmp ");
    cmd.execute();
  end;
  
end;



//-----------------------------------------------------------------------------
// Валюты, используемые участниками
//-----------------------------------------------------------------------------
class TCng_Currency

  //-----------------------------------------------------------------------------
  macro Merge
    var cmd = RsdCommand(
        "insert into dcng_currency_dbt " +
        "  (t_Subj_Type, t_Subj_Code, t_Is_Payment, t_Code_Currency) " +
        "  select t_Subj_Type, t_Subj_Code, t_Is_Payment, t_Code_Currency " +
        "  from dcng_currency_tmp ");
    cmd.execute();
  end;
  
end;



//-----------------------------------------------------------------------------
// Группы полей, заполняемых для участника
//-----------------------------------------------------------------------------
class TCng_Attr_Grps

  //-----------------------------------------------------------------------------
  macro Merge
    var cmd = RsdCommand(
        "insert into dcng_attr_grps_dbt " +
        "  (t_Subj_Type, t_Subj_Code, t_GroupNum, t_SeqNumber) " +
        "  select t_Subj_Type, t_Subj_Code, t_GroupNum, t_SeqNumber " +
        "  from dcng_attr_grps_tmp ");
    cmd.execute();
  end;
  
end;



//-----------------------------------------------------------------------------
// класс для получения обновлений справочников
//-----------------------------------------------------------------------------
private class TAbonentObject

  var Service;
  var Response;
  var FractionLoading = false;
  var FL_FQuantity = 0;
  var FL_LastFraction = 0;
  var FL_GUID = "";

  //-----------------------------------------------------------------------------
  class TGet_Changes
    class (TRequestAttributesBase) A
      var VERSION:integer;
      var TYPE_VERSION:string = "I";
      var PACK:string = "ZLIB";

      InitTRequestAttributesBase("TAbonentObject", "GET_CHANGES");

      POINT_CODE = GetCodeInfoInteraction( );
    end;

    var attr = A;
  end;

  //-----------------------------------------------------------------------------
  class TGet_Changes_Portion
    class (TRequestAttributesBase) A
      var VERSION:integer;
      var TYPE_VERSION:string = "I";
      var PACK:string = "ZLIB";
      var PORTION:integer = 1;
      var BOOK_ID:string;
      var PART:integer;

      InitTRequestAttributesBase("TAbonentObject", "GET_CHANGES");

      POINT_CODE = GetCodeInfoInteraction( );
    end;

    var attr = A;
  end;

  //-----------------------------------------------------------------------------
  class TCheck
    class (TRequestAttributesBase) A
      var VERSION:integer;
      var TYPE_VERSION:string = "I";

      InitTRequestAttributesBase("TAbonentObject", "CHECK");

      POINT_CODE = GetCodeInfoInteraction( );
    end;

    var attr = A;
  end;
  
  //-----------------------------------------------------------------------------
  class TResponse
    class (TResponseAttributesBase) A
      var FULL:integer = 0;
      var VERSION:integer = 0;
      var BOOK_ID:string = "";
      var PART:integer = 0;
      var TOTAL:integer = 0;
    end;
    
    var BANKS:string;
    var BANKSERV:string;
    var SERV:string;
    var BNK_CITY:string;
    var COUNTRY:string;
    var REGION:string;
    var FEATURE:string;
    var FEAT_TXT:string;
    var ATTRLIST:string;
    var MIN_MAX:string;
    var SCEN_ITEM:string;
    var ID_TYPE:string;

    var attr = A;

    //-----------------------------------------------------------------------------
    macro GetTables
      var tables = GetObjProps(this);
      tables.size = tables.size - 1;    // все поля, кроме attr
      return tables;
    end;
    
  end;


  //-----------------------------------------------------------------------------
  private macro MakeTableObject(name)
    return GenObject("T" + name);
  end;


  //-----------------------------------------------------------------------------
  private macro FillTableObject(obj, datapacket, compressmethod)
    var packet = TDataPacket;
    packet.ROWDATA[0] = obj;

    var xml_doc = Service.Decode(datapacket, compressmethod);

    // Уберем нечитаемые символы
    var posCode7 = Index( xml_doc, StrFor( 7 ) );

    while ( posCode7 > 0 )
      xml_doc = SubStr( xml_doc, 1, posCode7 - 1 ) + " " + SubStr( xml_doc, posCode7 + 1 );

      posCode7 = Index( xml_doc, StrFor( 7 ) );
    end;

    CheckError(RtXml2Rsl(xml_doc, packet),
               "Ошибка формата ответного XML-сообщения");

    return packet;
  end;

  //-----------------------------------------------------------------------------
  private macro UpdateStatusLine(msg:string)
    if (FL_FQuantity > 0)
      msg = "Часть " + FL_LastFraction + " из " + FL_FQuantity + ". " + msg;
    end;
    message(msg);
  end;

  //-----------------------------------------------------------------------------
  private macro ProcessTable(name, datapacket, compressmethod)
    if ((datapacket == null) or (datapacket == ""))
      return;
    end;
    
    UpdateStatusLine("Загрузка во временную таблицу " + name + "...");
    var packet = FillTableObject(MakeTableObject(name), datapacket, compressmethod);

    var obj;
    for (obj, packet.ROWDATA)
      obj.Insert();
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro ProcessDbf(name)
    var ver, max_ver = 0;
    var obj = MakeTableObject(name);

    if (not open(DbfFile, PathToLocalFiles + name + ".dbf"))
      return 0;
    end;
    
    while (next(DbfFile))
      ver = obj.FillFromDbf(DbfFile);
      if (max_ver < ver)
        max_ver = ver;
      end;

      obj.Insert();
    end;
    close(DbfFile);

    return max_ver;
  end;

  //-----------------------------------------------------------------------------
  macro GetCurrentVersion()
    var ver = 0;
    var cmd = RsdCommand(
        "select t_Version from drmtr_ps_dbt " +
        "where t_PsCode = 'CNG' ");
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      ver = rs.value(0);
    end;

    return ver;
  end;

  //-----------------------------------------------------------------------------
  private macro SaveVersion(ver)
    var cmd = RsdCommand(
        "update drmtr_ps_dbt " +
        "set t_Version = ? " +
        "where t_PsCode = 'CNG' ");
    cmd.addParam("Version", RSDBP_IN, ver);
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  private macro pointsWithoutAddress( )

    var ppCode = "";
    var subjCode = 0;
    var subjType = 0;
    var pointId = 0;
    var cmd2;
    var rs2;
    var cmd = RsdCommand(
      "select distinct T_PP_CODE, T_SUBJ_CODE, T_SUBJ_TYPE " +
      "from " +
      "( " +
          "select C.T_PP_CODE, C.T_ID as T_SUBJ_CODE, 1 as T_SUBJ_TYPE " +
          "from dCNG_Country_tmp C " +
          "where C.T_PP_CODE <> '' " +
        "union " +
          "select C.T_PP_CODE, C.T_ID as T_SUBJ_CODE, 4 as T_SUBJ_TYPE " +
          "from dCNG_City_tmp C " +
          "where C.T_PP_CODE <> '' " +
        "union " +
          "select B.T_PP_CODE, C.T_ID as T_SUBJ_CODE, 1 as SUBJ_TYPE " +
          "from dCNG_Banks_tmp B, dCNG_Country_dbt C " +
          "where C.T_PP_CODE = B.T_PP_CODE " +
        "union " +
          "select B.T_PP_CODE, C.T_ID as T_SUBJ_CODE, 4 as SUBJ_TYPE " +
          "from dCNG_Banks_tmp B, dCNG_City_dbt C " +
          "where C.T_PP_CODE = B.T_PP_CODE"
      ") " +
      "order by T_PP_CODE, T_SUBJ_TYPE desc"
    );

    cmd.execute( );

    var rs = RsdRecordset( cmd );

    while ( rs.moveNext( ) )

      ppCode = rs.value( "T_PP_CODE" );
      subjCode = rs.value( "T_SUBJ_CODE" );
      subjType = Int( rs.value( "T_SUBJ_TYPE" ) );

      cmd2 = RsdCommand( "select * "
                         "from dcng_currency_dbt cng_currency "
                         "where cng_currency.t_subj_code = :subj_code and "
                               "cng_currency.t_subj_type = :subj_type" );

      cmd2.addParam( "subj_code", RSDBP_IN, subjCode );
      cmd2.addParam( "subj_type", RSDBP_IN, subjType );

      rs2 = RsdRecordset( cmd2 );

      cmd2.execute( );

      // Для города страны есть валюта
      if ( rs2.moveNext( ) )
        cmd2 = RsdCommand( "select cng_banks.t_id as pointId "
                           "from dcng_banks_tmp cng_banks "
                           "where cng_banks.t_pp_code = :pp_code" );

        cmd2.addParam( "pp_code", RSDBP_IN, ppCode );

        rs2 = RsdRecordset( cmd2 );

        if ( rs2.moveNext( ) )
          // Удаляем валюты для точки
          pointId = rs2.value( "pointId" );

          cmd2 = RsdCommand( "delete from dcng_currency_dbt cng_currency "
                             "where cng_currency.t_subj_type = 3 and "
                                   "cng_currency.t_subj_code = :pointId" );

          cmd2.addParam( "pointId", RSDBP_IN, pointId );

          cmd2.execute( );

          // Копируем валюты для точки из страны или города
          cmd2 = RsdCommand( "insert into dcng_currency_dbt "
                             "( t_subj_type, t_subj_code, t_is_payment, t_code_currency ) "  
                             "select 3, :pointId, cng_currency2.t_is_payment, cng_currency2.t_code_currency "
                             "from dcng_currency_dbt cng_currency2 "
                             "where cng_currency2.t_subj_code = :subj_code and "
                                   "cng_currency2.t_subj_type = :subj_type" );

          cmd2.addParam( "pointId", RSDBP_IN, pointId );
          cmd2.addParam( "subj_code", RSDBP_IN, subjCode );
          cmd2.addParam( "subj_type", RSDBP_IN, subjType );

          cmd2.execute( );
        end;
      end;
    end;
  end;
       
  //-----------------------------------------------------------------------------
  macro fillIdCngBnk

    var cmdDeleteCngBnk;

    cng_id_type.Clear( );
    cng_id_type.KeyNum = 0;

    while ( cng_id_type.next )
      typedul_cng_bnk.Clear( );
      typedul_cng_bnk.KeyNum = 0;

      typedul_cng_bnk.rec.type_cng = cng_id_type.rec.id;

      if ( typedul_cng_bnk.GetEQ( ) )
        ;
      else
        wasAddCngTypeDUL = true;

        typedul_cng_bnk.Clear( );
        typedul_cng_bnk.rec.type_cng = cng_id_type.rec.id;
        typedul_cng_bnk.rec.type_bnk = -1;

        typedul_cng_bnk.Insert( );
      end;
    end;

    cmdDeleteCngBnk = RsdCommand( "delete from dtypedul_cng_bnk_dbt typedul_cng_bnk where not exists ( select 1 from dcng_id_type_dbt cng_id_type where cng_id_type.t_id = typedul_cng_bnk.t_type_cng )" );

    cmdDeleteCngBnk.execute;
  end;

  //-----------------------------------------------------------------------------
  private macro GetLoadingOper(): integer
    var loading_oper = 0;
    var cmd = RsdCommand(
        "select t_LoadingOper from drmtr_ps_dbt " +
        "where t_PsCode = 'CNG' ");
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      loading_oper = rs.value(0);
    end;

    return loading_oper;
  end;

  //-----------------------------------------------------------------------------
  private macro SetLoadingOper(loading_oper:integer)
    var cmd = RsdCommand(
        "update drmtr_ps_dbt " +
        "set t_LoadingOper = ? " +
        "where t_PsCode = 'CNG' ");
    cmd.addParam("LoadingOper", RSDBP_IN, loading_oper);
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  private macro ClearLoading()
    var cmd = RsdCommand(
        "update drmtr_ps_dbt " +
        "set t_LoadingOper = 0, t_FL_FQuantity = 0, t_FL_LastFraction = 0, t_FL_GUID = chr(1) " +
        "where t_PsCode = 'CNG' ");
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  private macro CheckConcurent(locker:TFSLocker)
    var locked = locker.Lock();
    var loading_oper = GetLoadingOper();
    
    if (not locked or ((loading_oper != 0) and (loading_oper != {oper})))
      if (not locked)
        loading_oper = locker.ReadLocker();
      end;
      
      DoError("Процедуру загрузки выполняет " + GetFioOper(loading_oper) + " (" + loading_oper + ").|Продолжение невозможно.");
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro CheckForAbort()
    var loading_oper = GetLoadingOper();
    
    if (loading_oper != {oper})
      if (loading_oper == 0)
        DoError("Процедура обновления прервана по требованию пользователя");
      else
        DoError("Управление загрузкой перехвачено операционистом:|" + GetFioOper(loading_oper) + " (" + loading_oper + ").");
      end;
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro IsFractionLoading(): bool
    FractionLoading = false;
    var cmd = RsdCommand(
        "select t_FractionLoading, t_FL_FQuantity, t_FL_LastFraction, t_FL_GUID from drmtr_ps_dbt " +
        "where t_PsCode = 'CNG' ");
    cmd.NullConversion = true;
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext() and (rs.value(0) != ""))
      FractionLoading = true;
      FL_FQuantity = rs.value(1);
      FL_LastFraction = rs.value(2);
      FL_GUID = rs.value(3);
    end;

    return FractionLoading;
  end;

  //-----------------------------------------------------------------------------
  private macro CountLoadedFractions(): integer
    if ((FL_FQuantity == 0) or (FL_LastFraction == 0))
      return 0;
    end;

    FL_LastFraction = 0;
    while (FL_LastFraction <= FL_FQuantity)
      var name = FLPath + "FL_" + FL_GUID + "_" + string(FL_LastFraction + 1) + ".xml";
      if (not ExistFile(name))
        break;
      end;
      FL_LastFraction = FL_LastFraction + 1;
    end;

    return FL_LastFraction;
  end;

  //-----------------------------------------------------------------------------
  private macro SaveFraction(xml:string)
    FL_LastFraction = FL_LastFraction + 1;
    
    var name = FLPath + "FL_" + FL_GUID + "_" + string(FL_LastFraction) + ".xml";
    var f = TStreamDoc(name, "W");
    f.WriteLine(xml);

    var cmd = RsdCommand(
        "update drmtr_ps_dbt " +
        "set t_FL_FQuantity = ?, t_FL_LastFraction = ?, t_FL_GUID = ? " +
        "where t_PsCode = 'CNG' ");
    cmd.addParam("FL_FQuantity", RSDBP_IN, FL_FQuantity);
    cmd.addParam("FL_LastFraction", RSDBP_IN, FL_LastFraction);
    cmd.addParam("FL_GUID", RSDBP_IN, FL_GUID);
    cmd.NullConversion = true;
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  private macro ReadFraction(): TResponse
    var xml = "";
    
    var name = FLPath + "FL_" + FL_GUID + "_" + string(FL_LastFraction) + ".xml";
    var f = TStreamDoc(name, "R");
    while (f.ReadLine())
      xml = xml + f.str;
    end;

    var resp = TResponse;
    CheckError(RtXml2Rsl(xml, resp),
        "Ошибка формата сохраненного ответного XML-сообщения, файл " + name);
    return resp;
  end;

  //-----------------------------------------------------------------------------
  macro UpdateTrn
    if (Response.attr.FULL != 0)
      ClearWorkTables();
    end;
    
    var datapacket;
    var obj;
    var table, tables = Response.GetTables();
    for (table, tables)
      datapacket = GenGetProp(Response, table);
      if ((datapacket != null) and (datapacket != ""))
        obj = MakeTableObject(table);

        if (Response.attr.FULL == 0)
          obj.Erase();
          obj.DeleteChildren();
        end;

        obj.Merge();
      end;
    end;

    obj = TCng_Attr_Grps;
    obj.Merge();
  
    obj = TCng_Currency;
    obj.Merge();
  
    obj = TCng_Attr_Chng;
    obj.Merge();

    pointsWithoutAddress( );

    fillIdCngBnk( );

    SaveVersion(Response.attr.VERSION);
  
  OnError(err)
    AddErrMessage(err.Message);
    AbortTrn();
  end;


  //-----------------------------------------------------------------------------
  macro GetChanges(version)
    var locker = TFSLocker(FLPath + "cng_refload.txt");
    CheckConcurent(locker);
    SetLoadingOper({oper});
    
    var req;
    Response = TResponse;

    ClearTempTables();

    var table, tables = Response.GetTables();

    var ver;

    RTsessionActionMess( "RtlGrPr=ContactNG:загр.справ." );

    if (UseLocalFiles)
      Response.attr.FULL = 1;
      Response.attr.VERSION = 0;
      
      for (table, tables)
        ver = ProcessDbf(table);
        if (ver != 0)
          GenSetProp(Response, table, string(ver));
        end;
        
        if (Response.attr.VERSION < ver)
          Response.attr.VERSION = ver;
        end;
      end;
    else
      if (not IsFractionLoading())  // загрузка целиком
        req = TGet_Changes;
        req.attr.VERSION = version;

        Service = CNGService;
        Service.TimeOut = ServiceTimeOut;
        UpdateStatusLine("Запрос к КЦ...");
        service.Request(req, Response, true);

        if (Response.attr.RE != 0)
          DoError("Ошибка обновления справочников Contact NG (" + Response.attr.RE + ") " + Response.attr.ERR_TEXT);
        end;

        CheckForAbort();
        
        for (table, tables)
          ProcessTable(table, GenGetProp(Response, table), req.attr.PACK);
        end;
      else  // загрузка по частям
        var isResponseFullForPart = false;

        Service = CNGService;
        Service.TimeOut = ServiceTimeOut;

        CountLoadedFractions();
        
        while ((FL_LastFraction < FL_FQuantity) or (FL_LastFraction == 0))  // загрузка недостающих частей
          req = TGet_Changes_Portion;
          req.attr.VERSION = version;
          if (FL_LastFraction == 0)
            req.attr.PART = 0;
          else
            req.attr.BOOK_ID = FL_GUID;
            req.attr.PART = FL_LastFraction + 1;
          end;

          Response = TResponse;

          var inXMLRequest = RtRsl2Xml(req, "REQUEST");
          var outXMLResponse;
          UpdateStatusLine("Запрос к КЦ...");
          Service.TransmitToWebService(inXMLRequest, outXMLResponse, true);
          CheckError(RtXml2Rsl(outXMLResponse, Response),
              "Ошибка формата ответного XML-сообщения");

          if (Response.attr.RE != 0)
            DoError("Ошибка обновления справочников Contact NG (" + Response.attr.RE + ") " + Response.attr.ERR_TEXT);
          end;

          if ( ( Response.attr.PART == 0 ) and ( ValType( Response.attr.FULL ) == V_INTEGER ) and ( Response.attr.FULL == 1 ) ) // Прислали все сразу
             CheckForAbort();

             for (table, tables)
               ProcessTable(table, GenGetProp(Response, table), req.attr.PACK);
             end;

             isResponseFullForPart = true;

             break;
          end;
          
          if (Response.attr.PART != FL_LastFraction + 1)
            DoError("Вместо части " + string(FL_LastFraction + 1) + " получена часть " + Response.attr.PART);
          end;

          if (FL_LastFraction == 0)
            FL_GUID = Response.attr.BOOK_ID;
            FL_FQuantity = Response.attr.TOTAL;
          end;

          CheckForAbort();
          
          SaveFraction(outXMLResponse);

          if (FL_LastFraction == FL_FQuantity)
            break;
          end;
        end;

        if ( not isResponseFullForPart )

          CheckForAbort();
          
          FL_LastFraction = 1;
          while (FL_LastFraction <= FL_FQuantity)
            var resp = ReadFraction();
            if (FL_LastFraction == 1)
              Response = resp;
            end;

            for (table, tables)
              var datapacket = GenGetProp(resp, table);
              if ((datapacket != null) and (datapacket != ""))
                ProcessTable(table, datapacket, "ZLIB");
                GenSetProp(Response, table, string(version));

                CheckForAbort();
              end;
            end;

            FL_LastFraction = FL_LastFraction + 1;
          end;
        end;
      end;
    end;

    RTsessionActionEnd();

    CheckForAbort();
    
    message("Обновление рабочих справочников...");
    CheckError(ProcessTrn(0, R2M(this, "UpdateTrn")), "Ошибка обновления справочников Contact NG");
    message(" ");
    ClearLoading();

  /*onError(err)
    ClearLoading();
    runError(err);*/
  end;
  
  //-----------------------------------------------------------------------------
  macro Check(version)
    var req = TCheck;
    req.attr.VERSION = version;

    Response = TResponse;

    Service = CNGService;
    Service.Request(req, Response, true);

    return Response;
  end;
  
end;



//-------------------------------------------------------------------------------------------------
// 
//-------------------------------------------------------------------------------------------------

checkPing( );

var obj = TAbonentObject;
var res;

var ver = obj.GetCurrentVersion();

if (UseLocalFiles)
  if (ver == 0)
    obj.GetChanges(0);
  end;
else
  res = obj.Check(ver);

  if (res.attr.RE == 0)
    obj.GetChanges(ver);
  elif (res.attr.RE != -9999)
    DoError("Ошибка обновления справочников Contact NG (" + res.attr.RE + ") " + res.attr.ERR_TEXT);
  end;
end;

if ( wasAddCngTypeDUL )
  MsgBox( "Производилось обновление списка типов ДУЛ системы Contact|Не забудьте самостоятельно скорректировать таблицу соответствий");
end;

onError(err);
  if ( Index( StrUpr( err.Message ), "SQL ERROR" ) > 0 )
    ShowError("Произошла ошибка во время загрузки справочников Contact NG|Обратитесь к разработчику");
  else
    ShowError(err.Message);
  end;
  
