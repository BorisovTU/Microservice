import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpReceiveFromCC( p_opNum, p_subOpNum, p_psId, p_transfer )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;

  if ( opNum == 0 )
    opNum = MTOP_entry;
    subOpNum = MTSUBOP_entry_pay;
  end;
   
  // =====================================================================
  macro startInit( )

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

    return true;
  end;
   
  // =====================================================================
  macro finalInit( )

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro processClientInfo : bool

    var depclnt = TClientList;
    var stat = true;

    if ( transfer.rec.recPaperNumber != "" )
      transfer.rec.recPartyID = depclnt.FindCodeByPaper( transfer.rec.recPaperKind, transfer.rec.recPaperSeries, transfer.rec.recPaperNumber );
    end;

    // Клиент по документу не найден, заведем нового -------------------
    if ( transfer.rec.recPartyID == 0 )
      depclnt.CurRec.Clear( );

      depclnt.CurRec.rec.CodClient = 0;

      depclnt.CurRec.rec.Name1 = transfer.rec.recName1;
      depclnt.CurRec.rec.Name2 = transfer.rec.recName2;
      depclnt.CurRec.rec.Name3 = transfer.rec.recName3;

      depclnt.CurRec.rec.Born = transfer.rec.recBorn;

      if ( transfer.rec.recNotResident != 0 )
        depclnt.CurRec.rec.NotResident = "X";
      end;
      depclnt.CurRec.rec.NRCountry = transfer.rec.recCitizenship;

      if ( ( transfer.rec.recPaperSeries == "" ) and ( transfer.rec.recPaperNumber == "" ) )
        depclnt.CurRec.rec.PaperKind = -1;
      else
        depclnt.CurRec.rec.PaperKind       = transfer.rec.recPaperKind;
        depclnt.CurRec.rec.PaperSeries     = transfer.rec.recPaperSeries;
        depclnt.CurRec.rec.PaperNumber     = transfer.rec.recPaperNumber;
        depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
        depclnt.CurRec.rec.PaperIssuer     = transfer.rec.recPaperIssuer;
        depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.recPaperIssuerCode;
        depclnt.CurRec.rec.IsMain          = "X";
      end;
          
      if ( ( transfer.rec.recCountry == "" ) and ( transfer.rec.recPostIndex == "" ) and ( transfer.rec.recAdress == "" ) and ( transfer.rec.recPhoneNumber == "" ) )
        ;
      else
        depclnt.CurRec.rec.AdressType  = 1;
        depclnt.CurRec.rec.Country     = transfer.rec.recCountry;
        depclnt.CurRec.rec.PostIndex   = transfer.rec.recPostIndex;
        depclnt.CurRec.rec.CodeRegion  = transfer.rec.recRegion;
        depclnt.CurRec.rec.Place       = transfer.rec.recPlaceName;
        depclnt.CurRec.rec.lCodePlace  = transfer.rec.recPlace;
        depclnt.CurRec.rec.Address     = transfer.rec.recAdress;
        depclnt.CurRec.rec.PhoneNumber = transfer.rec.recPhoneNumber;
      end;

      depclnt.CurRec.rec.FNcash = transfer.rec.branch;
      
      depclnt.MayInsert = true;

      stat = depclnt.Insert( );

      if ( stat )
        transfer.rec.recPartyID = depclnt.CurRec.rec.CodClient;
      else
        PushErrorMessage( "Ошибка при вставке нового клиента" );
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro check( )

    var cmd;
    var rs;
    var stat = false;

    // Найдем операцию -------------------------------------------------
    cmd = RsdCommand( "select * " + 
                      "from drmto_trans_dbt rmto_trans " +
                      "where rmto_trans.t_branch = :branch and " +
                            "rmto_trans.t_psId = :psId and " +
                            "rmto_trans.t_transCode = :transCode" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "branch", RSDBP_IN, transfer.rec.branch );
    cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
    cmd.addParam( "transCode", RSDBP_IN, transfer.rec.transCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      stat = false;
      PushErrorMessage( "Перевод c идентификатором \"" + transfer.rec.transCode + "\" уже принят из КЦ" );
    else
      stat = true;
    end;

    // Заведем клиента в базу ------------------------------------------
    if ( stat and ( transfer.rec.recPartyID == 0 ) )
      stat = processClientInfo( );
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 4 );
    var doc;
    var stat = true;
    var statOpenAcc = 0;
    var cmd, rs;

    var vReferenc = 0;
    var vAccount = "";
    var vTypeAccount = "";
    var vNeedOpenAccount = 0;
    var vNeedCloseAccount = 0;
    var clientForDepAcc = defCodeServiceClient( false );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.recPartyID;
    end;

    // Определим вид вклада и прочие параметры -------------------------
    vTypeAccount = determineTypeAccount( true, transfer.rec.curCode );

    if ( system.rec.closeAccFlag == "" )
      cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                        "from ddepositr_dbt depositr "
                        "where depositr.t_CodClient = ? and "
                              "depositr.t_FNCash = ? and "
                              "depositr.t_Open_Close = chr( 0 ) and "
                              "depositr.t_Type_Account = ? and "
                              "depositr.t_Code_Currency = ? and "
                              "depositr.t_Action != 2 "
                        "order by depositr.t_CodClient" );
                                  
      cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
      cmd.addParam( "FNCash", RSDBP_IN, transfer.rec.branch );
      cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
      cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

      rs = RsdRecordSet( cmd );

      cmd.Execute( );

      if ( rs.moveNext )
        vAccount = rs.value( "Account" );
        vReferenc = rs.value( "Referenc" );
      else
        vAccount = "";
      end;
    end;

    if ( system.rec.closeAccFlag != "" )
      vNeedOpenAccount = 1;
      vNeedCloseAccount = 1;
    else
      vNeedCloseAccount = 0;

      if ( vAccount != "" )
        vNeedOpenAccount = 0;
      else
        vNeedOpenAccount = 1;
      end;
    end;

    // Если нужно открыть вкладной счет, откроем его -------------------
    if ( vNeedOpenAccount == 1 )
      depositr.Clear;
      depositr.rec.Type_Account  = vTypeAccount;
      depositr.rec.Code_Currency = transfer.rec.curCode;
      depositr.rec.CodClient     = clientForDepAcc;
      depositr.rec.Open_Date     = {curdate};

      statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

      if ( statOpenAcc != 0 )
        stat = false;
        if ( statOpenAcc > 100 )
          PushErrorMessage( statOpenAcc );
        else
          PushErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
        end;
      else
        vReferenc = depositr.rec.Referenc;
        vAccount = depositr.rec.Account;
      end;
    end;

    if ( stat )
      transfer.rec.accRef = vReferenc;
    end;
    
    // Поступление срочного перевода из КЦ на выдачу -------------------
    if ( stat )
      if ( vNeedOpenAccount == 1 )
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 1 );
        else
          doc = newDepDoc( 2 );
        end;
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 3 );
        else
          doc = newDepDoc( 4 );
        end;
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Поступление срочного перевода из КЦ на выдачу";

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при вставке документа в список" );
      end;
    end;

    // Комиссия получателю за выдачу срочного перевода -----------------
    if ( stat and ( transfer.rec.sumCommisOur != 0 ) )
      if ( transfer.rec.curCodeCommis == 0 )
        doc = newPayDoc( 5 );
      else
        doc = newPayDoc( 6 );
      end;

      if ( transfer.rec.curCodeCommis == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;

      doc.rec.CodCur = transfer.rec.curCodeCommis;
      doc.rec.InSum = transfer.rec.sumCommisOur;
      doc.rec.Ground = "Комиссия получателю за выдачу срочного перевода";
      doc.rec.Action = D_INSERT;
      doc.rec.VidDoc = StrFor( 1 ); // Безналичный
      doc.rec.DebetCredit = DOC_CREDIT; // Приходный документ

      initClientInfoInPayDoc( doc, true );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при вставке документа в список" );
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro runPanel( )

    setFlagOperationPanel( false );

    return true;
  end;

  // =====================================================================
  macro errorMessage( )
    ;
  end;
 
  // =====================================================================
  InitTMtOperation( p_psId, NULL, NULL, NULL, p_transfer );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )
         
  var initTransfer = TMtTransfer( );
  var opReceiveFromCC = NULL;

  initTransfer.Clear( );
  initTransfer.rec.branch = NumFNcash( );
  initTransfer.rec.psId = psId;
  /*
  initTransfer.rec.transCode = "1918";
  initTransfer.rec.ourFlag = 0;
  initTransfer.rec.accRef = 0;
  initTransfer.rec.sendDate = {curdate};
  initTransfer.rec.curCode = 0;
  initTransfer.rec.sum = $51;
  initTransfer.rec.curCodeCommis = 0;
  initTransfer.rec.sumCommisOur = $5.1;
  initTransfer.rec.state = "ONSET_____";
  initTransfer.rec.destCode = 0;
  initTransfer.rec.action = D_INSERT;

  initTransfer.rec.senderName1 = "От";
  initTransfer.rec.senderName2 = "Кого";
  initTransfer.rec.senderName3 = "Пришло";

  initTransfer.rec.recNPflag = 1;
  initTransfer.rec.recName1 = "Этот";
  initTransfer.rec.recName2 = "Вкладчик";
  initTransfer.rec.recName3 = "Тестовый";
  initTransfer.rec.recBorn = Date( 12, 12, 1970 );
  initTransfer.rec.recCitizenship = "JPN";
  initTransfer.rec.recNotResident = 0;
  initTransfer.rec.recPaperKind = 0; // Паспорт гаржданина РФ
  initTransfer.rec.recPaperSeries = "126675";
  initTransfer.rec.recPaperNumber = "45722";
  initTransfer.rec.recPaperIssuedDate = Date( 10, 10, 2001 );
  initTransfer.rec.recPaperIssuer = "ОВД Васильевского острова";
  initTransfer.rec.recCountry = "RUS";
  initTransfer.rec.recPlace = "Москва";
  initTransfer.rec.recAdress = "г Москва, ул Вострухина, д. 5, кв. 1656";
  */

  opReceiveFromCC = TMtOpReceiveFromCC( opNum, subOpNum, psId, initTransfer );

  return opReceiveFromCC;
end;
                                          	
