/*                                                                */
/* Товары поставщиков.                                            */
/*                                                                */
/* R_VEWARE.MAC                                                   */
/*                                                                */
/* 21.10.97 А.Лебедев                                             */
/*                                                                */
/*
import   "FUN_LVM.MAC" ;
*/
import  LvmGlVar
        ,"L_CONST.MAC"
        ,"L_GODFNC.MAC"
        ,"clientba.mac"
        ,"L_NMCRNC.MAC"
        ,"L_NMWARE.MAC"
       ;



file wareprc ("wareprc.dbt", "balance.def")  KEY  2; /* Ware, Clnt, KodPrice, Currency */
file kodprice ("kodprice.dbt", "balance.def");



macro PrintTitle
[----------------------------------------------------- ########## - ######## --

                              ТОВАРЫ ПОСТАВЩИКОВ

 +----------------+----------------+-----------------------+------------------+
 |           Код товара            |                       |                  |
 |  у поставщика  |  по каталогу   |      Поставщик        |       Цены       |
 +----------------+----------------+-----------------------+------------------+]
 (Date(), Time());
end;



macro PrintLine (addr)
record veware ("vendware.dbt", "balance.def");
var    rc;
var    a;
var    szname, szcurr, szprice, fn, c, p;
array  aPrice, aClient;
var    ClientName ;

  /* настроить буфер */
  setbuff (veware, addr);

  /* наименование клиента */

ClientName = GetClientName(0, veware.Clnt );

  /* зачитать цены в массив */
  wareprc.Ware     = veware.Ware;
/*
  wareprc.ClntDprt = veware.ClntDprt;
*/
  wareprc.Clnt     = veware.Clnt;
  wareprc.KodPrice = 0;
  wareprc.Currency = 0;
  rc = GetGE (wareprc);
  a = 0;
  aSize (aPrice, a);
  while ((rc) and
         (wareprc.Ware     == veware.Ware) and
/*         (wareprc.ClntDprt == veware.ClntDprt) and */
         (wareprc.Clnt     == veware.Clnt))
    aPrice(a + 0) = wareprc.KodPrice;
    aPrice(a + 1) = wareprc.Currency;
    aPrice(a + 2) = wareprc.Price;
    a = a + 3;
    rc = Next (wareprc);
  end;

  /* разбить имя клиента */
  StrSplit (ClientName, aClient, 23);/*, 23, aSize (aPrice)/3);*/

  fn = FALSE;
  kodprice.KodPrice = -1300;
  currency.FIID = -1300;
  rc = TRUE;
  c = 0; p = 0;
  while (rc or (c < aSize(aClient)) or (p < aSize(aPrice)))
    /* выделить строку для имени */
    if (c < aSize (aClient))
      szname = aClient(c);
      c = c + 1;
    else
      szname = "";
    end;

    if (p < aSize(aPrice))
      if ((NOT fn) and
          ((p < 3) or (aPrice (p + 0) != aPrice (p-3 + 0))))
        fn = TRUE;
        kodprice.KodPrice = aPrice (p + 0);
        if (NOT GetEQ(kodprice))
          kodprice.NamePrice = "";
        end;
        szprice = kodprice.NamePrice;
        szcurr = "";
        currency.FIID = -1300;
      else
        fn = FALSE;
        if ((p < 3) or (aPrice (p + 1) != aPrice (p-3 + 1)))
          currency.FIID = aPrice (p + 1);
            szcurr = GetCompresNameCurrency (aPrice (p+1));
        end;
        szprice = aPrice (p + 2);
        p = p + 3;
      end;
    else
      szcurr  = "";
      szprice = "";
    end;

    if (rc)  println();  end;
    /* вывод одной строки */
[ ################ ################ ####################### ### ############## ]
    (veware.VendKod, veware.Ware,
     szname, szcurr, szprice:r);

    /* переход на следующий шаг цикла */
    rc = FALSE;
    veware.VendKod   = "";
    veware.Ware      = "";
    veware.Name_Vend = "";
  end;
end;



macro PrintEnd
[+----------------+----------------+-----------------------+------------------+];
end;

