/*                                                                */
/* Каталог товаров. Выполнение групповой установки флагов.        */
/*                                                                */
/* ENRENDWR.MAC                                                   */
/*                                                                */
/* Вывод списка необработанных товаров.                           */
/*                                                                */
/* 20.10.97 А.Лебедев                                             */
/*                                                                */


macro PrintTitle
[----------------------------------------------------- ########## - ######## --

              СПИСОК ТОВАРОВ, НЕ ОБРАБОТАННЫХ В ГРУППОВОЙ ОПЕРАЦИИ

 +-------------------------+--------------------------------------------------+
 |       Код товара        | Причина невыполнения, операция                  |
 +-------------------------+--------------------------------------------------+]
 (Date(), Time());
end;


/*
macro getfmsg (NumMsg)
file fmsg ("balance.msg");
  if (open (fmsg))
    fmsg.Number = NumMsg;
    if (GetEQ(fmsg))
     return  fmsg.Contets;
    end;
    return  "Сообщение N" + String(NumMsg);
  end;
end;

macro TextError (tstat)
  if (tstat > 0)
    return  "Ошибка Btrieve: " + String(tstat);
  elif (tstat < 0)
    return  getfmsg (-tstat);
  end;
  return  "(0)-неизвестная ошибка";
end;
*/

array aa;
macro Operation (textbits)
var pre = StrLen (textbits) + 1;
  if (SubStr (textbits, pre - 1, 1) == "1")
    aa (asize (aa)) = "Установка признака \"Товар является услугой\"";
  end;
  if (SubStr (textbits, pre - 2, 1) == "1")
    aa (asize (aa)) = "Установка признака \"Учет по комплектам\"";
  end;
  if (SubStr (textbits, pre - 3, 1) == "1")
    aa (asize (aa)) = "Установка признака \"Включать услугу в накладную\"";
  end;

  if (SubStr (textbits, pre - 5, 1) == "1")
    aa (asize (aa)) = "Снятие признака \"Товар является услугой\"";
  end;
  if (SubStr (textbits, pre - 6, 1) == "1")
    aa (asize (aa)) = "Снятие признака \"Учет по комплектам\"";
  end;
  if (SubStr (textbits, pre - 7, 1) == "1")
    aa (asize (aa)) = "Снятие признака \"Включать услугу в накладную\"";
  end;

  if (SubStr (textbits, pre - 9, 1) == "1")
    aa (asize (aa)) = "Перемещение (копирование) товаров.";
  end;
end;

macro SplitText (Text, len)
var i;
  i = 1;
  while (i < StrLen(Text))
    if ((SubStr (Text, i, 1) == "|") or (i > len))
      if (i > 1)
        aa (asize (aa)) = SubStr (Text, 1, i - 1);
      else
        aa (asize (aa)) = "";
      end;
      if (SubStr (Text, i, 1) == "|")
        Text = SubStr (Text, i + 1);
      else
        Text = SubStr (Text, i);
      end;
      i = 1;
    else
      i = i + 1;
    end;
  end;
  /* последний сегмент */
  aa (asize (aa)) = SubStr (Text, 1);
end;

macro PrintLine (Ware, bit, tstat, textbits, texterr)
var a;

  a = 0;
  aSize (aa, a);
  SplitText (texterr, 50);
[
  ######################### ################################################## ]
  (Ware, aa(a));
  a = a + 1;
  while (a < aSize (aa))
[                           ################################################## ]
    (aa(a));
    a = a + 1;
  end;

  a = 0;
  aSize (aa, a);
  Operation (textbits); /* зполнить массив операций */
  while (a < aSize (aa))
[                           ################################################## ]
    (aa(a));
    a = a + 1;
  end;
end;



macro PrintEnd
[+-------------------------+--------------------------------------------------+];
end;

