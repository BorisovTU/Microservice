import InsCarryDoc, "dv_categ.mac", "dv_car.mac";

private var CarryError = "";

PRIVATE MACRO SayCarryError( error )
   if( isOprMultiExec() == false )
      msgbox( error );
   else
      CarryError = error;
   end;
   return false;
END;

private macro GetClientCodeByID( ClientID:integer ):string
  var Sql = " SELECT c.t_Code " +
            "   FROM dpartcode_dbt c " +
            "  WHERE c.t_PartyID = ? " +
            "    AND c.t_CodeKind = 1 ";

  var Query = DL_RSDCommand(Sql);
  Query.addParam(ClientID);

  var rs = Query.Execute();

  if( rs.MoveNext() )
     return rs.Code;
  end;

  return "";  
end;

private macro РасчетнаяОперацияВУ( accTrn :variant /*: RsbAccTransaction*/, FD, ВидРО ) : bool
  var InnAcc = TRecHandler("dlinacc.dbt");
  var DealSum = $0, spground, dealcode = ""; 

  InnAcc.rec.ID         = 0;  
  InnAcc.rec.Boffice    = OBJTYPE_BACKOFFICE_DV;
  InnAcc.rec.OperType   = ВидРО;
  InnAcc.rec.DocumentID = FD.ID;
  
  InnAcc.rec.AccTrnID   = accTrn.AccTrnID;
  InnAcc.rec.Date       = accTrn.Date_Carry; 
  InnAcc.rec.Chapter    = accTrn.Chapter;
  InnAcc.rec.DebAcc     = accTrn.AccountPayer;
  InnAcc.rec.KredAcc    = accTrn.AccountReceiver;
  
  if (accTrn.Chapter == 22)
   InnAcc.rec.FIKind    = FIKIND_DERIVATIVE;
  else
   InnAcc.rec.FIKind    = FIKIND_CURRENCY;
  end;
   
  InnAcc.rec.FIID       = accTrn.FIIDPayer;
  InnAcc.rec.Sum        = accTrn.SumPayer;
  InnAcc.rec.Department = accTrn.Department;
  InnAcc.rec.Oper       = accTrn.Oper;
  InnAcc.rec.DocumentKind = FD.Kind;

  /*за дату проводки InnAcc.Date ищем документ вида "Распорядительная записка"*/
  InnAcc.rec.GroundKind = 27;
  spground = TRecHandler("spground.dbt");

  if( GetDV_MaxGroundByDeal( spground, InnAcc.rec.DocumentID, InnAcc.rec.DocumentKind, InnAcc.rec.GroundKind, InnAcc.rec.Date ) )
     InnAcc.rec.GroundNum = spground.rec.XLD;
  end;

  InnAcc.rec.PartyID  = UnknownParty;
  InnAcc.rec.PlaceID  = UnknownParty;

  InnAcc.rec.DealKind = FD.Kind;
  dealcode            = FD.DealCode();/*номер внебиржевой операции*/

  var dlinaccCache = RsbSQLInsert("dlinacc.dbt");
  var realIDs = TArray();
  dlinaccCache.AddRecord(InnAcc);
  dlinaccCache.AddReturning( "t_id", V_INTEGER );
  if(not dlinaccCache.insert())
     return false;
  else
     dlinaccCache.GetReturning( realIDs );
     var cmd = RsdCommand("UPDATE ddlinacc_dbt SET t_FMTBLOBDATA_XXXX = utl_raw.cast_to_raw(\'"+dealcode+"\') WHERE t_ID = ?");
     cmd.addParam("", RSDBP_IN, realIDs[0]);
     cmd.Execute();
  end;

  return true;
end;

/* Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
PRIVATE MACRO Проводка( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                        Chapter ,FIID, СуммаОперации, docum,
                        Result_Carry, Kind_Oper,Numb_Document,Ground,
                        PaymentID, FIRolePayer, FIRoleReceiver, trnId:@integer,
                        СчетДебетFIID, СчетКредитFIID, FIID2, СуммаОперации2,
                        PaymID_type, FD2, IsInnerCarry, ВидРО,
                        SideTransaction, ExRateExtra, DebPairAcc, CredPairAcc,
                        IsSumEqDebet, NatSum:@money, Date_Rate:date, Rate:double, IsInverse:bool,
                        IsSummaryCarry:bool, /*Признак сводной проводки*/
                        DebAccNumber:@string, CredAccNumber:@string, SkipRateCarry:bool
                      )
 var stat:bool = true, CуммаДебет = $0, СуммаКредит  = $0, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";
 var tr, NoCarry:bool = false;
 var PaymentObj = null;
 var ID_Operation = 0, ID_Step = 0;
 var _FD;
 var MassTransMode = false;

 record AccountPayer(account);
 record AccountReceiver(account);

 CarryError = "";

 if( IsSummaryCarry == null )
    IsSummaryCarry = false;
 end;

 if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
    return SayCarryError( "В проводке не задана ни одна из сумм." );
 end;

 if ( isOprMultiExec() )
   var Query =   " SELECT oprtemp.t_ID_Operation, oprtemp.t_ID_Step, oprtemp.t_Dockind, koper.t_Systypes " +
                 "   FROM doprtemp_view oprtemp, doproper_dbt oper, doprkoper_dbt koper " +
                 "   WHERE oprtemp.t_ID_Operation =  oper.T_ID_Operation " +
                 "   AND oper.T_Kind_Operation = koper.T_Kind_Operation ";    
   var cmd = DL_RSDCommand(Query); 
   var DataSet = cmd.Execute();
   if( DataSet.moveNext() )
      if( (DataSet.DocKind == 194) and (DataSet.Systypes == "") )
         MassTransMode = true;
         ID_Operation = DataSet.ID_Operation;
         ID_Step      = DataSet.ID_Step;
      end;
   end;
 end;
  
 /*оставить только копейки*/
 /*для первой суммы, если она задана*/
 if( СуммаОперации != null )
    СуммаОперации = money( round( СуммаОперации, 2 ) );    
    if( СуммаОперации < $0 )  
       return SayCarryError( "Сумма проводки < 0." );
    end;
 else  
   СуммаОперации = $0;
 end;
 /*для второй суммы, если она задана*/
 if( СуммаОперации2 != null )
    СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
    if( СуммаОперации2 < $0 )  
       return SayCarryError( "Вторая сумма проводки < 0." );
    end;
 else
   СуммаОперации2 = $0;
 end;

 if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
    return true;
 end;

 if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
    if( (not FD.IsExistAccount(СчетДебет, null, true, FIRolePayer, AccountPayer, DebPairAcc, ДатаВалютирования, СчетДебетFIID)) and
        (not FD.OpenAccount(СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, null/*PayerMcAccDoc*/, DebPairAcc))
      )
       if( not FD.ReOpenAccount(СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, DebPairAcc) )
           return SayCarryError( "Ошибка при определении счета по категории " + СчетДебет + " в проводке.");
       end;
    end;
 else
    copy( AccountPayer, СчетДебет );
 end;

 if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
    if( FD2 == null )    
      _FD = FD;
    else
      _FD = FD2;
    end;
    if( (not _FD.IsExistAccount(СчетКредит, null, true, FIRoleReceiver, AccountReceiver, CredPairAcc, ДатаВалютирования, СчетКредитFIID)) and
        (not _FD.OpenAccount(СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, null/*ReceiverMcAccDoc*/, CredPairAcc))
      )
       if( not _FD.ReOpenAccount(СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, CredPairAcc) )
           return SayCarryError( "Ошибка при определении счета по категории " + СчетКредит + " в проводке." );
       end;
    end;
 else
    copy( AccountReceiver, СчетКредит );
 end;
 
 if( PaymID_type == null )
    PaymID_type = PMMET_ID;
 end;

 if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency )

    /*задана только вторая сумма в валюте одного из счетов*/
    if( (FIID == null) AND (FIID2 != null) )
       FIID = FIID2;
    elif( (FIID == null) AND (FIID2 == null) )
       return SayCarryError("Не задана ни одна из валют проводки.");
    end;

    if( СуммаОперации != $0 )
       CуммаДебет = СуммаКредит = СуммаОперации;
    elif( СуммаОперации2 != $0 )
       CуммаДебет = СуммаКредит = СуммаОперации2;
    end;

    if( (FIID != AccountPayer.Code_Currency) /*AND (FIID != AccountReceiver.Code_Currency)*/ )
       /* return SayCarryError( "В проводке не совпадают валюты счетов: " 
                                + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                                "| и валюта документа: " + string(FIID) );
       раньше здесь выдавали ошибку, теперь попробуем сконвертировать */
       if( not DV_SmartConvertSum(CуммаДебет, CуммаДебет, ДатаВалютирования, FIID, AccountPayer.Code_Currency, false) )
          return SayCarryError( GetCurrencyConvertErrorMsg(FIID, AccountPayer.Code_Currency, ДатаВалютирования) );
       end;
       СуммаКредит = CуммаДебет;
    end;

 else /* валюты разные - мультивалютная проводка */
     
    СуммаКредит = $0;
    CуммаДебет  = $0;
    
    /*задана одна сумма в валюте одного из счетов*/
    if( (FIID != null) AND (FIID2 == null) )
       if( FIID == AccountPayer.Code_Currency )
          CуммаДебет  = СуммаОперации;
       else
          СуммаКредит = СуммаОперации;
       end;
    /*задана только вторая сумма в валюте одного из счетов*/
    elif( (FIID2 != null) AND (FIID == null) )
       if( FIID2 == AccountPayer.Code_Currency )
          CуммаДебет  = СуммаОперации2;
       else
          СуммаКредит = СуммаОперации2;
       end;
    /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
    elif( (FIID2 != null) AND (FIID != null) )                         
      if( FIID == AccountPayer.Code_Currency ) 
         CуммаДебет  = СуммаОперации;
      elif( FIID2 == AccountPayer.Code_Currency ) 
         CуммаДебет  = СуммаОперации2;
      end;

      if( FIID == AccountReceiver.Code_Currency ) 
         СуммаКредит = СуммаОперации;
      elif( FIID2 == AccountReceiver.Code_Currency ) 
         СуммаКредит = СуммаОперации2;
      end;
    else
       return SayCarryError("Не задана ни одна из валют проводки.");
    end;

    if( (Rate == null) or (Rate == 0.0) )
       if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                   CуммаДебет, СуммаКредит, ДатаВалютирования ) )
          return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
       end;

       if ( (CуммаДебет > 10000000000000000) or (СуммаКредит > 10000000000000000) )
          return SayCarryError("Арифметическая операция: переполнение.");
       end;
    end;
 end;

 if( (IsSummaryCarry == false) AND 
     DV_НужнаСводнаяПроводка(FD, /*AccountPayer*/СчетДебет, /*AccountReceiver*/СчетКредит, Chapter/*, PayerMcAccDoc, ReceiverMcAccDoc*/)
   )
    stat = DV_ДобавитьСводнуюПроводку(FD, AccountPayer, AccountReceiver, ДатаВалютирования, CуммаДебет, СуммаКредит/*IIF(AccountPayer.Code_Currency == AccountReceiver.Code_Currency, $0, СуммаКредит)*/, Result_Carry, Ground);
    if( not stat )
       return SayCarryError("Ошибка при вставке документа сводной проводки.");
    else
       NoCarry = true; /*сформировали сводную проводку - ниже выполнять проводку не надо*/

       if ((FD.kind == 4813) or  (FD.kind == 4815) or (FD.kind == 199)) //teg 15.01.19 нельзя выкидывать 199 биржевые иначе не ставятся
	       NoCarry = false;
       end;	
    end;
 end;

 if( NoCarry == false )
    if( (PaymID_type != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )
       /*проводку формируем по платежу*/
       PaymentObj = RSBPayment( PaymentID );
       tr = PaymentObj.MakeTransaction();
    else
       /*проводку формируем без платежа*/
       if (MassTransMode)
          tr = RsbAccTransactionData();
       else
          tr = RsbAccTransaction();
       end;

       tr.Shifr_Oper = ""; // стираем, потом заполним правильно
    end;
            
    if( tr == NULL )
       return SayCarryError("Ошибка при создании проводки по платежу");
    end;
   
    tr.Chapter         = Chapter;
    tr.Date_Carry      = ДатаВалютирования;
    if( PaymentObj != null )
       tr.Number_Pack  = PaymentObj.NumberPack;
    end;
    tr.Numb_Document   = Numb_Document;
    tr.ResultCarry     = Result_Carry;
    tr.Kind_Oper       = Kind_Oper;
    tr.Ground          = Ground;
    tr.Department      = {OperDprt};
    //tr.Oper            = {oper};
    tr.FIIDPayer       = AccountPayer.Code_Currency;
    tr.FIIDReceiver    = AccountReceiver.Code_Currency;
    tr.AccountPayer    = AccountPayer.Account;
    tr.AccountReceiver = AccountReceiver.Account;
    if( tr.Shifr_Oper == "" )
       tr.Shifr_Oper = DL_GetShifrOper(Chapter, AccountPayer, AccountReceiver);
    end;
   //TEG 02.05.19 ролевая модель   
   /* Определяем конечного операциониста для проводки */
     if ((FD.kind==140) or (FD.kind==152) or (FD.kind==4626) or (FD.kind==4627) or (FD.kind==4607) or (FD.kind == 4633) or (FD.kind == 4632) )
         Tr.Oper = GetMainOperInGroup(FD.kind);
     elif  (FD.kind==194) 
         Tr.Oper = GetMainOperInGroup(FD.kind,FD.DVOPER.rec.operkind);
     elif(FD.kind==199)
       	   if(FD.ВидБазовогоАктива() == FIKIND_AVOIRISS)
              Tr.Oper = GetMainOperInGroup(101);
       	   else
              Tr.Oper = GetMainOperInGroup(FD.kind,FD.deal.rec.kind);
           end;
     else  
           Tr.Oper = GetMainOperInGroup(FD.kind,FD.deal.rec.kind);
     end;

     /*KD Определение операциониста по проволкам корректировки по ОХ */
     If( (FD.kind==199) and (FD.deal.rec.kind == 22720) )
        If((AccountPayer.balance == "47452") OR (AccountPayer.balance == "47447") OR (AccountPayer.balance == "47450") OR (AccountPayer.balance == "47445") OR  
        (AccountReceiver.balance == "47452") OR (AccountReceiver.balance == "47447") OR (AccountReceiver.balance == "47450") OR (AccountReceiver.balance == "47445")) 
           Tr.Oper = GetMainOperInGroup(102,12300);
        end;
        If( (AccountPayer.balance == "47451")  OR (AccountPayer.balance == "47446") OR (AccountReceiver.balance == "47451")  OR (AccountReceiver.balance == "47446")) 
           Tr.Oper = GetMainOperInGroup(109);
        end;

        If( (AccountPayer.balance == "51526")  OR (AccountPayer.balance == "51527") OR (AccountReceiver.balance == "51526")  OR (AccountReceiver.balance == "51527")) 
           Tr.Oper = GetMainOperInGroup(141);
        end;

        If( (AccountPayer.balance == "50428")  OR (AccountPayer.balance == "50429") OR (AccountReceiver.balance == "50428")  OR (AccountReceiver.balance == "50429")) 
           Tr.Oper = GetMainOperInGroup(141);
        end;

     end;

     if (Tr.Oper <= 0)
         Tr.Oper = {Oper};
     end;
   /**/
    //начнём процесс пачкования . Критерий Биржа/ВнебиржеваяСделкаСПФИ . Для Биржи -30 , внебиржа - 40 . Остальное - 35.
    if (StrUpr(genclassname(FD)) == StrUpr("DVFirstDocNDeal"))
      if (FD.Биржевая())
       tr.Number_Pack  = 35;
      else
       tr.Number_Pack  = 30;
      end;
      if(FD.ВидБазовогоАктива() == FIKIND_AVOIRISS)
                tr.Number_Pack  = 10;
      end;  
      if ((FD.ВалютныйРынок()) and (FD.IsClient())) /*bpv*/
         tr.Number_Pack  = 90; 
      end;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFirstDocFXDeal") )
      if (FD.Биржевая())
       tr.Number_Pack  = 35;
      else
      	if (FD.deal.rec.kind == 12735) //банкнотные сделки 
          tr.Number_Pack  = 70;
        else
        tr.Number_Pack  = 30;
        end;
      end;

      if ((FD.ВалютныйРынок()) and (FD.IsClient())) /*bpv*/
         tr.Number_Pack  = 90;
      end;

    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFirstDocPmGr") ) //графики только для внебирж. процентных свопов
    	tr.Number_Pack  = 30;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTDOCOPER") ) //первичка расчетов с биржей
    	if (fd.DVOper.rec.operkind== 12602)
          tr.Number_Pack  = 35;
    	else
          tr.Number_Pack  = 40;//пока иначе пачка по умолчанию
    	end;    
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTCSA") ) // Операции по CSA
          tr.Number_Pack  = 80;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTDOCDLCOMM") ) // Новая операция расчетов с биржей
       if (FD.IsClient())
    	tr.Number_Pack  = 90;
       else
    	tr.Number_Pack  = 40;
       end;	
   else
    	tr.Number_Pack  = 40;
    end;
    //TEG для сделок по срочному рынку запишем идентификатор сделки в поле userfild1
    if ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTDOCDEAL") ) //первичка расчетов с биржей
       if (FD.kind==192)
          tr.userfield1= FD.DEAL.rec.ID;
       end;
    end;

    if( IsSumEqDebet != null )
       if( IsSumEqDebet == true )
          tr.SumPayer    = $0;
          tr.SumReceiver = СуммаОперации;
       else
          tr.SumPayer    = СуммаОперации;
          tr.SumReceiver = $0;
       end;
    else
       tr.SumPayer      = CуммаДебет;
       tr.SumReceiver   = СуммаКредит;
    end;
   
    if( SideTransaction != null )
       tr.SideTransaction = SideTransaction;
    end;
   
    if( ExRateExtra != null )
       tr.ExRateExtra = ExRateExtra;
    end;
   
    if( (Date_Rate != null) and (Date_Rate != date(0,0,0)) )
       tr.Date_Rate = Date_Rate;
    end;
   
    if( (Rate != null) and (Rate != 0.0) )
       tr.Rate = Rate;
       if( IsInverse != null )
          tr.IsInverse = IsInverse;
       end;
    end;

    if ((fd.Kind == DL_DVFXDEAL) and IsDVFXBNOTE(DV_GetOperationGroup(fd.Deal.rec.Kind)) and (FD.ВалютаБазовогоАктива() != FD.ВалютаКонтрактива()))
      tr.TypeDocument = "Q";
    end;
   
    if( (SkipRateCarry!=null) and (SkipRateCarry==true) and (AccountPayer.Code_Currency != AccountReceiver.Code_Currency) )
       var SumEq_Payer = tr.SumPayer, SumEq_Receiver = tr.SumReceiver;

       if (StrUpr(GenClassName(FD)) == "DVFIRSTDOCNDEAL")

        if (FD.ВалютаТребования() == NATCUR)
          tr.SumEquivalentCarry = SumEq_Payer;
        elif (FD.ВалютаОбязательства() == NATCUR)
          tr.SumEquivalentCarry = SumEq_Receiver;
        else
           //TEG 08.01.19 пробуем для свопа покрытие зафиксировать
           if( not DV_SmartConvertSum(SumEq_Payer, tr.SumPayer, ДатаВалютирования, AccountPayer.Code_Currency, NATCUR, false) )
               return SayCarryError( GetCurrencyConvertErrorMsg(AccountPayer.Code_Currency, NATCUR, ДатаВалютирования) );
           end;
           if( not DV_SmartConvertSum(SumEq_Receiver, tr.SumReceiver, ДатаВалютирования, AccountReceiver.Code_Currency, NATCUR, false) )
               return SayCarryError( GetCurrencyConvertErrorMsg(AccountReceiver.Code_Currency, NATCUR, ДатаВалютирования) );
           end;
           if ((FD.deal.rec.kind==2715) or (FD.deal.rec.kind==12715))
	        if ( (FD.ВалютаТребования() != NATCUR) and (FD.ВалютаОбязательства() != NATCUR) )
                      if (FD.IsBuy())
                          tr.SumEquivalentCarry = SumEq_Receiver;//SumEq_Payer;/*PNV*/
                      else
                         tr.SumEquivalentCarry = SumEq_Payer;// SumEq_Receiver;/*PNV*/
                      end;  
                else	
                      if (FD.IsBuy())
                          tr.SumEquivalentCarry = SumEq_Payer;
                      else
                          tr.SumEquivalentCarry = SumEq_Receiver;
                      end;  
                end;					  		
                //mda 16.02.2019 Данный алгоритм применим и для сделок ПФИ поэтому , тут будет ветка для ПФИ
           elif (FD.deal.rec.kind == 12695) //внебирж опцион
                if (FD.IsBuyOption())
                    tr.SumEquivalentCarry = SumEq_Receiver;
                else
                    tr.SumEquivalentCarry = SumEq_Payer;
                end;
           elif (FD.deal.rec.kind == 22720) //IRS/CIRS процентный своп
                tr.SumEquivalentCarry = SumEq_Receiver;
           elif ( ((FD.deal.rec.kind == 12690) and (FD.ВидБазовогоАктива(true) == FIKIND_CURRENCY)) or (FD.deal.rec.kind == 22710) or (FD.deal.rec.kind == 12745) )//внебиржевой форвард и валютный своп, Покупка\продажа Т+3
                if (FD.IsBuy())
                    tr.SumEquivalentCarry = SumEq_Receiver;
                else
                    tr.SumEquivalentCarry = SumEq_Payer;
                end;
           else
                tr.SumEquivalentCarry = min(SumEq_Payer,SumEq_Receiver);
           end;
        end;
      else

        /*заполняем tr.SumEquivalentCarry, чтобы не было ядерной проводки по переоценке*/
        if ( FD.kind != 4632)
             if( (FD.ВалютаБазовогоАктива()!=NATCUR) and (FD.ВалютаКонтрактива()==NATCUR) )/*Покупка/продажа иностранной валюты за рубли, покупка/продажа драг. металла за рубли*/
                  if( AccountPayer.Code_Currency==NATCUR )
                      tr.SumEquivalentCarry = SumEq_Payer;
                 else/*AccountReceiver.Code_Currency==NATCUR*/
                      tr.SumEquivalentCarry = SumEq_Receiver;
                 end;
             else
                 if( not DV_SmartConvertSum(SumEq_Payer, tr.SumPayer, ДатаВалютирования, AccountPayer.Code_Currency, NATCUR, false) )
                     return SayCarryError( GetCurrencyConvertErrorMsg(AccountPayer.Code_Currency, NATCUR, ДатаВалютирования) );
                 end;
                 if( not DV_SmartConvertSum(SumEq_Receiver, tr.SumReceiver, ДатаВалютирования, AccountReceiver.Code_Currency, NATCUR, false) )
                     return SayCarryError( GetCurrencyConvertErrorMsg(AccountReceiver.Code_Currency, NATCUR, ДатаВалютирования) );
                 end;
                 if( (FD.ВидАктиваОбязательства()==FIKIND_METAL) and (FD.ВалютаТребования() != NATCUR) )/*Продажа драг. металла за иностранную валюту*/
                      tr.SumEquivalentCarry = SumEq_Payer;
                 else
                      if ( StrUpr(genclassname(FD)) == StrUpr("DVFirstDocFXDeal") )
                           if (FD.deal.rec.kind==12730)
                               tr.SumEquivalentCarry = SumEq_Receiver;
                          /*PNV 509039*/
                           elif (FD.IsBNote())
                               tr.SumEquivalentCarry = SumEq_Receiver;
                         /*PNV 509039*/
                           else
                               tr.SumEquivalentCarry = min(SumEq_Payer,SumEq_Receiver);
                           end;    
                     else
                           tr.SumEquivalentCarry = min(SumEq_Payer,SumEq_Receiver);
                     end; 
                 end;
             end;
        else
             if( not DV_SmartConvertSum(SumEq_Payer, tr.SumPayer, ДатаВалютирования, AccountPayer.Code_Currency, NATCUR, false) )
                 return SayCarryError( GetCurrencyConvertErrorMsg(AccountPayer.Code_Currency, NATCUR, ДатаВалютирования) );
             end;
             if( not DV_SmartConvertSum(SumEq_Receiver, tr.SumReceiver, ДатаВалютирования, AccountReceiver.Code_Currency, NATCUR, false) )
                 return SayCarryError( GetCurrencyConvertErrorMsg(AccountReceiver.Code_Currency, NATCUR, ДатаВалютирования) );
             end;
             tr.SumEquivalentCarry = min(SumEq_Payer,SumEq_Receiver); 
        end;
       end;
       /*чтобы не было ядерной проводки урегулирования*/
       tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/
    end;

    if( MassTransMode == false )
      if( not tr.Carry() )
         return SayCarryError("Ошибка при выполнении проводки");
      end;
      trnId = tr.AccTrnId;
    end;
   
    if( IsInnerCarry )
       if( not РасчетнаяОперацияВУ( tr, FD, ВидРО ) )
          return SayCarryError("Ошибка при вставке расчетной операции внутреннего учета");
       end;
    end;
   
    if( NatSum != null )
       if( tr.FIIDPayer == NATCUR )
          NatSum = tr.SumPayer;
       elif( tr.FIIDReceiver == NATCUR )
          NatSum = tr.SumReceiver;
       else
          NatSum = $0.0;
       end;
    end;

    if( DebAccNumber != null )
       DebAccNumber = tr.AccountPayer;
    end;

    if( CredAccNumber != null )
       CredAccNumber = tr.AccountReceiver;
    end;

 end;

 if( not stat ) 
    return SayCarryError("Ошибка при вставке документа проводки.");
 end;

 return stat;
END;

PRIVATE MACRO ПроводкаПоПлатежу( FD, PaymentObj:RSBPayment, trnID:@integer, Chapter:INTEGER, Dat:DATE, DebMcAccDoc, CredMcAccDoc, _TaxSum, _TaxFIID ):BOOL

  var ПИ_ДелатьПроводку:bool = true;
  var СчетДебет = TRecHandler("account"), СчетКредит = TRecHandler("account");
  var AccNalog = TRecHandler("account");
  var IsGetAccount = false;
  var TaxSum = $0, TaxFIID = -1, TS = $0;
  var SumEq_Payer = $0, SumEq_Receiver = $0;
  var Acc_str = ""; /*PNV*/
  file AccBuff( "account.dbt" ); /*PNV*/

  ClearRecord(СчетДебет);
  ClearRecord(СчетКредит);

  CarryError = "";

  if(_TaxSum != NULL)
    TaxSum  = _TaxSum;
    TaxFIID = _TaxFIID;
  end;
/*CHVA*/
    if (StrUpr(genclassname(FD)) == StrUpr("DVFirstDocNDeal"))
      if (FD.Биржевая())
       PaymentObj.Numberpack  = 35;
      else
       PaymentObj.Numberpack  = 30;
      end;
      if(FD.ВидБазовогоАктива() == FIKIND_AVOIRISS)
         PaymentObj.Numberpack  = 10;
      end;
      if ((FD.ВалютныйРынок()) and (FD.IsClient())) /*bpv*/
         PaymentObj.NumberPack  = 90;
      end;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFirstDocFXDeal") )
      if (FD.Биржевая())
       PaymentObj.Numberpack  = 35;
      else
        if (FD.deal.rec.kind == 12735) //банкнотные сделки 
          PaymentObj.Numberpack = 70;
        else
          PaymentObj.Numberpack  = 30;
        end;
      end;
      if ((FD.ВалютныйРынок()) and (FD.IsClient())) /*bpv*/
         PaymentObj.NumberPack  = 90;
      end;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFirstDocPmGr") ) //графики только для внебирж. процентных свопов
      PaymentObj.Numberpack  = 30;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTDOCOPER") ) //первичка расчетов с биржей
      if (fd.DVOper.rec.operkind== 12602)
          PaymentObj.Numberpack  = 35;
      else
          PaymentObj.Numberpack  = 40;//пока иначе пачка по умолчанию
      end;    
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTCSA") ) // Платёж по CSA
    	PaymentObj.Numberpack  = 80;
    elif ( StrUpr(genclassname(FD)) == StrUpr("DVFIRSTDOCDLCOMM") ) //Операция расчеты с Биржей
       if (FD.IsClient())
          PaymentObj.Numberpack  = 90;
       else
          PaymentObj.Numberpack  = 40; // иначе по умолчанию
       end;
    else
     PaymentObj.Numberpack  = 40;
    end;
    //TEG 02.05.19 ролевая модель   
   /* Определяем конечного операциониста для проводки */
      if ((PaymentObj.DocKind==140) or (PaymentObj.DocKind==152) or (PaymentObj.DocKind==4626) or (PaymentObj.DocKind==4627) or (PaymentObj.DocKind==4632) or ( /*bpv*/PaymentObj.DocKind == 4813 ))
         PaymentObj.Oper = GetMainOperInGroup(PaymentObj.DocKind);
      elif (PaymentObj.DocKind==194)
         PaymentObj.Oper = GetMainOperInGroup(PaymentObj.DocKind,FD.DVOPER.rec.operkind);
      else  
         PaymentObj.Oper = GetMainOperInGroup(PaymentObj.DocKind,FD.deal.rec.kind);
      end;
      if ( PaymentObj.Oper<= 0)
            PaymentObj.Oper = {Oper};
      end;
   /**/

/*CHVA*/

    //Simanov. Если платёж уже исполнен - не делать проводку. Иначе задваивается проводка после квитовки
    if ( (ПИ_СтутусПлатежИсполнен(PaymentObj.PaymStatus)) and (StrUpr(genclassname(FD)) != StrUpr("DVFIRSTCSA")) )/*PNV*/ 
      ПИ_ДелатьПроводку = false;
    elif( (PaymentObj.ReceiverBankID != {OurBank}) or (PaymentObj.PayerBankID != {OurBank}) ) /*внешний платеж*/
      if( (PaymentObj.ReceiverBankID != {OurBank}) and (PaymentObj.PayerBankID == {OurBank}) ) /*исходящий*/
        if( /*(not IsBNote) and*/ ПИ_ИнтегрРежимРаботы() and (not ФИСС_ДелатьПроводкуПоИсхПлатежам()) )
            ПИ_ДелатьПроводку = false;
        end;
      elif( (PaymentObj.ReceiverBankID != {OurBank}) and (PaymentObj.PayerBankID != {OurBank}) ) /*транзитный?*//*PNV*/ 
           ПИ_ДелатьПроводку = true;
      end;
    end;

  if( ПИ_ДелатьПроводку )
     var tr = PaymentObj.MakeTransaction();

     if( tr == NULL )
        return SayCarryError("Ошибка при формировании документа проводки.");
     end;

     /*Проводки через класс платежа всегда подставляют первую главу*/
     if( Chapter == NULL )
        Chapter = 1;
     end;
     tr.Chapter = Chapter;

     if( Dat != null )
        tr.Date_Carry = Dat;
     else
        tr.Date_Carry = PaymentObj.ValueDate;
     end;
     tr.Oper = PaymentObj.Oper;
     if( tr.Shifr_Oper == "" )
        /*PNV */
        if (tr.AccountReceiver == "")
            tr.AccountReceiver = ПолучитьКоррсчетИзКоррсхемы(tr.FIIDReceiver);
        end;
        if (tr.AccountPayer == "")
            tr.AccountPayer = ПолучитьКоррсчетИзКоррсхемы(tr.FIIDPayer);
        end;

        if (tr.AccountReceiver == tr.AccountPayer)
           if ((PaymentObj.Receiveraccount != "") and (PaymentObj.ReceiverBankID == {OurBank}))
               tr.AccountReceiver = PaymentObj.Receiveraccount;
           end; 
           if ((PaymentObj.Payeraccount != "") and (PaymentObj.PayerBankID == {OurBank}))        
               tr.AccountPayer = PaymentObj.Payeraccount;
           end; 
        end;
        /*PNV*/

        if( not IsGetAccount )
           if( DL_GetAccount(tr.Chapter, tr.FIIDPayer, tr.AccountPayer, СчетДебет) and
               DL_GetAccount(tr.Chapter, tr.FIIDReceiver, tr.AccountReceiver, СчетКредит)
             )
              IsGetAccount = true;
           end;
        end;

        if( IsGetAccount )
           tr.Shifr_Oper = DL_GetShifrOper(tr.Chapter, СчетДебет.rec, СчетКредит.rec);
        end;
     end;

     if( not IsGetAccount )
        if( DL_GetAccount(tr.Chapter, tr.FIIDPayer, tr.AccountPayer, СчетДебет) and
            DL_GetAccount(tr.Chapter, tr.FIIDReceiver, tr.AccountReceiver, СчетКредит)
          )
           IsGetAccount = true;
        end;
     end;

     /*PNV i-support 487208*/
     if ( (FD.Kind == DL_DVFXDEAL) and (FD.ВидБазовогоАктива() == FIKIND_METAL) and (tr.FIIDPayer == FD.ВалютаБазовогоАктива()) )
           Acc_str = ПолучитьКоррсчетИзКоррсхемы(tr.FIIDPayer, IIF(PaymentObj.InCorschem > 0 , PaymentObj.InCorschem, ПолучитьКорсхемуПоУмолчанию(PaymentObj.payerbankid, PaymentObj.basefiid))); 
           if (Acc_str == tr.AccountPayer)  
              if( DV_GetAccount( 1, FD.ВалютаБазовогоАктива(), Acc_str, AccBuff, true ) == true ) 
                  tr.ground = "Зачисление стоимости ДМ (" + FD.BaseFI().rec.name + ") с обезличенного металлического счета " + AccBuff.nameaccount + " по сделке №"+ FD.DealCode() + " от " + FD.ДатаСделки() + " по " + IIF(FD.isbuypart(), "покупке ", "продаже ") + "(" + FD.BaseFI().rec.name + "). К/агент: " + GetPartyNames(FD.GetContractorID(),2)[0];
              end; 
           end;
     elif ( (FD.Kind == DL_DVFXDEAL) and (FD.ВидБазовогоАктива() == FIKIND_METAL))
              /*PNV i-support 488546 - если заполнено примечание, значит сделка со слитками*/ 
              /*PNV i-support 500025 - формируем это основание для всех сделок с ДМ, даже если примечание не заполнено*/ 
              var str = readNoteForObject(OBJTYPE_FXOPER_DV, UniID( FD.deal, OBJTYPE_FXOPER_DV), 101);
             /*PNV i-support 506064*/
              var v_DM_FI = TRecHandler( "fininstr.dbt" );
              if( ПолучитьФинИн( FD.ВалютаБазовогоАктива(), v_DM_FI ) != 0 )
                  v_DM_FI.Clear();
              end;
              if (FD.isbuy())  
                  tr.ground = "ПЕРЕВОД СРЕДСТВ ПО СДЕЛКЕ "+ FD.DealCode() + " ОТ " + FD.ДатаСделки() + " НА " + IIF(FD.isbuypart(), "ПОКУПКУ", "ПРОДАЖУ") + " ДМ(" + StrUpr(v_DM_FI.rec.name) + ") К/агент: " + GetPartyNames(FD.GetContractorID(),2)[0];              //end;
              else  
                  tr.ground = "Списание обязательств по сделке "+ FD.DealCode() + " от " + FD.ДатаСделки() + " на " + IIF(FD.isbuypart(), "покупку ", "продажу ") + "слитков. К/агент: " + GetPartyNames(FD.GetContractorID(),2)[0];
              end;                     
              /*PNV i-support 488546*/
     end;
     /*PNV*/
     /*PNV 514269*/
     if ( ( (FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVDEALT3)) and (FD.ВидБазовогоАктива() == FIKIND_METAL) and (tr.FIIDPayer == FD.ВалютаБазовогоАктива()) ) /*PNV 522410*/
           if ((Substr(PaymentObj.Payeraccount, 1, 5) == "61213") and (tr.AccountPayer != PaymentObj.Payeraccount))  
               tr.AccountPayer = PaymentObj.Payeraccount;
               tr.FIIDPayer = PaymentObj.Payerfiid;
           end;
     elif ( (( FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVDEALT3)) and (FD.ВидБазовогоАктива() == FIKIND_METAL) and (tr.FIIDReceiver == FD.ВалютаБазовогоАктива()) ) /*PNV 522410*/
           if ((Substr(PaymentObj.Receiveraccount, 1, 5) == "61213") and (tr.AccountReceiver != PaymentObj.Receiveraccount))  
               tr.AccountReceiver = PaymentObj.Receiveraccount;
               tr.FIIDReceiver = PaymentObj.receiverfiid;
           end;
     end;
     /*PNV 514269*/  
 
     /*для случая продажи драг металла за рубли или иностранную валюту*/
     if( ((FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVDEALT3)) and (not FD.IsDealMet()) and (FD.ДатаИсполнения() != FD.ДатаСделки()) and (not FD.IsClient()) and (/*not FD.IsBNote() and*/ (not FD.Isswap()) ) and /*PNV 522410*/
         (FD.ВидАктиваОбязательства() == FIKIND_METAL) and (FD.ВидАктиваТребования() == FIKIND_CURRENCY) and ( FD.ВалютаТребования() != NATCUR ) and
         ((PaymentObj.Purpose == BAi) or (PaymentObj.Purpose == BRi)) 
       )
          tr.SumReceiver = PaymentObj.ReceiverAmount;
          tr.SumPayer = PaymentObj.PayerAmount;
          /*рассчитываем нужные суммы в проводке*/
          if( not DV_SmartConvertSum(SumEq_Payer, FD.СуммаТребования(), tr.Date_Carry, FD.ВалютаТребования(), NATCUR, false) )
              return SayCarryError( GetCurrencyConvertErrorMsg(FD.ВалютаТребования(), NATCUR, tr.Date_Carry) );
          end;
          /*заполняем tr.SumEquivalentCarry, чтобы не было ядерной проводки по переоценке*/

          tr.SumEquivalentCarry = SumEq_Payer;
          if (FD.isSale() and (FD.ВалютаТребования() != NATCUR))
               tr.SumPayer = FD.СуммаТребования();
           tr.SumReceiver = SumEq_Payer;
          else
               tr.SumReceiver = FD.СуммаТребования();
          end;

          /*чтобы не было ядерной проводки урегулирования*/
          tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/
     elif( (FD.Kind == DL_DVFXDEAL) /*and (FD.ДатаИсполнения() == FD.ДатаСделки())*/ and (not FD.IsClient()) and (not FD.IsBNote() and (not FD.Isswap()) ) and
          (FD.ВидАктиваТребования() == FIKIND_METAL) and (FD.ВидАктиваОбязательства() == FIKIND_CURRENCY) and  (FD.ВалютаОбязательства() != NATCUR ) and
         ((PaymentObj.Purpose == BAi) or (PaymentObj.Purpose == BRi)) 
       )
          /*рассчитываем нужные суммы в проводке*/
           tr.SumReceiver = FD.СуммаОбязательства();
           if( not DV_SmartConvertSum(SumEq_Payer, tr.SumPayer, tr.Date_Carry, tr.FIIDPayer, NATCUR, false) )
               return SayCarryError( GetCurrencyConvertErrorMsg(FD.ВалютаОбязательства(), NATCUR, tr.Date_Carry) );
           end;
          /*заполняем tr.SumEquivalentCarry, чтобы не было ядерной проводки по переоценке*/
           tr.SumEquivalentCarry = SumEq_Payer;
          /*чтобы не было ядерной проводки урегулирования*/
          tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/

     elif (( (FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVNDEAL) or (FD.Kind == DL_DVDEALT3 )) and (FD.ВидБазовогоАктива() == FIKIND_METAL) )
             tr.SumReceiver = PaymentObj.ReceiverAmount;
             tr.SumPayer = PaymentObj.PayerAmount;

             if ( (FD.isSale()) and (not FD.Isswap() ) )
                   tr.SumPayer = FD.СуммаТребования();
                   tr.SumReceiver = FD.СуммаТребования();
                   if ( (FD.ВалютаТребования() != NATCUR) and ((tr.FIIDPayer == NATCUR) or (tr.FIIDReceiver == NATCUR)) )
                         if( not DV_SmartConvertSum(SumEq_Payer, FD.СуммаТребования(), tr.Date_Carry, FD.ВалютаТребования(), NATCUR, false) )
                             return SayCarryError( GetCurrencyConvertErrorMsg(FD.ВалютаТребования(), NATCUR, tr.Date_Carry) );
                         end;
                         tr.SumReceiver = SumEq_Payer;
                         tr.SumEquivalentCarry = tr.SumReceiver;
                         tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/
                   end;
             elif ( (FD.isSale()) and (FD.dealpart() == 2 ) )
                   tr.SumPayer = FD.СуммаТребования();
                   tr.SumReceiver = FD.СуммаТребования();
                   if ( (FD.ВалютаТребования() != NATCUR) and ((tr.FIIDPayer == NATCUR) or (tr.FIIDReceiver == NATCUR)) )
                       if( not DV_SmartConvertSum(SumEq_Payer, FD.СуммаТребования(), tr.Date_Carry, FD.ВалютаТребования(), NATCUR, false) )
                           return SayCarryError( GetCurrencyConvertErrorMsg(FD.ВалютаТребования(), NATCUR, tr.Date_Carry) );
                       end;
                       tr.SumReceiver = SumEq_Payer;
                       tr.SumEquivalentCarry = tr.SumReceiver;
                       tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/
                   end;
             elif ( (FD.ДатаИсполнения() != FD.ДатаСделки()) and (not FD.Isswap() ) and (FD.isSale()) )    
                  tr.SumReceiver = FD.СуммаОбязательства();
             end;
     elif ( ((FD.Kind == DL_DVFXDEAL) or (FD.Kind == DL_DVNDEAL)) and (FD.IsClient()) ) /*bpv ни в одну из предыдущих не попадаем. Делаем свою*//*PNV i-support 502911*/
       tr.SumReceiver = PaymentObj.ReceiverAmount;
       tr.SumPayer = PaymentObj.PayerAmount;
       tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/ 
     end;

     if((ValType(TaxSum) != V_UNDEF) and (TaxSum > 0))

       if(TaxFIID != СчетДебет.rec.Code_Currency)
         if( not DV_SmartConvertSum(TS, TaxSum, tr.Date_Carry, TaxFIID, СчетДебет.rec.Code_Currency, false) )
            return SayCarryError( GetCurrencyConvertErrorMsg(TaxFIID, СчетДебет.rec.Code_Currency, tr.Date_Carry) );
         end;

         tr.SumPayer = tr.SumPayer - TS;
       else
         tr.SumPayer = tr.SumPayer - TaxSum;
       end;

       if(TaxFIID != СчетКредит.rec.Code_Currency)
         if( not DV_SmartConvertSum(TS, TaxSum, tr.Date_Carry, TaxFIID, СчетКредит.rec.Code_Currency, false) )
            return SayCarryError( GetCurrencyConvertErrorMsg(TaxFIID, СчетКредит.rec.Code_Currency, tr.Date_Carry) );
         end;

         tr.SumReceiver = tr.SumReceiver - TS;
       else
         tr.SumReceiver = tr.SumReceiver - TaxSum;
       end;
     end;
     tr.Numb_Document = "";

     if( IsGetAccount )
        if( DV_НужнаСводнаяПроводка(FD, СчетДебет, СчетКредит, tr.Chapter, DebMcAccDoc, CredMcAccDoc, PaymentObj.Purpose) )
           if( not DV_ДобавитьСводнуюПроводку(FD, СчетДебет, СчетКредит, tr.Date_Carry, tr.SumPayer, tr.SumReceiver, tr.ResultCarry, tr.Ground) )
              return SayCarryError("Ошибка при вставке документа сводной проводки.");
           else
              ПИ_ДелатьПроводку = false; /*сформировали сводную проводку - ниже выполнять проводку не надо*/
           end;
        end;
     end;

     if( ПИ_ДелатьПроводку )
        if( not tr.Carry() )
           return SayCarryError("Ошибка при выполнении проводки");
        end;
        trnId = tr.AccTrnId;
     end;

     if((ValType(TaxSum) != V_UNDEF) and (TaxSum > 0))

       var trtax = PaymentObj.MakeTransaction();

       if( trtax == NULL )
          return SayCarryError("Ошибка при формировании документа проводки.");
       end;

       trtax.Chapter      = tr.Chapter;
       trtax.Date_Carry   = tr.Date_Carry;
       trtax.FIIDPayer    = tr.FIIDPayer;   
       trtax.AccountPayer = tr.AccountPayer;

       if(TaxFIID != trtax.FIIDPayer)
         if( not DV_SmartConvertSum(TS, TaxSum, trtax.Date_Carry, TaxFIID, trtax.FIIDPayer, false) )
            return SayCarryError( GetCurrencyConvertErrorMsg(TaxFIID, trtax.FIIDPayer, trtax.Date_Carry) );
         end;

         trtax.SumPayer = TS;
       else
         trtax.SumPayer = TaxSum;
       end;

       if( not FD.OpenAccount( "-Бюджет, ф.налоги", null, null, FIROLE_UNDEF, AccNalog, trtax.Date_Carry, NATCUR ) )
          return SayCarryError("Ошибка при открытии счета по категории <-Бюджет, ф.налоги>");
       end;

       trtax.FIIDReceiver    = NATCUR;   
       trtax.AccountReceiver = AccNalog.rec.Account;

       if(TaxFIID != trtax.FIIDReceiver)
         if( not DV_SmartConvertSum(TS, TaxSum, trtax.Date_Carry, TaxFIID, trtax.FIIDReceiver, false) )
            return SayCarryError( GetCurrencyConvertErrorMsg(TaxFIID, trtax.FIIDReceiver, trtax.Date_Carry) );
         end;

         trtax.SumReceiver = TS;
       else
         trtax.SumReceiver = TaxSum;
       end;

       if((DL_UseNUTX() == true) and (FD.IsClient()) and (IsInstNeresForNUTX(GetFactReceiverForNUTX(FD.Deal.rec.Client), PaymentObj.ValueDate) == true))
          trtax.Ground = "Удержание налога с клиента ЮЛН";
       else
          trtax.Ground = "Удержание налога с к/а";
       end;

       if( not trtax.Carry() )
          return SayCarryError("Ошибка при выполнении проводки");
       end;
     end;
  end;

  return true;
END;

private macro MakeTrnLinkId(ID_Operation, ID_Step, TrnId)
  var query =
    " INSERT INTO doprdocs_dbt (T_DOCKIND, "
   +"                           T_DOCUMENTID, "
   +"                           T_ID_OPERATION, "
   +"                           T_ID_STEP, "
   +"                           T_PART, "
   +"                           T_STATUS, "
   +"                           T_ORIGIN, "
   +"                           T_SERVDOCKIND, "
   +"                           T_SERVDOCID, "
   +"                           T_ACCTRNID) "
   +"      VALUES (1, "
   +"              CHR (1), "
   +"              ?, "
   +"              ?, "
   +"              1, "
   +"              0, "
   +"              0, "
   +"              0, "
   +"              0, "
   +"              ?); ";
  var cmd = RSDCommand(query);
  cmd.addParam("", RSDBP_IN, ID_Operation);
  cmd.addParam("", RSDBP_IN, ID_Step);
  cmd.addParam("", RSDBP_IN, TrnId);
  cmd.execute();
end;

private macro AccountingLostComis( Rep, trnDate:date, makeTransaction:bool, isNoData:@bool )
  var rDLComis = TRecHandler("dlcomis.dbt");
  var ClientAcc306 = TRecHandler("account"),
      ClientAcc47423 = TRecHandler("account"),
      ClientAcc70601 = TRecHandler("account"),
      DebAccBY = TRecHandler("account"),
      KredAccBY = TRecHandler("account");
  var FD, Ground, GroundBY, PaymentObj:RsbPayment;
  var trnId = 0, err = 0;

  var Sql = " SELECT dlcomis.t_ID, dlcomis.t_DocKind, dlcomis.t_DocID, dlcomis.t_Sum, dlcomis.t_NDS, comis.t_FIID_Comm, fin.t_CCY, contr.t_Number ContrName, " +
            "        dlcomis.t_Contract, dlcomis.t_FeeType, dlcomis.t_ComNumber, dlcomis.t_InPutMedium, dlcomis.t_IsBack, dlcomis.t_ComType, dlcomis.t_ServOperID, oper.t_ID_Operation " +
            "   FROM ddlcomis_lost_dbt dlcomis, dsfcomiss_dbt comis, dfininstr_dbt fin, dsfcontr_dbt contr, doproper_dbt oper " +
            "  WHERE dlcomis.t_PlanPayDate <= ? " +
            "    AND dlcomis.t_RunCarry != chr(88) " +
            "    AND dlcomis.t_FeeType = comis.t_FeeType " +
            "    AND dlcomis.t_ComNumber = comis.t_Number " +
            "    AND comis.t_FIID_Comm = fin.t_FIID " +
            "    AND dlcomis.t_Contract = contr.t_ID " +
            "    AND oper.T_DocumentID = LPAD(dlcomis.t_DocID, 34, '0') " +
            "    AND oper.t_DocKind = dlcomis.t_DocKind " +
            " order by dlcomis.t_Contract, dlcomis.t_PlanPayDate, dlcomis.t_ID ";

  var Query = DL_RSDCommand(Sql);
  Query.addParam(trnDate);

  if( not makeTransaction ) //Чтобы не печатать данные в отчёт многократно, если не выполняем проводки
     isNoData = true;
  end;

  if( Query.GetCount() == 0 )
     isNoData = true;
  else
     InitProgress(Query.GetCount(), "Обработка комиссий", "Обработка комиссий");
  end;
  var cnt = 0;

  var Data = Query.Execute();

  while( Data.MoveNext() ) 
     err = 0;
     if( Data.rec.DocKind == DL_DVFXDEAL )
        FD = DVFirstDocFXDeal( Data.rec.DocKind, Data.rec.DocId );
     else
        FD = DVFirstDocNDeal( Data.rec.DocKind, Data.rec.DocId );
     end;

     if( FD.ВалютныйРынок() and FD.IsClient() )

        RslDefCon.BeginTrans();

        Ground = "Комиссия брокера. Сделка " + IIF(FD.Kind==DL_DVFXDEAL,"","СВОП ") + "N " + FD.DealCode() + " по брокерскому договору N "+FD.ClientDBOContrName()+" дата " + FD.deal.rec.date + ". НДС не облагается";

        var SubContrFD = DL_SubContrFD(FD.Deal.rec.ClientContr);
        if( not SubContrFD.OpenAccount("ДС клиента, ц/б", NULL, NULL, ClientAcc306, trnDate, Data.rec.FIID_Comm) )
           err = 1;
        end;
        if( not FD.OpenAccount("+РасчетыКомисс1", null, null, FIROLE_UNDEF, ClientAcc47423, trnDate, Data.rec.FIID_Comm) )
           err = 1;
        end;
        if( not FD.OpenAccount("+КВ, ц/б", null, null, FIROLE_COMISS_CLIENT, ClientAcc70601, trnDate, Data.rec.FIID_Comm) )
           err = 1;
        end;

        trnId = 0;
        if( makeTransaction and (not err) and (not Проводка( FD, /*FIROLE_COMISS_CLIENT*/
                                                             ClientAcc47423, ClientAcc70601,     
                                                             trnDate,                
                                                             1,                               
                                                             Data.rec.FIID_Comm,              
                                                             Data.rec.Sum,                  
                                                             null,
                                                             INPCARRY,                        
                                                             "",                           
                                                             "",                           
                                                             Ground,
                                                             0, null, FIROLE_COMISS_CLIENT, @trnId,
                                                             Data.rec.FIID_Comm, 0              
                                                           )
                                               )
           )                                                                  
           err = 1;
        end;
        if( (not err) and (trnId > 0) )
            MakeTrnLinkId(data.rec.ID_Operation, 0, trnId);
        end;

        var PaySum = Data.rec.Sum - Data.rec.NDS;
        if( makeTransaction )
           if( not err )
              if( DVND_CreatePayment( FD, PM_PURP_COMMBANK, FD.Kind, FD.Deal.rec.ID,
                                      trnDate,
                                      FD.Deal.rec.Client, {ourbank},
                                      {ourbank}, {ourbank},
                                      PaySum, Data.rec.FIID_Comm,
                                      Ground,
                                      ClientAcc306, ClientAcc47423,
                                      FD.Deal.rec.Department, NULL, NULL,
                                      @PaymentObj
                                    ) != 0
                )
                 err = 1;
              else
                PaymentObj.Update();
              end;
           end;

           trnId = 0;
           if( (not err) and (not ПроводкаПоПлатежу(FD, PaymentObj, @trnId)) )
              err = 1;
           end;
           if( (not err) and (trnId > 0) )
              MakeTrnLinkId(data.rec.ID_Operation, 0, trnId);
           end;

           if( not err )
              if( ПИ_ИсполнитьПлатеж(PaymentObj) )
                 err = 1;
              else
                 PaymentObj.Update();
              end;
           end;
        end;

        if( ПИ_ВестиВнутреннийУчет() )
           GroundBY = FD.ВУ_ОснованиеПроводки(8, false);

           if( not FD.OpenAccount(FD.ВУ_ПолучитьКатегориюДебет(8, false), null, null, FD.ВУ_ПолучитьРольФИ(8, true, false), DebAccBY, trnDate, Data.rec.FIID_Comm) )
              err = 1;
           end;
           if( not FD.OpenAccount(FD.ВУ_ПолучитьКатегориюКредит(8, false), null, null, FD.ВУ_ПолучитьРольФИ(8, true, false), KredAccBY, trnDate, Data.rec.FIID_Comm) ) 
              err = 1;
           end;

           trnId = 0;
           if( makeTransaction and (not err) and (not Проводка( FD, 
                                                                DebAccBY, KredAccBY,
                                                                trnDate,
                                                                FD.ВУ_ОпределениеГлавы(Data.rec.FIID_Comm),
                                                                Data.rec.FIID_Comm,
                                                                Data.rec.Sum,
                                                                null, 
                                                                INPCARRY,
                                                                "",
                                                                "",
                                                                GroundBY,
                                                                0, null, null, @trnId,
                                                                Data.rec.FIID_Comm, Data.rec.FIID_Comm,
                                                                null, null, null, null,
                                                                true,
                                                                8
                                                              )
                                                 )
             )
              err = 1;
           end;
           if( (not err) and (trnId > 0) )
              MakeTrnLinkId(data.rec.ID_Operation, 0, trnId);
           end;
        end;

        if( makeTransaction )
           rDLComis.Clear();
           rDLComis.rec.ID          = 0;
           rDLComis.rec.DocKind     = Data.rec.DocKind;
           rDLComis.rec.DocId       = Data.rec.DocId;
           rDLComis.rec.Contract    = Data.rec.Contract;
           rDLComis.rec.FeeType     = Data.rec.FeeType;
           rDLComis.rec.ComNumber   = Data.rec.ComNumber;
           rDLComis.rec.Sum         = Data.rec.Sum;
           rDLComis.rec.NDS         = Data.rec.NDS;
           rDLComis.rec.Date        = trnDate;
           rDLComis.rec.PlanPayDate = trnDate;
           rDLComis.rec.FactPayDate = trnDate;
           rDLComis.rec.InPutMedium = Data.rec.InPutMedium;
           rDLComis.rec.ServOperID  = Data.rec.ServOperID;
           rDLComis.rec.IsBack      = UNSET_CHAR;
           rDLComis.rec.ComType     = Data.rec.ComType; 
           rDLComis.rec.Version     = 1;
           
           if( (not err) and DL_InsertComis(rDLComis) )
              MsgBox("Ошибка вставки комиссии с номером \""+ Data.rec.ComNumber + "\" по сделке с ID = " + Data.rec.DocId + ": " + GetErrMsg());
              err = 1;
           end;

           if( not err )
              var cmd = RsdCommand("UPDATE ddlcomis_lost_dbt SET t_RunCarry = chr(88), t_FactPayDate = ? WHERE t_ID = ?");
              cmd.addParam("", RSDBP_IN, trnDate);
              cmd.addParam("", RSDBP_IN, Data.rec.ID);
              cmd.Execute();
           end;
        end;

        if( err )
           RslDefCon.RollbackTrans();
        else
           RslDefCon.CommitTrans();

           Rep.AddPrintCell(FD.DealCode(), 0, 0, "l");
           Rep.AddPrintCell(GetClientCodeByID(FD.ClientID()), 0, 0, "l");
           Rep.AddPrintCell(Data.rec.ContrName, 0, 0, "l"); 
           Rep.AddPrintCell(ClientAcc47423.rec.Account, 0, 0, "l"); 
           Rep.AddPrintCell(ClientAcc70601.rec.Account, 0, 0, "l"); 
           Rep.AddPrintCell(Data.rec.Sum, 0, 2, "r:a");
           Rep.AddPrintCell(Data.rec.CCY, 0, 0, "l");
           Rep.AddPrintCell(Ground, 0, 0, "l");
           Rep.AddStr();
       
           Rep.AddPrintCell(FD.DealCode(), 0, 0, "l");
           Rep.AddPrintCell(GetClientCodeByID(FD.ClientID()), 0, 0, "l");
           Rep.AddPrintCell(Data.rec.ContrName, 0, 0, "l"); 
           Rep.AddPrintCell(ClientAcc306.rec.Account, 0, 0, "l"); 
           Rep.AddPrintCell(ClientAcc47423.rec.Account, 0, 0, "l"); 
           Rep.AddPrintCell(PaySum, 0, 2, "r:a");
           Rep.AddPrintCell(Data.rec.CCY, 0, 0, "l");
           Rep.AddPrintCell(Ground, 0, 0, "l");
           Rep.AddStr();
       
           if( ПИ_ВестиВнутреннийУчет() )
              Rep.AddPrintCell(FD.DealCode(), 0, 0, "l");
              Rep.AddPrintCell(GetClientCodeByID(FD.ClientID()), 0, 0, "l");
              Rep.AddPrintCell(Data.rec.ContrName, 0, 0, "l"); 
              Rep.AddPrintCell(DebAccBY.rec.Account, 0, 0, "l"); 
              Rep.AddPrintCell(KredAccBY.rec.Account, 0, 0, "l"); 
              Rep.AddPrintCell(Data.rec.Sum, 0, 2, "r:a");
              Rep.AddPrintCell(Data.rec.CCY, 0, 0, "l");
              Rep.AddPrintCell(GroundBY, 0, 0, "l");
              Rep.AddStr();
           end;
        end;
     end;
     UseProgress( cnt = cnt + 1 );
  end;

  RemProgress();

  return 0;

OnError(er)
  MsgBox(er.module+ " "+er.line+" "+er.message);
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RemProgress();

  return 1;
end;

macro ExecuteAccounting( trnDate:date, makeTransaction:bool )
  var isNoData = false;

  var tableHeader = 
 "┌──────────────────────┬───────────────────┬────────────────────┬─────────────────────┬─────────────────────┬────────────────────┬─────────────────┬───────────────────────────────────────────────────────────────────┐\n"+
 "│      Код сделки      │    Код клиента    │      Номер ДО      │        Дебет        │        Кредит       │   Сумма проводки   │    Валюта БА    │                             Основание                             │\n"+
 "├──────────────────────┼───────────────────┼────────────────────┼─────────────────────┼─────────────────────┼────────────────────┼─────────────────┼───────────────────────────────────────────────────────────────────┤";

  var Rep = CMakeReport(tableHeader);

  //Во избежание случайных ошибок, вроде дублирования индекса при вставке платежа (запрос 295025), запустим процедуру несколько раз, чтобы обработать максимум комиссий
  var i = 0;
  while( (i < 3) and (not isNoData) )
     AccountingLostComis( Rep, trnDate, makeTransaction, @isNoData );
     i = i + 1;
  end;   

  Rep.PrintWinRep();
  Rep.ShowWinRep();

  return 0;
end;

var trnDate = {curdate};
if (not GetDate(trnDate, "Введите дату формирования проводок") )
  return;
end;

var makeTrn = false;
GetTRUE( makeTrn, "Формировать комиссии и проводки вместе с формированием отчёта?" );

ExecuteAccounting( trnDate, makeTrn );
