/*
$Name:          ibr_pr_pers.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Отчет о блокировании/разблокировании пользователей"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import pr_pers;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const LOG_FILE_NAME = "ibr_pr_pers_web";
private const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/** 
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade    = TIbrConstructorFacade();
    var report    = facade.createReport("ibr_pr_pers", "Отчет по пользователям", "ibr_pr_pers.mac", "ibrReportExecute", "ibrReportVerification");  

    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldTypeAc("Доступ", "Доступ", 13, "Все пользователи", null, null, null, null, null, null, null, null, false));

    groupReport.add(facade.createFieldLlValues("Статус", "Статус", "Работающие", 1847));
   
    groupReport.add(facade.createFieldTypeAc("Приложение", "Приложение", 22, null, null, null, null, null, null, null, null, null, false));
    groupReport.add(facade.createFieldCheckBox("ПолныеИмяПриложений", "Показывать полные наименования приложений", false));

    groupReport.add(facade.createFieldCheckBox("Архив", "Показывать доступ к архиву", true));
    groupReport.add(facade.createFieldLlValues("ДоступАрхив", "", "Все", 1848));
    
    groupReport.add(facade.createFieldTypeAc("Сортировка", "Порядок сортировки", 182, "123", null, null, null, null, null, null, null, null, false));
    
    tabReport.add(groupReport);
    report.add(tabReport);
    
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    var isShowOcp :bool = false;
    var access : string = "";
    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );
      
    if (getValue(report, gRep+"Доступ").valueString == "Все пользователи")
        access = "";
    else
        access = getValue(report, gRep+"Доступ").valueString;
    end;
   
    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"Подразделения").valueInteger,
                                          access,
                                          getValue(report, gRep+"Статус").valueInteger,
                                          getValue(report, gRep+"Приложение").valueString,
                                          getValue(report, gRep+"ПолныеИмяПриложений").valueBoolean,
                                          getValue(report, gRep+"Архив").valueBoolean,
                                          getValue(report, gRep+"ДоступАрхив").valueInteger,
                                          getValue(report, gRep+"Сортировка").valueString,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          LOG_FILE_NAME);
    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(-1, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;
    
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;