/*
$Name:          Averest.mac
$Module:        Внутренняя отчетность
$Description:   Отчет о средних остатках на счетах
*/
/*───────────────────────────────────────────────────────────────────────────┐
 RS-Bank 5.1                                             R-Style Software Lab

  Отчет о средних остатках на счетах за период (Рубли + Валюта)

CREATED:  16-04-97 Киреев А.Г.
NOTES:    Для выпуска отчета по ограниченному списку счетов необходимо
          наличие текстового файла "..\\mac\\report\\averest.txt"
          со списком балансовых счетов или масок б.с. с указанием главы.

MODIFICATIONS: EI 16-05-97
               LCh 20.09.99 v.5.1
  08.08.2001 - Sal.
    "По всем главам" означает по 1, 2, 3, 4, 5.
    + Проверка исключена ли глава из официального учета.
  04.07.2005  BugZ  SCR 67395 Фильтрация счетов по ТС/РС.
  04.07.2005  ABP Контроль открытых опердней
  20.10.2005  ABP Все переделал по обновленному ТЗ

└───────────────────────────────────────────────────────────────────────────*/

IMPORT FIInter, CTInter, Календарь, ReportInter, Reporting, lib_path, globals, cb_sql;
import rep_lib;
import ofstream;

FILE BalanceEnumeration() txt;

CONST ACSORT_CLIENT  = 0,
      ACSORT_ACCOUNT = 1,
      ACSORT_MAKET   = 3;

CONST AVERAGE_NORMAL        = 0, /* Среднеарифметические остатки */
      AVERAGE_CHRONOLOGICAL = 1; /* Среднехронологические остатки */

CONST SET_CHAR = 88,
      END_LINE = "\n";

CONST TA_RUB = 6; /* Рублевое покрытие */

/* Переменные устанавливаются при запуске макроса */
VAR ПоСпискуСчетов,   /* Выпуск отчета по списку счетов */
    НулевыеОстатки,   /* Обработка л\с с нулевыми остатками */
    ItogByClient,     /* Итоги по клиентам */
    ItogByBalance,    /* Итоги по б\с */
    NatCurEquivalent, /* Расчет по счетам покрытия для ин. валют */
    ExcludeOcp;       /* Исключение счетов ОВП из расчета */

VAR NumDprt,
    BeginDate,
    EndDate,
    RepDays,
    AccountFilter,
    OcpAccountServer;

VAR CurrentBalance = "",
    CurrentClient = "",
    CurrentCurrency = "",
    RestAccClnt  = $0,
    RestAccClntN = $0,
    RestAccBlnc  = $0,
    RestAccBlncN = $0,
    OverallRestN = $0,
    OverallAccounts = 0;

VAR DataFileName = FindPath( "averest.txt" );
var reportBodyFileName;

/*-----------------------------------------------*/
MACRO PrSumAvr(Sum)         /* Вывод сумм */
  if (Апострофы)
    return string(Sum:0:2:a);
  else
    return string(Sum:0:2);
  end;
END;

/*-----------------------------------------------*/
MACRO PutHead(Average, StrDate)
  FILE party(party);

  var Superior, sep, AverageName, Name, Comment;

  println({curdate}:f);
  println();

  party.PartyID = {OurBank};
  GetEQ(party);
  Superior = readNoteForObject(OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 21, EndDate);

  if(( Superior == "") or ( ValType( Superior ) == V_UNDEF ))
    println({Name_Bank}:103:w:c);
  else
    sep = index( Superior, "|");
    while(sep)
       println(substr(Superior, 1, sep-1):103:w:c);
       Superior = substr(Superior, sep + 1);
       sep = index(Superior, "|");
    end;
    println(Superior:103:w:c);
  end;
  println();

  if (Average == AVERAGE_NORMAL)
    AverageName = " СРЕДНЕАРИФМЕТИЧЕСКИХ ";
  else
    AverageName = " СРЕДНЕХРОНОЛОГИЧЕСКИХ ";
  end;
  println(string("СПРАВКА О"+AverageName+"ОСТАТКАХ"):103:c);
  println(string(strupr(StrDate)):103:c);
  println();
END;

MACRO PutTable(Average)
  var AverageName;

  if (Average == AVERAGE_NORMAL)
    AverageName = " Среднеарифметические";
  else
    AverageName = "Среднехронологические";
  end;
[┌──────────────────────┬─────────────────────────┬──────┬─────────────┬────────────────────────────────┐
 │ Наименование клиента │  Номер лицевого счета   │ Код  │Дата открытия│ ##################### остатки  │
 │                      │                         │валюты│    счета    ├───────────────┬────────────────┤
 │                      │                         │      │             │В валюте счета │  В нац. валюте │
 ├──────────────────────┼─────────────────────────┼──────┼─────────────┼───────────────┼────────────────┤
](AverageName);
END;

MACRO PutAccount(DataSet)
  var OpenDate;

  DtTmSplit(DataSet.OpenDate, OpenDate);

  if (NatCurEquivalent == SET_CHAR)
    if ((DataSet.AverRestN == 0) and (НулевыеОстатки != SET_CHAR))
      return;
    end;

    [│######################│#########################│######│#############│               │################│]
    (DataSet.NameClnt, DataSet.Acc:f:c, DataSet.FI_Code:c, OpenDate:f:c, PrSumAvr(DataSet.AverRestN):r);
  else
    if ((DataSet.AverRest == 0) and (DataSet.AverRestN == 0) and (НулевыеОстатки != SET_CHAR))
      return;
    end;

    [│######################│#########################│######│#############│###############│################│]
    (DataSet.NameClnt, DataSet.Acc:f:c, DataSet.FI_Code:c, OpenDate:f:c, PrSumAvr(DataSet.AverRest):r, PrSumAvr(DataSet.AverRestN):r);
  end;

  OverallAccounts = OverallAccounts + 1;

  OverallRestN = OverallRestN + DataSet.AverRestN;
END;

MACRO InitNewClient(DataSet)
  if (NatCurEquivalent != SET_CHAR)
    RestAccClnt = DataSet.AverRest;
  end;
  RestAccClntN    = DataSet.AverRestN;
  CurrentClient   = DataSet.NameClnt;
  CurrentCurrency = DataSet.FI_Code;
END;

MACRO PutClientItog(DataSet)
  if (ItogByClient != SET_CHAR)
    return;
  end;

  if (NatCurEquivalent == SET_CHAR)
    if (DataSet.NameClnt == CurrentClient)
      RestAccClntN = RestAccClntN + DataSet.AverRestN;
      return;
    end;

    if ((RestAccClntN == 0) and (НулевыеОстатки != SET_CHAR))
      InitNewClient(DataSet);
      return;
    end;

    [│  ИТОГО по клиенту    │                         │      │             │               │                │
     │  ####################│                         │      │             │               │################│]
    (CurrentClient, PrSumAvr(RestAccClntN):r);
  else
    if ((DataSet.NameClnt == CurrentClient) and (DataSet.FI_Code == CurrentCurrency))
      RestAccClnt  = RestAccClnt  + DataSet.AverRest;
      RestAccClntN = RestAccClntN + DataSet.AverRestN;
      return;
    end;

    if ((RestAccClntN == 0) and (RestAccClnt == 0) and (НулевыеОстатки != SET_CHAR))
      InitNewClient(DataSet);
      return;
    end;

    [│  ИТОГО по клиенту    │                         │      │             │               │                │
     │  ####################│                         │      │             │               │                │
     │  в валюте #####      │                         │      │             │###############│################│]
    (CurrentClient, CurrentCurrency:l, PrSumAvr(RestAccClnt):r, PrSumAvr(RestAccClntN):r);
  end;

  InitNewClient(DataSet);
END;

MACRO InitNewBalance(DataSet)
  if (NatCurEquivalent != SET_CHAR)
    RestAccBlnc = DataSet.AverRest;
  end;
  RestAccBlncN   = DataSet.AverRestN;
  CurrentBalance = DataSet.Balance;
END;

MACRO PutBalanceItog(DataSet)
  if (ItogByBalance != SET_CHAR)
    return;
  end;

  if (NatCurEquivalent == SET_CHAR)
    if (DataSet.Balance == CurrentBalance)
      RestAccBlncN = RestAccBlncN + DataSet.AverRestN;
      return;
    end;

    if ((RestAccBlncN == 0) and (НулевыеОстатки != SET_CHAR))
      InitNewBalance(DataSet);
      return;
    end;

    [│  ИТОГО по б/с        │                         │      │             │               │                │
     │     #########        │                         │      │             │               │################│]
    (CurrentBalance:l, PrSumAvr(RestAccBlncN):r);
  else
    if (DataSet.Balance == CurrentBalance)
      RestAccBlnc  = RestAccBlnc  + DataSet.AverRest;
      RestAccBlncN = RestAccBlncN + DataSet.AverRestN;
      return;
    end;

    if ((RestAccBlncN == 0) and (RestAccBlnc == 0) and (НулевыеОстатки != SET_CHAR))
      InitNewBalance(DataSet);
      return;
    end;

    [│  ИТОГО по б/с        │                         │      │             │               │                │
     │     #########        │                         │      │             │###############│################│]
    (CurrentBalance:l, PrSumAvr(RestAccBlnc):r, PrSumAvr(RestAccBlncN):r);
  end;

  InitNewBalance(DataSet);
END;

MACRO PutItog()
[├──────────────────────┼─────────────────────────┼──────┼─────────────┼───────────────┼────────────────┤
 │    ВСЕГО счетов      │#########################│      │             │               │                │
 │    ИТОГО по счетам   │                         │      │             │               │################│
 └──────────────────────┴─────────────────────────┴──────┴─────────────┴───────────────┴────────────────┘
](OverallAccounts:l, PrSumAvr(OverallRestN):r);
END;

/* Формирование условия на отбор счетов из daccblnc_dbt по маске (маскам) б\с */
MACRO MakeAccBlncCond_Balance(Chapter, AccBlncAlias, BalanceMask, NumPlan)
  /* Формирование условия для заданного номера плана */
  MACRO MakeAccBlncMask(AccBlncAlias, BalanceMask, NumPlan)
    if (BalanceMask != "")
      BalanceMask = "(" + ConvertMaskToSQLFormat(BalanceMask, AccBlncAlias+".t_Balance") + " AND " + AccBlncAlias+".t_numPlan = " + NumPlan+ ")";
    else
      BalanceMask = "(" + AccBlncAlias+".t_Balance <> "+GetSQLString("") + " AND " + AccBlncAlias+".t_numPlan = " + NumPlan + ")";
    end;
    return BalanceMask;
  END;

  var abCond = "",
      BlncMask = "",
      i;

  var enChapterArr     = TArray(),
      enNumPlanArr     = TArray(),
      enBalanceMaskArr = TArray();

  if (ПоСпискуСчетов != SET_CHAR)

    abCond = getChapterFilterSqlClause(NumPlan, chapter, "ac.t_chapter");

    if (BalanceMask != "")
      BlncMask = "(" + ConvertMaskToSQLFormat(BalanceMask, AccBlncAlias+".t_Balance") + " AND " + AccBlncAlias+".t_numPlan = " + NumPlan+ ")";
    else
      BlncMask =  "(" + AccBlncAlias+".t_Balance <> "+GetSQLString("") + " AND " + AccBlncAlias+".t_numPlan = " + NumPlan + ")";
    end;

    abCond = abCond + " AND " + BlncMask;

  else
  /* Обрабатывем список масок б\с */
  /* Считывание данных из файла со списком масок б\с */
    i = 0;
    rewind(BalanceEnumeration);
    while(next(BalanceEnumeration))
      enChapterArr[i]     = int(BalanceEnumeration(0));
      enNumPlanArr[i]     = int(BalanceEnumeration(1));
      enBalanceMaskArr[i] = trim(BalanceEnumeration(2));
      i = i+1;
    end;

    if (i == 0)
      println("Нет данных для отчета");
      exit(1);
    end;

    i = 0;
    BalanceMask = MakeAccBlncMask(AccBlncAlias, enBalanceMaskArr[i], enNumPlanArr[i]);
    abCond = abCond + "(ac.t_Chapter = "+enChapterArr[i]+" AND "+BalanceMask+")";
    i = i+1;
    while(i < enChapterArr.Size)
      BalanceMask = MakeAccBlncMask(AccBlncAlias, enBalanceMaskArr[i], enNumPlanArr[i]);
      abCond = abCond + " OR (ac.t_Chapter = "+enChapterArr[i]+" AND "+BalanceMask+")";
      i = i+1;
    end;
  end;

  return abCond;
END;

MACRO MakeAccountQuery(Chapter, Currency, BalanceMask, AccountMask, NumPlan, Average, Order, AccBlncAlias)
  var ShowOCP;
  var Select = "SELECT ", From = "FROM ", Where = "WHERE ", OrderBy = "ORDER BY ", CmdText = "";
  var ac, ab;

  /* Формирование запроса на получение сренеарифметического остатка для валюты */
  MACRO RestANC(fAccount, fChapter, fCurrency, fDefault, restCur)
    var query = "", i;
    var restCurrency = ternary(restCur == NULL, fCurrency, restCur);

    i = BeginDate;
    while(i < EndDate)
      query = query+"rsb_account.restac("+fAccount+", "+fCurrency+", "+GetSQLDate(i)+", "+fChapter+", "+fDefault+", "+restCurrency+")+";
      i = i+1;
    end;
    query = query+"rsb_account.restac("+fAccount+", "+fCurrency+", "+GetSQLDate(i)+", "+fChapter+", "+fDefault+", "+restCurrency+")";

    query = string("NVL((",query,")/", RepDays, ", 0)");

    return query;
  END;

  /**********************************************************************************************
    Проверка рубильника нужна только при выпуске по данным панели и с незаданными масками счетов.
    Во всех прочих случаях (отбор по маскам или по списку) что выбрали, то и покажем.
    См. tz384-02-averest.doc
  **********************************************************************************************/
  ShowOCP = (ExcludeOcp == false) or (BalanceMask != "") or (AccountMask != "") or (ПоСпискуСчетов == SET_CHAR);

  ac = "daccount_dbt ac";
  ab = "daccbalance_dbt "+AccBlncAlias;

  Select = Select + "pt.t_ShortName NameClnt, "    +END_LINE+
                    "ac.t_Account Acc," +END_LINE+
                    "fi.t_FI_Code FI_Code, "       +END_LINE+
                    "ac.t_Open_Date OpenDate, "    +END_LINE;

  if (Average == AVERAGE_NORMAL)
      if (NatCurEquivalent != SET_CHAR)
        Select = Select + RestANC("ac.t_Account", "ac.t_Chapter", "ac.t_Code_Currency", 0) + " AverRest, "+END_LINE;
      end;

      Select = Select + RestANC("ac.t_Account", "ac.t_Chapter", "ac.t_Code_Currency", 0, NATCUR) + " AverRestN, "+END_LINE;
  else
    /* Из-за особенностей ф-ции rsb_account.restap (restapc) в нее нужно передавать BeginDate-1 */
    /*//14 May 07 Malakhova Irina 106691*/
      if (NatCurEquivalent != SET_CHAR)
        Select = Select + "rsb_account.restapc(ac.t_Account, "+
                                               "ac.t_Code_Currency, "+
                                               GetSQLDate(BeginDate-1)+", "+
                                               GetSQLDate(EndDate - 1)+", "+
                                               "ac.t_Chapter, 0) AverRest, "+END_LINE;
      end;
      Select = Select + "rsb_account.restap(ac.t_Account, "+
                                            GetSQLDate(BeginDate-1)+", "+
                                            GetSQLDate(EndDate - 1)+", "+
                                            "ac.t_Chapter, 0"+
                                            ",ac.t_Code_Currency,0) AverRestN, "+END_LINE;
  end;
  Select = Select + "ac.t_Balance Balance,"+END_LINE;
  Select = Select + "ac.t_Sort Maket";
  Select = Select + END_LINE;

  From = From + ac+", "+ab+", "+"dparty_dbt pt, dfininstr_dbt fi";
  From = From + END_LINE;

  Where = Where + "("+MakeAccBlncCond_Balance(Chapter, AccBlncAlias, BalanceMask, NumPlan)+")"+END_LINE;
  if (AccountMask != "")
    Where = Where + " AND ("+ConvertMaskToSQLFormat(AccountMask, "ac.t_Account")+")"+END_LINE;
  end;

  Where = Where + " AND ac.t_AccountId = "+AccBlncAlias+".t_AccountId";

  if (Currency >= 0)
    Where = Where + " AND ac.t_Code_Currency = "+Currency+END_LINE;
  elif (Currency == -2)
    Where = Where + " AND ac.t_Code_Currency <> " + NATCUR + END_LINE;
  end;
  Where = Where + " AND fi.t_FIID = ac.t_Code_Currency"+END_LINE;
  Where = Where + " AND ac.t_Open_Date <= "+GetSQLDate(EndDate)+
                  " AND (  ac.t_Close_Date >= "+GetSQLDate(BeginDate)+
                  "      OR ac.t_Close_Date = "+GetSQLDate(Date(0,0,0))+")"+END_LINE;
  Where = Where + " AND INSTR(ac.t_type_account, cnst.getTA("+string(TA_RUB)+")) = 0"+END_LINE;

  if (ShowOCP == false)
    Where = Where + " AND NOT" + OcpAccountServer.GetAsSqlString("ac")+END_LINE;
  end;

  Where = Where + " AND "+AccountFilter.GetAsSqlString("ac")+END_LINE;

  Where = Where + " AND pt.t_PartyID = ac.t_Client";
  Where = Where + END_LINE;

  if (Order == ACSORT_CLIENT)
    OrderBy = OrderBy + "NameClnt";
    if (Currency != 0)
      OrderBy = OrderBy + ", FI_Code";
    end;
    OrderBy = OrderBy + END_LINE;
  elif (Order == ACSORT_ACCOUNT)
    OrderBy = OrderBy + "Balance, Acc";
    OrderBy = OrderBy + END_LINE;
  elif (Order == ACSORT_MAKET)
    if (ItogByClient == SET_CHAR)
      OrderBy = OrderBy + "NameClnt, ";
    end;
    if (ItogByBalance == SET_CHAR)
      OrderBy = OrderBy + "Balance, ";
    end;
    if (Currency != 0)
      OrderBy = OrderBy + "FI_Code, ";
    end;
    OrderBy = OrderBy + "Maket";
    OrderBy = OrderBy + END_LINE;
  else
    OrderBy = "";
  end;

  CmdText = Select + From + Where + OrderBy;

  return CmdText;
END;

/**************************************************************************/
/* Точка входа в программу                                                */
/**************************************************************************/
MACRO Report( _NumDprt, Chapter, PeriodKind,
              Date1,       /* нач.дата периода или номер месяца\квартала*/
              Date2,       /* конечн.дата периода или год */
              Currency, Balance, Account,
              organizationStructure, issueMode,
              NumPlan, Average, Order, cSetA,
              cSetClose, cSetZero, cItogClient, cItogBalance,
              cNatCurEquivalent, cExludeOCP
            )

  ПоСпискуСчетов   = cSetClose;
  НулевыеОстатки   = cSetZero;
  ItogByClient     = cItogClient;
  ItogByBalance    = cItogBalance;
  NatCurEquivalent = cNatCurEquivalent;
  ExcludeOcp       = ternary(cExludeOCP == SET_CHAR, true, false);


  if ( cSetA == SET_CHAR )
    Апострофы = True;
  else
    Апострофы = False;
  end;


  MACRO LastDayInMonth( Mon, Year )
    if (Mon == 12)
      return date(31,Mon, Year);
    else
      return date(1, Mon+1,Year)-1;
    end;
  END;

  var mon, year, StrDate, DataSet, CmdText, ProgressHead, DepartmentList;



  datesplit( {curdate}, null, mon, year );

  NumDprt = _NumDprt;

  if   ( PeriodKind == 1 )   /* За месяц */
    BeginDate = date( 1, Date1, Date2);
    EndDate   = LastDayInMonth( Date1, Date2 );
    StrDate = "за "+strlwr(monname(Date1))+ " "+string(Date2)+" г.";
  elif ( PeriodKind == 2 )   /* За квартал */
    BeginDate = date( 1, Date1*3-2, Date2);
    EndDate   = LastDayInMonth( Date1*3, Date2 );
    StrDate = "за "+string(Date1)+ " квартал "+string(Date2)+" г.";
  elif ( PeriodKind == 3 )   /* За год */
    BeginDate = date( 1, 1, Date2);
    EndDate   = date( 31, 12, Date2 );
    StrDate = "за "+string(Date2)+" г.";
  else                       /* За период */
    BeginDate = Date1;
    EndDate   = Date2;
    if ( BeginDate == EndDate )
      StrDate = "за "+string(Date1:f);
    else
      StrDate = "за период с "+string(Date1:f)+ " по "+string(Date2:f);
    end;
  end;

  RepDays = NDays(EndDate)-NDays(BeginDate)+1;

  /* Фильтрация по ТС/РС */
  DepartmentList = RepDepartmentList( organizationStructure, issueMode, NumDprt );
  AccountFilter  = RepAccountFilter( DepartmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);  /* Global */

  /* Фильтрация счетов ОВП */
  OcpAccountServer = RepOcpAccountServer(Chapter, Currency, DepartmentList);

  /* Контроль открытых опердней */
  if (RepOperdaysOpened(departmentList, BeginDate, EndDate).ShouldContinue == false)
    exit(1);
  end;

  PutHead(Average, StrDate);
  if (ПоСпискуСчетов == SET_CHAR)     /* по списку счетов */
    if ( not Open( BalanceEnumeration, DataFileName) )
      println("Ошибка открытия файла данных " + DataFileName);
      MsgBox( "Ошибка открытия файла данных |" + DataFileName);
      Exit(1);
    end;
  end;

  if (Average == AVERAGE_NORMAL)
    ProgressHead = " среднеарифметических остатках";
  else
    ProgressHead = " среднехронологических остатках";
  end;

  BegAction(1000, "Получение данных о"+ProgressHead, false);

  CmdText = MakeAccountQuery(Chapter, Currency, Balance, Account, NumPlan, Average, Order, "ab");

  DataSet = TRsbDataSet(CmdText);
  EndAction();

  InitProgress(-1, "Выпуск отчета...", "Выпуск справки о"+ProgressHead);
  if (DataSet.MoveNext())
    CurrentClient   = DataSet.NameClnt;
    CurrentCurrency = DataSet.FI_Code;
    CurrentBalance  = DataSet.Balance;
    PutTable(Average);
    PutBalanceItog(DataSet);
    PutClientItog(DataSet);
    PutAccount(DataSet);
    UseProgress(OverallAccounts);
  end;

  while(DataSet.MoveNext())
    PutBalanceItog(DataSet);
    PutClientItog(DataSet);
    PutAccount(DataSet);
    UseProgress(OverallAccounts);
  end;

  if (OverallAccounts > 0)
    DataSet.Balance  = "";
    DataSet.NameClnt = "";
    DataSet.FI_Code  = "";
    PutBalanceItog(DataSet);
    PutClientItog(DataSet);
    PutItog();
  else
    println("Нет данных для отчета");
  end;

  RemProgress();
  return true;
END;

macro ibrCreateReport( _NumDprt, Chapter, PeriodKind,
                        Date1,       
                        Date2,      
                        Currency, Balance, Account,
                        organizationStructure, issueMode,
                        NumPlan, Average, Order, cSetA,
                        cSetClose, cSetZero, cItogClient, cItogBalance,
                        cNatCurEquivalent, cExludeOCP, fileName
                    )
    var output = TOFStream(fileName);

    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    Report( _NumDprt, Chapter, PeriodKind,
           Date1,       /* нач.дата периода или номер месяца\квартала*/
           Date2,       /* конечн.дата периода или год */
           Currency, Balance, Account,
           organizationStructure, issueMode,
           NumPlan, Average, Order, cSetA,
           cSetClose, cSetZero, cItogClient, cItogBalance,
           cNatCurEquivalent, cExludeOCP
        );

    output.resetOutputFile();

    return true;      
end;

/*eof*/
