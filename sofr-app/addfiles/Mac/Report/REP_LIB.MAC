/*───────────────────────────────────────────────────────────────────────────┐
 RS-Bank 5.1                                      R-Style Software Lab Ltd

  Вспомогательные функции для отчетов

CREATED:  22.09.99 LCh
NOTES:

└───────────────────────────────────────────────────────────────────────────*/

import CTInter, BankInter, FIInter, globals;
import lib_lang, PTInter, RsbDataSet, lib_str; /*26 Jan 07 Fri 17:37:18 Malakhova Irina*/
import cb_sql;
import Reporting;

file fi( fininstr );            /* Справочник валют */
file dprt( dp_dep );            /* Справочник филиалов */
FILE Главы( obchaptr );
private FILE party( party );

const NOTEKIND_PARTY_REPBANKNAME = 21;  // номер примечания "имя банка для отчётности"

var Апострофы = FALSE,
    БезКопеек = FALSE,
    ACCOUNTTEMPLATE; /* Шаблон отображения номера лицевого счета */

const  PathTemplate = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ОТОБРАЖЕНИЕ ПОЛЕЙ\\ACCOUNTTEMPLATE";

/*--------- Настройка ---------------------------*/

var ФорматНулевыхСумм = 0,
    УчетСчетаОВП      = FALSE;

array  СчетОВП1,СчетОВП2,СчетОВП3,СчетОВП4;
       СчетОВП1(0) = "ОВП";
       СчетОВП2(0) = "ОВП";
       СчетОВП3(0) = "ОВП-99998"; СчетОВП3(1) = "ОВП-99999";
       СчетОВП4(0) = "ОВП";

/*-----------------------------------------------*/
macro ВключитьПечатьАпострофов()
     Апострофы = TRUE;  /* Вывод с апострофами */
end;

macro ПечатьБезКопеек( Глава )
  if ( Глава == 5 ) БезКопеек = TRUE;
  else              БезКопеек = FALSE;
  end;
end;

macro УчетОВП()
  return УчетСчетаОВП;
end;

/*-----------------------------------------------*/
MACRO PrSum( Sum )         /* Вывод сумм */
  if ( БезКопеек )
    if ( Апострофы )    return string(Sum:0:0:a);
    else                return string(Sum:0:0);
    end;
  elif ( (Sum == 0) and (ФорматНулевыхСумм==1) )
                        return "0";
  else
    if ( Апострофы )    return string(Sum:a);
    else                return string(Sum);
    end;
  end;
END;

/*==========================================*/
/* Прочитать значение(строку) ключа из настроек банка */
macro GetRegString( RegName )
  var  err, Str = "";
   GetRegistryValue( RegName, V_STRING, Str, err );
   if ( err  ==  7 )
     msgbox( "Не определен ключ "+RegName+" в настройках банка" );exit( 1 );
   end;
   return Str;
end;

/*-----------------------------------------------*/
macro МаскаНомераЛицевого( )
   ACCOUNTTEMPLATE = GetRegString( PathTemplate );
end;

/*-----------------------------------------------*/
MACRO PrAccount( Account)         /* Вывод номера лиц.счета */
  var sept = ".", str = "",
      template = ACCOUNTTEMPLATE,
      i = index( template, sept );

  while( (Account!="" ) and (i > 0) )
      str = str + substr(Account, 1, i-1) + sept;
      Account  = substr(Account, i);
      template = substr(template,i+1 );
      i = index( template, sept );
  end;
  return  str + Account;
END;

/* ----------------------------------------------------- */
/*  Дополняет строку num слева нулями до len знаков      */
macro repLZ( num, len )
    var str1 = trim( string( num ) ),
        len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else  return  mkstr("0", len-len1 ) + str1;
    end;
end;

/* ----------------------------------------------------- */
/* Вывод даты через точку   */
macro PutDatePoint( dat )
   var Day, Mon, Year;
   datesplit( dat, Day, Mon, Year );
   if ( Year == 0 ) return ""; end;
   return repLZ(Day,2) + "." + repLZ(Mon,2) + "." + repLZ(Year,4);
end;

/*-----------------------------------------------*/

macro НазваниеФилиала(Филиал)
  var DpName, DpComment;

  if ( Филиал == 0 )
    return "";
  else
    if( CB_GetDepartmentCodeAndName(Филиал,DpName,DpComment) != 0 )
      DpName    = "";
      DpComment = "";
    end;
    return DpName + " " + DpComment;
  end;
end;


MACRO НазваниеГлавы( Глава )
  var Name = "";
  if ( Глава > 0 )
    Главы.Chapter = Глава;
    if ( GetEQ ( Главы ))
            Name = Главы.Symbol +"."+ Главы.Name;
    else    Name = "Глава "+string(Глава)+" не найдена";
    end;
  end;
  return Name;
END;

MACRO ИмяОперациониста( )
    FILE pers( person );
    pers.Oper = {oper};
    if( getEQ( pers ) )
        return pers.Name;
    else
        return "";
    end;
END;

// банк - ID или структура буфера (party.dbt) банка. дата - дата, за кот. берём значение.
MACRO ИмяБанкаДляОтчетов( банк, дата )
    if( ValType( банк ) == V_INTEGER )
        party.PartyId = банк;
        GetEq( party );
        банк = party;
    end;

    var Superior = readNoteForObject( OBJTYPE_PARTY, UniID( банк, OBJTYPE_PARTY ), NOTEKIND_PARTY_REPBANKNAME, дата );

    if( ( Superior == "" ) or ( Superior == NULL ) )
        Superior = {Name_Bank};
    end;

    return Superior;
END;

/*===============================================*/
MACRO  СчетИзМассива( БалСчет, Массив )  /* Подстрока */
   var i = 0;
   while ( i < asize( Массив ) )
/*     if ( БалСчет == Массив(i) ) return TRUE; end;*/
     if ( substr(БалСчет,1,strlen(Массив(i))) == Массив(i) ) return TRUE; end;
     i = i + 1;
   end;
   return FALSE;
END;

/*-----------------------------------------------*/
MACRO  ПроверкаОВП( Глава, БалСчет )
  if   ( Глава == 1 ) return СчетИзМассива( БалСчет, СчетОВП1 );
  elif ( Глава == 2 ) return СчетИзМассива( БалСчет, СчетОВП2 );
  elif ( Глава == 3 ) return СчетИзМассива( БалСчет, СчетОВП3 );
  elif ( Глава == 4 ) return СчетИзМассива( БалСчет, СчетОВП4 );
  else return FALSE;
  end;
END;

/*-----------------------------------------------*/
MACRO  ПроверкаОВП2( Глава, БалСчет, Result )   /* Для вызова из С-шника */
  if   ( Глава == 1 ) Result = СчетИзМассива( БалСчет, СчетОВП1 );
  elif ( Глава == 2 ) Result = СчетИзМассива( БалСчет, СчетОВП2 );
  elif ( Глава == 3 ) Result = СчетИзМассива( БалСчет, СчетОВП3 );
  elif ( Глава == 4 ) Result = СчетИзМассива( БалСчет, СчетОВП4 );
  else Result = FALSE;
  end;
END;

/*===============================================*/
/* определение вида остатка с учетов вида счета при нулевом остатке */

const ACTIV  = "А",
      PASSIV = "П";

MACRO ОстатокАктив( Остаток, ВидСчета )
  if   (Остаток<0) return TRUE;
  elif (Остаток>0) return FALSE;
  elif (ВидСчета==ACTIV )  return TRUE;
  elif (ВидСчета==PASSIV ) return FALSE;
  else  return TRUE; /* нулевой остаток АП счета */
  end;
end;

MACRO АП(Остаток, ВидСчета )
     if ( ОстатокАктив( Остаток, ВидСчета ) ) return "актив";
     else return "пассив";
     end;
END;

MACRO ДК(Остаток, ВидСчета )
     if ( ОстатокАктив( Остаток, ВидСчета ) ) return "Д";
     else return "К";
     end;
END;

MACRO Актив(Остаток, ВидСчета )
     if ( ОстатокАктив( Остаток, ВидСчета ) ) return PrSum(abs(Остаток));
     else return "";
     end;
END;

MACRO Пассив(Остаток, ВидСчета )
     if ( ОстатокАктив( Остаток, ВидСчета ) ) return "";
     else return PrSum(Остаток);
     end;
END;

macro getDepartmentId(departmentCodeName : String, organizationStructure : Integer)
    const TERRITORIAL = 1;

    var departmentId;

    var departmentSource;
    if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
      departmentSource = "drepdpdep_vw";
    else
      departmentSource = "drepdpreg_vw";
    end;

    var query = "SELECT t_nodeId"
       + "\n" + "  FROM " + departmentSource
       + "\n" + " WHERE t_codeName = " + getSqlString(departmentCodeName);
    var ds = TRsbDataset(query);
    ds.setFieldType("t_nodeId", V_INTEGER);
    if (ds.next())
        departmentId = ds.nodeId;
    else
        departmentId = 0;
    end;

    return departmentId;
end;

macro getDepartmentCode(departmentId : String, organizationStructure : Integer)
    const TERRITORIAL = 1;

    var departmentCode;

    var departmentSource;
    if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
      departmentSource = "drepdpdep_vw";
    else
      departmentSource = "drepdpreg_vw";
    end;

    var query = "SELECT t_codeName"
       + "\n" + "  FROM " + departmentSource
       + "\n" + " WHERE t_nodeId = " + departmentId;
    var ds = TRsbDataset(query);
    ds.setFieldType("t_codeName", V_STRING);
    if (ds.next())
        departmentCode = ds.codeName;
    else
        departmentCode = "";
    end;

    return departmentCode;
end;

macro getFiId(fiCode : String, onlyForeign : Bool)
    defaultParm(onlyForeign, false);
    var fiId;
    if (fiCode == "")
        if (onlyForeign)
            fiId = -2; //ALLCURRENCY
        else
            fiId = -1; //ALLFININSTR
        end;
    else
        fiId = ПолучитьКодФинИн(fiCode);
    end;
    return fiId;
end;

/**
 * Печать шапки отчета (АЗ "Шапки СБ") в буфер
 */
macro printBankHeaderBuf(dprt, orgStructure, width, buf:TArray, shift)

  defaultParm(shift, 0);

  const TERRITORIAL = 1;

  var j;
  var str;
  var ds, query;
  var departments = ternary(orgStructure == TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw");
  var isSb;

  query =          "SELECT t_partyId"
          + "\n" + "  FROM " + departments
          + "\n" + " WHERE t_code = " + dprt;

  ds = TRsbDataset(query);
  ds.setFieldType("t_partyId", V_INTEGER);
  if (ds.next() and (ds.partyId != null) and (ds.partyId != 0))
    isSb = isBankType(ds.partyId, PT_KIND_SAVINGS_BANK);
  else
    isSb = false;
  end;

  /* 09.03.2006 ABP Сортировка по убыванию уровня иерархии, т.к. осуществляется проход к корню.
     Т.е. первый уровень - это узел, для которого выпускается отчет. */
  query =          " SELECT t_nodeType t_nodeType,"
          + "\n" + "        t_name     t_name"
          + "\n" + "   FROM " + departments
          + "\n" + "CONNECT BY PRIOR t_parentNodeId = t_nodeId"
          + "\n" + "  START WITH t_code = " + dprt
          + "\n" + " ORDER BY level DESC";

  ds = TRsbDataset(query);
  ds.setFieldType("t_nodeType", V_INTEGER);
  ds.setFieldType("t_name",     V_STRING);

  while (ds.next())

    if ((IsSb == false) and (ds.nodeType == 2)) /* для не СБ РФ узлы ВСП не печатаем */
      continue;
    end;

    var name = trim(ds.name);
    if (strlen(name) > width)
      j = 0;
      str = substr(name, width * j + 1, width);
      while (strlen(str) > 0)
        buf.value(buf.size) = mkStr(" ", shift) + strAlign(str, width);
        j = j + 1;
        str = subStr(name, width * j + 1, width);
      end;
    else
      buf.value(buf.size) = mkStr(" ", shift) + strAlign(name, width, STR_ALIGN_CENTER);
    end;

  end;

end;

/**
 * Печать шапки отчета (АЗ "Шапки СБ") в текущий поток вывода
 */
macro printBankHeader(dprt, orgStructure, width, shift)

  defaultParm(shift, 0);
  var buf = TArray();

  printBankHeaderBuf(dprt, orgStructure, width, buf, shift);

  for (var i, buf)
    println(i);
  end;

end;

/**
 * Получение списка "всех" глав заданного плана.
 * "Все главы" для плана счетов 0 (основной план счетов): все неисключенные из официального учета главы, связанные с планом 0.
 * "Все главы" для прочих планов счетов: все главы, связанные с планом.
 * @param planNumber - номер плана счетов
 * @return список (массив) записей со структурой obchaptr.dbt
 * @author ABP
 * @version 1.0
 * @since v.6.20.030.042
 */
macro getAllChaptersList(planNumber : Integer) : Object

    var chapters = TArray();
    var dataSource = TBFile("obchaptr.dbt");
    var filterText : String;
    var ch = TRecHandler("obchaptr.dbt");

    filterText = "EXISTS (SELECT NULL"
        + "\n" + "          FROM dnameplan_chaptr_dbt plan_chapt"
        + "\n" + "         WHERE plan_chapt.t_iNumPlan = " + planNumber
        + "\n" + "           AND plan_chapt.t_chapter = t.t_chapter"
        + "\n" + "       )";

    if (planNumber == 0)
        filterText = filterText
        + "\n" + "AND t.t_excludeFromFormalAcnt != 'X'";
    end;

    dataSource.addFilter(filterText);

    while (dataSource.next())
        chapters(chapters.size) = TRecHandler("obchaptr.dbt");
        copy(chapters(chapters.size-1), dataSource);
    end;

    if (chapters.size == 0)
        chapters = null;
    end;

    return chapters;

end;

/**
 * Получение списка номеров "всех" глав заданного плана.
 * "Все главы" для плана счетов 0 (основной план счетов): все неисключенные из официального учета главы, связанные с планом 0.
 * "Все главы" для прочих планов счетов: все главы, связанные с планом.
 * @param planNumber - номер плана счетов
 * @return список (массив) номеров глав
 * @author ABP
 * @version 1.0
 * @since v.6.20.030.042
 */
macro getAllChaptersNumberList(planNumber : Integer) : Object

    var chapters = TArray();
    var chaptersList = getAllChaptersList(planNumber);

    if (chaptersList != null)
        for (var chapter, chaptersList)
            chapters(chapters.size) = chapter.rec.chapter;
        end;
    else
        chapters(0) = 0; // Несуществующая глава
    end;

    return chapters;

end;

private macro getChapterFilterSqlClauseEx(wrkChapters : Object, chapterFieldName : String) : String
    var filterText = "";

    if (wrkChapters.size > 0)
        for (var chapter, wrkChapters)
            filterText = filterText + " OR " + chapterFieldName + " = " + chapter;
        end;
        filterText = "(" + substr(filterText, 5) + ")";
    else
        filterText = "(" + getSqlBoolIdentity(false) + ")";
    end;

    return filterText;
end;

private macro getChapterFilterSqlClauseByChapterAndPlan(planNumber : Integer, chapterNumber : Integer, chapterFieldName : String) : String
    var wrkChapters = TArray();

    if (chapterNumber != 0)
        wrkChapters(0) = chapterNumber;
    else
        wrkChapters = getAllChaptersNumberList(planNumber);
    end;

    return getChapterFilterSqlClauseEx(wrkChapters, chapterFieldName);
end;

private macro getChapterFilterSqlClauseByChapterList(chapterNumber : Object, chapterFieldName : String) : String
    return getChapterFilterSqlClauseEx(chapterNumber, chapterFieldName);
end;

/**
 * Получение sql-выражения для фильтрации по главе.
 * "Все главы" для плана счетов 0 (основной план счетов): все неисключенные из официального учета главы, связанные с планом 0.
 * "Все главы" для прочих планов счетов: все главы, связанные с планом.
 * @param param1 (planNumber или chapterNumber) - номер плана счетов или массив глав счетов
 * @param param2 (chpaterNumber или chapterFieldName) - номер главы учета (0 - "все" главы) или название поля "глава"
 * @param param3 (chapterFieldName или отсутствует) - название поля "глава"
 * @return текст sql-выражения
 * @author ABP
 * @version 1.1
 * @since v.6.20.030.042
 */
macro getChapterFilterSqlClause() : String

    var param1;
    var param2;
    var param3;

    var filterText;

    // 24.02.2012 ABP Контроль типов возложим на интерпретатор
    if (parmCount() == 2)
        getParm(0, param1);
        getParm(1, param2);
        filterText = getChapterFilterSqlClauseByChapterList(param1, param2);
    else
        getParm(0, param1);
        getParm(1, param2);
        getParm(2, param3);
        filterText = getChapterFilterSqlClauseByChapterAndPlan(param1, param2, param3);
    end;

    return filterText;

end;

/**
 * Получение sql-выржения для фильтрации по наборному полю
 *
 * @param valueFieldName Названия поля, по которому нужно выполнять фильтрацию
 * @param pattern Шаблон значений для фильтрации
 * @param method Метод фильтрации
 * @return текст sql-выражения
 *
 * @author ABP
 * @version 1.0
 * @since v.6.20.030.54
 *
 */
macro getPatternFilterClause(valueFieldName : String, pattern : Variant, method : Integer, isInclude : Bool) : String
    var patternString : String;

    var compareCondition = "";

    if (valType(isInclude) == V_UNDEF)
        isInclude = true;
    end;

    if (not isInclude)
        compareCondition = "NOT ";
    end;

    if (valType(pattern) == V_GENOBJ)
        patternString = strAssemble(pattern);
    else
        patternString = pattern;
    end;

    var clause = "";
    var length = strlen(patternString);

    if (length == 0)
        clause = "(1 = 1)";
        return clause;
    end;

    if (method == REP_CONDITION_EQ)
        clause = compareCondition + "REGEXP_LIKE(" + valueFieldName + ", '^[" + patternString + "]{" + length + "}$')";
    elif (method == REP_CONDITION_AND)
        clause = compareCondition + "REGEXP_LIKE(" + valueFieldName + ", '([^" + patternString + "]*[" + patternString + "]){" + length + "}[^" + patternString + "]*')";
    elif (method == REP_CONDITION_OR)
        clause = compareCondition + "REGEXP_LIKE(" + valueFieldName + ", '[" + patternString + "]')";
    else
        // 17.08.2012 ABP В случае неизвестного метода используем "И"
        clause = compareCondition + "REGEXP_LIKE(" + valueFieldName + ", '([^" + patternString + "]*[" + patternString + "]){" + length + "}[^" + patternString + "]*')";
    end;

    return clause;
end;

/**
 * Проверка использования региональной структуры
 *
 * @return признак использования
 *
 * @author ABP
 * @version 1.0
 * @since v.6.20.031.46
 */
macro isRegionalStructureUsed() : Bool
    var ds = TRsbDataset("SELECT 1 FROM DUAL WHERE EXISTS (SELECT 1 FROM drepdpreg_vw)");
    return (ds.next());
end;
