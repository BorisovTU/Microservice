/*
$Name:          pr_jrncrtrn.mac
$Module:        Внутренняя отчетность
$Description:   Журнал исправительных оборотов
*/

/**
 *  RS-Bank 6.0
 *
 *  File Name   : pr_jrncrtrn.mac                                   January 24, 2007
 *  Programmer  :
 *  Description : Журнал исправительных оборотов.
 *  Comment     :
 *  Modify      : Ivkina 24.01.2007
 */

IMPORT OprInter,FIInter,CTInter,"treport.mac", BankInter, oralib, cb_sql, "prpm.mac", globals;
import Reporting;
import rep_lib;
import ofstream;
//------------------------------------------------------------------------------
private var ALLCURRENCY = -2;   /* Все ин. валюты */
var reportBodyFileName;

/*ширина полей таблицы*/
var widthDate   = 10;
var widthNumb   = 5;
var widthType   = 12;
var widthCur    = 6;
var widthDebet  = 30;
var widthCredit = 30;
var widthSum    = 14;
var widthSumRur = 19;
var widthReason = 30;

/*общая ширина отчета*/
var width = 2 + widthDate  + 2 + widthNumb   + 2 + widthType + 2 + widthCur    +
            2 + widthDebet + 2 + widthCredit + 2 + widthSum  + 2 + widthSumRur + 2 + widthReason + 2;
//------------------------------------------------------------------------------
private macro PrintHeadOfLog1(Dep_Number, OrgStructure, First_Date, Second_Date, FIID)

  var fininsrtString = "";

  printBankHeader(Dep_Number, OrgStructure, width);
  println();
  println(StrAlign("Журнал", width, STR_ALIGN_CENTER));
  println(StrAlign("исправительных оборотов", width, STR_ALIGN_CENTER));
  if(FIID != ALLFININSTR)
    if(FIID != ALLCURRENCY)
      fininsrtString = ПолучитьКодФинИн(FIID) + " (" + ПолучитьИмяФинИн(FIID) + ") ";
    end;
    println(StrAlign("в валюте " + fininsrtString, width, STR_ALIGN_CENTER));
  end;
  println(StrAlign("за период с " + string(First_Date:f) + " по " + string(Second_Date:f), width, STR_ALIGN_CENTER));
  println();
end;

//-------------------------------------------------
PRIVATE MACRO PrintHeadCages1(Report1 : CTableReport)

  Report1.AddColumn("Дата",               widthDate   );
  Report1.AddColumn("№ док",              widthNumb   );
  Report1.AddColumn("Вид оборотов",       widthType   );
  Report1.AddColumn("Валюта",             widthCur    );
  Report1.AddColumn("Дебет",              widthDebet  );
  Report1.AddColumn("Кредит",             widthCredit );
  Report1.AddColumn("Сумма в валюте",     widthSum    );
  Report1.AddColumn("Эквивалент в рублях",widthSumRur );
  Report1.AddColumn("Основание",          widthReason );
  Report1.PrintHead;

END;

macro printOrder(orderKind)
    if (orderKind == REP_ORDER_KIND_ALL)
        println("Вид ордера: Все");
    elif (orderKind == REP_ORDER_KIND_BANK)
        println("Вид ордера: Банковский");
    elif (orderKind == REP_ORDER_KIND_MEMORIAL)
        println("Вид ордера: Мемориальный");
    end;
end;

//-----------------------------------------------------------------
PRIVATE MACRO PrintOneLogHead1(Report : CTableReport, rs, Chapter, orderKind)

  var  chapt;

  chapt = TRsbDataset( "SELECT T_SYMBOL,T_NAME FROM DOBCHAPTR_DBT WHERE T_CHAPTER = "+Chapter);
  IF(chapt.moveNext)
[Глава ##### #########################](Chapter,chapt.value(1));
printOrder(orderKind);
  ELSE
[Глава #################];
printOrder(orderKind);
  END;
END;

//-----------------------------------------------------
PRIVATE MACRO PrintOneDoc1(Report : CTableReport, rs, flag)

    var  cur,cur_cmd;
    var  SumR : moneyl;
    var  ID;
    var  count;
    var  FIID_num = 4;
    var rateDate;

    rateDate = sqlDate2date(rs.value(15));
    rateDate = ternary(strlen(trim(string(ratedate))) != 0, ratedate, sqlDate2date(rs.value(8)));

    cur = TRsbDataset( "SELECT T_FI_CODE FROM DFININSTR_DBT WHERE T_FIID = "+string(rs.value(FIID_num)));

    if (flag==1) Report.PrintSeparator(TRUE);end;

    if(cur.moveNext)
        if(int(rs.value(FIID_num)) != NATCUR)
            ConvSum( SumR , rs.value(5), rateDate, int(rs.value(FIID_num)) );
            Report.PrintStringTransferByWord(sqlDate2date(rs.value(8)),rs.value(11),rs.value(12),
                cur.value(0),string(rs.value(6):f),string(rs.value(7):f),rs.value(5),SumR,rs.value(10));
        else
            Report.PrintStringTransferByWord(sqlDate2date(rs.value(8)),rs.value(11),rs.value(12),
                cur.value(0),string(rs.value(6):f),string(rs.value(7):f),rs.value(5),"",rs.value(10));
        end;
    else
        if(int(FIID_num) != NATCUR)
            ConvSum( SumR , rs.value(5), rateDate, int(rs.value(FIID_num)) );
            Report.PrintStringTransferByWord(sqlDate2date(rs.value(8)),rs.value(11),rs.value(12),
                "",string(rs.value(6):f),string(rs.value(7):f),rs.value(5),SumR,rs.value(10));
        else
            Report.PrintStringTransferByWord(sqlDate2date(rs.value(8)),rs.value(11),rs.value(12),
                "",string(rs.value(6):f),string(rs.value(7):f),rs.value(5),"",rs.value(10));
        end;
    end;
END;

//---------------------------------------------------
PRIVATE MACRO PrintLogPodval1(Report : CTableReport)
  println();
  println("Главный бухгалтер________________________ /                             /");
  println();
  println("Бухгалтер        ________________________ /                             /");
END;
//-----------------------------------------------------
MACRO CorReport2(DprtID, OrgStructure, IssueMode, Chapter, FIID, First_Date, Second_Date, Correct_Kind, Order_Kind, fileName)
    var rs , cmd , strcmd, strcmd_cb_doc = "", strcmd_multydoc = "", strcmd_bankOrder = "";
    var Report1=CTableReport( 0, FALSE, FALSE);
    var Chapter_ = 0, Dep_ = -1, FIID_ = -1;
    var Chapter_new, Dep_new, FIID_new;
    var Count_ispr, Count_err;
    var NextRecord;
    var SumMoney : moneyl;
    var wherecond = "";
    var flag;
    var departmentList;
    var branchFilter;
    var accountFilter;

    var i;
    var correctKindSign;
    var correctKindSqlStr = TArray();
    var allCorrectKindSignsStr = "";

    if (fileName != null)
        var printOfstream = TOfstream(fileName);

        printOfstream = TOfstream(fileName);
        printOfstream.setOutputFile();
        reportBodyFileName = printOfstream.getFileName();
    end;

    if (Order_Kind == REP_ORDER_KIND_BANK) //банковский ордер
        correctKindSign   = arrCreate("И", "К", "N");
    else // все
        correctKindSign   = arrCreate("S", "И", "К", "N");
    end;

    departmentList = RepDepartmentList(OrgStructure, IssueMode, DprtID);
    branchFilter   = RepBranchFieldFilter(departmentList);
    accountFilter  = RepAccountFilter(departmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);

    i = 0;
    while (i < correctKindSign.size)
        correctKindSqlStr[i] = "'%" + correctKindSign[i] + "%'";
        allCorrectKindSignsStr = allCorrectKindSignsStr + correctKindSign[i];
        i = i + 1;
    end;

    /* Контроль опердней */
    if(RepOperdaysOpened( departmentList, First_Date, Second_Date ).ShouldContinue == false)
        exit(1);
    end;

    strcmd = "TRUNCATE TABLE DISPR_DOCS_TMP;";
    execSQL(strcmd);
    strcmd = "TRUNCATE TABLE DOPR_DOCS_TMP;";
    execSQL(strcmd);

    strcmd_cb_doc = "pm.t_DocKind = " + string(DLDOC_MEMORIALORDER) + "AND pm.t_Purpose = " + string(PM_PURP_MEMORDER) + " ";
    strcmd_cb_doc = strcmd_cb_doc + "AND pm.t_DocumentID = d.t_DocumentID ";
    strcmd_cb_doc = strcmd_cb_doc + "AND rm.t_PaymentID = pm.t_PaymentID ";
    strcmd_cb_doc = strcmd_cb_doc + "AND pm.t_ValueDate >= TO_DATE('"+string(First_Date)+"','DD.MM.YYYY') ";
    strcmd_cb_doc = strcmd_cb_doc + "AND pm.t_ValueDate <= TO_DATE('"+string(Second_Date)+"','DD.MM.YYYY') ";
    strcmd_cb_doc = strcmd_cb_doc + "AND" + BranchFilter.GetAsSqlString("pm.t_Department");

    strcmd_bankOrder = "pm.t_DocKind = " + string(DLDOC_BANKORDER);
    strcmd_bankOrder = strcmd_bankOrder + "AND pm.t_DocumentID = i.t_DocumentID ";
    strcmd_bankOrder = strcmd_bankOrder + "AND rm.t_PaymentID = pm.t_PaymentID ";
    strcmd_bankOrder = strcmd_bankOrder + "AND pm.t_ValueDate >= TO_DATE('"+string(First_Date)+"','DD.MM.YYYY') ";
    strcmd_bankOrder = strcmd_bankOrder + "AND pm.t_ValueDate <= TO_DATE('"+string(Second_Date)+"','DD.MM.YYYY') ";
    strcmd_bankOrder = strcmd_bankOrder + "AND" + BranchFilter.GetAsSqlString("pm.t_Department");

    strcmd_multydoc = "pm.t_DocKind = "+string(CB_MULTYDOC)+" ";
    strcmd_multydoc = strcmd_multydoc + "AND pm.t_DocumentID = d.t_AutoKey ";
    strcmd_multydoc = strcmd_multydoc + "AND pm.t_Purpose = "+string(PM_PURP_MULTYDOC)+" ";
    strcmd_multydoc = strcmd_multydoc + "AND rm.t_PaymentID = pm.t_PaymentID ";
    strcmd_multydoc = strcmd_multydoc + "AND pm.t_ValueDate >= TO_DATE('"+string(First_Date)+"','DD.MM.YYYY') ";
    strcmd_multydoc = strcmd_multydoc + "AND pm.t_ValueDate <= TO_DATE('"+string(Second_Date)+"','DD.MM.YYYY') ";
    strcmd_multydoc = strcmd_multydoc + "AND" + BranchFilter.GetAsSqlString("pm.t_Department");

    IF (FIID == ALLCURRENCY)
        strcmd_cb_doc = strcmd_cb_doc + "AND d.T_CODE_CURRENCY != "+string(NATCUR)+" ";
        strcmd_bankOrder = strcmd_bankOrder + "AND pm.t_fiId != "+string(NATCUR)+" ";
        strcmd_multydoc = strcmd_multydoc + "AND DECODE(pm.t_FIID," + string(NATCUR) + ", pm.t_payFIID, pm.t_FIID) != " +  string(NATCUR);
    ELIF (FIID != ALLFININSTR)
        strcmd_cb_doc = strcmd_cb_doc + "AND d.T_CODE_CURRENCY = "+string(FIID)+" ";
        strcmd_bankOrder = strcmd_bankOrder + "AND pm.t_fiId = "+string(FIID)+" ";
        strcmd_multydoc = strcmd_multydoc + "AND DECODE(pm.t_FIID," + string(NATCUR) + ", pm.t_payFIID, pm.t_FIID) = " +  string(FIID);
    END;

    strcmd_cb_doc = strcmd_cb_doc + "AND " + getChapterFilterSqlClause(Chapter, "d.t_chapter") + " ";
    strcmd_bankOrder = strcmd_bankOrder + "AND " + getChapterFilterSqlClause(Chapter, "pm.t_chapter") + " ";
    strcmd_multydoc = strcmd_multydoc + "AND " + getChapterFilterSqlClause(Chapter, "d.t_chapter") + " ";

    /* фильтр по типу исправительного документа */
    if (Correct_Kind == 1)
        strcmd_cb_doc   = strcmd_cb_doc   + "AND REGEXP_LIKE(d.T_TYPEDOCUMENT, '[" + allCorrectKindSignsStr + "]', 'c')";
        strcmd_bankOrder = strcmd_bankOrder + "AND REGEXP_LIKE(pm.T_TYPEDOCUMENT, '[" + allCorrectKindSignsStr + "]', 'c')";
        strcmd_multydoc = strcmd_multydoc + "AND REGEXP_LIKE(d.T_TYPE_DOCUMENT, '[" + allCorrectKindSignsStr + "]', 'c')";
    else
        if (Order_Kind != REP_ORDER_KIND_BANK)
            strcmd_cb_doc =   strcmd_cb_doc   + "AND (d.T_TYPEDOCUMENT LIKE "  + correctKindSqlStr[Correct_Kind-2] + ") ";
            strcmd_bankOrder = strcmd_bankOrder + "AND (pm.T_TYPEDOCUMENT LIKE "  + correctKindSqlStr[Correct_Kind-2] + ") ";
            strcmd_multydoc = strcmd_multydoc + "AND (d.T_TYPE_DOCUMENT LIKE " + correctKindSqlStr[Correct_Kind-2] + ") ";
        else
            strcmd_cb_doc =   strcmd_cb_doc   + "AND (d.T_TYPEDOCUMENT LIKE "  + correctKindSqlStr[Correct_Kind-3] + ") ";
            strcmd_bankOrder = strcmd_bankOrder + "AND (pm.T_TYPEDOCUMENT LIKE "  + correctKindSqlStr[Correct_Kind-3] + ") ";
            strcmd_multydoc = strcmd_multydoc + "AND (d.T_TYPE_DOCUMENT LIKE " + correctKindSqlStr[Correct_Kind-3] + ") ";
        end;
    end;

    if (Order_Kind != REP_ORDER_KIND_MEMORIAL)
        strcmd = "INSERT INTO DISPR_DOCS_TMP SELECT pm.T_DOCUMENTID, pm.t_docKind, pm.T_OPER, pm.T_CHAPTER, pm.t_fiId T_FIID_FROM, pm.t_Amount, pm.t_PayerAccount, pm.t_ReceiverAccount, pm.t_ValueDate, pm.t_Department, rm.t_Ground, rm.t_Number, pm.T_TYPEDOCUMENT, acc1.T_BALANCE, acc2.T_BALANCE, i.t_rateDate "+
                 " FROM DACCISPR_DBT i, DACCOUNT_DBT acc1, DACCOUNT_DBT acc2, DPMPAYM_DBT pm, DPMRMPROP_DBT rm "+
                 " WHERE "+strcmd_bankOrder +
                 " AND i.t_DocKind = " + string(DLDOC_BANKORDER)+
                 " AND ( " + AccountFilter.GetAsSqlString("acc1")+
                 "      OR " + AccountFilter.GetAsSqlString("acc2")+" ) "+
                 " AND acc1.T_CHAPTER = pm.T_CHAPTER AND acc1.T_ACCOUNT = pm.t_PayerAccount AND acc1.T_CODE_CURRENCY = pm.t_fiId"+
                 " AND acc2.T_CHAPTER = pm.T_CHAPTER AND acc2.T_ACCOUNT = pm.t_ReceiverAccount AND acc2.T_CODE_CURRENCY = pm.t_fiId;";

        execSQL(strcmd);
    end;

    if (Order_Kind != REP_ORDER_KIND_BANK)
    strcmd = "INSERT INTO DISPR_DOCS_TMP SELECT d.T_DOCUMENTID, pm.t_docKind, d.T_OPER, d.T_CHAPTER, d.T_CODE_CURRENCY T_FIID_FROM, pm.t_Amount, pm.t_PayerAccount, pm.t_ReceiverAccount, pm.t_ValueDate, pm.t_Department, rm.t_Ground, rm.t_Number, d.T_TYPEDOCUMENT, acc1.T_BALANCE, acc2.T_BALANCE, i.t_rateDate "+
             " FROM DCB_DOC_DBT d, DACCISPR_DBT i, DACCOUNT_DBT acc1, DACCOUNT_DBT acc2, DPMPAYM_DBT pm, DPMRMPROP_DBT rm "+
             " WHERE "+strcmd_cb_doc+
                 " AND d.T_DOCUMENTID = i.T_DOCUMENTID AND i.t_DocKind = " + string(DLDOC_MEMORIALORDER) +
             " AND ( " + AccountFilter.GetAsSqlString("acc1")+
             "      OR " + AccountFilter.GetAsSqlString("acc2")+" ) "+
             " AND acc1.T_CHAPTER = d.T_CHAPTER AND acc1.T_ACCOUNT = pm.t_PayerAccount AND acc1.T_CODE_CURRENCY = d.T_CODE_CURRENCY"+
             " AND acc2.T_CHAPTER = d.T_CHAPTER AND acc2.T_ACCOUNT = pm.t_ReceiverAccount AND acc2.T_CODE_CURRENCY = d.T_CODE_CURRENCY;";
    execSQL(strcmd);
    end;

    if (Order_Kind != REP_ORDER_KIND_BANK) // Мультивалютные документы считаются мемориальными ордерами, поэтому при выборе банковского ордера не включаются
   strcmd = "INSERT INTO DISPR_DOCS_TMP SELECT d.T_AUTOKEY T_DOCUMENTID, "+CB_MULTYDOC+" T_DOCKIND, d.T_OPER, d.T_CHAPTER, DECODE(pm.t_FIID," + string(NATCUR) + ", pm.t_payFIID, pm.t_FIID), DECODE(pm.t_fiId, " + string(NATCUR) + ", pm.t_PayAmount, pm.t_Amount) T_SUM, pm.t_PayerAccount T_ACCOUNT_PAYER, pm.t_ReceiverAccount T_ACCOUNT_RECEIVER, pm.t_ValueDate T_DATE_CARRY, pm.t_Department, rm.t_Ground, rm.t_Number, d.T_TYPE_DOCUMENT, acc1.T_BALANCE, acc2.T_BALANCE, i.t_rateDate "+
             " FROM DMULTYDOC_DBT d, DACCISPR_DBT i, DACCOUNT_DBT acc1, DACCOUNT_DBT acc2, DPMPAYM_DBT pm, DPMRMPROP_DBT rm "+
             " WHERE "+strcmd_multydoc+
             " AND d.T_AUTOKEY = i.T_DOCUMENTID AND i.T_DOCKIND = "+CB_MULTYDOC+
             " AND ( " + AccountFilter.GetAsSqlString("acc1")+
             "      OR " + AccountFilter.GetAsSqlString("acc2")+" ) "+
             " AND acc1.T_CHAPTER = d.T_CHAPTER AND acc1.T_ACCOUNT = pm.t_PayerAccount AND acc1.T_CODE_CURRENCY = pm.t_FIID "+
             " AND acc2.T_CHAPTER = d.T_CHAPTER AND acc2.T_ACCOUNT = pm.t_ReceiverAccount AND acc2.T_CODE_CURRENCY = pm.t_PayFIID; ";
    execSQL(strcmd);
    end;

    rs = TRsbDataset("SELECT * FROM DISPR_DOCS_TMP ORDER BY T_CHAPTER,T_FIID_FROM,T_DATE_CARRY,T_NUMB_DOCUMENT");

    PrintHeadOfLog1(DprtID, OrgStructure, First_Date, Second_Date, FIID);

    NextRecord = rs.moveNext;
    flag=1;
    if(NextRecord)

        Chapter_new = int(rs.value(3));
        FIID_new = int(rs.value(4));

        Chapter_ = Chapter_new;
        FIID_ = FIID_new;

        PrintOneLogHead1(Report1, rs, Chapter_, Order_Kind);
        PrintHeadCages1(Report1);
        PrintOneDoc1(Report1, rs, flag);


        NextRecord = rs.moveNext;
        while( NextRecord )
            Chapter_new = int(rs.value(3));
            FIID_new = int(rs.value(4));
            if( (Chapter_ != Chapter_new) OR
                (FIID_ != FIID_new) )
                Report1.PrintBottom;
                IF(Chapter_ != Chapter_new)
                    PrintOneLogHead1(Report1, rs, Chapter_new, Order_Kind);
                ELSE
                END;

                Report1.PrintHead;
                flag=0;
                Chapter_ = Chapter_new;
                FIID_ = FIID_new;
            end;
            PrintOneDoc1(Report1,rs,flag);
            flag=1;
            NextRecord = rs.moveNext;
        end;
        Report1.PrintBottom;
        PrintLogPodval1(Report1);
    else
        println(StrAlign("Нет данных для отчета", width, STR_ALIGN_CENTER));
    end;

    if ((fileName != null) and (ExistFile(reportBodyFileName, 0)))
        printOfstream.resetOutputFile(); 
        return true;
    end;
END;
