/*
$Name:        ibr_docoper.mac
$Module:      Отчеты банка
$Description: Документы по счетам операциониста
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import docoper;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_DOC = "Параметры";
private const GRP_PARAMS = "Дополнительные Параметры";
private const gRep = TAB_REPORT + "@" + GRP_DOC + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";

private var fileName = "ibr_docoper_web";  

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_docoper", "Документы по счетам операциониста", "ibr_docoper.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_DOC, 1);
    var groupParams = facade.createGroup(GRP_PARAMS, 2);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));

    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));

    facade.resetFieldsOrder();
    groupParams.add(facade.createFieldCheckBox("ПоЧужимДокументам", "По чужим документам", false));
    groupParams.add(facade.createFieldCheckBox("Апострофы", "C апострофами", true));

    tabReport.add(groupReport);
    tabReport.add(groupParams);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    var result : Object = TRcbResult();

    result.valueBoolean = produceReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                        getValue(report, gRep+"Структура").valueInteger,
                                        getValue(report, gRep+"Режим").valueInteger,  
                                        TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),
                                        getValue(report, gRep+"НачалоПериода").valueDate,
                                        getValue(report, gRep+"КонецПериода").valueDate,
                                        false,// нет виджета групп операционистов
                                        getValue(report, gRep+"Операционист").valueInteger,
                                        getValue(report, gPrm+"ПоЧужимДокументам").valueBoolean,
                                        getValue(report, gPrm+"Апострофы").valueBoolean,
                                        fileName);
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;