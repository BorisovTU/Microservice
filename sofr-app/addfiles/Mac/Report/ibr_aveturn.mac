/*
$Name:          ibr_Aveturn.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Средние обороты по счетам"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ReptCBInter;         //для определения вида периода
import aveturn;

import DatesUtility;
import Календарь;

private CONST P_WEEK   = 2,
              P_MONTH  = 3,
              P_QUART  = 4,
              P_PERIOD = 6;

private const TAB_REPORT = "Параметры отчета",
              GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP,
              GRP_TURN = "Параметры",
              GRP_PARAMS = "Дополнительные параметры";

private var fileName = "ibr_Aveturn_web";
private var m_month, 
            m_year,
            m_numQuarter,
            m_dateIn,
            m_dateOut;
            
private const gRep = TAB_REPORT + "@" + GRP_TURN + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";
    
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

private macro checkQuarter(endDate : Date): integer
    var year;

    dateSplit(endDate, NULL, NULL, year);
       
    if (endDate   <= Date(31, 03, year))
        m_numQuarter = 1;
    elif (endDate <= Date(30, 06, year))
        m_numQuarter = 2;
    elif (endDate <= Date(30, 09, year))
        m_numQuarter = 3;
    elif (endDate <= Date(31, 12, year))
        m_numQuarter = 4;
    end;
       
    return m_numQuarter;
end;

private macro checkDate(d, m, y) : bool
    if (Date(d, m, y) > {CURDATE})
        repErrorsQueue().add(0, "Отчетный период еще не наступил");
         return false;
    end;

    return true;
 end;
 
macro GetWeekDay(dat)
    return Mod( Int( dat ),7 );
end;
    
private macro checkPeroid(beginDate : Date, endDate : Date, peroid : integer) : bool
    var beginDay, beginMonth, beginYear, beginQuart; 
    var endDay, endMonth, endYear, endQuart; 

    DateSplit(beginDate, beginDay, beginMonth, beginYear); 
    DateSplit(endDate, endDay, endMonth, endYear);
    
    if (peroid == P_MONTH)
        if (checkDate(beginDay, beginMonth, endYear))
            if (beginDay == 1)
                if (endDate == Date(GetDaysInMonth(endDate), beginMonth, beginYear))
                    m_month   = beginMonth;
                    m_year    = endYear;
                    m_dateIn  = beginDate;
                    m_dateOut = endDate;
                    
                    return true;
                else
                    repErrorsQueue().add(0, "Конец периода не равен последней дате месяца");
                    return false;
                end;
            else
                repErrorsQueue().add(0, "Начало периода не равно первой дате месяца");
                return false;
            end;
         end;

    elif (peroid == P_QUART)
        if (checkDate(beginDay, beginMonth, endYear))
            if ((in(beginMonth,1,4,7,10)) and (beginDay == 1))
                if (in(endMonth,3,6,9,12)) 
                    if (endDate == Date(GetDaysInMonth(endDate), beginMonth + 2, beginYear))
                        m_month   = beginMonth;
                        m_year    = endYear;
                        m_dateIn  = beginDate;
                        m_dateOut = endDate;
                        
                        return true;
                    else
                        repErrorsQueue().add(0, "Конец периода не равен последней дате месяца");
                        return false;
                    end;
                else
                    repErrorsQueue().add(0, "Месяц конца периода не равен последнему песяцу квартала");
                    return false;
                end;
            else
                repErrorsQueue().add(0, "Начало периода не равно первой дате месяца квартала");
                return false;
            end;
        end;
        
    elif (peroid == P_WEEK)
        if (GetWeekDay(beginDate) == 1)
            if (endDate == (beginDate + 6))
                m_month   = beginMonth;
                m_year    = endYear;
                m_dateIn  =  beginDate;
                m_dateOut = endDate;
                
                return true;
            else
                repErrorsQueue().add(0, "Конец периода не воскресенье");
                return false;
            end;
         else
            repErrorsQueue().add(0, "Начало периода не понедельник");
            return false;
         end;
    elif (peroid == P_PERIOD)
            m_month   = beginMonth;
            m_year    = endYear;
            m_dateIn  =  beginDate;
            m_dateOut = endDate;
                
           return true;
    end;

    return false;
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_Aveturn", "Средние обороты по счетам", "ibr_Aveturn.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_TURN, 1);
    var groupParams = facade.createGroup(GRP_PARAMS, 2);
    
    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения",    "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура",         "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим",             "Режим"));
    
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов",  "План счетов",                 "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов",                "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта",             "Валюта",                      NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта",     "Только ин.валюта",            false));
    
    groupReport.add(facade.createFieldLlValues("ВидПериода",          "Вид периода",                 "Неделя", 1631));
    groupReport.add(facade.createFieldDate("НачалоПериода",          "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода",           "Конец периода"));
    
    groupReport.add(facade.createFieldBalance("Балансовый",          "Балансовый"));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет",         "Лицевой счета"));
    
    facade.resetFieldsOrder();  
    groupParams.add(facade.createFieldCheckBox("НулевыеОстатки",     "Нулевые остатки",             false));
    groupParams.add(facade.createFieldCheckBox("ЗакрытеСчета",       "Закрыте счета",               false));
    groupParams.add(facade.createFieldCheckBox("Апострофами",        "С апострофами",               false));

    
    tabReport.add(groupReport);
    tabReport.add(groupParams);
    
    report.add(tabReport);
   
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,                                                 
                                          getValue(report, gRep+"Режим").valueInteger,  
                                          TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString), 
                                          TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),  
                                          getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                          getValue(report, gRep+"ВидПериода").valueInteger,
                                          m_month,
                                          checkQuarter(getValue(report, gRep+"НачалоПериода").valueDate),
                                          m_year,
                                          getValue(report, gRep+"НачалоПериода").valueDate,
                                          getValue(report, gRep+"КонецПериода").valueDate, 
                                          getValue(report, gRep+"Балансовый").valueString,
                                          getValue(report, gRep+"ЛицевойСчет").valueString,
                                          getValue(report, gPrm+"НулевыеОстатки").valueBoolean,       
                                          getValue(report, gPrm+"ЗакрытеСчета").valueBoolean,
                                          getValue(report, gPrm+"Апострофами").valueBoolean,
                                          fileName);

    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;
    
    if (getValue(report, gRep+"НачалоПериода").valueDate > getValue(report, gRep+"КонецПериода").valueDate)
        repErrorsQueue().add(0, "Дата начала не может превышать дату конца периода");
        result.valueBoolean = FALSE;
    elif (checkPeroid(getValue(report, gRep+"НачалоПериода").valueDate,
                      getValue(report, gRep+"КонецПериода").valueDate,
                      getValue(report, gRep+"ВидПериода").valueInteger) != true)
        result.valueBoolean = FALSE;
    end;
    
    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;