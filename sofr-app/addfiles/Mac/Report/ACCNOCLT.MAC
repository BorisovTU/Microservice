/*
$Name:          accnoclt.mac 
$Module:        Отчеты банка
$Description:   Счета без клиентов
*/
/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 *  File Name   : accnoclt.mac                                   February 07, 2007
 *  Programmer  : Ivkina
 *  Description : Счета без клиентов
 *  Comment     :
 *  Modify      :
 */


import Reporting;
import rep_lib;
import Календарь;
import FIInter;
import cb_sql;
import ofstream;

private const END_LINE    = "\n";
private const ALLCURRENCY = -2;  /* все ин валюты*/

var reportBodyFileName;

private macro makeTextChapterAndNumPlan(planNumber, chapterNumber)

    return   "(        acbl.t_numplan = " + planNumber
    + "\n" + " AND " + getChapterFilterSqlClause(planNumber, chapterNumber, "acc.t_chapter")
    + "\n" + ")";

end;

/**
 *  Формирование текста sql запроса относительно валюты
 */
private macro makeTextQuery(dataInfo, allAcc)

    var textSelect;
    var textFrom;
    var textWhere;

    var repDate = getSQLDate(dataInfo.repDate);

    if (allAcc == true)
        textSelect =
            "SELECT  count(*)                                                 allAcc  " +END_LINE;
    else
        textSelect =
            "SELECT                                                                   " +END_LINE+
            "acc.t_chapter                                                   chapter, " +END_LINE+
            "acc.t_account                                                   account, " +END_LINE+
            "acc.t_nameaccount                                           nameaccount, " +END_LINE+
            "(SELECT t_fi_code                                                        " +END_LINE+
            "   FROM dfininstr_dbt                                                    " +END_LINE+
            "  WHERE t_fiid = acc.t_code_currency)                              fiid, " +END_LINE+
            "acc.t_open_date                                               open_date, " +END_LINE+
            "acc.t_close_date                                             close_date, " +END_LINE+
            "rsb_rep_ac.prevdatin (acc.t_account,                                     " +END_LINE+
            "                      acc.t_code_currency,                               " +END_LINE+
            "                      acc.t_chapter                                      " +END_LINE+
            "                     )                                              dpd  " +END_LINE;
    end;

    textFrom =
            "FROM daccount_dbt acc, daccBalance_dbt acbl, dbalance_dbt bal            " +END_LINE;

    textWhere =
            " WHERE                                                                   " +END_LINE+
              dataInfo.accountFilter.GetAsSqlString( "acc" )                            +END_LINE+
            " AND acbl.t_accountId = acc.t_accountId                                  " +END_LINE+
            " AND bal.t_balance = acbl.t_balance                                      " +END_LINE+
            " AND bal.t_inumplan = acbl.t_numplan                                     " +END_LINE+
            " AND bal.t_chapter = acc.t_chapter                                       " +END_LINE+
            " AND acc.t_open_date < " + repDate + "                                   " +END_LINE+
            " AND NOT" + dataInfo.ocpAccountServer.GetAsSqlString("acc")                +END_LINE+ /*без ВСП*/
            " AND INSTR(bal.t_Type_Balance, 'T') = 0                                  " +END_LINE; /*без тех счета*/


    if (dataInfo.codeCurrency == ALLCURRENCY)
        textWhere = textWhere +
            " AND acc.t_code_currency  != " + NATCUR                                    +END_LINE;
    elif (dataInfo.codeCurrency >= NATCUR)
        textWhere = textWhere +
            " AND acc.t_code_currency  = " + dataInfo.codeCurrency                      +END_LINE;
    end;


    textWhere = textWhere +
            " AND ( " + makeTextChapterAndNumPlan(dataInfo.numPlan, dataInfo.chapter)+ " )"      +END_LINE;

    if(allAcc == false)
        textWhere = textWhere +
            " AND NOT EXISTS (SELECT t_partyid                                        " +END_LINE+
            "                 FROM dpartyown_dbt                                      " +END_LINE+
            "                 WHERE     t_partyid = acc.t_client                      " +END_LINE+
            "                       AND t_partykind = " + PTK_CLIENT + " )            " +END_LINE;
    end;

    return textSelect + textFrom + textWhere;
end;

/**
 * Формирование текста sql запроса
 */
private macro textQuery(dataInfo, allAcc)

    var textCommand = "";

    textCommand = makeTextQuery(dataInfo, allAcc);

    if (allAcc == false)
        textCommand =  textCommand + " ORDER BY chapter, account ";
    end;

    return textCommand;
end;

/*
 * Печать отчета
 */
private macro printAccountNoClient(dataInfo, accNoClt)

    private macro printStr()
     [ ├───────┼───────────────────────────┼───────────────────────────┼──────┼────────────┼────────────┼────────────┤
       │ ##### │ ######################### │ ######################### │ #### │ ########## │ ########## │ ########## │]
       (
       accNoClt.chapter:r,
       accNoClt.account:r,
       accNoClt.nameaccount:l:w,
       accNoClt.fiid:r,
       accNoClt.open_date:f,
       accNoClt.close_date:f,
       nvl(accNoClt.dpd:f, "")
       );
    end;

    var head = "";
    var width = 113;
    var allAccNoClt = 0;
    var accountClient;
    var allAcc;

    if (dataInfo.codeCurrency == NATCUR)
       head = "РУБЛЕВЫЕ";
    elif ((dataInfo.codeCurrency == ALLCURRENCY) OR (dataInfo.codeCurrency > NATCUR))
       head = "ВАЛЮТНЫЕ";
    end;

    PrintBankHeader(dataInfo.dprtID, dataInfo.orgStructure, width);
    println();
    println(StrAlign(head + " СЧЕТА, У КОТОРЫХ НЕТ КЛИЕНТА ", width, STR_ALIGN_CENTER));
    println(StrAlign(string(dataInfo.repDate:m:f), width, STR_ALIGN_CENTER));
    println();

   if (accNoClt.next())
     [ ┌───────┬───────────────────────────┬───────────────────────────┬──────┬────────────┬────────────┬────────────┐
       │ Глава │   Номер лицевого счета    │        Наименование       │ Код  │    Дата    │    Дата    │     ДПД    │
       │       │                           │       лицевого счета      │валюты│  открытия  │  закрытия  │            │];
       printStr();
       allAccNoClt = allAccNoClt + 1;

       while(accNoClt.next())
           printStr();
           allAccNoClt = allAccNoClt + 1;
       end;

     [ └───────┴───────────────────────────┴───────────────────────────┴──────┴────────────┴────────────┴────────────┘];

       println();

       accountClient = TRsbDataSet(textQuery(dataInfo, true));
       accountClient.SetFieldType("allAcc" , V_INTEGER);

       if (accountClient.next())
          allAcc = accountClient.allAcc;
          if (accountClient.next())
              allAcc = allAcc + accountClient.allAcc;
          end;
       end;

       println("  Итого проверено: ", allAcc);
       println("  Найдено счетов, не имеющих клиента: ", allAccNoClt);
   else
       println(StrAlign("Нет данных для отчета.", width, STR_ALIGN_CENTER));
   end;
end;


/**
 * Класс с параметрами отчета
 */
private CLASS TDataReport
(
    pDprtID,
    pOrgStructure,
    pIssueMode,
    pNumPlan,
    pChapter,
    pCodeCurrency,
    pDate
)
    var departmentList;                        /**/
    var accountFilter;                         /*фильтр счетов*/
    var ocpAccountServer;                      /*счета ОВП*/
    var dprtID           = pDprtID;            /*номер узла ТС*/
    var orgStructure     = pOrgStructure;      /*структура*/
    var issueMode        = pIssueMode;         /*режим выпуска отчета*/
    var numPlan          = pNumPlan;           /*массив номеров планов*/
    var chapter          = pChapter;           /*глава счетов*/
    var codeCurrency     = pCodeCurrency;      /*код валюты*/
    var repDate          = pDate;              /*дата отчета*/

    departmentList   = RepDepartmentList(orgStructure, issueMode, dprtID);
    accountFilter    = RepAccountFilter(departmentList);
    ocpAccountServer = RepOcpAccountServer(chapter, codeCurrency, departmentList);
end;

/**
 * Начало
 */
macro accountNoClient(dprtID, orgStructure, numPlan, issueMode, chapter, currency, repDate)

    var opd;
    var textCommand;
    var dataReport;
    var accountNoClient;

    dataReport = TdataReport(dprtID, orgStructure, issueMode, numPlan, chapter, currency, repDate);

    /* Контроль открытых опердней */
    opd = RepOperdaysOpened(dataReport.departmentList, dataReport.repDate, dataReport.repDate);
    if (opd.ShouldContinue == false)
        exit(1);
    end;

    BegAction(1000, "Получение данных о счетах", false);
        textCommand = textQuery(dataReport, false);

        SQL_Execute("{ CALL rsb_rep_ac.set_dat(" + GetSQLDate(dataReport.repDate) + ", " + GetSQLDate(dataReport.repDate) + ") }");

        accountNoClient = TRsbDataSet(textCommand);

        accountNoClient.SetFieldType("open_date" , V_DATE);
        accountNoClient.SetFieldType("close_date", V_DATE);
        accountNoClient.SetFieldType("dpd"       , V_DATE);
    EndAction();


    printAccountNoClient(dataReport, accountNoClient);
end;

macro ibrCreateReport(dprtID, orgStructure, issueMode, numPlan, chapter, dateIn, codeCurrency, nameFile)
    var output = TOFStream(nameFile);

    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    accountNoClient(dprtID, orgStructure, numPlan, issueMode, chapter, codeCurrency, dateIn);

    output.resetOutputFile();
    return true;  
end;