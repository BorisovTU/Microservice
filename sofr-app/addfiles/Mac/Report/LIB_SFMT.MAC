/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                           R-Style Software Lab

  Библиотека функций форматирования данных

CREATED: 27.06.99 Мол.
MODIFICATIONS:

NOTES:

└───────────────────────────────────────────────────────────────────────────*/

/* Дополнить строку лидирующими нулями справа */
MACRO LeadZero( str, fldLen)
  if ( strlen( str) < fldLen )
    str = MkStr( "0", fldLen - strlen( str)) + str;
  end;

  return str;
END;

/* Преобразовать дату в строку с лидирующими нулями и разделителем '.' */
MACRO СтрДата( Дата)
VAR
    D, M, Y;

  DateSplit( Дата, D, M, Y);

  return ( LeadZero( string( D), 2) + "." + LeadZero( string( M), 2) + "." + string( Y));
END;

/* Форматирует денежное значение в строку заданной длины в зависимости от
   использования значений "в тысячах" и апострофов */
MACRO FormatDMtoS( dm, sz, use_a, use_th)
Var s;

  if ( use_a )
    if ( use_th ) /* в тысячах */
      s = string( DoubleL( dm):0:0:a);
    else
      s = string( dm:0:2:a);
    end;
  else
    if ( use_th ) /* в тысячах */
      s = string( DoubleL( dm):0:0);
    else
      s = string( dm:0:2);
    end;
  end;

  s = MkStr( " ", sz - strlen( s)) + s;

  return s;
END;

/* Отцентрировать строку в строке длины l */
MACRO ДополнитьСтрПоЦентру( s, l)
VAR
   d, len = strlen( s), s1;

  if ( l > len )
    s1 = MkStr( " ", (l-len) / 2);
    s = s1 + s + s1;
    len = l - strlen( s);
    if ( len > 0 )
      s = s + MkStr( " ", len);
    end;
  end;

  return s;
END;

MACRO ЗаменитьПодстроку( Строка, Подстрока, НоваяПодСтрока)
VAR
   i = Index( Строка, Подстрока);

  if ( i > 0 )
    Строка =   SubStr( Строка, 1, i-1)
             + НоваяПодСтрока
             + SubStr( Строка, i + StrLen( Подстрока));
  end;

  return Строка;
END;

MACRO УдалитьПодстроку( s, sub)
var
   ind = index( s, sub);

  if ( ind > 0 )
    s =  SubStr( s, 1, ind-1) + SubStr( s, ind+StrLen( sub));
  end;

  return s;
END;


/* Возвращает имя месяца в родительном падеже */
MACRO RMonName( m)
VAR
  name = MonName( m),
  len = strlen( name);

  if ( Substr( name, len, 1) == "т" )
    return ( name + "а");
  else
    return ( SubStr( name, 1, len-1 ) + "я");
  end;
END;

/*eof*/