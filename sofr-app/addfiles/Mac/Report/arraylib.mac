macro ArrayToString( A, delim )
   var i = 0;
   var Size = 0; 
   var result = "";

   If  (ValType(A) == V_Array)
    Size = ASize(A)
   Elif(ValType(A) == V_GenObj) 
    Size = A.Size
   End;

   while(i<Size)
    if( i != 0 ) 
     result = result + delim;
    end; 

    result = String(result, A(i));
    i = i + 1;
   end;

   return result;
end;

macro StringToArray( A, str, delim )
   var i = 0;
   var pos;  

   If  (ValType(A) == V_Array)
    ASize(A, 0)
   Elif(ValType(A) == V_GenObj) 
    A.Size = 0
   Else
    Return
   End;
        
   pos = Index(str,delim);
   while(pos != 0)
    A(i) = SubStr(str, 1, pos-1);
    str  = SubStr(str, pos+1);
    i = i + 1;	

    pos = Index(str, delim);  
   end;  
             
   if(str != "")
    A(i) = str;
    i = i + 1;    
   end;

   return i;
end;


/*────────────────────────Сортировка объекта массив─────────────────────*/
/* Сортирует передаваемый массив чисел, возвращает массив индексов	*/
/* первоначального массива после сортировки.				*/

Macro SortTArray(A)
 var High  = A.Size - 1;
 var Id = TArray;
 var i, n, T, j;

 With (Id)
  While (Size <= High)
   Id(Size) = Size;
  End;
 End;

 i = 0;
 While (i < High)
  n = i;
  j = High;
  While (j > i)
   If (A(n) > A(j))
    n = j
   End;
   j = j - 1;
  End;

  T = A(i);
  A(i) = A(n);
  A(n) = T;

  T = Id(i);
  Id(i) = Id(n);
  Id(n) = T;

  i = i + 1;
 End;

 Return Id;
End;  


Class (TArray) TSortArray
 InitTArray;

 Macro Add(Val)
  var i = Size;
  While ((i > 0)   and (Val < Value(i-1)))
   Value(i) = Value(i-1);
   i = i-1
  End;
  Value(i) = Val;
 End;

 Macro Prn
  var i = 0;
  While (i < Size)
   Println(i:5:l,Value(i));
   i = i+1;
  End;
 End;

End;


Class (TArray) T2Array
 InitTArray;

 Macro Item(x,y)
  var Val;
  If(ValType(Value(x)) == V_Undef)
   Value(x) = TArray;
  End;
  If(GetParm(3,Val))
   Value(x)(y) = Val;
  Else
   Return Value(x)(y)
  End;
 End;

 Macro Prn
  var i = 0, j;
  While (i < Size)
   j = 0;
   While (j < Value(i).Size)
    Print(Value(i)(j):10);
    j = j + 1;
   End;
   Println;
   i = i+1;
  End;
 End;

End;

Macro ToArray(Arr)
 Array A;
 Var i = 0;
 While(i < Arr.Size)
  A(i) = Arr.Value(i);
  i = i + 1
 End;
 SetParm(1, A)
End;

/* Поиск элемента в любом массиве (Array, TArray) 
	Возращает True - элемент найден,
	В третий параметр возвращает индекс найденного элемента.
*/

Macro FindInArray(A, Val)
	Var
		i	= 0,
		Size	= 0,
		Type	= ValType(A),
		TypeVal	= ValType(Val);
		
	If  (Type == V_Array)
		Size = ASize(A)
	Elif(Type == V_GenObj) 
		Size = A.Size
	Else
		Return False
	End;

	While ( i < Size )
		If	(
			(ValType(A(i)) == TypeVal)
			and (A(i) == Val)
			)
			SetParm(2,i);
			Return True
		End;
		i = i + 1
	End;
	SetParm(2,-1);
	Return False
End;


/* Поиск подстроки элемента в любом массиве (Array, TArray) 
	True, если sMacro( Val, A(i) ) вернет True.
	В четвёртый параметр возвращает индекс найденного элемента.
*/
Macro FindInArrayMacro	(
			A, 
			Val,
			sMacro
			)
	Var
		i	= 0,
		Size	= 0,
		Type	= ValType(A);

	If  (Type == V_Array)
		Size = ASize(A)
	Elif(Type == V_GenObj) 
		Size = A.Size
	Else
		Return False
	End;

	While (i < Size)
		If	(
			ExecMacro2( sMacro, Val, A(i) )
			)
			SetParm(3,i);
			Return True
		End;
		i = i + 1
	End;
	SetParm(3,-1);
	Return False
End;


Macro ArrayAdd(A, B)
 var i = 0;
 While(i < B.Size)
  A(A.Size) = B.Value(i);
  i = i + 1;
 End;
End;