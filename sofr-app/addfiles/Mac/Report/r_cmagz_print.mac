/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *  Module  : r_cmagz_print.mac
 *  Purpose : Управление печатью отчета "Кассовый журнал".
 *  Comment : Управление печатью отчета "Кассовый журнал".
 *  @since    20.09.2010
 *  @author   Shestakov Dmitry
 *  @version  6.00.020.29
 */
import BankInter;
import ofstream;
import r_cmagz_parameters;
import r_cmagz_data;
import r_cmagz_view;

/**
 * Печать отчета "Кассовый журнал".
 */
class TCashJournalPrint(printOfStream : TOfstream)

    var m_printOfstream;

    var m_view;

    var m_data;

    /**
     * Печать части отчета.
     */
    private macro printReportLevel(view, dataset, parentPartRecord)

        var partResultRecord = dataset.getRecord();
        dataset.next();
    
        view.printPartHead(partResultRecord);
    
        while (not (dataset.EOF() or (dataset.resultLevel >= partResultRecord.resultLevel)))
            if (dataset.resultLevel == 0)
                view.printData(dataset, partResultRecord);
                dataset.next();
            else
                printReportLevel(view, dataset, partResultRecord);
            end;
        end;

        view.printPartResult(partResultRecord);
        if (  (dataset.resultLevel == partResultRecord.resultLevel)
            or (dataset.EOF() and (parentPartRecord == null)))
            m_view.closePart(partResultRecord);
        end;

    end;

    /**
     * Печать отчетов.
     */
    private macro printForm(view, dataset)
        if (not dataset.next())
            return NO_DOCUMENTS;
        else
            var globalResultRecord = dataset.getRecord();
            dataset.next();
                
            while (not dataset.EOF())
                if (dataset.isAccountResult)
                    printReportLevel(view, dataset, null);
                else
                    dataset.next();
                end;
            end;
        end;
        return WELL_DONE;
    end;

    /**
     * Выполнение печати.
     */
    macro execute()

        var stat;

        m_printOfstream.setOutputFile();

        stat = printForm(m_view, m_data.getReportDataSet());

        m_printOfstream.resetOutputFile();

        return stat;
    end;    


    private macro constructorTCashJournalPrint(printOfStream : TOfstream)
        m_printOfstream = printOfStream;

        m_data = TCJData(); 
        m_view = TCJView();
    end;

    constructorTCashJournalPrint(printOfStream);
end;