/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                            R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Библиотечный файл - класс множеств и операции над ним.

CREATED : 30.06.99 Мол
FILE_NAME : Lib_Sets.MAC
MODIFICATIONS:

NOTES: Функции добавления элементов множества принимают строковые
       значения и перед помещением их в массив приводят к строковому типу.
└───────────────────────────────────────────────────────────────────────────*/

CLASS TSet()
VAR
    Values = TArray(),
    currPtr = -1;

  MACRO AddInterval( F_Elem, L_Elem ) /* Добавить числовой интервал */
  VAR
     i = Values.Size;

    while ( F_Elem <= L_Elem )
      Values(i) = String( F_Elem );
      i = i + 1;
      F_Elem = F_Elem + 1;
    end;
  END;

  MACRO AddValue( Elem ) /* Добавить значение */
    Values.Value( Values.Size) = String( Elem );

    return Elem;
  END;

  /* Определяет, присутствует ли строковое значение в множестве */
  MACRO Contain( Value)
  VAR
     i = 0, sz = Values.Size, v = trim( string( Value));

    while ( i < sz )
      if ( Values.Value(i) == v )
        return TRUE;
      end;
      i = i + 1;
    end;

    return FALSE;
  END;

  /* Добавляет значение в множество, если такого значения ещё не было */
  MACRO AddCheck( Value: string)
    if ( Not Contain( Value)  )
      AddValue( Value);
    end;

    return Value;
  END;

  MACRO Count() /* Возвращает число элементов множества */
    return Values.Size;
  END;

  MACRO At( i: integer) /* Возвращает элемент множества по индексу */
    return Values.Value(i);
  END;

  MACRO Reset() /* Установить число элементов в 0 */
    Values.Size = 0;
  END;
END;

/*eof*/













