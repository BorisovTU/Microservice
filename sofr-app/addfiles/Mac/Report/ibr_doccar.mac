/*
$Name:        ibr_doccar.mac
$Module:      Отчеты банка
$Description: Журнал исправительных оборотов
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import doccar;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REP    = "Параметры";
private const GRP_PRM    = "Дополнительные параметры";
private const GRP_TOTAL  = "Итого по:";

private const gRep = TAB_REPORT + "@" + GRP_REP + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PRM + "@";
private const gTtl = TAB_REPORT + "@" + GRP_TOTAL + "@";

private const CLASSIFICATOR = 1768;

private var fileName = "ibr_ibr_doccar_web";  

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_doccar", "Журнал исправительных оборотов", "ibr_doccar.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_REP, 1);
    var groupParams = facade.createGroup(GRP_PRM, 2);
    var groupTotal = facade.createGroup(GRP_TOTAL, 3);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "0"));
    groupReport.add(facade.createFieldCheckBox("ИсклГлавы", "С искл. главами", false));

    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", ""));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));
    groupReport.add(facade.createFieldDate("Дата", "Дата отчета"));

    facade.resetFieldsOrder();
    groupParams.add(facade.createFieldCheckBox("СПОД", "По проводкам СПОД", false));
    groupParams.add(facade.createFieldCheckBox("СуммаВалюте", "Сумма в ин.валюте", false));
    groupParams.add(facade.createFieldCheckBox("Апострофами", "С апострофами", false));
    groupParams.add(facade.createFieldSort("Сортировка", "Сортировка", "", CLASSIFICATOR));

    facade.resetFieldsOrder();
    groupTotal.add(facade.createFieldCheckBox("счетам", "б/счетам", false));
    groupTotal.add(facade.createFieldCheckBox("подразделению", "подразделению", false));

    tabReport.add(groupReport);
    tabReport.add(groupParams);
    tabReport.add(groupTotal);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();
    var numPlan         = TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString);
    var i      :integer = 0;

    var Pattern = strTransferByWord(getValue(report, gPrm+"Сортировка").valueString, 1);

    while (i < Pattern.size)
        Pattern[i] = TIbrCommon().getElementLlValues(Pattern[i], CLASSIFICATOR);
        i = i + 1;
    end; 

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"Подразделения").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          numPlan,
                                          TIbrCommon().getArrayAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString, 
                                                                              numPlan,
                                                                              getValue(report, gRep+"ИсклГлавы").valueBoolean),
                                          getValue(report, gRep+"ИсклГлавы").valueBoolean,
                                          getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                          getValue(report, gRep+"ТолькоИнВалюта").valueBoolean,
                                          getValue(report, gRep+"Дата").valueDate,
                                          getValue(report, gPrm+"СПОД").valueBoolean,
                                          getValue(report, gPrm+"СуммаВалюте").valueBoolean,
                                          getValue(report, gPrm+"Апострофами").valueBoolean,
                                          getValue(report, gTtl+"счетам").valueBoolean,
                                          getValue(report, gTtl+"подразделению").valueBoolean,
                                          Pattern
                                          );
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gRep+"Валюта").valueString == "810") and 
        (getValue(report, gPrm+"СуммаВалюте").valueBoolean ))
        repErrorsQueue().add(0, "Флаг \"Сумма в ин.валюте\" устанавливается для валют, отличных от национальной");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;