/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank v 4.31                                     R-Style Software Lab Ltd
  Файл подсистемы "Отчеты ЦБ"

  Библиотечный файл - класс множеств и операции над ним.

CREATED : 27.10.98 Асл.
FILE_NAME : Sets.MAC
MODIFICATIONS:
  11.02.99 Мол. Добавил метод AddCheck()
  06.12.99 Мол. Добавил класс Hash
  19.01.00 Мол. Добавил арифметические функции в класс Hash
	14.05.00 Мол. Инициализация класса Set списком параметров

NOTES: Функции добавления элементов множества принимают числовые/строковые
       значения и перед помещением их в массив приводят к строковому типу.
└───────────────────────────────────────────────────────────────────────────*/

CLASS Set()
VAR
   i, v, Values = TArray;

  MACRO AddInterval( F_Elem, L_Elem ) /* Добавить числовой интервал */
    i = Values.Size;

    while ( F_Elem <= L_Elem )
      Values(i) = String( F_Elem );
      i = i + 1;
      F_Elem = F_Elem + 1;
    end;
  END;

  MACRO AddValue( Elem ) /* Добавить значение */
    Values.Value( Values.Size) = String( Elem );

    return Elem;
  END;

  /* Определяет, присутствует ли значение в множестве */
  MACRO In( Value)
  VAR
     sz = Values.Size;
    
    v = trim(string( Value));
    i = 0;

    while ( i < sz )
      if ( Values.Value(i) == substr(v, 1, strlen( Values.Value(i))) )
        return TRUE;
      end;
      i = i + 1;
    end;

    return FALSE;
  END;

  /* Добавляет значение в множество, если такого значения ещё не было */
  MACRO AddCheck( Value: string)
    if ( Not In( Value)  )
      AddValue( Value);
    end;

    return Value;
  END;

  MACRO Count() /* Возвращает число элементов множества */
    return Values.Size;
  END;

  MACRO At( i: integer) /* Возвращает элемент множества по индексу */
    return Values.Value(i);
  END;

  MACRO Reset() /* Установить число элементов в 0 */
    Values.Size = 0;
  END;

  /* Инициализация списком параметров */
  i = 1;
  while ( GetParm( i, v) ) AddValue( v ); i = i + 1; end;
END;

/* Хранение значений по строковому ключу */
CLASS Hash()
VAR
   _Keys  = TArray,
   Values = TArray;

  MACRO GetIndex( _Key: string)
  var
     i = 0, count = Values.Size;

    while ( i < count )
      if ( _Key == _Keys.Value( i) )
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  END;

  MACRO Ins( _Key: string, _Val)
  var
     idx = GetIndex( _Key);

    if ( idx >= 0 )
      Values.Value( idx) = _Val;
    else
      idx = _Keys.Size;
      _Keys.Value( idx)  = _Key;
      Values.Value( idx) = _Val;
    end;
  END;

  MACRO Del( _Key: string)
  var
     idx = GetIndex( _Key);

    if ( idx >= 0 )
      Values.Value( idx) = NULL;
    end;
  END;

  /* Определяет, присутствует ли значение в множестве */
  MACRO Contain( _Key: string)
    if ( GetIndex( _Key) >= 0 )
      return True;
    end;
    return False;
  END;

  MACRO Count() /* Возвращает число элементов */
    return _Keys.Size;
  END;

  MACRO At( _Key: string) /* Возвращает элемент множества по ключу */
  var
     idx = GetIndex( _Key);

    if ( idx >= 0 )
      return Values.Value(idx);
    end;

    return NULL;
  END;

  MACRO Name_I( i: integer); /* Возвратить i-й ключ */
    return _Keys.Value( i);
  END;

  MACRO CheckIndex( idx: integer)
    if ( (idx < 0) or (idx >= Count()) )
      MsgBox( "Недопустимый индекс при работе с элементами множества (", idx, ")");
      Exit( 1);
    end;
  END;

  MACRO AtIndex( idx: integer) /* Возвращает элемент множества по индексу */
    return Values.Value( idx);
  END;

  MACRO SetAtIndex( idx: integer, Val) /* Установить значение элемента множества по индексу */
    CheckIndex( idx);
    Values.Value( idx) = Val;
  END;

  MACRO AddAtIndex( idx: integer, Val) /* Установить значение элемента множества по индексу */
    CheckIndex( idx);
    Values.Value( idx) = Values.Value( idx) + Val;
  END;

  MACRO Reset() /* Установить число элементов в 0 */
    _Keys.Size = 0;
    Values.Size = 0;
  END;

  /*---- Арифметические функции ----*/

  MACRO Add( _Key: string, _val) /* Добавить к значению некоторое число. Возвратить сумму */
  var
     idx = GetIndex( _Key),
     v;

    if ( idx >= 0 )
      v = Values.Value(idx);

      if ( v == NULL ) /* Возможно после функии Del */
        v = _val;
      else
        v = v + _val;
      end;

      Values.Value(idx) = v;
    else
      Ins( _Key, _Val);
    end;

    return v;
  END;

END;

/*eof*/