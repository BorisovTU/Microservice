/*
$Name:          ibr_bwp_lib.mac 
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Отчет по счетам, входящим в РПС"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import bwp_lib;

private const TAB_REPORT = "Параметры отчета";

private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report = facade.createReport("ibr_bwp_lib", "Отчет по счетам, входящим в РПС", "ibr_bwp_lib.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода",  "Конец периода  "));
    tabReport.add(groupReport);
    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
   
    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"НачалоПериода").valueDate,
                                          getValue(report, gRep+"КонецПериода").valueDate
                                          );
    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;
    
    result.refreshProtocolAndMessage();
    return result;
    
    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"НачалоПериода").valueDate > {curdate})
        repErrorsQueue().add(0, "Дата начала периода не может быть больше даты текущего дня");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;


    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;