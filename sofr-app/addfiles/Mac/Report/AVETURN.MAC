
/*
$Name:          aveturn.mac
$Module:        Внутренняя отчетность
$Description:   Средние обороты по счетам
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.31                                         R-Style Software Lab

  File Name   : aveturn.mac                               December 17,1998
  Programmer  : LCh
  Description : Средние обороты
  Comment     : Ivkina Отчет полностью переписан 25.10.2006
  Modify      : Shestakov Dmitry 24.08.2009 переписана расчетная часть
                для учета исправительных оборотов (только сторно).
              : ABP 11.10.2011 Переделал на drepdocumentwithcorrection_vw
                               для нормального учета исправительных оборотов.
                               Привел в божеский вид выборку данных с
                               учетом МВА.
              : 23.04.2012 - SCR 173200. Средние обороты по счетам: Ошибка в
                шапке отчета
└───────────────────────────────────────────────────────────────────────────*/
//IMPORT OprInter;
import globals;
//import CTInter;
import cb_sql;
IMPORT Reporting; /* RepDepartmentList */
//import acv;
//import "prpm.mac";
import rep_lib;
import testLib;
import RepSqlContextInitializer;

private CONST NATCUR      =  0,
              ALLFININSTR = -1,
              ALLCURRENCY = -2;

private CONST END_LINE = "\n";

var reportBodyFileName;

/*----------------------------------------------------------------------------------------------------------------------------*/

MACRO ШапкаОтчета( StrName, StrDate )
[
 ####################################################################################################################################
 ####################################################################################################################################

]
(StrName:c, StrDate:c);
END;

MACRO ШапкаТаблицы( Название )
[┌───────┬────────────────────────────────────────┬────────────┬──────────────────────┬─────────────────────────────────────────────┐
 │       │                                        │    Дата    │   Номер расчетного   │#############################################│
 │  №/№  │        Наименование организации        │  открытия  │        счета         ├──────────────────────┬──────────────────────┤
 │       │                                        │            │                      │    Приход (руб.)     │    Расход (руб.)     │
 ├───────┼────────────────────────────────────────┼────────────┼──────────────────────┼──────────────────────┼──────────────────────┤
](Название:c);
END;

MACRO Счет( Номер,Клиент, Счет, ДатаОткрытия, Приход, Расход, Апострофы )
  if ( Апострофы )
[│#######│########################################│############│######################│######################│######################│
]( Номер:c, Клиент, ДатаОткрытия:c, Счет, Приход:a, Расход:a );
  else
[│#######│########################################│############│######################│######################│######################│
]( Номер:c, Клиент, ДатаОткрытия:c, Счет, Приход, Расход );
  end;
END;

MACRO Итоги( Колич, Приход, Расход, Апострофы )
  if ( Апострофы )
[├───────┴────────────────────────────────────────┴────────────┴──────────────────────┴──────────────────────┴──────────────────────┤
 │         Всего счетов      : ###################                                                                                  │
 ├────────────────────────────────────────────────────────────────────────────────────┬──────────────────────┬──────────────────────┤
 │         Всего по счетам   :                                                        │######################│######################│
 └────────────────────────────────────────────────────────────────────────────────────┴──────────────────────┴──────────────────────┘
]( Колич, Приход:a, Расход:a );
  else
[├───────┴────────────────────────────────────────┴────────────┴──────────────────────┴──────────────────────┴──────────────────────┤
 │         Всего счетов      : ###################                                                                                  │
 ├────────────────────────────────────────────────────────────────────────────────────┬──────────────────────┬──────────────────────┤
 │         Всего по счетам   :                                                        │######################│######################│
 └────────────────────────────────────────────────────────────────────────────────────┴──────────────────────┴──────────────────────┘
]( Колич, Приход, Расход );
  end;
END;

MACRO НетДанных( )
 println("Нет данных для отчета.");
END;

/* Класс с параметрами отчета */
private CLASS TAveturnInfo
(
  pDprtID,
  pOrgStructure,
  pIssueMode,
  pNumPlan,
  pChapter,
  pCurrency,
  pDateIn,
  pDateOut,
  pBalanceMask,
  pAccountMask,
  pcSetZero,
  pcSetClose,
  pcSetA
)

  var Account, /* объект TRsbDataSet*/
      DepartmentList,
      AccountFilter,
      OcpAccountServer,
      DprtID        = pDprtID,
      OrgStructure  = pOrgStructure,
      IssueMode     = pIssueMode,
      NumPlan       = pNumPlan,
      Chapter       = pChapter,
      Currency      = pCurrency,
      DateIn        = pDateIn,
      DateOut       = pDateOut,
      BalanceMask   = pBalanceMask,
      AccountMask   = pAccountMask,
      cSetZero      = pcSetZero,
      cSetClose     = pcSetClose,
      cSetA         = pcSetA;

  private macro Constructor()
    DepartmentList      = RepDepartmentList(OrgStructure, IssueMode, DprtID);
    AccountFilter       = RepAccountFilter(DepartmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);
    OcpAccountServer    = RepOcpAccountServer(Chapter, Currency, DepartmentList);

    sql_execute("BEGIN"
        +"\n" + "   rep_data.initCorrectDocumentParameters(1,"+ getSqlDate(DateIn) + ","+ getSqlDate(DateOut) +");"
        +"\n" + "END;"
               );

  end;

  Constructor();
end;


/* Формирование условия на отбор счетов из daccblnc_dbt по маске (маскам) б\с */
private MACRO MakeBlncCond_Balance(BlncAlias, Info)
  var abCond = "",
      BlncMask = "";

  abCond = getChapterFilterSqlClause(Info.NumPlan, Info.Chapter, "ac.t_chapter");

  if (Info.BalanceMask != "")
    BlncMask = "(" + ConvertMaskToSQLFormat(Info.BalanceMask, BlncAlias+".t_Balance") + " AND " + BlncAlias+".t_numPlan = " + info.NumPlan+ ")";
  else
    BlncMask = "(" + BlncAlias+".t_Balance <> "+GetSQLString("") + " AND " + BlncAlias+".t_numPlan = " + info.NumPlan + ")";
  end;

  abCond = abCond + " AND " + BlncMask;

  return "("+abCond+")";
END;

/* Условие для выборки проводок */
private macro joinDocuments(info)

    var joinType = ternary(info.cSetZero, "LEFT OUTER", "INNER");
    var zeroFilter = ternary(info.cSetZero, "", "\n" + "AND doc.t_inCoverSum != 0");

    var join = joinType + " JOIN drepdocumentwithcorrection_vw doc ON"
                 + "\n" + " (    (   doc.t_debetAccountId = ac.t_accountId"
                 + "\n" + "       OR doc.t_creditAccountId = ac.t_accountId"
                 + "\n" + "      )"
                 + "\n" + "  AND doc.t_date BETWEEN " + GetSQLDate(info.DateIn)
                 + "\n" + "                     AND " + GetSQLDate(info.DateOut)
                 +        zerofilter
                 + "\n" + " )";

    return join;
END;

/* Условие where для выборки основных счетов */
private macro whereAccount(blncAlias, info)

    var where = "";

    where = where + info.AccountFilter.GetAsSqlString("ac")                                                            +END_LINE;
    where = where + "AND NOT" + info.OcpAccountServer.GetAsSqlString("ac")                                             +END_LINE;
    where = where + "AND " + MakeBlncCond_Balance(blncAlias, info)                                                     +END_LINE;

    if (info.AccountMask != "")
        where = where + " AND (" + ConvertMaskToSQLFormat(info.AccountMask, "ac.t_Account")+")"                        +END_LINE;
    end;

    if (info.Currency == ALLCURRENCY)
        where = where + "AND ac.t_Code_Currency != " + NATCUR;
    elif (info.Currency != ALLFININSTR)
        where = where + "AND ac.t_Code_Currency = " + info.Currency;
    end;

    where = where + "AND ac.t_Open_Date <= "+ GetSQLDate(info.DateOut)                                                 +END_LINE;

    if(info.cSetClose)
        where = where + "AND (    (ac.t_Close_Date >= "+ GetSQLDate(info.DateIn)  + " OR  ac.t_Open_Close = chr(0))"   +END_LINE+
                        "     OR  (ac.t_Close_Date <= "+ GetSQLDate(info.DateOut) + " OR  ac.t_Open_Close != chr(0)))" +END_LINE;
    else
        where = where + "AND (ac.t_Close_Date >= "+ GetSQLDate(info.DateOut) +" OR  ac.t_Open_Close = chr(0))"         +END_LINE;
    end;

    return where;
END;

/* Формирование текста SQL-запроса  */
private MACRO asQuery(info)

    var select;
    var from;
    var where;
    var joinBal;
    var joinDoc;
    var joinPrt;

    var cmdText = "";

    var accBlncAlias = "ab";
    var ac, ab;

    var expenditureClause;
    var receiptClause;

    ac = "daccount_dbt ac";
    ab = "daccbalance_dbt "  + accBlncAlias;

    // Расход
    expenditureClause =        "(   (    doc.t_debetAccountId = ac.t_accountId"
                      + "\n" + "     AND ac.t_kind_account IN ('П', 'АП', '0')"
                      + "\n" + "    )"
                      + "\n" + " OR (    doc.t_creditAccountId = ac.t_accountId"
                      + "\n" + "     AND ac.t_kind_account = 'А'"
                      + "\n" + "    )"
                      + "\n" + ")";

    // Приход
    receiptClause =        "(   (    doc.t_creditAccountId = ac.t_accountId"
                  + "\n" + "     AND ac.t_kind_account IN ('П', 'АП', '0')"
                  + "\n" + "    )"
                  + "\n" + " OR (    doc.t_debetAccountId = ac.t_accountId"
                  + "\n" + "     AND ac.t_kind_account = 'А'"
                  + "\n" + "    )"
                  + "\n" + ")";


    var days = info.dateOut-info.dateIn + 1;

    select =        "SELECT part.t_shortName client,"
           + "\n" + "       ac.t_open_date                            dateOpen,"
           + "\n" + "       ac.t_account                              acc,"
           + "\n" + "       CASE WHEN " + expenditureClause
           + "\n" + "            THEN NVL(doc.t_inCoverSum, 0)"
           + "\n" + "            ELSE 0"
           + "\n" + "       END                                       payerValue,"
           + "\n" + "       CASE WHEN " + receiptClause
           + "\n" + "            THEN NVL(doc.t_inCoverSum, 0)"
           + "\n" + "            ELSE 0"
           + "\n" + "       END                                       receiverValue,"
           + "\n" + "       ac.t_sort                                 srt"
           + "\n" + "";

    from = "FROM " + ac + END_LINE;

    joinBal = "INNER JOIN " + ab + " ON("+END_LINE+
              "           " + accBlncAlias + ".t_accountId = ac.t_accountId"+END_LINE+
              "                        )"+END_LINE;

    joinDoc = joinDocuments(info);
    joinPrt = "INNER JOIN dparty_dbt part ON (part.t_partyid = ac.t_client)" + END_LINE;

    where = "WHERE " + whereAccount(accBlncAlias, info);

    CmdText = "(" + select + from + joinBal + joinDoc + joinPrt + where + ")" +END_LINE;
    return cmdText;

END;

MACRO ExecuteQuery(Info)

    var cmdText;
    var Days = Info.DateOut-Info.DateIn + 1;

    cmdText = "WITH data AS " + asQuery(Info)
     + "\n" + "SELECT data.client                          AS t_client,"
     + "\n" + "       data.dateOpen                        AS t_dateOpen,"
     + "\n" + "       data.acc                             AS t_acc,"
     + "\n" + "       SUM(data.payerValue)/" + Days + "    AS t_payer,"
     + "\n" + "       SUM(data.receiverValue)/" + Days + " AS t_receiver,"
     + "\n" + "       data.srt                             AS t_srt"
     + "\n" + "  FROM data"
     + "\n" + " GROUP BY data.acc,"
     + "\n" + "          data.srt,"
     + "\n" + "          data.client,"
     + "\n" + "          data.dateOpen"
     + "\n" + " ORDER BY t_srt,"
     + "\n" + "          t_acc";

    Info.Account = TRsbDataSet(cmdText);

    Info.Account.SetFieldType("t_dateOpen", V_DATE);
    Info.Account.SetFieldType("t_receiver", V_MONEY);
    Info.Account.SetFieldType("t_payer",    V_MONEY);
END;

/* Основной макрос формирования оборотов */
MACRO PrintAcpay(
  DprtID,
  OrgStructure,
  IssueMode,
  NumPlan,
  Chapter,
  Currency,
  Period,
  Mon,
  Quart,
  Year,
  DateIn,
  DateOut,
  BalanceMask,
  AccountMask,
  cSetZero,
  cSetClose,
  cSetA
)
    var profiler = TSqlProfiler(getTxtFileName("!aveturn_sqlprofile"));
    profiler.clear();
    profiler.on();

    var Info = TAveturnInfo(DprtID, OrgStructure, IssueMode, NumPlan, Chapter, Currency, DateIn, DateOut,
                            BalanceMask, AccountMask, cSetZero,cSetClose, cSetA );

    /* Контроль открытых опердней */
    if (RepOperdaysOpened(Info.DepartmentList, Info.DateIn, Info.DateOut).ShouldContinue == false)
        exit(1);
    end;

    BegAction(1000, "Получение данных о средних оборотах");
        ExecuteQuery(Info);
    EndAction();

    InitProgress(-1, "Выпуск отчета...", "Выпуск очета о средних оборотах");
    var Width   = 133,
        StrName = "",
        StrDate = "",
        Name    = "",
        NAcc    = 0,
        Debet   = Money(0),
        Credit  = Money(0);

    if( (Period == REP_KINDPERIOD_PERIOD) OR ( Period == REP_KINDPERIOD_WEEK) )
      StrName = "СРЕДНИЕ ОБОРОТЫ ПО ЛИЦЕВЫМ СЧЕТАМ ЗА ПЕРИОД";
      StrDate = "c " + Info.DateIn + " по " + Info.DateOut;
      Name    = "Средние обороты по лицевому счету";
      SetOutput(GetTxtFileName("av_p_ob"));
      reportBodyFileName = GetTxtFileName("av_p_ob");
    elif( Period == REP_KINDPERIOD_QUARTER )
      StrName = "СРЕДНЕКВАРТАЛЬНЫЕ ОБОРОТЫ ПО ЛИЦЕВЫМ СЧЕТАМ";
      StrDate = "за " + Quart + " квартал " + Year + "г.";
      Name    = "Среднеквартальные обороты по лицевому счету";
      SetOutput(GetTxtFileName("av_q_ob"));
      reportBodyFileName = GetTxtFileName("av_q_ob");
    elif( Period == REP_KINDPERIOD_MONTH )
      StrName = "СРЕДНЕМЕСЯЧНЫЕ ОБОРОТЫ ПО ЛИЦЕВЫМ СЧЕТАМ";
      StrDate = "за " + monname(Mon) + " " + Year + "г.";
      Name    = "Среднемесячные обороты по лицевому счету";
      SetOutput(GetTxtFileName("av_mn_ob"));
      reportBodyFileName = GetTxtFileName("av_mn_ob");
    end;

    /* Шапка банка */
    PrintBankHeader(Info.DprtID, Info.OrgStructure, Width);

    ШапкаОтчета( StrName, StrDate );

    if(Info.Account.Next())
        ШапкаТаблицы(Name);
        if (      (info.cSetZero == true)
            or not(Info.Account.Receiver < 0.005)         // некоторые обороты после
            or not(Info.Account.Payer < 0.005))           // "усреднения" почти нулевые

            NAcc = NAcc + 1;
            Debet  = Debet  + Info.Account.Receiver;
            Credit = Credit + Info.Account.Payer;
            Счет( NAcc, Info.Account.Client, Info.Account.Acc, Info.Account.DateOpen, Info.Account.Receiver, Info.Account.Payer, Info.cSetA);
        end;
        while(Info.Account.Next())
            if (      (info.cSetZero == true)
                or not(Info.Account.Receiver < 0.005)         // некоторые обороты после
                or not(Info.Account.Payer < 0.005))           // "усреднения" почти нулевые
                NAcc = NAcc + 1;
                Debet  = Debet  + Info.Account.Receiver;
                Credit = Credit + Info.Account.Payer;
                Счет( NAcc, Info.Account.Client, Info.Account.Acc, Info.Account.DateOpen, Info.Account.Receiver, Info.Account.Payer, Info.cSetA);
            end;
        end;

        if (NAcc < 1)
            НетДанных();
        end;

        Итоги( NAcc, Debet, Credit, Info.cSetA );
    else
        НетДанных();
    end;
    RemProgress();

    profiler.off();

END;

macro ibrCreateReport(DprtID, OrgStructure, IssueMode, NumPlan, ChapterIn, Currency, Period, Mon, Quart,
                        Year, DateIn, DateOut, BalanceMask, AccountMask, cSetZero, cSetClose, cSetA)
    var params = RepParameterCollection();

    params.add(RepParameter(DEPARTMENT_CODE,        REP_PT_INT,     dprtID));
    params.add(RepParameter(ORGANIZATION_STRUCTURE, REP_PT_INT,     orgStructure));
    params.add(RepParameter(ISSUE_MODE,             REP_PT_INT,     issueMode));
    params.add(RepParameter(BALANCE_PLAN_NUMBER,    REP_PT_INT,     NumPlan));
    params.add(RepParameter(CHAPTER,                REP_PT_INT,     ChapterIn));
    params.add(RepParameter(CODE_CURRENCY,          REP_PT_INT,     Currency));
    params.add(RepParameter(BEGIN_DATE,             REP_PT_DATE,    dateIn));
    params.add(RepParameter(END_DATE,               REP_PT_DATE,    dateOut));

    initializeSqlContext(params);

    PrintAcpay(DprtID, OrgStructure, IssueMode, NumPlan, ChapterIn, Currency, Period, Mon, Quart,
                 Year, DateIn, DateOut, BalanceMask, AccountMask, cSetZero, cSetClose, cSetA);

    return true;
end;