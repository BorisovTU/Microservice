/**
 * RS-Bank v6                                             R-Style Softlab
 *
 * Module:   pr_accrests.mac
 * Author:   Andrew B. Panov
 * Modified: 24 мая 2011 г. 10:36:12
 * Purpose:  Реализация отбора данных и печати ведомости начисления процентов
 * Comment:  Отчет "Ведомость начисления процентов"
 */

//import testlib;

import FIInter;
import globals;

import Reporting;
import ofstream;
import cb_sql;
import lib_str;
import lib_arr;
import lib_lang;
import rep_lib;

import testLib;

private const ALLCURRENCY = -2; // все ин. валюты
private const ALLPARTY = -1;    // все субъекты

private const DISTRIBUTION = "Размещение",
              ATTRACTION = "Привлечение";

private var reportBodyFileName;

private var printOFStream;

private class TParameters(departmentId, organizationStructure, issueMode, currencyId, clientId, isGroup, operNumber, beginDate, endDate,
                          backoffice, contractNumber, contractDirection, contractUsertype, contractUsertypeMethod, chargeObjectKind, chapterNumber,
                          balanceMask, accountMask, objectKind, objectNumber, useIndividualSchedule, scheduleKind, scheduleNumber, totalsModel)

    private var m_departmentId : Integer;
    private var m_organizationStructure : Integer;

    private var m_departmentList : Object;
    private var m_currencyId : Integer;
    private var m_clientId : Integer;
    private var m_isGroup : Bool;
    private var m_operNumber : Integer;
    private var m_beginDate : Date;
    private var m_endDate : Date;
    private var m_backoffice : Integer;
    private var m_contractNumber : Integer;
    private var m_contractDirection : Integer;
    private var m_contractUsertype : String;
    private var m_contractUsertypeMethod : Integer;
    private var m_chargeObjectKind : Integer;
    private var m_chapterNumber : Integer;
    private var m_balanceMask : String;
    private var m_accountMask : String;
    private var m_objectKind : Integer;
    private var m_objectNumber : String;
    private var m_useIndividualSchedule : Bool;
    private var m_scheduleKind : Integer;
    private var m_scheduleNumber : Integer;
    private var m_sortModel : Object;
    private var m_totalsModel : Object;

    private var m_departmentFilter : Object;

    macro getDepartmentId()
        return m_departmentId;
    end;

    macro getOrganizationStructure()
        return m_organizationStructure;
    end;

    macro getDepartmentList()
        return m_departmentList;
    end;

    macro getCurrencyId()
        return m_currencyId;
    end;

    macro getClientId()
        return m_clientId;
    end;

    macro getIsGroup()
        return m_isGroup;
    end;

    macro getOperNumber()
        return m_operNumber;
    end;

    macro getBeginDate()
        return m_beginDate;
    end;

    macro getEndDate()
        return m_endDate;
    end;

    macro getBackoffice()
        return m_backoffice;
    end;

    macro getContractNumber()
        return m_contractNumber;
    end;

    macro getContractDirection()
        return m_contractDirection;
    end;

    macro getContractUsertype()
        return m_contractUsertype;
    end;

    macro getContractUsertypeMethod()
        return m_contractUsertypeMethod;
    end;

    macro getChargeObjectKind()
        return m_chargeObjectKind;
    end;

    macro getChapterNumber()
        return m_chapterNumber;
    end;

    macro getBalanceMask()
        return m_balanceMask;
    end;

    macro getAccountMask()
        return m_accountMask;
    end;

    macro getObjectKind()
        return m_objectKind;
    end;

    macro getObjectNumber()
        return m_objectNumber;
    end;

    macro getUseIndividualSchedule()
        return m_useIndividualSchedule;
    end;

    macro getScheduleKind()
        return m_scheduleKind;
    end;

    macro getScheduleNumber()
        return m_scheduleNumber;
    end;

    macro getSortModel()
        return m_sortModel;
    end;

    macro getTotalsModel()
        return m_totalsModel;
    end;

    macro getDepartmentFilter()
        return m_departmentFilter;
    end;

    private macro constructor(departmentId, organizationStructure, issueMode, currencyId, clientId, isGroup, operNumber, beginDate, endDate,
                              backoffice, contractNumber, contractDirection, contractUsertype, contractUsertypeMethod, chargeObjectKind,
                              chapterNumber, balanceMask, accountMask, objectKind, objectNumber, useIndividualSchedule, scheduleKind, 
                              scheduleNumber, totalsModel)

        m_departmentId = departmentId;
        m_organizationStructure = organizationStructure;

        m_departmentList = RepDepartmentList(organizationStructure, issueMode, departmentId);
        m_currencyId = currencyId;
        m_clientId = clientId;
        m_isGroup = isGroup;
        m_operNumber = operNumber;
        m_beginDate = beginDate;
        m_endDate = endDate;
        m_backoffice = backoffice;
        m_contractNumber = contractNumber;
        m_contractDirection = contractDirection;
        m_contractUsertype = contractUsertype;
        m_contractUsertypeMethod = contractUsertypeMethod;
        m_chargeObjectKind = chargeObjectKind;
        m_chapterNumber = chapterNumber;
        m_balanceMask = balanceMask;
        m_accountMask = accountMask;
        m_objectKind = objectKind;
        m_objectNumber = objectNumber;
        m_useIndividualSchedule = useIndividualSchedule;
        m_scheduleKind = scheduleKind;
        m_scheduleNumber = scheduleNumber;

        if (totalsModel == REP_PERCENT_TOTALS_ALL)
            m_totalsModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_BRANCH)
            m_totalsModel = arrCreate(REP_PATTERNSORT_DEPARTMENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_BANKUSER)
            m_totalsModel = arrCreate(REP_PATTERNSORT_OPER);
            m_sortModel = arrCreate(REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_CLIENT)
            m_totalsModel = arrCreate(REP_PATTERNSORT_CLIENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_CLIENT);
        end;

        m_departmentFilter = RepBranchFieldFilter(m_departmentList);

    end;

    constructor(departmentId, organizationStructure, issueMode, currencyId, clientId, isGroup, operNumber, beginDate, endDate,
                backoffice, contractNumber, contractDirection, contractUsertype, contractUsertypeMethod, chargeObjectKind, chapterNumber, balanceMask,
                accountMask, objectKind, objectNumber, useIndividualSchedule, scheduleKind, scheduleNumber, totalsModel);

end;

private class TDataSource(parameters : TParameters)

    private var m_parameters : TParameters;

    private var m_directionFilter : String;
    private var m_fiFilter : String;
    private var m_contractFilter : String;
    private var m_backofficeFilter : String;
    private var m_accountFilter : String;
    private var m_clientFilter : String;
    private var m_userFilter : String;
    private var m_chapterFilter : String;
    private var m_balanceFilter : String;
    private var m_usertypeFilter : String;
    private var m_scheduleFilter : String;

//    private var m_contractDataSource : String;

    private var m_branchName;
    private var m_userName;
    private var m_clientName;

    private var m_orderingClause;

    private macro makeOrderingClause()

        var sortModel = m_parameters.getSortModel();

        m_branchName = "'dummy'";
        m_userName = "'dummy'";
        m_clientName = "'dummy'";
        m_orderingClause = "";

        m_orderingClause = m_orderingClause
            + "\n" + " ORDER BY t_orderingDirection ASC,"
            + "\n" + "          t_fiCodeAndName ASC, t_fiId ASC,";

        var i = 0;
        while (i < sortModel.size)

            if (sortModel(i) == REP_PATTERNSORT_DEPARTMENT) // по подразделению

                m_branchName = "(SELECT branch.t_shortName"
                      + "\n" + "   FROM drepDpDep_vw branch"
                      + "\n" + "  WHERE branch.t_nodeId = account.t_branchId"
                      + "\n" + ")";
                m_orderingClause = m_orderingClause
                    + "\n" + "          t_branchName ASC, t_branchId ASC,";

            elif (sortModel(i) == REP_PATTERNSORT_OPER) // по операционисту

                m_userName = "(SELECT bankUser.t_name"
                    + "\n" + "   FROM repUser_vw bankUser"
                    + "\n" + "  WHERE bankUser.t_id = contract.t_operId"
                    + "\n" + ")";
                m_orderingClause = m_orderingClause
                    + "\n" + "          t_userName ASC, t_userId ASC,";

            elif (sortModel(i) == REP_PATTERNSORT_CLIENT) // по клиенту

                m_clientName = "(SELECT party.t_shortName"
                      + "\n" + "   FROM drepParty_vw party"
                      + "\n" + "  WHERE party.t_id = contract.t_clientId"
                      + "\n" + ")";
                m_orderingClause = m_orderingClause
                      + "\n" + "          t_clientName ASC, t_clientId ASC,";

            end;

            i = i +1;

        end;

        m_orderingClause = m_orderingClause
            + "\n" + "          t_contractNBA ASC, t_contractId ASC,"
            + "\n" + "          t_scheduleCalcDate ASC";

    end;

    private macro makeFilters()

        if (m_parameters.getCurrencyId() == ALLFININSTR)
            m_fiFilter = "";
        elif (m_parameters.getCurrencyId() == ALLCURRENCY)
            m_fiFilter = "\n" + "   AND contract.t_fiId != 0";
        else
            m_fiFilter = "\n" + "   AND contract.t_fiId = " + m_parameters.getCurrencyId();
        end;

        if (m_parameters.getBackoffice() != -1)
            m_backofficeFilter = "\n" + "   AND contract.t_backofficeCode = " + m_parameters.getBackoffice();
        end;

        if (m_parameters.getClientId() != ALLPARTY)
            m_clientFilter = "\n" + "   AND contract.t_clientId = " + m_parameters.getClientId();
        end;

        if (m_parameters.getOperNumber() != 0)
            if (m_parameters.getIsGroup())
                m_userFilter = "\n" + "   AND EXISTS (SELECT NULL"
                             + "\n" + "                 FROM repUserGroup_vw userGroup"
                             + "\n" + "                WHERE userGroup.t_userId = contract.t_operId"
                             + "\n" + "                  AND userGroup.t_id = " + m_parameters.getOperNumber()
                             + "\n" + "              )";
            else
                m_userFilter = "\n" + "   AND contract.t_operId = " + m_parameters.getOperNumber()
            end;
        end;

        if (m_parameters.getContractNumber() != 0)

            m_contractFilter = "\n" + "   AND contract.t_number = " + m_parameters.getContractNumber();

        else

            if (m_parameters.getContractDirection() != REP_PERCENT_CONTRACT_DIRECTION_ALL)
                m_directionFilter = "\n" + "   AND contract.t_directionKind = " + m_parameters.getContractDirection();
            end;

            if (m_parameters.getAccountMask() != "")
                m_accountFilter = "\n" + "   AND " + convertMaskToSqlFormat(m_parameters.getAccountMask(), "contract.t_account");
            end;

            if (m_parameters.getChapterNumber() != 0)
                m_chapterFilter = "\n" + "   AND contract.t_chapter = " + m_parameters.getChapterNumber();
            end;

            if (m_parameters.getBalanceMask() != "")
                m_balanceFilter = "\n" + "   AND " + convertMaskToSqlFormat(m_parameters.getBalanceMask(), "account.t_basicPlanBalance");
            end;

            if (m_parameters.getContractUsertype() != "")
                m_usertypeFilter = "\n" + " AND " + getPatternFilterClause("contract.t_userType", m_parameters.getContractUsertype(), m_parameters.getContractUsertypeMethod());
            end;

            if (m_parameters.getUseIndividualSchedule())
                m_scheduleFilter = "\n" + "   AND contract.t_standardChargeScheduleNumber IS NULL";
            else
                if (m_parameters.getScheduleNumber() != 0)
                    m_scheduleFilter = "\n" + "   AND contract.t_standardChargeScheduleNumber = " + m_parameters.getScheduleNumber();
                else
                    m_scheduleFilter = "\n" + "   AND contract.t_standardChargeScheduleNumber IS NOT NULL";
                end;
            end;

        end;

    end;

    macro getDataSource()
        var query = "";

        query =  "SELECT DECODE(contract.t_directionKind,"
                              + REP_PERCENT_CONTRACT_DIRECTION_IN + ", 2,"
                              + REP_PERCENT_CONTRACT_DIRECTION_OUT + ", 1)            AS t_orderingDirection,"
        + "\n" + "       DECODE(contract.t_directionKind,"
                              + REP_PERCENT_CONTRACT_DIRECTION_IN + ", " + getSqlString(ATTRACTION) + ","
                              + REP_PERCENT_CONTRACT_DIRECTION_OUT + ", " + getSqlString(DISTRIBUTION) + ") AS t_contractDirection,"
        + "\n" + "       contract.t_fiId                AS t_fiId,"
        + "\n" + "       (SELECT fi.t_code || ' ' || fi.t_name"
        + "\n" + "          FROM drepFinInstrument_vw fi"
        + "\n" + "         WHERE contract.t_fiId = fi.t_id"
        + "\n" + "       )                              AS t_fiCodeAndName,"
        + "\n" + "       contract.t_id                  AS t_contractId,"
        + "\n" + "       contract.t_number "
                "||','|| contract.t_backoffice "
                "||','|| contract.t_account             AS t_contractNBA,"
        + "\n" + "       schedule.t_calcDate            AS t_scheduleCalcDate,"
        + "\n" + "       schedule.t_periodBeginDate     AS t_schedulePeriodBeginDate,"
        + "\n" + "       schedule.t_periodEndDate       AS t_schedulePeriodEndDate,"
        + "\n" + "       NVL(schedule.t_sum, 0)         AS t_scheduleFactSum,"
        + "\n" + "       schedule.t_date                AS t_scheduleFactDate,"
        + "\n" + "       contract.t_clientId            AS t_clientId,"
        + "\n" + "   " + m_clientName + "               AS t_clientName,"
        + "\n" + "       contract.t_operId              AS t_userId,"
        + "\n" + "   " + m_userName + "                 AS t_userName,"
        + "\n" + "       account.t_branchId             AS t_branchId,"
        + "\n" + "   " + m_branchName + "               AS t_branchName"
        + "\n" + "  FROM repPercentContract_vw contract,"
        + "\n" + "       drepAccount_vw account,"
        + "\n" + "       repPercentChargesSchedule_vw schedule"
        + "\n" + " WHERE contract.t_id = schedule.t_contractId"
        + "\n" + "   AND contract.t_fiId = account.t_fiId"
        + "\n" + "   AND contract.t_chapter = account.t_chapter"
        + "\n" + "   AND contract.t_account = account.t_account"
        + "\n" + "   AND " + m_parameters.getDepartmentFilter().getAsSqlString("account.t_branchId")
        + "\n" + "   AND schedule.t_calcDate BETWEEN " + getSqlDate(m_parameters.getBeginDate())
                                             + " AND " + getSqlDate(m_parameters.getEndDate())
        +            m_directionFilter
        +            m_fiFilter
        +            m_contractFilter
        +            m_backofficeFilter
        +            m_accountFilter
        +            m_clientFilter
        +            m_userFilter
        +            m_chapterFilter
        +            m_balanceFilter
        +            m_usertypeFilter
        +            m_scheduleFilter
        +            m_orderingClause
        ;

        var dataSource = TRsbDataset(query);

        dataSource.setFieldType("t_scheduleCalcDate", V_DATE);
        dataSource.setFieldType("t_schedulePeriodBeginDate", V_DATE);
        dataSource.setFieldType("t_schedulePeriodEndDate", V_DATE);
        dataSource.setFieldType("t_scheduleFactSum", V_MONEY);
        dataSource.setFieldType("t_scheduleFactDate", V_DATE);

        return dataSource;
    end;

    private macro constructor(parameters)
        m_parameters = parameters;

        m_directionFilter = "";
        m_fiFilter = "";
        m_contractFilter = "";
        m_backofficeFilter = "";
        m_accountFilter = "";
        m_clientFilter = "";
        m_userFilter = "";
        m_chapterFilter = "";
        m_balanceFilter = "";
        m_usertypeFilter = "";
        m_scheduleFilter = "";

        makeOrderingClause();
        makeFilters();
    end;

    constructor(parameters);

end;

private class TView(parameters : Object)

    private var m_parameters : Object;
    private var m_tableView : CTableReport;

    private macro createTableView()

        m_tableView = CTableReport();

        m_tableView.AddColumn("Дата|операции", 10, AL_CENTER);
        m_tableView.AddColumn("Начальная|дата", 10, AL_CENTER);
        m_tableView.AddColumn("Конечная|дата", 10, AL_CENTER);
        m_tableView.AddColumn("Фактическая сумма|процентов", 26, AL_RIGHT);
        m_tableView.AddColumn("Фактическая|дата", 10, AL_CENTER);

    end;

    macro getReportWidth()
        return m_tableView.getSumLen();
    end;

    macro printReportHeader()

        var beginDate  = m_parameters.getBeginDate();
        var endDate = m_parameters.getEndDate();
        var clientName;
        var backofficeName;
        var dataSource;

        // 31.05.2011 ABP Этот параметр нужно доставать непосредственно из панели
        dataSource = TRsbDataset("SELECT t_code"
                        + "\n" + "  FROM dllvalues_dbt"
                        + "\n" + " WHERE t_list = 1118"
                        + "\n" + "   AND t_element = " + m_parameters.getBackoffice()
                                );
        if (dataSource.next())
            backOfficeName = dataSource.code;
        else
            backOfficeName = "Все";
        end;

        // 31.05.2011 ABP Этот параметр нужно доставать непосредственно из панели
        dataSource = TRsbDataset("SELECT t_shortName"
                        + "\n" + "  FROM drepParty_vw"
                        + "\n" + " WHERE t_id = " + m_parameters.getClientId()
                                );

        if (dataSource.next())
            clientName = dataSource.shortName;
        else
            clientName = "Все";
        end;

        printBankHeader(m_parameters.getDepartmentId(), m_parameters.getOrganizationStructure(), getReportWidth());

        println();
        println(strAlign("Ведомость начисления процентов", getReportWidth(), STR_ALIGN_CENTER));

        if (beginDate == endDate)
            println(strAlign("за " + beginDate, getReportWidth(), STR_ALIGN_CENTER));
        else
            println(strAlign("c " + beginDate + " по " + endDate, getReportWidth(), STR_ALIGN_CENTER));
        end;

        println();
        println("Бэк-офис: " + backofficeName);

        println();
        println("Клиент: " + clientName);
        println("Процентный договор: " + ternary(m_parameters.getContractNumber() == 0, "Все", m_parameters.getContractNumber()));

        println();
        if (m_parameters.getChargeObjectKind() == REP_PERCENT_CHARGE_OBJECT_KIND_ACCOUNT) //лицевой счет
            println("Лицевой счет: " + ternary(m_parameters.getAccountMask() == "", "Все", m_parameters.getAccountMask()));
        else
            println("Вид объекта: " + m_parameters.getObjectKind());
            println("Номер объекта: " + m_parameters.getObjectNumber());
        end;
        println();

    end;

    macro printTableHeader()
        m_tableView.printHead("");
    end;

    macro printTableBottom()
        m_tableView.printBottom("");
        m_tableView.printFreeString("");
    end;

    macro printReportBottom()
        println();
        println("Операционист ", {name_oper});
    end;

    macro printString()
        var parameters = TArray();
        var expression = "";

        var i = 1;
        while (getParm(i, parameters(i-1)))
          expression = expression + string(",", "parameters(", i-1, ")");
          i = i + 1;
        end;

        expression = "m_tableView.printString(" + subStr(expression, 2) + ")";
        execExp(expression);
    end;

    macro printFreeString(str)
        println(str);
    end;

    macro printDirectionHeader(direction)
        printLn();
        printFreeString("Направление: " + direction);
    end;

    macro printNoData(directionKind, distributionIsEmpty : bool, attractionIsEmpty : bool)
        if (((directionKind == 2) or (directionKind == -1)) and (distributionIsEmpty == true))
            printLn();
            printDirectionHeader(DISTRIBUTION);
            println("Нет данных для отчета");
        end;
        if (((directionKind == 1) or (directionKind == -1)) and (attractionIsEmpty == true))
            printLn();
            printDirectionHeader(ATTRACTION);
            println("Нет данных для отчета");
        end;
    end;

    macro printCurrencyHeader(currencyName)
        printFreeString("Валюта " + currencyName);
    end;

    private macro printTotals(caption, totals)
        var totalsLength = strLen(string(totals : 0 : 2));
        var columns = m_tableView.getAColumns();
        var calculatedCaptionLength = 2*4+3 + columns(0).getLen() + columns(1).getLen() + columns(2).getLen() + columns(3).getLen() - totalsLength;

        caption = strRpad(caption, calculatedCaptionLength, ".");
        m_tableView.printFreeString(caption + totals);
    end;

    macro printContractTotals(caption, totals)
        printTotals(subStr("Итого по ПД " + caption, 1, 51), totals);
    end;

    macro printClientTotals(caption, totals)
        printTotals(subStr("Итого по клиенту " + caption, 1, 35), totals);
    end;

    macro printUserTotals(caption, totals)
        printTotals(subStr("Итого по операционисту " + caption, 1, 60), totals);
    end;

    macro printBranchTotals(caption, totals)
        printTotals(subStr("Итого по подразделению " + caption, 1, 60), totals);
    end;

    macro printCurrencyTotals(caption, totals)
        printTotals("Итого сумма начисленных процентов" + caption, totals);
    end;

    private macro constructor(parameters)
        m_parameters = parameters;

        createTableView();

        printOFStream = TOFStream("percChargeSheet");
        printOFStream.clear();
        printOFStream.setOutputFile();
    end;

    private macro destructor()
        printOFStream.resetOutputFile();
    end;

    constructor(parameters);

end;

private class TController(parameters : Object)

    private var m_parameters;

    // 31.05.2011 ABP Подсчет итогов нужно было бы засунуть в TDataSource. Вот только непонятно как...
    macro execute(view : Object, dataSource : Object)

        var isEmptyReport : Bool = true;

        var currentDirection = null;

        var currentCurrency = null;
        var currencyTotals = $0;

        var currentBranch = null;
        var branchTotals = $0;

        var currentUser = null;
        var userTotals = $0;

        var currentClient = null;
        var clientTotals = $0;

        var currentContract = null;
        var contractTotals = $0;

        var totalsModel = m_parameters.getTotalsModel();

        var contractDirection = m_parameters.getContractDirection();

        var contractNBA;
        var branchName;
        var userName;
        var clientName;

        var needTableHeader;
        var needCurrencyHeader;

        var i;

        var attractionIsEmpty : bool = true;   // Направление "Привлечение" пусто ?
        var distributionIsEmpty : bool = true;   // Направление "Размещение" пусто ?

        macro printTotals(position)
            var j = totalsModel.size - 1;

            while (j >= position)
                if (totalsModel(j) == REP_PATTERNSORT_DEPARTMENT)
                    if (currentBranch != null)
                        view.printBranchTotals(branchName, branchTotals);
                    end;
                    currentBranch = dataSource.branchId;
                    branchTotals = $0;
                elif (totalsModel(j) == REP_PATTERNSORT_OPER)
                    if (currentUser != null)
                        view.printUserTotals(userName, userTotals);
                    end;
                    currentUser = dataSource.userId;
                    userTotals = $0;
                elif (totalsModel(j) == REP_PATTERNSORT_CLIENT)
                    if (currentClient != null)
                        view.printClientTotals(clientName, clientTotals);
                    end;
                    currentClient = dataSource.clientId;
                    clientTotals = $0;
                end;

                j = j - 1;
            end;
        end;

        view.printReportHeader();

        while (dataSource.next())

            isEmptyReport = false;

            needTableHeader = false;
            needCurrencyHeader = false;

            if (currentContract != dataSource.contractId)
                if (currentContract != null)
                    view.printTableBottom();
                    view.printContractTotals(contractNBA, contractTotals);
                end;

                needTableHeader = true;
                currentContract = dataSource.contractId;
                contractTotals = $0;
            end;

            if (needTableHeader) // сменился номер договора, возможно, понадобятся итоги по подразделению, и/или оперу, и/или клиенту
                // если сменилось валюта/направление/подразделение/опер/клиент, то итоги точно понадобятся
                if (   (currentBranch != dataSource.branchId)
                    or (currentUser != dataSource.userId)
                    or (currentClient != dataSource.clientId)
                    or (currentDirection != dataSource.contractDirection)
                    or (currentCurrency != dataSource.fiId)
                   )
                    i = 0;
                    while (i < totalsModel.size)
                        if (totalsModel(i) == REP_PATTERNSORT_DEPARTMENT)
                            if (   (currentBranch != dataSource.branchId)
                                or (currentDirection != dataSource.contractDirection)
                                or (currentCurrency != dataSource.fiId)
                               )
                                printTotals(i);
                                needTableHeader = true;
                                break;
                            end;
                        elif (totalsModel(i) == REP_PATTERNSORT_OPER)
                            if (   (currentUser != dataSource.userId)
                                or (currentDirection != dataSource.contractDirection)
                                or (currentCurrency != dataSource.fiId)
                               )
                                printTotals(i);
                                needTableHeader = true;
                                break;
                            end;
                        elif (totalsModel(i) == REP_PATTERNSORT_CLIENT)
                            if (   (currentClient != dataSource.clientId)
                                or (currentDirection != dataSource.contractDirection)
                                or (currentCurrency != dataSource.fiId)
                               )
                                printTotals(i);
                                needTableHeader = true;
                                break;
                            end;
                        end;

                        i = i + 1;
                    end;
                end;
            end;

            if (currentCurrency != dataSource.fiId)
                if (currentCurrency != null)
                    view.printCurrencyTotals("", currencyTotals);
                    view.printFreeString("");
                end;
                needTableHeader = true;
                currentCurrency = dataSource.fiId;

                currencyTotals = $0;

                currentBranch = dataSource.branchId;
                branchTotals = $0;

                currentUser = dataSource.userId;
                userTotals = $0;

                currentClient = dataSource.clientId;
                clientTotals = $0;

                currentContract = dataSource.contractId;
                contractTotals = $0;

                needCurrencyHeader = true;
            end;

            if (currentDirection != dataSource.contractDirection)
                view.printDirectionHeader(dataSource.contractDirection);
                currentDirection = dataSource.contractDirection;

                if (currentDirection == DISTRIBUTION)
                    distributionIsEmpty = false;
                else
                    attractionIsEmpty = false;
                end;
                currentCurrency = dataSource.fiId;
                currencyTotals = $0;

                currentBranch = dataSource.branchId;
                branchTotals = $0;

                currentUser = dataSource.userId;
                userTotals = $0;

                currentClient = dataSource.clientId;
                clientTotals = $0;

                currentContract = dataSource.contractId;
                contractTotals = $0;
            end;

            if (needCurrencyHeader)
                view.printCurrencyHeader(dataSource.t_fiCodeAndName);
            end;

            if (needTableHeader)
                view.printFreeString("");
                view.printTableHeader();
            end;

            branchTotals   = branchTotals + dataSource.scheduleFactSum;
            userTotals     = userTotals + dataSource.scheduleFactSum;
            clientTotals   = clientTotals + dataSource.scheduleFactSum;
            contractTotals = contractTotals + dataSource.scheduleFactSum;
            currencyTotals = currencyTotals + dataSource.scheduleFactSum;

            view.printString(ternary(dataSource.scheduleCalcDate == null, "", string(dataSource.scheduleCalcDate : f)),
                             ternary(dataSource.schedulePeriodBeginDate == null, "", string(dataSource.schedulePeriodBeginDate : f)),
                             ternary(dataSource.schedulePeriodEndDate == null, "", string(dataSource.schedulePeriodEndDate : f)),
                             string(dataSource.scheduleFactSum : 0 : 2),
                             ternary(dataSource.scheduleFactDate == null, "", string(dataSource.scheduleFactDate : f))
                            );

            contractNBA = dataSource.contractNBA;
            branchName  = dataSource.branchName;
            userName    = dataSource.userName;
            clientName  = dataSource.clientName;
        end;

        if (isEmptyReport)
            view.printNoData(contractDirection, distributionIsEmpty, attractionIsEmpty);
        else
            view.printTableBottom();
            view.printContractTotals(contractNBA, contractTotals);

            i = totalsModel.size - 1;
            while (i >= 0)
                if (totalsModel(i) == REP_PATTERNSORT_DEPARTMENT)
                    view.printBranchTotals(branchName, branchTotals);
                elif (totalsModel(i) == REP_PATTERNSORT_OPER)
                    view.printUserTotals(userName, userTotals);
                elif (totalsModel(i) == REP_PATTERNSORT_CLIENT)
                    view.printClientTotals(clientName, clientTotals);
                end;

                i = i - 1;
            end;

            view.printCurrencyTotals("", currencyTotals);

            view.printNoData(contractDirection, distributionIsEmpty, attractionIsEmpty);
        end;

        view.printReportBottom();

    end;

    private macro constructor(parameters : Object)
        m_parameters = parameters;
    end;

    constructor(parameters);

end;

macro produceReport(departmentId, organizationStructure, issueMode, currencyId, clientId, isGroup, operNumber, beginDate, endDate,
                    backoffice, contractNumber, contractDirection, contractUsertype, contractUsertypeMethod, chargeObjectKind, chapterNumber, 
                    balanceMask, accountMask, objectKind, objectNumber, useIndividualSchedule, scheduleKind, scheduleNumber, totalsModel
                   )

    var profiler = TSQLProfiler(getTxtFileName("!PercChargeSheetProfile"));
    profiler.clear();
    profiler.on();

    var parameters = TParameters(departmentId, organizationStructure, issueMode, currencyId, clientId, isGroup, operNumber, beginDate,
                                 endDate, backoffice, contractNumber, contractDirection, contractUsertype, contractUsertypeMethod, chargeObjectKind,
                                 chapterNumber,balanceMask, accountMask, objectKind, objectNumber, useIndividualSchedule, scheduleKind, 
                                 scheduleNumber, totalsModel
                                );

    /* Контроль открытых опердней */
    if (not RepOperdaysOpened(parameters.getDepartmentList(), parameters.getBeginDate(), parameters.getEndDate()).shouldContinue())
        exit(1);
    end;

    TController(parameters).execute(TView(parameters), TDataSource(parameters).getDataSource());

    reportBodyFileName = printOfstream.getFileName();

    return true;

end;

macro showReport()

    printOfstream.show();

end;

macro saveReport(reportName, controlName)

    printOfstream.save(reportName);

end;
