/*
$Name:        ibr_mo_print.mac
$Module:      Отчеты банка
$Description: Мемориальные ордера
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import mo_print;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_MO = "Параметры";
private const gRep = TAB_REPORT + "@" + GRP_MO + "@";

private var fileName = "ibr_mo_print_web";  

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

macro getDateDocKind(): TArray
    var query: String = "";
    var dataSet: Object = NULL;
    var result: TIbrDynamicSource = TIbrDynamicSource();
    var metaList: TArray = TArray();
    var dataList: TArray = TArray();

    query =  " SELECT t_result_carry as code,"
    + "\n" + "        t_short_name   as name"
    + "\n" + "   FROM drescarry_dbt"
    + "\n" + "ORDER BY t_result_carry";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("code", V_INTEGER);
    dataSet.setFieldType("name", V_STRING);

    while (dataSet.moveNext())
        dataList[dataList.size] = TIbrData(dataSet.code, dataSet.name);
    end;

    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_CODE, "code").setBinding();
    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_NAME, "name");

    result.initialize(metaList, dataList);

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

macro getDateOperationCipher(): TArray
    var query: String = "";
    var dataSet: Object = NULL;
    var result: TIbrDynamicSource = TIbrDynamicSource();
    var metaList: TArray = TArray();
    var dataList: TArray = TArray();

    query = "SELECT t_shifr_oper as code,"
    + "\n" + "      t_comment    as name"
    + "\n" + " FROM dcipher_dbt t"
    + "\n" + "WHERE t.t_NotUsed != 'X'";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("code", V_STRING);
    dataSet.setFieldType("name", V_STRING);

    while (dataSet.moveNext())
        dataList[dataList.size] = TIbrData(dataSet.code, dataSet.name);
    end;

    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_CODE, "code").setBinding();
    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_NAME, "name");

    result.initialize(metaList, dataList);

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_mo_print", "Мемориальные ордера", "ibr_mo_print.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_MO, 1);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));

    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет", "Лицевой счета"));
    groupReport.add(facade.createFieldList("ВидДокумента", "Вид документа", "Все документы", "ibr_mo_print","getDateDocKind"));

    groupReport.add(facade.createFieldText("Пачка", "Номер пачки", 0));
    groupReport.add(facade.createFieldList("ШифрОперации","Шифр операции", "Все", "ibr_mo_print", "getDateOperationCipher"));
    groupReport.add(facade.createFieldSort("Сортировка", "Сортировка", "1259", 1744));

    tabReport.add(groupReport);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    var result : Object = TRcbResult();
    var i : integer = 0;
    var pattern = strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1);

    while (i < pattern.size)
        pattern[i] = TIbrCommon().getElementLlValues(pattern[i], 1635);
        i = i + 1;
    end;

    result.valueBoolean = printReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                        getValue(report, gRep+"Структура").valueInteger,
                                        getValue(report, gRep+"Режим").valueInteger,
                                        TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),
                                        getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                        getValue(report, gRep+"НачалоПериода").valueDate,
                                        getValue(report, gRep+"КонецПериода").valueDate,
                                        getValue(report, gRep+"ТолькоИнВалюта").valueBoolean,
                                        getValue(report, gRep+"Операционист").valueInteger,
                                        getValue(report, gRep+"ЛицевойСчет").valueString,
                                        ternary(getValue(report, gRep+"ВидДокумента").valueString == "Все документы", 0, getValue(report, gRep+"ВидДокумента").valueString),
                                        getValue(report, gRep+"Пачка").valueInteger,
                                        ternary(getValue(report, gRep+"ШифрОперации").valueString == "Все", "", getValue(report, gRep+"ШифрОперации").valueString),
                                        pattern
                                        );

    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;    

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;