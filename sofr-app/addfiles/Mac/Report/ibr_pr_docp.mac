/*
$Name:        ibr_pr_docp.mac
$Module:      Отчеты банка
$Description: Справка о проведенных документах
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;      
import pr_docp;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_DOC = "Параметры";
private const GRP_SMASH = "Разбивать по";
private const GRP_PARAMS = "Дополнительные Параметры";
private const gRep = TAB_REPORT + "@" + GRP_DOC + "@";
private const gSm  = TAB_REPORT + "@" + GRP_SMASH + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";

private var fileName = "ibr_pr_docp_web";  

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_pr_docp", "Справка о проведенных документах", "ibr_pr_docp.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_DOC, 1);
    var groupSmash  = facade.createGroup(GRP_SMASH, 2);
    var groupParams = facade.createGroup(GRP_PARAMS, 3);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));
    
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));
    
    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    groupReport.add(facade.createFieldText("Пачка", "Номер пачки", 0));
    groupReport.add(facade.createFieldSort("Сортировка", "Вид сортировки", "6", 1635));
    
    facade.resetFieldsOrder();
    groupSmash.add(facade.createFieldCheckBox("РазибватьПачкам", "Пачкам", false));
    groupSmash.add(facade.createFieldCheckBox("РазибватьОпер", "Операционистам", true));

    facade.resetFieldsOrder();
    groupParams.add(facade.createFieldCheckBox("ЭквивНацВалюте", "Эквивалент в нац.валюте", false));
    groupParams.add(facade.createFieldCheckBox("КаждыйДень", "Печать за каждый день", false));
    groupParams.add(facade.createFieldCheckBox("ДанныеПлатежей", "Данные из платажей", false));
    groupParams.add(facade.createFieldCheckBox("Апострофы", "С апострофами", false));
    groupParams.add(facade.createFieldCheckBox("ПечатьРазниц", "Печать курсовых разниц", false));

    tabReport.add(groupReport);
    tabReport.add(groupSmash);
    tabReport.add(groupParams);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();
    var i : integer = 0;
    var pattern = strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1);

    while (i < pattern.size)
        pattern[i] = TIbrCommon().getElementLlValues(pattern[i], 1635);
        i = i + 1;
    end;     

    result.valueBoolean = makeReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                        getValue(report, gRep+"Структура").valueInteger,
                                        getValue(report, gRep+"Режим").valueInteger,  
                                        TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),     
                                        TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),                                        
                                        getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                        getValue(report, gRep+"НачалоПериода").valueDate,
                                        getValue(report, gRep+"КонецПериода").valueDate,
                                        false,// нет виджета групп операционистов
                                        getValue(report, gRep+"Операционист").valueInteger,
                                        getValue(report, gRep+"Пачка").valueInteger,
                                        pattern,
                                        getValue(report, gSm+"РазибватьПачкам").valueBoolean,
                                        getValue(report, gSm+"РазибватьОпер").valueBoolean,
                                        getValue(report, gPrm+"ЭквивНацВалюте").valueBoolean,
                                        getValue(report, gPrm+"КаждыйДень").valueBoolean,
                                        getValue(report, gPrm+"ДанныеПлатежей").valueBoolean,
                                        getValue(report, gPrm+"Апострофы").valueBoolean,
                                        getValue(report, gPrm+"ПечатьРазниц").valueBoolean,
                                        fileName);
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gSm+"РазибватьПачкам").valueBoolean) and (getValue(report, gSm+"РазибватьОпер").valueBoolean != true))
        repErrorsQueue().add(0, "Флаг \"Разбивать по пачкам\" устанавливается вместе с \"Разбивать по операционистам\"");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;    

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;