/*
$Name:          pr_crtrn.mac
$Module:        Внутренняя отчетность
$Description:   Отчет по исправительным оборотам.
*/

/**
 *  RS-Bank 6.0
 *
 *  File Name   : pr_crtrn.mac
 *  Programmer  :
 *  Description : Отчет по исправительным оборотам.
 *  Comment     :
 *  Modify      : Ivkina 24.01.2007
 *                30.11.2011 ABP Переделал. Хотя структуру с целом оставил неизменной.
 */

import testLib;

import globals;
import cb_sql;

import BankInter;
import OprInter;
import FIInter;
import CTInter;
import prpm;

import Reporting;
import treport;
import rep_lib;

import ofstream;

FILE cb_doc   ("cb_doc.dbt");
FILE multydoc ("multydoc.dbt");
FILE bankOrder("pmpaym.dbt");

var blockSum = TArray();
var blockSumR = TArray();

var blockCount = TArray();
var SumAll : moneyl;
var CountAll;
var reportBodyFileName;

private var ALLCURRENCY = -2;   /* Все ин. валюты */

/*ширина полей таблицы*/
var widthSign   = 7;
var widthDate   = 14;
var widthNumb   = 11;
var widthType   = 3;
var widthCur    = 6;
var widthDebet  = 30;
var widthCredit = 30;
var widthSum    = 14;
var widthSumRur = 19;
var widthReason = 22;
var widthOper   = 15;
/*общая ширина отчета*/
var width = 2 + widthSign   + 2 + widthDate + 2 + widthNumb   + 2 + widthType   + 2 +  widthCur + 2 + widthDebet +
            2 + widthCredit + 2 + widthSum  + 2 + widthSumRur + 2 + widthReason + 2 + widthOper + 2;

var correctKindSign   = arrCreate("S", "И", "К", "N");
var correctKindString = arrCreate("сторнирующих (S)",   "исправительных (И)",
                                  "корректирующих (К)", "новых корректирующих (N)");
//------------------------------------------------------
PRIVATE macro PrintHeadOfReport1(Dep_Number, OrgStructure, First_Date, Second_Date)
  printBankHeader(Dep_Number, OrgStructure, width);
  println();
  println(StrAlign("Отчет по исправительным оборотам", width, STR_ALIGN_CENTER));
  println(StrAlign("за период с " + string(First_Date:f) + " по " + string(Second_Date:f), width, STR_ALIGN_CENTER));
  println();
end;
//--------------------------------------------
macro PrintOneBlokCages1(Report1:CTableReport)
  Report1.AddColumn("Признак",             widthSign  );
  Report1.AddColumn("Дата документа",      widthDate  );
  Report1.AddColumn("№ документа",         widthNumb  );
  Report1.AddColumn("Тип",                 widthType  );
  Report1.AddColumn("Валюта",              widthCur   );
  Report1.AddColumn("Дебет",               widthDebet );
  Report1.AddColumn("Кредит",              widthCredit);
  Report1.AddColumn("Сумма в валюте",      widthSum   , AL_RIGHT);
  Report1.AddColumn("Эквивалент в рублях", widthSumRur, AL_RIGHT);
  Report1.AddColumn("Основание",           widthReason);
  Report1.AddColumn("№ пользователя",     widthOper  , AL_RIGHT);
  Report1.PrintHead;
end;
//----------------------------------------------------
MACRO PrintOneBlokHead1(Report : CTableReport, Chapter, FIID)

  var  dp;
  var  chapt;
  var  cur;

  chapt = TRsbDataset( "SELECT T_SYMBOL,T_NAME FROM DOBCHAPTR_DBT WHERE T_CHAPTER = "+Chapter);
  IF(chapt.moveNext)
  [Глава #### ###################](chapt.value(0),chapt.value(1));
  ELSE
    [Глава ######################################];
  END;

  cur = TRsbDataset( "SELECT T_FI_CODE,T_NAME FROM DFININSTR_DBT WHERE T_FIID = "+FIID);
  if(cur.moveNext)
  [Валюта #### ##################################################](cur.value(0),cur.value(1));
  else
[Валюта ################## ###########];
  end;
END;
//-------------------------------------------------------------------------
PRIVATE MACRO printOneOperation1(report : CTableReport, rs, ratedate)

    var cur;
    var currencyStr = "";
    var sumStr = "";
    var sumNatCurStr = "";

    report.PrintSeparator(TRUE);

    cur = TRsbDataset("SELECT T_FI_CODE FROM DFININSTR_DBT WHERE T_FIID = " + rs.code_currency);
    if (cur.moveNext)
        currencyStr = String(cur.fi_code);
    end;

    sumStr = String(rs.sum);
    sumNatCurStr = ternary(rs.code_currency != NATCUR, String(rs.sum_natCur), "");

    report.printStringTransferByWord("V", "", "", rs.typeDocument, currencyStr, String(rs.account_payer : f),
                                     String(rs.account_receiver : f), sumStr, sumNatCurStr, rs.ground, rs.oper);

END;
//-----------------------------------------------------
PRIVATE MACRO PrintIsprDocument1(Report : CTableReport, rs, Type)
    var cur;
    var currencyStr = "";
    var SumR : Money;
    var SumRStr = "";

    Report.PrintSeparator(TRUE);

    cur = TRsbDataset("SELECT T_FI_CODE FROM DFININSTR_DBT WHERE T_FIID = " + rs.fiId);
    if (cur.moveNext)
        currencyStr = cur.fi_code;
    end;

    ConvSum(SumR, rs.sum, rs.date, rs.fiId);
    SumRStr = String(SumR);

    Report.PrintStringTransferByWord("Ошиб.", String(rs.date : f), rs.t_numb_document, rs.t_type_document, currencyStr,
                                     String(rs.t_account_payer : f), String(rs.t_account_receiver : f), rs.sum,
                                     SumRStr, rs.ground, rs.oper
                                    );
END;

private macro getDocumentCarries(docKind : Integer, uniDocumentId : String, ground : String) : Object
    var dataset : Object;
    var document = TRecHandler("accTrn");
    var opr : Object;
    var count : Integer;
    var strcmd : String;
    var isFirst : Bool;
    var carryGround : String = "";
    var sum : Money = $0;
    var fiId : Integer = 0;

    opr = TRsbDataset("SELECT t_id_operation"
             + "\n" + "  FROM doproper_dbt"
             + "\n" + " WHERE t_docKind = " + docKind
             + "\n" + "   AND t_documentId = " + getSqlString(uniDocumentId)
                     );
    opr.setFieldType("id_operation", V_INTEGER);

    if (opr.next())

        document.clear();
        count = 0;
        strcmd = "";
        isFirst = true;

        while (getDocsByOperStep(document, opr.id_operation, 0))

            carryGround = ternary((strupr(strsubst(document.rec.ground, " ", "")) == strupr(strsubst(ground, " ", ""))), "", document.rec.ground);

            if (document.rec.fiId_payer == document.rec.fiId_receiver)
                fiId = document.rec.fiId_payer;
                sum = document.rec.sum_payer;
            elif ((document.rec.fiId_payer != document.rec.fiId_receiver) and (document.rec.fiId_payer != NATCUR))
                fiId = document.rec.fiId_payer;
                sum = document.rec.sum_payer;
            elif ((document.rec.fiId_payer != document.rec.fiId_receiver) and (document.rec.fiId_payer == NATCUR))
                fiId = document.rec.fiId_receiver;
                sum = document.rec.sum_receiver;
            end;

            if (document.rec.result_carry == DELTARATE_MCD)
                fiId = NATCUR;
                sum = document.rec.sum_natCur;
            end;

            if (not isFirst)
                strcmd = strcmd
                + "\n" + "UNION ALL"
                + "\n";
            end;

            isFirst = false;

            strcmd = strcmd +
                    "SELECT " + count + "                                             AS t_kind_of_doc,"
            + "\n" + "      " + document.rec.accTrnId + "                             AS t_autokey,"
            + "\n" + "      " + document.rec.chapter + "                              AS t_chapter,"
            + "\n" + "      " + fiId + "                                              AS t_code_currency,"
            + "\n" + "      " + getSqlString(document.rec.account_payer) + "          AS t_account_payer,"
            + "\n" + "      " + getSqlString(document.rec.account_receiver) + "       AS t_account_receiver,"
            + "\n" + "      " + sum + "                                               AS t_sum,"
            + "\n" + "      " + document.rec.sum_natCur + "                           AS t_sum_natCur,"
            + "\n" + "      " + getSqlDate(document.rec.date_carry) + "               AS t_date_carry,"
            + "\n" + "      " + getSqlDate(document.rec.date_carry) + "               AS t_date_value,"
            + "\n" + "      " + document.rec.oper + "                                 AS t_oper,"
            + "\n" + "      " + document.rec.department + "                           AS t_department,"
            + "\n" + "      " + getSqlString(document.rec.numb_document) + "          AS t_numb_document,"
            + "\n" + "      " + getSqlString(carryGround) + "                         AS t_ground,"
            + "\n" + "      " + getSqlString(nvl(document.rec.typeDocument, "")) + "  AS t_typeDocument"
            + "\n" + " FROM dual";

            count = count+1;
        end;

        if (strcmd != "")
            dataset = TRsbDataset(strcmd);

            dataset.setFieldType("T_KIND_OF_DOC", V_INTEGER);
            dataset.setFieldType("T_AUTOKEY", V_INTEGER);
            dataset.setFieldType("T_CHAPTER", V_INTEGER);
            dataset.setFieldType("T_CODE_CURRENCY", V_INTEGER);
            dataset.setFieldType("T_ACCOUNT_PAYER", V_STRING);
            dataset.setFieldType("T_ACCOUNT_RECEIVER", V_STRING);
            dataset.setFieldType("T_SUM", V_MONEY);
            dataset.setFieldType("T_SUM_NATCUR", V_MONEY);
            dataset.setFieldType("T_DATE_CARRY", V_DATE);
            dataset.setFieldType("T_DATE_VALUE", V_DATE);
            dataset.setFieldType("T_OPER", V_INTEGER);
            dataset.setFieldType("T_DEPARTMENT", V_INTEGER);
            dataset.setFieldType("T_NUMB_DOCUMENT", V_STRING);
            dataset.setFieldType("T_GROUND", V_STRING);
            dataset.setFieldType("T_TYPEDOCUMENT", V_STRING);
        else
            dataset = TRsbDataset("SELECT NULL FROM dual WHERE 1 = 0");
        end;

    end;

    return dataset;
end;

//-----------------------------------------------------
PRIVATE MACRO PrintOneBlok1(Report : CTableReport, rs, Show_Op,flag) // rs имеет формат DISPR_DOCS_TMP

    var strcmd;
    var cur;
    var currencyStr : String;
    var sdoc;
    var scarry;
    var mcarry;
    var carry;
    var opr;
    var SumR : Money;
    var SumRStr : String;
    var id;
    var count;
    var docDate : Date;
    var rateDate : Date;
    var carryGround;

    var isFirst = true;
    var sum : Money;
    var fiId : Integer;

    var stat = 0;

    if (flag==1) Report.PrintSeparator(TRUE); end;

    docDate = rs.date_carry;
    rateDate = rs.rateDate;

/* исправляющий документ -------------------------------------------------------------------------*/

    cur = TRsbDataset("SELECT t_fi_code FROM dfininstr_dbt WHERE t_fiid = " + rs.fiId);
    if (cur.moveNext)
        currencyStr = string(cur.fi_code);
    else
        currencyStr = "";
    end;

    if (rs.fiId != NATCUR)
        ConvSum(SumR , rs.sum, rateDate, rs.fiId);
        SumRStr = string(SumR);
    else
        SumRStr = "";
    end;

    Report.PrintStringTransferByWord("Испр.", String(docDate : f), rs.numb_document, rs.type_document, currencyStr,
        string(rs.account_payer : f), string(rs.account_receiver : f), rs.sum, SumRStr, rs.ground, rs.oper);

/* проводки исправляющего документа --------------------------------------------------------------*/

    if (Show_Op == true)

        if (rs.docKind == DLDOC_MEMORIALORDER)

            cb_doc.documentId = rs.documentId;

            if (getEQ(cb_doc))
                carry = getDocumentCarries(rs.docKind, uniId(cb_doc, 0, rs.docKind), rs.ground);
            end;

        elif (rs.docKind == DLDOC_BANKORDER)

            bankOrder.paymentId = rs.documentId;

            if (getEQ(bankOrder))
                carry = getDocumentCarries(rs.docKind, uniId(bankOrder, 0, rs.docKind), rs.ground);
            end;

        elif (rs.docKind == CB_MULTYDOC)

            multydoc.autokey = rs.documentId;

            if (getEQ(multydoc))
                carry = getDocumentCarries(rs.docKind, uniId(multydoc, 0, rs.docKind), rs.ground);
            end;

        end;

        while(carry.moveNext)
          printOneOperation1(report, carry, rateDate);
        end;

    end;

/* исправленный документ -------------------------------------------------------------------------*/

    var memDocWasPrinted = false;

    //получена запись с проводкой и, возможно, документом
    if (rs.srcDocKind == CB_MULTYDOC)

      strcmd = "SELECT d.t_oper                                                       AS t_oper,"
      + "\n" + "       DECODE(pm.t_fiId, " + NATCUR + ", pm.t_payFiId, pm.t_fiId)     AS t_fiId,"
      + "\n" + "       DECODE(pm.t_fiId, " + NATCUR + ", pm.t_payAmount, pm.t_amount) AS t_sum,"
      + "\n" + "       pm.t_payerAccount                                              AS t_account_payer,"
      + "\n" + "       pm.t_receiverAccount                                           AS t_account_receiver,"
      + "\n" + "       pm.t_valueDate                                                 AS t_date,"
      + "\n" + "       rm.t_ground                                                    AS t_ground,"
      + "\n" + "       rm.t_number                                                    AS t_numb_document,"
      + "\n" + "       d.t_type_document                                              AS t_type_document"
      + "\n" + "  FROM dmultydoc_dbt d,"
      + "\n" + "       dpmpaym_dbt pm,"
      + "\n" + "       dpmrmprop_dbt rm"
      + "\n" + " WHERE d.t_autokey = " + rs.srcDocumentId
      + "\n" + "   AND pm.t_documentId = d.t_autokey"
      + "\n" + "   AND pm.t_docKind = " + CB_MULTYDOC
      + "\n" + "   AND pm.t_purpose = " + PM_PURP_MULTYDOC
      + "\n" + "   AND rm.t_paymentId = pm.t_paymentId";

      mcarry = TRsbDataset(strcmd);

      mcarry.setFieldType("t_oper", V_INTEGER);
      mcarry.setFieldType("t_fiId", V_INTEGER);
      mcarry.setFieldType("t_sum", V_MONEY);
      mcarry.setFieldType("t_account_payer", V_STRING);
      mcarry.setFieldType("t_account_receiver", V_STRING);
      mcarry.setFieldType("t_date", V_DATE);
      mcarry.setFieldType("t_ground", V_STRING);
      mcarry.setFieldType("t_numb_document", V_STRING);
      mcarry.setFieldType("t_type_document", V_STRING);

      if (mcarry.moveNext)
          PrintIsprDocument1(Report, mcarry, 1);

          /* проводки исправленной мультивалютной проводки ---------------------------------------*/
          if (Show_Op == true)

              multydoc.autokey = rs.srcDocumentId;

              if (getEQ(cb_doc))
                  carry = getDocumentCarries(rs.srcDocKind, uniId(multydoc, 0, rs.srcDocKind), mcarry.ground);
              end;

              while(carry.moveNext)
                printOneOperation1(report, carry, "");
              end;
          end;
      end;

//        elif (rs.srcDocKind == DLDOC_BANKORDER)
//
//            memDocWasPrinted = false;
//
//            strcmd = "SELECT pm.t_oper                AS t_oper,"
//            + "\n" + "       CASE"
//            + "\n" + "           WHEN pm.t_fiId = pm.t_payFiId"
//            + "\n" + "           THEN pm.t_fiId"
//            + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
//            + "\n" + "            AND pm.t_fiId != " + NATCUR
//            + "\n" + "           THEN pm.t_fiId"
//            + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
//            + "\n" + "            AND pm.t_fiId = " + NATCUR
//            + "\n" + "           THEN pm.t_payFiId"
//            + "\n" + "       END                      AS t_code_currency,"
//            + "\n" + "       CASE"
//            + "\n" + "           WHEN pm.t_fiId = pm.t_payFiId"
//            + "\n" + "           THEN pm.t_amount"
//            + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
//            + "\n" + "            AND pm.t_fiId != " + NATCUR
//            + "\n" + "           THEN pm.t_amount"
//            + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
//            + "\n" + "            AND pm.t_fiId = " + NATCUR
//            + "\n" + "           THEN pm.t_payAmount"
//            + "\n" + "       END                      AS t_amount,"
//            + "\n" + "       pm.t_payerAccount        AS t_payerAccount,"
//            + "\n" + "       pm.t_receiverAccount     AS t_receiverAccount,"
//            + "\n" + "       pm.t_valueDate           AS t_valueDate,"
//            + "\n" + "       rm.t_ground              AS t_ground,"
//            + "\n" + "       rm.t_number              AS t_number,"
//            + "\n" + "       pm.t_typeDocument        AS t_typeDocument"
//            + "\n" + "  FROM dpmpaym_dbt pm,"
//            + "\n" + "       dpmrmprop_dbt rm"
//            + "\n" + " WHERE pm.t_documentId = " + rs.srcDocumentId
//            + "\n" + "   AND pm.t_docKind = " + DLDOC_BANKORDER
//            + "\n" + "   AND pm.t_purpose IN (" + PM_PURP_BANKORDER + "," + PM_PURP_CBANKORDER + ")"
//            + "\n" + "   AND rm.t_paymentId = pm.t_paymentId"
//
//            sdoc = TRsbDataset(strcmd);
//            sdoc.setFieldType("t_ValueDate", V_DATE);
//
//            if (sdoc.next)
//
//                report.printSeparator(TRUE);
//
//                cur = TRsbDataset("SELECT t_fi_code FROM dfininstr_dbt WHERE t_fiid = " + sdoc.code_currency);
//                if (cur.moveNext)
//                    currencyStr = string(cur.fi_code);
//                else
//                    currencyStr = "";
//                end;
//
//                if (sdoc.code_currency != NATCUR)
//                    convSum(sumR, sdoc.amount, sdoc.valueDate, sdoc.code_currency );
//                    sumRStr = string(sumR);
//                else
//                    sumRStr = "";
//                end;
//
//                report.printStringTransferByWord("Ошиб.", string(sdoc.valueDate : f),
//                    sdoc.number, sdoc.typeDocument, currencyStr, string(sdoc.payerAccount:f),
//                    string(sdoc.receiverAccount:f), sdoc.amount, sumRStr, sdoc.ground, sdoc.oper);
//
//                memDocWasPrinted = true;
//
//                if (show_op == true)
//
//                    bankOrder.paymentId = rs.srcDocumentId;
//
//                    if (getEQ(bankOrder))
//                        carry = getDocumentCarries(rs.srcDocKind, uniId(bankOrder, 0, rs.srcDocKind), sdoc.ground);
//                    end;
//
//                    while(carry.moveNext)
//                      printOneOperation1(report, carry, "");
//                    end;
//                end;
//            end;

    elif (rs.srcDocKind == DLDOC_MEMORIALORDER)

        memDocWasPrinted = false;

        strcmd =     "     SELECT cb.t_code_currency, pm.t_Amount, pm.t_ValueDate,"
            + "\n" + "       rm.t_Number, cb.t_typedocument, pm.t_PayerAccount,"
            + "\n" + "       pm.t_ReceiverAccount, rm.t_Ground, cb.t_oper"
            + "\n" + "       FROM dcb_doc_dbt cb"
            + "\n" + " INNER JOIN dpmpaym_dbt pm"
            + "\n" + "            ON pm.t_documentid = cb.t_documentid"
            + "\n" + " INNER JOIN dpmrmprop_dbt rm"
            + "\n" + "            ON rm.t_paymentid = pm.t_paymentid"
            + "\n" + " WHERE cb.t_documentid = " + int(rs.srcDocumentId);
        sdoc = TRsbDataset(strcmd);
        sdoc.setFieldType("t_ValueDate", V_DATE);

        if (sdoc.next)

            report.printSeparator(TRUE);

            cur = TRsbDataset("SELECT t_fi_code FROM dfininstr_dbt WHERE t_fiid = " + sdoc.code_currency);
            if (cur.moveNext)
                currencyStr = string(cur.fi_code);
            else
                currencyStr = "";
            end;

            if (sdoc.code_currency != NATCUR)
                convSum(sumR, sdoc.amount, sdoc.valueDate, sdoc.code_currency );
                sumRStr = string(sumR);
            else
                sumRStr = "";
            end;

            report.printStringTransferByWord("Ошиб.", string(sdoc.valueDate : f),
                sdoc.number, sdoc.typeDocument, currencyStr, string(sdoc.payerAccount:f),
                string(sdoc.receiverAccount:f), sdoc.amount, sumRStr, sdoc.ground, sdoc.oper);

            memDocWasPrinted = true;

            /* проводки исправленного ордера ---------------------------------------------*/
            if (show_op == true)

                cb_doc.documentId = rs.srcDocumentId;

                if (getEQ(cb_doc))
                    carry = getDocumentCarries(rs.srcDocKind, uniId(cb_doc, 0, rs.srcDocKind), sdoc.ground);
                end;

                while(carry.moveNext)
                  printOneOperation1(report, carry, "");
                end;
            end;
        end;
    end;

    if ((not memDocWasPrinted) and (rs.srcDocKind != CB_MULTYDOC) and (ValType(rs.acctrnId) != V_UNDEF))
    /* проводка */

        strcmd =     "SELECT DECODE(t_fiId_payer, " + NATCUR + ", t_fiId_receiver, t_fiId_payer) AS t_code_currency,"
            + "\n" + "       DECODE(t_fiId_payer, " + NATCUR + ", t_sum_receiver, t_sum_payer)   AS t_sum,"
            + "\n" + "       t_date_carry,"
            + "\n" + "       t_numb_document,"
            + "\n" + "       t_typedocument,"
            + "\n" + "       t_account_payer,"
            + "\n" + "       t_account_receiver,"
            + "\n" + "       t_ground,"
            + "\n" + "       t_oper"
            + "\n" + "  FROM daccTrn_dbt"
            + "\n" + " WHERE t_acctrnId = " + rs.acctrnId;

        scarry = TRsbDataset(strcmd);
        scarry.setFieldType("t_sum", V_MONEY);
        scarry.setFieldType("t_date_carry", V_DATE);

        if (scarry.next)

            report.printSeparator(TRUE);

            cur = TRsbDataset("SELECT t_fi_code FROM dfininstr_dbt WHERE t_fiid = " + scarry.code_currency);
            if (cur.moveNext)
                currencyStr = string(cur.fi_code);
            else
                currencyStr = "";
            end;

            if (scarry.code_currency != NATCUR)
                convSum(sumR, scarry.sum, scarry.date_carry, scarry.code_currency);
                sumRStr = string(SumR);
            else
                sumRStr = "";
            end;

            report.printStringTransferByWord("Ошиб. V", string(scarry.date_carry : f),
                "", scarry.typedocument, currencyStr, string(scarry.account_payer:f),
                string(scarry.account_receiver:f), scarry.sum, SumRStr, scarry.ground, scarry.oper);
        end;
    end;

END;
//------------------------------------------------------------
PRIVATE MACRO SplitMoney( Sum : money, Sum_Rub : @string, Sum_Kop : @string )

  Sum_Rub = substr( string( Sum ), 1, index( string(Sum), ".") - 1);
  Sum_Kop = substr( string( Sum ),    index( string(Sum), ".") + 1);

END;
//--------------------------------------------------------------------------
PRIVATE MACRO PrintOneBlokPodval1(Report : CTableReport, FIID)
    var cur;
    var FI_Code   = "";
    var Sum_Rub;
    var Sum_Kop;
    var i;

    cur = TRsbDataset( "SELECT T_CCY FROM DFININSTR_DBT WHERE T_FIID = "+string(FIID));
    if(cur.moveNext)
        FI_Code = string(cur.value(0));
    end;

    i = 0;
    println();
    while (i < correctKindSign.size)

        if (blockCount[i] != 0)

            SplitMoney( blockSumR[i], @Sum_Rub, @Sum_Kop );

            PrintLn("Итого ", correctKindString[i], " документов ", blockCount[i], " на сумму ",
                    blockSum[i], " ", FI_Code, "  ", Sum_Rub, " рублей ", Sum_Kop, " копеек");
        end;
        i = i + 1;
    end;

    SumAll = SumAll + blockSumR[i];
    CountAll = CountAll + blockCount[i];

    SplitMoney( blockSumR[i], @Sum_Rub, @Sum_Kop );
    PrintLn("ВСЕГО исправляющих документов ", blockCount[i], " на сумму ",
                                                    Sum_Rub, " рублей ", Sum_Kop, " копеек");
    println();
END;
//--------------------------------------------------------------------------
PRIVATE MACRO PrintPodval1(Report : CTableReport)
  var Sum_Rub,Sum_Kop;

  SplitMoney( SumAll, @Sum_Rub, @Sum_Kop );

  /*11 Dec 06 Mon 17:18:39 Malakhova Irina*/
  PrintLn("ВСЕГО по отчету исправляющих документов ", countAll, " на сумму ",
                                                      Sum_Rub, " рублей ", Sum_Kop, " копеек");
  println();
  println();
  println("Главный бухгалтер________________________ /                             /");
  println();
  println("Бухгалтер        ________________________ /                             /");

END;
//-------------------------------------------------------------------
MACRO CorReport1(Dep_Number,
                 OrgStructure,
                 IssueMode,
                 Chapter,
                 FIID,
                 First_Date,
                 Second_Date,
                 Oper,
                 BalanceAccount,
                 Account,
                 Correct_Kind,
                 Show_Op,
                 fileName)

    var profiler = TSQLProfiler(getTxtFileName("!pr_crtrn_sqlprofile"));
    profiler.clear();
    profiler.on();

    if (fileName != null)
        var printOfstream = TOfstream(fileName);

        printOfstream = TOfstream(fileName);
        printOfstream.setOutputFile();
        reportBodyFileName = printOfstream.getFileName();
    end;

    var correctionalDocumentsQueryText = "";
    var accountNumberFilter = "";
    var balanceNumberFilter = "";

    var  rs;
    var  Report1 = CTableReport(0, FALSE, FALSE);
    var  Chapter_ = 0, FIID_ = -1;
    var  Chapter_new, FIID_new;
    var  rateDate : Date;
    var  sumRub = Money(0);
    var  NextRecord;

    var  flag;
    var  PRB = TArray();
    var  departmentList;
    var  accountFilter;

    var i;
    var correctKindSqlStr = TArray();
    var allCorrectKindSignsStr = "";
    var moreThenOneBlock = false;

    macro currencyFilter(docKind)
        var currencyFilter = "";

        if (FIID == ALLCURRENCY)
            if (docKind == DLDOC_MEMORIALORDER)
                currencyFilter = "\n" + "   AND d.t_code_currency != " + NATCUR;
            elif (docKind == DLDOC_BANKORDER)
                currencyFilter = "\n" + "   AND " + NATCUR + " != ANY(pm.t_fiId, pm.t_payFiId)"; // 29.11.2011 ABP Хотя бы одна сторона в инвалюте
            elif (docKind == CB_MULTYDOC)
                currencyFilter = "\n" + "   AND 1 = 1"; // 29.11.2011 ABP Мультивалютные документы - всегда инвалютные
            end;
        elif (FIID != ALLFININSTR)
            if (docKind == DLDOC_MEMORIALORDER)
                currencyFilter = "\n" + "   AND d.t_code_currency = " + FIID;
            elif (docKind == DLDOC_BANKORDER)
                currencyFilter = "\n" + "   AND CASE"
                               + "\n" + "       WHEN pm.t_fiId = pm.t_payFiId"
                               + "\n" + "       THEN pm.t_fiId"
                               + "\n" + "       WHEN pm.t_fiId != pm.t_payFiId"
                               + "\n" + "        AND pm.t_fiId != " + NATCUR
                               + "\n" + "       THEN pm.t_fiId"
                               + "\n" + "       WHEN pm.t_fiId != pm.t_payFiId"
                               + "\n" + "        AND pm.t_fiId = " + NATCUR
                               + "\n" + "       THEN pm.t_payFiId"
                               + "\n" + "       END = " + FIID;
            elif (docKind == CB_MULTYDOC)
                currencyFilter = "\n" + "   AND DECODE(pm.t_fiId, " + NATCUR + ", pm.t_payFiId, pm.t_fiId) = " + FIID;
            end;
        end;

        return currencyFilter;
    end;

    macro chapterFilter(docKind)
        var chapterFilter = "";

        if (in(docKind, DLDOC_MEMORIALORDER, CB_MULTYDOC))
            chapterFilter = "\n" + "   AND " + getChapterFilterSqlClause(Chapter, "d.t_chapter");
        elif (docKind == DLDOC_BANKORDER)
            chapterFilter = "\n" + "   AND " + getChapterFilterSqlClause(Chapter, "pm.t_chapter");
        end;

        return chapterFilter;
    end;

    macro correctionalKindFilter(docKind)
        var correctionalKindFilter = "";

        if (Correct_Kind == 1)
            if (docKind == DLDOC_MEMORIALORDER)
                correctionalKindFilter = "\n" + "   AND REGEXP_LIKE(d.t_typeDocument, '[" + allCorrectKindSignsStr + "]', 'c')";
            elif (docKind == DLDOC_BANKORDER)
                correctionalKindFilter = "\n" + "   AND REGEXP_LIKE(pm.t_typeDocument, '[" + allCorrectKindSignsStr + "]', 'c')";
            elif (docKind == CB_MULTYDOC)
                correctionalKindFilter = "\n" + "   AND REGEXP_LIKE(d.t_type_document, '[" + allCorrectKindSignsStr + "]', 'c')";
            end;
        else
            if (docKind == DLDOC_MEMORIALORDER)
                correctionalKindFilter = "\n" + "   AND (d.t_typeDocument LIKE "  + correctKindSqlStr[Correct_Kind-2] + ")";
            elif (docKind == DLDOC_BANKORDER)
                correctionalKindFilter = "\n" + "   AND (pm.t_typeDocument LIKE "  + correctKindSqlStr[Correct_Kind-2] + ")";
            elif (docKind == CB_MULTYDOC)
                correctionalKindFilter = "\n" + "   AND (d.t_type_document LIKE "  + correctKindSqlStr[Correct_Kind-2] + ")";
            end;
        end;

        return correctionalKindFilter;
    end;

    macro userNumberFilter(docKind)
        var userNumberFilter = "";

        if (Oper != 0)
            if (docKind == DLDOC_MEMORIALORDER)
                userNumberFilter = "\n" + "   AND d.t_oper = " + Oper;
            elif (docKind == DLDOC_BANKORDER)
                userNumberFilter = "\n" + "   AND pm.t_oper = " + Oper;
            elif (docKind == CB_MULTYDOC)
                userNumberFilter = "\n" + "   AND d.t_oper = " + Oper;
            end;
        end;

        return userNumberFilter;
    end;

    departmentList = RepDepartmentList(OrgStructure, IssueMode, Dep_Number);

    /* Контроль опердней */
    if(RepOperdaysOpened( departmentList, First_Date, Second_Date ).ShouldContinue == false)
      exit(1);
    end;

    i = 0;
    while (i < correctKindSign.size)
        correctKindSqlStr[i] = "'%" + correctKindSign[i] + "%'";
        allCorrectKindSignsStr = allCorrectKindSignsStr + correctKindSign[i];
        i = i + 1;
    end;

    accountFilter  = RepAccountFilter(departmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);

    sql_truncate("dopr_docs_tmp");

    if (Account != "")
        accountNumberFilter = "\n" + "   AND (   (" + convertMaskToSqlFormat(Account, "pm.t_payerAccount") + ")"
                              "\n" + "        OR (" + convertMaskToSQLFormat(Account, "pm.t_receiverAccount") + ")"
                              "\n" + "       )";
    end;

    if (BalanceAccount != "")
        balanceNumberFilter = "\n" + "   AND (   (" + convertMaskToSQLFormat(BalanceAccount, "acc1.t_balance") + ")"
                              "\n" + "        OR (" + convertMaskToSQLFormat(BalanceAccount, "acc2.t_balance") + ")"
                              "\n" + "       )";
    end;

    correctionalDocumentsQueryText =
                 "SELECT d.t_documentId           AS t_documentId,"
        + "\n" + "   " + DLDOC_MEMORIALORDER + "  AS t_docKind,"
        + "\n" + "       d.t_oper                 AS t_oper,"
        + "\n" + "       d.t_chapter              AS t_chapter,"
        + "\n" + "       d.t_code_currency        AS t_fiId,"
        + "\n" + "       pm.t_amount              AS t_sum,"
        + "\n" + "       pm.t_payerAccount        AS t_account_payer,"
        + "\n" + "       pm.t_receiverAccount     AS t_account_receiver,"
        + "\n" + "       pm.t_valueDate           AS t_date_carry,"
        + "\n" + "       pm.t_department          AS t_department,"
        + "\n" + "       rm.t_ground              AS t_ground,"
        + "\n" + "       rm.t_number              AS t_numb_document,"
        + "\n" + "       d.t_typeDocument         AS t_type_document,"
        + "\n" + "       acc1.t_balance           AS t_balance_from,"
        + "\n" + "       acc2.t_balance           AS t_balance_to,"
        + "\n" + "       i.t_rateDate             AS t_rateDate,"
        + "\n" + "       i.t_srcDocKind           AS t_srcDocKind,"
        + "\n" + "       i.t_srcDocumentId        AS t_srcDocumentId,"
        + "\n" + "       i.t_accTrnId             AS t_accTrnId"
        + "\n" + "  FROM dcb_doc_dbt d,"
        + "\n" + "       daccispr_dbt i,"
        + "\n" + "       daccount_dbt acc1,"
        + "\n" + "       daccount_dbt acc2,"
        + "\n" + "       dpmpaym_dbt pm,"
        + "\n" + "       dpmrmprop_dbt rm"
        + "\n" + " WHERE pm.t_docKind = " + DLDOC_MEMORIALORDER
        + "\n" + "   AND pm.t_documentId = d.t_documentId"
        + "\n" + "   AND pm.t_purpose = " + PM_PURP_MEMORDER
        + "\n" + "   AND rm.t_paymentId = pm.t_paymentId"
        + "\n" + "   AND pm.t_valueDate BETWEEN " + getSqlDate(First_Date)
        + "\n" + "                          AND " + getSqlDate(Second_Date)
        + "\n" + "   AND d.t_documentId = i.t_documentId"
        + "\n" + "   AND i.t_docKind = " + DLDOC_MEMORIALORDER
        + "\n" + "   AND acc1.t_chapter = d.t_chapter"
        + "\n" + "   AND acc1.t_account = pm.t_payerAccount"
        + "\n" + "   AND acc2.t_chapter = d.t_chapter"
        + "\n" + "   AND acc2.t_account = pm.t_receiverAccount"
        + "\n" + "   AND (" + accountFilter.getAsSqlString("acc1")
        + "\n" + "        OR " + accountFilter.getAsSqlString("acc2")
        + "\n" + "       )"
        +            currencyFilter(DLDOC_MEMORIALORDER)
        +            chapterFilter(DLDOC_MEMORIALORDER)
        +            accountNumberFilter
        +            balanceNumberFilter
        +            correctionalKindFilter(DLDOC_MEMORIALORDER)
        +            userNumberFilter(DLDOC_MEMORIALORDER)
        + "\n" + "UNION ALL"
        + "\n" + "SELECT pm.t_paymentId           AS t_documentId,"
        + "\n" + "   " + DLDOC_BANKORDER + "      AS t_docKind,"
        + "\n" + "       pm.t_oper                AS t_oper,"
        + "\n" + "       pm.t_chapter             AS t_chapter,"
        + "\n" + "       CASE"
        + "\n" + "           WHEN pm.t_fiId = pm.t_payFiId"
        + "\n" + "           THEN pm.t_fiId"
        + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
        + "\n" + "            AND pm.t_fiId != " + NATCUR
        + "\n" + "           THEN pm.t_fiId"
        + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
        + "\n" + "            AND pm.t_fiId = " + NATCUR
        + "\n" + "           THEN pm.t_payFiId"
        + "\n" + "       END                      AS t_fiId,"
        + "\n" + "       CASE"
        + "\n" + "           WHEN pm.t_fiId = pm.t_payFiId"
        + "\n" + "           THEN pm.t_amount"
        + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
        + "\n" + "            AND pm.t_fiId != " + NATCUR
        + "\n" + "           THEN pm.t_amount"
        + "\n" + "           WHEN pm.t_fiId != pm.t_payFiId"
        + "\n" + "            AND pm.t_fiId = " + NATCUR
        + "\n" + "           THEN pm.t_payAmount"
        + "\n" + "       END                      AS t_sum,"
        + "\n" + "       pm.t_payerAccount        AS t_account_payer,"
        + "\n" + "       pm.t_receiverAccount     AS t_account_receiver,"
        + "\n" + "       pm.t_valueDate           AS t_date_carry,"
        + "\n" + "       pm.t_department          AS t_department,"
        + "\n" + "       rm.t_ground              AS t_ground,"
        + "\n" + "       rm.t_number              AS t_numb_document,"
        + "\n" + "       pm.t_typeDocument        AS t_type_document,"
        + "\n" + "       acc1.t_balance           AS t_balance_from,"
        + "\n" + "       acc2.t_balance           AS t_balance_to,"
        + "\n" + "       i.t_rateDate             AS t_rateDate,"
        + "\n" + "       i.t_srcDocKind           AS t_srcDocKind,"
        + "\n" + "       i.t_srcDocumentId        AS t_srcDocumentId,"
        + "\n" + "       i.t_accTrnId             AS t_accTrnId"
        + "\n" + "  FROM daccispr_dbt i,"
        + "\n" + "       daccount_dbt acc1,"
        + "\n" + "       daccount_dbt acc2,"
        + "\n" + "       dpmpaym_dbt pm,"
        + "\n" + "       dpmrmprop_dbt rm"
        + "\n" + " WHERE pm.t_docKind = " + DLDOC_BANKORDER
        + "\n" + "   AND pm.t_purpose IN (" + PM_PURP_BANKORDER + "," + PM_PURP_CBANKORDER + ")"
        + "\n" + "   AND rm.t_paymentId = pm.t_paymentId"
        + "\n" + "   AND pm.t_valueDate BETWEEN " + getSqlDate(First_Date)
        + "\n" + "                          AND " + getSqlDate(Second_Date)
        + "\n" + "   AND pm.t_paymentId = i.t_documentId"
        + "\n" + "   AND i.t_docKind = " + DLDOC_BANKORDER
        + "\n" + "   AND acc1.t_chapter = pm.t_chapter"
        + "\n" + "   AND acc1.t_account = pm.t_payerAccount"
        + "\n" + "   AND acc2.t_chapter = pm.t_chapter"
        + "\n" + "   AND acc2.t_account = pm.t_receiverAccount"
        + "\n" + "   AND (" + accountFilter.getAsSqlString("acc1")
        + "\n" + "        OR " + accountFilter.getAsSqlString("acc2")
        + "\n" + "       )"
        +            currencyFilter(DLDOC_BANKORDER)
        +            chapterFilter(DLDOC_BANKORDER)
        +            accountNumberFilter
        +            balanceNumberFilter
        +            correctionalKindFilter(DLDOC_BANKORDER)
        +            userNumberFilter(DLDOC_BANKORDER)
        + "\n" + "UNION ALL"
        + "\n" + "SELECT d.t_autokey                                                    AS t_documentId,"
        + "\n" + "   " + CB_MULTYDOC + "                                                AS t_docKind,"
        + "\n" + "       d.t_oper                                                       AS t_oper,"
        + "\n" + "       d.t_chapter                                                    AS t_chapter,"
        + "\n" + "       DECODE(pm.t_fiId, " + NATCUR + ", pm.t_payFiId, pm.t_fiId)     AS t_fiId,"
        + "\n" + "       DECODE(pm.t_fiId, " + NATCUR + ", pm.t_payAmount, pm.t_amount) AS t_sum,"
        + "\n" + "       pm.t_payerAccount                                              AS t_account_payer,"
        + "\n" + "       pm.t_receiverAccount                                           AS t_account_receiver,"
        + "\n" + "       pm.t_valueDate                                                 AS t_date_carry,"
        + "\n" + "       pm.t_department                                                AS t_department,"
        + "\n" + "       rm.t_ground                                                    AS t_ground,"
        + "\n" + "       rm.t_number                                                    AS t_numb_document,"
        + "\n" + "       d.t_type_document                                              AS t_type_document,"
        + "\n" + "       acc1.t_balance                                                 AS t_balance_from,"
        + "\n" + "       acc2.t_balance                                                 AS t_balance_to,"
        + "\n" + "       i.t_rateDate                                                   AS t_rateDate,"
        + "\n" + "       i.t_srcDocKind                                                 AS t_srcDocKind,"
        + "\n" + "       i.t_srcDocumentId                                              AS t_srcDocumentId,"
        + "\n" + "       i.t_accTrnId                                                   AS t_accTrnId"
        + "\n" + "  FROM dmultydoc_dbt d,"
        + "\n" + "       daccispr_dbt i,"
        + "\n" + "       daccount_dbt acc1,"
        + "\n" + "       daccount_dbt acc2,"
        + "\n" + "       dpmpaym_dbt pm,"
        + "\n" + "       dpmrmprop_dbt rm"
        + "\n" + " WHERE pm.t_docKind = " + CB_MULTYDOC
        + "\n" + "   AND pm.t_documentId = d.t_autokey"
        + "\n" + "   AND pm.t_purpose = " + PM_PURP_MULTYDOC
        + "\n" + "   AND rm.t_paymentId = pm.t_paymentId"
        + "\n" + "   AND pm.t_valueDate BETWEEN " + getSqlDate(First_Date)
        + "\n" + "                          AND " + getSqlDate(Second_Date)
        + "\n" + "   AND d.t_autokey = i.t_documentId"
        + "\n" + "   AND i.t_docKind = " + CB_MULTYDOC
        + "\n" + "   AND acc1.t_chapter = d.t_chapter"
        + "\n" + "   AND acc1.t_account = pm.t_payerAccount"
        + "\n" + "   AND acc2.t_chapter = d.t_chapter"
        + "\n" + "   AND acc2.t_account = pm.t_receiverAccount"
        + "\n" + "   AND (" + accountFilter.getAsSqlString("acc1")
        + "\n" + "        OR " + accountFilter.getAsSqlString("acc2")
        + "\n" + "       )"
        +            currencyFilter(CB_MULTYDOC)
        +            chapterFilter(CB_MULTYDOC)
        +            accountNumberFilter
        +            balanceNumberFilter
        +            correctionalKindFilter(CB_MULTYDOC)
        +            userNumberFilter(CB_MULTYDOC)
        + "\n" + "ORDER BY t_department,"
        + "\n" + "         t_chapter,"
        + "\n" + "         t_fiId,"
        + "\n" + "         t_date_carry,"
        + "\n" + "         t_numb_document"
        ;

    rs = TRsbDataset(correctionalDocumentsQueryText);

    rs.setFieldType("t_documentId", V_INTEGER);
    rs.setFieldType("t_docKind", V_INTEGER);
    rs.setFieldType("t_oper", V_INTEGER);
    rs.setFieldType("t_chapter", V_INTEGER);
    rs.setFieldType("t_fiId", V_INTEGER);
    rs.setFieldType("t_sum", V_MONEY);
    rs.setFieldType("t_account_payer", V_STRING);
    rs.setFieldType("t_account_receiver", V_STRING);
    rs.setFieldType("t_date_carry", V_DATE);
    rs.setFieldType("t_department", V_INTEGER);
    rs.setFieldType("t_ground", V_STRING);
    rs.setFieldType("t_numb_document", V_STRING);
    rs.setFieldType("t_type_document", V_STRING);
    rs.setFieldType("t_balance_from", V_STRING);
    rs.setFieldType("t_balance_to", V_STRING);
    rs.setFieldType("t_rateDate", V_DATE);
    rs.setFieldType("t_srcDocKind", V_INTEGER);
    rs.setFieldType("t_srcDocumentId", V_STRING);
    rs.setFieldType("t_accTrnId", V_INTEGER);

    PrintHeadOfReport1(Dep_Number, OrgStructure, First_Date, Second_Date);

    NextRecord = rs.moveNext;

/* вывод */
    if(NextRecord)

        Chapter_new = rs.chapter;
        FIID_new    = rs.fiId;

        Chapter_ = Chapter_new;
        FIID_    = FIID_new;

        rateDate = ternary(nvl(rs.rateDate, Date(0,0,0)) != Date(0,0,0), rs.rateDate, rs.date_carry);

        i = 0;
        while (i < correctKindSign.size)
            if (index(rs.value(12), correctKindSign[i]) != 0)
                blockSum[i] = rs.sum;
                ConvSum(blockSumR[i], rs.sum, ratedate, FIID_new);
                blockCount[i] = 1;
            else
                blockSum[i] = $0;
                blockSumR[i] = $0;
                blockCount[i] = 0;
            end;
            i = i + 1;
        end;

        blockSum[i] = rs.sum;
        ConvSum( blockSumR[i], blockSum[i], ratedate, FIID_new );
        blockCount[i] = 1;

        SumAll = $0;
        CountAll = 0;

        PrintOneBlokHead1(Report1, Chapter_, FIID_);
        PrintOneBlokCages1(Report1);
        PrintOneBlok1(Report1, rs, Show_Op, 0);

        flag=1;
        NextRecord = rs.moveNext;

        if(NextRecord)
            while( NextRecord )
                Chapter_new = rs.chapter;
                FIID_new    = rs.fiId;

                rateDate = ternary(nvl(rs.rateDate, Date(0,0,0)) != Date(0,0,0), rs.rateDate, rs.date_carry);

                if( (Chapter_ != Chapter_new) OR (FIID_ != FIID_new) )

                    Report1.PrintBottom;

                    PrintOneBlokPodval1(Report1, FIID_);

                    PrintOneBlokHead1(Report1, Chapter_new, FIID_new);

                    Report1.PrintHead;

                    flag=0;
                    Chapter_ = Chapter_new;
                    FIID_ = FIID_new;

                    /* Обнуление сумм и счетчика по блоку */
                    i = 0;
                    while (i < (correctKindSign.size + 1))
                        blockSum[i] = $0;
                        blockSumR[i] = $0;
                        blockCount[i] = 0;
                        i = i + 1;
                    end;

                    moreThenOneBlock = true;

                end;

                PrintOneBlok1(Report1,rs,Show_Op,flag);

                flag=1;
                i = 0;
                while (i < correctKindSign.size)
                    if (index(rs.value(12), correctKindSign[i]) != 0)

                        blockSum[i] = blockSum[i] + rs.sum;
                        ConvSum( @sumRub, rs.sum, ratedate, FIID_new );
                        blockSumR[i] = blockSumR[i] + sumRub;
                        blockCount[i] = blockCount[i] + 1;
                    end;
                    i = i + 1;
                end;

                blockSum[i] = blockSum[i] + Money(rs.value(5));
                ConvSum( @sumRub, Money(rs.value(5)), ratedate, FIID_new );
                blockSumR[i] = blockSumR[i] + sumRub;
                blockCount[i] = blockCount[i] + 1;

                NextRecord = rs.moveNext;

                if(NextRecord != true)

                    Report1.PrintBottom;
                    PrintOneBlokPodval1(Report1, FIID_);
                end;
            end;
        else

            Report1.PrintBottom;
            PrintOneBlokPodval1(Report1, FIID_); // T_DATE_CARRY
        end;

        if (moreThenOneBlock)
            PrintPodval1(Report1);
        end;
    else

        println(StrAlign("Нет данных для отчета", width, STR_ALIGN_CENTER));
    end;

    profiler.off();

    if ((fileName != null) and (ExistFile(reportBodyFileName, 0)))
        printOfstream.resetOutputFile();
    end;

    return true;
END;
