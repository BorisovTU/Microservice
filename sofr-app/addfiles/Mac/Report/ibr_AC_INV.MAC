/*
$Name:          ac_inv.mac 
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Инвентарная опись лицевых счетов"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ac_inv;

private const TAB_REPORT = "Параметры отчета";
private const TAB_EXTENDED = "Дополнительные параметры";

private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_EXTENDED = "Дополнительные параметры";

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report = facade.createReport("ibr_ac_inv", "Инвентарная опись лицевых счетов", "ibr_ac_inv.mac ", "ibrReportExecute", "ibrReportVerification");
    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();
    var tabExtended = facade.createTab(TAB_EXTENDED, 3);
    var groupExtended = facade.createGroup(GRP_EXTENDED, 1);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("Дата", "Дата"));
    
    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
   
    groupReport.add(facade.createFieldBalance("Балансовый", "Балансовый"));
    groupReport.add(facade.createFieldSort("Сортировка", "Сортировка", "91A", 1751));

    facade.resetFieldsOrder();
    groupExtended.add(facade.createFieldCheckBox("ВходящиеОстатки", "Входящие остатки", false));
    groupExtended.add(facade.createFieldCheckBox("НулевыеОстатки", "Нулевые остатки", false));
    groupExtended.add(facade.createFieldCheckBox("ЗакрытыеСчета", "Закрытые счета", false));
    groupExtended.add(facade.createFieldCheckBox("Апострофы", "С апострофами", false));

    tabReport.add(groupReport);
    tabExtended.add(groupExtended);

    report.add(tabReport);
    report.add(tabExtended);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";   

    var result: Object  = TRcbResult();
    var Pattern = strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1);
    var isShowOcp = false;
    var isWeb : bool = true;
    var j = 0;

    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );

    while (j < Pattern.size)
        if (Pattern[j] == "A")
            Pattern[j] = 10;
        end;
        j = j + 1;
    end;

    result.valueBoolean = ИнвВедомость(getValue(report, gRep+"Подразделения").valueInteger,
                                       getValue(report, gRep+"Структура").valueInteger,
                                       getValue(report, gRep+"Режим").valueInteger,
                                       TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),
                                       TIbrCommon().getIdentifierAccountChapter( getValue(report, gRep+"ГлаваСчетов").valueString),
                                       getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                       getValue(report, gRep+"Дата").valueDate,
                                       false, // группа операционистов
                                       getValue(report, gRep+"Операционист").valueInteger,
                                       getValue(report, gRep+"Балансовый").valueString,                                 
                                       Pattern,
                                       getValue(report, gExt+"ВходящиеОстатки").valueBoolean,
                                       getValue(report, gExt+"НулевыеОстатки").valueBoolean,
                                       getValue(report, gExt+"ЗакрытыеСчета").valueBoolean,
                                       getValue(report, gExt+"Апострофы").valueBoolean,
                                       not isShowOcp,
                                       isWeb);
    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(-1, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString) == -1)
        repErrorsQueue().add(0, "Недопустимая глава счетов");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;