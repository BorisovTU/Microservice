/*
$Name:          r_cmagz.mac
$Module:        Внутренняя отчетность
$Description:   Кассовый журнал
*/
/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *  Module  : cj_main.mac
 *  Purpose : Выпуск отчета "Кассовый журнал".
 *  Comment : Выпуск отчета "Кассовый журнал".
 *  @since    20.09.2010
 *  @author   Shestakov Dmitry
 *  @version  6.00.020.29
 */
import BankInter;
import Reporting;
import ofstream;
import rep_lib;
import RepParametersNames;
import RepOpenedOperdaysChecker;
import r_cmagz_parameters;
import r_cmagz_data;
import r_cmagz_print;

private var printOfstream = TOfstream("cashJournal");
var reportBodyFileName;

macro makeReport(departmentCode,     orgStructure,     issueMode,   currencyCode,
                 periodBeginingDate, periodEndingDate, operOrGroup, operNumber,
                 packNumber,         inspectorNumber,  journalForm, accountNumber,
                 groupingKind,       sortReport,       sortRecord,  needSpellSum, fileName)

    var operdaysPassed = null;
    if (genClassName(departmentCode) == "RepParameterCollection")
        var parameters = departmentCode;

        orgStructure = parameters.getValue(ORGANIZATION_STRUCTURE);
        issueMode = parameters.getValue(ISSUE_MODE);
        departmentCode = getDepartmentId(parameters.getValue(DEPARTMENT_CODE), orgStructure);
        currencyCode = getFiId(parameters.getValue("Currency"), parameters.getValue("OnlyForeignCurrency"));
        periodBeginingDate = parameters.getValue("BeginDate");
        periodEndingDate = parameters.getValue("EndDate");
        operOrGroup = parameters.getValue("isGroup");
        operNumber = parameters.getValue("Oper");
        packNumber = parameters.getValue("Pack");
        inspectorNumber = parameters.getValue("Inspector");
        journalForm = parameters.getValue("Form");
        accountNumber = parameters.getValue("Account");
        groupingKind = parameters.getValue("GroupingKind");
        sortReport = strSplit2(parameters.getValue("ReportsOrder"), 1, true);
        sortRecord = strSplit2(parameters.getValue("DocsOrder"), 1, true);
        needSpellSum = ternary(parameters.getValue("AmountsInWords"), 88, 0);

        operdaysPassed = parameters.getValue("$OperdaysPassed");
    end;

    /*инициализация параметров отчета*/
    cjParameters(departmentCode,     orgStructure,     issueMode,   currencyCode,
                 periodBeginingDate, periodEndingDate, operOrGroup, operNumber,
                 packNumber,         inspectorNumber,  journalForm, accountNumber,
                 groupingKind,       sortReport,       sortRecord,  needSpellSum);

    if (not TCJData().isAccountsExists())
        return NO_ACCOUNTS;
    end;

    /* Контроль открытых опердней */
    if (operdaysPassed == null)
        operdaysPassed = checkOpenedOperdays(cjParameters().getDepartmentList(), cjParameters().getDateIn(), cjParameters().getDateOut());
    end;
    if (not operdaysPassed)
        return OPERDAY_NOT_PASSED;
    end;

    var retVal;

    if (fileName != null)
        printOfstream = TOfstream(fileName);
    end;

    begAction(1000, "Выпускается отчет. Ждите...");
        retVal = TCashJournalPrint(printOfstream).execute();
    endAction();

    reportBodyFileName = printOfstream.getFileName();
    RepTxtFilesQueue().add(reportBodyFileName, false);
    printOfstream.resetOutputFile();

    return retVal;
end;

macro showReport()
    printOfstream.show();
end;

macro getReportBodyFileName()
    return reportBodyFileName;
end;
