/*
$Name:          ibr_prncorr.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Некорректные проводки по валютным счетам"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import prncorr;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const LOG_FILE_NAME = "ibr_prncorr_web";

private var m_llClass : integer  = 1767;

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade    = TIbrConstructorFacade();
    var report    = facade.createReport("ibr_cltnoacc", "Некорректные проводки по валютным счетам", "ibr_prncorr.mac", "ibrReportExecute", "ibrReportVerification");  

    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCheckBox("ИсклГлавы", "С искл. главами", false));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта"));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));
    
    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    
    groupReport.add(facade.createFieldSort("Сортировка", "Сортировка", "", 1767));
    
    tabReport.add(groupReport);

    report.add(tabReport);
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";

    var result: Object  = TRcbResult();
    var Pattern = strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1);
    var i : integer = 0;
    var chaptersClose;

    while (i < Pattern.size)
        Pattern[i] = TIbrCommon().getElementLlValues(Pattern[i], m_llClass);
        i = i + 1;
    end;

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"Подразделения").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),
                                          TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),
                                          ternary(getValue(report, gRep+"ИсклГлавы").valueBoolean, 1, 0),
                                          getFiId(getValue(report, gRep+"Валюта").valueString),
                                          getValue(report, gRep+"НачалоПериода").valueDate,
                                          getValue(report, gRep+"КонецПериода").valueDate,
                                          0, // группа операционист не используется
                                          getValue(report, gRep+"Операционист").valueInteger,
                                          Pattern,
                                          LOG_FILE_NAME);
    // При удачном выполнении
   if (result.valueBoolean != TRUE)
        repErrorsQueue().add(-1, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString) == -1)
        repErrorsQueue().add(0, "Недопустимая глава счетов");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;