/*
$Name:          repoperday.mac
$Module:        Отчетность
$Description:   Протокол открытых опер. дней
*/

/**
*  RS-Bank 6.0                                           R-Style Software Lab
*
*  File Name   : repoperday.mac                                June 16, 2010
*  Programmer  : ABP
*  Description : Протокол открытых опер. дней
*  Comment     :
*  Modify      :
*/

import Reporting;
import treport;
import Ofstream;

private const LOG_FILE_NAME = "RepOpenedOperdaysLog";

private var instance = null;
/**
 * Класс протокола контроля незакрытых опердней
 *
 * @since   v.6.20.030.37
 * @author  ABP
 * @version 1.0
 */
private class TRepOperdaysOpenedLogImpl(operdaysOpened : Object)
    private const MAX_PHASE_COLUMN_WIDTH = 50;

    private var m_operdaysOpened : RepOperdaysOpened;
    private var m_table : CTableReport;
    private var m_ofstream : TOfstream;

    private macro getPhaseName(str)
        if (strlen(str) > MAX_PHASE_COLUMN_WIDTH)
            return string(substr(str, 1, MAX_PHASE_COLUMN_WIDTH-3), "...");
        else
            return str;
        end;
    end;

    private macro printHeader()
        m_table.printHead("Внимание! В отчет попадают данные по незакрытым операционным дням.\n");
    end;

    private macro printLine(data)
        m_table.printString(String(data.operDate : f),
                            data.getBranchName(),
                            getPhaseName(data.getMinPhaseName()),
                            getPhaseName(data.getMaxPhaseName())
                           );
    end;

    private macro printFooter()
        m_table.printBottom();
    end;

    macro create()
        var data = RepOperdaysOpenedRecord();
        m_ofstream.clear();
        m_ofstream.setOutputFile();

        printHeader();

        while (m_operdaysOpened.getNext(data))
            printLine(data);
        end;

        printFooter();

        m_ofstream.resetOutputFile();
    end;

    macro getFileName()
        return m_ofstream.getFileName();
    end;

    macro getRepOperdaysOpenedObject()
        return m_operdaysOpened;
    end;

    macro setRepOperdaysOpenedObject(obj : RepOperdaysOpened)
        m_operdaysOpened = obj;
    end;

    private macro constructorTRepOperdaysOpenedLogImpl(operdaysOpened : Object)
        m_operdaysOpened = operdaysOpened;
        m_table = CTableReport();

        m_table.addColumn("Дата",                                Double(10),                     AL_CENTER);
        m_table.addColumn("Отделение",                           Double(12),                     AL_LEFT);
        m_table.addColumn("Минимальная фаза операционного дня",  Double(MAX_PHASE_COLUMN_WIDTH), AL_LEFT);
        m_table.addColumn("Максимальная фаза операционного дня", Double(MAX_PHASE_COLUMN_WIDTH), AL_LEFT);

        m_ofstream = TOfstream(LOG_FILE_NAME, false);
    end;

    constructorTRepOperdaysOpenedLogImpl(operdaysOpened);
end;

macro TRepOperdaysOpenedLog(operdaysOpened : Object)
    if ((instance != null) and (instance.getRepOperdaysOpenedObject() == null))
        instance.setRepOperdaysOpenedObject(operdaysOpened);
    end;

    if (instance == null)
        instance = TRepOperdaysOpenedLogImpl(operdaysOpened);
    end;

    return instance;
end;

macro createLog(operdaysOpened : Object)
    TRepOperdaysOpenedLog(operdaysOpened).create();
end;

macro getLogFileName() : String
    return TRepOperdaysOpenedLog(null).getFileName();
end;
