/*
$Name:          repException.mac
$Module:        Отчетность
$Description:   Обработка исключений
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл проекта RS-Reporting

  Обработка исключений

  Создан: 26.07.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

import Reporting;

private const DELIMITER = "$DELIMITER$";

/**
 *  Базовый класс исключений. ВСЕ классы исключений должны наследоваться от него.
 *  Comment: Пользовательское сообщение предваряется символом заданным в константе DELIMITER.
 *           Это необходимо для того, чтоб обойти приписывание инструментом к сообщению об ошибке
 *           строки "Error:"
 */
class TException(errorMessage : String, needContext : Bool)

    private var m_errorMessage : String = errorMessage;

    private var m_needContext : Bool;

    if (valType(needContext) != V_BOOL)
        m_needContext = false;
    else
        m_needContext = needContext;
    end;

    macro what()
        if ((m_errorMessage == null) or (m_errorMessage == ""))
            return "";
        else
            return DELIMITER + m_errorMessage;
        end;
    end;

    macro context()
        return m_needContext;
    end;

end;

/**
 *  Вывод сообщения о исключительной ситуации
 */
macro exceptionMessage(error)

    var i,
        str = "";

    i = index(error.message, DELIMITER);
    if (i > 0)
        str = subStr(error.message, i + strLen(DELIMITER));
    end;

    if (str == "")
        str = error.message + "|Модуль: " + error.module + ", Строка: " + error.line;
    else
        if (isEqClass("TException", error.err))
            if (error.err.context())
                str = str + "|Модуль: " + error.module + ", Строка: " + error.line;
            end;
        end;
    end;

    RepErrorsQueue().add(0, string(str));
    msgBox(str);

    return str;

end;

/**
 *  Функция-генератор исключений
 */
macro throw(exception : Variant)

    if (isEqClass("TException", exception))
        RepErrorsQueue().add(0, string(exception.what()));
        runError(exception.what(), exception);
    else
        RepErrorsQueue().add(0, string(exception));
        runError(string(exception));
    end;

end;

/**
 *  Вызов чистого виртуального метода
 */
class (TException) TPureVirtualMethodCallException(methodName : String)
    initTException("Вызов чистого виртуального метода: " + methodName, true);
end;

/**
 *  Неверное количество параметров
 */
class (TException) TWrongParametersCountException(methodName : String)
    initTException("Неверное количество параметров в методе: " + methodName, true);
end;

/**
 * Переменная имеет неопределнное значение
 */
class (TException) TVariableIsNotDefined(nameVariable: String)
    initTException("Неопределённое значение переменной: " + nameVariable, true);
end;

/**
 * Неверное количество фактических параметров
 */
class (TException) TInvalidNumberOfParametersException(methodName : String)
    initTException("Неверное количество фактических параметров при вызове " + methodName, true);
end;

/**
 * Неопределенное значение параметра
 */
class (TException) TUnspecifiedValueException(methodName : String, parmName : String)
    initTException("Неопределенное значение параметра " + parmName + " при вызове " + methodName, true);
end;

/**
 * Неверный тип параметра
 */
class (TException) TIncorrectParameterTypeException(methodName : String, parmName : String)
    initTException("Неверный тип параметра " + parmName + " при вызове " + methodName, true);
end;

/**
 * Неверное значение параметра
 */
class (TException) TInvalidValueException(methodName : String, parmName : String)
    initTException("Неверное значение параметра " + parmName + " при вызове " + methodName, true);
end;
