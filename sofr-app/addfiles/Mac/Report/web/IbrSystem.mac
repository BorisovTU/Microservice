/*
$Name: IbrSystem.mac
$Module: Отчеты банка
$Description: Общеподсистемные функции
*/

/**
 * Класс общих функций подсистемы
 */
class TIbrSystem

    /**
     * Форматирование строки.
     * Маркеты вида {0},{1},{2} и так далее заменяются передаваемыми параметрами
     *
     * @param Первый параметр - исходная строка
     * @param Переменный список параметров - подставляемые значения
     *
     * @return String Результирующая строка
     */
    macro stringFormat(): String

        /**
         * Счетчик параметров
         * Первый параметр - исходная строка
         */
        var counter: Integer = 2;

        /**
         * Значение параметра
         */
        var value: Variant = NULL;

        /**
         * Результат - форматированная строка
         */
        var result: String = "";

        getParm(1, value);

        if (valType(value) != V_STRING)

            rcbRunError(TIbrResource.exception.INCORRECT_PARAMETER_PART + "TIbrSystem::stringFormat");

        end;

        result = value;

        while (getParm(counter, value))

            result  = strSubSt(result, "{" + (counter - 2) + "}", String(value));
            counter = counter + 1;

        end;

        return result;

    end;

    /**
     * Проверить объкт на пустое или неопределённое значение
     *
     * @param value Variant Проверяемое значение
     *
     * @return Bool TRUE, если значение пустое или неопределённое,
               иначе - FALSE
     */
    macro isNullOrEmpty(value: Variant): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        if ((value == NULL) OR
            (valType(value) == V_UNDEF))

            result = TRUE;

        end;

        if ((valType(value) == V_STRING) AND
            (value == ""))

            result = TRUE;

        end;

        return result;

    end;


    /**
     * Преобразовать логический тип в sql-представление
     *
     * @param isTrue Bool Объект логического типа
     *
     * @return String Логический тип в sql-представлении
     */
    macro sqlChar(isTrue: Bool): String

        /**
         * Результат
         */
        var result: String = "";

        isTrue AND (result = TIbrConstant.system.SQL_TRUE);
        isTrue OR  (result = TIbrConstant.system.SQL_FALSE);

        return result;

    end;

    /**
     * Преобразовать sql-представление логического типа в rsl-представление
     *
     * @param sqlChar String sql-представление логического типа
     *
     * @return TRUE, если sql-представление истинно, иначе - FALSE.
     */
    macro rslBoolean(sqlChar: String): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        result = result AND (NOT isNullOrEmpty(sqlChar));
        result = result AND (strUpr(sqlChar) == "X");

        return result;

    end;

    /**
     * Установить признак запуска из веб-интерфейса
     */
    macro setIsWeb()

        replaceMacro("rcbWebCommon_isWebSystem", "rcbWebCommon_getTrue" );

    end;

    /**
     * Признак запуска из веб-интефрейса
     *
     * @return True, если работаем из веб-интефрейса, иначе - False.
     */
    macro isWeb(): Bool

        return rcbWebCommon_isWebSystem();

    end;

end;
