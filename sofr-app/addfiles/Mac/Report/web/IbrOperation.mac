/*
$Name: IbrOperation.mac
$Module: Отчеты банка
$Description: Универсальная панель. Операции отчета
*/

/**
 * Класс операций отчета
 * Функциональный класс
 */
class TIbrOperation()

    /**
     * Получить список операций по идентификатору отчета
     *
     * @param identifierReport Integer Идентификатор отчета
     * @param report Object Отчет
     *
     * @return Список операций
     */
    macro getData(identifierReport: Integer, report: @Object): TArray

        if (identifierReport == NULL)

            throw_web(TIbrUndefinedValueException("IbrReport_getData", "identifierReport"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: TArray  = TArray();

        /**
         * Текущая операция
         */
        var currentOperation: Object = NULL;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().operation.column;

        query =  "SELECT ibrOperation.t_identifierOperation AS " + constantColumn.IDENTIFIER_OPERATION + ","
        + "\n" + "       ibrOperation.t_type                AS " + constantColumn.TYPE                 + ","
        + "\n" + "       ibrOperation.t_name                AS " + constantColumn.NAME                 + ","
        + "\n" + "       ibrOperation.t_macroFile           AS " + constantColumn.MACRO_FILE           + ","
        + "\n" + "       ibrOperation.t_service             AS " + constantColumn.SERVICE_OPERATION    + ","
        + "\n" + "       ibrOperation.t_description         AS " + constantColumn.DESCRIPTION          + ","
        + "\n" + "       ibrOperation.t_sortWeight          AS " + constantColumn.SORT_WEIGHT          + ","
        + "\n" + "       ibrOperation.t_isUser              AS " + constantColumn.IS_USER
        + "\n" + "  FROM dWebIbrOperation_dbt ibrOperation"
        + "\n" + " WHERE ibrOperation.t_identifierReport = " + identifierReport;

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType(constantColumn.IDENTIFIER_OPERATION, V_INTEGER);
        dataSet.setFieldType(constantColumn.TYPE,                 V_INTEGER);
        dataSet.setFieldType(constantColumn.NAME,                 V_STRING);
        dataSet.setFieldType(constantColumn.MACRO_FILE,           V_STRING);
        dataSet.setFieldType(constantColumn.SERVICE_OPERATION,    V_STRING);
        dataSet.setFieldType(constantColumn.DESCRIPTION,          V_STRING);
        dataSet.setFieldType(constantColumn.SORT_WEIGHT,          V_INTEGER);
        dataSet.setFieldType(constantColumn.IS_USER,              V_STRING);

        while (dataSet.moveNext())

            currentOperation = TIbrOperationWeb();
            currentOperation.initialize(dataSet);

            if (NOT TIbrSystem.isWeb())

                currentOperation.report = report;

            end;

            result.value(result.size) = currentOperation;

        end;

        return result;

    end;

    /**
     * Заполнить занчениями из источника данных
     *
     * @param operation TIbrOperationWeb Операция
     * @param dataSet Object Набор данных
     */
    macro initialize(operation: TIbrOperationWeb, dataSet: Object)

        if (operation == NULL)

            throw_web(TIbrUndefinedValueException("TIbrOperation::initialize", "operation"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrOperation::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().operation.column;

        operation.identifierOperation = execExp("dataSet." + constantColumn.IDENTIFIER_OPERATION);
        operation.type                = execExp("dataSet." + constantColumn.TYPE);
        operation.name                = execExp("dataSet." + constantColumn.NAME);
        operation.macroFile           = execExp("dataSet." + constantColumn.MACRO_FILE);
        operation.serviceOperation    = execExp("dataSet." + constantColumn.SERVICE_OPERATION);
        operation.description         = execExp("dataSet." + constantColumn.DESCRIPTION);
        operation.sortWeight          = execExp("dataSet." + constantColumn.SORT_WEIGHT);
        operation.isUser = TIbrSystem().rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Установить порядок сортировки
     *
     * @param operation TIbrOperationWeb Операция
     * @param sortWeightParameter Integer Порядок сортировки
     *
     * @return True, если порядок сортировки изменён успешно, иначе - False.
     */
    macro setSortWeight(operation: TIbrOperationWeb, sortWeightParameter: Integer): Bool

        if (operation == NULL)

            throw_web(TIbrIncorrectParameterException("TIbrOperationWeb::setSortWeight", "operation"));

        end;

        if (sortWeightParameter == NULL)

            throw_web(TIbrIncorrectParameterException("TIbrOperationWeb::setSortWeight", "sortWeightParameter"));

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().setSortWeight
        (
            TIbrConstant.operation.TABLE_NAME,
            TIbrConstant.operation.PRIMARY_KEY,
            operation.identifierOperation,
            sortWeightParameter
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.CHANGE_SORTWEIGHT_OPERATION,
                    operation.name,
                    operation.sortWeight,
                    sortWeightParameter
                )
            );

            operation.sortWeight = sortWeightParameter;

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_CHANGE_SORTWEIGHT_OPERATION,
                        sortWeightParameter,
                        operation.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Удаление операции
     *
     * @param operation TIbrOperationWeb Операция
     *
     * @return True, если операция удалена, иначе - False
     */
    macro delete(operation: TIbrOperationWeb): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().delete
        (
            TIbrConstant.operation.TABLE_NAME,
            TIbrConstant.operation.PRIMARY_KEY,
            operation.identifierOperation
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_OPERATION,
                    operation.name
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_OPERATION,
                        operation.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Получить операцию по наименованию
     *
     * @param identifierReport Integer Идентификатор отчета
     * @param operationName String Наименование операции
     *
     * @return Object Объект операции
     */
    macro getOperation(identifierReport: Integer, operationName: String): Object

        if (TIbrSystem.isNullOrEmpty(operationName))

            throw_web(TIbrUndefinedValueException("TIbrOperation::getOperation", "operationName"));

        end;

        /**
         * Результат - объект операции
         */
        var result: Object = NULL;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT ibrOperation.t_identifierOperation,"
        + "\n" + "       ibrOperation.t_type,"
        + "\n" + "       ibrOperation.t_name,"
        + "\n" + "       ibrOperation.t_macroFile,"
        + "\n" + "       ibrOperation.t_service,"
        + "\n" + "       ibrOperation.t_description,"
        + "\n" + "       ibrOperation.t_sortWeight,"
        + "\n" + "       ibrOperation.t_isUser"
        + "\n" + "  FROM dWebIbrOperation_dbt ibrOperation"
        + "\n" + " WHERE ibrOperation.t_identifierReport = " + identifierReport
        + "\n" + "   AND UPPER(ibrOperation.t_name) = UPPER('" + operationName + "')";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_identifierOperation", V_INTEGER);
        dataSet.setFieldType("t_type",                V_INTEGER);
        dataSet.setFieldType("t_name",                V_STRING);
        dataSet.setFieldType("t_macroFile",           V_STRING);
        dataSet.setFieldType("t_service",             V_STRING);
        dataSet.setFieldType("t_description",         V_STRING);
        dataSet.setFieldType("t_sortWeight",          V_INTEGER);
        dataSet.setFieldType("t_isUser",              V_INTEGER);

        if (dataSet.moveNext())

            result = TIbrOperationWeb();
            result.identifierOperation = dataSet.identifierOperation;
            result.type                = dataSet.type;
            result.name                = dataSet.name;
            result.macroFile           = dataSet.macroFile;
            result.serviceOperation    = dataSet.service;
            result.description         = dataSet.description;
            result.sortWeight          = dataSet.sortWeight;
            result.isUser = TIbrSystem.rslBoolean(dataSet.isUser);

        else

            throw_web(TIbrException(TIbrResource().message.OPERATION_NOT_FOUND));

        end;

        return result;

    end;

    /**
     * Изменить параметры операции
     *
     * @param oldOperationWeb TIbrOperationWeb Текущие (старые) параметры отчета
     * @param newOperationWeb TIbrOperationWeb Новые параметры отчета
     *
     * @return TRUE, если значения изменены успешно, иначе - FASLE
     */
    macro update(oldOperationWeb: TIbrOperationWeb, newOperationWeb: TIbrOperationWeb): Bool

        if (oldOperationWeb == NULL)

            throw_web
            (
                TIbrUndefinedValueException
                (
                    "TIbrOperation::update",
                    "oldOperationWeb"
                )
            );

        end;

        if (newOperationWeb == NULL)

            throw_web
            (
                TIbrUndefinedValueException
                (
                    "TIbrOperation::update",
                    "newOperationWeb"
                )
            );

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        /**
         * sql-выражение установки нового значения
         */
        var sqlSet: String = "";

        /**
         * Ошибка сообщения
         */
        var errorMessage: String = "";

        if (oldOperationWeb.type != newOperationWeb.type)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_type = " + newOperationWeb.type;

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.type,
                newOperationWeb.type
            )

        end;

        if (oldOperationWeb.name != newOperationWeb.name)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_name = '" + newOperationWeb.name + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.name,
                newOperationWeb.name
            )

        end;

        if (oldOperationWeb.macroFile != newOperationWeb.macroFile)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_macroFile = '" + newOperationWeb.macroFile + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.macroFile,
                newOperationWeb.macroFile
            )

        end;

        if (oldOperationWeb.serviceOperation != newOperationWeb.serviceOperation)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_service = '" + newOperationWeb.serviceOperation + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.serviceOperation,
                newOperationWeb.serviceOperation
            )

        end;

        if (oldOperationWeb.description != newOperationWeb.description)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_description = '" + newOperationWeb.description + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.description,
                newOperationWeb.description
            )

        end;

        if (oldOperationWeb.sortWeight != newOperationWeb.sortWeight)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_sortWeight = " + newOperationWeb.sortWeight;

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_OPERATION_MESSAGE,
                "тип",
                oldOperationWeb.sortWeight,
                newOperationWeb.sortWeight
            )

        end;

        result = TIbrBase().update
        (
            TIbrConstant.operation.TABLE_NAME,
            TIbrConstant.operation.PRIMARY_KEY,
            oldOperationWeb.identifierOperation,
            sqlSet
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.UPDATE_OPERATION,
                    oldOperationWeb.name
                )
            );
            printLn(errorMessage);

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_UPDATE_OPERATION,
                        oldOperationWeb.name
                    )
                )
            );

        end;

        return result;

    end;

end;
