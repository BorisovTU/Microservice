/*
$Name: IbrConstructorGroup.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Группы
*/

/**
 * Группы отчета
 * Используются для формирования групп панели отчета
 *
 * @param nameParameter String Наименование
 * @param sortWeightParameter Integer Порядок сортирвоки
 * @param isUserParameter Bool Признак пользовательской вкладки
 */
class (TIbrConstructorBase) TIbrConstructorGroup
(
    nameParameter:       String,
    sortWeightParameter: Integer,
    isUserParameter:     Bool
)
    /**
     * Данные группы
     */
    private var group: Object = NULL;

    /**
     * Идентификатор отчета
     */
    private var identifierReport: Integer = 0;

    /**
     * Идентификатор вкладки
     */
    private var identifierTab: Integer = 0;

    /**
     * Идентификатор группы
     */
    private var identifierGroup: Integer = 0;

    /**
     * Список полей
     */
    var fieldList: TArray = TArray();

    /**
     * Проверка данных
     */
    private macro check(): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        if ((identifierReport == NULL) OR
            (identifierReport == 0))

            objectException = TIbrException(TIbrResource.message.NO_IDENTIFIER_REPORT);
            result = FALSE;

        else

            result = TRUE;

        end;

        if ((identifierTab == NULL) OR
            (identifierTab == 0))

            objectException = TIbrException(TIbrResource.message.NO_IDENTIFIER_TAB);
            result = FALSE;

        else

            result = TRUE;

        end;

        return result;

    end;

    /**
     * Установить идентификатор отчета
     */
    macro setIdentifierReport(identifierReportParameter: Integer)

        identifierReport = identifierReportParameter;

    end;

    /**
     * Установить идентификатор родителя (вкладки)
     */
    macro setIdentifierTab(identifierTabParameter: Integer)

        identifierTab = identifierTabParameter;

    end;

    /**
     * Выполнить группу (фактически - сохранение группы в БД)
     *
     * @result True, если группы добавлена успешно, иначе - False.
     */
    private macro runGroup(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool  = FALSE;

        identifierGroup = getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_GROUP);

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierDecoration,"
        + "\n" + "             t_identifierParent,"
        + "\n" + "             t_identifierReport,"
        + "\n" + "             t_type,"
        + "\n" + "             t_name,"
        + "\n" + "             t_sortWeight,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + identifierGroup                    + ","
        + "\n" + "             "  + identifierTab                      + ","
        + "\n" + "             "  + identifierReport                   + ","
        + "\n" + "             "  + TIbrConstant.decoration.type.GROUP + ","
        + "\n" + "             '" + group.name                         + "',"
        + "\n" + "             "  + group.sortWeight                   + ","
        + "\n" + "             "  + TIbrSystem.sqlChar(group.isUser)   + ")";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение группы
     *
     * @return Bool Результат выполнения
     */
    macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        /**
         * Поле
         */
        var field: Object = NULL;

        result = result AND (check()    OR outputError());
        result = result AND (runGroup() OR outputError());
        result = result AND printDoneMessage();

        for (field, fieldList)

            field.setIdentifierGroup(identifierGroup);
            field.run();

        end;

        return result;

    end;

    /**
     * Применить (сохранить)
     */
    macro commit()

        run();

    end;

    /**
     * Добавление составляющих
     *
     * @param field Object Поле (тип TIbrConstructorField),
     */
    macro add(field: Object)

        if (isEqClass("TIbrConstructorField", field))

            fieldList[fieldList.size] = field;

        else

            throw_web(TIbrIncorrectParameterException("TIbrConstructorGroup::add"));

        end;

    end;

    macro get()
        group.fieldList.size = 0;
        for (var field, fieldList)
            var dto = field.get();
            group.fieldList[group.fieldList.size] = dto;
        end;
        return group;
    end;

    /**
     * Конструктор класса
     *
     * @param nameParameter String Наименование
     * @param sortWeightParameter Integer Порядок сортирвоки
     * @param isUserParameter Bool Признак пользовательской операции
     */
    private macro constructorTIbrConstructorGroup
    (
        nameParameter:       String,
        sortWeightParameter: Integer,
        isUserParameter:     Bool
    )

        if (TIbrSystem.isNullOrEmpty(nameParameter))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorGroup::constructorTIbrConstructorGroup"
                )
            );

        end;

        nameTable    = TIbrConstant.decoration.TABLE_NAME;
        nameKeyField = TIbrConstant.decoration.PRIMARY_KEY;

        doneMessage = TIbrResource.message.GROUP_CREATED_SUCCESSFULLY;
        objectName  = nameParameter;

        group = TIbrDecorationWeb();
        group.type       = TIbrConstant().decoration.type.GROUP;
        group.name       = nameParameter;
        group.sortWeight = NVL(sortWeightParameter, 0);
        group.isUser     = NVL(isUserParameter, TRUE);

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorGroup(nameParameter, sortWeightParameter, isUserParameter);

end;
