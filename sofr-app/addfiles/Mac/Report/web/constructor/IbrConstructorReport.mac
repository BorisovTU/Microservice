/*
$Name: IbrConstructorReport.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Отчет
*/

import IbrConstructorBase;

/**
 * Конструктор отчета
 *
 * @param nameParameter String Наименование (уникальное)
 * @param nameDisplayedParameter String Отображаемое наименование
 * @param macroFileParameter String Макрофайл
 * @param serviceExecuteParameter String Сервис выполнения
 * @param serviceValidateParameter String Сервис валидации
 * @param isUserParameter Bool Признак пользовательского отчета
 */
class (TIbrConstructorBase) TIbrConstructorReport
    (
        nameParameter:            String,
        nameDisplayedParameter:   String,
        macroFileParameter:       String,
        serviceExecuteParameter:  String,
        serviceValidateParameter: String,
        isUserParameter:          Bool
    )

    /**
     * Объект отчета
     */
    private var report: Object = NULL;

    /**
     * Список операций
     */
    private var operationList: TArray = TArray();

    /**
     * Список вкладок
     */
    private var tabList: TArray = TArray();

    /**
     * Идентификатор отчета
     */
    private var identifierReport: Integer = 0;

    /**
     * Добавление отчета
     */
    private macro addReport(reportParameter: Object)

        if (isEqClass("TIbrReportWeb", reportParameter))

            report = reportParameter;

        else

            throw_web(TIbrIncorrectParameterException("TIbrConstructorReport::addReport"));

        end;

    end;

    /**
     * Добавить операцию
     *
     * @param operation TIbrConstructorOperation Объект операции
     */
    private macro addOperation(operation: Object)

        operationList[operationList.size] = operation;

    end;

    /**
     * Добавить вкладку
     *
     * @param item TIbrConstructorTab Объект вкладки
     */
    private macro addTab(tab: Object)

        tabList[tabList.size] = tab;

    end;

    /**
     * Добавление составляющих
     *
     * @param item Object Либо операция (тип TIbrConstructorOperation),
     *        либо вкладка (тип TIbrConstructorTab)
     */
    macro add(item: Object)

        if (isEqClass("TIbrConstructorOperation", item))


            addOperation(item);

        else

            if (isEqClass("TIbrConstructorTab", item))

                addTab(item);

            else

                throw_web(TIbrIncorrectParameterException("TIbrConstructorReport::add"));

            end;

        end;

    end;

    /**
     * Проверка данных
     */
    private macro check(): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        // Проверка на уникальность имени отчета
        result = isUnique(report.name);

        if (NOT result)

            objectException = TIbrException(TIbrResource.message.NOT_UNIQUE_NAME_REPORT);

        end;

        return result;

    end;

    /**
     * Выполнить отчет (фактически - сохранение отчета в БД).
     */
    private macro runReport(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        identifierReport =
            getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_REPORT);

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierReport,"
        + "\n" + "             t_name,"
        + "\n" + "             t_nameDisplayed,"
        + "\n" + "             t_macroFile,"
        + "\n" + "             t_serviceExecute,"
        + "\n" + "             t_serviceValidate,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + identifierReport       + ","
        + "\n" + "             '" + report.name            + "',"
        + "\n" + "             '" + report.nameDisplayed   + "',"
        + "\n" + "             '" + report.macroFile       + "',"
        + "\n" + "             '" + report.serviceExecute  + "',"
        + "\n" + "             '" + report.serviceValidate + "',"
        + "\n" + "             " + TIbrSystem.sqlChar(report.isUser) + ")";

        result = sql_execute(query);

        if (NOT result)

            objectException = TIbrException(TIbrResource.message.ERROR_CREATE_REPORT)

        end;

        return result;

    end;


    /**
     * Выполнить
     */
    private macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        /**
         * Операция
         */
        var operation: Object = NULL;

        /**
         * Вкладка
         */
        var tab: Object = NULL;

        result = result AND (check()     OR outputError());
        result = result AND (runReport() OR outputError());
        result = result AND printDoneMessage();

        for (operation, operationList)

            operation.setIdentifierReport(identifierReport);
            result = result AND operation.run(identifierReport);

        end;

        for (tab, tabList)

            tab.setIdentifierReport(identifierReport);
            result = result AND tab.run();

        end;

        return result;

    end;

    /**
     * Применение\сохранение
     */
    macro commit(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        result = result AND (beginTransaction()  OR outputError());
        result = result AND (run()               OR outputError());
        result = result AND (commitTransaction() OR outputError());

        // В случае ошибки
        result = result OR rollbackTransaction() OR outputError();

        return result;

    end;

    /**
     * Получить сконструированный объект отчета
     */
    macro get() : Object
        var dto;
        report.decorationList.size = 0;
        for (var tab, tabList)
            dto = tab.get();
            report.decorationList[report.decorationList.size] = dto;
        end;

        report.operationList.size = 0;
        for (var operation, operationList)
            dto = operation.get();
            report.operationList[report.operationList.size] = dto;
        end;

        return report;
    end;

    /**
     * Конструктор класса
     *
     * @param nameParameter String Наименование (уникальное)
     * @param nameDisplayedParameter String Отображаемое наименование
     * @param macroFileParameter String Макрофайл
     * @param serviceExecuteParameter String Сервис выполнения
     * @param serviceValidateParameter String Сервис валидации
     * @param isUserParameter Bool Признак пользовательского отчета
     */
    private macro constructorTIbrConstructorReport
    (
        nameParameter:            String,
        nameDisplayedParameter:   String,
        macroFileParameter:       String,
        serviceExecuteParameter:  String,
        serviceValidateParameter: String,
        isUserParameter:          Bool
    )

        if ((TIbrSystem.isNullOrEmpty(nameParameter))           OR
            (TIbrSystem.isNullOrEmpty(macroFileParameter))      OR
            (TIbrSystem.isNullOrEmpty(serviceExecuteParameter)) )

            throw_web(TIbrIncorrectParameterException("TIbrConstructorReport::constructorTIbrConstructorReport"));

        end;

        /**
         * Объект отчета
         */
        var report: Object = TIbrReportWeb();


        nameTable    = TIbrConstant.report.TABLE_NAME;
        nameKeyField = TIbrconstant.report.PRIMARY_KEY;

        doneMessage = TIbrResource.message.REPORT_CREATED_SUCCESSFULLY;

        if (TIbrSystem.isNullOrEmpty(nameDisplayedParameter))

            report.nameDisplayed = nameParameter;

        else

            report.nameDisplayed = nameDisplayedParameter;

        end;

        objectName  = report.nameDisplayed;

        report.name            = nameParameter;
        report.macroFile       = macroFileParameter;
        report.serviceExecute  = serviceExecuteParameter;
        report.serviceValidate = serviceValidateParameter;
        report.isUser          = NVL(isUserParameter, TRUE);

        addReport(report);

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorReport
    (
        nameParameter,
        nameDisplayedParameter,
        macroFileParameter,
        serviceExecuteParameter,
        serviceValidateParameter,
        isUserParameter
    );

    onError(errorObject)
        rollbackTransaction();
        rcbRunError(errorObject());

end;
