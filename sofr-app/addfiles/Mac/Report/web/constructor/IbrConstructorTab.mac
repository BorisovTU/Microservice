/*
$Name: IbrConstructorTab.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Вкладки
*/

/**
 * Вкладки отчета
 * Используются для формирования вкладок панели отчета
 *
 * @param nameParameter String Наименование
 * @param sortWeightParameter Integer Порядок сортирвоки
 * @param isUserParameter Bool Признак пользовательской вкладки
 */
class (TIbrConstructorBase) TIbrConstructorTab
(
    nameParameter:       String,
    sortWeightParameter: Integer,
    isUserParameter:     Bool
)

    /**
     * Данные вкладки
     */
    private var tab: Object = NULL;

    /**
     * Идентификатор отчета
     */
    private var identifierReport: Integer = 0;

    /**
     * Идентификатор вкладки
     */
    private var identifierTab: Integer = 0;

    /**
     * Список групп
     */
    var groupList: TArray = TArray();

    /**
     * Проверка данных
     */
    private macro check(): Bool

        if ((identifierReport == NULL) OR
            (identifierReport == 0))

            objectException = TIbrException(TIbrResource.message.NO_IDENTIFIER_REPORT);
            return FALSE;

        else

            return TRUE;

        end;

    end;

    /**
     * Установить идентификатор отчета
     */
    macro setIdentifierReport(identifierReportParameter: Integer)

        identifierReport = identifierReportParameter;

    end;

    /**
     * Выполнить вкладку (фактически - сохранение вкладки в БД)
     *
     * @result True, если вкладка добавлена успешно, иначе - False.
     */
    macro runTab(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool  = FALSE;

        identifierTab = getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_TAB);

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierDecoration,"
        + "\n" + "             t_identifierParent,"
        + "\n" + "             t_identifierReport,"
        + "\n" + "             t_type,"
        + "\n" + "             t_name,"
        + "\n" + "             t_sortWeight,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + identifierTab                        + ","
        + "\n" + "             0,"
        + "\n" + "             "  + identifierReport                     + ","
        + "\n" + "             "  + TIbrConstant.decoration.type.TAB     + ","
        + "\n" + "             '" + tab.name                             + "',"
        + "\n" + "             "  + tab.sortWeight                       + ","
        + "\n" + "             "  + TIbrSystem.sqlChar(tab.isUser) + ")";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение вкладки
     *
     * @return Bool Результат выполнения
     */
    macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        /**
         * Группа
         */
        var group: Object = NULL;

        result = result AND (check()  OR outputError());
        result = result AND (runTab() OR outputError());
        result = result AND printDoneMessage();

        for (group, groupList)

            group.setIdentifierReport(identifierReport);
            group.setIdentifierTab(identifierTab);
            result = result AND (group.run());

        end;

        return result;

    end;

    /**
     * Применить (сохранить)
     */
    macro commit()

        run();

    end;

    /**
     * Добавление составляющих
     *
     * @param group Object Группа (тип TIbrConstructorGroup),
     */
    macro add(group: Object)

        if (isEqClass("TIbrConstructorGroup", group))

            groupList[groupList.size] = group;

        else

            throw_web(TIbrIncorrectParameterException("TIbrConstructorTab::add"));

        end;

    end;

    macro get()
        tab.children.size = 0;
        for (var group, groupList)
            var dto = group.get();
            tab.children[tab.children.size] = dto;
        end;
        return tab;
    end;

    /**
     * Конструктор класса
     *
     * @param nameParameterString Наименование
     * @param sortWeightParameter Integer Порядок сортирвоки
     * @param isUserParameter Bool Признак пользовательской операции
     */
    private macro constructorTIbrConstructorTab
    (
        nameParameter:       String,
        sortWeightParameter: Integer,
        isUserParameter:     Bool
    )

        if (TIbrSystem.isNullOrEmpty(nameParameter))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorTab::constructorTIbrConstructorTab"
                )
            );

        end;

        nameTable    = TIbrConstant.decoration.TABLE_NAME;
        nameKeyField = TIbrConstant.decoration.PRIMARY_KEY;

        doneMessage = TIbrResource.message.TAB_CREATED_SUCCESSFULLY;
        objectName  = nameParameter;

        tab = TIbrDecorationWeb();
        tab.type       = TIbrConstant().decoration.type.TAB;
        tab.name       = nameParameter;
        tab.sortWeight = NVL(sortWeightParameter, 0);
        tab.isUser     = NVL(isUserParameter, TRUE);

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorTab(nameParameter, sortWeightParameter, isUserParameter);

end;
