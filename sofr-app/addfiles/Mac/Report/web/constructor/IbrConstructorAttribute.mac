/*
$Name: IbrConstructorAttribute.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Атрибут
*/

/**
 * Атрибуты поля
 * Используются для формирования контрола поля
 *
 * @param nameParameter String Наименование
 * @param valueParameter Variant Значение
 * @param isUserParameter Bool Признак пользовательской операции
 */
class (TIbrConstructorBase) TIbrConstructorAttribute
    (
        nameParameter:   String,
        valueParameter:  Variant,
        isUserParameter: Bool
    )

    /**
     * Данные атрибута
     */
    var attribute: Object = NULL;

    /**
     * Идентификатор поля
     */
    var identifierField: Integer = 0;

    /**
     * Идентификатор атрибута
     */
    var identifierAttribute: Integer = 0;

    /**
     * Проверка данных
     */
    private macro check(): Bool

        return TRUE;

    end;

    /**
     * Выполнить атрибут (фактически - сохранение атрибута в БД).
     *
     * @result True, если атрибут добавлен успешно, иначе - False.
     */
    private macro runAttribute(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = FALSE;

        identifierAttribute = getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_ATTRIBUTE);

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierAttribute,"
        + "\n" + "             t_identifierField,"
        + "\n" + "             t_nameParameter,"
        + "\n" + "             t_valueParameter,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + identifierAttribute      + ","
        + "\n" + "             "  + identifierField          + ","
        + "\n" + "             '" + attribute.nameParameter  + "',"
        + "\n" + "             "  + getSqlString(attribute.valueParameter) + ","
        + "\n" + "             "  + TIbrSystem.sqlChar(attribute.isUser)
        + "\n" + "             )";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение атрибута
     *
     * @param identifierFieldParameter Integer Идентификатор поля
     *
     * @return Bool Результат выполнения
     */
    macro run(identifierFieldParameter: Integer): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        identifierField = identifierFieldParameter;

        result = result AND (check()        OR outputError());
        result = result AND (runAttribute() OR outputError());
        result = result AND printDoneMessage();

        return result;

    end;

    /**
     * Установить порядок сортировки
     */
    macro setSortWeight(identifierObject, sortWeight)

        throw_web(TIbrException("Не поддерживается"));

    end;

    macro get()
        return attribute;
    end;

    /**
     * Конструктор класса
     *
     * @param nameParameter String Наименование
     * @param valueParameter Variant Значение
     * @param isUserParameter Bool Признак пользовательского атрибута
     */
    private macro constructorTIbrConstructorAttribute
    (
        nameParameter:   String,
        valueParameter:  Variant,
        isUserParameter: Bool
    )

        if (TIbrSystem.isNullOrEmpty(nameParameter))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorAttribute::constructorTIbrConstructorAttribute"
                )
            );

        end;

        nameTable    = TIbrConstant.attribute.TABLE_NAME;
        nameKeyField = TIbrConstant.attribute.PRIMARY_KEY;

        doneMessage = TIbrResource.message.ATTRIBUTE_CREATED_SUCCESSFULLY;
        objectName  = nameParameter;

        attribute = TIbrAttributeWeb();
        attribute.nameParameter  = nameParameter;
        attribute.isUser         = NVL(isUserParameter, TRUE);


        if (valueParameter != null)
            if (valType(valueParameter) == V_BOOL)
                attribute.valueParameter = ternary(valueParameter, "X", "");
            else
                attribute.valueParameter = String(valueParameter);
            end;
        else
            attribute.valueParameter = valueParameter;
        end;

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorAttribute(nameParameter, valueParameter, isUserParameter);


end;
