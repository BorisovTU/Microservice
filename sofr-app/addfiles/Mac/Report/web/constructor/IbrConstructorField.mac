/*
$Name: IbrConstructorField.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Поле
*/

import IbrConstructorValue;

/**
 * Поле отчета
 * Используются для формирования полей
 *
 * @param nameParameter String Наименование
 * @param sortWeightParameter Integer Порядок сортирвоки
 * @param isUserParameter Bool Признак пользовательского поля
 * @param valueDefaultParameter Variant Значение по умолчанию
 */
class (TIbrConstructorBase) TIbrConstructorField
(
    nameParameter:         String,
    sortWeightParameter:   Integer,
    isUserParameter:       Bool,
    valueDefaultParameter: Variant
)

    /**
     * Объект поля
     */
    var field: Object = NULL;

    /**
     * Идентификатор группы
     */
    var identifierGroup: Integer = 0;

    /**
     * Значение по умолчанию
     */
    var valueDefault: Variant = NULL;

    /**
     * Объект конструктора значения для поля по умполчанию
     */
    var valueConstructor: Object = NULL;

    /**
     * Список атрибутов
     */
    private var attributeList: TArray = TArray();

    /**
     * Проверка данных
     */
    private macro check(): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        // Проверка на уникальность имени отчета
        result = isUnique(field.name);

        if (NOT result)

            objectException = TIbrException(TIbrResource.message.NOT_UNIQUE_NAME_FIELD);

        end;

        if ((identifierGroup == NULL) OR
            (identifierGroup == 0))

            objectException = TIbrException(TIbrResource.message.NO_IDENTIFIER_GROUP);
            result = FALSE;

        else

            result = TRUE;

        end;

        return result;

    end;

    /**
     * Установить идентификатор группы
     */
    macro setIdentifierGroup(identifierGroupParameter: Integer)

        identifierGroup = identifierGroupParameter;

    end;

    /**
     * Выполнить поле по умолчанию (фактически - сохранение поля в БД)
     *
     * @result True, если поле добавлено успешно, иначе - False.
     */
    private macro runField(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool  = FALSE;

        field.identifierField = getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_FIELD);

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierField,"
        + "\n" + "             t_identifierValue,"
        + "\n" + "             t_identifierValueDefault,"
        + "\n" + "             t_identifierDecoration,"
        + "\n" + "             t_name,"
        + "\n" + "             t_sortWeight,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES (" + field.identifierField + ","
        + "\n" + "             '',"
        + "\n" + "             "  + valueConstructor.getIdentifier() + ","
        + "\n" + "             "  + identifierGroup                  + ","
        + "\n" + "             '" + field.name                       + "',"
        + "\n" + "             "  + field.sortWeight                 + ","
        + "\n" + "             "  + TIbrSystem.sqlChar(field.isUser) + ")";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение значения
     *
     * @return Bool Результат выполнения
     */
    macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        /**
         * Атрибут
         */
        var attribute: Object = NULL;

        valueConstructor.run();

        result = result AND (check()    OR outputError());
        result = result AND (runField() OR outputError());
        result = result AND printDoneMessage();

        for (attribute, attributeList)

            result = result AND attribute.run(field.identifierField);

        end;

        return result;

    end;

    /**
     * Применить (сохранить)
     */
    macro commit()

        run();

    end;

   /**
     * Добавление составляющих
     *
     * @param attribute Object Поле (тип TIbrConstructorAttribute),
     */
    macro add(attribute: Object)

        if (isEqClass("TIbrConstructorAttribute", attribute))

            attributeList[attributeList.size] = attribute;

        else

            throw_web(TIbrIncorrectParameterException("TIbrConstructorAttribute::add"));

        end;

    end;

    macro get()
        field.attributeList.size = 0;
        for (var attribute, attributeList)
            var dto = attribute.get();
            field.attributeList[field.attributeList.size] = dto;
        end;
        field.value = TIbrValueWeb();
        field.valueDefault = valueConstructor.get();

        return field;
    end;

    /**
     * Конструктор класса
     *
     * @param nameParameter String Наименование
     * @param sortWeightParameter Integer Порядок сортирвоки
     * @param isUserParameter Bool Признак пользовательского поля
     * @param valueDefaultParameter Variant Значение по умолчанию
     */
    private macro constructorTIbrConstructorField
    (
        nameParameter:         String,
        sortWeightParameter:   Integer,
        isUserParameter:       Bool,
        valueDefaultParameter: Variant
    )

        if (TIbrSystem.isNullOrEmpty(nameParameter))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorField::constructorTIbrConstructorField"
                )
            );

        end;

        nameTable    = TIbrConstant.field.TABLE_NAME;
        nameKeyField = TIbrConstant.field.PRIMARY_KEY;

        doneMessage = TIbrResource.message.FIELD_CREATED_SUCCESSFULLY;
        objectName  = nameParameter;

        field = TIbrFieldWeb();
        field.name             = nameParameter;
        field.sortWeight       = NVL(sortWeightParameter, 0);
        field.isUser           = NVL(isUserParameter, TRUE);

        valueDefault = TIbrValueWeb();
        valueDefault.valueString  = "";
        valueDefault.valueBoolean = FALSE;
        valueDefault.valueInteger = 0;
        valueDefault.valueDate    = Date(0, 0, 0);
        valueDefault.isUser       = TRUE;

        /**
         * Признак успешного выполнения
         */
        var isDone: Bool = TRUE;

        if (valType(valueDefaultParameter) == V_STRING)

            valueDefault.valueString = valueDefaultParameter;

        end;

        if (valType(valueDefaultParameter) == V_INTEGER)

            valueDefault.valueInteger = valueDefaultParameter;

        end;

        if (valType(valueDefaultParameter) == V_DATE)

            valueDefault.valueDate = valueDefaultParameter;

        end;

        if (valType(valueDefaultParameter) == V_BOOL)

            valueDefault.valueBoolean = valueDefaultParameter;

        end;

        if ((valType(valueDefaultParameter) == V_GENOBJ) AND
            (isEqClass("TIbrValueWeb", valueDefaultParameter)))

            valueDefault = valueDefaultParameter;

        end;

        valueConstructor = TIbrConstructorValue(valueDefault);

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorField
    (
        nameParameter,
        sortWeightParameter,
        isUserParameter,
        valueDefaultParameter
    );

end;
