/*
$Name: IbrConstructorValue.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Значение
*/

/**
 * Значение по умолчанию для поля
 * Используются для формирования значения по умолчанию для поля
 *
 * @param valueDefault Object Значение по умолчанию
 */
class (TIbrConstructorBase) TIbrConstructorValue(valueDefault: Object)

    /**
     * Объект значения
     */
    private var value: Object = NULL;

    /**
     * Проверка данных
     */
    private macro check(): Bool

        return TRUE;

    end;

    /**
     * Получить идентификатор
     */
    macro getIdentifier()

        return value.identifierValue;

    end;

    /**
     * Выполнить значение по умолчанию (фактически - сохранение значения в БД)
     *
     * @result True, если значение добавлено успешно, иначе - False.
     */
    private macro runValue(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool  = FALSE;

        value.identifierValue = getNextIdentifier(TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_VALUE);

        var day, month, year;

        dateSplit(value.valueDate, day, month, year);

        if ((day == 0) AND (month == 0) AND (year == 0))

            day = month = year = 1;

        end;

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierValue,"
        + "\n" + "             t_valueString,"
        + "\n" + "             t_valueBoolean,"
        + "\n" + "             t_valueInteger,"
        + "\n" + "             t_valueDate,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + value.identifierValue                  + ","
        + "\n" + "             '" + value.valueString                      + "',"
        + "\n" + "             "  + TIbrSystem.sqlChar(value.valueBoolean) + ","
        + "\n" + "             "  + value.valueInteger                     + ","
        + "\n" + "             TO_DATE('" + day + "," + month + "," + year + "', 'DD.MM.YYYY'),"
        + "\n" + "             "  + TIbrSystem.sqlChar(value.isUser)       + ")";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение значения
     *
     * @return Bool Результат выполнения
     */
    macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        result = result AND (check()    OR outputError());
        result = result AND (runValue() OR outputError());
        result = result AND printDoneMessage();

        return result;

    end;

    macro get()
        return value;
    end;

    /**
     * Конструктор класса
     *
     * @param valueDefault Object Значение по умолчанию
     */
    private macro constructorTIbrConstructorValue(valueDefault: Object)

        if ((TIbrSystem.isNullOrEmpty(valueDefault)) OR
            (NOT isEqClass("TIbrValueWeb", valueDefault)))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorValue::constructorTIbrConstructorValue"
                )
            );

        end;

        nameTable    = TIbrConstant.value.TABLE_NAME;
        nameKeyField = TIbrConstant.value.PRIMARY_KEY;

        doneMessage = TIbrResource.message.VALUE_CREATED_SUCCESSFULLY;

        value = TIbrValueWeb();
        value.valueString  = NVL(valueDefault.valueString,  "");
        value.valueBoolean = NVL(valueDefault.valueBoolean, FALSE);
        value.valueInteger = NVL(valueDefault.valueInteger, 0);
        value.valueDate    = NVL(valueDefault.valueDate,    Date(2, 1, 1) - 1);
        value.isUser       = NVL(valueDefault.isUser,       FALSE);

        objectName  = " string ("  + String(value.valueString)  + ")" +
                      " boolean (" + String(value.valueBoolean) + ")" +
                      " integer (" + String(value.valueInteger) + ")" +
                      " date ("    + String(value.valueDate)    + ")";

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorValue(valueDefault);

end;
