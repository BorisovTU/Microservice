/*
$Name: IbrConstructorOperation.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Операции
*/

/**
 * Операции отчета
 * Используются для формирования панели инструментов
 *
 * @param typeParameter Integer Тип
 * @param nameParameter String Наименование
 * @param macroFileParameter String Макрофайл
 * @param serviceOperationParameter String Сервис операции
 * @param descriptionParameter String Описание
 * @param sortWeightParameter Integer Порядок сортирвоки
 * @param isUserParameter Bool Признак пользовательской операции
 */
class (TIbrConstructorBase) TIbrConstructorOperation
    (
        typeParameter:             Integer,
        nameParameter:             String,
        macroFileParameter:        String,
        serviceOperationParameter: String,
        descriptionParameter:      String,
        sortWeightParameter:       Integer,
        isUserParameter:           Bool
    )

    /**
     * Данные операции
     */
    var operation: Object = NULL;

    /**
     * Идентификатор отчета
     */
    var identifierReport: Integer = 0;

    /**
     * Идентификатор операции
     */
    var identifierOperation: Integer = 0;

    /**
     * Проверка данных
     */
    private macro check(): Bool

        if ((identifierReport == NULL) OR
            (identifierReport == 0))

            objectException = TIbrException(TIbrResource.message.NO_IDENTIFIER_REPORT);
            return FALSE;

        else

            return TRUE;

        end;

    end;

    /**
     * Установить идентификатор отчета
     */
    macro setIdentifierReport(identifierReportParameter: Integer)

        identifierReport = identifierReportParameter;

    end;

    /**
     * Выполнить операцию (фактически - сохранение операции в БД).
     *
     * @result True, если операция добавлена успешно, иначе - False.
     */
    private macro runOperation(): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = FALSE;

        identifierOperation = getNextIdentifier
        (
            TIbrResource.message.ERROR_GET_NEXT_IDENTIFIER_OPERATION
        );

        query =  "INSERT INTO " + nameTable
        + "\n" + "            (t_identifierOperation,"
        + "\n" + "             t_identifierReport,"
        + "\n" + "             t_type,"
        + "\n" + "             t_name,"
        + "\n" + "             t_macroFile,"
        + "\n" + "             t_service,"
        + "\n" + "             t_description,"
        + "\n" + "             t_sortWeight,"
        + "\n" + "             t_isUser)"
        + "\n" + "     VALUES ("  + identifierOperation        + ","
        + "\n" + "             "  + identifierReport           + ","
        + "\n" + "             "  + operation.type             + ","
        + "\n" + "             '" + operation.name             + "',"
        + "\n" + "             '" + operation.macroFile        + "',"
        + "\n" + "             '" + operation.serviceOperation + "',"
        + "\n" + "             '" + operation.description      + "',"
        + "\n" + "             "  + operation.sortWeight       + ","
        + "\n" + "             " + TIbrSystem.sqlChar(operation.isUser)
        + "\n" + "             )";

        result = sql_execute(query);

        return result;

    end;

    /**
     * Выполнение операции
     *
     * @return Bool Результат выполнения
     */
    macro run(): Bool

        /**
         * Результат
         */
        var result: Bool = TRUE;

        result = result AND (check()        OR outputError());
        result = result AND (runOperation() OR outputError());
        result = result AND printDoneMessage();

        return result;

    end;

    /**
     * Применить (сохранить)
     */
    macro commit()

        run();

    end;

    macro get()
        return operation;
    end;

    /**
     * Конструктор класса
     *
     * @param typeParameter Integer Тип
     * @param nameParameter String Наименование
     * @param macroFileParameter String Макрофайл
     * @param serviceOperationParameter String Сервис операции
     * @param descriptionParameter String Описание
     * @param sortWeightParameter Integer Порядок сортирвоки
     * @param isUserParameter Bool Признак пользовательской операции
     */
    private macro constructorTIbrConstructorOperation
    (
        typeParameter:             Integer,
        nameParameter:             String,
        macroFileParameter:        String,
        serviceOperationParameter: String,
        descriptionParameter:      String,
        sortWeightParameter:       Integer,
        isUserParameter:           Bool
    )

        if ((TIbrSystem.isNullOrEmpty(typeParameter)) OR
            (TIbrSystem.isNullOrEmpty(nameParameter)) OR
            (TIbrSystem.isNullOrEmpty(macroFileParameter)) OR
            (TIbrSystem.isNullOrEmpty(serviceOperationParameter)))

            rollbackTransaction();

            throw_web
            (
                TIbrIncorrectParameterException
                (
                    "TIbrConstructorOperation::constructorTIbrConstructorOperation"
                )
            );

        end;

        nameTable    = TIbrConstant.operation.TABLE_NAME;
        nameKeyField = TIbrConstant.operation.PRIMARY_KEY;

        doneMessage = TIbrResource.message.OPERATION_CREATED_SUCCESSFULLY;
        objectName  = nameParameter;

        operation = TIbrOperationWeb();
        operation.type             = typeParameter;
        operation.name             = nameParameter;
        operation.macroFile        = macroFileParameter;
        operation.serviceOperation = serviceOperationParameter;
        operation.description      = NVL(descriptionParameter, "");
        operation.sortWeight       = NVL(sortWeightParameter, 0);
        operation.isUser           = NVL(isUserParameter, TRUE);

    end;

    // Вызов конструктора класса
    constructorTIbrConstructorOperation
    (
        typeParameter,
        nameParameter,
        macroFileParameter,
        serviceOperationParameter,
        descriptionParameter,
        sortWeightParameter,
        isUserParameter
    );

end;
