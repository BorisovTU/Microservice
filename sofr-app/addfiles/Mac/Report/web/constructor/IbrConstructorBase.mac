/*
$Name: IbrConstructorBase.mac
$Module: Отчеты банка
$Description: Конструктор отчета. Базовый класс
*/

/**
 * Базовый класс конструктора
 */
class TIbrConstructorBase()

    /**
     * Список sql-выражений
     */
    private var queryList: TArray = TArray();

    /**
     * Список объектов
     */
    private var objectList: TArray = TArray();

    /**
     * Объект исключения
     */
    private var objectException: Object = NULL;

    /**
     * Наименование таблицы
     */
    private var nameTable: String = "";

    /**
     * Наименование ключевого поля
     */
    private var nameKeyField: String = "";

    /**
     * Сообщение успешного добаления объекта
     */
    private var doneMessage: String = "";

    /**
     * Наименование объекта
     */
    private var objectName: String = "";

    /**
     * Начало транзакции
     */
    private macro beginTransaction(): Bool

        RslDefCon.BeginTrans();

        return TRUE;

    end;

    /**
     * Фиксация изменения
     */
    private macro commitTransaction(): Bool

        RslDefCon.CommitTrans();

        return TRUE;

    end;

    /**
     * Откатить изменения
     */
    private macro rollbackTransaction(): Bool

        if (RslDefCon.isInTrans())

            RslDefCon.RollbackTrans();

        end;

        return TRUE;

    end;

    /**
     * Добавить элемент
     */
    macro add(item: Object)

        rollbackTransaction();
        throw_web(TIbrPureVirtualMethodCallException("TIbrConstructorBase::add"));

    end;

    /**
     * Проверка
     */
    macro check()

        rollbackTransaction();
        throw_web(TIbrPureVirtualMethodCallException("TIbrConstructorBase::check"));

    end;

    /**
     * Выполнить
     */
    macro run(): Bool

        rollbackTransaction();
        throw_web(TIbrPureVirtualMethodCallException("TIbrConstructorBase::run"));

    end;

    /**
     * Напечатать собщение об успешном добавлении Объекта
     */
    private macro printDoneMessage(): Bool

        printLn
        (
            TIbrSystem.stringFormat
            (
                doneMessage,
                objectName
            )
        );

        return TRUE;

    end;

    /**
     * Вывести сообщение об ошибке
     */
    private macro outputError()

        if (NOT TIbrSystem.isNullOrEmpty(objectException))

            rollbackTransaction();
            throw_web(objectException);

        end;

    end;

    /**
     * Получить идентификатор отчета, для добавления в БД
     *
     * @param errorMessage String Сообщение об ошибке
     *
     * @exception
     *
     * @return Integer Идентификатор таблицы, с которым можно добавить отчет в БД
     */
    private macro getNextIdentifier(errorMessage: String): Integer

        if (TIbrSystem.isNullOrEmpty(errorMessage))

            rollbackTransaction();
            throw_web(TIbrIncorrectParameterException("IbrConstructorBase::getNextIdentifier"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: Integer = 0;

        query =  "SELECT NVL(MAX(ibrTable." + nameKeyField + ") + 1, 1) AS t_nextIdentifier"
        + "\n" + "  FROM " + nameTable + " ibrTable";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_nextIdentifier", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.nextIdentifier;

        else

            rollbackTransaction();
            throw_web(TIbrException(errorMessage));

        end;

        return result;

    end;


    /**
     * Проверить существование объекта
     *
     * @param nameObject String Значение наименования поля, по которому проверяем
     *
     */
    private macro isUnique(nameObject: String): Bool

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        query =  "SELECT ibrReport.*"
        + "\n" + "  FROM " + nameTable + " ibrReport"
        + "\n" + " WHERE ibrReport.t_name = '" + nameObject + "'";

        dataSet = TRsbDataSet(query);

        if (dataSet.moveNext())

            result = FALSE;

        else

            result = TRUE;

        end;

        return result;

    end;

end;
