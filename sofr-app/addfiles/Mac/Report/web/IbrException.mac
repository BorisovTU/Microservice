/*
$Name: IbrException.mac
$Module: Отчеты банка
$Description: Исключения
*/

/**
 * Базовый класс исключения
 */
class TIbrException(errorMessageParameter: String)

    /**
     * Сообщение исключения
     */
    private var errorMessage: String = "";

    /**
     * Конструктор класса
     */
    private macro constructorIbrException(errorMessageParameter: String)

        if (errorMessageParameter == NULL)

            rcbRunError
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.exception.INCORRECT_PARAMETER,
                    "IbrException::constructorIbrException"
                )
            );

        end;

        errorMessage = errorMessageParameter;

    end;

    /**
     * Получить сообщение об ошибке
     */
    macro getMessage(): String

        return errorMessage;

    end;

    // Вызов конструктора класса
    constructorIbrException(errorMessageParameter);

end;

/**
 * Функция генерирования ошибки
 */
macro throw_web(exception: Variant)

    if (exception == NULL)

        rcbRunError
        (
            TIbrSystem.stringFormat
            (
                TIbrResource.exception.INCORRECT_PARAMETER,
                "throw_web"
            )
        );

    end;

    if (isEqClass("TIbrException", exception))

        rcbRunError(exception.getMessage());

    else

        rcbRunError(String(exception));

    end;

end;

/**
 * Неопределённое значение параметра
 *
 * @param methodName Наименование метода
 * @param parameterName Наименование параметра
 */
class (TIbrException) TIbrUndefinedValueException(methodName: String, parameterName: String)

    initTIbrException
    (
        TIbrSystem.stringFormat
        (
            TIbrResource.exception.UNDEFINED_VALUE,
            parameterName,
            methodName
        )
    );

end;

/**
 * Вызов чистого виртуального метода
 *
 * @param methodName Наименование метода
 */
class (TIbrException) TIbrPureVirtualMethodCallException(methodName: String)

    initTIbrException
    (
        TIbrSystem.stringFormat
        (
            TIbrResource.exception.PURE_VIRTUAL_METHOD_CALL,
            methodName
        )
    );

end;

/**
 * Некорректные параметры
 *
 * @param methodName Наименование метода
 */
class (TIbrException) TIbrIncorrectParameterException(methodName: String)

    initTIbrException
    (
        TIbrSystem.stringFormat
        (
            TIbrResource.exception.INCORRECT_PARAMETER,
            methodName
        )
    );

end;
