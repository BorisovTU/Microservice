/*
$Name: IbrDataSource.mac
$Module: Отчеты банка
$Description: Источники данных
*/

import IbrImport;

const SCHEDULE_KIND_CALCULATION: Integer = 1;
const SCHEDULE_KIND_ACCRUAL:     Integer = 2;
const SCHEDULE_KIND_PAYMENT:     Integer = 3;

/**
 * Получить план счетов
 */
macro ibrDataSource_getChartOfAccounts(): TArray

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    /**
     * Метаинформация
     */
    var metaList: TArray = TArray();

    /**
     * Данные
     */
    var dataList: TArray = TArray();

    query =  "SELECT accountPlan.t_iNumPlan AS t_identifierPlan,"
    + "\n" + "       accountPlan.t_namePlan AS t_namePlan"
    + "\n" + "  FROM dNamePlan_dbt accountPlan";

    dataSet = TRsbDataSet(query);

    while (dataSet.moveNext())

        dataList[dataList.size] = TIbrData(dataSet.identifierPlan, dataSet.namePlan);

    end;

    metaList[metaList.size] = TIbrMeta(TIbrResource().plan.LABEL_IDENTIFIER, "identifierPlan");
    metaList[metaList.size] = TIbrMeta(TIbrResource().plan.LABEL_NAME, "namePlan").setBinding();

    result.initialize(metaList, dataList);

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Получить данные справочника llvalues
 * Специфика ориентирована на поле сортировки
 *
 * @param classificator Integer Либо наименование, либо идентификатор классификатора
 *
 * @return TArray of TIbrDynamicSource Список данных справочника
 */
macro ibrDataSource_getLlValues(classificator: Variant): TArray

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    /**
     * Метаинформация
     */
    var metaList: TArray = TArray();

    /**
     * Данные
     */
    var dataList: TArray = TArray();

    /**
     * Условие sql-выражения
     */
    var where: String = "";

    if (valType(classificator) == V_INTEGER)

        where = string(classificator);

    else

        if (valType(classificator) == V_STRING)

            where =
              "\n" + "        (SELECT t_classificator "
            + "\n" + "          FROM dllclass_dbt"
            + "\n" + "         WHERE t_shortName = '" + classificator + "')";

        else

            where = string(0);

        end;

    end;

    query =  "SELECT t_code, t_name "
    + "\n" + "  FROM dllvalues_dbt"
    + "\n" + " WHERE t_list = 2902"
    + "\n" + "   AND t_element IN "
    + "\n" + "    (SELECT t_element "
    + "\n" + "      FROM dLlClsVal_dbt"
    + "\n" + "     WHERE t_classificator = " + where
    + "\n" + "        )";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_code", V_STRING);
    dataSet.setFieldType("t_name", V_STRING);

    while (dataSet.moveNext())

        dataList[dataList.size] = TIbrData(dataSet.code, dataSet.name);

    end;

    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_CODE, "code").setBinding();
    metaList[metaList.size] = TIbrMeta(TIbrConstant().llvalues.LABEL_NAME, "name");

    result.initialize(metaList, dataList);

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Получить список процентных договоров
 *
 * @return TArray of TIbrDynamicSource Список процентных договоров
 */
macro ibrDataSource_getPercentContract(): TArray

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    /**
     * Метаинформация
     */
    var metaList: TArray = TArray();

    /**
     * Данные
     */
    var dataList: TArray = TArray();

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_NUMBER,
        "identifierPercent"
    ).setBinding();

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_ACCOUNT,
        "accountPercent"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_CLIENT,
        "clientPercent"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_DATE_BEGIN,
        "dateBeginPercent"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_DATE_END,
        "dateEndPercent"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().percentContract.LABEL_DATE_CLOSE,
        "dateClosePercent"
    );

    query =  "SELECT percentContract.t_number    AS t_number,"
    + "\n" + "       percentContract.t_account   AS t_account,"
    + "\n" + "       party.t_shortName           AS t_client,"
    + "\n" + "       percentContract.t_beginDate AS t_dateBegin,"
    + "\n" + "       percentContract.t_endDate   AS t_dateEnd,"
    + "\n" + "       percentContract.t_closeDate AS t_dateClose"
    + "\n" + "  FROM dprccontract_dbt percentContract,"
    + "\n" + "       dparty_dbt party"
    + "\n" + " WHERE percentContract.t_Client = party.t_PartyId(+)";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_number",    V_INTEGER);
    dataSet.setFieldType("t_account",   V_STRING);
    dataSet.setFieldType("t_client",    V_STRING);
    dataSet.setFieldType("t_dateBegin", V_DATE);
    dataSet.setFieldType("t_dateEnd",   V_DATE);
    dataSet.setFieldType("t_dateClose", V_DATE);

    while (dataSet.moveNext())

        dataList[dataList.size] = TIbrData
        (
            dataSet.number,
            dataSet.account,
            dataSet.client,
            dataSet.dateBegin,
            dataSet.dateEnd,
            dataSet.dateClose
        );

    end;

    result.initialize(metaList, dataList);

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Получить данные графика по указанному виду
 *
 * @param kind Integer Вид графика
 *
 * @return TArray of TIbrData Массив данных графиков
 */
private macro getScheduleData(kind: Integer): TArray

    /**
     * Данные
     */
    var dataList: TArray = TArray();

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    query =  "SELECT percentSchedule.t_number         AS t_number,"
    + "\n" + "       percentSchedule.t_shortName      AS t_name,"
    + "\n" + "       groupPercentSchedule.t_shortName AS t_groupName,"
    + "\n" + "       percentSchedule.t_openDate       AS t_openDate,"
    + "\n" + "       percentSchedule.t_closeDate      AS t_closeDate"
    + "\n" + "  FROM dPrcSched_dbt percentSchedule,"
    + "\n" + "       dPrcGrSched_dbt groupPercentSchedule"
    + "\n" + " WHERE percentSchedule.t_ScheduleKind = " + SCHEDULE_KIND_ACCRUAL
    + "\n" + "   AND percentSchedule.t_groupScheduleId = groupPercentSchedule.t_groupScheduleId(+)"
    + "\n" + "   AND (percentSchedule.t_IsUser <> 'X' "
    + "\n" + "        OR percentSchedule.t_IsUser IS NULL)"
    + "\n" + "   AND (percentSchedule.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY')"
    + "\n" + "        OR percentSchedule.t_CloseDate IS NULL)"
    + "\n" + " ORDER BY percentSchedule.t_scheduleId";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_number",    V_INTEGER);
    dataSet.setFieldType("t_name",      V_STRING);
    dataSet.setFieldType("t_groupName", V_STRING);
    dataSet.setFieldType("t_openDate",  V_DATE);
    dataSet.setFieldType("t_closeDate", V_DATE);

    while (dataSet.moveNext())

        dataList[dataList.size] = TIbrData
        (
            dataSet.number,
            dataSet.name,
            dataSet.groupName,
            dataSet.openDate,
            dataSet.closeDate
        );

    end;

    return dataList;

end;

/**
 * Получить метаданные для графиков
 *
 * @return TArray of TIbrMeta Массив метаданных графиков
 */
private macro getScheduleMeta(): TArray

    /**
     * Метаинформация
     */
    var metaList: TArray = TArray();

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().schedule.LABEL_NUMBER,
        "identifier"
    ).setBinding();

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().schedule.LABEL_NAME,
        "name"
    ).setBindingDouble();

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().schedule.LABEL_GROUP,
        "group"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().schedule.LABEL_DATE_OPEN,
        "dateOpen"
    );

    metaList[metaList.size] = TIbrMeta
    (
        TIbrResource().schedule.LABEL_DATE_CLOSE,
        "dateClose"
    );

    return metaList;

end;

/**
 * Получить список графиков расчета
 *
 * @return TArray of TIbrDynamicSource Список графиков расчета
 */
macro ibrDataSource_getScheduleOfCalculation(): TArray

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    result.initialize
    (
        getScheduleMeta(),
        getScheduleData(SCHEDULE_KIND_CALCULATION)
    );

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Получить список графиков начисления
 *
 * @return TArray of TIbrDynamicSource Список графиков начисления
 */
macro ibrDataSource_getScheduleOfAccrual(): TArray

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    result.initialize
    (
        getScheduleMeta(),
        getScheduleData(SCHEDULE_KIND_ACCRUAL)
    );

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;

/**
 * Получить список графиков оплаты
 *
 * @return TArray of TIbrDynamicSource Список графиков оплаты
 */
macro ibrDataSource_getScheduleOfPayment(): TArray

    /**
     * Результат - массив строк
     */
    var result: TIbrDynamicSource = TIbrDynamicSource();

    result.initialize
    (
        getScheduleMeta(),
        getScheduleData(SCHEDULE_KIND_PAYMENT)
    );

    return result;

    onError(errorObject)
        rcbRunError(errorObject);

end;
