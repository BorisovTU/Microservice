/*
$Name: IbrCommon.mac
$Module: Отчеты банка
$Description: Общие алгоритмы
*/

macro cmpIbrObj(v1, v2)
    if (v1.sortWeight < v2.sortWeight)
        return -1;
    elif (v1.sortWeight > v2.sortWeight)
        return 1;
    else
        return 0;
    end;
end;

/**
 * Общие алгоритмы
 */
class TIbrCommon()

    /**
     * Получить идентификатор финансового инструмента по его коду
     *
     * @param codeFinancialInstrument String Код финансового инструмента
     *
     * @return Integer Код финансового инструмента.
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierFinancialInstrument(codeFinancialInstrument: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if (TIbrSystem().isNullOrEmpty(codeFinancialInstrument))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_fiId"
        + "\n" + "  FROM dFinInstr_dbt"
        + "\n" + " WHERE UPPER(t_fi_code) = '" + STRUPR(codeFinancialInstrument) + "'";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_fiId", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.fiId;

        end;

        return result;

    end;

    /**
     * Получить идентификатор клиента (субъекта) по его короткому наименованию
     *
     * @param shortName String Краткое наименование
     *
     * @return Integer Идентификатор клиента.
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierParty(shortName: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if (TIbrSystem().isNullOrEmpty(shortName))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_partyId"
        + "\n" + "  FROM dParty_dbt"
        + "\n" + " WHERE UPPER(t_shortName) = '" + strUpr(shortName) + "'";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_partyId", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.partyId;

        end;

        return result;

    end;

    /**
     * Получить идентификатор клиента (субъекта) по его полному наименованию
     *
     * @param fullName String Полное наименование
     *
     * @return Integer Идентификатор подразделения
     *         Если не найдено, то возращается -1.
     */
    macro getCodeBranch(fullName: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if (TIbrSystem().isNullOrEmpty(fullName))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT dep.t_code"
        + "\n" + "  FROM ddp_dep_dbt dep"
        + "\n" + " WHERE dep.t_partyId IN (SELECT t_partyId"
        + "\n" + "                           FROM dParty_dbt"
        + "\n" + "                          WHERE UPPER(t_name) = '" + strUpr(fullName) + "'"
        + "\n" + "                        )";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_code", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.code;

        end;

        return result;

    end;

    /**
     * Получить идентификатор операциониста по его наименованию
     *
     * @param name String Наименование
     *
     * @return Integer Идентификатор операциониста
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierUser(name: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if (TIbrSystem().isNullOrEmpty(name))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_oper"
        + "\n" + "  FROM dperson_dbt"
        + "\n" + " WHERE UPPER(t_name) = '" + strUpr(name) + "'";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_oper", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.oper;

        end;

        return result;

    end;

    /**
     * Получить идентификатор плана счетов по наименованию
     *
     * @param name String Наименование
     *
     * @return Integer Идентификатор плана счетов
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierChartOfAccounts(name: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if (TIbrSystem().isNullOrEmpty(name))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_iNumPlan"
        + "\n" + "  FROM dNamePlan_dbt"
        + "\n" + " WHERE UPPER(t_namePlan) = '" + strUpr(name) + "'";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_iNumPlan", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.iNumPlan;

        end;

        return result;

    end;

    /**
     * Получить идентификатор главы счетов по наименованию
     *
     * @param name String Наименование
     *
     * @return Integer Идентификатор главы счетов
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierAccountChapter(name: String): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        if (TIbrSystem().isNullOrEmpty(name))

            return result;

        end;

        //эвристика: если заданная глава состоит только из цифр, полагаем, что пользователь задал номер главы вручную
        if (StrIsNumber(Trim(name)))
            //все главы
            if (name == "0")
                result = 0;
                return result;
            else
                query =  "SELECT t_chapter"
                + "\n" + "  FROM dObChaptr_dbt"
                + "\n" + " WHERE t_chapter = " + name;

                dataSet = TRsbDataSet(query);
                dataSet.setFieldType("t_chapter", V_INTEGER);

                if (dataSet.moveNext())
                    result = dataSet.chapter;
                end;
                return result;
            end;
        end;

        query =  "SELECT t_chapter"
        + "\n" + "  FROM dObChaptr_dbt"
        + "\n" + " WHERE UPPER(t_name) = '" + strUpr(name) + "'";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_chapter", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.chapter;

        end;

        return result;

    end;

   /**
     * Получить массив идентификаторов глав счетов
     *
     * @param name             String  Наименование
     * @param numPlan          Variant Номер плана
     * @param excludedChapters String  С исключенными главами
     * @return TArray  Массив идентификаторов главы счетов (если глава счетов = 0 иначе поиск по имени)
     */
    macro getArrayAccountChapter(name: String, numPlan: Variant, excludedChapters : Bool): TArray
        var result                  =  TArray;
        var query:           String = "";
        var dataSet:         Object = NULL;
        var excludedWhere:   String = "\n" + "   AND chapt.t_excludeFromFormalAcnt != 'X'";

        if (TIbrSystem().isNullOrEmpty(name))

            return result;

        end;

        if (StrIsNumber(Trim(name)))

            if (name == "0")
                query =  "SELECT chapt.t_chapter"
                + "\n" + "  FROM dObChaptr_dbt chapt,"
                + "\n" + "dnameplan_chaptr_dbt plan_chapt"
                + "\n" + " WHERE  plan_chapt.t_inumPlan = "+ numPlan
                + "\n" + "   AND chapt.t_chapter = plan_chapt.t_chapter";

                if (excludedChapters != true)
                   query = query + excludedWhere;
                end;

                dataSet = TRsbDataSet(query);
                dataSet.setFieldType("t_chapter", V_INTEGER);

            end;
        else
            query =  "SELECT t_chapter"
            + "\n" + "  FROM dObChaptr_dbt"
            + "\n" + " WHERE UPPER(t_name) = '" + strUpr(name) + "'";

            dataSet = TRsbDataSet(query);
            dataSet.setFieldType("t_chapter", V_INTEGER);
        end;

        while (dataSet.moveNext())
            result[result.size] = dataSet.chapter;
        end;

        return result;  

    end;

    /**
     * Получить идентификатор справочника llvalues по классификатору
     *
     * @param classificator Integer Классификатор
     *
     * @return Integer Идентификатор справочинка llvalues
     *         Если не найдено, то возращается -1.
     */
    macro getIdentifierLlValues(classificator: Integer): Integer

        /**
         * Результат
         */
        var result: Integer = -1;

        if ((classificator == NULL) OR (classificator <= 0))

            return result;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_list"
        + "\n" + "  FROM dllclass_dbt"
        + "\n" + " WHERE t_classificator = " + classificator;

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_list", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.list;

        end;

        return result;

    end;

    macro getValue(report: Object, fieldName : String) : Object/*TIbrValueWeb*/
        var path = strcut(fieldName, "@");

        for (var tab, report.decorationList)
            if (strupr(tab.name) != strupr(path[0]))
                continue;
            end;
            for (var group, tab.children)
                if (strupr(group.name) != strupr(path[1]))
                    continue;
                end;
                for (var field, group.fieldList)
                    if (strupr(field.name) == strupr(path[2]))
                        return field.value;
                    end;
                end;
            end;
        end;

        return null;
    end;

    macro getElementLlValues(code: string, classificator): Integer
        var result: Integer = -1;
        var query: String = "";
        var dataSet: Object = NULL;

        if (((code == NULL) OR (code == "")) or ((classificator == NULL) OR (classificator <= 0)))
            return result;
        end;

        query =  "SELECT llValues.t_element"
        + "\n" + "  FROM dllValues_dbt llValues,"
        + "\n" + "       dllclass_dbt llClass"
        + "\n" + " WHERE llValues.t_code = '" + code + "'"
        + "\n" + "   AND llClass.t_classificator = " + classificator
        + "\n" + "   AND llValues.t_list = llClass.t_list";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_element", V_INTEGER);

        if (dataSet.moveNext())
            result = dataSet.element;
        end;

        return result;
    end;
end;
