/*
$Name: IbrValue.mac
$Module: Отчеты банка
$Description: Универсальная панель. Значения полей
*/

/**
 * Класс значения поля для универсальной панели
 * Функциональный класс
 */
class TIbrValue()

    /**
     * Заполнить занчениями из источника данных
     *
     * @param value TIbrValueWeb Значение поля
     * @param dataSet Object Набор данных
     */
    macro initialize(value: TIbrValueWeb, dataSet: Object)

        if (value == NULL)

            throw_web(TIbrUndefinedValueException("TIbrValue::initialize", "value"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrValue::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().value.column;

        value.identifierValue = execExp("dataSet." + constantColumn.IDENTIFIER_VALUE);
        value.valueString     = execExp("dataSet." + constantColumn.VALUE_STRING);
        value.valueBoolean    = execExp("dataSet." + constantColumn.VALUE_BOOLEAN);
        value.valueInteger    = execExp("dataSet." + constantColumn.VALUE_INTEGER);
        value.valueDate       = execExp("dataSet." + constantColumn.VALUE_DATE);
        value.isUser = TIbrSystem.rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Удалить все значения, на которые нет ссылок с полей
     */
    macro removeAllWithoutReference()

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = TRUE;

        query =  " DELETE FROM dWebIbrValue_dbt ibrValue"
        + "\n" + "  WHERE NOT EXISTS"
        + "\n" + "       (SELECT 1"
        + "\n" + "          FROM dWebIbrField_dbt ibrField"
        + "\n" + "         WHERE ibrField.t_identifierValue = ibrValue.t_identifierValue"
        + "\n" + "            OR ibrField.t_identifierValueDefault = ibrValue.t_identifierValue)";

        result = sql_execute(query);

        if (result)

            result = sql_execute("COMMIT");

        end;

        if (result)

            printLn(TIbrResource.message.DELETE_VALUE)

        end;

        return result;

    end;

    /**
     * Метод очищает данные, которые отсутствуют в классе TIbrValue
     *
     * @param report Object Объект значения
     *
     * @return report TIbrValueWeb Объект значения
     */
    macro reconstruction(value: Object): TIbrValueWeb

        var result:           TIbrValueWeb = TIbrValueWeb();
        var propertyName:     String = "";
        var propertyNameList: TArray = getObjProps(result);

        for (propertyName, propertyNameList)

            execExp("result." + propertyName + " = value." + propertyName + ";");

        end;

        return result;

    end;

end;
