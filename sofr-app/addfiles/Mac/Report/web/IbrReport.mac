/*
$Name: IbrReport.mac
$Module: Отчеты банка
$Description: Универсальная панель. Справочник отчетов
*/

class (TIbrException) TIbrIncorrectWsCreateReportException(methodName: String)
    initTIbrException
    (
        TIbrSystem().stringFormat
        (
            TIbrResource().exception.INCORRECT_WS_CREATE_REPORT,
            methodName
        )
    );
end;

/**
 * Класс отчета универсальной панели.
 * Функциональный класс
 */
class TIbrReport()

    /**
     * Получить sql-выражение по идентификатору
     */
    private macro getQueryByIdentifier(identifierReport: Integer): String

        if (identifierReport == NULL)

            throw_web(TIbrUndefinedValueException("getQueryByIdentifier", "identifierReport"));

        end;

        /**
         * Результат - выражение sql-запроса на выборку данных об отчёте
         */
        var result: String = "";

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().report.column;

        result = "SELECT ibrReport.t_identifierReport AS " + constantColumn.IDENTIFIER_REPORT + ","
        + "\n" + "       ibrReport.t_name             AS " + constantColumn.NAME              + ","
        + "\n" + "       ibrReport.t_nameDisplayed    AS " + constantColumn.NAME_DISPLAYED    + ","
        + "\n" + "       ibrReport.t_macroFile        AS " + constantColumn.MACRO_FILE        + ","
        + "\n" + "       ibrReport.t_serviceExecute   AS " + constantColumn.SERVICE_EXECUTE   + ","
        + "\n" + "       ibrReport.t_serviceValidate  AS " + constantColumn.SERVICE_VALIDATE  + ","
        + "\n" + "       ibrReport.t_isUser           AS " + constantColumn.IS_USER
        + "\n" + "  FROM dWebIbrReport_dbt ibrReport"
        + "\n" + " WHERE ibrReport." + constantColumn.IDENTIFIER_REPORT + " = " + identifierReport;

        return result;

    end;

    /**
     * Получить sql-выражение по наименованию
     */
    private macro getQueryByName(nameReport: String): String

        if (nameReport == NULL)

            throw_web(TIbrUndefinedValueException("TIbrReport::getQueryByName", "nameReport"));

        end;

        /**
         * Результат - выражение sql-запроса на выборку данных об отчёте
         */
        var result: String = "";

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().report.column;

        result = "SELECT ibrReport.t_identifierReport AS " + constantColumn.IDENTIFIER_REPORT + ","
        + "\n" + "       ibrReport.t_name             AS " + constantColumn.NAME              + ","
        + "\n" + "       ibrReport.t_nameDisplayed    AS " + constantColumn.NAME_DISPLAYED    + ","
        + "\n" + "       ibrReport.t_macroFile        AS " + constantColumn.MACRO_FILE        + ","
        + "\n" + "       ibrReport.t_serviceExecute   AS " + constantColumn.SERVICE_EXECUTE   + ","
        + "\n" + "       ibrReport.t_serviceValidate  AS " + constantColumn.SERVICE_VALIDATE  + ","
        + "\n" + "       ibrReport.t_isUser           AS " + constantColumn.IS_USER
        + "\n" + "  FROM dWebIbrReport_dbt ibrReport"
        + "\n" + " WHERE ibrReport.t_name = '" + nameReport + "'";

        return result;

    end;

    /**
     * Получить информацию об отчете
     *
     * @param reportIdentifier Integer Идентификатор отчета
     * @param reportName String Наименование отчета
     *
     * @result TIbrReportWeb Информация об отчёте
     */
    macro getData(reportIdentifier: Integer, reportName: String, wsCreateIbrReport : String): Object
        var result: Object  = TIbrReportWeb();

        if ((wsCreateIbrReport != NULL) and (wsCreateIbrReport != ""))
            if (index(wsCreateIbrReport, "@") != 0)
                var service = substr(wsCreateIbrReport, 1, index(wsCreateIbrReport, "@") - 1);
                var function = substr(wsCreateIbrReport, index(wsCreateIbrReport, "@") + 1);
                result = execMacroFile(service, function);
            else
                throw_web(TIbrIncorrectWsCreateReportException("TIbrReport::getData"));
            end;
        else
            var query: String = "";

            if ((reportIdentifier != NULL) and (reportIdentifier > 0))
                query = getQueryByIdentifier(reportIdentifier);
            else
                if (not TIbrSystem().isNullOrEmpty(reportName))
                    query = getQueryByName(reportName);
                else
                    throw_web(TIbrUndefinedValueException("TIbrReport::getData", "reportIdentifier, reportName, wsCreateIbrReport"));
                end;
            end;

            var dataSet: Object = NULL;
            var constantColumn: Object = TIbrConstant().report.column;

            dataSet = TRsbDataSet(query);
            dataSet.setFieldType(constantColumn.IDENTIFIER_REPORT, V_INTEGER);
            dataSet.setFieldType(constantColumn.NAME,              V_STRING);
            dataSet.setFieldType(constantColumn.NAME_DISPLAYED,    V_STRING);
            dataSet.setFieldType(constantColumn.MACRO_FILE,        V_STRING);
            dataSet.setFieldType(constantColumn.SERVICE_EXECUTE,   V_STRING);
            dataSet.setFieldType(constantColumn.SERVICE_VALIDATE,  V_STRING);
            dataSet.setFieldType(constantColumn.IS_USER,           V_STRING);

            if (dataSet.moveNext())
                result.initialize(dataSet);
            else
                rcbRunError(TIbrResource().message.REPORT_NOT_FOUND);
            end;

            result.decorationList = TIbrDecoration().getData(result.identifierReport, result);
            result.operationList  = TIbrOperation().getData(result.identifierReport, result);
        end;

        return result;

        onError(errorObject)
            rcbRunError(errorObject);

    end;

    /**
     * Заполнить знчениями из источника данных
     *
     * @param report TIbrReportWeb Объект отчета
     * @param dataSet TRsbDataset Набор данных
     */
    macro initialize(report: TIbrReportWeb, dataSet: Object)

        if (report == NULL)

            throw_web(TIbrUndefinedValueException("TIbrReport::initialize", "report"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrReport::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().report.column;

        report.identifierReport = execExp("dataSet." + constantColumn.IDENTIFIER_REPORT);
        report.name             = execExp("dataSet." + constantColumn.NAME);
        report.nameDisplayed    = execExp("dataSet." + constantColumn.NAME_DISPLAYED);
        report.macroFile        = execExp("dataSet." + constantColumn.MACRO_FILE);
        report.serviceExecute   = execExp("dataSet." + constantColumn.SERVICE_EXECUTE);
        report.serviceValidate  = execExp("dataSet." + constantColumn.SERVICE_VALIDATE);
        report.isUser = TIbrSystem.rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Удаление отчета
     *
     * @param report TIbrReportWeb Отчет
     *
     * @return True, если отчет удален, иначе - False
     */
    macro delete(report: TIbrReportWeb): Bool

        if (report == NULL)

            throw_web
            (
                TIbrUndefinedValueException
                (
                    "TIbrReport::delete",
                    "report"
                )
            );

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().delete
        (
            TIbrConstant.report.TABLE_NAME,
            TIbrConstant.report.PRIMARY_KEY,
            report.identifierReport
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_REPORT,
                    report.name
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_REPORT,
                        report.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Получить количество отчетов
     */
    macro getCount(): Integer

        /**
         * Результат
         */
        var result: Integer = 0;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT COUNT(*) AS t_count"
        + "\n" + "  FROM dWebIbrReport_dbt ibrReport";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_count", V_INTEGER);

        if (dataSet.moveNext())

            result = dataSet.count;

        else

            result = 0;

        end;

        return result;

    end;

    /**
     * Получить список идентификаторов отчетов
     */
    macro getIdentifierList(): TArray

        /**
         * Результат
         */
        var result: TArray = TArray();

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT ibrReport.t_identifierReport"
        + "\n" + "  FROM " + TIbrConstant.report.TABLE_NAME + " ibrReport";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_identifierReport", V_INTEGER);

        while (dataSet.moveNext())

            result[result.size] = dataSet.identifierReport;

        end;

        return result;

    end;

    /**
     * Изменить параметры отчета
     *
     * @param oldReportWeb TIbrReportWeb Текущие (старые) параметры отчета
     * @param newReportWeb TIbrReportWeb Новые параметры отчета
     *
     * @return TRUE, если значения изменены успешно, иначе - FASLE
     */
    macro update(oldReportWeb: TIbrReportWeb, newReportWeb: TIbrReportWeb): Bool

        if (oldReportWeb == NULL)

            throw_web
            (
                TIbrUndefinedValueException
                (
                    "TIbrReport::update",
                    "oldReportWeb"
                )
            );

        end;

        if (newReportWeb == NULL)

            throw_web
            (
                TIbrUndefinedValueException
                (
                    "TIbrReport::update",
                    "newReportWeb"
                )
            );

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        /**
         * sql-выражение установки нового значения
         */
        var sqlSet: String = "";

        /**
         * Ошибка сообщения
         */
        var errorMessage: String = "";

        if (oldReportWeb.nameDisplayed != newReportWeb.nameDisplayed)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_nameDisplayed = '" + newReportWeb.nameDisplayed + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_REPORT_MESSAGE,
                "отображаемое наименование",
                oldReportWeb.nameDisplayed,
                newReportWeb.nameDisplayed
            )

        end;

        if (oldReportWeb.macroFile != newReportWeb.macroFile)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_macroFile = '" + newReportWeb.macroFile + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_REPORT_MESSAGE,
                "макрофайл",
                oldReportWeb.macroFile,
                newReportWeb.macroFile
            )

        end;

        if (oldReportWeb.serviceExecute != newReportWeb.serviceExecute)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_serviceExecute = '" + newReportWeb.serviceExecute + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_REPORT_MESSAGE,
                "сервис выполнения",
                oldReportWeb.serviceExecute,
                newReportWeb.serviceExecute
            )

        end;

        if (oldReportWeb.serviceValidate != newReportWeb.serviceValidate)

            if (NOT TIbrSystem().isNullOrEmpty(sqlSet))

                sqlSet = sqlSet + ",";

            end;

            sqlSet = sqlSet + " t_serviceValidate = '" + newReportWeb.serviceValidate + "'";

            errorMessage = errorMessage +
            TIbrSystem.stringFormat
            (
                TIbrResource.message.UPDATE_REPORT_MESSAGE,
                "сервис проверки",
                oldReportWeb.serviceValidate,
                newReportWeb.serviceValidate
            )

        end;

        result = TIbrBase().update
        (
            TIbrConstant.report.TABLE_NAME,
            TIbrConstant.report.PRIMARY_KEY,
            oldReportWeb.identifierReport,
            sqlSet
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.UPDATE_REPORT,
                    oldReportWeb.name
                )
            );
            printLn(errorMessage);

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_UPDATE_REPORT,
                        oldReportWeb.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Найти поле по наименование для отчета.
     * Регистр наименования поля не учитывается.
     *
     * @param report TIbrReportWeb Отчет, для котого ищем поле
     * @param name String Наименование поля.
     *
     * @return TIbrFieldWeb Объект поля, если найдено, иначе NULL;
     */
    macro searchField(report: Object, name: String): Object

        if (report == NULL)

            throw_web(TIbrUndefinedValueException("TIbrReport::searchField", "report"));

        end;

        if (TIbrSystem.isNullOrEmpty(name))

            throw_web(TIbrUndefinedValueException("TIbrReport::searchField", "name"));

        end;

        var isFound: Bool   = FALSE;
        var field:   Object = NULL;
        var tab:     Object = NULL;
        var group:   Object = NULL;
        var result:  Object = NULL;

        for (tab, report.decorationList)

            for (group, tab.children)

                for (field, group.fieldList)

                    if (strUpr(field.name) == strUpr(name))

                        result  = field;
                        isFound = TRUE;
                        break;

                    end;

                end;

                if (isFound)

                    break;

                end;

            end;

            if (isFound)

                break;

            end;

        end;

        return result;

    end;

    /**
     * Метод очищает данные значений и значений по умолчанию,
     * которые не содержатся в классе TIbrValue
     *
     * @param report Object Объект отчета
     *
     * @return report TIbrReportWeb Объект отчета
     */
    macro reconstruction(report: Object): TIbrReportWeb

        var tab:   Object = NULL;
        var group: Object = NULL;
        var field: Object = NULL;

        for (tab, report.DecorationList)

            for (group, tab.Children)

                for (field, group.fieldList)

                    field.value        = TIbrValue().reconstruction(field.value);
                    field.valueDefault = TIbrValue().reconstruction(field.valueDefault);

                end;

            end;

        end;

        return report;

    end;

end;
