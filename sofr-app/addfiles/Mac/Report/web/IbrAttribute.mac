/*
$Name: IbrAttribute.mac
$Module: Отчеты банка
$Description: Универсальная панель. Атрибут поля
*/

/**
 * Класс атрубита поля
 * Функциональный класс
 */
class TIbrAttribute()

    /**
     * Служебный класс
     */
    private class TIbrDictionary()
        /**
         * Идентификатор
         */
        var identifier: Integer = 0;

        /**
         * Список
         */
        var list: TArray = TArray();
    end;

    /**
     * Переменная существует в рамках одной сессии (обращения к сервису)
     * Представляет словарь:
     *     ключ - идентификатор поля,
     *     значение - массив атрибутов.
     */
    private var attributeList: TArray = TArray();

    /**
     * Получение данных из БД
     *
     * @param identifierReport Integer Идентификатор отчета
     * @param field Object Поле
     */
    private macro cacheData(identifierReport: Integer, field: @Object)

        if (identifierReport == NULL)

            throw_web(TIbrUndefinedValueException("TIbrAttribute::getData", "identifierReport"));

        end;

        if ((attributeList != NULL) AND (attributeList.size != 0))

            // Данные уже закешированы
            return;

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: TArray  = TArray();

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().attribute.column;

        /**
         * Текущий атрибут
         */
        var currentAttribute: Object = NULL;

        /**
         * Предыдущий атрибут
         */
        var previsionAttribute: Object = NULL;

        /**
         * Словарь
         */
        var currentDictionary: Object = NULL;

        query  = "SELECT ibrAttribute.t_identifierAttribute AS " + constantColumn.IDENTIFIER_ATTRIBUTE + ","
        + "\n" + "       ibrAttribute.t_identifierField     AS " + constantColumn.IDENTIFIER_FIELD     + ","
        + "\n" + "       ibrAttribute.t_nameParameter       AS " + constantColumn.NAME_PARAMETER       + ","
        + "\n" + "       ibrAttribute.t_valueParameter      AS " + constantColumn.VALUE_PARAMETER      + ","
        + "\n" + "       ibrAttribute.t_isUser              AS " + constantColumn.IS_USER
        + "\n" + "  FROM dWebIbrAttribute_dbt ibrAttribute"
        + "\n" + " WHERE t_identifierField = "
        + "\n" + "    (SELECT t_identifierField"
        + "\n" + "       FROM dWebIbrField_dbt ibrField"
        + "\n" + "      WHERE ibrField.t_identifierField = ibrAttribute.t_identifierField "
        + "\n" + "        AND t_identifierDecoration = "
        + "\n" + "        (SELECT t_identifierDecoration"
        + "\n" + "           FROM dWebIbrDecoration_dbt ibrDecoration"
        + "\n" + "          WHERE t_identifierReport = " + identifierReport
        + "\n" + "            AND ibrDecoration.t_identifierDecoration = ibrField.t_identifierDecoration))"
        + "\n" + " ORDER BY ibrAttribute.t_identifierField, t_identifierAttribute";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType(constantColumn.IDENTIFIER_ATTRIBUTE, V_INTEGER);
        dataSet.setFieldType(constantColumn.IDENTIFIER_FIELD,     V_INTEGER);
        dataSet.setFieldType(constantColumn.NAME_PARAMETER,       V_STRING);
        dataSet.setFieldType(constantColumn.VALUE_PARAMETER,      V_STRING);
        dataSet.setFieldType(constantColumn.IS_USER,              V_STRING);

        currentDictionary = TIbrDictionary();

        while (dataSet.moveNext())

            currentAttribute = TIbrAttributeWeb();
            currentAttribute.initialize(dataSet);
            currentAttribute.field = field;

            if (attributeList.size == 0)

                previsionAttribute = currentAttribute;
                attributeList[attributeList.size] = currentDictionary;

            end;

            if (currentAttribute.identifierField != previsionAttribute.identifierField)

                currentDictionary = TIbrDictionary();
                attributeList[attributeList.size] = currentDictionary;

            end;

            currentDictionary.identifier = currentAttribute.identifierField;
            currentDictionary.list[currentDictionary.list.size] = currentAttribute;

            previsionAttribute = currentAttribute;
        end;

    end;

    /**
     * Получить список атрибутов для поля
     *
     * @param identifierReport Integer Идентификатор отчета
     * @param identifierField Integer Идентификатор поля
     * @param field Object Поле
     *
     * @return TArray of TIbrAttributeWeb Список атрибутов поля
     *         Если атрибутов у поля нет, то возвращается пустой массив
     */
    macro getData
    (
        identifierReport: Integer,
        identifierField:  Integer,
        field:            @Object
    ): TArray

        if (identifierReport == NULL)

            throw_web(TIbrUndefinedValueException("TIbrAttribute::getData", "identifierReport"));

        end;

        if (identifierField == NULL)

            throw_web(TIbrUndefinedValueException("TIbrAttribute::getData", "identifierField"));

        end;

        // Кешируем данные
        cacheData(identifierReport, field);

        /**
         * Список атрибутов
         */
        var result: TArray = TArray();

        /**
         * Счетчик
         */
        var counter: Integer = 0;

        while (counter < attributeList.size)

            if (attributeList[counter].identifier == identifierField)

                result = attributeList[counter].list;

            end;

            counter = counter + 1;
        end;

        return result;

        onError(errorObject)
            rcbRunError(errorObject);

    end;

    /**
     * Заполнить занчениями из источника данных
     *
     * @param attribute TIbrAttribute Атрибут поля
     * @param dataSet Object Набор данных
     */
    macro initialize(attribute: TIbrAttributeWeb, dataSet: Object)

        if (attribute == NULL)

            throw_web(TIbrUndefinedValueException("TIbrAttribute::initialize", "attribute"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrAttribute::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: TIbrConstant = TIbrConstant().attribute.column;

        attribute.identifierAttribute = execExp("dataSet." + constantColumn.IDENTIFIER_ATTRIBUTE);
        attribute.identifierField     = execExp("dataSet." + constantColumn.IDENTIFIER_FIELD);
        attribute.nameParameter       = execExp("dataSet." + constantColumn.NAME_PARAMETER);
        attribute.valueParameter      = execExp("dataSet." + constantColumn.VALUE_PARAMETER);
        attribute.isUser = TIbrSystem.rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Удаление атрибута
     *
     * @param attribute TIbrAttributeWeb Поле
     *
     * @return True, если поле удалено, иначе - False
     */
    macro delete(attribute: TIbrAttributeWeb): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().delete
        (
            TIbrConstant.attribute.TABLE_NAME,
            TIbrConstant.attribute.PRIMARY_KEY,
            attribute.identifierAttribute
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_ATTRIBUTE,
                    attribute.nameParameter + ": " +
                        attribute.valueParameter
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_ATTRIBUTE,
                        attribute.name
                    )
                )
            );

        end;

        return result;

    end;

end;
