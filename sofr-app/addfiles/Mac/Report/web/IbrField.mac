/*
$Name: IbrField.mac
$Module: Отчеты банка
$Description: Универсальная панель. Справочник полей
*/

/**
 * Класс поля
 * Функциональный класс
 */
class TIbrField()

    /**
     * Заполнить занчениями из источника данных
     *
     * @param field TIbrFieldWeb Поле
     * @param dataSet Object Набор данных
     */
    macro initialize(field: TIbrFieldWeb, dataSet: Object)

        if (field == NULL)

            throw_web(TIbrUndefinedValueException("TIbrField::initialize", "field"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrField::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().field.column;

        field.identifierField = execExp("dataSet." + constantColumn.IDENTIFIER_FIELD);
        field.name            = execExp("dataSet." + constantColumn.NAME);
        field.sortWeight      = execExp("dataSet." + constantColumn.SORT_WEIGHT);
        field.isUser = TIbrSystem().rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Установить порядок сортировки
     *
     * @param field TIbrFieldWeb Поле
     * @param sortWeightParameter Integer Порядок сортировки
     *
     * @return True, если порядок сортировки изменён успешно, иначе - False.
     */
    macro setSortWeight(field: TIbrFieldWeb, sortWeightParameter: Integer): Bool

        if (TIbrSystem.isNullOrEmpty(sortWeightParameter))

            throw_web(TIbrIncorrectParameterException("TIbrField::setSortWeight"));

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().setSortWeight
        (
            TIbrConstant.field.TABLE_NAME,
            TIbrConstant.field.PRIMARY_KEY,
            field.identifierField,
            sortWeightParameter
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.CHANGE_SORTWEIGHT_FIELD,
                    field.name,
                    field.sortWeight,
                    sortWeightParameter
                )
            );

            field.sortWeight = sortWeightParameter;

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_CHANGE_SORTWEIGHT_FIELD,
                        sortWeightParameter,
                        field.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Удаление поля
     *
     * @param field TIbrFieldWeb Поле
     *
     * @return True, если поле удалено, иначе - False
     */
    macro delete(field: TIbrFieldWeb): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        //сначала удаляем значения
        result = TIbrBase().delete
        (
            TIbrConstant.value.TABLE_NAME,
            TIbrConstant.value.PRIMARY_KEY,
            field.identifierField
        );

/*
        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_VALUE,
                    value.name
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_VALUE,
                        value.name
                    )
                )
            );

        end;
*/
        //теперь удаляем поля
        result = TIbrBase().delete
        (
            TIbrConstant.field.TABLE_NAME,
            TIbrConstant.field.PRIMARY_KEY,
            field.identifierField
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_FIELD,
                    field.name
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_FIELD,
                        field.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Получить поле по наименованию
     *
     * @param fieldName String Наименование группы
     *
     * @return Object Объект группы (без зависимых элементов)
     */
    macro getField(fieldName: String): Object

        if (TIbrSystem.isNullOrEmpty(fieldName))

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getField", "fieldName"));

        end;

        /**
         * Результат - объект вкладки
         */
        var result: Object = NULL;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT t_identifierField,"
        + "\n" + "       t_name,"
        + "\n" + "       t_sortWeight,"
        + "\n" + "       t_isUser"
        + "\n" + "  FROM dWebIbrField_dbt ibrField"
        + "\n" + " WHERE UPPER(ibrField.t_name) = UPPER('" + fieldName + "')";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_identifierField",        V_INTEGER);
        dataSet.setFieldType("t_name",                   V_STRING);
        dataSet.setFieldType("t_sortWeight",             V_INTEGER);
        dataSet.setFieldType("t_isUser",                 V_INTEGER);

        if (dataSet.moveNext())

            result = TIbrFieldWeb();
            result.identifierField        = dataSet.identifierField;
            result.name                   = dataSet.name;
            result.sortWeight             = dataSet.sortWeight;
            result.isUser                 = TIbrSystem.rslBoolean(dataSet.isUser);

        else

            throw_web(TIbrException(TIbrResource().message.FIELD_NOT_FOUND));

        end;

        return result;

    end;

    macro existsField(fieldName: String)

        return FALSE;

    end;

    /**
     * Найти атрибут по наименованию для поля.
     * Регистр наименования атрибута не учитывается.
     *
     * @param field TIbrFieldWeb Поле
     * @param name String Наименование атрибута.
     *
     * @return TIbrAttributeWeb Объект атрибута, если найдено, иначе NULL;
     */
    macro searchAttribute(field: Object, name: String): Object

        if (field == NULL)

            throw_web(TIbrUndefinedValueException("TIbrField::searchAttribute", "field"));

        end;

        if (TIbrSystem.isNullOrEmpty(name))

            throw_web(TIbrUndefinedValueException("TIbrField::searchAttribute", "name"));

        end;

        var result:    Object = NULL;
        var attribute: Object = NULL;

        for (attribute, field.attributeList)

            if (strUpr(attribute.nameParameter) == strUpr(name))

                result = attribute;
                break;

            end;

        end;

        return result;

    end;

end;
