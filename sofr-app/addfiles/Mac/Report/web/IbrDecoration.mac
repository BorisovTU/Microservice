/*
$Name: IbrDecoration.mac
$Module: Отчеты банка
$Description: Универсальная панель. Справочник групп
*/

/**
 * Класс декораций (вкладки, группы)
 * Функциональный класс
 */
class TIbrDecoration()

    private macro isNull(dataSet: Object, nameField: String): Bool

        return (execExp("dataSet." + nameField) == NULL);

    end;

    /**
     * Получить иерархический список декораций для отчета
     *
     * @param identifierReport Integer Идентификатор отчета
     *
     * @return TArray of TIbrDecorationWeb Иерархический список декораций
     */
    macro getData(identifierReport: Integer, report: @Object): TArray

        if (identifierReport == NULL)

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getData", "identifierReport"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        /**
         * Результат
         */
        var result: TArray = TArray();

        /**
         * Значение поля по умолчанию
         */
        var valueDefault: Object = NULL;

        /**
         * Поле
         */
        var field: Object = NULL;

        /**
         * Текущая декорация
         */
        var currentDecoration: Object = NULL;

        /**
         * Список дочерних элементов
         */
        var children: TArray = NULL;

        /**
         * Идентификатор декорации
         */
        var identifierDecoration: Integer = 0;

        /**
         * Константы полей (колонок) декораций
         */
        var constantDecoration: Object = TIbrConstant().decoration.column;

        /**
         * Константы полей (колонок) полей
         */
        var constantField: Object = TIbrConstant().field.column;

        /**
         * Константы полей (колонок) значений
         */
        var constantValue: Object = TIbrConstant().value.column;

        /**
         * Тип декорации
         */
        var decorationType: Integer = 0;

        query =  "SELECT level AS t_level, "
        + "\n" + "       ibrData.*"
        + "\n" + "  FROM"
        + "\n" + "( "
        + "\n" + "     SELECT ibrDecoration.t_identifierDecoration AS " + constantDecoration.IDENTIFIER_DECORATION + ","
        + "\n" + "            ibrDecoration.t_identifierParent     AS " + constantDecoration.IDENTIFIER_PARENT     + ","
        + "\n" + "            ibrDecoration.t_type                 AS " + constantDecoration.TYPE                  + ","
        + "\n" + "            ibrDecoration.t_name                 AS " + constantDecoration.NAME                  + ","
        + "\n" + "            ibrDecoration.t_sortWeight           AS " + constantDecoration.SORT_WEIGHT           + ","
        + "\n" + "            ibrDecoration.t_isUser               AS " + constantDecoration.IS_USER               + ","
        + "\n" + "            ibrField.t_identifierField           AS " + constantField.IDENTIFIER_FIELD           + ","
        + "\n" + "            ibrField.t_name                      AS " + constantField.NAME                       + ","
        + "\n" + "            ibrField.t_sortWeight                AS " + constantField.SORT_WEIGHT                + ","
        + "\n" + "            ibrField.t_isUser                    AS " + constantField.IS_USER                    + ","
        + "\n" + "            ibrValueDefault.t_identifierValue    AS " + constantValue.IDENTIFIER_VALUE           + ","
        + "\n" + "            ibrValueDefault.t_valueString        AS " + constantValue.VALUE_STRING               + ","
        + "\n" + "            ibrValueDefault.t_valueBoolean       AS " + constantValue.VALUE_BOOLEAN              + ","
        + "\n" + "            ibrValueDefault.t_valueInteger       AS " + constantValue.VALUE_INTEGER              + ","
        + "\n" + "            ibrValueDefault.t_valueDate          AS " + constantValue.VALUE_DATE                 + ","
        + "\n" + "            ibrValueDefault.t_isUser             AS " + constantValue.IS_USER
        + "\n" + "       FROM dWebIbrDecoration_dbt ibrDecoration,"
        + "\n" + "            dWebIbrField_dbt ibrField,"
        + "\n" + "            dWebIbrValue_dbt ibrValueDefault"
        + "\n" + "      WHERE ibrDecoration.t_identifierReport = " + identifierReport
        + "\n" + "        AND ibrDecoration.t_identifierDecoration = ibrField.t_identifierDecoration(+)"
        + "\n" + "        AND ibrField.t_identifierValueDefault = ibrValueDefault.t_identifierValue(+)"
        + "\n" + ") ibrData   "
        + "\n" + "  START WITH ibrData.t_identifierParent = 0"
        + "\n" + "CONNECT BY PRIOR ibrData.t_identifierDecoration = ibrData.t_identifierParent";

        dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

        dataSet.setFieldType(constantDecoration.IDENTIFIER_DECORATION, V_INTEGER);
        dataSet.setFieldType(constantDecoration.IDENTIFIER_PARENT,     V_INTEGER);
        dataSet.setFieldType(constantDecoration.TYPE,                  V_INTEGER);
        dataSet.setFieldType(constantDecoration.NAME,                  V_STRING);
        dataSet.setFieldType(constantDecoration.SORT_WEIGHT,           V_INTEGER);
        dataSet.setFieldType(constantDecoration.IS_USER,               V_STRING);

        dataSet.setFieldType(constantField.IDENTIFIER_FIELD, V_INTEGER);
        dataSet.setFieldType(constantField.NAME,             V_STRING);
        dataSet.setFieldType(constantField.SORT_WEIGHT,      V_INTEGER);
        dataSet.setFieldType(constantField.IS_USER,          V_STRING);

        dataSet.setFieldType(constantValue.IDENTIFIER_VALUE, V_INTEGER);
        dataSet.setFieldType(constantValue.VALUE_STRING,     V_STRING);
        dataSet.setFieldType(constantValue.VALUE_INTEGER,    V_INTEGER);
        dataSet.setFieldType(constantValue.VALUE_DATE,       V_DATE);
        dataSet.setFieldType(constantValue.IS_USER,          V_STRING);

        var isEndOfTable: Bool = FALSE;

        while (NOT dataSet.EOF())

            isEndOfTable = dataSet.moveNext();

            if (NOT isEndOfTable)
                break;
            end;

            decorationType = execExp("dataSet." + constantDecoration.TYPE);

            if (decorationType == TIbrConstant().decoration.type.TAB)

                currentDecoration = TIbrDecorationWeb();

                if (NOT TIbrSystem.isWeb())

                    currentDecoration.report = report;

                end;

                currentDecoration.initialize(dataSet);
                children = currentDecoration.children;
                result.value(result.size) = currentDecoration;

            else

                if (decorationType == TIbrConstant().decoration.type.GROUP)

                    currentDecoration = TIbrDecorationWeb();

                    if (NOT TIbrSystem.isWeb())

                        currentDecoration.report = report;

                    end;

                    currentDecoration.initialize(dataSet);
                    children.value(children.size) = currentDecoration;

                    if (NOT isNull(dataSet, constantField.IDENTIFIER_FIELD))

                        valueDefault = TIbrValueWeb();
                        valueDefault.initialize(dataSet);

                        field = TIbrFieldWeb();

                        if (NOT TIbrSystem.isWeb())

                            field.decoration = currentDecoration;

                        end;

                        field.initialize(dataSet);

                        field.valueDefault  = valueDefault;
                        field.attributeList = TIbrAttribute().getData(identifierReport, field.identifierField);

                        currentDecoration.fieldList.value(currentDecoration.fieldList.size) = field;

                    end;

                    while (NOT dataSet.EOF())

                        isEndOfTable = dataSet.moveNext();

                        if (NOT isEndOfTable)
                            break;
                        end;

                        decorationType = execExp("dataSet." + constantDecoration.TYPE);

                        if (decorationType == TIbrConstant().decoration.type.TAB)

                            dataSet.movePrev();
                            break;

                        else

                            if (decorationType == TIbrConstant().decoration.type.GROUP)

                                if (NOT isNull(dataSet, constantField.IDENTIFIER_FIELD))

                                    if (dataSet.nameDecoration != currentDecoration.name)

                                        currentDecoration = TIbrDecorationWeb();

                                        if (NOT TIbrSystem.isWeb())

                                            currentDecoration.report = report;

                                        end;

                                        currentDecoration.initialize(dataSet);
                                        children.value(children.size) = currentDecoration;

                                    end;

                                    valueDefault = TIbrValueWeb();
                                    valueDefault.initialize(dataSet);

                                    field = TIbrFieldWeb();

                                    if (NOT TIbrSystem.isWeb())

                                        field.decoration = currentDecoration;

                                    end;

                                    field.initialize(dataSet);

                                    field.valueDefault  = valueDefault;
                                    field.attributeList = TIbrAttribute().getData(identifierReport, field.identifierField);

                                    currentDecoration.fieldList.value(currentDecoration.fieldList.size) = field;

                                end;

                            else

                                throw_web(TIbrException(TIbrResource().message.DECORATION_NOT_FOUND));

                            end;

                        end;

                    end;

                else

                    throw_web(TIbrException(TIbrResource().message.DECORATION_NOT_FOUND));

                end;

            end;

        end;

        return result;

    end;

    /**
     * Заполнить занчениями из источника данных
     *
     * @param decoration TIbrDecorationWeb Декорация (вкладка, группа)
     * @param dataSet TRsbDataSet Набор данных
     */
    macro initialize(decoration: TIbrDecorationWeb, dataSet: Object)

        if (decoration == NULL)

            throw_web(TIbrUndefinedValueException("TIbrDecoration::initialize", "decoration"));

        end;

        if (dataSet == NULL)

            throw_web(TIbrUndefinedValueException("TIbrDecoration::initialize", "dataSet"));

        end;

        /**
         * Константы
         */
        var constantColumn: Object = TIbrConstant().decoration.column;

        decoration.identifierDecoration = execExp("dataSet." + constantColumn.IDENTIFIER_DECORATION);
        decoration.identifierParent     = execExp("dataSet." + constantColumn.IDENTIFIER_PARENT);
        decoration.type                 = execExp("dataSet." + constantColumn.TYPE);
        decoration.name                 = execExp("dataSet." + constantColumn.NAME);
        decoration.sortWeight           = execExp("dataSet." + constantColumn.SORT_WEIGHT);
        decoration.isUser = TIbrSystem.rslBoolean(execExp("dataSet." + constantColumn.IS_USER));

    end;

    /**
     * Установить порядок сортировки
     *
     * @param decoration TIbrDecorationWeb Декорация (вкладка, группа)
     * @param sortWeightParameter Integer Порядок сортировки
     *
     * @return True, если порядок сортировки изменён успешно, иначе - False.
     */
    macro setSortWeight(decoration: TIbrDecorationWeb, sortWeightParameter: Integer): Bool

        if (sortWeightParameter == NULL)

            throw_web(TIbrIncorrectParameterException("TIbrDecoration::setSortWeight"));

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().setSortWeight
        (
            TIbrConstant.decoration.TABLE_NAME,
            TIbrConstant.decoration.PRIMARY_KEY,
            decoration.identifierDecoration,
            sortWeightParameter
        );

        /**
         * Сообщение
         */
        var message: String = "";

        if (decoration.type == TIbrConstant.decoration.type.TAB)

            message = TIbrResource.message.CHANGE_SORTWEIGHT_TAB;

        end;

        if (decoration.type == TIbrConstant.decoration.type.GROUP)

            message = TIbrResource.message.CHANGE_SORTWEIGHT_GROUP;

        end;

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    message,
                    decoration.name,
                    decoration.sortWeight,
                    sortWeightParameter
                )
            );
            decoration.sortWeight = sortWeightParameter;

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_CHANGE_SORTWEIGHT_DECORATION,
                        sortWeightParameter,
                        decoration.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Удаление декорации (группы, вкладки)
     *
     * @param decoration TIbrDecorationWeb Декорация (группа, вкладка)
     *
     * @return True, если декорация удалена, иначе - False
     */
    macro delete(decoration: TIbrDecorationWeb): Bool

        /**
         * Результат
         */
        var result: Bool = FALSE;

        result = TIbrBase().delete
        (
            TIbrConstant.decoration.TABLE_NAME,
            TIbrConstant.decoration.PRIMARY_KEY,
            decoration.identifierDecoration,
            "OR t_identifierParent = " + decoration.identifierDecoration
        );

        if (result)

            printLn
            (
                TIbrSystem.stringFormat
                (
                    TIbrResource.message.DELETE_DECORATION,
                    decoration.name
                )
            );

        else

            throw_web
            (
                TIbrException
                (
                    TIbrSystem.stringFormat
                    (
                        TIbrResource.message.NO_DELETE_DECORATION,
                        decoration.name
                    )
                )
            );

        end;

        return result;

    end;

    /**
     * Получить вкладку по наименованию
     *
     * @param tabName String Наименование вкладки
     *
     * @return Object Объект вкладки (без зависимых элементов)
     */
    macro getTab(tabName: String): Object

        if (TIbrSystem.isNullOrEmpty(tabName))

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getTab", "tabName"));

        end;

        /**
         * Результат - объект вкладки
         */
        var result: Object = NULL;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT ibrTab.t_identifierDecoration,"
        + "\n" + "       ibrTab.t_identifierParent,"
        + "\n" + "       ibrTab.t_identifierReport,"
        + "\n" + "       ibrTab.t_type,"
        + "\n" + "       ibrTab.t_name,"
        + "\n" + "       ibrTab.t_sortWeight,"
        + "\n" + "       ibrTab.t_isUser"
        + "\n" + "  FROM dWebIbrDecoration_dbt ibrTab"
        + "\n" + " WHERE ibrTab.t_type = " + TIbrConstant().decoration.type.TAB
        + "\n" + "   AND UPPER(ibrTab.t_name) = UPPER('" + tabName + "')";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_identifierDecoration", V_INTEGER);
        dataSet.setFieldType("t_identifierParent",     V_INTEGER);
        dataSet.setFieldType("t_identifierReport",     V_INTEGER);
        dataSet.setFieldType("t_type",                 V_INTEGER);
        dataSet.setFieldType("t_name",                 V_STRING);
        dataSet.setFieldType("t_sortWeight",           V_INTEGER);
        dataSet.setFieldType("t_isUser",               V_INTEGER);

        if (dataSet.moveNext())

            result = TIbrDecorationWeb();
            result.identifierDecoration = dataSet.identifierDecoration;
            result.identifierParent     = dataSet.identifierParent;
            result.identifierReport     = dataSet.identifierReport;
            result.type                 = dataSet.type;
            result.name                 = dataSet.name;
            result.sortWeight           = dataSet.sortWeight;
            result.isUser = TIbrSystem.rslBoolean(dataSet.isUser);

        else

            throw_web(TIbrException(TIbrResource().message.TAB_NOT_FOUND));

        end;

        return result;

    end;

    /**
     * Проверить существование вкладки по наименованию
     *
     * @param tabName String Наименование вкладки
     *
     * @return Bool True, если вкладка существует, иначе - FALSE
     */
    macro existsTab(tabName: String): Bool

        if (TIbrSystem.isNullOrEmpty(tabName))

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getTab", "existsTab"));

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT 1"
        + "\n" + "  FROM dWebIbrDecoration_dbt ibrTab"
        + "\n" + " WHERE ibrTab.t_type = " + TIbrConstant().decoration.type.TAB
        + "\n" + "   AND UPPER(ibrTab.t_name) = UPPER('" + tabName + "')";

        dataSet = TRsbDataSet(query);

        if (dataSet.moveNext())

            result = TRUE;

        else

            result = FALSE;

        end;

        return result;

    end;

    /**
     * Получить группу по наименованию
     *
     * @param groupName String Наименование группы
     *
     * @return Object Объект группы (без зависимых элементов)
     */
    macro getGroup(groupName: String): Object

        if (TIbrSystem.isNullOrEmpty(groupName))

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getGroup", "groupName"));

        end;

        /**
         * Результат - объект вкладки
         */
        var result: Object = NULL;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT ibrGroup.t_identifierDecoration,"
        + "\n" + "       ibrGroup.t_identifierParent,"
        + "\n" + "       ibrGroup.t_identifierReport,"
        + "\n" + "       ibrGroup.t_type,"
        + "\n" + "       ibrGroup.t_name,"
        + "\n" + "       ibrGroup.t_sortWeight,"
        + "\n" + "       ibrGroup.t_isUser"
        + "\n" + "  FROM dWebIbrDecoration_dbt ibrGroup"
        + "\n" + " WHERE ibrGroup.t_type = " + TIbrConstant().decoration.type.GROUP
        + "\n" + "   AND UPPER(ibrGroup.t_name) = UPPER('" + groupName + "')";

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_identifierDecoration", V_INTEGER);
        dataSet.setFieldType("t_identifierParent",     V_INTEGER);
        dataSet.setFieldType("t_identifierReport",     V_INTEGER);
        dataSet.setFieldType("t_type",                 V_INTEGER);
        dataSet.setFieldType("t_name",                 V_STRING);
        dataSet.setFieldType("t_sortWeight",           V_INTEGER);
        dataSet.setFieldType("t_isUser",               V_INTEGER);

        if (dataSet.moveNext())

            result = TIbrDecorationWeb();
            result.identifierDecoration = dataSet.identifierDecoration;
            result.identifierParent     = dataSet.identifierParent;
            result.identifierReport     = dataSet.identifierReport;
            result.type                 = dataSet.type;
            result.name                 = dataSet.name;
            result.sortWeight           = dataSet.sortWeight;
            result.isUser = TIbrSystem.rslBoolean(dataSet.isUser);

        else

            throw_web(TIbrException(TIbrResource().message.GROUP_NOT_FOUND));

        end;

        return result;

    end;

    /**
     * Проверить существование группы по наименованию
     *
     * @param groupName String Наименование вкладки
     *
     * @return Bool True, если вкладка существует, иначе - FALSE
     */
    macro existsGroup(groupName: String): Bool

        if (TIbrSystem.isNullOrEmpty(groupName))

            throw_web(TIbrUndefinedValueException("TIbrDecoration::getGroup", "existsGroup"));

        end;

        /**
         * Результат
         */
        var result: Bool = FALSE;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Набор данных
         */
        var dataSet: Object = NULL;

        query =  "SELECT 1"
        + "\n" + "  FROM dWebIbrDecoration_dbt ibrGroup"
        + "\n" + " WHERE ibrGroup.t_type = " + TIbrConstant().decoration.type.GROUP
        + "\n" + "   AND UPPER(ibrGroup.t_name) = UPPER('" + groupName + "')";

        dataSet = TRsbDataSet(query);

        if (dataSet.moveNext())

            result = TRUE;

        else

            result = FALSE;

        end;

        return result;

    end;

end;
