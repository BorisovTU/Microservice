/*
$Name: IbrBase.mac
$Module: Отчеты банка
$Description: Общая базовая функциональность классов
*/

/**
 * Общие алгоритмы отчета и составляющих
 */
class TIbrBase()

    /**
     * Установить порядок сортировки
     *
     * @param nameTable String Наименование таблицы
     * @param nameKeyField String Наименование ключевого поля
     * @param identifierObject Integer Идентификатор объекта
     * @param sortWeight Integer Порядок сортировки
     *
     * @return True, если порядок сортировки изменён, иначе, False.
     */
    macro setSortWeight
    (
        nameTable:        String,
        nameKeyField:     String,
        identifierObject: Integer,
        sortWeight:       Integer
    ): Bool

        if ((TIbrSystem.isNullOrEmpty(nameTable))   OR
            (TIbrSystem.isNullOrEmpty(nameKeyField) OR
            (valType(identifierObject) == V_UNDEF)  OR
            (valType(sortWeight)       == V_UNDEF)))

            throw_web(TIbrIncorrectParameterException("TIbrBase::setSortWeight"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = TRUE;

        query =  "UPDATE " + nameTable + " ibrTable"
        + "\n" + "   SET ibrTable.t_sortWeight = " + sortWeight
        + "\n" + " WHERE ibrTable." + nameKeyField + " = " + identifierObject;

        result = result AND sql_execute(query);
        result = result AND sql_execute("COMMIT");

        return result;

    end;

    /**
     * Удаление объекта
     *
     * @param nameTable String Наименование таблицы
     * @param nameKeyField String Наименование ключевого поля
     * @param identifierObject Integer Идентификатор объекта
     * @param sqlWhere String Sql-выражение дополнительного условия
     *
     * @return True, если объект удален, иначе, False.
     */
    macro delete
    (
        nameTable:        String,
        nameKeyField:     String,
        identifierObject: Integer,
        sqlWhere:         String
    ): Bool

        if ((TIbrSystem.isNullOrEmpty(nameTable))   OR
            (TIbrSystem.isNullOrEmpty(nameKeyField)) OR
            (valType(identifierObject) == V_UNDEF))

            throw_web(TIbrIncorrectParameterException("TIbrBase::delete"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = TRUE;

        query =  "DELETE FROM " + nameTable + " ibrTable"
        + "\n" + " WHERE ibrTable." + nameKeyField + " = " + identifierObject;

        if (sqlWhere != NULL)

            query = query + "\n" + " " + sqlWhere;

        end;

        result = result AND sql_execute(query);
        result = result AND sql_execute("COMMIT");

        return result;

    end;

    /**
     * Изменить параметры объекта
     *
     * @param nameTable String Наименование таблицы
     * @param nameKeyField String Наименование ключевого поля
     * @param identifierObject Integer Идентификатор объекта
     * @param sqlSet String Sql-выражение установки нового значения
     * @param sqlWhere String Sql-выражение дополнительного условия
     *
     * @return TRUE, если значения изменены успешно, иначе - FASLE
     */
    macro update
    (
        nameTable:        String,
        nameKeyField:     String,
        identifierObject: Integer,
        sqlSet:           String,
        sqlWhere:         String
    ): Bool

        if ((TIbrSystem.isNullOrEmpty(nameTable))    OR
            (TIbrSystem.isNullOrEmpty(nameKeyField)) OR
            (TIbrSystem.isNullOrEmpty(sqlSet))       OR
            (valType(identifierObject) == V_UNDEF))

            throw_web(TIbrIncorrectParameterException("TIbrBase::update"));

        end;

        /**
         * Выражение sql-запроса
         */
        var query: String = "";

        /**
         * Результат
         */
        var result: Bool = TRUE;

        query =  "UPDATE " + nameTable + " ibrTable"
        + "\n" + "   SET " + sqlSet
        + "\n" + " WHERE ibrTable." + nameKeyField + " = " + identifierObject;

        if (sqlWhere != NULL)

            query = query + "\n" + " " + sqlWhere;

        end;

        result = result AND sql_execute(query);
        result = result AND sql_execute("COMMIT");

        return result;

    end;

end;
