/*
$Name:          ibr_r_cmagz.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Кассовый журнал"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;      
import r_cmagz;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_MAGAZ = "Параметры";
private const GRP_PARAMS = "Дополнительные параметры";

private const gRep = TAB_REPORT + "@" + GRP_MAGAZ + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";

private var fileName = "ibr_cashJournal_web";  
 
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_r_cmagz", "Кассовый журнал", "ibr_r_cmagz.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_MAGAZ, 1);
    var groupParams = facade.createGroup(GRP_PARAMS, 2);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));

    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    groupReport.add(facade.createFieldText("Пачка", "Пачка", 0));
    groupReport.add(facade.createFieldUser("Проверяющий", "Проверяющий", 0));

    groupReport.add(facade.createFieldNameAlg("ВидЖурнала", "Вид журнала", 3, 3960));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет", "Лицевой счета")); 

    facade.resetFieldsOrder();  
    groupParams.add(facade.createFieldNameAlg("ФормаЖурнала", "Форма кассового журнала", 1, 3961));
    groupParams.add(facade.createFieldSort("СортировкаОтчетов", "Сортировка отчетов", "G", 1721));
    groupParams.add(facade.createFieldSort("СортировкаДокументов", "Сортировка документов", "6", 1719));

    groupParams.add(facade.createFieldCheckBox("СуммыПрописью", "Суммы прописью", true));

    tabReport.add(groupReport);
    tabReport.add(groupParams);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();
    var i      :integer = 0;

    var PatternReports = strTransferByWord(getValue(report, gPrm+"СортировкаОтчетов").valueString, 1);

    while (i < PatternReports.size)
        PatternReports[i] = TIbrCommon().getElementLlValues(PatternReports[i], 1721);
        i = i + 1;
    end; 

    result.valueBoolean = makeReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                     getValue(report, gRep+"Структура").valueInteger,
                                     getValue(report, gRep+"Режим").valueInteger,
                                     getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                     getValue(report, gRep+"НачалоПериода").valueDate,
                                     getValue(report, gRep+"КонецПериода").valueDate, 
                                     0, // номер группы, нет виджета в вебе, ограничение реализации
                                     getValue(report, gRep+"Операционист").valueInteger,
                                     getValue(report, gRep+"Пачка").valueInteger,
                                     getValue(report, gRep+"Проверяющий").valueInteger,
                                     getValue(report, gRep+"ВидЖурнала").valueInteger,
                                     getValue(report, gRep+"ЛицевойСчет").valueString,
                                     getValue(report, gPrm+"ФормаЖурнала").valueInteger,
                                     PatternReports,
                                     strTransferByWord(getValue(report, gPrm+"СортировкаДокументов").valueString, 1),
                                     ternary(getValue(report, gPrm+"СуммыПрописью").valueBoolean, 88, 0),
                                     fileName);

    // При удачном выполнении
    if (result.valueBoolean != 0)
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    else
        repTxtFilesQueue().add(reportBodyFileName);
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;