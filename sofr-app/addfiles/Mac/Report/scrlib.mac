/*
╔════════════════════════════════════════════════════════════════════════════╗
║  Отдел Поддержки           R-Style SoftWare Lab               Вачьян Юрий  ║
╠════════════════════════════════════════════════════════════════════════════╣
║                           Библиотека скроллингов                           ║
╚════════════════════════════════════════════════════════════════════════════╝
┌────────────────────────────────────────────────────────────────────────────┐
│21.12.99 - Создание. Необходим tscr.mac и scrlib.lbr                        │
└────────────────────────────────────────────────────────────────────────────┘
*/

Import tscr, ifmac;

var Фильтр = TArray;
var НачальноеЗначение;
var ПервыйВызов;
var VersionRSBank = "5.10";

Macro ЗаменитьМетоды(Name)
  GenAttach(Скроллинг, "ScrNext",       Name+"Next");
  GenAttach(Скроллинг, "ScrPrev",       Name+"Prev");
  GenAttach(Скроллинг, "ScrReWind",     Name+"ReWind");
End;

/*************************************************************************/

Macro ПозицияОткрытогоЛицевого( Валюта, Счет, ДоПосле)
var f, BoolOpen;
        If(Валюта == 0)
                f = TBFile("account");
                f.Rec.Code_Currency = Валюта
        Else
                f = TBFile("account$");
        End;
 f.Rec.Account = Счет;
 If(ДоПосле == "До")
  BoolOpen = f.GetLE;
  While (BoolOpen and Not
        ((f.Rec.Open_Close == "") and
        (f.Rec.Code_Currency == Валюта)))
   BoolOpen = f.Prev;
  End;
 Else
  BoolOpen = f.GetGE;
  While (BoolOpen and Not
        ((f.Rec.Open_Close == "") and
        (f.Rec.Code_Currency == Валюта)))
   BoolOpen = f.Next;
  End;
 End;
 If(BoolOpen)
  Return f.GetPos
 Else
  Return 0
 End;
End;

Macro AccBalNmNext
var    Position;
 If(ПервыйВызов)
        ПервыйВызов = False;
        If(StrLen(Фильтр(1)) < 20)
                Скроллинг.f.Rec.Open_Close      = "";
                Скроллинг.f.Rec.Code_Currency   = Фильтр(0);
                Скроллинг.f.Rec.Sort            = Фильтр(1);
        Return  Скроллинг.f.GetGE and
                (Скроллинг.f.Rec.Open_Close == "") and
                ((Скроллинг.f.Rec.Code_Currency == Фильтр(0)) or (Фильтр(0) == 0)) and
                (Index(Скроллинг.f.Rec.Sort, Фильтр(1)) == 1)
        Else
                Position = ПозицияОткрытогоЛицевого(Фильтр(0) , Фильтр(1), "После");
                If(Position == 0)
                        Return False
                Else
                        Скроллинг.f.GetDirect(Position);
                        Фильтр(1) = "";
                        Return True
                End;
        End;
 Else
        Return  Скроллинг.f.Next and
                (Скроллинг.f.Rec.Open_Close == "") and
                ((Скроллинг.f.Rec.Code_Currency == Фильтр(0)) or (Фильтр(0) == 0)) and
                (Index(Скроллинг.f.Rec.Sort, Фильтр(1)) == 1)
 End;
End;

Macro AccBalNmPrev
var    Position;
 If(ПервыйВызов)
        ПервыйВызов = False;
        If(StrLen(Фильтр(1)) < 20)
                Скроллинг.f.Rec.Open_Close      = "";
                Скроллинг.f.Rec.Code_Currency   = Фильтр(0);
                Скроллинг.f.Rec.Sort            = mkstr(254,40);
                StrSet(Скроллинг.f.Rec.Sort,1,Фильтр(1));
                Return Скроллинг.f.GetLE and
                                (Скроллинг.f.Rec.Open_Close == "") and
                                ((Скроллинг.f.Rec.Code_Currency == Фильтр(0)) or (Фильтр(0) == 0)) and
                                (Index(Скроллинг.f.Rec.Sort, Фильтр(1)) == 1)
        Else
                Position = ПозицияОткрытогоЛицевого(Фильтр(0) , Фильтр(1), "До");
                If(Position == 0)
                        Return False
                Else
                        Скроллинг.f.GetDirect(Position);
                        Фильтр(1) = "";
                        Return True
                End;
        End;
 Else
        Return  Скроллинг.f.Prev and
                (Скроллинг.f.Rec.Open_Close == "") and
                ((Скроллинг.f.Rec.Code_Currency == Фильтр(0)) or (Фильтр(0) == 0)) and
                (Index(Скроллинг.f.Rec.Sort, Фильтр(1)) == 1)
 End;
End;

Macro AccBalNmReWind
 ПервыйВызов = True;
 Скроллинг.ScrReWind;
End;

Macro ЛицевыеБалансовыеНазвания(Файл, Панель)
        If(ValType(Файл) == V_Undef)
                If(Фильтр(0) == 0)
                        Файл = TBFile("account",Null,3)
                Else
                        Файл = TBFile("account$",Null,4)
                End;
        End;
        Скроллинг = tscr(Файл, Панель,  "Account",      "Account",
                                        "Balance",      "Balance",
                                        "NameAccount",  "NameAccount");

        ЗаменитьМетоды("AccBalNm");
        Скроллинг.FindVal = Фильтр(1);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;

Macro ЛицевыеВалютаНазвания(Файл, Панель)
        If(ValType(Файл) == V_Undef)
                If(Фильтр(0) == 0)
                        Файл = TBFile("account",Null,3)
                Else
                        Файл = TBFile("account$",Null,4)
                End;
        End;
        Скроллинг = tscr(Файл, Панель,  "Account",      "Account",
                                        "Balance",      "Balance",
                                        "NameAccount",  "NameAccount");

        ЗаменитьМетоды("AccBalNm");
        Скроллинг.FindVal = Фильтр(1);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;

Macro ЛицевыеФилиалыНазвания( Панель, Файл )


        If(ValType(Файл) == V_Undef)
                If(Фильтр(0) == 0)
                        Файл = TBFile("account",Null,3)
                Else
                        Файл = TBFile("account$",Null,4)
                End;
        End;
        Скроллинг = tscr(Файл, Панель,  "Account",      "Account",
                                        "Department",   "Department",
                                        "NameAccount",  "NameAccount");

        ЗаменитьМетоды("AccBalNm");
        Скроллинг.FindVal = Фильтр(1);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;


/********Скроллинг балансовых счетов***********************************************************/
/*
        Фильтр(0) - Глава
        Фильтр(1) - Валюта
        Фильтр(2) - План
        Фильтр(3) - Счет
*/

Macro BalKndNmNext
        If(ПервыйВызов)
                ПервыйВызов = False;
                If(Скроллинг.f.FldIndex("Chapter") > -1)
                        Скроллинг.f.("Chapter") = Фильтр(0);
                End;
                Скроллинг.f.("iNumPlan")        = Фильтр(2);
                Скроллинг.f.("Balance")         = Фильтр(3);
                Return  Скроллинг.f.GetGE and
                        IfChapter(Скроллинг.f, Фильтр(0)) and
                        (Скроллинг.f.("iNumPlan") == Фильтр(2)) and
                        (Index(Скроллинг.f.("Balance"), Фильтр(3)) == 1)
        Else
                Return  Скроллинг.f.Next and
                        IfChapter(Скроллинг.f, Фильтр(0)) and
                        (Скроллинг.f.("iNumPlan") == Фильтр(2)) and
                        (Index(Скроллинг.f.("Balance"), Фильтр(3)) == 1)
        End;
End;

Macro BalKndNmPrev
        If(ПервыйВызов)
                ПервыйВызов = False;
                If(Скроллинг.f.FldIndex("Chapter") > -1)
                        Скроллинг.f.("Chapter") = Фильтр(0);
                End;
                Скроллинг.f.("iNumPlan")        = Фильтр(2);
                Скроллинг.f.("Balance")         = mkstr(254,12);
                StrSet(Скроллинг.f.("Balance"), 1, Фильтр(3));
                Return  Скроллинг.f.GetLE and
                        IfChapter(Скроллинг.f, Фильтр(0)) and
                        (Скроллинг.f.("iNumPlan") == Фильтр(2)) and
                        (Index(Скроллинг.f.("Balance"), Фильтр(3)) == 1)
        Else
                Return  Скроллинг.f.Prev and
                        IfChapter(Скроллинг.f, Фильтр(0)) and
                        (Скроллинг.f.("iNumPlan") == Фильтр(2)) and
                        (Index(Скроллинг.f.("Balance"), Фильтр(3)) == 1)
        End;
End;

Macro BalKndNmReWind
        ПервыйВызов = True;
        Скроллинг.ScrReWind;
End;

Macro БалансовыеВидНазвания( Панель, Файл )
     var i;
        Фильтр = TArray;

        i = 2;
        While(GetParm( i, Фильтр(i-2) ))
                i = i + 1
        End;

        If(Valtype(Фильтр(0)) != V_Integer)
                Фильтр(0) = 1;
        End;

        If(Valtype(Фильтр(1)) != V_Integer)
                Фильтр(1) = 0;
        End;

        If(Valtype(Фильтр(2)) != V_Integer)
                Фильтр(2) = 0;
        End;

        If(Valtype(Фильтр(3)) != V_String)
                Фильтр(3) = "";
        End;
        If(ValType(Файл) == V_Undef)
                If((Фильтр(0) == 0) or (Фильтр(0) ==1) or (VersionRSBank == "5.10"))
                        If(Фильтр(1) == 0)
                                Файл = TBFile("balance")
                        Else
                                Файл = TBFile("balance")
                        End;
                Elif(Фильтр(0) > 1)
                        If(Фильтр(1) == 0)
                                Файл = TBFile("obalanc")
                        Else
                                Файл = TBFile("obalanc$")
                        End;
                End;
        End;

        Скроллинг = tscr(Файл, Панель,  "Balance",      "Balance",
                                        "Kind_Account", "Kind_Account",
                                        "Name_Part",    "Name_Part");

        ЗаменитьМетоды("BalKndNm");

        Скроллинг.FindVal = Фильтр(3);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;


/********Скроллинг филиалов***********************************************************/
/*
Фильтр(0) - Имя/Индекс поля
Фильтр(1) - Значение
*/

Macro DepCNNamNext
        While(Скроллинг.f.Next)
/*
                MsgBox(String(Скроллинг.f.(Фильтр(0)))," ",Фильтр(1));
*/
                If(Index(StrUpr(String(Скроллинг.f.(Фильтр(0)))), Фильтр(1)) != 0)
                        Return True
                End;
        End;
        Return False
End;

Macro DepCNNamPrev

        While(Скроллинг.f.Prev)
/*
                MsgBox(String(Скроллинг.f.(Фильтр(0))), " ",Фильтр(1));
*/
                If(Index(StrUpr(String(Скроллинг.f.(Фильтр(0)))), Фильтр(1)) != 0)
                        Return True
                End;
        End;
        Return False
End;

Macro DepCNNamReWind
        ПервыйВызов = True;
        Скроллинг.f.ReWind
End;

Macro ФилиалыКодыНазвания(Файл, Панель)
        If(ValType(Файл) == V_Undef)
                Файл = TBFile("dp_dep")
        End;
        Скроллинг = tscr(Файл, Панель,
                                        "Name",      "Name",
                                        "Code",      "Code",
                                        "Code",      "Code");
        ЗаменитьМетоды("DepCNNam");
        Скроллинг.FindVal = Фильтр(1);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;


/********Скроллинг валют***********************************************************/
/*
Фильтр(0) - Имя/Индекс поля
Фильтр(1) - Значение
*/

Macro CurCodNmNext
        While(Скроллинг.f.Next)
                If(Not ((Скроллинг.f.(0) == 0) and Фильтр(2)))
                        If(Index(StrUpr(String(Скроллинг.f.(Фильтр(0)))), Фильтр(1)) != 0)
                                Return True
                        End;
                End;
        End;
        Return False
End;

Macro CurCodNmPrev
        While(Скроллинг.f.Prev)
                If(Not ((Скроллинг.f.(0) == 0) and Фильтр(2)))
                        If(Index(StrUpr(String(Скроллинг.f.(Фильтр(0)))), Фильтр(1)) != 0)
                                Return True
                        End;
                End;
        End;
        Return False
End;

Macro CurCodNmReWind
        ПервыйВызов = True;
        Скроллинг.f.ReWind
End;

Macro ВалютыКодыНазвания( Панель, Файл )
     var i;
        Фильтр = TArray;

        i = 2;
        While(GetParm( i, Фильтр(i-2) ))
                i = i + 1
        End;

        If(Valtype(Фильтр(1)) == V_Undef)
                Фильтр(1) = "";
        Else
                Фильтр(1) = String(Фильтр(1));
        End;

        If(VersionRSBank == "5.10")
                If(ValType(Файл) == V_Undef)
                        Файл = TBFile("fininstr")
                End;
                Скроллинг = tscr(Файл, Панель,  "ISO_Number",   "ISO_Number",
                                                "Ccy",          "Ccy",
                                                "Name",         "Name");
                If(Valtype(Фильтр(0)) == V_Undef)
                        Фильтр(0) = "ISO_Number";
                End;
        Else
                If(ValType(Файл) == V_Undef)
                        Файл = TBFile("currency")
                End;
                Скроллинг = tscr(Файл, Панель,  "ISO_Number",   "Code_Currency",
                                                "Ccy",          "Short_Name",
                                                "Name",         "Name_Currency");
                If(Valtype(Фильтр(0)) == V_Undef)
                        Фильтр(0) = "Code_Currency";
                End;
        End;

        If(Valtype(Фильтр(2)) != V_Bool)
                Фильтр(2) = False;
        End;

        ЗаменитьМетоды("CurCodNm");
        Скроллинг.FindVal = Фильтр(1);
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;

/********Скроллинг пользователей***************************************************/
/*
Фильтр(0) - Значение поля диалога
*/

Macro OperNameNext
 var bNext;
        If(ПервыйВызов)
                ПервыйВызов = False;
                Скроллинг.f.Rec.Oper = Фильтр(0);
                bNext = Скроллинг.f.GetGE;
        Else
                bNext = Скроллинг.f.Next
        End;
        While   (bNext and (Скроллинг.f.Rec.UserClosed != ""))
                bNext = Скроллинг.f.Next
        End;
        Return bNext and (Скроллинг.f.Rec.UserClosed == "")
End;

Macro OperNamePrev
   var     bNext = Скроллинг.f.Prev;
        While   (bNext and (Скроллинг.f.Rec.UserClosed != ""))
                bNext = Скроллинг.f.Prev
        End;
        Return bNext and (Скроллинг.f.Rec.UserClosed == "")
End;

Macro OperNameReWind
        Скроллинг.f.ReWind
End;

Macro ПользователиСИменами( Панель, Файл )
  var i;
        If(ValType(Файл) == V_Undef)
                Файл = TBFile("person")
        End;

        Фильтр = TArray;

        i = 2;
        While(GetParm( i, Фильтр(i-2) ))
                i = i + 1
        End;

        If(Valtype(Фильтр(0)) == V_Undef)
                Фильтр(0) = "0"
        End;

        Фильтр(0) = String(Фильтр(0));

        Скроллинг = tscr(Файл, Панель,  "Oper", "Oper",
                                        "Name", "Name");
        ЗаменитьМетоды("OperName");
        ПервыйВызов = True;
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;


/*
Фильтр(0) - Значение поля диалога
*/

Macro OperNameФильтрNext
        While(Скроллинг.f.Next)
                If((/*Скроллинг.f.Rec.UserBlocked
                        +*/ Скроллинг.f.Rec.UserClosed
                        + Скроллинг.f.Rec.ChangeParolNextTime == "") and
                        ((Фильтр(0) == "0") or
                        (Index(String(Скроллинг.f.Rec.Oper), Фильтр(0)) == 1)))
                        Return True
                End;
        End;
        Return False
End;

Macro OperNameФильтрPrev
        While(Скроллинг.f.Prev)
                If((/*Скроллинг.f.Rec.UserBlocked
                        +*/ Скроллинг.f.Rec.UserClosed
                        + Скроллинг.f.Rec.ChangeParolNextTime == "") and
                        ((Фильтр(0) == "0") or
                        (Index(String(Скроллинг.f.Rec.Oper), Фильтр(0)) == 1)))
                        Return True
                End;
        End;
        Return False
End;

Macro OperNameФильтрReWind
        Скроллинг.f.ReWind
End;

Macro ПользователиСИменамиФильтр( Панель, Файл )
     var i;
        If(ValType(Файл) == V_Undef)
                Файл = TBFile("person")
        End;

        Фильтр = TArray;

        i = 2;
        While(GetParm( i, Фильтр(i-2) ))
                i = i + 1
        End;

        If(Valtype(Фильтр(0)) == V_Undef)
                Фильтр(0) = "0"
        End;

        Фильтр(0) = String(Фильтр(0));

        Скроллинг = tscr(Файл, Панель,  "Oper", "Oper",
                                        "Name", "Name");
        ЗаменитьМетоды("OperNameФильтр");
        Return Rundialog(Панель,"ОбработчикСкроллинга");
End;


/****************Скроллинг клиентов********************/
/*
Фильтр(0) - Номер клиента
*/

Macro ClnNameNext
        If      ( ПервыйВызов )
                ПервыйВызов = False;
                Скроллинг.f.Rec.Client  = Фильтр(0);
                Return Скроллинг.f.GetGE
        Else
                Return Скроллинг.f.Next
        End;
End;

Macro ClnNamePrev
        Return Скроллинг.f.Prev
End;

Macro ClnNameReWind
        Скроллинг.f.ReWind
End;

Macro КлиентыНаименования( Панель, Файл )
        If(ValType(Файл) == V_Undef)
                Файл = TBFile("client")
        End;

        GetParm( 2, Фильтр(0) );
        If(Valtype(Фильтр(0)) == V_Undef)
                Фильтр(0) = 0
        End;

        ПервыйВызов = True;

        Скроллинг = tscr(Файл, Панель,  "Client",       "Client",
                                        "Name",         "Name_Client");
        ЗаменитьМетоды( "ClnName" );
        Скроллинг.FindVal = Фильтр(0);
        Return Rundialog( Панель, "ОбработчикСкроллинга");
End;
