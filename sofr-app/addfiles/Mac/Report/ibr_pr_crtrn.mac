/*
$Name:        ibr_pr_crtrn.mac
$Module:      Отчеты банка
$Description: Отчет по исправительным оборотам
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;      
import pr_crtrn;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_CRT = "Параметры";
private const gRep = TAB_REPORT + "@" + GRP_CRT + "@";

private var fileName = "ibr_pr_crtrn_web";  

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_pr_crtrn", "Отчет по исправительным оборотам", "ibr_pr_crtrn.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_CRT, 1);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCheckBox("ИсклГлавы", "С искл. главами", false));

    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", ""));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Период с"));
    groupReport.add(facade.createFieldDate("КонецПериода", "          по"));

    groupReport.add(facade.createFieldUser("Операционист", "Номер пользователя", 0));

    groupReport.add(facade.createFieldBalance("Балансовый", "Балансовый"));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет", "Лицевой счета"));

    groupReport.add(facade.createFieldNameAlg("ВидИсправительных", "Вид исправительных", 1, 3962));

    groupReport.add(facade.createFieldCheckBox("ПроводкиВОтчете", "Показывать проводки в отчете", false));

    tabReport.add(groupReport);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();
    var numPlan         = TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString);

    result.valueBoolean = CorReport1(getValue(report, gRep+"Подразделения").valueInteger,
                                     getValue(report, gRep+"Структура").valueInteger,
                                     getValue(report, gRep+"Режим").valueInteger,
                                     TIbrCommon().getArrayAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString, 
                                                                         numPlan,
                                                                         getValue(report, gRep+"ИсклГлавы").valueBoolean),
                                     getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                     getValue(report, gRep+"НачалоПериода").valueDate,
                                     getValue(report, gRep+"КонецПериода").valueDate,  
                                     getValue(report, gRep+"Операционист").valueInteger,
                                     getValue(report, gRep+"Балансовый").valueString,
                                     getValue(report, gRep+"ЛицевойСчет").valueString,
                                     getValue(report, gRep+"ВидИсправительных").valueInteger,
                                     getValue(report, gRep+"ПроводкиВОтчете").valueBoolean,
                                     fileName);
   if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;    

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;