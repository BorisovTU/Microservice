/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                           R-Style Software Lab

  File Name   : prpccalc.mac                      26.05.2011
  Programmer  : gww
  Description : Ведомость оплат процентов
  Comment     :
  Modify
└───────────────────────────────────────────────────────────────────────────*/
import FIInter;
import Reporting;
import rep_lib;
import ofstream;



import testLib;

private CONST ALLFININSTR = -1,
              ALLCURRENCY = -2,
              DISTRIBUTION = "Размещение",
              ATTRACTION = "Привлечение";

private class TPercPayPrint(parameters)
    var m_tempOfstream = TOfstream("percPay");
    var m_tablePrinter : CTableReport;
    var m_tableDocPrinter = CTableReport(0, true, true);
    var m_parameters;

    macro setHead()
        m_tablePrinter.AddColumn("Дата|операции"   , 10, AL_CENTER);
        m_tablePrinter.AddColumn("Начальная|дата"  , 14, AL_CENTER);
        m_tablePrinter.AddColumn("Конечная|дата"   , 14, AL_CENTER);
        m_tablePrinter.AddColumn("Сумма|процентов" , 16, AL_RIGHT);
        m_tablePrinter.AddColumn("Фактическая|сумма оплаты", 16, AL_RIGHT);
        m_tablePrinter.AddColumn("Фактическая| дата", 11, AL_CENTER);
        m_tablePrinter.AddColumn("Статус операции", 25, AL_LEFT);

        m_tableDocPrinter.AddColumn("№док."  , 5, AL_CENTER);
        m_tableDocPrinter.AddColumn("Статус"  , 11, AL_CENTER);
        m_tableDocPrinter.AddColumn("Дата"  , 10, AL_CENTER);
        m_tableDocPrinter.AddColumn("Счет Дт"  , 25, AL_CENTER);
        m_tableDocPrinter.AddColumn("Вал"  , 3, AL_CENTER);
        m_tableDocPrinter.AddColumn("Сумма Дт"  , 16, AL_RIGHT);
        m_tableDocPrinter.AddColumn("Счет Кт"  , 25, AL_CENTER);
        m_tableDocPrinter.AddColumn("Вал"  , 3, AL_CENTER);
        m_tableDocPrinter.AddColumn("Сумма Кт"  , 16, AL_RIGHT);
    end;

    macro getReportWidth()
        return m_tablePrinter.GetSumLen();
    end;

    macro printNoData(directionKind, distributionIsEmpty : bool, attractionIsEmpty : bool)
        if (((directionKind == 2) or (directionKind == -1)) and (distributionIsEmpty == true))
            printLn();
            printLn("Направление: " + DISTRIBUTION);
            println("Нет данных для отчета");
        end;
        if (((directionKind == 1) or (directionKind == -1)) and (attractionIsEmpty == true))
            printLn();
            printLn("Направление: " + ATTRACTION);
            println("Нет данных для отчета");
        end;
    end;

    macro printReportHead()
        var dateIn  = m_parameters.getDateIn();
        var dateOut = m_parameters.getDateOut();
        var clientName;
        var backOfficeName;
        var dataSet;

        if(m_parameters.getClient() == -1)
            clientName = "Все";
        else
            dataSet = TRsbDataSet("SELECT party.t_shortName client FROM drepParty_vw party WHERE party.t_id = " + m_parameters.getClient());
            dataSet.next();
            clientName = dataSet.client;
        end;

        if(m_parameters.getSubSystem() == -1)
            backOfficeName = "Все";
        else
            dataSet = TRsbDataSet("SELECT val.t_code backOffice FROM dllvalues_dbt val WHERE val.t_list = 1118 AND val.t_element = " + m_parameters.getSubSystem());
            dataSet.next();
            backOfficeName = dataSet.backOffice;
        end;

        printBankHeader(m_parameters.getDepartmentCode(), m_parameters.getOrgStructure(), getReportWidth());

        println();
        println(strAlign("Ведомость оплат процентов", getReportWidth(), STR_ALIGN_CENTER));
        printLn();

        if (dateIn == dateOut)
            printLn(strAlign("за " + dateIn, getReportWidth(), STR_ALIGN_CENTER));
        else
            printLn(strAlign("c " + dateIn + " по " + dateOut, getReportWidth(), STR_ALIGN_CENTER));
        end;


        printLn();
        printLn("Бэк-офис: " + backOfficeName);
        printLn();
        printLn("Клиент: " + clientName);
        printLn("Процентный договор: " + ternary(m_parameters.getPercContractNumber() == 0, "Все", m_parameters.getPercContractNumber()));
        printLn();
        printLn("Лицевой счет: " + ternary(m_parameters.getAccount() == "", "Все", m_parameters.getAccount()));

        if (m_parameters.getPercContractChargeObjectKind() != 1) //лицевой счет
            printLn();
            printLn("Вид объекта: " + m_parameters.getSubSystem());
            printLn("Номер объекта: " + m_parameters.getSubSystem());
        end;

    end;

    macro printHead()
        [┌────────────┬─────────────────────────────────┬──────────────────┬──────────────────┬─────────────┬───────────────────────────┐
         │    Дата    │          Период оплат           │ Сумма процентов  │    Фактическая   │ Фактическая │          Статус           │
         │  операции  ├────────────────┬────────────────┤                  │   сумма оплаты   │    дата     │         операции          │
         │            │ Начальная дата │ Конечная дата  │                  │                  │             │                           │
         ├────────────┼────────────────┼────────────────┼──────────────────┼──────────────────┼─────────────┼───────────────────────────┤
        ];
    end;

    macro printScheduleHead()
                [┌────────────┬────────────────┬────────────────┬──────────────────┬──────────────────┬─────────────┬───────────────────────────┐];
    end;

    macro printItog(nameItog1, nameItog2, sum1, sum2, lines)

        if (lines)
                [│##############################################│##################│##################│                                         │] ("Итого " + nameItog1, sum1, sum2);
            if (nameItog2 != "")
                [│##############################################│                  │                  │                                         │] (nameItog2);
            end;
                [└──────────────────────────────────────────────┴──────────────────┴──────────────────┴─────────────────────────────────────────┘];
        else
                [ ############################################## ################## ##################] ("Итого " + nameItog1, sum1, sum2);
            if (nameItog2 != "")
                [ ##############################################] (nameItog2);
            end;

        end;
    end;

    macro execute(dataSet : Object)
        var isEmpty : bool = true;
        var curCurrency = null;
        var currencySum = 0;
        var currencySumFact = 0;
        var curContract = null;
        var contractName = "";
        var contractSum = 0;
        var contractSumFact = 0;
        var curOper = null;
        var operSum = 0;
        var operSumFact = 0;
        var operName = "";
        var curBranch = null;
        var branchName = "";
        var branchSum = 0;
        var branchSumFact = 0;
        var curClient = null;
        var clientSum = 0;
        var clientSumFact = 0;
        var clientName = "";
        var curDirection = null;
        var needTotals = false;
        var needPrev = false;
        var isNext = false;
        var totalsModel = m_parameters.getTotalsModel();
        var contractDirection = m_parameters.getPercContractDirection();
        var curSchedule;
        var needPayments = m_parameters.getShowPayments();
        var i;
        var attractionIsEmpty : bool = true;   // Направление "Привлечение" пусто ?
        var distributionIsEmpty : bool = true;   // Направление "Размещение" пусто ?

        printReportHead();

        isNext = dataSet.next();

        while (isNext)
            isEmpty = false;
            needTotals = false;

            if (curContract != dataSet.contractId)
                if (curContract != null)
                    printItog("по ПД ", contractName, contractSum, contractSumFact, true);
                    needTotals = true;

                    if (curCurrency != dataSet.fiId)
                        curBranch = null;
                        curOper   = null;
                        curClient = null;
                    end;
                end;

                curContract = dataSet.contractId;
                contractName = dataSet.contractNumber + ", " + dataSet.account;
                contractSum = 0;
                contractSumFact = 0;
            end;

            if (curDirection != dataSet.direction)
                curBranch = null;
                curOper = null;
                curBranch = null;
                curClient = null;
                curCurrency = null;
            end;

            if (needTotals)
                if (   (curBranch != dataSet.branchId)
                    or (curOper != dataSet.operId)
                    or (curClient != dataSet.clientId)
                   )
                    i = 0;
                    while (i < totalsModel.size)
                        if (totalsModel(i) == REP_PATTERNSORT_DEPARTMENT)
                            if (curBranch != dataSet.branchId)
                                printItog("по подразделению: ", branchName, branchSum, branchSumFact, false);

                                curBranch  = dataSet.branchId;
                                branchName = dataSet.branchName;
                                branchSum = 0;
                                branchSumFact = 0;
                            end;
                        elif (totalsModel(i) == REP_PATTERNSORT_OPER)
                            if (curOper != dataSet.operId)
                                printItog("по операционисту: ", operName, operSum, operSumFact, false);                                curOper  = dataSet.operId;
                                operName = dataSet.operName;
                                operSum = 0;
                                operSumFact = 0;
                            end;
                        elif (totalsModel(i) == REP_PATTERNSORT_CLIENT)
                            if (curClient != dataSet.clientId)
                                printItog("по клиенту: ", clientName, clientSum, clientSumFact, false);

                                curClient  = dataSet.clientId;
                                clientName = dataSet.clientName;
                                clientSum = 0;
                                clientSumFact = 0;
                            end;
                        end;

                        i = i + 1;
                    end;
                end;
            end;

            if (curDirection != dataSet.direction)
                if (curDirection != null)
                    printItog("сумма оплаченных процентов ", "", currencySum, currencySumFact, false);
                end;
                curDirection = dataSet.direction;

                printLn();
                println("Направление: " + ternary(dataSet.direction == 2, DISTRIBUTION, ATTRACTION));
                if (dataSet.direction == 2)
                    distributionIsEmpty = false;
                else
                    attractionIsEmpty = false;
                end;
            end;

            if (curCurrency != dataSet.fiId)
                if (curCurrency != null)
                    printItog("сумма оплаченных процентов ", "", currencySum, currencySumFact, false);
                end;

                needTotals = true;
                curBranch   = dataSet.branchId;
                branchName  = dataSet.branchName;
                curOper     = dataSet.operId;
                operName    = dataSet.operName;
                curClient   = dataSet.clientId;
                clientName  = dataSet.clientName;
                curCurrency = dataSet.fiId;
                currencySum = 0;
                currencySumFact = 0;

                printLn();
                println("Валюта " + dataSet.fiCode + " " + dataSet.fiName);
                printHead();

            end;

            branchSum   = branchSum   + dataSet.sum;
            operSum     = operSum     + dataSet.sum;
            clientSum   = clientSum   + dataSet.sum;
            contractSum = contractSum + dataSet.sum;
            currencySum = currencySum + dataSet.sum;

            branchSumFact   = branchSumFact   + dataSet.factSum;
            operSumFact     = operSumFact     + dataSet.factSum;
            clientSumFact   = clientSumFact   + dataSet.factSum;
            contractSumFact = contractSumFact + dataSet.factSum;
            currencySumFact = currencySumFact + dataSet.factSum;

            m_tablePrinter.printString(dataSet.calcDate, dataSet.beginDate, dataSet.endDate,
                                      string(dataSet.sum :0:2), string(dataSet.factSum :0:2),
                                      dataSet.factDate, dataSet.status);

            if (needPayments)
                if (dataSet.payDocScheduleId != null)
                    curSchedule = dataSet.payScheduleId;
                    m_tablePrinter.printBottom();
                    m_tableDocPrinter.printHead();
                    needPrev = false;
                    isNext = true;
                    while ((curSchedule == dataSet.payDocScheduleId) and isNext)
                        m_tableDocPrinter.printString(dataSet.docNumber, dataSet.docStatus, dataSet.docDate,
                                                     dataSet.docDebetAcc, dataSet.docCurDebet, dataSet.docDebetSum,
                                                     dataSet.docCreditAcc, dataSet.docCurCredit, dataSet.docCreditSum);

                        isNext = dataSet.next();
                        needPrev = true;
                    end;
                    if (needPrev and isNext)
                        dataSet.prev();
                    end;
                    printLn("");

                end;
            end;

            if (isNext)
                isNext = dataSet.next()
            end;
        end;

        if (isEmpty)
            printNoData(contractDirection, distributionIsEmpty, attractionIsEmpty);
        else

            printItog("по ПД ", contractName, contractSum, 0, true);

            i = 0;
            while (i < totalsModel.size)
                if (totalsModel(i) == REP_PATTERNSORT_DEPARTMENT)
                    printItog("по подразделению: ", dataSet.branchName, branchSum,branchSumFact, false);
                elif (totalsModel(i) == REP_PATTERNSORT_OPER)
                    printItog("по операционисту: ", dataSet.operName, operSum, operSumFact, false);
                elif (totalsModel(i) == REP_PATTERNSORT_CLIENT)
                    printItog("по клиенту: ", dataSet.clientName, clientSum, clientSumFact, false);
                end;

                i = i + 1;
            end;

            printItog("сумма оплаченных процентов ", "", currencySum, currencySumFact, false);

            printNoData(contractDirection, distributionIsEmpty, attractionIsEmpty);
        end;

        printLn();
        printLn("Операционист " + {name_oper});

        m_tempOfstream.show();
    end;

    private macro constructorTPercPayPrint(parameters)
        m_parameters = parameters;
        m_tempOfstream.setOutputFile();
        m_tablePrinter = CTableReport();
        setHead();
    end;

    constructorTPercPayPrint(parameters);
end;


class TPercPayData(param)

    macro constructor(param)
        SQL_Execute("{ CALL  rep_data.setBalancePlanNumber(0) }");
        SQL_Execute("{ CALL  rep_data.setBeginDate(" + GetSQLDate(param.getDateIn()) + ") }");
        SQL_Execute("{ CALL  rep_data.setEndDate(" +  GetSQLDate(param.getDateOut()) + ") }");
   end;

    macro getQueryText(param)
        var fromText;
        var whereText;
        var sortText;
        var selectText;
        var branchName = ", 'dummy' branchName";
        var operName   = ", 'dummy' operName";
        var clientName = ", 'dummy' clientName";
        var sortModel = param.getSortModel();
        var i;

        selectText = " SELECT finInstr.t_code fiCode, finInstr.t_name fiName, finInstr.t_id fiId, percContract.t_id contractId, percPaySchedule.t_id payScheduleId,"
            + "\n" + "        percContract.t_number contractNumber, percContract.t_account account, percPaySchedule.t_calcDate calcDate, perccontract.t_directionKind direction,"
            + "\n" + "        percPaySchedule.t_periodBeginDate beginDate, percPaySchedule.t_periodEndDate endDate, percPaySchedule.t_calcSum sum, perccontract.t_operId, "
            + "\n" + "        percPaySchedule.t_date factDate, percPaySchedule.t_sum factSum, percPaySchedule.t_statusName status, account.t_branchId branchId, percContract.t_clientId ";

         fromText = "    FROM repPercentContract_vw percContract,                    "
           + "\n" + "         drepFinInstrument_vw finInstr,                         "
           + "\n" + "         drepAccount_vw account,                                "
           + "\n" + "         repPercentPaySchedule_vw percPaySchedule               ";

        whereText = "   WHERE percContract.t_fiid     = fininstr.t_id                "
           + "\n" + "     AND percContract.t_account  = account.t_account            "
           + "\n" + "     AND percContract.t_id       = percPaySchedule.t_contractid "
           + "\n" + "     AND percPaySchedule.t_calcDate BETWEEN " + getSqlDate(param.getDateIn())
           + "\n" + "                                      AND " + getSqlDate(param.getDateOut())
           + "\n" + "     AND " + RepBranchAndDepartmentFieldFilter(param.getDepartmentList()).GetAsSqlString("account.t_departmentId",
                                                                                                              "account.t_branchId");
        if ((param.getCodeCurrency() != ALLCURRENCY) AND (param.getCodeCurrency() != ALLFININSTR))
            whereText = whereText + " AND fininstr.t_id = " + param.getCodeCurrency();
        elif (param.onlyForeignCur())
            whereText = whereText + " AND fininstr.t_id <> " + NATCUR;
        end;

        if (param.getOper() != 0)
            if (param.getOperOrGroup() > 0)
                fromText  = fromText  + ", repUserGroup_vw operGroup ";
                whereText = whereText + " AND operGroup.t_userId = percContract.t_operId ";
                whereText = whereText + " AND operGroup.t_id = " + param.getOper();
            else
                whereText = whereText + " AND perccontract.t_operId = " + param.getOper();
            end;
        end;

        if (param.getSubSystem() > 0)
            whereText = whereText + " AND perccontract.t_backOfficecode = " + param.getSubSystem();
        end;

        if (param.getClient() > 0)
            whereText  = whereText  + " AND perccontract.t_clientId = " + param.getClient();
        end;

        if (param.getPercContractNumber() != 0)

            whereText = whereText + " AND percContract.t_number = " + param.getPercContractNumber();

        else

            if (param.getAccount() != "")
                whereText = whereText + " AND (" + convertMaskToSQLFormat(param.getAccount(), "perccontract.t_account") + ")";
            end;

            if ((param.getPercContractDirection() != 3) and (param.getPercContractDirection() > 0))
                whereText = whereText + " AND perccontract.t_directionKind = " + param.getPercContractDirection();
            end;

            if (param.getChapter() != 0)
                whereText = whereText + " AND perccontract.t_chapter = " + param.getChapter();
            end;

            if (param.getBalance() != "")
                whereText = whereText + " AND (" + convertMaskToSQLFormat( param.getBalance(), "account.t_basicPlanBalance") + ")";
            end;

            if (param.getPercContractUserType() != "")
                whereText = whereText + " AND " + getPatternFilterClause("perccontract.t_userType", param.getPercContractUserType(), param.getPercContractUserTypeMethod());
                // whereText = whereText + " AND regexp_like(perccontract.t_userType,'[" + param.getPercContractUserType()+"]')";
            end;

            if (param.getUseIndividualSchedule() == 1)
                whereText = whereText + " AND perccontract.t_standardPayScheduleNumber IS NULL ";
            elif (param.getScheduleNumber() > 0)
                whereText = whereText + " AND perccontract.t_standardPayScheduleNumber = " + param.getScheduleNumber();
            else
                whereText = whereText + " AND perccontract.t_standardPayScheduleNumber IS NOT NULL ";
            end;

        end;

        if (param.getShowPayments == 1)
            selectText = selectText + ", percPayDoc.t_number docNumber, percPayDoc.t_statusName docStatus"
                             + "\n" + ", percPayDoc.t_date docDate, percPayDoc.t_debetAccount docDebetAcc"
                             + "\n" + ", finInstrDebet.t_code docCurDebet, finInstrCredit.t_code docCurCredit"
                             + "\n" + ", percPayDoc.t_debetSum docDebetSum, percPayDoc.t_creditAccount docCreditAcc"
                             + "\n" + ", percPayDoc.t_creditSum docCreditSum,  percPayDoc.t_payscheduleid payDocScheduleId";
            fromText   = fromText   + ", repPercentPayDocument_vw percPayDoc"
                             + "\n" + ", drepFinInstrument_vw finInstrDebet"
                             + "\n" + ", drepFinInstrument_vw finInstrCredit";
            whereText  = whereText  + " AND percPayDoc.t_payScheduleId (+)= percPaySchedule.t_id"
                             + "\n" + " AND percPayDoc.t_debetFiId = finInstrDebet.t_id (+)"
                             + "\n" + " AND percPayDoc.t_creditFiId = finInstrCredit.t_id (+)";
        end;

        sortText = " ORDER BY perccontract.t_directionKind DESC, finInstr.t_code asc, ";

        i = 0;
        while (i < sortModel.size)
            if (sortModel(i) == REP_PATTERNSORT_DEPARTMENT) // по подразделению
                branchName = ", (SELECT branch.t_shortName"
                      + "\n" + "   FROM drepDpDep_vw branch"
                      + "\n" + "  WHERE branch.t_nodeId = account.t_branchId"
                      + "\n" + ") branchName";
                sortText = sortText + "branchName ASC, account.t_branchId ASC,";
            elif (sortModel(i) == REP_PATTERNSORT_OPER) // по операционисту
                operName = ", (SELECT bankUser.t_name"
                    + "\n" + "   FROM repUser_vw bankUser"
                    + "\n" + "  WHERE bankUser.t_id = percContract.t_operId"
                    + "\n" + ") operName";
                sortText = sortText + " operName ASC, percContract.t_operId ASC,";
            elif (sortModel(i) == REP_PATTERNSORT_CLIENT) // по клиенту
                clientName = ", (SELECT party.t_shortName"
                      + "\n" + "   FROM drepParty_vw party"
                      + "\n" + "  WHERE party.t_id = percContract.t_clientId"
                      + "\n" + ") clientName";
                sortText = sortText + "clientName ASC, percContract.t_clientId ASC,";
            end;

            i = i +1;

        end;
        sortText = sortText + " percContract.t_backOffice asc, percContract.t_number asc, percContract.t_id asc, percPaySchedule.t_calcDate asc";
        selectText = selectText + branchName + operName + clientName;

        return selectText + fromText + whereText + sortText;
    end;

    macro getDataSet(param)
        var dataSet : Object;
        dataSet = TRsbDataSet(getQueryText(param), RSDVAL_CLIENT, RSDVAL_STATIC);

        dataSet.setFieldType("calcDate", V_DATE);
        dataSet.setFieldType("beginDate", V_DATE);
        dataSet.setFieldType("endDate", V_DATE);
        dataSet.setFieldType("sum", V_MONEY);
        dataSet.setFieldType("factSum", V_MONEY);
        dataSet.setFieldType("factDate", V_DATE);

        if (param.getShowPayments() == 1)
            dataSet.setFieldType("docDate", V_DATE);
            dataSet.setFieldType("docDebetSum", V_MONEY);
            dataSet.setFieldType("docCreditSum", V_MONEY);
        end;

        return dataSet;
    end;

    constructor(param);
end;

class TParamPercentPay(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualSchedule, scheduleNumber, percContractDirection, totalsModel,
                                      percContractChargeObjectKind, chapter, balance, account, showPayments)

    private var m_numDprt;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_codeCurrency;
    private var m_foreignCur;
    private var m_dateIn;
    private var m_dateOut;
    private var m_oper;
    private var m_operOrGroup;
    private var m_subSystem;
    private var m_percContractNumber;
    private var m_client;
    private var m_percContractUserType;
    private var m_percContractUserTypeMethod;
    private var m_useIndividualSchedule;
    private var m_scheduleNumber;
    private var m_percContractDirection;
    private var m_sortModel : Object;
    private var m_totalsModel : Object;
    private var m_percContractChargeObjectKind;
    private var m_chapter;
    private var m_balance;
    private var m_account;
    private var m_departmentList;
    private var m_sortByBranch;
    private var m_sortByOper;
    private var m_sortByClient;
    private var m_showPayments;

    macro getPercContractChargeObjectKind()
        return m_percContractChargeObjectKind;
    end;

    macro getDateIn()
        return m_dateIn;
    end;

    macro getDateOut()
        return m_dateOut;
    end;

    macro getDepartmentList()
        return m_departmentList;
    end;

    macro getOrgStructure()
        return m_orgStructure;
    end;

    macro getDepartmentCode()
        return m_numDprt;
    end;

    macro getCodeCurrency()
        return m_codeCurrency;
    end;

    macro onlyForeignCur()
        return m_foreignCur;
    end;

    macro getPercContractNumber()
        return m_percContractNumber;
    end;

    macro getAccount()
        return m_account;
    end;

    macro getOper()
        return m_oper;
    end;

    macro getOperOrGroup()
        return m_operOrGroup;
    end;

    macro getSubSystem()
        return m_subSystem;
    end;

    macro getPercContractDirection()
        return m_percContractDirection;
    end;

    macro getChapter()
        return m_chapter;
    end;

    macro getBalance()
        return m_balance;
    end;

    macro getUseIndividualSchedule()
        return m_useIndividualSchedule;
    end;

    macro getClient()
        return m_client;
    end;

   macro getSortModel()
        return m_sortModel;
    end;

    macro getTotalsModel()
        return m_totalsModel;
    end;

    macro getScheduleNumber()
        return m_scheduleNumber;
    end;

    macro getSortByBranch()
        return m_sortByBranch;
    end;

    macro getSortByOper()
        return m_sortByOper;
    end;

    macro getSortByClient()
        return m_sortByClient;
    end;

    macro setSortByBranch(val)
        m_sortByBranch = val;
    end;

    macro setSortByOper(val)
        m_sortByOper = val;
    end;

    macro setSortByClient(val)
        m_sortByClient = val;
    end;

    macro getPercContractUserType()
        return m_percContractUserType;
    end;

    macro getPercContractUserTypeMethod()
        return m_percContractUserTypeMethod;
    end;

    macro getShowPayments()
        return m_showPayments;
    end;

    macro constructor(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualSchedule, scheduleNumber, percContractDirection, totalsModel,
                                      percContractChargeObjectKind, chapter, balance, account, showPayments)

        m_numDprt                      = numDprt;
        m_orgStructure                 = orgStructure;
        m_issueMode                    = issueMode;
        m_codeCurrency                 = codeCurrency;
        m_foreignCur                   = foreignCur;
        m_dateIn                       = dateIn;
        m_dateOut                      = dateOut;
        m_oper                         = oper;
        m_subSystem                    = subSystem;
        m_percContractNumber           = percContractNumber;
        m_client                       = client;
        m_percContractUserType         = percContractUserType;
        m_percContractUserTypeMethod   = percContractUserTypeMethod;
        m_useIndividualSchedule        = useIndividualSchedule;
        m_scheduleNumber               = scheduleNumber;
        m_percContractDirection        = percContractDirection;
        m_percContractChargeObjectKind = percContractChargeObjectKind;
        m_chapter                      = chapter;
        m_balance                      = balance;
        m_account                      = account;
        m_operOrGroup                  = operOrGroup;
        m_showPayments                 = showPayments;
        m_sortByBranch                 = false;
        m_sortByOper                   = false;
        m_sortByClient                 = false;

        if (totalsModel == REP_PERCENT_TOTALS_ALL)
            m_totalsModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_BRANCH)
            m_totalsModel = arrCreate(REP_PATTERNSORT_DEPARTMENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_DEPARTMENT, REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_BANKUSER)
            m_totalsModel = arrCreate(REP_PATTERNSORT_OPER);
            m_sortModel = arrCreate(REP_PATTERNSORT_OPER, REP_PATTERNSORT_CLIENT);
        elif (totalsModel == REP_PERCENT_TOTALS_CLIENT)
            m_totalsModel = arrCreate(REP_PATTERNSORT_CLIENT);
            m_sortModel = arrCreate(REP_PATTERNSORT_CLIENT);
        end;

        m_departmentList = RepDepartmentList(m_orgStructure, m_issueMode, m_numDprt);

    end;

    constructor(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualSchedule, scheduleNumber, percContractDirection, totalsModel,
                                      percContractChargeObjectKind, chapter, balance, account, showPayments);
end;

macro printPercentReport(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                  client, percContractUserType, percContractUserTypeMethod, useIndividualSchedule, scheduleNumber, percContractDirection, totalsModel,
                                  percContractChargeObjectKind, chapter, balance, account, showPayments)

    var profiler = TSQLProfiler(getTxtFileName("!PercPaySheetProfile"));
    profiler.clear();
    profiler.on();

    var parameters = TParamPercentPay (numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                             client, percContractUserType, percContractUserTypeMethod, useIndividualSchedule, scheduleNumber, percContractDirection, totalsModel,
                                             percContractChargeObjectKind, chapter, balance, account, showPayments);

    /* Контроль открытых опердней */
     if (not RepOperdaysOpened(parameters.getDepartmentList(), parameters.getDateIn(), parameters.getDateOut()).shouldContinue())
        exit(1);
    end;

    var percPayPrint = TPercPayPrint(parameters).execute(TPercPayData(parameters).getDataSet(parameters));

    return true;

end;
