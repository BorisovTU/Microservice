/*
    Баланс кредитной организации
    Протокол выполнения процедуры нормализации
*/
import Log_Lib; 

private array ListOfOptions; 
private array R5_Error;
private array R5ErrorData;
private array R5RP_Parameter;
private var sizeOfList = 0;
private var R5_DataCharacter = "%";
private var Detailed;
private var 
    R5RP_Balance    = 0,
    R5RP_OutActive  = 1,
    R5RP_OutPassive = 2,
    R5RP_Debet      = 3,
    R5RP_Credit     = 4,
    R5RP_InActive   = 5,
    R5RP_InPassive  = 6,
    R5RP_Roubles    = 7,
    R5RP_Currency   = 8;

private class TOption
    var m_path;
    var m_type;
end;

private macro AddOption(path, type)
    var option:TOption;
    option.m_path = path;
    option.m_type = type;
    if(type == null) 
        option.m_type = V_String;
    end;
    ListOfOptions(sizeOfList) = option;
    sizeOfList = sizeOfList + 1;
end;

private macro Initialize(_detailed, unit)
    
    /*
      Detailed = 1 - сокращенный протокол, значения не выводятся
      Detailed = 2 - подробный протокол, перечисляются только узлы с ошибками (узлы без ошибок 
                      в макрос переданы не будут)
      Detailed = 3 - подробный протокол, перечисляются все узлы нормализации
    */
    Detailed = _detailed; //Переменная реестра: "REPORT\\FLOOR\\R5CHECKING"
    

    /* Надписи, которые выводятся в соответствующие строки колонки "Параметр" */
    R5RP_Parameter( R5RP_Balance    ) = " ";
    R5RP_Parameter( R5RP_OutActive  ) = "Исх.Актив";
    R5RP_Parameter( R5RP_OutPassive ) = "Исх.Пассив";
    R5RP_Parameter( R5RP_Debet      ) = "Дебет";
    R5RP_Parameter( R5RP_Credit     ) = "Кредит";
    R5RP_Parameter( R5RP_InActive   ) = "Вх.актив";
    R5RP_Parameter( R5RP_InPassive  ) = "Вх.пассив";
    R5RP_Parameter( R5RP_Roubles    ) = "   Рубли";
    R5RP_Parameter( R5RP_Currency   ) = "   Валюта";

    /* Список переменных реестра, которые будут отображены в протоколе 
        в разделе "Заданные настройки". Есил тип переменной отличается 
        от V_String, то необходимо его указать. */
    AddOption("BANK_INI/Общие параметры/Переменные/BalanceTerm");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBl");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlA");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlB");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlC");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlD");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlE");
    AddOption("BANK_INI/Общие параметры/Переменные/RoundMistkBlF");
    AddOption("REPORT/Floor/Use_Exclusions", V_Bool);
    AddOption("REPORT/Floor/Exclude_1");
    AddOption("REPORT/Floor/Exclude_2");
    AddOption("REPORT/Floor/Exclude_3");
    AddOption("REPORT/Floor/Exclude_4");
    AddOption("REPORT/Floor/Exclude_5");
    /* Усли последний символ в строке ошибки == R5_DataCharacter, то на его место будет дописано соответствующее значение
        из R5ErrorData */
    R5_Error(0) = " ";
    R5_Error(1) = "Погрешность округления >= " + "1" /*String(unit:0:2)*/ + ". Разность между нормализованным и округленным "+
                  "значениями составляет %";
    R5_Error(2) = "Сумма рублевой и валютной составляющих не равна итогу. Разность между итоговым и "+
                  "суммарным значением составляющих равна %";
    R5_Error(3) = "Изменение нулевых оборотов";
    R5_Error(4) = "Нет баланса по строке в рублях. Разность между исходящим остатком и расчитанным исходящим остатком "+
                  "составляет %";
    R5_Error(5) = "Нет баланса по строке в валюте. Разность между исходящим остатком и расчитанным исходящим остатком "+
                  "составляет %";
    R5_Error(6) = "Нет баланса по строке в суммах. Разность между исходящим остатком и расчитанным исходящим остатком "+
                  "составляет %";
end;

private macro SetErrorData(ErrorCode, ErrorData)
    R5ErrorData(ErrorCode) = ErrorData;
end;

private macro GetRegVal( option:TOption )
    var stat, err, Val;
    stat = GetRegistryValue( option.m_path, option.m_type, Val, err );
    if ( stat == V_STRING ) 
     Val = "\"" + Val + "\"";
    elif (stat == V_BOOL)   
     if(Val) 
            Val = "YES";
        else    
            Val = "NO";
     end;
    end; 
    if (err != 0) 
     Val = "";
    end;
    return Val;
end;

private macro PrintHeader()
    [ Баланс кредитной организации];
    [ Протокол выполнения процедуры нормализации];
    [ ];
    var hours, minutes;
    timesplit(time(), hours, minutes);
    [ Дата и время выполнения ########## ##.##](Date(), hours:o, minutes:o);
    [ Исполнитель #### ####################################]({oper}, Исполнитель:l);
    [ Заданные настройки:];
    var i = 0;
    while( i < sizeOfList )
        println(" " + ListOfOptions(i).m_path + " = " + getRegVal(ListOfOptions(i)));
        i = i + 1;  
    end;    

    [ ]; 
    if (Detailed==1)
    [┌─┬─────┬──────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────────┐];
    [│ │№ б/с│ Параметр │                                                Примечание                                               │];
    [└─┴─────┴──────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
    else
    [┌─┬─────┬──────────┬────────────────┬────────────────┬────────────────┬──────────────────────────────────────────────────────┐];
    [│ │№ б/с│ Параметр │    Реальное    │   Округленное  │ Нормализованное│                      Примечание                      │];
    [└─┴─────┴──────────┴────────────────┴────────────────┴────────────────┴──────────────────────────────────────────────────────┘];
    end;
end;

private macro PrintFooter()
    [──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
end;

private macro PrintErrorMore(SplittedError)
    var i = 1;
    while(i<SplittedError.size)
    if (Detailed==1)
    [                        ##################################################################################################### ]
        (SplittedError[i]);
    else
    [                                                                           ################################################## ]
        (SplittedError[i]);
    end;
        i = i + 1;
    end;
end;

private macro GetErrorStrings(Errors)
    var errorStrings = TArray();
    var errorCode, errorString;
    if((Errors==""))
        errorStrings[0] = " ";
    end;
    var i = 0;
    while((i < StrLen(Errors)))
        errorCode = CodeFor(SubStr(Errors,i+1,1));
        if(SubStr(R5_Error[errorCode], StrLen(R5_Error[errorCode]), 1) == R5_DataCharacter)
            errorString = String(SubStr(R5_Error[errorCode], 1, StrLen(R5_Error[errorCode]) - 1), R5ErrorData[ErrorCode]:0:2);
        else
            errorString = R5_Error[errorCode];
        end;
        errorStrings[i] = errorString;
        i = i + 1;
    end;
    return errorStrings;
end;

private macro PrintString(bs, Parameter, Real, Rounded, Normalized, Errors)
    var isError = "";
    var splittedError;
    var errorStrings = GetErrorStrings(Errors);
    if( StrLen(Errors) > 0 ) 
        isError = "!";
    end;
    
    if (Detailed==1)
    SplittedError = StrSplit2(errorStrings[0],101,105);
    [ # ##### ########## ######################################################################################################### ]
    (isError, bs, R5RP_Parameter(Parameter), 
        SplittedError[0]);
    else
    SplittedError = StrSplit2(errorStrings[0],50,54);
    [ # ##### ########## ################ ################ ################ ###################################################### ]
    (isError, bs, R5RP_Parameter(Parameter), Real:0:2:a, Rounded:0:0:a, Normalized:0:0:a, 
        SplittedError[0]);
    end;
    PrintErrorMore(SplittedError);
    var i = 1;
    while(i < errorStrings.size())
    if (Detailed==1)
    SplittedError = StrSplit2(errorStrings[i],101,105);
    [                    ######################################################################################################### ]
        (SplittedError[0]);
    else
        SplittedError = StrSplit2(errorStrings[i],50,54);
    [                                                                       ###################################################### ]
        (SplittedError[0]);
    end;
        PrintErrorMore(SplittedError);
        i = i + 1;
    end;
end;

private macro PrintBSString(bs, Errors)
    PrintString(bs, R5RP_Balance , "", "", "", Errors)
end;

