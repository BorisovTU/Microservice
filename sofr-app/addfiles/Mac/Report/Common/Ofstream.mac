/*
$Name:          Ofstream.mac
$Module:        Отчетность
$Description:   Поток вывода в формате plain text
*/

import BankInter;
import Reporting;
import lib_lang;
import rsexts;
/**
 * Класс для управления потоком вывода в файл.
 */
class TOfstream(fileName : String, needClear : Bool)
    /**
     * Имя файла вывода.
     */
    private var m_fileName : String;

    /**
     * Название файла, в который выполнялся вывод перед вызовом setOutputFile.
     */
    private var m_lastOutput : String;

    /**
     * Открыть поток.
     */
    macro setOutputFile()
        m_lastOutput = setOutput(m_fileName, true);
    end;

    /**
     * Закрыть поток.
     */
    macro resetOutputFile()
        setOutput(m_lastOutput, true);
    end;

    /**
     * Показать файл.
     */
    macro show()
        FILE f_out() txt;
        resetOutputFile();

        if (ExistFile(m_fileName, 0) and open(f_out, m_fileName))
            RepTxtFilesQueue().add(m_fileName);
            viewFile(f_out);
            close(f_out);
            return TRUE;
        else
            RepErrorsQueue().add(0, string("Не найден файл:\n", m_fileName));
            msgbox("Не найден файл: |", m_fileName);
        end;
        return FALSE;
    end;

    /**
     * Сохранить файл с заданным именем.
     * @param fileName - имя файла, в который нужно сохранить. Если имя неполное (без пути), сохранение будет произведено в TxtFiles.
     */
    macro save(fileName)
        var stat = false;
        var path = "";
        var name = "";
        var ext = "";

        FILE f_out() txt;
        resetOutputFile();

        if (fileName == null)
            splitFile(m_fileName, name);
            fileName = name + ".txt";
        end;

        path = splitFile(fileName, name, ext);
        if (path == "")
            fileName = getTxtFileName(fileName);
        end;

        path = splitFile(fileName, name);
        fileName = path + name + ext;

        if (existFile(m_fileName, 0))
            stat = copyFile(m_fileName, fileName);
        else
            open(f_out, fileName);
            println("Не найден файл: ", m_fileName);
            close(f_out);
            stat = false;
        end;

        RepTxtFilesQueue().add(fileName, false);

        return stat;
    end;

    /**
     * Очистить поток
     */
    macro clear()
        setOutput(setOutput(m_fileName, false), true);
    end;

    /**
     * Получить имя файла вывода.
     */
    macro getFileName() : string
        return m_fileName;
    end;

    /**
     * Конструктор.
     *
     * @param     fileName  имя файла
     */
    private macro constructor(fileName : String, needClear : Bool)
        defaultParm(needClear, true);

        m_fileName = getTxtFileName(fileName);

        if (needClear)
            clear();
        end;
    end;

    /**
     * Деструктор.
     */
    private macro destructor()
        resetOutputFile();
    end;

    constructor(fileName, needClear);
end;