/*
$Name:          DatesUtility.mac
$Module:        Отчетность
$Description:   Утилиты для работы с датами
*/

import lib_date;
import lib_lang;

class TDatesUtility()
    private macro isWorkDateImpl(isWorkDate, day) : Bool
        if (isWorkDate == null)
            return isWorkDayBranch(day) == 1;
        elif (valType(isWorkDate) == V_GENOBJ)
            return isWorkDate.isWorkDate(day);
        else
            return execMacro2(isWorkDate, day);
        end;

        return false;
    end;

    macro makeFromString(stringDate : String) : Date
        return dateMakeFromString(stringDate);
    end;

    macro isFirstDayInMonth(day : Date) : Bool
        var d;
        dateSplit(day, d);
        return d == 1;
    end;

    macro isLastDayInMonth(day : Date) : Bool
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if (m == 12)
            m = 1;
            y = y + 1;
        else
            m = m + 1;
        end;

        return day == Date(1, m, y) - 1;
    end;

    macro getFirstDayInMonth(d : Date) : Date
        var m;
        var y;
        dateSplit(d, NULL, m, y);

        return Date(1, m, y);
    end;

    macro getLastDayInMonth(day : Date) : Bool
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if (m == 12)
            m = 1;
            y = y + 1;
        else
            m = m + 1;
        end;

        return Date(1, m, y) - 1;
    end;

    // isWorkDate - коллбэк для определения рабочего дня
    //              если null, то используется isWorkDayBranch из интера "Календарь"
    //              если объект, то вызывается метод isWorkDate(), должен возвращать bool
    //              если строка, то вызывается функция с заданным именем, должна возвращать bool
    //              если ссылка на метод, то вызывается метод по ссылке, должен возвращать bool
    //              если ссылка на функцию, то вызывается функция по ссылке, должна возвращать bool
    macro getNearPreviosWorkDay(day : Date, includeCurrentDate : Bool, isWorkDate : Variant) : Date
        defaultParm(includeCurrentDate, false);

        var previosWorkDay = day;
        if (not includeCurrentDate)
            previosWorkDay = previosWorkDay - 1;
        end;

        while (not isWorkDateImpl(isWorkDate, previosWorkDay))
            previosWorkDay = previosWorkDay - 1;
        end;

        return previosWorkDay;
    end;

end;
