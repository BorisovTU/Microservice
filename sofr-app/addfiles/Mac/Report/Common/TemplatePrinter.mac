/**
 * Module:  TTemplatePrinter.h
 * Author:  Ivkina
 * Modified: 11 февраля 2008 г. 10:18:46
 * Purpose: Declaration of the class TTemplatePrinter
 * Comment: Печать таблицы.
 */

import prnfrm;
import RsbDataSet;
import lib_lang;

/**
 * Печать таблицы.
 */
class TTemplatePrinter(templateFileName : String)
    /**
     * Имя файла шаблона.
     */
    private var m_templateFileName : String;
    /**
     * Печать денежных сумм с апострофами?
     */
    private var m_isApostrophe : Bool;
    /**
     * Количество знаков после запятой.
     */
    private var m_dimention : Integer;

    macro getTemplateFileName()
        return m_templateFileName;
    end;

    macro setPrintingMoneyApostrophe(isApostrophe : Bool, dimention : Integer)
        m_isApostrophe = isApostrophe;
    end;

    macro setPrintingMoneyDimention(dimention : Integer)
        m_dimention = dimention;
    end;


    /**
     * Печать блока таблицы.
     *
     * @param     blockName  имя блока вывода
     * @param     dataSet запись с данными для вывода
     */
    macro tableBlockPrint(blockName : String, dataSet)
        var repForm : TRepForm;
        var field : TPattFieldR;
        var str : String = "";

        repForm = TRepForm(m_templateFileName, false, blockName);

        var i = 0;
        field = repForm.field(i);
        while (field != null)
            if (m_isApostrophe)
                field.value = execExp("String(NVL(dataSet." + field.name +", \" \"):0:"+m_dimention+":a)");
            else
                field.value = execExp("String(NVL(dataSet." + field.name +", \" \"):0:"+m_dimention+")");
            end;

            i = i + 1;
            field = repForm.field(i);
        end;

        onError(e);
        if (e.code != 40)
            runError();
        end;

        str = repForm.toString();
        repForm.write();

        return str;
    end;

    /**
     * @param     templateFileName   имя файла с шаблоном
     */
    private macro constructor(templateFileName : String)
        m_templateFileName = templateFileName;
        m_isApostrophe     = false;
        m_dimention        = 0;
    end;

    constructor(templateFileName);
end;