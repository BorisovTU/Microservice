/*
RS-Bank 5.1
Макрос конвертации 5 плана счетов для формы 2 и разнесение лицевых по
балансовым счетам - то биш по символам отчетности
*/


file balance (balance) write;
file dbffile () dbf;
file dbffl () dbf;
file accblnc (accblnc) write;

NumPlan=5;
path_to_dbf0 = "..\\simvol.dbf";
path_to_dbf1 = "..\\psimvol.dbf";

macro delete_plan  
  balance.iNumPlan=NumPlan;
  balance.Chapter=1;
  balance.balance="";
  while ( (getGE(balance)) and (balance.iNumPlan==NumPlan) )
    delete(balance);rewind(balance);
    balance.iNumPlan=NumPlan;
    balance.Chapter=1;
    balance.balance="";
  end;
  msgbox("End delete Plan, press ESC.");
end;

Macro Loop_dbffl
  i=0;Initprogress(Nrecords(dbffl));
  while(next(dbffl))
    i=i+1;UseProgress(i);
    balance.Chapter=1;
    balance.INumPlan=NumPlan;
    balance.Name_Part="";
    balance.UserField1="";
    balance.Balance=substr(trim(string(dbffl.SIMVOL)),1,1);
    if ( not getEQ(balance) ) insert(balance); end;
    balance.Balance=substr(trim(string(dbffl.SIMVOL)),1,3);
    if ( not getEQ(balance) ) insert(balance); end;
    balance.Balance=trim(string(dbffl.SIMVOL));
    balance.Name_Part=trim(string(dbffl.NAME));
    balance.UserField1=trim(string(dbffl.OPEN_NAME));
    if ( not getEQ(balance) ) insert(balance); end;
  end;
  Remprogress;
  msgbox("End insert Plan, press ESC.");
end;

Macro Loop_dbffile
  i=0;Initprogress(Nrecords(dbffile));
  while(next(dbffile))
    i=i+1;UseProgress(i);
    balance.Chapter=1;
    balance.INumPlan=NumPlan;
    balance.Name_Part="";
    balance.UserField1="";
    balance.Balance=substr(trim(string(dbffile.SIMVOL)),1,1);
    if ( not getEQ(balance) ) insert(balance); end;
    balance.Balance=substr(trim(string(dbffile.SIMVOL)),1,3);
    if ( not getEQ(balance) ) insert(balance); end;
    balance.Balance=trim(string(dbffile.SIMVOL));
    if ( not getEQ(balance) ) insert(balance); end;
    balance.Balance=trim(string(dbffile.SIMVOL))+"."+trim(string(dbffile.POD_SIMVOL));
    balance.Name_Part=trim(string(dbffile.NAME));
    balance.UserField1=trim(string(dbffile.OPEN_NAME));
    if ( not getEQ(balance) ) insert(balance); end;
  end;
  Remprogress;
  msgbox("End insert Plan, press ESC.");
end;

Macro Loop_accblnc
  i=0;InitProgress(Nrecords(accblnc));
  while(next(accblnc))
    i=i+1;UseProgress(i);
    if ( (accblnc.Chapter==1) and (substr(accblnc.account,1,5)>="70101") and (substr(accblnc.account,1,5)<="70209") )
      bal=substr(accblnc.account,14,5);
      if (substr(accblnc.account,19,2)!="00")
        bal=bal+"."+substr(accblnc.account,19,2);
      end;
      balance.iNumPlan=NumPlan;
      balance.Chapter=1;
      balance.balance=bal;
      if (getEQ(balance))
        accblnc(fldIndex(accblnc,"Balance"+string(NumPlan)))=bal;
        [############################### #################]
        (accblnc.account,bal);
        if (not update(accblnc) )
          msgbox("bad update account:",accblnc.account);
        end;
      else
        [Balance #################  for account ######################## is not found]
        (bal,accblnc.account);
      end;
    end;
  end;
  RemProgress;
end;

Macro Main
  if ( not open(dbffl,path_to_dbf0) ) 
    msgbox ("файл с исходными данными не открыт|",string(dbffile)); 
    exit(1); 
  end;
  if ( not open(dbffile,path_to_dbf1) )
    msgbox ("файл с исходными данными не открыт|",string(dbffile));
    exit(1);
  end;
  delete_plan;
  Loop_dbffl;
  Loop_dbffile;
  Loop_accblnc;
end;

/*****************************************/
Main;



