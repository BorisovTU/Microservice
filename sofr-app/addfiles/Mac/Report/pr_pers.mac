/*
$Name: pr_pers.mac
$Module: Внутренняя отчетность
$Description: Отчет по пользователям
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank v6                                                 R-Style SoftLab

  File Name   : pr_pers.mac
  Programmer  : LCh
  Description : Отчет по пользователям
  Comment     :
  Modification:
    02.07.2005 BugZ SCR 67395 Фильтрация по ТС/РС.
    04.07.2005 ABP Контроль открытых опердней
└───────────────────────────────────────────────────────────────────────────*/
IMPORT BankInter, globals;
IMPORT Reporting;
import rep_lib; /*26 Jan 07 Fri 17:19:28 Malakhova Irina 98356*/
import lib_arr;
IMPORT oralib;
IMPORT ofstream;

FILE temp ( "doc_key.tmp" ) write;  /* временный файл для сортировки отчета */
FILE persn ( person );

FILE typeac( typeac ) KEY 0;

Var
 NumDprt,
 FullNameProg, /* Полные наименования приложений (TRUE/FALSE) */
 NeedArh;     /* Показывать доступ к архиву (TRUE/FALSE) */

/*26 Jan 07 Fri 17:07:55 Malakhova Irina 68356*/
var width = 83;
var shift = 0;
var reportBodyFileName;

CONST   /* Поля сортировки */
    N_CLOSED    = "1", /* Статус */
    N_ACCESS    = "2", /* Доступ */
    N_OPERNUM   = "3", /* Номер пользователя */
    N_ACARH     = "4", /* Доступ к архиву */
    N_IDENTPROG = "5"; /* Приложения */
/*--------------------------------- */

macro GetNameTypePers( cType )
  typeac.iNumType = 13;           /*Уровни доступов*/
  typeac.Type_Account = cType;
  if( GetEQ( typeac ))
         return typeac.Name_Type;
  else  return "не найден тип "+cType;
  end;
end;

macro GetNameProg( cType )
  typeac.iNumType = 22;           /*Приложения*/
  typeac.Type_Account = cType;
  if( GetEQ( typeac ))
         return typeac.Name_Type;
  else  return "не найден тип "+cType;
  end;
end;

macro  GetUserStatus( UserClosed)
  if ( UserClosed == "" ) return "Р";
  else  return "З";
  end;
end;


MACRO GetNameOper ( Oper)

   Persn.Oper = Oper;
   if ( GetEQ( Persn) )
      return ( Persn.Name );
   else
    msgbox ("Не найден операционист: " + string(Oper));
    return "";
   end;

END;

macro CheckIdentProg( szIdentProgram, maskIdentProg)
  if (strbrk( szIdentProgram, maskIdentProg )==0 )
     return FALSE;
  end;
  return TRUE;
end;

/*--------------------------------- */
MACRO GetSort( f, maskSort, arhCarryDay)
  var i = 0, st_sort = "";

  macro  GetOneField( p )
    if   ( p == N_CLOSED    )  return repLZ( f.UserClosed,      1 ); /* Статус */
    elif ( p == N_ACCESS    )  return repLZ( f.cTypePerson,     1 ); /* Доступ */
    elif ( p == N_OPERNUM   )  return repLZ( f.Oper,            5 ); /* Номер пользователя */
    elif ( p == N_ACARH     )  return repLZ( arhCarryDay,       4 ); /* Доступ к архиву */
    elif ( p == N_IDENTPROG )  return repLZ( f.szIdentProgram, 16 ); /* Приложения */
    else return "";
    end;
  end;

   while( i < strlen(maskSort) )
      i = i + 1;
      st_sort = st_sort + GetOneField( substr(maskSort,i,1) );
   end;
 /*  println("sts=",st_sort);*/
   return st_sort;
END;

/*--------------------------------- */
MACRO PrintHeader(OrgStructure)
var dprtcode = "", dprtname = "";

if ( NeedArh )
    width = 100;
else
    width = 91;
end;
/*26 Jan 07 Fri 17:16:24 Malakhova Irina 68356*/
PrintBankHeader(NumDprt, OrgStructure, width, shift);
PrintLn();
PrintLn(mkStr(" ", shift) + strAlign("Отчёт по пользователям", width, STR_ALIGN_CENTER, " "));
PrintLn();

  if ( NeedArh )
[
 ┌───┬─────────────────────────────────────────┬───────────────┬─────────────────────┬────────┬──────┐
 │ № │          Пользователь                   │               │                     │Доступ к│      │
 │п/п├─────┬───────────────────────────────────┤    Доступ     │     Приложения      │ архиву,│Статус│
 │   │  №  │       ФИО                         │               │                     │   дн.  │      │
 ├───┼─────┼───────────────────────────────────┼───────────────┼─────────────────────┼────────┼──────┤
];
  else
[
 ┌───┬─────────────────────────────────────────┬───────────────┬─────────────────────┬──────┐
 │ № │          Пользователь                   │               │                     │      │
 │п/п├─────┬───────────────────────────────────┤    Доступ     │     Приложения      │Статус│
 │   │  №  │       ФИО                         │               │                     │      │
 ├───┼─────┼───────────────────────────────────┼───────────────┼─────────────────────┼──────┤
];
  end;

END;

MACRO PrintFooter
  if ( NeedArh )     /* Показывать доступ к архиву */
[└───┴─────┴───────────────────────────────────┴───────────────┴─────────────────────┴────────┴──────┘

];
  else
[└───┴─────┴───────────────────────────────────┴───────────────┴─────────────────────┴──────┘

];
  end;
END;


MACRO PrintOnePers(counter, persn)
  var  NameProg = "", i = 2, NumProg = strlen(persn.szIdentProgram);

  var arhCarryDay; // глубина архива
  var regError;    // ошибка чтения реестра

  GetRegistryValue("COMMON\\ОПЕРАЦИОННЫЕ ДНИ\\ГЛУБИНА АРХИВА", V_INTEGER,
                   arhCarryDay, regError, false, persn.oper);

  if ( FullNameProg ) /* Полные наименования приложений */
    if (NumProg != 0 )
      NameProg = GetNameProg( substr(persn.szIdentProgram,1,1));
    end;

    if ( NeedArh )     /* Показывать доступ к архиву */
[│###│#####│###################################│###############│ ################### │ ###### │   #  │]
( counter, persn.Oper, getNameOper(persn.Oper):w,
  GetNameTypePers( persn.cTypePerson ),
  NameProg,
  arhCarryDay,
  GetUserStatus(persn.UserClosed)
);
    else
[│###│#####│###################################│###############│ ################### │   #  │]
( counter, persn.Oper, getNameOper(persn.Oper):w,
  GetNameTypePers( persn.cTypePerson ),
  NameProg,
  GetUserStatus(persn.UserClosed)
);
    end;

    while ( i <= NumProg )
      NameProg = GetNameProg( substr(persn.szIdentProgram,i,1));

      if ( NeedArh )     /* Показывать доступ к архиву */
[│   │     │                                   │               │ ################### │        │      │]
(   NameProg );
      else
[│   │     │                                   │               │ ################### │      │]
(   NameProg );
      end;

      i = i +1;
    end;
      if ( NeedArh )     /* Показывать доступ к архиву */
[│   │     │                                   │               │                     │        │      │];
      else
[│   │     │                                   │               │                     │      │];
      end;


  else
    if ( NeedArh )     /* Показывать доступ к архиву */
[│###│#####│###################################│###############│ ################### │ ###### │   #  │]
( counter, persn.Oper, getNameOper(persn.Oper):w,
  GetNameTypePers( persn.cTypePerson ),
  persn.szIdentProgram,
  arhCarryDay,
  GetUserStatus(persn.UserClosed)
);
    else
[│###│#####│###################################│###############│ ################### │   #  │]
( counter, persn.Oper, getNameOper(persn.Oper):w,
  GetNameTypePers( persn.cTypePerson ),
  persn.szIdentProgram,
  GetUserStatus(persn.UserClosed)
);
    end;
  end;
END;

/*--------------------------------- */
/* Печать из врем файла  */

MACRO PutReport(count_rec, OrgStructure)
 var counter = 0, first = TRUE;

 InitProgress(  count_rec /*NRecords( temp )*/,
               "", "Печать отчета из временного файла" );

 rewind( temp );
 keynum( temp, 0 );

 while( next (temp ) )

/*    if ( GetDirect( persn, temp.Addr) )*/
    persn.Oper = temp.Addr;

    if ( GetEQ( persn ) )

     if ( first )
       first = FALSE;
       PrintHeader(OrgStructure);
     end;

     counter = counter + 1;
     UseProgress( counter );

     PrintOnePers(counter, persn);

    else
      msgbox( "Ошибка чтения записи" );
    end;

 end;

 RemProgress();

 if( count_rec == 0  )
     /*26 Jan 07 Fri 17:49:09 Malakhova Irina 68356*/
     printLn(mkStr(" ", shift) + strAlign("Нет данных для отчета.", width, STR_ALIGN_CENTER, " "));
 else
     PrintFooter;
 end;

END;



/* --------Функция вызывается из программы ----------*/
MACRO Report(
    _Dprt,                  /* Филиал (0-все) */
    _TypePerson,            /* Доступ ("" - все) */
    _StatClose,             /* Статус (0-работающие,1-закрытые,2-все) */
    _IdentProg,             /* Приложения ("" - все) */
    _FullNameProg,          /* Полные наименования приложений (TRUE/FALSE) */
    _NeedArh,               /* Показывать доступ к архиву (TRUE/FALSE) */
    _KindArh,               /* Вид доступа к архиву (0-все,1-с доступом,2-без доступа) */
    _Sort,                  /* Сортировка ("12345") */
    _OrganizationStructure, /* Территориальная структура */
    _IssueMode)             /* Режим выпуска отчета */


 var departmentListArray;

  /* Фильтр пользователей */
  macro FiltrPersn( persn, arhCarryDay )
//    if ( (_Dprt != 0 ) and (persn.CodeDepart != _Dprt)  ) return FALSE; end;
    if ( arrFind(departmentListArray, persn.CodeDepart) < 0 ) return FALSE; end;
    if ( (_TypePerson != "" ) and (((Index(_TypePerson, persn.cTypePerson)) == 0) or (persn.cTypePerson != _TypePerson))) return FALSE; end;
    if ( _StatClose != 2 )
      if   ((_StatClose == 0) and (persn.UserClosed != "") ) return FALSE;
      elif ((_StatClose == 1) and (persn.UserClosed == "") ) return FALSE;
      end;
    end;
    if ( (_IdentProg != "" ) and not CheckIdentProg(persn.szIdentProgram, _IdentProg) )
          return FALSE; end;
    if ( _KindArh != 0 )
      if   ((_KindArh == 1) and (arhCarryDay == 0) ) return FALSE;
      elif ((_KindArh == 2) and (arhCarryDay != 0) ) return FALSE;
      end;
    end;
    return TRUE;
  end;

 var count_rec = 0;

 NumDprt = _Dprt;
 FullNameProg = _FullNameProg; /* Полные наименования приложений (TRUE/FALSE) */
 NeedArh = _NeedArh;      /* Показывать доступ к архиву (TRUE/FALSE) */
 /* Фильтрация по ТС/РС */
 var departmentList = RepDepartmentList( _organizationStructure, _issueMode, _Dprt );

 /* Контроль открытых опердней */
 var opd = RepOperdaysOpened(departmentList, {curdate}, {curdate});
 if (opd.ShouldContinue == false)
   exit(1);
 end;

 if (not Open(temp))
     MsgBox ("Невозможно открыть временный файл ",temp);
     exit(1);
 else
     execSQL("TRUNCATE TABLE DDOC_KEY_TMP;");
 end;

 departmentListArray = execExp("arrCreate" + departmentList.getAsSqlsetFull());

 var arhCarryDay; // глубина архива
 var regError;    // ошибка чтения реестра
 rewind( persn );
 while (next(persn) )

   GetRegistryValue("COMMON\\ОПЕРАЦИОННЫЕ ДНИ\\ГЛУБИНА АРХИВА", V_INTEGER,
                    arhCarryDay, regError, false, persn.oper);

   if ( FiltrPersn( persn, arhCarryDay ) )
     ClearRecord ( temp );

     temp.Sort  =  GetSort (persn, _Sort, arhCarryDay);
     temp.Addr  =  persn.Oper; /*GetPos( persn );*/
     if (not insert(temp) )
            msgbox("Ошибка записи во временный файл");
            exit(1);
     end;
     count_rec = count_rec + 1;
   end;
 end;

 PutReport(count_rec, _OrganizationStructure); /*26 Jan 07 Fri 17:42:49 Malakhova Irina*/


END;

macro ibrCreateReport( Dprt, TypePerson, StatClose, IdentProg, FullNameProg, NeedArh, KindArh, Sort, organizationStructure, issueMode, nameFile)
    var output = TOFStream(nameFile);

    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    Report(Dprt, TypePerson, StatClose, IdentProg, FullNameProg, NeedArh, KindArh, Sort, organizationStructure, issueMode);

    output.resetOutputFile();

    return true;
end;

/*------------------------------*/
/*
Report(
    0,         /* Филиал (0-все) */
    "",   /* Доступ ("" - все) */
    2,    /* Статус (0-работающие,1-закрытые,2-все) */
    "",    /* Приложения ("" - все) */
    TRUE, /* Полные наименования приложений (TRUE/FALSE) */
    TRUE,      /* Показывать доступ к архиву (TRUE/FALSE) */
    0,      /* Вид доступа к архиву (0-все,1-с доступом,2-без доступа) */
    "12345" );        /* Сортировка ("12345") */
*/
/*EOF*/
