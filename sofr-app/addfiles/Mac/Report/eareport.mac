/*
$Name:          eareport.mac
$Module:        Отчетность
$Description:   Процедуры и функции для работы с ЭА.
*/

/**
 * RS-Bank v6                                             R-Style Softlab
 *
 * Module:   eareport.mac
 * Author:   Andrew B. Panov
 * Modified: 25 июня 2012 г. 17:28:12
 * Purpose:  Реализация функций выгрузки в электронный архив
 * Comment:
 */
import globals;
import Reporting;
import cb_sql;

private const EDKIND_FORMAT_CONTAINER = 1;
private const EDOC_UPLOADMODE_SIMPLE = 0;

private const ED_ABED_LMA_FORMAT = 1;
private const ED_DEV_FORMAT = 0;

private macro getData(edKind : Integer, format : Integer, kindId : Integer, method : Integer)
    var stat = 0;
    var command;
    var dataset;

    command = RsdCommand("SELECT edkind.t_format    AS t_format,"
                + "\n" + "       repkindea.t_kindId AS t_kindId,"
                + "\n" + "       repkindea.t_method AS t_method"
                + "\n" + "  FROM dedkind_dbt edkind,"
                + "\n" + "       drepkindea_dbt repkindea"
                + "\n" + " WHERE edkind.t_eDocKindId = repkindea.t_edKindId"
                + "\n" + "   AND edkind.t_eDocKindId = ?"
                        );
    command.addParam("eDocKind", RSDBP_IN, edKind);
    dataset = TRsbDataset(command);
    if (dataset.next())
        setParm(1, dataset.format);
        setParm(2, dataset.kindId);
        setParm(3, dataset.method);
        stat = 0;
    else
        stat = 1;
    end;

    return stat;
end;

private macro getBranchIdsList(branchId : Integer, uploadFormat : Integer)
    var branchList = TArray();

    if (uploadFormat != ED_DEV_FORMAT)
        branchList[0] = branchId;
        return branchList;
    end;

    var command;
    var dataset;

    command = RsdCommand("WITH ts AS"
                + "\n" + "( SELECT ts.t_code,"
                + "\n" + "         ts.t_parentCode,"
                + "\n" + "         ts.t_nodeType,"
                + "\n" + "         ts.t_eaStorageDir"
                + "\n" + "    FROM ddp_dep_dbt ts"
                + "\n" + " CONNECT BY PRIOR ts.t_code = ts.t_parentCode"
                + "\n" + "   START WITH ts.t_code = ?"
                + "\n" + ")"
                + "\n" + "  SELECT node.t_code AS t_code"
                + "\n" + "    FROM ts node"
                + "\n" + "   WHERE node.t_code NOT IN ( SELECT selfEaStorage.t_code"
                + "\n" + "                                FROM ts selfEaStorage"
                + "\n" + "                             CONNECT BY PRIOR selfEaStorage.t_code = selfEaStorage.t_parentCode"
                + "\n" + "                                 AND selfEaStorage.t_nodeType = 2"
                + "\n" + "                               START WITH selfEaStorage.t_eaStorageDir != " + getSqlString("")
                + "\n" + "                                      AND selfEaStorage.t_nodeType = 1"
                + "\n" + "                                      AND selfEaStorage.t_code != ?"
                + "\n" + "                            )"
                        );
    command.addParam("t_code1", RSDBP_IN, branchId);
    command.addParam("t_code2", RSDBP_IN, branchId);
    dataset = TRsbDataset(command);

    while (dataset.next())
        branchList[branchList.size] = dataset.code;
    end;

    if (branchList[0] == null)
        branchList = null;
    end;

    return branchList;
end;

/**
 * Формирование списка выгружаемых документов
 *
 * @since v.6.20.030.53
 * @author ABP
 * @version 1.2
 * @param mode Режим запуска: первичная или повторная выгрузка
 * @param reportDate Дата
 * @param branchId id узла ТС
 * @param edKind Вид ЭД (EDocKind)
 * @param additionalParm Текстовая строка доп. параметров
 * @param uploadFormat Формат выгрузки
 * @return 0-успех, !0 - номер ошибки
 */
macro fillReportList(mode : Integer, reportDate : date, branchId : Integer, edKind : Integer, additionalParm : String, uploadFormat : Integer) : Integer

    defaultParm(uploadFormat, ED_ABED_LMA_FORMAT);

    var stat = 0;
    var command;
    var format = -1;
    var kindId = -1;
    var method = -1;

    stat = getData(edKind, format, kindId, method);

    if (format != EDKIND_FORMAT_CONTAINER)
        stat = 1;
    end;

//    if (kindId == REP_KINDSEA_BALANCE_SHEET)
//        stat = 29825;
//    end;

    // безинтерфейсный выпуск отчета и сохранение его в реестр отчетов
    if ((method == REP_REGMETHOD_AUTO) and (mode == EDOC_UPLOADMODE_SIMPLE))
        if (kindId == REP_KINDSEA_ACCSTATEMENT)
            begAction(1000, "Формирование выписки по лицевым счетам");

            var branchList = getBranchIdsList(branchId, uploadFormat);
            if (branchList != null)
                for (var i, branchList)
                    repProcessAccountStatementEa(i, reportDate);
                end;
            end;

            endAction();
        end;
    end;

    if (stat == 0)
        command = RsdCommand("BEGIN"
                    + "\n" + "  ? := rep_earch.fillReportList(?,?,?,?,?,?,?);"
                    + "\n" + "END;"
                            );

        command.addParam("error",          RSDBP_RETVAL, V_INTEGER);
        command.addParam("mode",           RSDBP_IN, mode);
        command.addParam("reportDate",     RSDBP_IN, reportDate);
        command.addParam("branchId",       RSDBP_IN, branchId);
        command.addParam("edKind",         RSDBP_IN, edKind);
        command.addParam("additionalParm", RSDBP_IN, additionalParm);
        command.addParam("kindId",         RSDBP_IN, kindId);
        command.addParam("uploadFormat",   RSDBP_IN, uploadFormat);

        sql_execute(command);

        stat = command.value("error");
    end;

    return stat;

end;

/**
 * Формирование списка выгружаемых документов для старой реализации ведомости остатков
 *
 * @since v.6.20.030.54
 * @author ABP
 * @version 1.1
 * @param mode Режим запуска: первичная или повторная выгрузка
 * @param reportDate Дата
 * @param branchId id узла ТС
 * @param edKind Вид ЭД (EDocKind)
 * @param additionalParm Текстовая строка доп. параметров
 * @param uploadFormat Формат выгрузки
 * @return 0-успех, !0 - номер ошибки
 */
macro fillBalanceSheetList(mode : Integer, reportDate : date, branchId : Integer, edKind : Integer, additionalParm : String, uploadFormat : Integer) : Integer

    defaultParm(uploadFormat, ED_ABED_LMA_FORMAT);

    var stat = 0;
    var command;
    var format = -1;
    var kindId = -1;
    var method = -1;

    stat = getData(edKind, format, kindId, method);

    if (format != EDKIND_FORMAT_CONTAINER)
        stat = 1;
    end;

    if (kindId != REP_KINDSEA_BALANCE_SHEET)
        stat = 29823;
    end;

    if (stat == 0)
        command = RsdCommand("BEGIN"
                    + "\n" + "  ? := rep_earch.fillBalanceSheetList(?,?,?,?,?,?,?);"
                    + "\n" + "END;"
                            );

        command.addParam("error",          RSDBP_RETVAL, V_INTEGER);
        command.addParam("mode",           RSDBP_IN, mode);
        command.addParam("reportDate",     RSDBP_IN, reportDate);
        command.addParam("branchId",       RSDBP_IN, branchId);
        command.addParam("edKind",         RSDBP_IN, edKind);
        command.addParam("additionalParm", RSDBP_IN, additionalParm);
        command.addParam("kindId",         RSDBP_IN, kindId);
        command.addParam("uploadFormat",   RSDBP_IN, uploadFormat);

        sql_execute(command);

        stat = command.value("error");
    end;

    return stat;

end;

/**
 * Формирование тела выгружаемых документов
 *
 * @since v.6.20.030.53
 * @author ABP
 * @version 1.1
 * @param mode Режим запуска: первичная или повторная выгрузка
 * @param edKind Вид ЭД (EDocKind)
 * @param additionalParm Текстовая строка доп. параметров
 * @param uploadFormat Формат выгрузки
 */
macro fillReportData(mode : Integer, edKind : Integer, additionalParm : String, uploadFormat : Integer) : Integer

    defaultParm(uploadFormat, ED_ABED_LMA_FORMAT);

    var stat = 0;
    var command;
    var dataset;
    var format = -1;

    command = RsdCommand("SELECT edkind.t_format  AS t_format"
                + "\n" + "  FROM dedkind_dbt edkind"
                + "\n" + " WHERE edkind.t_eDocKindId = ?"
                        );
    command.addParam("eDocKind", RSDBP_IN, edKind);
    dataset = TRsbDataset(command);
    if (dataset.next())
        format = dataset.format;
        stat = 0;
    else
        stat = 5;
    end;

    if (format != EDKIND_FORMAT_CONTAINER)
        stat = 5;
    end;

    if (stat == 0)
        command = RsdCommand("BEGIN"
                    + "\n" + "    ? := rep_earch.fillReportData(?,?,?,?);"
                    + "\n" + "END;"
                            );

        command.addParam("error",          RSDBP_RETVAL, V_INTEGER);
        command.addParam("mode",           RSDBP_IN, mode);
        command.addParam("edKind",         RSDBP_IN, edKind);
        command.addParam("additionalParm", RSDBP_IN, additionalParm);
        command.addParam("uploadFormat",   RSDBP_IN, uploadFormat);

        sql_execute(command);

        stat = command.value("error");
    end;

    return stat;

end;
