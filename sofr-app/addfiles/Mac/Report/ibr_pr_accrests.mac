/*
$Name:          ibr_pr_accrests.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Справка об остатках по счетам"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ReptCBInter;         //для определения вида периода
import pr_accrests;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_ACC = "Параметры";
private const GRP_PARAMS = "Дополнительные параметры";

private const CLASSIFICATOR = 1749;

private const gRep = TAB_REPORT + "@" + GRP_ACC + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";
    
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_pr_accrests", "Справка об остатках по счетам", "ibr_pr_accrests.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_ACC, 1);
    var groupParams = facade.createGroup(GRP_PARAMS, 2);
    
    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));
    
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта",NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));
    
    groupReport.add(facade.createFieldDate("Дата", "Дата отчета"));
    
    groupReport.add(facade.createFieldClient("Клиент", "Клиент"));
    
    groupReport.add(facade.createFieldUser("Операционист", "Операционист"));
    
    groupReport.add(facade.createFieldBalance("Балансовый", "Балансовый"));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет", "Лицевой счета"));
   
    facade.resetFieldsOrder();  
    groupParams.add(facade.createFieldCheckBox("ИтогиПоРазделам", "Итоги по главам", true));
    groupParams.add(facade.createFieldCheckBox("НулевыеОстатки", "Нулевые остатки", false));
    groupParams.add(facade.createFieldCheckBox("ОстатВалСчет", "Остатки в валюте счета", false));
    groupParams.add(facade.createFieldCheckBox("ЗакрытыеСчета", "Закрытые счета", true));
    groupParams.add(facade.createFieldCheckBox("Апострофами", "С апострофами", false));
    groupParams.add(facade.createFieldSort("Сортировка", "Сортировка", "A", CLASSIFICATOR));

    tabReport.add(groupReport);
    tabReport.add(groupParams);

    report.add(tabReport);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result : Object = TRcbResult();
    var i      :integer = 0;

    var Pattern = strTransferByWord(getValue(report, gPrm+"Сортировка").valueString, 1);

    while (i < Pattern.size)
        Pattern[i] = TIbrCommon().getElementLlValues(Pattern[i], CLASSIFICATOR);
        i = i + 1;
    end; 

    result.valueBoolean = produceReport(getValue(report, gRep+ "Подразделения").valueInteger,                                             
                                        getValue(report, gRep+"Структура").valueInteger,                                                 
                                        getValue(report, gRep+"Режим").valueInteger,                                                    
                                        TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),   
                                        TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),                                          
                                        getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                        getValue(report, gRep+"Дата").valueDate, 
                                        getValue(report, gRep+"Клиент").valueInteger, 
                                        false, // ограничение реализации, групп нет                                         
                                        getValue(report, gRep+"Операционист").valueInteger,
                                        getValue(report, gRep+"Балансовый").valueString,
                                        getValue(report, gRep+"ЛицевойСчет").valueString,
                                        getValue(report, gPrm+"Апострофами").valueBoolean,
                                        getValue(report, gPrm+"НулевыеОстатки").valueBoolean,
                                        getValue(report, gPrm+"ОстатВалСчет").valueBoolean,
                                        getValue(report, gPrm+"ИтогиПоРазделам").valueBoolean,
                                        getValue(report, gPrm+"ЗакрытыеСчета").valueBoolean,
                                        Pattern
                                        );

    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;
    
    if ((getFiId(getValue(report, gRep+"Валюта").valueString, true) == 0) and getValue(report, gPrm+"ОстатВалСчет").valueBoolean)
        repErrorsQueue().add(0, "Флаг \"Остатки в валюте счета\" устанавливается только для иностранной валюты");
        result.valueBoolean = FALSE;
    end;
    
    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;