/*ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  RS-Bank 6.0                                           R-Style Software Lab

  File Name   : prpcrate.mac                      26.05.2011
  Programmer  : gww
  Description : „ ­­ë¥ ® ¯ à ¬¥âà å ¯à®æ¥­â­ëå áâ ¢®ª ¯® „
  Comment     :
  Modify
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ*/
import FIInter;
import Reporting;
import rep_lib;
import ofstream;

import testLib;

private CONST ALLFININSTR = -1,
              ALLCURRENCY = -2;

private class TPercRatePrint(parameters)
    var m_tempOfstream = TOfstream("percRate");
    var m_tablePrinter : CTableReport;
    var m_parameters;
    var m_printFields = TArray();
    var headString = TArray();


    macro getReportWidth()
        return m_tablePrinter.GetSumLen();
    end;

    macro printNoData()
        printLn();
        println("¥â ¤ ­­ëå ¤«ï ®âç¥â ");
    end;

    macro printReportHead()
        var dateIn  = m_parameters.getDateIn();
        var dateOut = m_parameters.getDateOut();
        var backOfficeName;
        var directionName;
        var clientName;
        var contractName;
        var accountName;
        var objectKind = "";
        var objectNumber = "";
        var currencyName;
        var rateGroupName;
        var rateName;
        var dataSet;
        var isFirstColumn = true;
        var i = 0;

        headString(0) = "";
        headString(1) = "";
        headString(2) = "";
        headString(3) = "";
        headString(4) = "";
        headString(5) = "";

        if(m_parameters.getSubSystem() == -1)
            backOfficeName = "‚á¥";
            m_tablePrinter.AddColumn("íª-®ä¨á"   , 14, AL_CENTER);
            headString(0) = "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂ";
            headString(1) = "³                ³";
            headString(2) = "³    íª-®ä¨á    ³";
            headString(3) = "³                ³";
            headString(4) = "³                ³";
            headString(5) = "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "backOffice";
            isFirstColumn = false;
        else
            dataSet = TRsbDataSet("SELECT val.t_code backOffice FROM dllvalues_dbt val WHERE val.t_list = 1118 AND val.t_element = " + m_parameters.getSubSystem());
            dataSet.next();
            backOfficeName = dataSet.backOffice;
        end;

        if((m_parameters.getPercContractDirection() != 3) and (m_parameters.getPercContractDirection() > 0))
            directionName = ternary(m_parameters.getPercContractDirection() == 1, "à¨¢«¥ç¥­¨¥", " §¬¥é¥­¨¥")
        else
            directionName = "‚á¥";
            m_tablePrinter.AddColumn(" ¯"  , 1, AL_CENTER);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "³";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÂ";
            headString(1) = headString(1) + "     ³";
            headString(2) = headString(2) + " ¯à.³";
            headString(3) = headString(3) + "     ³";
            headString(4) = headString(4) + "     ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "directionKindName";
            isFirstColumn = false;
        end;

        if (m_parameters.getCodeCurrency() < 0)
            currencyName = "‚á¥";
            m_tablePrinter.AddColumn("‚ «"  , 3, AL_CENTER);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "³";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÂ";
            headString(1) = headString(1) + "     ³";
            headString(2) = headString(2) + " ‚ « ³";
            headString(3) = headString(3) + "     ³";
            headString(4) = headString(4) + "     ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "fiCode";
            isFirstColumn = false;
        else
            dataSet = TRsbDataSet("SELECT finInstr.t_fi_code codeCurrency, finInstr.t_name curName FROM dfinInstr_dbt finInstr WHERE finInstr.t_fiid = " + m_parameters.getCodeCurrency());
            dataSet.next();

            currencyName = dataSet.codeCurrency + " " + dataSet.curName;
        end;

        if (m_parameters.getPercContractNumber() != 0)
            contractName = m_parameters.getPercContractNumber();
        else
            contractName = "‚á¥";
            m_tablePrinter.AddColumn("ü „" , 10, AL_RIGHT);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "³";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÄÄÄÄÄÄÄÂ";
            headString(1) = headString(1) + "            ³";
            headString(2) = headString(2) + "    ü „    ³";
            headString(3) = headString(3) + "            ³";
            headString(4) = headString(4) + "            ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÄÄÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "contractNumber";
            isFirstColumn = false;
        end;

        if (m_parameters.getAccount() != "")
            accountName = m_parameters.getAccount();
        else
            accountName = "‚á¥";
            m_tablePrinter.AddColumn("‹¨æ¥¢®© áç¥â/|¯à¨ª« ¤­®© ®¡ê¥ªâ", 25, AL_CENTER);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "³";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂ";
            headString(1) = headString(1) + "       ‹¨æ¥¢®© áç¥â/       ³";
            headString(2) = headString(2) + "                           ³";
            headString(3) = headString(3) + "     à¨ª« ¤­®© ®¡ê¥ªâ     ³";
            headString(4) = headString(4) + "                           ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "account";
            isFirstColumn = false;
        end;

        if(m_parameters.getClient() > 0)
            dataSet = TRsbDataSet("SELECT party.t_shortName client FROM drepParty_vw party WHERE party.t_id = " + m_parameters.getClient());
            dataSet.next();
            clientName = dataSet.client;
        else
            clientName = "‚á¥";
            m_tablePrinter.AddColumn("Š«¨¥­â", 15, AL_CENTER);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "³";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂ";
            headString(1) = headString(1) + "                 ³";
            headString(2) = headString(2) + "      Š«¨¥­â     ³";
            headString(3) = headString(3) + "                 ³";
            headString(4) = headString(4) + "                 ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "clientName";
            isFirstColumn = false;
        end;

        if(m_parameters.getRateGroupNumber() > 0)
            rateGroupName = m_parameters.getRateGroupNumber();
            dataSet = TRsbDataSet("SELECT t_shortName groupName FROM dprcGrRate_dbt WHERE t_number = " + m_parameters.getRateGroupNumber());
            dataSet.next();

            rateGroupName = rateGroupName + " " + dataSet.groupName;
        else
            rateGroupName = "‚á¥";
        end;

        if(m_parameters.getRateNumber() > 0)
            dataSet = TRsbDataSet("SELECT t_name rateName FROM repPercentRate_vw WHERE t_isNotUser <> 'X' and t_number =" + m_parameters.getRateNumber());
            dataSet.next();
            rateName = m_parameters.getRateNumber() + " " +dataSet.rateName;
        else
            rateName = "‚á¥";
            m_tablePrinter.AddColumn("ü", 5, AL_CENTER);
            m_tablePrinter.AddColumn("¨¬ï ¯à®æ.áâ ¢ª¨", 15, AL_CENTER);
            if (isFirstColumn)
                headString(0) = headString(0) + "Ú";
                headString(1) = headString(1) + "³";
                headString(2) = headString(2) + "³";
                headString(3) = headString(3) + "Ã";
                headString(4) = headString(4) + "³";
                headString(5) = headString(5) + "Ã";
            end;

            headString(0) = headString(0) + "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂ";
            headString(1) = headString(1) + "    à®æ¥­â­ ï áâ ¢ª     ³";
            headString(2) = headString(2) + "                         ³";
            headString(3) = headString(3) + "ÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
            headString(4) = headString(4) + "   ü   ³    ­ §¢ ­¨¥     ³";
            headString(5) = headString(5) + "ÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅ";

            m_printFields[m_printFields.size] = "rateNumber";
            m_printFields[m_printFields.size] = "nameRate";
            isFirstColumn = false;
        end;

        m_tablePrinter.AddColumn("ˆ­¤.", 4, AL_CENTER);
        m_tablePrinter.AddColumn("„ â ", 10, AL_CENTER);
        m_tablePrinter.AddColumn("‘â ¢ª ", 15, AL_CENTER);

        if (isFirstColumn)
            headString(0) = headString(0) + "Ú";
            headString(1) = headString(1) + "³";
            headString(2) = headString(2) + "³";
            headString(3) = headString(3) + "³";
            headString(4) = headString(4) + "³";
            headString(5) = headString(5) + "Ã";
        end;

        headString(0) = headString(0) + "ÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿";
        headString(1) = headString(1) + "      ³             à ¬¥âàë         ³";
        headString(2) = headString(2) + "      ³       ¯à®æ¥­â­ëå áâ ¢®ª      ³";
        headString(3) = headString(3) + " ˆ­¤. ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";
        headString(4) = headString(4) + "      ³    ¤ â     ³     áâ ¢ª  %    ³";
        headString(5) = headString(5) + "ÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´";

        m_printFields[m_printFields.size] = "isUser";
        m_printFields[m_printFields.size] = "rateDate";
        m_printFields[m_printFields.size] = "rateValue";

        printBankHeader(m_parameters.getDepartmentCode(), m_parameters.getOrgStructure(), getReportWidth());

        println();
        println(strAlign("„ ­­ë¥ ® ¯ à ¬¥âà å ¯à®æ¥­â­ëå áâ ¢®ª ¯® „", getReportWidth(), STR_ALIGN_CENTER));
        printLn();

        if (dateIn == dateOut)
            printLn(strAlign("§  " + dateIn, getReportWidth(), STR_ALIGN_CENTER));
        else
            printLn(strAlign("c " + dateIn + " ¯® " + dateOut, getReportWidth(), STR_ALIGN_CENTER));
        end;

        printLn();
        printLn("íª-®ä¨á: " + backOfficeName);
        printLn();
        printLn(" ¯à ¢«¥­¨¥: " + directionName);
        printLn();
        printLn("Š«¨¥­â: " + clientName);
        printLn("à®æ¥­â­ë© ¤®£®¢®à: " + contractName);
        printLn();
        printLn("‹¨æ¥¢®© áç¥â: " + accountName);

        printLn();
        printLn("‚ «îâ : " + currencyName);

        printLn();
        printLn("ƒàã¯¯  áâ ¢®ª: " + rateGroupName);
        printLn("à®æ¥­â­ ï áâ ¢ª : " + rateName);

    end;

    macro execute(dataSet)
        var isEmpty : bool = true;
        var contractName = "";
        var operName = "";
        var branchName = "";
        var curClient = null;
        var clientName = "";
        var printArray = TArray();
        var curPD = null;
        var i;

        printReportHead();

        while (dataSet.next())
            isEmpty = false;

            if (curPD != dataSet.contractId)
                if (curPD != null)
                    m_tablePrinter.printSeparator();
                else
                    i = 0;
                    while (i < 6)
                        printLn(headString(i));
                        i = i + 1;
                    end;
                end;
                curPD = dataSet.contractId;

                i = 0;
                while (i < m_printFields.size )
                    if ( m_printFields(i) != "rateValue" )
                        printArray(i) = dataSet.value(m_printFields(i));
                    else
                        printArray(i) = string(dataSet.value(m_printFields(i)) : 12 : 2 : c );
                    end;
                    i = i + 1;
                end;
            else
                i = 0;
                while (i < m_printFields.size )
                    if (i < m_printFields.size - 2)
                        printArray(i) = "";
                    else
                        if ( m_printFields(i) != "rateValue" )
                           printArray(i) = dataSet.value(m_printFields(i));
                        else
                           printArray(i) = string(dataSet.value(m_printFields(i)) : 12 : 2 : c );
                        end;


                    end;
                    i = i + 1;
                end;
            end;

            m_tablePrinter.PrintStringTransferByWord(printArray);
        end;

        if (isEmpty)
            printNoData();
        else
            m_tablePrinter.printBottom();
        end;

        printLn();
        printLn("¯¥à æ¨®­¨áâ " + {name_oper});

        m_tempOfstream.show();
    end;

    private macro constructorTPercRatePrint(parameters)
        m_parameters = parameters;
        m_tempOfstream.setOutputFile();
        m_tablePrinter = CTableReport();
    end;

    constructorTPercRatePrint(parameters);
end;


class TPercRateData(param)

    macro constructor(param)
        SQL_Execute("{ CALL  rep_data.setBalancePlanNumber(0) }");
        SQL_Execute("{ CALL  rep_data.setBeginDate(" + GetSQLDate(param.getDateIn()) + ") }");
        SQL_Execute("{ CALL  rep_data.setEndDate(" +  GetSQLDate(param.getDateOut()) + ") }");
   end;

    macro getQueryText(param)
        var fromText;
        var whereText;
        var sortText;
        var selectText;
        var sortingArraySize = param.getSort.Size();
        var i;

        selectText = " SELECT finInstr.t_code fiCode, finInstr.t_id fiId, percContract.t_id contractId,           "
            + "\n" + "        percContract.t_number contractNumber, percContract.t_account account, percRateHistory.t_date rateDate,     "
            + "\n" + "        DECODE(percContract.t_directionKind,  1, '', 2, '') directionKindName, "
            + "\n" + "        percRateHistory.t_value rateValue, percRate.t_name nameRate,  percRate.t_number rateNumber,            "
            + "\n" + "        percRate.t_isUser isUser        ";

          fromText = "   FROM repPercentContract_vw percContract,                    "
            + "\n" + "        drepFinInstrument_vw finInstr,                         "
            + "\n" + "        drepAccount_vw account,                                "
            + "\n" + "        repPercentRate_vw percRate,                            "
            + "\n" + "        repPercentRateChangeHistory_vw percRateHistory         ";
         whereText = "  WHERE percContract.t_fiid     = fininstr.t_id                "
            + "\n" + "    AND percContract.t_account  = account.t_account            "
            + "\n" + "    AND percContract.t_id       = percRateHistory.t_contractid "
            + "\n" + "    AND percRateHistory.t_rateId  = percRate.t_id              "
            + "\n" + "    AND percRateHistory.t_date >= NVL((SELECT MAX(maxDate.t_date)"
            + "\n" + "                                         FROM repPercentRateChangeHistory_vw maxDate"
            + "\n" + "                                        WHERE maxDate.t_contractId = percRateHistory.t_contractid"
            + "\n" + "                                          AND maxDate.t_rateId = percRateHistory.t_rateId"
            + "\n" + "                                          AND maxDate.t_date <= " + getSqlDate(param.getDateIn())
            + "\n" + "                                      ), " + getSqlDate(param.getDateIn())
            + "\n" + "                                     )"
            + "\n" + "    AND percRateHistory.t_date <= " + getSqlDate(param.getDateOut())
            + "\n" + "    AND " + RepBranchAndDepartmentFieldFilter(param.getDepartmentList()).GetAsSqlString("account.t_departmentId",
                                                                                                               "account.t_branchId");

        if ((param.getCodeCurrency() != ALLCURRENCY) AND (param.getCodeCurrency() != ALLFININSTR))
            whereText = whereText + " AND fininstr.t_id = " + param.getCodeCurrency();
        elif (param.onlyForeignCur())
            whereText = whereText + " AND fininstr.t_id <> " + NATCUR;
        end;

        if (param.getOper() != 0)
            if (param.getOperOrGroup() > 0)
                fromText  = fromText  + ", repUserGroup_vw operGroup ";
                whereText = whereText + " AND operGroup.t_userId = percContract.t_operId ";
                whereText = whereText + " AND operGroup.t_id = " + param.getOper();
            else
                whereText = whereText + " AND perccontract.t_operId = " + param.getOper();
            end;
        end;

        if (param.getSubSystem() > 0)
            whereText = whereText + " AND perccontract.t_backOfficecode = " + param.getSubSystem();
        else
            selectText = selectText + ", (SELECT val.t_code backOffice "
                             + "\n" + "     FROM dllvalues_dbt val "
                             + "\n" + "    WHERE val.t_list = 1118 "
                             + "\n" + "      AND val.t_element = percContract.t_backOfficecode ) backOffice";
        end;

        if (param.getClient() > 0)
            whereText  = whereText  + " AND perccontract.t_clientId = " + param.getClient();
        else
            selectText = selectText + ",(SELECT party.t_shortName"
                             + "\n" + "   FROM drepParty_vw party"
                             + "\n" + "  WHERE party.t_id = percContract.t_clientId"
                             + "\n" + ") clientName";
        end;

        if (param.getPercContractNumber() != 0)

            whereText = whereText + " AND percContract.t_number = " + param.getPercContractNumber();

        else

            if (param.getAccount() != "")
                whereText = whereText + " AND (" + convertMaskToSQLFormat(param.getAccount(), "perccontract.t_account") + ")";
            end;

            if ((param.getPercContractDirection() != 3) and (param.getPercContractDirection() > 0))
                whereText = whereText + " AND perccontract.t_directionKind = " + param.getPercContractDirection();
            end;

            if (param.getChapter() != 0)
                whereText = whereText + " AND perccontract.t_chapter = " + param.getChapter();
            end;

            if (param.getBalance() != "")
                whereText = whereText + " AND (" + convertMaskToSQLFormat( param.getBalance(), "account.t_basicPlanBalance") + ")";
            end;

            if (param.getPercContractUserType() != "")
                whereText = whereText + " AND " + getPatternFilterClause("perccontract.t_userType", param.getPercContractUserType(), param.getPercContractUserTypeMethod());
                // whereText = whereText + " AND regexp_like(perccontract.t_userType,'[" + param.getPercContractUserType()+"]')";
            end;

            if (param.getUseIndividualRate() == 1)
                whereText = whereText + " AND percRate.t_isUser = 'X' ";
            else
                if (param.getRateGroupNumber() > 0)
                    whereText = whereText + " AND percRate.t_groupNumber = " + param.getRateGroupNumber();
                end;

                if (param.getRateNumber() > 0)
                    whereText = whereText + " AND percRate.t_Number = " + param.getRateNumber();
                end;

            end;

        end;

        sortText = " ORDER BY ";

        if (sortingArraySize != 0)
            i = 0;

            while (i < sortingArraySize)
              if (param.getSort()[i] == REP_PATTERNSORT_DEPARTMENT) // ¯® ¯®¤à §¤¥«¥­¨î
                  sortText = sortText + "account.t_branchId asc, ";
              elif (param.getSort()[i] == REP_PATTERNSORT_OPER) // ¯® ®¯¥à æ¨®­¨áâã
                  sortText = sortText + "perccontract.t_operId asc, ";
              elif (param.getSort()[i] == REP_PATTERNSORT_CLIENT) // ¯® ª«¨¥­âã
                  sortText = sortText + "perccontract.t_clientId asc, ";
              end;

              i = i +1;
            end;
        end;

        sortText = sortText + " percRate.t_number asc,";
        sortText = sortText + " percContract.t_backOffice asc, percContract.t_backOfficeCode asc, ";
        sortText = sortText + " percContract.t_directionKind  desc,";
        sortText = sortText + " finInstr.t_code asc, percContract.t_fiId asc,";
        sortText = sortText + " percContract.t_number asc, percContract.t_id asc,";
        sortText = sortText + " percRateHistory.t_date";

        return selectText + fromText + whereText + sortText;
    end;

    macro getDataSet(param)
        var dataSet;
        dataSet = TRsbDataSet(getQueryText(param));

        dataSet.setFieldType("rateDate", V_DATE);

        return dataSet;
    end;

    constructor(param);
end;

class TParamPercentRate(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualRate, rateNumber, percContractDirection,
                                      patternSort, percContractChargeObjectKind, chapter, balance, account, rateGroupNumber)

    private var m_numDprt;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_codeCurrency;
    private var m_foreignCur;
    private var m_dateIn;
    private var m_dateOut;
    private var m_oper;
    private var m_operOrGroup;
    private var m_subSystem;
    private var m_percContractNumber;
    private var m_client;
    private var m_percContractUserType;
    private var m_percContractUserTypeMethod;
    private var m_useIndividualRate;
    private var m_percContractDirection;
    private var m_patternSort;
    private var m_percContractChargeObjectKind;
    private var m_chapter;
    private var m_balance;
    private var m_account;
    private var m_rateGroupNumber;
    private var m_departmentList;
    private var m_rateNumber;

    macro getPercContractChargeObjectKind()
        return m_percContractChargeObjectKind;
    end;

    macro getDateIn()
        return m_dateIn;
    end;

    macro getDateOut()
        return m_dateOut;
    end;

    macro getDepartmentList()
        return m_departmentList;
    end;

    macro getOrgStructure()
        return m_orgStructure;
    end;

    macro getDepartmentCode()
        return m_numDprt;
    end;

    macro getCodeCurrency()
        return m_codeCurrency;
    end;

    macro onlyForeignCur()
        return m_foreignCur;
    end;

    macro getPercContractNumber()
        return m_percContractNumber;
    end;

    macro getAccount()
        return m_account;
    end;

    macro getOper()
        return m_oper;
    end;

    macro getOperOrGroup()
        return m_operOrGroup;
    end;

    macro getSubSystem()
        return m_subSystem;
    end;

    macro getPercContractDirection()
        return m_percContractDirection;
    end;

    macro getChapter()
        return m_chapter;
    end;

    macro getBalance()
        return m_balance;
    end;

    macro getClient()
        return m_client;
    end;

    macro getPercContractUserType()
        return m_percContractUserType;
    end;

    macro getPercContractUserTypeMethod()
        return m_percContractUserTypeMethod;
    end;

    macro getUseIndividualRate()
        return m_useIndividualRate;
    end;

    macro getRateGroupNumber()
        return m_rateGroupNumber;
    end;

    macro getRateNumber()
        return m_rateNumber;
    end;

    macro getSort()
        return m_patternSort;
    end;

    macro constructor(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualRate, rateNumber, percContractDirection,
                                      patternSort, percContractChargeObjectKind, chapter, balance, account, rateGroupNumber)

        m_numDprt                      = numDprt;
        m_orgStructure                 = orgStructure;
        m_issueMode                    = issueMode;
        m_codeCurrency                 = codeCurrency;
        m_foreignCur                   = foreignCur;
        m_dateIn                       = dateIn;
        m_dateOut                      = dateOut;
        m_oper                         = oper;
        m_subSystem                    = subSystem;
        m_percContractNumber           = percContractNumber;
        m_client                       = client;
        m_percContractUserType         = percContractUserType;
        m_percContractUserTypeMethod   = percContractUserTypeMethod;
        m_useIndividualRate            = useIndividualRate;
        m_percContractDirection        = percContractDirection;
        m_patternSort                  = patternSort;
        m_percContractChargeObjectKind = percContractChargeObjectKind;
        m_chapter                      = chapter;
        m_balance                      = balance;
        m_account                      = account;
        m_operOrGroup                  = operOrGroup;
        m_rateGroupNumber              = rateGroupNumber;
        m_rateNumber                   = rateNumber;

        m_departmentList = RepDepartmentList(m_orgStructure, m_issueMode, m_numDprt);

    end;


    constructor(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                      client, percContractUserType, percContractUserTypeMethod, useIndividualRate, rateNumber, percContractDirection,
                                      patternSort, percContractChargeObjectKind, chapter, balance, account, rateGroupNumber);
end;

macro printPercentReport(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                  client, percContractUserType, percContractUserTypeMethod, useIndividualRate, rateNumber, percContractDirection, 
                                  patternSort, percContractChargeObjectKind, chapter, balance, account, rateGroupNumber)


    var profiler = TSQLProfiler(getTxtFileName("!PercRateSheetProfile"));
    profiler.clear();
    profiler.on();

    var parameters = TParamPercentRate(numDprt, orgStructure, issueMode, codeCurrency, foreignCur, dateIn, dateOut, operOrGroup, oper, subSystem, percContractNumber,
                                             client, percContractUserType, percContractUserTypeMethod, useIndividualRate, rateNumber, percContractDirection,
                                             patternSort, percContractChargeObjectKind, chapter, balance, account, rateGroupNumber);

    /* Š®­âà®«ì ®âªàëâëå ®¯¥à¤­¥© */
     if (not RepOperdaysOpened(parameters.getDepartmentList(), parameters.getDateIn(), parameters.getDateOut()).shouldContinue())
        exit(1);
    end;

    var percRatePrint = TPercRatePrint(parameters).execute(TPercRateData(parameters).getDataSet(parameters));

    return true;

end;
