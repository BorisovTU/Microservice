/*
$Name:          ibr_BK_OPACC.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Книга регистрации открытых счетов"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import Bk_opacc;

private const TAB_REPORT = "Параметры отчета";
private const TAB_EXTENDED = "Дополнительные параметры";

private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_EXTENDED = "Дополнительные параметры";

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report = facade.createReport("dyn_opacc", "Книга регистрации открытых счетов", "ibr_BK_OPACC.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();
    var tabExtended = facade.createTab(TAB_EXTENDED, 3);
    var groupExtended = facade.createGroup(GRP_EXTENDED, 1);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldNameAlg("ФормаОтчета", "Форма отчета", 0, 2244)); // поменять справочник
    groupReport.add(facade.createFieldLlValues("Подсистема", "Подсистема", 1, 1628)); // поменять справочник

    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Период с:"));
    groupReport.add(facade.createFieldDate("КонецПериода", "по:"));
    groupReport.add(facade.createFieldDate("СданАрхив", "Сдана в архив:"));

    groupReport.add(facade.createFieldNameAlg("Счета", "Счета", 1, 2080)); // поменять справочник

    facade.resetFieldsOrder();
    groupExtended.add(facade.createFieldCheckBox("ОтчетПоОткрытымСчетам", "Отчет по открытым счетам", true));
    groupExtended.add(facade.createFieldCheckBox("ОтчетПоЗакрытымСчетам", "Отчет по закрытым счетам", true));
    groupExtended.add(facade.createFieldCheckBox("ЗаКаждыйДень", "За каждый день", false));
    groupExtended.add(facade.createFieldCheckBox("ПечатьДанныхО_Фондах", "Печать данных о фондах", false));

    tabReport.add(groupReport);
    tabExtended.add(groupExtended);

    report.add(tabReport);
    report.add(tabExtended);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";
 
    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"ФормаОтчета").valueInteger,
                                          getValue(report, gRep+"Подсистема").valueInteger,
                                          getValue(report, gRep+"Подразделения").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          getValue(report, gRep+"НачалоПериода").valueDate,
                                          getValue(report, gRep+"КонецПериода").valueDate,
                                          getValue(report, gRep+"СданАрхив").valueDate,
                                          TIbrCommon().getIdentifierAccountChapter( getValue(report, gRep+"ГлаваСчетов").valueString),
                                          getValue(report, gRep+"Счета").valueInteger, 
                                          getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                          getValue(report, gExt+"ОтчетПоОткрытымСчетам").valueBoolean,
                                          getValue(report, gExt+"ОтчетПоЗакрытымСчетам").valueBoolean,
                                          getValue(report, gExt+"ЗаКаждыйДень").valueBoolean,
                                          getValue(report, gExt+"ПечатьДанныхО_Фондах").valueBoolean,
                                          TRUE);

    if ( NOT (result.valueBoolean))
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString) == -1)
        repErrorsQueue().add(0, "Недопустимая глава счетов");
        result.valueBoolean = FALSE;
    end;

    if (((getValue(report, gRep+"ФормаОтчета").valueInteger == 0) or (getValue(report, gRep+"ФормаОтчета").valueInteger == 2)) and 
        not (getValue(report, gExt+"ОтчетПоОткрытымСчетам").valueBoolean))
        repErrorsQueue().add(0, "Если выбрана Форма отчета  'Книга регистрации' или 'Книга регистрации на дату'," +  "\n" + 
                                 "флаг 'Отчет по открытым счетам' должен быть установлен");
        result.valueBoolean = FALSE;
    end;

    if (((getValue(report, gRep+"ФормаОтчета").valueInteger == 0) or (getValue(report, gRep+"ФормаОтчета").valueInteger == 2)) and 
        (getValue(report, gExt+"ЗаКаждыйДень").valueBoolean))
        repErrorsQueue().add(0, "Если выбрана Форма отчета 'Книга регистрации' или 'Книга регистрации на дату'," + "\n" +
                                "то флаг 'За каждый день периода' должен быть не установлен.");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gRep+"ФормаОтчета").valueInteger == 2) and (getValue(report, gExt+"ОтчетПоЗакрытымСчетам").valueBoolean))
        repErrorsQueue().add(0, "Если выбрана Форма отчета 'Книга регистрации на дату'," +  "\n" + 
                                 "флаг 'Отчет по закрытым счетам' должен быть не установлен");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gRep+"ФормаОтчета").valueInteger == 2) and 
        (getValue(report, gRep+"НачалоПериода").valueDate != {curdate}))
        repErrorsQueue().add(0, "Если выбрана Форма отчета 'Книга регистрации на дату'," + "\n" +
                                "то 'Период с' должен быть равен текущей дате");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"НачалоПериода").valueDate > {curdate})
        repErrorsQueue().add(0, "Дата начала периода не может быть больше даты текущего дня");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"КонецПериода").valueDate < getValue(report, gRep+"НачалоПериода").valueDate)
        repErrorsQueue().add(0, "Дата конца периода расчета должна быть больше даты начала");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"СданАрхив").valueDate < getValue(report, gRep+"КонецПериода").valueDate)
        repErrorsQueue().add(0, "Дата сдачи в архив не может быть меньше даты окончания периода");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;