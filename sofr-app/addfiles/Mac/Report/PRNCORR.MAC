/*
$Name:          prncorr.mac
$Module:        ‚­ãâà¥­­ïï ®âç¥â­®áâì
$Description:   ¥ª®àà¥ªâ­ë¥ ¯à®¢®¤ª¨ ¯® ¢ «îâ­ë¬ áç¥â ¬
*/
/*ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
RS-Bank 6.0                                           R-Style Software Lab

File Name   : prncorr.mac                      03.08.2011
Programmer  : gww
Description : ¥ª®àà¥ªâ­ë¥ ¯à®¢®¤ª¨ ¯® ¢ «îâ­ë¬ áç¥â ¬
Comment     :
Modifications:
    19.04.2012 - SCR 173293. ‚¥¤®¬®áâì ­¥ª®àà¥ªâ­ëå ¯à®¢®¤®ª. Žè¨¡ª¨ ¢ ¯¥ç â­®© ä®à¬¥
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ*/
import FIInter;
import rep_lib;
import ofstream;
import Reporting;
import ocp;
import RepSqlContextInitializer;

private CONST ALLFININSTR = -1,
                ALLCURRENCY = -2;

var reportBodyFileName;

private class TNCorrPrint(parameters)
    var m_tempOfstream = TOfstream("ncorr");
    var m_tablePrinter : CTableReport;
    var m_parameters;

    macro setHead()
        m_tablePrinter.AddColumn("„ â ",    10, AL_CENTER);
        m_tablePrinter.AddColumn("®¬",   5, AL_CENTER);
        m_tablePrinter.AddColumn("‘ç¥â„â",  25, AL_CENTER);
        m_tablePrinter.AddColumn("‚ «" ,  4,  AL_CENTER);
        m_tablePrinter.AddColumn("‘ã¬¬  „â", 16, AL_RIGHT);
        m_tablePrinter.AddColumn("‘ç¥â Šâ",  25, AL_CENTER);
        m_tablePrinter.AddColumn("‚ «",   4,  AL_CENTER);
        m_tablePrinter.AddColumn("‘ã¬¬  Šâ", 16, AL_RIGHT);
        m_tablePrinter.AddColumn("„¥¡¥â æ", 16, AL_RIGHT);
        m_tablePrinter.AddColumn("Šà¥¤¨â æ", 16, AL_RIGHT);

    end;

    macro getReportWidth()
        return m_tablePrinter.GetSumLen();
    end;

    macro printNoData()
        printLn();
        println("¥â ¤ ­­ëå ¤«ï ®âç¥â ");
    end;

    macro printReportHead()
        var dateIn  = m_parameters.getDateIn();
        var dateOut = m_parameters.getDateOut();

        printBankHeader(m_parameters.getDepartmentCode(), m_parameters.getOrgStructure(), getReportWidth());

        println();
        println(strAlign("‚¥¤®¬®áâì ­¥ª®àà¥ªâ­ëå ¯à®¢®¤®ª ¯® ¢ «îâ­ë¬ áç¥â ¬", getReportWidth(), STR_ALIGN_CENTER));
        printLn();

        if (dateIn == dateOut)
            printLn(strAlign("§  " + dateIn, getReportWidth(), STR_ALIGN_CENTER));
        else
            printLn(strAlign("c " + dateIn + " ¯® " + dateOut, getReportWidth(), STR_ALIGN_CENTER));
        end;

    end;

    macro printTableHead()
       [ÚÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        ³    „ â     ³ ®¬¥à ³                           ³‚ «îâ ³                  ³                           ³‚ «îâ ³                  ³       ª¢¨¢ «¥­â ¢ ­ æ.¢ «îâ¥       ³
        ³            ³ ¤®ªã¬¥³          ‘ç¥â „â          ³      ³      ‘ã¬¬  „â    ³          ‘ç¥â Šâ          ³      ³      ‘ã¬¬  Šâ    ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
        ³            ³ ­â    ³                           ³      ³                  ³                           ³      ³                  ³    ˆ§ ¯à®¢®¤®ª   ³    ® ªãàáã –   ³
        ³            ³       ³                           ³      ³                  ³                           ³      ³                  ³      „â (Šâ)     ³       „â (Šâ)    ³
        ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
        ];

    end;

    macro execute(dataSet, badCurDataSet)
        var isEmpty : bool = true;
        var curChapter   = null;
        var curGroupOper = 0;
        var curOper      = null;
        var strName = "";
        var needTableHead = false;
        var data;
        var strBadCur = "";

        printReportHead();

        while (badCurDataSet.next())
            if (isEmpty)
                isEmpty = false;
            else
                strBadCur = strBadCur + ",";
            end;

            strBadCur = strBadCur + badCurDataSet.fiCode;
        end;

        if (not isEmpty)
            printLn();
            printLn("Šãàá – ¤«ï ¢ «îâë " + strBadCur + " ­¥ § ¤ ­, áç¥â  ¢ ®âç¥â ­¥ ¢ª«îç¥­ë");
            printLn();

            isEmpty = true;
        end;

        while (dataSet.next())
            if (curChapter != dataSet.chapter)
                if (curChapter != null)
                    m_tablePrinter.printBottom();
                end;

                curChapter = dataSet.chapter;
                curGroupOper = 0;
                curOper = null;
                data = TRsbDataSet( "SELECT t_name name FROM dobchaptr_dbt WHERE t_chapter = " + curChapter );
                if( data.moveNext )
                    strName = data.name;
                end;

                printLn();
                printLn(strAlign("ƒ« ¢  " + curChapter + ". " + strName, getReportWidth(), STR_ALIGN_CENTER));
                printLn();
                needTableHead = true;
            end;

            if (curGroupOper != dataSet.groupOperId)
                curGroupOper = dataSet.groupOperId;
                curOper = null;

                printLn("ƒàã¯¯  ®¯¥à æ¨®­¨áâ®¢ ü " + curGroupOper);
                needTableHead = true;
            end;

            if (curOper != dataSet.oper)
                curOper = dataSet.oper;

                printLn("Ž¯¥à æ¨®­¨áâ ü " + curOper);
                needTableHead = true;
            end;

            isEmpty = false;

            if (needTableHead)
                printTableHead();
                needTableHead = false;
            else
                m_tablePrinter.printSeparator();
            end;

            m_tablePrinter.printString(dataSet.dateDoc, dataSet.numberDoc, dataSet.debetAcc, dataSet.debetFiCode, dataSet.debetSum,
                                       dataSet.creditAcc, dataSet.creditFiCode, dataSet.creditSum, dataSet.debetNatCurSum, dataSet.debetConvSum);

            if(dataSet.debetFiCode != dataSet.creditFiCode)
                m_tablePrinter.printString("","","","","","","","", dataSet.creditNatCurSum, dataSet.creditConvSum );
            end;
        end;

        if (isEmpty)
            printNoData();
        else
            m_tablePrinter.printBottom();
        end;

        printLn();
        printLn("Ž¯¥à æ¨®­¨áâ " + {name_oper});

        m_tempOfstream.show();
    end;

    private macro constructorTNCorrPrint(parameters)
        m_parameters = parameters;
        m_tempOfstream.setOutputFile();
        m_tablePrinter = CTableReport();
        setHead();
    end;

    constructorTNCorrPrint(parameters);
end;

class TNCorrData(param)
    var m_param = param;
    var m_selectText;
    var m_fromText;
    var m_whereText;

    private macro setCommonQueryText();
        var documentFilter = RepBranchAndDepartmentFieldFilter(m_param.getDepartmentList());

        m_fromText = "   FROM drepDocument_vw doc,      "
            + "\n" + "        drepAccount_vw  accDebet, "
            + "\n" + "        drepAccount_vw  accCredit ";

        m_whereText = " WHERE doc.t_debetAccountId = accDebet.t_accountId  "
            + "\n" + "    AND doc.t_creditAccountId = accCredit.t_accountId "
            + "\n" + "    AND doc.t_isRevaluation = 0 "
            + "\n" + "    AND doc.t_isDeltaRate   = 0 "
            + "\n" + "    AND accDebet.t_isSecurities = 0 "
            + "\n" + "    AND accCredit.t_isSecurities = 0 "
            + "\n" + "    AND (   doc.t_debetFiId != 0 "
            + "\n" + "         OR doc.t_creditFiId != 0 "
            + "\n" + "        ) "
            + "\n" + "    AND doc.t_date BETWEEN " + getSqlDate(m_param.getDateIn())
            + "\n" + "                       AND " + getSqlDate(m_param.getDateOut())
            + "\n" + "    AND (   " + documentFilter.getAsSqlString("accDebet.t_departmentId",  "accDebet.t_branchId")
            + "\n" + "         OR " + documentFilter.getAsSqlString("accCredit.t_departmentId", "accCredit.t_branchId")
            + "\n" + "        )";

        if (not FlagOfPrintAccountsOCP())
            m_whereText = m_whereText + "AND (accDebet.t_type not like '%™%')"
            + "\n" + "                   AND (accCredit.t_type not like '%™%')";
        end;

        if ((m_param.getCodeCurrency() != ALLCURRENCY) AND (m_param.getCodeCurrency() != ALLFININSTR))
            m_whereText = m_whereText + " AND (   doc.t_debetFiId = " + m_param.getCodeCurrency()
                           + "\n" + "      OR doc.t_creditFiId = " + m_param.getCodeCurrency()
                           + "\n" + "     )";
        end;


        if (m_param.getOper() != 0)
            if (m_param.getOperOrGroup() > 0)
                m_fromText  = m_fromText  + ", repUserGroup_vw operGroup ";
                m_whereText = m_whereText + " AND operGroup.t_userId = doc.t_operId ";
                m_whereText = m_whereText + " AND operGroup.t_id = " + m_param.getOper();
            else
                m_whereText = m_whereText + " AND doc.t_operId = " + m_param.getOper();
            end;
        end;

        if (m_param.getChapter() != 0)
            m_whereText = m_whereText + " AND doc.t_chapter = " + m_param.getChapter();
        end;

    end;

    private macro getBadCurQueryText()
        var selectText = " SELECT t_code fiCode";
        var groupText  = " GROUP BY t_code";
        var fromText   =  m_fromText + ", drepFinInstrument_vw finInstr";
        var whereText  = m_whereText + " AND (  (finInstr.t_id = doc.t_debetFiId  AND doc.t_debetConvertionCBSum  IS NULL)"
                              + "\n" + "     OR (finInstr.t_id = doc.t_creditFiId AND doc.t_creditConvertionCBSum IS NULL))";

        return selectText + fromText + whereText + groupText;
    end;

    private macro getDataQueryText()
        var sortText;
        var groupOper;
        var whereText;

        var selectText = " SELECT doc.t_date                                           dateDoc,         "
                + "\n" + "        doc.t_number                                         numberDoc,       "
                + "\n" + "        doc.t_chapter                                        chapter,         "
                + "\n" + "        doc.t_operId                                         oper,            "
                + "\n" + "        doc.t_debetAccount                                   debetAcc,        "
                + "\n" + "        doc.t_creditAccount                                  creditAcc,       "
                + "\n" + "        doc.t_debetFiId                                      debetFiId,       "
                + "\n" + "        doc.t_creditFiId                                     creditFiId,      "
                + "\n" + "        (SELECT t_code                                                        "
                + "\n" + "           FROM drepFinInstrument_vw finInstr                                 "
                + "\n" + "          WHERE finInstr.t_id = doc.t_debetFiId)             debetFiCode,     "
                + "\n" + "        (SELECT t_code                                                        "
                + "\n" + "           FROM drepFinInstrument_vw finInstr                                 "
                + "\n" + "          WHERE finInstr.t_id = doc.t_creditFiId)            creditFiCode,    "
                + "\n" + "        doc.t_debetSum                                       debetSum,        "
                + "\n" + "        doc.t_creditSum                                      creditSum,       "
                + "\n" + "        (doc.t_inCoverSum + nvl(doc.t_debetDeltaRateSum,0))  debetNatCurSum,  "
                + "\n" + "        (doc.t_inCoverSum + nvl(doc.t_creditDeltaRateSum,0)) creditNatCurSum, "
                + "\n" + "        doc.t_debetConvertionCBSum                           debetConvSum,    "
                + "\n" + "        doc.t_creditConvertionCBSum                          creditConvSum    ";

        whereText = m_whereText + " AND NOT doc.t_debetConvertionCBSum  IS NULL"
                         + "\n" + " AND NOT doc.t_creditConvertionCBSum IS NULL"
                         + "\n" + " AND (   (doc.t_inCoverSum + NVL(doc.t_creditDeltaRateSum, 0)) != doc.t_creditConvertionCBSum"
                         + "\n" + "      OR (doc.t_inCoverSum + NVL(doc.t_debetDeltaRateSum, 0))  != doc.t_debetConvertionCBSum"
                         + "\n" + "     )";


        sortText = " ORDER BY doc.t_chapter asc ";
        groupOper = ", 0 groupOperId";
        if (m_param.getOper() != 0)
            if (m_param.getOperOrGroup() > 0)
                groupOper = ", operGroup.t_id groupOperId ";
                sortText  = sortText  + ", operGroup.t_id asc";
            end;
        end;

        sortText = sortText + ", doc.t_operId asc";
        selectText = selectText + groupOper;

        var sortingArraySize = m_param.getSort.Size();
        var i;

        if (sortingArraySize != 0)
            i = 0;

            while (i < sortingArraySize)
                if (m_param.getSort()[i] == REP_PATTERNSORT_ACCOUNT)
                sortText = sortText + ", debetAcc asc";
                elif (m_param.getSort()[i] == REP_PATTERNSORT_CURRENCY)
                sortText = sortText + ",debetFiCode asc, doc.t_debetFiId asc";
                elif (m_param.getSort()[i] == REP_PATTERNSORT_NUMBER_DOCUMENT)
                sortText = sortText + ", numberDoc asc, doc.t_id asc";
                elif (m_param.getSort()[i] == REP_PATTERNSORT_DATE_DOCUMENT)
                sortText = sortText + ", dateDoc asc";
                end;

                i = i +1;
            end;
        end;

        return selectText + m_fromText + whereText + sortText;
    end;

    macro getDataSet()
        var dataSet;
        dataSet = TRsbDataSet(getDataQueryText());

        dataSet.setFieldType("dateDoc", V_DATE);
        dataSet.setFieldType("debetSum",  V_MONEY);
        dataSet.setFieldType("creditSum",  V_MONEY);
        dataSet.setFieldType("debetConvSum", V_MONEY);
        dataSet.setFieldType("creditConvSum", V_MONEY);
        dataSet.setFieldType("debetNatCurSum", V_MONEY);
        dataSet.setFieldType("creditNatCurSum", V_MONEY);

        return dataSet;
    end;

    macro getBadCurDataSet()
        return TRsbDataSet(getBadCurQueryText());
    end;

    macro constructor()
        SQL_Execute("{ CALL  rep_data.setBalancePlanNumber(" + m_param.getPlanNumber() + ") }");
        SQL_Execute("{ CALL  rep_data.setBeginDate(" + GetSQLDate(m_param.getDateIn()) + ") }");
        SQL_Execute("{ CALL  rep_data.setEndDate(" +  GetSQLDate(m_param.getDateOut()) + ") }");

        setCommonQueryText();
    end;

    constructor();
end;

class TParamNCorr(numDprt, orgStructure, issueMode, numPlan, chapter, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort)

    private var m_numDprt;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_codeCurrency;
    private var m_dateIn;
    private var m_dateOut;
    private var m_oper;
    private var m_operOrGroup;
    private var m_sort;
    private var m_chapter;
    private var m_departmentList;
    private var m_planNumber;
    private var m_withExcludedChapters;


    macro getDateIn()
        return m_dateIn;
    end;

    macro getDateOut()
        return m_dateOut;
    end;

    macro getDepartmentList()
        return m_departmentList;
    end;

    macro getOrgStructure()
        return m_orgStructure;
    end;

    macro getDepartmentCode()
        return m_numDprt;
    end;

    macro getCodeCurrency()
        return m_codeCurrency;
    end;

    macro getOper()
        return m_oper;
    end;

    macro getOperOrGroup()
        return m_operOrGroup;
    end;

    macro getChapter()
        return m_chapter;
    end;

    macro getSort()
        return m_sort;
    end;

    macro getPlanNumber()
        return m_planNumber;
    end;

    macro getWithExcludedChapters()
        return m_withExcludedChapters;
    end;

    macro constructor(numDprt, orgStructure, issueMode, numPlan, chapter, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort)

        m_numDprt              = numDprt;
        m_orgStructure         = orgStructure;
        m_issueMode            = issueMode;
        m_codeCurrency         = codeCurrency;
        m_dateIn               = dateIn;
        m_dateOut              = dateOut;
        m_oper                 = oper;
        m_chapter              = chapter;
        m_operOrGroup          = operOrGroup;
        m_sort                 = sort;
        m_planNumber           = numPlan;
        m_withExcludedChapters = withExcludedChapters;

        m_departmentList = RepDepartmentList(m_orgStructure, m_issueMode, m_numDprt);
    end;


    constructor(numDprt, orgStructure, issueMode, numPlan, chapter, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort);
end;


/*
    Žá­®¢­ ï äã­ªæ¨ï
*/
macro printReport(numDprt, orgStructure, issueMode, numPlan, chapter, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort)

    var parameters = TParamNCorr(numDprt, orgStructure, issueMode, numPlan, chapter, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort);

    /* Š®­âà®«ì ®âªàëâëå ®¯¥à¤­¥© */
    if (not RepOperdaysOpened(parameters.getDepartmentList(), parameters.getDateIn(), parameters.getDateOut()).shouldContinue())
        exit(1);
    end;

    var nCorrData  = TNCorrData(parameters);
    var nCorrPrint = TNCorrPrint(parameters).execute(nCorrData.getDataSet(), nCorrData.getBadCurDataSet());

    return true;

end;

macro ibrCreateReport(dprtID, orgStructure, issueMode, numPlan, chapterIn, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort, nameFile)
    var output = TOFStream(nameFile); 
    var params = RepParameterCollection();

    params.add(RepParameter(DEPARTMENT_CODE,        REP_PT_INT,     dprtID));
    params.add(RepParameter(ORGANIZATION_STRUCTURE, REP_PT_INT,     orgStructure));
    params.add(RepParameter(ISSUE_MODE,             REP_PT_INT,     issueMode));
    params.add(RepParameter(BALANCE_PLAN_NUMBER,    REP_PT_INT,     numPlan));
    params.add(RepParameter(CHAPTER,                REP_PT_INT,     chapterIn));
    params.add(RepParameter(CODE_CURRENCY,          REP_PT_INT,     codeCurrency));
    params.add(RepParameter(BEGIN_DATE,             REP_PT_DATE,    dateIn));
    params.add(RepParameter(END_DATE,               REP_PT_DATE,    dateOut));
    params.add(RepParameter("withExcludedChapters", REP_PT_FLAG,    withExcludedChapters));
    params.add(RepParameter("operOrGroup",          REP_PT_INT,     operOrGroup));
    params.add(RepParameter("oper",                 REP_PT_INT,     oper));
    params.add(RepParameter("sort",                 REP_OT_GENERIC, sort));

    initializeSqlContext(params); 

    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    printReport(dprtID, orgStructure, issueMode, numPlan, chapterIn, withExcludedChapters, codeCurrency, dateIn, dateOut, operOrGroup, oper, sort);

    output.resetOutputFile();
    return true;  
end;