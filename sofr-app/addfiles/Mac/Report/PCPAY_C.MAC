/*------------- Планируемые и фактические оплаты процентов --------------*/
/*-------------        по валютным лицевым счетам          --------------*/
/*-------------      без учета квитованности оплат         --------------*/
/* 19.01.98 LCh Добавлено поле в панели диалога и фильтр по польз.типу счета */
/* 28.09.2001 - Sal. Подкачка справочника по пользовательским типам счетов   */

import BankInter, FIInter, rslscr;

file Валюта (fininstr);
file   ЛицевыеСчета   ( "account$.dbt" )  key 0;
file   СчетПроцентов  ( pcacc )  key 1;
file   Счета          ( pcacc )  key 3;
file   Оплата         ( pccalc ) key 6;
file   ТипыСчета      ( typeac )   KEY 0;
file   ПользТипыСчета ( typeac )   KEY 0 WRITE;

record Диалог         ( "pcpaya"   ) dialog;
record ЛистПоТипуСчета( "pcpay_lt" ) dialog;

var BreakVal = 1;
var КодВалюты;
var НачальнаяДата;
var СуммаПланируемыхОплат;
var СуммаФактическихОплат;
var ОбщаяСуммаПланируемыхОплат = $0;
var ОбщаяСуммаФактическихОплат = $0;
var ЛицевойСчет;
var ТипВладельцаСчетаПроц = 1002;
var ОтменитьОтчет;
var ПользТипСчета;

const РасчетСовершен = 2;
const ВсеОплаты = 0;
const ПланируемаяОплата = 1;
const ФактическаяОплата = 2;
const ДлинаСчета = 25;
const ZeroAc = "";


MACRO GetCurrency( FI_Code, FIID, Name ) /* Взять валюту по коду */
  if ( trim(FI_Code) == "" ) 
         SetParm ( 1 , -1 );
         SetParm ( 2 , "Все валюты");
  else
    keynum( Валюта, 1 );
    Валюта.FI_Code = FI_Code;
    if ( getEQ(Валюта) ) 
         SetParm ( 1 , Валюта.FIID );
         SetParm ( 2 , Валюта.Name );
    else 
      msgbox("В справочнике нет валюты с кодом "+FI_Code );
      return false;
    end;
  end;
  return true;
END;

MACRO GetCodCurrency(  FIID, FI_Code, Name ) /* Взять валюту по FIID */
  if (  FIID == -1 ) 
         SetParm ( 1 , "" );
         SetParm ( 2 , "Все валюты");
  else
    keynum( Валюта, 0 );
    Валюта.FIID  = FIID;
    if ( getEQ(Валюта) ) 
         SetParm ( 1 , Валюта.FI_Code);
         SetParm ( 2 , Валюта.Name );
    else 
      msgbox("В справочнике нет валюты с номером "+FIID );
      return false;
    end;
  end;
  return true;
END;

/* вызов "листалки" валют */
macro ВыбратьВалюту( FIID, FI_Code, Name )
  array СписокКодовВалют;
  array СписокДляМенюВалют;
  var number = 0, stat;

  СписокКодовВалют(number) = -1;
  СписокДляМенюВалют(number) = "    Все валюты";
  number = number + 1;

  keynum( Валюта, 2 );
  Валюта.FI_Kind = FIKIND_CURRENCY;
  Валюта.AvoirKind = 0;
  Валюта.FI_Code   = "";
  stat = getGE(Валюта);
  while( stat  and (Валюта.FI_Kind == FIKIND_CURRENCY) )
    if (  Валюта.FIID > 0 )
      СписокКодовВалют(number) = Валюта.FIID;
      СписокДляМенюВалют(number) = String(Валюта.FI_Code:3) +" "+Валюта.Name;
      number = number + 1;
    end;
    stat = next( Валюта )
  end;

  number = Menu( СписокДляМенюВалют, " Выберите валюту: ", " Валюта " );
  if ( number < 0 )
     return False;
  end;
  SetParm ( 0 , СписокКодовВалют(number) );
  SetParm ( 1 , substr(СписокДляМенюВалют(number), 1,3) );
  SetParm ( 2 , substr(СписокДляМенюВалют(number), 5) );
  return True;
end;

/* вызов "листалки" счетов */
macro ВыбратьСчет( Account, Currency )
      record AC( account );
      if (ListAccount (AC, 1, Currency, Account))
         SetParm ( 0 , AC.Account);
         SetParm ( 1 , AC.Code_Currency);
         return True;
      end;
     return False;
end;

macro ОбработчикЛиста( dlg, cmd, id, ikey )
  if ( cmd == DLG_KEY )
    if ( ikey == 13 )
     GetDirect(Scrff, Apos(FindRow(dlg,id)));
     return CM_SAVE;
    end;
  end;
  return ScrollMes( dlg, cmd, id, ikey );
end;
                       
/* вызов "листалки" типов счетов */
macro ВыбратьТипСчета( СсылкаНаДиалог )
   Var isFirst = true,
       NameFile = GetWorkFileName("type_ac");

   macro ПоПользТипам()
     if ( isFirst )
        isFirst = false;
        ClearRecord(ТипыСчета);
        ТипыСчета.iNumType = 5;
        return GetGE(ТипыСчета) and ( ТипыСчета.iNumType == 5 );
     else
        return next(ТипыСчета) and ( ТипыСчета.iNumType == 5 );
     end;
   end;

   if ( not Clone( ПользТипыСчета, NameFile) )
     MsgBox("Ошибка создания файла");
     exit(1);
   end;

   while ( ПоПользТипам() ) 
     if ( Index( СсылкаНаДиалог.ПользТипСчета, ТипыСчета.Type_Account) <= 0 ) 
       ПользТипыСчета.iNumType = ТипыСчета.iNumType;
       ПользТипыСчета.Type_Account = ТипыСчета.Type_Account;
       ПользТипыСчета.Name_Type = ТипыСчета.Name_Type;
       ПользТипыСчета.Contens = ТипыСчета.Contens;
       if ( not Insert(ПользТипыСчета) )
         MsgBox("Ошибка вставки записи в файл");
         exit(1);
       end;
     end;
   end;

   if ( Nrecords(ПользТипыСчета) == 0 )
     MsgBox("Нет записей");
     return false;
   end;

   SetScroll( ПользТипыСчета, "Тип", "Type_Account", 
                              "Имя", "Name_Type"  
            );
   if ( RunDialog( ЛистПоТипуСчета, "ОбработчикЛиста") )
     СсылкаНаДиалог.ПользТипСчета = СсылкаНаДиалог.ПользТипСчета + Scrff.Type_Account;
     Clone( ПользТипыСчета );
     Close(ПользТипыСчета);
     return true;
   end;
              
   return false;
end;

macro ОбработчикДиалога( СсылкаНаДиалог, Сообщение, ТекущееПоле, КодКлавиши )
var error=true;

   if( Сообщение==DLG_SAVE )

       if( trim(СсылкаНаДиалог.ЛицевойСчет) == ZeroAc) error=false;
       else
          Счета.ObjectType=ТипВладельцаСчетаПроц;
          Счета.Account=СсылкаНаДиалог.ЛицевойСчет;
          GetCurrency( СсылкаНаДиалог.КодВалюты, КодВалюты, СсылкаНаДиалог.Валюта ); 
          Счета.FIID   = КодВалюты;
          Счета.CloseDate=date(0,0,0);
          if( getGE( Счета ) )
             if( (Счета.ObjectType==ТипВладельцаСчетаПроц) and
                       (Счета.Account==СсылкаНаДиалог.ЛицевойСчет) and
                       ( (КодВалюты == -1) or (Счета.FIID==КодВалюты) ) )
                error=false;
             end;
          end;
       end;
       if( error )
          msgbox( "Нет счетов процентов по указанному|лицевому счету" );
          return CM_CANCEL;
       end;
    end;
    if(( Сообщение == DLG_KEY ) and ( КодКлавиши == 317) )
        if (fldname (СсылкаНаДиалог, ТекущееПоле) == "КодВалюты" )
          if ( ВыбратьВалюту( КодВалюты, СсылкаНаДиалог.КодВалюты, СсылкаНаДиалог.Валюта ) )
            UpdateFields( СсылкаНаДиалог );
          end;
        elif (fldname (СсылкаНаДиалог, ТекущееПоле) == "ЛицевойСчет" )
          if ( ВыбратьСчет( СсылкаНаДиалог.ЛицевойСчет, КодВалюты ) )
            GetCodCurrency( КодВалюты, СсылкаНаДиалог.КодВалюты, СсылкаНаДиалог.Валюта ); 
            UpdateFields( СсылкаНаДиалог );
          end;
        elif (fldname (СсылкаНаДиалог, ТекущееПоле)== "ПользТипСчета"  )
          while ( ВыбратьТипСчета( СсылкаНаДиалог ) )
          end;
          UpdateFields( СсылкаНаДиалог );
        end;
     end;
     if( (Сообщение == DLG_REMFOCUS) and 
       (fldname (СсылкаНаДиалог, ТекущееПоле) == "КодВалюты" ) )
      if (not GetCurrency( СсылкаНаДиалог.КодВалюты, КодВалюты, СсылкаНаДиалог.Валюта ) ) 
         return CM_CANCEL;
      else
         UpdateFields( СсылкаНаДиалог );
      end;
     end;
end;

macro ВвестиИсходныеДанные()
   Диалог.ТипВладельцаСчетаПроц = "Лицевой счет";
   Диалог.ЛицевойСчет = ZeroAc; /* +MkStr(" ",ДлинаСчета-1); */
   Диалог.ПользТипСчета = "";

   Диалог.КодВалюты = "";
   GetCurrency( Диалог.КодВалюты, КодВалюты, Диалог.Валюта ); 

   ОтменитьОтчет = not RunDialog( Диалог, "ОбработчикДиалога" );
   if( ОтменитьОтчет )   exit( 1 ); end;

   ЛицевойСчет   = Диалог.ЛицевойСчет;
   GetCurrency( Диалог.КодВалюты, КодВалюты, Диалог.Валюта ); 
   НачальнаяДата = Диалог.НачальнаяДата;
   ПользТипСчета = trim( Диалог.ПользТипСчета );
end;


macro НапечататьЗаголовок()
[   Общий список планируемых и фактических оплат процентов по лицевым счетам      ];
   if ( ПользТипСчета > "" )
[                  с пользовательским типом  ###############                      ]( ПользТипСчета:l);
   end;
[                                                                                 ];
[─────────────────────────┬──────────┬──────────┬────────────────────┬──────────────────────];
[       Счет              │   Дата   │  Номер   │        Сумма       │        Сумма         ];
[                         │  оплаты  │  оплаты  │ планируемой оплаты │ фактической оплаты   ];
[─────────────────────────┴──────────┴──────────┴────────────────────┴──────────────────────];
end;


macro НапечататьПланируемуюОплату()
[ #                        #                   #                    #                     # ]
( СчетПроцентов.Account:l, Оплата.PayDate:l, Оплата.AutoKey:r,Оплата.Sum:r, $0:r );
end;


macro НапечататьФактическуюОплату()
[ #                        #                   #                    #                     # ]
( СчетПроцентов.Account:l, Оплата.PayDate:l, Оплата.AutoKey:r, $0:r, Оплата.Sum:r );
end;


macro НапечататьОплаты()
   /* Устанавливаем ключи для выбора оплат */
   Оплата.AutoPerc = СчетПроцентов.AutoKey;
   Оплата.CalcFlag = РасчетСовершен;
   Оплата.PayDate  = date(0,0,0);

   СуммаПланируемыхОплат = $0;
   СуммаФактическихОплат = $0;
   BreakVal = 1;

   if( not getGE( Оплата ) )  return; end;
   while( BreakVal )
      /* проверяем на попадание в диапазон */
      if( (Оплата.AutoPerc != СчетПроцентов.AutoKey) or
          (Оплата.CalcFlag != РасчетСовершен) )
             BreakVal = 0;
      else
         if( Оплата.PayDate >= НачальнаяДата )
            if( Оплата.PayType == ПланируемаяОплата )
               НапечататьПланируемуюОплату();
               СуммаПланируемыхОплат = СуммаПланируемыхОплат + Оплата.sum;
            else
               НапечататьФактическуюОплату();
               СуммаФактическихОплат = СуммаФактическихОплат + Оплата.sum;
            end;
         end;
      end;

      if( not next(  Оплата ) )   BreakVal = 0; end;
   end;
end;


macro НапечататьИтог()
[───────────────────────────────────────────────────────────────────────────────────────────];
[ Общая сумма оплат:                                         #                     # ]
( СуммаПланируемыхОплат:r, СуммаФактическихОплат:r );
[───────────────────────────────────────────────────────────────────────────────────────────];
[ ];
end;


macro НапечататьШапкуСчета()
   [ ];
   [  Оплаты по счету процентов номер #               лицевого счета # ]
      (СчетПроцентов.AutoKey:l, СчетПроцентов.Account:l);
   [ ];
end;

macro FiltrType(Account, Currency) /* Фильтр для л/счетов по пользовательскому типу */
   var i = strlen(ПользТипСчета);
   if  ( ( trim(ЛицевойСчет) != ZeroAc) or (i==0) )
        return TRUE;
   end;
   ЛицевыеСчета.Chapter = 1;
   ЛицевыеСчета.Account = Account;
   ЛицевыеСчета.Code_Currency = Currency;
   if( not geteq( ЛицевыеСчета ) ) return FALSE;
   end;
   while( i > 0 )
      if ( index(ЛицевыеСчета.UserTypeAccount,substr(ПользТипСчета,i,1) )> 0 ) 
        return TRUE;
      end;   
      i = i - 1;
   end;
   return FALSE;
end;



ВвестиИсходныеДанные();
if( ОтменитьОтчет == true )   return; end;

НапечататьЗаголовок();

/* Устанавливаем ключи для выбора счета процентов */
СчетПроцентов.ObjectType = ТипВладельцаСчетаПроц;
СчетПроцентов.CloseDate = date(0,0,0);
СчетПроцентов.Account = ЛицевойСчет;
СчетПроцентов.FIID = КодВалюты;

BreakVal = 1;
if( not getGE( СчетПроцентов ) )   return; end;
while( BreakVal )
   /* проверяем на попадание в диапазон */
   if( (СчетПроцентов.ObjectType == ТипВладельцаСчетаПроц) and
       (СчетПроцентов.CloseDate == date(0,0,0)) and
       ( (СчетПроцентов.Account == ЛицевойСчет) or
         (trim(ЛицевойСчет) == ZeroAc) ) )
      if( ( (КодВалюты == -1) or (СчетПроцентов.FIID == КодВалюты) )
            and FiltrType(СчетПроцентов.Account, СчетПроцентов.FIID) )
         НапечататьШапкуСчета();
         НапечататьОплаты();
         НапечататьИтог();
         ОбщаяСуммаПланируемыхОплат = ОбщаяСуммаПланируемыхОплат + СуммаПланируемыхОплат;
         ОбщаяСуммаФактическихОплат = ОбщаяСуммаФактическихОплат + СуммаФактическихОплат;
      end;
      BreakVal = 1;
   else
      BreakVal = 0;
   end;

   if( not next( СчетПроцентов ) )  BreakVal = 0; end;
end;



/* Общий итог по всем договорам */
if( КодВалюты != 0 )
[ ];
[ ];
[────────────────────────────────────────────────────────────────────────────────────];
[ Общая сумма оплат по всем счетам:                          #                     # ]
( ОбщаяСуммаПланируемыхОплат:r, ОбщаяСуммаФактическихОплат:r );
[────────────────────────────────────────────────────────────────────────────────────];
end;
