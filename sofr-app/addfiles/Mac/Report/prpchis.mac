/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                         R-Style Software Lab

  File Name   : prpchis.mac                                 October 15,2001
  Programmer  : LCh
  Description : Ведомость начисления процентов.
  Comment     :
  Modifications:

└───────────────────────────────────────────────────────────────────────────*/
import globals, CTInter;

const           /*  Параметры сортировки записей и накопления сумм */
  ПоФилиалу  = 0,
  ПоОперу    = 1,
  ПоВалюте   = 2,
  ПоГруппе   = 3,
  ПоОбъекту  = 4,
  ЗаПериод   = 5,
  ПоОперации = 6,
  SizeArr    = 7;

const FLAG_DOC   = "X",  /* Признак наличия операц.документа для фактич.оплаты */
      FLAG_NODOC = "";

var  Сумма = TArray,  /* Суммы по параметрам сортировки(накопления) */
     Итог  = TArray;  /* Флаг использования параметра сортировки(накопления) */

var  ВидОбъекта,
     ФлагГруппыОперов = FALSE,
     needTable  = TRUE,
     firstItog  = -1,   /* Последний заголовок-первые итоги*/
     firstLine  = TRUE;

record pcaddhis( pcaddhis );
FILE party(party );              /* Справочник субъектов*/

/*------------------- Вспомогательные функции ---------------------*/

macro   Добавить_В_Итоги( Проценты )
  var i = 0;
  while( i < SizeArr )
    if ( Итог(i) )      Сумма(i) = Сумма(i) + Проценты;
    end;
    i = i + 1;
  end;
end;

macro    Печать_Шапки
  if ( not needTable ) return; end;
[
 ┌─────────────────────────┬──────────┬─────────────────────┬────────────────┬──────────┐
 │#########################│   Дата   │  Период начисления  │   Начисленная  │Начисление│
 │                         │начисления├──────────┬──────────┤      сумма     │ выполнено│
 │                         │          │  Начало  │   Конец  │                │          │
 ├─────────────────────────┼──────────┼──────────┼──────────┼────────────────┼──────────┤
]( ВидОбъекта:c);
  firstLine  = FALSE;
  needTable  = FALSE;
end;

macro    Печать_Итогов( Name, i )
   if ( not Итог(i) )  return; end;

   if ( needTable )  /* Шапка таблицы завершена итогами */
[  ИТОГО ############################                        ################

]( Name, Сумма(i) );
   else               /* Шапка таблицы напечатана, итогов пока нет */

    if ( firstItog  == i)
[├─────────────────────────┼──────────┼──────────┼──────────┼────────────────┼──────────┤
 │ Итого ##################│          │          │          │################│          │
]( Name, Сумма(i) );
      firstLine = TRUE;
    else
[├─────────────────────────┼──────────┼──────────┼──────────┼────────────────┼──────────┤
 │ Итого ##################│          │          │          │################│          │
 └─────────────────────────┴──────────┴──────────┴──────────┴────────────────┴──────────┘

]( Name, Сумма(i) );
      needTable = TRUE;
    end;
  end;
end;

macro    Печать_Заголовка( Name )
[
  ##########################################################################
]( Name:w );
end;

macro  Обнулить_Данные( i )
  firstItog  = i;
  Итог(i) = TRUE;
  Сумма(i) = $0;
end;


/*------------------- Функции, вызываемые из программы ---------------------*/

macro    Заголовок_Отчета( _ДатаНачала, _ДатаКонца, _ВидОбъекта )
  var i = 0,
      Superior, pt, sep;
  while( i < SizeArr )
    Итог(i) = FALSE;
    i = i + 1;
  end;

  ВидОбъекта = _ВидОбъекта;

  party.PartyID = {OurBank};
  getEQ( party );
  Superior = readNoteForObject( OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 21, _ДатаКонца );

  if(( Superior == "") or ( ValType( Superior ) == V_UNDEF ))
  [
    ##########################################################################################
  ]( {Name_Bank}:w );
  else
    [
    ];
    sep = index( Superior, "|");
    while(sep)
      [                                       #
      ]  (substr( Superior, 1, sep-1 ):c);
      Superior = substr( Superior, sep + 1 );
      sep = index( Superior, "|");
    end;
    [                                       #

    ]  ( Superior:c );
  end;

[
                          ВЕДОМОСТЬ НАЧИСЛЕНИЙ ПРОЦЕНТОВ
                    за  период  с  ##########  по  ##########
]( _ДатаНачала, _ДатаКонца );

  if (_ВидОбъекта != "" )
       ВидОбъекта = _ВидОбъекта;
       Печать_Заголовка( "Вид объекта " + ВидОбъекта);
  else ВидОбъекта = "Объект";
  end;

end;


macro    Итоги_Отчета
   if ( not needTable )  /* Шапка таблицы не завершена */
[└─────────────────────────┴──────────┴──────────┴──────────┴────────────────┴──────────┘

];
   end;
end;

/*---------------------*/
macro    Заголовок_Клиента( Код,  Название )
  Печать_Заголовка( "Клиент  " + Код + " " + Название);
end;

/*---------------------*/
var firstType = TRUE;

macro    Заголовок_Типа( Тип,  Название )
  if ( Тип == "" ) return; end;
  if ( Название == "" )  Название = "не найден";
  end;
  if ( firstType )
   firstType = FALSE;
[
  Польз.тип счетов процентов # #
]( Тип, Название );
  else
[                            # #
]( Тип, Название );
  end;
end;

/*---------------------*/
macro    Заголовок_Филиала( Номер, Код, Название )
  if ( Номер == 0 ) return; end;
  Обнулить_Данные( ПоФилиалу );
  Печать_Заголовка( "Филиал  " + Код + " " + Название);
end;

macro    Итог_Филиала
  Печать_Итогов( "по филиалу", ПоФилиалу );
end;

/*---------------------*/
macro    Заголовок_Группы( Вид, Группа, Название )
  if ( Вид == "" ) return; end;
  Обнулить_Данные( ПоГруппе );
  if ( Группа == "" )  Группа = "не указана";
  end;
  Печать_Заголовка( "Группа  " + Вид + " " + Группа + " " + Название);
end;

macro    Итог_Группы
  Печать_Итогов( "по группе", ПоГруппе );
end;

/*---------------------*/
macro    Заголовок_Опера( Номер, Название, ФлагГруппы )
  ФлагГруппыОперов = ФлагГруппы;
  Обнулить_Данные( ПоОперу );
  if ( ФлагГруппыОперов )
       Печать_Заголовка( "Группа операционистов  " + Название);
  else Печать_Заголовка( "Операционист  " + Название);
  end;
end;

macro    Итог_Опера
  if ( ФлагГруппыОперов )
       Печать_Итогов( "по группе операционистов", ПоОперу );
  else Печать_Итогов( "по операционисту", ПоОперу );
  end;
end;
/*---------------------*/
macro    Заголовок_Валюты( Номер, Код, Название )
  if ( Код == "" ) return; end;
  Обнулить_Данные( ПоВалюте );
  Печать_Заголовка( "Валюта  " + Код + " " + Название);
end;

macro    Итог_Валюты
  Печать_Итогов( "по валюте", ПоВалюте );
end;

/*---------------------*/
macro    Заголовок_Операции( Номер, Название )
  if ( Номер == 0 ) return; end;
  Обнулить_Данные( ПоОперации );
  Печать_Заголовка( "Операция  " + Название);
end;

macro    Итог_Операции
  Печать_Итогов( "по операции", ПоОперации );
end;

/*---------------------*/
macro    Заголовок_Объекта( НомерВида, _ВидОбъекта, НомерОбъекта, КодКлиента, НазваниеКлиента )
/*  Печать_Заголовка( ВидОбъекта+" " + Номер );*/
  Обнулить_Данные( ПоОбъекту );
end;

macro    Итог_Объекта
  Печать_Итогов( "по объекту", ПоОбъекту );
end;

/*---------------------*/
macro    Заголовок_Периода( Дата )
/*  Печать_Заголовка( string(Дата) );*/
  Обнулить_Данные( ЗаПериод );
end;

macro    Итог_Периода
  Печать_Итогов( "за дату", ЗаПериод );
end;

/*---------------------*/
macro    Печать_Строки( НомерОбъекта, aa )
  var DocFlag;

  Печать_Шапки();

  setbuff( pcaddhis, aa );

  if( pcaddhis.oprID != 0 )  /* Есть начисление */
           DocFlag = FLAG_DOC;
  else     DocFlag = FLAG_NODOC;
  end;
  if ( firstLine )
       firstLine  = FALSE;
[├─────────────────────────┼──────────┼──────────┼──────────┼────────────────┼──────────┤];
   end;
[│#########################│##########│##########│##########│################│     #    │
]( НомерОбъекта, pcaddhis.AddDate, pcaddhis.BeginDate, pcaddhis.EndDate,
   pcaddhis.AddSum, DocFlag);

  Добавить_В_Итоги( pcaddhis.AddSum );

end;

/*-END--*/
