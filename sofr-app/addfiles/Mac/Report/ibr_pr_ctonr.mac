/*
$Name:          ibr_pr_ctonr.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Состояние (соответствие) валютных счетов"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import pr_ctonr;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const LOG_FILE_NAME = "ibr_pr_ctonr_web";

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade    = TIbrConstructorFacade();
    var report    = facade.createReport("ibr_pr_ctonr", "Состояние (соответствие) валютных счетов", "ibr_pr_ctonr.mac", "ibrReportExecute", "ibrReportVerification");  

    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта"));

    groupReport.add(facade.createFieldDate("ДатаОтчета", "Дата отчета: "));
    
    groupReport.add(facade.createFieldNameAlg("ФормаОтчета", "Форма отчета", 1, 3959));
    
    groupReport.add(facade.createFieldSort("Сортировка", "Сортировка", "", 1634));
    tabReport.add(groupReport);

    report.add(tabReport);
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    var isShowOcp = false;
    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );
    
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"Подразделения").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),
                                          TIbrCommon().getIdentifierAccountChapter( getValue(report, gRep+"ГлаваСчетов").valueString),
                                          getFiId(getValue(report, gRep+"Валюта").valueString),
                                          getValue(report, gRep+"ДатаОтчета").valueDate,
                                          getValue(report, gRep+"ФормаОтчета").valueInteger,
                                          strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1),
                                          ternary(isShowOcp, 1, 0));
    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(-1, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString) == -1)
        repErrorsQueue().add(0, "Недопустимая глава счетов");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;