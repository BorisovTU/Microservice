/*
$Name:          pr_ctonr.mac
$Module:        Отчётность внутреняя
$Description:   Ведомость соответствия валютных счетов
*/
/*───────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Отчётность внутреняя"


File Name   : pr_ctonr.mac
CREATED: June 29,1999
Programmer  : VBKesha
MODIFICATIONS:
  29.07.2011 - SCR 166491. Реализовать новый отчет
                           "Ведомость состояния (соответствия) валютных счетов"
  19.04.2012 - SCR 179660. Ведомость соответствия валютных счетов. Не выводится атрибут.
  20.04.2012 - SCR 173197. Ведомость валютных счетов. Ошибки в печатной форме
  26.04.2012 - SCR 173375. Ведомость валютных счетов. Неверно выводится шапка отчета
└──────────────────────────────────────────────────────────────────────────────────────*/


import RsbDataSet;
import rep_lib;
import Reporting;
import ofstream;

var stream : TOfstream;
var report : CTableReport;

var reportBodyFileName;

macro initColumn(iOutForm);

    report.addColumn("Номер лицевого счета", 25, 0);
    report.addColumn("Код|валюты", 3, 0);
    report.addColumn("Остаток в валюте счета", 22);
    report.addColumn("Остаток в нац. валюте", 21);
    report.addColumn("Остаток по курсу ЦБ", 19);
    report.addColumn("Сумма|несоответствия", 16);

    if (iOutForm == 1)
        report.addColumn("Тип|счета|\"Н\"", 1, 0);
    end;

end;

macro printHeader(iOutForm, dateOut, numDprt, orgStructure)

   printBankHeader(numDprt, orgStructure, report.getSumLen());
   println();
   var typeRep;

   if (iOutForm == 1)
        typeRep = " состояния ";
   else
        typeRep = " соответствия ";
   end;

   [##########################################################################################################################]
   ("Ведомость" + typeRep + "валютных счетов" : c);

   [##########################################################################################################################



   ]
   ("за " + dateOut : c);

end;


macro printH4(query)

    var data, msg;

    data = TRsbDataSet(query) ;

    if (data.moveNext())

        msg = "Курс ЦБ для валюты ";


        while (data.eof == false)

            msg = msg + data.t_code;

            if (data.moveNext())
                msg = msg + ", ";
            end;

        end;

        msg = msg + " не задан, счета в отчет не включены";

    println(msg);
    println();
    println();

    end;

end;


macro printBottom ()

    [

##########################################################################################################################]
   ("Операционист       " + {name_oper} : l);

end;

macro printChapterName(chapter)
    var data, chapterName;

    data = TRsbDataSet("SELECT t_name FROM dobChaptr_dbt Cha where Cha.t_chapter=" + chapter);
    data.moveNext();
    chapterName = data.t_name;
    println("Глава " + chapter + ". " + chapterName);

end;


macro printChatpters(chapter, iOutForm, query)

Var data, chapterName, curChapter;

    data = TRsbDataSet(query);
    curChapter = -1;

    data.moveNext();

    if (data.eof != false)
        if (chapter > 0)
          printChapterName(chapter);
        end;

          [##########################################################################################################################]
          ("Данные отсутствуют" : c );

    else

        while (data.eof == false)
            if (curChapter != data.t_chapter)
                if (curChapter > 0 )
                    report.printBottom();
                    println();
                end;

            curChapter = data.t_chapter;

            printChapterName(curChapter);
            report.PrintHead();
            end;

            if (iOutForm == 1)
                report.printString(data.t_account, data.t_code, string(data.t_outrest : 22 : 2 : r), string(data.t_outcoverrest : 21 : 2 : r), string(data.t_outconvertioncbrest : 19 : 2 : r), string(data.RMCB : 16 : 2 : r), data.x);
            else
                report.printString(data.t_account, data.t_code, string(data.t_outrest : 22 : 2 : r), string(data.t_outcoverrest : 21 : 2 : r), string(data.t_outconvertioncbrest : 19 : 2 : r), string(data.RMCB : 16 : 2 : r));
            end;

            data.moveNext();
        end;

        if (curChapter > 0)
            report.printBottom();
        end;
    end;
end;


macro calc(
   numDprt,
   orgStructure,
   issueMode,
   numPlan,
   chapter,
   codeCurrency,
   dateOut,
   iOutForm,
   pRslPatternSort,
   ocp
)


    var selectRep, selectH4;
    var filterOcp, filteriOutForm, filterCodeCurrency, filterChapter;
    var sorting;
    var i;
    var t;

    if (ocp == 0)
        filterOcp = "     AND acc.t_type not like '%Щ%'                                          " + "\n" ;
    else
        filterOcp = "";
    end;

    if (iOutForm == 2)
        filteriOutForm =
                "      AND (acc.t_type not like '%Н%')                                           " + "\n" ;
    else
        filteriOutForm = ""
    end;


    if (codeCurrency > 0)
        filterCodeCurrency =
                "      AND acc.T_FIID = " + codeCurrency                                          + "\n" ;
    else
        filterCodeCurrency = ""
    end;

    if (chapter > 0)
        filterChapter =
                "      AND acc.t_chapter = " + chapter                                            + "\n" ;
    else
        filterChapter = ""
    end;

    sorting   = "ORDER BY t_chapter asc";

    if (pRslPatternSort.size > 0)

        for (i, 0, pRslPatternSort.size, 1)


            if (pRslPatternSort[i] == REP_PATTERNSORT_DEPARTMENT)
                sorting = sorting + ", dpp.t_shortName asc, acc.t_branchId asc";
            end;

            if (pRslPatternSort[i] == REP_PATTERNSORT_ACCOUNT)
                sorting = sorting + ", acc.t_account asc";
            end;

            if (pRslPatternSort[i] == REP_PATTERNSORT_CURRENCY)
                sorting = sorting + ", fin.t_code asc, acc.t_fiId asc";
            end;

        end;

    else

        sorting = sorting + ", acc.t_account asc";

    end;


     selectRep = "SELECT ACC.T_CHAPTER,                                                         " + "\n" +
                  "ACC.T_ACCOUNT,                                                               " + "\n" +
                  "FIN.T_CODE,                                                                  " + "\n" +
                  "ACC.T_OUTREST,                                                               " + "\n" +
                  "ACC.T_OUTCOVERREST,                                                          " + "\n" +
                  "ACC.T_OUTCONVERTIONCBREST,                                                   " + "\n" +
                  "(ACC.T_OUTCOVERREST - acc.T_OUTCONVERTIONCBREST) AS rmcb,                    " + "\n" +
                  "(CASE WHEN acc.t_type LIKE '%Н%' THEN 'Х' ELSE ' ' END) AS X                 " + "\n" +
                  "FROM drepAccount_vw acc, drepFinInstrument_vw Fin, drepDpDep_vw dpp          " + "\n" +
                  "WHERE     (fin.t_id = acc.t_fiid)                                            " + "\n" +
                  "      AND (acc.t_branchId=dpp.t_code)                                        " + "\n" +
                  "      AND (acc.t_isOpenedAtEndDate=1)                                        " + "\n" +
                  "      AND (acc.t_fiId <> 0)                                                  " + "\n" +
                  "      AND (acc.t_outConvertionCBRest is not null)                            " + "\n" +
                  "      AND (acc.t_isSecurities<>1)                                            " + "\n" +
                  "      AND ((ACC.T_OUTCOVERREST - acc.T_OUTCONVERTIONCBREST) <> 0)            " + "\n" +
                  filterOcp                                                                              +
                  filteriOutForm                                                                         +
                  filterCodeCurrency                                                                     +
                  filterChapter                                                                          +
                  sorting;


     selectH4 = "SELECT DISTINCT(FIN.T_CODE)                                                    " + "\n" +
                 "FROM drepAccount_vw acc, drepFinInstrument_vw Fin, drepDpDep_vw dpp           " + "\n" +
                 "WHERE     (FIN.T_ID = ACC.T_FIID)                                             " + "\n" +
                 "      AND (acc.t_branchId=dpp.t_code)                                         " + "\n" +
                 "      AND (acc.t_isOpenedAtEndDate=1)                                         " + "\n" +
                 "      AND (acc.t_fiId <> 0)                                                   " + "\n" +
                 "      AND (acc.t_isSecurities<>1)                                             " + "\n" +
                 "      AND (acc.t_outConvertionCBRest is null)                                 " + "\n" +
                 filterOcp                                                                               +
                 filteriOutForm                                                                          +
                 filterCodeCurrency                                                                      +
                 filterChapter;

    report = CTableReport();

    stream = TOfstream("ctonr");

    stream.setOutputFile();

    initColumn(iOutForm);
    printHeader(iOutForm, dateOut, numDprt, orgStructure);
    printH4(selectH4);
    printChatpters(chapter, iOutForm, selectRep);
    printBottom();

    stream.resetOutputFile();

    return true;
end;


macro show()

    stream.show();

    return true;
end;

macro ibrCreateReport(numDprt, orgStructure, issueMode, numPlan, chapter, codeCurrency, dateOut, iOutForm, pRslPatternSort, ocp)

    if (calc(numDprt, orgStructure, issueMode, numPlan, chapter, codeCurrency, dateOut, iOutForm, pRslPatternSort, ocp))
        reportBodyFileName = stream.getFileName();
        return true;
    end;

    return false;
end;