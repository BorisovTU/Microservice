/**
 @file 		mac\Report\pr_accstatement.mac
 @brief 	Выписки и справки по лицевым счетам

 # menu
 - отчетность внутренняя \ Аналитическая \ Счета \ выписка по лицевым счетам

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |19.08.2025 |Велигжанин А.В.|DEF-98300                                       |Поправлены шаблоны (см. Templs\Reporting\Report)
 |           |               |                                                |Поле "дата проводки", задан формат "Текстовый"
 |15.08.2025 |Велигжанин А.В.|DEF-97737                                       |В пустом отчете Excel не показываем
 |12.08.2025 |Велигжанин А.В.|DEF-97737                                       |Удален код isPostgres, так как пока в проде не применяется
 |24.03.2008 |Ivkina         |SCR 119536                                      |Обработка проводок комиссий
 |04.10.2007 |Malakhova      |SCR 110424                                      |Историзация кодов субъектов
*/
import Reporting, ReportInter, Календарь, FIInter, PTInter, CTInter,
       PaymInter, OprInter, CurrInter, BankInter;
import cb_sql, globals, lib_str, lib_lang, lib_arr;
import treport;
import acv;
import RsbObjFactory;
import ofstream;
import rep_lib;

import or_rep_h;
import CompositeTableField;

import repcache;

import param;

import testLib;

import asImport;

import "dlwreps.mac";

private class TTimeCounter(testCaption, event)
    private const MAX_STORAGE_SIZE = 100000;

    private var m_fromTime;
    private var m_tillTime;

    private var m_testCaption;
    private var m_event;

    private var m_storageTestCaptionArray;
    private var m_storageEventArray;
    private var m_storageFromTimeArray;
    private var m_storageTillTimeArray;
    private var m_storageIndex;


    macro start()
        m_fromTime = time();
    end;

    macro stop()
        m_tillTime = time();

        m_storageFromTimeArray[m_storageIndex] = m_fromTime;
        m_storageTillTimeArray[m_storageIndex] = m_tillTime;

        m_storageIndex = m_storageIndex + 1;
    end;

    macro constructor(testCaption, event)

        m_testCaption = testCaption;
        m_event = event;

        m_storageTestCaptionArray = TArray(MAX_STORAGE_SIZE);
        m_storageEventArray       = TArray(MAX_STORAGE_SIZE);
        m_storageFromTimeArray    = TArray(MAX_STORAGE_SIZE);
        m_storageTillTimeArray    = TArray(MAX_STORAGE_SIZE);

        var i = 0;
        while (i < MAX_STORAGE_SIZE)
            m_storageTestCaptionArray[i] = m_testCaption;
            m_storageEventArray[i]       = m_event;

            i = i + 1;
        end;

        m_storageIndex = 0;

    end;

    macro destructor()
        if (m_storageIndex > 0)

            var i = 0;
            while (i < m_storageIndex)
                m_storageFromTimeArray[i] = int(m_storageFromTimeArray[i]);
                m_storageTillTimeArray[i] = int(m_storageTillTimeArray[i]);

                i = i + 1;
            end;

            m_storageTestCaptionArray.size = m_storageIndex;
            m_storageEventArray.size       = m_storageIndex;
            m_storageFromTimeArray.size    = m_storageIndex;
            m_storageTillTimeArray.size    = m_storageIndex;

            var query = "INSERT INTO accst_timelog"
               + "\n" + "("
               + "\n" + "    test_caption,"
               + "\n" + "    event,"
               + "\n" + "    fromTime,"
               + "\n" + "    tillTime"
               + "\n" + ")"
               + "\n" + "VALUES"
               + "\n" + "("
               + "\n" + "    ?,"
               + "\n" + "    ?,"
               + "\n" + "    ?,"
               + "\n" + "    ?"
               + "\n" + ")"
            ;

            var inserter = RsbSQLInsert(query, 4, m_storageIndex);

            inserter.addParam(V_STRING,  m_storageTestCaptionArray, strlen(m_testCaption) + 1);
            inserter.addParam(V_STRING,  m_storageEventArray, strlen(m_event) + 1);
            inserter.addParam(V_INTEGER, m_storageFromTimeArray);
            inserter.addParam(V_INTEGER, m_storageTillTimeArray);

            inserter.insert();

            sql_execute("COMMIT");

        end;
    end;

    constructor(testCaption, event);
end;

private var m_useOptimizedPaymentFinding = true;

//var globalTestCaption2 = "(02.03.2009-02.03.2009) parts of processFDocs";
//
//var timeCounter9  = TTimeCounter(globalTestCaption2, "FindPayment ");
//var timeCounter12  = TTimeCounter(globalTestCaption2, "FindPaymentOpt ");
//var timeCounter13;
//if (m_useOptimizedPaymentFinding)
//    timeCounter13  = TTimeCounter(globalTestCaption2, "getPaymentDataOpt ");
//else
//    timeCounter13  = TTimeCounter(globalTestCaption2, "getPaymentData ");
//end;

private const V_SPECVAL = 26; // sql NULL

private var wSPOD = 4;

private CONST wDATE   = 10,
              wSHIFR  = 2,
              wNUMBD  = 10,
              wBIC    = 9,
              wACC    = 25,
              wSUMM   = 16,
              wGROUND = 50,
              wSTRNUM = 8;

private const TYPE_STORNO = "Сторно";
private const TYPE_SPOD = "СПОД";
private const TYPE_NOTSPOD = "";

private CONST DBLPRN_DELIMITER_WIDTH = 4; /* кол-во пробелов между колонками при двойной печати */

private CONST END_LINE     = "\n";
private CONST SET_CHAR     = "X";
private CONST NOTFOUND     = "не найден";
private CONST EMPTY_STRING = "";

private CONST TERRITORIAL = 1,
              REGIONAL    = 2;

private CONST BRANCH     = 1, /* Подразделение */
              DEPARTMENT = 2, /* Филиал */
              BANK       = 3; /* Банк */

private CONST RATETYPE_CB = 7;

private CONST ALLCURRENCY = -2,
              ALLFININSTR = -1,
              NATCUR      =  0;

private CONST ZeroDate = Date(0, 0, 0);

/* Возвращаемые значения */
private CONST WELLDONE         = 0,
              ERROR            = 1,
              NOACC            = 2,
              NODOC            = 3,
              OPERDAYNOTPASSED   = 4,
              EXISTSOPENOPERDAYS = 5;

/* Формы выпуска выписок (печать приложений) */
private CONST ACC_FORMS_DEBIT   = 1, // дебетовыми
              ACC_FORMS_CREDIT  = 2, // кредитовыми
              ACC_FORMS_ALL     = 3, // всеми
              ACC_FORMS_NOTHING = 4; // без приложений

/* Форматы выписки */
private CONST ACC_FORMATS_CLIENT = 1, // клиентский
              ACC_FORMATS_BANK   = 2; // внутрибанковский

/* Не менять !!!*/
private CONST DEBIT_C  = ACC_FORMS_DEBIT,
              CREDIT_C = ACC_FORMS_CREDIT;

/* Индексы для массива с данными по печати первичек */
private CONST DS_DOC   = 0,
              DS_DK    = 1,
              DS_PAYM  = 2,
              DS_CARRY = 3,
              DS_CUR   = 4,
              DS_CBDOC = 5,
              DS_LAST  = 6; /* всегда должен быть последним */

/* формы печати первичек */
private CONST PRN_SKIP    = 0, /* не печатать */
              PRN_RURPAYM = 1, /* платеж      */
              PRN_CURPAYM = 2, /* платеж      */
              PRN_MDOC    = 3, /* мультивалютный документ */
              PRN_RURMO   = 4, /* мемордер    */
              PRN_CURMO   = 5; /* мемордер    */

private var tempSortTableName = "drepaccst_tmp";
private var cbAccountViewName = "";
private var cbCacheAccountViewName = "";

var reportBodyFileName;
private var wordReportWorkFileName;

private var NAcc = 0;
private var NDoc = 0;

// Виды первичных документов, которые обрабатывает макрос печати РБ, см. prpmpaym.mac
private var DocKinds = ArrCreate("01", "02", "05", "06", "08", "16", "17");

private var hasInvalidLoansAccount = false;
var invalidLoansAccountProtocol;

private var views = TArray();
private var excelPrintControllers = RcbArray();

/* bool to int */
private MACRO B2I(val)
  if (val)
    return 1;
  else
    return 0;
  end;
END;

/* bool to stringalized int */
private MACRO b2si(val)
  if (val)
    return "1";
  else
    return "0";
  end;
END;

/**
 * Панель активности. Обертка для BegAction/EndAction
 *
 * @since   6.20.030.37
 * @author  ABP
 * @version 1.0
 */
private class TActionPanel()

    macro beginAct(lag, caption)
        if (GetDialogFlag() == 1)
            begAction(lag, caption);
        end;
    end;

    macro endAct()
        if (GetDialogFlag() == 1)
            endAction();
        end;
    end;

end;

/**
 * Класс-утилита для работы с данными платежа при обработке первичных документов проводок выписки.
 * Так же обрабатывает мультивалютные документы, породившие платеж
 * Реализует шаблон "Singleton".
 *
 * @since   v.6.20.030.55
 * @author  ABP
 * @version 2.0
 */
private class TPayment(key, payment, debitProperties, creditProperties, rProperties, multydoc)
    var m_key : Integer = key;

    var m_payment = payment;
    var m_debitProperties = debitProperties;
    var m_creditProperties = creditProperties;
    var m_rProperties = rProperties;
    var m_multydoc = multydoc;

end;

private var m_paymentsUtilityObject = null;
private class TPaymentsUtilityImpl(parameters : Object)

    private const CONTAINER_INITIAL_SIZE = 100;

    private var m_parameters : Object;

    private var m_paymentAlias : String;
    private var m_paymentDebitPropertiesAlias : String;
    private var m_paymentCreditPropertiesAlias : String;
    private var m_paymentRPropertiesAlias : String;
    private var m_multydocAlias : String;

    private var m_paymentFieldAliasPrefix : String;
    private var m_paymentDebitPropertiesFieldAliasPrefix : String;
    private var m_paymentCreditPropertiesFieldAliasPrefix : String;
    private var m_paymentRPropertiesFieldAliasPrefix : String;
    private var m_multydocFieldAliasPrefix : String;

    private var m_paymentFldInfo : TArray;
    private var m_paymentDebitPropertiesFldInfo : TArray;
    private var m_paymentCreditPropertiesFldInfo : TArray;
    private var m_paymentRPropertiesFldInfo : TArray;
    private var m_multydocFldInfo : TArray;

    private var m_container : TRepCache;

    private var m_query : String;

    macro createValue(key)
        var value = TPayment(key, TRecHandler("pmpaym.dbt"),
                                  TRecHandler("pmprop.dbt"),
                                  TRecHandler("pmprop.dbt"),
                                  TRecHandler("pmrmprop.dbt"),
                                  TRecHandler("multydoc.dbt")
                            );
        findPayment(value.m_key, 0, 0, 0, 0, true,
                    value.m_payment,
                    value.m_debitProperties,
                    value.m_creditProperties,
                    value.m_rProperties
                   );

        var fileMultydoc = TBFile("multydoc.dbt", "R", 0);
        fileMultydoc.rec.autokey = value.m_payment.rec.documentId;
        if (fileMultydoc.getEq())
            copy(value.m_multydoc, fileMultydoc);
        else
            fileMultydoc.rec.autokey = 0;
        end;

        return value;
    end;

    macro createKey(value)
        return value.key;
    end;

    private macro createContainer(initialSize)
        if (m_container == null)
            if (initialSize == null)
                initialSize = CONTAINER_INITIAL_SIZE;
            end;
            m_container = TRepCache(CACHE_MODE_ORDERED, initialSize, r2m(this, "createValue"), r2m(this, "createKey"));
        end;
    end;

    private macro fillRecord(recHandler : TRecHandler, dataset : Object, fldInfo : TArray)
        var i;
        var size = fldInfo.size;

        i = 0;
        while (i < size)
            var value = execExp("dataset." + fldInfo.value(i));
            if (value != null)
                recHandler.(i/5) = value;
            end;

            i = i + 5;
        end;
    end;

    macro getPaymentRecord(paymentId : Integer) : TRecHandler
        createContainer();
        return m_container.get(paymentId).m_payment;
    end;

    macro getPaymentDebitPropertiesRecord(paymentId : Integer) : TRecHandler
        createContainer();
        return m_container.get(paymentId).m_debitProperties;
    end;

    macro getPaymentCreditPropertiesRecord(paymentId : Integer) : TRecHandler
        createContainer();
        return m_container.get(paymentId).m_creditProperties;
    end;

    macro getPaymentRPropertiesRecord(paymentId : Integer) : TRecHandler
        createContainer();
        return m_container.get(paymentId).m_rProperties;
    end;

    macro getMultydocRecord(paymentId : Integer) : TRecHandler
        createContainer();
        return m_container.get(paymentId).m_multydoc;
    end;

    macro createData(datasourceName : String)

        if ((m_parameters.outForm == ACC_FORMS_NOTHING) or (not m_useOptimizedPaymentFinding))
            return;
        end;

//timeCounter12.start();
        var action = TActionPanel();
        action.beginAct(100, "Получение данных о платежах");

        m_query = "SELECT COUNT(" + datasourceName + ".t_paymentId) t_paymentsAmount"
         + "\n" + "  FROM " + datasourceName
         + "\n" + " WHERE " + datasourceName + ".t_paymentId IS NOT NULL"
         + "\n" + "   AND " + datasourceName + ".t_paymentId != 0"
        ;

        var dataset = TRsbDataset(m_query);
        dataset.next();
        createContainer(dataset.paymentsAmount);

        m_query = "SELECT " + getSqlFldList("pmpaym.dbt", null, m_paymentAlias, m_paymentFieldAliasPrefix)
         + "\n" + "       " + ", " + getSqlFldList("pmprop.dbt", null, m_paymentDebitPropertiesAlias, m_paymentDebitPropertiesFieldAliasPrefix)
         + "\n" + "       " + ", " + getSqlFldList("pmprop.dbt", null, m_paymentCreditPropertiesAlias, m_paymentCreditPropertiesFieldAliasPrefix)
         + "\n" + "       " + ", " + getSqlFldList("pmrmprop.dbt", null, m_paymentRPropertiesAlias, m_paymentRPropertiesFieldAliasPrefix)
         + "\n" + "       " + ", " + getSqlFldList("multydoc.dbt", null, m_multydocAlias, m_multydocFieldAliasPrefix)
         + "\n" + "  FROM " + datasourceName
         + "\n" + "       " + ", dpmpaym_dbt " + m_paymentAlias
         + "\n" + "       " + ", dpmprop_dbt " + m_paymentDebitPropertiesAlias
         + "\n" + "       " + ", dpmprop_dbt " + m_paymentCreditPropertiesAlias
         + "\n" + "       " + ", dpmrmprop_dbt " + m_paymentRPropertiesAlias
         + "\n" + "       " + ", dmultydoc_dbt " + m_multydocAlias
         + "\n" + " WHERE " + datasourceName + ".t_paymentId = " + m_paymentAlias + ".t_paymentId"
         + "\n" + "   AND " + datasourceName + ".t_paymentId = " + m_paymentDebitPropertiesAlias + ".t_paymentId"
         + "\n" + "   AND " + datasourceName + ".t_paymentId = " + m_paymentCreditPropertiesAlias + ".t_paymentId"
         + "\n" + "   AND " + datasourceName + ".t_paymentId = " + m_paymentRPropertiesAlias + ".t_paymentId"
         + "\n" + "   AND " + m_paymentDebitPropertiesAlias + ".t_debetCredit" + " = 0"
         + "\n" + "   AND " + m_paymentCreditPropertiesAlias + ".t_debetCredit" + " = 1"
         + "\n" + "   AND " + m_paymentAlias + ".t_documentId = " + m_multydocAlias + ".t_autokey(+)"
         + "\n" + " ORDER BY " + datasourceName + ".t_paymentId"
        ;

        dataset = TRsbDataset(m_query);

        var paymentId;
        var value;
        var hasData = dataset.next();
        while (hasData)
            paymentId = execExp("dataset." + m_paymentFieldAliasPrefix + "paymentId");
            value = TPayment(paymentId, TRecHandler("pmpaym.dbt"),
                                        TRecHandler("pmprop.dbt"),
                                        TRecHandler("pmprop.dbt"),
                                        TRecHandler("pmrmprop.dbt"),
                                        TRecHandler("multydoc.dbt")
                            );

            fillRecord(value.m_payment, dataset, m_paymentFldInfo);
            fillRecord(value.m_debitProperties, dataset, m_paymentDebitPropertiesFldInfo);
            fillRecord(value.m_creditProperties, dataset, m_paymentCreditPropertiesFldInfo);
            fillRecord(value.m_rProperties, dataset, m_paymentRPropertiesFldInfo);

            if ((value.m_payment.rec.docKind == CB_MULTYDOC) and (value.m_multydoc.rec.autokey != null))
                fillRecord(value.m_multydoc, dataset, m_multydocFldInfo);
            else
                value.m_multydoc.rec.autokey = 0;
            end;

            m_container.add(value.m_key, value, null, true);
            hasData = dataset.next();
        end;

        action.endAct();
//timeCounter12.stop();
    end;

    private macro constructor(parameters : Object)
        m_parameters = parameters;

        if ((m_parameters.outForm == ACC_FORMS_NOTHING) or (not m_useOptimizedPaymentFinding))
            return;
        end;

        m_paymentAlias = "payment";
        m_paymentDebitPropertiesAlias = "debitProperties";
        m_paymentCreditPropertiesAlias = "creditProperties";
        m_paymentRPropertiesAlias = "rProperties";
        m_multydocAlias = "multydoc";

        m_paymentFieldAliasPrefix = "t_pm_";
        m_paymentDebitPropertiesFieldAliasPrefix = "t_dProp_";
        m_paymentCreditPropertiesFieldAliasPrefix = "t_cProp_";
        m_paymentRPropertiesFieldAliasPrefix = "t_rProp_";
        m_multydocFieldAliasPrefix = "t_mcd_";

        var i;

        m_paymentFldInfo = TRecHandler("pmpaym.dbt").getFldInfo();
        i = 0;
        while (i < m_paymentFldInfo.size)
            m_paymentFldInfo.value(i) = m_paymentFieldAliasPrefix + m_paymentFldInfo.value(i);
            i = i + 5;
        end;

        m_paymentDebitPropertiesFldInfo = TRecHandler("pmprop.dbt").getFldInfo();
        m_paymentCreditPropertiesFldInfo = TRecHandler("pmprop.dbt").getFldInfo();
        i = 0;
        while (i < m_paymentDebitPropertiesFldInfo.size)
            m_paymentDebitPropertiesFldInfo.value(i) = m_paymentDebitPropertiesFieldAliasPrefix + m_paymentDebitPropertiesFldInfo.value(i);
            m_paymentCreditPropertiesFldInfo.value(i) = m_paymentCreditPropertiesFieldAliasPrefix + m_paymentCreditPropertiesFldInfo.value(i);
            i = i + 5;
        end;

        m_paymentRPropertiesFldInfo = TRecHandler("pmrmprop.dbt").getFldInfo();
        i = 0;
        while (i < m_paymentRPropertiesFldInfo.size)
            m_paymentRPropertiesFldInfo.value(i) = m_paymentRPropertiesFieldAliasPrefix + m_paymentRPropertiesFldInfo.value(i);
            i = i + 5;
        end;

        m_multydocFldInfo = TRecHandler("multydoc.dbt").getFldInfo();
        i = 0;
        while (i < m_multydocFldInfo.size)
            m_multydocFldInfo.value(i) = m_multydocFieldAliasPrefix + m_multydocFldInfo.value(i);
            i = i + 5;
        end;
    end;

    constructor(parameters);

end;

private macro TPaymentsUtility(parameters : Object) : TPaymentsUtilityImpl
    if (m_paymentsUtilityObject == null)
        m_paymentsUtilityObject = TPaymentsUtilityImpl(parameters);
    end;
    return m_paymentsUtilityObject;
end;

/**
 * Элемент кеша ИД идентификаторов субъектов-банков
 * @since v.6.20.029.154
 * @author ABP
 * @version 1.0
 */
private class TPartyIdCachedDataElement(_code : String, _key : Integer, _partyId : Integer)
    var partyId : Integer = _partyId;

    var key : Integer = _key;
    var code : String = _code;
end;

/**
 * Кэширующий ИД идентификаторов субъектов-банков.
 * Кеширует идентификатор субъекта.
 *
 * @since v.6.20.029.154
 * @author ABP
 * @version 1.0
 */
private var m_partyIdCachedDataObject = null;
private class TPartyIdCachedDataImpl()

    private var m_cache : TArray;
    private var m_queryText : String;
    private var m_fillCommand : Object;

    private macro add(code : String, key : Integer, codeKind : Integer, requiredDate : Date, isEmptyAtKey : Bool) : Object

        var value : Object;
        var partyId : Integer;

        m_fillCommand.value("code") = code;
        m_fillCommand.value("codeKind") = codeKind;
        m_fillCommand.value("date") = requiredDate;

        m_fillCommand.execute();

        if (valType(m_fillCommand.value("partyId")) == V_SPECVAL)
            partyId = {ourBank};
        else
            partyId = m_fillCommand.value("partyId");
        end;

        value = TPartyIdCachedDataElement(code, key, partyId);

        if (isEmptyAtKey)
            m_cache(key) = TArray();
            m_cache(key).(0) = value;
        else
            m_cache(key).(m_cache(key).size) = value;
        end;

        return value;

    end;

    /**
     * Получить элемент кеша
     * @param partyId Идентификатор субъекта
     * @return объект класса TPartyCachedDataElement или null
     */
    macro get(code : String, codeKind : Integer, requiredDate : Date) : Object
        var i;
        var size;
        var key : Integer = int(string(codeKind, int(requiredDate-Date(31,12,1990))));
        var result = m_cache(key);

        if (result == null)
            return add(code, key, codeKind, requiredDate, true);
        else
            i = 0;
            size = result.size;
            while ((i < size) and (result(i).code != code))
                i = i + 1;
            end;
            if (i < size)
                return result(i);
            else
                return add(code, key, codeKind, requiredDate, false);
            end;
        end;
    end;

    /**
     * Получить id субъекта по коду заданного вида, действующего на заданную дату
     * @param code Код субъекта
     * @param codeKind Вид кода субъекта
     * @param requiredDate Дата действия кода субъекта
     * @return id субъекта
     */
    macro getPartyId(code : String, codeKind : Integer, requiredDate : Date) : Integer
        return get(code, codeKind, requiredDate).partyId;
    end;

    private macro constructor()
        m_cache = TArray();

        m_fillCommand = RsdCommand(rslDefCon);
        m_fillCommand.nullConversion = true;

        var queryText = "BEGIN ? := rsb_rep_pt.get_partyIdByCode(?,?,?); END;";

        m_fillCommand.cmdText = queryText;
        m_fillCommand.addParam("partyId", RSDBP_RETVAL, V_INTEGER);
        m_fillCommand.addParam("codeKind", RSDBP_IN, 0);
        m_fillCommand.addParam("code", RSDBP_IN, "");
        m_fillCommand.addParam("date", RSDBP_IN, Date());
    end;

    constructor();

end;

private macro TPartyIdCachedData() : TPartyIdCachedDataImpl
    if (m_partyIdCachedDataObject == null)
        m_partyIdCachedDataObject =  TPartyIdCachedDataImpl();
    end;
    return m_partyIdCachedDataObject;
end;

/**
 * Элемент кеша ИД субъектов-банков
 *
 * @since v.6.20.029.154
 * @author ABP
 * @version 1.0
 */
private class TPartyCachedDataElement(_partyId : Integer, _isResident : Bool)
    var partyId : Integer = _partyId;
    var isResident : Bool = _isResident;
end;

/**
 * Кэширующий ИД субъектов-банков.
 * Кеширует признак резидентности.
 *
 * @since v.6.20.029.154
 * @author ABP
 * @version 2.0
 */
private var m_partyCachedDataObject = null;
private class TPartyCachedDataImpl()
    private var m_queryText : String;
    private var m_fillCommand : Object;

    private var m_cache : TRepCache;

    private macro createCache()
        if (m_cache == null)
            m_cache = TRepCache(CACHE_MODE_DISORDERED, 100, r2m(this, "createValue"), r2m(this, "createKey"));
        end;
    end;

    macro createValue(key)
        var value : Object = null;

        m_fillCommand.value("partyId") = key;
        m_fillCommand.execute();

        var dataSet = TRsbDataset(m_fillCommand);

        if (dataSet.next())
            value = TPartyCachedDataElement(key, dataSet.notResident != SET_CHAR);
        end;

        return value;
    end;

    macro createKey(value)
        return value.partyId;
    end;

    /**
     * Получить элемент кеша
     * @param partyId Идентификатор субъекта
     * @return объект класса TPartyCachedDataElement или null
     */
    macro get(partyId : Integer) : Object
        return m_cache.get(partyId);
    end;

    /**
     * Получить признак резидента для субъекта
     * @param partyId Идентификатор субъекта
     * @return Признак резидента для субъекта
     */
    macro getResidentFlag(partyId : Integer) : Bool
        return get(partyId).isResident;
    end;

    private macro constructor()
        createCache();

        m_fillCommand = RsdCommand(rslDefCon);
        m_fillCommand.nullConversion = true;

        var queryText = "SELECT party.t_notResident t_notResident"
               + "\n" + "  FROM dparty_dbt party"
               + "\n" + " WHERE party.t_partyId = ?";

        m_fillCommand.cmdText = queryText;
        m_fillCommand.addParam("partyId", RSDBP_IN, 0);
    end;

    constructor();
end;

private macro TPartyCachedData() : TPartyCachedDataImpl
    if (m_partyCachedDataObject == null)
        m_partyCachedDataObject = TPartyCachedDataImpl();
    end;
    return m_partyCachedDataObject;
end;

/**
 * Формирование текстов SQL-запросов для механизма DocInfo по:
 * - рублевым или валютным проводкам по основному счету,
 * - рублевым проводкам переоценки,
 * - проводкам курсовой разницы МВП в корреспонденции со счетом покрытия или клиентским счетом МВП
 * - проводкам курсовой разницы МВП в корреспонденции со счетом курсовых разниц
 *
 * @since 6.00.020.27
 * @author ABP
 * @version 1.0
 */
private class TCacheQueryMaker(parameters : Object)

    private var m_parameters : Object;

    private var m_dateIn : String;

    private var m_dateOut : String;

    private var m_mainQuery : String;

    private var m_fieldList : String;

    private macro createFieldsList()
        m_fieldList = m_parameters.docInfo.getFieldListAsString("document");
    end;

    private macro getFieldList()
        return m_fieldList;
    end;

    private macro getAccountDataSourceClause(subsystem)
        return ternary(subsystem == REP_SUBSYSTEMS_CB, cbCacheAccountViewName, tempSortTableName);
    end;

    private macro getSubsystemFilterClause(subsystem)
        return ternary(subsystem == REP_SUBSYSTEMS_CB, "(1 = 1)", "account.t_subsystem = " + REP_SUBSYSTEMS_CB + " AND account.t_acc = account.t_odbAccount");
    end;

    private macro makeMainQuery()

        macro getQueryTextCb(correspondingAccountField)

            var query;

            query =          "SELECT " + getFieldList()
                    + "\n" + "  FROM daccTrn_dbt document,"
                    + "\n" + "       " + getAccountDataSourceClause(REP_SUBSYSTEMS_CB) + " account"
                    + "\n" + " WHERE document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                    + "\n" + "   AND document.t_state = 1"
                    + "\n" + "   AND " + getSubsystemFilterClause(REP_SUBSYSTEMS_CB)
                    + "\n" + "   AND account.t_accId = " + correspondingAccountField
                    ;

            return query;

        end;

        macro getQueryTextLoans(correspondingAccountField, correspondingFiIdField)

            var query;

            query =          "SELECT " + getFieldList()
                    + "\n" + "  FROM daccTrn_dbt document,"
                    + "\n" + "       " + getAccountDataSourceClause(REP_SUBSYSTEMS_LOANS) + " account"
                    + "\n" + " WHERE document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                    + "\n" + "   AND document.t_state = 1"
                    + "\n" + "   AND " + getSubsystemFilterClause(REP_SUBSYSTEMS_LOANS)
                    + "\n" + "   AND account.t_acc = " + correspondingAccountField
                    + "\n" + "   AND account.t_chapter = document.t_chapter"
                    + "\n" + "   AND account.t_currency = " + correspondingFiIdField
                    ;

            return query;

        end;

        var query;

        if (m_parameters.subsystem == REP_SUBSYSTEMS_ALL)
            query =  getQueryTextLoans("document.t_account_payer", "document.t_fiId_payer")
            + "\n" + "UNION ALL"
            + "\n" + getQueryTextLoans("document.t_account_receiver", "document.t_fiId_receiver")
            + "\n" + "UNION ALL"
            + "\n" + getQueryTextCb("document.t_accountId_payer")
            + "\n" + "UNION ALL"
            + "\n" + getQueryTextCb("document.t_accountId_receiver")
            ;
        elif (m_parameters.subsystem == REP_SUBSYSTEMS_LOANS)
            query =  getQueryTextLoans("document.t_account_payer", "document.t_fiId_payer")
            + "\n" + "UNION ALL"
            + "\n" + getQueryTextLoans("document.t_account_receiver", "document.t_fiId_receiver")
            ;
        else
            query =  getQueryTextCb("document.t_accountId_payer")
            + "\n" + "UNION ALL"
            + "\n" + getQueryTextCb("document.t_accountId_receiver")
            ;
        end;

        return "(" + query + ")";

    end;

    private macro make()
        m_mainQuery = makeMainQuery();
    end;

    /**
     * Получить текст запроса для основных проводок
     *
     * @returns текст запроса для основных проводок
     */
    macro getMainQuery()
        return m_mainQuery;
    end;

    /**
     * Конструктор. При вызове формирует тексты запросов
     *
     * @param     parameters           параметры отчета
     */
    private macro constructor(parameters : Object)

        m_parameters = parameters;

        m_dateIn = getSqlDate(m_parameters.dateIn);

        m_dateOut = getSqlDate(m_parameters.dateOut);

        m_mainQuery = "";

        createFieldsList();

        make();

    end;

    constructor(parameters);

end;

/**
 * Формирование текстов SQL-запросов для печати проводок
 *
 * @since 6.00.020.27
 * @author ABP
 * @version 1.0
 */
private class TDocumentQueryMaker(parameters : Object)

    private const CARRY_TYPE_MAIN            = 0;
    private const CARRY_TYPE_RATE_DIFFERENCE = 1;
    private const CARRY_TYPE_REVALUATION     = 2;

    private var m_parameters : Object;

    private var m_dateIn : String;

    private var m_dateOut : String;

    private var m_mainAccount : Integer;

    private var m_needEquivalent : Bool;

    private var m_query : Object;

    private macro addParameter(name, value)
        m_query.addParam(name, RSDBP_IN, value);
        return "?";
    end;

    private macro getEquivalentClause()
        if (m_needEquivalent)
            return "document.t_sum_NatCur ";
        else
            return "0";
        end;
    end;

    private macro getCommissionAccountProcessing(fieldSelectDoc : String, fieldSelectPayment : String) : String

        var text;

        if (m_parameters.docOnlyData == true)

            text = " " + fieldSelectDoc + " ";

        else

           if (m_parameters.commissionIncomeAccounts == "")
               text = " " + fieldSelectPayment + " ";
           else
               text =   " CASE"
               + "\n" + "     WHEN (" + m_parameters.getPayerComissionAccounts() + ")"
               + "\n" + "       OR (" + m_parameters.getReceiverComissionAccounts() + ")"
               + "\n" + "         THEN " + fieldSelectDoc
               + "\n" + "     ELSE " + fieldSelectPayment
               + "\n" + "  END ";
           end;

        end;

        return text;

    end;

    private macro getCorrespondingAccountClause(isDebit)
        if (isDebit)
            return getCommissionAccountProcessing("document.t_account_receiver", "documentInfo.t_receiverAccount");
        else
            return getCommissionAccountProcessing("document.t_account_payer", "documentInfo.t_payerAccount");
        end;
    end;

    private macro getStornoNoteClause()
        if (m_parameters.showStorno)
            return   "       CASE"
            + "\n" + "           WHEN INSTR(document.t_typeDocument, 'С') != 0"
            + "\n" + "            AND document.t_fiId_payer = document.t_fiId_receiver" // одновалютная исправленная проводка
            + "\n" + "           THEN NVL((SELECT " + getSqlString(TYPE_STORNO)
            + "\n" + "                  || '|' || TO_CHAR(paym.t_valueDate, 'DD.MM.YYYY')"
            + "\n" + "                  || '|' || SUBSTR(prop.t_number, 1, 12)"
            + "\n" + "                       FROM dcb_doc_dbt doc,"
            + "\n" + "                            daccispr_dbt ispr,"
            + "\n" + "                            dpmpaym_dbt paym,"
            + "\n" + "                            dpmrmprop_dbt prop "
            + "\n" + "                      WHERE ispr.t_accTrnId = document.t_accTrnId"
            + "\n" + "                        AND ispr.t_docKind = " + DLDOC_MEMORIALORDER
            + "\n" + "                        AND ispr.t_documentId = doc.t_documentId"
            + "\n" + "                        AND ispr.t_docKind = paym.t_docKind"
            + "\n" + "                        AND ispr.t_documentId = paym.t_documentId"
            + "\n" + "                        AND prop.t_paymentId = paym.t_paymentId"
            + "\n" + "                        AND INSTR(doc.t_typeDocument, 'S') != 0"
            + "\n" + "                    ),"
            + "\n" + "                " + getSqlString("")
            + "\n" + "                   )"
            + "\n" + "           WHEN INSTR(document.t_typeDocument, 'С') != 0"
            + "\n" + "            AND document.t_fiId_payer != document.t_fiId_receiver" // мультивалютная исправленная проводка
            + "\n" + "           THEN NVL((SELECT " + getSqlString(TYPE_STORNO)
            + "\n" + "                  || '|' || TO_CHAR(paym.t_valueDate, 'DD.MM.YYYY')"
            + "\n" + "                  || '|' || SUBSTR(prop.t_number, 1, 12)"
            + "\n" + "                       FROM dmultydoc_dbt doc,"
            + "\n" + "                            daccispr_dbt ispr,"
            + "\n" + "                            dpmpaym_dbt paym,"
            + "\n" + "                            dpmrmprop_dbt prop "
            + "\n" + "                      WHERE ispr.t_accTrnId = document.t_accTrnId"
            + "\n" + "                        AND ispr.t_docKind = " + CB_MULTYDOC
            + "\n" + "                        AND ispr.t_documentId = doc.t_autokey"
            + "\n" + "                        AND ispr.t_docKind = paym.t_docKind"
            + "\n" + "                        AND ispr.t_documentId = paym.t_documentId"
            + "\n" + "                        AND prop.t_paymentId = paym.t_paymentId"
            + "\n" + "                        AND INSTR(doc.t_type_document, 'S') != 0"
            + "\n" + "                    ),"
            + "\n" + "                " + getSqlString("")
            + "\n" + "                   )"
            + "\n" + "           ELSE " + getsqlString("")
            + "\n" + "       END"
        else
            return "     " + getSqlString("");
        end;
    end;

    private macro makeMainQuery()

        macro getSelectClause(isDebit)

            var query;

            query =          "SELECT NVL(documentInfo.t_paymentId, 0)                                      paymentId,"
                    + "\n" + "       NVL(documentInfo.t_carryId, 0)                                        carryId,"
                    + "\n" + "   " + getCorrespondingAccountClause(isDebit) + "                            account,"
                    + "\n" + "   " + ternary(isDebit, DEBIT_C, CREDIT_C) + "                               dk,"
                    + "\n" + "   " + ternary(isDebit, "document.t_sum_payer", "0") + "                     sumDebit,"
                    + "\n" + "   " + ternary(isDebit, "0", "document.t_sum_receiver") + "                  sumCredit,"
                    + "\n" + "   " + ternary(isDebit, "NVL(" + getEquivalentClause() + ", 0)", "0") + "    sumDebitEq,"
                    + "\n" + "   " + ternary(isDebit, "0", "NVL(" + getEquivalentClause() + ", 0)") + "    sumCreditEq,"
                    + "\n" + "       documentInfo.t_payerBankCode                                          payerBankCode,"
                    + "\n" + "       documentInfo.t_payerCodeKind                                          payerCodeKind,"
                    + "\n" + "       documentInfo.t_receiverBankCode                                       receiverBankCode,"
                    + "\n" + "       documentInfo.t_receiverCodeKind                                       receiverCodeKind,"

                    + "\n" + "       document.t_shifr_oper                                                 shifr_oper,"
                    + "\n" + "       document.t_typeDocument                                               typeDocument,"
                    + "\n" + "       INSTR(document.t_typeDocument, 'З')                                   isSpod,"

                    + "\n" + "   " + getStornoNoteClause() + "                                             stornoNote,"

                    + "\n" + "       CASE"
                    + "\n" + "           WHEN documentInfo.t_paymentId IS NULL"
                    + "\n" + "           THEN 1"
                    + "\n" + "           WHEN (SELECT COUNT(pmprop.t_paymentId)"
                    + "\n" + "                   FROM dpmprop_dbt pmprop,"
                    + "\n" + "                        dpmpaym_dbt payment"
                    + "\n" + "                  WHERE pmprop.t_paymentId = documentInfo.t_paymentId"
                    + "\n" + "                    AND pmprop.t_corschem = -1"
                    + "\n" + "                    AND payment.t_paymentId = pmprop.t_paymentId"
                    + "\n" + "                    AND payment.t_startDepartment = payment.t_endDepartment"
                    + "\n" + "                ) = 2"
                    + "\n" + "           THEN 1"
                    + "\n" + "           ELSE 0"
                    + "\n" + "       END                                                                   isInternalDocument,"

                    + "\n" + "       documentInfo." + ternary(isDebit, "t_payerFiId", "t_receiverFiId") + " code_currency,"
                    + "\n" + "       documentInfo.t_autokey                                                autokey,"
                    + "\n" +         getCommissionAccountProcessing("document.t_ground",
                                                                    "documentInfo.t_ground") + "           ground,"
                    + "\n" +         getCommissionAccountProcessing("document.t_numb_document",
                                                                    "documentInfo.t_numb_document") + "    numb_document,"
                    + "\n" + "       documentInfo.t_date_carry                                             date_carry,"
                    + "\n" + "       documentInfo.t_receiverAccount                                        receiverAccount,"
                    + "\n" + "       documentInfo.t_payerAccount                                           payerAccount,"
                    + "\n" + "       documentinfo.t_payerCorrAcc                                           payerCorrAcc,"
                    + "\n" + "       documentinfo.t_receiverCorrAcc                                        receiverCorrAcc,"

                    + "\n" + "   " + m_parameters.getDocumentInfoTableFieldList() + ","
                    + "\n" + "   " + m_parameters.getDocumentTableFieldList() + ","
                    + "\n" + "   " + CARRY_TYPE_MAIN + "                                                   carryType,"

                    + "\n" + "   " + ternary(isDebit, "document.t_sum_payer", "0") + "                     sumDebit_sort,"
                    + "\n" + "   " + ternary(isDebit, "0", "document.t_sum_receiver") + "                  sumCredit_sort,"
                    + "\n" + "       document.t_date_carry                                                 dateCarry_sort,"
                    + "\n" + "       CASE"
                    + "\n" + "           WHEN document.t_exRateAccTrnId != 0"
                    + "\n" + "           THEN document.t_exRateAccTrnId"
                    + "\n" + "           ELSE document.t_accTrnId"
                    + "\n" + "       END                                                                   documentKey,"
                    + "\n" + "       document.t_accTrnId                                                   accTrnId"
                    ;

            return query;

        end;

        // Фильтр на проводки КР и переоценки
        var specialDocumentFilter = "";

        // В выписке по валютному счету исключаем КР и переоценку. Они будут отобраны отдельно
        if (m_parameters.account.currency != NATCUR)
            specialDocumentFilter = "\n" + "    AND document.t_result_carry NOT IN (" + DELTARATE
                                                                                + "," + REGRESTOUTCARRY
                                                                                + "," + DELTARATE_ADD
                                                                                + "," + DELTARATE_MCD
                                                                                + ")";
        end;

        var query;

        query =          getSelectClause(true)
                + "\n" + "  FROM daccTrn_dbt document,"
                + "\n" + "       " + m_parameters.getCacheTableName() + " documentInfo"
                + "\n" + " WHERE document.t_accTrnId = documentInfo.t_autokey"
                + "\n" + "   AND document.t_accountId_payer = " + addParameter("mainAccount_debit", m_mainAccount)
                + "\n" + "   AND document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                +        specialDocumentFilter
                + "\n" + " UNION ALL"
                + "\n" + getSelectClause(false)
                + "\n" + "  FROM daccTrn_dbt document,"
                + "\n" + "       " + m_parameters.getCacheTableName() + " documentInfo"
                + "\n" + " WHERE document.t_accTrnId = documentInfo.t_autokey"
                + "\n" + "   AND document.t_accountId_receiver = " + addParameter("mainAccount_credit", m_mainAccount)
                + "\n" + "   AND document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                +        specialDocumentFilter
                ;

        return "(" + query + ")";

    end;

    private macro makeSpecialDocumentQuery()

        macro getSelectClause(isDebit)

            var query;

            query =          "SELECT 0                                                                  paymentId,"
                    + "\n" + "       0                                                                  carryId,"
                    + "\n" + "   " + getCorrespondingAccountClause(isDebit) + "                         account,"
                    + "\n" + "   " + ternary(isDebit, DEBIT_C, CREDIT_C) + "                            dk,"
                    + "\n" + "       0                                                                  sumDebit,"
                    + "\n" + "       0                                                                  sumCredit,"
                    + "\n" + "   " + ternary(isDebit, "document.t_sum_NatCur", "0") + "                 sumDebitEq,"
                    + "\n" + "   " + ternary(isDebit, "0", "document.t_sum_NatCur") + "                 sumCreditEq,"
                    + "\n" + "   " + getSqlString({MFO_Bank}) + "                                       payerBankCode,"
                    + "\n" + "   " + PTCK_BIC + "                                                       payerCodeKind,"
                    + "\n" + "   " + getSqlString({MFO_Bank}) + "                                       receiverBankCode,"
                    + "\n" + "   " + PTCK_BIC + "                                                       receiverCodeKind,"

                    + "\n" + "       document.t_shifr_oper                                              shifr_oper,"
                    + "\n" + "       document.t_typeDocument                                            typeDocument,"
                    + "\n" + "       INSTR(document.t_typeDocument, 'З')                                isSpod,"

                    + "\n" + "   " + getStornoNoteClause() + "                                          stornoNote,"

                    + "\n" + "       1                                                                  isInternalDocument,"
                    + "\n" + "   " + ternary(isDebit, "t_payerFiId", "t_receiverFiId") + "              code_currency,"
                    + "\n" + "       documentInfo.t_autokey                                             autokey,"
                    + "\n" + "       document.t_ground                                                  ground,"
                    + "\n" + "       document.t_numb_document                                           numb_document,"
                    + "\n" + "       document.t_date_carry                                              date_carry,"
                    + "\n" + "       document.t_account_receiver                                        receiverAccount,"
                    + "\n" + "       document.t_account_payer                                           payerAccount,"
                    + "\n" + "   " + getSqlString({CORAC_Bank}) + "                                     payerCorrAcc,"
                    + "\n" + "   " + getSqlString({CORAC_Bank}) + "                                     receiverCorrAcc,"

                    + "\n" + "   " + m_parameters.getDocumentInfoTableFieldList() + ","
                    + "\n" + "   " + m_parameters.getDocumentTableFieldList() + ","

                    + "\n" + "       CASE"
                    + "\n" + "           WHEN document.t_result_carry = " + DELTARATE_MCD
                    + "\n" + "           THEN " + CARRY_TYPE_RATE_DIFFERENCE
                    + "\n" + "           ELSE " + CARRY_TYPE_REVALUATION
                    + "\n" + "       END                                                                carryType,"

                    + "\n" + "       CASE"
                    + "\n" + "           WHEN document.t_result_carry = " + DELTARATE_MCD
                    + "\n" + "           THEN 0"
                    + "\n" + "           ELSE NULL"
                    + "\n" + "       END                                                                sumDebit_sort,"
                    + "\n" + "       CASE"
                    + "\n" + "           WHEN document.t_result_carry = " + DELTARATE_MCD
                    + "\n" + "           THEN 0"
                    + "\n" + "           ELSE NULL"
                    + "\n" + "       END                                                                sumCredit_sort,"

                    + "\n" + "       document.t_date_carry                                              dateCarry_sort,"
                    + "\n" + "       document.t_accTrnId                                                documentKey,"
                    + "\n" + "       document.t_accTrnId                                                accTrnId"
                    ;

            return query;

        end;

        var query = "";

        query =          getSelectClause(true)
                + "\n" + "  FROM daccTrn_dbt document,"
                + "\n" + "       " + m_parameters.getCacheTableName() + " documentInfo"
                + "\n" + " WHERE document.t_accTrnId = documentInfo.t_autokey"
                + "\n" + "   AND document.t_fiId_receiver = " + NATCUR
                + "\n" + "   AND document.t_accountId_payer = " + addParameter("special_connectAccount_debit", m_mainAccount)
                + "\n" + "   AND document.t_result_carry IN (" +       DELTARATE
                                                               + "," + REGRESTOUTCARRY
                                                               + "," + DELTARATE_ADD
                                                               + "," + DELTARATE_MCD
                                                         + ")"
                + "\n" + "   AND document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                + "\n" + " UNION ALL"
                + "\n" + getSelectClause(false)
                + "\n" + "  FROM daccTrn_dbt document,"
                + "\n" + "       " + m_parameters.getCacheTableName() + " documentInfo"
                + "\n" + " WHERE document.t_accTrnId = documentInfo.t_autokey"
                + "\n" + "   AND document.t_fiId_payer = " + NATCUR
                + "\n" + "   AND document.t_accountId_receiver = " + addParameter("special_connectAccount_credit", m_mainAccount)
                + "\n" + "   AND document.t_result_carry IN (" +       DELTARATE
                                                               + "," + REGRESTOUTCARRY
                                                               + "," + DELTARATE_ADD
                                                               + "," + DELTARATE_MCD
                                                         + ")"
                + "\n" + "   AND document.t_date_carry BETWEEN " + m_dateIn + " AND " + m_dateOut
                ;

        return "(" + query + ")";

    end;

    private macro make()
        var query = "";
        var needSpecialDocument = false;

        if (m_parameters.account.currency != NATCUR)  // проводки переоценки и курсовых разниц для счетов в нацвалюте были уже отобраны раньше
            if (m_needEquivalent and (not m_parameters.cSetExcludeRevalue))
                needSpecialDocument = true;
            end;
        end;

        if (not needSpecialDocument)

            query =          makeMainQuery()
                    + "\n" + "ORDER BY doc_date_carry, sumCredit, sumDebit, accTrnId";

        else

            query =          "SELECT accountDocument.*,"
                    + "\n" + "       FIRST_VALUE(accountDocument.dateCarry_sort)"
                    + "\n" + "       OVER (PARTITION BY accountDocument.documentKey"
                    + "\n" + "                 ORDER BY accountDocument.carryType)               t_key_dateCarry,"
                    + "\n" + "       FIRST_VALUE(accountDocument.sumDebit_sort)"
                    + "\n" + "       OVER (PARTITION BY accountDocument.documentKey"
                    + "\n" + "             ORDER BY accountDocument.carryType)                   t_key_sumDebit,"
                    + "\n" + "       FIRST_VALUE(accountDocument.sumCredit_sort)"
                    + "\n" + "       OVER (PARTITION BY accountDocument.documentKey"
                    + "\n" + "                 ORDER BY accountDocument.carryType)               t_key_sumCredit,"
                    + "\n" + "       accountDocument.documentKey                                 t_key_documentKey,"
                    + "\n" + "       accountDocument.carryType                                   t_key_carryType"
                    + "\n" + "  FROM (" + makeMainQuery()
                    + "\n" + "        UNION ALL"
                    + "\n" + "        " + makeSpecialDocumentQuery()
                    + "\n" + "       ) accountDocument"
                    + "\n" + " ORDER BY t_key_dateCarry,"
                    + "\n" + "          t_key_sumDebit NULLS LAST,"
                    + "\n" + "          t_key_sumCredit NULLS LAST,"
                    + "\n" + "          t_key_documentKey,"
                    + "\n" + "          t_key_carryType,"
                    + "\n" + "          accountDocument.accTrnId"
                    ;

        end;

        m_query.cmdText = query;

    end;

    /**
     * Получить RsdCommand запроса для отбора проводок
     *
     * @returns RsdCommand запроса для отбора проводок
     */
    macro getQuery()
        return m_query;
    end;

    /**
     * Конструктор. При вызове формирует текст запроса
     *
     * @param     parameters           параметры отчета
     */
    private macro constructor(parameters : Object)

        m_parameters = parameters;

        m_dateIn = getSqlDate(m_parameters.dateIn);

        m_dateOut = getSqlDate(m_parameters.dateOut);

        m_mainAccount = m_parameters.account.accId;

        m_needEquivalent = (m_parameters.account.currency != NATCUR) and (m_parameters.natcurEquivalent);

        m_query = RsdCommand(rslDefCon);
        m_query.nullConversion = true;

        make();

    end;

    constructor(parameters);

end;

/* Класс с параметрами отчета */
private CLASS TAccStatementInfo
(
  pDprtID,
  pOrgStructure,
  pIssueMode,
  pNumPlan,
  pChapter,
  pCurrency,
  pDateIn,
  pDateOut,
  pIsIncludeClient,
  pClient,
  pOper,
  pBalanceMask,
  pAccountMask,
  pKindSort,
  pOutForm,
  pAlg,
  pSubsystem,
  pPrGround,
  pDblPrint,
  pDocOnlyData,
  pNatCurEquivalent,
  pSPODTurnsTotal,
  pcSetA,
  pcSetZTurn,
  pcSetCr,
  pcEveryDayOfPrd,
  pcSetExcludeRevalue,
  pExcludeOcp,
  pAccountType,
  pIsIncludeType,
  pAccountUserType,
  pIsIncludeUserType,
  pNeedText,
  pNeedExcel,
  pNeedWord,
  pShowStorno,
  pUserTypeMethod,
  pExcludeInternetAccount,
  pTypeMethod,
  pExcelReportKind,
  pWordReportKind,
  pPrintTotalsByDays
)
  private var OldDateIn = Date(0,0,0), OldDateOut = Date(0,0,0), OldCurrency = NATCUR;

  private var Rate_In  = TRecHandler("ratedef.dbt"),
              Rate_Out = TRecHandler("ratedef.dbt"),;

  private var m_documentTableFieldList : String;

  private var m_documentInfoTableFieldList : String;

  var commissionIncomeAccounts : String;

  var OcpAccountServer;

  var DocInfo;
  private var m_cacheTableName;

  var
      pr_documentr = TBFile("accTrn.dbt"),
      pr_documentc = TBFile("accTrn.dbt");

  var pr_pmpaym   = TRecHandler("pmpaym.dbt"),
      pr_debit    = TRecHandler("pmprop.dbt"),
      pr_credit   = TRecHandler("pmprop.dbt"),
      pr_pmrmprop = TRecHandler("pmrmprop.dbt");

  var pr_multydoc = TRecHandler("multydoc.dbt");

  var Account, /* объект TRsbDataSet*/
      DepartmentList,
      AccountFilter,
      BranchAndDprtFilter,
      RateIn,
      RateOut,
      CurrentFormat,
      StatementLineNumber,
      printFullNameClient,
      DprtID                 = pDprtID,
      OrgStructure           = pOrgStructure,
      IssueMode              = pIssueMode,
      NumPlan                = pNumPlan,
      Chapter                = pChapter,
      Currency               = pCurrency,
      DateIn                 = pDateIn,
      DateOut                = pDateOut,
      isIncludeClient        = ternary (pIsIncludeClient == 1, true, false),
      Client                 = pClient,
      Oper                   = pOper,
      BalanceMask            = pBalanceMask,
      AccountMask            = pAccountMask,
      KindSort               = pKindSort,
      OutForm                = pOutForm,
      Format                 = pAlg,
      Subsystem              = pSubsystem,
      PrGround               = pPrGround,
      DblPrint               = pDblPrint,
      DocOnlyData            = pDocOnlyData,
      NatCurEquivalent       = pNatCurEquivalent,
      SPODTurnsTotal         = pSPODTurnsTotal,
      cSetA                  = pcSetA,
      cSetZTurn              = pcSetZTurn,
      cSetCr                 = pcSetCr,
      cEveryDayOfPrd         = pcEveryDayOfPrd,
      cSetExcludeRevalue     = pcSetExcludeRevalue,
      ExcludeOcp             = pExcludeOcp,
      AccountType            = pAccountType,
      isIncludeType          = pIsIncludeType,
      AccountUserType        = pAccountUserType,
      isIncludeUserType      = pIsIncludeUserType,
      needText               = pNeedText,
      needExcel              = pNeedExcel,
      needWord               = pNeedWord,
      showStorno             = pShowStorno,
      userTypeMethod         = pUserTypeMethod,
      excludeInternetAccount = pExcludeInternetAccount,
      typeMethod             = pTypeMethod,
      excelReportKind        = pExcelReportKind,
      wordReportKind         = pWordReportKind,
      printTotalsByDays      = pPrintTotalsByDays;

  var initialDateIn = dateIn;
  var initialDateOut = dateOut;

  var needEquivalent = false;

  private var m_payerComissionAccounts;
  private var m_receiverComissionAccounts;

  private macro RateAsString(dat, Rate)
    var strRate = "";

    if (ПолучитьЗначениеКурса(Rate, dat) != 0)
      Rate.rec.Rate = 0;
    end;

    ПолучитьСтрокуЗначенияКурса(Rate, 0, strRate);

    if (Rate.rec.Scale != 1)
      if (Rate.rec.IsInverse != "")
        strRate = string(Rate.rec.Scale) + "/" + strRate;
      else
        strRate = strRate + "/" + string(Rate.rec.Scale);
      end;
    elif (Rate.rec.IsInverse != "")
      strRate = string(Rate.rec.Scale) + "/" + strRate;
    end;

    return strRate;
  end;

  macro getDocumentTableFieldList()
    return m_documentTableFieldList;
  end;

  macro getDocumentInfoTableFieldList()
    return m_documentInfoTableFieldList;
  end;

  macro getPayerComissionAccounts()
    return m_payerComissionAccounts;
  end;

  macro getReceiverComissionAccounts()
    return m_receiverComissionAccounts;
  end;

  macro getCacheTableName()
    return m_cacheTableName;
  end;

  private macro Constructor()
    var stat = 0;
    var i;

    DepartmentList = RepDepartmentList(OrgStructure, IssueMode, DprtID);

    AccountFilter = RepAccountFilter(DepartmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);
    BranchAndDprtFilter = RepBranchAndDepartmentFieldFilter(DepartmentList);

    OcpAccountServer = RepOcpAccountServer(Chapter, Currency, DepartmentList);

    DocInfo = RepDocInfoServer(PTCK_ALL, B2I(DocOnlyData));

    Апострофы = cSetA;

    m_documentTableFieldList = getSqlFldList("accTrn.dbt", null, "document", "doc_");

    m_documentInfoTableFieldList = getSqlFldList("docinfo.rec", null, "documentInfo", "docInf_");

    getRegistryValue("REPORT/ВЫПИСКА/СЧЕТА ПРОВОДОК КОМИССИИ", V_STRING, commissionIncomeAccounts, stat);

    if (stat != 0)
        commissionIncomeAccounts = "";
    else
        commissionIncomeAccounts = trim(commissionIncomeAccounts);
    end;

    getRegistryValue("REPORT/ВЫПИСКА/ПОЛНОЕ_НАИМЕНОВАНИЕ_КЛИЕНТА", V_BOOL, printFullNameClient, stat);

    if (stat != 0)
        printFullNameClient = true;
    end;

    m_payerComissionAccounts = convertMaskToSQLFormat(commissionIncomeAccounts, "document.t_account_payer");
    m_receiverComissionAccounts = convertMaskToSQLFormat(commissionIncomeAccounts, "document.t_account_receiver");

    m_cacheTableName = docInfo.getCacheTableName();

  end;

  macro getOutRestForDay(restDate, needEquivalent)
      var restCurrencyClause;
      var queryText;

      if (needEquivalent)
          restCurrencyClause = "0";
      else
          restCurrencyClause = account.currency;
      end;

      queryText = "SELECT NVL(t_rest,0) as t_outRest"
         + "\n" + "       FROM drestdate_dbt rest"
         + "\n" + "      WHERE rest.t_accountId = " + account.accId
         + "\n" + "        AND rest.t_restCurrency = " + restCurrencyClause
         + "\n" + "        AND rest.t_restDate = (SELECT MAX(maxDate.t_restDate)"
         + "\n" + "                                 FROM drestdate_dbt maxDate"
         + "\n" + "                                WHERE maxDate.t_accountId = " + account.accId
         + "\n" + "                                  AND maxDate.t_restCurrency = " + restCurrencyClause
         + "\n" + "                                  AND maxDate.t_restDate <= " + getSqlDate(restDate)
         + "\n" + "                              )";

      var hasData = TRsbDataset(queryText);

      var sum : Money = $0.0;

      if (hasData.moveNext())
          sum = hasData.outRest;
      end;

      return sum;
  end;

  /* Фильтр по маске б\с, номеру плана, фильтр технических счетов */
  macro accountFilterMaskBalance(accountAlias)
      defaultParm(accountAlias, "");

      const balanceField = "ab.t_balance" + numPlan;
      const blncMask = ternary(balanceMask != "", "\n" + "           AND rsb_mask.compareStringWithMask(" + getSqlString(balanceMask) + ", balance.t_balance) = 1", "");

      var abCond = balanceField + " != " + getSqlString("")
          + "\n" + " AND EXISTS (SELECT NULL"
          + "\n" + "              FROM dbalance_dbt balance"
          + "\n" + "             WHERE balance.t_balance = " + balanceField
          + "\n" + "               AND balance.t_chapter = ab.t_chapter"
          + "\n" + "               AND balance.t_iNumPlan = " + numPlan
          + "\n" + "               AND LENGTH(TRANSLATE('|' || balance.t_type_balance, '|' || 'Т', '|')) != LENGTH(balance.t_type_balance)"
          +                    blncMask
          + "\n" + "           )"
          ;

      return abCond;
  end;

  macro createLoansAccount()
     var fillCommand;

     var chapterList = strAssemble(chapter, ",");
     departmentList.saveToTable();

     fillCommand = RsdCommand("BEGIN"
                    + "\n" + "    rep_accountstatement.init(?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ?,"
                    + "\n" + "                              ? "
                    + "\n" + "                             );"
                    + "\n" + ""
                    + "\n" + "    rep_accountstatement.fillLoansAccountData('replnsaccount_vw',"
                    + "\n" + "                                              'repLnsContract_vw',"
                    + "\n" + "                                              'ddoc_acc_dbt'"
                    + "\n" + "                                             );"
                    + "\n" + "END;"
                                  );

     fillCommand.addParam("dateIn",              RSDBP_IN, dateIn);
     fillCommand.addParam("dateOut",             RSDBP_IN, dateOut);
     fillCommand.addParam("isIncludeClient",     RSDBP_IN, ternary(isIncludeClient, "X", "O"));
     fillCommand.addParam("clientId",            RSDBP_IN, client);
     fillCommand.addParam("operNumber",          RSDBP_IN, oper);
     fillCommand.addParam("currencyId",          RSDBP_IN, currency);
     fillCommand.addParam("chapterNumber",       RSDBP_IN, chapterList);
     fillCommand.addParam("accountMask",         RSDBP_IN, convertMaskToSqlFormat(accountMask, "$STRING_TO_REPLACE$"));
     fillCommand.addParam("balanceMask",         RSDBP_IN, convertMaskToSqlFormat(balanceMask, "$STRING_TO_REPLACE$"));
     fillCommand.addParam("showZeroTurn",        RSDBP_IN, ternary(cSetZTurn, "X", "O"));
     fillCommand.addParam("subsystemCbId",       RSDBP_IN, REP_SUBSYSTEMS_CB);
     fillCommand.addParam("subsystemLoansId",    RSDBP_IN, REP_SUBSYSTEMS_LOANS);
     fillCommand.addParam("printFullNameClient", RSDBP_IN, ternary(printFullNameClient, "X", "O"));

     sql_execute(fillCommand);

     return NULL;
  end;

  macro DPD()
    return Ternary(NVL(Account.DPD, "") == ZeroDate, "", string(Account.DPD:f));
  end;

  macro DPD_R()
    return Ternary(NVL(Account.DPD_R, "") == ZeroDate, "", string(Account.DPD_R:f));
  end;

  macro INN()
    return NVL(Account.INN, "");
  end;

  macro Next()

    var retVal;

    if (Account.Next())
      if ((Account.Currency != NATCUR) and ((OldDateIn != DateIn) or (OldDateOut != DateOut) or (OldCurrency != Account.Currency)))
        if (not ПолучитьКурс(Rate_In, NATCUR, Account.Currency, RATETYPE_CB))
          Copy(Rate_Out, Rate_In);
          RateIn  = RateAsString(DateIn, Rate_In);
          RateOut = RateAsString(DateOut, Rate_Out);

          OldDateIn   = DateIn;
          OldDateOut  = DateOut;
          OldCurrency = Account.Currency;
        else
          RateIn  = NOTFOUND;
          RateOut = NOTFOUND;
        end;
      end;

      /* Определяем формат выписки для текущего счета */
      if (Account.InitialSubsystem == REP_SUBSYSTEMS_LOANS)
        CurrentFormat = ACC_FORMATS_BANK;
      else
        CurrentFormat = Format;
      end;

      StatementLineNumber = 0;

      retVal = true;
    else
        retVal = false;
    end;

    needEquivalent = NatCurEquivalent and (Account.Currency != NATCUR) and (Account.InitialSubsystem == REP_SUBSYSTEMS_CB);

    return retVal;

  end;

  macro DebugPrint()
    println(Account.Acc:25, Account.AccName:50, Account.NameClnt:30,
            Account.Oper:25, Account.CurrencyName:20, Account.RestIn:20,
            Account.RestInEq:20, Account.RestOut:20, Account.RestOutEq:20, RateIn:15:r,
            RateOut:15:r, Account.TypeAccount:10:r);
  end;

  Constructor();
END;

/* Класс проводки */
private CLASS TDocument(pOrgStructure)
  private var PayerBankID, ReceiverBankID,
              OrgStructure = pOrgStructure,
              Subsystem, InitialSubsystem, CurrentFormat,
              DocumentCb = TRecHandler("accTrn.dbt");

  private var m_isCbDocument;
  private var m_departmentsDatasetName = ternary(pOrgStructure == TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw");

  private var m_needFDocs;

  var DocStorage, /* данные для печати приложений */

      DataSet, /* объект TRsbDataSet или RepLnsDocumentSt */

      OverallDocs,

      AllSumDebit : Money,
      AllSumCredit : Money,
      AllSumDebitEq : Money,
      AllSumCreditEq : Money,

      AllSumDebitSPOD : Money,
      AllSumCreditSPOD : Money,
      AllSumDebitSPODEq : Money,
      AllSumCreditSPODEq : Money,

      DocT = "";

  macro Constructor(CmdText, accInfo)
    var queryText;

    subsystem        = accInfo.account.subsystem;            /* подсистема-источник данных */
    initialSubsystem = accInfo.account.initialSubsystem;     /* подсистема-источник счета  */
    currentFormat    = accInfo.currentFormat;                /* формат выписки для текущего счета */
    m_needFDocs      = accInfo.outForm != ACC_FORMS_NOTHING; /* признак печати приложений */

    if (subsystem == REP_SUBSYSTEMS_LOANS)

      queryText = "SELECT (SELECT account.t_chapter"
         + "\n" + "          FROM repLnsAccount_vw account"
         + "\n" + "         WHERE account.t_id = document.a_accId"
         + "\n" + "       )                                             AS t_chapter,"
         + "\n" + "       (SELECT account.t_departmentId"
         + "\n" + "          FROM repLnsAccount_vw account"
         + "\n" + "         WHERE account.t_id = document.a_accId"
         + "\n" + "       )                                             AS t_department,"
         + "\n" + "       (SELECT cbaccount.t_oper"
         + "\n" + "          FROM daccount_dbt cbaccount,"
         + "\n" + "               repLnsAccount_vw account"
         + "\n" + "         WHERE account.t_id = document.a_accId"
         + "\n" + "           AND cbaccount.t_account = account.t_cbAccount"
         + "\n" + "           AND cbaccount.t_chapter = account.t_chapter"
         + "\n" + "           AND cbaccount.t_code_currency = account.t_fiId"
         + "\n" + "       )                                             AS t_operNumber,"
         + "\n" + "       CASE"
         + "\n" + "           WHEN EXISTS (SELECT NULL"
         + "\n" + "                          FROM repLnsAccount_vw account"
         + "\n" + "                         WHERE account.t_id = document.a_accId"
         + "\n" + "                           AND account.t_account = document.a_debit"
         + "\n" + "                       )"
         + "\n" + "           THEN " + DEBIT_C
         + "\n" + "           ELSE " + CREDIT_C
         + "\n" + "       END                                           AS t_dk,"
         + "\n" + "       CASE"
         + "\n" + "           WHEN EXISTS (SELECT NULL"
         + "\n" + "                          FROM repLnsAccount_vw account"
         + "\n" + "                         WHERE account.t_id = document.a_accId"
         + "\n" + "                           AND account.t_account = document.a_debit"
         + "\n" + "                       )"
         + "\n" + "           THEN document.a_carrySum"
         + "\n" + "           ELSE 0"
         + "\n" + "       END                                           AS t_sumDebit,"
         + "\n" + "       0                                             AS t_sumDebitEq,"
         + "\n" + "       CASE"
         + "\n" + "           WHEN EXISTS (SELECT NULL"
         + "\n" + "                          FROM repLnsAccount_vw account"
         + "\n" + "                         WHERE account.t_id = document.a_accId"
         + "\n" + "                           AND account.t_account = document.a_credit"
         + "\n" + "                       )"
         + "\n" + "           THEN document.a_carrySum"
         + "\n" + "           ELSE 0"
         + "\n" + "       END                                           AS t_sumCredit,"
         + "\n" + "       0                                             AS t_sumCreditEq,"
         + "\n" + "       CASE"
         + "\n" + "           WHEN EXISTS (SELECT NULL"
         + "\n" + "                          FROM repLnsAccount_vw account"
         + "\n" + "                         WHERE account.t_id = document.a_accId"
         + "\n" + "                           AND account.t_account = document.a_debit"
         + "\n" + "                       )"
         + "\n" + "           THEN document.a_credit"
         + "\n" + "           ELSE document.a_debit"
         + "\n" + "       END                                           AS t_account,"
         + "\n" + "       document.a_carryDate                          AS t_date_carry,"
         + "\n" + "       document.a_curCode                            AS t_code_currency,"
         + "\n" + "       0                                             AS t_carryId,"
         + "\n" + "       0                                             AS t_paymentId,"
         + "\n" + "       0                                             AS t_autokey,"
         + "\n" + "       document.a_ground                             AS t_ground,"
         + "\n" + "       document.a_outDocId                           AS t_outDocId,"
         + "\n" + "       document.a_debit                              AS t_debit,"
         + "\n" + "       document.a_credit                             AS t_credit,"
         + "\n" + "       0                                             AS t_payerCodeKind,"
         + "\n" + "       CHR(1)                                        AS t_payerBankCode,"
         + "\n" + "       0                                             AS t_receiverCodeKind,"
         + "\n" + "       CHR(1)                                        AS t_receiverBankCode,"
         + "\n" + "       1                                             AS isInternalDocument,"
         + "\n" + "       document.a_carrysum                           AS t_carrysum"
         + "\n" + "  FROM ormLnsDocument document"
         + "\n" + " WHERE document.a_accId = " + int(accInfo.account.accId)
         + "\n" + "   AND (    document.a_carryDate >= " + getSqlDate(accInfo.dateIn)
         + "\n" + "        AND document.a_carryDate <= " + getSqlDate(accInfo.dateOut)
         + "\n" + "       )"
         + "\n" + " ORDER BY t_date_carry,"
         + "\n" + "          t_sumDebit,"
         + "\n" + "          t_sumCredit";

      dataset = TRsbDataset(queryText);

      dataset.setFieldType("t_date_carry",  V_DATE);
      dataset.setFieldType("t_sumDebit",    V_MONEY);
      dataset.setFieldType("t_sumCredit",   V_MONEY);
      dataset.setFieldType("t_sumDebitEq",  V_MONEY);
      dataset.setFieldType("t_sumCreditEq", V_MONEY);
      dataset.setFieldType("t_paymentId",   V_INTEGER);

    else

      DataSet = TRsbDataset(CmdText);

      DataSet.SetFieldType("Date_Carry",  V_DATE);
      DataSet.SetFieldType("SumDebit",    V_MONEY);
      DataSet.SetFieldType("SumCredit",   V_MONEY);
      DataSet.SetFieldType("SumDebitEq",  V_MONEY);
      DataSet.SetFieldType("SumCreditEq", V_MONEY);
      dataset.setFieldType("paymentId",   V_INTEGER);

    end;

    OverallDocs = 0;

    AllSumDebit    = $0;
    AllSumCredit   = $0;
    AllSumDebitEq  = $0;
    AllSumCreditEq = $0;

    AllSumDebitSPOD    = $0;
    AllSumCreditSPOD   = $0;
    AllSumDebitSPODEq  = $0;
    AllSumCreditSPODEq = $0;

    if (m_needFDocs)
      DocStorage = TArray(10,10);
    end;
  end;

  private macro CheckDocumentCb()
    if (m_isCbDocument == null)
      m_isCbDocument = ((DocumentCb.rec.Date_Carry == DataSet.Date_Carry)   and
                        (DocumentCb.rec.Sum_Payer == DataSet.CarrySum)      and
                        (DocumentCb.rec.Account_Payer == DataSet.Debit)     and
                        (DocumentCb.rec.Account_Receiver == DataSet.Credit));
    end;

    return m_isCbDocument;
  end;

  private macro GetIDs()
      payerBankID = TPartyIdCachedData().getPartyId(DataSet.PayerBankCode, DataSet.PayerCodeKind, DataSet.Date_Carry);
      receiverBankID = TPartyIdCachedData().getPartyId(DataSet.ReceiverBankCode, DataSet.ReceiverCodeKind, DataSet.Date_Carry);
  end;

  /* 31.10.2006 ABP SCR 94026. Получение корректного корреспондирующего счета */
  private macro GetCorrectCbCorrespondingAccount(isDebit)
      var correctCbCorrespondingAccount = "";
      var ds;
      var query;
      var queryText;

      //DEF-61903 требуется брать счет из проводки
      /*if (dataSet.account != "")
          return dataSet.account;
      end;*/

      if ((isDebit) and (DataSet.ReceiverAccount != ""))
        return DataSet.ReceiverAccount;
      end;

      if ((not isDebit) and (DataSet.PayerAccount != ""))
        return DataSet.PayerAccount;
      end;

      /* Чтоб лишний раз БД не дергать*/
      if (DataSet.PaymentId != 0)

          queryText =  "SELECT doc.t_account_payer    AS t_payerAccount,"
              + "\n" + "       doc.t_account_receiver AS t_receiverAccount"
              + "\n" + "  FROM daccTrn_dbt " + " doc,"
              + "\n" + "       dpmdocs_dbt pmdoc"
              + "\n" + " WHERE pmdoc.t_PaymentId = ?"
              + "\n" + "   AND pmdoc.t_accTrnId = doc.t_accTrnId";

          query = RsdCommand(rslDefCon, queryText);
          query.addParam("paymentId", RSDBP_IN, DataSet.PaymentId);
          query.nullConversion = true;

          ds = TRsbDataset(query);

          if (ds.next())
              if (isDebit)
                  correctCbCorrespondingAccount = nvl(ds.receiverAccount, "");
              else
                  correctCbCorrespondingAccount = nvl(ds.payerAccount, "");
              end;

              if (ds.next())
                correctCbCorrespondingAccount = "";
              end;
          else
              correctCbCorrespondingAccount = "";
          end;
      else
          correctCbCorrespondingAccount = "";
      end;

      return correctCbCorrespondingAccount;
  end;

  private macro GetCorrectLoansCorrespondingAccount()
      if(CheckDocumentCb())
          if (DataSet.DK == DEBIT_C)
            return DocumentCb.rec.Account_Receiver;
          else
            return DocumentCb.rec.Account_Payer;
          end;
      elif(DataSet.Account != "")
          return DataSet.Account;
      else
          if (DataSet.DK == DEBIT_C)
            return DocumentCb.rec.Account_Receiver;
          else
            return DocumentCb.rec.Account_Payer;
          end;
      end;
  end;

  macro CorrBankAcc()
    if (dataSet.isInternalDocument == 0)
      if (PayerBankID == {OurBank})
        if (DataSet.ReceiverCorrAcc == NULL)
          return EMPTY_STRING;//NOTFOUND;
        else
          return DataSet.ReceiverCorrAcc;
        end;
      else
        if (DataSet.PayerCorrAcc == NULL)
          return EMPTY_STRING;//NOTFOUND;
        else
          return DataSet.PayerCorrAcc;
        end;
      end;
    else
      return EMPTY_STRING;
    end;
  end;

  macro BIC()
    var err, code;
    if (dataSet.isInternalDocument == 0)
      if (PayerBankID == {OurBank})
        if (TPartyCachedData().getResidentFlag(ReceiverBankID))
            code = nvl(DataSet.docInf_ReceiverBic, EMPTY_STRING);
        else /*Если банк-корреспондент является нерезидентом*/
            code = nvl(DataSet.docInf_ReceiverSwift, EMPTY_STRING);
        end;
      else
        if (TPartyCachedData().getResidentFlag(PayerBankID))
            code = nvl(DataSet.docInf_PayerBic, EMPTY_STRING);
        else
            code = nvl(DataSet.docInf_PayerSwift, EMPTY_STRING);
        end;
      end;
    else
      code = EMPTY_STRING;/*Если проводка внутренняя, то поле отчета "БИК" не заполняется*/
    end;

    return code;
  end;

  macro Shifr_Oper()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return DataSet.Shifr_Oper;
    else
      return DocumentCb.rec.Shifr_Oper;
    end;
  end;

  macro Numb_Document()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return DataSet.Numb_Document;
    else
      return DocumentCb.rec.Numb_Document;
    end;
  end;

  macro Ground()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return DataSet.Ground;
    else
      if (CheckDocumentCb())
        return DocumentCb.rec.Ground;
      else
        return DataSet.Ground;
      end;
    end;
  end;

  macro SumDebit()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return DataSet.SumDebit;
    else
      if (CheckDocumentCb())
        if (DataSet.DK == DEBIT_C)
          return DocumentCb.rec.Sum_Payer;
        else
          return $0;
        end;
      else
        return DataSet.SumDebit;
      end;
    end;
  end;

  macro SumCredit()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return DataSet.SumCredit;
    else
      if (CheckDocumentCb())
        if (DataSet.DK == CREDIT_C)
          return DocumentCb.rec.Sum_Receiver;
        else
          return $0;
        end;
      else
        return DataSet.SumCredit;
      end;
    end;
  end;

  macro SumDebitEq()
    return DataSet.SumDebitEq;
  end;

  macro SumCreditEq()
    return DataSet.SumCreditEq;
  end;

  macro Account()
    if (Subsystem == REP_SUBSYSTEMS_CB)
      return GetCorrectCbCorrespondingAccount(DataSet.DK == DEBIT_C);
    else
      return GetCorrectLoansCorrespondingAccount();
    end;
  end;

  macro Next()
    var DocKind, DocCmdText, ds, ExistsOneDocumentCb;
    var query;

    if (DataSet.Next())
      GetIDs();
      if (CurrentFormat == ACC_FORMATS_BANK)
        /* нужно только при сборе данных по документам Loans (аналитика счета ведется в Loans) */
        if (Subsystem == REP_SUBSYSTEMS_LOANS)

          if (DataSet.Code_Currency == NATCUR)
            DocKind = DLDOC_CARRY;
          else
            DocKind = DLDOC_CARRYC;
          end;

          DocCmdText = "SELECT doc.*"
              + "\n" + "  FROM doprdocs_dbt odoc,"
              + "\n" + "       daccTrn_dbt doc"
              + "\n" + " WHERE odoc.t_Id_Operation = ?"
              + "\n" + "   AND odoc.t_DocKind = ?"
              + "\n" + "   AND odoc.t_DocumentId != CHR(1)"
              + "\n" + "   AND doc.t_accTrnId = odoc.t_accTrnId";

          query = RsdCommand(rslDefCon, DocCmdText);
          query.addParam("outDocumentId", RSDBP_IN, DataSet.OutDocID);
          query.addParam("documentKind", RSDBP_IN, DocKind);
          query.nullConversion = true;

          ds = TRsbDataset(query);
          if (ds.Next())
            ds.GetRecord().CopyTo(DocumentCb.rec);

            DocumentCb.rec.accTrnId         = 0;
            DocumentCb.rec.SHIFR_OPER       = Shifr_Oper();
            DocumentCb.rec.NUMB_DOCUMENT    = Numb_Document();
            DocumentCb.rec.GROUND           = Ground();

            ExistsOneDocumentCb = true;
            if (ds.Next())
              ExistsOneDocumentCb = false;
            end;
          else
            ExistsOneDocumentCb = false;
          end;

          if (not ExistsOneDocumentCb)

            DocumentCb.rec.accTrnId             = dataset.autokey;
            DocumentCb.rec.state                = 1;
            DocumentCb.rec.Chapter              = dataset.chapter;
            DocumentCb.rec.Date_Carry           = dataset.t_date_carry;
            DocumentCb.rec.Date_Rate            = date(0,0,0);
            DocumentCb.rec.FIID_Payer           = dataset.code_currency;
            DocumentCb.rec.FIID_Receiver        = dataset.code_currency;
            DocumentCb.rec.AccountID_Payer      = 0;
            DocumentCb.rec.AccountID_Receiver   = 0;
            DocumentCb.rec.Account_Payer        = dataset.debit;
            DocumentCb.rec.Account_Receiver     = dataset.credit;
            DocumentCb.rec.Sum_NatCur           = $0;
            DocumentCb.rec.Sum_Payer            = dataset.carrySum;
            DocumentCb.rec.Sum_Receiver         = dataset.carrySum;
            DocumentCb.rec.FIIDEq_Payer         = 0;
            DocumentCb.rec.FIIDEq_Receiver      = 0;
            DocumentCb.rec.SkipRestEqChange     = "";
            DocumentCb.rec.SumEq_Payer          = $0;
            DocumentCb.rec.SumEq_Receiver       = $0;
            DocumentCb.rec.Result_Carry         = 1;
            DocumentCb.rec.Number_Pack          = 0;
            DocumentCb.rec.Oper                 = dataset.operNumber;
            DocumentCb.rec.Department           = dataset.department;
            DocumentCb.rec.Branch               = dataset.department;
            DocumentCb.rec.Numb_Document        = "";
            DocumentCb.rec.Ground               = dataset.ground;
            DocumentCb.rec.Shifr_Oper           = "09";
            DocumentCb.rec.Kind_Oper            = "1";
            DocumentCb.rec.TypeDocument         = "";
            DocumentCb.rec.UserTypeDocument     = "";
            DocumentCb.rec.Priority             = 0;
            DocumentCb.rec.MinPhase             = 0;
            DocumentCb.rec.MaxPhase             = 0;
            DocumentCb.rec.SystemDate           = date(0,0,0);
            DocumentCb.rec.SystemTime           = time(0,0,0);
            DocumentCb.rec.CheckSum             = "";
            DocumentCb.rec.ExRateAccTrnID       = 0;
            DocumentCb.rec.ParentAccTrnID       = 0;
            DocumentCb.rec.ClaimID              = 0;
            DocumentCb.rec.MethodID             = 0;
            DocumentCb.rec.MinimizationTurn     = "";
            DocumentCb.rec.ExRateAccPlusDebet   = "";
            DocumentCb.rec.ExRateAccPlusCredit  = "";
            DocumentCb.rec.ExRateAccMinusDebet  = "";
            DocumentCb.rec.ExRateAccMinusCredit = "";
            DocumentCb.rec.SkipRecalcSumNatCur  = "";
            DocumentCb.rec.FlagRecalcSum        = 0;
            DocumentCb.rec.Rate                 = 0;
            DocumentCb.rec.Scale                = 1;
            DocumentCb.rec.Point                = 2;
            DocumentCb.rec.IsInverse            = "";
            DocumentCb.rec.UserField1           = "";
            DocumentCb.rec.UserField2           = "";
            DocumentCb.rec.UserField3           = "";
            DocumentCb.rec.UserField4           = "";
            DocumentCb.rec.NU_Status            = 0;
            DocumentCb.rec.NU_Kind              = 0;
            DocumentCb.rec.NU_StartDate         = date(0,0,0);
            DocumentCb.rec.NU_EndDate           = date(0,0,0);
            DocumentCb.rec.NU_AckDate           = date(0,0,0);
            DocumentCb.rec.FU_AccTrnID          = 0;

          end;
        end;
      end;

      OverallDocs = OverallDocs+1;

      AllSumDebit    = AllSumDebit+SumDebit;
      AllSumCredit   = AllSumCredit+SumCredit;
      AllSumDebitEq  = AllSumDebitEq+SumDebitEq;
      AllSumCreditEq = AllSumCreditEq+SumCreditEq;

      docT = TYPE_NOTSPOD;
      if (Subsystem == REP_SUBSYSTEMS_CB)
        if (dataset.stornoNote != "")
          var stornoNoteArray = TArray();
          stornoNoteArray = strCut(dataset.stornoNote, "|");

          docT =        stornoNoteArray.value(0)
               + "\n" + String(Date(stornoNoteArray.value(1)) : f)
               + "\n" + stornoNoteArray.value(2)
               + "\n" + mkstr("_", wSPOD-1);
        end;

        if (DataSet.isSpod > 0)
          AllSumDebitSPOD    = AllSumDebitSPOD+SumDebit;
          AllSumCreditSPOD   = AllSumCreditSPOD+SumCredit;
          AllSumDebitSPODEq  = AllSumDebitSPODEq+SumDebitEq;
          AllSumCreditSPODEq = AllSumCreditSPODEq+SumCreditEq;

          docT = docT + ternary(docT != TYPE_NOTSPOD, "\n", "") + TYPE_SPOD;
        end;
      end;

      if (m_needFDocs)
        if (Subsystem == REP_SUBSYSTEMS_CB)
          DocStorage[DocStorage.Size] = ArrCreate(DataSet.Autokey,
                                                  DataSet.DK,
                                                  DataSet.PaymentID,
                                                  DataSet.CarryID,
                                                  DataSet.Code_Currency
                                                 );
        else
            if (dataSet.autoKey > 0)
                DocStorage[DocStorage.Size] = ArrCreate(DataSet.Autokey,
                                                        DataSet.DK,
                                                        DataSet.PaymentID,
                                                        DataSet.CarryID,
                                                        DataSet.Code_Currency,
                                                        DocumentCb
                                                       );
            end;
        end;
      end;

      m_isCbDocument = null;

      return true;
    end;

    return false;
  end;

  macro getSubsystem()
      return Subsystem;
  end;
END;

/************************************************************************************************************************/
/*****************************************************  ПЕЧАТЬ  *********************************************************/
/*****************************************************  НАЧАЛО  *********************************************************/
/************************************************************************************************************************/
/**
 * Структура записи для печати документов
 *
 * @since   v.6.20.030.049
 * @author  ABP
 * @version 1.0
 */
private class TDocumentRecord(pNote, pStatementLineNumber, pDate_carry, pShifr_oper, pNumb_document, pBic, pGround, pCorrBankAcc,
                              pAccount, pSumDebit, pSumCredit, pSumDebitEq, pSumCreditEq, pAllSumDebit, pAllSumCredit, pAllSumCrediEq,
                              pAllSumDebitEq, pOverallDocs, pSubSystem, pRestTotal, pRestTotalEq)

    var note                = pNote;
    var statementLineNumber = pStatementLineNumber;
    var date_carry          = pDate_carry;
    var shifr_oper          = pShifr_oper;
    var numb_document       = pNumb_document;
    var bic                 = pBic;
    var ground              = pGround;
    var corrBankAcc         = pCorrBankAcc;
    var account             = pAccount;
    var sumDebit            = pSumDebit;
    var sumCredit           = pSumCredit;
    var sumDebitEq          = pSumDebitEq;
    var sumCreditEq         = pSumCreditEq;
    var allSumDebit         = pAllSumDebit;
    var allSumCredit        = pAllSumCredit;
    var allSumDebitEq       = pAllSumDebitEq;
    var allSumCreditEq      = pAllSumCrediEq;
    var overallDocs         = pOverallDocs;
    var subSystem           = pSubSystem;
    var restTotal           = pRestTotal;
    var restTotalEq         = pRestTotalEq;

    macro getSubsystem()
        return subSystem;
    end;

    macro initialize()
        this.allSumCredit    = $0.0;
        this.allSumDebit     = $0.0;
        this.allSumDebitEq   = $0.0;
        this.allSumCreditEq  = $0.0;
        this.restTotal       = $0.0;
        this.restTotalEq     = $0.0;
    end;
end;

/**
 * Движок печати в plain text на основе CTableReport
 *
 * @since   v.6.20.030.051
 * @author  ABP
 * @version 1.0
 */
private class TTableReportPrintEngine()

    private var m_printEngine : Object;
    private var m_tablesPool : TArray;
    private var m_isTableStored : Bool;
    private var m_tableHeader : String;
    private var m_cells : TArray;
    private var m_isTableCell : Bool;
    private var m_needTableHeader : Bool;
    private var m_needTableFooter : Bool;
    private var m_useEmptyDataBottom : Bool;
    private var m_previousHeader : Object;

    macro addNewSheetBreak(name, header)
        m_tablesPool.value(m_tablesPool.size) = m_printEngine.getPrintBuffer();
        m_isTableStored = true;
        m_isTableStored = false;
        m_needTableHeader = null;
        m_needTableFooter = null;
        m_useEmptyDataBottom = null;
        if (m_previousHeader != header)
            m_printEngine = CTableReport(0, false, false, true, 0);
            this.setHeader(header);
        end;
    end;

    macro addPrintCell(value : Variant, width : Integer, precision : Integer, format : String, valueType : Integer, flagPrint : Bool)
        if (valueType == REP_ELEM_TABL)
            m_isTableCell = true;
            if (m_needTableHeader == null)
                m_needTableHeader = true;
            end;
            if (valType(value) == V_MONEY)
                precision = 2;
            end;
        else
            m_isTableCell = false;
        end;

        m_cells.value(m_cells.size) = execExp("String(value :" + width + ":" + precision + format + ")");
    end;

    macro addStr()
        if (m_cells.size == 0)
            this.addEmptyString();
            return;
        end;

        if (m_isTableCell)
            if (m_needTableHeader)
                m_printEngine.printFreeString(m_tableHeader);
                m_needTableHeader = false;
                m_needTableFooter = true;
            end;
            if (m_cells.size == 1)
                m_printEngine.printFreeString(m_cells.value(0), true);
                m_useEmptyDataBottom = true;
            else
                m_printEngine.printStringTransferByWord(m_cells);
                m_useEmptyDataBottom = false;
            end;
        else
            if (m_needTableFooter)
                if (m_useEmptyDataBottom)
                    m_printEngine.printBottomExt(m_printEngine.getAColumns().size);
                else
                    m_printEngine.printBottom();
                end;
                m_needTableFooter = false;
                m_needTableHeader = null;
                m_useEmptyDataBottom = null;
            end;
            var str = "";
            for (var i, m_cells)
                str = str + i;
            end;
            m_printEngine.printFreeString(str);
        end;

        m_cells.size = 0;
    end;

    macro addEmptyStr()
        m_printEngine.printFreeString("");
    end;

    macro setHeader(header : Object)
        var columns = header.value(0).getColumns();
        var column;
        m_tableHeader = header.value(1);

        var i = 0;
        while (i < columns.size)
            column = columns.value(i).getTableHeaderField();
            m_printEngine.addColumn(column.text, Double(column.width), column.format);
            i = i + 1;
        end;

        m_previousHeader = header;
    end;

    macro getHeaderWidth()
        return m_printEngine.getSumLen();
    end;

    macro getColWidthTable(columnNumber)
        return m_printEngine.getAColumns().value(columnNumber).getLen();
    end;

    macro getWidthBeforeCol(columnNumber)
        return m_printEngine.getWidthBeforeCol(columnNumber);
    end;

    macro getSheetNumber(name : String) : Integer
        return -1;
    end;

    macro setCurSheet(number : Integer)
        return true;
    end;

    macro printRep(needDoublePrint)
        if (not m_isTableStored)
            m_tablesPool.value(m_tablesPool.size) = m_printEngine.getPrintBuffer();
            m_isTableStored = true;
        end;

        var i;
        var j;

        var maxWidth = 0;
        if (needDoublePrint)
            var length;
            i = 0;
            while (i < m_tablesPool.size)
                j = 0;
                while (j < m_tablesPool.value(i).size)
                    length = strlen(m_tablesPool.value(i).value(j));
                    if (length > maxWidth)
                        maxWidth = length;
                    end;
                    j = j + 1;
                end;
                i = i + 1;
            end;
        end;

        i = 0;
        while (i < m_tablesPool.size)
            j = 0;
            while (j < m_tablesPool.value(i).size)
                print(m_tablesPool.value(i).value(j));
                if (needDoublePrint)
                    print(mkStr(" ", maxWidth - strlen(m_tablesPool.value(i).value(j)) + DBLPRN_DELIMITER_WIDTH));
                    print(m_tablesPool.value(i).value(j));
                end;
                println();
                j = j + 1;
            end;
            i = i + 1;
        end;
    end;

    private macro constructorTTableReportPrintEngine()
        m_printEngine = CTableReport(0, false, false, true, 0);
        m_isTableStored = false;
        m_tablesPool = TArray(100, 100);
        m_cells = TArray(20, 5);
        m_needTableHeader = null;
        m_needTableFooter = null;
        m_useEmptyDataBottom = null;
        m_previousHeader = null;
    end;

    constructorTTableReportPrintEngine();

end;

/**
 * CMakeReport с перегруженным методом GetRegim_LoadNewApplication - для перекрытия настройки BANK_INI\WINDOWS REPORT\LOADNEWAPPLICATION
 *
 * @since   v.6.20.030.050
 * @author  ABP
 * @version 1.0
 */
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;

    /**
     * Поиск листа по имени с учетом листов, автоматически добавляемых при превышении макс. кол-ва строк
     * Метод корректно работает только в том случае, когда закладки не менялись местами
     * Отсутствует в базовом классе
     */
    macro getSheetNumber(name : String) : Integer
        var sheetNumber = lsSheet.size() - 1;

        name = strUpr(name);
        while (sheetNumber >= 0)
            var sheetName = strUpr(lsSheet.get(sheetNumber).sheetName);
            if ((sheetName == name) or (index(sheetName, name + strUpr("_Продолжение(")) > 0))
                return sheetNumber;
            end;
            sheetNumber = sheetNumber - 1;
        end;

        return sheetNumber;
    end;
end;

/**
 * Элемент кеша заголовков таблицы для печатного представления
 *
 * @since   v.6.20.031.006
 * @author  ABP
 * @version 1.1
 */
private class TTableHeaderCacheElement(_key : Integer, _header : String, _template : Object)
    var key : Integer =_key;
    var header : String = _header;
    var template : Object = _template;
end;

/**
 * Кеширующий источник заголовков таблицы для печатного представления
 *
 * @since   v.6.20.031.006
 * @author  ABP
 * @version 1.0
 */
private class TTableHeaderMaker(parameters)
    private var m_cache : TArray;
    private var m_parameters : Object;

    private macro makeKey(cSetA, currentFormat, prGround, accountFiId, natCurEquivalent, initialSubsystem) : Integer
        var key : String = "";

        key = key + b2si(cSetA);
        key = key + b2si(currentFormat == ACC_FORMATS_BANK);
        key = key + b2si(prGround);
        key = key + b2si(accountFiId == NATCUR);
        key = key + b2si(natCurEquivalent);
        key = key + b2si(natCurEquivalent and (initialSubsystem == REP_SUBSYSTEMS_CB));

        return int(key);
    end;

    private macro add(key : Integer,
                      cSetA : Bool,
                      currentFormat : Integer,
                      prGround : Bool,
                      accountFiId : Integer,
                      natCurEquivalent : Bool,
                      initialSubsystem : Integer
                     ) : Object

        var header;
        var template = TCompositeTableField();

        var noteHeader = TTableHeaderField(" ",  wSPOD, STR_ALIGN_RIGHT);
        var noteData   = TTableDataField("note", noteHeader.width, getFormatSpecifier(noteHeader.format));

        var lineHeader = TTableHeaderField("Номер|строки",      wSTRNUM, STR_ALIGN_RIGHT);
        var lineData   = TTableDataField("statementLineNumber", lineHeader.width, getFormatSpecifier(lineHeader.format));

        var carryDateHeader = TTableHeaderField("Дата|проводки", wDATE+1, STR_ALIGN_RIGHT);
        var carryDateData   = TTableDataField("date_carry",      carryDateHeader.width, getFormatSpecifier(carryDateHeader.format));

        var contentHeader = TTableHeaderField("Содержание операции", wGROUND, STR_ALIGN_LEFT);
        var contentData   = TTableDataField("ground",                contentHeader.width, getFormatSpecifier(contentHeader.format));

        var shifrHeader = TTableHeaderField("ВО",       wSHIFR, STR_ALIGN_RIGHT);
        var shifrData   = TTableDataField("shifr_oper", shifrHeader.width, getFormatSpecifier(shifrHeader.format));

        var documentNumberHeader = TTableHeaderField("Номер|документа|клиента", wNUMBD, STR_ALIGN_RIGHT);
        var documentNumberData   = TTableDataField("numb_document",             documentNumberHeader.width, getFormatSpecifier(documentNumberHeader.format));

        var bankCodeHeader = TTableHeaderField("БИК", wBIC, STR_ALIGN_RIGHT);
        var bankCodeData   = TTableDataField("bic",   bankCodeHeader.width, getFormatSpecifier(bankCodeHeader.format));

        var groundHeader = TTableHeaderField("Основание", wGROUND, STR_ALIGN_LEFT);
        var groundData   = TTableDataField("ground",      groundHeader.width, getFormatSpecifier(groundHeader.format));

        var accountCorrHeader = TTableHeaderField("Кор.счет",  wACC, STR_ALIGN_CENTER);
        var accountCorrData   = TTableDataField("corrBankAcc", accountCorrHeader.width, getFormatSpecifier(accountCorrHeader.format));

        var accountNumberHeader = TTableHeaderField("Номер cчета", wACC, STR_ALIGN_CENTER);
        var accountNumberData   = TTableDataField("account",       accountNumberHeader.width, getFormatSpecifier(accountNumberHeader.format));

        var accountHeader = TTableHeaderField("Счет|плательщика/получателя", wACC, STR_ALIGN_CENTER);
        var accountData = TTableDataField("account",                         accountHeader.width, getFormatSpecifier(accountHeader.format));

        var debitHeader    = TTableHeaderField("Дебет",  wSUMM, STR_ALIGN_RIGHT);
        var creditHeader   = TTableHeaderField("Кредит", wSUMM, STR_ALIGN_RIGHT);
        var roubleHeader   = TTableHeaderField("Рубли",  wSUMM, STR_ALIGN_RIGHT);
        var currencyHeader = TTableHeaderField("Валюта", wSUMM, STR_ALIGN_RIGHT);

        var debitData    = TTableDataField("sumDebit",    debitHeader.width, getFormatSpecifier(debitHeader.format) + ternary(cSetA, ":a", ""));
        var creditData   = TTableDataField("sumCredit",   creditHeader.width, getFormatSpecifier(creditHeader.format) + ternary(cSetA, ":a", ""));
        var debitEqData  = TTableDataField("sumDebitEq",  roubleHeader.width, getFormatSpecifier(roubleHeader.format) + ternary(cSetA, ":a", ""));
        var creditEqData = TTableDataField("sumCreditEq", currencyHeader.width, getFormatSpecifier(currencyHeader.format) + ternary(cSetA, ":a", ""));

        var amountHeader1 = TCompositeTableField();
        var amountHeader2 = TCompositeTableField();

        if (m_parameters.spodTurnsTotal or m_parameters.ShowStorno)
            template.addField(noteHeader, noteData);
        end;

        if (currentFormat == ACC_FORMATS_BANK)

            template.addField(lineHeader, lineData);
            template.addField(carryDateHeader, carryDateData);
            template.addField(shifrHeader, shifrData);
            template.addField(documentNumberHeader, documentNumberData);

            if (prGround)
                template.addField(contentHeader, contentData);
            end;

            template.addField(accountHeader, accountData);

            if (accountFiId == NATCUR)
                template.addField(debitHeader, debitData);
                template.addField(creditHeader, creditData);
            else
                if (natCurEquivalent and (initialSubsystem == REP_SUBSYSTEMS_CB))

                    amountHeader1.setTableField(TTableField("Дебет", null, STR_ALIGN_CENTER));
                    amountHeader1.addField(currencyHeader, debitData);
                    amountHeader1.addField(roubleHeader, debitEqData);

                    amountHeader2.setTableField(TTableField("Кредит", null, STR_ALIGN_CENTER));
                    amountHeader2.addField(currencyHeader, creditData);
                    amountHeader2.addField(roubleHeader, creditEqData);

                    template.addField(amountHeader1);
                    template.addField(amountHeader2);
                else
                    template.addField(debitHeader, debitData);
                    template.addField(creditHeader, creditData);
                end;
            end;

        else

            template.addField(carryDateHeader, carryDateData);
            template.addField(shifrHeader, shifrData);
            template.addField(documentNumberHeader, documentNumberData);

            if (accountFiId == NATCUR)
                template.addField(accountCorrHeader, accountCorrData);
            end;

            template.addField(bankCodeHeader, bankCodeData);
            template.addField(accountNumberHeader, accountNumberData);

            if (accountFiId == NATCUR)
                template.addField(debitHeader, debitData);
                template.addField(creditHeader, creditData);
            else
                if (natCurEquivalent and (initialSubsystem == REP_SUBSYSTEMS_CB))

                    amountHeader1.setTableField(TTableField("Валюта", null, STR_ALIGN_CENTER));
                    amountHeader1.addField(debitHeader, debitData);
                    amountHeader1.addField(creditHeader, creditData);

                    amountHeader2.setTableField(TTableField("Рубли", null, STR_ALIGN_CENTER));
                    amountHeader2.addField(debitHeader, debitEqData);
                    amountHeader2.addField(creditHeader, creditEqData);

                    template.addField(amountHeader1);
                    template.addField(amountHeader2);
                else
                    amountHeader1.setTableField(TTableField("Валюта", null, STR_ALIGN_CENTER));
                    amountHeader1.addField(debitHeader, debitData);
                    amountHeader1.addField(creditHeader, creditData);

                    template.addField(amountHeader1);
                end;
            end;

            if (prGround)
                template.addField(groundHeader, groundData);
            end;
        end;

        header = "";
        for (var i, template.getReportHeader())
            header = header + "\n" + i;
        end;
        header = substr(header, 2);

        m_cache.value(key) = TTableHeaderCacheElement(key, header, template);

        return m_cache.value(key);

    end;

    /**
     * Получить элемент кеша
     * @param cSetA Признак печати сумм с апострофами
     * @param currentFormat Формат выписки по текущему счету
     * @param prGround Признак печати основания
     * @param accountFiId ФИ счета
     * @param natCurEquivalent Признак печати рублевого эквивалента
     * @param initialSubsystem Подсистема-источник счета
     * @return объект класса TTableHeaderCacheElement или null
     */
    macro get(cSetA : Bool,
              currentFormat : Integer,
              prGround : Bool,
              accountFiId : Integer,
              natCurEquivalent : Bool,
              initialSubsystem : Integer
             ) : Object
        var key : Integer = makeKey(cSetA, currentFormat, prGround, accountFiId, natCurEquivalent, initialSubsystem);
        var result = m_cache.value(key);

        if (result == null)
            return add(key, cSetA, currentFormat, prGround, accountFiId, natCurEquivalent, initialSubsystem);
        else
            return result;
        end;
    end;

    private macro constructor(parameters)
        m_cache = TArray();
        m_parameters = parameters;

        if (parameters.showStorno)
            wSPOD = 12;
        else
            wSPOD = 4;
        end;
    end;

    constructor(parameters);
end;

private var m_cachedHeaderMaker : TTableHeaderMaker;

/**
 * Элемент кеша заголовков банка для печатного представления
 *
 * @since   v.6.20.031.006
 * @author  ABP
 * @version 1.0
 */
private class TBankHeaderCacheElement(_key : Integer, _header : Object)
    var key : Integer =_key;
    var header : Object = _header;
end;

/**
 * Кеширующий источник заголовков банка для печатного представления
 *
 * @since   v.6.20.031.006
 * @author  ABP
 * @version 1.0
 */
private class TBankHeaderMaker()
    private var m_cache : TArray;

    private macro add(key : Integer, dprtId : Integer, orgStructure : Integer, reportWidth : Integer) : Object
        var header = TArray();

        printBankHeaderBuf(dprtId, orgStructure, reportWidth, header);
        var i = 0;
        while (i < header.size)
            header.value(i) = trim(header.value(i));
            i = i + 1;
        end;
        m_cache.value(key) = TBankHeaderCacheElement(key, header);

        return m_cache.value(key);
    end;

    /**
     * Получить элемент кеша
     * @param dprtId Id узла ТС
     * @param orgStructure Тип ТС
     * @param reportWidth Ширина поля вывода заголовка
     * @return Объект класса TBankHeaderCacheElement или null
     */
    macro get(dprtId : Integer, orgStructure : Integer, reportWidth : Integer) : Object
        // 14.03.2012 ABP dprtId и orgStructure не меняются в рамках одного отчета
        var key : Integer = reportWidth;//makeKey(dprtId, orgStructure, reportWidth);
        var result = m_cache.value(key);

        if (result == null)
            return add(key, dprtId, orgStructure, reportWidth);
        else
            return result;
        end;
    end;

    private macro constructor()
        m_cache = TArray();
    end;

    constructor();
end;

private var m_cachedBankHeader = TBankHeaderMaker();

/**
 * Базовое печатное представление на основе Windows Reports
 *
 * @since   v.6.20.030.049
 * @author  ABP
 * @version 1.1
 */
private class TWinRepView(parameters)
    private var m_printEngine : Object;
    private var m_separator : String;
    private var m_reportWidth : Integer;
    private var m_leftHalfWidth : Integer;
    private var m_rightHalfWidth : Integer;
    private var m_tableWidth : Integer;
    private var m_needDoublePrint : Bool;
    private var m_template : TCompositeTableField;
    private var m_parameters : Object;

    private macro getTableHeader() : String
        var header = m_cachedHeaderMaker.get(m_parameters.cSetA,
                                             m_parameters.currentFormat,
                                             m_parameters.prGround,
                                             m_parameters.account.currency,
                                             m_parameters.natCurEquivalent,
                                             m_parameters.account.initialSubsystem
                                            );

        m_template = header.template;

        return header.header;
    end;

    private macro addString(value : String, width : Integer, precision : Integer, format : String, valueType : Integer, flagPrint : Bool)
        m_printEngine.addPrintCell(value, width, precision, format, valueType, flagPrint);
        m_printEngine.addStr();
    end;

    private macro getFullFileName(path, name)
        return toAnsi(path + "\\" + name);
    end;

    macro initialize(parameters)

        m_tableWidth = m_printEngine.getHeaderWidth();
        m_reportWidth = m_tableWidth+1;
        m_leftHalfWidth = Int(m_reportWidth / 2.0);
        if (mod(Double(m_reportWidth), 2.0) == 0.0)
            m_rightHalfWidth = Int(m_reportWidth / 2.0);
        else
            m_rightHalfWidth = Int(m_reportWidth / 2.0) + 1;
        end;

        m_separator = mkstr("─", m_reportWidth);
    end;

    macro printEmptyData()
        addString("Данные отсутствуют", m_tableWidth, 0, getFormatSpecifier(STR_ALIGN_CENTER), REP_ELEM_TABL);
    end;

    macro printEmptyReport()
        addString("Данные отсутствуют", 0, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
    end;

    macro printRow(dataset)
        var data;
        var precision;

        for (var i, m_template.getDataColumns())
            data = execExp("dataset." + i.dataSourceName);
            precision = ternary (valType(data) == V_MONEY, 2, 0);

            m_printEngine.addPrintCell(data, 0, precision, i.format, REP_ELEM_TABL);
        end;
        m_printEngine.addStr();
    end;

    /**
     * Печать шапки.
     */
    macro printHeader(parameters)
        var i, y : Integer;

        m_printEngine.addEmptyStr();
        addString(m_separator, m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_CENTER), REP_ELEM_STR);

        for (i, m_cachedBankHeader.get(parameters.dprtId, parameters.orgStructure, m_reportWidth).header)
            addString(i, m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_CENTER), REP_ELEM_STR);
        end;

        m_printEngine.addEmptyStr();

        addString(String("Выписка за период: c ", parameters.dateIn : f, " по ", parameters.dateOut : f), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);

        m_printEngine.addEmptyStr();

        addString(String("Лицевой счет: ", parameters.account.acc : f), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);

        var stringNameCaption : String  = "Название: ";
        var stringNameLength  : Integer = strlen(stringNameCaption);
        var stringNameValue             = TArray();

        stringNameValue = strTransferByWord(parameters.account.accName, m_reportWidth - stringNameLength);

        m_printEngine.addPrintCell(stringNameCaption, stringNameLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        m_printEngine.addPrintCell(trim(stringNameValue.value(0)), m_reportWidth - stringNameLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        m_printEngine.addStr();
        y = 1;
        while (y < stringNameValue.size)
            m_printEngine.addPrintCell(" ", stringNameLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(trim(stringNameValue.value(y)), m_reportWidth - stringNameLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addStr();
            y = y + 1;
        end;

        m_printEngine.addEmptyStr();

        var clientNameCaption : String = "Клиент : ";
        var clientNameCaptionLength : Integer = strlen(clientNameCaption);
        var clientName = TArray();

        clientName = strTransferByWord(parameters.account.nameClnt, m_reportWidth - clientNameCaptionLength);

        m_printEngine.addPrintCell(clientNameCaption, clientNameCaptionLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        m_printEngine.addPrintCell(trim(clientName.value(0)), m_reportWidth - clientNameCaptionLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        m_printEngine.addStr();
        i = 1;
        while (i < clientName.size)
            m_printEngine.addPrintCell(" ", clientNameCaptionLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(trim(clientName.value(i)), m_reportWidth - clientNameCaptionLength, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addStr();
            i = i + 1;
        end;

        addString(String("ИНН: ", parameters.inn), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);

        m_printEngine.addEmptyStr();

        addString(String("Операционист: ", parameters.account.oper), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);

        var dpdString : String = "";
        if (    (parameters.account.currency == NATCUR)
            and (index(parameters.account.typeAccount, "П") > 0)
            and (parameters.ocpAccountServer.isSuitable(parameters.account.chapter, parameters.account.currency, parameters.account.acc))
           )
            dpdString = "";
        else
            dpdString = parameters.dpd;
        end;

        if (parameters.natCurEquivalent and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB))
            if (parameters.ocpAccountServer.isSuitable(parameters.account.chapter, parameters.account.currency, parameters.account.acc))
                dpdString = "";
            else
                dpdString = parameters.dpd_r;
            end;

            addString(String("Дата последней операции по счету в нац. валюте: ", dpdString), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        end;

        m_printEngine.addEmptyStr();

        if (  (parameters.account.currency == NATCUR)
            or
              (    (parameters.account.currency != NATCUR)
               and (not parameters.natCurEquivalent)
              )
            or (parameters.account.initialSubsystem == REP_SUBSYSTEMS_LOANS)
           )
            addString(parameters.account.currencyName, m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        else
            m_printEngine.addPrintCell(parameters.account.currencyName, m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(String("Курс ЦБ: ", parameters.rateIn), m_rightHalfWidth, 0, getFormatSpecifier(STR_ALIGN_RIGHT), REP_ELEM_STR);
            m_printEngine.addStr();
        end;

        addString(String("Дата последней операции по счету: ", dpdString), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);

        m_printEngine.addEmptyStr();

        m_printEngine.addPrintCell("Входящий остаток " + АП(parameters.account.restIn, parameters.account.kind_account), m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        m_printEngine.addPrintCell(abs(parameters.account.restIn), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
        m_printEngine.addStr();

        if (    (parameters.account.currency != NATCUR)
            and parameters.natCurEquivalent
            and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
           )
            m_printEngine.addPrintCell("Входящий остаток " + АП(parameters.account.restInEq, parameters.account.kind_account) + " эквивалент", m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(abs(parameters.account.restInEq), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            m_printEngine.addStr();
        end;

    end;

    macro printFooter(parameters, docs)
        private macro getColumnNumberByDatasource(dataSourceName) : Integer
            var number : Integer = 0;
            var hasData : Bool = false;

            for (var i, m_template.getDataColumns())
                if (trim(strupr(i.dataSourceName)) == trim(strupr(dataSourceName)))
                    hasData = true;
                    break;
                end;
                number = number + 1;
            end;

            if (not hasData)
                number = -1;
            end;

            return number;
        end;

        if ((parameters.printTotalsByDays) and (isEqClass("TDocumentRecord", docs)))
            m_printEngine.addPrintCell("Итого обороты:", m_printEngine.getWidthBeforeCol(getColumnNumberByDatasource("sumDebit")), 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(docs.allSumDebit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            if (    parameters.natCurEquivalent
                and (parameters.account.currency != NATCUR)
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
                m_printEngine.addPrintCell(docs.allSumDebitEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebitEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumCredit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumCreditEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCreditEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            else
                m_printEngine.addPrintCell(docs.allSumCredit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            end;
            m_printEngine.addStr();

            if ((docs.getSubsystem() != REP_SUBSYSTEMS_LOANS) and parameters.spodTurnsTotal and docs.dataSet.isSpod)
                m_printEngine.addPrintCell("В том числе СПОД:", m_printEngine.getWidthBeforeCol(getColumnNumberByDatasource("sumDebit")), 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumDebitSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                if (    parameters.natCurEquivalent
                    and (parameters.account.currency != NATCUR)
                    and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                    )
                    m_printEngine.addPrintCell(docs.allSumDebitSpodEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebitEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                    m_printEngine.addPrintCell(docs.allSumCreditSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                    m_printEngine.addPrintCell(docs.allSumCreditSpodEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCreditEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                else
                    m_printEngine.addPrintCell(docs.allSumCreditSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                end;
                m_printEngine.addStr();
            end;

            m_printEngine.addPrintCell("Исходящий остаток " + ternary(parameters.printTotalsByDays, "за " + docs.date_carry, ""), m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(abs(docs.restTotal), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            m_printEngine.addStr();

            if (    (parameters.account.currency != NATCUR)
                and parameters.natCurEquivalent
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
                m_printEngine.addPrintCell("Исходящий остаток " + ternary(parameters.printTotalsByDays, " за " + docs.date_carry, "") + " эквивалент", m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
                m_printEngine.addPrintCell(abs(docs.restTotalEq), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addStr();
            end;
        else 
            addString(String("Всего документов: ", docs.overallDocs), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell("Итого обороты:", m_printEngine.getWidthBeforeCol(getColumnNumberByDatasource("sumDebit")), 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(docs.allSumDebit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            if (    parameters.natCurEquivalent
                and (parameters.account.currency != NATCUR)
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
                m_printEngine.addPrintCell(docs.allSumDebitEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebitEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumCredit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumCreditEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCreditEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            else
                m_printEngine.addPrintCell(docs.allSumCredit, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            end;
            m_printEngine.addStr();

            if ((docs.getSubsystem() != REP_SUBSYSTEMS_LOANS) and parameters.spodTurnsTotal and docs.dataSet.isSpod)
                m_printEngine.addPrintCell("В том числе СПОД:", m_printEngine.getWidthBeforeCol(getColumnNumberByDatasource("sumDebit")), 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
                m_printEngine.addPrintCell(docs.allSumDebitSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                if (    parameters.natCurEquivalent
                    and (parameters.account.currency != NATCUR)
                    and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                    )
                    m_printEngine.addPrintCell(docs.allSumDebitSpodEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumDebitEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                    m_printEngine.addPrintCell(docs.allSumCreditSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                    m_printEngine.addPrintCell(docs.allSumCreditSpodEq, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCreditEq"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                else
                    m_printEngine.addPrintCell(docs.allSumCreditSPOD, m_printEngine.getColWidthTable(getColumnNumberByDatasource("sumCredit"))+1, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                end;
                m_printEngine.addStr();
            end;

            m_printEngine.addPrintCell("Исходящий остаток " + АП(parameters.account.restOut, parameters.account.kind_account), m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
            m_printEngine.addPrintCell(abs(parameters.account.restOut), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
            m_printEngine.addStr();

            if (    (parameters.account.currency != NATCUR)
                and parameters.natCurEquivalent
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
                m_printEngine.addPrintCell("Исходящий остаток " + АП(parameters.account.restOutEq, parameters.account.kind_account) + " эквивалент", m_leftHalfWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
                m_printEngine.addPrintCell(abs(parameters.account.restOutEq), m_rightHalfWidth, 2, getFormatSpecifier(STR_ALIGN_RIGHT)+ternary(parameters.cSetA, ":a", ""), REP_ELEM_STR);
                m_printEngine.addStr();
            end;
        end;

        if (    (parameters.account.currency != NATCUR)
            and parameters.natCurEquivalent
            and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
           )
            addString(String("Курс ЦБ: ", parameters.rateOut), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_RIGHT), REP_ELEM_STR);
        end;

    end;

    macro printFD(stringalizedFD : TArray)
        return;
    end;

    /**
     * Получить экземпляр CMakeReport.
     */
    macro getPrintEngine()
        return m_printEngine;
    end;

    /**
     *  Конструктор.
     */
    private macro constructor(parameters : Object)
//        m_printEngine = CMakeReport("", SEP_DEFAULT, 100000);
        m_parameters = parameters;
        m_printEngine = TMakeReport("", SEP_DEFAULT, 100000);
        m_printEngine.setFlagShowMidSeparator(false);
        m_printEngine.setFlagShowGrid(false);

        FlagHighQualityRep = true;

        m_needDoublePrint = parameters.dblPrint;
    end;

    constructor(parameters);

end;

/**
 * Печатное представление plain text на основе Windows Reports
 *
 * @since   v.6.20.030.049
 * @author  ABP
 * @version 1.1
 */
private class (TWinRepView) TPlainTextView(parameters)
    private var m_isFirstInit : Bool;
    private var m_printOFStream : TOFStream;

    private macro getTableHeader() : Variant
        if (isEqClass("TTableReportPrintEngine", m_printEngine))
            var header = getTableHeader();
            return arrCreate(m_template, header);
        else
            return getTableHeader();
        end;
    end;

    macro initialize(parameters)

        if (m_isFirstInit)
            m_printOFStream.clear();
            m_printEngine.setHeader(getTableHeader());
            if (not isEqClass("TTableReportPrintEngine", m_printEngine))
                m_printEngine.getCurSheet().setSheetName(parameters.account.acc);
            end;
            m_isFirstInit = false;
        else
            m_printEngine.addNewSheetBreak(parameters.account.acc, getTableHeader());
        end;

        initialize(parameters);
    end;

    macro printEmptyReport()
        m_printOFStream.clear();
        printEmptyReport();
    end;

    macro printFD(stringalizedFD : TArray)
        var maxWidth = m_reportWidth;
        var length;
        var i;

        for (i, stringalizedFD)
            length = strlen(i);
            if (length > maxWidth)
                maxWidth = length;
            end;
        end;
        m_reportWidth = maxWidth;

        m_printEngine.addEmptyStr();
        addString(m_separator, m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        for (i, stringalizedFD)
            addString(strRPad(i, m_reportWidth, " "), m_reportWidth, 0, getFormatSpecifier(STR_ALIGN_LEFT), REP_ELEM_STR);
        end;
        m_printEngine.addEmptyStr();
    end;

    macro getFileName()
        return m_printOFStream.getFileName();
    end;

    macro print();
        m_printOFStream.setOutputFile();
        m_printEngine.printRep(m_needDoublePrint);
        m_printOFStream.resetOutputFile();

        reportBodyFileName = m_printOFStream.getFileName();
    end;

    macro show()
        m_printOFStream.show();
    end;

    macro clear()
        m_printOFStream.clear();
    end;

    macro save(reportName)
        m_printOFStream.save(reportName);
    end;

    /**
     *  Конструктор.
     */
    private macro constructorTPlainTextView(parameters : Object)
        initTWinRepView(parameters);
        m_printEngine = TTableReportPrintEngine();

        m_isFirstInit = true;

        var name;
//        var dd;
//        var mm;
//        var yyyy;
//        var dateIn;
//        var dateOut;
//
//        dateSplit(parameters.dateIn, dd, mm, yyyy);
//        dateIn = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);
//
//        dateSplit(parameters.dateOut, dd, mm, yyyy);
//        dateOut = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);
//
//        if (dateIn == dateOut)
//            name =       ternary(parameters.currency == NATCUR, "ordacc", "ordacc$")
//                 + "_" + dateIn;
//        else
//            name =       ternary(parameters.currency == NATCUR, "ordacc", "ordacc$")
//                 + "_" + dateIn
//                 + "_" + dateOut;
//        end;
//
//        var extension;
//        splitFile(getTxtFileName("dummy"), null, extension);
//        extension = strsubst(extension, ".", "");
//        name = name + "_" + extension;
        name = ternary(parameters.currency == NATCUR, "ordacc", "ordacc$");

        m_printOFStream = TOFStream(name, false);
    end;

    constructorTPlainTextView(parameters);
end;

/**
 * Печатное представление Excel на основе Windows Reports
 *
 * @since   v.6.20.030.049
 * @author  ABP
 * @version 1.1
 */
private class (TWinRepView) TExcelView(parameters)
    private var m_isFirstInit : Bool;

    macro initialize(parameters)

        if (m_isFirstInit)
            m_printEngine.setHeader(getTableHeader());
            m_printEngine.getCurSheet().setSheetName(parameters.account.acc);
            m_isFirstInit = false;
        else
            if (m_parameters.excelReportKind == REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT)
                var sheetNumber = m_printEngine.getSheetNumber(parameters.account.acc);
                if (sheetNumber != -1)
                    m_printEngine.setCurSheet(sheetNumber);
                    m_printEngine.setHeader(getTableHeader());
                else
                    m_printEngine.addNewSheetBreak(parameters.account.acc, getTableHeader());
                end;
            else
                m_printEngine.addNewSheetBreak(parameters.account.acc, getTableHeader());
            end;
        end;

        initialize(parameters);
    end;

    macro print()
        m_printEngine.setFlagShowReport(false);
        m_printEngine.printWinRep();
        m_printEngine.showWinRep();

        if (in(m_parameters.excelReportKind, REP_EXCEL_REPORT_KIND_UNDEFINED, REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT))
            reportBodyFileName = getFullFileName(m_printEngine.reportPath, m_printEngine.reportFileName_xls);
        end;
    end;

    macro show()
        var excel = CDAOMSExcel(null, true);
        excel.open(getFullFileName(m_printEngine.reportPath, m_printEngine.reportFileName_xls), WINREP_FORMAT_XLSX);
        excel.show();
    end;

    private macro constructorTExcelView(parameters : Object)
        initTWinRepView(parameters);

        m_printEngine.setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
        m_printEngine.setModeBigReport(parameters.excelReportKind != REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT);
        m_printEngine.setFlagShowIndicator(false);
        m_isFirstInit = true;

        var name;
        var dd;
        var mm;
        var yyyy;
        var dateIn;
        var dateOut;

        if (parameters.excelReportKind == REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT)
            dateIn = parameters.initialDateIn;
            dateOut = parameters.initialDateOut;
        else
            dateIn = parameters.dateIn;
            dateOut = parameters.dateOut;
        end;

        dateSplit(dateIn, dd, mm, yyyy);
        dateIn = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        dateSplit(dateOut, dd, mm, yyyy);
        dateOut = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        if (dateIn == dateOut)
            name =       ternary(parameters.currency == NATCUR, "ordacc", "ordacc$")
                 + "_" + dateIn;
        else
            name =       ternary(parameters.currency == NATCUR, "ordacc", "ordacc$")
                 + "_" + dateIn
                 + "_" + dateOut;
        end;

        m_printEngine.setFileNameWin(name);
    end;

    constructorTExcelView(parameters);

end;

/**
 * Печатное представление Word на основе Windows Reports
 *
 * @since   v.6.20.030.049
 * @author  ABP
 * @version 1.0
 */
private class (TWinRepView) TWordView(parameters)
    private var m_isFirstInit : Bool;
    private var m_enginesPool : TArray;

    private macro makeFileName(dateIn, dateOut, account, currency)
        var name;
        var dd;
        var mm;
        var yyyy;

        dateSplit(dateIn, dd, mm, yyyy);
        dateIn = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        dateSplit(dateOut, dd, mm, yyyy);
        dateOut = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        if (dateIn == dateOut)
            name =       ternary(currency == NATCUR, "ordacc", "ordacc$")
                 + "_" + account
                 + "_" + dateIn;
        else
            name =       ternary(currency == NATCUR, "ordacc", "ordacc$")
                 + "_" + account
                 + "_" + dateIn
                 + "_" + dateOut;
        end;

        return name;
    end;

    macro initialize(parameters)
        m_printEngine.setFlagShowIndicator(false);

        m_parameters = parameters;
        m_printEngine.setHeader(getTableHeader());

        initialize(parameters);
    end;

    macro printHeader(parameters)
        m_printEngine.setFileNameWin(makeFileName(parameters.dateIn, parameters.dateOut, parameters.account.acc, parameters.account.currency));
        printHeader(parameters);
    end;

    macro printFooter(parameters, docs)
        printFooter(parameters, docs);
        m_enginesPool.value(m_enginesPool.size) = m_printEngine;
    end;

    macro printRow(dataset)
        var data;
        var temp;

        for (var i, m_template.getDataColumns())
            data = execExp("dataset." + i.dataSourceName);
            if (i.dataSourceName == "ground")
                temp = "";
                for (var j, strTransferByWord(data, i.width))
                    temp = temp + "\n" + trim(j);
                end;
                data = substr(temp, 2);
            end;
            m_printEngine.addPrintCell(data, null, null, i.format, REP_ELEM_TABL);
        end;
        m_printEngine.addStr();
    end;

    macro printEmptyReport()
        printEmptyReport();
        m_enginesPool.value(m_enginesPool.size) = m_printEngine;
    end;

    macro print()
        var i = 0;
        while (i < m_enginesPool.size)
            var engine = m_enginesPool.value(i);
            engine.setFlagShowReport(false);
            engine.printWinRep();
            engine.showWinRep();
            i = i + 1;
        end;
    end;

    macro show()
        var word = CDAOMSWord(null, true);

        if (m_parameters.wordReportKind == REP_WORD_REPORT_KIND_ONE_FILE)
            word.open(reportBodyFileName, WINREP_FORMAT_DOCX);
            word.show();
        else
            var i = 0;
            while (i < m_enginesPool.size)
                var engine = m_enginesPool.value(i);

                word.open(getFullFileName(engine.reportPath, engine.reportFileName_doc), WINREP_FORMAT_DOCX);
                word.show();
                i = i + 1;
            end;
        end;
    end;

    private macro addStatements(word, startIndex)
        var i = nvl(startIndex, 0);
        while (i < m_enginesPool.size)
            var engine = m_enginesPool.value(i);
            word.application.selection.goto(3, -1);
            word.application.selection.insertNewPage();
            word.application.selection.insertFile(getFullFileName(engine.reportPath, engine.reportFileName_doc));
            OR_DeleteFile(getFullFileName(engine.reportPath, engine.reportFileName_doc));
            i = i + 1;
        end;
    end;

    macro creatReportFile(parameters)
        var nodeNumber;
        splitFile(getTxtFileName("dummy"), null, nodeNumber);
        nodeNumber = strsubst(nodeNumber, ".", "");

        reportBodyFileName = getFullFileName(m_printEngine.reportPath, makeFileName(parameters.initialDateIn, parameters.initialDateOut, "all", parameters.currency) + "_" + nodeNumber + ".doc");
        wordReportWorkFileName = getFullFileName(m_printEngine.reportPath, makeFileName(parameters.initialDateIn, parameters.initialDateOut, "all", parameters.currency) + "_" + nodeNumber + ".work");

        var word = CDAOMSWord(null, true);
        word.open(getFullFileName(m_enginesPool.value(0).reportPath, m_enginesPool.value(0).reportFileName_doc));
        // Сохраняем во временный файл
        word.saveAs(wordReportWorkFileName);
        word.close();
        word.open(wordReportWorkFileName);
        OR_DeleteFile(getFullFileName(m_enginesPool.value(0).reportPath, m_enginesPool.value(0).reportFileName_doc));

        addStatements(word, 1);

        return word;
    end;

    macro addReportTo(word)
        addStatements(word);
    end;

    private macro constructorTWordView(parameters : Object)
        // 12.09.2016 ABP Попробовать печать в один файл каждую выписку с новой страницы
        initTWinRepView(parameters);
        m_printEngine = TWinRepView(parameters).getPrintEngine();
        m_printEngine.setWinRepOutput(WINREP_OUTPUT_WORD, WINREP_FORMAT_DOCX);

        m_isFirstInit = true;
        m_enginesPool = TArray();
    end;

    constructorTWordView(parameters);
end;


/**
 * Печатное представление Word на основе POI
 *
*/ 
private class TPOIWordView(parameters)
    private var m_isFirstInit : Bool;
    private var m_enginesPool : TArray;

	private var fTxt;
	private var fNameTxtMess = ""; //"..\\TxtFile\\accstatement_";
	private var tpl_nam="", p_Templ = "", Templ_Dir="";
	private var TemplName_NATCUR = "account_statement.dotx", 
				TemplName_CUR = "account_statement_val.dotx", 
				TemplName_CUR_EQV = "account_statement_val_equiv.dotx";

    private macro makeFileName(dateIn, dateOut, account, currency)
        var name;
        var dd;
        var mm;
        var yyyy;

        dateSplit(dateIn, dd, mm, yyyy);
        dateIn = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        dateSplit(dateOut, dd, mm, yyyy);
        dateOut = String(yyyy) + String(mm : 2 : 0 : o) + String(dd : 2 : 0 : o);

        if (dateIn == dateOut)
            name = /*      ternary(currency == NATCUR, "ordacc", "ordacc$") +*/
                 "_" + account
                 + "_" + dateIn;
        else
            name = /*      ternary(currency == NATCUR, "ordacc", "ordacc$") +*/
                 "_" + account
                 + "_" + dateIn
                 + "_" + dateOut;
        end;

        return name;
    end;

	//выбор и поиск шаблона
	macro find_templ(parameters)
		var StrErr;
		if (parameters.account.currency == NATCUR) //rur //parameters.currency 
			p_Templ = TemplName_NATCUR;
		else
			if(parameters.natcurEquivalent);
				p_Templ = TemplName_CUR_EQV;
			else
				p_Templ = TemplName_CUR;
			end;
		end;
		GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
		tpl_nam = FindPath(toANSI(p_Templ), Templ_Dir); //..\Templs\*_.dotx
		if (strlen(trim(tpl_nam)) == 0)
			runerror("Ошибка поиска шаблона", "Ошибка поиска шаблона " + p_Templ);
		end;  
	end;

	//Запись служебной информации в начало файла
	private macro initTxtFile(parameters)
		fTxt.WriteLine(p_Templ);             //Шаблон dotx
		fTxt.WriteLine(SubStr(p_Templ, 1, index(p_Templ, ".dotx")-1) + makeFileName(parameters.dateIn, parameters.dateOut, parameters.account.acc, parameters.account.currency));   //Файл docx
	end;
	
	private macro SetNameTxtFile(parameters)
		var day, mon, year, hour, min, sec;
		DateSplit(Date(), day, mon, year);
		TimeSplit(Time(), hour, min, sec);
		
		fNameTxtMess = "accstatement" + "_" + trim(parameters.account.acc) + "_";
		fNameTxtMess = fNameTxtMess + String(year:4:o) + String(mon:2:o) + String(day:2:o) + "_" + String(hour:2:o) + String(min:2:o) + String(sec:2:o);
		fNameTxtMess = GetTxtFileName (fNameTxtMess); // + "." + UserNumber();
	end;

    macro initialize(parameters)
		//поиск шаблона
		find_templ(parameters);

		SetNameTxtFile(parameters);
		
		fTxt = TStreamDoc(fNameTxtMess, "C", "rsoem ", 1/*unix*/);
		if ( ValType(fTxt) != V_UNDEF )
		else
			runerror("Текстовый файл вывода не открыт", "Текстовый файл вывода не открыт " + fNameTxtMess);
		end;

		initTxtFile(parameters);
    end;
	
    macro show()
		/**/
    end;
  	

    macro printHeader(parameters) //формируем шапку выписки в текстовом файле 
        var i, y : Integer;
		var clientNameCaption : String = "Клиент : ";
		fTxt.WriteLine ("~Name_Bank");
		fTxt.WriteLine (m_cachedBankHeader.get(parameters.dprtId, parameters.orgStructure, 100).header);
		
		fTxt.WriteLine ("~Period");
        fTxt.WriteLine (String(" c ", parameters.dateIn : f, " по ", parameters.dateOut : f));

		fTxt.WriteLine("~Account");
        fTxt.WriteLine(String( parameters.account.acc : f) );
		
		fTxt.WriteLine("~Name_Account");
		fTxt.WriteLine(trim(parameters.account.accName));

		fTxt.WriteLine("~Client");
		//fTxt.WriteLine(trim(clientNameCaption));
		fTxt.WriteLine(parameters.account.nameClnt);
	
		fTxt.WriteLine("~INN");
		fTxt.WriteLine(parameters.inn);

		fTxt.WriteLine("~Oper");
		fTxt.WriteLine(parameters.account.oper);

        var dpdString : String = "";
        if (    (parameters.account.currency == NATCUR)
            and (index(parameters.account.typeAccount, "П") > 0)
            and (parameters.ocpAccountServer.isSuitable(parameters.account.chapter, parameters.account.currency, parameters.account.acc))
           )
            dpdString = "";
        else
            dpdString = parameters.dpd;
        end;

        if (parameters.natCurEquivalent and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB))
            if (parameters.ocpAccountServer.isSuitable(parameters.account.chapter, parameters.account.currency, parameters.account.acc))
                dpdString = "";
            else
                dpdString = parameters.dpd_r;
            end;
			fTxt.WriteLine("~Date_Last_Nat");
			fTxt.WriteLine(dpdString);
		else
        end;	

		fTxt.WriteLine("~Name_Currency");
        if (  (parameters.account.currency == NATCUR)
            or
              (    (parameters.account.currency != NATCUR)
               and (not parameters.natCurEquivalent)
              )
            or (parameters.account.initialSubsystem == REP_SUBSYSTEMS_LOANS)
           )
            fTxt.WriteLine(parameters.account.currencyName);
        else
            fTxt.WriteLine(parameters.account.currencyName + "\n" + String("Курс ЦБ: ", parameters.rateIn));
        end;

		fTxt.WriteLine("~Date_Last");
		fTxt.WriteLine(dpdString);

		fTxt.WriteLine("~Kind_Account_In");
		fTxt.WriteLine(АП(parameters.account.restIn, parameters.account.kind_account));
		
		fTxt.WriteLine("~Rest_In");
		fTxt.WriteLine(String(abs(parameters.account.restIn):a));

        if (    (parameters.account.currency != NATCUR)
            and parameters.natCurEquivalent
            and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
           )
			fTxt.WriteLine("~Kind_Account_In_Nat");
			fTxt.WriteLine(АП(parameters.account.restIn, parameters.account.kind_account) + " эквивалент");

			fTxt.WriteLine("~Rest_In_Nat");
			fTxt.WriteLine(String(abs(parameters.account.restInEq):a));
        end;
				
    end;

    macro printFooter(parameters, docs)

        if ((parameters.printTotalsByDays) and (isEqClass("TDocumentRecord", docs)))
			fTxt.WriteLine("~Num_Doc");
			fTxt.WriteLine(String(abs(docs.overallDocs):a));

            if (    parameters.natCurEquivalent
                and (parameters.account.currency != NATCUR)
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
				fTxt.WriteLine("~Itog_Debet_Nat");
				fTxt.WriteLine(String(abs(docs.allSumDebitEq):a));

				fTxt.WriteLine("~Itog_Credit_Nat");
				fTxt.WriteLine(String(abs(docs.allSumCreditEq):a));
				
				fTxt.WriteLine("~Itog_Credit");
				fTxt.WriteLine(String(abs(docs.allSumCredit):a));
            else
				fTxt.WriteLine("~Itog_Credit");
				fTxt.WriteLine(String(abs(docs.allSumCredit):a));
            end;
			fTxt.WriteLine("~Itog_Debet");
			fTxt.WriteLine(String(docs.allSumDebit:a));

            if ((docs.getSubsystem() != REP_SUBSYSTEMS_LOANS) and parameters.spodTurnsTotal and docs.dataSet.isSpod)
				fTxt.WriteLine("~SPOD");
				fTxt.WriteLine("В том числе СПОД:");

				fTxt.WriteLine("~SPOD_Itog_Debet");
				fTxt.WriteLine(String(abs(docs.allSumDebitSPOD):a));

                if (    parameters.natCurEquivalent
                    and (parameters.account.currency != NATCUR)
                    and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                    )
						
					fTxt.WriteLine("~SPOD_Itog_Debet_Nat");
					fTxt.WriteLine(String(abs(docs.allSumDebitSpodEq):a));

					fTxt.WriteLine("~SPOD_Itog_Credit");
					fTxt.WriteLine(String(abs(docs.allSumCreditSPOD):a));

					fTxt.WriteLine("~SPOD_Itog_Credit_Nat");
					fTxt.WriteLine(String(abs(docs.allSumCreditSpodEq):a));
                else
					fTxt.WriteLine("~SPOD_Itog_Credit");
					fTxt.WriteLine(String(abs(docs.allSumCreditSPOD):a));
                end;
            end;

			fTxt.WriteLine("~Kind_Account_Out");
			fTxt.WriteLine(" за " + docs.date_carry);
			
			fTxt.WriteLine("~Rest_Out");
			fTxt.WriteLine(String(abs(docs.restTotal):a));

            if (    (parameters.account.currency != NATCUR)
                and parameters.natCurEquivalent
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
				fTxt.WriteLine("~Kind_Account_Out");
				fTxt.WriteLine(" за " + docs.date_carry + " эквивалент");

				fTxt.WriteLine("~Rest_Out_Nat");
				fTxt.WriteLine(String(abs(docs.restTotalEq):a));
            end;
        else 
			fTxt.WriteLine("~Num_Doc");
			fTxt.WriteLine(String(docs.overallDocs));

			fTxt.WriteLine("~Itog_Debet");
			fTxt.WriteLine(String(docs.allSumDebit));

            if (    parameters.natCurEquivalent
                and (parameters.account.currency != NATCUR)
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
				fTxt.WriteLine("~Itog_Debet_Nat");
				fTxt.WriteLine(String(abs(docs.allSumDebitEq):a));

				fTxt.WriteLine("~Itog_Credit_Nat");
				fTxt.WriteLine(String(abs(docs.allSumCreditEq):a));			
				
				fTxt.WriteLine("~Itog_Credit");
				fTxt.WriteLine(String(docs.allSumCredit));
            else
				fTxt.WriteLine("~Itog_Credit");
				fTxt.WriteLine(String(docs.allSumCredit));
            end;

            if ((docs.getSubsystem() != REP_SUBSYSTEMS_LOANS) and parameters.spodTurnsTotal and docs.dataSet.isSpod)
				fTxt.WriteLine("~SPOD");
				fTxt.WriteLine("В том числе СПОД: ");

				fTxt.WriteLine("~SPOD_Itog_Debet");
				fTxt.WriteLine(String(docs.allSumDebitSPOD):a);

                if (    parameters.natCurEquivalent
                    and (parameters.account.currency != NATCUR)
                    and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                    )
					fTxt.WriteLine("~SPOD_Itog_Debet_Nat");
					fTxt.WriteLine(String(abs(docs.allSumDebitSpodEq):a));

					fTxt.WriteLine("~SPOD_Itog_Credit");
					fTxt.WriteLine(String(abs(docs.allSumCreditSPOD):a));

					fTxt.WriteLine("~SPOD_Itog_Credit_Nat");
					fTxt.WriteLine(String(abs(docs.allSumCreditSpodEq):a));
                else
					fTxt.WriteLine("~SPOD_Itog_Credit");
					fTxt.WriteLine(String(abs(docs.allSumCreditSPOD):a));			
                end;
			else
				fTxt.WriteLine("~DeleteRows");
				fTxt.WriteLine("3"); //Номер таблицы
				fTxt.WriteLine("%");
				fTxt.WriteLine("3"); //номер первой удаляемой строки
				fTxt.WriteLine("3"); //номер последней удаляемой строки
            end;

			fTxt.WriteLine("~Kind_Account_Out");
			fTxt.WriteLine(АП(parameters.account.restOut, parameters.account.kind_account));

			fTxt.WriteLine("~Rest_Out");
			fTxt.WriteLine(String(abs(parameters.account.restOut):a));

            if (    (parameters.account.currency != NATCUR)
                and parameters.natCurEquivalent
                and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
                )
				fTxt.WriteLine("~Kind_Account_Out_Nat>");				
				fTxt.WriteLine("Исходящий остаток " + АП(parameters.account.restOutEq, parameters.account.kind_account) + " эквивалент");
				
				fTxt.WriteLine("~Rest_Out_Nat");
				fTxt.WriteLine(String(abs(parameters.account.restOutEq):a));
            end;
        end;

        if (    (parameters.account.currency != NATCUR)
            and parameters.natCurEquivalent
            and (parameters.account.initialSubsystem == REP_SUBSYSTEMS_CB)
           )
			fTxt.WriteLine("~Rate_CB");
			fTxt.WriteLine(String(parameters.rateOut));
		else
			fTxt.WriteLine("~Rate_CB");
			fTxt.WriteLine("");
        end;
		//эмуляция метода close
		fTxt = TStreamDoc(fNameTxtMess, "R", "rsoem ", 1/*unix*/);

    end;

    macro printRow(dataset)
		if (dataset.statementLineNumber > 1)
			fTxt.WriteLine("~MultipleRow");  /*добавляем строку таблицы*/
			fTxt.WriteLine("2");			/*идентификатор таблицы*/      
			fTxt.WriteLine("1");   		  /*необходимое количество строк*/
			//println();      /*закладка для таблицы*/
			//println(row);   /*номер ряда, который необходимо размножить*/
			//println(1);     /*добавить в конец таблицы*/
		end;
	
		fTxt.WriteLine("~Date_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.date_carry);
		
		fTxt.WriteLine("~VO_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.shifr_oper);

		fTxt.WriteLine("~Num_Doc_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.numb_document);
		
		fTxt.WriteLine("~CorrBankAcc_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.corrBankAcc);

		fTxt.WriteLine("~BIC_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.bic);
		
		fTxt.WriteLine("~Account_Partner_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.account);

		fTxt.WriteLine("~Debet_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(String(dataset.sumDebit));

		fTxt.WriteLine("~Credit_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(String(dataset.sumCredit));

		fTxt.WriteLine("~Debet_Nat_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(String(dataset.sumDebitEq));

		fTxt.WriteLine("~Credit_Nat_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(String(dataset.sumCreditEq));

		fTxt.WriteLine("~Ground_" + string(dataset.statementLineNumber));
		fTxt.WriteLine(dataset.ground);
    end;

    macro printEmptyData()
		/*Обнуляем поля*/
		fTxt.WriteLine("~Date_1");
		fTxt.WriteLine(" Данные отсутствуют");
		
		fTxt.WriteLine("~VO_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Num_Doc_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~CorrBankAcc_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~BIC_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Account_Partner_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Debet_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Credit_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Debet_Nat_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Credit_Nat_1");
		fTxt.WriteLine("");
		fTxt.WriteLine("~Ground_1");
		fTxt.WriteLine("");

		fTxt.WriteLine("~MergeCells");           // Объединение ячеек
		fTxt.WriteLine(string(2));               // идентификатор таблицы
		fTxt.WriteLine("%");  
		fTxt.WriteLine("0");                     // очистить строку
		if ( index(StrUpr(p_Templ), "VAL") > 0 )
			fTxt.WriteLine("3, 1");
			fTxt.WriteLine("3, 6");			
		else
			fTxt.WriteLine("2, 1");
			fTxt.WriteLine("2, 5");	
		end;
    end;

    macro printEmptyReport()
		printEmptyData();
    end;

    macro print()
		var RealFileName="";
        var i = 0;
/*		
        while (i < m_enginesPool.size)
            var engine = m_enginesPool.value(i);
            engine.setFlagShowReport(false);
*/			
//            engine.printWinRep();
//            engine.showWinRep();

			//Вызов функции из dlwreps.mac 
			DLWREPS_PrintReportFromTagFile( fNameTxtMess, @RealFileName);//, false/*CloseDoc*/, true/*SaveDocTwice*/ ); 
/*			
            i = i + 1;
        end;
*/
    end;

    private macro constructorTPOIWordView(parameters : Object)
    /**
     *  Конструктор.
     */
        m_isFirstInit = true;
    end;

    constructorTPOIWordView(parameters);
end;

/************************************************************************************************************************/
/*****************************************************  ПЕЧАТЬ  *********************************************************/
/***************************************************  ОКОНЧАНИЕ *********************************************************/
/************************************************************************************************************************/

/************************************************************************************************************************/
/************************************************  ОСНОВНЫЕ СЧЕТА  ******************************************************/
/*****************************************************  НАЧАЛО  *********************************************************/
/************************************************************************************************************************/

/* Инициализация ХП */
private MACRO InitPck(AccInfo)

  SQL_Execute("{ CALL rsb_rep_pt.set_selfid(" + {OurBank} + ") }");

  return WELLDONE;
END;

/* Условие WHERE для выборки основных счетов ГКБО */
private MACRO MakeAccWhere(Currency, AccInfo, isCache)
  var Where = "";

  var accountType = "";

  Where = Where + AccInfo.AccountFilter.GetAsSqlString("ac")+END_LINE;

  Where = Where + " AND " + getChapterFilterSqlClause(AccInfo.Chapter, "ac.t_chapter");

  if (AccInfo.ExcludeOcp == true)
    Where = Where + " AND NOT" + AccInfo.OcpAccountServer.GetAsSqlString("ac")+END_LINE;
  end;

  if (AccInfo.AccountMask != "")
    Where = Where + " AND ("+ConvertMaskToSQLFormat(AccInfo.AccountMask, "ac.t_Account")+")"+END_LINE;
  end;

  if( AccInfo.Oper != 0 )
    Where = Where + " AND ac.t_oper = "+AccInfo.Oper+END_LINE;
  end;

  if (Currency == ALLCURRENCY)
    Where = Where + " AND ac.t_Code_Currency != "+NATCUR+END_LINE;
  elif (Currency != ALLFININSTR)
    Where = Where + " AND ac.t_Code_Currency = "+Currency+END_LINE;
  end;

  Where = Where + " AND ac.t_Open_Date BETWEEN " + getSqlDate(ZeroDate) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE+
                  " AND (   ac.t_Close_Date BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(Date(31,12,9999)) + END_LINE+
                  "     OR ac.t_Close_Date = " + getSqlDate(ZeroDate) + END_LINE+
                  "    )"+END_LINE;

  if (AccInfo.Client > 0)
    if (AccInfo.isIncludeClient)
    Where = Where + " AND ac.t_Client = "+AccInfo.Client+END_LINE;
    else
        Where = Where + " AND ac.t_Client != "+AccInfo.Client+END_LINE;
    end;
  end;

  Where = Where + " AND ab.t_accountId = ac.t_accountId"+END_LINE;
  Where = Where + " AND "+AccInfo.accountFilterMaskBalance("ac")+END_LINE;

  if ((not AccInfo.cSetZTurn) and (not isCache))
    if (Currency == NATCUR)
      Where = Where + " AND EXISTS(SELECT /*+ NO_UNNEST*/1" + END_LINE +
                      "             FROM drestdate_dbt restDate" + END_LINE +
                      "            WHERE restDate.t_accountId = ac.t_accountId" + END_LINE +
                      "              AND restDate.t_restCurrency = ac.t_code_currency" + END_LINE +
                      "              AND restDate.t_restDate BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                      "          )" + END_LINE
                      ;
    else
      if (AccInfo.NatCurEquivalent == true) /* Должны быть док. по счету или переоценка */
        if (AccInfo.cSetExcludeRevalue == true) /* Переоценка не считается документом по валютному счету */
          Where = Where + " AND EXISTS(SELECT 1" + END_LINE +
                          "             FROM daccTrn_dbt" + END_LINE +
                          "            WHERE ac.t_accountId = t_accountId_payer" + END_LINE +
                          "              AND ac.t_code_currency <> " + NATCUR + END_LINE +
                          "              AND t_state = 1" + END_LINE +
                          "              AND t_date_carry BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                          "              AND t_result_carry NOT IN (" + DELTARATE
                                                                + "," + REGRESTOUTCARRY
                                                                + "," + DELTARATE_ADD
                                                                + ")"                         + END_LINE +
                          "           UNION ALL" + END_LINE +
                          "           SELECT 1" + END_LINE +
                          "             FROM daccTrn_dbt" + END_LINE +
                          "            WHERE ac.t_accountId = t_accountId_receiver" + END_LINE +
                          "              AND ac.t_code_currency <> " + NATCUR + END_LINE +
                          "              AND t_state = 1" + END_LINE +
                          "              AND t_date_carry BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                          "              AND t_result_carry NOT IN (" + DELTARATE
                                                                + "," + REGRESTOUTCARRY
                                                                + "," + DELTARATE_ADD
                                                                + ")"                         + END_LINE +
                          "           UNION ALL" + END_LINE +
                          "           SELECT /*+ NO_UNNEST*/1" + END_LINE +
                          "             FROM drestdate_dbt restDate" + END_LINE +
                          "            WHERE restDate.t_accountId = ac.t_accountId" + END_LINE +
                          "              AND restDate.t_restCurrency = ac.t_code_currency" + END_LINE +
                          "              AND ac.t_code_currency = " + NATCUR + END_LINE +
                          "              AND restDate.t_restDate BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                          "          )" + END_LINE
                          ;
        else /* Переоценка считается документом по валютному счету */
          Where = Where + " AND EXISTS(SELECT /*+ NO_UNNEST*/1" + END_LINE +
                          "             FROM drestdate_dbt restDate" + END_LINE +
                          "            WHERE restDate.t_accountId = ac.t_accountId" + END_LINE +
                          "              AND restDate.t_restCurrency = ac.t_code_currency" + END_LINE +
                          "              AND restDate.t_restDate BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                          "          )" + END_LINE
                          ;
        end;
      else /* должны быть док. по счету, кроме КР и переоценки */
        Where = Where + " AND EXISTS(SELECT 1" + END_LINE +
                        "             FROM daccTrn_dbt" + END_LINE +
                        "            WHERE ac.t_accountId = t_accountId_payer" + END_LINE +
                        "              AND ac.t_code_currency <> " + NATCUR + END_LINE +
                        "              AND t_state = 1" + END_LINE +
                        "              AND t_date_carry BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                        "              AND t_result_carry NOT IN (" + DELTARATE
                                                              + "," + REGRESTOUTCARRY
                                                              + "," + DELTARATE_ADD
                                                              + "," + DELTARATE_MCD
                                                              + ")"                           + END_LINE +
                        "           UNION ALL" + END_LINE +
                        "           SELECT 1" + END_LINE +
                        "             FROM daccTrn_dbt" + END_LINE +
                        "            WHERE ac.t_accountId = t_accountId_receiver" + END_LINE +
                        "              AND ac.t_code_currency <> " + NATCUR + END_LINE +
                        "              AND t_state = 1" + END_LINE +
                        "              AND t_date_carry BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                        "              AND t_result_carry NOT IN (" + DELTARATE
                                                              + "," + REGRESTOUTCARRY
                                                              + "," + DELTARATE_ADD
                                                              + "," + DELTARATE_MCD
                                                              + ")"                           + END_LINE +
                        "           UNION ALL" + END_LINE +
                        "           SELECT /*+ NO_UNNEST*/1" + END_LINE +
                        "             FROM drestdate_dbt restDate" + END_LINE +
                        "            WHERE restDate.t_accountId = ac.t_accountId" + END_LINE +
                        "              AND restDate.t_restCurrency = ac.t_code_currency" + END_LINE +
                        "              AND ac.t_code_currency = " + NATCUR + END_LINE +
                        "              AND restDate.t_restDate BETWEEN " + getSqlDate(AccInfo.DateIn) + " AND " + getSqlDate(AccInfo.DateOut) + END_LINE +
                        "          )" + END_LINE
                        ;
      end;
    end;
  end;

  if (AccInfo.AccountType != "")
      Where = Where + " AND " + getPatternFilterClause("ac.t_type_Account", AccInfo.AccountType, AccInfo.typeMethod, AccInfo.isIncludeType) + END_LINE;
   end;

  if (AccInfo.AccountUserType != "")
      Where = Where + " AND " + getPatternFilterClause("ac.t_UserTypeAccount", AccInfo.AccountUserType, AccInfo.userTypeMethod, AccInfo.isIncludeUserType) + END_LINE;
  end;

  if (AccInfo.cSetCr)
    accountType = "К";
  end;

  if (AccInfo.excludeInternetAccount)

    var isContinuation = " CASE"
                + "\n" + "     WHEN EXISTS (SELECT NULL"
                + "\n" + "                    FROM DOBJATCOR_dbt atr"
                + "\n" + "                   WHERE atr.t_object = TO_CHAR(ac.t_chapter, 'FM00') || TO_CHAR(ac.t_code_currency, 'FM0000000') || ac.t_account"
                + "\n" + "                     AND atr.t_objectType = category.t_objectType"
                + "\n" + "                     AND atr.t_groupId = category.t_id"
                + "\n" + "                     AND atr.t_validToDate = DECODE (category.t_validFromDate, TO_DATE ('01.01.0001', 'DD.MM.YYYY'),"
                + "\n" + "                                                     category.t_validFromDate, category.t_validFromDate - 1)"
                + "\n" + "                 )"
                + "\n" + "     THEN 1"
                + "\n" + "     ELSE 0"
                + "\n" + " END";

    var hasContinuation = " CASE"
                + "\n" + "     WHEN EXISTS (SELECT NULL"
                + "\n" + "                    FROM DOBJATCOR_dbt atr"
                + "\n" + "                   WHERE atr.T_OBJECT = TO_CHAR(ac.t_chapter, 'FM00') || TO_CHAR(ac.t_code_currency, 'FM0000000') || ac.t_account"
                + "\n" + "                     AND atr.t_objectType = category.t_objectType"
                + "\n" + "                     AND atr.t_groupId = category.t_id"
                + "\n" + "                     AND atr.t_validFromDate = DECODE (category.t_validToDate, TO_DATE ('31.12.9999', 'DD.MM.YYYY'),"
                + "\n" + "                                                       category.t_validToDate, category.t_validToDate + 1)"
                + "\n" + "                 )"
                + "\n" + "     THEN 1"
                + "\n" + "     ELSE 0"
                + "\n" + " END";

    Where = Where + " AND (   EXISTS (SELECT NULL"
           + "\n" + "                  FROM DREPCATEGORY_VW category"
           + "\n" + "                 WHERE category.t_objectId = TO_CHAR(ac.t_chapter, 'FM00') || TO_CHAR(ac.t_code_currency, 'FM0000000') || ac.t_account"
           + "\n" + "                   AND category.t_value = 'И'"
           + "\n" + "                   AND category.t_id = 14"
           + "\n" + "                   AND category.t_objectType = " + OBJTYPE_ACCOUNT
           + "\n" + "                   AND category.t_isInstalledOverPeriod = 1"
           + "\n" + "                   AND (   (    category.t_isInstalledForBeginDate = 1"
           + "\n" + "                            AND category.t_isInstalledForEndDate = 0"
           + "\n" + "                            AND " + hasContinuation + " = 0"
           + "\n" + "                           )"
           + "\n" + "                         OR (    category.t_isInstalledForBeginDate = 0"
           + "\n" + "                             AND category.t_isInstalledForEndDate = 1"
           + "\n" + "                             AND " + isContinuation + " = 0"
           + "\n" + "                            )"
           + "\n" + "                         OR (    category.t_isInstalledForBeginDate = 0"
           + "\n" + "                             AND category.t_isInstalledForEndDate = 0"
           + "\n" + "                             AND (    " + hasContinuation + " = 0"
           + "\n" + "                                   OR " + isContinuation + " = 0"
           + "\n" + "                                 )"
           + "\n" + "                            )"
           + "\n" + "                        )"
           + "\n" + "                 )"
           + "\n" + "     OR NOT EXISTS (SELECT NULL"
           + "\n" + "                      FROM DREPCATEGORY_VW category"
           + "\n" + "                     WHERE category.t_objectId = TO_CHAR(ac.t_chapter, 'FM00') || TO_CHAR(ac.t_code_currency, 'FM0000000') || ac.t_account"
           + "\n" + "                       AND category.t_value = 'И'"
           + "\n" + "                       AND category.t_id = 14"
           + "\n" + "                       AND category.t_objectType = " + OBJTYPE_ACCOUNT
           + "\n" + "                       AND category.t_isInstalledOverPeriod = 1"
           + "\n" + "                   )"
           + "\n" + "    )" + END_LINE;
  end;

  Where = Where + END_LINE;

  return Where;
END;

/* ORDER BY для выборки основных счетов */
private macro MakeAcOrderBy(AccInfo)
  var orderBy = "";

  for (var i, accInfo.kindSort)

    if (i == REP_PATTERNSORT_DEPARTMENT)
      orderBy = orderBy + "," + "t_department, t_branch ";
    elif (i == REP_PATTERNSORT_OPER)
      orderBy = orderBy + "," + "t_oper ";
    elif (i == REP_PATTERNSORT_CLIENT)
      orderBy = orderBy + "," + "t_nameClnt ";
    elif (i == REP_PATTERNSORT_ACCOUNT)
      orderBy = orderBy + "," + "t_acc ";
    elif (i == REP_PATTERNSORT_CHAPTER)
      orderBy = orderBy + "," + "t_chapter ";
    elif (i == REP_PATTERNSORT_SYSTEM_MAKET)
      orderBy = orderBy + "," + "t_sort ";
    end;

  end;

  if (orderBy != "")
    orderBy = "ORDER BY " + substr(orderBy, 2);
  end;

  return orderBy;
end;

/* Формирование SQL-запроса для отбора основных счетов ГКБО */
private MACRO MakeAccountQuery(Currency, AccInfo, requiredDate, isCache)

  macro makeRest(restDate, isEquivalent, needEquivalent)
    var restCurrencyClause;
    var queryText;

    if (isEquivalent and not needEquivalent)
        queryText = "0";
    else
        if (isEquivalent)
            restCurrencyClause = "t_restCurrency = 0";
        else
            restCurrencyClause = "t_restCurrency = ac.t_code_currency";
        end;

    queryText = "NVL((SELECT t_rest"
       + "\n" + "       FROM drestdate_dbt rest"
       + "\n" + "      WHERE rest.t_accountId = ac.t_accountId"
       + "\n" + "        AND rest." + restCurrencyClause
       + "\n" + "        AND rest.t_restDate = (SELECT MAX(maxDate.t_restDate)"
       + "\n" + "                                 FROM drestdate_dbt maxDate"
       + "\n" + "                                WHERE maxDate.t_accountId = ac.t_accountId"
       + "\n" + "                                  AND maxDate." + restCurrencyClause
       + "\n" + "                                  AND maxDate.t_restDate <= " + getSqlDate(restDate)
       + "\n" + "                              )"
       + "\n" + "    ), 0"
       + "\n" + "   )"
       ;
    end;

    return queryText;
  end;

  defaultParm(isCache, false);

  var Select;
  var From;
  var Where = "WHERE ", CmdText = "";
  var requiredDateString;

  if ((valType(requiredDate) != V_UNDEF) and ((requiredDate != NULL) or (requiredDate != date(0, 0, 0))))
       requiredDateString = ", " + string(getSqlDate(requiredDate));
  else
       requiredDateString = "";
  end;

  if (isCache)

      Select = "SELECT ac.t_accountId        accId,"
      + "\n" + "       ac.t_Account          AccountNumber,"
      + "\n" + "       ac.t_chapter          chapterNumber,"
      + "\n" + "       CHR(1)                connAcc,"
      + "\n" + "       0                     connChapter,"
      + "\n" + "       ac.t_code_currency    curCode,"
      + "\n" + "   " + REP_SUBSYSTEMS_CB + " subsystem";

  else
      Select = "SELECT ac.t_accountId                                                                   AccId,"
      + "\n" + "       ac.t_Account                                                                     AccountNumber,"
      + "\n" + "       ac.t_NameAccount                                                                 AccountName,"
      + "\n" + "       ac.t_Account                                                                     OdbAccount,"
      + "\n" + "       NVL((SELECT CASE"
      + "\n" + "                       WHEN 1 = " + b2i(accInfo.printFullNameClient)
      + "\n" + "                       THEN party.t_name"
      + "\n" + "                       ELSE party.t_shortname"
      + "\n" + "                   END"
      + "\n" + "              FROM dparty_dbt party"
      + "\n" + "             WHERE party.t_partyId = ac.t_client"
      + "\n" + "           ), CHR(1)"
      + "\n" + "          )                                                                             ClientName,"
      + "\n" + "       rsb_rep_pt.get_PartyInn(ac.t_Client, 1" + requiredDateString + ")                ClientInn,"
      + "\n" + "       NVL((SELECT person.t_name"
      + "\n" + "              FROM dperson_dbt person"
      + "\n" + "             WHERE person.t_oper = ac.t_oper"
      + "\n" + "           ), CHR(1)"
      + "\n" + "          )                                                                             OperName,"
      + "\n" + "       rsb_rep_ac.prevdatin(ac.t_accountId, ac.t_code_currency, 1)                      Dpd_R,"
      + "\n" + "       rsb_rep_ac.prevdatin(ac.t_accountId, ac.t_code_currency, 0)                      Dpd,"
      + "\n" + "       NVL((SELECT fi.t_name"
      + "\n" + "              FROM dfininstr_dbt fi"
      + "\n" + "             WHERE fi.t_fiId = ac.t_code_currency"
      + "\n" + "           ), CHR(1)"
      + "\n" + "          )                                                                             CurrencyName,"
      + "\n" + "   " + makeRest(accInfo.dateIn-1, false, accInfo.natCurEquivalent) + "                  AccountInRest,"
      + "\n" + "   " + makeRest(accInfo.dateIn-1,  true, accInfo.natCurEquivalent) + "                  AccountInRestEq,"
      + "\n" + "   " + makeRest(accInfo.dateOut,  false, accInfo.natCurEquivalent) + "                  AccountOutRest,"
      + "\n" + "   " + makeRest(accInfo.dateOut,   true, accInfo.natCurEquivalent) + "                  AccountOutRestEq,"
      + "\n" + "       ac.t_Type_Account                                                                TypeAccount,"
      + "\n" + "       ac.t_Chapter                                                                     ChapterNumber,"
      + "\n" + "       ac.t_Kind_Account                                                                KindAccount,"
      + "\n" + "       ac.t_Sort                                                                        Sort,"
      + "\n" + "       ac.t_Branch                                                                      Branch,"
      + "\n" + "       CHR(1)                                                                           connAcc,"
      + "\n" + "       0                                                                                connChapter,"
      + "\n" + "       ac.t_Department                                                                  Department,"
      + "\n" + "       ac.t_Code_Currency                                                               CurCode,"
      + "\n" + "   " + REP_SUBSYSTEMS_CB + "                                                            subsystem,"
      + "\n" + "   " + REP_SUBSYSTEMS_CB + "                                                            initialSubsystem,"
      + "\n" + "       NULL                                                                             RevalDocsExist,  " // 12.11.2007 ABP deprecated
      + "\n" + "       0 t_credOperID_Ref,"
      + "\n" + "       CHR(1) t_numExternDoc,"
      + "\n" + "       0 t_stepID,"
      + "\n" + "       0 t_creditNumber_Ref,"
      + "\n" + "       0 t_outdebcur,"
      + "\n" + "       0 t_outcredcur,"
      + "\n" + "       0 t_carrySum,"
      + "\n" + "       0 t_credCarrySum,"
      + "\n" + "       NULL t_regDate,"
      + "\n" + "       NULL t_carryDate,"
      + "\n" + "       CHR(1) t_outDebAccount,"
      + "\n" + "       CHR(1) t_debit,"
      + "\n" + "       CHR(1) t_outCredAccount,"
      + "\n" + "       CHR(1) t_credit,"
      + "\n" + "       0 t_curCode,"
      + "\n" + "       CHR(1) t_typeDocument,"
      + "\n" + "       CHR(1) t_ground";


  end;

  From = "\n" + "FROM daccount_dbt ac,"
       + "\n" + "     daccblnc_dbt ab"
       + "\n" + "";

  Where = Where + MakeAccWhere(Currency, AccInfo, isCache);

  CmdText = Select + From + Where;
  return CmdText;

END;

private macro makeAccountQueryText(accInfo, requiredDate, isCache)

    return makeAccountQuery(accInfo.currency, accInfo, requiredDate, isCache);

end;

/************************************************************************************************************************/
/************************************************  ОСНОВНЫЕ СЧЕТА  ******************************************************/
/**************************************************  ОКОНЧАНИЕ  *********************************************************/
/************************************************************************************************************************/

/* Печать приложений */
private MACRO processFDocs(AccInfo, Docs) : TArray

  macro FillDocumentBufferByLoansData(DocumentBuffer, DocStorage)

    if (DocStorage[DS_DOC] != 0)
      DocumentBuffer.rec.AccTrnId = DocStorage[DS_DOC];
      DocumentBuffer.GetEQ();
    else
      Copy(DocumentBuffer, DocStorage[DS_CBDOC]);
    end;

    return DocumentBuffer;
  end;

  macro FilterDocs(DocStorage, OutForm)
    return ((DocStorage[DS_DK] == OutForm) or (OutForm == ACC_FORMS_ALL));
  end;

  macro GetPrnForm(DocStorage)
    if (DocStorage[DS_PAYM] != 0)
//timeCounter13.start();
      var paymentsUtility = TPaymentsUtility(accInfo);
      accInfo.pr_pmpaym = paymentsUtility.getPaymentRecord(docStorage[DS_PAYM]);
      accInfo.pr_debit = paymentsUtility.getPaymentDebitPropertiesRecord(docStorage[DS_PAYM]);
      accInfo.pr_credit = paymentsUtility.getPaymentCreditPropertiesRecord(docStorage[DS_PAYM]);
      accInfo.pr_pmrmprop = paymentsUtility.getPaymentRPropertiesRecord(docStorage[DS_PAYM]);
      accInfo.pr_multydoc = paymentsUtility.getMultydocRecord(docStorage[DS_PAYM]);
//timeCounter13.stop();

      // Входящие валютные платежи печатаются в форме сообщения SWIFT, независимо от шифра операции
      if (in(accInfo.pr_pmpaym.rec.docKind, WL_INDOC, WL_WIPM) and (accInfo.pr_pmpaym.rec.fiId != NATCUR))
        return PRN_CURPAYM;
      end;

      // Банковский ордер печатаем в форме банковского ордера по 2360-У
      if (nvl(accInfo.pr_pmrmprop.rec.shifrOper, "") == "17")
        return PRN_RURPAYM;
      end;

      // Мультивалютные документы печатаются в форме мемордера по 2161-У для мультивалютного документа.
      // Те платежи, для которых не нашлось соответствующей первички, будут обработаны макросом печати платежа в РБ по шифру операции.
      // Если же и шифр операции среди поддерживаемых не обнаружен, то будет напечан мемордер по 2161-У
      if ((accInfo.pr_pmpaym.rec.docKind == CB_MULTYDOC) and (accInfo.pr_multydoc.rec.autokey != 0))
        return PRN_MDOC;
      end;

      // Мемордер или документ ПЗО печатается в форме мемордера по 2161-У
      if ((accInfo.pr_pmpaym.rec.docKind == DLDOC_MEMORIALORDER) or (nvl(accInfo.pr_pmrmprop.rec.shifrOper, "") == ""))
        if (docStorage[DS_CUR] == NATCUR)
          return PRN_RURMO;
        else
          return PRN_CURMO;
        end;
      end;

      // Первички, которые умеет печатать prpmpaym.mac ему и отдадим
      if (arrFind(DocKinds, nvl(accInfo.pr_pmrmprop.rec.ShifrOper, "")) != -1)
        return PRN_RURPAYM;
      end;

      // Все остальное печатаем в форме мемордера по 2161-У
      if (docStorage[DS_CUR] == NATCUR)
        return PRN_RURMO;
      else
        return PRN_CURMO;
      end;
    end;

    // Мультивалютные проводки печатаем в форме мемордера по 2161-У
    if (docStorage[DS_CARRY] != 0)
      return PRN_RURMO;
    end;

    // Проводки, не привязанные к платежу, печатаем в форме мемордера по 2161-У
    if (docStorage[DS_CUR] == NATCUR)
      return PRN_RURMO;
    else
      return PRN_CURMO;
    end;
  end;

  var stringalizedFD = TArray();

  var i = 0;
  while(i < Docs.DocStorage.Size)
    var fltDocs = FilterDocs(Docs.DocStorage[i], AccInfo.OutForm);
    if (fltDocs == false)
      i = i+1;
      continue;
    end;

    var fdoutput : TOFStream = TOFStream("fdoutput");
    fdoutput.setOutputFile();

    var printForm = GetPrnForm(Docs.DocStorage[i]);
    if (printForm == PRN_RURPAYM)

      ExecMacroFile("pr_accst_rpm", "PrintDoc", 1, AccInfo.pr_pmpaym, AccInfo.pr_debit, AccInfo.pr_credit, AccInfo.pr_pmrmprop);

    elif (printForm == PRN_CURPAYM)

      ExecMacroFile("pr_accst_cpm", "PrintDoc", 1, AccInfo.pr_pmpaym, AccInfo.pr_debit, AccInfo.pr_credit, AccInfo.pr_pmrmprop);

    elif (printForm == PRN_RURMO)

      AccInfo.pr_documentr = FillDocumentBufferByLoansData(AccInfo.pr_documentr, Docs.DocStorage[i]);
      ExecMacroFile("pr_accst_rmo", "PrintDoc", 1, AccInfo.pr_documentr);

    elif (printForm == PRN_CURMO)

      AccInfo.pr_documentc = FillDocumentBufferByLoansData(AccInfo.pr_documentc, Docs.DocStorage[i]);
      ExecMacroFile("pr_accst_cmo", "PrintDoc", 1, AccInfo.pr_documentc);

    elif (printForm == PRN_MDOC)

      ExecMacroFile("pr_accst_mcd", "PrintDoc", 1, accInfo.pr_multydoc, accInfo.pr_pmpaym, accInfo.pr_debit, accInfo.pr_credit, accInfo.pr_pmrmprop);

    end;

    fdoutput.resetOutputFile();

    var j = stringalizedFD.size;
    stringalizedFD.value(j) = TArray();
    var fdout = TStreamDoc(fdoutput.getFileName(), "R");
    var str = "";
    while (fdout.readLine(str))
        stringalizedFD.value(j).value(stringalizedFD.value(j).size) = str;
    end;

    i = i+1;
  end;

  return stringalizedFD;
end;

/* Обработка документов счета */
private MACRO ProcessDocuments(accInfo, docs, viewPool, excelPrintController, isFirstExcelPrint)
    var query;
    var viewIterator;
    var needFooter = false;
    var isContinue = false;
    var fdocs;
    var carryDocument = TDocumentRecord();
    var i;
    if (accInfo.account.subsystem == REP_SUBSYSTEMS_CB)
      query = TDocumentQueryMaker(accInfo).getQuery();
    else
      query = ""; /* данные нужно собирать по документам RS-Loans */
    end;
    docs.constructor(query, accInfo);

    isContinue = docs.next();
    if (isContinue or accInfo.cSetZTurn)
        needFooter = true;
        i = 0;
        while (i < viewPool.size)
            viewPool.value(i).initialize(accInfo);
            viewPool.value(i).printHeader(accInfo);
            i = i + 1;
        end;
        if(excelPrintController)
            var bankName = "";
            for (i, m_cachedBankHeader.get(accInfo.dprtId, accInfo.orgStructure, 10000).header)
                bankName = bankName + i + "\n";
            end;
            excelPrintController.printHeader(accInfo, bankName, isFirstExcelPrint);
        end;
    end;

    if ((not isContinue) and (needFooter))
        i = 0;
        while (i < viewPool.size)
            viewPool.value(i).printEmptyData();
            i = i + 1;
        end;
        if(excelPrintController)
            excelPrintController.printEmptyData();
        end;
    end;

    var totalsByDay = TDocumentRecord();
        totalsByDay.date_carry = String(docs.dataSet.date_carry : f);
        totalsByDay.subsystem  = docs.getSubsystem();
        totalsByDay.initialize();

    while (isContinue)
        NDoc = NDoc+1;
        i = 0;

        // Заполнение carryDocument. Необходимо делать именно тут, поскольку
        // при печати ТОЛЬКО в Excel viewPool пуст.
        accInfo.statementLineNumber = accInfo.statementLineNumber + 1;
        carryDocument.note =                docs.docT;
        carryDocument.statementLineNumber = accInfo.statementLineNumber;
        carryDocument.date_carry          = String(docs.dataSet.date_carry : f);
        carryDocument.shifr_oper          = docs.shifr_oper;
        carryDocument.numb_document       = docs.numb_document;
        carryDocument.bic                 = docs.Bic;
        carryDocument.ground              = StrSubst(StrSubst(docs.Ground(), StrFor(10), " "), StrFor(13), " ");
        carryDocument.corrBankAcc         = docs.corrBankAcc;
        carryDocument.account             = docs.account;
        carryDocument.sumDebit            = docs.sumDebit;
        carryDocument.sumCredit           = docs.sumCredit;
        carryDocument.sumDebitEq          = docs.sumDebitEq;
        carryDocument.sumCreditEq         = docs.sumCreditEq;

        while (i < viewPool.size)
            if (accInfo.printTotalsByDays)
                if (totalsByDay.date_carry != carryDocument.date_carry)
                    viewPool.value(i).printFooter(accInfo, totalsByDay);
                end;
            end;

            viewPool.value(i).printRow(carryDocument);

            i = i + 1;
        end;

        if(excelPrintController)
            if (accInfo.printTotalsByDays)
                if (totalsByDay.date_carry != carryDocument.date_carry)
                    excelPrintController.printFooter(accInfo, totalsByDay);
                end;
            end;

            excelPrintController.printRow(carryDocument);
        end;

        if (accInfo.printTotalsByDays)
            totalsByDay.restTotal   = accInfo.getOutRestForDay(carryDocument.date_carry, false);
            totalsByDay.restTotalEq = accInfo.getOutRestForDay(carryDocument.date_carry, true);
            if (totalsByDay.date_carry != carryDocument.date_carry)
                totalsByDay.date_carry     = carryDocument.date_carry;
                totalsByDay.subsystem      = docs.getSubsystem();
                totalsByDay.allSumDebit    = docs.sumDebit;
                totalsByDay.allSumCredit   = docs.sumCredit;
                totalsByDay.allSumDebitEq  = docs.sumDebitEq;
                totalsByDay.allSumCreditEq = docs.sumCreditEq;
            else
                totalsByDay.overallDocs    = docs.overallDocs;
                totalsByDay.allSumDebit    = totalsByDay.allSumDebit    + docs.sumDebit;
                totalsByDay.allSumCredit   = totalsByDay.allSumCredit   + docs.sumCredit;
                totalsByDay.allSumDebitEq  = totalsByDay.allSumDebitEq  + docs.sumDebitEq;
                totalsByDay.allSumCreditEq = totalsByDay.allSumCreditEq + docs.sumCreditEq;
            end;
            totalsByDay.allSumDebitSpod    = docs.allSumDebitSpod;
            totalsByDay.allSumCreditSpod   = docs.allSumCreditSpod;
            totalsByDay.allSumDebitSpodEq  = docs.allSumDebitSpodEq;
            totalsByDay.allSumCreditSpodEq = docs.allSumCreditSpodEq;
        end;

        isContinue = docs.next();
    end;

    if (needFooter)
        if(excelPrintController)
            if (accInfo.printTotalsByDays)
                excelPrintController.printFooter(accInfo, totalsByDay);
            else
                excelPrintController.printFooter(accInfo, docs);
            end;
        end;

        i = 0;
        while (i < viewPool.size)
            if (accInfo.printTotalsByDays)
                viewPool.value(i).printFooter(accInfo, totalsByDay);
            else
                viewPool.value(i).printFooter(accInfo, docs);
                if (accInfo.outForm != ACC_FORMS_NOTHING)
                    fdocs = processFDocs(accInfo, docs);
                    var j = 0;
                    while (j < fdocs.size)
                        viewPool.value(i).printFD(fdocs.value(j));
                        j = j + 1;
                    end;
                end;
            end;
            i = i + 1;
        end;
    end;

END;

private macro insertCbAccount(accountQueryText, cacheAccountQueryText)

    var commandText =        "CREATE OR REPLACE VIEW " + cbAccountViewName
                    + "\n" + "("
                    + "\n" + " t_accId,"
                    + "\n" + " t_acc,"
                    + "\n" + " t_accName,"
                    + "\n" + " t_odbAccount,"
                    + "\n" + " t_nameClnt,"
                    + "\n" + " t_inn,"
                    + "\n" + " t_oper,"
                    + "\n" + " t_dpd_r,"
                    + "\n" + " t_dpd,"
                    + "\n" + " t_currencyName,"
                    + "\n" + " t_restIn,"
                    + "\n" + " t_restInEq,"
                    + "\n" + " t_restOut,"
                    + "\n" + " t_restOutEq,"
                    + "\n" + " t_typeAccount,"
                    + "\n" + " t_chapter,"
                    + "\n" + " t_kind_account,"
                    + "\n" + " t_sort,"
                    + "\n" + " t_branch,"
                    + "\n" + " t_connAcc,"
                    + "\n" + " t_connChapter,"
                    + "\n" + " t_department,"
                    + "\n" + " t_currency,"
                    + "\n" + " t_subsystem,"
                    + "\n" + " t_initialSubsystem,"
                    + "\n" + " t_revalDocsExist,"

                    + "\n" + " t_credOperID_Ref,"
                    + "\n" + " t_numExternDoc,"
                    + "\n" + " t_stepID,"
                    + "\n" + " t_creditNumber_Ref,"
                    + "\n" + " t_outdebcur,"
                    + "\n" + " t_outcredcur,"
                    + "\n" + " t_carrySum,"
                    + "\n" + " t_credCarrySum,"
                    + "\n" + " t_regDate,"
                    + "\n" + " t_carryDate,"
                    + "\n" + " t_outDebAccount,"
                    + "\n" + " t_debit,"
                    + "\n" + " t_outCredAccount,"
                    + "\n" + " t_credit,"
                    + "\n" + " t_curCode,"
                    + "\n" + " t_typeDocument,"
                    + "\n" + " t_ground"
                    + "\n" + ")"
                    + "\n" + "AS"
                    + "\n" + accountQueryText;

    sql_execute(commandText);

    commandText =        "CREATE OR REPLACE VIEW " + cbCacheAccountViewName
                + "\n" + "("
                + "\n" + " t_accId,"
                + "\n" + " t_acc,"
                + "\n" + " t_chapter,"
                + "\n" + " t_connAcc,"
                + "\n" + " t_connChapter,"
                + "\n" + " t_currency,"
                + "\n" + " t_subsystem"
                + "\n" + ")"
                + "\n" + "AS"
                + "\n" + cacheAccountQueryText;

    sql_execute(commandText);

end;

private macro setCbAccountsViewName()
    var viewNumberDataSource = TRsbDataset("SELECT ddocinfo_rec_seq.nextval t_number FROM dual");

    viewNumberDataSource.setFieldType("t_number", V_INTEGER);

    if (viewNumberDataSource.next())
        cbAccountViewName = "drepaccstaccount_" + viewNumberDataSource.number;
    else
        exit(1);
    end;
end;

private macro setCacheCbAccountsViewName()
    var viewNumberDataSource = TRsbDataset("SELECT ddocinfo_rec_seq.nextval t_number FROM dual");

    viewNumberDataSource.setFieldType("t_number", V_INTEGER);

    if (viewNumberDataSource.next())
        cbCacheAccountViewName = "drepaccstaccount_" + viewNumberDataSource.number;
    else
        exit(1);
    end;
end;

class TPrimaryDocsLoans(plainTextView)
    private var m_plainTextView = plainTextView;

    private macro getData()
          var queryText = "SELECT T_CREDOPERID_REF,"
         + "\n" + "               T_CREDITNUMBER_REF,"
         + "\n" + "               T_NUMEXTERNDOC,"
         + "\n" + "               T_STEPID,"
         + "\n" + "               T_OUTDEBCUR,"
         + "\n" + "               T_OUTCREDCUR,"
         + "\n" + "               T_CARRYSUM,"
         + "\n" + "               T_CREDCARRYSUM,"
         + "\n" + "               T_OUTDEBACCOUNT,"
         + "\n" + "               T_DEBIT,"
         + "\n" + "               T_OUTCREDACCOUNT,"
         + "\n" + "               T_CREDIT,"
         + "\n" + "               T_CURCODE,"
         + "\n" + "               T_REGDATE,"
         + "\n" + "               T_CARRYDATE,"
         + "\n" + "               T_TYPEDOCUMENT,"
         + "\n" + "               T_GROUND,"
         + "\n" + "               T_CHAPTER,"
         + "\n" + "               T_DEPARTMENT"
         + "\n" + "  FROM drepaccst_tmp repacc"
         + "\n" + " WHERE repacc.T_CREDITNUMBER_REF > 0"
         + "\n" + " ORDER BY repacc.T_CREDITNUMBER_REF";

      var dataset = TRsbDataset(queryText);

      dataset.setFieldType("T_REGDATE",  V_DATE);
      dataset.setFieldType("T_CARRYDATE",  V_DATE);
      dataset.setFieldType("T_CARRYSUM",    V_MONEY);
      dataset.setFieldType("T_CREDCARRYSUM",   V_MONEY);

      return dataset;
    end;

    macro print()
        var dataset = getData();
        record doc ("doc_acc.dbt", "loans.def");
        var chapterCmdText, chapterQuery, chapterDataSet;

        while(dataSet.moveNext)
            doc.CredOperID_Ref = dataSet.t_CredOperID_Ref;
            doc.NumExternDoc = dataSet.t_NumExternDoc;
            doc.StepID = dataSet.t_StepID;
            doc.CreditNumber_Ref = dataSet.t_CreditNumber_Ref;
            doc.Outdebcur = dataSet.t_Outdebcur;
            doc.Outcredcur = dataSet.t_Outcredcur;
            doc.CarrySum = dataSet.t_CarrySum;
            doc.CredCarrySum = dataSet.t_CredCarrySum;
            doc.RegDate = dataSet.t_RegDate;
            doc.CarryDate = dataSet.t_CarryDate;
            doc.OutDebAccount = dataSet.t_OutDebAccount;
            doc.debit = dataSet.t_debit;
            doc.OutCredAccount = dataSet.t_OutCredAccount;
            doc.credit = dataSet.t_credit;

            chapterCmdText = "SELECT chapter.t_symbol"
                    + "\n" + "  FROM dobchaptr_dbt chapter"
                    + "\n" + " WHERE chapter.t_chapter = ?";

            chapterQuery = RsdCommand(rslDefCon, chapterCmdText);
            chapterQuery.addParam("chapterId", RSDBP_IN, dataSet.t_Chapter);
            chapterDataSet = TRsbDataSet(chapterQuery);
            if (chapterDataSet.moveNext())
                doc.Chapter = chapterDataSet.t_symbol;
            end;

            doc.CurCode = dataSet.t_CurCode;
            doc.Department = dataSet.t_Department;
            doc.TypeDocument = dataSet.t_TypeDocument;
            doc.Ground = dataSet.t_Ground;

            execMacroFile("mo_203.mac", "ПечатьМемориальногоОрдера", doc, null, "A", m_plainTextView.getFileName());
        end;
    end;
end;

private macro GetUsePoiMode():bool
   var usePoiMode:bool = false;
   var StrErr:string = "";
   var RegKey:string = "BANK_INI\\WINDOWS REPORT\\ИСПОЛЬЗОВАТЬ APACHE POI";
  
   if( NOT GetRegistryValue(RegKey, V_UNDEF, usePoiMode, StrErr) )
       MsgBox("В реестре не найден ключ ["+RegKey+"]!|", StrErr);
   end; 
   
   return usePoiMode;
end;


/* Основной макрос формирования выписки */
MACRO PrintAccountStatement
(
  DprtID,
  OrgStructure,
  IssueMode,
  NumPlan,
  Chapter,
  Currency,
  DateIn,
  DateOut,
  isIncludeClient,
  Client,
  Oper,
  BalanceMask,
  AccountMask,
  KindSort,
  OutForm,
  Alg,
  Subsystem,
  PrGround,
  DblPrint,
  DocOnlyData,
  NatCurEquivalent,
  SPODTurnsTotal,
  cSetA,
  cSetZTurn,
  cSetCr,
  cEveryDayOfPrd,
  cSetExcludeRevalue,
  ExcludeOcp,
  AccountType,
  isIncludeType,
  AccountUserType,
  isIncludeUserType,
  needText,
  needExcel,
  needWord,
  showStorno,
  userTypeMethod,
  excludeInternetAccount,
  typeMethod,
  excelReportKind,
  wordReportKind,
  printTotalsByDays
)
  var RetVal = WELLDONE,
      DateInc, LastDate,
      AccInfo, CmdText,
      Docs,
      AccCaption,
      LoansAccount,
      isFirstExcelPrint = true;

  var viewIterator;

  var accountQueryForDocInfo;

  var notProcessingAccountClause;

  var cacheQueryMaker;
  var profiler = TSQLProfiler(getTxtFileName("!pr_accstatement_profile"));
  profiler.clear();
  profiler.on();

  var action = TActionPanel();

  var i;
  var reportUsePoiMode = GetUsePoiMode();

  AccInfo = TAccStatementInfo(DprtID, OrgStructure, IssueMode, NumPlan, Chapter, Currency, DateIn, DateOut, isIncludeClient, Client, Oper,
                              BalanceMask, AccountMask, KindSort, OutForm, Alg, Subsystem, PrGround, DblPrint, DocOnlyData,
                              NatCurEquivalent, SPODTurnsTotal, cSetA, cSetZTurn, cSetCr, cEveryDayOfPrd, cSetExcludeRevalue, ExcludeOcp,
                              AccountType, isIncludeType, AccountUserType, isIncludeUserType, needText, needExcel, needWord, showStorno,
                              userTypeMethod, excludeInternetAccount, typeMethod, excelReportKind, wordReportKind, printTotalsByDays
                             );

  if (not (accInfo.needText or accInfo.needExcel or accInfo.needWord))
    msgbox("Ни один из форматов печати не выбран!");
    retVal = ERROR;
  end;

  /* Контроль открытых опердней */
  var repOperDays = RepOperdaysOpened(AccInfo.DepartmentList, AccInfo.DateIn, AccInfo.DateOut);

  if ((RetVal == WELLDONE) and (repOperDays.ShouldContinue == false))
    RetVal = OPERDAYNOTPASSED;
  end;

  m_cachedHeaderMaker = TTableHeaderMaker(AccInfo);

  if (RetVal == WELLDONE)
    RetVal = InitPck(AccInfo);
  end;

  if (RetVal == WELLDONE)
    LastDate = AccInfo.DateOut;

    if (AccInfo.cEveryDayOfPrd == true)
      DateInc = 1;
    else
      DateInc = NDays(AccInfo.DateOut)-NDays(AccInfo.DateIn)+1;
    end;

    NAcc = 0;
    while(AccInfo.DateIn <= LastDate)
      var viewPool = TArray();

      AccInfo.DateOut = AccInfo.DateIn + DateInc-1;

      SQL_Execute("{ CALL rsb_rep_ac.set_dat(" + GetSQLDate(AccInfo.DateIn) + ", " + GetSQLDate(AccInfo.DateOut) + ") }");

      SQL_Execute("{ CALL  rep_data.setBeginDate(" + GetSQLDate(AccInfo.DateIn) + ") }");
      SQL_Execute("{ CALL  rep_data.setEndDate(" +  GetSQLDate(AccInfo.DateOut) + ") }");

      sql_Execute("CALL rsvoxprm.setPrmVal('BeginDate', UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(AccInfo.DateIn)+", 'DD.MM.YYYY')))");
      sql_Execute("CALL rsvoxprm.setPrmVal('EndDate',   UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(AccInfo.DateOut)+", 'DD.MM.YYYY')))");

      if (   (accInfo.subsystem == REP_SUBSYSTEMS_ALL)
          or (accInfo.subsystem == REP_SUBSYSTEMS_LOANS)
         )
          sql_truncate(tempSortTableName);
      end;

      Docs = TDocument(AccInfo.OrgStructure);

      if (accInfo.subsystem == REP_SUBSYSTEMS_CB)
          action.beginAct(1000, "Получение данных о лицевых счетах ГКБО");
          cmdText = makeAccountQueryText(accInfo, dateOut);
          accountQueryForDocInfo = makeAccountQueryText(accInfo, dateOut, true);
          setCbAccountsViewName();
          setCacheCbAccountsViewName();
          insertCbAccount(cmdText, accountQueryForDocInfo);
          action.endAct();

      else

          action.beginAct(1000, "Получение данных о лицевых счетах RS-Loans");
          accInfo.createLoansAccount();
          action.endAct();

          // специальный вариант вставки данных из ГКБО с учетом обработанных счетов других подсистем
          action.beginAct(1000, "Получение данных о лицевых счетах ГКБО");
          cmdText = makeAccountQueryText(accInfo, dateOut);
          accountQueryForDocInfo = makeAccountQueryText(accInfo, dateOut, true);

          notProcessingAccountClause = " AND NOT EXISTS(SELECT 1 FROM drepaccst_tmp acctmp WHERE acctmp.t_odbAccount = ac.t_account)";

          cmdText = cmdText + notProcessingAccountClause;
          accountQueryForDocInfo = accountQueryForDocInfo + notProcessingAccountClause;

          setCbAccountsViewName();
          setCacheCbAccountsViewName();
          insertCbAccount(cmdText, accountQueryForDocInfo);
          action.endAct();

      end;

      cacheQueryMaker = TCacheQueryMaker(accInfo);
      accInfo.docInfo.cacheInformation(cacheQueryMaker.getMainQuery());
      TPaymentsUtility(accInfo).createData(accInfo.getCacheTableName());

      var accountGettingAction = TActionPanel();
      accountGettingAction.beginAct(100, "Выборка счетов");

      if (accInfo.subsystem == REP_SUBSYSTEMS_ALL)

        cmdText = "SELECT " + sqlGetFieldsString(cbAccountViewName)
         + "\n" + "  FROM " + cbAccountViewName
         + "\n" + " UNION ALL "
         + "\n" + "SELECT " + sqlGetFieldsString(cbAccountViewName)
         + "\n" + "  FROM " + tempSortTableName
         + "\n" + "   WHERE t_acc <> t_odbAccount ";

      elif (accInfo.subsystem == REP_SUBSYSTEMS_CB)

        cmdText = "SELECT " + sqlGetFieldsString(cbAccountViewName)
         + "\n" + "  FROM " + cbAccountViewName

      else

        cmdText = "SELECT " + sqlGetFieldsString(tempSortTableName)
         + "\n" + "  FROM " + cbAccountViewName + " acc"
         + "\n" + "  WHERE EXISTS (SELECT 1 FROM " + tempSortTableName + " accLoans WHERE accLoans.t_acc = acc.t_acc)"
         + "\n" + " UNION ALL "
         + "\n" + "SELECT " + sqlGetFieldsString(tempSortTableName)
         + "\n" + "  FROM " + tempSortTableName
         + "\n" + "  WHERE t_acc <> t_odbAccount";
      end;

      CmdText = CmdText
       + "\n" + "";

      CmdText = CmdText + MakeAcOrderBy(AccInfo);
      AccInfo.Account = TRsbDataSet(CmdText);

      AccInfo.Account.SetFieldType("DPD",       V_DATE);
      AccInfo.Account.SetFieldType("DPD_R",     V_DATE);
      AccInfo.Account.SetFieldType("Currency",  V_INTEGER);
      AccInfo.Account.SetFieldType("RestIn",    V_MONEY);
      AccInfo.Account.SetFieldType("RestInEq",  V_MONEY);
      AccInfo.Account.SetFieldType("RestOut",   V_MONEY);
      AccInfo.Account.SetFieldType("RestOutEq", V_MONEY);
      AccInfo.Account.SetFieldType("Chapter",   V_INTEGER);

      accountGettingAction.endAct();

      if (accInfo.needText)
        viewPool.value(viewPool.size) = TPlainTextView(accInfo);
      end;

      // if (accInfo.needExcel)
        // if (excelReportKind == REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT)
          // if (views.size == 0)
            // // Для первого дня периода с данными (или для всего периода) добавляем новый объект
            // viewPool.value(viewPool.size) = TExcelView(accInfo);
          // else
            // // Для последующих дней используем ссылку на объект для первого дня, т.к. все данные размещаются в одном файле
            // viewPool.value(viewPool.size) = views.value(0).value(viewPool.size);
          // end;
        // else
          // viewPool.value(viewPool.size) = TExcelView(accInfo);
        // end;
      // end;

      if (accInfo.needWord)
		if (reportUsePoiMode)
		  viewPool.value(viewPool.size) = TPOIWordView(accInfo);
		else
		  viewPool.value(viewPool.size) = TWordView(accInfo);
		end;
      end;

      AccCaption = "Формирование выписки за";
      if (AccInfo.DateIn == AccInfo.DateOut)
        AccCaption = AccCaption + " " + string(AccInfo.DateOut:f);
      else
        AccCaption = AccCaption + " период с " + string(AccInfo.DateIn:f) + " по " + string(AccInfo.DateOut:f);
      end;

      InitProgress(-1, "", AccCaption);

      var hasData : Bool;
      hasData = false;

/*
SCR 238041
Повтор из-за "двойной" выборки. По лоансу в DREPACCST_TMP отбирается два счета как бы по двум документам, но счет !ОДИН!.
А выборка документов в конструкторе TDocument идет по счету, поэтому отбираются 2 документа для первого счета из DREPACCST_TMP, а потом еще два документа по "второму" счету из DREPACCST_TMP"
*/
      var excelPrintController = null;
      var currentAccount = "";
      while(AccInfo.Next())
        hasData = true;

        if (accInfo.needExcel)
            if(excelPrintController == null)
                if((excelPrintControllers.size == 0) OR (excelReportKind != REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT))
                    excelPrintControllers[excelPrintControllers.size] = objectFactoryInstance.createReportPrintController(accInfo.printTotalsByDays);
                    excelPrintControllers[excelPrintControllers.size - 1].defineFileName(AccInfo);
                end;
                excelPrintController = excelPrintControllers[excelPrintControllers.size - 1];
            end;
        end;

        if (currentAccount == AccInfo.Account.acc)
            continue;
        end;

        currentAccount = AccInfo.Account.acc;
        if ((AccInfo.Account.ODBAccount == "") and (AccInfo.Account.initialSubsystem == REP_SUBSYSTEMS_LOANS))
          if (not hasInvalidLoansAccount)
            hasInvalidLoansAccount = true;
            invalidLoansAccountProtocol = TOfstream("invalidLoansAccount");
            invalidLoansAccountProtocol.setOutputFile();
            println("Не обработаны следующие счета п/с \"Кредитование\":");
            invalidLoansAccountProtocol.resetOutputFile();
          end;

          invalidLoansAccountProtocol.setOutputFile();
          println(AccInfo.Account.Acc);
          invalidLoansAccountProtocol.resetOutputFile();

        else
            ProcessDocuments(accInfo, docs, viewPool, excelPrintController, isFirstExcelPrint);

            NAcc = NAcc+1;
        end;

        UseProgress(NAcc);
      end;

      RemProgress();

      AccInfo.DateIn = AccInfo.DateIn+DateInc;
      if (cbAccountViewName != "")
        sql_execute("DROP VIEW " + cbAccountViewName);
        sql_execute("DROP VIEW " + cbCacheAccountViewName);
      end;

      if (hasData)
          var tmpViewPool;
// 08.09.2016 ABP !!! Для текста сделать аналогично - оставить только первый экземпляр TPlainTextView
          if (views.size != 0)
              // Убираем ссылку на представление Excel, она уже не нужна, все данные находятся в views.value(0)
              tmpViewPool = TArray();
              for (var it, viewPool)
                  if (not (isEqClass("TExcelView", it) and (excelReportKind == REP_EXCEL_REPORT_KIND_ONE_FILE_BY_ACCOUNT)))
                      tmpViewPool.value(tmpViewPool.size) = it;
                  end;
              end;
          else
              tmpViewPool = viewPool;
          end;

          views.value(views.size) = tmpViewPool;
          hasData = false;
      end;

      isFirstExcelPrint = false;
    end;
  end;

  var printAction = TActionPanel();
  printAction.beginAct(100, "Печать отчета");
  if (   ((NDoc == 0) and (NAcc != 0) and (not AccInfo.cSetZTurn) and (RetVal == WELLDONE)) //    RetVal = NODOC;
      or ((NAcc == 0) and (RetVal == WELLDONE)) //    RetVal = NOACC;
     )
    viewPool = TArray();

    // 05.04.2012 ABP Для вывода пустых отчетов
    if (accInfo.needText)
      viewPool.value(viewPool.size) = TPlainTextView(accInfo);
    end;

    if((NAcc > 0) OR (NDoc > 0)) // DEF-97930 Для пустого отчета Excel не показываем
      if (accInfo.needExcel)
        viewPool.value(viewPool.size) = TExcelView(accInfo);
      elif (false) // (accInfo.needWord) Запретил вывод Word-а, так как толком не работает, а разбираться некогда
		if (reportUsePoiMode)
		  viewPool.value(viewPool.size) = TPOIWordView(accInfo);
		else
		  viewPool.value(viewPool.size) = TWordView(accInfo);
              end;
      end;
    end;

    views.value(0) = viewPool;

    i = 0;
    while (i < views.value(0).size)
      views.value(0).value(i).printEmptyReport();
      i = i + 1;
    end
  end;

  if (retVal == WELLDONE)
    var word = null;
    var tempDays = TArray();

    // Для plain text надо очистить поток, т.к. в web он не очищается
    for (i, views.value(0))
        if (isEqClass("TPlainTextView", i))
            i.clear();
            break;
        end;
    end;

    for (i, views)
      var tempFormats = TArray();
      for (var j, i)
        j.print();

        if (    (accInfo.wordReportKind == REP_WORD_REPORT_KIND_ONE_FILE)
            and isEqClass("TWordView", j) //TPOIWordView ?
           )
            // Создаем в первом попавшемся экземпляре TWordView общий файл, который будем показывать и отдавать в безынтерфейсных вызовах
            if (word == null)
                word = j.creatReportFile(accInfo);
                tempFormats.value(tempFormats.size) = j; // экземпляр TWordView оставляем в пуле представлений
            else
            // добавляем данные из текущего экземпляра TWordView данные в общий файл по ссылке
                j.addReportTo(word);
            end;
        else
            tempFormats.value(tempFormats.size) = j;
        end;
      end;

      tempDays.value(tempDays.size) = tempFormats;
    end;

    views = tempDays;
    if (word != null)
        word.saveAs(reportBodyFileName);
        word.close();
        OR_DeleteFile(wordReportWorkFileName);
    end;

    if (accInfo.needText)
      if ((accInfo.subsystem == REP_SUBSYSTEMS_ALL) or (accInfo.subsystem == REP_SUBSYSTEMS_LOANS))
        if (accInfo.outForm != ACC_FORMS_NOTHING)
          TPrimaryDocsLoans(viewPool.value(0)).print();
        end;
      end;
    end;
  end;
  printAction.endAct();

//  profiler.off();

  println("Обработано счетов: ", NAcc);
  println("Обработано документов: ", NDoc);

  if (retVal == WELLDONE)
      if (repOperDays.rewind())
          retVal = EXISTSOPENOPERDAYS;
      end;
  end;

  return retVal;
END;

// Вывод отчета и протоколов на экран
macro showReport()
    if (getDialogFlag() == 0)
        return;
    end;

    // Список некорректных счетов Loans
    if (hasInvalidLoansAccount)
        invalidLoansAccountProtocol.setOutputFile();
        println("");
        println("Неверно заданы счета ОДБ!!!");
        println("");
        println("Нажмите <Esc> для продолжения");
        invalidLoansAccountProtocol.resetOutputFile();
        invalidLoansAccountProtocol.show();
    end;

    var i;
    var j;

    // Сначала выводим Excel и Word
    for (i, views)
        for (j, i)
            if (not isEqClass("TPlainTextView", j))
                j.show();
            end;
        end;
    end;
    var printAction = TActionPanel();
    printAction.beginAct(100, "Сохранение отчета в Excel");
    excelPrintControllers = null;
    printAction.endAct();

    // Затем plain text. Во всех элементах массива views это представление связано с одним и тем же потоком вывода,
    // поэтому показываем только первое представление
    for (j, views.value(0))
        if (isEqClass("TPlainTextView", j))
            j.show();
            break;
        end;
    end;

end;

macro saveReport(reportName)

    for (var j, views.value(0))
        if (isEqClass("TPlainTextView", j))
            j.save(reportName);
            break;
        end;
    end;

end;

macro batchProduce(reportName,
                   dprtID, orgStructure, issueMode, numPlan, chapter, currency, dateIn, dateOut, isIncludeClient, client, oper, balanceMask,
                   accountMask, kindSort, outForm, alg, subsystem, prGround, dblPrint, docOnlyData, natCurEquivalent,
                   spodTurnsTotal, cSetA, cSetZTurn, cSetCr, cEveryDayOfPrd, cSetExcludeRevalue, excludeOcp, accountType, isIncludeType,
                   accountUserType, isIncludeUserType, needText, needExcel, needWord, showStorno, userTypeMethod, excludeInternetAccount,
                   typeMethod, excelReportKind, wordReportKind
                  )

    var dialogFlag = setDialogFlag(0);

    printAccountStatement(dprtID, orgStructure, issueMode, numPlan, chapter, currency, dateIn, dateOut, isIncludeClient, client, oper, balanceMask,
                          accountMask, kindSort, outForm, alg, subsystem, prGround, dblPrint, docOnlyData, natCurEquivalent,
                          spodTurnsTotal, cSetA, cSetZTurn, cSetCr, cEveryDayOfPrd, cSetExcludeRevalue, excludeOcp, accountType, isIncludeType,
                          accountUserType, isIncludeUserType, needText, needExcel, needWord, showStorno, userTypeMethod, excludeInternetAccount,
                          typeMethod, excelReportKind, wordReportKind
                         );

    saveReport(reportName);

    setDialogFlag(dialogFlag);

end;
