/*
$Name:          TbsWinRepView.mac
$Module:        Внутренняя отчетность
$Description:   Класс печатного представления "ОСВ" на основе Windows Reports
*/


/**
 * Класс печатного представления "ОСВ" на основе Windows Reports
 *
 * @since   v.6.00.020.30.48
 * @author  ABP
 * @version 1.0
 */
import or_rep_h;

import TbsParameters;
import TbsView;

/**
 * Печатное представление ОСВ на основе Windows Reports
 * @since   v.6.00.020.30.48
 * @author  ABP
 * @version 1.0
 */
class (TTbsView) TTbsWinRepView(template : Object)

    private var m_precision : Integer;
    private var m_apostrophe : String;
    private var m_printEngine : Object;
    private var m_template : Object;

    /**
     * Установить ширину отчета.
     */
    macro setReportWidth(reportWidth : Integer)
        if (reportWidth == NULL)
            m_reportWidth = m_printEngine.getHeaderWidth();
        else
            m_reportWidth = reportWidth;
        end;

        m_separateLine = " " + mkstr("-", m_reportWidth - 2) + " ";
    end;

    macro printNullData()
        m_printEngine.addPrintCell("Данные отсутствуют.", m_reportWidth+1, 0, "l:", REP_ELEM_STR);
        m_printEngine.addStr();
    end;

    private macro printRow(name, dataset)
        var data = "";

        for (var i, m_template.getRows())
            if (strlwr(i.first) == strlwr(name))
                for (var j, i.second)
                    if (j.isCaption)
                        data = j.dataSourceName;
                    else
                        data = execExp("dataset." + j.dataSourceName);
                    end;
                    m_printEngine.addPrintCell(data, nvl(j.width, m_reportWidth), m_precision, j.format+":"+m_apostrophe, REP_ELEM_TABL);
                end;
                m_printEngine.addStr();
                break;
            end;
        end;
    end;

    /**
     * Печать разделителя.
     */
    macro printSeparator()
    end;

    /**
     * Печать шапки банка.
     */
    macro printBankHead()
        var bankName = TArray();

        printBankHeaderBuf(m_departmentCode, m_orgStructure, m_reportWidth, bankName);
        for (var i, bankName)
            m_printEngine.addPrintCell(i, m_reportWidth+1, 0, "c:", REP_ELEM_STR);
            m_printEngine.addStr();
        end;
    end;

    /**
     * Печать шапки отчета.
     */
    macro printReportHead()
        m_printEngine.addPrintCell(getTbsFormText(), m_reportWidth+1, 0, "c:", REP_ELEM_STR);
        m_printEngine.addStr();
        m_printEngine.addPrintCell(getPeriodTypeText(), m_reportWidth+1, 0, "c:", REP_ELEM_STR);
        m_printEngine.addStr();
    end;

    /**
     * Печать наименования банка и шапки отчета.
     */
    macro printHead()
        m_printEngine.addEmptyStr();
        printBankHead();
        printReportHead();
    end;

    /**
     * Печать шапки главы.
     */
    macro printChapterHead(dataSet)
        var align = STR_ALIGN_CENTER;

        printHead();

        var headDataSet = getHeadDataset(dataSet);
        while (headDataSet.next())
            m_printEngine.addPrintCell(headDataSet.name, m_reportWidth+1, 0, "c:", REP_ELEM_STR);
            m_printEngine.addStr();
        end;

        m_printEngine.addPrintCell(getRecalcCurrencyText(),m_reportWidth+1, 0, "l:", REP_ELEM_STR);
        m_printEngine.addStr();
    end;

    /**
     * Печать подписей.
     */
    macro printSignature()
        m_printEngine.addEmptyStr();
        m_printEngine.addPrintCell(getBossString(), m_reportWidth+1, 0, "l:", REP_ELEM_STR);
        m_printEngine.addStr();
        m_printEngine.addEmptyStr();
        m_printEngine.addPrintCell(getBookString(), m_reportWidth+1, 0, "l:", REP_ELEM_STR);
        m_printEngine.addStr();
        m_printEngine.addEmptyStr();
    end;

    /**
     * Начало печати данных по главе.
     */
    macro beginChapter(dataSet)
        printChapterHead(dataSet);
    end;

    /**
     * Начало печати раздела.
     */
    macro beginPart(dataSet)
        var data = "";
        var str = "";

        for (var i, m_template.getRows())
            if (strlwr(i.first) == strlwr("HeaderPart"))
                for (var j, i.second)
                    if (j.isCaption)
                        data = j.dataSourceName;
                    else
                        data = execExp("dataset." + j.dataSourceName);
                    end;
                    str = str + string(data);
                end;
                break;
            end;
        end;

        if (str != "")
            m_printEngine.addPrintCell(str, m_printEngine.getHeaderWidth(), 0, "l:", REP_ELEM_TABL);
            m_printEngine.addStr();
        end;
    end;

    /**
     * Получить экземпляр CMakeReport.
     */
    macro getPrintEngine()
        return m_printEngine;
    end;

    macro setFileName(fileName : String)
        getPrintEngine().setFileName(fileName);
    end;

    /**
     *  Конструктор.
     */
    private macro constructor(template : Object)
        m_template = template;
        m_template.createResultFile();

        initTTbsView(m_template.getTemplateFileName());

        var header;
        for (var i, m_template.getRows())
            if (strlwr(i.first) == strlwr("Header"))
                header = i.second.value(0).dataSourceName;
                break;
            end;
        end;

        m_precision = ternary(twoDigitDimention, 2, 0);
        m_apostrophe = ternary(tbsParameters().getIsPrintApostrophe(), "a:", "");

        m_printEngine = CMakeReport(header, SEP_DEFAULT, 50000);
//        m_printEngine.setFlagShowMidSeparator(false);
        m_printEngine.setFlagShowGrid(false);
    end;

    constructor(template);
end;
