/*
$Name:          ControlOverdraftAccounts.mac
$Module:        Внутренняя отчетность
$Description:   Контроль л/с на наличие "красного сальдо"
*/

/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 *  File Name   : ControlOverdraftAccounts.mac               February 15, 2008
 *  Programmer  : ABP
 *  Description : Контроль л/с на наличие "красного сальдо"
 *  Comment     :
 *  Modify      :
 */

class TControlOverdraftAccounts(dataSource : Object)

    private var m_dataSource : Object;

    /**
     * Печать заголовка
     */
    private macro printCaption()
        println();
        println("КОНТРОЛЬ НА НАЛИЧИЕ \"КРАСНОГО САЛЬДО\"");
        println();
    end;

    /**
     * Печать сообщения об успешном контроле
     */
    private macro printSuccessful()
        println("Контроль прошел успешно");
    end;

    /**
     * Печать сообщения о неуспешном контроле
     */
    private macro printFail()

        var report = CTableReport(0, false, false);

        var isDone = false;

        println("Выявлено \"красное сальдо\" по счетам:");

        [┌─────┬───────────────────────────┬─────┬─────────────────────────────────────────────────────┬─────────────────────────────────────────────────────┐
         │ Код │   Номер лицевого счета    │ Вид │                  Входящий остаток                   │                  Исходящий остаток                  │
         │ вал │                           │     ├──────────────────────────┬──────────────────────────┼──────────────────────────┬──────────────────────────┤
         │     │                           │     │      в валюте счета      │         в рублях         │      в валюте счета      │         в рублях         │
         ├─────┼───────────────────────────┼─────┼──────────────────────────┼──────────────────────────┼──────────────────────────┼──────────────────────────┤];

        while (not isDone)
        [│ ### │ ######################### │  #  │ ######################## │ ######################## │ ######################## │ ######################## │]
        (m_dataSource.isoNumber,
                 m_dataSource.accountNumber,
                                              m_dataSource.accountKind,
                                                   m_dataSource.inRest,
                                                                               m_dataSource.inRest_R,
                                                                               m_dataSource.outRest,
                                                                                                                                     m_dataSource.outRest_R);

            isDone = not m_dataSource.next();
        end;

        println("└─────┴───────────────────────────┴─────┴──────────────────────────┴──────────────────────────┴──────────────────────────┴──────────────────────────┘");
    end;

    /**
     * Конструктор
     *
     * @param     dataSource
     */
    private macro constructor(dataSource : Object)
        m_dataSource = dataSource;
    end;

    /**
     * Выполнение контроля
     */
    macro execute()

        printCaption();

        if (m_dataSource.next())
            printFail();
        else
            printSuccessful();
        end;

    end;

    constructor(dataSource);

end;
