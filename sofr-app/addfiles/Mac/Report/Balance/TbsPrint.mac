/*
$Name:          TbsPrint.mac
$Module:        Внутренняя отчетность
$Description:   Класс печати "ОСВ"
*/

/**
 * Класс управления печатью отчета "ОСВ"
 *
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */

import TbsView;
import TbsData;
import TbsTemplateBuilder;
import TbsWinRepView;
import ofstream;

/**
 * Печать отчета "ОСВ".
 */
class TTbsPrint(printOfstream : TOfstream)

    var m_printOfstream;

    var m_template;

    var m_view = TArray();

    var m_dataSet = TArray();

    var m_print;

    /**
     * Печать таблицы.
     */
    private macro tablePrint(view : TTbsView, dataset)

        var chapterItogRecord;
        var currencyItogRecord;
        var partItogRecord;
        var balance1ItogRecord;
        var balance2ItogRecord;
        var accountRecord;

        var haveChapterItogRecord = false;
        var haveCurrencyItogRecord = false;
        var havePartItogRecord = false;
        var haveBalance1ItogRecord = false;
        var haveBalance2ItogRecord = false;

        var isEof = not dataset.moveNext();

        var progress = 0;

        if (isEof)
            view.printNullData();
        end;

        InitProgress(-1, "", "Выпуск отчета...");
        while ((not isEof) and dataset.isCurrencyItog)
            chapterItogRecord = dataset.getRecord();
            isEof = not dataset.moveNext(dataset);
            if(not isEof)
                view.beginChapter(chapterItogRecord);
                haveChapterItogRecord = true;
            end;

            while ((not isEof) and (not dataset.isCurrencyItog))
                if (dataset.isPartItog)
                    currencyItogRecord = dataset.getRecord();
                    isEof = not dataset.moveNext(dataset);
                    if((not isEof) and ((tbsParameters().getFiId() != NATCUR) or (tbsParameters.getRecalcCurrency() >= 0)) )
                        view.beginCurrency(currencyItogRecord);
                        haveCurrencyItogRecord = true;
                    end;
                end;

                while ((not isEof) and (not dataset.isPartItog) and (not dataset.isCurrencyItog))
                    if (dataset.isBalance1Itog)
                        partItogRecord = dataset.getRecord();
                        isEof = not dataset.moveNext(dataset);
                        if(not isEof)
                            view.beginPart(partItogRecord);
                            havePartItogRecord = true;
                        end;
                    end;

                    while ((not isEof) and (not dataset.isBalance1Itog) and (not dataset.isPartItog) and (not dataset.isCurrencyItog))
                        if (dataset.isBalance2Itog)
                            balance1ItogRecord = dataset.getRecord();
                            isEof = not dataset.moveNext(dataset);
                            haveBalance1ItogRecord = true;
                        end;

                        while ((not isEof) and (not dataset.isBalance2Itog) and (not dataset.isBalance1Itog) and (not dataset.isPartItog) and (not dataset.isCurrencyItog))
                            if (dataset.isAccount)
                                balance2ItogRecord = dataset.getRecord();
                                isEof = not dataset.moveNext(dataset);
                                haveBalance2ItogRecord = true;
                            end;

                            while ((not isEof) and (not dataset.isAccount))
                                accountRecord = dataset.getRecord();
                                accountRecord.account = string(accountRecord.account:f);
                                view.printAccount(accountRecord);
                                isEof = not dataset.moveNext();
                                progress = progress + 1;
                                UseProgress(progress);
                            end;

                            if (haveBalance2ItogRecord)
                                view.printAccount(balance2ItogRecord);
                                view.printSeparator();
                                haveBalance2ItogRecord = false;
                            end;

                        end;

                        if (haveBalance1ItogRecord)
                            view.printAccount(balance1ItogRecord);
                            view.printSeparator();
                            haveBalance1ItogRecord = false;
                        end;

                    end;

                    if (havePartItogRecord)
                        view.endPart(partItogRecord);
                        view.printSeparator();
                        havePartItogRecord = false;
                    end;

                end;

                if (haveCurrencyItogRecord)
                    view.endCurrency(currencyItogRecord);
                    view.printSeparator();
                    haveCurrencyItogRecord = false;
                end;

            end;

            if (haveChapterItogRecord)
                view.endChapter(chapterItogRecord);
                haveChapterItogRecord = false;
            end;

        end;

        RemProgress();
    end;

    /**
     * Печать отчета.
     */
    macro execute()

        m_dataSet[0] = TTbsData().getReportDataSet();

        var oldOutput;
        var i = 0;

        m_printOfstream.setOutputFile();

        m_view[0].setReportWidth();

        while (i < m_view.size)
            m_view[i].getPrintEngine().clearAllStr();
            tablePrint(m_view[i], m_dataset[i]);

            if (tbsParameters().needPlainText())
                m_view[i].getPrintEngine().printRep();
            end;

            if (tbsParameters().needXls())
                m_view[i].getPrintEngine().setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
                m_view[i].getPrintEngine().printWinRep("ОСВ");
            end;

            i = i + 1;
        end;

        m_printOfstream.resetOutputFile();

        return m_view;

    end;


    private macro constructor(printOfstream : TOfstream)
        m_printOfstream = printOfstream;

        if((tbsParameters().getReportForm()   == REP_OSV_FORM_TURN) OR
           (tbsParameters().getReportForm()   == REP_OSV_FORM_TURN_NAME))
            m_template = TTurnsTbsTemplate();
        elif((tbsParameters().getReportForm() == REP_OSV_FORM_REST) OR
             (tbsParameters().getReportForm() == REP_OSV_FORM_REST_NAME))
            m_template = TRestsTbsTemplate();
        end;

        if (tbsParameters().needPlainText())
            m_view[0] = TTbsView(m_template.createResultFile());
        end;

        if (tbsParameters().needXls())
            m_view[0] = TTbsWinRepView(m_template);
        end;
    end;

    constructor(printOfstream);
end;
