/*
$Name:          main_turnoverbs.mac
$Module:        Внутренняя отчетность
$Description:   Выпуск оборотно-сальдовой ведомости
*/

/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 * File Name   : main_turnoverbs.mac
 * Description : Выпуск оборотно-сальдовой ведомости
 * Comment     :
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 * MODIFICATIONS:
       20.04.2014 - SCR 174170. ОСВ. Выход из меню выбора формата печати.
 */

import testLib;

import cb_sql;
import TbsCalculator;
import TbsControl;
import TbsPrint;

private var printOfstream;
private var controlOfstream;
private var winRepView;

var reportBodyFileName;     //имя файла отчета
var controlBodyFileName;    //имя файла протокола контроля

private macro chooseFormat(needPlainText, needXls)
    var choices = TArray();
    var choice;

    choices(0) = "     ТЕКСТ     ";
    choices(1) = "     EXCEL     ";
    choices(2) = " ТЕКСТ и EXCEL ";

    choice = menu(choices, "Выберите способ печати отчета", "Печатать в" );

    needPlainText = false;
    needXls = false;

    if (choice == 0)
        needPlainText = true;
        needXls = false;
    elif (choice == 1)
        needPlainText = false;
        needXls = true;
    elif (choice == 2)
        needPlainText = true;
        needXls = true;
    else
        needPlainText = false;
        needXls = false;
    end;

    setParm(0, needPlainText);
    setParm(1, needXls);
end;

/**
 *  Отчет.
 */
macro produceTurnoverBsReport(
                              departmentId,
                              organizationStructure,
                              issueMode,
                              chapterNumber,
                              planNumber,
                              currencyId,
                              periodKind,
                              dateIn,
                              dateOut,
                              reportForm,
                              totalsLevel,
                              natcurRepAccounts,
                              showNatcurEq,
                              recalcCurrency,
                              isGroup,
                              oper,
                              client,
                              balanceMask : String,
                              accountMask : String,
                              accType,
                              accUserType : String,
                              accUserTypeMethod : Integer,
                              needSpodTurnTotals,
                              needApostrophes,
                              needZeroAccounts,
                              needExcludeZeroTurns,
                              needExcludeZeroRests,
                              needEverydayReport,
                              needUseBwp,
                              needExcludeOcpAccounts,
                              sortPattern,
                              isplainText,                  //только в случае веб-интерфейса!
                              isExcel                       //только в случае веб-интерфейса!
                             )

    var sqlProfiler = TSqlProfiler(getTxtFileName("tbs_sqlprf"));
    sqlProfiler.clear();
    sqlProfiler.on();

    var needPlainText;
    var needXls;

    //веб-интерфейс передает 2 дополнительных параметра: isplainText и isExcel
    if (    (isplainText != null)
        and (isExcel     != null))
        needPlainText = isplainText;
        needXls       = isExcel;
    else
        //в EasyWin для выбора формата вывода вызываем меню
        chooseFormat(needPlainText, needXls);
        if (    (needPlainText == false)
            and (needXls       == false))

            return;
        end;
    end;

    /*инициализация параметров для оборотно-сальдовой ведомости*/
    initTbsParameters(departmentId, organizationStructure, issueMode, chapterNumber, planNumber,
                      currencyId, periodKind, dateIn, dateOut, reportForm, totalsLevel, natcurRepAccounts,
                      showNatcurEq, recalcCurrency, isGroup, oper, client,
                      ConvertMaskToSQLFormat(balanceMask, "STRING_TO_REPLACE"),
                      ConvertMaskToSQLFormat(accountMask, "STRING_TO_REPLACE"),
                      accType, accUserType, accUserTypeMethod, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                      needExcludeZeroTurns, needExcludeZeroRests,needEverydayReport, needUseBwp,
                      needExcludeOcpAccounts,
                      sortPattern,
                      needPlainText, needXls);

    /* Контроль открытых опердней */
    if (isEqClass("RepDepartmentList", tbsParameters().getDepartmentList()))
        var opd = RepOperdaysOpened(tbsParameters().getDepartmentList(),
                                    tbsParameters().getDateIn(),
                                    tbsParameters().getDateOut());
        if (not opd.shouldContinue())
            exit(1);
        end;
    end;

    var tbsOutFileName;
    if   (tbsParameters().getReportForm() == REP_OSV_FORM_TURN)       tbsOutFileName = "obsal";
    elif (tbsParameters().getReportForm() == REP_OSV_FORM_TURN_NAME)  tbsOutFileName = "osv_nac";
    elif (tbsParameters().getReportForm() == REP_OSV_FORM_REST)       tbsOutFileName = "ostat";
    elif (tbsParameters().getReportForm() == REP_OSV_FORM_REST_NAME)  tbsOutFileName = "ost_nac";
    else                                                              tbsOutFileName = "turnoverbs";
    end;

    printOfstream   = TOfstream(tbsOutFileName);
    controlOfstream = TOfstream("ctrl_turnoverbs");
    winRepView      = TArray();

    var tbsPrint;
    var controlTbsController = TTbsController(controlOfstream);
    var m_dateOut = dateOut;
    var wrExample;
    var i;
    var day;
    var month;
    var year;
    if (tbsParameters().getIsPrintEveryDay() == true)

        tbsParameters().setDateOut(tbsParameters().getDateIn());
        while (tbsParameters().getDateOut() <= m_dateOut)
            dateSplit(tbsParameters().getDateOut(), day, month, year);
            TTbsCalculator().calculate();
            tbsPrint = TTbsPrint(printOfstream);
            wrExample = tbsPrint.execute();
            for (i, wrExample)
                i.setFileName(tbsOutFileName + "_" + string(year : 4) + string(month : 2 : 0 : o) + string(day : 2 : 0 : o));
            end;
            winRepView = arrAdd(winRepView, wrExample);
            controlTbsController.control();

            tbsParameters().setDateIn(tbsParameters().getDateIn() + 1);
            tbsParameters().setDateOut(tbsParameters().getDateOut() + 1);
        end;

    else

        TTbsCalculator().calculate();
        tbsPrint = TTbsPrint(printOfstream);
        wrExample = tbsPrint.execute();
        for (i, wrExample)
            i.setFileName(tbsOutFileName);
        end;
        winRepView = arrAdd(winRepView, wrExample);
        controlTbsController.control();

    end;

    controlBodyFileName = controlOfstream.getFileName();

    if (tbsParameters().needPlainText())
        reportBodyFileName = printOfstream.getFileName();
    end;

    return true;
end;

macro showReport()

    controlOfstream.show();

    if (tbsParameters().needPlainText())
        printOfstream.show();
    end;

    if (tbsParameters().needXls())
        for (var i, winRepView)
            i.getPrintEngine().showWinRep();
        end;
    end;

end;

macro saveReport(reportName, controlName)

    controlOfstream.save(controlName);
    printOfstream.save(reportName);

end;

macro batchProduce(reportName, controlName,
                   departmentId, organizationStructure, issueMode, chapterNumber, planNumber, currencyId, periodKind, dateIn, dateOut,
                   reportForm, totalsLevel, natcurRepAccounts, showNatcurEq, recalcCurrency, isGroup, oper, client, balanceMask, accountMask, accType,
                   accUserType, accUserTypeMethod, needSpodTurnTotals, needApostrophes, needZeroAccounts, needExcludeZeroTurns,
                   needExcludeZeroRests, needEverydayReport, needUseBwp, needExcludeOcpAccounts, sortPattern)

    var dialogFlag = setDialogFlag(0);

    produceTurnoverBsReport(departmentId, organizationStructure, issueMode, chapterNumber, planNumber, currencyId, periodKind, dateIn,
                            dateOut, reportForm, totalsLevel, natcurRepAccounts, showNatcurEq, recalcCurrency, isGroup, oper, client, balanceMask,
                            accountMask, accType, accUserType, accUserTypeMethod, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                            needExcludeZeroTurns, needExcludeZeroRests, needEverydayReport, needUseBwp, needExcludeOcpAccounts, sortPattern,
                            true, false);

    saveReport(reportName, controlName);

    setDialogFlag(dialogFlag);

end;
