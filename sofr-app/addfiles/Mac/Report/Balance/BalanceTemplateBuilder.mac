/*
$Name:        BalanceTemplateBuilder.mac
$Module:      Внутренняя отчетность
$Description: Классы для создания шаблона печати валютного баланса
*/
import TemplateBuilder;
import lib_arr;
import repException;

/**
 * Класс, печати шаблона таблицы.
 */
class (TTemplateBuilder)TBalanceTemplateBuilder(resultFileName : String)

    /**
     * Заголовки стоблцов
     */
    const balanceTitle      = "Балансовый счет";
    const balanceNameTitle  = "Название счета";
    const inRestTitle       = "Входящий остаток";
    const outRestTitle      = "Исходящий остаток";
    const activeTitle       = "Актив";
    const passiveTitle      = "Пассив";
    const currencyTitle     = "Валюта счета";
    const debetTitle        = "Дебет";
    const creditTitle       = "Кредит";

    var m_reportWidth;
    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        throw(TPureVirtualMethodCallException("TBalanceTemplate::createCompositeTableField"));
    end;

    /**
     * Печать шаблона шапки отчета в файл шаблона.
     */
    private macro printTableHeader(kindAccount)
        var header = m_compositeTableField.getReportHeader();
        var i = 0;
        println("<FORM:Header" + NVL(kindAccount, "") + ">");
        while(i < header.size)
            println(header[i]);
            i = i + 1;
        end;

        m_reportWidth = strlen(header[1]);
        println("</FORM>");
    end;

    /**
     * Печать шаблона содержательной части отчета в файл шаблона.
     */
    private macro printTableRow(kindAccount)
        println();
        println("<FORM:Row" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getReportString());
        println("</FORM>");
        println();
    end;

    /**
     * Печать шаблона конца таблицы отчета в файл шаблона.
     */
    private macro printTableEnd(kindAccount)
        var changeFields = TArray();

        if (kindAccount == "_Active")
            changeFields(0) = arrCreate("balance", "Итого АКТИВ:        " );
        elif (kindAccount == "_Passive")
            changeFields(0) = arrCreate("balance", "Итого ПАССИВ:       " );
        else
            changeFields(0) = arrCreate("balance", "Итого:              " );
        end;

        println();
        println("<FORM:End" + NVL(kindAccount, "") + ">");
        if (balanceParameters.getIsPrintCover)
            changeFields(1) = arrCreate("inActiveRest",   "");
            changeFields(2) = arrCreate("inPassiveRest",  "");
            changeFields(3) = arrCreate("outActiveRest",  "");
            changeFields(4) = arrCreate("outPassiveRest", "");
            changeFields(5) = arrCreate("debet",          "");
            changeFields(6) = arrCreate("credit",         "");
        end;
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
    end;

    /**
     * Печать шаблона части по СПОДу отчета в файл шаблона.
     */
    private macro printTableRowSpod(kindAccount)
        var changeFields = TArray();
        var reportString : string;

        changeFields(0) = arrCreate("balance",          "В том числе СПОД    ");
        changeFields(1) = arrCreate("inActiveRest",     "");
        changeFields(2) = arrCreate("inPassiveRest",    "");
        changeFields(3) = arrCreate("outActiveRest",    "");
        changeFields(4) = arrCreate("outPassiveRest",   "");
        changeFields(5) = arrCreate("inActiveRestR",    "");
        changeFields(6) = arrCreate("inPassiveRestR",   "");
        changeFields(7) = arrCreate("outActiveRestR",   "");
        changeFields(8) = arrCreate("outPassiveRestR",  "");

        println();
        println("<FORM:RowSpod" + NVL(kindAccount, "") + ">");
        if (balanceParameters.getIsPrintSpod)
            reportString = m_compositeTableField.getChangeReportString(changeFields);
            reportString = strSubst(reportString, "Debet",  "debetSpod");
            reportString = strSubst(reportString, "Credit", "creditSpod");
            println(reportString);
        end;
        println("</FORM>");

        changeFields(9)  = arrCreate("debet",   "");
        changeFields(10) = arrCreate("credit",  "");

        println();
        println("<FORM:RowSpodCurTotal" + NVL(kindAccount, "") + ">");
        if (balanceParameters.getIsPrintSpod)
            reportString = m_compositeTableField.getChangeReportString(changeFields);
            reportString = strSubst(reportString, "Debet",  "debetSpod");
            reportString = strSubst(reportString, "Credit", "creditSpod");
            println(reportString);
        end;
        println("</FORM>");
    end;

    /**
     * Печать шаблона глав таблицы отчета в файл шаблона.
     */
    private macro printChapter(kindAccount)
        var changeFields = TArray();
        if (kindAccount == "_Active")
            changeFields(0) = arrCreate("balance", "Итого АКТИВ:                          " );
        elif (kindAccount == "_Passive")
            changeFields(0) = arrCreate("balance", "Итого ПАССИВ:                         " );
        else
            changeFields(0) = arrCreate("balance", "Итого:                                " );
        end;

        if (balanceParameters.getIsPrintCover)
            changeFields(1) = arrCreate("inActiveRestR",   "");
            changeFields(2) = arrCreate("inPassiveRestR",  "");
            changeFields(3) = arrCreate("outActiveRestR",  "");
            changeFields(4) = arrCreate("outPassiveRestR", "");
            changeFields(5) = arrCreate("debetR",          "");
            changeFields(6) = arrCreate("creditR",         "");
        end;

        println();
        println("<FORM:HeaderChapter" + NVL(kindAccount, "") + ">");
        println();
        println("<!--titleChapter:c:w-->" + mkStr("#", m_reportWidth));
        println("</FORM>");
        println();
        println("<FORM:EndChapter" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
        println();
    end;

    /**
     * Печать шаблона итогов таблицы отчета в файл шаблона.
     */
    private macro printIssue(kindAccount)
        var changeFields = TArray();
        changeFields(0) = arrCreate("balance", "Итого по разделу    ");
        println();
        println("<FORM:HeaderIssue" + NVL(kindAccount, "") + ">");
        println("Раздел <!-- part:r -->#. <!-- partName:r -->#");
        println("</FORM>");
        println();
        println("<FORM:EndIssue" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
        println();
    end;

    /**
     * Печать шаблона валюты таблицы отчета в файл шаблона.
     */
    private macro printCurrency(kindAccount)
        var changeFields = TArray();
        changeFields(0) = arrCreate("balance", "Итого по валюте     ");
        println();
        println("<FORM:HeaderCurrency" + NVL(kindAccount, "") + ">");
        println("<!-- titleCurrency:r -->#");
        println("</FORM>");
        println();
        println("<FORM:EndCurrency" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
        println();
    end;

    /**
     * Создание результирующего файла шаблона.
     */
    macro createResultFile(kindAccount) : string
        var oldOutput = setOutput(m_resultFileName,  true);
        printTableHeader(kindAccount);
        printTableRow(kindAccount);
        printTableRowSpod(kindAccount);
        printChapter(kindAccount);
        printIssue(kindAccount);
        printCurrency(kindAccount);
        printTableEnd(kindAccount);
        setOutput(oldOutput,   false);

        return m_resultFileName;
    end;

    /**
     *  Получение названия нац валюты
     */
    private macro getNatCurrencyName()
        var natCurName = "";
        var dataSet = TRsbDataSet("SELECT t_name_currency name FROM dcurrency_dbt WHERE t_code_currency = 0");
        if (dataSet.next())
            natCurName = dataSet.name;
        end;
        return natCurName;
    end;

    /**
     * Описание полей заголовка.
     */
    private var balanceHeader      = TTableHeaderField(balanceTitle,         20, STR_ALIGN_CENTER);
    private var balanceNameHeader  = TTableHeaderField(balanceNameTitle,     20, STR_ALIGN_CENTER);
    private var inRestHeader       = TTableHeaderField(inRestTitle,        null, STR_ALIGN_CENTER);
    private var outRestHeader      = TTableHeaderField(outRestTitle,       null, STR_ALIGN_CENTER);
    private var activeHeader       = TTableHeaderField(activeTitle,        null, STR_ALIGN_CENTER);
    private var passiveHeader      = TTableHeaderField(passiveTitle,       null, STR_ALIGN_CENTER);
    private var currencyHeader     = TTableHeaderField(currencyTitle,      null, STR_ALIGN_CENTER);
    private var natCurrencyHeader  = TTableHeaderField(getNatCurrencyName, null, STR_ALIGN_CENTER);
    private var debetHeader        = TTableHeaderField(debetTitle,         null, STR_ALIGN_CENTER);
    private var creditHeader       = TTableHeaderField(creditTitle,        null, STR_ALIGN_CENTER);

    /**
     * Описание полей таблицы.
     */
    private var balanceData        = TTableDataField("balance",      5, "c");
    private var balanceNameData    = TTableDataField("balanceName", 65, "l");

    private macro currencyData(fieldDataSourceName, width)
        return  TTableDataField(fieldDataSourceName, width, "r")
    end;

    private macro natCurrencyData(fieldDataSourceName, width)
        return  TTableDataField(fieldDataSourceName+"R", width, "r")
    end;

    macro amountView(compositeTableField : TCompositeTableField, header, fieldDataSourceName : String, width)
        var amount;
        if (balanceParameters.getIsPrintCover)
            amount = compositeTableField.addField(header);
            amount.addField(currencyHeader,    currencyData(fieldDataSourceName, width));
            amount.addField(natCurrencyHeader, natCurrencyData(fieldDataSourceName, width));
        else
            compositeTableField.addField(header, currencyData(fieldDataSourceName, width));
        end;
    end;

    macro constructor(resultFileName : String)
        initTTemplateBuilder(resultFileName);
    end;

    /**
     * Получение максимальных значений сумм. (необходимо для установления ширины колонки, см. SCR 179768)
     */
    private macro getMaxSumValue():Object

        var p =  "";
        var th = "";

        if (balanceParameters.getMoneyDimension == REP_BALANCE_DIMENSION_THOUSAND)
            p  = "l";
            th = "th";
        else
            p  = "d";
        end;

        var queryMax = "SELECT "
        "\n" + "       MAX (inActiveRest)   maxInActiveRest,   "
        "\n" + "       MAX (inPassiveRest)  maxInPassiveRest,  "
        "\n" + "       MAX (debet)          maxDebet,          "
        "\n" + "       MAX (credit)         maxCredit,         "
        "\n" + "       MAX (outActiveRest)  maxOutActiveRest,  "
        "\n" + "       MAX (outPassiveRest) maxOutPassiveRest, "
        "\n" + "       MAX (debetSpod)      maxDebetSpod,      "
        "\n" + "       MAX (creditSpod)     maxCreditSpod      "
        "\n" + "    FROM ";

        var query = "( SELECT "
        "\n" + "       SUM (blrept.t_"+p+"minactive"+th+"r)    inActiveRest,   "
        "\n" + "       SUM (blrept.t_"+p+"minpassive"+th+"r)   inPassiveRest,  "
        "\n" + "       SUM (blrept.t_"+p+"mdebet"+th+"r)       debet,          "
        "\n" + "       SUM (blrept.t_"+p+"mcredit"+th+"r)      credit,         "
        "\n" + "       SUM (blrept.t_"+p+"moutactive"+th+"r)   outActiveRest,  "
        "\n" + "       SUM (blrept.t_"+p+"moutpassive"+th+"r)  outPassiveRest, "
        "\n" + "       SUM (blrept.t_"+p+"mfinaldebet"+th+"r)  debetSpod,      "
        "\n" + "       SUM (blrept.t_"+p+"mfinalcredit"+th+"r) creditSpod      "
        "\n" + "    FROM dblrept_tmp blrept ) ";

        var data = TRsbDataSet(queryMax + query);
        return data;
    end;

    private var m_fieldWidth = getMaxSumValue();
    m_fieldWidth.next();

    private var inActiveRestWidth   = strlen(string(m_fieldWidth.maxInActiveRest));
    private var inPassiveRestWidth  = strlen(string(m_fieldWidth.maxInPassiveRest));
    private var debetWidth          = strlen(string(m_fieldWidth.maxDebet));
    private var creditWidth         = strlen(string(m_fieldWidth.maxCredit));
    private var outActiveRestWidth  = strlen(string(m_fieldWidth.maxOutActiveRest));
    private var outPassiveRestWidth = strlen(string(m_fieldWidth.maxOutPassiveRest));

    constructor(resultFileName);
end;

/**
 * Баланс оборотов.
 */
class (TBalanceTemplateBuilder)TTurnBalanceTemplate()
    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(balanceHeader, balanceData);

        compositeTableField = m_compositeTableField.addField(inRestHeader);
        amountView(compositeTableField, activeHeader,  "inActiveRest",  Max(inActiveRestWidth,  Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + inActiveRestWidth / 3);
        amountView(compositeTableField, passiveHeader, "inPassiveRest", Max(inPassiveRestWidth, Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + inPassiveRestWidth / 3);

        amountView(m_compositeTableField, debetHeader,  "Debet",  Max(debetWidth,  Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + debetWidth / 3);
        amountView(m_compositeTableField, creditHeader, "Credit", Max(creditWidth, Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + creditWidth / 3);

        compositeTableField = m_compositeTableField.addField(outRestHeader);
        amountView(compositeTableField, activeHeader,  "outActiveRest",  Max(outActiveRestWidth,  Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + outActiveRestWidth / 3);
        amountView(compositeTableField, passiveHeader, "outPassiveRest", Max(outPassiveRestWidth, Max(strlen(currencyTitle), strlen(getNatCurrencyName()))) + outPassiveRestWidth / 3);
    end;

    initTBalanceTemplateBuilder();
end;

/**
 * Баланс оборотов без входящего остатка.
 */
class (TBalanceTemplateBuilder)TNotInRestTurnBalanceTemplate()
    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(balanceHeader, balanceData);

        amountView(m_compositeTableField, debetHeader,  "Debet", debetWidth + debetWidth / 3);
        amountView(m_compositeTableField, creditHeader, "Credit", creditWidth + creditWidth / 3);

        compositeTableField = m_compositeTableField.addField(outRestHeader);
        amountView(compositeTableField, activeHeader,  "outActiveRest", outActiveRestWidth + outActiveRestWidth / 3);
        amountView(compositeTableField, passiveHeader, "outPassiveRest", outPassiveRestWidth + outPassiveRestWidth / 3);
    end;

    initTBalanceTemplateBuilder();
end;

/**
 * Баланс остатков.
 */
class (TBalanceTemplateBuilder)TRestBalanceTemplate()
    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(balanceHeader, balanceData);

        compositeTableField = m_compositeTableField.addField(outRestHeader);
            amountView(compositeTableField, activeHeader,  "outActiveRest", outActiveRestWidth + outActiveRestWidth / 3);
            amountView(compositeTableField, passiveHeader, "outPassiveRest", outPassiveRestWidth + outPassiveRestWidth / 3);
    end;

    initTBalanceTemplateBuilder();
end;

/**
 * Баланс остатков с названиями б/с(актив).
 */
class (TBalanceTemplateBuilder)TActiveNameRestBalanceTemplate()

    /**
     * Печать шаблона итога по главе в файл шаблона.
     */
    private macro printChapterEndActive(kindAccount)
        var changeFields = TArray();
        changeFields(0) = arrCreate("balance", "Итого АКТИВ:");

        println();
        println("<FORM:EndChapter" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
        println();
    end;

    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(balanceNameHeader, balanceNameData);
        m_compositeTableField.addField(balanceHeader, balanceData);

        amountView(m_compositeTableField, activeHeader,  "outActiveRest", outActiveRestWidth + outActiveRestWidth / 3);
    end;

    initTBalanceTemplateBuilder();
end;

/**
 * Баланс остатков с названиями б/с(пассив).
 */
class (TBalanceTemplateBuilder)TPassiveNameRestBalanceTemplate()

    /**
     * Печать шаблона итога по главе в файл шаблона.
     */
    private macro printChapterEndPassive(kindAccount)
        var changeFields = TArray();
        changeFields(0) = arrCreate("balance", "Итого ПАССИВ:");

        println();
        println("<FORM:EndChapter" + NVL(kindAccount, "") + ">");
        println(m_compositeTableField.getChangeReportString(changeFields));
        println("</FORM>");
        println();
    end;

    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(balanceNameHeader, balanceNameData);
        m_compositeTableField.addField(balanceHeader, balanceData);

        amountView(m_compositeTableField, passiveHeader,  "outPassiveRest", outPassiveRestWidth + outPassiveRestWidth / 3);
    end;

    initTBalanceTemplateBuilder();
end;