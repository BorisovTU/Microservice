/*
$Name:         TbsView.mac
$Module:       Внутренняя отчетность
$Description:  Класс печати "ОСВ" по шаблону
*/
/**
 * Класс печати "ОСВ" по шаблону
 *
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */
import printableView;
import tbsParameters;

/**
 * Печать отчета "Баланс".
 */
class (TPrintableView) TTbsView(templateName : String)
    /**
     * Параметры отчета.
     */
    private var m_parameters : Object;
    private var m_separateLine : String;

    private macro printRow(name, dataset)
        m_printer.tableBlockPrint(name, dataSet);
    end;

    /**
     * Установить ширину отчета.
     */
    macro setReportWidth(reportWidth : Integer)
        var i = 1;
        file templateFile() txt;

        if (reportWidth == NULL)
            if (not open(templateFile, m_printer.getTemplateFileName))
                throw(TException("Ошибка открытия файла " + m_printer.getTemplateFileName, true));
            end;

            while (next(templateFile))
                if (templateFile.str == "<FORM:Header>")
                    next(templateFile);
                    m_reportWidth = strlen(templateFile.str);
                    break;
                end;

            end;
        else
            m_reportWidth = reportWidth;
        end;

        m_separateLine = " ";
        while(i < m_reportWidth)
            m_separateLine = m_separateLine + "-";
            i = i + 1;
        end;
    end;


    /**
     *  Название отчета.
     */
    macro getTbsFormText()
        var formId = tbsParameters().getReportForm();
        if ((formId == REP_OSV_FORM_REST) or (formId == REP_OSV_FORM_REST_NAME))
            return "Сальдовая ведомость";
        elif ((formId == REP_OSV_FORM_TURN) or (formId == REP_OSV_FORM_TURN_NAME))
            return "Оборотно-сальдовая ведомость";
        else
            return "";
        end;
    end;

    /**
     * Вид периода.
     */
    macro getPeriodTypeText()
        var monthNum;
        var year;

        var month = TArray(13);
        month(1) = " январь ";
        month(2) = " февраль ";
        month(3) = " март ";
        month(4) = " апрель ";
        month(5) = " май ";
        month(6) = " июнь ";
        month(7) = " июль ";
        month(8) = " август ";
        month(9) = " сентябрь ";
        month(10)= " октябрь ";
        month(11)= " ноябрь ";
        month(12)= " декабрь ";

        dateSplit(tbsParameters().getDateIn(), null, monthNum);
        dateSplit(tbsParameters().getDateIn(), null, null, year);

        if ((tbsParameters().getPeriodKind()  == REP_KINDPERIOD_DAY) or tbsParameters().getIsPrintEveryDay())
            return "за " + tbsParameters().getDateOut();
        elif (tbsParameters().getPeriodKind() == REP_KINDPERIOD_MONTH)
            return "за " + month(monthNum) + " " + year + "г.";
        elif (tbsParameters().getPeriodKind() == REP_KINDPERIOD_QUARTER)
            return "за " + (monthNum + 2)/3 + " квартал";
        elif (tbsParameters().getPeriodKind() == REP_KINDPERIOD_YEAR)
            return "за " + year + "г.";
        else
            return "c " + tbsParameters().getDateIn() + " по " + tbsParameters().getDateOut();
        end;
    end;

    /**
     *  Валюта пересчета
     */
    macro getRecalcCurrencyText()
        if (tbsParameters.getRecalcCurrency() >=0)
            var dataSet = TRsbDataSet("SELECT t_name name, t_fi_code code FROM dfininstr_dbt WHERE t_fiId = " + tbsParameters.getRecalcCurrency());
            if (dataSet.next())
                return "Пересчет: " + dataSet.code + " " + dataset.name;
            end;
            return "Пересчет: " + tbsParameters
        else
            return "";
        end;
    end;

    /**
     * Печать разделителя.
     */
    macro printSeparator()
        println(m_separateLine);
    end;

    /**
     * Печать шапки отчета.
     */
    macro printReportHead()
        var align = STR_ALIGN_CENTER;

        println(strAlign(getTbsFormText(), m_reportWidth, align));
        println(strAlign(getPeriodTypeText(), m_reportWidth, align));
    end;

    /**
     * Печать шапки главы.
     */
    private macro getHeadDataset(dataSet)
        var headDataSet= TRsbDataSet("("
                            + "\n" + " SELECT ('Глава '||t_symbol||'. '||T_NAME) t_name,"
                            + "\n" + "        0                                  t_sort"
                            + "\n" + "   FROM dobchaptr_dbt"
                            + "\n" + "  WHERE t_excludeFromFormalacnt != 'X' AND t_chapter = " + dataSet.chapter  //tbsParameters().getChapter()
                            + "\n" + "  UNION ALL"
                            + "\n" + " SELECT ('План счетов '|| plan.t_inumplan || '. ' || plan.t_nameplan),"
                            + "\n" + "        1"
                            + "\n" + "   FROM dnameplan_dbt plan"
                            + "\n" + "  WHERE plan.t_iNumPlan = " + tbsParameters().getPlan()
                            + "\n" + ")"
                            + "\n" + "ORDER BY t_sort"
                                    );

        return headDataSet;
    end;

    macro printChapterHead(dataSet)
        var align = STR_ALIGN_CENTER;

        printHead();

        var headDataSet = getHeadDataset(dataSet);
        while (headDataSet.next())
            println(strAlign(headDataSet.name, m_reportWidth, align));
        end;

        println(strAlign(getRecalcCurrencyText(), m_reportWidth, STR_ALIGN_LEFT));
    end;

    /**
     * Печать подписей.
     */
    private macro getBossString()
        return {Name_Boss}+" ___________________ "+{FIO_Boss};
    end;

    private macro getBookString()
        return {Name_Book}+" ___________________ "+{FIO_Book};
    end;

    macro printSignature()
        println();
        println(strAlign(getBossString(), m_reportWidth, STR_ALIGN_LEFT));
        println();
        println(strAlign(getBookString(), m_reportWidth, STR_ALIGN_LEFT));
        println();
    end;

    /**
     * Печать строки с оборотами СПОД.
     */
    macro printSpod(dataSet)
        if (tbsParameters().getIsPrintSpod() and ((dataSet.debetSpod != 0) or (dataSet.creditSpod != 0)))

            if ((not dataset.isCurrencyItog) or (tbsParameters().getFiId() == NATCUR))
                printRow("RowSpod", dataSet);
            elif ((tbsParameters().getShowNatcurEq()) or (tbsParameters.getRecalcCurrency() >= 0))
                printRow("RowCurCoverSpod", dataSet);
            end;
        end;
    end;

    /**
     * Начало печати данных по главе.
     */
    macro beginChapter(dataSet)
        printChapterHead(dataSet);
        printRow("Header", dataSet);
    end;

    /**
     * Окончание печати данных по главе.
     */
    macro endChapter(dataSet)
        if (tbsParameters().getFiId() == NATCUR)
            printRow("EndChapterNat", dataSet);
        elif ((tbsParameters().getShowNatcurEq()) or (tbsParameters.getRecalcCurrency() >= 0))
            printRow("EndChapterCurCover", dataSet);
        end;

        printSpod(dataSet);
        printSignature();
    end;

    /**
     * Начало печати данных по валюте.
     */
    macro beginCurrency(dataSet)
        printRow("HeaderCurrency", dataSet);
    end;

    /**
     * Окончание печати данных по валюте.
     */
    macro endCurrency(dataSet)
        printRow("EndCurrency", dataSet);
        printSpod(dataSet);
    end;

    /**
     * Начало печати раздела.
     */
    macro beginPart(dataSet)
        printRow("HeaderPart", dataSet);
    end;

    /**
     * Окончание печати раздела.
     */
    macro endPart(dataSet)
        printRow("EndPart", dataSet);
        printSpod(dataSet);
    end;

    /**
     * Печать итогов балансового 1-го порядка.
     */
    macro endBalance1(dataSet)
        printRow("EndBalance1", dataSet);
        printSpod(dataSet);
    end;

    /**
     * Печать итогов балансового 2-го порядка.
     */
    macro endBalance2(dataSet)
        printRow("EndBalance2", dataSet);
        printSpod(dataSet);
    end;

    /**
     * Печать строки по балансовому счету.
     */
    macro printAccount(dataSet)
        printRow("Row", dataSet);
        printSpod(dataSet);
    end;

    macro setFileName(fileName : String)
        return;
    end;

    /**
     *  Конструктор.
     */
    private macro constructor(templateName : String)
        initTPrintableView(tbsParameters().getDepartmentCode(),
                           tbsParameters().getOrgStructure(),
                           templateName);
        m_printer.setPrintingMoneyApostrophe(tbsParameters().getIsPrintApostrophe());
        m_printer.setPrintingMoneyDimention(ternary(twoDigitDimention, 2, 0));
    end;

    constructor(templateName);
end;
