/*
$Name:              TbsCalculator.mac
$Module:            Внутренняя отчетность
$Description:       Расчет ОСВ
*/
/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 * File Name   : TbsCalculator.mac
 * Description : Расчет ОСВ
 * Comment     :
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */

import oralib;
import Reporting;
import TbsParameters;




/**
 * Класс реализует расчет баланса банка
 *
 * @since   v.6.00.020.28
 * @author  ABP
 */
class TTbsCalculator()

    private const DAT_SIGN = 0;
    private const DAT_TYPE = 1;

    private var m_accountTypeDefinitionMethod : Integer;

    /**
     * Инициализация пакета rsb_turnoverbs
     */
    private macro initializePackage(chapterNumber)

        var cmd = RsdCommand("BEGIN rsb_turnoverbs.init(?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?); END;");

        var plan = tbsParameters().getPlan();

        var p_is_curr = ternary(tbsParameters().getFiId() == NATCUR, 0, 1);
        var p_codcurr = ternary(tbsParameters().getFiId() < 0, 0, tbsParameters().getFiId());
        var p_begdate = tbsParameters().getDateIn();
        var p_enddate = tbsParameters().getDateOut();
        var p_chapter = chapterNumber;
        var p_numplan = plan;
        var p_partbln = ternary(tbsParameters().getTotalsLevel() == ACC_SUM_OSV_SECT, 1, 0);
        var p_covsign = ternary(tbsParameters().getFiId() == NATCUR, tbsParameters().getNatcurRepAccounts(), 0);
        var p_bwpused = ternary(tbsParameters().getIsUseBwp(), 1, 0);
        var p_prnzero = ternary(tbsParameters().getIsPrintZeroData(), 1, 0);
        var p_showocp = ternary(tbsParameters().getIsExcludeOcp(), 0, 1);
        var p_realrub = ternary(tbsParameters().getFiId() != NATCUR, ternary(tbsParameters().getShowNatcurEq(), 1, 0), 0);
        var p_opernum = tbsParameters().getOper();
        var p_is_ogrp = ternary(tbsParameters().isGroup(), 1, 0);
        var p_clnt_id = tbsParameters().getClient();
        var p_defacct = m_accountTypeDefinitionMethod;
        var p_balmask = tbsParameters().getBalanceMask(); // string
        var p_accmask = tbsParameters().getAccountMask(); // string
        var p_typeacc = tbsParameters().getAccType();     // string
        var p_usrtacc = tbsParameters().getAccUserType(); // string
        var p_usrtmethod = tbsParameters().getAccUserTypeMethod();
        var p_useDepartmentFilter = ternary(tbsParameters().useDepartmentFilter(), 1, 0);


        cmd.addParam("p_is_curr",    RSDBP_IN, p_is_curr);
        cmd.addParam("p_codcurr",    RSDBP_IN, p_codcurr);
        cmd.addParam("p_begdate",    RSDBP_IN, p_begdate);
        cmd.addParam("p_enddate",    RSDBP_IN, p_enddate);
        cmd.addParam("p_chapter",    RSDBP_IN, p_chapter);
        cmd.addParam("p_numplan",    RSDBP_IN, p_numplan);
        cmd.addParam("p_partbln",    RSDBP_IN, p_partbln);
        cmd.addParam("p_covsign",    RSDBP_IN, p_covsign);
        cmd.addParam("p_bwpused",    RSDBP_IN, p_bwpused);
        cmd.addParam("p_prnzero",    RSDBP_IN, p_prnzero);
        cmd.addParam("p_showocp",    RSDBP_IN, p_showocp);
        cmd.addParam("p_realrub",    RSDBP_IN, p_realrub);
        cmd.addParam("p_opernum",    RSDBP_IN, p_opernum);
        cmd.addParam("p_is_ogrp",    RSDBP_IN, p_is_ogrp);
        cmd.addParam("p_clnt_id",    RSDBP_IN, p_clnt_id);
        cmd.addParam("p_defacct",    RSDBP_IN, p_defacct);
        cmd.addParam("p_balmask",    RSDBP_IN, p_balmask);
        cmd.addParam("p_accmask",    RSDBP_IN, p_accmask);
        cmd.addParam("p_typeacc",    RSDBP_IN, p_typeacc);
        cmd.addParam("p_usrtacc",    RSDBP_IN, p_usrtacc);
        cmd.addParam("p_usrtmethod", RSDBP_IN, p_usrtmethod);
        cmd.addParam("p_useDepartmentFilter", RSDBP_IN, p_useDepartmentFilter);

        sql_execute(cmd);

    end;

    /**
     * Инициализация параметров пересчета
     */
    private macro initializeRecalculation() // dblrate_tmp
    end;

    /**
     * Инициализация списка подразделений банка для фильтрации счетов баланса по ТС
     */
    private macro initializeDepartmentFilter()
        tbsParameters().getDepartmentList().saveToTable();
    end;

    /**
     * Инициализация фильтра счетов ОВП
     */

    private macro initializeOcpAccountFilter()
        tbsParameters().getOcpAccountServer().saveToTable();
    end;

    /**
     * Конструктор
     */
    private macro constructor()

        var errorCode;
        var accountTypeDefinitionMethod;

        sql_execute("BEGIN rsb_turnoverbs.clear(); END;");

        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ПЕРЕМЕННЫЕ\\DEFINEACCOUNTTYPE", V_STRING, accountTypeDefinitionMethod, errorCode);
        if (errorCode == 0)
            m_accountTypeDefinitionMethod = ternary(trim(strUpr(accountTypeDefinitionMethod)) == "TYPE", DAT_TYPE, DAT_SIGN);
        else
            m_accountTypeDefinitionMethod = DAT_TYPE;
        end;

        initializeRecalculation();

        initializeDepartmentFilter();

        initializeOcpAccountFilter();

    end;

    /**
     * Расчет баланса по главе
     */
    private macro calculateChapterBalance(chapterNumber : Integer)
        initializePackage(chapterNumber);
        sql_execute("BEGIN rsb_turnoverbs.calculate(); END;");
    end;

    /**
     * Расчет ОСВ
     */
    macro calculate()
        begAction(200, "Расчет ОСВ");

        var chapterNumber = tbsParameters().getChapter();
        var plan = tbsParameters().getPlan();

        if (chapterNumber > 0)

            calculateChapterBalance(chapterNumber);

        else

            chapterNumber = 1;
            while (chapterNumber <= 5)

                calculateChapterBalance(chapterNumber);
                chapterNumber = chapterNumber + 1;

            end;

        end;

        endAction();

    end;

    constructor();

end;
