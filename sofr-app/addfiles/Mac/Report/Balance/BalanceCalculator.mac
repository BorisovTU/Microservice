/*
$Name:              BalanceCalculator.mac
$Module:            Внутренняя отчетность
$Description:       Расчет валютного баланса
*/

/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 *  File Name   : BalanceCalculator.mac                      February 12, 2008
 *  Programmer  : ABP
 *  Description : Расчет валютного баланса
 *  Comment     :
 *  Modify      :
 */

import cb_sql;

import Reporting;

import BalanceParameters;

/**
 * Класс реализует расчет баланса банка
 *
 * @since   v.6.00.020.28
 * @author  ABP
 */
class TBalanceCalculator()

    private const DAT_SIGN = 0;

    private const DAT_TYPE = 1;

    private var m_accountTypeDefinitionMethod : Integer;

    /**
     * Инициализация пакета rsb_balance
     */
    private macro initializePackage(chapterNumber)

        var cmd = RsdCommand("BEGIN rsb_balance.init(?,?,?,?,?,?,?,?,?,?,?,?,?,?); END;");

        var plan = balanceParameters().getPlan();

        var p_is_curr = ternary(balanceParameters().getFiId() == NATCUR, 0, 1);
        var p_codcurr = ternary(balanceParameters().getFiId() < 0, 0, balanceParameters().getFiId());
        var p_begdate = balanceParameters().getDateIn();
        var p_enddate = balanceParameters().getDateOut();
        var p_chapter = chapterNumber;
        var p_numplan = plan;
        var p_partbln = ternary(balanceParameters().getIsPrintIssueItog(), 1, 0);
        var p_covsign = 0;
        var p_bwpused = 0;
        var p_prnzero = ternary(balanceParameters().getIsPrintZeroData(), 1, 0);
        var p_showocp = ternary(balanceParameters().getIsExcludeOcp(), 0, 1);
        var p_realrub = 1;
        var p_defacct = m_accountTypeDefinitionMethod;
        var p_useDepartmentFilter = ternary(balanceParameters().useDepartmentFilter(), 1, 0);


        cmd.addParam("p_is_curr", RSDBP_IN, p_is_curr);
        cmd.addParam("p_codcurr", RSDBP_IN, p_codcurr);
        cmd.addParam("p_begdate", RSDBP_IN, p_begdate);
        cmd.addParam("p_enddate", RSDBP_IN, p_enddate);
        cmd.addParam("p_chapter", RSDBP_IN, p_chapter);
        cmd.addParam("p_numplan", RSDBP_IN, p_numplan);
        cmd.addParam("p_partbln", RSDBP_IN, p_partbln);
        cmd.addParam("p_covsign", RSDBP_IN, p_covsign);
        cmd.addParam("p_bwpused", RSDBP_IN, p_bwpused);
        cmd.addParam("p_prnzero", RSDBP_IN, p_prnzero);
        cmd.addParam("p_showocp", RSDBP_IN, p_showocp);
        cmd.addParam("p_realrub", RSDBP_IN, p_realrub);
        cmd.addParam("p_defacct", RSDBP_IN, p_defacct);
        cmd.addParam("p_useDepartmentFilter", RSDBP_IN, p_useDepartmentFilter);

        sql_execute(cmd);

    end;

    /**
     * Инициализация параметров пересчета
     */
    private macro initializeRecalculation()
        // currently do nothing
    end;

    /**
     * Инициализация списка подразделений банка для фильтрации счетов баланса по ТС
     */
    private macro initializeDepartmentFilter()
        balanceParameters().getDepartmentList().saveToTable();
    end;

    /**
     * Инициализация фильтра счетов ОВП
     */
    private macro initializeOcpAccountFilter()
        balanceParameters().getOcpAccountServer().saveToTable();
    end;

    /**
     * Конструктор
     */
    private macro constructor()

        var errorCode;
        var accountTypeDefinitionMethod;

        sql_execute("BEGIN rsb_balance.clear(); END;");

        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ПЕРЕМЕННЫЕ\\DEFINEACCOUNTTYPE", V_STRING, accountTypeDefinitionMethod, errorCode);
        if (errorCode == 0)
            m_accountTypeDefinitionMethod = ternary(trim(strUpr(accountTypeDefinitionMethod)) == "TYPE", DAT_TYPE, DAT_SIGN);
        else
            m_accountTypeDefinitionMethod = DAT_TYPE;
        end;

        initializeRecalculation();

        initializeDepartmentFilter();

        initializeOcpAccountFilter();

    end;

    /**
     * Расчет баланса по главе
     */
    private macro calculateChapterBalance(chapterNumber : Integer)
        initializePackage(chapterNumber);

        sql_execute("BEGIN rsb_balance.calculate(); END;");

        if (balanceParameters().getMoneyDimension() == REP_BALANCE_DIMENSION_THOUSAND)
            sql_execute("BEGIN rsb_balance.round(); END;");

            if (m_accountTypeDefinitionMethod == DAT_TYPE)
                sql_execute("BEGIN rsb_balance.rollupRests(); END;");
            end;
        end;
    end;

    /**
     * Расчет баланса
     */
    macro calculate()

        var plan = balanceParameters().getPlan();
        var chapterNumber = balanceParameters().getChapter();

        if (chapterNumber > 0)

            calculateChapterBalance(chapterNumber);

        else

            chapterNumber = 1;
            while (chapterNumber <= 5)
                calculateChapterBalance(chapterNumber);
                chapterNumber = chapterNumber + 1;
            end;

        end;

    end;

    constructor();

end;
