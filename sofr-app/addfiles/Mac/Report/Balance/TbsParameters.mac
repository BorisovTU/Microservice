/*
$Name:              TbsParameters.mac
$Module:            Внутренняя отчетность
$Description:       Параметры ОСВ
*/
/**
 * Module:  TbsParameters.mac
 * Purpose: Declaration of the class TurnoverBsParameters
 *          Реализует порождающий паттерн "Одиночка" (см. Related Diagrams).
 * Comment: Параметры отчета.
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */
import ReportInter;
import repException;

import Reporting, ReportInter, Календарь, FIInter, PTInter, CTInter,
       PaymInter, OprInter, CurrInter, BankInter;
import cb_sql, globals, lib_str, lib_lang, lib_arr;
import acv;
import RsbObjFactory;

import param;

private const NODETYPE_FILIAL = 1, //Тип узла ТС - Филиал
              NODETYPE_BRANCH = 2; //Тип узла ТС - Подразделение

private const ORGSTRUCTURE_TERRITORIAL = 1; //Территориальная структура

private const MODE_BRANCH = 1; // Режим выпуска отчета - Подразделение
private const MODE_FILIAL = 2; // Режим выпуска отчета - Филиал
private const MODE_BANK   = 3; // Режим выпуска отчета - Банк

var TBS_USE_REPDEPARTMENTLIST = true;
if (GetRegistryValue("REPORT\\DISABLE DepartmentList 3", V_BOOL, TBS_USE_REPDEPARTMENTLIST) != null)
    TBS_USE_REPDEPARTMENTLIST = not TBS_USE_REPDEPARTMENTLIST;
else
    TBS_USE_REPDEPARTMENTLIST = true;
end;

private const ALLCURRENCY = -2;

const twoDigitDimention = true;

private class TDepartmentList(orgStructure : Integer, issueMode : Integer, departmentCode : Integer)
    private var m_departmentList;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_departmentCode;
    private var m_sourceTableName : String;
    private var m_resultTableName : String;

    private macro fillDepartmentTable()
        var strFilter : String;

        if (m_issueMode == MODE_BRANCH)
            strFilter = "(repDep.t_code = " + m_departmentCode + ")";
        elif (m_issueMode == MODE_FILIAL)
            strFilter = "(repDep.t_nodeType = " + NODETYPE_BRANCH + " OR repDep.t_code = " + m_departmentCode + ")";
        elif (m_issueMode == MODE_BANK)
            strFilter = "(1 = 1)";
        end;

        var strCmd = "INSERT INTO " + m_resultTableName + "(t_code, t_listType)"
            + "\n" + "SELECT DISTINCT repDep.t_code,                           "
            + "\n" + "                repDep.t_nodeType                        "
            + "\n" + "       FROM " + m_sourceTableName + " repDep             "
            + "\n" + " WHERE " + strFilter
            + "\n" + " START WITH repDep.t_code = " + m_departmentCode
            + "\n" + " CONNECT BY PRIOR repDep.t_nodeId = repDep.t_parentNodeId";

        sql_execute(strCmd);
    end;

    private macro clearTable()
        sql_execute("TRUNCATE TABLE " + m_resultTableName );
    end;

    macro saveToTable()
        if (TBS_USE_REPDEPARTMENTLIST)
            m_departmentList.saveToTable();
        else
            fillDepartmentTable();
        end;
    end;

    macro getDepartmentListObject()
        return m_departmentList;
    end;

    macro constructorTDepartmentList(orgStructure : Integer, issueMode : Integer, departmentCode : Integer)
        m_orgStructure   = orgStructure;
        m_issueMode      = issueMode;
        m_departmentCode = departmentCode;
        m_resultTableName = "dbldept_tmp";

        if (TBS_USE_REPDEPARTMENTLIST)
            m_departmentList = RepDepartmentList(orgStructure, issueMode, departmentCode);
        else
            m_departmentList = NULL;
            m_sourceTableName = ternary(orgStructure == ORGSTRUCTURE_TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw");

            clearTable();
        end;
    end;

    private macro destructor()
        clearTable();
    end;

    constructorTDepartmentList(orgStructure, issueMode, departmentCode);
end;

private class TOcpAccountServer(chapter : Integer, finInstrument : Integer, departmentList : Object)
    private var m_ocpAccountServer;
    private var m_chapter;
    private var m_finInstrument;
    private var m_departmentList;
    private var m_resultTableName : String;
    private var m_departmentListTableName : String;

    private macro fillOcpAccountServerTable()
        var strFilter : String = "";

        if (m_chapter > 0)
            strFilter = "AND account.t_chapter = " + m_chapter;
        end;

        if (m_finInstrument == ALLFININSTR)
            strFilter = strFilter + "AND (1 = 1)";
        elif (m_finInstrument == ALLCURRENCY)
            strFilter = strFilter + " AND account.t_code_currency != " + NATCUR;
        else
            strFilter = strFilter + " AND account.t_code_currency = " + m_finInstrument;
        end;

        var strCmd = "INSERT INTO " + m_resultTableName + "(t_chapter, t_fiId, t_account, t_accountId)"
            + "\n" + "SELECT account.t_chapter       t_chapter,"
            + "\n" + "       account.t_code_currency t_fiId,"
            + "\n" + "       account.t_account       t_account,"
            + "\n" + "       account.t_accountId     t_accountId"
            + "\n" + "  FROM daccount_dbt account"
            + "\n" + " WHERE INSTR(account.t_type_account, 'Щ') > 0"
            + "\n" + "   AND EXISTS(SELECT 1 "
            + "\n" + "                FROM " + m_departmentListTableName + " dep"
            + "\n" + "               WHERE account.t_branch = dep.t_code)"
            + "\n" +         strFilter;


        sql_execute(strCmd);
    end;

    private macro clearTable()
        sql_execute("TRUNCATE TABLE " + m_resultTableName );
    end;

    macro saveToTable()
        if (TBS_USE_REPDEPARTMENTLIST)
            m_ocpAccountServer.saveToTable();
        else
//            fillOcpAccountServerTable();
        end;
    end;

    macro constructorTOcpAccountServer(chapter : Integer, finInstrument : Integer, departmentList : Object)
        m_chapter        = chapter;
        m_finInstrument  = finInstrument;
        m_departmentList = departmentList;
        m_departmentListTableName = "dbldept_tmp";
        m_resultTableName = "dRepOcp_tmp";

        if (TBS_USE_REPDEPARTMENTLIST)
            m_ocpAccountServer = RepOcpAccountServer(chapter, finInstrument, departmentList);
        else
            m_ocpAccountServer = NULL;

            clearTable();
        end;
    end;

    private macro destructor()
        clearTable();
    end;

    constructorTOcpAccountServer(chapter, finInstrument, departmentList);
end;

/**
 * Глобальная ссылка для хранения одиночки.
 */
private var m_parameters = null;
/**
 * Параметры отчета.
 */
private class TTurnoverBsParameters(parmArray : TArray)
    /**
     * Код подразделения.
     */
    private var m_departmentCode : Integer;

    /**
     * Оргструктура.
     */
    private var m_orgStructure : Integer;

    /**
     * Режим выпуска.
     */
    private var m_issueMode : Integer;

    /**
     * Глава счетов.
     */
    private var m_chapter : Integer;

    /**
     * Номер плана счетов.
     */
    private var m_plan : Integer;

    /**
     * Валюта.
     */
    private var m_fiId : Integer;

    /**
     * Дата начала отчетного периода.
     */
    private var m_dateIn : Date;

    /**
     * Дата конца отчетного периода.
     */
    private var m_dateOut : Date;

    /**
     * Форма отчета.
     */
    private var m_reportForm : Integer;

    /**
     * Уровень итогов
     */
    private var m_totalsLevel : Integer;

    /**
     * Счета, включаемые в отчет по нац. валюте.
     */
    private var m_natcurRepAccounts : Integer;

    /**
     * Вывод эквивалена в нац. валюте для отчета в ин. валюте.
     */
    private var m_showNatcurEq : Bool;

    /**
     * Признак выпуска отчета по группе операционистов
     */
    private var m_isGroup : Bool;

    /**
     * Операционист.
     */
    private var m_oper : Integer;

    /**
     * Клиент.
     */
    private var m_client : Integer;

    /**
     * Маска балансового счета.
     */
    private var m_balanceMask : String;

    /**
     * Маска лицевого счета.
     */
    private var m_accountMask : String;

    /**
     * Системный тип счета.
     */
    private var m_accType : String;

    /**
     * Пользовательский тип счета.
     */
    private var m_accUserType : String;

    /**
     * Пользовательский метод отбора счетов.
     */
    private var m_accUserTypeMethod : Integer;

    /**
     * Учет Спод?
     */
    private var m_isPrintSpod : Bool;

    /**
     * С апострофами?
     */
    private var m_isPrintApostrophe : Bool;

    /**
     * Печатать нулевые данные?
     */
    private var m_isPrintZeroData : Bool;

    /**
     * Исключать нулевые обороты?
     */
    private var m_isExcludeZeroTurns : Bool;

    /**
     * Исключать нулевые остатки?
     */
    private var m_isExcludeZeroRests : Bool;

    /**
     * Печать отчета за каждый день периода?
     */
    private var m_isPrintEveryDay : Bool;

    /**
     * Использовать РПС?
     */
    private var m_isUseBwp : Bool;

    /**
     * Исключить счета ОВП?
     */
    private var m_isExcludeOcp : Bool;

    /**
     * Итоги по разделам
     */
    private var m_isPrintIssueItog : Bool;
    /**
     * Шаблон сортировки
     */
    private var m_sortPattern : TArray;

    private var m_departmentList : Object;

    private var m_periodKind : Integer;

    private var m_ocpAccountServer : Object;

    private var m_needPlainText : Bool;

    private var m_needXls : Bool;

    /**
    * Валюта пересчета
    */
    private var m_recalcCurrency : Integer;


    /**
     * @param     m_departmentCode
     * @param     m_orgStructure
     * @param     m_issueMode
     * @param     m_chapter
     * @param     m_plan
     * @param     m_fiId
     * @param     m_periodKind
     * @param     m_dateIn
     * @param     m_dateOut
     * @param     m_reportForm
     * @param     m_totalsLevel
     * @param     m_natcurRepAccounts
     * @param     m_showNatcurEq
     * @param     m_recalcCurrency
     * @param     m_isGroup
     * @param     m_oper
     * @param     m_client
     * @param     m_balanceMask
     * @param     m_accountMask
     * @param     m_accType
     * @param     m_accUserType
     * @param     m_accUserTypeMethod
     * @param     m_isPrintSpod
     * @param     m_isPrintApostrophe
     * @param     m_isPrintZeroData
     * @param     m_isExcludeZeroTurns
     * @param     m_isExcludeZeroRests
     * @param     m_isPrintEveryDay
     * @param     m_isUseBwp
     * @param     m_isExcludeOcp
     * @param     m_sortPattern
     * @param     m_needPlainText
     * @param     m_needXls
     */
    private macro constructor(parmArray : TArray)
        var i = -1;

        m_departmentCode     = parmArray[i=i+1];
        m_orgStructure       = parmArray[i=i+1];
        m_issueMode          = parmArray[i=i+1];
        m_chapter            = parmArray[i=i+1];
        m_plan               = parmArray[i=i+1];
        m_fiId               = parmArray[i=i+1];
        m_periodKind         = parmArray[i=i+1];
        m_dateIn             = parmArray[i=i+1];
        m_dateOut            = parmArray[i=i+1];
        m_reportForm         = parmArray[i=i+1];
        m_totalsLevel        = parmArray[i=i+1];
        m_natcurRepAccounts  = parmArray[i=i+1];
        m_showNatcurEq       = parmArray[i=i+1];
        m_recalcCurrency     = parmArray[i=i+1];
        m_isGroup            = parmArray[i=i+1];
        m_oper               = parmArray[i=i+1];
        m_client             = parmArray[i=i+1];
        m_balanceMask        = parmArray[i=i+1];
        m_accountMask        = parmArray[i=i+1];
        m_accType            = parmArray[i=i+1];
        m_accUserType        = parmArray[i=i+1];
        m_accUserTypeMethod  = parmArray[i=i+1];
        m_isPrintSpod        = parmArray[i=i+1];
        m_isPrintApostrophe  = parmArray[i=i+1];
        m_isPrintZeroData    = parmArray[i=i+1];
        m_isExcludeZeroTurns = parmArray[i=i+1];
        m_isExcludeZeroRests = parmArray[i=i+1];
        m_isPrintEveryDay    = parmArray[i=i+1];
        m_isUseBwp           = parmArray[i=i+1];
        m_isExcludeOcp       = parmArray[i=i+1];
        m_sortPattern        = parmArray[i=i+1];
        m_needPlainText      = parmArray[i=i+1];
        m_needXls            = parmArray[i=i+1];

        m_departmentList   = TDepartmentList(m_orgStructure, m_issueMode, m_departmentCode);
        m_ocpAccountServer = TOcpAccountServer(m_chapter, ALLFININSTR, m_departmentList.getDepartmentListObject());
    end;

    macro getDepartmentCode() : Integer
        return m_departmentCode;
    end;

    macro getOrgStructure() : Integer
        return m_orgStructure;
    end;

    macro getIssueMode() : Integer
        return m_issueMode;
    end;

    macro getChapter() : Integer
        return m_chapter;
    end;

    macro getPlan() : Integer
        return m_plan;
    end;

    macro getFiId() : Integer
        return m_fiId;
    end;

    macro getDateIn() : Date
        return m_dateIn;
    end;

    macro getDateOut() : Date
        return m_dateOut;
    end;

    macro getReportForm() : Integer
        return m_reportForm;
    end;

    macro getTotalsLevel() : Integer
        return m_totalsLevel;
    end;

    macro getNatcurRepAccounts() : Integer
        return m_natcurRepAccounts;
    end;

    macro getShowNatcurEq() : Bool
        return m_showNatcurEq;
    end;

    macro isGroup() : Bool
        return m_isGroup;
    end;

    macro getOper() : Integer
        return m_oper;
    end;

    macro getClient() : Integer
        return m_client;
    end;

    macro getBalanceMask() : String
        return m_balanceMask;
    end;

    macro getAccountMask() : String
        return m_accountMask;
    end;

    macro getAccType() : String
        return m_accType;
    end;

    macro getAccUserType() : String
        return m_accUserType;
    end;

    macro getAccUserTypeMethod() : String
        return m_accUserTypeMethod;
    end;

    macro getIsPrintSpod() : Bool
        return m_isPrintSpod;
    end;

    macro getIsPrintApostrophe() : Bool
        return m_isPrintApostrophe;
    end;

    macro getIsPrintZeroData() : Bool
        return m_isPrintZeroData;
    end;

    macro getIsExcludeZeroTurns() : Bool
        return m_isExcludeZeroTurns;
    end;

    macro getIsExcludeZeroRests() : Bool
        return m_isExcludeZeroRests;
    end;

    macro getIsPrintEveryDay() : Bool
        return m_isPrintEveryDay;
    end;

    macro getIsUseBwp() : Bool
        return m_isUseBwp;
    end;

    macro getIsExcludeOcp() : Bool
        return m_isExcludeOcp;
    end;

    macro getSortPattern() : TArray
        return m_sortPattern;
    end;

    macro getDepartmentList() : Object
        return m_departmentList;
    end;

    macro getPeriodKind() : Integer
        return m_periodKind;
    end;

    macro getOcpAccountServer() : Object
        return m_ocpAccountServer;
    end;

    macro needPlainText()
        return m_needPlainText;
    end;

    macro needXls()
        return m_needXls;
    end;

    macro getRecalcCurrency()
        return m_recalcCurrency;
    end;

    /**
     * @param     newM_dateIn
     */
    macro setDateIn(newM_dateIn : Date)
        m_dateIn = newM_dateIn;
    end;

    /**
     * @param     newM_dateOut
     */
    macro setDateOut(newM_dateOut : Date)
        m_dateOut = newM_dateOut;
    end;

    macro useDepartmentFilter()
        var queryString = "SELECT t_code "
                 + "\n" + "  FROM " + ternary(m_orgStructure == ORGSTRUCTURE_TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw")
                 + "\n" + " WHERE t_partyId = " + {OurBank};

        var ds = TRsbDataSet(queryString);

        if (ds.moveNext())
            return not ((ds.code == getDepartmentCode()) and (getIssueMode() == MODE_BANK));
        end;

        return true;
    end;

    constructor(parmArray);
end;

/**
 * Точка доступа.
 */
macro tbsParameters() : TTurnoverBsParameters
    return m_parameters;
end;

//инициализация
macro initTbsParameters()
    var i = 0;
    var parmArray = TArray();

    if (parmCount() == 0)
        throw(TWrongParametersCountException(tbsParameters));
    end;

    while (i < parmCount())
        getParm(i, parmArray[i]);
        i = i + 1;
    end;

    m_parameters = TTurnoverBsParameters(parmArray);
end;
