/*
$Name:          BalanceView.mac
$Module:        Внутренняя отчетность
$Description:   Класс печати "Баланс" (валюта)
*/

/**
 * Базовый класс печати отчета "Баланс"(валюта)
 *
 * @since   07.02.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 * MODIFICATIONS:
    18.04.2012 - SCR 171389. Баланс рублёвый. При выпуске за квартал неверно определяется номер
 */
import printableView;
import balanceParameters;

/**
 * Печать отчета "Баланс".
 */
class (TPrintableView) TBalanceView(templateName : String)

    /**
     * Установить ширину отчета.
     */
    macro setReportWidth(reportWidth : Integer, kindAccount)
        file templateFile() txt;

        if (reportWidth == NULL)
            if (not open(templateFile, m_printer.getTemplateFileName))
                throw(TException("Ошибка открытия файла " + m_printer.getTemplateFileName, true));
            end;

            while (next(templateFile))
                if (templateFile.str == "<FORM:Header" + NVL(kindAccount, "") + ">")
                    next(templateFile);
                    m_reportWidth = strlen(templateFile.str);
                    break;
                end;

            end;
        else
            m_reportWidth = reportWidth;
        end;
    end;

    /**
     *  Название отчета.
     */
    macro getBalanceFormText()
        var formId = balanceParameters.getBalanceForm();
        if   ((formId == REP_BALANCE_FORM_REST) or (formId == REP_BALANCE_FORM_REST_NAME))
            return "Баланс остатков";
        elif ((formId == REP_BALANCE_FORM_TURN) or (formId == REP_BALANCE_FORM_TURN_NOIN))
            return "Баланс оборотов";
        else
            return "";
        end;
    end;

    /**
     * Вид периода.
     */
    macro getPeriodTypeText()
        var monthNum;
        var year;

        var month = TArray(13);
        month(1) = " январь ";
        month(2) = " февраль ";
        month(3) = " март ";
        month(4) = " апрель ";
        month(5) = " май ";
        month(6) = " июнь ";
        month(7) = " июль ";
        month(8) = " август ";
        month(9) = " сентябрь ";
        month(10)= " октябрь ";
        month(11)= " ноябрь ";
        month(12)= " декабрь ";

        dateSplit(balanceParameters.getDateIn, null, monthNum);
        dateSplit(balanceParameters.getDateIn, null, null, year);

        if   (balanceParameters.getPeriodKind   == REP_KINDPERIOD_DAY)
            return "за " + balanceParameters.getDateOut;
        elif (balanceParameters.getPeriodKind == REP_KINDPERIOD_MONTH)
            return "за " + month(monthNum) + " " + year + " г.";
        elif (balanceParameters.getPeriodKind == REP_KINDPERIOD_QUARTER)
            return "за " + (monthNum + 2)/3 + " квартал " + year + " г." ;
        elif (balanceParameters.getPeriodKind == REP_KINDPERIOD_YEAR)
            return "за " + year + " г.";
        else
            return "c "  + balanceParameters.getDateIn + " по " + balanceParameters.getDateOut;
        end;
    end;

    /**
     * Печать шапки отчета.
     *
     */
    macro printReportHead()
        var align = STR_ALIGN_CENTER;
        var dataSet;

        println(strAlign(getBalanceFormText(), m_reportWidth, align));
        println(strAlign(getPeriodTypeText(), m_reportWidth, align));
    end;

    /**
     * Печать подписей.
     */
    macro printSignature()
        println();
        println(strAlign({Name_Boss}+" ___________________ "+{FIO_Boss}, m_reportWidth, STR_ALIGN_LEFT));
        println(strAlign({Name_Book}+" ___________________ "+{FIO_Book}, m_reportWidth, STR_ALIGN_LEFT));
        println();
    end;

    /**
     * Печать строки с оборотами СПОД.
     */
    macro printSpod(dataSet, kindAccount)
        if (balanceParameters.getIsPrintSpod and ((dataSet.debetSpod != 0) or (dataSet.creditSpod != 0)))
            m_printer.tableBlockPrint("RowSpod" + NVL(kindAccount, ""), dataSet);
        end;
    end;

    /**
     * Печать итоговой строки с оборотами СПОД по разным валютам.
     */
    macro printTotalCurSpod(dataSet, kindAccount)
        if (balanceParameters.getIsPrintSpod and ((dataSet.debetSpod != 0) or (dataSet.creditSpod != 0)))
            m_printer.tableBlockPrint("RowSpodCurTotal" + NVL(kindAccount, ""), dataSet);
        end;
    end;

    /**
     * Начало печати отчета.
     */
    macro beginReport(dataSet, kindAccount)
        m_printer.tableBlockPrint("Header" + NVL(kindAccount, ""), dataSet);
    end;

    /**
     * Начало печати данных по главе.
     */
    macro beginChapter(dataSet, kindAccount)
        m_printer.tableBlockPrint("HeaderChapter" + NVL(kindAccount, ""), dataSet);
    end;

    /**
     * Окончание печати данных по главе.
     */
    macro endChapter(dataSet, kindAccount)
        if (balanceParameters.getIsPrintCover)
            m_printer.tableBlockPrint("End" + NVL(kindAccount, ""), dataSet);
            printTotalCurSpod(dataSet);
        end;
    end;

    /**
     * Начало печати данных по валюте.
     */
    macro beginCurrency(dataSet, kindAccount)
        m_printer.tableBlockPrint("HeaderCurrency" + NVL(kindAccount, ""), dataSet);
    end;

    /**
     * Окончание печати данных по валюте.
     */
    macro endCurrency(dataSet, kindAccount)
        m_printer.tableBlockPrint("EndCurrency" + NVL(kindAccount, ""), dataSet);
        printSpod(dataSet);
    end;

    /**
     * Начало печати раздела.
     */
    macro beginIssue(dataSet, kindAccount)
        m_printer.tableBlockPrint("HeaderIssue" + NVL(kindAccount, ""), dataSet);
    end;

    /**
     * Окончание печати раздела.
     */
    macro endIssue(dataSet, kindAccount)
        m_printer.tableBlockPrint("EndIssue" + NVL(kindAccount, ""), dataSet);
        printSpod(dataSet);
    end;

    /**
     * Печать строки по балансовому счету.
     */
    macro printBalance(dataSet, kindAccount)
        m_printer.tableBlockPrint("Row" + NVL(kindAccount, ""), dataSet);
        printSpod(dataSet);
    end;

    /**
     *  Конструктор.
     */
    private macro constructor(templateName : String)
        initTPrintableView(balanceParameters.getDepartmentCode(), balanceParameters.getOrgStructure(), templateName);
        m_printer.setPrintingMoneyApostrophe(balanceParameters.getIsPrintApostrophe());
        m_printer.setPrintingMoneyDimention(ternary(balanceParameters.getMoneyDimension() == REP_BALANCE_DIMENSION_THOUSAND, 0, 2));
    end;

    constructor(templateName);
end;

//данные по главе баланса
private class TChapter(number, name, symbol)
    var m_number = number;
    var m_name   = name;
    var m_symbol = symbol;
    var m_title  = "Глава " + symbol;
end;

//класс справочника глав баланса
private class TChaptersBook()
    private var m_chapters = TArray();

    macro getChaptersAmount()
        return m_chapters.size;
    end;

    macro getNumberByOrder(order)
        return m_chapters[order - 1].m_number;
    end;

    macro getNameByNumber(number : integer)
        for (var i, 0, m_chapters.size - 1, 1)
            if (m_chapters[i].m_number == number)
                return m_chapters[i].m_name;
            end;
        end;

        return null;
    end;

    macro getTitleByNumber(number : integer)
        for (var i, 0, m_chapters.size - 1, 1)
            if (m_chapters[i].m_number == number)
                return m_chapters[i].m_title;
            end;
        end;

        return null;
    end;

    macro getSymbolByNumber(number : integer)
        for (var i, 0, m_chapters.size - 1, 1)
            if (m_chapters[i].m_number == number)
                return m_chapters[i].m_symbol;
            end;
        end;

        return null;
    end;

    macro getNumberBySymbol(symbol : String)
        for (var i, 0, m_chapters.size - 1, 1)
            if (m_chapters[i].m_symbol == symbol)
                return m_chapters[i].m_number;
            end;
        end;

        return null;
    end;

    private macro constructor()
        var dataSet;
        var query = "SELECT t_chapter,   "
        + "\n" + "          t_name,      "
        + "\n" + "          t_symbol     "
        + "\n" + "     FROM dobchaptr_dbt"
        + "\n" + "    WHERE t_excludeFromFormalacnt != 'X'";

        if (balanceParameters.getChapter() != 0)
            query = query
            + "\n" + "  AND t_chapter = " + balanceParameters.getChapter();
        end;

        query = query
        + "\n" + "    ORDER BY t_chapter ";

        dataSet = TRsbDataSet(query);

        while (dataSet.Next())
            m_chapters[m_chapters.size] = TChapter(dataSet.chapter, dataSet.name, dataSet.symbol);
        end;
    end;

    constructor();
end;

//класс печати отчета в формате Excel
class (TBalanceView) TBalanceExcelView(isActivePart : Bool)
    /* Перечисления для коллекции Borders */
    private const xlEdgeTop          = 8;
    private const xlEdgeBottom       = 9;
    private const xlEdgeRight        = 10;
    private const xlEdgeLeft         = 7;
    private const xlDiagonalDown     = 5;
    private const xlDiagonalUp       = 6;
    private const xlInsideVertical   = 11;
    private const xlInsideHorizontal = 12;

    /* Толщина линий */
    private const xlHairline = 1;     /* Очень тонкая */
    private const xlThin     = 2;     /* Тонкая */
    private const xlMedium   = -4138; /* Нормальная */
    private const xlThick    = 4;     /* Толстая */

    /* Константы выравнивания */
    private const xlTop    = -4160;
    private const xlCenter = -4108;
    private const xlBottom = -4107;
    private const xlRight  = -4152;
    private const xlLeft   = -4131;

    private var m_currentUseSystemSeparatorsValue;
    private var C_EE_EXCEL_NUMBER_FORMAT = ternary(balanceParameters.getIsPrintApostrophe() == true, "#'##0", "###0");         // целочисленный формат
    private var EXCEL_ws                 = NULL;            // EXCEL worksheet
    private var C_EE_EXCEL_POINT;                           // разделитель для дробной части чисел
    private var m_rowCurrent;                               // текущая строка на листе
    private var l_ob;                                       // объект Excel.Application
    private var startAX;
    private var exlname                  = "";
    private var m_columnsAmount;
    private var l_wb;                                       // EXCEL WorkBook
    private var m_chapterBook            = TChaptersBook(); //справочник глав баланса
    private var m_currentChapter         = null;            //текущая глава баланса
    private var m_planTitle;                                //заголовок плана счетов
    private var m_separator;                                //

    /* Преобразовать путь в полный */
    macro _processPath(p_path, path)
        var l_str;
        var l_path;
        var l_p;

        p_path = trim(p_path);

        if (substr(p_path, 1, 2 ) == "..") /* ссылка на вышестоящий каталог */
            p_path = substr(p_path, 3);
            l_str  = path;
            l_path = "";
            l_p    = strbrk(l_str, "\\");

            while (l_p != 0)
                l_path = l_path + substr(l_str, 1, l_p - 1) + "\\";
                l_str  = substr(l_str, l_p + 1);
                l_p    = strbrk(l_str, "\\");
            end;

            return (substr(l_path, 1, strlen(l_path) - 1) + p_path);
        elif (substr(p_path, 1, 1) == ".") /* ссылка на текущий каталог */
            return (path + substr(p_path, 2));
        end;

        return path + "\\" + p_path;
    end;

    /*
          Открытие шаблона отчета в EXCEL
           p_name: Opt: полный путь к файлу шаблона
    */
    macro ee_EXCEL_template_init(p_excel_ob, p_name)
        l_wb               = p_excel_ob.Workbooks.Add(p_name);
        EXCEL_ws           = l_wb.Sheets(1);
        p_excel_ob.Visible = true;

        m_currentUseSystemSeparatorsValue = p_excel_ob.useSystemSeparators;
        p_excel_ob.useSystemSeparators    = false;
        C_EE_EXCEL_POINT                  = p_excel_ob.application.DecimalSeparator;
    end;

    macro setOutputFile()
        var path = "";

        path = GetCurDir(false);

        //ищем файл шаблона
        var pathXls = FindPath(exlname, NULL, "xlt");

        if (pathXls == "" )
            msgbox("Не найден файл-шаблон EXCEL " + exlname + " в каталоге макрофайлов на сервере приложений.");
            Exit(1);
        end;

        if (isStandAlone())
            l_ob = ActiveX("Excel.Application", NULL, TRUE);
            path = _processPath(pathXls, path);
        else
            if (startAX == null)
                startAX = CreateObject("rsax", "TRsAxServer", "ReportAxServer", isStandalone());
            end;
            l_ob = startAX.CreateComObject("Excel.Application");

            if(not copyFile(pathXls, "$" + exlname))
                msgbox("Невозможно скопировать файл-шаблон EXCEL ", exlname, ".");
                return;
            end;

            GetFileInfo("$" + exlname, null, null, null, path);

            if (strBrk(path, "$") == 1)
                path = subStr(path, 2);
            end;
        end;

        ee_EXCEL_template_init(l_ob, path);

        m_separator = l_ob.ThousandsSeparator;
        l_ob.ThousandsSeparator = ternary(balanceParameters.getIsPrintApostrophe() == true, "'", "");
    end;

    /*
      Код колонки EXCEL по номеру
       p_idx: Req: номер колонки (нумерация с 1)
    */
    macro ee_cl_code(p_idx)
        var l_s;
        var s;

        p_idx = p_idx - 1; /* 0 == "A" */
        if (p_idx <= 25)
            return strfor(codefor("A") + p_idx);
        end;
        l_s = ee_cl_code(p_idx / 26 - 1);
        l_s = l_s + ee_cl_code(p_idx - (p_idx / 26) * 26);

        return s;
    end;

    /*
      Код фрагмента таблицы в EXCEL - для вызова EXCEL-процедур
       p_start_cl: Req: номер первой колонки
       p_start_ln: Req: номер первой строки
       p_end_cl  : Req: номер последней колонки
       p_end_ln  : Req: номер последней строки
    */
    macro ee_range_code(p_start_cl, p_start_ln, p_end_cl, p_end_ln)
        return string(ee_cl_code(p_start_cl), p_start_ln, ":", ee_cl_code(p_end_cl), p_end_ln);
    end;

    /*
      Установить формат заданного фрагмента таблицы EXCEL
       p_align : Req: код выравнивания  "l" или "c" или "r" (аналогично RSL)
       p_points: Opt: количество точек после запятой -
                      для форматирования чисел: Def - 0
       p_s_cl, p_s_ln, p_e_cl, p_e_ln: Req: параметры, определяющие границы
                                            форматируемой области (нач.колонка,
                                            нач. строка, кон. колонка и строка)
    */
    macro ee_EXCEL_set_cells_fmt(p_align, p_points, p_s_cl, p_s_ln, p_e_cl, p_e_ln)
        var l_r;
        var l_NumberFormat;

        l_r = EXCEL_ws.Range(ee_range_code(p_s_cl, p_s_ln, p_e_cl, p_e_ln));

        p_align = strlwr(p_align);
        if (p_align == "r")
            l_r.HorizontalAlignment = xlRight;
        elif (p_align == "c")
            l_r.HorizontalAlignment = xlCenter;
        else
            l_r.HorizontalAlignment = xlLeft;
        end;

        if (valtype(p_points) != V_INTEGER)
            if (p_points == "txt")
                l_r.NumberFormat = "@";
            end;
            return;
        end;

        l_NumberFormat = C_EE_EXCEL_NUMBER_FORMAT;
        if (p_points > 0)
            l_NumberFormat = l_NumberFormat + C_EE_EXCEL_POINT + mkstr("0", p_points);
        end;
        l_r.NumberFormat = l_NumberFormat;

        OnError(err);

        l_ob.Visible = TRUE;

        if (err.code == 40)
            runError("Печать прервана!");
            exit(1);
        else
            exceptionMessage(err);
            runError();
        end;
    end;

    /*
      Преобразование STRING -> DATE
       p_date: Req: Строка с датой
    */
    macro ee_str2date(p_date)
        var str_date = trim(p_Date);
        var p;
        var dd, mm, yy;

        p = strbrk(str_date, "-/.:");

        if (p == 0)
            return date(0,0,0);
        end;

        dd = int(substr(str_date, 1, p - 1));
        str_date = substr(str_date, p + 1, strlen(str_date) - p);
        p = strbrk(str_date, "-/.:" );

        if (p == 0 )
            return date(0,0,0 );
        end;

        mm = int(substr(str_date, 1, p - 1));
        str_date = substr(str_date, p + 1, strlen(str_date) - p);
        yy = int(str_date );

        return date(dd, mm, yy);
    end;

    /*
       Установить точность числа в ячейке EXCEL
       x    : колонка
       y    : строка
       prec : количество точек после запятой
    */
    private macro ee_EXCEL_set_cell_prec(x, y, prec)
        var l_r,
            l_NumberFormat;

        l_r = EXCEL_ws.Cells(y, x);
        l_NumberFormat = C_EE_EXCEL_NUMBER_FORMAT;
        if (prec > 0)
            l_NumberFormat = l_NumberFormat + C_EE_EXCEL_POINT + mkstr("0", prec);
        end;
        l_r.NumberFormat = l_NumberFormat;

        OnError(err);

        l_ob.Visible = TRUE;

        if (err.code == 40)
            runError("Печать прервана!");
            exit(1);
        else
            exceptionMessage(err);
            runError();
        end;
    end;

    /*
      Преобразовать STRING-значение к заданному в качестве параметра типу
       p_value: Req: строка содержащая значение для преобразования
       p_type:  Req: RSL-тип значения
    */
    macro ee_convtotype(p_value, p_type)
        p_type = int(p_type );

        if  (p_type == V_INTEGER)
            return int(p_value);
        elif ((p_type == V_MONEY  ) or
              (p_type == V_MONEYL ) or
              (p_type == V_DOUBLE ) or
              (p_type == V_DOUBLEL))
            return double(p_value);
        elif (p_type == V_DATE)
            return ee_str2date(p_value);
        end;

        return p_value;
    end;

    /*
       Код ячейки таблицы в EXCEL - для вызова EXCEL-процедур
        p_cl: Req: номер колонки
        p_ln: Req: номер строки
    */
    macro ee_cell_code(p_cl, p_ln)
        return string(ee_cl_code(p_cl), p_ln);
    end;

    /*
      Печать значения в заданную ячейку таблицы Excel
       p_value : Req: STRING-значение
       p_type  : Req: RSL-тип значения
       p_column: Req: колонка
       p_row   : Req: строка
    */
    macro ee_EXCEL_print_value(p_value, p_type, p_column, p_row)
        EXCEL_ws.Range(ee_cell_code(p_column, p_row)).FormulaR1C1 = ee_convtotype(p_value, p_type);

        OnError(err);

        l_ob.Visible = TRUE;

        if (err.code == 40)
            runError("Печать прервана!");
            exit(1);
        else
            exceptionMessage(err);
            runError();
        end;
    end;

    private macro addWorkSheet(sheetTitle, chapterTitle)
        var sheet;
        var buf = TArray();

        EXCEL_ws.Copy(EXCEL_ws);
        sheet = l_wb.Sheets(l_wb.Sheets.count - 1);

        //название банка
        printBankHeaderBuf(m_departmentCode, m_orgStructure, 200, buf);
        sheet.Range(ee_cell_code(1, 2)).Font.Bold = true;
        sheet.Range(ee_cell_code(1, 2)).FormulaR1C1 = buf[0];

        //период отчета
        sheet.Range(ee_cell_code(1, 4)).Font.Bold = true;
        sheet.Range(ee_cell_code(1, 4)).FormulaR1C1 = getPeriodTypeText();

        //название плана счетов
        sheet.Range(ee_cell_code(1, 5)).Font.Bold = true;
        sheet.Range(ee_cell_code(1, 5)).FormulaR1C1 = m_planTitle;

        //название главы
        sheet.Range(ee_cell_code(1, 6)).Font.Bold = true;
        sheet.Range(ee_cell_code(1, 6)).FormulaR1C1 = chapterTitle;

        //предполагаем, что данных нет
        sheet.Range(ee_cell_code(1, 10)).FormulaR1C1 = "Данные отсутствуют.";
        sheet.Range(ee_cell_code(1, 12)).FormulaR1C1 = {Name_Boss} + " ___________________ " + {FIO_Boss};
        sheet.Range(ee_cell_code(1, 13)).FormulaR1C1 = {Name_Book} + " ___________________ " + {FIO_Book};

        //скрываем лишние столбцы
        if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_TURN_NOIN)
            sheet.Columns(2).Hidden = true;
            sheet.Columns(3).Hidden = true;

            if (balanceParameters.getIsPrintCover() == true)
                sheet.Columns(4).Hidden = true;
                sheet.Columns(5).Hidden = true;
            end;
        end;

        //скрываем лишние столбцы
        if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST)
            sheet.Columns(2).Hidden = true;
            sheet.Columns(3).Hidden = true;
            sheet.Columns(4).Hidden = true;
            sheet.Columns(5).Hidden = true;

            if (balanceParameters.getIsPrintCover() == true)
                sheet.Columns(6).Hidden = true;
                sheet.Columns(7).Hidden = true;
                sheet.Columns(8).Hidden = true;
                sheet.Columns(9).Hidden = true;
            end;
        end;

        sheet.Name = sheetTitle;
    end;

    //метод ничего не делает, т.к. в Excel ширина отчета задается шаблоном документа и имеет другую семантику
    macro setReportWidth()
    end;

    //метод ничего не делает, т.к. все заголовки печатаюся при создании листа
    macro printHead()
    end;

    //название банка печатается при создании листа
    macro printBankHead()
    end;

    //шапка отчета печатается при создании листа
    macro printReportHead()
    end;

    //сообщение об отсутствии данных выводится при создании листа
    macro printNullData()
    end;

    /**
     * Печать строки по балансовому счету.
     */
    macro printBalance(dataSet, kindAccount)
        var isItog = false;

        //печать итогов
        if (dataset.isChapterItog == true)
            isItog = true;

            EXCEL_ws.Range(ee_range_code(1, m_rowCurrent, m_columnsAmount, m_rowCurrent)).Font.Bold = true;
            if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
                ee_EXCEL_print_value("Итого " + ternary(kindAccount == "_Active", "АКТИВ:", "ПАССИВ:"), V_STRING,  1, m_rowCurrent);
            else
                ee_EXCEL_print_value("Итого:", V_STRING, 1, m_rowCurrent);
            end;
        elif (dataset.isPartItog == true)
            isItog = true;
            EXCEL_ws.Range(ee_range_code(1, m_rowCurrent, m_columnsAmount, m_rowCurrent)).Font.Bold = true;
            ee_EXCEL_print_value("Итого по разделу:", V_STRING, 1, m_rowCurrent);

            if (dataset.isCurrencyItog == true)
                EXCEL_ws.Range(ee_range_code(1, m_rowCurrent, m_columnsAmount, m_rowCurrent)).Font.Bold = true;
                ee_EXCEL_print_value("Итого по валюте:", V_STRING, 1, m_rowCurrent);
            end;
        elif (dataset.isCurrencyItog == true)
            isItog = true;
            EXCEL_ws.Range(ee_range_code(1, m_rowCurrent, m_columnsAmount, m_rowCurrent)).Font.Bold = true;
            ee_EXCEL_print_value("Итого по валюте:", V_STRING, 1, m_rowCurrent);
        else
            if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
                ee_EXCEL_print_value(dataSet.balanceName, V_STRING, 1, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.balance,     V_STRING, 2, m_rowCurrent);
            else
                ee_EXCEL_print_value(dataSet.balance, V_STRING, 1, m_rowCurrent);
            end;
        end;

        //печать данных
        if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            ee_EXCEL_set_cells_fmt("r", 2, 3, m_rowCurrent, m_columnsAmount, m_rowCurrent);

            if (kindAccount == "_Active")
                ee_EXCEL_print_value(dataSet.outActiveRest, V_DOUBLEL, 3, m_rowCurrent);
            else
                ee_EXCEL_print_value(dataSet.outPassiveRest, V_DOUBLEL, 3, m_rowCurrent);
            end;

            if (balanceParameters.getIsPrintCover() == true)
                if (kindAccount == "_Active")
                    ee_EXCEL_print_value(dataSet.outActiveRestR, V_DOUBLEL, 4, m_rowCurrent);
                else
                    ee_EXCEL_print_value(dataSet.outPassiveRestR, V_DOUBLEL, 4, m_rowCurrent);
                end;
            end;
        end;

        if (   (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_TURN)
            or (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_TURN_NOIN)
            or (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST))
            ee_EXCEL_set_cells_fmt("r", 2, 2, m_rowCurrent, m_columnsAmount, m_rowCurrent);

            if (balanceParameters.getIsPrintCover() == true)
                ee_EXCEL_print_value(dataSet.inActiveRestR,   V_DOUBLEL, 3,  m_rowCurrent);
                ee_EXCEL_print_value(dataSet.inPassiveRestR,  V_DOUBLEL, 5,  m_rowCurrent);
                ee_EXCEL_print_value(dataSet.debetR,          V_DOUBLEL, 7,  m_rowCurrent);
                ee_EXCEL_print_value(dataSet.creditR,         V_DOUBLEL, 9,  m_rowCurrent);
                ee_EXCEL_print_value(dataSet.outActiveRestR,  V_DOUBLEL, 11, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.outPassiveRestR, V_DOUBLEL, 13, m_rowCurrent);

                if (dataset.isChapterItog != true)
                    ee_EXCEL_print_value(dataSet.inActiveRest,    V_DOUBLEL, 2,  m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.inPassiveRest,   V_DOUBLEL, 4,  m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.debet,           V_DOUBLEL, 6,  m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.credit,          V_DOUBLEL, 8,  m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.outActiveRest,   V_DOUBLEL, 10, m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.outPassiveRest,  V_DOUBLEL, 12, m_rowCurrent);
                end;
            else
                ee_EXCEL_print_value(dataSet.inActiveRest,   V_DOUBLEL, 2, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.inPassiveRest,  V_DOUBLEL, 3, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.debet,          V_DOUBLEL, 4, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.credit,         V_DOUBLEL, 5, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.outActiveRest,  V_DOUBLEL, 6, m_rowCurrent);
                ee_EXCEL_print_value(dataSet.outPassiveRest, V_DOUBLEL, 7, m_rowCurrent);
            end;
            //СПОД
            if (balanceParameters.getIsPrintSpod and ((dataSet.debetSpod != 0) or (dataSet.creditSpod != 0)))
                m_rowCurrent = m_rowCurrent + 1;

                if (isItog == true)
                    EXCEL_ws.Range(ee_range_code(1, m_rowCurrent, m_columnsAmount, m_rowCurrent)).Font.Bold = true;
                end;

                ee_EXCEL_print_value("В том числе СПОД", V_STRING,  1, m_rowCurrent);
                ee_EXCEL_set_cells_fmt("r", 2, 2, m_rowCurrent, m_columnsAmount, m_rowCurrent);

                if (balanceParameters.getIsPrintCover() == true)
                    ee_EXCEL_print_value(dataSet.creditSpodR, V_DOUBLEL, 7, m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.creditSpodR, V_DOUBLEL, 9, m_rowCurrent);

                    if (dataset.isChapterItog != true)
                        ee_EXCEL_print_value(dataSet.debetSpod,   V_DOUBLEL, 6, m_rowCurrent);
                        ee_EXCEL_print_value(dataSet.debetSpod,   V_DOUBLEL, 8, m_rowCurrent);
                    end;
                else
                    ee_EXCEL_print_value(dataSet.debetSpod,  V_DOUBLEL, 4, m_rowCurrent);
                    ee_EXCEL_print_value(dataSet.creditSpod, V_DOUBLEL, 5, m_rowCurrent);
                end;
            end;
        end;

        m_rowCurrent = m_rowCurrent + 1;
    end;

    //получить номер листа по номеру главы баланса
    private macro getSheetNumber(chapter)
        var sheetName = m_chapterBook.getTitleByNumber(chapter);

        for (var i, 1, l_wb.Sheets.count, 1)
            if (l_wb.Sheets(i).Name == sheetName)
                return i;
            end;
        end;

        msgbox("Лист " + sheetName + " не найден. Рассчет будет прерван");
        exit(1);
    end;

    /**
     * Печать подписей.
     */
    macro printSignature()
        if (m_currentChapter != null)
            EXCEL_ws = l_wb.Sheets(getSheetNumber(m_currentChapter));

            //на всякий случай подчищаем
            ee_EXCEL_print_value("", V_STRING, 1, m_rowCurrent);

            m_rowCurrent = m_rowCurrent + 1;
            ee_EXCEL_print_value({Name_Boss} + " ___________________ " + {FIO_Boss}, V_STRING, 1, m_rowCurrent);
            m_rowCurrent = m_rowCurrent + 1;
            ee_EXCEL_print_value({Name_Book} + " ___________________ " + {FIO_Book}, V_STRING, 1, m_rowCurrent);
        end;

        //показываем первый лист
        l_wb.Sheets(1).Activate();
    end;

    macro beginReport()
    end;

    macro beginCurrency(dataSet, kindAccount)
        EXCEL_ws.Range(ee_cell_code(1, m_rowCurrent)).Font.Bold = true;
        ee_EXCEL_print_value(dataSet.titleCurrency, V_STRING, 1, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    /**
     * Окончание печати данных по валюте.
     */
    macro endCurrency(dataSet, kindAccount)
        printBalance(dataSet, kindAccount);
    end;

    /**
     * Начало печати данных по главе.
     */
    macro beginChapter(dataSet, kindAccount)
        m_currentChapter = m_chapterBook.getNumberBySymbol(SubStr(dataSet.titleChapter, 7, 1));
        EXCEL_ws         = l_wb.Sheets(getSheetNumber(m_currentChapter));
        m_rowCurrent     = 10;
    end;

    /**
     * Окончание печати данных по главе.
     */
    macro endChapter(dataSet, kindAccount)
        printBalance(dataSet, kindAccount);
    end;

    /**
     * Начало печати раздела.
     */
    macro beginIssue(dataSet, kindAccount)
        EXCEL_ws.Range(ee_cell_code(1, m_rowCurrent)).Font.Bold = true;
        ee_EXCEL_print_value("Раздел " + dataSet.part + ". " + dataSet.partName, V_STRING, 1, m_rowCurrent);
        m_rowCurrent =  m_rowCurrent + 1;
    end;

    /**
     * Окончание печати раздела.
     */
    macro endIssue(dataSet, kindAccount)
        printBalance(dataSet, kindAccount);
    end;

    /**
     * Выборка существующих всех глав баланса
     */
    private macro getPlanName()
        var dataSet;
        var query = "select t_namePlan   "
        + "\n" + "     from dNamePlan_dbt"
        + "\n" + "    where t_iNumPlan = " + balanceParameters.getPlan();

        dataSet = TRsbDataSet(query);

        if (dataSet.Next())
            return dataSet.namePlan;
        else
            return "";
        end;
    end;

    private macro constructorTNatBalanceExcelView(isActivePart : Bool)
        initTPrintableView(balanceParameters.getDepartmentCode(), balanceParameters.getOrgStructure(), "");

        if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            if (balanceParameters.getIsPrintCover() == true)
                exlname = "rests_with_title_equ.xltx";
                m_columnsAmount = 4;
            else
                exlname = "rests_with_title.xltx";
                m_columnsAmount = 3;
            end
        elif (   (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_TURN)
              or (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_TURN_NOIN)
              or (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST))
            if (balanceParameters.getIsPrintCover() == true)
                exlname = "turns_equ.xltx";
                m_columnsAmount = 13;
            else
                exlname = "turns.xltx";
                m_columnsAmount = 7;
            end;
        end;

        setOutputFile();

        m_rowCurrent = 10;

        if (balanceParameters.getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            if   (isActivePart == true)
                if (balanceParameters.getIsPrintCover() == true)
                    ee_EXCEL_print_value("АКТИВ", V_STRING, 3, 8);
                else
                    ee_EXCEL_print_value("АКТИВ", V_STRING, 3, 9);
                end;
            elif (isActivePart == false)
                if (balanceParameters.getIsPrintCover() == true)
                    ee_EXCEL_print_value("ПАССИВ", V_STRING, 3, 8);
                else
                    ee_EXCEL_print_value("ПАССИВ", V_STRING, 3, 9);
                end;
            end;
        end;

        m_planTitle = "План счетов " + balanceParameters.getPlan() + ". " + getPlanName();

        //добавляем рабочие листы по числу глав баланса
        var chapterNumber;
        for (var i, 1, m_chapterBook.getChaptersAmount(), 1)
            chapterNumber = m_chapterBook.getNumberByOrder(i);
            addWorkSheet("Глава " + m_chapterBook.getSymbolByNumber(chapterNumber),
                         m_chapterBook.getTitleByNumber(chapterNumber) + ". " + m_chapterBook.getNameByNumber(chapterNumber));
        end;

        //удаляем последний лист книги (шаблон для всех созданных листов)
        //отключаем запрос подтверждения
        l_ob.DisplayAlerts = False;
        //удаляем лист
        l_wb.Sheets(l_wb.Sheets.count).Delete();
        //снова включаем запрос подтверждения
        l_ob.DisplayAlerts = true;
    end;
/*
    macro destructor()
        l_ob.ThousandsSeparator = m_separator;
    end;
*/
    constructorTNatBalanceExcelView(isActivePart);
end;