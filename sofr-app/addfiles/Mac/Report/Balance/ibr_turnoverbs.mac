/*
$Name:          ibr_turnoverbs.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Оборотно-сальдовая ведомость"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ReptCBInter;         //для определения вида периода
import main_turnoverbs;

private const TAB_REPORT = "Параметры отчета";
private const TAB_ACCOUNTS = "Параметры счетов";
private const TAB_EXTENDED = "Дополнительные параметры";

private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_ACCOUNTS = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_EXTENDED = "Дополнительные параметры";
private const GRP_EXCLUSIONS = "Исключить счета";
private const GRP_FORMAT = "Формат вывода";

private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report = facade.createReport("dyn_tbs", "Оборотно-сальдовая ведомость", "ibr_turnoverbs.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();
    var tabAccounts = facade.createTab(TAB_ACCOUNTS, 2);
    var groupAccounts = facade.createNotGroup();
    var tabExtended = facade.createTab(TAB_EXTENDED, 3);
    var groupExtended = facade.createGroup(GRP_EXTENDED, 1);
    var groupExclusions = facade.createGroup(GRP_EXCLUSIONS, 2);
    var groupFormat = facade.createGroup(GRP_FORMAT, 3);

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта", NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта", "Только ин.валюта", false));
    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));
    groupReport.add(facade.createFieldNameAlg("ФормаОтчета", "Форма отчета", 1, 2074));
    groupReport.add(facade.createFieldNameAlg("Итоги", "Итоги", 1, 2071));
    groupReport.add(facade.createFieldLlValues("ВключатьСчета", "Включать счета", 3, 1765));
    groupReport.add(facade.createFieldCheckBox("ЭквивалентВНацВалюте", "Эквивалент в нац.валюте", false));
    groupReport.add(facade.createFieldCurrency("Пересчет", "Пересчет", ""));
    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    groupReport.add(facade.createFieldClient("Клиент", "Клиент"));

    facade.resetFieldsOrder();
    groupAccounts.add(facade.createFieldBalance("Балансовый", "Балансовый"));
    groupAccounts.add(facade.createFieldAccount("МаскаСчета", "Маска счета"));
    groupAccounts.add(facade.createFieldTypeAc("ТипСчета", "Тип счета", 1, null, null, null, null, null, null, null, null, null, false));
    groupAccounts.add(facade.createFieldTypeAc("ПользТип", "Польз.тип", 2));

    facade.resetFieldsOrder();
    groupExtended.add(facade.createFieldCheckBox("ИтоговыеОборотыСПОД", "Итоговые обороты СПОД", false));
    groupExtended.add(facade.createFieldCheckBox("САпострофами", "С апострофами", false));
    groupExtended.add(facade.createFieldCheckBox("ПечатьНулевых", "Печать нулевых", true));
    groupExtended.add(facade.createFieldCheckBox("ЗаКаждыйДень", "За каждый день", false));
    groupExtended.add(facade.createFieldCheckBox("ИспользоватьРПС", "Использовать РПС", false));
    groupExtended.add(facade.createFieldSort("Сортировка", "Сортировка", "", 1739));

    facade.resetFieldsOrder();
    groupExclusions.add(facade.createFieldCheckBox("СНулевымиОборотами", "с нулевыми оборотами", false));
    groupExclusions.add(facade.createFieldCheckBox("СНулевымиОстатками", "с нулевыми остатками", false));

    facade.resetFieldsOrder();
    groupFormat.add(facade.createFieldCheckBox("PlainText", "plain text", true));
    groupFormat.add(facade.createFieldCheckBox("excel", "excel", false));

    tabReport.add(groupReport);
    tabAccounts.add(groupAccounts);
    tabExtended.add(groupExtended);
    tabExtended.add(groupExclusions);
    tabExtended.add(groupFormat);

    report.add(tabReport);
    report.add(tabAccounts);
    report.add(tabExtended);

    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gAcc = TAB_ACCOUNTS + "@" + GRP_ACCOUNTS + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";
    const gExc = TAB_EXTENDED + "@" + GRP_EXCLUSIONS + "@";
    const gFmt = TAB_EXTENDED + "@" + GRP_FORMAT + "@";

    var periodKind = ПолучитьВидПериода(getValue(report, gRep+"НачалоПериода").valueDate, getValue(report, gRep+"КонецПериода").valueDate);

    var isShowOcp = false;
    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );

    result.valueBoolean = produceTurnoverBsReport(getValue(report, gRep+"Подразделения").valueInteger,
                                                  getValue(report, gRep+"Структура").valueInteger,
                                                  getValue(report, gRep+"Режим").valueInteger,
                                                  TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),
                                                  TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),
                                                  getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                                  periodKind,
                                                  getValue(report, gRep+"НачалоПериода").valueDate,
                                                  getValue(report, gRep+"КонецПериода").valueDate,
                                                  getValue(report, gRep+"ФормаОтчета").valueInteger,
                                                  getValue(report, gRep+"Итоги").valueInteger,
                                                  getValue(report, gRep+"ВключатьСчета").valueInteger,
                                                  getValue(report, gRep+"ЭквивалентВНацВалюте").valueBoolean,
                                                  getFiId(getValue(report, gRep+"Пересчет").valueString),
                                                  false,
                                                  getValue(report, gRep+"Операционист").valueInteger,
                                                  getValue(report, gRep+"Клиент").valueInteger,
                                                  getValue(report, gAcc+"Балансовый").valueString,
                                                  getValue(report, gAcc+"МаскаСчета").valueString,
                                                  getValue(report, gAcc+"ТипСчета").valueString,
                                                  getValue(report, gAcc+"ПользТип").valueString,
                                                  getValue(report, gAcc+"ПользТип").valueInteger,
                                                  getValue(report, gExt+"ИтоговыеОборотыСпод").valueBoolean,
                                                  getValue(report, gExt+"САпострофами").valueBoolean,
                                                  getValue(report, gExt+"ПечатьНулевых").valueBoolean,
                                                  getValue(report, gExc+"СНулевымиОборотами").valueBoolean,
                                                  getValue(report, gExc+"СНулевымиОстатками").valueBoolean,
                                                  getValue(report, gExt+"ЗаКаждыйДень").valueBoolean,
                                                  getValue(report, gExt+"ИспользоватьРпс").valueBoolean,
                                                  not isShowOcp,
                                                  strSplit2(getValue(report, gExt+"Сортировка").valueString, 1, true, 1),
                                                  getValue(report, gFmt+"PlainText").valueBoolean,
                                                  getValue(report, gFmt+"Excel").valueBoolean
                                                 );

    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        //выводим протокол контроля
        repTxtFilesQueue().add(controlBodyFileName);

        //выводим отчет в текстовом варианте
        if (getValue(report, gFmt+"PlainText").valueBoolean)
            repTxtFilesQueue().add(reportBodyFileName);
        end;

    // При неудачном выполнении
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    const gAcc = TAB_ACCOUNTS + "@" + GRP_ACCOUNTS + "@";
    const gExt = TAB_EXTENDED + "@" + GRP_EXTENDED + "@";
    const gExc = TAB_EXTENDED + "@" + GRP_EXCLUSIONS + "@";
    const gFmt = TAB_EXTENDED + "@" + GRP_FORMAT + "@";

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString) == -1)
        repErrorsQueue().add(0, "Недопустимая глава счетов");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"НачалоПериода").valueDate > getValue(report, gRep+"КонецПериода").valueDate)
        repErrorsQueue().add(0, "Дата начала периода не может превышать дату его окончания");
        result.valueBoolean = FALSE;
    end;

    if (    (getValue(report, gFmt+"PlainText").valueBoolean == false)
        and (getValue(report, gFmt+"Excel").valueBoolean     == false))
        repErrorsQueue().add(0, "Не задан формат печати");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
