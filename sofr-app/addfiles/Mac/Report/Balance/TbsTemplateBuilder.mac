 /*
 $Name: TbsTemplateBuilder.mac
 $Module: Внутренняя отчетность
 $Description:  Класс шаблона печати отчета "ОСВ"
 */


/**
 * Класс шаблона печати отчета "ОСВ"
 *
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */

import TemplateBuilder;
import RcbClassLibInter;
import lib_arr;
import repException;

/**
 * Класс, печати шаблона таблицы.
 */
class (TTemplateBuilder)TTbsTemplateBuilder(resultFileName : String)

    private const ACCOUNTNUMBER_FIELD_WIDTH = 27;
    private const ACCOUNTNAME_FIELD_WIDTH = 40;
    private const AMOUNT_FIELD_WIDTH = 18;

    private var m_rows : TArray;

    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        throw(TPureVirtualMethodCallException("TTbsTemplateBuilder::createCompositeTableField"));
    end;

    /**
     * Печать шаблона шапки отчета в файл шаблона.
     */
    private macro printTableHeader()
        var header = m_compositeTableField.getReportHeader();
        var headerString = "";
        println("<FORM:Header>");
        for (var i, header)
            println(i);
            headerString = headerString + "\n" + i;
        end;
        println("</FORM>");

        headerString = substr(headerString, 2);
        m_rows(m_rows.size) = TRcbPair("Header", arrCreate(TTableDataField(headerString, 0, "l", true)));
    end;

    /**
     * Печать шаблона содержательной части отчета в файл шаблона.
     */
    private macro printTableRow()
        println();
        println("<FORM:Row>");
        println(m_compositeTableField.getReportString());
        println("</FORM>");
        println();
        m_rows(m_rows.size) = TRcbPair("Row", m_compositeTableField.getDataColumns());
    end;

    /**
     * Печать шаблона конца балансового счета2 в файл шаблона.
     */
    private macro printBalance2End()
        var changeFields = TArray();
        changeFields(0) = arrCreate("accountName", "");
        var changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println();
        println("<FORM:EndBalance2>");
        println(changedField.getReportString());
        println("</FORM>");
        m_rows(m_rows.size) = TRcbPair("EndBalance2", changedField.getDataColumns());
    end;

    /**
     * Печать шаблона конца таблицы отчета в файл шаблона.
     */
    private macro printBalance1End()
        var changeFields = TArray();
        changeFields(0) = arrCreate("accountName", "");
        var changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println();
        println("<FORM:EndBalance1>");
        println(changedField.getReportString());
        println("</FORM>");
        m_rows(m_rows.size) = TRcbPair("EndBalance1", changedField.getDataColumns());
    end;

    /**
     * Печать шаблона конца таблицы отчета в файл шаблона.
     */
    private macro printChapterEnd()
        var changeFields = TArray();
        changeFields(0) = arrCreate("account",     "Итого");
        changeFields(1) = arrCreate("accountName", "");
        var changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println();
        println("<FORM:EndChapterNat>");
        println(changedField.getReportString());
        println("</FORM>");
        m_rows(m_rows.size) = TRcbPair("EndChapterNat", changedField.getDataColumns());

        changeFields(2) = arrCreate("inActiveRest",     "");
        changeFields(3) = arrCreate("inPassiveRest",    "");
        changeFields(4) = arrCreate("debet",            "");
        changeFields(5) = arrCreate("credit",           "");
        changeFields(6) = arrCreate("outActiveRest",    "");
        changeFields(7) = arrCreate("outPassiveRest",   "");
        changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println();
        println("<FORM:EndChapterCurCover>");
        println(changedField.getReportString());
        println("</FORM>");
        m_rows(m_rows.size) = TRcbPair("EndChapterCurCover", changedField.getDataColumns());

    end;

    /**
     * Печать шаблона части по СПОДу отчета в файл шаблона.
     */
    private macro printTableRowSpod()
        var changeFields = TArray();

        changeFields(0) = arrCreate("account",          "В том числе СПОД: ");
        changeFields(1) = arrCreate("accountName",      "");
        changeFields(2) = arrCreate("inActiveRest",     "");
        changeFields(3) = arrCreate("inPassiveRest",    "");
        changeFields(4) = arrCreate("outActiveRest",    "");
        changeFields(5) = arrCreate("outPassiveRest",   "");
        changeFields(6) = arrCreate("inActiveRestR",    "");
        changeFields(7) = arrCreate("inPassiveRestR",   "");
        changeFields(8) = arrCreate("outActiveRestR",   "");
        changeFields(9) = arrCreate("outPassiveRestR",  "");
        var changedField;

        println();
        println("<FORM:RowSpod>");
        if (tbsParameters.getIsPrintSpod)
            changeFields(10) = arrCreate("debet",   "&debetSpod");
            changeFields(11) = arrCreate("credit",  "&creditSpod");
            changeFields(12) = arrCreate("debetR",  "&debetSpodR");
            changeFields(13) = arrCreate("creditR", "&creditSpodR");
            changedField = m_compositeTableField.getChangedDataRow(changeFields);
            println(changedField.getReportString());
            m_rows(m_rows.size) = TRcbPair("RowSpod", changedField.getDataColumns());
        end;
        println("</FORM>");

        println();
        println("<FORM:RowCurCoverSpod>");
        if (tbsParameters.getIsPrintSpod)
            changeFields(10) = arrCreate("debet",   "");
            changeFields(11) = arrCreate("credit",  "");
            changeFields(12) = arrCreate("debetR",  "&debetSpodR");
            changeFields(13) = arrCreate("creditR", "&creditSpodR");
            changedField = m_compositeTableField.getChangedDataRow(changeFields);
            println(changedField.getReportString());
            m_rows(m_rows.size) = TRcbPair("RowCurCoverSpod", changedField.getDataColumns());
        end;
        println("</FORM>");
    end;

    /**
     * Печать шаблона разделов таблицы отчета в файл шаблона.
     */
    private macro printPart()
        println();
        println("<FORM:HeaderPart>");
        println("Раздел <!-- part:r -->#. <!-- partName:r -->#");
        println("</FORM>");
        println();
        // При изменении шаблона HeaderPart необходимо изменять метод TTbsWinRepView::beginPart
        m_rows(m_rows.size) = TRcbPair("HeaderPart", arrCreate(TTableDataField("Раздел ", 0, "l", true),
                                                               TTableDataField("part", 0, "r"),
                                                               TTableDataField(". ", 0, "l", true),
                                                               TTableDataField("partName", 0, "r")
                                                              )
                                      );

        var changeFields = TArray();
        changeFields(0) = arrCreate("account",     "Итого по разделу");
        changeFields(1) = arrCreate("accountName", "");
        var changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println("<FORM:EndPart>");
        println(changedField.getReportString());
        println("</FORM>");
        println();
        m_rows(m_rows.size) = TRcbPair("EndPart", changedField.getDataColumns());
    end;

    /**
     * Печать шаблона валюты таблицы отчета в файл шаблона.
     */
    private macro printCurrency()
        println();
        println("<FORM:HeaderCurrency>");
        println("<!-- titleCurrency:l -->#");
        println("</FORM>");
        println();
        m_rows(m_rows.size) = TRcbPair("HeaderCurrency", arrCreate(TTableDataField("titleCurrency", null, "l")));

        var changeFields = TArray();
        changeFields(0) = arrCreate("account", "Итого по валюте");
        changeFields(1) = arrCreate("accountName", "");
        var changedField = m_compositeTableField.getChangedDataRow(changeFields);
        println("<FORM:EndCurrency>");
        println(changedField.getReportString());
        println("</FORM>");
        println();
        m_rows(m_rows.size) = TRcbPair("EndCurrency", changedField.getDataColumns());
    end;

    /**
     * Создание результирующего файла шаблона.
     */
    macro createResultFile() : string
        var oldOutput = setOutput(m_resultFileName,  false);
        printTableHeader();
        printTableRow();
        printChapterEnd();
        printTableRowSpod();
        printPart();
        printCurrency();
        printBalance1End();
        printBalance2End();
        setOutput(oldOutput,   false);

        return m_resultFileName;
    end;

    /**
     *  Получение названия валюты пересчета
     */
    private macro getCurrencyName(fiId)
        var curName = "";
        var dataSet = TRsbDataSet("SELECT t_name_currency name FROM dcurrency_dbt WHERE t_code_currency = " + fiId);
        if (dataSet.next())
            if (dataSet.name == "Рубли")
                curName = "Российские рубли";
            else curName = dataSet.name;
            end;
        end;
        return curName;
    end;

    /**
     * Описание полей заголовка.
     */
    private var accountHeader     = TTableHeaderField("Лицевой счет",       ACCOUNTNUMBER_FIELD_WIDTH, STR_ALIGN_CENTER);
    private var accountNameHeader = TTableHeaderField("Наименование счета",     ACCOUNTNAME_FIELD_WIDTH, STR_ALIGN_CENTER);
    private var inRestHeader      = TTableHeaderField("Входящий остаток",   null, STR_ALIGN_CENTER);
    private var outRestHeader     = TTableHeaderField("Исходящий остаток",  null, STR_ALIGN_CENTER);
    private var activeHeader      = TTableHeaderField("Актив",              null, STR_ALIGN_CENTER);
    private var passiveHeader     = TTableHeaderField("Пассив",             null, STR_ALIGN_CENTER);
    private var currencyHeader    = TTableHeaderField("Валюта",             null, STR_ALIGN_CENTER);
    private var natCurrencyHeader = TTableHeaderField(ternary(tbsParameters.getRecalcCurrency() >= 0, "Валюта пересчета",getCurrencyName(NATCUR)), null, STR_ALIGN_CENTER);
    private var debetHeader       = TTableHeaderField("Дебет",              null, STR_ALIGN_CENTER);
    private var creditHeader      = TTableHeaderField("Кредит",             null, STR_ALIGN_CENTER);

    /**
     * Описание полей таблицы.
     */
    private var accountData        = TTableDataField("account",     ACCOUNTNUMBER_FIELD_WIDTH, "l");
    private var accountNameData    = TTableDataField("accountName", ACCOUNTNAME_FIELD_WIDTH, "l");

    private macro currencyData(fieldDataSourceName)
        return  TTableDataField(fieldDataSourceName,     AMOUNT_FIELD_WIDTH, "r")
    end;

    private macro coverData(fieldDataSourceName)
        return  TTableDataField(fieldDataSourceName+"R", AMOUNT_FIELD_WIDTH, "r")
    end;


    macro amountView(compositeTableField : TCompositeTableField, header, fieldDataSourceName : String)
        var amount;
        if ((tbsParameters().getRecalcCurrency() >= 0) or tbsParameters().getShowNatcurEq())
            amount = compositeTableField.addField(header);
            amount.addField(currencyHeader,    currencyData(fieldDataSourceName));
            amount.addField(natCurrencyHeader, coverData(fieldDataSourceName));
        elif (tbsParameters.getFiId() == NATCUR)
            compositeTableField.addField(header, coverData(fieldDataSourceName));
        else
            compositeTableField.addField(header, currencyData(fieldDataSourceName));
        end;
    end;

    macro getRows()
        return m_rows;
    end;

    macro getHeader()
        return m_compositeTableField;
    end;

    macro constructorTTbsTemplateBuilder(resultFileName : String)
        initTTemplateBuilder(resultFileName);
        m_rows = TArray();
    end;
    constructorTTbsTemplateBuilder(resultFileName);
end;


/**
 * Баланс оборотов л/с.
 */
class (TTbsTemplateBuilder)TTurnsTbsTemplate()
    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(accountHeader, accountData);
        if (tbsParameters().getReportForm() == REP_OSV_FORM_TURN_NAME)
            m_compositeTableField.addField(accountNameHeader, accountNameData);
        end;

        compositeTableField = m_compositeTableField.addField(inRestHeader);
        amountView(compositeTableField, activeHeader,  "inActiveRest");
        amountView(compositeTableField, passiveHeader, "inPassiveRest");

        amountView(m_compositeTableField, debetHeader,  "Debet");
        amountView(m_compositeTableField, creditHeader, "Credit");

        compositeTableField = m_compositeTableField.addField(outRestHeader);
        amountView(compositeTableField, activeHeader,  "outActiveRest");
        amountView(compositeTableField, passiveHeader, "outPassiveRest");
    end;

    initTTbsTemplateBuilder();
end;

/**
 * Баланс остатков л/с.
 */
class (TTbsTemplateBuilder)TRestsTbsTemplate()

    /**
     * Создание структуры таблицы.
     */
    private macro createCompositeTableField() /*: TCompositeTableField*/
        var compositeTableField;
        m_compositeTableField = TCompositeTableField();

        m_compositeTableField.addField(accountHeader, accountData);
        if (tbsParameters().getReportForm() == REP_OSV_FORM_REST_NAME)
            m_compositeTableField.addField(accountNameHeader, accountNameData);
        end;

        compositeTableField =
        m_compositeTableField.addField(outRestHeader);
        amountView(compositeTableField, activeHeader,  "outActiveRest");
        amountView(compositeTableField, passiveHeader, "outPassiveRest");
    end;

    initTTbsTemplateBuilder();
end;
