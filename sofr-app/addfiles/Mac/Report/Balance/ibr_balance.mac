/*
$Name:          ibr_balance.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Баланс (Валюта)"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ReptCBInter;         //для определения вида периода
import main_balance;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const GRP_BALANCE = "БАЛАНС";
private const GRP_FORMAT = "ВЫВОДИТЬ В ФОРМАТЕ";

private const gRep = TAB_REPORT + "@" + GRP_BALANCE + "@";
private const gFmt = TAB_REPORT + "@" + GRP_FORMAT + "@";
    
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_balance", "Баланс (валюта)", "ibr_balance.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_BALANCE, 1);
    var groupFormat = facade.createGroup(GRP_FORMAT, 2);
    
    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));
    
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов", "План счетов", "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов", "Все главы"));
    groupReport.add(facade.createFieldCurrency("Валюта", "Валюта"));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода", "Конец периода"));
    
    groupReport.add(facade.createFieldNameAlg("ФормаОтчета", "Форма отчета", "Оборотный",3964));
    groupReport.add(facade.createFieldNameAlg("Размерность", "Размерность", 1, 2016));
    groupReport.add(facade.createFieldCheckBox("ИтогиПоРазделам", "Итоги по разделам", false));
    groupReport.add(facade.createFieldCheckBox("ЭквивалентНацВалюте", "Эквивалент в нац.валюте", false));
        
    groupReport.add(facade.createFieldCheckBox("ОборотыСПОД", "Обороты СПОД", false));
    groupReport.add(facade.createFieldCheckBox("Апострофами", "С апострофами", false));
    groupReport.add(facade.createFieldCheckBox("ПечатьНулевых", "Печать нулевых", true));
    groupReport.add(facade.createFieldCheckBox("ЗаКаждыйДень", "За каждый день", false));
    
    facade.resetFieldsOrder();
    groupFormat.add(facade.createFieldCheckBox("plainText", "Plain text", true));
    groupFormat.add(facade.createFieldCheckBox("Excel", "Excel", false));
    
    tabReport.add(groupReport);
    tabReport.add(groupFormat);
    
    report.add(tabReport);
    
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object    
    var result: Object    = TRcbResult();
    var isShowOcp         = false;
    var chapter : integer = 0;
    var periodKind        = ПолучитьВидПериода(getValue(report, gRep+"НачалоПериода").valueDate, 
                                               getValue(report, gRep+"КонецПериода").valueDate
                                               );

    if (getValue(report, gRep+"ГлаваСчетов").valueString == "Все главы")
        chapter = 0;
    else
        chapter = TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString); 
    end;

    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );

    result.valueBoolean = produceReport(getValue(report, gRep+ "Подразделения").valueInteger,                                             //departmentId
                                        getValue(report, gRep+"Структура").valueInteger,                                                 //organizationStructure
                                        getValue(report, gRep+"Режим").valueInteger,                                                     //issueMode,
                                        TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),     //planNumber,
                                        chapter,                                                                                        //chapterNumber,
                                        getFiId(getValue(report, gRep+"Валюта").valueString, true),
                                        periodKind,                                                                                             //periodKind
                                        getValue(report, gRep+"НачалоПериода").valueDate,                                               //dateIn
                                        getValue(report, gRep+"КонецПериода").valueDate,                                                //dateOut
                                        getValue(report, gRep+"ФормаОтчета").valueInteger,                                              //reportForm
                                        getValue(report, gRep+"Размерность").valueInteger,                                               //dimension
                                        getValue(report, gRep+"ИтогиПоРазделам").valueBoolean,                                         //needPartTotals
                                        getValue(report, gRep+"ЭквивалентНацВалюте").valueBoolean,                                     //needNationalCurrency
                                        getValue(report, gRep+"ОборотыСПОД").valueBoolean,                                              //needSpodTurnTotals
                                        getValue(report, gRep+"Апострофами").valueBoolean,                                             //needApostrophes
                                        getValue(report, gRep+"ПечатьНулевых").valueBoolean,                                            //needZeroAccounts
                                        not isShowOcp,                                                                             //needExcludeOcpAccounts - по C-коду (если в реестре Да - false, Нет - true)
                                        getValue(report, gRep+"ЗаКаждыйДень").valueBoolean,                                            //needEverydayReport
                                        getValue(report, gFmt+"plainText").valueBoolean,                                                //needPlainText
                                        getValue(report, gFmt+"Excel").valueBoolean,                                                     //needExcelText
                                        true                                                                                                    //запуск из WEB-интерфейса
                                       );

    // При удачном выполнении
    if (result.valueBoolean == TRUE)

       //выводим протокол контроля
        repTxtFilesQueue().add(controlBodyFileName);

        //выводим отчет в текстовом варианте (в Excel выведется автоматически)
        if (getValue(report, gFmt+"plainText").valueBoolean == true)
            repTxtFilesQueue().add(reportBodyFileName);
        end;

    // При неудачном выполнении
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if (getFiId(getValue(report, gRep+"Валюта").valueString, true) == 0)
        repErrorsQueue().add(0, "Расчет проводится по всем валютам, за исключением нац. валюты");
        result.valueBoolean = FALSE;
    end;

    if (getValue(report, gRep+"НачалоПериода").valueDate > getValue(report, gRep+"КонецПериода").valueDate)
        repErrorsQueue().add(0, "Дата начала периода не может превышать дату его окончания");
        result.valueBoolean = FALSE;
    end;

    if (    (getValue(report, gFmt+"PlainText").valueBoolean == false)
        and (getValue(report, gFmt+"Excel").valueBoolean     == false))
        repErrorsQueue().add(0, "Не задан формат печати");
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;