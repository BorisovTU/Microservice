/*
$Name:          BalancePrint.mac
$Module:        Внутренняя отчетность
$Description:   Базовый класс печати отчета "Баланс" (валюта)
*/

/**
 * Базовый класс печати отчета "Баланс"(валюта)
 *
 * @since   07.02.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import balanceView;
import balanceData;
import balanceTemplateBuilder;
import balanceParameters;
import ofstream;
import balanceView;

private const ACTIVE  = "_Active",
              PASSIVE = "_Passive";

/**
 * Печать отчета "Баланс".
 */
class TBalancePrint(printOfstream : TOfstream)

    var m_printOfstream;

    var m_template;

    var m_views = TArray();
    var m_viewPlainText = TArray();
    var m_viewExcelText = TArray();

    var m_dataSet = TArray();;

    var m_kindAccount = TArray();

    /**
     * Печать таблицы.
     */
    private macro tablePrint(viewsArray : TArray, dataset, m_kindAccount)
        var currencyItogRecord;
        var issueItogRecord;
        var chapterItogRecord;
        var itogRecord;
        var view;
        var i;
        var isEof = not dataset.moveNext();

        if (isEof)
            i = 0;
            while (i < viewsArray.size)
                view = viewsArray[i];
                view.printHead();
                view.beginReport(dataset, m_kindAccount);
                println();
                view.printNullData();
                view.printSignature();
                i = i + 1;
            end;
        end;

        if ((not isEof) and dataset.isBalance)
            itogRecord = dataset.getRecord();
            isEof = not dataset.moveNext(dataset);

            while ((not isEof) and dataset.isCurrencyItog)

                i = 0;
                while (i < viewsArray.size)
                    view = viewsArray[i];
                    view.printHead();
                    view.beginChapter(dataset, m_kindAccount);
                    view.beginReport(dataset, m_kindAccount);
                    i = i + 1;
                end;

                if (dataset.isChapterItog)
                    chapterItogRecord = dataset.getRecord();
                    isEof = not dataset.moveNext();
                end;

                while ((not isEof) and dataset.isCurrencyItog and (not dataset.isChapterItog) )

                    i = 0;
                    while (i < viewsArray.size)
                        view = viewsArray[i];
                        view.beginCurrency(dataset, m_kindAccount);
                        i = i + 1;
                    end;

                    currencyItogRecord = dataset.getRecord();
                    isEof = not dataset.moveNext(dataset);

                    while ((not isEof) and (not dataset.isCurrencyItog))
                        if (dataset.isPartItog)
                            i = 0;
                            while (i < viewsArray.size)
                                view = viewsArray[i];
                                view.beginIssue(dataset, m_kindAccount);
                                i = i + 1;
                            end;
                            issueItogRecord = dataset.getRecord();
                            isEof = not dataset.moveNext();
                        end;

                        while ((not isEof) and (not dataset.isPartItog) and (not dataset.isCurrencyItog))
                            i = 0;
                            while (i < viewsArray.size)
                                view = viewsArray[i];
                                view.printBalance(dataset, m_kindAccount);
                                i = i + 1;
                            end;
                            isEof = not dataset.moveNext();
                        end;

                        if (issueItogRecord)
                            i = 0;
                            while (i < viewsArray.size)
                                view = viewsArray[i];
                                view.endIssue(issueItogRecord, m_kindAccount);
                                i = i + 1;
                            end;
                        end;
                    end;

                    i = 0;
                    while (i < viewsArray.size)
                        view = viewsArray[i];
                        view.endCurrency(currencyItogRecord, m_kindAccount);
                        i = i + 1;
                    end;
                end;

                if (chapterItogRecord)
                    i = 0;
                    while (i < viewsArray.size)
                        view = viewsArray[i];
                        view.endChapter(chapterItogRecord, m_kindAccount);
                        view.printSignature();
                        i = i + 1;
                    end;
                end;
            end;
        end;
    end;

    /**
     * Печать отчета.
     */
    macro execute()
        var i = 0;

        if (BalanceParameters().getIsNeedPlainText())
            m_printOfstream.setOutputFile();
        end;

        if (balanceParameters.getBalanceForm == REP_BALANCE_FORM_REST_NAME)
            m_dataSet[0] = TBalanceData().getReportDataSet(" AND blblnc.t_kind_account <> 'П' ");
            m_dataSet[1] = TBalanceData().getReportDataSet(" AND blblnc.t_kind_account = 'П' ");

            for (i, 0, 1, 1)
                m_views.size = 0;
                if (BalanceParameters().getIsNeedPlainText())
                    m_viewPlainText[i].setReportWidth(null, m_kindAccount[i]);
                    m_views[m_views.size] = m_viewPlainText[i];
                end;

                if (BalanceParameters().getIsNeedExcelText())
                    m_views[m_views.size] = m_viewExcelText[i];
                end;

                tablePrint(m_views, m_dataset[i], m_kindAccount[i]);
            end;
        else
            m_dataSet[0] = TBalanceData().getReportDataSet();

            if (BalanceParameters().getIsNeedPlainText())
                m_viewPlainText[0].setReportWidth();
                m_views[m_views.size] = m_viewPlainText[0];
            end;

            if (BalanceParameters().getIsNeedExcelText())
                m_views[m_views.size] = m_viewExcelText[0];
            end;

            tablePrint(m_views, m_dataSet[0]);
        end;

        if (BalanceParameters().getIsNeedPlainText())
            m_printOfstream.resetOutputFile();
        end;
    end;

    private macro constructor(printOfstream : TOfstream)
         m_kindAccount[m_kindAccount.size] = ACTIVE;
         m_kindAccount[m_kindAccount.size] = PASSIVE;
         m_printOfstream = printOfstream;

        if (BalanceParameters().getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            if (balanceParameters().getIsNeedPlainText())
                m_template = TActiveNameRestBalanceTemplate();
                setOutput(m_template.createResultFile(m_kindAccount[0]),  false);
                m_viewPlainText[0]  = TBalanceView(m_template.createResultFile(m_kindAccount[0]), true);
                m_template = TPassiveNameRestBalanceTemplate();
                m_viewPlainText[1]  = TBalanceView(m_template.createResultFile(m_kindAccount[1]), false);
            end;

            if (balanceParameters().getIsNeedExcelText())
                m_viewExcelText[0] = TBalanceExcelView(true);
                m_viewExcelText[1] = TBalanceExcelView(false);
            end;
        elif(BalanceParameters().getBalanceForm() == REP_BALANCE_FORM_TURN_NOIN)
            m_template = TNotInRestTurnBalanceTemplate();
            setOutput(m_template.createResultFile(),  false);
            m_viewPlainText[0]  = TBalanceView(m_template.createResultFile());

            if (balanceParameters().getIsNeedExcelText())
                m_viewExcelText[0] = TBalanceExcelView();
            end;
        elif(BalanceParameters().getBalanceForm() == REP_BALANCE_FORM_TURN)
            m_template = TTurnBalanceTemplate();
            setOutput(m_template.createResultFile(),  false);
            m_viewPlainText[0]  = TBalanceView(m_template.createResultFile());

            if (balanceParameters().getIsNeedExcelText())
                m_viewExcelText[0] = TBalanceExcelView();
            end;
        elif(BalanceParameters().getBalanceForm() == REP_BALANCE_FORM_REST)
            m_template = TRestBalanceTemplate();
            setOutput(m_template.createResultFile(),  false);
            m_viewPlainText[0]  = TBalanceView(m_template.createResultFile());

            if (balanceParameters().getIsNeedExcelText())
                m_viewExcelText[0] = TBalanceExcelView();
            end;
        end;
    end;

    constructor(printOfstream);
end;
