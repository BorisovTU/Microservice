/*
$Name:          main_natbalance.mac
$Module:        Внутренняя отчетность
$Description:   Выпуск рублевого баланса
*/

/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : main_natBalance.mac
 * Description : Выпуск рублевого баланса
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import testLib;

import NatBalanceCalculator;
import NatControlBalanceController;
import NatBalancePrint;
import ofstream;

import natBalanceData;

private var printOfstream;
private var controlOfstream;

var reportBodyFileName;     //имя файла отчета
var controlBodyFileName;    //имя файла протокола контроля

/**
 *  Отчет.
 */
macro produceReport(departmentId, organizationStructure, issueMode,
                    planNumber, chapterNumber, natcurRepAccounts,
                    periodKind, dateIn, dateOut,
                    reportForm, dimension, transFiId, needPartTotals,
                    needSpodTurnTotals, needApostrophes, needZeroAccounts,
                    needExcludeOcpAccounts, needEverydayReport,
                    needPlainText, needExcelText,
                    isWeb)

    var sqlProfiler = TSqlProfiler(getTxtFileName("blnc_nat_sqlprf"));
    sqlProfiler.clear();
    sqlProfiler.on();

    /*инициализация параметров для балансового отчета*/
    initParameters(
                   departmentId, organizationStructure, issueMode,
                   planNumber, chapterNumber, natcurRepAccounts,
                   periodKind, dateIn, dateOut,
                   reportForm, dimension, transFiId, needPartTotals,
                   needSpodTurnTotals, needApostrophes, needZeroAccounts,
                   needExcludeOcpAccounts, needEverydayReport,
                   needPlainText, needExcelText,
                   ternary(isWeb == null, false, isWeb)
                  );

    /* Контроль открытых опердней */
    if (isEqClass("RepDepartmentList", natBalanceParameters().getDepartmentList()))
        var opd = RepOperdaysOpened(natBalanceParameters().getDepartmentList(),
                                    natBalanceParameters().getDateIn(),
                                    natBalanceParameters().getDateOut());
        if (not opd.shouldContinue())
            exit(1);
        end;
    end;

    printOfstream   = TOfstream("nat_balance");
    controlOfstream = TOfstream("ctrl_balance");

    var natBalancePrint;
    var natControlBalanceController = TControlBalanceController(controlOfstream);

    var m_dateOut = natBalanceParameters().getDateOut();

    begAction(1000, "Выпускается отчет. Ждите...");

    if (natBalanceParameters().getIsPrintEveryDay() == true)
        natBalanceParameters().setDateOut(natBalanceParameters().getDateIn());

        while (natBalanceParameters().getDateOut() <= m_dateOut)
            TBalanceCalculator().calculate();
            natBalancePrint = TNatBalancePrint(printOfstream);
            natBalancePrint.execute();
            natControlBalanceController.control();

            natBalanceParameters().setDateIn(natBalanceParameters().getDateIn() + 1);
            natBalanceParameters().setDateOut(natBalanceParameters().getDateOut() + 1);
        end;
    else
        TBalanceCalculator().calculate();
        natBalancePrint = TNatBalancePrint(printOfstream);
        natBalancePrint.execute();
        natControlBalanceController.control();
    end;

    endAction();

    reportBodyFileName  = printOfstream.getFileName();
    controlBodyFileName = controlOfstream.getFileName();

    return true;
end;

macro showReport()
    //протокол контроля
    controlOfstream.show();

    //если задан plain text, то выводим отчет в текстовом виде
    if (natBalanceParameters().getIsNeedPlainText())
        printOfstream.show();
    end;

    //если задан Excel, то выводим документ Excel
    if (natBalanceParameters().getIsNeedExcelText())
    end;
end;

macro saveReport(reportName, controlName)
    controlOfstream.save(controlName);
    printOfstream.save(reportName);
end;

macro batchProduce(reportName, controlName,
                   departmentId, organizationStructure, issueMode, planNumber, chapterNumber, natcurRepAccounts, periodKind, dateIn,
                   dateOut, reportForm, dimension, transFiId, needPartTotals, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                   needExcludeOcpAccounts, needEverydayReport,
                   needPlainText, needExcelText)

//    var dialogFlag = setDialogFlag(0);

    produceReport(departmentId, organizationStructure, issueMode, planNumber, chapterNumber, natcurRepAccounts, periodKind, dateIn,
                  dateOut, reportForm, dimension, transFiId, needPartTotals, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                  needExcludeOcpAccounts, needEverydayReport,
                  needPlainText, needExcelText);

    saveReport(reportName, controlName);

//    setDialogFlag(dialogFlag);
end;
