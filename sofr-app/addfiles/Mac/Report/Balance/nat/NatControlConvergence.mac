/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatControlConvergence.mac
 * Description : Контроль сходимости
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

/**
 * Контроль сходимости
 */
class TControlConvergence(dataSource : Object)

    private var m_dataSource : Object;

    /**
     * Печать заголовка
     */
    private macro printCaption()
        println();
        println("КОНТРОЛЬ НА СХОДИМОСТЬ БАЛАНСА");
        println();
    end;

    /**
     * Печать сообщения об успешном контроле
     */
    private macro printSuccessful()
        println("Контроль прошел успешно");
    end;

    /**
     * Печать сообщения о неуспешном контроле
     */
    private macro printFail()

        var report = CTableReport(0, false, false);

        var isDone = false;

        report.addColumn("Гл", 3, AL_CENTER);
        report.addColumn("Остаток по активу/|Обороты по дебету", 24, AL_RIGHT);
        report.addColumn("Остаток по пассиву/|Обороты по кредиту", 24, AL_RIGHT);
        report.addColumn("Разность", 24, AL_RIGHT);
        report.addColumn("Примечание", 35, AL_LEFT);

        report.printHead("Баланс нарушен. Проверьте правильность открытия счетов и отражения операций.");

        while (not isDone)

            report.printString(m_dataSource.chapterNumber,
                               m_dataSource.firstValue,
                               m_dataSource.secondValue,
                               m_dataSource.difference,
                               m_dataSource.note);

            isDone = not m_dataSource.next();

        end;

        report.printBottom();

    end;

    /**
     * Конструктор
     *
     * @param     dataSource
     */
    private macro constructor(dataSource : Object)
        m_dataSource = dataSource;
    end;

    /**
     * Выполнение контроля
     */
    macro execute()

        printCaption();

        if (m_dataSource.next())
            printFail();
        else
            printSuccessful();
        end;

    end;

    constructor(dataSource);

end;
