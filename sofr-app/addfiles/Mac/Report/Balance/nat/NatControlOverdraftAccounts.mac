/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatControlOverdraftAccounts.mac
 * Description : Контроль б/с на наличие "красного сальдо"
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

/**
 * Контроль л/с на наличие "красного сальдо"
 */
class TControlOverdraftAccounts(dataSource : Object)

    private var m_dataSource : Object;

    /**
     * Печать заголовка
     */
    private macro printCaption()
        println();
        println("КОНТРОЛЬ НА НАЛИЧИЕ \"КРАСНОГО САЛЬДО\"");
        println();
    end;

    /**
     * Печать сообщения об успешном контроле
     */
    private macro printSuccessful()
        println("Контроль прошел успешно");
    end;

    /**
     * Печать сообщения о неуспешном контроле
     */
    private macro printFail()

        var isDone = false;
        var report = CTableReport(0, false, false);

        report.addColumn("Номер лицевого счета", 25, AL_LEFT);
        report.addColumn("Вид", 3, AL_CENTER);
        report.addColumn("Входящий остаток", 24, AL_RIGHT);
        report.addColumn("Исходящий остаток", 24, AL_RIGHT);

        report.printHead("Выявлено \"красное сальдо\" по счетам:");

        while (not isDone)
            report.printString(m_dataSource.accountNumber,
                               m_dataSource.accountKind,
                               m_dataSource.inRest,
                               m_dataSource.outRest);

            isDone = not m_dataSource.next();
        end;

        report.printBottom();

    end;

    /**
     * Конструктор
     *
     * @param     dataSource
     */
    private macro constructor(dataSource : Object)
        m_dataSource = dataSource;
    end;

    /**
     * Выполнение контроля
     */
    macro execute()

        printCaption();

        if (m_dataSource.next())
            printFail();
        else
            printSuccessful();
        end;

    end;

    constructor(dataSource);

end;
