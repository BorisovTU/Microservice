/*
$Name:          NatBalanceParameters.mac
$Module:        Внутренняя отчетность
$Description:   Класс параметров отчета "Баланс"
*/

/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatBalanceParameters.mac
 * Description : Параметры отчета
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import ReportInter;
import repException;

import Reporting, ReportInter, Календарь, FIInter, PTInter, CTInter,
       PaymInter, OprInter, CurrInter, BankInter;
import cb_sql, globals, lib_str, lib_lang, lib_arr;
import acv;
import RsbObjFactory;

import param;

/**
 * Глобальная ссылка для хранения одиночки.
 */
private var m_parameters = null;

const DAT_SIGN = 0;
const DAT_TYPE = 1;

const NODETYPE_FILIAL = 1, //Тип узла ТС - Филиал
      NODETYPE_BRANCH = 2; //Тип узла ТС - Подразделение

const ORGSTRUCTURE_TERRITORIAL = 1; //Территориальная структура

const MODE_BRANCH = 1; // Режим выпуска отчета - Подразделение
const MODE_FILIAL = 2; // Режим выпуска отчета - Филиал
const MODE_BANK   = 3; // Режим выпуска отчета - Банк

var USE_REPDEPARTMENTLIST = true;
if (GetRegistryValue("REPORT\\DISABLE DepartmentList 1", V_BOOL, USE_REPDEPARTMENTLIST) != null)
    USE_REPDEPARTMENTLIST = not USE_REPDEPARTMENTLIST;
else
    USE_REPDEPARTMENTLIST = true;
end;

const ALLCURRENCY = -2;

private class TDepartmentList(orgStructure : Integer, issueMode : Integer, departmentCode : Integer)
    private var m_departmentList;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_departmentCode;
    private var m_sourceTableName : String;
    private var m_resultTableName : String;

    private macro fillDepartmentTable()
        var strFilter : String;

        if (m_issueMode == MODE_BRANCH)
            strFilter = "(repDep.t_code = " + m_departmentCode + ")";
        elif (m_issueMode == MODE_FILIAL)
            strFilter = "(repDep.t_nodeType = " + NODETYPE_BRANCH + " OR repDep.t_code = " + m_departmentCode + ")";
        elif (m_issueMode == MODE_BANK)
            strFilter = "(1 = 1)";
        end;

        var strCmd = "INSERT INTO " + m_resultTableName + "(t_code, t_listType)"
            + "\n" + "SELECT DISTINCT repDep.t_code,                           "
            + "\n" + "                repDep.t_nodeType                        "
            + "\n" + "       FROM " + m_sourceTableName + " repDep             "
            + "\n" + " WHERE " + strFilter
            + "\n" + " START WITH repDep.t_code = " + m_departmentCode
            + "\n" + " CONNECT BY PRIOR repDep.t_nodeId = repDep.t_parentNodeId";

        sql_execute(strCmd);
    end;

    private macro clearTable()
        sql_execute("TRUNCATE TABLE " + m_resultTableName );
    end;

    macro saveToTable()
        if (USE_REPDEPARTMENTLIST)
            m_departmentList.saveToTable();
        else
            fillDepartmentTable();
        end;
    end;

    macro getDepartmentListObject()
        return m_departmentList;
    end;

    macro constructorTDepartmentList(orgStructure : Integer, issueMode : Integer, departmentCode : Integer)
        m_orgStructure   = orgStructure;
        m_issueMode      = issueMode;
        m_departmentCode = departmentCode;
        m_resultTableName = "dbldept_tmp";

        if (USE_REPDEPARTMENTLIST)
            m_departmentList = RepDepartmentList(orgStructure, issueMode, departmentCode);
        else
            m_departmentList = NULL;
            m_sourceTableName = ternary(orgStructure == ORGSTRUCTURE_TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw");

            clearTable();
        end;
    end;

    private macro destructor()
        clearTable();
    end;

    constructorTDepartmentList(orgStructure, issueMode, departmentCode);
end;

private class TOcpAccountServer(chapter : Integer, finInstrument : Integer, departmentList : Object)
    private var m_ocpAccountServer;
    private var m_chapter;
    private var m_finInstrument;
    private var m_departmentList;
    private var m_resultTableName : String;
    private var m_departmentListTableName : String;

    private macro fillOcpAccountServerTable()
        var strFilter : String = "";

        if (m_chapter > 0)
            strFilter = "AND account.t_chapter = " + m_chapter;
        end;

        if (m_finInstrument == ALLFININSTR)
            strFilter = strFilter + "AND (1 = 1)";
        elif (m_finInstrument == ALLCURRENCY)
            strFilter = strFilter + " AND account.t_code_currency != " + NATCUR;
        else
            strFilter = strFilter + " AND account.t_code_currency = " + m_finInstrument;
        end;

        var strCmd = "INSERT INTO " + m_resultTableName + "(t_chapter, t_fiId, t_account, t_accountId)"
            + "\n" + "SELECT account.t_chapter       t_chapter,"
            + "\n" + "       account.t_code_currency t_fiId,"
            + "\n" + "       account.t_account       t_account,"
            + "\n" + "       account.t_accountId     t_accountId"
            + "\n" + "  FROM daccount_dbt account"
            + "\n" + " WHERE INSTR(account.t_type_account, 'Щ') > 0"
            + "\n" + "   AND EXISTS(SELECT 1 "
            + "\n" + "                FROM " + m_departmentListTableName + " dep"
            + "\n" + "               WHERE account.t_branch = dep.t_code)"
            + "\n" +         strFilter;


        sql_execute(strCmd);
    end;

    private macro clearTable()
        sql_execute("TRUNCATE TABLE " + m_resultTableName );
    end;

    macro saveToTable()
        if (USE_REPDEPARTMENTLIST)
            m_ocpAccountServer.saveToTable();
        else
//            fillOcpAccountServerTable();
        end;
    end;

    macro constructorTOcpAccountServer(chapter : Integer, finInstrument : Integer, departmentList : Object)
        m_chapter        = chapter;
        m_finInstrument  = finInstrument;
        m_departmentList = departmentList;
        m_departmentListTableName = "dbldept_tmp";
        m_resultTableName = "dRepOcp_tmp";

        if (USE_REPDEPARTMENTLIST)
            m_ocpAccountServer = RepOcpAccountServer(chapter, finInstrument, departmentList);
        else
            m_ocpAccountServer = NULL;

            clearTable();
        end;
    end;

    private macro destructor()
        clearTable();
    end;

    constructorTOcpAccountServer(chapter, finInstrument, departmentList);
end;

class recalcData(_curName : String, _curRate, _curRateDate : String)
    var curName     = _curName;
    var curRate     = _curRate;
    var curRateDate = _curRateDate;
end;

/**
 * Параметры отчета.
 */
private class TNatBalanceParameters(parmArray : TArray)
    /**
     * Код подразделения.
     */
    private var m_departmentCode : Integer;

    /**
     * Оргструктура.
     */
    private var m_orgStructure : Integer;

    /**
     * Режим выпуска.
     */
    private var m_issueMode : Integer;

    /**
     * Глава счетов.
     */
    private var m_chapter : Integer;

    /**
     * Счета, включаемые в отчет по нац. валюте.
     */
    private var m_natcurRepAccounts : Integer;

    /**
     * Номер плана счетов.
     */
    private var m_plan : Integer;

    /**
     * Вид отчетного периода.
     */
    private var m_periodKind : Integer;

    /**
     * Дата начала отчетного периода.
     */
    private var m_dateIn : Date;

    /**
     * Дата конца отчетного периода.
     */
    private var m_dateOut : Date;

    /**
     * Форма балансового отчета.
     */
    private var m_balanceForm : Integer;

    /**
     * Размерность денежных сумм.
     */
    private var m_moneyDimension : Integer;

    /**
     * Валюта пересчета.
     */
    private var m_transFiId : Integer;

    /**
     * Параметры пересчета.
     */
    private var m_recalcInfo : recalcData;

    /**
     * Итоги по разделам?
     */
    private var m_isPrintIssueItog : Bool;

    /**
     * Учет Спод?
     */
    private var m_isPrintSpod : Bool;

    /**
     * С апострофами?
     */
    private var m_isPrintApostrophe : Bool;

    /**
     * Печатать нулевые данные?
     */
    private var m_isPrintZeroData : Bool;

    /**
     * Признак исключения счетов ОВП
     */
    private var m_isExcludeOcp : Bool;

    /**
     * Печать отчета за каждый день периода?
     */
    private var m_isPrintEveryDay : Bool;

    /**
     * Печать отчета в текстовом виде?
     */
    private var m_isNeedPlainText : Bool;

    /**
     * Печать отчета таблице Excel?
     */
    private var m_isNeedExcelText : Bool;

    /**
     * Отчет выпускается из веба?
     */
    private var m_isWeb : Bool;

    private var m_departmentList : Object;

    private var m_ocpAccountServer : Object;

    private var m_recalcState : bool;

    private var m_accountTypeDefinitionMethod : Integer;

    /**
     * @param     departmentCode
     * @param     orgStructure
     * @param     issueMode
     *
     * @param     plan
     * @param     chapter
     * @param     natcurRepAccounts
     *
     * @param     periodKind
     * @param     dateIn
     * @param     dateOut
     *
     * @param     balanceForm
     * @param     dimension
     * @param     transFiId
     * @param     isPrintIssueItog
     *
     * @param     isPrintSpod
     * @param     isPrintApostrophe
     * @param     isPrintZeroData
     * @param     isExcludeOcp
     * @param     isPrintEveryDay

     * @param     needPlainText
     * @param     needExcelText
     */
    private macro constructor(parmArray : TArray)
        var i = -1;
        var errorCode;
        var accountTypeDefinitionMethod;

        m_departmentCode    = parmArray[i=i+1];
        m_orgStructure      = parmArray[i=i+1];
        m_issueMode         = parmArray[i=i+1];

        m_plan              = parmArray[i=i+1];
        m_chapter           = parmArray[i=i+1];
        m_natcurRepAccounts = parmArray[i=i+1];

        m_periodKind        = parmArray[i=i+1];
        m_dateIn            = parmArray[i=i+1];
        m_dateOut           = parmArray[i=i+1];

        m_balanceForm       = parmArray[i=i+1];
        m_moneyDimension    = parmArray[i=i+1];
        m_transFiId         = parmArray[i=i+1];
        m_isPrintIssueItog  = parmArray[i=i+1];

        m_isPrintSpod       = parmArray[i=i+1];
        m_isPrintApostrophe = parmArray[i=i+1];
        m_isPrintZeroData   = parmArray[i=i+1];
        m_isExcludeOcp      = parmArray[i=i+1];
        m_isPrintEveryDay   = parmArray[i=i+1];

        m_isNeedPlainText   = parmArray[i=i+1];
        m_isNeedExcelText   = parmArray[i=i+1];
        m_isWeb             = parmArray[i=i+1];

        m_departmentList   = TDepartmentList(m_orgStructure, m_issueMode, m_departmentCode);
        m_ocpAccountServer = TOcpAccountServer(m_chapter, ALLFININSTR, m_departmentList.getDepartmentListObject());

        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ПЕРЕМЕННЫЕ\\DEFINEACCOUNTTYPE", V_STRING, accountTypeDefinitionMethod, errorCode);
        if (errorCode == 0)
            m_accountTypeDefinitionMethod = ternary(trim(strUpr(accountTypeDefinitionMethod)) == "TYPE", DAT_TYPE, DAT_SIGN);
        else
            m_accountTypeDefinitionMethod = DAT_TYPE;
        end;

        m_recalcInfo = null;
    end;

    macro getDepartmentCode() : Integer
        return m_departmentCode;
    end;

    macro getOrgStructure() : Integer
        return m_orgStructure;
    end;

    macro getIssueMode() : Integer
        return m_issueMode;
    end;

    macro getChapter() : Integer
        return m_chapter;
    end;

    macro getNatcurRepAccounts() : Integer
        return m_natcurRepAccounts;
    end;

    macro getPlan() : Integer
        return m_plan;
    end;

    macro getPeriodKind() : Integer
        return m_periodKind;
    end;

    macro getDateIn() : Date
        return m_dateIn;
    end;

    macro getDateOut() : Date
        return m_dateOut;
    end;

    macro getBalanceForm() : Integer
        return m_balanceForm;
    end;

    macro getMoneyDimension() : Integer
        return m_moneyDimension;
    end;

    macro getTransFiId() : Integer
        return m_transFiId;
    end;

    macro getRecalcInfo() : recalcData
        return m_recalcInfo;
    end;

    macro getIsPrintIssueItog() : Bool
        return m_isPrintIssueItog;
    end;

    macro getIsPrintSpod() : Bool
        return m_isPrintSpod;
    end;

    macro getIsPrintApostrophe() : Bool
        return m_isPrintApostrophe;
    end;

    macro getIsPrintZeroData() : Bool
        return m_isPrintZeroData;
    end;

    macro getIsExcludeOcp() : Bool
        return m_isExcludeOcp;
    end;

    macro getIsPrintEveryDay() : Bool
        return m_isPrintEveryDay;
    end;

    macro getIsNeedPlainText() : Bool
        return m_isNeedPlainText;
    end;

    macro getIsNeedExcelText() : Bool
        return m_isNeedExcelText;
    end;

    macro getIsWeb() : Bool
        return m_isWeb;
    end;

    macro getDepartmentList() : Object
        return m_departmentList;
    end;

    macro getOcpAccountServer() : Object
        return m_ocpAccountServer;
    end;

    macro isRecalcInitiatedCorrectly() : bool
        return m_recalcState;
    end;

    /**
     * @param     blinfo
     * @param     chapter
     */
    macro getBlinfo(blinfo, chapter)
        blinfo.szBaseFileName = "blrept.tmp"; // наименование таблицы с рассчитанным балансом, обязательно нужно заполнить.
        blinfo.iChapter = chapter;
        execExp("blinfo.iPlanNumber" + chapter + " = m_plan");
        blinfo.iUnit = m_moneyDimension;
    end;

    macro getAccountTypeDefinitionMethod() : Integer
        return m_accountTypeDefinitionMethod;
    end;

    /**
     * @param     newM_dateIn
     */
    macro setDateIn(newM_dateIn : Date)
        m_dateIn = newM_dateIn;
    end;

    /**
     * @param     newM_dateOut
     */
    macro setDateOut(newM_dateOut : Date)
        m_dateOut = newM_dateOut;
    end;

    /**
     * @param     newM_recalcState
     */
    macro setRecalcState(newM_recalcState : bool)
        m_recalcState = newM_recalcState;
    end;

    macro setRecalcInfo(recalcInfo : recalcData)
        return m_recalcInfo = recalcInfo;
    end;

    /**
     * @param     newM_accountTypeDefinitionMethod
     */
    macro setAccountTypeDefinitionMethod(newM_accountTypeDefinitionMethod : Integer)
        m_accountTypeDefinitionMethod = newM_accountTypeDefinitionMethod;
    end;

    macro getNodeType()
        var queryString = "SELECT t_nodeType "
                 + "\n" + "  FROM " + ternary(m_orgStructure == ORGSTRUCTURE_TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw")
                 + "\n" + " WHERE t_nodeId = " + m_departmentCode;

        var ds = TRsbDataSet(queryString);

        if (ds.moveNext())
            return ds.nodeType;
        end;

        return NULL;
    end;

    macro useDepartmentFilter()
        var queryString = "SELECT t_code "
                 + "\n" + "  FROM " + ternary(m_orgStructure == ORGSTRUCTURE_TERRITORIAL, "dRepDpDep_vw", "dRepDpReg_vw")
                 + "\n" + " WHERE t_partyId = " + {OurBank};

        var ds = TRsbDataSet(queryString);

        if (ds.moveNext())
            return not ((ds.code == getDepartmentCode()) and (getIssueMode() == MODE_BANK));
        end;

        return true;
    end;

    constructor(parmArray);
end;

/**
 * Точка доступа.
 */
macro natBalanceParameters() : TNatBalanceParameters
    return m_parameters;
end;

//инициализация
macro initParameters()
    var i = 0;
    var parmArray = TArray();

    if (parmCount() == 0)
        throw(TWrongParametersCountException(natBalanceParameters));
    end;

    while (i < parmCount())
        getParm(i, parmArray[i]);
        i = i + 1;
    end;

    m_parameters = TNatBalanceParameters(parmArray);
end;
