/*
$Name:          NatControlRestsConformity.mac
$Module:        Внутренняя отчетность
$Description:   Контроль остатков счетов глав В и Г
*/

/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatControlRestsConformity.mac
 * Description : Контроль остатков счетов главы В (внебаланс)
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

/**
 * Контроль остатков счетов глав В (внебаланс) и Г (срочные сделки)
 */
class TControlOffBalanceRestsConformity(activeRestsDataSource : Object, passiveRestsDataSource : Object, chapter : Integer)

    private var m_activeRestsDataSource : Object;

    private var m_passiveRestsDataSource : Object;

    private var m_chapter : Integer;                //глава баланса

    /**
     * Печать заголовка
     */
    private macro printCaption()
        var header = "КОНТРОЛЬ ДЛЯ ГЛАВЫ ";
        if (m_chapter == 3)
            header = header + "В";
        elif (m_chapter == 4)
            header = header + "Г";
        end;
        println();
        println(header);
        println();
    end;

    /**
     * Печать сообщения об успешном контроле
     */
    private macro printSuccessful()
        println("Контроль прошел успешно");
    end;

    /**
     * Печать сообщения о неуспешном контроле
     *
     * @param     controlBalance    Номер контроллируемого счета (99999, 99998, 99997 или 99996)
     */
    private macro printFail(controlBalance : String)

        var accountTypeName = ternary((controlBalance == "99999") or (controlBalance == "99997"), "активных", "пассивных");
        var dataSource      = ternary((controlBalance == "99999") or (controlBalance == "99997"), m_activeRestsDataSource, m_passiveRestsDataSource);

        var report = CTableReport(0, false, false);

        var isDone = false;
        var excludedAccount;

        if (m_chapter == 3)
            excludedAccount = ternary(controlBalance == "99999", "99998", "99999");
        elif (m_chapter == 4)
            excludedAccount = ternary(controlBalance == "99997", "99996", "99997");
        end;

        report.addColumn("Сумма остатков на|" + accountTypeName + " счетах|(кроме " + excludedAccount + ")", 24, AL_RIGHT);
        report.addColumn("Остаток на счете|" + controlBalance, 24, AL_RIGHT);
        report.addColumn("Разность", 24, AL_RIGHT);
        report.addColumn("Примечание", 35, AL_LEFT);

        report.printHead();

        while (not isDone)

            report.printString(//dataSource.isoNumber,
                               dataSource.totals,
                               dataSource.controlValue,
                               dataSource.difference,
                               dataSource.note);

            isDone = not dataSource.next();

        end;

        report.printBottom();

    end;

    /**
     * Конструктор
     *
     * @param     activeRestsDataSource  Источник данных для контроля остатков активных счетов
     * @param     passiveRestsDataSource Источник данных для контроля остатков пассивных счетов
     */
    private macro constructor(activeRestsDataSource : Object, passiveRestsDataSource : Object, chapter : String)

        m_activeRestsDataSource = activeRestsDataSource;

        m_passiveRestsDataSource = passiveRestsDataSource;

        m_chapter = chapter;
    end;

    /**
     * Выполнение контроля
     */
    macro execute()

        printCaption();

        var res = true;

        if (m_activeRestsDataSource.next())
            if   (m_chapter == 3)
                printFail("99999");
            elif (m_chapter == 4)
                printFail("99997");
            end;
            res = false;
        end;

        if (m_passiveRestsDataSource.next())
            println();
            if   (m_chapter == 3)
                printFail("99998");
            elif (m_chapter == 4)
                printFail("99996");
            end;
            res = false;
        end;

        if (res)
            printSuccessful();
        end;

    end;

    constructor(activeRestsDataSource, passiveRestsDataSource, chapter);

end;
