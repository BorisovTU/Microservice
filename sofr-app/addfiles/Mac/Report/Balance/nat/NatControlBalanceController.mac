/*
$Name:              NatControlBalanceController.mac
$Module:            Внутренняя отчетность
$Description:       Контроль выпуска баланса банка
*/
/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatControlBalanceController.mac
 * Description : Контроль выпуска баланса банка
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import globals;
import natBalanceParameters;
import Ofstream;
import natControlBalanceData;
import natControlConvergence;
import natControlOverdraftAccounts;
import natControlRestsConformity;

/**
 * Контроллер контроля баланса
 */
class TControlBalanceController(ofstream : TOfstream)

    private var m_ofstream : TOfstream;

    /**
     * Фабрика источников данных
     */
    private var m_controlBalanceData : TControlBalanceData;

    /**
     * Конструктор
     */
    private macro constructor(ofstream : TOfstream)

        m_controlBalanceData = TControlBalanceData();

        m_ofstream = ofstream;

    end;

    /**
     * Печать "шапки" протокола
     */
    private macro printHeader()

        println("БАЛАНС КРЕДИТНОЙ ОРГАНИЗАЦИИ");
        println("ПРОТОКОЛ КОНТРОЛЯ");
        println();
        println("   Период отчета с ", natBalanceParameters().getDateIn(), " по ", natBalanceParameters().getDateOut());
        println("   Дата и время выпуска отчета ", Date(), "  ", Time());
        println("   Исполнитель ", {oper}, " ", {Name_Oper});

    end;

    /**
     * Выполнение контроля
     */
    macro control()
        var nodeType = natBalanceParameters().getNodeType();

        m_ofstream.setOutputFile();

        printHeader();

        // Контроль на "красное сальдо"
        var DISABLE_OVERDRAFT = false;
        if ((GetRegistryValue("REPORT\\DISABLE OVERDRAFT 1", V_BOOL, DISABLE_OVERDRAFT) == null) or not DISABLE_OVERDRAFT)
            TControlOverdraftAccounts(m_controlBalanceData.getOverdraftAccountsDataSource()).execute();
        end;

        // Следующие виды контраля не выполнять, есди: (Тип узла ТС = "ВСП") или (Тип узла ТС = "Филиал" и Режим выпуска отчета = "Подразделение")
        if (not ((nodeType == NODETYPE_BRANCH) or ((nodeType == NODETYPE_FILIAL) and (natBalanceParameters().getIssueMode() == MODE_BRANCH))))
            // Контроль сходимости - только если:
            // "Включать счета"  == "Все счета"
            if (natBalanceParameters().getNatcurRepAccounts() == 3)
                TControlConvergence(m_controlBalanceData.getConvergenceDataSource()).execute();
            end;

            // Контроль остатков главы В
            if ((natBalanceParameters().getChapter() == 0) or (natBalanceParameters().getChapter() == 3))
                TControlOffBalanceRestsConformity(m_controlBalanceData.getOffBalanceActiveRestsConformityChapter3DataSource(),
                                                  m_controlBalanceData.getOffBalancePassiveRestsConformityChapter3DataSource(),
                                                  3).execute();
            end;
            // Контроль остатков главы Г
            if ((natBalanceParameters().getChapter() == 0) or (natBalanceParameters().getChapter() == 4))
                TControlOffBalanceRestsConformity(m_controlBalanceData.getOffBalanceActiveRestsConformityChapter4DataSource(),
                                                  m_controlBalanceData.getOffBalancePassiveRestsConformityChapter4DataSource(),
                                                  4).execute();
            end;
        end;

        m_ofstream.resetOutputFile();

    end;

    constructor(ofstream);

end;
