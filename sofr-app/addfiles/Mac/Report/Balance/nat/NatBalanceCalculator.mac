/*
$Name:          NatBalanceCalculator.mac
$Module:        Внутренняя отчетность
$Description:   Расчет рублевого баланса
*/
/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 * File Name   : NatBalanceCalculator.mac                      February 12, 2008
 * Description : Расчет рублевого баланса
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import cb_sql;
import rcbconst;
import Reporting;
import BalanceInter;
import NatBalanceParameters;

/**
 * Класс реализует расчет баланса банка
 */
class TBalanceCalculator()

    /**
     * Инициализация пакета rsb_balance
     */
    private macro initializePackage(chapterNumber)

        var cmd = RsdCommand("BEGIN rsb_balance.init(?,?,?,?,?,?,?,?,?,?,?,?,?,?); END;");

        var plan = natBalanceParameters().getPlan();

        var p_is_curr = 0;
        var p_codcurr = 0;
        var p_begdate = natBalanceParameters().getDateIn();
        var p_enddate = natBalanceParameters().getDateOut();
        var p_chapter = chapterNumber;
        var p_numplan = plan;
        var p_partbln = ternary(natBalanceParameters().getIsPrintIssueItog(), 1, 0);
        var p_covsign = natBalanceParameters().getNatcurRepAccounts();
        var p_bwpused = 0;
        var p_prnzero = ternary(natBalanceParameters().getIsPrintZeroData(), 1, 0);
        var p_showocp = 0; //ternary(natBalanceParameters().getIsExcludeOcp(), 0, 1);
        var p_realrub = 0;
        var p_defacct = natBalanceParameters().getAccountTypeDefinitionMethod();
        var p_useDepartmentFilter = ternary(natBalanceParameters().useDepartmentFilter(), 1, 0);

        cmd.addParam("p_is_curr", RSDBP_IN, p_is_curr);
        cmd.addParam("p_codcurr", RSDBP_IN, p_codcurr);
        cmd.addParam("p_begdate", RSDBP_IN, p_begdate);
        cmd.addParam("p_enddate", RSDBP_IN, p_enddate);
        cmd.addParam("p_chapter", RSDBP_IN, p_chapter);
        cmd.addParam("p_numplan", RSDBP_IN, p_numplan);
        cmd.addParam("p_partbln", RSDBP_IN, p_partbln);
        cmd.addParam("p_covsign", RSDBP_IN, p_covsign);
        cmd.addParam("p_bwpused", RSDBP_IN, p_bwpused);
        cmd.addParam("p_prnzero", RSDBP_IN, p_prnzero);
        cmd.addParam("p_showocp", RSDBP_IN, p_showocp);
        cmd.addParam("p_realrub", RSDBP_IN, p_realrub);
        cmd.addParam("p_defacct", RSDBP_IN, p_defacct);
        cmd.addParam("p_useDepartmentFilter", RSDBP_IN, p_useDepartmentFilter);

        sql_execute(cmd);
    end;

    private macro setRateValuesForDate(_date_, rate, rcourse)

        const RATETYPE_CB = 7;

        var selectText = "WITH rateDef AS (SELECT t_rateId, t_rate, t_scale, t_point,"
                + "\n" + "                        TO_DATE(TO_CHAR(t_sinceDate, 'DD.MM.YYYY') || ' 23:59:59', 'DD.MM.YYYY HH24:MI:SS') t_sinceDate"
                + "\n" + "                   FROM drateDef_dbt"
                + "\n" + "                  WHERE t_fiid = " + NATCUR + " AND t_otherfi = " + natBalanceParameters().getTransFiId() + " AND t_type = " + RATETYPE_CB + " AND t_isDominant = 'X'),"
                + "\n" + "     rateHist"
                + "\n" + "         AS (SELECT MAX(t_sinceDate) OVER (PARTITION BY t_rateId) t_maxDate,"
                + "\n" + "                    t_rateId, t_rate, t_scale, t_point, t_sinceDate"
                + "\n" + "               FROM (SELECT rH.t_rateId, rH.t_rate, rH.t_scale, rH.t_point,"
                + "\n" + "                            TO_DATE(TO_CHAR(rH.t_sinceDate, 'DD.MM.YYYY') || ' ' || TO_CHAR(rH.t_inputTime, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS') t_sinceDate"
                + "\n" + "                       FROM dratehist_dbt rH, rateDef"
                + "\n" + "                      WHERE rH.t_rateId = rateDef.t_rateId"
                + "\n" + "                     UNION ALL"
                + "\n" + "                     SELECT * FROM rateDef)"
                + "\n" + "              WHERE t_sinceDate <= TO_DATE(TO_CHAR(" + getSqlDate(_date_) + ", 'DD.MM.YYYY') || '23:59:59', 'DD.MM.YYYY HH24:MI:SS'))"
                + "\n" + "SELECT fi.t_name t_curName,"
                + "\n" + "       rh.t_rate, rh.t_scale, rh.t_point,"
                + "\n" + "       TO_CHAR(rh.t_maxDate, 'DD.MM.YYYY') AS t_sinceDate"
                + "\n" + "  FROM rateHist rh, dfininstr_dbt fi"
                + "\n" + " WHERE rh.t_sinceDate = rh.t_maxDate"
                + "\n" + "   AND fi.t_fiid = " + natBalanceParameters().getTransFiId();

        var dataSet = TRsbDataSet(selectText);
        if (dataSet.next)
            setParm(2, dataSet.scale * pow(10, dataSet.point) / dataSet.rate);
            setParm(3, dataSet.rate);
            natBalanceParameters().setRecalcInfo(recalcData(dataSet.curName,
                                                            dataSet.rate / pow(10, dataSet.point) / nvl(dataSet.scale, 1),
                                                            dataSet.sinceDate)
                                                );
            return true;
        else
            return false;
        end;

    end;
    /**
     * Инициализация параметров пересчета
     */
    private macro initializeRecalculation()

        var cmd = RsdCommand("BEGIN rsb_balance.addCurrencyRate(?,?,?,?,?,?,?); END;");
        var ok = true;

        var p_codecur = NATCUR;
        var p_in_rate;
        var p_outrate;
        var p_rcourse;
        var p_scale   = 1; //масштаб
        var p_point   = 0;
        var p_outquot = 0; //признак обратного

        // из рублей (или рублевого эквивалента) в рубли не пересчитываем
        if (natBalanceParameters().getTransFiId() == NATCUR)
            natBalanceParameters().setRecalcState(false);
            return;
        end;

        ok =    setRateValuesForDate(natBalanceParameters().getDateIn(),  p_in_rate, p_rcourse)
            and setRateValuesForDate(natBalanceParameters().getDateOut(), p_outrate, p_rcourse);

        if (ok)
            natBalanceParameters().setRecalcState(true);
        else
            msgbox("Не найден курс ЦБ РФ для заданной валюты пересчета.\nВыпуск отчета будет продолжен без пересчета.");
            p_in_rate = 1;
            p_outrate = 1;
            p_rcourse = 1;
            natBalanceParameters().setRecalcState(false);
        end;

        cmd.addParam("p_codecur", RSDBP_IN, p_codecur);
        cmd.addParam("p_in_rate", RSDBP_IN, p_in_rate);
        cmd.addParam("p_outrate", RSDBP_IN, p_outrate);
        cmd.addParam("p_rcourse", RSDBP_IN, p_rcourse);
        cmd.addParam("p_scale",   RSDBP_IN, p_scale  );
        cmd.addParam("p_point",   RSDBP_IN, p_point  );
        cmd.addParam("p_outquot", RSDBP_IN, p_outquot);

        sql_execute(cmd);
    end;

    /**
     * Инициализация списка подразделений банка для фильтрации счетов баланса по ТС
     */
    private macro initializeDepartmentFilter()
        natBalanceParameters().getDepartmentList().saveToTable();
    end;

    /**
     * Инициализация фильтра счетов ОВП
     */
    private macro initializeOcpAccountFilter()
        natBalanceParameters().getOcpAccountServer().saveToTable();
    end;

    /**
     * Конструктор
     */
    private macro constructor()

        var useAccountDataFlag = false;

        sql_execute("BEGIN rsb_balance.clear(); END;");

        initializeRecalculation();

        initializeDepartmentFilter();

        initializeOcpAccountFilter();

    end;

    /**
     * Расчет баланса по главе
     */
    private macro calculateChapterBalance(chapterNumber : Integer)
        initializePackage(chapterNumber);
        record blinfo ("bl_info.rec"); // структура для передачи параметров отчета в нормализатор

        sql_execute("BEGIN rsb_balance.calculate(); END;");

        if (natBalanceParameters().getMoneyDimension() == RCB_CALCULATE_BALANCE_BU_THOUSEND_SM)
             if (chapterNumber != 5) // Для 5-й главы нормализация не выполняется
                 natBalanceParameters().getBlinfo(blinfo, chapterNumber);
                 ОкруглитьФайлБаланса(blinfo); // стандартная процедура, см. спавку RSL
             end;
        end;

        if (natBalanceParameters().getAccountTypeDefinitionMethod() == DAT_TYPE)
            sql_execute("BEGIN rsb_balance.rollupRests(); END;");
        end;
    end;

    /**
     * Расчет баланса
     */
    macro calculate()

        var chapterNumber = natBalanceParameters().getChapter();

        if (chapterNumber > 0)

            calculateChapterBalance(chapterNumber);

        else

            chapterNumber = 1;
            while (chapterNumber <= 5)
                calculateChapterBalance(chapterNumber);
                chapterNumber = chapterNumber + 1;
            end;

        end;

    end;

    constructor();
end;
