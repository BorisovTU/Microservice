/*
$Name:          NatControlBalanceData.mac
$Module:        Внутренняя отчетность
$Description:   Источники данных для контроля баланса
*/

/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatControlBalanceData.mac
 * Description : Источники данных для контроля баланса
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import cb_sql;
import FIInter;
import NatBalanceParameters;

/**
 * Класс фабрики источников данных для процедуры контроля баланса
 */
class TControlBalanceData()

    /**
     * Получение источника данных для контроля "красного сальдо"
     */
    macro getOverdraftAccountsDataSource() : Object

        var dateOut = natBalanceParameters().getDateOut();

        var queryText;

        queryText =          "SELECT t_account      t_accountNumber,"
                    + "\n" + "       t_kind_account t_accountKind,"
                    + "\n" + "       t_inRest       t_inRest,"
                    + "\n" + "       t_outRest      t_outRest"
                    + "\n" + "  FROM dblovrd_tmp"
                    + "\n" + " WHERE t_kind_account NOT IN ('0', 'АП')"
                    + "\n" + "    OR (   t_kind_account IN ('0', 'АП')"
                    + "\n" + "        AND " + getSqlDate(dateOut) + " < " + getSqlDate({CurDate})
                    + "\n" + "        AND t_outRest != 0"
                    + "\n" + "       )"
                    + "\n" + " ORDER BY t_accountNumber";

        return TRsbDataset(queryText);
    end;

    /**
     * Получение источника данных для контоля сходимости
     */
    macro getConvergenceDataSource() : Object

        var queryText;

        var inRestDifferenceNote = "Расхождение во входящих остатках";

        var outRestDifferenceNote = "Расхождение в исходящих остатках";

        var turnDifferenceNote = "Расхождение в оборотах";

        queryText =          "WITH"
                    + "\n" + "balanceTotals AS"
                    + "\n" + "("
                    + "\n" + "    SELECT SUM(t_dmInActiveR)   AS t_totalInActive,"
                    + "\n" + "           SUM(t_dmInPassiveR)  AS t_totalInPassive,"
                    + "\n" + "           SUM(t_dmDebetR)      AS t_totalDebet,"
                    + "\n" + "           SUM(t_dmCreditR)     AS t_totalCredit,"
                    + "\n" + "           SUM(t_dmOutActiveR)  AS t_totalOutActive,"
                    + "\n" + "           SUM(t_dmOutPassiveR) AS t_totalOutPassive,"
                    + "\n" + "           t_iChapter           AS t_chapterNumber"
                    + "\n" + "      FROM dblrept_tmp"
                    + "\n" + "     GROUP BY t_iChapter"
                    + "\n" + "),"
                    + "\n" + "balanceDifferences AS"
                    + "\n" + "("
                    + "\n" + "    SELECT t_totalInActive                         AS t_firstValue,"
                    + "\n" + "           t_totalInPassive                        AS t_secondValue,"
                    + "\n" + "           ABS(t_totalInActive - t_totalInPassive) AS t_difference,"
                    + "\n" + "           t_chapterNumber                         AS t_chapterNumber,"
                    + "\n" + "       " + getSqlString(inRestDifferenceNote) + "  AS t_note"
                    + "\n" + "      FROM balanceTotals"
                    + "\n" + "     WHERE t_totalInActive != t_totalInPassive"
                    + "\n" + "     UNION ALL"
                    + "\n" + "    SELECT t_totalOutActive                           AS t_firstValue,"
                    + "\n" + "           t_totalOutPassive                          AS t_secondValue,"
                    + "\n" + "           ABS(t_totalOutActive - t_totalOutPassive)  AS t_difference,"
                    + "\n" + "           t_chapterNumber                            AS t_chapterNumber,"
                    + "\n" + "       " + getSqlString(outRestDifferenceNote) + "    AS t_note"
                    + "\n" + "      FROM balanceTotals"
                    + "\n" + "     WHERE t_totalOutActive != t_totalOutPassive"
                    + "\n" + "     UNION ALL"
                    + "\n" + "    SELECT t_totalDebet                         AS t_firstValue,"
                    + "\n" + "           t_totalCredit                        AS t_secondValue,"
                    + "\n" + "           ABS(t_totalDebet - t_totalCredit)    AS t_difference,"
                    + "\n" + "           t_chapterNumber                      AS t_chapterNumber,"
                    + "\n" + "       " + getSqlString(turnDifferenceNote) + " AS t_note"
                    + "\n" + "      FROM balanceTotals"
                    + "\n" + "     WHERE t_totalDebet != t_totalCredit"
                    + "\n" + ")"
                    + "\n" + "SELECT balanceDifferences.t_chapterNumber  AS t_chapterNumber,"
                    + "\n" + "       balanceDifferences.t_firstValue     AS t_firstValue,"
                    + "\n" + "       balanceDifferences.t_secondValue    AS t_secondValue,"
                    + "\n" + "       balanceDifferences.t_difference     AS t_difference,"
                    + "\n" + "       balanceDifferences.t_note           AS t_note"
                    + "\n" + " FROM balanceDifferences balanceDifferences"
                    + "\n" + "ORDER BY t_chapterNumber";

        return TRsbDataset(queryText);

    end;

    /**
     * Формирование текста SQL-запроса источников данных для контроля глав В и Г
     *
     * @param     controlBalance
     */
    private macro makeOffBalanceRestsConformityQueryText(controlBalance : String, chapter : String) : String

        var queryText;

        var totalsField;
        var controlValueField;
        var totalsOutField;
        var controlOutValueField;
        var totalsInField;
        var controlInValueField;
        var totalsBalanceMask;
        var controlBalanceMask;

        var inRestDifferenceNote = "Расхождение во входящих остатках";
        var outRestDifferenceNote = "Расхождение в исходящих остатках";


        if (controlBalance == "99999")
            totalsOutField       = "t_dmOutActiveR";
            controlOutValueField = "t_dmOutPassiveR";
            totalsInField        = "t_dmInActiveR";
            controlInValueField  = "t_dmInPassiveR";
            totalsBalanceMask    = "99998%";
            controlBalanceMask   = "99999%";

        elif (controlBalance == "99998")
            totalsOutField       = "t_dmOutPassiveR";
            controlOutValueField = "t_dmOutActiveR";
            totalsInField        = "t_dmInPassiveR";
            controlInValueField  = "t_dmInActiveR";
            totalsBalanceMask    = "99999%";
            controlBalanceMask   = "99998%";

        elif (controlBalance == "99997")
            totalsOutField       = "t_dmOutActiveR";
            controlOutValueField = "t_dmOutPassiveR";
            totalsInField        = "t_dmInActiveR";
            controlInValueField  = "t_dmInPassiveR";
            totalsBalanceMask    = "99996%";
            controlBalanceMask   = "99997%";

        elif (controlBalance == "99996")
            totalsOutField       = "t_dmOutPassiveR";
            controlOutValueField = "t_dmOutActiveR";
            totalsInField        = "t_dmInPassiveR";
            controlInValueField  = "t_dmInActiveR";
            totalsBalanceMask    = "99997%";
            controlBalanceMask   = "99996%";
        end;

        queryText =          "WITH"
                    + "\n" + "balanceOutTotals AS"
                    + "\n" + "("
                    + "\n" + "    SELECT SUM(" + totalsOutField + ") AS balanceOutTotals"
                    + "\n" + "      FROM dblrept_tmp"
                    + "\n" + "     WHERE t_iChapter = " + chapter
                    + "\n" + "       AND t_balance NOT LIKE " + getSqlString(totalsBalanceMask)
                    + "\n" + "),"
                    + "\n" + "controlOutRests AS"
                    + "\n" + "("
                    + "\n" + "    SELECT SUM(" + controlOutValueField + ") AS controlOutRests"
                    + "\n" + "      FROM dblrept_tmp"
                    + "\n" + "     WHERE t_iChapter = " + chapter
                    + "\n" + "       AND t_balance LIKE " + getSqlString(controlBalanceMask)
                    + "\n" + "),"
                    + "\n" + "balanceInTotals AS"
                    + "\n" + "("
                    + "\n" + "    SELECT SUM(" + totalsInField + ") AS balanceInTotals"
                    + "\n" + "      FROM dblrept_tmp"
                    + "\n" + "     WHERE t_iChapter = " + chapter
                    + "\n" + "       AND t_balance NOT LIKE " + getSqlString(totalsBalanceMask)
                    + "\n" + "),"
                    + "\n" + "controlInRests AS"
                    + "\n" + "("
                    + "\n" + "    SELECT SUM(" + controlInValueField + ") AS controlInRests"
                    + "\n" + "      FROM dblrept_tmp"
                    + "\n" + "     WHERE t_iChapter = " + chapter
                    + "\n" + "       AND t_balance LIKE " + getSqlString(controlBalanceMask)
                    + "\n" + ")"
                    + "\n" + "    SELECT balanceInTotals                             AS t_totals,"
                    + "\n" + "           controlInRests                              AS t_controlValue,"
                    + "\n" + "           ABS(balanceInTotals - controlInRests)       AS t_difference,"
                    + "\n" + "           " + getSqlString(inRestDifferenceNote) + "  AS t_note"
                    + "\n" + "      FROM balanceInTotals, controlInRests"
                    + "\n" + "     WHERE balanceInTotals != controlInRests"
                    + "\n" + "     UNION ALL"
                    + "\n" + "    SELECT balanceOutTotals                            AS t_totals,"
                    + "\n" + "           controlOutRests                             AS t_controlValue,"
                    + "\n" + "           ABS(balanceOutTotals - controlOutRests)     AS t_difference,"
                    + "\n" + "           " + getSqlString(outRestDifferenceNote) + " AS t_note"
                    + "\n" + "      FROM balanceOutTotals, controlOutRests"
                    + "\n" + "     WHERE balanceOutTotals != controlOutRests";

        return queryText;
    end;

    /**
     * Получения источника данных для контроля соответствия активных остатков в главе B
     */
    macro getOffBalanceActiveRestsConformityChapter3DataSource() : Object
        return TRsbDataset(makeOffBalanceRestsConformityQueryText("99999", "3"));
    end;

    /**
     * Получения источника данных для контроля соответствия пассивных остатков в главе B
     */
    macro getOffBalancePassiveRestsConformityChapter3DataSource() : Object
        return TRsbDataset(makeOffBalanceRestsConformityQueryText("99998", "3"));
    end;

    /**
     * Получения источника данных для контроля соответствия активных остатков в главе Г
     */
    macro getOffBalanceActiveRestsConformityChapter4DataSource() : Object
        return TRsbDataset(makeOffBalanceRestsConformityQueryText("99997", "4"));
    end;

    /**
     * Получения источника данных для контроля соответствия пассивных остатков в главе Г
     */
    macro getOffBalancePassiveRestsConformityChapter4DataSource() : Object
        return TRsbDataset(makeOffBalanceRestsConformityQueryText("99996", "4"));
    end;

    /**
     * Конструктор
     */
    macro constructor()
        // do nothing
    end;

    constructor();
end;
