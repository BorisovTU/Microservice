/*
$Name:          NatBalancePrint.mac
$Module:        Внутренняя отчетность
$Description:   Класс управления печатью отчета "Баланс"
*/

/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : NatBalancePrint.mac
 * Description : Класс управления печатью отчета "Баланс"
 * Comment     :
 * @since      : 28.05.2010
 * @author     : Shestakov Dmitry
 * @version    : 6.00.020.30
 */

import natBalanceView;
import natBalanceData;
import natBalanceTemplateBuilder;
import natBalanceParameters;
import ofstream;

private const ACTIVE  = "АКТИВ",
              PASSIVE = "ПАССИВ";

/**
 * Печать отчета "Баланс".
 */
class TNatBalancePrint(printOfstream : TOfstream)
    var m_printOfstream;

    var m_template = TArray();

    var m_views = TArray();
    var m_viewPlainText = TArray();
    var m_viewExcelText = TArray();

    var m_dataSet = TArray();

    var m_nullData;

    private var m_nullReport = true;

    var m_kindAccount = TArray();

    /**
     * Печать таблицы.
     */
    private macro tablePrint(viewsArray : TArray, dataset, m_kindAccount)
        private const NON_PART_NUMBER = 999999;
        var chapterItogRecord;
        var partItogRecord;
        var balanceRecord;
        var view;
        var i;

        var haveChapterItogRecord = false;
        var havePartItogRecord    = false;

        var isEof = not dataset.moveNext();

        var progress = 0;

        if (isEof)
            i = 0;
            while (i < viewsArray.size)
                view = viewsArray[i];
                view.printHead();
                view.printNullData();
                view.printSignature();
                i = i + 1;
            end;
        elif (dataset.isTotalItog)
            isEof      = not dataset.moveNext(dataset);
            m_nullData = false;
        end;

        InitProgress(-1, "", "Подготовка печатной формы отчета...");
        while ((not isEof) and dataset.isChapterItog)

            chapterItogRecord = dataset.getRecord();
            isEof = not dataset.moveNext(dataset);
            if (not isEof)
                i = 0;
                while (i < viewsArray.size)
                    view = viewsArray[i];
                    view.beginChapter(chapterItogRecord, m_kindAccount);
                    i = i + 1;
                end;
                haveChapterItogRecord = true;
            end;

            while ((not isEof) and (not dataset.isChapterItog))
                if (dataset.isPartItog)
                    partItogRecord = dataset.getRecord();
                    isEof = not dataset.moveNext(dataset);
                    if ((not isEof) and (partItogRecord.part != NON_PART_NUMBER))
                        i = 0;
                        while (i < viewsArray.size)
                            view = viewsArray[i];
                            view.beginPart(partItogRecord, m_kindAccount);
                            i = i + 1;
                        end;
                        havePartItogRecord = true;
                    end;
                end;

                while ((not isEof) and (not dataset.isPartItog) and (not dataset.isChapterItog))
                    balanceRecord = dataset.getRecord();

                    i = 0;
                    while (i < viewsArray.size)
                        view = viewsArray[i];
                        view.printBalance(balanceRecord, m_kindAccount);
                        i = i + 1;
                    end;
                    isEof = not dataset.moveNext();
                    progress = progress + 1;
                    UseProgress(progress);
                end;

                if (havePartItogRecord)
                    i = 0;
                    while (i < viewsArray.size)
                        view = viewsArray[i];
                        view.endPart(partItogRecord, m_kindAccount);
                        i = i + 1;
                    end;
                    havePartItogRecord = false;
                end;
            end;

            if (haveChapterItogRecord)
                i = 0;
                while (i < viewsArray.size)
                    view = viewsArray[i];
                    view.endChapter(chapterItogRecord, m_kindAccount);
                    i = i + 1;
                end;
                haveChapterItogRecord = false;
            end;
        end;

        RemProgress();
    end;

    /**
     * Печать отчета.
     */
    macro execute()
        var oldOutput;
        var chapters;
        var i = 0;

        if (natBalanceParameters().getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            if (natBalanceParameters().getIsNeedPlainText())
                m_printOfstream.setOutputFile();
            end;

            chapters = TNatBalanceData().getChaptersSet();
                while (chapters.next())
                var p  = "";
                var th = "";

                if ((natBalanceParameters().getMoneyDimension() == RCB_CALCULATE_BALANCE_BU_THOUSEND_SM) and (chapters.ch != 5))
                    p  = "l";
                    th = "th";
                else
                    p  = "d";
                end;

                m_nullReport = false;
                if (natBalanceParameters().getAccountTypeDefinitionMethod() == DAT_TYPE)
                    m_dataSet[0] = TNatBalanceData().getReportDataSet(
                                 "     blblnc.t_kind_account <> 'П'"
                        + "\n" + " AND blrept.t_ichapter = " + chapters.ch + " "
                        );
                    m_dataSet[1] = TNatBalanceData().getReportDataSet(
                                 "     blblnc.t_kind_account = 'П'"
                        + "\n" + " AND blrept.t_ichapter = " + chapters.ch + " "
                        );
                else
                    m_dataSet[0] = TNatBalanceData().getReportDataSet(
                                 "     (   blrept.t_"+p+"moutactive"+th+"r > 0"
                        + "\n" + "      OR (    blblnc.t_kind_account <> 'П'"
                        + "\n" + "          AND blrept.t_"+p+"moutactive"+th+"r = 0"
                        + "\n" + "          AND blrept.t_"+p+"moutpassive"+th+"r = 0"
                        + "\n" + "         )"
                        + "\n" + "     )"
                        + "\n" + " AND blrept.t_ichapter = " + chapters.ch + " "
                        );
                    m_dataSet[1] = TNatBalanceData().getReportDataSet(
                                 "     (   blrept.t_"+p+"moutpassive"+th+"r > 0"
                        + "\n" + "      OR (    blblnc.t_kind_account = 'П'"
                        + "\n" + "          AND blrept.t_"+p+"moutactive"+th+"r = 0"
                        + "\n" + "          AND blrept.t_"+p+"moutpassive"+th+"r = 0"
                        + "\n" + "         )"
                        + "\n" + "     )"
                        + "\n" + " AND blrept.t_ichapter = " + chapters.ch + " "
                        );
                end;

                for (i, 0, 1, 1)
                    m_views.size = 0;
                    if (natBalanceParameters().getIsNeedPlainText())
                        m_viewPlainText[i].setReportWidth(null, m_kindAccount[i]);
                        m_views[m_views.size] = m_viewPlainText[i];
                    end;

                    if (natBalanceParameters().getIsNeedExcelText())
                        m_views[m_views.size] = m_viewExcelText[i];
                    end;

                    tablePrint(m_views, m_dataset[i], m_kindAccount[i]);
                end;
            end;

            if (m_nullReport)
                if (natBalanceParameters().getIsNeedPlainText())
                    m_viewPlainText[0].setReportWidth(null, m_kindAccount[0]);
                    m_viewPlainText[0].printHead();
                    m_viewPlainText[0].printNullData();
                    m_viewPlainText[0].printSignature();
                end;
            end;

            if (natBalanceParameters().getIsNeedPlainText())
                m_printOfstream.resetOutputFile();
            end;
        else
            m_dataSet[0] = TNatBalanceData().getReportDataSet();

            if (natBalanceParameters().getIsNeedPlainText())
                m_printOfstream.setOutputFile();
                m_viewPlainText[0].setReportWidth();
                m_views[m_views.size] = m_viewPlainText[0];
            end;

            if (natBalanceParameters().getIsNeedExcelText())
                m_views[m_views.size] = m_viewExcelText[0];
            end;

            tablePrint(m_views, m_dataset[0]);

            if (natBalanceParameters().getIsNeedPlainText())
                m_printOfstream.resetOutputFile();
            end;
        end;
    end;

    private macro constructor(printOfstream : TOfstream)
        m_kindAccount[m_kindAccount.size] = ACTIVE;
        m_kindAccount[m_kindAccount.size] = PASSIVE;
        m_printOfstream = printOfstream;
        m_nullData = true;

        if (natBalanceParameters().getBalanceForm() == REP_BALANCE_FORM_REST_NAME)
            if (natBalanceParameters().getIsNeedPlainText())
                m_template = TActiveNameRestBalanceTemplate();
                setOutput(m_template.createResultFile(m_kindAccount[0]),  false);
                m_viewPlainText[0]  = TNatBalanceView(m_template.createResultFile(m_kindAccount[0]), true);
                m_template = TPassiveNameRestBalanceTemplate();
                m_viewPlainText[1]  = TNatBalanceView(m_template.createResultFile(m_kindAccount[1]), false);
            end;

            if (natBalanceParameters().getIsNeedExcelText())
                m_viewExcelText[0]  = TNatBalanceExcelView(true);
                m_viewExcelText[1]  = TNatBalanceExcelView(false);
            end;
        elif(natBalanceParameters().getBalanceForm() == REP_BALANCE_FORM_TURN_NOIN)
            if (natBalanceParameters().getIsNeedPlainText())
                m_template = TNotInRestTurnBalanceTemplate();
                setOutput(m_template.createResultFile(),  false);
                m_viewPlainText[0]  = TNatBalanceView(m_template.createResultFile());
            end;

            if (natBalanceParameters().getIsNeedExcelText())
                m_viewExcelText[0]  = TNatBalanceExcelView();
            end;
        elif(natBalanceParameters().getBalanceForm() == REP_BALANCE_FORM_TURN)
            if (natBalanceParameters().getIsNeedPlainText())
                m_template = TTurnBalanceTemplate();
                setOutput(m_template.createResultFile(),  false);
                m_viewPlainText[0]  = TNatBalanceView(m_template.createResultFile());
            end;

            if (natBalanceParameters().getIsNeedExcelText())
                m_viewExcelText[0]  = TNatBalanceExcelView();
            end;
        elif(natBalanceParameters().getBalanceForm() == REP_BALANCE_FORM_REST)
            if (natBalanceParameters().getIsNeedPlainText())
                m_template = TRestBalanceTemplate();
                setOutput(m_template.createResultFile(),  false);
                m_viewPlainText[0]  = TNatBalanceView(m_template.createResultFile());
            end;

            if (natBalanceParameters().getIsNeedExcelText())
                m_viewExcelText[0]  = TNatBalanceExcelView();
            end;
        end;
    end;

    constructor(printOfstream);
end;