/*
$Name:          main_balance.mac
$Module:        Внутренняя отчетность
$Description:   Выпуск валютного баланса
*/

/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 *  File Name   : main_balance.mac                        February 12, 2008
 *  Programmer  : Ivkina, ABP
 *  Description : Выпуск валютного баланса
 *  Comment     :
 *  Modify      :
 */

import testLib;

import BalanceCalculator;
import ControlBalanceController;
import BalancePrint;

private var printOfstream;
private var controlOfstream;

var reportBodyFileName;     //имя файла отчета
var controlBodyFileName;    //имя файла протокола контроля

/**
 *  Отчет.
 */
macro produceReport(departmentId, organizationStructure, issueMode, planNumber,
                    chapterNumber, currencyId, periodKind, dateIn, dateOut,
                    reportForm, dimension, needPartTotals, needNationalCurrency,
                    needSpodTurnTotals, needApostrophes, needZeroAccounts,
                    needExcludeOcpAccounts, needEverydayReport,
                    needPlainText, needExcelText)

    var sqlProfiler = TSqlProfiler(getTxtFileName("blnc_cur_sqlprf"));
    sqlProfiler.clear();
    sqlProfiler.on();

    /*инициализация параметров для балансового отчета*/
    balanceParameters(departmentId, organizationStructure, issueMode, planNumber,
                      chapterNumber, currencyId, periodKind, dateIn, dateOut,
                      reportForm, dimension, needPartTotals, needNationalCurrency,
                      needSpodTurnTotals, needApostrophes, needZeroAccounts,
                      needExcludeOcpAccounts, needEverydayReport,
                      needPlainText, needExcelText);

    /* Контроль открытых опердней */
    if (isEqClass("RepDepartmentList", balanceParameters().getDepartmentList()))
        var opd = RepOperdaysOpened(balanceParameters().getDepartmentList(), balanceParameters().getDateIn(), balanceParameters().getDateOut());
        if (not opd.shouldContinue())
            exit(1);
        end;
    end;

    printOfstream = TOfstream("balance");
    controlOfstream = TOfstream("ctrl_balance");

    var balancePrint;
    var controlBalanceController = TControlBalanceController(controlOfstream);

    var m_dateOut = dateOut;

    begAction(1000, "Выпускается отчет. Ждите...");

    if (balanceParameters().getIsPrintEveryDay() == true)

        balanceParameters().setDateOut(balanceParameters().getDateIn());

        while (balanceParameters().getDateOut() <= m_dateOut)
            TBalanceCalculator().calculate();
            balancePrint = TBalancePrint(printOfstream);
            balancePrint.execute();
            controlBalanceController.control();

            balanceParameters().setDateIn(balanceParameters().getDateIn() + 1);
            balanceParameters().setDateOut(balanceParameters().getDateOut() + 1);
        end;

    else

        TBalanceCalculator().calculate();
        balancePrint = TBalancePrint(printOfstream);
        balancePrint.execute();
        controlBalanceController.control();

    end;

    endAction();

    reportBodyFileName = printOfstream.getFileName();
    controlBodyFileName = controlOfstream.getFileName();

    return true;
end;

macro showReport()
//    controlOfstream.show();
//    printOfstream.show();
    //протокол контроля
    controlOfstream.show();

    //если задан plain text, то выводим отчет в текстовом виде
    if (balanceParameters().getIsNeedPlainText())
        printOfstream.show();
    end;

    //если задан Excel, то выводим документ Excel
    if (balanceParameters().getIsNeedExcelText())
    end;
end;

macro saveReport(reportName, controlName)

    controlOfstream.save(controlName);
    printOfstream.save(reportName);

end;

macro batchProduce(reportName, controlName,
                   departmentId, organizationStructure, issueMode, planNumber, chapterNumber, currencyId, periodKind, dateIn, dateOut,
                   reportForm, dimension, needPartTotals, needNationalCurrency, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                   needExcludeOcpAccounts, needEverydayReport,
                   needPlainText, needExcelText)

//    var dialogFlag = setDialogFlag(0);

    produceReport(departmentId, organizationStructure, issueMode, planNumber, chapterNumber, currencyId, periodKind, dateIn, dateOut,
                  reportForm, dimension, needPartTotals, needNationalCurrency, needSpodTurnTotals, needApostrophes, needZeroAccounts,
                  needExcludeOcpAccounts, needEverydayReport,
                  needPlainText, needExcelText);

    saveReport(reportName, controlName);

//    setDialogFlag(dialogFlag);

end;
