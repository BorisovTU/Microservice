/*
$Name:              TbsControl.mac
$Module:            Внутренняя отчетность
$Description:       Контроль выпуска оборотно-сальдовой ведомости банка
*/
/**
 * RS-Bank 6.0                                           R-Style Software Lab
 *
 * File Name   : TbsControl.mac
 * Description : Контроль выпуска оборотно-сальдовой ведомости банка
 * Comment     :
 * @since   01.08.2009
 * @author  Shestakov Dmitry
 * @version 6.00.020.29
 */

import TbsParameters;
import ControlBalanceController;

/**
 * Контроллер контроля баланса
 */
class (TControlBalanceController) TTbsController(ofstream : TOfstream)

    /**
     * Конструктор
     */
    private macro constructorTTbsController(ofstream : TOfstream)
        initTControlBalanceController(ofstream);
    end;

    /**
     * Печать "шапки" протокола
     */
    private macro printHeader()

        println("ОБОРОТНО-САЛЬДОВАЯ ВЕДОМОСТЬ КРЕДИТНОЙ ОРГАНИЗАЦИИ");
        println("ПРОТОКОЛ КОНТРОЛЯ");
        println();
        println("   Период отчета с ", tbsParameters().getDateIn(), " по ", tbsParameters().getDateOut());
        println("   Дата и время выпуска отчета ", Date(), "  ", Time());
        println("   Исполнитель ", {oper}, " ", {Name_Oper});

    end;

    /**
     * Выполнение контроля
     */
    macro control()

        m_ofstream.setOutputFile();

        printHeader();

        // Контроль на "красное сальдо"
        var DISABLE_OVERDRAFT = false;
        if ((GetRegistryValue("REPORT\\DISABLE OVERDRAFT 3", V_BOOL, DISABLE_OVERDRAFT) == null) or not DISABLE_OVERDRAFT)
            TControlOverdraftAccounts(m_controlBalanceData.getOverdraftAccountsDataSource()).execute();
        end;

        // Контроль сходимости
        if ((not tbsParameters().getIsExcludeZeroTurns()) and
            (not tbsParameters().getIsExcludeZeroRests()) and
            (tbsParameters().getOper() == 0) and
            (tbsParameters().getClient() == -1) and
            (strlen(tbsParameters().getBalanceMask()) == 0) and
            (strlen(tbsParameters().getAccountMask()) == 0) and
            (strlen(tbsParameters().getAccType())     == 0) and
            (strlen(tbsParameters().getAccUserType()) == 0) and
            (tbsParameters().getIssueMode() != 1)           and
            (((tbsParameters().getNatcurRepAccounts() == REP_NATCUR_REP_ACCOUNTS_ALL) and
              (tbsParameters().getFiId() == NATCUR))
            or ((not tbsParameters().getIsExcludeOcp()) and
                    (tbsParameters().getFiId() != NATCUR))
            )
            )
            TControlConvergence(m_controlBalanceData.getTbsConvergenceDataSource(ternary(tbsParameters().getFiId() == NATCUR, 0, 1))).execute();
        end;

//        // Контроль остатков главы В
//        if (    (tbsParameters().getNatcurRepAccounts() == REP_NATCUR_REP_ACCOUNTS_ALL)
//            and (tbsParameters().getFiId() == NATCUR)
//            and (   (tbsParameters().getChapter() == 0)
//                 or (tbsParameters().getChapter() == 3)
//                )
//           )
//            TControlOffBalanceRestsConformity(m_controlBalanceData.getOffBalanceActiveRestsConformityDataSource(true),
//                                              m_controlBalanceData.getOffBalancePassiveRestsConformityDataSource(true)).execute();
//        end;

        m_ofstream.resetOutputFile();

    end;

    constructorTTbsController(ofstream);

end;
