/*------------- Планируемые и фактические оплаты процентов --------------*/
/*-------------        по рублевым лицевым счетам          --------------*/
/*-------------      без учета квитованности оплат         --------------*/
/* 19.01.98 LCh Добавлено поле в панели диалога и фильтр по польз.типу счета */
/* 28.09.2001 - Sal. Подкачка справочника по пользовательским типам счетов   */

import BankInter, rslscr;

file   СчетПроцентов  ( pcacc )    key 1;
file   Счета          ( pcacc )    key 3;
file   Оплата         ( pccalc )   key 6;
file   ЛицевыеСчета   ( account )  key 0;
file   Пользователь   ( person )   KEY 0;
file   ТипыСчета      ( typeac )   KEY 0;
file   ПользТипыСчета ( typeac )   KEY 0 WRITE;

record Диалог         ( "pcpaya_r" ) dialog;
record ЛистПоТипуСчета( "pcpay_lt" ) dialog;

var BreakVal = 1;
var НачальнаяДата;
var СуммаПланируемыхОплат;
var СуммаФактическихОплат;
var ОбщаяСуммаПланируемыхОплат = $0;
var ОбщаяСуммаФактическихОплат = $0;
var ЛицевойСчет;
var ТипВладельцаСчетаПроц = 1001;
var ОтменитьОтчет;
var ПользТипСчета;
var {oper};

const РасчетСовершен = 2;
const ВсеОплаты = 0;
const ПланируемаяОплата = 1;
const ФактическаяОплата = 2;
const ДлинаСчета = 25;
const ZeroAc = "";

/* вызов "листалки" счетов */
macro ВыбратьСчет( Account )
  record AC( account );
  if (ListAccount (AC, 1, 0, Account))
     SetParm ( 0 , AC.Account);
     return True;
  end;
  return False;
end;
                     
macro ОбработчикЛиста( dlg, cmd, id, ikey )
  if ( cmd == DLG_KEY )
    if ( ikey == 13 )
     GetDirect(Scrff, Apos(FindRow(dlg,id)));
     return CM_SAVE;
    end;
  end;
  return ScrollMes( dlg, cmd, id, ikey );
end;
                       
/* вызов "листалки" типов счетов */
macro ВыбратьТипСчета( СсылкаНаДиалог )
   Var isFirst = true, nType = 0,
       NameFile = GetWorkFileName("type_ac");

   macro ПоПользТипам()
     if ( isFirst )
        isFirst = false;
        ClearRecord(ТипыСчета);
        ТипыСчета.iNumType = 2;
        return GetGE(ТипыСчета) and ( ТипыСчета.iNumType == 2 );
     else
        return next(ТипыСчета) and ( ТипыСчета.iNumType == 2 );
     end;
   end;

   if ( not Clone( ПользТипыСчета, NameFile) )
     MsgBox("Ошибка создания файла");
     exit(1);
   end;

   while ( ПоПользТипам() ) 
     if ( Index( СсылкаНаДиалог.ПользТипСчета, ТипыСчета.Type_Account) <= 0 ) 
       ПользТипыСчета.iNumType = ТипыСчета.iNumType;
       ПользТипыСчета.Type_Account = ТипыСчета.Type_Account;
       ПользТипыСчета.Name_Type = ТипыСчета.Name_Type;
       ПользТипыСчета.Contens = ТипыСчета.Contens;
       if ( not Insert(ПользТипыСчета) )
         MsgBox("Ошибка вставки записи в файл");
         exit(1);
       end;
     end;
     nType = nType +1;
   end;

   if ( Nrecords(ПользТипыСчета) == 0 )
     if ( nType == 0 )   MsgBox("Нет записей");
     end;
     return false;
   end;

   SetScroll( ПользТипыСчета, "Тип", "Type_Account", 
                              "Имя", "Name_Type"  
            );
   if ( RunDialog( ЛистПоТипуСчета, "ОбработчикЛиста") )
     СсылкаНаДиалог.ПользТипСчета = СсылкаНаДиалог.ПользТипСчета + Scrff.Type_Account;
     Clone( ПользТипыСчета );
     Close(ПользТипыСчета);
     return true;
   end;
              
   return false;
end;

macro ОбработчикДиалога( СсылкаНаДиалог, Сообщение, ТекущееПоле, КодКлавиши )
var error=true;
var error2=False;

   if( Сообщение==DLG_SAVE )

       if( trim(СсылкаНаДиалог.ЛицевойСчет) == ZeroAc) error=false;
       else
             ЛицевыеСчета.Chapter = 1;
             ЛицевыеСчета.Account = СсылкаНаДиалог.ЛицевойСчет;
         if( geteq( ЛицевыеСчета ) and ( ЛицевыеСчета.Oper != {oper} ))  /* EI 17-06-97 */
             Пользователь.Oper = {oper};
             if( GetEq( Пользователь ) and (Пользователь.cTypePerson == "О") )
                  error2=True;
             else error2=False;
             end;
         end;

          Счета.ObjectType=ТипВладельцаСчетаПроц;
          Счета.Account=СсылкаНаДиалог.ЛицевойСчет;
          Счета.FIID=0;
          Счета.CloseDate=date(0,0,0);
          if( getGE( Счета ) )
             if( (Счета.ObjectType==ТипВладельцаСчетаПроц) and
                       (Счета.Account==СсылкаНаДиалог.ЛицевойСчет) )
                error=false;
             end;
          end;
       end;
       if( error )
          msgbox( "Нет счетов процентов по указанному|лицевому счету" );
          return CM_CANCEL;
       end;
       if( error2 )
          msgbox( "Запрещен доступ операционисту | к лицевому счету "+СсылкаНаДиалог.ЛицевойСчет );
          return CM_CANCEL;
       end;
    end;
    if(( Сообщение == DLG_KEY ) and ( КодКлавиши == 317) )
        if   (fldname (СсылкаНаДиалог, ТекущееПоле)== "ЛицевойСчет"  )
          if ( ВыбратьСчет( СсылкаНаДиалог.ЛицевойСчет ) )
            UpdateFields( СсылкаНаДиалог );
          end;
        elif (fldname (СсылкаНаДиалог, ТекущееПоле)== "ПользТипСчета"  )
          while ( ВыбратьТипСчета( СсылкаНаДиалог ) )
          end;
          UpdateFields( СсылкаНаДиалог );
        end;
    end;
end;

macro ВвестиИсходныеДанные()
   Диалог.ТипВладельцаСчетаПроц = "Лицевой счет";
   Диалог.ЛицевойСчет = ZeroAc; /* "0"+MkStr(" ",ДлинаСчета-1); */
   Диалог.ПользТипСчета = "";
   ОтменитьОтчет = not RunDialog( Диалог, "ОбработчикДиалога" );
   if( ОтменитьОтчет )   exit( 1 ); end;

   ЛицевойСчет = Диалог.ЛицевойСчет;
   НачальнаяДата = Диалог.НачальнаяДата;
   if( trim(ЛицевойСчет) == ZeroAc) 
        ПользТипСчета = trim( Диалог.ПользТипСчета );
   else ПользТипСчета = "";
   end;
end;


macro НапечататьЗаголовок()
[   Общий список планируемых и фактических оплат процентов по лицевым счетам      ];
   if ( ПользТипСчета > "" )
[                  с пользовательским типом  ###############                      ]( ПользТипСчета:l);
   end;
[                                                                                 ];
[──────────────────────────┬──────────┬──────────┬─────────────────────────┬───────────────────────────];
[          Счет            │   Дата   │  Номер   │           Сумма         │           Сумма           ];
[                          │  оплаты  │  оплаты  │    планируемой оплаты   │    фактической оплаты     ];
[──────────────────────────┴──────────┴──────────┴─────────────────────────┴───────────────────────────];
end;


macro НапечататьПланируемуюОплату()
[ #                         #                   #                         #                          # ]
( СчетПроцентов.Account:l, Оплата.PayDate:l, Оплата.AutoKey:r,Оплата.Sum:r, $0:r );
end;


macro НапечататьФактическуюОплату()
[ #                         #                   #                         #                          # ]
( СчетПроцентов.Account:l, Оплата.PayDate:l, Оплата.AutoKey:r, $0:r, Оплата.Sum:r );
end;


macro НапечататьОплаты()
   /* Устанавливаем ключи для выбора оплат */
   Оплата.AutoPerc = СчетПроцентов.AutoKey;
   Оплата.CalcFlag = РасчетСовершен;
   Оплата.PayDate  = date(0,0,0);

   СуммаПланируемыхОплат = $0;
   СуммаФактическихОплат = $0;
   BreakVal = 1;

   if( not getGE( Оплата ) )  return; end;
   while( BreakVal )
      /* проверяем на попадание в диапазон */
      if( (Оплата.AutoPerc != СчетПроцентов.AutoKey) or
          (Оплата.CalcFlag != РасчетСовершен) )
             BreakVal = 0;
      else
         if( Оплата.PayDate >= НачальнаяДата )
            if( Оплата.PayType == ПланируемаяОплата )
               НапечататьПланируемуюОплату();
               СуммаПланируемыхОплат = СуммаПланируемыхОплат + Оплата.sum;
            else
               НапечататьФактическуюОплату();
               СуммаФактическихОплат = СуммаФактическихОплат + Оплата.sum;
            end;
         end;
      end;

      if( not next(  Оплата ) )   BreakVal = 0; end;
   end;
end;


macro НапечататьИтог()
[──────────────────────────────────────────────────────────────────────────────────────────────────────];
[ Общая сумма оплат:                                                      #                          # ]
( СуммаПланируемыхОплат:r, СуммаФактическихОплат:r );
[──────────────────────────────────────────────────────────────────────────────────────────────────────];
[ ];
end;


macro НапечататьШапкуСчета()
   [ ];
   [  Оплаты по счету процентов номер #               лицевого счета # ]
      (СчетПроцентов.AutoKey:l, СчетПроцентов.Account:l);
   [ ];
end;

macro FiltrType(Account) /* Фильтр для л/счетов по пользовательскому типу */
   var i = strlen(ПользТипСчета);
   if  ( ( trim(ЛицевойСчет) != ZeroAc) or (i==0) )
        return TRUE;
   end;
   ЛицевыеСчета.Chapter = 1;
   ЛицевыеСчета.Account = Account;
   if( not geteq( ЛицевыеСчета ) ) return FALSE;
   end;
   while( i > 0 )
      if ( index(ЛицевыеСчета.UserTypeAccount,substr(ПользТипСчета,i,1) )> 0 ) 
        return TRUE;
      end;   
      i = i - 1;
   end;
   return FALSE;
end;

ВвестиИсходныеДанные();
if( ОтменитьОтчет == true )   return; end;

НапечататьЗаголовок();

/* Устанавливаем ключи для выбора счета процентов */
СчетПроцентов.ObjectType = ТипВладельцаСчетаПроц;
СчетПроцентов.CloseDate = date(0,0,0);
СчетПроцентов.Account = ЛицевойСчет;
СчетПроцентов.FIID = 0;

BreakVal = 1;
if( not getGE( СчетПроцентов ) )   return; end;
while( BreakVal )
   /* проверяем на попадание в диапазон */
   if( (СчетПроцентов.ObjectType == ТипВладельцаСчетаПроц) and
       (СчетПроцентов.CloseDate == date(0,0,0)) and
       ( (СчетПроцентов.Account == ЛицевойСчет) or
         (trim(ЛицевойСчет) == ZeroAc) ) )
     if( ( (IsAccessToAc( СчетПроцентов.Account, 0, {oper} )) == 0 ) and
            FiltrType(СчетПроцентов.Account) )

      НапечататьШапкуСчета();
      НапечататьОплаты();
      НапечататьИтог();
      ОбщаяСуммаПланируемыхОплат = ОбщаяСуммаПланируемыхОплат + СуммаПланируемыхОплат;
      ОбщаяСуммаФактическихОплат = ОбщаяСуммаФактическихОплат + СуммаФактическихОплат;
     end;
      BreakVal = 1;
   else
      BreakVal = 0;
   end;

   if( not next( СчетПроцентов ) )  BreakVal = 0; end;
end;



/* Общий итог по всем счетам */
[ ];
[ ];
[──────────────────────────────────────────────────────────────────────────────────────────────────────];
[ Общая сумма оплат по всем счетам:                                       #                          # ]
( ОбщаяСуммаПланируемыхОплат:r, ОбщаяСуммаФактическихОплат:r );
[──────────────────────────────────────────────────────────────────────────────────────────────────────];
