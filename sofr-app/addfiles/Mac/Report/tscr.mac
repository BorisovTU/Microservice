/*
╔════════════════════════════════════════════════════════════════════════════╗
║  Отдел Поддержки           R-Style SoftWare Lab               Вачьян Юрий  ║
╠════════════════════════════════════════════════════════════════════════════╣                             
║                           Скроллинг для файлов-объектов                    ║
║ с возможностью поиска и отметки записей (Selected - файл позиций записей)  ║
╚════════════════════════════════════════════════════════════════════════════╝
┌────────────────────────────────────────────────────────────────────────────┐
│03.08.99 - Создание                                                         │
└────────────────────────────────────────────────────────────────────────────┘
*/

Import arraylib, strings;

/*Константы!!!*/
const F1 = 315, F2 = 316, F3 = 317, F4 = 318, F5 = 319, F6 = 320, F7 = 321, F8 = 322, F9 = 323, F10 = 324, F11 = 325, F12 = 326,
      UP = 328, DOWN = 336,
      PGUP = 329, PGDOWN = 337,
      HOMEKEY = 327, ENDKEY = 335,
      PRESS = 513,
      TAB = 9, STAB = 271,
      LEFT =  331, RIGHT =  333,
      CTRLLEFT  =  371,	CTRLRIGHT =  372,
      CTRLEND   =  373, CTRLHOME  =  375,
      CTRLUP = 397,     CTRLDOWN  =  401,
      CtrlPgDown = 374, CtrlPgUp = 388,

      ENTER = 13,
      ESC = 27,
      BS = CodeFor(" "),
      PLUS = 43, MINUS = 45;

var Скроллинг;
var Selected;

Class TScr(ff, dl)
 var        f               = ff;
 var        dlg             = dl;
 var        Fields          = T2Array;
 var        Pos             = TArray;

 var        iDlgCmd         = 0;
 var        iDlgID          = 0;
 var        iDlgKey         = 0;

 var        LastId          = 0;
 var        Col             = 0;
 var        Row             = 0;
 var        tmp             = "";
 var        Modify          = True;
 var        VPressKey       = False;
 var        Moved           = False;
 var        First           = True;
 var        FindVal;
 var        NameSelectField = "";

 var        i = 3;
 var        j = 0;
 var        S, N;
        While (GetParm (i,S))
                Fields.Item(0,j,S);
                GetParm(i+1,S);
                If(ValType(S) == V_Undef)
                        N = f.FldIndex(Fields.Item(0,j))
                Else
                        N = f.FldIndex(S)
                End;
                If(N == -1)
                        MsgBox("Класс TScr|Неверное имя поля \"",S,"\"|файла ",f.FileName);
                        Exit(1)
                End;
                Fields.Item(1,j,N);
                i = i + 2;
                j = j + 1;
        end;
 
        i = 0;
        While(FldIndex(dlg,String(Fields.Item(0,0),i)) != -1)
                Pos.Value(i) = 0;
                i = i + 1;
        End;

Macro SetCurDlgParms( Cmd, Id, Key )
        iDlgCmd         = Cmd;
        iDlgID          = Id;
        iDlgKey         = Key;
End;

Macro IsSelect(Row)
        Selected.Rec.Pos = Pos.Value(Row);
        Return Selected.GetEQ
End;

Macro Select
        If(NameSelectField != "")
                If(IsSelect(Row))
                        dlg(FldIndex(dlg,String(NameSelectField,Row))) = "";
                        Selected.Delete
                Else
                        dlg(FldIndex(dlg,String(NameSelectField,Row))) = "X";
                        Selected.Insert
                End;
                UpdateFields(dlg)
        End;
End;

Macro SetFields(Row)
   var     k = 0;
        While (k < Fields.Value(0).Size)
                dlg(FldIndex(dlg,String(Fields.Item(0,k),Row))) = f.Rec()(Fields.Item(1,k));
                k = k + 1
        End;
        If(NameSelectField != "")
                If(IsSelect(Row))
                        dlg(FldIndex(dlg,String(NameSelectField,Row))) = "X"
                ELse
                        dlg(FldIndex(dlg,String(NameSelectField,Row))) = ""
                End;
        End;
End;

Macro ClearFields(Row)
   var     k = 0;
        While (k < Fields.Value(0).Size)
                dlg(FldIndex(dlg,String(Fields.Item(0,k),Row))) = "";
                k = k + 1
        End;
        If(NameSelectField != "")
                dlg(FldIndex(dlg,String(NameSelectField,Row))) = ""
        End;
End;

Macro ScrNext
        Return f.Next
End;

Macro ScrPrev
        Return f.Prev
End;

Macro ScrReWind
        First   = True;
        Return f.ReWind
End;

Macro GetDirectPos( pos_val )
    if ( not f.GetDirect(pos_val) )
      msgbox("Ошибка взятия записи файла!");
      return FALSE;
    end;
   return TRUE;
End;


Macro FillDown()
    var    k = 0;
        /* Заполнение с начала панели пока Next */
        While((k < Pos.Size) and ScrNext)
                Pos.Value(k) = f.GetPos;
                SetFields(k);
                k = k + 1;
        End; 
        If(k != 0)
                LastId = k - 1;    
        End;
        /* Если панель заполнена не до конца */
        If((k < Pos.Size) and (k != 0))
                /* Ищем запись с которой начать заполнение */
                if ( not GetDirectPos(Pos.Value(0)) )
                  return;
                end;
                k = Pos.Size - k;
                While((k > 0) and ScrPrev)
                        k = k - 1
                End;

                If(k == 0)  /* такая запись нашлась -> заполняем снизу */
                        ScrRewind;
                        This.FillUp(iDlgId)
                Else
                        k = LastId + 1;
                        GetDirectPos(Pos.Value(LastId)); 
                        While(k < Pos.Size)
                                ClearFields(k);
                                k = k + 1;
                        End;
                End;

                This.Position();
                Moved = True;
                SetFocus(dlg, FldIndex(Dlg, String(Fields.Item(0, Col), LastId)));
        End;
        UpdateFields(dlg);
End;

Macro FillUp()
    var    k = Pos.Size - 1;
        LastId = k;

        While((k >= 0) and ScrPrev)
                Pos.Value(k) = f.GetPos;
                SetFields(k);
                k = k - 1;
        End; 

        If(k >= 0)
                ScrRewind;
                This.FillDown(iDlgId);
                This.Position(iDlgId);
                Moved = True;
                SetFocus(dlg, FldIndex(Dlg, String( Fields.Item(0, Col), 0)));
        End;          

        UpdateFields(dlg);

OnError
End;

Macro OneDown
  var k,l;
        If((Pos.Size == 0) or (Pos.Value(Pos.Size-1) == 0))
                Return
        End;

        If(Not (GetDirectPos(Pos.Value(Pos.Size-1)) and ScrNext))
                Return
        End;
        k = 0;
        While(k < Pos.Size-1)
                Pos.Value(k) = Pos.Value(k+1);
                l = 0;
                While(l < Fields.Value(0).Size)
                        dlg(FldIndex(dlg,String(Fields.Item(0,l),k))) = dlg(FldIndex(dlg,String(Fields.Item(0,l),k+1)));
                        l = l + 1 
                End;
                If(NameSelectField != "")
                        dlg(FldIndex(dlg,String(NameSelectField,k))) = dlg(FldIndex(dlg,String(NameSelectField,k+1)))
                End;
                k = k + 1
        End;
        Pos.Value(k) = GetPos(f);
        SetFields(k);
        UpdateFields(dlg);
End;

Macro OneUp
  var k,l;
        If((Pos.Size == 0) or (Pos.Value(0) == 0))
                Return
        End;


        If(Not (GetDirectPos(Pos.Value(0)) and ScrPrev))
                Return
        End;

        k = Pos.Size-1;
        While(k > 0)
                Pos.Value(k) = Pos.Value(k-1);
                l = 0;
                While(l < Fields.Value(0).Size)
                        dlg(FldIndex(dlg,String(Fields.Item(0,l),k))) = dlg(FldIndex(dlg,String(Fields.Item(0,l),k-1)));
                        l = l + 1 
                End;
                If(NameSelectField != "")
                        dlg(FldIndex(dlg,String(NameSelectField,k))) = dlg(FldIndex(dlg,String(NameSelectField,k-1)))
                End;
                k = k - 1
        End;
        Pos.Value(k) = f.GetPos;
        SetFields(k);
        If(LastId < Pos.Size-1)
                LastId = LastId + 1
        End;
        UpdateFields(dlg);
End;


Macro Position()
  var k,l;
        Col = -1;
        Row = -1;

        Macro FindName(name)
           var     len = StrLen(name);
                If(name == SubStr(FldName(dlg, iDlgId),1,len))
                        S = SubStr(FldName(dlg, iDlgId),len+1);
                        If(S != "")
                                len = Int(S);
                                If((Len>0) or (S == "0"))
                                        Return len
                                End;
                        End
                End;
                Return -1
        End;

        k       = 0;
        While((Row == -1) and (k < Fields.Value(0).Size))
                Row     = FindName(Fields.Item(0,k));
                k       = k + 1
        End;
        If(Row != -1)
                Col = k - 1
        End;

End;

Macro CurPos
    return GetDirectPos(Pos.Value(Row)); 
End;

Macro Change(Val)
   If((tmp != Val) and not Moved and (Col != -1))
    Modify = False;
    GetTrue(Modify,"Изменить запись?");
    If(Modify)
     CurPos;
     f.Rec()(Fields.Item(1,Col)) = Val;
     If(f.Update)
      MsgBox("Запись изменена!")
     End;
    Else
     dlg(FldIndex(dlg,String(Fields.Item(0,Col),Row))) = tmp;
     UpdateFields(dlg);
    End;
   End;
End;

Macro _Home
  var k;
        Moved = True;
        k = Col - 1;
        While(k >= 0)
                SetFocus(dlg, FldIndex(Dlg, String(Fields.Item(0,k),Row)));
                k = k - 1
        End;
End;

Macro _End
  var k;
        Moved = True;
        k = Col + 1;
        While(k < Fields.Value(0).Size)
                SetFocus(dlg, FldIndex(Dlg, String(Fields.Item(0,k),Row)));
                k = k + 1
        End;
End;

Macro UserFind(Val)
        Return False
End;

Macro SetStatus()
    If( NameSelectField == "")
     Message("  ~Esc~ Выход  ~Enter~ Выбор  ~F4~ Поиск по маске  ~F1~ Помощь");
    Else
     Message("  ~Esc~ Выход  ~Enter~ Выбор  ~Пробел~ Отметить  ~F4~ Поиск  ~F1~ Помощь");
    End;
End;

Macro Find
  var Field = Fields.Item(1,Col),
      CurZnachField,
      FindPos ;

  Macro _Find
        While(ScrNext and (TestEvent != ESC))
                Message("  ~Esc~ Остановить  ",f.Rec()(Field));
                CurZnachField = String(f.Rec()(Field));
/*                If(Filter(CurZnachField, FindVal) or UserFind(CurZnachField))*/
                If((CompareStrWithMasks(FindVal,CurZnachField)==0) or UserFind(CurZnachField))
                        FindPos = f.GetPos;
                        ScrPrev;
                        FillDown();
                        FindInArray( Pos, FindPos, FindPos);
                        Moved = True;
                        SetFocus(dlg, FldIndex(dlg, String(FIelds.Item(0,Col),FindPos)));
                        Return True
                End;
        End;
        Return False
   End;

        CurPos;    /* поиск с текущей записи */
        if ( _Find )
             SetStatus();
             Return True
        End;

        ScrRewind; /* поиск с начала файла */
        if ( _Find )
             SetStatus();
             Return True
        End;

        SetStatus();
        Return False
End;

        Macro SetSelectField(NameField)
                NameSelectField = NameField;
                Selected = TBFile("Selected","Wc+",0, String("Slct",UserNumber,".dbt"),"tscr.def");
        End;

        Macro SetTmpValue(Value)
                If(Not VPressKey)
                        tmp = Value
                End;
                VPressKey = False
        End;

        Macro PressKey()
                VPressKey = True
        End;

Macro Обработчик ( Cmd, Id, Key )
        If (Cmd == Dlg_Key)
                If(Pos.Size == 0)
                        Return CM_Ignore
                End;

                If (Key == PGUP) /*Page Up*/
                        If(Pos.Value(0) == 0)
                                Return CM_Ignore
                        End;
                        if ( not GetDirectPos(Pos.Value(0)) )
                                Return CM_Ignore;
                        end;
                        FillUp;

                ElIf (Key == PGDOWN) /*Page Down*/
                        If(Pos.Value(Pos.Size-1) == 0)
                                Return CM_Ignore
                        End;
                        if ( not GetDirectPos(Pos.Value(Pos.Size-1)) )
                                Return CM_Ignore;
                        end;
                        FillDown;

                ElIf (Key == CtrlPgUp) /*Ctrl Page Up*/
                        ScrRewind;
                        FillDown;
                        Moved = True;
                        SetFocus( dlg, FldIndex( Dlg, String( Fields.Item( 0, Col ), 0)));

                ElIf (Key == CtrlPgDown) /*Ctrl Page Down*/
                        ScrRewind;
                        FillUp;
                        Moved = True;
                        SetFocus( dlg, FldIndex( Dlg, String( Fields.Item( 0, Col ), LastId )));

                ElIf ((Key == LEFT) or (Key == STAB) )
                   if ( (Row == 0) and (Col == 0) )
                     CurPos;
                     if ( not ScrPrev )
                       Moved = True;
                       SetFocus( dlg, FldIndex( Dlg, String( Fields.Item( 0, Fields.Value(0).Size-1 ), LastId )));
                       Return CM_Ignore;
                     end;
                   end;

                ElIf ((Key == RIGHT) or (Key == TAB) )
                   if ( (Row >= LastId) and (Col >= Fields.Value(0).Size-1) )
                     CurPos;
                     if ( not ScrNext )
                       Moved = True;
                       SetFocus( dlg, 0 );
                       Return CM_Ignore;
                     end;
                   end;
                End;

                Скроллинг.tmp = Dlg(iDlgId);
                Return CM_Default
        End;
        Return CM_Ignore
End;

End; /*Class*/

Macro UserFunction
End;

Macro ОбработчикСкроллинга( dlg, Cmd, Id, Key)

        Скроллинг.SetCurDlgParms( Cmd, Id, Key );

 If   (Cmd == Dlg_Init)
  ClearRecord(dlg);
  Скроллинг.ScrRewind;
  Скроллинг.FillDown();
  UpdateFields(dlg);
  SetFocus( dlg, 0);

 ElIf (Cmd == Dlg_SetFocus)

   Скроллинг.SetStatus();

   Скроллинг.Moved      = False;                           
   Скроллинг.Position(id);
   Скроллинг.SetTmpValue(Dlg(id));

 ElIf (Cmd == Dlg_RemFocus)

   Скроллинг.Change(Dlg(id));

 ElIf (Cmd == Dlg_Key)

  If((ValType(Скроллинг) != V_Undef) and (Скроллинг.Col == -1))
    Return Cm_Default
  End;

  If   (Key == UP) /*вверх*/
        If(Скроллинг.Row != 0)
                Return Cm_Default
        End;
        Скроллинг.Change(Dlg(id));
        Скроллинг.OneUp;
        Скроллинг.tmp = Dlg(id);
        Return Cm_Ignore

  ElIf (Key == DOWN) /*вниз*/
        If(Скроллинг.Row != Скроллинг.Pos.Size-1)
/* Если список неполный - останавливаться на последней записи*/
                If(Скроллинг.Row == Скроллинг.LastId)
                        Return Cm_Ignore
                Else
                        Return Cm_Default
                End;
                Return Cm_Default
        End;
        Скроллинг.Change(Dlg(id));
        Скроллинг.OneDown;
        Скроллинг.tmp = Dlg(id);
        Return Cm_Ignore

  ElIf (Key == CTRLUP ) /*Ctrl вверх*/
   Скроллинг.Change(Dlg(id));
   Скроллинг.OneDown;
   Скроллинг.tmp = Dlg(id);
   Return Cm_Ignore

  ElIf (Key == CTRLDOWN ) /*Ctrl вниз*/  
   Скроллинг.Change(Dlg(id));
   Скроллинг.OneUp;
   Скроллинг.tmp = Dlg(id);
   Return Cm_Ignore

  ElIf (Key == CTRLLEFT) /*Ctrl влево*/
   Скроллинг.Change(Dlg(id));
   Скроллинг._Home;
   Return Cm_Ignore

  ElIf (Key == CTRLRIGHT) /*Ctrl вправо*/
   Скроллинг.Change(Dlg(id));
   Скроллинг._End;
   Return Cm_Ignore

  ElIf (Key == HOMEKEY) /*Home*/
   Скроллинг._Home

  ElIf (Key == ENDKEY) /*End*/
   Скроллинг._End

  ElIf (Key == CTRLHOME) /*Ctrl Home*/
   SetFocus(dlg, FldIndex(Dlg,String(Скроллинг.Fields.Item(0,Скроллинг.Col),0)));

  ElIf (Key == CTRLEND) /*Ctrl End*/
   SetFocus(dlg, FldIndex(Dlg,String(Скроллинг.Fields.Item(0,Скроллинг.Col),Скроллинг.LastId)));

  ElIf (Key == F1) /*F1*/
   MsgBox("Навигация по файлу:\n",
          "──────────────────────────────────────\n",
/*StrFor(24)*/"Вверх ","             - вверх            \n",
/*StrFor(25)*/"Вниз  ","             - вниз             \n",
"Ctrl","-Вверх "/*StrFor(24),*/"        - прогон вверх     \n",
"Ctrl","-Вниз  "/*StrFor(25),*/"        - прогон вниз      \n",
"Ctrl","-Влево "/*StrFor(27),*/" (Home) - первый столбец   \n",
"Ctrl","-Вправо"/*StrFor(26),*/" (End)  - последний столбец\n",
          "Ctrl-Home          - первая строка    \n"+
          "Ctrl-End           - последняя строка \n"+
          "PageUp             - страница вверх   \n"+
          "PageDown           - страница вниз    \n"+
          "Ctrl-PageUp        - начало файла     \n"+
          "Ctrl-PageDown      - конец  файла     \n"+
          "F4                 - поиск по маске:  \n"+
          "         ? - любой символ     \n"+
          "         * - любая подстрока  \n"
         );
   Return Cm_Ignore

  ElIf (Key == F3) /*F3*/
        Скроллинг.PressKey;
        UserFunction(dlg, Id);

  ElIf (Key == ESC ) /*Else*/
   Скроллинг.Change(Dlg(id));

  ElIf (Key == F4) /*F4*/
   If((Скроллинг.Col != -1) and 
       GetString(Скроллинг.FindVal,"Введите маску поиска:",50))
    If(Not Скроллинг.Find)
     MsgBox("Значение не найдено!")
    End;
    Return Cm_Default
   End;

  ElIf (Key == F9) /*F9*/
   Return CM_Ignore

  ElIf (Key == BS /*32*/) /*Пробел*/
   Скроллинг.Select

  ElIf (Key == ENTER ) /*Enter*/
    if ( Скроллинг.Pos.Value(Скроллинг.Row) == 0) 
      MsgBox("Значение не выбрано!");
      Return CM_Ignore
    end;

   if ( Скроллинг.CurPos );
     Return CM_Save
   else
     Return CM_Ignore
   end;

  Else 
    return Скроллинг.Обработчик ( Cmd, Id, Key )
                
  End;


 ElIf (Cmd == Dlg_Destroy)
/*
  Скроллинг = null;
*/
 End;

OnError(e)

 MsgBox(e.Message,"|  Модуль ",e.Module,"|  Строка ",e.line);
 Dlg(Id) = Скроллинг.tmp;
 UpdateFields(dlg);

  Return Cm_Default;

End;


      
    
     