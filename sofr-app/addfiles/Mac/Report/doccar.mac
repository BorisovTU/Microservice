/*
$Name:        ibr_doccar.mac
$Module:      Внутренняя отчетность
$Description: Журнал исправительных оборотов
*/
/*──────────────────────────────────────────────────────────────────────────┐
RS-Bank 5.1                                          R-Style Software Lab

File Name   : doccarr1.mac                                   July 31,1997
Programmer  : Чепелева Л.
Description : Бухгалтерский журнал за день

Comment     : Перечень документов за любой день рублевых и валютных
                с итогами по мемориальным и кассовым документам (глава А),
                а также внебалансовых документов (глава В).
                Документы группируются по отделениям с подведением
                соответствующих итогов при значении флага
                ИтогиПоОтделениям = TRUE.
                Есть возможность исключить документы рублевого покрытия
                с помощью флага ИсключитьПокрытие = TRUE.
                Суммы - в рублях, для валютных документов код валюты и
                сумма в валюте выводятся второй строкой.

Modify      : LCh 09.02.98
                - Сортировка документов в порядке возрастания дебет.счетов.
                - Подведения итогов по бал.счетам 2-го и 1-го порядка.
                - Возможность печати документов по ВСЕМ главам ( необходима
                настройка пользователем массива "НомерРаздела" ).
                - Для мультивалютных проводок - учет реальных рублевых
                документов покрытия, иначе - пересчет в рубли по курсу
                на отчетную дату.
                - Используется структура временного файла  tmp_clnt.dbt.
                16.02.98 Kireev
                Для покупки, продажи и конверсии валют в бухгалтерском
                журнале отображаем только реальные рублевые документы
                15.10.98 LCh Изменения в соответствии с Приложением 10
                        360-У
                11.03.99 LCh Отчет по филиалам
                16.08.99 LCh Учет мультивалютных проводок
                23.12.99 LCh Отдельный журнал по ЗО, панель ввода параметров.
                        ЗО в феврале проводятся архивными проводками за
                        32-33 декабря с указанием реальной даты проводки
                        в поле "Дата документа".
                03.07.2001 - Sal. SCR 12154
                        Возможность сортировки записей в графе каждого балансового
                        в порядке возрастания сумм.
                20.07.2001 - Sal. SCR 22791 - SPACE = CodeFor(" ");
                29.05.2002 - LCh  Исправлена обработка документов мультивалютных
                        проводок: документ покрытия, если его счета не попадают
                        в массив исключений БалДляПроверки1 из настройки макроса
                03.11.2003 - Sarkisova SCR 43980
                        Вместо массива исключений БалДляПроверки1 используется
                        настройка реестра, с учетом масок.
    25.07.2005 BugZ SCR 69375 Фильтрация по ТС/РС.
    10.08.2005 BugZ SCR 71334 Опечатка в макросе, отсутствие файла в VM.
    22.10.2007 Malakhova 97262 Перенесла Бухгалтерский журнал в подсистему "Внутренняя отчетность"
                                Переименовала макрофайл из doccarcb.mac в doccar.mac
└───────────────────────────────────────────────────────────────────────────*/

import FIInter;
import rep_lib;
import ofstream;
import Reporting;
import ocp;
import ofstream;
import RepSqlContextInitializer;

private CONST ALLFININSTR = -1,
              ALLCURRENCY = -2;

var reportBodyFileName;

private class TDocCarPrint(parameters)
    var m_tempOfstream = TOfstream("doccar_report");
    var m_tablePrinter : CTableReport;
    var m_parameters;

    macro setHead()
        m_tablePrinter.AddColumn("Ном",   12, AL_CENTER);
        m_tablePrinter.AddColumn("СчетДт",  25, AL_LEFT);
        m_tablePrinter.AddColumn("Счет Кт",  25, AL_CENTER);
        m_tablePrinter.AddColumn("Сумма", 23, AL_RIGHT);
        m_tablePrinter.AddColumn("Кол.док.", 10, AL_RIGHT);
    end;

    macro getReportWidth()
        return m_tablePrinter.GetSumLen();
    end;

    macro printNoData()
        printLn();
        println("Нет данных для отчета");
        PrintLn();
    end;

    macro printReportHead()
        var dateReport  = m_parameters.getDateReport();
        var data;

        printBankHeader(m_parameters.getDepartmentCode(), m_parameters.getOrgStructure(), getReportWidth());

        println();
        println(strAlign("БУХГАЛТЕРСКИЙ ЖУРНАЛ", getReportWidth(), STR_ALIGN_CENTER));

        if (m_parameters.getIsSPODTuns())
            printLn(strAlign("по проводкам СПОД", getReportWidth(), STR_ALIGN_CENTER));
        end;

        printLn();

        printLn(strAlign("за " + dateReport + " г.", getReportWidth(), STR_ALIGN_CENTER));

        PrintLn();
        var strDepartment;
        data = TRsbDataSet( "SELECT t_codeName Code, t_name Name FROM dRepDpDep_vw WHERE t_nodeId = " + m_parameters.getDepartmentCode());
        if( data.moveNext )
           strDepartment = "Филиал " + Data.code + " " + Data.name;
        end;

        printLn(strAlign(strDepartment, getReportWidth(), STR_ALIGN_CENTER));

    end;

    macro printTableHead()
[
┌──────────────┬───────────────────────────────────────────────────────┬─────────────────────────┬────────────┐
│     Номер    │                      Номера счетов                    │      Суммы в рублях     │ Количество │
│   документа  │                                                       │        и копейках       │ документов*│
│  (последние  ├───────────────────────────┬───────────────────────────┤                         │            │
│   3 знака)   │         по дебету         │          по кредиту       │                         │            │
├──────────────┼───────────────────────────┼───────────────────────────┼─────────────────────────┼────────────┤
│       1      │            2              │             3             │             4           │      5     │
├──────────────┼───────────────────────────┼───────────────────────────┼─────────────────────────┼────────────┤
];
    end;

    macro getSum(sum) : String
        if (m_parameters.getIsSetA())
            return String(sum:0:2:a);
        else
            return String(sum:0:2);
        end;
    end;

    macro getDocNumber(number) : String
        var strNumber = Trim(number);

        if (StrLen(strNumber) <= 3 )
            return strNumber;
        end;

        return SubStr(strNumber, StrLen(strNumber)-2);
    end;

    macro execute(dataSet)
        var isEmpty : bool = TRUE;
        var curChapter = null;
        var curType    = null;
        var curBal1    = null;
        var curBal2    = null;
        var curBranch  = null;
        var strName = "";
        var data;
        var needInCur  = m_parameters.GetInCurrency();
        var needItogBalance = m_parameters.getIsItogBalance();
        var needItogDepartment = m_parameters.getIsDepartmentItog();
        var sumStr = "";
        var docInChapter = 0;
        var docInBal1 = 0;
        var docInBal2 = 0;
        var docInBranch = 0;
        var docInType = 0;
        var typeNumber = 1;
        var sumBal1 = 0;
        var sumBal2 = 0;
        var sumBranch = 0;
        var sumType = 0;
        var sumChapter = 0;

        printReportHead();
        printTableHead();

        while (dataSet.next())
            isEmpty = FALSE;

            if (needItogBalance)
                if (curBal2 == NULL)
                    curBal2 = dataSet.balance2;
                elif ((curBal2 != dataSet.balance2) or
                      ((curBranch != dataSet.branch) and (needItogDepartment == TRUE)) or
                      (curType != dataSet.typeDoc)  or
                      (curChapter != dataSet.chapter))
					m_tablePrinter.printSeparator();
                    m_tablePrinter.printString("Итого по сч.", " " + curBal2, "X", getSum(sumBal2), docInBal2);
                    m_tablePrinter.printSeparator();
                    curBal2 = dataSet.balance2;
                    docInBal2 = 0;
                    sumBal2 = 0;
                end;

                if (curBal1 == NULL)
                    curBal1 = dataSet.balance1;
                elif ((curBal1 != dataSet.balance1) or
                      ((curBranch != dataSet.branch) and (needItogDepartment == TRUE)) or
                      (curType != dataSet.typeDoc)  or
                      (curChapter != dataSet.chapter))
					m_tablePrinter.printSeparator();
                    m_tablePrinter.printString("Итого по сч.", " " + curBal1, "X", getSum(sumBal1), docInBal1);
                    m_tablePrinter.printSeparator();
                    curBal1 = dataSet.balance1;
                    docInBal1 = 0;
                    sumBal1 = 0;
                end;
            end;

            if (needItogDepartment)
                if (curBranch == NULL)
                    curBranch = dataSet.branch;
                elif ((curBranch != dataSet.branch) or
                      (curType != dataSet.typeDoc)  or
                      (curChapter != dataSet.chapter))
                    data = TRsbDataSet( "SELECT t_codeName name FROM dRepDpDep_vw WHERE t_nodeId = " + curBranch );
                    if( data.moveNext )
                        strName = data.name;
                    end;
                    if (not needItogBalance)
                        m_tablePrinter.printSeparator();
                    end;
					m_tablePrinter.printSeparator();
                    m_tablePrinter.printStringTransferByWord("Итого по отд. " + strName, "           X            ", "X", getSum(sumBranch), docInBranch);
                    m_tablePrinter.printSeparator();

                    curBranch = dataSet.branch;
                    docInBranch = 0;
                    sumBranch   = 0;
                    docInBal1   = 0;
                    sumBal1     = 0;
                    docInBal2   = 0;
                    sumBal2     = 0;
                end;
            end;

            if (curType != dataSet.typeDoc)
                if (curType != NULL)
                    if ((not needItogBalance) and (not needItogDepartment))
                        m_tablePrinter.printSeparator();
                        typeNumber  = typeNumber + 1;
                    end;

					m_tablePrinter.printSeparator();
                    m_tablePrinter.printStringTransferByWord("Итого по " + ternary(curType == 2,"кассовым документам","мемориальным документам"),
                                               "           X            ", "X", getSum(sumType), docInType);
                    m_tablePrinter.printSeparator();
                end;

                curType = dataSet.typeDoc;

                if (curChapter == dataSet.chapter)
                    print("│");
                    print(strAlign(String(typeNumber) + ". " + ternary(curType == 2,"Кассовые документы","Мемориальные документы") , getReportWidth() - 1, STR_ALIGN_CENTER));
                    printLn("│");
                end;


                docInBranch = 0;
                sumBranch   = 0;
                docInBal1   = 0;
                sumBal1     = 0;
                docInBal2   = 0;
                sumBal2     = 0;
                docInType   = 0;
                sumType     = 0;
            end;

            if (curChapter != dataSet.chapter)
                if (curChapter != null)
					m_tablePrinter.printSeparator();
                    m_tablePrinter.printStringTransferByWord("Всего по балансовым счетам", "           X            ", "X", getSum(sumChapter), docInChapter);
                    m_tablePrinter.printSeparator();
                end;

                typeNumber = 1;
                curChapter = dataSet.chapter;
                curType = dataSet.typeDoc;
                if (needItogDepartment)
                    curBranch = dataSet.branch;
                end;
                if (needItogBalance)
                    curBal1 = dataSet.balance1;
                    curBal2 = dataSet.balance2;
                end;

                data = TRsbDataSet( "SELECT t_name name FROM dobchaptr_dbt WHERE t_chapter = " + curChapter );
                if( data.moveNext )
                    strName = data.name;
                end;

                sumBranch  = 0;
                sumBal1    = 0;
                sumBal2    = 0;
                sumType    = 0;
                sumChapter = 0;

                docInChapter = 0;
                docInBal1    = 0;
                docInBal2    = 0;
                docInBranch  = 0;
                docInType    = 0;

				print("│");
                print(strAlign("Глава " + curChapter + " " + strName, getReportWidth() - 1, STR_ALIGN_CENTER));
				printLn("│");
				
                print("│");
                print(strAlign(String(typeNumber) + ". " + ternary(curType == 2,"Кассовые документы","Мемориальные документы") , getReportWidth() - 1, STR_ALIGN_CENTER));
                printLn("│");

            end;

            sumStr = getSum(dataSet.sumNatCur);

            m_tablePrinter.printString(getDocNumber(dataSet.numberDoc), dataSet.debetAcc, dataSet.creditAcc, sumStr, "     X    ");

            sumBal1    = sumBal1    + dataSet.sumNatCur;
            sumBal2    = sumBal2    + dataSet.sumNatCur;
            sumType    = sumType    + dataSet.sumNatCur;
            sumChapter = sumChapter + dataSet.sumNatCur;
            sumBranch  = sumBranch  + dataSet.sumNatCur;

            docInChapter = docInChapter + 1;
            docInBal1    = docInBal1 +1;
            docInBal2    = docInBal2 + 1;
            docInBranch  = docInBranch +1;
            docInType    = docInType +1;

            if ((needInCur == TRUE) and (dataSet.fiId != NATCUR))
                sumStr ="/" + dataSet.debetFiCode + ":" + getSum(dataSet.debetSum);

                m_tablePrinter.printString("","","", strAlign(sumStr,m_tablePrinter.getAColumns()(3).getLen(),STR_ALIGN_LEFT), "");
            end;
         end;

        if (isEmpty)
            printNoData();
        else
            if (needItogBalance)
				m_tablePrinter.printSeparator();
                m_tablePrinter.printString("Итого по сч.", " " + curBal2, "X", getSum(sumBal2), docInBal2);
                m_tablePrinter.printSeparator();

                m_tablePrinter.printString("Итого по сч.", " " + curBal1, "X", getSum(sumBal1), docInBal1);
                m_tablePrinter.printSeparator();
            end;

            if (needItogDepartment)
                data = TRsbDataSet( "SELECT t_codeName name FROM dRepDpDep_vw WHERE t_nodeId = " + curBranch );
                if( data.moveNext )
                    strName = data.name;
                end;
				m_tablePrinter.printSeparator();
                m_tablePrinter.printStringTransferByWord("Итого по отд. " + strName, "           X            ", "X", getSum(sumBranch), docInBranch);
                m_tablePrinter.printSeparator();
            end;

			m_tablePrinter.printSeparator();
            m_tablePrinter.printStringTransferByWord("Итого по " + ternary(curType == 2,"кассовым документам","мемориальным документам"),
                                       "           X            ", "X", getSum(sumType), docInType);
            m_tablePrinter.printSeparator();

            m_tablePrinter.printStringTransferByWord("Всего по балансовым счетам", "           X            ", "X", getSum(sumChapter), docInChapter);

            m_tablePrinter.printBottom();
        end;

        printLn("*Показывается количество документов, по которым сделаны бухгалтерские проводки.");
        printLn();
		var userName = TRsbDataSet("SELECT bankUser.t_name"
						+ "\n" + "   FROM repUser_vw bankUser"
						+ "\n" + "  WHERE bankUser.t_id = " + String({oper}));
		if (userName.Next())
			printLn(" Операционист " + String(userName.name));
			printLn(strAlign("\"___\"____________20___г.", getReportWidth(), STR_ALIGN_RIGHT));
		end;
                reportBodyFileName = m_tempOfstream.getFileName();
	        m_tempOfstream.show();
	end;

    private macro constructorTDocCarPrint(parameters)
        m_parameters = parameters;
        m_tempOfstream.setOutputFile();
        m_tablePrinter = CTableReport();
        setHead();
    end;

    constructorTDocCarPrint(parameters);
end;

class TDocCarData(param)

    macro getQueryText(param)
        var fromText;
        var whereText;
        var sortText;
        var selectText;
        var groupOper;
        var stat;
        var excludeBalance;

        getRegistryValue("REPTREG\REP_GROUPS\БУХГАЛТЕРСКИ_ЖУРНАЛ\БАЛ_ИСКЛ", V_STRING, excludeBalance, stat);

        if (stat != 0)
            excludeBalance = "";
        else
            excludeBalance = trim(excludeBalance);
        end;

        selectText = " SELECT doc.t_number numberDoc,                    "
            + "\n" + "        doc.t_debetAccount debetAcc,               "
            + "\n" + "        doc.t_creditAccount creditAcc,             "
            + "\n" + "        CASE WHEN doc.t_chapter = 5                "
            + "\n" + "             THEN doc.t_debetSum                   "
            + "\n" + "             ELSE doc.t_inCoverSum                 "
            + "\n" + "        END sumNatCur,                             "
            + "\n" + "        accDebet.t_balance balance2,               "
            + "\n" + "        accDebet.t_branchId branch,                "
            + "\n" + "        doc.t_chapter chapter,                     "
            + "\n" + "        doc.t_debetFiId fiId,                      "
            + "\n" + "        CASE WHEN (TRIM(accTrn.t_kind_oper) = '3') "
            + "\n" + "             THEN 2                                "
            + "\n" + "             ELSE 1                                "
            + "\n" + "        END typeDoc                                ";

        fromText = "    FROM drepDocument_vw doc,     "
            + "\n" + "         drepAccount_vw accDebet, "
            + "\n" + "         dAccTrn_dbt accTrn ";

        whereText = "   WHERE doc.t_id  = accTrn.t_accTrnId  "
            + "\n" + "     AND doc.t_debetAccountId  = accDebet.t_accountId "
            + "\n" + "     AND doc.t_date = " + getSqlDate(param.getDateReport())
            + "\n" + "     AND (" + RepBranchAndDepartmentFieldFilter(param.getDepartmentList()).GetAsSqlString("accDebet.t_departmentId", "accDebet.t_branchId") + ")";

        sortText = " ORDER BY ";

        if (param.GetInCurrency())
            selectText = selectText + ",doc.t_debetSum debetSum,"
                             + "\n" + "(SELECT t_code FROM drepFinInstrument_vw finInstr WHERE finInstr.t_id = doc.t_debetFiId) debetFiCode";
        end;

        if ((param.getCodeCurrency() != ALLCURRENCY) AND (param.getCodeCurrency() != ALLFININSTR))
            whereText = whereText + " AND (doc.t_debetFiId = " + param.getCodeCurrency() + ")"
        elif (param.getForeignCur())
            whereText = whereText + " AND (doc.t_debetFiId <> " + NATCUR + ")"
        end;

        var pChapter = param.getChapter();
        var inText = " AND " + getChapterFilterSqlClause(pChapter, "doc.t_chapter"); // условие для отбора данных по списку глав

        whereText = whereText + inText;
        sortText = sortText + "doc.t_chapter asc, typeDoc, ";

        if (param.getIsDepartmentItog())
            sortText = sortText + " branch, ";
        end;

        if (param.getIsItogBalance)
            selectText = selectText + ", SUBSTR(accDebet.t_balance, 0, 3) balance1";
            sortText = sortText + " balance1, balance2, ";
        end;

        if (param.getIsSPODTuns())
            whereText = whereText + " AND doc.t_isSPOD = 1";
        else
            whereText = whereText + " AND doc.t_isSPOD = 0";
        end;


        var sortingArraySize = param.getSort.Size();
        var i;

        if (sortingArraySize != 0)
            i = 0;

            while (i < sortingArraySize)
                if (param.getSort()[i] == REP_PATTERNSORT_SUM_DOCUMENT)
                sortText = sortText + " sumNatCur asc,";
                elif (param.getSort()[i] == REP_PATTERNSORT_BALANCE)
                sortText = sortText + " balance2 asc,";
                end;

                i = i +1;
            end;
        end;

        sortText = sortText + " debetAcc asc ";

        return selectText + fromText + whereText + sortText;
    end;

    macro getDataSet(param)
        var dataSet;
        dataSet = TRsbDataSet(getQueryText(param));

        dataSet.setFieldType("debetSum",  V_MONEY);
        dataSet.setFieldType("sumNatCur", V_MONEY);

        return dataSet;
    end;

    macro constructor(param)
        SQL_Execute("{ CALL  rep_data.setBalancePlanNumber(" + param.getPlanNumber() + ") }");
        SQL_Execute("{ CALL  rep_data.setBeginDate(" + GetSQLDate(param.getDateReport()) + ") }");
        SQL_Execute("{ CALL  rep_data.setEndDate(" +  GetSQLDate(param.getDateReport()) + ") }");
    end;

    constructor(param);
end;

class TParamDocCar(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort)

    private var m_numDprt;
    private var m_orgStructure;
    private var m_issueMode;
    private var m_codeCurrency;
    private var m_dateReport;
    private var m_sort;
    private var m_chapter;
    private var m_departmentList;
    private var m_planNumber;
    private var m_withExcludedChapters;
    private var m_foreignCur;
    private var m_isSPODTurns;
    private var m_inCurrency;
    private var m_isSetA;
    private var m_isItogBalance;
    private var m_IsDepartmentItog;

    macro getDateReport()
        return m_dateReport;
    end;

    macro getDepartmentList()
        return m_departmentList;
    end;

    macro getOrgStructure()
        return m_orgStructure;
    end;

    macro getDepartmentCode()
        return m_numDprt;
    end;

    macro getCodeCurrency()
        return m_codeCurrency;
    end;

    macro getChapter()
        return m_chapter;
    end;

    macro getSort()
        return m_sort;
    end;

    macro getPlanNumber()
        return m_planNumber;
    end;

    macro getWithExcludedChapters()
        return m_withExcludedChapters;
    end;

    macro getForeignCur()
        return m_foreignCur;
    end;

    macro getIsSPODTuns()
        return m_isSPODTurns;
    end;

    macro GetInCurrency()
        return m_inCurrency;
    end;

    macro getIsSetA()
        return m_isSetA;
    end;

    macro getIsItogBalance()
        return m_isItogBalance;
    end;

    macro getIsDepartmentItog()
        return m_IsDepartmentItog;
    end;

    macro constructor(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort)

        m_numDprt              = numDprt;
        m_orgStructure         = orgStructure;
        m_issueMode            = issueMode;
        m_codeCurrency         = codeCurrency;
        m_dateReport           = dateReport;
        m_chapter              = pChapter;
        m_sort                 = sort;
        m_planNumber           = numPlan;
        m_withExcludedChapters = withExcludedChapters;
        m_foreignCur           = foreignCur;
        m_isSPODTurns          = isSPODTurns;
        m_inCurrency           = inCurrency;
        m_isSetA               = isSetA;
        m_isItogBalance        = isItogBalance;
        m_IsDepartmentItog     = IsDepartmentItog;

        m_departmentList = RepDepartmentList(m_orgStructure, m_issueMode, m_numDprt);
    end;


    constructor(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort);
end;


/*
    Основная функция
*/
macro printReport(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort)
    var parameters = TParamDocCar(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort);

    /* Контроль открытых опердней */
    if (not RepOperdaysOpened(parameters.getDepartmentList(), parameters.getDateReport(), parameters.getDateReport()).shouldContinue())
        exit(1);
    end;

    var docCarPrint = TDocCarPrint(parameters).execute(TDocCarData(parameters).getDataSet(parameters));

    return true;
end;

macro ibrCreateReport(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, codeCurrency, foreignCur, dateReport, isSPODTurns,inCurrency,isSetA,isItogBalance,IsDepartmentItog, sort)
   var params = RepParameterCollection();

    params.add(RepParameter(DEPARTMENT_CODE,        REP_PT_INT,     numDprt));
    params.add(RepParameter(ORGANIZATION_STRUCTURE, REP_PT_INT,     orgStructure));
    params.add(RepParameter(ISSUE_MODE,             REP_PT_INT,     issueMode));
    params.add(RepParameter(BALANCE_PLAN_NUMBER,    REP_PT_INT,     numPlan));
    params.add(RepParameter(CHAPTER,                REP_PT_INT,     pChapter));
    params.add(RepParameter(CODE_CURRENCY,          REP_PT_INT,     codeCurrency));
    params.add(RepParameter(BEGIN_DATE,             REP_PT_DATE,    dateReport));
    params.add(RepParameter(END_DATE,               REP_PT_DATE,    dateReport));

    initializeSqlContext(params); 

    var result = printReport(numDprt, orgStructure, issueMode, numPlan, pChapter, withExcludedChapters, 
                            codeCurrency, foreignCur, dateReport, isSPODTurns, inCurrency, isSetA,
                            isItogBalance, IsDepartmentItog, sort);
    if (result)
        return true;
    end;

    return false; 
end;