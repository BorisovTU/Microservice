/*
$Name:         bk_opacc_Retail.mac
$Module:       Внутренняя отчетность
$Description:  Книга регистрации открытых счетов. Получение данных Retail
*/
/*───────────────────────────────────────────────────────────────────────────┐
  Книга регистрации открытых счетов
  Ведомость открытых и закрытых счетов

  Получение данных Retail

  File Name     : bk_opacc_Retail.mac
  Call functions: CalculateRetail
  Call from     : bk_opacc.mac
└───────────────────────────────────────────────────────────────────────────*/
import RsbObjFactory;
import bk_opacc;
import rep_lib;

/* Фильтр по главе и номеру плана */
macro fillBalanceData(parm)

    private macro getSelectFrom(chapterAndPlanClause)
        return          "(SELECT CHR(0) t_iscurrency, b.t_balance, MIN(b.t_name_part) t_name_part"
               + "\n" + "   FROM dbalance_dbt b"
               + "\n" + "  WHERE " + chapterAndPlanClause
               + "\n" + "  GROUP BY b.t_balance)";
    end;

    var query;
    var chapterAndPlanClause = "";

    // для выпуска по данным RS-Retail всегда берем план счетов 0
    chapterAndPlanClause = "b.t_iNumPlan = 0 AND " + getChapterFilterSqlClause(0, parm.chapter, "b.t_chapter");

    sql_execute("TRUNCATE TABLE drepbk_opaccblnc_tmp");

    query = getSelectFrom(chapterAndPlanClause);

    sql_execute("INSERT INTO drepbk_opaccblnc_tmp(t_iscurrency, t_balance, t_name_part)" + "\n" + query);

end;

/**
 * Инициализация пакета rep_bk_opacc
 */
private macro initializePackage(parm, isAccPlanSynchronized)

    var cmd = RsdCommand("BEGIN"
                + "\n" + "    rep_bk_opacc.initRetail(?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ?,"
                + "\n" + "                            ? "
                + "\n" + "                           );"
                + "\n" + "END;"
                        );

    var branchFilter = parm.BranchFieldFilter.GetAsSqlString("STRING_TO_REPLACE");

    var p_module                 = parm.module;
    var p_reportKind             = parm.reportKind;
    var p_accountType            = parm.accountType;
    var p_beginDate              = parm.beginDate;
    var p_endDate                = parm.endDate;
    var p_currencyId             = parm.currencyId;
    var p_accountMask            = nvl(parm.accountMask, "", strFor(1));
    var p_needAccOpen            = ternary(parm.needAccOpen, "X", "O");
    var p_needAccClose           = ternary(parm.needAccClose, "X", "O");
    var p_isAccPlanSynchronized  = ternary(isAccPlanSynchronized, "X", "O");
    var p_branchFilter           = branchFilter;
    var p_rep_subsystems_retail  = REP_SUBSYSTEMS_RETAIL;
    var p_rep_module_osb         = ОСБ;
    var p_rep_form               = ternary(ФормаОтчета, "X", "O");
    var p_mns_message_date       = ternary(ДатаСообщенияМнс, "X", "O");

    cmd.addParam("p_module"               , RSDBP_IN, p_module);
    cmd.addParam("p_reportKind"           , RSDBP_IN, p_reportKind);
    cmd.addParam("p_accountType"          , RSDBP_IN, p_accountType);
    cmd.addParam("p_beginDate"            , RSDBP_IN, p_beginDate);
    cmd.addParam("p_endDate"              , RSDBP_IN, p_endDate);
    cmd.addParam("p_currencyId"           , RSDBP_IN, p_currencyId);
    cmd.addParam("p_accountMask"          , RSDBP_IN, p_accountMask);
    cmd.addParam("p_needAccOpen"          , RSDBP_IN, p_needAccOpen);
    cmd.addParam("p_needAccClose"         , RSDBP_IN, p_needAccClose);
    cmd.addParam("p_isAccPlanSynchronized", RSDBP_IN, p_isAccPlanSynchronized);
    cmd.addParam("p_branchFilter"         , RSDBP_IN, p_branchFilter);
    cmd.addParam("p_rep_subsystems_retail", RSDBP_IN, p_rep_subsystems_retail);
    cmd.addParam("p_rep_module_osb"       , RSDBP_IN, p_rep_module_osb);
    cmd.addParam("p_rep_form"             , RSDBP_IN, p_rep_form);
    cmd.addParam("p_mns_message_date"     , RSDBP_IN, p_mns_message_date);

    sql_execute(cmd);

end;

/****************************************************************************/
/*                                Точка входа                               */
/****************************************************************************/
MACRO CalculateRetail( parm, tempTableName )

    Message( "Получение данных RS-Retail");
    BegAction(1000, "Получение данных RS-Retail", false);

    var isAccPlanSynchronized = true;

    fillBalanceData(parm);

    initializePackage(parm, isAccPlanSynchronized);

    sql_execute("BEGIN"
       + "\n" + "    rep_bk_opacc.fillRetailAccountData('repRtlDeposit_vw',"
       + "\n" + "                                       'repRtlDepAccount_vw',"
       + "\n" + "                                       'repRtlAccumPcAccount_vw'"
       + "\n" + "                                      );"
       + "\n" + "END;"
               );

    EndAction();

    return true;
END;
