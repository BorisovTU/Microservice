/*
$Name:         BK_OPACC.MAC
$Module:       Внутренняя отчетность
$Description:  Книга регистрации открытых счетов
*/
/*───────────────────────────────────────────────────────────────────────────┐
  Книга регистрации открытых счетов
  Ведомость открытых и закрытых счетов

  File Name     : bk_opacc.mac
  Call functions: Report(...), ReportSb(...)
  Menu          : Счета->Книга регистрации открытых счетов (во Внутр. отчётности),
                  Отчетность->Счета->Книга регистрации (в Отчётности СБ РФ)

DESCRIPTION: Для нормальной работы отчёта необходимо в макросе печати формы
             установить константы, определяющие параметры вашего принтера
             а также некоторые поля отчета.
└───────────────────────────────────────────────────────────────────────────*/
import RsbDataSet;
import Reporting;
import BankInter;
import CTInter;
import PTInter;
import globals;
import lib_arr;
import cb_sql;
import ocp;
import FIInter;
import rep_lib;
import ofstream;

/*------------------ Константы ---------------------------------------*/
const                          /* типы примечаний */
   NOTEKIND_PURP_ACC     =  7, /* Наименование (цель) счета */
   NOTEKIND_NUMB_CONTR   =  8, /* номер договора обслуживания */
   NOTEKIND_DATE_CONTR   =  9, /* дата договора обслуживания */
   NOTEKIND_GROUND_OPEN  = 10, /* Основание открытия счета */
   NOTEKIND_GROUND_CLOSE = 11, /* Основание закрытия счета */
   NOTEKIND_PERIODICITY  = 20, /* Порядок и периодичность выдачи выписок по счету */
   NOTEKIND_TAXMSGDATE   = 23; /* Дата сообщения налоговому органу */

const                          /* Виды обслуживания (из справочника servkind)*/
    SERVKIND_ARM         = 9,  /* АРМ позиционера */
    SERVKIND_RKO         = 3;  /* РКО */

/*01 Aug 07 Wed 11:06:33 Malakhova Irina 110772*/
const                          /*Виды дат*/
    OPEN_DATE            = 1,  /*Дата сообщения в налоговые органы об открытии счета*/
    CLOSE_DATE           = 2;  /*Дата сообщения в налоговые органы о закрытии счета*/

/*************** Константы для значений, получаемых из панели *****************/
const  RegBook           = 0,  /* Книга регистрации */
       OpenCloseList     = 1,  /* Ведомость откр./закр. счетов */
       RegBookOnDate     = 2;  /* Книга регистрации на дату */

const  DepartmentAll     = 0;  /* Все филиалы */

const  DefaultPlan       = 0;  /* Основной план счетов */

const  ChapterAll        = 0;  /* Все главы */

const  ALLCURRENCY       = -2; /* Все ин валюты*/

const  ВсеСчета          = 1,  /* только для Внутренней */
       СчетаБанка        = 2,
       СчетаКлиентов     = 3,  /* только для Внутренней */
       СчетаФизиков      = 4,  /* только для Сбера */
       СчетаЮриков       = 5;  /* только для Сбера */

const  БезПокрытия       = 3;

const  ВО  = 0,
       ОСБ = 1;

var reportBodyFileName;

/******************* Чтение настроек ******************************************/
var МаскаСчетовКлиентов; //МаскаСчетовФизЛиц,
var ПериодичностьВыдачиВыписок, ДатаСообщенияМнс, ФормаОтчета;
getRegistryValue( "REPORT\\Книга регистрации\\СЧЕТА КЛИЕНТОВ", V_STRING, МаскаСчетовКлиентов, NULL );
//getRegistryValue( "PS\\REQOPENACC\\СЧЕТА ФИЗИЧЕСКИХ ЛИЦ", V_STRING, МаскаСчетовФизЛиц, NULL );
getRegistryValue( "REPORT\\Книга регистрации\\Периодичность выдачи выписок", V_STRING, ПериодичностьВыдачиВыписок, NULL );
getRegistryValue( "REPORT\\Книга регистрации\\ДАТА СООБЩЕНИЯ МНС", V_BOOL, ДатаСообщенияМнс, NULL );
getRegistryValue( "REPORT\\Книга регистрации\\ФОРМА ПО ВНУТРИБАНК. СЧЕТАМ", V_BOOL, ФормаОтчета, NULL );

/*************** Глобальные переменные ****************************************/
private const TempTableName = "dbk_opacc_tmp";  /* имя врем. табл. */

private var prForm; /* печатная форма */
private var params; /* параметры отчета */
/******************************************************************************/
/* Класс, инкапсулирующий печатную форму. */
/* mfile - имя макрофайла печати (необяз.)
   mfunc - имя или указатель на функцию печати (необяз.)
*/
private CLASS TPrintForm( mfile, mfunc )
    private var MacroFile = mfile;
    private var MacroFunc = mfunc;

    /* Печать отчёта в соответствии с формой и параметрами */
    /* params - параметр, передаваемый в функцию печати (необяз.) */
    /* hasData - признак наличия данных для отчета, по умолчанию считается что данные есть, необязательный */
    MACRO Print( params, hasData )
        if( MacroFile == NULL )
            return ExecMacro2( MacroFunc, params, nvl(hasData, true) );
        else
            return ExecMacroFile( MacroFile,  MacroFunc, params, nvl(hasData, true) );
        end;
    END;
END;

/******************************************************************************/
/* Структура параметров отчёта.
   Параметры и их порядок в точности совпадают с данными-членами, поэтому такие имена. */
private CLASS TParams( mod, rk, ss, df, bf, bdf, ocpf, bd, ed, ad, ch, typ, cur, acc, op, cl, ev, pf )
    var Module                         = mod;    /* Модуль: ВО или ОСБ */
    var ReportKind                     = rk;     /* Вид отчёта: RegBook или OpenCloseList */
    var Subsystem                      = ss;     /* Подсистема: REP_SUBSYSTEMS_ALL, ...Cb, ...Loans, ...Retail */
    var AccDprtFilter                  = df;     /* Фильтр л/с по подразделениям (объект RepAccountFilter) */
    var BranchFieldFilter              = bf;     /* Фильтр л/с по подразделениям (объект RepBranchFieldFilter) */
    var BranchAndDepartmentFieldFilter = bdf;    /* Фильтр л/с по подразделениям (объект RepBranchAndDepartmentFieldFilter) */
    var ocpAccountServer               = ocpf;   /* Сервер счетов ОВП (объект RepOcpAccountServer) */
    var BeginDate                      = bd;     /* Дата начала периода */
    var EndDate                        = ed;     /* Дата окончания периода */
    var ArchDate                       = ad;     /* Дата сдачи в архив */
    var Chapter                        = ch;     /* Номер главы счетов или ChapterAll */
    var AccountType                    = typ;    /* Принадлежность счетов: ВсеСчета, СчетаБанка, СчетаКлиентов etc. */
    var CurrencyId                     = cur;    /* ID валюты или CurrencyAll */
    var AccountMask                    = acc;    /* Маска счетов(для ОСБ) */
    var NeedAccOpen                    = op;     /* Открытые счета (bool) */
    var NeedAccClose                   = cl;     /* Закрытые счета (bool) */
    var NeedEveryDay                   = ev;     /* За каждый день (bool) (только для Ведомости) */
    var NeedPrintFunds                 = pf;     /* Печатать данные о фондах (bool) */

    /* Получить номер плана счетов для главы */
    MACRO GetPlanNum( ch )
        if( ch == NULL )
            ch = Chapter;
        end;
        return DefaultPlan;
    END;
END;

/*************** Вспомогательные функции **************************************/
macro  Предприниматель( ClientID )
  FILE Person( persn );
  Person.PersonID = ClientID;
  if ( getEQ(Person) and ( Person.IsEmployer > 0 ) )
          return TRUE;
  else    return FALSE;
  end;
end;

/* Признак клиента - физ.лицо-НЕ предприниматель */
macro IsFizAndNotBisness( client )
  if ((client.LegalForm == PTLEGF_PERSN)  and    /*  физ.лицо */
        not Предприниматель(client.PartyID ) )
          return TRUE;
  else    return FALSE;
  end;
end;

/*01 Aug 07 Wed 11:02:48 Malakhova Irina 110772*/
/*Добавила параметр с видом даты*/
/* Получить значение для графы "дата сообщения налоговым органам". */
MACRO GetTaxMsgDate( temp, client, dateKind)
    /* Первая проверка для Loans и Retail, для кот. не надо выводить.*/
    if( ( temp.TaxMsgOpenDate == NULL ) or CompareStrWithMasks( МаскаСчетовКлиентов, temp.Balance )
                or IsFizAndNotBisness( client )
                or in(temp.backOffice, REP_SUBSYSTEMS_LOANS/* , REP_SUBSYSTEMS_DEPOSITS, REP_SUBSYSTEMS_RETAIL */)
      )
        return "";
    else
        if (dateKind == OPEN_DATE)
            return string(Date(temp.TaxMsgOpenDate):f);
        elif (dateKind == CLOSE_DATE)
            if ((temp.Close_Date > Params.EndDate) or (temp.Close_Date == ""))
                return "";
            else
                return string(Date(temp.TaxMsgCloseDate):f);
            end;
        end;
    end;
END;

/* Инициализация */
private MACRO Initialize( )
    SQL_Truncate( TempTableName );
    return true;
END;

/* Деинициализация */
private MACRO Uninitialize( )
    SQL_Truncate( TempTableName );
    return true;
END;

/* Выбирает данные, заносит во временную таблицу. */

private MACRO GetAccData( params )
    const nullStr = GetSqlString( "" );
    const nullDate= GetSqlDate(Date(0,0,0));

    /*//02 Aug 07 Thu 17:26:35 Malakhova Irina 110772*/
    const REQOPENA  = 230;
    const REQCLOSEA = 231;

    /* Вершина дерева ТС. Ипользуется в алгоритме поиска "настоящего" клиента счёта. */
    var topData = TRsbDataSet("SELECT t_partyId topId FROM ddp_dep_dbt WHERE  t_parentCode = 0");
    topData.SetFieldType("topId", V_INTEGER);
    topData.Next;
    const topId = topData.TopId;

    /* PL/SQL-функция, с пом. кот. вычисляется дата сообщения в ГНИ */
    var TaxMsgFunc;
    if( ДатаСообщенияМнс )
        TaxMsgFunc = "ps.PSREQ_GetMesFormDate";
    else
        TaxMsgFunc = "ps.PSREQ_GetMesSendDate";
    end;

    var accountTableName = "dRepAccount_vw";

    private macro getDateFilterText(openDateAlias, closeDateAlias) // "a.t_Open_Date", "a.t_Close_Date"
        var filter;

        /* Вспомогательные константы, чтобы дальше было проще. */
        const filterOpen = ternary(params.BeginDate == params.EndDate,
                     "                       " + openDateAlias + "  = "  + GetSQLDate(params.EndDate),
                     "                       " + openDateAlias + "  >= " + GetSQLDate(params.BeginDate)
            + "\n" + "                   AND " + openDateAlias + "  <= " + GetSQLDate(params.EndDate)
            );

        const filterOpenOnDate =  openDateAlias + "  <= " + GetSQLDate(params.EndDate)
            + "\n" + "             AND (   a.t_isOpened = 1"
            + "\n" + "                  OR " + closeDateAlias + " > " + GetSQLDate(params.EndDate) + ")";

        const filterClose = ternary(params.BeginDate == params.EndDate,
                     "                       " + closeDateAlias + " = "  + GetSQLDate(params.EndDate),
                     "                       " + closeDateAlias + " >= " + GetSQLDate(params.BeginDate)
            + "\n" + "                   AND " + closeDateAlias + " <= " + GetSQLDate(params.EndDate)
            + "\n" + "                   AND a.t_isOpened = 0"
            );
        if  ( params.ReportKind == RegBook ) // по этой форме всегда params.NeedAccOpen = true
            if( params.NeedAccOpen and params.NeedAccClose )
                filter =     "                   (("
                    + "\n" +                       filterOpen
                    + "\n" + "                    )"
                    + "\n" + "                    OR"
                    + "\n" + "                    ("
                    + "\n" +                       filterClose
                    + "\n" + "                   ))";
            else    /* только Open (открытые в период). Ситуацию, когда ни одного флага, проверили в начале */
                filter = filterOpen;
            end;
        elif( params.ReportKind == OpenCloseList )
            if(   params.NeedAccOpen )
                if( params.NeedAccClose )
                    /* Открытые хотя бы 1 день в течение периода */
                    filter =     "                   (("
                        + "\n" +                       filterOpen
                        + "\n" + "                    )"
                        + "\n" + "                    OR"
                        + "\n" + "                    ("
                        + "\n" +                       filterClose
                        + "\n" + "                   ))";
                else
                    filter = filterOpen;
                end;
            elif( params.NeedAccClose )
                filter = filterClose;
            end;
        elif (params.ReportKind == RegBookOnDate)
            filter =     "                   ("
                + "\n" +                       filterOpenOnDate
                + "\n" + "                   )"
        else
            RunError( "Ошибка программирования: некорректный вид отчёта" );
        end;

        return filter;
    end;

    private class auxPairLink()
        private var one;
        private var manyText;

        macro add(_one, _many)
            var i = 0;
            var haveOne = false;

            while(i < one.size)
                if (_one == one[i])
                    manyText[i] = manyText[i] + ", " + string(_many);
                    haveOne = true;
                end;
                i = i + 1;
            end;

            if (not haveOne)
                one[one.size] = _one;
                manyText[manyText.size] = string(_many);
            end;
        end;

        macro constructorAuxPairLink()
            one = TArray();
            manyText = TArray();
        end;

        constructorAuxPairLink();
    end;

    private class (auxPairLink) planChaptersLink()
        macro plan()
            return one;
        end;

        macro chaptersText()
            return manyText;
        end;

        macro constructorPlanChaptersLink()
            initAuxPairLink();
        end;

        constructorPlanChaptersLink();
    end;

    private macro getChapterPlanFilterText(planNumAlias, chapterAlias)
        var filter = "";

        filter = "(" +     planNumAlias + " = " + params.getPlanNum()
        + "\n" + " AND " + getChapterFilterSqlClause(params.getPlanNum(), params.chapter, chapterAlias)
        + "\n" + ")";

        return filter;
    end;

    var cmd =    "INSERT INTO " + TempTableName
        + "\n" + "   (t_Account, t_AccountCb, t_Balance, t_Code_Currency, t_Chapter,"
        + "\n" + "    t_Open_Date, t_Close_Date, t_Client, t_NameAccount, t_OpenGround, t_CloseGround,"
        + "\n" + "    t_ContractNumber, t_ContractDate, t_ExtractPeriod,"
        + "\n" + "    t_TaxMsgOpenDate, t_TaxMsgCloseDate, t_backOffice, t_AccountType, t_Name_Account)"
        + "\n" + "WITH acc AS("
        + "\n" + "            SELECT /*+ INDEX_JOIN(a)*/"
        + "\n" + "                   a.t_Chapter                             t_Chapter,"
        + "\n" + "                   b.t_Balance                             t_Balance,"
        + "\n" + "                   b.t_name_part                           t_name_part,"
        + "\n" + "                   a.t_Account                             t_Account,"
        + "\n" + "                   a.t_fiId                                t_Code_Currency,"
        + "\n" + "                   a.t_openDate                            t_Open_Date,"
        + "\n" + "                   a.t_closeDate                           t_Close_Date,"
        + "\n" + "                   a.t_partyId                             t_Client,"
        + "\n" + "                   a.t_Branch                              t_Branch,"
        + "\n" + "                   a.t_type                                t_type_account,"
        + "\n" + "                   a.t_name                                t_NameAccount,"
        + "\n" + "                   a.t_serviceContractNumber               t_contractNumber,"
        + "\n" + "                   a.t_serviceContractDate                 t_contractDate,"
        + "\n" + "                   TO_CHAR(a.t_chapter, 'FM0x')"
        + "\n" + "                   || TO_CHAR(a.t_fiId, 'FM0xxxxxx')"
        + "\n" + "                   || a.t_account                          documentId,"
        + "\n" + "                   CASE"
        + "\n" + "                       WHEN (" + ConvertMaskToSQLFormat(МаскаСчетовКлиентов, "a.t_Balance") + " AND (rsb_rep_pt.is_our_bank (a.t_partyId, " + topId + ") = 0))"
        + "\n" + "                       THEN " + СчетаКлиентов
        + "\n" + "                       ELSE " + СчетаБанка
        + "\n" + "                   END t_AccountType"
        + "\n" + "              FROM " + accountTableName + " a,"
        + "\n" + "                   dbalance_dbt b"
        + "\n" + "             WHERE"
        + "\n" +                     getDateFilterText("a.t_openDate", "a.t_closeDate")
                                     /* фильтр по маске счета */
               +                     ternary(params.accountMask == "", "",
          "\n" + "                   AND (" + ConvertMaskToSQLFormat(params.AccountMask, "a.t_Account") + ")"
                                     )
                                     /* фильтр по валюте */
               +                     ternary(   (params.CurrencyId == ALLFININSTR)
                                             or (params.CurrencyId == ALLCURRENCY), "",
          "\n" + "                   AND a.t_fiId   = " + params.CurrencyId
                                     )
                                     /* СчетаБанка */
               +                     ternary(params.AccountType == СчетаБанка,
          "\n" + "                   AND (   NOT(" + ConvertMaskToSQLFormat(МаскаСчетовКлиентов, "a.t_Balance") + ")"
          "\n" + "                        OR (rsb_rep_pt.is_our_bank(a.t_partyId, " + topId + ") = 1))"
                                     , ""
                                     )
                                     /* СчетаКлиентов */
               +                     ternary(params.AccountType == СчетаКлиентов,
          "\n" + "                   AND (" + ConvertMaskToSQLFormat( МаскаСчетовКлиентов, "a.t_Balance") + ")"
          "\n" + "                   AND (rsb_rep_pt.is_our_bank(a.t_partyId, " + topId + ") = 0)"
                                     , ""
                                     )
        + "\n" + "                   AND " + params.AccDprtFilter.GetAsSqlString( "a" )
                                     /* Кроме ОВП */
               +                     ternary(FlagOfPrintAccountsOCP, "",
          "\n" + "                   AND NOT " + params.ocpAccountServer.getAsSqlString("a")
                                     )
        + "\n" + "                   AND"
        + "\n" +                     getChapterPlanFilterText("b.t_iNumPlan", "b.t_Chapter")
        + "\n" + "                   AND INSTR(b.t_type_balance, 'T') = 0"
                                     /* связи */
        + "\n" + "                   AND a.t_Chapter = b.t_Chapter"
        + "\n" + "                   AND a.t_balance = b.t_balance"
                                     /* Проверка, что в Loans и Retail нет таких счетов */
               +                     ternary(params.Subsystem == REP_SUBSYSTEMS_CB, "",
          "\n" + "                   AND NOT EXISTS(SELECT NULL"
        + "\n" + "                                    FROM dbk_opacc_tmp dt"
        + "\n" + "                                   WHERE dt.t_accountCb = a.t_account"
        + "\n" + "                                     AND dt.t_backoffice IN (" + REP_SUBSYSTEMS_RETAIL + ", " + REP_SUBSYSTEMS_LOANS + ", " + REP_SUBSYSTEMS_DEPOSITS + "))"
                                     )
        + "\n" + "            )"
        + "\n" + "SELECT a.t_Account,"
        + "\n" + "       a.t_Account,"
        + "\n" + "       a.t_Balance,"
        + "\n" + "       a.t_Code_Currency,"
        + "\n" + "       a.t_Chapter,"
        + "\n" + "       a.t_Open_Date,"
        + "\n" + "       a.t_Close_Date,"
        + "\n" + "       CASE"
        + "\n" + "           WHEN ((a.t_AccountType = " + СчетаКлиентов + ") OR (rsb_rep_pt.is_our_bank (a.t_Client, " + topId + ") <> 0))"
        + "\n" + "               THEN a.t_Client"
        + "\n" + "           ELSE"
        + "\n" + "               (SELECT t_PartyId"
        + "\n" + "                             FROM ddp_dep_dbt"
        + "\n" + "                 WHERE t_Code = a.t_Branch)"
        + "\n" + "       END t_Client,"
        + "\n" + "       NVL(rep_note.getTextAccountNote(a.documentId, " + NOTEKIND_PURP_ACC + ", " + GetSqlDate(params.EndDate) + "),"
        + "\n" + "           CASE"
        + "\n" + "               WHEN a.t_AccountType = " + СчетаКлиентов
        + "\n" + "                   THEN NVL("
        + "\n" + "                            (SELECT oa.t_FullName"
        + "\n" + "                               FROM dobjattr_dbt oa, dobjatcor_dbt oac"
        + "\n" + "                              WHERE oac.t_ObjectType = oa.t_ObjectType"
        + "\n" + "                                AND oac.t_GroupId = oa.t_GroupId"
        + "\n" + "                                AND oac.t_AttrId = oa.t_AttrId"
        + "\n" + "                                AND oac.t_ObjectType = " + OBJTYPE_ACCOUNT
        + "\n" + "                                AND oac.t_GroupId = " + OBJ_ACCOUNT_GROUP_KIND
        + "\n" + "                                AND oac.t_General = 'X'"
        + "\n" + "                                AND " + GetSqlDate( params.EndDate ) + " BETWEEN oac.t_ValidFromDate AND oac.t_ValidToDate"
        + "\n" + "                                AND oac.t_Object = a.documentId"
        + "\n" + "                            ),"
        + "\n" + "                            a.t_NameAccount"
        + "\n" + "                        )"
        + "\n" + "               ELSE"
        + "\n" + "                   a.t_NameAccount"
        + "\n" + "           END"
        + "\n" + "       ) t_NameAccount,"
        + "\n" + "       NVL(rep_note.getTextAccountNote(a.documentId, " + NOTEKIND_GROUND_OPEN + ", " + GetSqlDate(params.EndDate) + "),  " + nullStr + ") t_OpenGround,"
        + "\n" + "       NVL(rep_note.getTextAccountNote(a.documentId, " + NOTEKIND_GROUND_CLOSE + ", " + GetSqlDate(params.EndDate) + "), " + nullStr + ") t_CloseGround,"
        + "\n" + "       NVL(t_contractNumber, " + nullStr + "),"
        + "\n" + "       NVL(t_contractDate, " + nullDate + "),"
        + "\n" + "       NVL (rep_note.getTextAccountNote(a.documentId, " + NOTEKIND_PERIODICITY + ", " + GetSqlDate(params.EndDate) + "), " + GetSqlString(ПериодичностьВыдачиВыписок) + ") t_extractPeriod,"
        + "\n" + "       CASE "
        + "\n" + "           WHEN ((INSTR(a.t_type_account, 'L') = 0) AND (t_accountType = " + СчетаКлиентов + "))"
        + "\n" + "               THEN "
        + "\n" + "                   DECODE (" + TaxMsgFunc + "(" + REQOPENA + ", a.t_Account, a.t_Chapter, a.t_Code_Currency, 0), " + nullDate + ", a.t_Open_Date,"
        + "\n" + "                           " + TaxMsgFunc + "(" + REQOPENA + ", a.t_Account, a.t_Chapter, a.t_Code_Currency, 0))"
        + "\n" + "               ELSE " + nullDate
        + "\n" + "       END  t_TaxMsgOpenDate,"
        + "\n" + "       CASE "
        + "\n" + "           WHEN ((INSTR(a.t_type_account, 'L') = 0) AND (t_accountType = " + СчетаКлиентов + "))"
        + "\n" + "               THEN "
        + "\n" + "       CASE"
        + "\n" + "                       WHEN ((a.t_Close_Date >= " + GetSQLDate(params.BeginDate) + ") AND (a.t_Close_Date <= " + GetSQLDate(params.EndDate) + "))"
        + "\n" + "                           THEN  DECODE (" + TaxMsgFunc + "(" + REQCLOSEA + ", a.t_Account, a.t_Chapter, a.t_Code_Currency, 0), " + nullDate + ", a.t_Close_Date,"
        + "\n" + "                                         " + TaxMsgFunc + "(" + REQCLOSEA + ", a.t_Account, a.t_Chapter, a.t_Code_Currency, 0))"
        + "\n" + "                           ELSE a.t_close_date"
        + "\n" + "                   END "
        + "\n" + "               ELSE " + nullDate
        + "\n" + "       END  t_TaxMsgCloseDate,"
        + "\n" + "       " + REP_SUBSYSTEMS_CB + " t_backOffice,"
        + "\n" + "       a.t_AccountType t_AccountType,"
        + "\n" + "       a.t_name_part"
        + "\n" + "  FROM acc a" ;

    SQL_Execute( cmd, "Отбор лицевых счетов по Главной книге" );

    return true;
END;

/* Расчёт и занесение во временный файл по ГК */
private MACRO CalculateCb( params )
    var stat = true;

    Message( "Расчёт по данным Главной книги" );

    stat = GetAccData(params);

    return stat;
END;

/* Расчёт и занесение во временный файл */
MACRO Calculate( params )
    if( not params.NeedAccOpen and not params.NeedAccClose )
        /* Всё нормально, просто ничто не подходит. */
        return true;
    end;

    if(   params.Subsystem == REP_SUBSYSTEMS_ALL )
        return ExecMacroFile( "bk_opacc_Retail",   "CalculateRetail",   params, TempTableName )
           and ExecMacroFile( "bk_opacc_Loans",    "CalculateLoans",    params, TempTableName )
           and ExecMacroFile( "bk_opacc_Deposits", "calculateDeposits", params, TempTableName )
           and CalculateCb( params );
    elif( params.Subsystem == REP_SUBSYSTEMS_CB )
        return CalculateCb( params );
    elif( params.Subsystem == REP_SUBSYSTEMS_RETAIL )
        return ExecMacroFile( "bk_opacc_Retail", "CalculateRetail", params, TempTableName );
    elif( params.Subsystem == REP_SUBSYSTEMS_LOANS )
        return ExecMacroFile( "bk_opacc_Loans", "CalculateLoans", params, TempTableName );
    elif( params.Subsystem == REP_SUBSYSTEMS_DEPOSITS )
        return ExecMacroFile( "bk_opacc_Deposits", "calculateDeposits", params, TempTableName );
    else
        msgbox("Ошибка! Неизвестная подсистема!");
        return false;
    end;
END;

/* Печать отчёта. prForm - печатная форма (TPrintForm) */
private MACRO PrintReportWithForm( prForm, params )

    var result = false;
    var hasData = false;

    message("Печать отчёта");

    var output = TOFStream("bk_opacc_rep");
    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    // Проверим, нашли ли что-нибудь
    /*23 Mar 07 Malakhova Irina 103782*/
    var queryText = "";
    var data;

    queryText = "SELECT CASE WHEN EXISTS(SELECT NULL FROM " + TempTableName + ")"
       + "\n" + "            THEN 1"
       + "\n" + "            ELSE 0"
       + "\n" + "       END AS t_hasRecords"
       + "\n" + " FROM DUAL";

    data = TRsbDataset(queryText);
    data.next();
    hasData = (data.hasRecords != 0);

    if (    (not hasData)
        and (   params.NeedEveryDay
             or (params.accountType == ВсеСчета)
             or (    (params.ReportKind == OpenCloseList)
                 and (params.NeedAccOpen == params.NeedAccClose)
                )
            )
       )
        msgBox("Нет данных для отчета");
        println("Данные отсутствуют");
        result =  false;
    else
        result = prForm.print(params, hasData);
    end;

    output.resetOutputFile();
    output.show();

    return result;

END;

/******************************************************************************/
/* Основная функция для Внутренней отчётности - вызывается из панели */
MACRO Report( repKind, subsystem, dprtId, orgStructure, issueMode, begDate, endDate, archDate,
    chapter, accType, currId, needAccOpen, needAccClose, needEveryDay,
    needPrintFunds,isWeb
)
    /* Инициализация фильтра счетов по ТС/РС */
    var dprtList      = RepDepartmentList( orgStructure, issueMode, dprtId );
    var accFilter     = RepAccountFilter( dprtList );
    var brnFilter     = RepBranchFieldFilter( dprtList );
    var brnDprtFilter = RepBranchAndDepartmentFieldFilter( dprtList );
    var ocpAccServer  = RepOcpAccountServer(chapter, currId, dprtList);

    /* инициализация dRepAccount_vw*/
    sql_execute("CALL rep_data.setBeginDate(" + getSqlDate(begDate) + ")");
    sql_execute("CALL rep_data.setEndDate("   + getSqlDate(endDate)   + ")");
    sql_truncate("drepdepartment_tmp");
    sql_execute("CALL rep_data.setDepartmentCode(" + getSqlString(dprtList.getAsSqlSetFilial()) + ", "
                                                   + getSqlString(dprtList.getAsSqlSetVsp()) + ")");

    sql_execute("CALL rsvoxprm.setPrmVal('BeginDate', UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(begDate)+", 'DD.MM.YYYY')))");
    sql_execute("CALL rsvoxprm.setPrmVal('EndDate',   UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(endDate)+", 'DD.MM.YYYY')))");

    /* Контроль опердней */
    if( not RepOperdaysOpened( dprtList, begDate, endDate ).ShouldContinue )
        return false;
    end;

    params = TParams( ВО, repKind, subsystem, accFilter, brnFilter, brnDprtFilter, ocpAccServer, begDate,
                      endDate, archDate, chapter, accType, currId, "", needAccOpen,
                      needAccClose, needEveryDay, needPrintFunds );

    if(   (params.ReportKind == RegBook) or (params.ReportKind == RegBookOnDate) )
        prForm = TPrintForm( "bk_opacc_print.mac", "PrintAccRegBook" );
    elif( params.ReportKind == OpenCloseList )
        prForm = TPrintForm( "jrnacc02.mac", "PrintAccOpenCloseList" );
    else
        return false;
    end;

    var stat = Initialize( params );
    if( stat )
        stat = Calculate( params );
        if ((stat) and (isWeb))
            stat = PrintReportWithForm(prForm, params);
        end;
//        Uninitialize( params );
    end;

    return stat;
END; /* Report */

/******************************************************************************/
/* Основная функция для Внутренней отчётности - вызывается из веб интерфейса */
MACRO ibrCreateReport(repKind, subsystem, dprtId, orgStructure, issueMode, begDate, endDate, archDate,
                         chapter, accType, currId, needAccOpen, needAccClose, needEveryDay, needPrintFunds,isWeb)
    var stat = Report(repKind, subsystem, dprtId, orgStructure, issueMode, begDate, endDate, archDate,
                      chapter, accType, currId, needAccOpen, needAccClose, needEveryDay, needPrintFunds,isWeb);
    return stat;
end;

/******************************************************************************/
/* Основная функция для Отчётности СБ РФ - вызывается из панели */
MACRO ReportSb( repKind, subsystem, dprtId, orgStructure, issueMode, begDate, endDate,
    archDate, chapter, accType, currId, accountMask, needAccOpen, needAccClose
)
    /* Инициализация фильтра счетов по ТС/РС */
    var dprtList      = RepDepartmentList( orgStructure, issueMode, dprtId );
    var accFilter     = RepAccountFilter( dprtList );
    var brnFilter     = RepBranchFieldFilter( dprtList );
    var brnDprtFilter = RepBranchAndDepartmentFieldFilter( dprtList );
    var ocpAccServer  = RepOcpAccountServer(chapter, currId, dprtList);

    /* инициализация dRepAccount_vw*/
    sql_execute("CALL rep_data.setBeginDate(" + getSqlDate(begDate) + ")");
    sql_execute("CALL rep_data.setEndDate("   + getSqlDate(endDate)   + ")");
    sql_truncate("drepdepartment_tmp");
    sql_execute("CALL rep_data.setDepartmentCode(" + getSqlString(dprtList.getAsSqlSetFilial()) + ", "
                                                   + getSqlString(dprtList.getAsSqlSetVsp()) + ")");

    sql_execute("CALL rsvoxprm.setPrmVal('BeginDate', UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(begDate)+", 'DD.MM.YYYY')))");
    sql_execute("CALL rsvoxprm.setPrmVal('EndDate',   UTL_RAW.CAST_TO_RAW(TO_CHAR("+getSqlDate(endDate)+", 'DD.MM.YYYY')))");

    /* Контроль опердней */
    if( not RepOperdaysOpened( dprtList, begDate, endDate ).ShouldContinue )
        return false;
    end;

    var params = TParams( ОСБ, repKind, subsystem, accFilter, brnFilter, brnDprtFilter, ocpAccServer, begDate,
                          endDate, archDate, chapter, accType, currId, accountMask, needAccOpen,
                          needAccClose );

    var mfile;
    if(   (params.ReportKind == RegBook) or (params.ReportKind == RegBookOnDate) )
        mfile = "SbAccBookPrint.mac";
    elif( params.ReportKind == OpenCloseList )
        mfile = "SbAccOCListPrint.mac";
    else
        return false;
    end;

    var prForm = TPrintForm( mfile, "PrintReport" );

    var stat = Initialize( params );
    if( stat )
        stat = Calculate( params );
        if ( stat )
            stat = PrintReportWithForm( prForm, params );
        end;
        Uninitialize( params );
    end;

    return stat;
END; /* Report */

// Вывод отчета на экран (пока только для отчета ВО)
macro showReport()
    return PrintReportWithForm( prForm, params );
end;
