/*
$Name:        lib_path.mac
$Module:      Регламентируемая отчетность
$Description: Библиотека для работы с именами файлов и каталогов на RSL
*/
/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10.                                          R-Style Software Lab

  Библиотека для работы с именами файлов и каталогов на RSL

FILENAME: LIB_PATH.MAC
OLDFILENAME: PATH_LBR.MAC
CREATED: 28.01.99 Мол.
EXPORTS:

  FileFromPath( s)
    Извлекает "имя файла + расширение" из полного пути

  DirFromPath( s)
    Извлекает путь без имени файла из полного пути

  Dir_Chomp( s)
    Отрезает концевой символ '\' из пути

  GetWorkingDirString( RegId)
    Опрделяет путь к рабочему каталогу (WORKDIR, TEXTDIR или MACRODIR)

MODIFICATIONS:
  10.03.00 Мол. Добавлены функции для получения информации о каталогах

NOTES:

└───────────────────────────────────────────────────────────────────────────*/
IMPORT BankInter;
import rsexts;
import bnk_dttmfrmt;
import rep_lib;

/* Извлекает "имя файла + расширение" из полного пути */
MACRO FileFromPath( s)
var
   c, f = "", i = strlen( s);

  while ( i > 0 )
    c = SubStr( s, i, 1);

    if ( (c == ":") or (c == "\\") or (c == "/") )
      return f;
    end;

    f = c + f;
    i = i - 1;
  end;

  return f;
END;

/* Извлекает путь без имени файла из полного пути */
MACRO DirFromPath( s)
var
   lf = strlen( FileFromPath( s)),
   ls = strlen( s);

  return SubStr( s, 1, ls-lf);
END;

/* Отрезает концевой символ '\' из пути */
MACRO Dir_Chomp( s)
var
   l = strlen( s), c = SubStr( s, l, 1);

  if ( c == "\\" )
    s = SubStr( s, 1, l-1);
  end;

  return s;
END;

MACRO GetWorkingDirString( RegId)
var
   err, Str ="", i, RegName;

  RegName = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\" + RegId;
  GetRegistryValue( RegName, V_STRING, Str, err );

  if ( ( err  ==  7 ) or ( valtype( Str ) != V_STRING ) or ( Str == "" ) )
    Str = GetIniString( RegId );
    if ( ( valtype( Str ) != V_STRING ) or ( Str == "" ) )
      msgbox( "Не определен каталог " + RegId + " в настройках банка" );
      exit(1);
    end;
  end;

  if ( RegId == "MACRODIR" )
    i = index( Str, ";");

    if ( i )
      Str = substr( Str, 1, i-1);
    end;
  end;

  return Str;
END;

MACRO ReturnDirString( RegId, StrDef )
var
   err, Str ="", i, RegName;

  RegName = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\" + RegId;
  GetRegistryValue( RegName, V_STRING, Str, err );

  if ( ( err  ==  7 ) or ( valtype( Str ) != V_STRING ) or ( Str == "" ) )
    Str = GetIniString( RegId );
    if ( ( valtype( Str ) != V_STRING ) or ( Str == "" ) )
      msgbox( "Не определен каталог " + RegId + " в настройках банка" );
      exit(1);
    end;
  end;

  Str = Dir_Chomp(Str) + "\\";

  return Str;
END;

private macro str2Array(str: string, separator: string): TArray
   var resultArr = TArray();

   while (index(str, separator))
      resultArr[resultArr.size] = trim(SubStr(str, 1, index(str, separator) - 1));
      str = SubStr(str, index(str, separator) + 1);
   end;
   resultArr[resultArr.size] = trim(str);
   return resultArr;
end;

class TToolsPathOfFile()
   const SERV = 0;
   const TERM = 1;
   private const SLASH            = "\\";
   private const DISK_PATH_SUFFIX = ":" + SLASH;
   private const MIN_SYMB_IN_PATH = /*disk label*/1 + strlen(DISK_PATH_SUFFIX);

   // обрезание пути, в том числе и от концевых слэшей
   macro trimPath(path: string): string
      path = trim(path);
      while ((SubStr(path, strlen(path)) == SLASH) and
             (SubStr(path, strlen(path)-1) != DISK_PATH_SUFFIX))
         path = trim(SubStr(path, 1, strlen(path) - 1));
      end;
      return path;
   end;

   // получение пути на уровень выше
   macro getLevelUpPath(path: string): string
      while((SubStr(path, strlen(path)) != SLASH) and (strlen(path) > MIN_SYMB_IN_PATH))
         path = SubStr(path, 1, strlen(path) - 1);
      end;
      path = trimPath(path);
      if (".." == path) // если путь был относительным
         return "";     // добрались до корня
      end;
      return path;
   end;

   // получение корневого каталога на сервере или терминале
   macro getRootPath(kindPath: integer): string
      var dirStr = getCurDir(SERV != kindPath);
      if(isStandalone() or (SERV == kindPath))
         dirStr = getLevelUpPath(dirStr);
      end;
      return trimPath(dirStr);
   end;

   // получение абсолютного пути по относительному
   macro getAbsolutePath(path: string, kindPath: integer): string
      if (SubStr(path, 1, 1) == "$") path = SubStr(path, 2); end;
      if (index(path, DISK_PATH_SUFFIX))
         return path;
      end;

      var afterRootParts = str2Array(path, SLASH);
      var lvlUp = false;

      path = "";
      for (var item, afterRootParts)
         if (lvlUp and (".." == item))
            path = getLevelUpPath(path);
            lvlUp = false;
         else
            if (path and ("\\" != SubStr(path, StrLen(path), StrLen(path))))
               path = path + "\\";
            end;
            path = path + item;
         end;

         if (".." == item)
            lvlUp = true;
         end;
      end;
      return getRootPath(kindPath) + SLASH + StrSubst(path, "..\\", "");
   end;

   // получение аналогичного пути на терминале по пути на сервере
   macro getTerminalPathByServerPath(path: string): string
      return StrSubst(path, getRootPath(SERV), getRootPath(TERM));
   end;

   // получение аналогичного пути на сервере по пути на терминале
   macro getServerPathByTerminalPath(path: string): string
      return StrSubst(path, getRootPath(TERM), getRootPath(SERV));
   end;

   // получение уникального суффикса: дата + время + сессия
   macro getUniqueSuffix(): string
      return DateToStr_YYYYMMDD(Date(),"") + StrSubst(StrSubst(repLZ(trim(string(Time():m)), 11), ":", ""), ".", "") + userNumber;
   end;

   // создание пути в случае его отсуствия
   macro createPathIfNone(path: string)
      path = trimPath(getAbsolutePath(path));

      if (not isStandalone() and (SubStr(path, 1, 1) != "$"))
         path = "$" + path;// для трёхзвенки
      end;

      if (not GetFileInfo(path))
         var dirArr = str2Array(path, SLASH);
         path = dirArr[0] + SLASH;
         var ind = 1;
         while (ind < dirArr.size)
            if (not GetFileInfo(path)) MakeDir(path); end;
            if (strlen(path) == MIN_SYMB_IN_PATH) path = path + dirArr[ind];
            else                                  path = path + SLASH + dirArr[ind];
            end;
            ind = ind + 1;
         end;
         if (not GetFileInfo(path)) MakeDir(path); end;
      end;
   end;

   // копирование файла с сервера на терминал
   macro copyFileFromServerToTerminal(serverPath: string, terminalPath: string): bool
      serverPath = getAbsolutePath(serverPath, SERV);
      if (not terminalPath)
         terminalPath = "$" + getTerminalPathByServerPath(serverPath);
      end;
      if (SubStr(terminalPath, 1, 1) != "$")
         terminalPath = "$" + terminalPath;
      end;
      createPathIfNone(getAbsolutePath(getLevelUpPath(terminalPath)));
      return CopyFile(serverPath, terminalPath);
   end;

   // копирование файла с терминала на сервер
   macro copyFileFromTerminalToServer(terminalPath: string, serverPath: string): bool
      terminalPath = getAbsolutePath(terminalPath, TERM);
      if (not serverPath)
         serverPath = getServerPathByTerminalPath(terminalPath);
      end;
      createPathIfNone(getAbsolutePath(getLevelUpPath(serverPath)));
      return CopyFile("$" + terminalPath, serverPath);
   end;

   // создание файла на терминале
   macro createFileOnTerminal(fullFileName: string /* путь к файлу указывается относительно корня терминала */, fileContent: string)

      macro makeTemporaryFileOnServer(fileName: string)
         var newFile = TStreamDoc(GetTxtFileName(fileName), "C");
         newFile.WriteLine(fileContent);
         newFile.flush();
      end;

      var name, extension;
      var path = splitFile(fullFileName, name, extension);
      var fileName = name + extension;

      makeTemporaryFileOnServer(fileName);

      if (not copyFileFromServerToTerminal(GetTxtFileName(fileName), path + fileName))
         RunError(GenClassName(this) + "::" + GetCallStack()[0] + ": ошибка копирования файла на терминал");
      end;
   end;
end; // TToolsPathOfFile

/* eof */