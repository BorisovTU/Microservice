/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                            R-Style Software Lab

  Справка о постановке на учет в налоговом органе.

CREATED:  7.09.98 Стасевич
COMMENT:
      Для выпуска отчета необходимо наличие в одной из директорий
  макрофайлов файла настройки dstnalog.txt.
      При выпуске отчета анализируется структура файла настройки.
  При этом каждая строка имеет следующий формат -
  "Номер балансового счета" + "Специальный символ".
  "Специальный символ" показывает цель открытия счета на данном
  балансовом , например :
  "С" - ссудный счет
  "Р" - расчетный счет
  Счета, открытые на балансовых не указаных в файле настройки,
  игнорируются.

MODIFICATIONS :
  28.09.98 Сабитов
    Теперь можно настраивать названия целей открытия счетов и
    соответствующие "Специальные символы". Комментарии по этому
    поводу смотрите чуть ниже по тексту
└───────────────────────────────────────────────────────────────────────────*/
IMPORT BankInter,param,lib_path,"adress.mac";

ARRAY МассивЦельСчета;
/*--28.09.98-Сабитов-----------------------------------------------------------
                     НАСТРОЙКА МАССИВА ЦЕЛЕЙ СЧЕТОВ
  Настройка массива целей счетов происходит по принципу :
        - Четные номера элементов массива - "Специальный символ", определяющий
          цель счета в файле dstnalog.txt
        - Следующий за четным элемент массива - название цели счета

        Например, для того, чтобы определить цель счета "Корреспондентский"
        и "Специальный символ" для него - "К", необходимо дописать строку:

        МассивЦельСчета(N)="К";МассивЦельСчета(M)="Корреспондентский";

        ,где N=Номер_последнего_определенного_элемента_массива + 1
             M=N+1,
        причем N - четное число , а M - нечетное ;
-----------------------------------------------------------------------------*/

      МассивЦельСчета(0)="С";МассивЦельСчета(1)="Ссудный";
      МассивЦельСчета(2)="Р";МассивЦельСчета(3)="Расчетный";


FILE Лицевые( account );
FILE ЛицБал( accblnc );
FILE Клиенты(party);
/*FILE tax( taxrecvs );*/
FILE Данные() txt;

VAR НачДата, КонДата, ДатаОтчета_, НомерИзвещения, ЦельСчета,
    MaskAccount, БылиСчета;

VAR DataFileName = FindPath( "dstnalog.txt" );

MACRO ПечатьИзвещения( Лицевой )
 ARRAY inn;
 record RecordAdress ( adress );
 ClearRecord(RecordAdress);
 НайтиЮридическийАдресСубъекта(Клиенты.PartyID,RecordAdress);

[                                                                               ];
[                                                                  Приложение N1];
[                                                 к Порядку применения положений];
[                                          Указа Президента Российской Федерации];
[                                                        от 23 мая 1994 г. N1006];
[                                                                               ];
[ Госналогинспекция по___________________________                               ];
[                        (наименование,                                         ];
[ _______________________________________________                               ];
[       почтовый индекс,адрес,телефон)                                          ];
[ "__"_____________________________________199__г.                              ];
[                                                                               ];
[ N______________________________________________                               ];
[                                                                               ];
[                                                                               ];
[                                   Справка                                     ];
[              о постановке на учет в налоговом органе юридического             ];
[                 лица, его филиала, представительства и другого                ];
[                         обособленного подразделения                           ];
[                                                                               ];
[                                                                               ];
[ Предприятие__________________________________________________________________ ];
[                  (наименование предприятия(учреждения,организации),           ];
[                                                                               ];
[ _____________________________________________________________________________ ];
[                   местонахождение(почтовый индекс,адрес,телефон))             ];
[                                           ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐];
[ идентификационный номер налогоплательщика │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │ │];
[                                           └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘];
[ находится на налоговом учете.                                                 ];
[                                                                               ];
[ В соответствии с  Указом Президента  Российской Федерации  от  23 мая 1994  г.];
[ № 1006 "Об осуществлении комплексных мер по своевременному и полному внесению ];
[ в бюджет налогов и иных обязательных платежей"  представляется по письменному ];
[ заявлению предприятия, на которое в соответствии с законодательством возложена];
[ обязанность уплачивать  налоги  и  иные  обязательные платежи, для открытия в ];
[ банке или ином кредитном учреждении расчетного (текущего) счета для           ];
[ осуществления операций по основной деятельности или корреспондентского счета в];
[ РКЦ Банка России, в других банках или иных кредитных учреждениях.             ];
[                                                                               ];
[    Место гербовой                 Начальник государственной                   ];
[       печати                       налоговой инспекции____________________    ];
[                                                         (Ф.И.О.,подпись)      ];
[                                                                               ];
[                                                                               ];
[───────────────────────────────────────────────────────────────────────────────];
[ Заполняется банком (кредитным учреждением) или РКЦ Банка России               ];
[                                                                               ];
[    I.Остается в банке (кредитном учреждении) или РКЦ Банка России             ];
[                                                                               ];
[ Открыт #                                          счет                        ]( ЦельСчета:l );
[        (расчетный(текущий) или корреспондентский)                             ];
[                                                                               ];
[ N #                          дата #                                           ]( Лицевой:l, Лицевые.Open_Date:l );
[                                   ( дата занесения банком или иным            ];
[                                     кредитным учреждением, РКЦ Банка          ];
[                                     России в книгу регистрации счетов         ];
[                                     хозорганов, банков)                       ];
[ Отправлено извещение в                                                        ];
[ Госналогинспекцию                                                             ];
[ по #                         #                         #                     #]( RecordAdress.Place:l, RecordAdress.PostIndex:l, RecordAdress.Adress:l, RecordAdress.PhoneNumber:l );
[    (наименование,        почтовый индекс,             адрес,          телефон)];
[                                                                               ];
[ N #                               дата #                                      ]( НомерИзвещения:l, ДатаОтчета_:l );
[                                                                               ];
[ Руководитель банка(кредитного учреждения),                                    ];
[ РКЦ Банка России                 #                                            ]( {FIO_Boss}:l );
[                                  (Ф.И.О.,подпись)                             ];
[                                                                               ];
[ Главный бухгалтер                #                                            ]( {FIO_Book}:l );
[                                  (Ф.И.О.,подпись)                             ];
[                                                                               ];
[───────────────────────────────────────────────────────────────────────────────];
[                             Отрывная часть                                    ];
[    II.Направляется в налоговому инспекцию                                     ];
[                                                                               ];
[        Штамп банка                                               Госналогинспекция по #                                   ](  RecordAdress.Place:l );
[   (кредитного учреждения),                                                            (наименование,                      ];
[      РКЦ Банка России                                            #           #                                #           ](  RecordAdress.PostIndex:l,  RecordAdress.Adress:l,  RecordAdress.PhoneNumber:l );
[                                                                  почт.индекс,   адрес,                         телефон)   ];
[   #####################################################################################################################   ]( {Name_Bank}:w );
[   (наименование,                                почт.индекс,     #_______________________________________                 ]( ПочтовыйИндекс:l );
[   #                                       #                                   ]( {Post_Addr}:l, Номер_телефона:l );
[       адрес,                              телефон)                            ];
[                                                                               ];
[   _______________________________                                             ];

        /* Налогоплательщик */
  Клиенты.PartyID = Лицевые.Client;
  if (not getEQ( Клиенты ) )
     msgbox("Не найден клиент ", Клиенты.PartyID, " лицевого счета ", Лицевые.Account);
     exit(1);
  end;
/* 05.10.2007 Malakhova 110424*/
/*Заменила getPartyINN на repGetPartyINN*/
  StrSplit(repGetPartyINN(Клиенты.PartyId, ДатаОтчета_, 1), inn, 1, 1, 20 );

[                                                                               ];
[   #                                                                           ]( ДатаОтчета_:l );
[   N #                                                                         ]( НомерИзвещения:l );
[                                                                               ];
[                                  ИЗВЕЩЕНИЕ <*>                                ];
[ Доводим до Вашего сведения, что налогоплательщику_____________________________];
[ #                                #            #                        #      ]( Клиенты.Name:l, RecordAdress.PostIndex:l, RecordAdress.Adress:l, RecordAdress.PhoneNumber:l );
[ (наименование,местонахождение  (почт.индекс,      адрес,              телефон)];
[                                                                               ];
[                                           ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐];
[ идентификационный номер налогоплательщика │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│]( inn(0), inn(1), inn(2), inn(3), inn(4), inn(5), inn(6), inn(7), inn(8), inn(9), inn(10), inn(11), inn(12), inn(13), inn(14), inn(15), inn(16), inn(17), inn(18), inn(19) );
[                                           └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘];
[                                                                               ];
[                                                                               ];
[                                                                               ];
[ Открыт #                                  счет                                ]( ЦельСчета:l );
[        (расчетный(текущий) или корреспондентский)                             ];
[                                                                               ];
[ N #                          дата #                                           ]( Лицевой:l, Лицевые.Open_Date:l );
[                                   ( дата занесения банком или иным            ];
[                                     кредитным учреждением, РКЦ Банка          ];
[                                     России в книгу регистрации счетов         ];
[                                     хозорганов, банков)                       ];
[                                                                               ];
[ Руководитель банка(кредитного учреждения),                                    ];
[ РКЦ Банка России                 #                                            ]( {FIO_Boss}:l );
[                                  (Ф.И.О.,подпись)                             ];
[                                                                               ];
[ Главный бухгалтер                #                                            ]( {FIO_Book}:l );
[                                  (Ф.И.О.,подпись)                             ];
[                                                                               ];
[                                                                               ];
[                                                                               ];
[                                                                               ];
[       Место печати                                                            ];
[       ___________________________________________                             ];
[                                                                               ];
[       <*>Направляется банком или иным кредитным учреждением, РКЦ Банка России ];
[  не позднее 5 рабочих дней после дня открытия расчетного (текущего) счета в   ];
[  банке (кредитном учреждении) или корреспондентского счета в Банке России, в  ];
[  других банках или иных кредитных учреждениях.                                ];
[_______________________________________________________________________________];
[───────────────────────────────────────────────────────────────────────────────];
   БылиСчета = TRUE;
END;

macro CheckAccountMask( acc, mask )
   if ( (mask == "") or (CompareStrWithMasks( mask, acc ) == 0) ) return true;
   end;
   return false;
end;


MACRO ОпределитьЦельСчета (СпецСимвол)
   var i=1;
   while (not (i>Asize(МассивЦельСчета)))
      if (МассивЦельСчета(i-1)==СпецСимвол)
         return МассивЦельСчета (i);
      end;
      i=i+2;
   end;
   return "";
END;

MACRO ПодготовкаИзвещения( Лицевой )
  Лицевые.Chapter = 1;
  Лицевые.Account = Лицевой;
  getEQ( Лицевые );

  Клиенты.PartyID = Лицевые.Client;
  getEQ( Клиенты );

        /* Налоговая инспекция */
  Клиенты.PartyID = Клиенты.TaxInstitution;
  getEQ( Клиенты );

/*
  tax.iTaxInstitution = Клиенты.TaxInstitution;
  getEQ( tax );
*/
  ПечатьИзвещения( Лицевой );
  НомерИзвещения = НомерИзвещения + 1;
END;

MACRO ОбработатьОдинСчет( Лицевой )
  rewind( Данные );

  Лицевые.Chapter = 1;
  Лицевые.Account = Лицевой;
  getEQ( Лицевые );

  while ( next( Данные ) )
    if( Trim(Данные(0)) == Лицевые.Balance )
      ЦельСчета = ОпределитьЦельСчета (Trim(Данные(1)));
      ПодготовкаИзвещения( Лицевой );
    end;
  end;
END;

MACRO ОбработатьБалансовый( Балансовый )
 VAR контр;
  ЛицБал.Balance0 = Балансовый;
  ЛицБал.Account  = "";
  ЛицБал.Chapter = 1;
  контр = getGE( ЛицБал ) and ( ЛицБал.Balance0 == Балансовый )
                          and ( ЛицБал.Chapter == 1 );

  while( контр )

    if ( CheckAccountMask( ЛицБал.Account, MaskAccount ) )

      Лицевые.Chapter = ЛицБал.Chapter;
      Лицевые.Account = ЛицБал.Account;
      if ( getEQ( Лицевые) and ( Лицевые.Open_Date >= НачДата )
                        and ( Лицевые.Open_Date <= КонДата ))
        ПодготовкаИзвещения( Лицевые.Account );
      end;
    end;
    контр = next( ЛицБал ) and ( ЛицБал.Balance0 == Балансовый )
                           and ( ЛицБал.Chapter == 1 );
  end;
END;

/* Функция вызывается из системного модуля*/
MACRO Отчет( BegDate, EndDate, Account, RepDate, BegNum )
VAR контр;

  if ( not Open( Данные, DataFileName) )
    MsgBox( "Ошибка открытия файла данных |" + DataFileName);
    Exit(1);
  end;

  НачДата = BegDate;
  КонДата = EndDate;
  ДатаОтчета_ = RepDate;
  НомерИзвещения = BegNum;

  БылиСчета = FALSE;
  MaskAccount = Account;

  if ( (Account != "" ) and (strbrk(Account, "*?") == 0) )  /* Маска? */
    ОбработатьОдинСчет( Account );
  else
    rewind( Данные );
    while( next( Данные ) )
      ЦельСчета = ОпределитьЦельСчета (Trim(Данные(1)));
      ОбработатьБалансовый( Trim(Данные(0)) );
      message( Данные(0));
    end;
  end;

  if ( not БылиСчета )
    MsgBox( "Нет данных для отчета");
    Exit(1);
  end;

END;

/*eof*/