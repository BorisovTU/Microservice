/*
$Name:          ibr_pr_blprs.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Отчет о блокировании/разблокировании пользователей"
*/
import IbrImport;
import rcbWebCommon;
import IbrCommon;
import pr_blprs;

private const TAB_REPORT = "Параметры отчета";
private const GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP;
private const LOG_FILE_NAME = "ibr_pr_blprs_web";
    
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade    = TIbrConstructorFacade();
    var report    = facade.createReport("ibr_pr_blprs", "Блокирование/разблокирование пользователей за период", "ibr_pr_blprs.mac", "ibrReportExecute", "ibrReportVerification");  

    var tabReport = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createNotGroup();

    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения", "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура", "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим", "Режим"));

    groupReport.add(facade.createFieldDate("НачалоПериода", "Период с :"));
    groupReport.add(facade.createFieldDate("КонецПериода",  "По :"));
    
    groupReport.add(facade.createFieldLlValues("ВидБлокировки", "Вид блокировки", "Все", 1846));
    groupReport.add(facade.createFieldUser("Операционист", "Операционист", 0));
    
    groupReport.add(facade.createFieldTypeAc("Сортировка", "Порядок сортировки", 180, "123", null, null, null, null, null, null, null, null, false));
    
    tabReport.add(groupReport);
    report.add(tabReport);
    
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    var isShowOcp = false;
    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );

    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+"Подразделения").valueInteger,
                                          getValue(report, gRep+"НачалоПериода").valueDate,
                                          getValue(report, gRep+"КонецПериода").valueDate,
                                          getValue(report, gRep+"Операционист").valueInteger,
                                          strTransferByWord(getValue(report, gRep+"Сортировка").valueString, 1),
                                          getValue(report, gRep+"ВидБлокировки").valueInteger,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          LOG_FILE_NAME);
    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(-1, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object
    const gRep = TAB_REPORT + "@" + GRP_REPORT + "@";
    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

    //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;
   
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;