/**
 *  RS-Bank 6.0                                           R-Style Software Lab
 *
 *  File Name   : cltnoacc.mac                                   March 28, 2006
 *  Programmer  : Ivkina
 *  Description : Клиенты без счетов
 *  Comment     :
 *  Modify      :
 */

import Reporting;
import cb_sql;
import FIInter;
import acv;
import rep_lib;
import ofstream;

private const ALLCURRENCY = -2;   /*Все ин валюты*/
private const width       = 111;  /*Ширина вывода шапки банка*/

private var   msg = "НЕТ КЛИЕНТОВ БЕЗ СЧЕТОВ"; /*Сообщение при выводе пустого отчета, меняется на "НЕТ ДАННЫХ ДЛЯ ОТЧЕТА"*/

var reportBodyFileName;
/**
 * Печать нулевой даты, т.к. есть возможность задания начала обслуживания клиента 00.00.0000
 */
private macro printDate(ddate)

    var sdate;
    var fakeDate;

    if (ddate == Date(0,0,0))
        fakeDate = string(Date(1):f);
        if ((index(fakeDate, ".") == 3) and (substr(fakeDate, 1, 1) == "0"))
            sdate = "00.00.0000";
        else
            sdate = "0.00.0000";
        end;
    else
        sdate = string(ddate:f);
    end;

    return sdate;

end;

/**
 * Класс с параметрами отчета
 */
private CLASS TCltNoAccInfo
(
    pDprtID,
    pOrgStructure,
    pIssueMode,
    pNumPlan,
    pChapter,
    pDateIn,
    pCodeCurrency
)

    var departmentList;                        /**/
    var accountFilter;                         /*фильтр счетов*/
    var partyFilter;                           /*фильтр по субъектам*/
    var departmentFilter;                      /*фильтр по филиалам */
    var branchAndDprtFilter;                   /*фильтр узлам ТС c */
    var dprtID           = pDprtID;            /*номер узла ТС*/
    var orgStructure     = pOrgStructure;      /*структура*/
    var issueMode        = pIssueMode;         /*режим выпуска отчета*/
    var numPlan          = pNumPlan;           /*номер плана*/
    var chapter          = pChapter;           /*глава счетов*/
    var dateIn           = pDateIn;            /*дата отчета*/
    var codeCurrency     = pCodeCurrency;      /*код валюты*/

    private macro Constructor()
        departmentList       = RepDepartmentList(orgStructure, issueMode, dprtID);
        accountFilter        = RepAccountFilter(departmentList);
        partyFilter          = RepPartyFilter(departmentList);
        departmentFilter     = RepDepartmentFieldFilter(departmentList);
        branchAndDprtFilter  = RepBranchAndDepartmentFieldFilter(departmentList);
    end;

    Constructor();
end;

/**
 * Печать заголовка отчета
 */
private macro printHeader(clientInfo)

    var PRB = TArray();
    var typeAccount = "";
    var i = 0;
    /* Разделитель */
    PRB[0] = "";
    PRB[1] = MkStr("─", width);

    /* Шапка банка */
    printBankHeaderBuf(clientInfo.dprtID, clientInfo.orgStructure, width, PRB);
    while (i < PRB.Size)
        println(PRB[i]);
        i = i+1;
    end;

    if (clientInfo.codeCurrency == NATCUR)
        typeAccount = "РУБЛЕВЫХ СЧЕТОВ"
    elif ( clientInfo.codeCurrency == ALLFININSTR )
        typeAccount = "СЧЕТОВ"
    else
        typeAccount = "ВАЛЮТНЫХ СЧЕТОВ"
    end;

    println();
    println(StrAlign("КЛИЕНТЫ, У КОТОРЫХ НЕТ " + typeAccount, width, STR_ALIGN_CENTER));
    println(StrAlign( string(clientInfo.dateIn:f),            width, STR_ALIGN_CENTER));

    if (clientInfo.codeCurrency > NATCUR)
        println(StrAlign("     " + ПолучитьИмяФинИн(clientInfo.codeCurrency), width, STR_ALIGN_LEFT));
    end;

[┌──────────┬──────────────────────┬─────────────┬────────────┬────────────┬─────────────────┬─────────────────┐
 │          │                      │             │            │            │      Рубли      │      Валюта     │
 │Внутренний│ Наименование клиента │ Код клиента │   Начало   │   Конец    ├────────┬────────┼────────┬────────┤
 │    код   │                      │             │обслуживания│обслуживания│открытые│закрытые│открытые│закрытые│
 ├──────────┼──────────────────────┼─────────────┼────────────┼────────────┼────────┼────────┼────────┼────────┤];


end;

/**
 * Печать подвала
 */
private macro printBasement(allNClient, nClient)

   if (nClient)
  [└──────────┴──────────────────────┴─────────────┴────────────┴────────────┴────────┴────────┴────────┴────────┘
  ];
    else
  [├──────────┴──────────────────────┴─────────────┴────────────┴────────────┴────────┴────────┴────────┴────────┤
   │                                                #######################                                      │
   └─────────────────────────────────────────────────────────────────────────────────────────────────────────────┘
  ](msg:c);
    end;

[   Итого проверено: #
    Найдено клиентов, не имеющих счетов: #
](allNClient:l, nClient:l);

end;

/**
 * Печать одной записи
 */
private macro printRecord(dataSet)

[│ #########│######################│#############│ ########## │ ########## │########│########│########│########│]

(dataSet.id:l, dataSet.name:w, dataSet.idCod:w, printDate(dataSet.startdate):r, dataSet.finishdate:f:r, dataSet.onat:c, dataSet.cnat:c, dataSet.ocur:c, dataSet.ccur:c );

end;

private macro makeTextChapterAndNumPlan(planNumber, chapterNumber)

    return   "(        acbl.t_numplan = " + planNumber
    + "\n" + " AND " + getChapterFilterSqlClause(planNumber, chapterNumber, "acc.t_chapter")
    + "\n" + ")";

end;

/**
 * Формирования SELECT
 */
private macro makeTextSelect(clientInfo, textChAndPlan)

    var selectText;
    var selectNat;
    var selectCur;
    var selectWhere;
    var dateReport = getSQLDate(clientInfo.dateIn);


    selectNat  = "                       SELECT acc.t_account                                     "+
                 "                        FROM daccount_dbt acc, daccBalance_dbt acbl                ";

    selectCur  = "                       SELECT acc.t_account                                     "+
                 "                        FROM daccount_dbt acc, daccBalance_dbt acbl                ";

    selectWhere= "                        WHERE acc.t_client = pt.t_partyid                       "+
                 "                        AND " +  clientInfo.accountFilter.GetAsSqlString("acc");

    selectWhere =  selectWhere +      " AND " + " ( " + textChAndPlan  + " ) ";

    selectWhere = selectWhere +
               "                        AND acbl.t_accountId = acc.t_accountId                    ";


    selectNat =  selectNat + selectWhere + " AND acc.t_code_currency = " + NATCUR;
    selectCur =  selectCur + selectWhere + " AND acc.t_code_currency != " + NATCUR;

    if (clientInfo.codeCurrency > NATCUR ) /*т.е. конкретный ФИ, не равный нац. валюте*/
       selectCur = selectCur  +
               "                        AND acc.t_code_currency = " + clientInfo.codeCurrency;
    end;

    selectText =
               "  SELECT pt.t_partyid ID,                                                         "+
               "         rsb_rep_pt.get_ptname (pt.t_partyid) NAME,                               "+
               /* 04.10.2007 Malakhova 110424*/
               /*Заменила rsb_rep_pt.get_ptcode на rsb_rep_pt.get_PartyCode*/
               "         rsb_rep_pt.get_partyCode(1, pt.t_partyid, " + dateReport + ") idcod,     "+
               "         NVL ((SELECT MIN (clt.t_startdate)                                       "+
               "               FROM dclient_dbt clt                                               "+
               "               WHERE clt.t_partyid = pt.t_partyid                                 "+
               "                     AND " + clientInfo.departmentFilter.GetAsSqlString("clt.t_department") +
               "                     AND clt.t_branch = 0                                         "+
               "                     AND EXISTS (                                                 "+
               "                                SELECT dcl.t_partyid                              "+
               "                                  FROM dclient_dbt dcl                            "+
               "                                   WHERE dcl.t_partyid = pt.t_partyid             "+
               "                                   AND dcl.t_servicekind = clt.t_servicekind      "+
               "                                   AND " + clientInfo.branchAndDprtFilter.GetAsSqlString("dcl.t_department","dcl.t_branch") +
               "                                   AND dcl.t_branch <> 0)),                       "+
               /* clt.t_branch <> 0 - для исключения ситуации, когда клиенту задан вид обслуживания в филиале,
                  но не задано обслуживающее подразделение (или ВСП),
                  что соответствует записи в dclient_dbt с нулевым branch-ем*/
               "              TO_DATE ('01.01.0001', 'DD.MM.YYYY')) startdate,                    "+
               "         CASE                                                                     "+
               "             WHEN EXISTS (                                                        "+
               "                    SELECT clt.t_finishdate                                       "+
               "                      FROM dclient_dbt clt                                        "+
               "                     WHERE clt.t_partyid = pt.t_partyid                           "+
               "                       AND " + clientInfo.departmentFilter.GetAsSqlString("clt.t_department") +
               "                       AND clt.t_branch = 0                                       "+
               "                       AND clt.t_finishdate = TO_DATE ('01.01.0001', 'DD.MM.YYYY')"+
               "                       AND EXISTS (                                               "+
               "                              SELECT dcl.t_partyid                                "+
               "                                FROM dclient_dbt dcl                              "+
               "                               WHERE dcl.t_partyid = pt.t_partyid                 "+
               "                                 AND dcl.t_servicekind = clt.t_servicekind        "+
               "                                 AND " + clientInfo.branchAndDprtFilter.GetAsSqlString("dcl.t_department","dcl.t_branch") +
               "                                 AND dcl.t_branch <> 0))                          "+
               "                THEN TO_DATE ('01.01.0001', 'DD.MM.YYYY')                         "+
               "             ELSE NVL ((SELECT MAX (clt.t_finishdate)                             "+
               "                          FROM dclient_dbt clt                                    "+
               "                         WHERE clt.t_partyid = pt.t_partyid                       "+
               "                           AND " + clientInfo.departmentFilter.GetAsSqlString("clt.t_department") +
               "                           AND clt.t_branch = 0                                   "+
               "                           AND EXISTS (                                           "+
               "                                  SELECT dcl.t_partyid                            "+
               "                                    FROM dclient_dbt dcl                          "+
               "                                   WHERE dcl.t_partyid = pt.t_partyid             "+
               "                                     AND dcl.t_servicekind = clt.t_servicekind    "+
               "                                     AND " + clientInfo.branchAndDprtFilter.GetAsSqlString("dcl.t_department","dcl.t_branch") +
               "                                     AND dcl.t_branch <> 0)),                     "+
               "                       TO_DATE ('01.01.0001', 'DD.MM.YYYY')                       "+
               "                      )                                                           "+
               "         END finishdate,                                                          ";

    if (clientInfo.codeCurrency == ALLFININSTR)
        selectText = selectText +
               "         ' ' onat,                                                                "+
               "         ' ' cnat,                                                                "+
               "         ' ' ocur,                                                                "+
               "         ' ' ccur                                                                 ";
    elif (clientInfo.codeCurrency == NATCUR)
        selectText = selectText +
               "         ' ' onat,                                                                "+
               "         ' ' cnat,                                                                "+
               "         CASE                                                                     "+
               "           WHEN EXISTS ( " + selectCur                                             +
               "                        AND acc.t_open_date <= " + dateReport                       +
               "                        AND (   acc. t_open_close = CHR (0)                       "+
               "                             OR acc.t_close_date >= " +  dateReport +" )"          +
               "                       )                                                          "+
               "               THEN 'X'                                                           "+
               "             ELSE ' '                                                             "+
               "         END ocur,                                                                "+
               "         CASE                                                                     "+
               "           WHEN EXISTS ( " + selectCur                                             +
               "                        AND acc.t_open_close != CHR (0)                           "+
               "                        AND acc.t_close_date < "+  dateReport                      +
               "                       )                                                          "+
               "                THEN 'X'                                                          "+
               "              ELSE ' '                                                            "+
               "         END ccur                                                                 ";
    else
        selectText = selectText +
               "         CASE                                                                     "+
               "           WHEN EXISTS ( " + selectNat                                             +
               "                        AND acc.t_open_date <= " + dateReport                       +
               "                        AND (    acc.t_open_close = CHR (0)                       "+
               "                              OR acc.t_close_date >= " +  dateReport +" )"         +
               "                       )                                                          "+
               "                THEN 'X'                                                          "+
               "             ELSE ' '                                                             "+
               "         END onat,                                                                "+
               "         CASE                                                                     "+
               "           WHEN EXISTS ( " + selectNat                                             +
               "                        AND acc.t_open_close != CHR (0)                           "+
               "                        AND acc.t_close_date < "+ dateReport                       +
               "                       )                                                          "+
               "                THEN 'X'                                                          "+
               "             ELSE ' '                                                             "+
               "         END cnat,                                                                "+
               "         ' ' ocur,                                                                "+
               "         ' ' ccur                                                                 ";
    end;

    return selectText;
end;

/**
 * подзапрос, отбирающий счета клиента
 */
private macro whereNotAccount(clientInfo, textChAndPlan)

    var subQwery;

    var dateReport = GetSQLDate(clientInfo.dateIn);

    subQwery =   "          SELECT *                                              "+
                 "            FROM daccount_dbt acc,                              "+
                 "                 daccBalance_dbt acbl                           "+
                 "           WHERE pt.t_partyid  = acc.t_client                   "+
                 "             AND " +  clientInfo.accountFilter.GetAsSqlString("acc")+
                 "             AND "+ dateReport + " >= acc.t_open_date            "+
                 "             AND (   acc.t_open_close = CHR (0)                 "+
                 "                  OR acc.t_close_date >= " + dateReport          +
                 "                 )                                              ";

    subQwery  = subQwery  +
                 "             AND " + " ( " + textChAndPlan  + " ) ";


    subQwery  = subQwery  +
                 "             AND acbl.t_accountId = acc.t_accountId             ";


    if (clientInfo.codeCurrency == ALLCURRENCY )
      subQwery  = subQwery  +
                 "             AND acc.t_code_currency != " + NATCUR;
    elif (clientInfo.codeCurrency >= NATCUR ) /*т.е. конкретный ФИ*/
      subQwery  = subQwery  +
                 "             AND acc.t_code_currency = " + clientInfo.codeCurrency;
    end;

    return subQwery;
end;

/**
 * Формирования WHERE
 */
private macro makeTextWhere(clientInfo, textChAndPlan)

    var whereText;

    whereText = " WHERE " +  clientInfo.partyFilter.GetAsSqlString("pt.t_partyid", PTK_CLIENT, PTSK_ALL, Date(0,0,0), Date(31,12,9999)) +
                            /* удаление дублирующих клиентов, согласно ТЗ:
                               "Оригинал и дублер попадают в выборку, если они привязаны к заданному узлу ТС и у них нет счетов,
                                и => дублера исключаем. Если оригинал привязан к заданному узлу ТС и у него есть счета,
                                то дублера исключаем."
                              => дублера исключаем всегда, если он с оригиналам привязаны к одним узлам ТС.
                                 И нам не важно, что там со счетами оригинала в этом узле */
                            " AND (     pt.t_isdoubler <> 'X' " +
                            "      OR ( NOT " + clientInfo.partyFilter.GetAsSqlString("pt.t_mainpartyid", PTK_CLIENT, PTSK_ALL, Date(0,0,0), Date(31,12,9999)) + " ) " +
                            "     ) ";

    whereText = whereText + " AND NOT EXISTS ( " + whereNotAccount(clientInfo, textChAndPlan) + " ) ";

    return whereText;
end;

/**
 * Макрос формирования текста запроса
 */
private macro makeTextQuery(clientInfo,textChAndPlan)

    var commandText = " ";

    commandText = commandText +  makeTextSelect(clientInfo, textChAndPlan)
                              +" FROM dparty_dbt pt "
                              +  makeTextWhere(clientInfo, textChAndPlan)
                              + " ORDER BY idcod";

    return commandText;
end;

/**
 * Общее количество клиентов, привязанных по виду обслуживания к заданному подразделению
 */
private macro computeNClient(PartyFilter)

    var dataNClient;

    dataNClient =  TRsbDataSet(" SELECT count(*) nAllClient FROM dparty_dbt pt WHERE " +
                                 PartyFilter.GetAsSqlString("pt.t_partyid", PTK_CLIENT, PTSK_ALL, Date(0,0,0), Date(31,12,9999)));

    dataNClient.SetFieldType("nAllClient", V_INTEGER);

    dataNClient.next();

    return dataNClient.nAllClient;
end;

/**
 * Начало
 */
macro printClientNoAccount(dprtID, orgStructure, issueMode, numPlan, chapter, dateIn, codeCurrency)

    var clientInfo;
    var dataSet;
    var commandText;
    var textChAndPlan;
    var nAllClient = 0;
    var nClient = 0;

    clientInfo = TCltNoAccInfo(dprtID, orgStructure, issueMode, numPlan, chapter, dateIn, codeCurrency);

    /* Контроль открытых опердней */
    if (RepOperdaysOpened(clientInfo.departmentList, clientInfo.dateIn, clientInfo.dateIn).ShouldContinue == false)
        exit(1);
    end;

    printHeader(clientInfo);

    nAllClient = computeNClient(clientInfo.partyFilter);

    if (nAllClient != 0)
        textChAndPlan = makeTextChapterAndNumPlan(clientInfo.numPlan, clientInfo.chapter);

        BegAction(1000, "Получение данных о клиентах без счетов", false);
        commandText = makeTextQuery(clientInfo,textChAndPlan);
        DataSet     = TRsbDataSet(commandText);
        DataSet.SetFieldType("startdate" , V_DATE);
        DataSet.SetFieldType("finishdate", V_DATE);
        EndAction();

        BegAction(1000, "Печать данных о клиентах без счетов", false);
        while (DataSet.next())
            nClient = nClient + 1;
            printRecord(DataSet);
        end;
        EndAction();
    else
        msg = "НЕТ ДАННЫХ ДЛЯ ОТЧЕТА";
    end;

    printBasement(nAllClient, nClient);
end;

macro ibrCreateReport(dprtID, orgStructure, issueMode, numPlan, chapter, dateIn, codeCurrency, nameFile)
    var output = TOFStream(nameFile);

    output.setOutputFile();
    reportBodyFileName = output.getFileName();

    printClientNoAccount(dprtID, orgStructure, issueMode, numPlan, chapter, dateIn, codeCurrency);

    output.resetOutputFile();
    return true;  
end;