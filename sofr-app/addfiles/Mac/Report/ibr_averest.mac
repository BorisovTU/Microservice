/*
$Name:          ibr_Averest.mac
$Module:        Отчеты банка
$Description:   Сервисы контороля и расчета отчета "Отчет о средних остатках на счетах"
*/

import IbrImport;
import rcbWebCommon;
import IbrCommon;
import ReptCBInter;         //для определения вида периода
import Averest;

import DatesUtility;
import Календарь;


private CONST P_MONTH = 1,
              P_QUART = 2,
              P_YEAR  = 3;

private const TAB_REPORT = "Параметры отчета",
              GRP_REPORT = TIbrConstant().decoration.group.NOT_GROUP,
              GRP_REST = "Параметры",
              GRP_PARAMS = "Дополнительные параметры";

private var fileName = "ibr_Averest_web";
private var m_dateIn, 
            m_dateOut;

private const gRep = TAB_REPORT + "@" + GRP_REST + "@";
private const gPrm = TAB_REPORT + "@" + GRP_PARAMS + "@";
    
private macro getValue(report: Object, fieldName : String) : TIbrValueWeb
    return TIbrCommon.getValue(report, fieldName);
end;

private macro checkQuarter(endDate : Date): integer
    var numQuarter : Integer = -1;
    var year;

    dateSplit(endDate, NULL, NULL, year);
       
    if (endDate   <= Date(31, 03, year))
        numQuarter = 1;
    elif (endDate <= Date(30, 06, year))
        numQuarter = 2;
    elif (endDate <= Date(30, 09, year))
        numQuarter = 3;
    elif (endDate <= Date(31, 12, year))
        numQuarter = 4;
    end;
       
    return numQuarter;
end;

private macro checkDate(d, m, y) : bool
    if (Date(d, m, y) > {CURDATE})
        repErrorsQueue().add(0, "Отчетный период еще не наступил");
         return false;
    end;
    
    return true;
 end;

private macro checkPeroid(beginDate : Date, endDate : Date, peroid : integer) : bool
    var beginDay, beginMonth, beginYear, beginQuart; 
    var endDay, endMonth, endYear, endQuart; 

    DateSplit(beginDate, beginDay, beginMonth, beginYear); 
    DateSplit(endDate, endDay, endMonth, endYear);

    if (peroid == P_MONTH)
        if (checkDate(beginDay, beginMonth, endYear))
            m_dateIn  = beginMonth;
            m_dateOut = endYear;
            return true;
         end;

    elif (peroid == P_QUART)
        if (checkDate(beginDay, beginMonth, endYear))
            m_dateIn = checkQuarter(beginDate);
            m_dateOut = endYear;
            return true;
        end;
    elif (peroid == P_YEAR)
        m_dateIn =  2;
        m_dateOut = endYear;
        return true;
    end;
    
    
    return false;
end;

/**
 * Создание отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportCreate(): Object
    var facade = TIbrConstructorFacade();

    var report      = facade.createReport("ibr_Averest", "Средние остатки по счетам", "ibr_Averest.mac", "ibrReportExecute", "ibrReportVerification");
    var tabReport   = facade.createTab(TAB_REPORT, 1);
    var groupReport = facade.createGroup(GRP_REST, 1);
    var groupParams = facade.createGroup(GRP_PARAMS, 2);
    
    facade.resetFieldsOrder();
    groupReport.add(facade.createFieldDepartment("Подразделения",    "Подразделения"));
    groupReport.add(facade.createFieldStructure("Структура",         "Структура"));
    groupReport.add(facade.createFieldIssueMode("Режим",             "Режим"));
    
    groupReport.add(facade.createFieldChartOfAccounts("ПланСчетов",  "План счетов",                 "Основной план счетов"));
    groupReport.add(facade.createFieldAccountChapters("ГлаваСчетов", "Глава счетов",                "Балансовые счета"));
    groupReport.add(facade.createFieldCurrency("Валюта",             "Валюта",                      NATCUR));
    groupReport.add(facade.createFieldCheckBox("ТолькоИнВалюта",     "Только ин.валюта",            false));
    
    groupReport.add(facade.createFieldNameAlg("ВидПериода",          "Вид периода",                 "За период", 2221));
    groupReport.add(facade.createFieldDate("НачалоПериода",          "Начало периода"));
    groupReport.add(facade.createFieldDate("КонецПериода",           "Конец периода"));
    
    groupReport.add(facade.createFieldBalance("Балансовый",          "Балансовый"));
    groupReport.add(facade.createFieldAccount("ЛицевойСчет",         "Лицевой счета"));
    groupReport.add(facade.createFieldNameAlg("ВидСреднего",         "Вид среднего",                "Среднеарифметические", 2253));
    groupReport.add(facade.createFieldNameAlg("ВидСортировки",       "Вид сортировки",              "По клиентам", 2252));
     
    facade.resetFieldsOrder();  
    groupParams.add(facade.createFieldCheckBox("ИтогиПоКлиентам",    "Итоги по клиентам",           false));
    groupParams.add(facade.createFieldCheckBox("ИтогиПобалансам",    "Итоги по б/с счетам",         false));
    groupParams.add(facade.createFieldCheckBox("ЭквНацВалюте",       "Эквивалент в нац. валюте",    true));
    groupParams.add(facade.createFieldCheckBox("СписокСчетов",       "Список счетов",               false));
    groupParams.add(facade.createFieldCheckBox("НулевыеОстатки",     "Нулевые остатки",             true));
    groupParams.add(facade.createFieldCheckBox("Апострофами",        "С апострофами",               true));

    
    tabReport.add(groupReport);
    tabReport.add(groupParams);
    
    report.add(tabReport);
    
    return report.get();
end;

/**
 * Выполнение отчета
 *
 * @param report TIbrReport Отчет и составляющие отчета
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportExecute(report: Object): Object
    var result : Object = TRcbResult();
    var isShowOcp; 
    
    getRegistryValue("REPORT\\SHOW_OCP", V_BOOL, isShowOcp, NULL );

    result.valueBoolean = ibrCreateReport(getValue(report, gRep+ "Подразделения").valueInteger,
                                          TIbrCommon().getIdentifierAccountChapter(getValue(report, gRep+"ГлаваСчетов").valueString),
                                          getValue(report, gRep+"ВидПериода").valueInteger,
                                          m_dateIn,//getValue(report, gRep+"НачалоПериода").valueDate,
                                          m_dateOut,//getValue(report, gRep+"КонецПериода").valueDate, 
                                          getFiId(getValue(report, gRep+"Валюта").valueString, getValue(report, gRep+"ТолькоИнВалюта").valueBoolean),
                                          getValue(report, gRep+"Балансовый").valueString,
                                          getValue(report, gRep+"ЛицевойСчет").valueString,
                                          getValue(report, gRep+"Структура").valueInteger,
                                          getValue(report, gRep+"Режим").valueInteger,
                                          TIbrCommon().getIdentifierChartOfAccounts(getValue(report, gRep+"ПланСчетов").valueString),
                                          getValue(report, gRep+"ВидСреднего").valueInteger,
                                          getValue(report, gRep+"ВидСортировки").valueInteger,
                                          ternary(getValue(report, gPrm+"Апострофами").valueBoolean,     SET_CHAR, 0),
                                          ternary(getValue(report, gPrm+"СписокСчетов").valueBoolean,    SET_CHAR, 0),
                                          ternary(getValue(report, gPrm+"НулевыеОстатки").valueBoolean,  SET_CHAR, 0),
                                          ternary(getValue(report, gPrm+"ИтогиПоКлиентам").valueBoolean, SET_CHAR, 0),
                                          ternary(getValue(report, gPrm+"ИтогиПоБалансам").valueBoolean, SET_CHAR, 0),
                                          ternary(getValue(report, gPrm+"ЭквНацВалюте").valueBoolean,    SET_CHAR, 0),
                                          ternary(not(isShowOcp), SET_CHAR, 0),
                                          fileName);

    // При удачном выполнении
    if (result.valueBoolean == TRUE)
        repTxtFilesQueue().add(reportBodyFileName);
    else
        repErrorsQueue().add(0, "Выпуск отчета невозможен");
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Проверка (валидация) отчета
 *
 * @param report TIbrReportWeb Отчет и составляющие
 *
 * @result TRcbResult Результат выполнения
 */
macro ibrReportVerification(report: Object): Object

    /**
     * Результат
     */
    var result: Object  = TRcbResult();

    //полагаем, что все в порядке
    result.valueBoolean = TRUE;

     //проверка входных параметров
    if (getValue(report, gRep+"Подразделения").valueInteger == 0)
        repErrorsQueue().add(0, "Не задано подразделение");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gRep+"ВидСортировки").valueInteger == 0) and (getValue(report, gPrm+"ИтогиПобалансам").valueBoolean))
        repErrorsQueue().add(0, "Флаг \"Итоги по балансам\" не может быть устанавлен для сортировки вида \"По клиентам\"");
        result.valueBoolean = FALSE;
    end;

    if ((getValue(report, gRep+"ВидСортировки").valueInteger == 1) and (getValue(report, gPrm+"ИтогиПоКлиентам").valueBoolean))
        repErrorsQueue().add(0, "Флаг \"Итоги по балансам\" не может быть устанавлен для сортировки вида \"По клиентам\"");
        result.valueBoolean = FALSE;
    end; 

    if (getValue(report, gRep+"НачалоПериода").valueDate > getValue(report, gRep+"КонецПериода").valueDate)
        repErrorsQueue().add(0, "Дата начала не может превышать дату конца периода");
        result.valueBoolean = FALSE;
    elif (checkPeroid(getValue(report, gRep+"НачалоПериода").valueDate,
                      getValue(report, gRep+"КонецПериода").valueDate,
                      getValue(report, gRep+"ВидПериода").valueInteger) != true)
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;