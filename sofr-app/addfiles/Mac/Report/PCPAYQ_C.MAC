/*------------- Планируемые и фактические оплаты процентов --------------*/
/*-------------        по валютным лицевым счетам          --------------*/
/*-------------       с учетом квитованности оплат         --------------*/

import BankInter, FIInter, CTInter, globals;

file Валюта (fininstr);

file СчетПроцентов( pcacc ) key 1;
file Счета( pcacc )  key 3;
file ИсходнаяОплата( pccalc ) key 5;
file СквитованнаяСИсходнойОплата( pccalc ) key 0;
file СвязьОплат( pcquit );
file party(party );              /* Справочник субъектов*/

record Диалог( "pcpayqa" ) dialog;

var BreakVal = 1;
var ЛицевойСчет;
var КодВалюты;
var НачальнаяДата;
var ТипИсходнойОплаты;
var СуммаПланируемыхОплат;
var СуммаФактическихОплат;
var СуммаИсходныхОплат;
var СуммаСквитованныхСИсходнымиОплат;
var ОбщаяСуммаПланируемыхОплат = $0;
var ОбщаяСуммаФактическихОплат = $0;
var ОбщаяСуммаИсходныхОплат = $0;
var ОбщаяСуммаСквитованныхСИсходнымиОплат = $0;
var ТипВладельцаСчетаПроц = 1002;
var ПунктМеню;
var ОтменитьОтчет;
var first = true;

const РасчетСовершен = 2;
const ВсеОплаты = 0;
const ПланируемаяОплата = 1;
const ФактическаяОплата = 2;
const Счет = 1;
const Договор = 2;
const СквитованныеОплаты = 1;
const ДлинаСчета = 25;

array СписокНазванийИсходнойОплаты;
СписокНазванийИсходнойОплаты(0) = "Планируемая";
СписокНазванийИсходнойОплаты(1) = "Фактическая";


MACRO GetCurrency( FI_Code, FIID, Name ) /* Взять валюту по коду */
  if ( trim(FI_Code) == "" )
         SetParm ( 1 , -1 );
         SetParm ( 2 , "Все валюты");
  else
    keynum( Валюта, 1 );
    Валюта.FI_Code = FI_Code;
    if ( getEQ(Валюта) )
         SetParm ( 1 , Валюта.FIID );
         SetParm ( 2 , Валюта.Name );
    else
      msgbox("В справочнике нет валюты с кодом "+FI_Code );
      return false;
    end;
  end;
  return true;
END;
MACRO GetCodCurrency(  FIID, FI_Code, Name ) /* Взять валюту по FIID */
  if (  FIID == -1 )
         SetParm ( 1 , "" );
         SetParm ( 2 , "Все валюты");
  else
    keynum( Валюта, 0 );
    Валюта.FIID  = FIID;
    if ( getEQ(Валюта) )
         SetParm ( 1 , Валюта.FI_Code);
         SetParm ( 2 , Валюта.Name );
    else
      msgbox("В справочнике нет валюты с номером "+FIID );
      return false;
    end;
  end;
  return true;
END;

/* вызов "листалки" валют */
macro ВыбратьВалюту( FIID, FI_Code, Name )
  array СписокКодовВалют;
  array СписокДляМенюВалют;
  var number = 0, stat;

  СписокКодовВалют(number) = -1;
  СписокДляМенюВалют(number) = "    Все валюты";
  number = number + 1;

  keynum( Валюта, 2 );
  Валюта.FI_Kind = FIKIND_CURRENCY;
  Валюта.AvoirKind = 0;
  Валюта.FI_Code   = "";
  stat = getGE(Валюта);
  while( stat  and (Валюта.FI_Kind == FIKIND_CURRENCY) )
    if (  Валюта.FIID > 0 )
      СписокКодовВалют(number) = Валюта.FIID;
      СписокДляМенюВалют(number) = String(Валюта.FI_Code:3) +" "+Валюта.Name;
      number = number + 1;
    end;
    stat = next( Валюта )
  end;

  number = Menu( СписокДляМенюВалют, " Выберите валюту: ", " Валюта " );
  if ( number < 0 )
     return False;
  end;
  SetParm ( 0 , СписокКодовВалют(number) );
  SetParm ( 1 , substr(СписокДляМенюВалют(number), 1,3) );
  SetParm ( 2 , substr(СписокДляМенюВалют(number), 5) );
  return True;
end;

/* вызов "листалки" счетов */
macro ВыбратьСчет( Account, Currency )
      record AC( account );
      if (ListAccount (AC, 1, Currency, Account))
         SetParm ( 0 , AC.Account);
         SetParm ( 1 , AC.Code_Currency);
         return True;
      end;
     return False;
end;


macro ОбработчикДиалога( СсылкаНаДиалог, Сообщение, ТекущееПоле, КодКлавиши )
var error=true;

   if( Сообщение==DLG_SAVE )

       if( trim(СсылкаНаДиалог.ЛицевойСчет) == "0") error=false;
       else
          Счета.ObjectType=ТипВладельцаСчетаПроц;
          Счета.Account=СсылкаНаДиалог.ЛицевойСчет;
          GetCurrency( СсылкаНаДиалог.КодВалюты, КодВалюты, СсылкаНаДиалог.Валюта );
          Счета.FIID   = КодВалюты;
          Счета.CloseDate=date(0,0,0);
          if( getGE( Счета ) )
             if( (Счета.ObjectType==ТипВладельцаСчетаПроц) and
                       (Счета.Account==СсылкаНаДиалог.ЛицевойСчет) and
                      ( (КодВалюты == -1) or (Счета.FIID==КодВалюты) ) )
                error=false;
             end;
          end;
       end;
       if( error )
          msgbox( "Нет счетов процентов по указанному|лицевому счету" );
          return CM_CANCEL;
       end;
    end;
   if( Сообщение == DLG_KEY )
      if( КодКлавиши == 317)
        if (fldname (СсылкаНаДиалог, ТекущееПоле) == "ИсходнаяОплата" )

         ПунктМеню = Menu( СписокНазванийИсходнойОплаты, null, null, 45, 15 );
         if( ПунктМеню >= 0 )
            СсылкаНаДиалог.ИсходнаяОплата = СписокНазванийИсходнойОплаты( ПунктМеню );
            if( ПунктМеню == 0 )
               ТипИсходнойОплаты = ПланируемаяОплата;
            else
               ТипИсходнойОплаты = ФактическаяОплата;
            end;
            UpdateFields( СсылкаНаДиалог );
         end;
        elif (fldname (СсылкаНаДиалог, ТекущееПоле) == "КодВалюты" )
          if ( ВыбратьВалюту( КодВалюты, СсылкаНаДиалог.КодВалюты, СсылкаНаДиалог.Валюта ) )
            UpdateFields( СсылкаНаДиалог );
          end;
        elif (fldname (СсылкаНаДиалог, ТекущееПоле) == "ЛицевойСчет" )
          if ( ВыбратьСчет( СсылкаНаДиалог.ЛицевойСчет, КодВалюты ) )
            GetCodCurrency( КодВалюты, СсылкаНаДиалог.КодВалюты, СсылкаНаДиалог.Валюта );
            UpdateFields( СсылкаНаДиалог );
          end;
        end;
      elif( (КодКлавиши == 13) and (fldname (СсылкаНаДиалог, ТекущееПоле) == "ИсходнаяОплата") )
         return CM_SAVE;
      end;
   end;
   if( (Сообщение == DLG_REMFOCUS) and
       (fldname (СсылкаНаДиалог, ТекущееПоле) == "КодВалюты" ) )
      if (not GetCurrency( СсылкаНаДиалог.КодВалюты, КодВалюты, СсылкаНаДиалог.Валюта ) )
         return CM_CANCEL;
      else
         UpdateFields( СсылкаНаДиалог );
      end;
   end;
end;


macro ВвестиИсходныеДанные()
   Диалог.ТипВладельцаСчетаПроц = "Лицевой счет";

   ТипИсходнойОплаты = ПланируемаяОплата;
   Диалог.ИсходнаяОплата = СписокНазванийИсходнойОплаты(0);
   Диалог.ЛицевойСчет = "0"+MkStr(" ",ДлинаСчета-1);
   Диалог.КодВалюты = "";
   GetCurrency( Диалог.КодВалюты, КодВалюты, Диалог.Валюта );

   ОтменитьОтчет = not RunDialog( Диалог, "ОбработчикДиалога" );
   if( ОтменитьОтчет ) exit( 1 ); end;
   ЛицевойСчет = Диалог.ЛицевойСчет;

   GetCurrency( Диалог.КодВалюты, КодВалюты, Диалог.Валюта );

   НачальнаяДата     = Диалог.НачальнаяДата;

   if( ТипИсходнойОплаты == ПланируемаяОплата )
      keynum( СвязьОплат, 0 );
   else
      keynum( СвязьОплат, 1 );
   end;
end;


macro НапечататьЗаголовок()
 var Superior, pt, sep;

 party.PartyID = {OurBank};
 getEQ( party );
 Superior = readNoteForObject( OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 21 );

 if(( Superior == "") or ( ValType( Superior ) == V_UNDEF ))
 else
   sep = index( Superior, "|");
   while(sep)
      [                                               #
      ]  (substr( Superior, 1, sep-1 ):c);
      Superior = substr( Superior, sep + 1 );
      sep = index( Superior, "|");
   end;
   [                                               #

   ]  ( Superior:c );
 end;

[             Общий список планируемых и фактических оплат процентов по лицевым счетам                  ];
[                                     с учетом их сквитованности                                        ];
[─────────────────────────┬──────────┬──────────┬────────────────────┬──────────┬──────────┬──────────────────────];
[       Счет              │   Дата   │  Номер   │        Сумма       │   Дата   │  Номер   │        Сумма         ];
if( ТипИсходнойОплаты == ПланируемаяОплата )
[                         │  оплаты  │  оплаты  │ планируемой оплаты │  оплаты  │  оплаты  │ фактической оплаты   ];
else
[      Договор            │  оплаты  │  оплаты  │ фактической оплаты │  оплаты  │  оплаты  │ планируемой оплаты   ];
end;
[─────────────────────────┴──────────┴──────────┴────────────────────┴──────────┴──────────┴──────────────────────];
end;


macro НапечататьИсходнуюОплату()
[ #                        #                   #                    # ]
( СчетПроцентов.Account:l, ИсходнаяОплата.PayDate:l, ИсходнаяОплата.AutoKey:r,ИсходнаяОплата.QuitSum:r );
end;


macro НапечататьСквитованнуюСИсходнойОплату()
[                                                                     #                   #                    # ]
( СквитованнаяСИсходнойОплата.PayDate:l, СквитованнаяСИсходнойОплата.AutoKey:r, СвязьОплат.QuitSum:r );
end;


macro НапечататьСквитованныеСИсходнойОплаты()
   /* Устанавливаем ключи для выбора оплат */
   СвязьОплат.PlanAutoKey = ИсходнаяОплата.AutoKey;
   СвязьОплат.FactAutoKey = ИсходнаяОплата.AutoKey;

   BreakVal = 1;

   if( not getGE( СвязьОплат ) )  return; end;
   while( BreakVal )
      /* проверяем на попадание в диапазон */
      if( ТипИсходнойОплаты == ПланируемаяОплата )
         if( СвязьОплат.PlanAutoKey != ИсходнаяОплата.AutoKey )
            BreakVal = 0;
         else
            СквитованнаяСИсходнойОплата.AutoKey = СвязьОплат.FactAutoKey;
            if( not getGE( СквитованнаяСИсходнойОплата ) )  return; end;

            НапечататьСквитованнуюСИсходнойОплату();
            СуммаСквитованныхСИсходнымиОплат = СуммаСквитованныхСИсходнымиОплат + СвязьОплат.QuitSum;
         end;
      else
         if( СвязьОплат.FactAutoKey != ИсходнаяОплата.AutoKey )
            BreakVal = 0;
         else
            СквитованнаяСИсходнойОплата.AutoKey = СвязьОплат.PlanAutoKey;
            if( not getGE( СквитованнаяСИсходнойОплата ) )  return; end;

            НапечататьСквитованнуюСИсходнойОплату();
            СуммаСквитованныхСИсходнымиОплат = СуммаСквитованныхСИсходнымиОплат + СвязьОплат.QuitSum;
         end;
      end;

      if( not next( СвязьОплат ) )   BreakVal = 0; end;
   end;
end;


macro НапечататьОплаты()
   /* Устанавливаем ключи для выбора исходных оплат */
   ИсходнаяОплата.AutoPerc = СчетПроцентов.AutoKey;
   ИсходнаяОплата.CalcFlag = РасчетСовершен;
   ИсходнаяОплата.PayType  = ТипИсходнойОплаты;
   ИсходнаяОплата.PayDate  = НачальнаяДата;

   СуммаИсходныхОплат = $0;
   СуммаСквитованныхСИсходнымиОплат = $0;
   BreakVal = 1;

   if( not getGE( ИсходнаяОплата ) )  return; end;
   while( BreakVal )
      /* проверяем на попадание в диапазон */
      if( (ИсходнаяОплата.AutoPerc != СчетПроцентов.AutoKey) or
          (ИсходнаяОплата.CalcFlag != РасчетСовершен) or
          (ИсходнаяОплата.PayType  != ТипИсходнойОплаты) )
             BreakVal = 0;
      else
         if( (ИсходнаяОплата.PayDate >= НачальнаяДата) and
             (ИсходнаяОплата.QuitFlag >= СквитованныеОплаты) )
                НапечататьИсходнуюОплату();
                СуммаИсходныхОплат = СуммаИсходныхОплат + ИсходнаяОплата.QuitSum;
                НапечататьСквитованныеСИсходнойОплаты();
                BreakVal = 1;
         end;
      end;

      if( not next( ИсходнаяОплата ) )   BreakVal = 0; end;
   end;
end;


macro НапечататьШапкуСчета()
   [ ];
   [  Оплаты по счету процентов номер #               лицевого счета # ]
       (СчетПроцентов.AutoKey:l, СчетПроцентов.Account:l);
   [ ];
end;


macro РасчитатьСуммуПланируемыхИФактическихОплаты()
   /* Устанавливаем ключи для выбора оплат */
   ИсходнаяОплата.AutoPerc = СчетПроцентов.AutoKey;
   ИсходнаяОплата.CalcFlag = РасчетСовершен;
   ИсходнаяОплата.PayType  = ВсеОплаты;
   ИсходнаяОплата.PayDate  = date(0,0,0);

   СуммаПланируемыхОплат = $0;
   СуммаФактическихОплат = $0;
   BreakVal = 1;

   if( not getGE( ИсходнаяОплата ) )  return; end;
   while( BreakVal )
      /* проверяем на попадание в диапазон */
      if( (ИсходнаяОплата.AutoPerc != СчетПроцентов.AutoKey) or
          (ИсходнаяОплата.CalcFlag != РасчетСовершен) )
             BreakVal = 0;
      else
         if( ИсходнаяОплата.PayDate >= НачальнаяДата )
            if( ИсходнаяОплата.PayType == ПланируемаяОплата )
               СуммаПланируемыхОплат = СуммаПланируемыхОплат + ИсходнаяОплата.Sum;
            else
               СуммаФактическихОплат = СуммаФактическихОплат + ИсходнаяОплата.Sum;
            end;
         end;
      end;

      if( not next(  ИсходнаяОплата ) )   BreakVal = 0; end;
   end;
end;


macro НапечататьИтог()
РасчитатьСуммуПланируемыхИФактическихОплаты();
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
[ Итого Сквитовано:                                                 #                                          # ]
( СуммаИсходныхОплат:r, СуммаСквитованныхСИсходнымиОплат:r );
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
if( ТипИсходнойОплаты == ПланируемаяОплата )
[ Общая сумма оплат:                                                #                                          # ]
( СуммаПланируемыхОплат:r, СуммаФактическихОплат:r );
else
[ Общая сумма оплат:                                                #                                          # ]
( СуммаФактическихОплат:r, СуммаПланируемыхОплат:r );
end;
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
[ ];
end;

macro НетДанныхДляОтчета()
[ Нет данных, удовлетворяющих заданным условиям ];
end;

ВвестиИсходныеДанные();
if( ОтменитьОтчет == true )   return; end;

/* Устанавливаем ключи для выбора счета процентов */
СчетПроцентов.ObjectType = ТипВладельцаСчетаПроц;
СчетПроцентов.CloseDate = date(0,0,0);
СчетПроцентов.Account = ЛицевойСчет;
СчетПроцентов.FIID = КодВалюты;

BreakVal = 1;
if( not getGE( СчетПроцентов ) )   НетДанныхДляОтчета; return; end;
while( BreakVal )
   /* проверяем на попадание в диапазон */
   if( (СчетПроцентов.ObjectType == ТипВладельцаСчетаПроц) and
       (СчетПроцентов.CloseDate == date(0,0,0)) and
       ( (СчетПроцентов.Account == ЛицевойСчет) or
         (trim(ЛицевойСчет) == "0") ) )
      if( (КодВалюты == -1) or (СчетПроцентов.FIID == КодВалюты) )
         if( first )
           НапечататьЗаголовок();
           first = false;
         end;
         НапечататьШапкуСчета();
         НапечататьОплаты();
         НапечататьИтог();

         ОбщаяСуммаПланируемыхОплат = ОбщаяСуммаПланируемыхОплат + СуммаПланируемыхОплат;
         ОбщаяСуммаФактическихОплат = ОбщаяСуммаФактическихОплат + СуммаФактическихОплат;
         ОбщаяСуммаИсходныхОплат = ОбщаяСуммаИсходныхОплат + СуммаИсходныхОплат;
         ОбщаяСуммаСквитованныхСИсходнымиОплат = ОбщаяСуммаСквитованныхСИсходнымиОплат + СуммаСквитованныхСИсходнымиОплат;
      end;
      BreakVal = 1;
   else
      BreakVal = 0;
   end;

   if( not next( СчетПроцентов ) )  BreakVal = 0; end;
end;



/* Общий итог по всем счетам или договорам */
if( КодВалюты != -1 )
[ ];
[ ];
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
[ Итого сквитовано по всем счетам :                                 #                                          # ]
( ОбщаяСуммаИсходныхОплат:r, ОбщаяСуммаСквитованныхСИсходнымиОплат:r );
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
if( ТипИсходнойОплаты == ПланируемаяОплата )
[ Общая сумма оплат по всем счетам :                                #                                          # ]
( ОбщаяСуммаПланируемыхОплат:r, ОбщаяСуммаФактическихОплат:r );
else
[ Общая сумма оплат по всем счетам :                                #                                          # ]
( ОбщаяСуммаФактическихОплат:r, ОбщаяСуммаПланируемыхОплат:r );
end;
[─────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
end;

end.
