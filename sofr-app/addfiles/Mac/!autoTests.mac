/**
 @file 		!autoTests.mac
 @brief 	Процедура авто-тестирования
*/
IMPORT RSD;

private macro DateTimeStr
   var dd, mm, yyyy;
   var hh, min, sec;
   var dt: date;
   var tm: time;

   dt = date;
   tm = time;
   DateSplit(dt, dd, mm, yyyy);
   TimeSplit(tm, hh, min, sec);

   return string(yyyy:o:4)+"/"+string(mm:o:2)+"/"+string(dd:o:2)+" "+string(hh:o:2)+":"+string(min:o:2)+":"+string(sec:o:2);
end;

/** 
  @brief 		Возвращает строчку, если в параметре тип 26, то пустую
*/
MACRO GetStr(s)
  IF(ValType(s) == 26)
    RETURN "";
  END;
  RETURN string(s);
END;

/** 
  @brief 		Печатает сообщение
*/
MACRO Say(s)
  print(DateTimeStr()+" "+string(s));
END;

/** 
  @brief 		Печатает сообщение
*/
MACRO SayLn(s)
  println(DateTimeStr()+" "+string(s));
END;

/** 
  @brief 		Печатает сообщение
*/
MACRO SayEnd(s)
  println(string(s));
END;

/** 
  @brief 		Печатает сообщение
*/
MACRO SayNew()
  println();
END;

/** 
  @brief 		Вызов одного авто-теста
*/
MACRO TestOne(p_module, p_procname, p_testname, p_descr)
  var ret;

  Say(string(p_testname) + " (" + string(p_descr) + ")");
  ret = ExecMacroFile(p_module, p_procname);

  if(ret == 0) 
    SayEnd(" Ok");
  else
    SayEnd(" Err!");
  end;

  return ret;
ONERROR(err)
  SayEnd(" Exception!");
  return 1;
END;

/** 
  @brief 		Макрос завершения группы авто-тестов
*/
MACRO GrpTearDown(p_GrpModule, p_TearDown)
  var ret;

  SayLn("Завершение группы тестов ");

  if((p_GrpModule != "") AND (p_TearDown != "")) 
    Say("Вызов процедуры: " + string(p_GrpModule) + "." + string(p_TearDown));
    ret = ExecMacroFile(p_GrpModule, p_TearDown);
    if(ret == 0) 
      SayEnd(" Ok");
    else
      SayEnd(" Err!");
    end;
  end;

  return ret;
ONERROR(err)
  SayEnd(" Exception!");
  return 1;
END;

/** 
  @brief 		Макрос запуска группы авто-тестов
*/
MACRO GrpSetUp(p_GrpModule, p_SetUp, p_GrpDescr)
  var ret;

  SayNew();
  SayLn("Запуск группы тестов: " + string(p_GrpDescr));

  if((p_GrpModule != "") AND (p_SetUp != "")) 
    Say("Вызов процедуры: " + string(p_GrpModule) + "." + string(p_SetUp));
    ret = ExecMacroFile(p_GrpModule, p_SetUp);
    if(ret == 0) 
      SayEnd(" Ok");
    else
      SayEnd(" Err!");
    end;
  end;

  return ret;
ONERROR(err)
  SayEnd(" Exception!");
  return 1;
END;

/** 
  @brief 		Запуск автотестов
*/
macro CallAutoTests

  var Sql, cmd, rs
    , AllCount: integer = 0
    , OkCount: integer = 0
    , wrongArr, wrongTest
    , x_GrpModule: STRING = ""
    , x_TearDown: STRING = ""
    , x_SetUp: STRING = ""
    , x_PrevGroup: integer = -1
    , x_PrevGrpModule: STRING = ""
    , x_ValType;
  ;

  wrongArr = TArray;
  SayNew();
  SayLn("Запуск процедуры авто-тестирования");

  sql = "SELECT a.t_module, a.t_procname, a.t_testname, a.t_descr, a.t_group "
    + "  , g.t_module AS grp_module, g.t_descr AS grp_descr, g.t_setup, g.t_teardown "
    + "  FROM dautotests_dbt a, dautotestgroups_dbt g "
    + "  WHERE a.t_used = 'X' "
    + "  AND g.t_id = a.t_group "
    + "  AND a.t_group > 0 "
    + "  ORDER BY a.t_group, a.t_level, a.t_id"
  ;
  cmd = RsdCommand(sql);
  cmd.NullConversion = true;
  rs = RSDRecordSet(cmd);
  while (rs.movenext)
    if(x_PrevGroup != rs.value("t_group") ) 
       if(x_PrevGroup != -1)
         GrpTearDown(x_PrevGrpModule, x_TearDown); 
       end;
       x_PrevGroup = rs.value("t_group");
       x_ValType = ValType(rs.value("grp_module"));
       x_PrevGrpModule = GetStr( rs.value("grp_module") );
       x_TearDown = GetStr( rs.value("t_teardown") ) ;
       GrpSetUp(GetStr(rs.value("grp_module")), GetStr(rs.value("t_setup")), GetStr(rs.value("grp_descr"))); 
    end;
    AllCount = AllCount + 1;
    if( TestOne( GetStr(rs.value("t_module")), GetStr(rs.value("t_procname")), GetStr(rs.value("t_testname")), GetStr(rs.value("t_descr"))) == 0 )
       OkCount = OkCount + 1;
    else
       wrongArr[wrongArr.size] = rs.value("t_testname");
    end;
  end;

  if(x_PrevGroup != -1)
    GrpTearDown(x_PrevGrpModule, x_TearDown); 
  end;

  SayNew();
  SayLn("Выполнено тестов: " + string(AllCount));
  SayLn("Количество успешных тестов: " + string(OkCount));
  SayLn("Количество неуспешных тестов: " + string(AllCount - OkCount));

  SayNew();
  if( AllCount == OkCount ) 
    SayLn("Все тесты выполнены успешно.");
  else
    SayLn("Не все тесты выполнены успешно.");
    SayNew();
    SayLn("Список не выполненных тестов:");
    for (wrongTest, wrongArr)
      SayLn(wrongTest);
    end;
  end;

end;

CallAutoTests();

