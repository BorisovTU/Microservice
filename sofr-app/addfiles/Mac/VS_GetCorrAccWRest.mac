/**
 @file VS_GetCorrAccWRest.mac
 @brief Поиск и вывод остатка 

 Поиск и вывод остатка на счетах по КУ '+Корр, Свексель' у погашенных векселей
 
 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |29.09.23   |Пономарева К.В.|DEF-52292                                       |Реализация поиска и вывода остатка на счетах 
 |           |               |                                                |по КУ '+Корр, Свексель' у погашенных векселей в excel

*/

import VSInter, OprInter, "or_rep_h.mac", rsd, RsbDataSet;

/**
@brief Печать шапки таблицы
@param[in] Rep Объект отчета
*/
macro print_header(Rep)

  Rep.AddPrintCell("Номер векселя",                     25, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Номер счета КУ '+Корр, Свексель'",  25, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddPrintCell("Остаток счета",                     25, null, "c:ex_FS(b):ex_B(tblr)");
  Rep.AddStr();
  
end;

/**
@brief Поиск данных и печать отчета excel
@param[in] restDate Дата проверки остатков на счетах
*/
macro GetCorrAccWRest(restDate)
  var que, cmd, rs;
  var ЕстьСчета = false;

  var Rep = CMakeReport();

  que = "SELECT LTRIM (banner.t_bcnumber, ' ') VsNumber, " + 
        "       accdoc.t_account VsAccount, " + 
        "       ABS (rsb_account.restac (accdoc.t_Account, " + 
        "                                accdoc.t_Currency, " + 
        "                                ?, " + 
        "                                accdoc.t_Chapter, " + 
        "                                NULL)) VsRest" + 
        "  FROM dvsbanner_dbt banner, dmcaccdoc_dbt accdoc " + 
        " WHERE     banner.t_bcstatus = " + VSBANNER_STATUS_ENDED + 
        "       AND banner.t_backoffice = 'Y' " + 
        "       AND accdoc.t_docid = banner.t_bcid " + 
        "       AND accdoc.t_dockind = " + DL_VSBANNER + 
        "       AND accdoc.t_catid = " + 
        "              (SELECT cat.t_id " + 
        "                 FROM dmccateg_dbt cat " + 
        "                WHERE cat.t_code = '+Корр, Свексель') " + 
        "       AND rsb_account.restac (accdoc.t_Account, " + 
        "                               accdoc.t_Currency, " + 
        "                               ?, " + 
        "                               accdoc.t_Chapter, " + 
        "                               NULL) != 0 " ;
  cmd = RSDCommand(que);
  cmd.addParam( "", RSDBP_IN, restDate );
  cmd.addParam( "", RSDBP_IN, restDate );

  cmd.execute();

  rs = TRsbDataSet(cmd);
  while (rs.MoveNext())
    if (not ЕстьСчета)
      ЕстьСчета = true;
      print_header(Rep);
    end;
    Rep.AddPrintCell(rs.VsNumber, 25, 0, "c:ex_FS():ex_B(tblr)");
    Rep.AddPrintCell(rs.VsAccount, 25, 0, "c:ex_FS():ex_B(tblr)");
    Rep.AddPrintCell(rs.VsRest, 25, 0, "c:ex_FS():ex_B(tblr)"); 
    Rep.AddStr();
  end;

  if(ЕстьСчета)
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  else
    msgbox("На счетах по КУ '+Корр, Свексель' у погашенных векселей нет остатка.");
  end;

  return 0;
end;

var restDate = {curdate};
if (not GetDate(restDate, "Введите дату поиска остатка на счетах по КУ '+Корр, Свексель' у погашенных векселей") )
  return;
end;

GetCorrAccWRest( restDate );