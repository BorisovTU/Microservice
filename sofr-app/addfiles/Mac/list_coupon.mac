/*****************************************************************************/
/*                                                           R-Style Softlab */
/*                                                                           */
/*                   Отчет о предстоящих погашениях                          */
/*                                                                           */
/*  Create : Chesnokov D.S.                                                  */
/*  Date   : 08.02.2019                                                      */
/*****************************************************************************/


import globals, RSD, sp_class, "Календарь";

var BegDate:date, EndDate:date, Rep, rn = 1, sheet = 1; 
var Old_Count = 0;

//Переменные для варианта WindowsReports, поддерживающего POIReports
var csvFile, fName, csvTable, printEngine;

const C_COUPON:integer   = 1;
const C_AMORT:integer    = 2;
const C_AVOIRISS:integer = 3;

BegDate = {curdate};
GetDate(BegDate, "Укажите дату отчета");
EndDate = BegDate;
BegDate = GetDateAfterWorkDays(BegDate, -1);

if (BegDate != EndDate)
  BegDate = BegDate + 1;
end; 

macro PrintStr(Type, Name_FIID, ISIN, Kind_FIID, Num_Coupon,
               IsPArtial, DrawingDate,  Portfolio, NKD, Count, Cost, Place)
   
   if ((Place == "ИТОГО") and (Old_Count != Count))
     if(Type == C_COUPON)
       csvFile.AddCell(Name_FIID);
       csvFile.AddCell(ISIN); 
       csvFile.AddCell(""); /*C*/
       csvFile.AddCell(""); /*D*/
       csvFile.AddCell(""); /*E*/
       csvFile.AddCell(""); /*F*/
       csvFile.AddCell(""); /*G*/
       csvFile.AddCell(""); /*H*/
       csvFile.AddCell(Place);
       csvFile.AddCell(Count);
       csvFile.AddCell(Cost);
       csvFile.AddRow();
     end;
   elif ((Place == "ИТОГО") and (Old_Count == Count))    
   else

     DtTmSplit(DrawingDate, DrawingDate, null);

     csvFile.AddCell(Name_FIID);
     csvFile.AddCell(ISIN);
     csvFile.AddCell(Kind_FIID);
     if(Type == C_COUPON)
       csvFile.AddCell(Num_Coupon);
       csvFile.AddCell(""); //empty column E
       csvFile.AddCell(DrawingDate);
     else
       csvFile.AddCell(DrawingDate);
       csvFile.AddCell("");
       csvFile.AddCell("");
     end;
     
     csvFile.AddCell(Portfolio);
     if(Type == C_COUPON)
       csvFile.AddCell(NKD);
       csvFile.AddCell(Place);
     end; 
     csvFile.AddCell(Count);
     
     if(Type == C_COUPON)
       csvFile.AddCell(Cost);
     end; 

     csvFile.AddRow();
   end;
   if (Old_Count != Count)
     Old_Count = Count;
   end; 

END;

macro GetDrawingCoupon(Type)

  csvTable = printEngine.RegisterTable("templateTable"+Type, "templateTableHeader"+Type, true);
  
  csvFile = C_CSVFile();
  if (printEngine.UsePoi())
    csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
  end;
  fName = csvFile.CreateFullFileName("list_coupon_csv"+type+".txt");
  csvFile.FileOpen(fName, true);

  if (Type == C_COUPON)
    printEngine.SetValue_NameCell("title1", "Погашение купонов и амортизации на дату: " + EndDate);
    printEngine.SetValue_NameCell("title2", "Наличие ЦБ в портфеле определяется на дату: " + {Curdate});
    printEngine.CopyAllSheetInTotalBook (null, false, "templateCouponHeader", 0, "Протокол");
  elif (Type == C_AMORT)
    printEngine.CopyAllSheetInTotalBook (null, false, "titleAmort", 0, "Протокол");
    printEngine.CopyAllSheetInTotalBook (null, false, "templateAmortHeader", 0, "Протокол");
  end;

  var rs, cmd, str;
  str = "  SELECT * FROM ( "
      + "    SELECT fiw.t_fiid, "
      + "           fi.t_name, "
      + "           avr.t_isin, "
      + "           av_k.t_name as t_kind, "
      + "           fiw.t_number, "
      + "           DECODE(fiw.t_ispartial, CHR(88), 'YES', 'NO') as t_ispartial, "
      + "           TRUNC(fiw.t_drawingdate) as t_drawingdate , "
      + "           ll.t_name as t_portfolio, "
      + "           RSI_RSB_FIINSTR.CALCNKD_EX(fiw.t_fiid, fiw.t_drawingdate, 1, 1) as t_nkd, "
      + "           wrt.t_state,"
      + "           DECODE(wrt.t_state, 1, 'На балансе', 3, 'В РЕПО', 'ИТОГО') as t_state_desc,"
      + "           SUM(wrt.t_amount) as t_count, "
      + "           RSI_RSB_FIINSTR.CALCNKD_EX(fiw.t_fiid, fiw.t_drawingdate, SUM(wrt.t_amount) , 1) as t_cost, " 
      + "           GROUPING(ll.t_name) as gr_port, "
      + "           GROUPING(wrt.t_state) as gr_state "
      + "      FROM dfiwarnts_dbt fiw "
      + "      JOIN dpmwrtsum_dbt wrt ON (wrt.t_fiid = fiw.t_fiid and wrt.t_portfolio in (1, 2, 5, 6) and wrt.t_amount != 0 and wrt.t_state in (1,3)) "  /* 523609: PDV добавлен портфель ПВО*/ 
      + "      JOIN dfininstr_dbt fi ON (fi.t_fiid =  fiw.t_fiid AND fi.t_fi_kind = 2) "
      + "      JOIN davrkinds_dbt av_k ON (fi.t_avoirkind = av_k.t_avoirkind AND av_k.t_fi_kind = fi.t_fi_kind and av_k.t_root = 17) "
      + "      LEFT JOIN davoiriss_dbt avr ON (avr.t_fiid = fiw.t_fiid) "
      + "      JOIN dllvalues_dbt ll ON (ll.t_list = 1100 AND ll.t_element = wrt.t_portfolio) "
      + "     WHERE fiw.t_drawingdate <= ? AND fiw.t_ispartial = chr(?) AND fiw.t_SpIsClosed = chr(0) "
      + "     GROUP BY fiw.t_fiid, fi.t_name, avr.t_isin, av_k.t_name, fiw.t_number, fiw.t_ispartial, fiw.t_drawingdate, rollup(ll.t_name, wrt.t_state)) t "   
      + "   WHERE (t.gr_port = 0 AND t.gr_state = 0) OR (t.gr_port = 1 AND t.gr_state = 1) "
      + "   ORDER BY t.t_drawingdate, t.t_fiid, t.t_ispartial desc, t.t_state asc";

  cmd = RSDCommand(str);
  cmd.AddParam("1", RSDBP_IN, EndDate);
  cmd.AddParam("2", RSDBP_IN, IIF(Type == C_AMORT, 88, 0), V_INTEGER);

  rs = RSdRecordSet(cmd);
  while(rs and rs.Movenext)
    PrintStr(Type, rs.value("t_name"), rs.value("t_isin"), rs.value("t_kind"), rs.value("t_number"), 
                   rs.value("t_ispartial"), rs.value("t_drawingdate"), rs.value("t_portfolio"), rs.value("t_nkd"),
                   rs.value("t_count"), rs.value("t_cost"), rs.value("t_state_desc"));
  end;

  csvFile.FileClose();
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  csvTable.EndTable();

  printEngine.CopyAllSheetInTotalBook(null,         //Имя закладки,по умолчанию имя закладки из шаблона
                                    false, // true - на новый лист, false - на существующий(если имя листа отличается, всегда на новый лист)
                                    csvTable.GetTableRange(),     //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                      0,
                                    "Протокол") ;
end;

macro GetDrawingAvoir(Type)

  csvTable = printEngine.RegisterTable("templateTable"+Type, "templateTableHeader"+Type, true);
  csvFile = C_CSVFile();
  if (printEngine.UsePoi())
    csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
  end;
  fName = csvFile.CreateFullFileName("list_coupon_csv"+type+".txt");
  csvFile.FileOpen(fName, true);

  printEngine.CopyAllSheetInTotalBook (null, false, "templateShareHeader", 0, "Протокол");

  var rs, cmd, str;
  str = "  SELECT fi.t_fiid, "
      + "         fi.t_name, "
      + "         avr.t_isin, "
      + "         av_k.t_name as t_kind, "
      + "         fi.t_drawingdate, "
      + "         ll.t_name as t_portfolio, "
      + "         SUM(wrt.t_amount) as t_count "
      + "    FROM dfininstr_dbt fi "
      + "    JOIN dpmwrtsum_dbt wrt ON (wrt.t_fiid = fi.t_fiid and wrt.t_portfolio in (1, 2, 5, 6) and wrt.t_amount != 0)  "   /* 523609: PDV добавлен портфель ПВО*/
      + "    JOIN davrkinds_dbt av_k ON (fi.t_avoirkind = av_k.t_avoirkind AND av_k.t_fi_kind = fi.t_fi_kind) "
      + "    LEFT JOIN davoiriss_dbt avr ON (avr.t_fiid = fi.t_fiid) "
      + "    JOIN dllvalues_dbt ll ON (ll.t_list = 1100 AND ll.t_element = wrt.t_portfolio)  "
      + "   WHERE fi.t_drawingdate BETWEEN ? AND ? AND fi.t_fi_kind = 2 "
      + "   GROUP BY fi.t_fiid, fi.t_name, avr.t_isin, av_k.t_name, fi.t_drawingdate, ll.t_name "
      + "   ORDER BY fi.t_drawingdate, fi.t_fiid desc ";

  cmd = RSDCommand(str);
  cmd.AddParam("1", RSDBP_IN, BegDate);
  cmd.AddParam("2", RSDBP_IN, EndDate);

  rs = RSdRecordSet(cmd);
  while(rs and rs.Movenext)

    PrintStr(Type, rs.value("t_name"), rs.value("t_isin"), rs.value("t_kind"), null, 
             null, rs.value("t_drawingdate"), rs.value("t_portfolio"), null, rs.value("t_count"), null, null);
  end;

  csvFile.FileClose();
  csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
  csvTable.EndTable();

  printEngine.CopyAllSheetInTotalBook("Протокол",         //Имя закладки,по умолчанию имя закладки из шаблона
                                    false, // true - на новый лист, false - на существующий(если имя листа отличается, всегда на новый лист)
                                    csvTable.GetTableRange(),     //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                      0,
                                    "Протокол") ;
end;

/*
  Инциализируем класс Отчёта
*/
macro initRep()
    printEngine=CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, true, false);

    if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
    end;

    printEngine.CreateTotalBook();

    InitProgress(-1, "", "");

    printEngine.OpenTemplate("report_OD24.xlsx", true);
    printEngine.SheetChange(1);
   
end;

/*
  Показать окно отчёта
*/
macro showRep()
    printEngine.Close();
    printEngine.SaveTotalBook("report_OD24");
end;

initRep();
GetDrawingCoupon(C_COUPON);
GetDrawingCoupon(C_AMORT);
GetDrawingAvoir(C_AVOIRISS);
showRep();

exit(1);

//msgbox(BegDate);