/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос выдачи наличных из TCD без проведения операции  */
/*                                                             */
/***************************************************************/

  Import BankInter, deprintr, DeskInter, ListCodf, ListCurr, ListNomn, tcd_err, delarue;

  /*** Константы ***/
  const sNoRight   = "Вы не можете|работать с TCD";
  const sDistraint = "Вам запрещено|работать с TCD";

  const
    CASHOP_RETURN     = 2,   
    SB_INCASH         = 100;  /* Вид Application при выдаче   */

  /***  Файлы Retail ***/
  file Group( "rtgroup.dbt" ) key 2;

  /***  Файлы системы TCD  ***/
  file fOper ( TCD_Oper, "TCD.DEF" ) key 0 write;
  file fLoad ( TCD_Load, "TCD.DEF" ) key 0;
  file fSlot ( TCD_Slot, "TCD.DEF" ) key 1;
  file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file fUnit ( TCD_Unit, "TCD.DEF" ) key 0 write;

  record Parm(CashParm); /* Параметры для операции Retail InitCashDoc() */

  record dlgS( "PAYM_SUM", "TCD.LBR" ) dialog;

  record dlgR( "PAY_R",    "TCD.LBR" ) dialog;

  /*** Массивы распределения банкнот по номиналам ***/
  array aNumSlot;  /* Номера слотов с данной валютой в TCD */
  array aFlag;     /* Флаг: данный слот включен в список */
  array aNmnl0;    /* Номиналы данной валюты в слотах */
  array aNmnl1;    /* Номиналы после сортировки по убыванию */
  array aRest;     /* Остатки по кассетам */
  array aSlots;    /* Массив количества банкнот в предлагаемой сумме выдачи */
  var nSlots = 0;  /* Число слотов с данной валютой в TCD */
  var Teller = "0";  /* Направление выдачи (направо/налево) */

  var {oper}, {curdate};

  var Branch = NumRealFNCash;
  var OperBrigade = GetOperBrigade;

  var LoadRef       =  0;

  /* Валюта и сумма выдачи */
  var pCurr         = 0;
  var sCurr         = "RUR";
  var pSum          = $0L;
  var rSum          = $0L;

  var LimitNotes    = 999;
  var LimitNotesA;  /* Лимит на выдачу с условием аппаратного ограничения */

  var PersonRefLoad = 0;
  var UnitRecPos    = -1;
  var TCDRef        = 0;
  var TCD_ID        = "";

  var sComment      = "";  /* Комментарий */
  var sDispense     = "";

  var FlagInsert    = FALSE;

  var saveAutoConfirm;

  var ErrorCode, ErrorText;

  var stat          = 0;
  var ret           = TRUE;
  var MaxSum        = $0L;

  var i, j, k, m, n;

  /***********************************************************/
  /*       Получение номера СОМ-порта из параметров АБС      */
  /***********************************************************/
  macro GetParams

    var
      RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\COM-ПОРТ ДЛЯ TCD",
      TypeI    = V_INTEGER,
      ValueI   = 0,
      ErrCode,
      StatG = TRUE,
      COM_PORT = 0;

    if(GetRegistryValue(RegPath, TypeI, ValueI, ErrCode) == V_INTEGER)
      COM_PORT = ValueI;
      if ( ( COM_PORT < 1 )  or  ( COM_PORT > 4 ) )
        MsgBox( "Номер COM-порта "+String(COM_PORT)+
                "|задан неправильно.|Он должен быть в интервале от 1 до 4" );
        StatG = FALSE;
      end;
    else
      MsgBox( "Не возможно получить|параметр номера COM-порта" );
      StatG = FALSE;
    end;

    if ( StatG )
      if ( Param( COM_PORT, PARITY_VAR ) != 0 )
        MsgBox( "Не возможно передать|номер СОМ-порта в DLM" );
      end;
    end;

    return StatG;

  end;

  /***********************************************************/
  /*           Изменение статуса записи в файле TCD          */
  /***********************************************************/
  macro TCDRecUpdate

    GetDirect( fUnit, UnitRecPos );
    i = 0;
    while ( i < 6 )
      aFlag(i) = 0;
      i = i + 1;
    end;
    i = 0;
    while ( i < aSize( aNmnl1 ) )
      if ( ( fUnit.Cur0 == pCurr )  AND
           ( fUnit.Nominal0 == aNmnl1(i) )  AND
           ( aFlag(0) == 0 ) )
        fUnit.Rest0 = fUnit.Rest0 + aSlots(i);
        aFlag(0) = 1;
      end;
      if ( ( fUnit.Cur1 == pCurr )  AND
           ( fUnit.Nominal1 == aNmnl1(i) )  AND
           ( aFlag(1) == 0 ) )
        fUnit.Rest1 = fUnit.Rest1 + aSlots(i);
        aFlag(1) = 1;
      end;
      if ( ( fUnit.Cur2 == pCurr )  AND
           ( fUnit.Nominal2 == aNmnl1(i) )  AND
           ( aFlag(2) == 0 ) )
        fUnit.Rest2 = fUnit.Rest2 + aSlots(i);
        aFlag(2) = 1;
      end;
      if ( ( fUnit.Cur3 == pCurr )  AND
           ( fUnit.Nominal3 == aNmnl1(i) )  AND
           ( aFlag(3) == 0 ) )
        fUnit.Rest3 = fUnit.Rest3 + aSlots(i);
        aFlag(3) = 1;
      end;
      if ( ( fUnit.Cur4 == pCurr )  AND
           ( fUnit.Nominal4 == aNmnl1(i) )  AND
           ( aFlag(4) == 0 ) )
        fUnit.Rest4 = fUnit.Rest4 + aSlots(i);
        aFlag(4) = 1;
      end;
      if ( ( fUnit.Cur5 == pCurr )  AND
           ( fUnit.Nominal5 == aNmnl1(i) )  AND
           ( aFlag(5) == 0 ) )
        fUnit.Rest5 = fUnit.Rest5 + aSlots(i);
        aFlag(5) = 1;
      end;
      i = i + 1;
    end;
    return Update( fUnit );

  end;

  /***********************************************************/
  /*    Обработка событий диалога установки суммы и валюты   */
  /***********************************************************/
  macro EventProcS( dlg, cmd, id, key )
    if ( cmd == DLG_KEY )    /* Нажата клавиша ... */
      if ( key == 317 )      /* ... F3 */
        if ( ( id == 1 )  AND    /* В поле "Валюта" */
             ( ListCurrency() >= 0 ) )
          pCurr   = Curr.Code_Currency;
          sCurr   = Curr.Short_Name;
          dlg(id) = Curr.Short_Name;
          UpdateFields( dlg, id );
        end;
      elif ( key == 318 )  /* F4 */
        GetString( sComment, "Комментарий по операции", 70 );
      end;
    elif ( cmd == DLG_SAVE )
      if ( ( pCurr != fUnit.Cur0 )  AND
           ( pCurr != fUnit.Cur1 )  AND
           ( pCurr != fUnit.Cur2 )  AND
           ( pCurr != fUnit.Cur3 )  AND
           ( pCurr != fUnit.Cur4 )  AND
           ( pCurr != fUnit.Cur5 )  AND
           ( pCurr != fUnit.Cur6 ) )
        MsgBox( "Валюта ", sCurr, " не установлена|при текущей загрузке" );
        SetFocus( dlg, 1 );
        return CM_CANCEL;
      end;
      MaxSum = $0L;
      if   ( pCurr == fUnit.Cur0 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal0 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur1 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal1 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur2 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal2 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur3 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal3 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur4 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal4 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur5 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal5 * LimitNotes );
      end;
      if ( pCurr == fUnit.Cur6 )
        MaxSum = MaxSum + MoneyL( fUnit.Nominal6 * LimitNotes );
      end;
      if ( dlg.Sum < $0L )
        MsgBox( "Сумма не должна быть|отрицательной" );
        SetFocus( dlg, 0 );
        return CM_CANCEL;
      end;
      if ( dlg.Sum > MaxSum )
        MsgBox( "Сумма не должна быть|больше ", MaxSum );
        SetFocus( dlg, 0 );
        return CM_CANCEL;
      end;
      if ( ( fUnit.MaxSum != $0L )  AND  ( dlg.Sum > fUnit.MaxSum ) )
        MsgBox( "Задана сумма ", dlg.Sum,
                "|больше лимита выдачи ", fUnit.MaxSum );
        SetFocus( dlg, 0 );
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  end;

  /***********************************************************/
  /*   Обработка событий диалога распределения по номиналам  */
  /***********************************************************/
  macro EventProcR( dlg, cmd, id, key )
    if ( cmd == DLG_REMFOCUS )  /* Выход из поля */
      if ( id < 21 )
        if ( ( id / 3 ) <= nSlots )
          if   ( dlg(id) < 0 )
            MsgBox( "Число банкнот|не должно быть отрицательным" );
            return CM_CANCEL;
          elif ( dlg(id) > LimitNotes )
            MsgBox( "Число банкнот|не должно быть больше ", LimitNotes );
            return CM_CANCEL;
          else
            dlg(id+1) = MoneyL( dlg(id-1) * dlg(id) );
            i = 0;
            rSum = $0L;
            while ( i < nSlots )
              rSum = rSum + dlg(5+i*3);
              i = i + 1;
            end;
            dlg.T = rSum;
            UpdateFields( dlg );
          end;
        else
          dlg(id) = 0;
          UpdateFields( dlg, id );
        end;
      end;
    /*
    elif ( cmd == DLG_KEY )    /* Нажата клавиша ... */
      if ( key == 318 )  /* F4 */
        GetString( sComment, "Комментарий по операции", 70 );
      end; */
    elif ( cmd == DLG_SAVE )
      if ( ( id < 21 )  AND  ( ( id / 3 ) <= nSlots ) )
        dlg(id+1) = MoneyL( dlg(id-1) * dlg(id) );
      end;
      i = 0;
      rSum = $0L;
      while ( i < nSlots )
        rSum = rSum + dlg(5+i*3);
        i = i + 1;
      end;
      dlg.T = rSum;
      UpdateFields( dlg );
      if ( rSum > pSum )
        if ( NOT GetTrue( FALSE, "Итоговая сумма " + String(rSum) +
                                 " превышает запрошенную " + String(pSum) +
                                 ". Продолжить?" ) )
          return CM_CANCEL;
        end;
      end;
    end;
    return CM_DEFAULT;
  end;

  /***********************************************************/
  /* Установка данных по слотам: номиналы для данной валюты */
  /***********************************************************/
  macro SetSlots

    var SS = pSum;
    var S = 0, T = 0, U = 0, SN = 0, P = 0;
    var flag = TRUE;

    LimitNotesA = LimitNotes;

    /* Заполнение массива номиналов для заданной валюты */
    n = 0;
    if ( ( fUnit.Cur0 == pCurr )  AND  ( aRest(0) > LimitNotesA ) )
      aNumSlot(n) = 1;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal0;
      n = n + 1;
    end;
    if ( ( fUnit.Cur1 == pCurr )  AND  ( aRest(1) > LimitNotesA ) )
      aNumSlot(n) = 2;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal1;
      n = n + 1;
    end;
    if ( ( fUnit.Cur2 == pCurr )  AND  ( aRest(2) > LimitNotesA ) )
      aNumSlot(n) = 3;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal2;
      n = n + 1;
    end;
    if ( ( fUnit.Cur3 == pCurr )  AND  ( aRest(3) > LimitNotesA ) )
      aNumSlot(n) = 4;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal3;
      n = n + 1;
    end;
    if ( ( fUnit.Cur4 == pCurr )  AND  ( aRest(4) > LimitNotesA ) )
      aNumSlot(n) = 5;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal4;
      n = n + 1;
    end;
    if ( ( fUnit.Cur5 == pCurr )  AND  ( aRest(5) > LimitNotesA ) )
      aNumSlot(n) = 6;
      aFlag(n)    = 0;
      aNmnl0(n)   = fUnit.Nominal5;
      n = n + 1;
    end;

    if ( n == 0 )
      return 6;  /* Слотов с указанной валютой не обнаружено */
    else
      nSlots = n;
    end;

    aNmnl1(0) = aNmnl0(0);
    aNmnl1(1) = 0;
    aNmnl1(2) = 0;
    aNmnl1(3) = 0;
    aNmnl1(4) = 0;
    aNmnl1(5) = 0;

    /* Сортировка номиналов по возрастанию */
    i = 0;
    while ( i < n )
      j = 0;
      flag = TRUE;
      while ( j <= i )
        if ( aNmnl0(i) > aNmnl1(j) )
          if ( flag )
            k = i;
            while( k > j )
              aNmnl1(k) = aNmnl1(k-1);
              k = k - 1;
            end;
            aNmnl1(j) = aNmnl0(i);
            flag = FALSE;
          end;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;

    n = n - 1;

    /* Распределение суммы по номиналам */
    aSlots(0) =   0;
    aSlots(1) =   0;
    aSlots(2) =   0;
    aSlots(3) =   0;
    aSlots(4) =   0;
    aSlots(5) =   0;

    dlgR.SS = pSum;
    dlgR.CC = sCurr;
    dlgR.NN = n+1;

    dlgR.N0 = aNmnl1(0);
    dlgR.N1 = aNmnl1(1);
    dlgR.N2 = aNmnl1(2);
    dlgR.N3 = aNmnl1(3);
    dlgR.N4 = aNmnl1(4);
    dlgR.N5 = aNmnl1(5);

    dlgR.M0 = 0;
    dlgR.M1 = 0;
    dlgR.M2 = 0;
    dlgR.M3 = 0;
    dlgR.M4 = 0;
    dlgR.M5 = 0;

    dlgR.S0 = 0;
    dlgR.S1 = 0;
    dlgR.S2 = 0;
    dlgR.S3 = 0;
    dlgR.S4 = 0;
    dlgR.S5 = 0;

    dlgR.T  = 0;

    if ( aNmnl1(n) != 0 )
      S = (Int(Double(SS)) / aNmnl1(n)) * aNmnl1(n);
    else
      return 21;
    end;

    if ( S == 0 )
      MsgBox( "Сумма к выдаче ", pSum,
              "|меньше наименьшего номинала ", aNmnl1(n) );
      return 22;
    end;

    i = 0;
    while ( i <= n )
      if ( aNmnl1(i) < S )
        SN = SN + aNmnl1(i);
      end;
      i = i + 1;
    end;

    if ( SN != 0 )
      P = S / SN + 1;
    else
      P = 0;
    end;

    i = 0;
    while ( i <= n )
      U = ( S - T ) / aNmnl1(i);
      if ( U > LimitNotes )
        U = LimitNotes;
      end;
      if ( ( i < n )  and ( U > P ) )
        aSlots(i) = P;
      else
        aSlots(i) = U;
      end;
      T = T + aNmnl1(i)*aSlots(i);
      i = i + 1;
    end;

    dlgR.M0 = aSlots(0);
    dlgR.M1 = aSlots(1);
    dlgR.M2 = aSlots(2);
    dlgR.M3 = aSlots(3);
    dlgR.M4 = aSlots(4);
    dlgR.M5 = aSlots(5);

    dlgR.S0 = Money(aNmnl1(0)*aSlots(0));
    dlgR.S1 = Money(aNmnl1(1)*aSlots(1));
    dlgR.S2 = Money(aNmnl1(2)*aSlots(2));
    dlgR.S3 = Money(aNmnl1(3)*aSlots(3));
    dlgR.S4 = Money(aNmnl1(4)*aSlots(4));
    dlgR.S5 = Money(aNmnl1(5)*aSlots(5));

    dlgR.T  = Money( T );

    if ( RunDialog( dlgR, "EventProcR" ) )
      i = 0;
      while ( i < nSlots )
        aSlots(i) = dlgR( 4 + 3*i );
        i = i + 1;
      end;
      /* rSum = dlgR.T; */
      sComment = dlgR.RR;
      return 0;
    else
      return 9;
    end;

  end;

  /***********************************************************/
  /*                   Определение сейфа                     */
  /***********************************************************/
  macro GetNativeSafe
    Group.Branch = Branch;
    Group.Date   = {curdate};
    Group.ID     = OperBrigade;
    if ( GetEQ(Group) )
      return Group.SafeID;
    else
      MsgBox("Не найден сейф бригады № ", OperBrigade);
      Exit;
    end;
  end;

  /***********************************************************/
  /***             Процедура выдачи без оперции            ***/
  /***********************************************************/

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    if ( fPers.Status != 0 )
      MsgBox( sDistraint );
      stat = 1;
    else
      PersonRefLoad = fPers.Reference;
      if ( fPers.Direction == 0 )
        Teller = "0";   /* Направо */
      else
        Teller = "1";   /* Налево */
      end;
    end;
  else
    MsgBox( sNoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      UnitRecPos = GetPos( fUnit );
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
      if ( fUnit.MaxNotes == 0 )
        LimitNotes = 999;
      else
        LimitNotes = fUnit.MaxNotes;
      end;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Проверка статуса загрузки */
  if ( stat == 0 )
    if ( fUnit.LoadStatus != 2 )
      MsgBox( "TCD \"", TCD_ID, "\" не загружен.|Выдача невозможна" );
      stat = 4;
      return 4;
    else
      fLoad.Reference = fUnit.LoadRef;
      if ( GetEQ( fLoad ) )
        LoadRef = fLoad.Reference;
      else
        stat = 27;
        return 27;
      end;
    end;
  end;

  /* Получение количества загруженных банкнот */
  if ( stat == 0 )
    i = 0;
    while ( i < 6 )
      aRest(i) = 0;
      i = i + 1;
    end;
    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGT( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )
      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        aRest(fSlot.SlotNumber-1) = fSlot.SlotQuantityLoad;
        ret = Next( fSlot );
      end;
    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      stat = 17;
      return 17;
    end;
    if ( stat == 0 )
      aRest(0) = aRest(0) - fUnit.Rest0;
      aRest(1) = aRest(1) - fUnit.Rest1;
      aRest(2) = aRest(2) - fUnit.Rest2;
      aRest(3) = aRest(3) - fUnit.Rest3;
      aRest(4) = aRest(4) - fUnit.Rest4;
      aRest(5) = aRest(5) - fUnit.Rest5;
    end;
  end;

  /* Диалог задания суммы выдачи */
  if ( stat == 0 )
    dlgS.Curr = "RUR";
    if ( RunDialog( dlgS, "EventProcS" ) )
      pSum = dlgS.Sum;
    else
      stat = 5;
    end;
  end;

  /* Распределение суммы по кассетам */
  if ( stat == 0 )
    stat = SetSlots();
  end;

  /* Создание записи об операции TCD */
  if ( stat == 0 )
    ClearRecord( fOper );
    fOper.Reference        = 0;
    fOper.Type1            = 0; /* Платеж */
    fOper.Type2            = 0; /* Резервный тип */
    fOper.Status           = 1; /* Инициация опреации */
    fOper.FNCash           = Branch;
    fOper.Date             = Date();
    fOper.Time             = Time();
    fOper.CurDate          = {curdate};
    fOper.TCDRef           = TCDRef;
    fOper.PersonRef        = PersonRefLoad;
    fOper.Direction        = fPers.Direction;
    fOper.ErrorStep        = 0;
    fOper.ErrorCode        = 0;
    fOper.Sum              = rSum;
    fOper.Currency         = pCurr;
    fOper.iApplicationKind = 0;
    fOper.ApplicationKey   = "";
    fOper.Commentary       = sComment;
    FlagInsert = Insert( fOper );
    if ( NOT FlagInsert )
      ErrorCode = Status( ErrorText );
      MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD: ", ErrorText );
      stat = 10;
    end;
  end;

  /* Операция Retail "Пополнение сейфа кассира из TCD" */
  if ( stat == 0 )
    ClearRecord(Parm);
    Parm.Type            = 0;      /* Платежеспосбные ресурсы */
    Parm.TypeValue       = 1;      /* Деньги */
    Parm.CodIntValue     = pCurr;  /* Валюта */
    Parm.CashOper        = TCD_ID;
    Parm.CashDateDoc     = {curdate};
    Parm.NumOper         = CASHOP_RETURN;
    Parm.ValueMoney      = rSum;
    Parm.ApplicationKind = SB_INCASH;
    Parm.ApplicationKey  = FormApplicationKey( SB_INCASH );
    Parm.CashPatern      = GetNativeSafe;
    /* Parm.CashPatern      = "TCD 1"; */
    Parm.Ground          = SubStr( sComment, 1, 36 );
    saveAutoConfirm      = AutoConfirm(AC_STRONG);  /* Включение автоподтверждения */
    if( InitCashDoc(Parm) )
      InterDesk_EndDocBunch;
    else
      stat = 11;
    end;
    AutoConfirm(saveAutoConfirm);
  end;

  /* Изменение статуса опреации TCD и сохранение Application Reatil */
  if ( ( stat == 0 )  AND  FlagInsert )
    if ( GetPos( fOper )  AND  GetDirect( fOper ) )
      fOper.Status           = 2; /* Операция Retail прошла успешно */
      fOper.iApplicationKind = Parm.ApplicationKind;
      fOper.ApplicationKey   = Parm.ApplicationKey;
      if ( NOT Update( fOper ) )
        ErrorCode = Status( ErrorText );
        MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
        stat = 12;
      end;
    else
      ErrorCode = Status( ErrorText );
      MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
      stat = 12;
    end;
  end;

  /* Команда на выдачу  */
  if ( stat == 0 )
    /* Формирование строки распределения банкнот */
    i = 0;
    sDispense = Teller;
    while ( i < nSlots )
      j = 0;
      while ( j < nSlots )
        if ( ( aNmnl0(i) == aNmnl1(j) )  AND
             ( aFlag(j)  == 0 )          AND
             ( aSlots(j) != 0 ) )
        sDispense = sDispense +
                    String(aNumSlot(i)) +
                    SubStr(String((aSlots(j)+1000):4),2,3);
        aFlag(j) = 1;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
    ErrInit();
    if ( GetParams() )
      stat = Dispense( sDispense );
    else
      stat = 57;
    end;
    if ( ( stat > 0 )  AND  ( stat < 54 ) )
      ErrMess( stat );
      if ( ( stat == 1 )  OR  ( stat == 4 ) )  /* Число банкнот < 150-200 или сброс в Reject-кассету. */
        stat = 0;
      end;
    end;
  end;

  /* Изменение статуса опреации TCD */
  if ( stat == 0 )
    if ( FlagInsert )
      fOper.Status = 0; /* Выдача прошла успешно */
      if ( NOT Update( fOper ) )
        ErrorCode = Status( ErrorText );
        MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
        stat = 20;
      end;
      /* Изменение остатков по слотам */
      if ( stat == 0 )
        if ( NOT TCDRecUpdate() )
          stat = 21;
        end;
      end;
    else
      stat = 24;
    end;
  else
    if ( FlagInsert )
      fOper.Status = 3; /* Ош.выдачи, начато удаление оп.Reatil */
      if ( NOT Update( fOper ) )
        ErrorCode = Status( ErrorText );
        MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
        stat = 25;
      end;
      if ( stat == 0 )
        if ( UnconfirmCashDoc( SB_INCASH, Parm.ApplicationKey ) )
          fOper.Status = 4; /* Ош.выдачи, операция Reatil удалена */
          if ( NOT Update( fOper ) )
            ErrorCode = Status( ErrorText );
            MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
            stat = 23;
          end;
        end;
      end;
    end;
  end;

  /* Завершение работы */
  if ( stat == 0 )
    if ( NOT GetTrue( TRUE, "Сумма " + String(rSum:a) + " " + sCurr +
                      " получена правильно ?" ) )
      GetString( sComment, "Комментарий по операции", 70 );
      fOper.Commentary = sComment;
      fOper.Status = 6; /* Деньги при выдаче получены неправильно */
      if ( NOT Update( fOper ) )
        ErrorCode = Status( ErrorText );
        MsgBox( "Ошибка ", ErrorCode, " при записи опрации TCD:|", ErrorText );
        stat = 25;
      else
        stat = 5;
      end;
    end;
  else
    if ( (stat != 1 )  AND  ( stat != 5 )  AND  ( stat != 22 ) )
      if   ( stat == 6 )
        MsgBox( "Не найдено кассет по валюте ", sCurr,
                "|с подходящим числом банкнот для выдачи" );
      elif ( stat == 9 )
        MsgBox( "Вы отказались от выдачи" );
      else
        MsgBox( "При выдаче произошла ошибка ", stat );
      end;
    end;
  end;

end;
