import rslscrp;

file   Curr ( currency ) key 0;
record scrR ( ListCurr, "TCD.LBR" ) dialog;

/* Заданные суммы загрузки по валютам */
var   nLoadCur;
array LoadCur, LoadCurName, LoadSum;

/********************************************************/
/*               Скролинг списка валют                  */
/********************************************************/
macro ListCurrency

  SetScroll( Curr, "F0_", "Short_Name" );

  if ( RunDialog( scrR, "ScrollMes" ) )
    return Curr.Code_Currency;
  else
    return -1;
  end;

end;

/********************************************************/
/*                    Поиск валюты                      */
/********************************************************/
macro FindCurrency( CodeCur )

  ReWind(Curr);
  Curr.Code_Currency = CodeCur;
  return GetEQ(Curr);

end;

/********************************************************/
/*    Проверка правильности задания сумм по валютам     */
/********************************************************/
macro TestCurr( IsCur )
  var i = 0, j = 0;
  var zeroCur = TRUE, zeroSum = TRUE, errCur = 0, dubl = 0;
  while ( i < nLoadCur )
    if ( LoadCur(i) != -1 )
      zeroCur = FALSE;
    end;
    if ( ( LoadCur(i) != -1 ) AND ( LoadSum(i) != $0L ) )
      zeroSum = FALSE;
    end;
    if ( ( IsCur == 0 ) AND ( LoadCur(i) != 0 ) )
      errCur = -3;
    end;
    if ( ( IsCur == 1 ) AND ( LoadCur(i) == 0 ) )
      errCur = -4;
    end;
    j = i + 1;
    while ( j < nLoadCur )
      if ( ( LoadCur(i) != -1 )  AND  ( LoadCur(i) == LoadCur(j) ) )
        dubl = j;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;
  if ( errCur )
    return errCur;
  end;
  if ( zeroCur )
    return -1;
  end;
  if ( zeroSum )
    return -2;
  end;
  return dubl;
end;
