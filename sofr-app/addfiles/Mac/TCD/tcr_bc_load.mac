//

import BankInter, DeprIntr, DeskInter;
import RCW, RSD;
import tcr_bc_err;

/***  Файлы системы TCD  ***/
private file fLoad ( TCD_Load, "TCD.DEF" ) key 0;
private file fSlot ( TCD_Slot, "TCD.DEF" ) key 2;
private file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
private file fUnit ( TCD_Unit, "TCD.DEF" ) key 0;

/*** Константы ***/
private const CASHOP_REINFORCE = 1;  // Подкрепление кассира
private const CASHOP_RETURN    = 2;  // Возврат средств в кладовую
private const SB_EXTCASH       = 150;  // При загрузке

private var TCD_ID = "BrnCash1";

private var {oper}, {curdate};

private var Branch = NumFNCash();
private var OperBrigade = GetOperBrigade();

/* Параметры для функции InitCashDoc */
private record Parm( CashParm );

private file Group( "rtgroup.dbt" ) key 2;

private var agent = CreateObject( "rtbrcash.d32", "TBranchCashAgent", NULL, false );

private var bce = BC_ErrCode;
private var res = 0;
private var ret = TRUE;

private var sX1 = "", sX2 = "";
private array cCURR1, cSUM1;
private array cCURR2, cSUM2, cSUM;

/***********************************************************/
/*               Поиск валюты в массивах                   */
/***********************************************************/
private macro getArrCurr( curr, vvv )
  var i = 0, n = 0;
  if ( vvv == 1 )
    n = aSize( cCURR1 );
  else
    n = aSize( cCURR2 );
  end;
  while ( i < n )
    if ( vvv == 1 )
      if ( cCURR1[i] == curr )
        return i;
      end;
    else
      if ( cCURR2[i] == curr )
        return i;
      end;
    end;
    i = i + 1;
  end;
  return -1;
end;

/***********************************************************/
/*                  Рассчет сумм по XML                    */
/***********************************************************/
private macro SumFromXML( s, vvv )
  var c1, v1, v2, t1, t2;
  var vC, vV, vT;
  var i = Index( s, "<Cassette>" );
  var ii = 0;

  while ( i > 0 )
    c1 = Index( SubStr( s, i ), "<Currency>" );

    v1 = Index( SubStr( s, i ), "<Value>" );
    v2 = Index( SubStr( s, i ), "</Value>" );

    t1 = Index( SubStr( s, i ), "<Count>" );
    t2 = Index( SubStr( s, i ), "</Count>" );

    vC = SubStr( s, i + c1 + 9, 3 );
    vV = Int( SubStr( s, i + v1 + 6, v2 - v1 - 7 ) );
    vT = Int( SubStr( s, i + t1 + 6, t2 - t1 - 7 ) );

    ii = getArrCurr( vC, vvv );

    if ( vvv == 1 )
      if ( ii > -1 )
        cSUM1[ii] = cSUM1[ii] + ( vV * vT );
      else
        ii = aSize( cCURR1 );
        cCURR1[ii] = vC;
        cSUM1[ii]  = vV * vT;
      end;
    else
      if ( ii > -1 )
        cSUM2[ii] = cSUM2[ii] + ( vV * vT );
      else
        ii = aSize( cCURR2 );
        cCURR2[ii] = vC;
        cSUM2[ii]  = vV * vT;
      end;
    end;

    s = SubStr( s, 1, i - 1 ) + "*" + SubStr( s, i + 1 );
    i = Index( s, "<Cassette>" );
  end;
end;

/***********************************************************/
/*                   Определение сейфа                     */
/***********************************************************/
private macro GetNativeSafe
  Group.Branch = Branch;
  Group.Date   = {curdate};
  Group.ID     = OperBrigade;
  if ( GetEQ( Group ) )
    return Group.SafeID;
  else
    MsgBox( "Не найден сейф бригады № ", OperBrigade );
    Exit;
  end;
end;

/*************************************************************/
/*                   Получение кода валюты                   */
/*************************************************************/
private macro getCodeCurr( shnm )
  var cmd, rs;

  cmd = RsdCommand( "select t_short_name from dcurrency_dbt where t_code_currency = ? " );
  cmd = RsdCommand( "select t_code_currency from dcurrency_dbt where t_short_name = ? " );

  cmd.addParam( "sn", RSDBP_IN );
  cmd.value( "sn" ) = shnm;

  cmd.execute();
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    return rs.value( 0 );
  else
    return -1;
  end;
end;

/*************************************************************/
/* Процедура транзакции операций Retail пополнения сейфа TCD */
/*************************************************************/
private macro TrnProcCash
  var Ret = FALSE;
  var iii = 0, jjj = 0, nnn = aSize( cCURR2 );
  var numOp = CASHOP_REINFORCE;
  var sum = 0;

  while ( iii < nnn )

    if ( cSUM[iii] != $0 )

      if ( cSUM[iii] > 0 )
        numOp = CASHOP_REINFORCE;
        sum = cSUM[iii];
      else
        numOp = CASHOP_RETURN;
        sum = -cSUM[iii];
      end;

      ClearRecord( Parm );
      Parm.Type            = 0;           /* Платежеспосбные ресурсы */
      Parm.TypeValue       = 1;           /* Деньги */
      Parm.CodIntValue     = getCodeCurr( cCURR2[iii] );
      Parm.CashOper        = TCD_ID;
      Parm.CashDateDoc     = {curdate};
      Parm.NumOper         = numOp;
      Parm.ValueMoney      = sum * 1.00;
      Parm.ApplicationKind = SB_EXTCASH;
      Parm.ApplicationKey  = FormApplicationKey( SB_EXTCASH );
      Parm.CashPatern      = GetNativeSafe();
      Parm.Ground          = "Приход в сейф ЭКР \"" + TCD_ID + "\"";
      Ret = InitCashDoc( Parm );
      if ( Ret )
      else
        AbortTrn();
      end;

    end;
    iii = iii + 1;
  end; 
end;

/*************************************************************/
//                        Загрузка.
/*************************************************************/
  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    if ( fPers.Status != 0 )
      MsgBox( "Вам запрещено|работать с TCD" );
      ret = FALSE;
    end;
  else
    MsgBox( "Вы не можете|работать с TCD" );
    ret = FALSE;
  end;

  /* Поиск записи по TCD */
  if ( ret )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      TCD_ID = fUnit.ID;
    else
      MsgBox( "Запись по TCD|не найдена" );
      ret = FALSE;
    end;
  end;

  if ( ret  AND  ( agent.login( 1, 1 ) ) )
    agent.OpenDay(1);
    if ( agent.GetSafeInfo2( 1, sX1 ) )
      if ( agent.Replenishment3( 1, "1" ) )  // Загрузка.
        if ( agent.GetSafeInfo2( 1, sX2 ) )
          var saveAutoConfirm = AutoConfirm( AC_STRONG );  /* Включение автоподтверждения */
          var iii = 0, jjj = 0, nnn = 0;

          SumFromXML( sX1, 1 );
          SumFromXML( sX2, 2 );

          nnn = aSize( cCURR2 );

          // Подсчет сумм загрузки.
          while ( iii < nnn )
            jjj = getArrCurr( cCURR2[iii], 1 );
            if ( jjj > -1 )  // Валюта cCURR1[jjj] имеется.
              cSUM[iii] = cSUM2[iii] - cSUM1[jjj];
              // MsgBox( "Ошибка в сумме загрузки: валюта " + cCURR2[iii] + ", сумма до " + cSUM1[jjj] + " > суммы после " + cSUM2[iii] );
            else
              cSUM[iii] = cSUM2[iii];
            end;
            iii = iii + 1;
          end;

          if ( NOT ProcessTrn( 1, "TrnProcCash" ) )
            MsgBox( "Ошибка прихода в сейф ЭКР" );
          end;
          AutoConfirm( saveAutoConfirm );
        else
          MsgBox( "Ошибка GetSafeInfo2() после загрузки" );
          ret = FALSE;
        end;
      else
        ret = FALSE;
      end;
      agent.GetLastBranchCashResult( res );  // Получение кода ошибки.
      if ( res != 0 )
        MsgBox( bce.ErrMsg( res ) );  // Сообщение об ошибке.
        ret = FALSE;
      end;
    else
      MsgBox( "Ошибка GetSafeInfo2() перед загрузкой" );
      ret = FALSE;
    end;
    return ret;
  end;

return ret;
end;
