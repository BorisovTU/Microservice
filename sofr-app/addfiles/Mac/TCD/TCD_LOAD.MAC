/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос загрузки TCD                                    */
/*                                                             */
/***************************************************************/

  Import BankInter, deprintr, DeskInter, ListCodf, ListCurr, ListNomn, tcd_err, delarue;

  /*** Константы ***/
  const CASHOP_REINFORCE = 1;    
  const SB_EXTCASH     = 150;  /* При загрузке */

  const NoRight = "Вы не можете|работать с TCD";

  /***  Файлы Retail  ***/
  file person   (person, "bank.def" ) key 0; /* Список операционистов */
  file Group( "rtgroup.dbt" ) key 2;

  /***  Файлы системы TCD  ***/
  file fLoad ( TCD_Load, "TCD.DEF" ) key 0 write;
  file fSlot ( TCD_Slot, "TCD.DEF" ) key 2 write;
  file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file fUnit ( TCD_Unit, "TCD.DEF" ) key 0 write;

  /* Параметры для функции InitCashDoc */
  record Parm(CashParm);

  /* Диалоги задания сумм загрузки по валютам */
  record dlgS1( "Sum1", "tcd.lbr" ) dialog;
  record dlgS2( "Sum2", "tcd.lbr" ) dialog;
  record dlgS3( "Sum3", "tcd.lbr" ) dialog;
  record dlgS4( "Sum4", "tcd.lbr" ) dialog;
  record dlgS5( "Sum5", "tcd.lbr" ) dialog;
  record dlgS6( "Sum6", "tcd.lbr" ) dialog;

  /* Диалог запуска загрузки */
  record dlgN0( "LoadOpen", "tcd.lbr" ) dialog;

  /* Диалоги задания номиналов загрузки */
  record dlgN1( "Load1",    "tcd.lbr" ) dialog;
  record dlgN2( "Load2",    "tcd.lbr" ) dialog;
  record dlgN3( "Load3",    "tcd.lbr" ) dialog;
  record dlgN4( "Load4",    "tcd.lbr" ) dialog;
  record dlgN5( "Load5",    "tcd.lbr" ) dialog;
  record dlgN6( "Load6",    "tcd.lbr" ) dialog;

  var {oper}, {curdate};

  var Branch = NumFNCash;
  var OperBrigade = GetOperBrigade;

  /* Массивы параметров загрузки */
  array Slots, SlotCass, SlotStat, Cassette, Currency, Nominal, Notes, SlotSum;
  array Cur_R, Cur_N, Sum_P, Sum_R;
  array ApplRetail;
  array aStat;

  /* Дата и время загрузки */
  var LoadDate, LoadTime;

  var retLoad = 0;   /* Код возврата команды загрузки  */
  var strLoad = "123456789012345678901234567890";  /* Строка перечисления кассет  */

  var CassettesNumb;  /* Число кассет в TCD */

  var stat          = 0;
  var PersonRefLoad = 0;
  var TCDRef        = 0;
  var TCD_ID        = "";
  var LoadRef       = 0;
  var UnitRecPos    = -1;
  var LoadRecPos    = -1;
  var StatComTCD    = 0;
  var sCommentary   = "";
  var FlagUnLoad    = TRUE;
  var saveAutoConfirm;

  var CassetteCurr = -1;
  var CassetteNmnl = -1;

  /* Вспомогательные переменные */
  var i, j, k;
  var Flag;
  var Ret;
  var StatT = 0;

  /***********************************************************/
  /*                   Определение сейфа                     */
  /***********************************************************/
  macro GetNativeSafe
    Group.Branch = Branch;
    Group.Date   = {curdate};
    Group.ID     = OperBrigade;
    if ( GetEQ(Group) )
      return Group.SafeID;
    else
      MsgBox("Не найден сейф бригады № ", OperBrigade);
      Exit;
    end;
  end;

  /***********************************************************/
  /*       Получение номера СОМ-порта из параметров АБС      */
  /***********************************************************/
  macro GetParams

    var
      RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\COM-ПОРТ ДЛЯ TCD",
      TypeI    = V_INTEGER,
      ValueI   = 0,
      ErrCode,
      StatG = TRUE,
      COM_PORT = 0;

    if(GetRegistryValue(RegPath, TypeI, ValueI, ErrCode) == V_INTEGER)
      COM_PORT = ValueI;
      if ( ( COM_PORT < 1 )  or  ( COM_PORT > 4 ) )
        MsgBox( "Номер COM-порта "+String(COM_PORT)+
                "|задан неправильно.|Он должен быть в интервале от 1 до 4" );
        StatG = FALSE;
      end;
    else
      MsgBox( "Не возможно получить|параметр номера COM-порта" );
      StatG = FALSE;
    end;

    if ( StatG )
      if ( Param( COM_PORT, PARITY_VAR ) != 0 )
        MsgBox( "Не возможно передать|номер СОМ-порта в DLM" );
      end;
    end;

    return StatG;

  end;


  /***********************************************************/
  /*         Поиск операциониста в базе данных  */
  /***********************************************************/
  macro GetPerson (CodePerson)
    person.Oper = CodePerson;
    return GetEQ(person);
  end;

  /***********************************************************/
  /*         Обработка событий панели запуска загрузки       */
  /***********************************************************/
  macro EventProc0( dlg, cmd, id, key )
    if ( cmd == DLG_BUTTON )
      Message( "Выполняется загрузка TCD ..." );
      return CM_CANCEL;
    elif ( ( cmd == DLG_KEY ) AND ( key ==  27 ) )  /* Нажата клавиша Esc */
      return CM_IGNORE;
    end;
    return CM_DEFAULT;
  end;

  /***********************************************************/
  /*   Обработка событий диалога установки сумм по валютам   */
  /***********************************************************/
  macro EventProcC( dlg, cmd, id, key )
    var r;
    if ( cmd == DLG_KEY )    /* Нажата клавиша ... */
      r = ( id - 3 ) / 2;
      if ( key == 317 )      /* ... F3 */
        if ( ( ( id - 3 - r * 2 ) == 1 )  AND    /* В поле "Валюта" */
             ( ListCurrency() >= 0 ) )
          LoadCur(r)     = Curr.Code_Currency;
          LoadCurName(r) = Curr.Short_Name;
          dlg(id)        = Curr.Short_Name;
          UpdateFields( dlg, id );
        end;
        return CM_DEFAULT;
      elif ( key == 322 )    /* ... F8 */
        if ( ( id - 3 - r * 2 ) == 1 )
          LoadSum(r)     = $0L;
          LoadCur(r)     = -1;
          LoadCurName(r) = "";
          dlg(id-1)      = $0L;
          dlg(id)        = "";
          UpdateFields( dlg, id-1 );
          UpdateFields( dlg, id );
        end;
      end;
    elif ( cmd == DLG_SAVE )
      i = 0;
      while ( i < nLoadCur )
        LoadSum(i) = dlg(i*2+3);
        i = i + 1;
      end;
      i = TestCurr( fUnit.IsCur );
      if ( i == -1 )
        MsgBox( "Ни одна валюта не определена" );
        SetFocus( dlg, 0 );
        return CM_CANCEL;
      elif ( i == -2 )
        MsgBox( "Ни одна сумма не задана" );
        SetFocus( dlg, 1 );
        return CM_CANCEL;
      elif ( i == -3 )
        MsgBox( "Для TCD работа с валютами запрещена" );
        return CM_CANCEL;
      elif ( i == -4 )
        MsgBox( "Для TCD работа с рублями запрещена" );
        return CM_CANCEL;
      elif ( i > 0 )
        MsgBox( "Валюта ", LoadCurName(i), " задана дважды" );
        SetFocus( dlg, i * 2 );
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  end;

  /***********************************************************/
  /*            Изменение итоговых сумм по кассетам          */
  /***********************************************************/
  macro SetSumR( dlg, CurrS, SumOld, SumNew )
    var i = 0;
    var j = aSize( Cur_R );
    while ( i < j )
      if ( Cur_R(i) == CurrS )
        Sum_R(i) = Sum_R(i) - SumOld;
        Sum_R(i) = Sum_R(i) + SumNew;
        dlg( 11 + CassettesNumb * 6 + i * 3 ) = Sum_R(i);
      end;
      i = i + 1;
    end;
  end;

  /***********************************************************/
  /*      Обработка событий диалога установки номиналов      */
  /***********************************************************/
  macro EventProcN( dlg, cmd, id, key )
    var idR, idI, saveCurr, saveSumL;
    if ( ( id > 10 )  AND  ( id < 9 + 6 * CassettesNumb ) )
      idI = ( id - 11 ) / 6;
      idR = id - 11 - idI * 6;
    else
      idR = -1;
      idI = -1;
    end;
    if ( ( cmd == DLG_KEY ) )      /* Нажата клавиша ... */
      if ( key == 317 )            /* ... F3 */
        if   ( idR == 0 )          /* В поле "Валюта" */
          if ( ListCurrency() >= 0 )
            i = 0;
            Flag = FALSE;
            while ( i < nLoadCur )
              if ( ( LoadCur(i) == Curr.Code_Currency )  AND
                   ( LoadSum(i) != $0L ) )
                Flag = TRUE;
              end;
              i = i + 1;
            end;
            if ( Flag )
              saveCurr = Currency(idI);
              Currency(idI) = Curr.Code_Currency;
              dlg(11+6*idI) = Curr.Short_Name;
              saveSumL = dlg(14+6*idI);
              if ( saveCurr != Currency(idI) )  /* Валюта изменилась */
                SetSumR( dlg, saveCurr, saveSumL, $0L );
                SetSumR( dlg, Currency(idI), $0L, saveSumL );
              end;
              UpdateFields( dlg );
            else
              MsgBox( "По валюте ", Curr.Short_Name,
                      "|сумма не была задана" );
            end;
          end;
        elif ( idR == 1 )            /* В поле "Номинал" */
          if ( ListNominal( Currency(idI) ) > 0 )
            Nominal(idI) = Nomn.Nominal;
            dlg(12+6*idI) = Nomn.Nominal;
            UpdateFields( dlg, id );
          end;
        end;
      elif ( ( key ==  27 )  OR  /* ...Esc */
             ( key == 323 ) )    /* ... F9 */
        return CM_IGNORE;
      end;
    elif ( ( cmd == DLG_REMFOCUS )  AND  ( ( idR == 1 )  OR  ( idR == 2 ) ) )
      saveSumL = dlg(id+3-idR);
      dlg(id+3-idR) = MoneyL( dlg(id+1-idR) * dlg(id+2-idR) / 100. );
      SetSumR( dlg, Currency(idI), saveSumL, dlg(id+3-idR) );
      UpdateFields( dlg );
    elif ( cmd == DLG_BUTTON )
      if ( id == 9 + 9*CassettesNumb )
        FlagUnLoad = FALSE;
        return CM_SAVE;
      else
        FlagUnLoad = TRUE;
        return CM_CANCEL;
      end;
    elif ( cmd == DLG_SAVE )
      i = 0;
      while ( i < aSize(Sum_P) )
        if ( Sum_P(i) != Sum_R(i) )
          SetFocus( dlg, 13+6*i );
          MsgBox( "Для валюты ", Cur_N(i),
                  "|сумма по банкнотам ", Sum_R(i):a,
                  "|не совападает с заданной ", Sum_P(i):a );
          return CM_CANCEL;
        end;
        i = i + 1;
      end;
    end;
    return CM_DEFAULT;
  end;

  /***********************************************************/
  /*           Изменение статуса записи в файле TCD          */
  /***********************************************************/
  macro TCDStatRecUpdate( a_stat )

    GetDirect( fUnit, UnitRecPos );
    fUnit.LoadRef    = fLoad.Reference;
    fUnit.LoadStatus = a_stat;
    return Update( fUnit );

  end;

  /***********************************************************/
  /*                  Запись в файл загрузки                 */
  /***********************************************************/
  macro LoadRecInsert

    var retIns;

    ClearRecord(fLoad);
    fLoad.Reference      = 0;
    fLoad.Type           = 0; /* */
    fLoad.Status         = 1;
    fLoad.FNCash         = Branch;
    fLoad.TCDRef         = TCDRef;
    fLoad.Date           = LoadDate;
    fLoad.Time           = LoadTime;
    fLoad.CurDate        = {curdate};
    fLoad.RejectCassette = 0;
    fLoad.LoadReturn     = 0;
    fLoad.PersonRefLoad  = PersonRefLoad;
    fLoad.Commentary     = "";

    retIns = Insert( fLoad );

    if ( retIns )
      LoadRef    = fLoad.Reference;
      LoadRecPos = GetPos( fLoad );
    else
      LoadRecPos = -1;
    end;

    return retIns;

  end;

  /***********************************************************/
  /*         Изменение статуса записи в файле загрузки       */
  /***********************************************************/
  macro LoadRecUpdate( a_stat, a_ret )

    GetDirect( fLoad, LoadRecPos );

    fLoad.Status         = a_stat;
    fLoad.LoadReturn     = a_ret;
    fLoad.Commentary     = sCommentary;

    return Update( fLoad );

  end;


  /*************************************************************/
  /* Процедура транзакции операций Retail пополнения сейфа TCD */
  /*************************************************************/
  macro TrnProcCash

    i = 0;
    j = 0;
    while ( i < nLoadCur )
      if ( LoadCur(i) != -1 )
        ClearRecord(Parm);
        Parm.Type            = 0;           /* Платежеспосбные ресурсы */
        Parm.TypeValue       = 1;           /* Деньги */
        Parm.CodIntValue     = LoadCur(i);  /* Валюта */
        Parm.CashOper        = TCD_ID;
        Parm.CashDateDoc     = {curdate};
        Parm.NumOper         = CASHOP_REINFORCE;
        Parm.ValueMoney      = LoadSum(i) * 100.;  /* Сумма (*100. - коррекция Tools) */
        Parm.ApplicationKind = SB_EXTCASH;
        Parm.ApplicationKey  = FormApplicationKey(SB_EXTCASH);
        Parm.CashPatern      = GetNativeSafe;
        Parm.Ground          = "Пополнение сейфа TCD \"" + TCD_ID + "\"";
        Ret = InitCashDoc(Parm);
        if( Ret )
          ApplRetail( j ) = Parm.ApplicationKey;
          j = j + 1;
        else
          AbortTrn();
        end;
      end;
      i = i + 1;
    end;

  end;

  /*************************************************************/
  /*         Поиск индекса в массивах параметров слотов        */
  /*************************************************************/
  macro GetIndexSlot( numSlot )
    i = 0;
    while ( i <= CassettesNumb )
      if ( SlotCass(i) == numSlot )
        return i-1;
      end;
      i = i + 1;
    end;
    return -1;
  end;

  /*************************************************************/
  /*          Процедура транзакции завершения загрузки         */
  /*************************************************************/
  macro TrnProcLoad

    /* Запись данных по слотам */
    i = 0;
    j = 0;
    Ret = TRUE;
    while ( Ret  AND  ( i < 7 ) )
      if ( Slots( i ) == 1 )
        ClearRecord( fSlot );
        fSlot.LoadRef            = LoadRef;
        fSlot.TCDRef             = TCDRef;
        fSlot.SlotNumber         = i;
        fSlot.SlotStatus         = SlotStat( i );
        fSlot.SlotReturn         = SlotStat( i );
        if ( i != 0 )
          fSlot.SlotCurrency       = Currency( j );
          fSlot.SlotNominal        = Nominal( j );
          fSlot.SlotQuantityLoad   = Notes( j );
          fSlot.SlotQuantityUnLoad = 0;
          j = j + 1;
        end;
        fSlot.CassetteID = Cassette( j );

        Ret = Insert( fSlot );
        if ( NOT Ret )
          StatT = 1;
        end;

      end;
      i = i + 1;
    end;

    /* Установка статуса успешной загрузки */
    if ( StatT == 0 )
      if ( NOT LoadRecUpdate( 0, 0 ) )
        StatT = 10;
      end;
    end;

    /* Установка статуса TCD: загрузка прошла успешно */
    if ( StatT == 0 )
      GetDirect( fUnit, UnitRecPos );
      fUnit.LoadRef    = fLoad.Reference;
      fUnit.LoadStatus = 2;
      fUnit.WorkSlots  = CassettesNumb;
      /* Формирование информации по слотам для записи в файл TCD */
      if ( ( CassettesNumb > 0 )  AND  ( Slots(1) == 1 ) )
        j = GetIndexSlot( 1 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur0     = Currency(j);
          fUnit.Nominal0 = Nominal(j);
        end;
      else
        fUnit.Cur0     = -1;
        fUnit.Nominal0 = 0;
      end;
      fUnit.Rest0 = 0;
      if ( ( CassettesNumb > 1 )  AND  ( Slots(2) == 1 ) )
        j = GetIndexSlot( 2 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur1     = Currency(j);
          fUnit.Nominal1 = Nominal(j);
        end;
      else
        fUnit.Cur1     = -1;
        fUnit.Nominal1 = 0;
      end;
      fUnit.Rest1 = 0;
      if ( ( CassettesNumb > 2 )  AND  ( Slots(3) == 1 ) )
        j = GetIndexSlot( 3 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur2     = Currency(j);
          fUnit.Nominal2 = Nominal(j);
        end;
      else
        fUnit.Cur2     = -1;
        fUnit.Nominal2 = 0;
      end;
      fUnit.Rest2 = 0;
      if ( ( CassettesNumb > 3 )  AND  ( Slots(4) == 1 ) )
        j = GetIndexSlot( 4 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur3     = Currency(j);
          fUnit.Nominal3 = Nominal(j);
        end;
      else
        fUnit.Cur3     = -1;
        fUnit.Nominal3 = 0;
      end;
      fUnit.Rest3 = 0;
      if ( ( CassettesNumb > 4 )  AND  ( Slots(5) == 1 ) )
        j = GetIndexSlot( 5 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur4     = Currency(j);
          fUnit.Nominal4 = Nominal(j);
        end;
      else
        fUnit.Cur4     = -1;
        fUnit.Nominal4 = 0;
      end;
      fUnit.Rest4 = 0;
      if ( ( CassettesNumb > 5 )  AND  ( Slots(6) == 1 ) )
        j = GetIndexSlot( 6 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur5     = Currency(j);
          fUnit.Nominal5 = Nominal(j);
        end;
      else
        fUnit.Cur5     = -1;
        fUnit.Nominal5 = 0;
      end;
      fUnit.Rest5 = 0;
      if ( ( CassettesNumb > 6 )  AND  ( Slots(7) == 1 ) )
        j = GetIndexSlot( 7 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur6     = Currency(j);
          fUnit.Nominal6 = Nominal(j);
        end;
      else
        fUnit.Cur6     = -1;
        fUnit.Nominal6 = 0;
      end;
      fUnit.Rest6 = 0;
      if ( ( CassettesNumb > 7 )  AND  ( Slots(8) == 1 ) )
        j = GetIndexSlot( 8 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur7     = Currency(j);
          fUnit.Nominal7 = Nominal(j);
        end;
      else
        fUnit.Cur7     = -1;
        fUnit.Nominal7 = 0;
      end;
      fUnit.Rest7 = 0;
      if ( ( CassettesNumb > 8 )  AND  ( Slots(9) == 1 ) )
        j = GetIndexSlot( 9 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur8     = Currency(j);
          fUnit.Nominal8 = Nominal(j);
        end;
      else
        fUnit.Cur8     = -1;
        fUnit.Nominal8 = 0;
      end;
      fUnit.Rest8 = 0;
      if ( ( CassettesNumb > 9 )  AND  ( Slots(10) == 1 ) )
        j = GetIndexSlot( 10 );
        if ( j == -1 )
          StatT = 11;
        else
          fUnit.Cur9     = Currency(j);
          fUnit.Nominal9 = Nominal(j);
        end;
      else
        fUnit.Cur9     = -1;
        fUnit.Nominal9 = 0;
      end;
      fUnit.Rest9 = 0;
      if ( ( StatT == 0 )  AND  ( NOT Update( fUnit ) ) )
        StatT = 12;
      end;
    end;

    if ( StatT != 0 )
      AbortTrn();
    end;

  end;

  /***********************************************************/
  /*              Получение информации о кассете             */
  /***********************************************************/
  macro GetCassetteParm( cID )
    ClearRecord( fSlot );
    fSlot.CassetteID = cID;
    fSlot.Reference  = 2147483647;  /* Max Long */
    if ( getLE( fSlot )  AND  ( fSlot.CassetteID == cID ) )
      CassetteCurr = fSlot.SlotCurrency;
      CassetteNmnl = fSlot.SlotNominal;
    else
      CassetteCurr = -1;
      CassetteNmnl = 0;
    end;
  end;

  /***********************************************************/
  /***              Начало процедуры загрузки              ***/
  /***********************************************************/

  /* Заполнение массива формулировок статусов */
  aStat(0) = "TCD выгружен";
  aStat(1) = "Загрузка не закончена";
  aStat(2) = "TCD загружен";
  aStat(3) = "Загрузка отменена";
  aStat(4) = "Начата выгрузка";
  aStat(5) = "Ошибка при выгрузке";

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    PersonRefLoad = fPers.Reference;
  else
    MsgBox( NoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      UnitRecPos = GetPos( fUnit );
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Проверка статуса загрузки */
  if ( stat == 0 )
    if ( fUnit.LoadStatus != 0 )
      MsgBox( "TCD \"", TCD_ID, "\"|имеет статус|\"", aStat(fUnit.LoadStatus),
              "\"|Загрузка возможна|только после выгрузки" );
      stat = 4;
    end;
  end;

  /* Задание сумм по валютам */
  if ( stat == 0 )
    if ( fUnit.IsCur == 0 )
      nLoadCur = 1;
    else
      nLoadCur = fUnit.SlotsNum;
    end;
    i = 0;
    while ( i < nLoadCur )
      LoadCur(i) = -1;
      LoadSum(i) = $0L;
      i = i + 1;
    end;
    dlgS6.TCD     = fUnit.ID;
    dlgS6.Date    = Date();
    dlgS6.CurDate = {curdate};
    if ( fUnit.IsCur != 1 )  /* Не только валюта */
      LoadCur(0) = 0;
      dlgS6.Cur0 = "RUR";
    end;
    if   ( nLoadCur == 1 )
      Copy( dlgS1, dlgS6 );
      Ret = RunDialog( dlgS1, "EventProcC" );
    elif ( nLoadCur == 2 )
      Copy( dlgS2, dlgS6 );
      Ret = RunDialog( dlgS2, "EventProcC" );
    elif ( nLoadCur == 3 )
      Copy( dlgS3, dlgS6 );
      Ret = RunDialog( dlgS3, "EventProcC" );
    elif ( nLoadCur == 4 )
      Copy( dlgS4, dlgS6 );
      Ret = RunDialog( dlgS4, "EventProcC" );
    elif ( nLoadCur == 5 )
      Copy( dlgS5, dlgS6 );
      Ret = RunDialog( dlgS5, "EventProcC" );
    else
      Ret = RunDialog( dlgS6, "EventProcC" );
    end;
    if ( NOT Ret )
      stat = 77;
    end;
  end;

  /* Заполнение массива названий валют */
  if ( stat == 0 )
    while ( i < nLoadCur )
      if ( LoadCur(i) != -1 )
        if ( FindCurrency( LoadCur(i) ) )
          LoadCurName(i) = Curr.Short_Name;
        else
          LoadCurName(i) = String(Currency(i));
        end;
      end;
      i = i + 1;
    end;
  end;

  /* Запись в файл загрузки */
  if ( stat == 0 )

    LoadDate = Date();
    LoadTime = Time();

    if ( NOT LoadRecInsert() )
      stat = 5;
    end;
  end;

  /* Записи в Retail о пополнении сейфа TCD по валютам */
  if ( stat == 0 )

    saveAutoConfirm      = AutoConfirm(AC_STRONG);  /* Включение автоподтверждения */

    Ret = ProcessTrn( 1, "TrnProcCash" );

    AutoConfirm(saveAutoConfirm);

    if( Ret )
      InterDesk_EndDocBunch;
    else
      MsgBox( "Невозможна запись|о пополнении сейфа TCD" );
      stat = 6;
    end;

  end;

  /* Установка статуса загрузки: запись в Retatil о пополнении */
  if ( stat == 0 )
    if ( NOT LoadRecUpdate( 2, 0 ) )
      stat = 7;
    end;
  end;

  /* Диалог "Вставьте кассеты, закройте дверцу сейфа..." */
  if ( stat == 0 )
    RunDialog( dlgN0, "EventProc0" );
  end;

  /* Установка статуса TCD: загрузка начата, но не закончена */
  if ( stat == 0 )
    if ( NOT TCDStatRecUpdate( 1 ) )
      stat = 8;
    end;
  end;

  /* Установка статуса загрузки: подана команда TCD загрузки */
  if ( stat == 0 )
    if ( NOT LoadRecUpdate( 3, 0 ) )
      stat = 9;
    end;
  end;

  /* Команда TCD по загрузке */
  if ( stat == 0 )
    ErrInit();
    if ( GetParams() )
      stat = Load( retLoad, strLoad );
    else
      stat = 17;
    end;

    if ( stat != 0 )
      if ( ( stat > 0 )  AND  ( stat < 54 ) )
        ErrMess( stat );
        if ( stat == 1 )  /* Число банкнот меньше 150-200 */
          stat = 0;
        end;
      else
        MsgBox( "Функция Load()|вернула код ошибки ", stat );
      end;
    end;
    if ( retload != 0 )
      MsgBox( "Команда TCD загрузки|закончена с кодом ошибки ", retLoad );
    end;
  end;

  /* Проверка соответствия числа кассет числу валют */
  if ( stat == 0 )
    i = 0;
    while ( i < 7 )
      Slots   ( i ) = 0;  /* Начальная инициализация массива слотов */
      Cassette( i ) = "";
      i = i + 1;
    end;
    i = 0;
    k = StrLen( strLoad );
    CassettesNumb = 0;
    while ( i < k )
      j = Int(SubStr(strLoad,i+1,1)); /* Индекс слота */
      Slots( j )    = 1;                          /* Слот содержит кассету */
      SlotCass(i/7) = j;
      SlotStat( j ) = Int(SubStr(strLoad,i+2,1)); /* Статус слота */
      Cassette(i/7) = SubStr(strLoad,i+3,5);      /* Идентификатор слота */
      if ( j != 0 )
        CassettesNumb = CassettesNumb + 1;        /* Число кассет в TCD */
      end;
      i = i + 7;
    end;
    if ( CassettesNumb > fUnit.SlotsNum )
      MsgBox( "Ошибка в параметрах TCD.| ",
              "|Полученное число кассет ", CassettesNumb,
              "|больше заданного числа слотов ", fUnit.SlotsNum,
              ".| |Загрузка отменяется" );
      stat = 33;
    end;
  end;

  /* Установка статуса загрузки: результат команды TCD загрузки */
  if ( stat == 0 )
    if ( NOT LoadRecUpdate( 4, 0 ) )  /* Команда TCD прошла успешно */
      stat = 9;
    end;
  /*
  else
    if ( NOT LoadRecUpdate( 5, StatComTCD ) )  /* Команда TCD прошла с ошибкой */
      stat = 9;
    end; */
  end;

  /* Задание номиналов слотов */
  if ( stat == 0 )

    /* Определение параметров кассет в слотах */
    i = 0;
    while ( i <= CassettesNumb )
      Currency(i) = 0;
      Nominal(i)  = 1;
      Notes(i)    = 0;
      SlotSum(i)  = $0L;
      i = i + 1;
    end;

    /* Поля параметров загрузки */
    dlgN6.TCD         = fUnit.ID;
    dlgN6.Oper        = {oper};
    dlgN6.LoadType    = "Стандартная";
    dlgN6.Reject      = "Да";
    dlgN6.DateLoad    = LoadDate;
    dlgN6.TimeLoad    = LoadTime;
    dlgN6.CurDateLoad = {curdate};
    dlgN6.Comment     = "";

    /* Ф.И.О. кассира */
    if ( GetPerson( {oper} ) )
      dlgN6.FIO = SubStr( person.Name, 1, 23 );
    else
      dlgN6.FIO = "???";
    end;

    /* Поиск информации по кассетам */
    i = 0;
    while ( i < CassettesNumb )
      GetCassetteParm( Cassette(i+1) );
      Currency(i) = CassetteCurr;
      Nominal(i)  = CassetteNmnl;
      i = i + 1;
    end;

    /* Поля по слотам */
    i = 0;
    while ( i < CassettesNumb )
      j = i * 6;
      dlgN6( 9+j) = SlotCass(i+1);
      dlgN6(10+j) = Cassette(i+1);
      if ( FindCurrency( Currency(i) ) )
        dlgN6(11+j) = Curr.Short_Name;
      else
        dlgN6(11+j) = String(Currency(i));
      end;
      dlgN6(12+j) = Nominal(i);
      dlgN6(13+j) = 0;
      dlgN6(14+j) = $0L;
      i = i + 1;
    end;

    /* Начальная инициализация итоговых сумм */
    i = 0;
    j = 0;
    while ( i < nLoadCur )
      if ( LoadCur(i) != -1 )
        Cur_R(j) = LoadCur(i);
        Cur_N(j) = LoadCurName(i);
        Sum_P(j) = LoadSum(i);
        Sum_R(j) = $0L;
        j = j + 1;
      end;
      i = i + 1;
    end;

    /* Инициализация итоговых сумм в панели */
    dlgN6.CurR0 = Cur_N(0);
    dlgN6.SumP0 = Sum_P(0);
    if ( j > 1 )
      dlgN6.CurR1 = Cur_N(1);
      dlgN6.SumP1 = Sum_P(1);
      if ( j > 2 )
        dlgN6.CurR2 = Cur_N(2);
        dlgN6.SumP2 = Sum_P(2);
        if ( j > 3 )
          dlgN6.CurR3 = Cur_N(3);
          dlgN6.SumP3 = Sum_P(3);
          if ( j > 4 )
            dlgN6.CurR4 = Cur_N(4);
            dlgN6.SumP4 = Sum_P(4);
            if ( j > 5 )
              dlgN6.CurR5 = Cur_N(5);
              dlgN6.SumP5 = Sum_P(5);
            end;
          end;
        end;
      end;
    end;

    /* Диалог установки параметров кассет */
    if   ( CassettesNumb == 1 )
      Copy( dlgN1, dlgN6 );
      Ret = RunDialog( dlgN1, "EventProcN" );
      Copy( dlgN6, dlgN1 );
    elif ( CassettesNumb == 2 )
      Copy( dlgN2, dlgN6 );
      Ret = RunDialog( dlgN2, "EventProcN" );
      Copy( dlgN6, dlgN2 );
    elif ( CassettesNumb == 3 )
      Copy( dlgN3, dlgN6 );
      Ret = RunDialog( dlgN3, "EventProcN" );
      Copy( dlgN6, dlgN3 );
    elif ( CassettesNumb == 4 )
      Copy( dlgN4, dlgN6 );
      Ret = RunDialog( dlgN4, "EventProcN" );
      Copy( dlgN6, dlgN4 );
    elif ( CassettesNumb == 5 )
      Copy( dlgN5, dlgN6 );
      Ret = RunDialog( dlgN5, "EventProcN" );
      Copy( dlgN6, dlgN5 );
    else
      Ret = RunDialog( dlgN6, "EventProcN" );
    end;

    /* Сохранение количества банкнот в кассетах */
    if ( Ret )
      i = 0;
      while ( i < CassettesNumb )
        Notes(i) = dlgN6(13+i*6);
        i = i + 1;
      end;
      sCommentary = dlgN6.Comment;
      /* Транзакция завершения загрузки */
      if ( ( stat == 0 )  AND  Ret )
        Ret = ProcessTrn( 1, "TrnProcLoad" );
        if ( ( NOT Ret )  OR  ( StatT != 0 ) )
          MsgBox( "Невозможна запись|данных завершения загрузки" );
          stat = 6;
        else
          MsgBox( " |Загрузка выполнена успешно| " );
        end;
      end;
    else
      /* Отмена загрузки */
      i = 0;
      while ( i < aSize(ApplRetail) )  /* Откат операций Retail */
        stat = UnconfirmCashDoc( SB_EXTCASH, ApplRetail( i ) );
        i = i + 1;
      end;
      ErrInit();
      stat = UnLoad();
      if ( stat == 0 )
        /* Установка статуса TCD: выгрузка прошла успешно */
        if ( NOT TCDStatRecUpdate( 0 ) )
          stat = 9;
        end;
        if ( ( stat == 0 )  AND
             ( NOT LoadRecUpdate( 7, 0 ) ) )
          stat = 10;
        end;
      else
        /* Сообщение об ошибке */
        if ( ( stat > 0 )  AND  ( stat < 54 ) )
          ErrMess( stat );
        end;
        /* Установка статуса TCD: ошибка при выгрузке */
        if ( NOT LoadRecUpdate( 8, stat ) )
          stat = 11;
        end;
        if ( ( stat == 0 )  AND
             ( NOT TCDStatRecUpdate( 5 ) ) )
          stat = 12;
        end;
      end;
    end;

  end;

end;
