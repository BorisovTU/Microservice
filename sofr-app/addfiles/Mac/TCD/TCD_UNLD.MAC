/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос выгрузки TCD                                    */
/*                                                             */
/***************************************************************/

  Import BankInter, deprintr, DeskInter, ListCodf, ListCurr, ListNomn, tcd_err, delarue;

  /*** Константы ***/
  const NoRight = "Вы не можете|работать с TCD";

  /***  Файлы системы TCD  ***/
  file fLoad ( TCD_Load, "TCD.DEF" ) key 0 write;
  file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file fSlot ( TCD_Slot, "TCD.DEF" ) key 1 write;
  file fUnit ( TCD_Unit, "TCD.DEF" ) key 0 write;

  var {oper};

  var Branch = NumFNCash;

  var LoadRef       = 0;

  var stat          = 0;
  var PersonRefLoad = 0;
  var UnitRecPos    = -1;
  var LoadRecPos    = -1;
  var TCDRef        = 0;
  var TCD_ID        = "";

  /***********************************************************/
  /*       Получение номера СОМ-порта из параметров АБС      */
  /***********************************************************/
  macro GetParams

    var
      RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\COM-ПОРТ ДЛЯ TCD",
      TypeI    = V_INTEGER,
      ValueI   = 0,
      ErrCode,
      StatG = TRUE,
      COM_PORT = 0;

    if(GetRegistryValue(RegPath, TypeI, ValueI, ErrCode) == V_INTEGER)
      COM_PORT = ValueI;
      if ( ( COM_PORT < 1 )  or  ( COM_PORT > 4 ) )
        MsgBox( "Номер COM-порта "+String(COM_PORT)+
                "|задан неправильно.|Он должен быть в интервале от 1 до 4" );
        StatG = FALSE;
      end;
    else
      MsgBox( "Не возможно получить|параметр номера COM-порта" );
      StatG = FALSE;
    end;

    if ( StatG )
      if ( Param( COM_PORT, PARITY_VAR ) != 0 )
        MsgBox( "Не возможно передать|номер СОМ-порта в DLM" );
      end;
    end;

    return StatG;

  end;

  /************************************************************/
  /* Перенесение количества выданных банкнот из записи по TCD */
  /************************************************************/
  macro SetCassettesPay

    var ret = TRUE;
    var NumCassettes = 0;

    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )

      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        if   ( fSlot.SlotNumber == 1 )
          fSlot.SlotQuantityPay = fUnit.Rest0;
        elif ( fSlot.SlotNumber == 2 )
          fSlot.SlotQuantityPay = fUnit.Rest1;
        elif ( fSlot.SlotNumber == 3 )
          fSlot.SlotQuantityPay = fUnit.Rest2;
        elif ( fSlot.SlotNumber == 4 )
          fSlot.SlotQuantityPay = fUnit.Rest3;
        elif ( fSlot.SlotNumber == 5 )
          fSlot.SlotQuantityPay = fUnit.Rest4;
        elif ( fSlot.SlotNumber == 6 )
          fSlot.SlotQuantityPay = fUnit.Rest5;
        else
          fSlot.SlotQuantityPay = 0;
        end;
        ret = Update( fSlot );
        if ( ret )
          ret = Next( fSlot );
        else
          MsgBox( "Невозможно перенести|данные по выдаче|для кассеты ",
                  fSlot.CassetteID );
          return FALSE;
        end;
      end;

    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      return FALSE;
    end;

    return TRUE;

  end;

  /***********************************************************/
  /*           Изменение статуса записи в файле TCD          */
  /***********************************************************/
  macro TCDStatRecUpdate( a_stat )

    GetDirect( fUnit, UnitRecPos );
    fUnit.LoadStatus = a_stat;
    return Update( fUnit );

  end;

  /***********************************************************/
  /*         Изменение статуса записи в файле загрузки       */
  /***********************************************************/
  macro LoadRecUpdate( a_stat, a_ret )

    GetDirect( fLoad, LoadRecPos );

    fLoad.Status          = a_stat;
    fLoad.LoadReturn      = a_ret;
    fLoad.PersonRefUnLoad = PersonRefLoad;
    /* fLoad.Commentary      = ""; */

    return Update( fLoad );

  end;

  /***********************************************************/
  /*                Начало процедуры выгрузки                */
  /***********************************************************/

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    PersonRefLoad = fPers.Reference;
  else
    MsgBox( NoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      UnitRecPos = GetPos( fUnit );
      TCDRef     = fUnit.Reference;
      TCD_ID     = fUnit.ID;
      LoadRef    = fUnit.LoadRef;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Проверка статуса загрузки */
  if ( stat == 0 )
    if ( fUnit.LoadStatus == 0 )
      MsgBox( "TCD \"", TCD_ID, "\" не загружен.|Выгрузка не выполняется" );
      stat = 4;
    end;
  end;

  /* Запрос на выгрузку */
  if ( stat == 0 )
    if ( NOT GetTrue( FALSE, "Вы действительно хотите " +
                             "произвести выгрузку TCD "
                             "\"" + TCD_ID + "\" ?" ) )
      stat = 5;
    end;
  end;

  /* Установка статуса TCD: начата выгрузка */
  if ( stat == 0 )
    if ( NOT TCDStatRecUpdate( 4 ) )
      stat = 6;
    end;
  end;

  /* Чтение записи загрузки */
  if ( stat == 0 )
    ClearRecord( fLoad );
    fLoad.Reference = fUnit.LoadRef;
    if ( GetEQ( fLoad ) )
      LoadRecPos = GetPos( fLoad );
    else
      MsgBox( "Запись по загрузке с Reference = ", fUnit.LoadRef,
              "|не найдена" );
      stat = 7;
    end;
  end;

  /* Установка статуса загрузки: начата выгрузка */
  if ( stat == 0 )
    if ( NOT LoadRecUpdate( 6, 0 ) )
      stat = 8;
    end;
  end;

  /* Команда TCD выгрузки */
  if ( stat == 0 )

    Message( "Выполняется выгрузка TCD ..." );

    ErrInit();
    if ( GetParams() )
      stat = UnLoad();
    else
      stat = 57;
    end;

    if ( stat == 0 )
      /* Перенос информации по выдачам по кассетам */
      if ( NOT SetCassettesPay() )
        stat = 29;
      end;
      /* Установка статуса TCD: выгрузка прошла успешно */
      if ( ( stat == 0 )  AND
           ( NOT TCDStatRecUpdate( 0 ) ) )
        stat = 9;
      end;
      if ( ( stat == 0 )  AND
           ( NOT LoadRecUpdate( 7, 0 ) ) )
        stat = 10;
      end;
    else
      /* Вывод сообщения об ошибке */
      if ( ( stat > 0 )  AND  ( stat < 54 ) )
        ErrMess( stat );
      end;
      /* Установка статуса TCD: ошибка при выгрузке */
      if ( NOT LoadRecUpdate( 8, stat ) )
        stat = 11;
      end;
      if ( ( stat == 0 )  AND
           ( NOT TCDStatRecUpdate( 5 ) ) )
        stat = 12;
      end;
    end;

  end;

  /* Завершающие сообщения */
  if ( stat == 0 )
    MsgBox( " |Выгрузка завершена успешно| " );
  else
    if ( stat == 5 )
      MsgBox( " |Выгрузка отменена оперционистом| " );
    elif ( stat == 4 )
    else
      MsgBox( " |Выгрузка завершилась|с ошибкой ", stat, "| " );
    end;
  end;

end;
