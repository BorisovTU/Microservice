//

import BankInter, DeprIntr, DeskInter, ListCurr;
import rcw;
import tcr_bc_err;

private const 
  CASHOP_REINFORCE = 1,
  CASHOP_RETURN    = 2,
  SB_INCASH        = 100;  // Вид Application при выдаче.

private var {oper}, {curdate}, {DeskAutomation};

private var Branch = NumFNCash();
private var OperBrigade = GetOperBrigade();

private var pCurr = 0;
private var dispenseSum = 0;
private var TCD_ID = "";

private var TrnErr = 0;  // Ошибка, обнаруженная в процедуре транзакции.

private var sComment = "Выдача из ЭК";  // Комментарий.

private record OpParm( "op_parm.rec" );  // Параметры для InterDesk_InitDocBunch().
private record Parm( CashParm );         // Параметры для операции Retail InitCashDoc().

private file Group( "rtgroup.dbt" ) key 2;

private file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
private file fUnit ( TCD_Unit, "TCD.DEF" ) key 0 write;


/***********************************************************/
/*            Получение стола кассира для TCD              */
/***********************************************************/
macro GetWorkDeskName( OperID, BrigadeID )
  var
    OperStr = String( OperID ),
    BrigadeStr = String( BrigadeID );

  return MkStr( "0", 4 - StrLen( OperStr ) ) + OperStr + "/" +
         MkStr( "0", 2 - StrLen( BrigadeStr ) ) + BrigadeStr;
end;

/***********************************************************/
/*                   Определение сейфа                     */
/***********************************************************/
private macro GetNativeSafe
  Group.Branch = Branch;
  Group.Date   = {curdate};
  Group.ID     = OperBrigade;
  if ( GetEQ( Group ) )
    return Group.SafeID;
  else
    MsgBox( "Не найден сейф бригады ", OperBrigade );
    Exit;
  end;
end;

/***********************************************************/
/*        Транзакция формирования кассовых документов      */
/***********************************************************/
private macro CashDocTransaction()

  var sCurr3;
  var stat_t;
  var NativeSafe;
  var vApplKey;
  var saveAutoConfirm;

  TrnErr = 0;

  /* Формирование связки документов */
  InterDesk_EndDocBunch();

  sCurr3 = "000" + String( pCurr );

  ClearRecord( OpParm );
  OpParm.Date      = {curdate};
  OpParm.Operation = CASHOP_RETURN;
  /* OpParm.SubOp     = */
  OpParm.ObjectRef = "000001" + SubStr( sCurr3, strlen(sCurr3)-2 );
  OpParm.Cashier   = {oper};

  vApplKey = FormApplicationKey( SB_INCASH );

  if ( InterDesk_InitDocBunch( SB_INCASH, vApplKey, OpParm ) )
    stat_t = 0;
  else
    stat_t = 14;
    TrnErr = 14;
  end;

  /* Операция Retail "Пополнение сейфа кассира из TCD" */
  if ( stat_t == 0 )
    saveAutoConfirm = AutoConfirm( AC_STRONG );  // Включение автоподтверждения.

    NativeSafe = GetNativeSafe();

    ClearRecord(Parm);
    Parm.Type            = 0;      /* Платежеспосбные ресурсы */
    Parm.TypeValue       = 1;      /* Деньги */
    Parm.CodIntValue     = pCurr;  /* Валюта */
    Parm.CashOper        = TCD_ID;
    Parm.CashDateDoc     = {curdate};
    Parm.NumOper         = CASHOP_RETURN;
    Parm.ValueMoney      = dispenseSum;
    Parm.ApplicationKind = SB_INCASH;
    /* Parm.ApplicationKey  = FormApplicationKey( SB_INCASH ); */
    Parm.ApplicationKey  = vApplKey;
    Parm.CashPatern      = NativeSafe;
    Parm.Ground          = SubStr( sComment, 1, 36 );
    if ( NOT InitCashDoc( Parm ) )
      stat_t = 11;
      TrnErr = 11;
    end;
    if ( {DeskAutomation} )
      ClearRecord(Parm);
      Parm.Type            = 0;      /* Платежеспосбные ресурсы */
      Parm.TypeValue       = 1;      /* Деньги */
      Parm.CodIntValue     = pCurr;  /* Валюта */
      Parm.CashOper        = GetWorkDeskName( {oper}, OperBrigade );
      Parm.CashDateDoc     = {curdate};
      Parm.NumOper         = CASHOP_REINFORCE;
      Parm.ValueMoney      = dispenseSum;
      Parm.ApplicationKind = SB_INCASH;
      Parm.ApplicationKey  = FormApplicationKey( SB_INCASH );
      Parm.CashPatern      = NativeSafe;
      Parm.Ground          = SubStr( sComment, 1, 36 );
      if ( NOT InitCashDoc( Parm ) )
        stat_t = 12;
        TrnErr = 12;
      end;
    end;

    if ( stat_t == 0 )
      InterDesk_EndDocBunch();
    end;

    AutoConfirm( saveAutoConfirm );
  end;

  if ( stat_t == 0 )
    return TRUE;
  else
    AbortTrn();
  end;

end;


/***********************************************************/
/*                   Выдача по операции                    */
/***********************************************************/
macro PayProc( p_Sum, p_Curr )

  var agent = CreateObject( "rtbrcash.d32", "TBranchCashAgent", NULL, false );
  var stat = 0;

  var PersonRefLoad = 0;
  var Teller        = "0";  // Направление выдачи (направо/налево).
  var UnitRecPos    = -1;
  var TCDRef        = 0;

  var pSum = 0;
  var sCurr = "RUR";

  var bce = BC_ErrCode;
  var res = 0;

  var ret = TRUE;

  pCurr = p_Curr;

  if ( FindCurrency( p_Curr ) )
    sCurr = Curr.Short_Name;
  else
    sCurr = String( p_Curr );
  end;

  // pSum = 5000;  // Для отладки

  // Проверка на то, что тип ЭК = Branch Cash.
  /*
  if ( stat == 0 )
    if ( verifTypeDeLaRue( {oper} ) == FALSE )
      return 0;
    end;
  end;  
  */

  pSum = Int ( p_Sum * 100 );

  // msgbox( "Pay pSum=", pSum );

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    if ( fPers.Status != 0 )
      MsgBox( "Вам запрещено|работать с TCD" );
      stat = 1;
    else
      PersonRefLoad = fPers.Reference;
      if ( fPers.Direction == 0 )
        Teller = "0";   /* Направо */
      else
        Teller = "1";   /* Налево */
      end;
    end;
  else
    MsgBox( "Вы не можете|работать с TCD" );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      UnitRecPos = GetPos( fUnit );
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
      /*
      if ( fUnit.MaxNotes == 0 )
        LimitNotes = 999;
      else
        LimitNotes = fUnit.MaxNotes;
      end;
      */
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Проверка статуса загрузки */
  /*
  if ( stat == 0 )
    if ( fUnit.LoadStatus == 0 )
      MsgBox( "TCD \" ", TCD_ID, " \" не загружен.|Выдача невозможна" );
      stat = 4;
    else
      fLoad.Reference = fUnit.LoadRef;
      if ( GetEQ( fLoad ) )
        LoadRef = fLoad.Reference;
      else
        stat = 27;
        return 27;
      end;
    end;
  end;
  */

  if ( ( stat == 0 )  AND  ( agent.login( 1, 1 ) ) )

    if ( agent.Dispense2( sCurr, pSum, dispenseSum, "1" ) )
      dispenseSum = dispenseSum / 100;

      /* Кассовая операция "Пополнение сейфа кассира из TCD" */
      if ( ( dispenseSum != 0 )  AND  ( NOT ProcessTrn( 0, "CashDocTransaction" ) ) )
        stat = TrnErr;
        if ( stat == 11 )
          MsgBox( "Возможно, субъект кассы \"" + TCD_ID +
                  "\" не входит ни в какую смену" );
          ret = FALSE;
        end;
        if ( stat == 12 )
          MsgBox( "Ошибка при подкреплении кассира.|Возможно, не включен субъект кассы ЭКР" );
          ret = FALSE;
        end;
      end;
    else
      ret = FALSE;
    end;

    if ( dispenseSum != 0 )
      agent.GetLastBranchCashResult( res );  // Получение кода ошибки.
      if ( res != 0 )
        MsgBox( bce.ErrMsg( res ) );  // Сообщение об ошибке.
        ret = FALSE;
      end;
    end;

  end;

  if ( stat != 0 )
    ret = FALSE;
  end;

  return ret;

end;
