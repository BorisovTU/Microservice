/*************************************************\
  R-Style Softlab
  RS-Retail
  Электронный кассир Pro Cash 6000
  Воронцов Ю.В., IIX.2006

Глобальные объявления для макросов Pro Cash 6000
\*************************************************/

import rcw;

import rsd;

//Import BankInter, deprintr, DeskInter, ListCodf; //, ListCurr, ListNomn, tcd_err;//, delarue;
Import BankInter, deprintr, DeskInter;

//флаг отладки ( true - вывод сообщений )
//const _flag_debug = true;
//const _flag_debug_ret = true;
const _flag_debug = false;
const _flag_debug_ret = false;


var name_pc_Macro = "";
macro debugmsg(inmsg:string)
  if(_flag_debug == true)
    msgbox(" Macro:"+name_pc_Macro+ "  Msg:"+inmsg);
  end;
end;//debugmsg
macro debugln(inmsg:string)
  if(_flag_debug == true)
    println(" Macro:"+name_pc_Macro+ "  Msg:"+inmsg);
  end;
end;//debugmsg


//*******************************************************
//строка подключения по каналу по умолчанию
const _CONNECT_STR_DEF = "Yury";
//строка подключения по каналу
var _CONNECT_STR="";

//название сервера RSL-модуля (ProCashDevice)
const _PROCASH_DEVICE_SERVER = "ProCash";

//название класса в RSL-модуле
const _PROCASH_DEVICE_CLASS = "TProCashDevice";

//название объекта RSL-модуля
const _PROCASH_DEVICE_OBJ = "ProCash";

//flag for local connect the PD-Device
const _MODE_SPECIFIED_TCD = 1;//подключиться к указанному ЭК 
const _MODE_SMENA_TCD = 2; //подключиться к ЭК своей смены
const _MODE_LOCAL_TCD = 3; //подключиться к своему ЭК
//var _MODE__TCD = 4;


const _REPMODE_LASTOP = 1;
const _REPMODE_RESTINFO = 2;

//коды направления выдачи для ProCash 6000
const SETSIDE_LEFT_PC = false;
const SETSIDE_RIGHT_PC = true;

//коды направления выдачи в таблице dtcd_pers_dbt
const SETSIDE_LEFT_DB = 1;
const SETSIDE_RIGHT_DB = 0;     

//*******************************************************
//Параметры: TCD_PARM.aTCDRef - ID ЭК смены, к которой относится сейф-участник операции
//record TCD_PARM ("TCDOpPrm.");   /* Параметры для TCD */

//Таблица ЭК
file tcd_unit ("tcd_unit.dbt") key 0;


//*******************************************************
//Объект устройства ЭК
var objPCDevice;

//*******************************************************
//Структура информации о платёжном документе
record CashDoc ( sb_casdc );

//*******************************************************
//Функция поиска в tcd_unit.dbt
//Use: var rs_ID; var rs_NetAddress; var rs_Desc; findInTcdUnit( 1 , @rs_ID , @rs_NetAddress, @rs_Desc);
macro findInTcdUnit( in_TCDRef:integer, outID:@string, outNetAddress:@string, outDesc:@string ): bool
   
 var cmd;
 var rs;
 
 //инициализация выходных строк
 outID = "";
 outNetAddress = "";
 outDesc = "";

 cmd = RsdCommand(  (" SELECT * FROM DTCD_UNIT_DBT WHERE t_Reference = "+string(in_TCDRef) ) );
 cmd.execute();

 rs = RsdRecordset (cmd);

 while( rs.moveNext() )
  

  if ( ( ValType ( rs.Value("t_NetAddress") ) == V_STRING )  )
    outNetAddress = rs.Value("t_NetAddress");
  else
    //println(   "ERROR: DTCD_UNIT_DBT.t_NetAddress= "+ string ( rs.Value("t_NetAddress") ) +" - не правильный тип данных." );
    //return false;
    outNetAddress = "";
  end;

  if ( ( ValType ( rs.Value("t_ID") ) == V_STRING )  )
    outID = rs.Value("t_ID");
  else
    //println(   "ERROR: DTCD_UNIT_DBT.t_ID= "+  string ( rs.Value("t_ID") )  +" - не правильный тип данных."  );
    //return false;
    outID = "";
  end;

  if ( ( ValType ( rs.Value("t_Description") ) == V_STRING )  )
    outDesc = rs.Value("t_Description");
  else
    //println( "ERROR: DTCD_UNIT_DBT.t_Description= "+  string ( rs.Value("t_Description") )  +" - не правильный тип данных." );
    //  return false;
    outDesc = "";
  end;
  
  return true;
 end;//while

 println (  "ERROR: TCD с DTCD_UNIT_DBT.t_Reference = ",in_TCDRef," отсутствует в таблице TCD_UNIT.DBT." );
 return false;
  
end;//macro findInTcdUnit()          


//Значения поля tcd_unit.LoadStatus (по dklstcdu.c) 
const _LOADSTATUS_UnLoad = 0;//    "Выгружен",
const _LOADSTATUS_LoadInterrupt = 1;//    "Загрузка не закончена",
const _LOADSTATUS_IsLoad = 2;//    "Загружен",
const _LOADSTATUS_LoadStop = 3;//    "Загрузка отменена",
const _LOADSTATUS_UnLoadInterrupt = 4;//    "Выгрузка не закончена",
const _LOADSTATUS_UnloadError = 5;//    "Ошибка при выгрузке"
const _LOADSTATUS_LoadError = 6;//    "Ошибка при загрузке"  (add)

//Установка поля "Загрузка" для TCD
macro setLoadStatusTCD( in_loadstatus:integer, in_TCDRef:integer ): bool
   var stat = true;
   var cmd;
   var strcom = "";
   
   strcom = " UPDATE dtcd_unit_dbt SET t_LoadStatus = "+ string(in_loadstatus) +" WHERE t_Reference = "+ string(in_TCDRef) +" ;";
   
   debugmsg(strcom);
   
   cmd = RSDCommand(strcom);
   cmd.execute();

   stat = true;
   return stat;    
end;//setLoadStatusTCD


//Проверка на наличие связи пользователя с TCD
macro verifPers( in_oper:integer, in_reference:integer ):bool
   
   var stat = true;
   var cmd;
   var rs;
   var strcom = "";
   
   strcom = "SELECT * FROM DTCD_PERS_DBT WHERE t_Oper = "+ string(in_oper) + " AND t_TCDRef = "+string(in_reference)+" ;";
   
   debugmsg(strcom);
   
   cmd = RSDCommand(strcom);
   
   cmd.execute();
   
   rs = RSDRecordset(cmd);
   if( rs.moveNext )
     debugmsg("есть ");
     stat = true;
   else
     debugmsg("нет");
     stat = false;
   end;
   
   return stat;    

end;//verifPers

//Установить направление выдачи денег в соответствии с полем t_Direction таблицы dtcd_pers_dbt
macro setPCSideWithdrawal( in_oper:integer )
   var stat = true;
   var cmd;
   var rs;
   var strcom = "";
   var db_direct = -1;//найденное в dtcd_pers_dbt направление выдачи 
   
   strcom = " SELECT * FROM dtcd_pers_dbt WHERE t_Oper = " + string(in_oper);
   
   debugmsg("setPCSideWithdrawal():"+strcom);
   
   cmd = RSDCommand(strcom);
   
   cmd.execute();
   
   rs = RSDRecordset(cmd);
   if( rs.moveNext )
     db_direct = rs.value("t_Direction");     
     stat = true;
     debugmsg("есть направление выдачи для пользователя "+string(in_oper)+" = "+string(db_direct));
   else
     stat = false;
     debugmsg("нет направления выдачи для пользователя "+string(in_oper)+" = "+string(db_direct));
   end;
   
   if( db_direct == SETSIDE_LEFT_DB )         //влево
     objPCDevice.setSide(SETSIDE_LEFT_PC);     
     debugmsg("Установлена выдача ВЛЕВО");
   else
     if ( db_direct == SETSIDE_RIGHT_DB )     //вправо
       objPCDevice.setSide(SETSIDE_RIGHT_PC);     
       debugmsg("Установлена выдача ВПРАВО");
     else
       objPCDevice.setSide(SETSIDE_LEFT_PC);  //по умолчанию - влево     
       debugmsg("Установлена выдача ВЛЕВО - по умолчанию");
     end
   end;
   
   return stat;    

end;


//*******************************************************
//Подключиться к ЭК
macro connectProCash( in_reference:integer, in_modeConnect:integer ) : bool
  var stat;

  var curoper = GetOper();
  
  //Создание объекта TProCashDevice *****************
  objPCDevice = CreateObject (_PROCASH_DEVICE_SERVER,_PROCASH_DEVICE_CLASS,_PROCASH_DEVICE_OBJ);

  //Поиск адреса в таблице tcd_unit.dbt по TCD_PARM.aTCDRef ********************
  //tcd_unit.REFERENCE = in_reference;//TCD_PARM.aTCDRef;
  //if (getEQ (tcd_unit))
  var rs_ID=""; var rs_NetAddress=""; var rs_Desc="";
  if( findInTcdUnit( in_reference , @rs_ID , @rs_NetAddress, @rs_Desc) ) 
     //Запоминание сетевого адреса
     stat = true;

     //IP-адрес или имя хоста компьютера, к которому подключено устройство

     //пропускаем мусор в поле
     if ( ( ValType ( rs_NetAddress) ) == V_STRING  )  
        if( strlen(rs_NetAddress)<=1 )
          rs_NetAddress = "";
        end;     
     else
        rs_NetAddress = "";
     end;

     //результирующая строка соединения
     _CONNECT_STR = rs_NetAddress;
     
  else
     _CONNECT_STR = _CONNECT_STR_DEF;
//     println ("TCD с REFERENCE=",in_reference," отсутствует в таблице TCD_UNIT.DBT");
     stat = false;
  end; //if getEQ
  
  //сетевое присоединение
  debugmsg("address for connect= "+_CONNECT_STR);
  
  //Подключиться к своему TCD**********************
  if(in_modeConnect == _MODE_LOCAL_TCD)
     debugmsg("_MODE_LOCAL_TCD"); 
     stat = true;
     
  //Подключиться к указанному TCD   
  elif(in_modeConnect == _MODE_SPECIFIED_TCD)
     debugmsg("_MODE_SPECIFIED_TCD"); 

    if( verifPers( curoper, in_reference ) == false )
       debugmsg("call connect");
       stat = objPCDevice.connect( _CONNECT_STR );
    else
       stat = true;
  end;      

  //Подключиться к TCD своей смены 
  elif(in_modeConnect == _MODE_SMENA_TCD)
     debugmsg("_MODE_SMENA_TCD");
    if( verifPers( curoper, in_reference ) == false )
       debugmsg("call connect");
       stat = objPCDevice.connect( _CONNECT_STR );
    else
       stat = true;   
    end;   

  else
     debugmsg("Неопределён режим функции connectProCash");
     println ("Неопределён режим функции connectProCash");
     stat = false;
  end;

  if(stat == true)  //Успешно соединились
    //Определить - есть ли в TCD деньги 
    if( ( objPCDevice.getAvailableAmount() ) >0 )
      setLoadStatusTCD(_LOADSTATUS_IsLoad,in_reference);
    else
      setLoadStatusTCD(_LOADSTATUS_UnLoad,in_reference);
    end;  
  
    //Установить направление выдачи
    //stat = 
    setPCSideWithdrawal( curoper );
  else
    msgbox("Неудачное соединение с сервером ProCashC.d32");  
  end;//if  

  //резюме
  if(stat == true)
     return true;
  else
     return false;
  end;//if

end;//macro connectProCash

//*******************************************************
//Получение информации о платёжном документе, scr#96739 
macro getCashDoc( adrCashDoc ) 
  
  var stat = false;

  if( ValType(adrCashDoc) == V_MEMADDR )
    if ( adrCashDoc != NULL )    
      SetBuff( CashDoc, adrCashDoc );
      stat = true;
    end;
  end;
  
  debugmsg(
  "\nПлатёжный документ, Часть 1\n"+
   string(CashDoc.Branch)+  " -  Филиал \n"+
   string(CashDoc.RefValue)+  " -  Системный код ценности \n"+
   string(CashDoc.CashOper)+  " -  Субъект кассы \n"+
   string(CashDoc.CashDateDoc)+  " -  Дата документа \n"+
   string(CashDoc.TypeDoc)+  " -  Тип документа (отложен/проведен) \n"+
   string(CashDoc.NumOper)+  " -  Номер Операции (приход,расхрд и т.д.) \n"+
   string(CashDoc.FlagDebCredOper)+  " -  Дебетовая или кредитовая операция \n"+
   string(CashDoc.ValueMoney)+  " -  Денежная сумма документа \n"+
   string(CashDoc.ValueCol)+  " -  Количество экземпляров ( для штучных ценностей ) \n"+
   string(CashDoc.Series)+  " -   Серия \n"+
   string(CashDoc.MinRegistrNumber)+  " -  Нижний регистрационный номер ценности \n"+
   string(CashDoc.MaxRegistrNumber)+  " -  Верхний регистрационный номер ценности \n"+
   string(CashDoc.Character)+  " -  Характеристика ( для инд. учета ) \n"+
   string(CashDoc.SubOper)+  " -  Подвид операции \n"+
   string(CashDoc.NumbDocumCash)+  " -  Номер кассового жетона \n"+
   string(CashDoc.ApplicationKind)+  " -  Ссылка на первичный документ \n"+
   string(CashDoc.ApplicationKey)+  " -  -- \n"
   );

  debugmsg(
  "\nПлатёжный документ, Часть 2\n"+
   string(CashDoc.CashPatern)+  " -  Счет кладовой или код контролера \n"+
   string(CashDoc.Brigade)+  " -  Бригада \n"+
   string(CashDoc.Ground)+  " -  Основание \n"+
   string(CashDoc.Mode)+  " -  Режим работы \n"+
   string(CashDoc.Nominal)+  " -  Номинал \n"+
   string(CashDoc.OrdNumber)+  " -  Номер ордера \n"+
   string(CashDoc.BagID)+  " -  ID сумки \n"+
   string(CashDoc.BagSubOp)+  " -  Подвид операции КБ \n"+
   string(CashDoc.RemotePeer)+  " -  Контрагент в др. подр. \n"+
   string(CashDoc.DestBranch)+  " -   Филиал назначения \n"+
   string(CashDoc.Oper)+  " -   Операционист, сформировавший документ \n"
   );

  debugmsg(
  "\nПлатёжный документ, Часть 3\n"+
   string(CashDoc.ConfirmedBy)+  " -  Кем подтверждено \n"+
   string(CashDoc.Collector)+  " -  Инкассатор, которому переданы ценности \n"+
   string(CashDoc.Action)+  " -  Действие над записью \n"+
   string(CashDoc.NumSession)+  " -  Сессия выгрузки \n"+
   string(CashDoc.SubAccount)+  " -  Субсчет для CashOper \n"+
   string(CashDoc.SubAccountPatern)+  " -  Субсчет для CashPatern \n"+
   string(CashDoc.Controler)+  " -  Кем проконтролировано \n"+
   string(CashDoc.Reserved)+  " -  Резерв \n"
 
  );
 
  return stat;
end;

//*******************************************************
//Проверить режим национальной валютности
macro validateCurCurrency( in_currency:integer ) : bool

  debugmsg( " Currency= "+string( in_currency ) );
  
  if(in_currency == 0)
    return true;
  else
    return false;
  end;
  
end;//macro validateCurCurrency



//*******************************************************
//Standard Connect to TCD
//macro procStandConnect( in_modeConnect:integer ) : bool
macro procStandConnect( in_modeConnect:integer, in_aTCDRef:integer ) : bool

  //if( validateCurCurrency(TCD_PARM.aCurrency) == false )
  //  //msgbox("Работа с ЭК осуществляется только с национальной валютой.");
  //  return false;
  //  //return true;
  //end;

  //if( connectProCash( TCD_PARM.aTCDRef, in_modeConnect ) == false )
  if( connectProCash( in_aTCDRef, in_modeConnect ) == false )
    return false;//error connect
  end;//connect to PC-device



  return true;//all OK

end; //macro procStatdConnrct
