/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос сброса состояния TCD                            */
/*                                                             */
/***************************************************************/

  Import BankInter, deprintr, DeskInter;

  /***  Файлы системы TCD  ***/
  file fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file fUnit ( TCD_Unit, "TCD.DEF" ) key 0 write;

  /*** Константы ***/
  const NoRight = "Вы не можете|работать с TCD";

  var {oper};

  var Branch = NumFNCash;

  var PersonRefLoad = 0;
  var UnitRecPos    = -1;
  var TCDRef        = 0;
  var TCD_ID        = "";

  var ErrorCode, ErrorText;

  var stat = 0;

  /***********************************************************/
  /***             Процедура сброса состояния                */
  /***********************************************************/

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    PersonRefLoad = fPers.Reference;
  else
    MsgBox( NoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      UnitRecPos = GetPos( fUnit );
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Запрос на выполнение сброса */
  if ( stat == 0 )
    if ( GetTrue( FALSE, "Вы действительно хотите сбросить состяние TCD?" ) )
      fUnit.LoadStatus = 0;
      if ( Update( fUnit ) )
        MsgBox( "Состояние TCD \"", fUnit.ID, "\"|сброшено успешно" );
      else
        ErrorCode = Status( ErrorText );
        MsgBox( "Ошибка ", ErrorCode, " при сбросе состояния TCD:|", ErrorText );
      end;
    end;
  end;
