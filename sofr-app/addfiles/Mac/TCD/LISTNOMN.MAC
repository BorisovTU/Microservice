import RSLScrp, ListCurr;

file   Nomn ( TCD_Nomn, "TCD.DEF" ) key 1;
record scrN ( ListNomn, "TCD.LBR" ) dialog;

var CurCurr;
var FirstRecN = True;

macro TestRecN( ff )
  if ( ( ff.Status == 0 )  AND  ( ff.Currency == CurCurr ) )
    return True;
  end;
  return False;
end;

macro NextCodfN( ff )
  if ( FirstRecN )
    FirstRecN = False;
    ClearRecord( ff );
    ff.Status = 0;
    ff.Currency = CurCurr;
    ff.Nominal = 0;
    if ( GetGE( ff ) )
      return TestRecN( ff );
    end;
  else
    if ( Next( ff ) )
      return TestRecN( ff );
    end;
  end;
  return False;
end;

macro PrevCodfN( ff )
  if ( FirstRecN )
    FirstRecN = False;
    ClearRecord( ff );
    ff.Status = 0;
    ff.Currency = CurCurr;
    ff.Nominal = 32760;
    if ( GetLE( ff ) )
      return TestRecN( ff );
    end;
  else
    if ( Prev( ff ) )
      return TestRecN( ff );
    end;
  end;
  return False;
end;

macro RewindCodfN( ff )
  Rewind( ff );
  FirstRecN = True;
end;

macro ListNominal( CurrN )

  var Ret = 0;
  var CurrName;

  CurCurr = CurrN;

  ReplaceMacro( "Next",   "NextCodfN"   );
  ReplaceMacro( "Prev",   "PrevCodfN"   );
  ReplaceMacro( "Rewind", "RewindCodfN" );

  ClearRecord( Nomn );
  Nomn.Status = 0;
  Nomn.Currency = CurCurr;
  Nomn.Nominal = 0;
  if ( ( NOT GetGE( Nomn ) )  OR  ( NOT TestRecN( Nomn ) ) )
    if ( FindCurrency( CurrN ) )
      CurrName = Curr.Short_Name;
    else
      CurrName = "???";
    end;
    MsgBox( "Не найдены номиналы|по валюте ", CurrName );
    Ret = -1;
  end;

  if ( Ret == 0 )
    SetScroll( Nomn, "F0_", "Nominal" );

    if ( RunDialog( scrN, "ScrollMes" ) )
      Ret = Nomn.Nominal;
    else
      Ret = -1;
    end;
  end;

  ReplaceMacro( "Next" );
  ReplaceMacro( "Prev" );
  ReplaceMacro( "Rewind" );

  return Ret;

end;

