/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос отчета по текущей информации о TCD              */
/*                                                             */
/***************************************************************/

  Import deprintr, ListCurr;

  file   fLoad ( TCD_Load, "TCD.DEF" ) key 4;
  file   fSlot ( TCD_Slot, "TCD.DEF" ) key 1;
  file   fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file   fUnit ( TCD_Unit, "TCD.DEF" ) key 0;

  const NoRight = "Вы не можете|работать с TCD";

  array StatTCD;  /* Формулировки статусов TCD */

  /* Массивы параметров кассет */
  array SlotNumb, SlotStat, SlotCurr, SlotNmnl, SlotLoad, SlotRest, SlotCsst;

  /* Массивы итоговых сумм по валютам */
  array totalISO, totalSum;

  var {oper}, {curdate};

  var PersonRefLoad = 0;
  var TCDRef        = 0;
  var TCD_ID        = "";
  var LoadRef       = 0;
  var NumCassettes  = 0;

  var Slots      = 0;
  var UnitStatus = "";
  var UnitDate   = Date();
  var UnitTime   = Time();

  var Branch = NumFNCash;

  var stat = 0;
  var i, j, k, l;

  /********************************************************/
  /*                  Формирование отчета                 */
  /********************************************************/
  macro Report

    var sCurr;
    var nCass;

    var iii = 0, nnn = aSize( totalISO );

    if ( NumCassettes == 0 )
      nCass = 0;
    else
      nCass = NumCassettes - 1;
    end;

    [
      Текущая информация по электронному кассиру #
      ═══════════════════════════════════════════════════

      Количество слотов                  #
      Количество кассет в слотах         #
      Текущее состояние                  #
      Дата последней загрузки/выгрузки   #
      Время последней загрузки/выгрузки  #

      ]
    ( TCD_ID, Slots, nCass, UnitStatus, UnitDate, UnitTime:l );

    [ ┌──────┬─────────┬────────┬─────────┬──────────┬──────────┬────────┐
      │      │         │        │         │ Загружен-│ Предпо-  │        │
      │      │         │        │         │ ное коли-│ лагаемый │        │
      │      │         │        │         │ чество   │ остаток  │        │
      │ Слот │ Кассета │ Валюта │ Номинал │ банкнот  │ банкнот  │ Статус │
      ├──────┼─────────┼────────┼─────────┼──────────┼──────────┼────────┤];

     i = 0;
     while ( i < NumCassettes )
       if ( SlotNumb(i) != 0 )
         if ( FindCurrency( SlotCurr(i) ) )
           sCurr = Curr.Short_Name;
         else
           sCurr = String(SlotCurr(i));
         end;
         println( " │   ",
                  SlotNumb(i), "  │  ",
                  SlotCsst(i):5, "  │  ",
                  sCurr:4, "  │  ",
                  SlotNmnl(i):5, "  │  ",
                  SlotLoad(i):6, "  │  ",
                  (SlotLoad(i)-SlotRest(i)):6, "  │   ",
                  SlotStat(i), "    │" );
       end;
       i = i + 1;
     end;
     [ └──────┴─────────┴────────┴─────────┴──────────┴──────────┴────────┘];


     println( "        Итого сумма:" );

     while ( iii < nnn )
       if ( FindCurrency( totalISO( iii ) ) )
         sCurr = Curr.Short_Name;
       else
         sCurr = String( totalISO( iii ) );
       end;
       println( "                     " +
                String( sCurr:3 ) +
                "                " +
                String( ( totalSum( iii ) * 1.0 ):a:20:2 ) );
       iii = iii + 1;
     end;

     if ( SlotLoad(0) != 0 )
       [

         Данные по кассете отказа "#####"

         ┌────────┬─────────┬──────────┐
         │        │         │Обнаружен-│
         │        │         │ ное коли-│
         │        │         │ чество   │
         │ Валюта │ Номинал │ банкнот  │
         ├────────┼─────────┼──────────┤] (SlotCsst(0));
       i = 0;
       while ( i < NumCassettes )
         if ( SlotNumb(i) == 0 )
           if ( FindCurrency( SlotCurr(i) ) )
             sCurr = Curr.Short_Name;
           else
             sCurr = String(SlotCurr(i));
           end;
           println( " │  ",
                    sCurr:4, "  │  ",
                    SlotNmnl(i):5, "  │  ",
                    SlotLoad(i):6, "  │" );
         end;
         i = i + 1;
       end;
       [ └────────┴─────────┴──────────┘];
     end;

  end;

  /***********************************************************/
  /*             Получение информации по кассетам            */
  /***********************************************************/
  macro GetCassettes

    var ret = TRUE;

    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )

      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        SlotNumb(NumCassettes) = fSlot.SlotNumber;
        SlotStat(NumCassettes) = fSlot.SlotStatus;
        SlotCurr(NumCassettes) = fSlot.SlotCurrency;
        SlotNmnl(NumCassettes) = fSlot.SlotNominal;
        if ( fSlot.SlotNumber == 0 )
          SlotLoad(NumCassettes) = fSlot.SlotQuantityUnLoad;
        else
          SlotLoad(NumCassettes) = fSlot.SlotQuantityLoad;
        end;
        SlotCsst(NumCassettes) = fSlot.CassetteID;
        NumCassettes = NumCassettes + 1;
        ret = Next( fSlot );
      end;
    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      return FALSE;
    end;

    return TRUE;

  end;

  /********************************************************/
  /*    Получение остатка банкнот по валюте и номиналу    */
  /********************************************************/
  macro GetRest( _SlotCurr, _SlotNmnl )
    if   ( ( fUnit.Cur0 == _SlotCurr )  AND  ( fUnit.Nominal0 == _SlotNmnl ) )
      return fUnit.Rest0;
    elif ( ( fUnit.Cur1 == _SlotCurr )  AND  ( fUnit.Nominal1 == _SlotNmnl ) )
      return fUnit.Rest1;
    elif ( ( fUnit.Cur2 == _SlotCurr )  AND  ( fUnit.Nominal2 == _SlotNmnl ) )
      return fUnit.Rest2;
    elif ( ( fUnit.Cur3 == _SlotCurr )  AND  ( fUnit.Nominal3 == _SlotNmnl ) )
      return fUnit.Rest3;
    elif ( ( fUnit.Cur4 == _SlotCurr )  AND  ( fUnit.Nominal4 == _SlotNmnl ) )
      return fUnit.Rest4;
    elif ( ( fUnit.Cur5 == _SlotCurr )  AND  ( fUnit.Nominal5 == _SlotNmnl ) )
      return fUnit.Rest5;
    elif ( ( fUnit.Cur6 == _SlotCurr )  AND  ( fUnit.Nominal6 == _SlotNmnl ) )
      return fUnit.Rest6;
    else
      return -1;
    end;
  end;

  /********************************************************/
  /*            Подсчет итоговых сумм по валютам          */
  /********************************************************/
  macro addTotalSum( Curr, Nmnl, Load, Rest )
    var ii = 0, nn = aSize( totalISO ), find = FALSE;

    while ( ii < nn )
      if ( totalISO( ii ) == Curr )
        find = TRUE;  /* Нашли валюту в итоговом массиве */
          totalSum( ii ) = totalSum( ii ) + ( Load - Rest ) * Nmnl;
      end;
      ii = ii + 1;
    end;

    /* Образуем новый элемент итоговых массивов */
    if ( NOT find )
      totalISO( ii ) = Curr;
      totalSum( ii ) = ( Load - Rest ) * Nmnl;
    end;

  end;

  /********************************************************/
  /***                Процедура инкассации              ***/
  /********************************************************/

  StatTCD(0) = "TCD выгружен";
  StatTCD(1) = "Загрузка не закончена";
  StatTCD(2) = "TCD загружен";
  StatTCD(3) = "Загрузка отменена";
  StatTCD(4) = "Начата выгрузка";
  StatTCD(5) = "Ошибка при выгрузке";

//macro mainProCash(in_aTCDRef:integer, in_aCurrency:integer, in_aSum:money ):integer
macro mainProCash(in_aTCDRef:integer, in_aCurrency:integer, in_aSum:money , in_adrCashDoc ):integer
 
 //Если TCDRef не передан
 if(in_aTCDRef == 0)
    /* Поиск записи по кассиру */
    ClearRecord( fPers );
    fPers.Oper = {oper};
    if ( getEQ( fPers ) )
      PersonRefLoad = fPers.Reference;
    else
      MsgBox( NoRight );
      stat = 1;
    end;

    /* Поиск записи по TCD */
    if ( stat == 0 )
      ClearRecord( fUnit );
      fUnit.Reference = fPers.TCDRef;
      if ( getEQ( fUnit ) )
        TCDRef = fUnit.Reference;
        TCD_ID = fUnit.ID;
      else
        MsgBox( "Запись по TCD|не найдена" );
        stat = 2;
      end;
    end;
 else
 //Если TCDRef передан
    if ( stat == 0 )
      ClearRecord( fUnit );
      fUnit.Reference = in_aTCDRef;
      if ( getEQ( fUnit ) )
        TCDRef = fUnit.Reference;
        TCD_ID = fUnit.ID;
      else
        MsgBox( "Запись по TCD|не найдена" );
        stat = 2;
      end;
    end;
 end;  

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Поиск записи по последней загрузке */
  if ( stat == 0 )
    ClearRecord( fLoad );
    fLoad.TCDRef    = TCDRef;
    fLoad.Reference = 2147483647;  /* Max Long */
    if ( ( NOT GetLE( fLoad ) )  OR  ( fLoad.TCDRef != TCDRef ) )
      MsgBox( "Не найдены записи загрузки/выгрузки|по филиалу ", Branch,
              "|для TCD \"", TCD_ID, "\"" );
      LoadRef = -1;
    else
      LoadRef = fLoad.Reference;
    end;
  end;

  /* Получение информации по кассетам */
  if ( ( stat == 0 )  AND  ( LoadRef > -1 ) )
    GetCassettes();
  end;

  /* Формирование массива остатков */
  if ( stat == 0 )
    i = 0;
    while ( i < NumCassettes )
      SlotRest(i) = GetRest( SlotCurr(i), SlotNmnl(i) );
      addTotalSum( SlotCurr(i), SlotNmnl(i), SlotLoad(i), SlotRest(i) );
      i = i + 1;
    end;
  end;

  /* Подготовка и выдача отчета */
  if ( stat == 0 )
    Slots      = fUnit.SlotsNum;
    UnitStatus = StatTCD(fUnit.LoadStatus);
    UnitDate   = fLoad.Date;
    UnitTime   = fLoad.Time;

    Report();
  end;

end;

//end;
