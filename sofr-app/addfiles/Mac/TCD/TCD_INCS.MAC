/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос инкассации TCD                                  */
/*                                                             */
/***************************************************************/

  Import deprintr, ListCodf, ListCurr;

  file   fLoad ( TCD_Load, "TCD.DEF" ) key 1 write;
  file   fSlot ( TCD_Slot, "TCD.DEF" ) key 1 write;
  file   fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file   fUnit ( TCD_Unit, "TCD.DEF" ) key 0;

  record dlgF ( "INC_FLTR", "TCD.LBR" ) dialog;
  record scrI ( "INC_SCRL", "TCD.LBR" ) dialog;

  /* Диалоги задания сумм загрузки по валютам */
  record dlg1( "INC_PAN1", "TCD.LBR" ) dialog;
  record dlg2( "INC_PAN2", "TCD.LBR" ) dialog;
  record dlg3( "INC_PAN3", "TCD.LBR" ) dialog;
  record dlg4( "INC_PAN4", "TCD.LBR" ) dialog;
  record dlg5( "INC_PAN5", "TCD.LBR" ) dialog;
  record dlg6( "INC_PAN6", "TCD.LBR" ) dialog;

  const NoRight = "Вы не можете|работать с TCD";

  const StatUnLd  = 7;  /* Статус записи загрузки: Успешная выгрузка */
  const StatIncs  = 9;  /* Статус записи загрузки: Проведена инкассация */

  const DateMin   = Date(  1,  1, 1990 );
  const TimeMin   = Time(  0,  0,  0 );
  const TimeMax   = Time( 23, 59, 59 );

  array SlotNumb, SlotStat, SlotCurr, SlotNmnl, SlotRef;
  array SlotLoad, SlotCsst, SlotUnLd, SlotRjct, SlotPay;
  array sTrnRet;
  array sType;
  array sStatus;

  /* Массивы итоговых сумм по валютам */
  array totalISO;
  array totalRSumL, totalRSumP, totalRSumU, totalRSumD;  /* Отчет */
  array totalSumReject;  /* Возврат */
  array totalBSumL, totalBSumP, totalBSumU, totalBSumR, totalBSumD;  /* Баланс */

  /* Параметры фильтра */
  var pDateMin = DateMin,  pDateMax = Date();
  var pStatMin = StatUnLd, pStatMax = StatIncs;
  var pType = 0;

  var sCassetteID;

  var {oper}, {curdate};

  var Branch = NumFNCash;

  var FirstRecI = True;

  var PersonRefIncass =  0;
  var TCDRef          =  0;
  var TCD_ID          = "";
  var LoadRef         =  0;
  var NumCassettes    =  0;
  var PayCassettes    =  0;  /* Число кассет выгрузки */
  var LoadRecPos      = -1;

  var sComment;

  var stat = 0;
  var i, j, k, l;
  var ret = TRUE;
  var TrnRet;

  /***********************************************************/
  /*               Процедуры фильтра скролинга               */
  /***********************************************************/
  macro TestRecI( ff )
    if ( ( ff.FNCash == Branch )    AND
         ( ff.TCDRef == TCDRef )    AND
         ( ff.Date   >= pDateMin )  AND
         ( ff.Date   <= pDateMax )  AND
         ( ff.Status >= pStatMin )  AND
         ( ff.Status <= pStatMax ) )
      return True;
    end;
    return False;
  end;

  macro NextIncass( ff )
    if ( FirstRecI )
      FirstRecI = False;
      ClearRecord( ff );
      ff.FNCash = Branch;
      ff.TCDRef = TCDRef;
      ff.Status = pStatMin;
      ff.Date   = pDateMin;
      ff.Time   = TimeMin;
      if ( GetGE( ff ) )
        return TestRecI( ff );
      end;
    else
      if ( Next( ff ) )
        return TestRecI( ff );
      end;
    end;
    return False;
  end;

  macro PrevIncass( ff )
    if ( FirstRecI )
      FirstRecI = False;
      ClearRecord( ff );
      ff.FNCash = Branch;
      ff.TCDRef = TCDRef;
      ff.Status = pStatMax;
      ff.Date   = pDateMax;
      ff.Time   = TimeMax;
      if ( GetLE( ff ) )
        return TestRecI( ff );
      end;
    else
      if ( Prev( ff ) )
        return TestRecI( ff );
      end;
    end;
    return False;
  end;

  macro RewindIncass( ff )
    Rewind( ff );
    FirstRecI = True;
  end;

  macro MakeIncass( dlg )
    i = 0;
    while ( i < NRec )
      /* k = Int( dlg(FldIndex(dlg,"Stat"+i)) ); */
      GetDirect( fLoad, APos(i) );
      k = fLoad.Status;
      if ( ( k > 6 )  AND  ( k < 10 ) )
        dlg(FldIndex(dlg,"Stat"+i)) = sStatus(k-7);
      else
        dlg(FldIndex(dlg,"Stat"+i)) = "???";
      end;
      i = i + 1;
    end;
  end;

  /********************************************************/
  /*      Подсчет итоговых сумм по валютам для отчета     */
  /********************************************************/
  macro addTotalReportSum( Curr, Nmnl, Load, Pay, UnLoad )
    var ii = 0, nn = aSize( totalISO ), find = FALSE;

    while ( ii < nn )
      if ( totalISO( ii ) == Curr )
        find = TRUE;  /* Нашли валюту в итоговом массиве */
        totalRSumL( ii ) = totalRSumL( ii ) + Load   * Nmnl;
        totalRSumP( ii ) = totalRSumP( ii ) + Pay    * Nmnl;
        totalRSumU( ii ) = totalRSumU( ii ) + UnLoad * Nmnl;
        totalRSumD( ii ) = totalRSumD( ii ) + ( Load - Pay - UnLoad ) * Nmnl;
      end;
      ii = ii + 1;
    end;

    /* Образуем новый элемент итоговых массивов */
    if ( NOT find )
      totalISO ( ii ) = Curr;
      totalRSumL( ii ) = Load   * Nmnl;
      totalRSumP( ii ) = Pay    * Nmnl;
      totalRSumU( ii ) = UnLoad * Nmnl;
      totalRSumD( ii ) = ( Load - Pay - UnLoad ) * Nmnl;
    end;

  end;

  /***********************************************************/
  /*             Получение информации по кассетам            */
  /***********************************************************/
  macro GetCassettes

    ret = TRUE;
    NumCassettes = 0;
    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )

      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        SlotRef (NumCassettes) = fSlot.Reference;
        SlotNumb(NumCassettes) = fSlot.SlotNumber;
        SlotStat(NumCassettes) = fSlot.SlotStatus;
        SlotCurr(NumCassettes) = fSlot.SlotCurrency;
        SlotNmnl(NumCassettes) = fSlot.SlotNominal;
        SlotLoad(NumCassettes) = fSlot.SlotQuantityLoad;
        SlotPay (NumCassettes) = fSlot.SlotQuantityPay;
        if ( fSlot.SlotNumber != 0 )
          SlotUnLd(NumCassettes) = fSlot.SlotQuantityUnLoad;
          SlotRjct(NumCassettes) = 0;
          PayCassettes = PayCassettes + 1;
        else
          SlotUnLd(NumCassettes) = 0;
          SlotRjct(NumCassettes) = fSlot.SlotQuantityUnLoad;
        end;
        SlotCsst(NumCassettes) = fSlot.CassetteID;

        NumCassettes = NumCassettes + 1;
        ret = Next( fSlot );
      end;
      i = 0;
      while ( i < NumCassettes )
        if ( SlotNumb(i) != 0 )
          SlotRjct(i) = 0;
          j = 0;
          while ( j < NumCassettes )
            if ( ( SlotCurr(i) == SlotCurr(j) )  AND
                 ( SlotNmnl(i) == SlotNmnl(j) ) )
              SlotRjct(i) = SlotRjct(j);
            end;
            j = j + 1;
          end;
        end;
        i = i + 1;
      end;
    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      return FALSE;
    end;

    return TRUE;

  end;

  /***********************************************************/
  /*     Обработка событий панели фильтра выбора выгрузок    */
  /***********************************************************/
  macro EventProcF( dlg, cmd, id, key )

    if ( cmd == DLG_REMFOCUS )
      if ( id < 2 )
        if ( dlg.Date1 > dlg.Date2 )
          MsgBox( "Дата начала ", dlg.Date1,
                  "|больше даты окончания ", dlg.Date2 );
          return CM_CANCEL;
        end;
      end;
    elif ( cmd == DLG_KEY )
      if ( key == 27 )
        if ( NOT GetTrue( FALSE, "Вы действительно хотите отменить инкассацию ?" ) )
          return CM_IGNORE;
        end;
      elif ( key == 317 )  /* F3 */
        if ( id == 2 )
          i = ListCodf( 51, 0, 0 );
          if ( i >= 0 )
            if ( i > 2 )
              i = 2;
            end;
            dlg.Type = sType(i);
            pType = i;
          end;
        end;
      end;
    elif ( cmd == DLG_SAVE )
      pDateMin = dlg.Date1;
      pDateMax = dlg.Date2;
      if   ( pType == 0 )  /* Выгруженные */
        pStatMin = StatUnLd;
        pStatMax = StatUnLd;
      elif ( pType == 1 )  /* Инкассации  */
        pStatMin = StatIncs;
        pStatMax = StatIncs;
      else                 /* Все         */
        pStatMin = StatUnLd;
        pStatMax = StatIncs;
      end;
    end;
    return CM_DEFAULT;
  end;

  /********************************************************/
  /*          Скролинг записей загрузки\выгрузки          */
  /********************************************************/
  macro LoadScroll

    var Stat = 0;

    /* Фильтр для выбора выгрузки */
    if ( Stat == 0 )
      dlgF.Date1 = DateMin;
      dlgF.Date2 = Date();
      dlgF.Type  = sType(0);
      if ( NOT RunDialog( dlgF, "EventProcF" ) )
        Stat = 27;
      end;
    end;

    ClearRecord( fLoad );
    fLoad.FNCash = Branch;
    fLoad.TCDRef = TCDRef;
    fLoad.Status = pStatMin;
    fLoad.Date   = pDateMin;
    fLoad.Time   = TimeMin;
    if ( ( NOT GetGE( fLoad ) )  OR  ( NOT TestRecI( fLoad ) ) )
      MsgBox( "Не найдены записи выгрузки|по филиалу ", Branch,
              "|для TCD \"", TCD_ID, "\"" );
      Stat = -1;
    end;

    /* Скролинг выбора выгрузки */
    if ( Stat == 0 )

      ReplaceMacro( "Next",     "NextIncass"   );
      ReplaceMacro( "Prev",     "PrevIncass"   );
      ReplaceMacro( "Rewind",   "RewindIncass" );
      ReplaceMacro( "UserFill", "MakeIncass"   );

      SetScroll( fLoad, "Date", "Date", "Time", "Time",
                        "Stat", "Status",
                        "Comment", "Commentary" );

      if ( RunDialog( scrI, "ScrollMes" ) )
        LoadRecPos = GetPos( fLoad );
        Stat = fLoad.Reference;
      else
        Stat = -1;
      end;

      ReplaceMacro( "Next" );
      ReplaceMacro( "Prev" );
      ReplaceMacro( "Rewind" );
      ReplaceMacro( "UserFill" );

    end;

    return Stat;

  end;

  /***********************************************************/
  /*         Подсчет итоговой суммы по кассете возврата      */
  /***********************************************************/
  macro calcTotalReject( Curr, Sum )
    var ii = 0, nn = aSize( totalISO );
    while ( ii < nn )
      if ( totalISO( ii ) == Curr )
        totalSumReject( ii ) = totalSumReject( ii ) + Sum;
      end;
      ii = ii + 1;
    end;
  end;

  /********************************************************/
  /*      Подсчет итоговых сумм по валютам для отчета     */
  /********************************************************/
  macro addTotalBalansSum( Curr, Nmnl, Load, Pay, UnLoad, Rjct )
    var ii = 0, nn = aSize( totalISO );

    while ( ii < nn )
      if ( totalISO( ii ) == Curr )
        totalBSumL( ii ) = totalBSumL( ii ) + Load   * Nmnl;
        totalBSumP( ii ) = totalBSumP( ii ) + Pay    * Nmnl;
        totalBSumU( ii ) = totalBSumU( ii ) + UnLoad * Nmnl;
        totalBSumR( ii ) = totalBSumR( ii ) + Rjct   * Nmnl;
        totalBSumD( ii ) = totalBSumD( ii ) +
                             ( Load - Pay - UnLoad - Rjct ) * Nmnl;
      end;
      ii = ii + 1;
    end;

  end;

  /***********************************************************/
  /*        Обработка событий панели инкассации кассет       */
  /***********************************************************/
  macro EventProc( dlg, cmd, id, key )
    var idR, idI, r;
    r = PayCassettes * 7 + 2;
    if ( id < r );
      idI = ( id - 2 ) / 7;
      idR = id - idI * 7;
    else
      idI = ( id - r ) / 4;
      idR = id - idI * 4;
    end;

    if ( cmd == DLG_REMFOCUS )
      if   ( ( id < r )  AND  ( idR == 7 ) )
        if ( dlg(id) < 0 )
          MsgBox( "Размер выгрузки ", dlg(id),
                  "|Выгрузка не может быть отрицательной" );
          return CM_CANCEL;
        end;
        if ( dlg(id) > dlg(id-2) )
          MsgBox( "Сумма выгрузки ", dlg(id),
                  "|задана больше суммы загрузки ", dlg(id-2) );
          return CM_CANCEL;
        end;
        dlg(id+1) = MoneyL( ( dlg(id-2) - dlg(id-1) - dlg(id) ) * dlg(id-3) / 100. );
        UpdateFields( dlg );
      elif ( ( id > r )  AND  ( ( idR - r ) == 2 ) )
        if ( dlg(id) < 0 )
          MsgBox( "Сумма возврата ", dlg(id),
                  "|Возврат не может быть отрицательным" );
          return CM_CANCEL;
        end;
        if ( dlg(id) > dlg(5+7*idI) )
          MsgBox( "Сумма возврата ", dlg(id),
                  "|задана больше суммы загрузки ", dlg(5+7*idI) );
          return CM_CANCEL;
        end;
        dlg(id+1) = MoneyL( dlg(id) * dlg(id-1) / 100. );
        UpdateFields( dlg );
      end;
    elif ( cmd == DLG_KEY )
      if ( key == 27 )
        if ( NOT GetTrue( FALSE, "Вы действительно хотите отменить инкассацию ?" ) )
          return CM_IGNORE;
        end;
      elif ( key == 318 )  /* F4 */
        GetString( sComment, "Комментарий загрузки-выгрузки", 70 );
      end;
    elif ( cmd == DLG_SAVE )
      i = 0;
      j = 0;
      while ( j < NumCassettes )
        if ( SlotCsst(j) == dlg(2+7*i) )
          SlotUnLd(j) = dlg(7+7*i);
          SlotRjct(j) = dlg(4+7*PayCassettes+4*i);
          /* Перенос данных в кассету отказа */
          k = 0;
          while ( k < NumCassettes )
            if ( ( SlotNumb(k) == 0 )            AND
                 ( SlotCurr(k) == SlotCurr(j) )  AND
                 ( SlotNmnl(k) == SlotNmnl(j) ) )
              /* SlotUnLd(k) = SlotUnLd(j); */
              SlotRjct(k) = SlotRjct(j);
            end;
            k = k + 1;
          end;
          i = i + 1;
        end;
        j = j + 1;
      end;
    end;
    return CM_DEFAULT;
  end;

  /*************************************************************/
  /*         Процедура транзакции завершения инкассации        */
  /*************************************************************/
  macro TrnProcIncass

    var curSlotUnLoad;

    TrnRet = 0;

    /* Запись данных по слотам */
    ret = TRUE;
    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )

      while ( ret  AND  ( TrnRet == 0 )  AND  ( fSlot.LoadRef == LoadRef ) )

        if ( fSlot.SlotNumber == 0 )
          /* Запись информации по Reject-кассетам */
          if ( fLoad.Status != StatIncs )
            /* Для начальной инкассации */
            fSlot.SlotCurrency       = SlotCurr( 1 );
            fSlot.SlotNominal        = SlotNmnl( 1 );
            fSlot.SlotQuantityUnLoad = SlotRjct( 1 );
            ret = Update( fSlot );
            i = 1;
            while ( ret  AND  ( i < (NumCassettes-1) ) )
              fSlot.Reference          = 0;
              fSlot.SlotCurrency       = SlotCurr( i+1 );
              fSlot.SlotNominal        = SlotNmnl( i+1 );
              fSlot.SlotQuantityUnLoad = SlotRjct( i+1 );
              ret = Insert( fSlot );
              i = i + 1;
            end;
            if ( NOT ret )
              TrnRet = 2;
              sCassetteID = fSlot.CassetteID;
            end;
          else
            /* Для редактирования инкассации */
            curSlotUnLoad = -1;
            i = 0;
            while ( i < NumCassettes )
              if ( SlotRef( i ) == fSlot.Reference )
                curSlotUnLoad = SlotRjct( i );
              end;
              i = i + 1;
            end;
            if ( curSlotUnLoad == -1 )
              TrnRet = 7;
              sCassetteID = fSlot.CassetteID;
              ret = FALSE;
            else
              fSlot.SlotQuantityUnLoad = curSlotUnLoad;
              ret = Update( fSlot );
            end;
          end;
        else
          /* Кассеты выдачи */
          curSlotUnLoad = -1;
          i = 0;
          while ( i < NumCassettes )
            if ( SlotRef( i ) == fSlot.Reference )
              curSlotUnLoad = SlotUnLd( i );
            end;
            i = i + 1;
          end;
          if ( curSlotUnLoad == -1 )
            TrnRet = 1;
            sCassetteID = fSlot.CassetteID;
            ret = FALSE;
          else
            fSlot.SlotQuantityUnLoad = curSlotUnLoad;
            ret = Update( fSlot );
          end;
        end;

        if ( ret )
          ret = Next( fSlot );
        else
          TrnRet = 2;
          sCassetteID = fSlot.CassetteID;
        end;
      end;
    else
      TrnRet = 3;
      sCassetteID = "";
    end;

    /* Установка статуса успешной загрузки */
    if ( TrnRet == 0 )
      GetDirect( fLoad, LoadRecPos );
      fLoad.Status = 9;  /* Статус: Инкассация */
      fLoad.PersonRefIncass = PersonRefIncass;
      fLoad.Commentary = sComment;
      if ( NOT Update( fLoad ) )
        TrnRet = 4;
        sCassetteID = "";
      end;
    end;

    if ( TrnRet != 0 )
      AbortTrn();
    end;

  end;

  /***********************************************************/
  /*                   Отчет по инкассации                   */
  /***********************************************************/
  macro CassettesReport

    var cCur = "";

    var iii = 0, nnn = aSize( totalISO ), sCurr;

    /* Отчет по кассетам выдачи */
    ret = TRUE;
    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )
      [
                     Отчет по инкассации за дату ########## время ######## кассир ####


        ┌──────┬──────────┬────────┬─────────┬───────────┬───────────┬───────────┬──────────────────────┐
        │ Слот │ Кассета  │ Валюта │ Номинал │ Загружено │   Выдано  │ Выгружено │    Сумма разности    │
        ├──────┼──────────┼────────┼─────────┼───────────┼───────────┼───────────┼──────────────────────┤]
      ( fLoad.Date, fLoad.Time, {oper} );

      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        if ( fSlot.SlotNumber != 0 )
        if ( FindCurrency( fSlot.SlotCurrency ) )
          cCur = " " + Curr.Short_Name;
        else
          cCur = "   " + String(fSlot.SlotCurrency);
        end;
        [ │ #### │ ######## │ ###### │ ####### │ ######### │ ######### │ ######### │ #################### │]
        ( fSlot.SlotNumber:4, fSlot.CassetteID:8, cCur:6, fSlot.SlotNominal:7,
          fSlot.SlotQuantityLoad:9, fSlot.SlotQuantityPay:9, fSlot.SlotQuantityUnLoad:9,
          MoneyL( ( fSlot.SlotQuantityLoad - fSlot.SlotQuantityPay - fSlot.SlotQuantityUnLoad ) *
                    fSlot.SlotNominal ):20:2 );
          if ( fSlot.SlotNumber != 0 )
            addTotalReportSum( fSlot.SlotCurrency,
                               fSlot.SlotNominal,
                               fSlot.SlotQuantityLoad,
                               fSlot.SlotQuantityPay,
                               fSlot.SlotQuantityUnLoad );
          end;
        end;
        ret = Next( fSlot );
      end;
      [ └──────┴──────────┴────────┴─────────┴───────────┴───────────┴───────────┴──────────────────────┘];

      println( "        Итого сумма:" );

      nnn = aSize( totalISO );
      while ( iii < nnn )
        if ( FindCurrency( totalISO( iii ) ) )
          sCurr = Curr.Short_Name;
        else
          sCurr = String( totalISO( iii ) );
        end;
        println( "                      " +
                 String( sCurr:3 ) +
                 "                 " +
                 String( ( totalRSumL( iii ) * 1.0 ):a:10:2 ) +
                 "  " +
                 String( ( totalRSumP( iii ) * 1.0 ):a:10:2 ) +
                 "  " +
                 String( ( totalRSumU( iii ) * 1.0 ):a:10:2 ) +
                 String( ( totalRSumD( iii ) * 1.0 ):a:20:2 ) );
        iii = iii + 1;
      end;

    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      return FALSE;
    end;

    /* Начальное обнуление сумм по кассете возврата */
    iii = 0;
    nnn = aSize( totalISO );
    while ( iii < nnn )
      totalSumReject( iii ) = 0;
      iii = iii + 1;
    end;

    /* Отчет по кассете возврата */
    ret = TRUE;
    ClearRecord( fSlot );
    fSlot.LoadRef = LoadRef;
    fSlot.SlotNumber = 0;
    if ( GetGE( fSlot )  AND  ( fSlot.LoadRef == LoadRef ) )
      [
                   Кассета возврата "#####"

        ┌────────┬─────────┬───────────┬────────────────┐
        │ Валюта │ Номинал │  Банкнот  │ Сумма возврата │
        ├────────┼─────────┼───────────┼────────────────┤]
      ( fSlot.CassetteID:5 );
      while ( ret  AND  ( fSlot.LoadRef == LoadRef ) )
        if ( fSlot.SlotNumber == 0 )
        if ( FindCurrency( fSlot.SlotCurrency ) )
          cCur = " " + Curr.Short_Name;
        else
          cCur = "   " + String(fSlot.SlotCurrency);
        end;
        [ │ ###### │ ####### │ ######### │ ############## │]
        ( cCur:6, fSlot.SlotNominal:7,
          fSlot.SlotQuantityUnLoad:9,
          MoneyL( fSlot.SlotQuantityUnLoad * fSlot.SlotNominal ):a:14:2 );
          calcTotalReject( fSlot.SlotCurrency,
                           fSlot.SlotQuantityUnLoad * fSlot.SlotNominal );
        end;
        ret = Next( fSlot );
      end;
      [ └────────┴─────────┴───────────┴────────────────┘];

      println( "  Итого:" );
      iii = 0;
      nnn = aSize( totalISO );
      while ( iii < nnn )
        if ( FindCurrency( totalISO( iii ) ) )
          sCurr = Curr.Short_Name;
        else
          sCurr = String( totalISO( iii ) );
        end;
        println( "    " +
                 String( sCurr:3 ) +
                 "                      " +
                 String( ( totalSumReject( iii ) * 1.0 ):a:19:2 ) );
        iii = iii + 1;
      end;

    else
      MsgBox( "Записи по кассетам|для загрузки с Reference = ", LoadRef,
              "|не найдены" );
      return FALSE;
    end;

    /* Начальное обнуление сумм по балансу */
    iii = 0;
    nnn = aSize( totalISO );
    while ( iii < nnn )
      totalBSumL( iii ) = 0;
      totalBSumP( iii ) = 0;
      totalBSumU( iii ) = 0;
      totalBSumR( iii ) = 0;
      totalBSumD( iii ) = 0;
      iii = iii + 1;
    end;

    [
                                 Баланс по номиналам

      ┌────────┬─────────┬───────────┬───────────┬───────────┬─────────┬──────────────────────┐
      │ Валюта │ Номинал │ Загружено │   Выдано  │ Выгружено │ Возврат │    Сумма разности    │
      ├────────┼─────────┼───────────┼───────────┼───────────┼─────────┼──────────────────────┤];
    i = 0;
    while ( i < aSize( SlotLoad ) )
      if ( SlotNumb(i) != 0 )
        if ( FindCurrency( SlotCurr(i) ) )
          cCur = " " + Curr.Short_Name;
        else
          cCur = "   " + String( SlotCurr(i) );
        end;
        [ │ ###### │ ####### │ ######### │ ######### │ ######### │ ####### │ #################### │]
        ( cCur:6, SlotNmnl(i):7,
          SlotLoad(i):9, SlotPay(i):9, SlotUnLd(i):9, SlotRjct(i):7,
          MoneyL( SlotNmnl(i) * ( SlotLoad(i) - SlotPay(i) - SlotUnLd(i) - SlotRjct(i) ) ):a:20 );
          addTotalBalansSum( SlotCurr(i),
                             SlotNmnl(i),
                             SlotLoad(i),
                             SlotPay(i),
                             SlotUnLd(i),
                             SlotRjct(i) );
      end;
      i = i + 1;
    end;
    [ └────────┴─────────┴───────────┴───────────┴───────────┴─────────┴──────────────────────┘

    ];

    println( "  Итого:" );
    iii = 0;
    nnn = aSize( totalISO );
    while ( iii < nnn )
      if ( FindCurrency( totalISO( iii ) ) )
        sCurr = Curr.Short_Name;
      else
        sCurr = String( totalISO( iii ) );
      end;
      println( "    " +
               String( sCurr:3 ) +
               "                 " +
               String( ( totalBSumL( iii ) * 1.0 ):a:10:2 ) +
               "  " +
               String( ( totalBSumP( iii ) * 1.0 ):a:10:2 ) +
               "  " +
               String( ( totalBSumU( iii ) * 1.0 ):a:10:2 ) +
               String( ( totalBSumR( iii ) * 1.0 ):a:10:2 ) +
               " " +
               String( ( totalBSumD( iii ) * 1.0 ):a:19:2 ) );
      iii = iii + 1;
    end;

    println( "\n" );

    i = 0;
    while ( i < aSize( SlotLoad ) )
      if ( SlotNumb(i) != 0 )
        if ( ( SlotLoad(i) - SlotPay(i) - SlotUnLd(i) - SlotRjct(i) ) != 0 )
          if ( FindCurrency( SlotCurr(i) ) )
            cCur = " " + Curr.Short_Name;
          else
            cCur = "   " + String( SlotCurr(i) );
          end;
          println( "  Нарушен баланс числа банкнот по валюте ", cCur:4,
                   ", номиналу ", SlotNmnl(i):5 );
        end;
      end;
      i = i + 1;
    end;

    return TRUE;

  end;


  /********************************************************/
  /***                Процедура инкассации              ***/
  /********************************************************/

  sTrnRet(1) = "Не найдена запись|по кассете ";
  sTrnRet(2) = "Невозможно обновить запись|по кассете ";
  sTrnRet(3) = "Невозможно прочитать записи по кассете";
  sTrnRet(4) = "Невозможно обновить|запись загрузки/выгрузки";

  sType(0) = "Выгруженные";
  sType(1) = "Инкассация";
  sType(2) = "Все";

  sStatus(0) = "Выгружен";
  sStatus(1) = "Ошибка выгр.";
  sStatus(2) = "Инкассация";

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = {oper};
  if ( getEQ( fPers ) )
    PersonRefIncass = fPers.Reference;
  else
    MsgBox( NoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Инициализация массивов */
  i = 0;
  while ( i < 7 )
    SlotRjct(i) = 0;
    i = i + 1;
  end;

  /* Скролинг файла загрузок\выгрузок TCD */
  if ( stat == 0 )
    LoadRef = LoadScroll();
    if ( LoadRef > -1 )
      sComment = fLoad.Commentary;
      if ( GetCassettes() )
        dlg6.Date = fLoad.Date;
        dlg6.Oper = {oper};
        i = 0;
        j = 0;
        k = 44;
        l = 0;
        while ( i < NumCassettes )
          if ( SlotNumb( i ) != 0 )
            dlg6(2+j) = SlotCsst(i);
            if ( FindCurrency( SlotCurr(i) ) )
              dlg6(3+j) = Curr.Short_Name;
              dlg6(k+l) = Curr.Short_Name;
            else
              dlg6(3+j) = String(SlotCurr(i));
              dlg6(k+l) = String(SlotCurr(i));
            end;
            dlg6(4+j)   = SlotNmnl(i);
            dlg6(5+j)   = SlotLoad(i);
            dlg6(6+j)   = SlotPay (i);
            dlg6(7+j)   = SlotUnLd(i);
            dlg6(8+j)   = MoneyL( ( SlotLoad(i) - SlotPay(i) - SlotUnLd(i) ) *
                                    SlotNmnl(i) / 100. );
            dlg6(k+l+1) = SlotNmnl(i);
            dlg6(k+l+2) = SlotRjct(i);
            dlg6(k+l+3) = MoneyL( SlotRjct(i) * SlotNmnl(i) / 100. );
            j = j + 7;
            l = l + 4;
          end;
          i = i + 1;
        end;

        if   ( PayCassettes == 1 )
          Copy( dlg1, dlg6 );
          ret = RunDialog( dlg1, "EventProc" );
          Copy( dlg6, dlg1 );
        elif ( PayCassettes == 2 )
          Copy( dlg2, dlg6 );
          ret = RunDialog( dlg2, "EventProc" );
          Copy( dlg6, dlg2 );
        elif ( PayCassettes == 3 )
          Copy( dlg3, dlg6 );
          ret = RunDialog( dlg3, "EventProc" );
          Copy( dlg6, dlg3 );
        elif ( PayCassettes == 4 )
          Copy( dlg4, dlg6 );
          ret = RunDialog( dlg4, "EventProc" );
          Copy( dlg6, dlg4 );
        elif ( PayCassettes == 5 )
          Copy( dlg5, dlg6 );
          ret = RunDialog( dlg5, "EventProc" );
          Copy( dlg6, dlg5 );
        else
          ret = RunDialog( dlg6, "EventProc" );
        end;
      end;
    else
      stat = -1;
    end;
  end;

  /* Транзакция завершения загрузки */
  if ( ret  AND  ( stat == 0 ) )
    ret = ProcessTrn( 1, "TrnProcIncass" );
    if( NOT ret )
      MsgBox( "Невозможна запись|данных завершения инкассации" );
      if ( TrnRet )
        MsgBox( sTrnRet(TrnRet) + sCassetteID );
        stat = 7;
      end;
    end;
  end;

  /* Отчет по инкассации */
  if ( stat == 0 )
    CassettesReport();
  end;
