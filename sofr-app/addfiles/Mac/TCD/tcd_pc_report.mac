/*********************************************************\
  R-Style Softlab
  RS-Retail      
  Электронный кассир Pro Cash 6000               
  Воронцов Ю.В., IIX.2006  

 Отчет по последней операции - tcd_pc_rop.mac
 Отчет об остатках денег в электронном кассире - tcd_pc_rinf.mac

 Точки вызова: 
 Отчет по последней операции - tcd_pc_rop.mac
 Отчет об остатках денег в электронном кассире - tcd_pc_rinf.mac
 
\*********************************************************/
//Параметры: TCD_PARM.aTCDRef - ID ЭК смены, к которой относится сейф-участник операции

Import tcd_pc_def;

var {oper}, {curdate},{curtime};

var objReport;//объект рапорта о барабанах
var objInfo; //объект информации о барабане

var stat;
var str2="???????";

//Таблица оперов
file person ("person.dbt") key 0;

//параметры барабана
var drumPar:money; //номинал
var drumCount:integer; //количество
var drumSum:money; //сумма в нём
var numDr:integer;//номер барабана

var strNameOper;//имя операциониста из person.dbt

var procashSUM:money;//сумма денег во всех барабанах

//массивы для "ИТОГО"
//массив значений номиналов купюр
array aNotePar;
//массив сумм по номиналам
array aNoteSum;
//массив количества купюр
array aNoteCount;

//индекс элементов в массивах по номиналу
var inom;

//Инициализация массивов для "ИТОГО"
macro initNoteInfo()
  //инициализация массива номиналов купюр
   aNotePar(0) = 10;
   aNotePar(1) = 50;
   aNotePar(2) = 100;
   aNotePar(3) = 500;
   aNotePar(4) = 1000;
   aNotePar(5) = 5000;

  //инициализация массива сумм по номиналам
   aNoteSum(0) = 0;
   aNoteSum(1) = 0;
   aNoteSum(2) = 0;
   aNoteSum(3) = 0;
   aNoteSum(4) = 0;
   aNoteSum(5) = 0;

  //инициализация массива колва купюр по номиналам
   aNoteCount(0) = 0;
   aNoteCount(1) = 0;
   aNoteCount(2) = 0;
   aNoteCount(3) = 0;
   aNoteCount(4) = 0;
   aNoteCount(5) = 0;

end; //macro initNoteInfo 

//Затолкнуть информацию про номинал банкнот в массивы 
macro pushNoteInfo( in_drumPar:money,in_drumCount:integer,in_drumSum:money )
   var inom =0;
   var max_inom=5;
   
   //поиск номинала в массиве номиналов
   inom = 0;
   while ( ( in_drumPar != aNotePar(inom) ) and ( inom <= max_inom ) )
     inom = inom +1;
     if(inom > max_inom ) return; end;
   end;

   if(inom <= max_inom)
     //занесение данных в массивы с таким же индекcом
     aNoteSum(inom) = aNoteSum(inom) + in_drumSum;
     aNoteCount(inom) = aNoteCount(inom) + in_drumCount;
   end;//if
   
   

end;//macro pushNoteInfo


//************************************************************
//Получить отчёт о барабанах ЭК 
//1 - Информация о последней операции
//2 - Информация об остатках в ЭК
macro getProCashReport( in_fun_mode:integer , in_TCDRef:integer ) : bool
     
  if(in_fun_mode == _REPMODE_LASTOP) 
  //2.1.        Получить данные о купюрах, выданных (принятых) 
  //в рамках последней выполненной операции (метод TProCashDevice.getLastOperationPars)
     str2 = "операции с электронным кассиром";
     objReport = objPCDevice.getLastOperationPars();
  else
    if(in_fun_mode == _REPMODE_RESTINFO)
    //2.1. Получить данные об остатках в ЭК (метод TProCashDevice.getMoneyRestInfo)
     str2 = "остатках электронного кассира";
     objPCDevice.reInit(); //scr#96847- обновление информации об остатках
     objReport = objPCDevice.getMoneyRestInfo();
    else
       return false;//("Неопределённый режим для функции getProCashReport("+string(in_fun_mode)+")");
    end;
  end;
	
	//2.2.        Вывести отчет. Форма приведена в Приложении 1
	
	//получить имя сотрудника из таблицы dperson_dbt (t_Name)
	//person.oper = 9999;//{oper};
	person.oper = {oper};
	if (getEQ (person))
           strNameOper = person.Name;
	else
    	   strNameOper = "";
	end; //if getEQ
	
	//Ищем поле ID для ЭК
        var rs_ID; var rs_NetAddress; var rs_Desc;
        if( findInTcdUnit( in_TCDRef , @rs_ID , @rs_NetAddress, @rs_Desc) )
        else
          rs_ID = "?????"; 
        end;
	
	//инициализация массивов для "ИТОГО"
	initNoteInfo();
	

	//-------------------------------------------------------------------------------------
	//Приложение 1. Формат файла-отчета об остатках электронного кассира/операции с электронным кассиром
	// ДАТА: <{curdate}>  ВРЕМЯ: <текущее время>  СОТРУДНИК: <{oper}> - <имя сотрудника>
	[
	ДАТА: ##################     ВРЕМЯ: ####################  
	СОТРУДНИК: ##### - ###############################################]
	({curdate}:l,time:l,{oper}:r,strNameOper:w:l);
	
	[  ];
	
	//Файл-отчет об <"остатках электронного кассира" | "операции с электронным кассиром"> <tcd_unit.ID> 
//	[Файл-отчет об ###################################### - ###################]( str2:w , tcd_unit.ID:w );
	[Файл-отчет об ###################################### - ###################]( str2:w , rs_ID:w );
	
	[  ];
	
	//<tcd_unit.Description>
//	[##############################################################################](tcd_unit.Description:w); 
	[##############################################################################](rs_Desc:w); 
	
	[  ];
	
	//╣Барабана        Номинал барабана        Количество купюр        Сумма
	[!------------------------------------------------------------------------------!];
	[!   N Барабана   !   Номинал барабана    !   Количество купюр  !    Сумма      !];
	//[! ╣ Барабана     ! Номинал барабана      ! Количество купюр    ! Сумма         !];
	[!------------------------------------------------------------------------------!];
	
	//1-й барабан
	objReport.getFirstDrum( numDr, objInfo );
	drumPar = objInfo.getPar();//номинал купюр
	drumCount = objInfo.getCount();//количество купюр
	drumSum = drumPar * drumCount;//сумма в барабане
	pushNoteInfo(drumPar,drumCount,drumSum);
	//  [ ╣ Барабана     ! Номинал барабана      ! Количество купюр    ! Сумма         ];
	[!################!#######################!#####################!###############!]
	(numDr:c, drumPar:c, drumCount:c, drumSum:c:w );
	[!------------------------------------------------------------------------------!];
	
	while(objReport.getNextDrum( numDr, objInfo ))
  	drumPar = objInfo.getPar();//номинал купюр
  	drumCount = objInfo.getCount();//количество купюр
  	drumSum = drumPar * drumCount;//сумма в барабане
  	pushNoteInfo(drumPar,drumCount,drumSum);
	[!################! ######################! ####################! ##############!]
	(numDr:c, drumPar:c, drumCount:c, drumSum:c );
	[!------------------------------------------------------------------------------!];
	end; //while
	
	//        ИТОГО        Номинал         Количество купюр        Сумма
	[!      ИТОГО     !       Номинал         !  Количество купюр   !     Сумма     !];
	//[!      ИТОГО     ! Номинал               ! Количество купюр    ! Сумма         !];
	[!------------------------------------------------------------------------------!];
	
	inom = 0;
	procashSUM = 0;//сумма в ЭК во всех барабанах
	while( inom <= 5)
	
  	  if( 
  	           //(aNoteCount(inom)!=0) and 
  	           (aNoteSum(inom)!=0)
  	  )
    	//        100        136        13600
	[!                ! ##################### ! ################### ! ##############!]
	(  aNotePar(inom):c, aNoteCount(inom):c, aNoteSum(inom):w:c  );
	[!------------------------------------------------------------------------------!];
	
    	  //сумма в ЭК во всех барабанах
    	  procashSUM = procashSUM + aNoteSum(inom);
    	
     	  end;//if
	
  	  inom = inom + 1;
	
	end;//while
	
	//        ИТОГО: 14300 руб
	[        ИТОГО: ######################## руб.](procashSUM:c);
	[  ];
	
  return true;
	
end;//macro getProCashReport


//1 - Информация о последней операции
//2 - Информация об остатках в ЭК
//getProCashReport(2);
