import rslscrp;

file   Codf ( TCD_Codf, "TCD.DEF" ) key 2;
record scrC ( ListCodf, "TCD.LBR" ) dialog;

const PARITY_VAR = 1;  /* Вариант паритетности для работы через СОМ-порт */

var CurEntity, CurType, CurKind;
var FirstRec = True;

macro TestRec( ff )
  if ( ( ff.EntityCode == CurEntity )  and
       ( ff.Type       == CurType   )  and
       ( ff.Kind       == CurKind   ) )
    return True;
  end;
  return False;
end;

macro NextCodf( ff )
  if ( FirstRec )
    FirstRec = False;
    ClearRecord( ff );
    ff.EntityCode = CurEntity;
    ff.Type = CurType;
    ff.Kind = CurKind;
    ff.Priority = 0;
    ff.Code = 0;
    if ( GetGE( ff ) )
      return TestRec( ff );
    end;
  else
    if ( Next( ff ) )
      return TestRec( ff );
    end;
  end;
  return False;
end;

macro PrevCodf( ff )
  if ( FirstRec )
    FirstRec = False;
    ClearRecord( ff );
    ff.EntityCode = CurEntity;
    ff.Type = CurType;
    ff.Kind = CurKind;
    ff.Priority = 9999;
    ff.Code = 9999;
    if ( GetLE( ff ) )
      return TestRec( ff );
    end;
  else
    if ( Prev( ff ) )
      return TestRec( ff );
    end;
  end;
  return False;
end;

macro RewindCodf( ff )
  Rewind( ff );
  FirstRec = True;
end;

macro ListCodf( Entity, Type, Kind )

  var Ret = 0;

  CurEntity = Entity;
  CurType   = Type;
  CurKind   = Kind;

  ClearRecord( Codf );
  Codf.EntityCode = CurEntity;
  Codf.Type = CurType;
  Codf.Kind = CurKind;
  Codf.Priority = 0;
  Codf.Code = 0;
  if ( ( NOT GetGE( Codf ) )  OR  ( NOT TestRec( Codf ) ) )
    MsgBox( "Не найдены кодификаторы|сущности ", CurEntity,
            "|с типом ", CurType, "|вида ", CurKind );
    Ret = -1;
  end;

  if ( Ret == 0 )

    ReplaceMacro( "Next",   "NextCodf"   );
    ReplaceMacro( "Prev",   "PrevCodf"   );
    ReplaceMacro( "Rewind", "RewindCodf" );

    SetScroll( Codf, "F0_", "CodeName" );

    if ( RunDialog( scrC, "ScrollMes" ) )
      Ret = Codf.Code;
    else
      Ret = -1;
    end;

    ReplaceMacro( "Next" );
    ReplaceMacro( "Prev" );
    ReplaceMacro( "Rewind" );

  end;

  return Ret;

end;
