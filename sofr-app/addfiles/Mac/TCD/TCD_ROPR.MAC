/***************************************************************/
/*                                                             */
/*      RS-Retail 2.0                                          */
/*                                                             */
/*      Система обслуживания TCD                               */
/*                                                             */
/*      Макрос отчета по операциям TCD                         */
/*                                                             */
/***************************************************************/

  Import deprintr, ListCurr;

  file   fPers ( TCD_Pers, "TCD.DEF" ) key 1;
  file   fUnit ( TCD_Unit, "TCD.DEF" ) key 0;
  file   fOper ( TCD_Oper, "TCD.DEF" ) key 3;

  record dlg( "REP_PARM", "TCD.LBR" ) dialog;

  const NoRight = "Вы не можете|работать с TCD";

  array aStat;

  /* Массивы итоговых сумм по валютам */
  array totalOperationISO, totalOperationSum;

  var {oper}, {curdate};

  var PersonRefLoad = 0;
  var TCDRef        = 0;
  var TCD_ID        = "";
  var LoadRef       = 0;
  var cCur          = "";

  var Branch = NumRealFNCash;

  var Casher, Date1, Date2;

  var sStat = "";

  var stat  = 0;
  var ret   = TRUE;

  var iii, nnn;

  /********************************************************/
  /*            Подсчет итоговых сумм по валютам          */
  /********************************************************/
  macro addTotalOperationSum( Curr, Summa )
    var ii = 0, nn = aSize( totalOperationISO ), find = FALSE;

    while ( ii < nn )
      if ( totalOperationISO( ii ) == Curr )
        find = TRUE;  /* Нашли валюту в итоговом массиве */
        totalOperationSum( ii ) = totalOperationSum( ii ) + Summa;
      end;
      ii = ii + 1;
    end;

    /* Образуем новый элемент итоговых массивов */
    if ( NOT find )
      totalOperationISO( ii ) = Curr;
      totalOperationSum( ii ) = Summa;
    end;

  end;

  /********************************************************/
  /***                Отчет по операциям                ***/
  /********************************************************/

  aStat(0) = "Успешная операция";
  aStat(1) = "Операция инициирована";
  aStat(2) = "Опер. пополнен. создана";
  aStat(3) = "Ош.выд., попытка удал.оп.";
  aStat(4) = "Ош.выд., операция удалена";
  aStat(5) = "Ош.выд., оп.невозм.удалить";
  aStat(6) = "Деньги получены неправильно";

  Casher = {oper};
  Date1  = Date();
  Date2  = Date1;

  /* Диалог получения параметров отчета */
  dlg.Oper  = Casher;
  dlg.Date1 = Date1;
  dlg.Date2 = Date2;

  if ( RunDialog( dlg ) )
    Casher = dlg.Oper;
    Date1  = dlg.Date1;
    Date2  = dlg.Date2;
  end;

  /* Поиск записи по кассиру */
  ClearRecord( fPers );
  fPers.Oper = Casher;
  if ( getEQ( fPers ) )
    PersonRefLoad = fPers.Reference;
  else
    MsgBox( NoRight );
    stat = 1;
  end;

  /* Поиск записи по TCD */
  if ( stat == 0 )
    ClearRecord( fUnit );
    fUnit.Reference = fPers.TCDRef;
    if ( getEQ( fUnit ) )
      TCDRef = fUnit.Reference;
      TCD_ID = fUnit.ID;
    else
      MsgBox( "Запись по TCD|не найдена" );
      stat = 2;
    end;
  end;

  /* Проверка филиала TCD */
  if ( ( stat == 0 )  AND  ( fUnit.FNCash != Branch ) )
    MsgBox( "Филиал TCD (# ", fUnit.FNCash,
            ") не является|текущим филиалом (# ", Branch, ")" );
    stat = 3;
  end;

  /* Поиск записи по последней загрузке */
  if ( stat == 0 )
    ClearRecord( fOper );
    fOper.FNCash = Branch;
    fOper.TCDRef = TCDRef;
    fOper.Date   = Date1;
    if ( ( GetGE( fOper ) )          AND
         ( fOper.FNcash == Branch )  AND
         ( fOper.TCDRef == TCDRef )  AND
         ( fOper.Date   >= Date1  )  AND
         ( fOper.Date   <= Date2  ) )
      [
         Отчет по операциям. Кассир ####. Даты от ########## до ##########.

        ┌────────────┬──────────┬─────────────────────┬─────┬───────────────────────────┬──────────────────────────────┬────────────┐
        │    Дата    │  Время   │        Сумма        │ Ва- │          Номер            │           Статус             │    Дата    │
        │            │          │       выдачи        │ люта│          счета            │                              │   опердня  │
        ├────────────┼──────────┼─────────────────────┼─────┼───────────────────────────┼──────────────────────────────┼────────────┤
      ] ( Casher, Date1, Date2 );
      while ( ret  AND
              ( fOper.FNcash == Branch )  AND
              ( fOper.TCDRef == TCDRef )  AND
              ( fOper.Date   >= Date1  )  AND
              ( fOper.Date   <= Date2  ) )
        if ( FindCurrency( fOper.Currency ) )
          cCur = Curr.Short_Name;
        else
          cCur = String(fOper.Currency);
        end;
        if ( fOper.Status < 6 )
          sStat = aStat( fOper.Status );
        else
          sStat = aStat( fOper.Status ) + ": " + fOper.Commentary;
        end;
        [ │ ########## │ ######## │ ################### │ ### │ ######################### │ ############################ │ ########## │]
        ( fOper.Date, fOper.Time, fOper.Sum:a:20:2, cCur:3,
          fOper.Account:25, sStat:w, fOper.CurDate );
        addTotalOperationSum( fOper.Currency, fOper.Sum );
        ret = Next( fOper );
      end;
      [ └────────────┴──────────┴─────────────────────┴─────┴───────────────────────────┴──────────────────────────────┴────────────┘];

      println( "                   Итого:" );

      iii = 0;
      nnn = aSize( totalOperationISO );

      while ( iii < nnn )
        if ( FindCurrency( totalOperationISO( iii ) ) )
          cCur = Curr.Short_Name;
        else
          cCur = String( totalOperationISO( iii ) );
        end;
        println( "                          " +
                 String( totalOperationSum( iii ):a:20:2 ) +
                 "   " +
                 String( cCur:3 ) );
        iii = iii + 1;
      end;

    else
      MsgBox( "Не найдены записи операций|по филиалу ", Branch,
              "|для TCD \"", TCD_ID, "\"" +
              "|от даты ", Date1, " до ", Date2 );
      LoadRef = -1;
    end;
  end;

end;
