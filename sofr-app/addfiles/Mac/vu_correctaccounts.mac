/*
$Name:             vu_correctaccounts.mac
$Module:           БОЦБ
$Description:      Корректировка счетов ВУ единоразовая
*/

IMPORT "sp_car.mac", "scservop.mac", "dlQuery.mac";

PRIVATE VAR CarryError = "";
PRIVATE VAR ID_Operation = 0;
PRIVATE RECORD AccountPayer(account);
PRIVATE RECORD AccountReceiver(account);

VAR tableHeader = 
 "┌──────────────────────┬───────────────────┬─────────────────────┬─────────────────────┬────────────────────┬────────────┬───────────────────────────────────────────────────────────────────┐\n"+
 "│      Код сделки      │    Код клиента    │        Дебет        │        Кредит       │   Сумма проводки   │    Дата    │                             Основание                             │\n"+
 "├──────────────────────┼───────────────────┼─────────────────────┼─────────────────────┼────────────────────┼────────────┼───────────────────────────────────────────────────────────────────┤";

PRIVATE VAR Rep = CMakeReport(tableHeader);

PRIVATE MACRO SayCarryError( error )
   if( isOprMultiExec() == false )
      msgbox( error );
   else
      CarryError = error;
   end;
   return false;
END;

PRIVATE MACRO GetClientCodeByID( ClientID:integer ):string
  var Sql = " SELECT c.t_Code " +
            "   FROM dpartcode_dbt c " +
            "  WHERE c.t_PartyID = ? " +
            "    AND c.t_CodeKind = 1 ";

  var Query = DL_RSDCommand(Sql);
  Query.addParam(ClientID);

  var rs = Query.Execute();

  if( rs.MoveNext() )
     return rs.Code;
  end;

  return "";  
END;

PRIVATE MACRO GetContractByAcc(Account, Currency)
  var query, cmd, DataSet;
  var Contract = 0;

  query =   " select t_ClientContrID "
          + "   from dmcaccdoc_dbt "
          + "  where t_Account = ? "
          + "    and t_Currency = ? "
          + "    and t_Chapter = 1 "
          + "    and t_IsCommon = 'X' ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Account);
  cmd.AddParam(Currency);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    Contract = DataSet.ClientContrID;
  end;

  return Contract;
END;

PRIVATE MACRO MakeTrnLinkId(ID_Operation, ID_Step, TrnId, InnAccId)
  var query = "";
  var cmd;

  if (TrnId > 0) 
      query =
          " INSERT INTO doprdocs_dbt (T_DOCKIND, "
         +"                           T_DOCUMENTID, "
         +"                           T_ID_OPERATION, "
         +"                           T_ID_STEP, "
         +"                           T_PART, "
         +"                           T_STATUS, "
         +"                           T_ORIGIN, "
         +"                           T_SERVDOCKIND, "
         +"                           T_SERVDOCID, "
         +"                           T_ACCTRNID) "
         +"      VALUES (1, "
         +"              CHR (1), "
         +"              ?, "
         +"              ?, "
         +"              1, "
         +"              0, "
         +"              0, "
         +"              0, "
         +"              0, "
         +"              ?); ";
     cmd = RSDCommand(query);
     cmd.addParam("", RSDBP_IN, ID_Operation);
     cmd.addParam("", RSDBP_IN, ID_Step);
     cmd.addParam("", RSDBP_IN, TrnId);
     cmd.execute();
  else
      query =
          " INSERT INTO doprdocs_dbt (T_DOCKIND, "
         +"                           T_DOCUMENTID, "
         +"                           T_ID_OPERATION, "
         +"                           T_ID_STEP, "
         +"                           T_PART, "
         +"                           T_STATUS, "
         +"                           T_ORIGIN, "
         +"                           T_SERVDOCKIND, "
         +"                           T_SERVDOCID, "
         +"                           T_ACCTRNID) "
         +"      VALUES (4601, "
         +"              LPAD(?, 10, '0'), "
         +"              ?, "
         +"              ?, "
         +"              1, "
         +"              0, "
         +"              0, "
         +"              0, "
         +"              0, "
         +"              null); ";
     cmd = RSDCommand(query);
     cmd.addParam("", RSDBP_IN, InnAccId);
     cmd.addParam("", RSDBP_IN, ID_Operation);
     cmd.addParam("", RSDBP_IN, ID_Step);
     cmd.execute();
  end;
END;

PRIVATE MACRO РасчетнаяОперацияВУ( accTrn :variant /*: RsbAccTransaction*/, FD, ВидРО ) : bool
  var InnAcc = TRecHandler("dlinacc.dbt");
  var DealSum = $0, spground, dealcode = ""; 

  InnAcc.rec.ID         = 0;  
  InnAcc.rec.Boffice    = OBJTYPE_BACKOFFICE_DV;
  InnAcc.rec.OperType   = ВидРО;
  InnAcc.rec.DocumentID = FD.ID;
  
  InnAcc.rec.AccTrnID   = accTrn.AccTrnID;
  InnAcc.rec.Date       = accTrn.Date_Carry; 
  InnAcc.rec.Chapter    = accTrn.Chapter;
  InnAcc.rec.DebAcc     = accTrn.AccountPayer;
  InnAcc.rec.KredAcc    = accTrn.AccountReceiver;
  
  if (accTrn.Chapter == 22)
   InnAcc.rec.FIKind    = FIKIND_DERIVATIVE;
  else
   InnAcc.rec.FIKind    = FIKIND_CURRENCY;
  end;
   
  InnAcc.rec.FIID       = accTrn.FIIDPayer;
  InnAcc.rec.Sum        = accTrn.SumPayer;
  InnAcc.rec.Department = accTrn.Department;
  InnAcc.rec.Oper       = accTrn.Oper;
  InnAcc.rec.DocumentKind = FD.Kind;

  /*за дату проводки InnAcc.Date ищем документ вида "Распорядительная записка"*/
  InnAcc.rec.GroundKind = 27;
  spground = TRecHandler("spground.dbt");

  InnAcc.rec.PartyID  = UnknownParty;
  InnAcc.rec.PlaceID  = UnknownParty;

  InnAcc.rec.DealKind = FD.Kind;
  dealcode            = FD.nptxop.rec.code;/*номер операции*/

  var dlinaccCache = RsbSQLInsert("dlinacc.dbt");
  var realIDs = TArray();
  dlinaccCache.AddRecord(InnAcc);
  dlinaccCache.AddReturning( "t_id", V_INTEGER );
  if(not dlinaccCache.insert())
     return false;
  else
     dlinaccCache.GetReturning( realIDs );
     var cmd = RsdCommand("UPDATE ddlinacc_dbt SET t_FMTBLOBDATA_XXXX = utl_raw.cast_to_raw(\'"+dealcode+"\') WHERE t_ID = ?");
     cmd.addParam("", RSDBP_IN, realIDs[0]);
     cmd.Execute();
     MakeTrnLinkId(ID_Operation, 4, 0, realIDs[0]);
  end;
  return true;
END;

PRIVATE MACRO ЮзерПроводкаПоКатегориямУчета(
  FD:VARIANT, 
  СчетДебет:VARIANT, СчетКредит:VARIANT, 
  ДатаВалютирования:DATE,
  Chapter:INTEGER, 
  FIID:INTEGER,
  СуммаОперации:MONEY,
  Numb_Document:STRING,
  Ground:STRING,
  FIRolePayer:INTEGER, FIRoleReceiver:INTEGER,
  СчетДебетFIID:INTEGER, СчетКредитFIID:INTEGER,
  IsInnerCarry,
  ВидРО:INTEGER,
  FDDebetParm:VARIANT, FDCreditParm:VARIANT
)
  var CуммаДебет = $0, СуммаКредит  = $0, tr = NULL, PaymentObj = NULL, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";

  if( СуммаОперации == null )  
     return SayCarryError( "В проводке не задана ни одна из сумм." );
  end;

  /*оставить только копейки для первой суммы, если она задана*/
  if( СуммаОперации != null )
     if( (Chapter == 5) or (Chapter == 22) )
        /*количество ц/б может быть дробным*/
     else
        СуммаОперации = money( round( СуммаОперации, 2 ) );    
     end;
     if( СуммаОперации < $0 )  
        return SayCarryError( "Сумма проводки < 0." );
     end;
  else  
    СуммаОперации = $0;
  end;

  if( СуммаОперации == $0 )  
     return true;
  end;

  if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
     If(СчетДебет == "ДС Общий")
          FIRolePayer = FIROLE_SETTL_AGENT;
     end;
     If(СчетДебет == "ДС Клиринговый, ВУ")
          FIRolePayer = FIROLE_SETTL_AGENT;
     end;

     if( (StrUpr( GenClassName(FD) ) == StrUpr("TS_CALCORCB_FD")) OR 
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_ReqAssetFD")) OR
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_OrderFD")) OR
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_OrderFD_DV"))
       )
        if( not FD.OpenAccount( FDDebetParm, СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID) )          
            return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
        end;
     else
        if( not FD.OpenAccount( СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID) )          
            return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
        end;
     end;
  else
     copy( AccountPayer, СчетДебет );
  end;

  if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
  
      If(СчетКредит == "ДС Общий")
         FIRoleReceiver = FIROLE_SETTL_AGENT;
     end;

     If(СчетДебет == "ДС Клиринговый, ВУ")
        FIRolePayer = FIROLE_SETTL_AGENT;
     end;

     if( (StrUpr( GenClassName(FD) ) == StrUpr("TS_CALCORCB_FD")) OR
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_ReqAssetFD")) OR
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_OrderFD")) OR
         (StrUpr( GenClassName(FD) ) == StrUpr("TS_OrderFD_DV"))
       )
        if( not FD.OpenAccount( FDCreditParm, СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID) )
           return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
        end;
     else
        if( not FD.OpenAccount( СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID) )
           return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
        end;
     end;
  else
     copy( AccountReceiver, СчетКредит );
  end;

  if( FIID == null )
     return SayCarryError("Не задана ни одна из валют проводки.");
  end;

  if( (FIID != AccountPayer.Code_Currency) AND
      (FIID != AccountReceiver.Code_Currency) )
     return SayCarryError( "В проводке не совпадают валюты счетов: " 
                            + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                           "| и валюта документа: " + string(FIID) );
  end;

  if( СуммаОперации != $0 )
     CуммаДебет = СуммаКредит = СуммаОперации;
  end;
  tr = RsbAccTransaction();

/*DAN.Ролевая модель Определяем конечного операциониста для проводки.  */
      var retvalGetMO = GetMainOperInGroup(FD.kind);
      if (retvalGetMO > 0)
	 Tr.Oper = retvalGetMO;
      else
         Tr.Oper = {Oper};
      end;
/*DAN*/

  if( tr == NULL )
     return SayCarryError("Ошибка при формировании документа проводки.");
  end;
  tr.Chapter         = Chapter;
  tr.Date_Carry      = ДатаВалютирования;
  tr.Numb_Document   = Numb_Document;
  tr.ResultCarry     = INPCARRY;
  tr.Kind_Oper       = "";
  tr.Ground          = Ground;
  tr.Department      = {OperDprt};
  tr.FIIDPayer       = AccountPayer.Code_Currency;
  tr.FIIDReceiver    = AccountReceiver.Code_Currency;
  tr.SumPayer        = CуммаДебет;
  tr.SumReceiver     = СуммаКредит;
  tr.AccountPayer    = AccountPayer.Account;
  tr.AccountReceiver = AccountReceiver.Account;
  if( PaymentObj != null )
     tr.Number_Pack  = PaymentObj.NumberPack;
  end;

  if( not tr.Carry() )
      return SayCarryError("Ошибка при выполнении проводки");
  else
     if(tr.AccTrnID > 0)
        MakeTrnLinkId(ID_Operation, 4, tr.AccTrnID, 0);
     end; 
  end;
  
  if( IsInnerCarry )
     if( not РасчетнаяОперацияВУ( tr, FD, ВидРО ) )
        return SayCarryError("Ошибка при вставке расчетной операции внутреннего учета");
     end;
  end;

  return true;
END;

PRIVATE MACRO PrintLine (nptxop, DebAccBY, KredAccBY, Sum, operdate, GroundBY)
   Rep.AddPrintCell(nptxop.code, 0, 0, "l");
   Rep.AddPrintCell(GetClientCodeByID(nptxop.Client), 0, 0, "l");
   Rep.AddPrintCell(DebAccBY, 0, 0, "l"); 
   Rep.AddPrintCell(KredAccBY, 0, 0, "l"); 
   Rep.AddPrintCell(Sum, 0, 2, "r:a");
   Rep.AddPrintCell(operdate, 0, 2, "r:a");
   Rep.AddPrintCell(GroundBY, 0, 0, "l");
   Rep.AddStr();
END; 

PRIVATE MACRO ПроводкаПоКатегориямВнутреннегоУчета( ВидРО, FD, Doc, CarryDate, FIID, CarrySumm, FDDebetParm:VARIANT, FDCreditParm:VARIANT, DealCode, GroundStr, AccDeb:VARIANT, AccKred:VARIANT, _FIRolePayer:integer, _FIRoleReceiver:integer, trnId )
  var ClassName:string = "";
  var Ground:string = "";
  var AccountDeb, AccountKred;
  var FIRolePayer:integer = -1, FIRoleReceiver:integer = -1;

  if( not ВестиВнутреннийУчет() )
     return 1;
  end;
 
  if(ValType(GroundStr) == V_STRING)
     Ground = GroundStr;
  else
     Ground = FD.ВУ_ОснованиеПроводки( ВидРО );
  end;

  if( (StrUpr(GenClassName(FD)) == StrUpr("SPFirstDocDLCOMM")) AND ((FD.Kind == DL_ISSUE_UNION) OR (FD.Kind == DL_CHGAVRNOM) OR ((FD.Kind == DL_SETTLEMENT) AND (AccDeb != NULL) AND (AccKred != NULL))) )
     AccountDeb  = AccDeb;
     AccountKred = AccKred;
  elif((StrUpr(GenClassName(FD)) == StrUpr("DLFirstDocNPTXOP")) AND (FD.Kind == DL_HOLDNDFL) AND (AccDeb != NULL) AND (AccKred != NULL))
     AccountDeb  = AccDeb;
     AccountKred = AccKred;
  else
     AccountDeb  = FD.ВУ_ПолучитьКатегориюДебет( ВидРО, null, CarryDate, FIID );
     AccountKred = FD.ВУ_ПолучитьКатегориюКредит( ВидРО, null, CarryDate, FIID );
  end;

  if( _FIRolePayer != NULL )
     FIRolePayer = _FIRolePayer;
  else
     FIRolePayer = FD.ВУ_ПолучитьРольФИ(ВидРО, true);
  end;

  if( _FIRoleReceiver != NULL )
     FIRoleReceiver = _FIRoleReceiver;
  else
     FIRoleReceiver = FD.ВУ_ПолучитьРольФИ(ВидРО, false);
  end;

  return ЮзерПроводкаПоКатегориямУчета( FD,
                                    AccountDeb,
                                    AccountKred,
                                    CarryDate,
                                    FD.ВУ_ОпределениеГлавы(FIID),
                                    FIID,
                                    CarrySumm,
                                    "1 ",
                                    Ground,
                                    FIRolePayer,
                                    FIRoleReceiver,
                                    FIID, FIID,
                                    true,
                                    FD.ВУ_ПолучитьРО(ВидРО),
                                    FDDebetParm, FDCreditParm
                                  );

END;

PRIVATE MACRO ВыполнитьКорректировкуОстатков()
  var FD, FDsub;
  var Select, DataSet, Query, Progress;
  var S = 0, S1 = 0, S2 = 0, stat = 0, NRec = 0, i = 0;
  var DtAccCat;
  var CtAccCat;
  var DtAccount = TrecHandler( "account.dbt");
  var CtAccount = TrecHandler( "account.dbt");

  Query     = " select * from (select " +
              " (select t_name from doprkoper_dbt ko where ko.t_kind_operation = nptxop.t_kind_operation) as t_opr, " +
              " nptxop.t_code,  " +
              " nptxop.t_taxsum,  " +
              " nptxop.t_tax, " +
              " nptxop.t_status, " +
              " nptxop.t_client, " +
              " nptxop.t_id, " +
              " oproper.t_id_operation, " +
              " (select t_code from dobjcode_dbt where t_objecttype = 3 and t_codekind = 1 and t_state = 0 and t_objectid = nptxop.t_client) as cft_code, " +
              " nptxop.t_operdate, " +
              " (select count(1) from doprdocs_dbt oprdocs, dacctrn_dbt acc where " +
              " oprdocs.t_id_operation = oproper.t_id_operation and oprdocs.t_dockind = 1 " +
              " and acc.t_acctrnid = oprdocs.t_acctrnid and acc.t_chapter = 1) as acc_1, " +
              " (select count(1) from doprdocs_dbt oprdocs, dacctrn_dbt acc where " +
              " oprdocs.t_id_operation = oproper.t_id_operation and oprdocs.t_dockind = 1 " +
              " and acc.t_acctrnid = oprdocs.t_acctrnid and acc.t_chapter = 21) as acc_21, " +
              " NVL ( (SELECT acc.T_SUM_PAYER " +
              "        FROM doprdocs_dbt oprdocs, dacctrn_dbt acc " + 
              "         WHERE     oprdocs.t_id_operation = oproper.t_id_operation " + 
              "           AND oprdocs.t_dockind = 1 " +
              "           AND acc.t_acctrnid = oprdocs.t_acctrnid " + 
              "           AND acc.t_chapter = 1 " + 
              "           AND acc.T_ACCOUNT_RECEIVER LIKE '60301%69'), " + 
              "        0)    nalog_15,  " +
              "  NVL ( (SELECT acc.T_SUM_PAYER  " +
              "     FROM doprdocs_dbt oprdocs, dacctrn_dbt acc  " +
              "      WHERE     oprdocs.t_id_operation = oproper.t_id_operation " +
              "       AND oprdocs.t_dockind = 1 " +
              "       AND acc.t_acctrnid = oprdocs.t_acctrnid " +
              "       AND acc.t_chapter = 1 " +  
              "       AND acc.T_ACCOUNT_RECEIVER LIKE '60301%40'), " +
              "        0)    nalog_13" +
              "  from dnptxop_dbt nptxop, doproper_dbt oproper " +
              " where 1 = 1 " +
              " and nptxop.t_operdate between to_date('01012022','ddmmyyyy') and to_date('01012023','ddmmyyyy') " +
              " and nptxop.t_dockind = 4608 " +
              " and nptxop.t_kind_operation = 2038 " +
              " and nptxop.t_subkind_operation = 10 " +
              " and oproper.t_kind_operation = nptxop.t_kind_operation " +
              " and lpad(nptxop.t_id,34,0) = oproper.t_documentid " +
              " and nptxop.t_tax > 0 " +
              " and nptxop.t_status = 2 ) ds " +
              " where ds.ACC_1 > 0 and ds.ACC_21 <=0 " ;
    
  Select = DL_RSDCommand(Query);
  NRec = Select.GetCount(Query);

  Progress = TProgressBar;
  Progress.Init(NRec, "Обработка операций", "Обработка операций удержания НДФЛ");

  DataSet = Select.Execute();
  while(DataSet.moveNext())
        stat = 0;        
        FD = DLFirstDocNPTXOP(DL_HOLDNDFL, DataSet.ID ); 
        S1 = DataSet.nalog_13;
        S2 = DataSet.nalog_15;
        S = S1 + S2;/*сумма проводки ВУ*/

        DtAccCat = "ДС, Расч. с клиентом, ВУ";
        CtAccCat = "ДС Клиента, ВУ";
        FD.nptxop.rec.Contract = GetContractByAcc(FD.nptxop.rec.Account, FD.nptxop.rec.Currency);
         
        if(FD.nptxop.rec.Contract > 0)
           FDsub = DL_SubContrFD(FD.nptxop.rec.Contract);
        end;
        if(FDsub != NULL)
           if( not FDsub.RecreateAccount(DtAccCat, null, true, DtAccount, {Curdate}, NATCUR) )
               PrintLine(FD.nptxop.rec, DtAccount.rec.account, CtAccount.rec.account, S, date(DataSet.operdate), "Ошибка при актуализации счета " + DtAccCat + " по договору " + FDsub.GetSubContr().rec.number);     
               stat = 1;
           end;
           
           if( not FDsub.RecreateAccount(CtAccCat, null, true, CtAccount, {Curdate}, NATCUR) )
               PrintLine(FD.nptxop.rec, DtAccount.rec.account, CtAccount.rec.account, S, date(DataSet.operdate), "Ошибка при актуализации счета " + CtAccCat + " по договору " + FDsub.GetSubContr().rec.number);     
               stat = 1;
           end;
        else
           DtAccount = DtAccCat;
           CtAccount = CtAccCat;
        end;
        ID_Operation = DataSet.ID_Operation;

        if ((S > 0) and (stat == 0))
           if( not ПроводкаПоКатегориямВнутреннегоУчета(17, 
                                                FD, 
                                                "1", 
                                                date(DataSet.operdate), 
                                                NATCUR, 
                                                S,
                                                null,
                                                null,
                                                null,
                                                "Удержание налога по операции " + DataSet.Code,
                                                DtAccount,
                                                CtAccount
                                              ) )
              stat = 1; 
              return msgbox( "Ошибка при выполнении проводки ВУ" );                                                        
            else
              PrintLine(FD.nptxop.rec, DtAccount.rec.account, CtAccount.rec.account, S, date(DataSet.operdate), "Удержание налога по операции " + DataSet.Code);     
            end;         
         end;
        i = i + 1;
        Progress.Use();
  end;
  Progress.Remove;
END;

private macro УдалитьПроводкиВУ()
  var Select, DataSet, Query, Progress, cmd;
  var stat = 0, NRec = 0, i = 0;
  var accTrn : RsbAccTransaction;

  Query     = " select t_AccTrnID from DACCTRN_DBT where T_DATE_CARRY = to_date('28.01.2022','DD.MM.YYYY') and t_chapter = 21 and t_account_payer = '21810990000000000000'" ;
    
  Select = DL_RSDCommand(Query);
  NRec = Select.GetCount(Query);

  Progress = TProgressBar;
  Progress.Init(NRec, "Удаление проводок ВУ", "Удаление проводок ВУ");

  DataSet = Select.Execute();
  accTrn = RsbAccTransaction;

  while(DataSet.moveNext())
        stat = accTrn.Find(DataSet.AccTrnID);

        if(stat == 0)
           stat = accTrn.Delete();
        end;

        SQL_Execute(" DELETE FROM ddlinacc_dbt where t_AccTrnID =  " + DataSet.AccTrnID + " and T_DOCUMENTKIND = 4608 ", null, false );
        cmd = DL_RSDCommand();

        i = i + 1;
        Progress.Use();
  end;
  Progress.Remove;
end;


PRIVATE MACRO Запуск()
  var isNoData = false;

  УдалитьПроводкиВУ();
  ВыполнитьКорректировкуОстатков();
  Rep.PrintWinRep();
  Rep.ShowWinRep();

  return 0;
END;

Запуск();