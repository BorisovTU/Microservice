/*
$Name:          f501ArSettings.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501Ar. Параметры отчетной формы.
*/

private class TRcbSettingsReportData(_key, _value)
    var key = _key;
    var value = _value;
end;

class TRcbSettings()
    private var m_settingsContainer : Object;
    private var m_settingsReportData : RcbArray;

    private macro getValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)
        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (m_settingsContainer.getValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_settingsReportData.push_back(TRcbSettingsReportData(path, String(value) + note));
    end;

    // Чтение из реестра настройки, которая не сохраняется в переменной
    private macro _getRegistryValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)
        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (getRegistryValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_settingsReportData.push_back(TRcbSettingsReportData(path, String(value) + note));
    end;

    private macro getReportString(key, caption)
        m_settingsReportData.moveFirst();
        while (m_settingsReportData.moveNext())
            var reportData = m_settingsReportData.getCurrentItem();
            if (reportData.key == key)
                if (caption == null)
                    return reportData.key + " = " + reportData.value;
                else
                    return caption + " = " + reportData.value;
                end;
            end;
        end;
        return null;
    end;

    private macro printLine(protocol, value, caption)
        if (value != null)
            protocol.printServiceLine(ternary(caption == null, value, caption + " = " + value));
        end;
    end;

    private macro makeTabbedString(value, nTabs)
        nTabs = nvl(nTabs, 1);
        if (value != null)
            value = mkstr("\t", nTabs) + value;
        end;

        return value;
    end;

    private macro readImpl()
    end;

    macro read()
        m_settingsContainer.read();
        m_settingsReportData.clear();
        readImpl();
    end;

    private macro initialize()
        throw(TPureVirtualMethodCallException("TRcbSettings::initialize"));
    end;

    private macro constructorTRcbSettings()
        m_settingsReportData = RcbArray();

        initialize();
        read();
    end;

    constructorTRcbSettings();
end;

/**
 * форма 501Ar. Класс параметров отчетной формы.
 */
private var m_settings = null;
class (TRcbSettings) TF501ArSettingsImpl()
    private const REGISTRY_PATH = "REPTREG/REP_GROUPS/ФОРМА 501-УП";

    private var m_clientSortMethod : Integer;
    private var m_isSortByBalance : Bool;

    macro printCalculationReport(protocol)
        printLine(protocol, "Значения настроек, используемых при расчете:");
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/СОРТИРОВКА/ПО КЛИЕНТАМ")));
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/СОРТИРОВКА/ПО БАЛАНСОВЫМ")));
        printLine(protocol, "");
    end;

    macro printNormalizationReport(protocol)
    end;

    macro getThreshold(thresholdDate)
        var query = "SELECT thrsh.t_value"
           + "\n" + "  FROM drepthrshmm_dbt thrsh"
           + "\n" + " WHERE thrsh.t_valueDate = (SELECT MAX(maxDate.t_valueDate)"
           + "\n" + "                              FROM drepthrshmm_dbt maxDate"
           + "\n" + "                             WHERE maxDate.t_valueDate <= " + getSqlDate(thresholdDate)
           + "\n" + "                           )"
                  ;
        var ds = TRsbDataset(query);
        ds.setFieldType("t_value", V_MONEY);
        var threshold = $0;
        if (ds.next())
            threshold = ds.t_value;
        end;

        return threshold;
    end;

    private macro readImpl()
        getValue(REGISTRY_PATH + "/СОРТИРОВКА/ПО КЛИЕНТАМ", V_INTEGER, m_clientSortMethod, 0);
        getValue(REGISTRY_PATH + "/СОРТИРОВКА/ПО БАЛАНСОВЫМ", V_BOOL, m_isSortByBalance, false);
    end;

    macro isSortByBalance()
        return m_isSortByBalance;
    end;

    macro getClientSortMethod()
        return m_clientSortMethod;
    end;

    private macro initialize()
        m_settingsContainer = objectFactoryInstance.createSettingsContainer("НАСТРОЙКИ_ОТЧЕТНОЙ_ФОРМЫ");
        m_settingsContainer.addBranchData(arrCreate(REGISTRY_PATH, true));
    end;

    private macro constructorTF501ArSettingsImpl()
        initTRcbSettings();
    end;

    constructorTF501ArSettingsImpl();
end;

macro TF501ArSettings() : TRcbSettings
    if (m_settings == null)
        m_settings = TF501ArSettingsImpl();
    end;

    return m_settings;
end;
