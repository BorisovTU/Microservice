//Исправление начального дисконта на лотах по счетам
import sp_car, spserv, secinter, dlquery;

private const TestMode = 0; //режим исполнения. 0 - отражаем в БД, иначе только протокол
private const CalcDate = {curdate};

//Занести разницу на лот с максимальным количеством
private macro CorrectLots(FIID, Portfolio, Diff, IsBPP)
  var query;
  var State = 1;

  if(IsBPP == true)
    State = 3;
  end;

  query =   " UPDATE dpmwrtsum_dbt lot "
          + "    SET lot.t_BegDiscount = lot.t_BegDiscount + " + Diff
          + "  WHERE lot.t_SumID = (SELECT l.t_SumID "
          + "                         FROM dpmwrtsum_dbt l "
          + "                        WHERE l.t_Party = -1 "
          + "                          AND l.t_Portfolio = " + Portfolio
          + "                          AND l.t_State = " + State
          + "                          AND l.t_FIID = " + FIID
          + "                          AND l.t_Amount = (SELECT MAX(l1.t_Amount) "
          + "                                              FROM dpmwrtsum_dbt l1 "
          + "                                             WHERE l1.t_Party     = l.t_Party "
          + "                                               AND l1.t_Portfolio = l.t_Portfolio "
          + "                                               AND l1.t_State     = l.t_State "
          + "                                               AND l1.t_FIID      = l.t_FIID " 
          + "                                           ) "
          + "                          AND ROWNUM = 1"
          + "                      ) ";


  SQL_Execute(query, null, false );

  return 0;

OnError(er)
  msgbox("Ошибка при обновлении лота");
  return 1;
end;

private class TProtocolData(_FI_Code, _FaceValue, _Amount, _Account, _RestAc, _CalcBegDiscount, _LotsBegDiscount, _LotsDiff)
  var FI_Code         = _FI_Code;        
  var FaceValue       = _FaceValue;      
  var Amount          = _Amount;         
  var Account         = _Account;        
  var RestAc          = _RestAc;         
  var CalcBegDiscount = _CalcBegDiscount;
  var LotsBegDiscount = _LotsBegDiscount;
  var LotsDiff        = _LotsDiff;       
end;

private macro ExecCorrect()
  var query, cmd, DataSet;
  var err = 0;
  var protocolArr = TArray();

  /* Formatted on 27.01.2022 12:04:34 (QP5 v5.149.1003.31008) */
  query =   " WITH rec AS (SELECT q1.*, "
          + "                     NVL ( "
          + "                        (SELECT SUM (lot.t_Amount) "
          + "                           FROM DPMWRTSUM_DBT lot "
          + "                          WHERE     lot.t_FIID = q1.t_FIID "
          + "                                AND lot.t_Amount > 0 "
          + "                                AND lot.t_Party = -1 "
          + "                                AND lot.t_Portfolio = q1.t_Portfolio "
          + "                                AND lot.t_State = 1), "
          + "                        0) "
          + "                        AS SumAmount1, "
          + "                     NVL ( "
          + "                        (SELECT SUM (lot.t_BegDiscount) "
          + "                           FROM DPMWRTSUM_DBT lot "
          + "                          WHERE     lot.t_FIID = q1.t_FIID "
          + "                                AND lot.t_Amount > 0 "
          + "                                AND lot.t_Party = -1 "
          + "                                AND lot.t_Portfolio = q1.t_Portfolio "
          + "                                AND lot.t_State = 1), "
          + "                        0) "
          + "                        AS SumBegDiscount1, "
          + "                     NVL ( "
          + "                        (SELECT SUM (lot.t_Amount) "
          + "                           FROM DPMWRTSUM_DBT lot "
          + "                          WHERE     lot.t_FIID = q1.t_FIID "
          + "                                AND lot.t_Amount > 0 "
          + "                                AND lot.t_Party = -1 "
          + "                                AND lot.t_Portfolio = q1.t_Portfolio "
          + "                                AND lot.t_State = 3), "
          + "                        0) "
          + "                        AS SumAmount3, "
          + "                     NVL ( "
          + "                        (SELECT SUM (lot.t_BegDiscount) "
          + "                           FROM DPMWRTSUM_DBT lot "
          + "                          WHERE     lot.t_FIID = q1.t_FIID "
          + "                                AND lot.t_Amount > 0 "
          + "                                AND lot.t_Party = -1 "
          + "                                AND lot.t_Portfolio = q1.t_Portfolio "
          + "                                AND lot.t_State = 3), "
          + "                        0) "
          + "                        AS SumBegDiscount3, "
          + "                     fin.t_FaceValue,  "
          + "                     fin.t_FaceValueFI, fin.t_FI_Code "
          + "                FROM (  SELECT v4.t_Portfolio, "
          + "                               v4.t_FIID, "
          + "                               SUM (v4.t_BegDiscount - q.t_BegDiscountChange) "
          + "                          FROM (SELECT s.t_FIID, "
          + "                                       (SELECT COUNT (1) "
          + "                                          FROM dpmwrtsum_dbt s1 "
          + "                                         WHERE     s1.t_FIID = s.t_FIID "
          + "                                               AND s1.t_Party = -1 "
          + "                                               AND s1.t_Amount > 0 "
          + "                                               AND ROWNUM = 1) "
          + "                                          Cnt, "
          + "                                       lnk.* "
          + "                                  FROM dpmwrtlnk_dbt lnk, dpmwrtsum_dbt s "
          + "                                 WHERE lnk.t_CreateDate >= TO_DATE ('01.07.2020', 'DD.MM.YYYY') "
          + "                                   AND s.t_SumID = lnk.t_BuyID "
          + "                                   AND s.t_Party = -1 "
          + "                                   AND s.t_Amount = 0 "
          + "                                   AND EXISTS "
          + "                                          (SELECT 1 "
          + "                                             FROM v_scwrthistex v,"
          + "                                                  v_scwrthistex v2 "
          + "                                            WHERE v.t_SumID = lnk.t_BuyID "
          + "                                                  AND v.t_Instance = lnk.t_BuyInstance "
          + "                                                  AND v.t_Party = -1 "
          + "                                                  AND v.t_Amount = 0 "
          + "                                                  AND v.t_BegDiscount = 0 "
          + "                                                  AND v2.t_SumID = v.t_SumID "
          + "                                                  AND v2.t_Instance = v.t_Instance - 1 "
          + "                                                  AND v2.t_BegDiscount <> 0 "
          + "                                                  AND v2.t_BegDiscount <> lnk.t_BegDiscountChange)) q, "
          + "                               v_scwrthistex v4 "
          + "                         WHERE     q.Cnt > 0 "
          + "                               AND v4.t_SumID = q.t_BuyID "
          + "                               AND v4.t_Instance = q.t_BuyInstance - 1 "
          + "                      GROUP BY v4.t_Portfolio, v4.t_FIID) q1, "
          + "                     dfininstr_dbt fin "
          + "               WHERE fin.t_FIID = q1.t_FIID), "
          + "       mcacc AS (  SELECT m1.t_Portfolio, m1.t_FIID, m1.t_Currency, m1.t_Value4, m1.t_Account, "
          + "                          ABS (rsb_account.restac(m1.t_Account, m1.t_Currency, "+GetSQLDate(CalcDate)+", m1.t_Chapter, NULL)) AS SumAcc "
          + "                     FROM (SELECT DISTINCT mcacc.t_Account, "
          + "                                           mcacc.t_Currency,"
          + "                                           mcacc.t_Chapter, "
          + "                                           rec.t_Portfolio, "
          + "                                           mcacc.t_FIID, tpl.t_Value4 "
          + "                             FROM rec, dmcaccdoc_dbt mcacc, dmctempl_dbt tpl "
          + "                            WHERE mcacc.t_CatID = 31 " /*Наш портфель*/
          + "                                  AND mcacc.t_FIID = rec.t_FIID "
          + "                                  AND mcacc.t_DisablingDate = TO_DATE ('01.01.0001', 'DD.MM.YYYY') "
          + "                                  AND mcacc.t_Currency = rec.t_FaceValueFI "
          + "                                  AND tpl.t_CatID = mcacc.t_CatID "
          + "                                  AND tpl.t_Number = mcacc.t_TemplNum "
          + "                                  AND tpl.t_Value1 = rec.t_Portfolio) m1 "
          + "                 ) "
          + " SELECT rec.*, mcacc.t_Account, "
          + "        mcacc.SumAcc, "
          + "        (CASE WHEN mcacc.t_Value4 = 0 THEN rec.SumAmount1 * rec.t_FaceValue - mcacc.SumAcc ELSE 0 END) as CalcBegDiscount1, "
          + "        (CASE WHEN mcacc.t_Value4 = 1 THEN rec.SumAmount3 * rec.t_FaceValue - mcacc.SumAcc  ELSE 0 END) as CalcBegDiscount3, "
          + "        (CASE WHEN mcacc.t_Value4 = 0 THEN rec.SumAmount1 * rec.t_FaceValue - mcacc.SumAcc - rec.SumBegDiscount1 ELSE 0 END) as Diff1, "
          + "        (CASE WHEN mcacc.t_Value4 = 1 THEN rec.SumAmount3 * rec.t_FaceValue - mcacc.SumAcc - rec.SumBegDiscount3 ELSE 0 END) as Diff3 "
          + "   FROM rec, mcacc "
          + "  WHERE     mcacc.t_FIID = rec.t_FIID "
          + "        AND mcacc.t_Portfolio = rec.t_Portfolio "
          + "        AND mcacc.t_Currency = rec.t_FaceValueFI ";
  
  cmd = DL_RSDCommand(query);

  RslDefCon.BeginTrans();

  InitProgress(-1, "Подготовка данных", "Подготовка данных");
  DataSet = cmd.Execute();
  RemProgress();

  InitProgress(-1, "Корректировка лотов", "Корректировка лотов");
  while((DataSet.moveNext()) and (not err))

    if(DataSet.Diff1 > 0)
      protocolArr[protocolArr.size] = TProtocolData(DataSet.FI_Code, 
                                                    ЧислоCЗаданнойТочностью(DataSet.FaceValue,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumAmount1,0), 
                                                    DataSet.Account, 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumAcc,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.CalcBegDiscount1,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumBegDiscount1,2), 
                                                    "+"+ЧислоCЗаданнойТочностью(DataSet.Diff1,2));

      err = CorrectLots(DataSet.FIID, DataSet.Portfolio, DataSet.Diff1, false);
    end;

    if(DataSet.Diff3 > 0)
      protocolArr[protocolArr.size] = TProtocolData(DataSet.FI_Code, 
                                                    ЧислоCЗаданнойТочностью(DataSet.FaceValue,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumAmount3,0), 
                                                    DataSet.Account, 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumAcc,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.CalcBegDiscount3,2), 
                                                    ЧислоCЗаданнойТочностью(DataSet.SumBegDiscount3,2), 
                                                    "+"+ЧислоCЗаданнойТочностью(DataSet.Diff3,2));
      err = CorrectLots(DataSet.FIID, DataSet.Portfolio, DataSet.Diff3, false);
    end;
  end;
  RemProgress();
  
  
  if(err)
    println("При выполнении возникла ошибка. Корректировка начального дисконта не выполнена");
    RslDefCon.RollbackTrans();
  else
    if(TestMode)
      RslDefCon.RollbackTrans();

      println("ВНИМАНИЕ!!! Включен тестовый режим. Фиксация изменения в ДБ не выполнена!");
      println();

    else 
      RslDefCon.CommitTrans();
    end;

    if(protocolArr.size > 0)
      println("                                               Корректировка начального дисконта на лотах");
      println("");
      println("┌─────┬─────────────────┬────────────────┬────────────────┬──────────────────────┬────────────────┬─────────────┬─────────────┬─────────────────┐");
      println("│     │                 │                │   Кол-во ц/б   │                      │    Остаток на  │  Расчетный  │  Начальный  │     Сумма       │");
      println("│     │     Код ц/б     │     Номинал    │   в портфеле   │     Счет вложений    │      счете     │  начальный  │   дисконт   │  корректировки  │");
      println("│     │                 │                │                │                      │                │   дисконт   │   на лотах  │     лотов       │");
      println("│     │                 │                │                │                      │                │   (3*4-6)   │             │                 │");
      println("├─────┼─────────────────┼────────────────┼────────────────┼──────────────────────┼────────────────┼─────────────┼─────────────┼─────────────────┤");
      println("│  1  │        2        │        3       │       4        │           5          │        6       │      7      │       8     │        9        │");
      println("├─────┼─────────────────┼────────────────┼────────────────┼──────────────────────┼────────────────┼─────────────┼─────────────┼─────────────────┤");

      var i = 0;
      while(i < protocolArr.size)
                                                                                                                                
              [│#####│#################│################│################│######################│################│#############│#############│#################│]
              (string(i+1):c,
               string(protocolArr[i].FI_Code):c,                                                                                 
               string(protocolArr[i].FaceValue):c,      
               string(protocolArr[i].Amount):r,         
               string(protocolArr[i].Account):c,        
               string(protocolArr[i].RestAc):r,         
               string(protocolArr[i].CalcBegDiscount):r, 
               string(protocolArr[i].LotsBegDiscount):r,
               string(protocolArr[i].LotsDiff):r        
              );
      
        i = i + 1;
      end;
                                                                                                                                            
      println("└─────┴─────────────────┴────────────────┴────────────────┴──────────────────────┴────────────────┴─────────────┴─────────────┴─────────────────┘");

    else
      println("Расхождений не найдено");
    end;
  end;
  
 
OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RemProgress();
  msgbox("Ошибка при выполнении");
end;


ExecCorrect();

