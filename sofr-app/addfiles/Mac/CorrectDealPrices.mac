import secinter;

private const TestMode = false;              // для внесения правок в бд установить значение "false"
private const CorrectRelativePrice = true; // исправлять признак "Относительная цена"

private const Rep = CMakeReport();

private const HeadTable = "┌───────────┬────────────┬────────────────┬────────────────────┬──────────────┬──────────────┬───┬──────────┬──────┐\n"
                          "│ ID сделки │ Код сделки │   Кол-во ц/б   │     Стоимость      │ Старая цена  │  Новая цена  │ Т │ Относит. │ Изм. │";

private const RepWidth = index( HeadTable, "\n" );

private macro CorrectDealPrices
  var query, cmd, DataSet;
  var tick = TBFile( "dl_tick" );
  var leg  = TBFile( "dl_leg", IIF(TestMode, "R", "W"), 1 );
  var old_price, saveRelativePrice, max_diff = 0.0, sum_diff = 0.0, count_diff = 0;
  var is_first = false;

  Rep.AddPrintCell( IIF( TestMode, "!!! ТЕСТОВЫЙ РЕЖИМ !!!", "!!! РЕЖИМ ВНЕСЕНИЯ ПРАВОК В БД !!!" ), RepWidth, 0, "c:", REP_ELEM_STR );
  Rep.AddStr();

  Rep.AutoScan( "[\n" + HeadTable + "]" );

  query = " SELECT tick.t_DealID, leg.t_ID "
          "  FROM ddl_tick_dbt tick, ddl_leg_dbt leg "
          " WHERE leg.t_DealID = tick.t_DealID "
          "   AND leg.t_LegKind = 0 "
          "   AND leg.t_LegID = 0 "
          "   AND tick.t_BofficeKind = 101 "
          "   AND (EXISTS (select 1 "
          "                from dsptkchng_dbt chng "
          "               where chng.t_DealID = tick.t_DealID "
          "                 and chng.t_OldChangeKind = 4) " // было изменение по срокам
          "        OR tick.t_ChangeKind = 4) ";

  cmd = DL_RSDCommand(query);
  DataSet = cmd.Execute();

  while( DataSet.moveNext() )
    var is_changed = false;
    var incorrectRelativePrice = false;

    tick.Clear();
    leg.Clear();
    tick.rec.DealID = DataSet.DealID;
    leg.rec.ID = DataSet.ID;

    if( NOT( tick.getEQ() AND leg.getEQ() ) )
       Rep.AddPrintCell( "!!! ОШИБКА !!!", RepWidth, 0, "l:", REP_ELEM_STR );
       break;
    end;

    Rep.AddPrintCell( tick.rec.DealID, 11, 0, "r" );
    Rep.AddPrintCell( tick.rec.DealCode, 12, 0, "c" );
    Rep.AddPrintCell( leg.rec.Principal, 16, 0, "r" );
    Rep.AddPrintCell( string( leg.rec.Cost ), 20, 0, "r" );
    Rep.AddPrintCell( string( leg.rec.Price ), 14, 0, "r" );

    old_price = leg.rec.Price;
    saveRelativePrice = leg.rec.RelativePrice;
    SP_ReCalcDealPrice(tick, leg);

    if( Round( leg.rec.Price, leg.rec.Point ) != Round( old_price, leg.rec.Point ) )
      leg.rec.RelativePrice = IIF( leg.rec.RelativePrice == UNSET_CHAR, SET_CHAR, UNSET_CHAR );

      SP_ReCalcDealPrice(tick, leg);

      if( Round( leg.rec.Price, leg.rec.Point ) != Round( old_price, leg.rec.Point ) )
        leg.rec.RelativePrice = saveRelativePrice;
        SP_ReCalcDealPrice(tick,leg);

        var diff = Abs( Round( leg.rec.Price, leg.rec.Point ) - Round( old_price, leg.rec.Point ) );
        is_changed = true;
        count_diff = count_diff + 1;
        sum_diff = sum_diff + diff;

        if( max_diff < diff )
          max_diff = diff;
        end;
      else
        incorrectRelativePrice = true;

        if( correctRelativePrice )
          is_changed = true;
          count_diff = count_diff + 1;
        end;
      end;
    end;

    Rep.AddPrintCell( string( leg.rec.Price ) , 14, 0,  "r" );
    Rep.AddPrintCell( leg.rec.Point, 3, 0,  "c" );
    Rep.AddPrintCell( IIF( incorrectRelativePrice, "'"+saveRelativePrice+"'->'"+leg.rec.RelativePrice+"'", leg.rec.RelativePrice ) , 10, 0,  "c" );
    Rep.AddPrintCell( IIF( is_changed , "X", " " ), 6, 0, "c" );
    Rep.AddStr();

    if( (NOT TestMode) AND is_changed )
      if( incorrectRelativePrice AND (NOT correctRelativePrice) ) // некорректный признак "Относительная цена", но не задана корректировка
        continue;
      end;

      leg.update;
    end;
  end;

  Rep.AddPrintCell( "Кол-во изменений:", 30, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( string( count_diff ), 15, 0, "r:", REP_ELEM_STR ); 
  Rep.AddStr();
  Rep.AddPrintCell( "Макс. изменеие:", 30, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( string( max_diff ), 15, 0, "r:", REP_ELEM_STR ); 
  Rep.AddStr();
  Rep.AddPrintCell( "Всего изменений:", 30, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( string( sum_diff ), 15, 0, "r:", REP_ELEM_STR ); 
  Rep.PrintRep();
end;   

CorrectDealPrices;