import dbf2ora, RSD, cb_sql, BankInter, globals;

/*Признак импорта клиринговых валют*/
var ImportKLR = false;

const english_char = "AaBCcEeЁёHKkMmOoPpTuXxYybDdFfGghIiJjLlnQqRrSstUVvWwZz!@#$%^&*=+№;:?\"\\/|`~[]{}<>";
const russian_char = "ААВССЕЕЕЕНККМТООРРТИХХУУ                                                       ";

file currlist_prv("currlist.dbf") dbf;
file currlist_dbf("currlist.dbf") dbf;
file goslist_dbf("goslist.dbf") dbf;

file isocur(isocur) write key 2;
file country(country) write key 4;
file cur_cntr(cur_cntr) write key 0;

macro PrintHeader()
[Протокол импорта справочников валют и стран ISO];
[
Дата: ########## ########
Пользователь: ##### #
] (Date(), Time(), {oper}, {Name_Oper});
end;

macro PrintCurrlist()
[
Справочник валют:];
end;                                                                                              
                                                                                                  
macro PrintGoslist()
[
Справочник стран:];
end;                                                                                              
                                                                                                  
macro PrintTableTop()
[┌──────┬────┬────┬───────────────────────────────────────┬─────────────────────────────────────────────────────────────────────────────────┐];
[│Запись│Цифр│Букв│Название                               │Причина включения в протокол                  │];
[├──────┼────┼────┼───────────────────────────────────────┼─────────────────────────────────────────────────────────────────────────────────┤];
end;                                                                                              
                                                                                                  
macro PrintTableCenter(basefile, digit, lat3, name, reason)
[│######│####│####│#######################################│#################################################################################│]
(basefile, digit, lat3, name, reason);
end;                                                                                              
                                                                                                  
macro PrintTableBottom()                                                                       
[└──────┴────┴────┴───────────────────────────────────────┴─────────────────────────────────────────────────────────────────────────────────┘];
end;

macro _CopyRSetToFBuff( fbuff, rset )
  var idx   = 0,
      nflds = FldNumber( fbuff ),
      val,
      NullConv;

  ClearRecord(fbuff);

  if( IsEqClass( "RsdRecordset", rset ))
    NullConv = rset.Command.NullConversion;
    rset.Command.NullConversion = true;
  end;

  while( idx < nflds )
    val = rset.value( "t_" + FldName( fbuff, idx ) );
    if( ValType( fbuff( idx ) ) == V_STRING )
      fbuff( idx ) = SQL_ConvTypeStr( val );
    elif( val != NULL )
      fbuff( idx ) = rset.value( "t_" + FldName( fbuff, idx ), null, ValType( fbuff( idx ) ) );
    end;
    idx = idx + 1;
  end;

  if( IsEqClass( "RsdRecordset", rset ))
    rset.Command.NullConversion = NullConv;
  end;
end;

macro normalization( oldstr )
  var newstr, i, retstr; 
  newstr = oldstr;
  retstr = "";
  
  i = 0;
  while( i < StrLen(english_char))
    i = i + 1;
    newstr = StrSubst( newstr, SubStr(english_char,i,1), SubStr(russian_char,i,1));
  end;
  newstr = Trim( newstr );
  newstr = StrUpr( newstr );

  i = 0;
  while( i < StrLen(newstr))
    i = i + 1;
    if( ( CodeFor(SubStr(newstr,i,1)) != 32 ) or ( CodeFor(SubStr(newstr,i-1,1)) != 32))
      retstr = retstr + SubStr(newstr,i,1);
    end;
  end;
  return retstr;
end;

macro sql_normalization ( column_name )
  var retstr;

  retstr = "REPLACE(REPLACE(REPLACE(UPPER(TRIM(TRANSLATE(" + column_name + ",'" + english_char + "','" + russian_char + "'))),' ', ' '||CHR(2)),CHR(2)||' '),CHR(2))";

  return retstr;
end;

/* Проверка соответствия полей таблиц*/
macro CheckCurrlistFields(TxtPath) : bool
  var rs;
  rs = RsdRecordset("SELECT t_iso_dig, t_iso_lat3, t_name_rush, t_begin_date, t_end_date FROM currlist_dbf");
  rs.movenext;
  return true;
  OnError(err);
  println("Файл " + TxtPath + "currlist.dbf имеет неизвестную структуру, импорт не выполнен");
  return false;
end;

/* Проверка соответствия полей таблиц*/
macro CheckGoslistFields(TxtPath) : bool
  var rs;
  rs = RsdRecordset("SELECT t_iso_lat2, t_iso_lat3, t_iso_dig, t_iso_curr, t_name_rush, t_short_name, t_name_engl, t_begin_date, t_end_date FROM goslist_dbf");
  rs.movenext;
  return true;
  OnError(err);
  println("Файл " + TxtPath + "goslist.dbf имеет неизвестную структуру, импорт не выполнен");
  return false;
end;

macro ExecuteImportCurrlist()
  PrintCurrlist();  
  PrintTableTop();  
  var currlist_rs;
  var isocur_rs;
  currlist_rs = RsdRecordset("SELECT * FROM currlist_dbf ORDER BY t_iso_dig, t_begin_date ASC");
  ClearRecord(currlist_prv);
  while(currlist_rs.movenext)
    _CopyRSetToFBuff(currlist_dbf, currlist_rs);
    if(ImportKLR or (currlist_dbf.iso_lat3 != "KLR"))
      isocur_rs = RsdRecordset("SELECT t_num FROM disocur_dbt WHERE t_numbercode = '" + currlist_dbf.iso_dig + "' AND t_isrevoked = CHR(0)");
      if(isocur_rs.movenext)
        ClearRecord(isocur);
        isocur.Num = isocur_rs.value(0);
        if(GetEQ(isocur))
          if(string(currlist_dbf.end_date) != " 1.01.0001")
            if(isocur.isrevoked != "X")
              PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Аннулирована " + string(currlist_dbf.end_date));
              isocur.isrevoked = "X";
              Update(isocur);
            end;
            _CopyRSetToFBuff(currlist_prv, currlist_rs);
          else
            if(isocur.isocode != currlist_dbf.iso_lat3)
              PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Изменен буквенный код с " + isocur.isocode + " на " + currlist_dbf.iso_lat3);
              isocur.isocode = currlist_dbf.iso_lat3;
              Update(isocur);
            end;
            if(trim(normalization(isocur.namecurr)) != trim(normalization(currlist_dbf.name_rush)))
              PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Изменено наименование с " + trim(isocur.namecurr) + " на " + trim(currlist_dbf.name_rush));
              isocur.namecurr = trim(currlist_dbf.name_rush);
              Update(isocur);
            end;
          end;
        end;
      else
        if(string(currlist_dbf.end_date) != " 1.01.0001")
          _CopyRSetToFBuff(currlist_prv, currlist_rs);
        end;
        if((string(currlist_dbf.end_date) == " 1.01.0001"))
         //and
         //  (currlist_prv.iso_dig != currlist_dbf.iso_dig) and
         //  (currlist_prv.iso_lat3 != currlist_dbf.iso_lat3))
          isocur_rs = RsdRecordset("SELECT t_num FROM disocur_dbt WHERE t_numbercode = '" + currlist_dbf.iso_dig + "' " +
                                                                   "AND t_isocode = '" + currlist_dbf.iso_lat3 + "' " +
                                                                   "AND " + sql_normalization("t_namecurr") + " = " + 
                                                                            sql_normalization("'" + currlist_dbf.name_rush + "'") + " " +
                                                                   "AND t_isrevoked = CHR(88)");
          if(isocur_rs.movenext)
            ClearRecord(isocur);
            isocur.Num = isocur_rs.value(0);
            if(GetEQ(isocur))
              PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Аннулирование снято");
              isocur.isrevoked = "";
              Update(isocur);
            end;
          else
            isocur_rs = RsdRecordset("SELECT t_num FROM disocur_dbt WHERE t_numbercode = '" + currlist_dbf.iso_dig + "' " +
                                                                     "AND t_isocode = '" + currlist_dbf.iso_lat3 + "' " +
                                                                     "AND t_isrevoked = CHR(88)");
            if(isocur_rs.movenext)
              ClearRecord(isocur);
              isocur.Num = isocur_rs.value(0);
              if(GetEQ(isocur))
                isocur.isrevoked = "";
                PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Изменено наименование  с " + trim(isocur.namecurr) + " на " + trim(currlist_dbf.name_rush) + ". Аннулирование снято");
                isocur.namecurr = trim(currlist_dbf.name_rush);
                Update(isocur);
              end;
            else
              /*Ввод новой записи*/
              ClearRecord(isocur);
              isocur.numbercode = currlist_dbf.iso_dig;
              isocur.isocode = currlist_dbf.iso_lat3;
              isocur.namecurr = currlist_dbf.name_rush;
              PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Введена новая запись");
              Insert(isocur);
            end;
          end;
        end;
      end;
    end;
  end;
  isocur_rs = RsdRecordset("SELECT i.t_num FROM disocur_dbt i " +
                           " WHERE NOT EXISTS(SELECT 1 FROM currlist_dbf c " +
                                             " WHERE c.t_iso_dig = i.t_numbercode" + 
                                               " AND c.t_iso_lat3 = i.t_isocode)" + 
                             " AND i.t_isrevoked = CHR(0)");
  while(isocur_rs.movenext)
    ClearRecord(isocur);
    isocur.Num = isocur_rs.value(0);
    if(GetEQ(isocur))
      PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Отсутствует в файле, аннулирована");
      isocur.isrevoked = "X";
      Update(isocur);
    end;
  end;
  isocur_rs = RsdRecordset("SELECT i.t_num FROM disocur_dbt i " +
                           " WHERE i.t_isocode IN (SELECT ii.t_isocode FROM disocur_dbt ii " +
                                                  " WHERE ii.t_isrevoked = CHR(0) " + 
                                                  " GROUP BY ii.t_isocode " + 
                                                 " HAVING COUNT(*) > 1)" + 
                             " AND i.t_isrevoked = CHR(0)" +
                            " ORDER BY i.t_isocode");
  while(isocur_rs.movenext)
    ClearRecord(isocur);
    isocur.Num = isocur_rs.value(0);
    if(GetEQ(isocur))
      PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Совпадают буквенные коды");
    end;
  end;
  isocur_rs = RsdRecordset("SELECT i.t_num FROM disocur_dbt i " +
                           " WHERE UPPER(i.t_namecurr) = i.t_namecurr");
  while(isocur_rs.movenext)
    ClearRecord(isocur);
    isocur.Num = isocur_rs.value(0);
    if(GetEQ(isocur))
      PrintTableCenter("база", isocur.numbercode, isocur.isocode, isocur.namecurr, "Название не содержит строчных букв");
    end;
  end;
  PrintTableBottom();  
end;

macro ExecuteImportGoslist()
  var goslist_rs;
  var country_rs;
  var isocur_rs;
  var country_count;
  PrintGoslist();  
  PrintTableTop();  
  goslist_rs = RsdRecordset("SELECT * FROM goslist_dbf ORDER BY t_iso_lat3, t_iso_dig");
  while(goslist_rs.movenext)
    _CopyRSetToFBuff(goslist_dbf, goslist_rs);
    country_rs = RsdRecordset("SELECT COUNT(*) as t_count FROM dcountry_dbt " + 
                              " WHERE t_codelat2 = '" + goslist_dbf.iso_lat2 + "' " +
                                 " OR t_codelat3 = '" + goslist_dbf.iso_lat3 + "' " +
                                 " OR t_codenum3 = '" + goslist_dbf.iso_dig  + "' ");
    country_rs.movenext;
    country_count = country_rs.value(0);
    country_rs = RsdRecordset("SELECT t_countryid FROM dcountry_dbt " + 
                              " WHERE t_codelat2 = '" + goslist_dbf.iso_lat2 + "' " +
                                 " OR t_codelat3 = '" + goslist_dbf.iso_lat3 + "' " +
                                 " OR t_codenum3 = '" + goslist_dbf.iso_dig  + "' ");
    while(country_rs.movenext)
      ClearRecord(country);
      country.countryid = country_rs.value(0);
      if(GetEQ(country))
        isocur_rs = RsdRecordset("SELECT t_num FROM disocur_dbt WHERE t_numbercode = '" + goslist_dbf.iso_curr + "'");
        if(isocur_rs.movenext)
          ClearRecord(cur_cntr);
          cur_cntr.numbercode = goslist_dbf.iso_curr;
          cur_cntr.codenum = country.codenum3;
          if(not GetEQ(cur_cntr))
            ClearRecord(cur_cntr);
            cur_cntr.numbercode = goslist_dbf.iso_curr;
            cur_cntr.codenum = country.codenum3;
            PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Добавлена национальная валюта " + goslist_dbf.iso_curr);
            Insert(cur_cntr);
          end;
        else
          PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Неизвестная национальная валюта " + goslist_dbf.iso_curr);
        end;
      end;
      if(string(goslist_dbf.end_date) == " 1.01.0001")
        if((country_count = 1) or
           ((country.codelat2 == goslist_dbf.iso_lat2) and
            (country.codelat3 == goslist_dbf.iso_lat3) and
            (country.codenum3 == goslist_dbf.iso_dig )))
          if(country.codelat2 != goslist_dbf.iso_lat2)
            PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Изменен двухбуквенный код с " + country.codelat2 + " на " + goslist_dbf.iso_lat2);
            country.codelat2 = goslist_dbf.iso_lat2;
          end;
          if(country.codelat3 != goslist_dbf.iso_lat3)
            PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Изменен трехбуквенный код с " + country.codelat3 + " на " + goslist_dbf.iso_lat3);
            country.codelat3 = goslist_dbf.iso_lat3;
          end;
          if(country.codenum3 != goslist_dbf.iso_dig)
            PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Изменен цифровой код с " + country.codenum3 + " на " + goslist_dbf.iso_dig);
            country.codenum3 = goslist_dbf.iso_dig;
          end;
          if(trim(normalization(country.name)) != trim(normalization(goslist_dbf.short_name)))
            PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Изменено короткое наименование с " + trim(country.name) + " на " + trim(goslist_dbf.short_name));
            country.name = trim(goslist_dbf.short_name);
          end;
          country.fullname = trim(goslist_dbf.name_rush);
          Update(country);
        end;
      end;
    end;
    if(country_count > 1)
      PrintTableCenter("файл", goslist_dbf.iso_dig, goslist_dbf.iso_lat3, goslist_dbf.short_name, "Множественность записей в базе");
    end;
    if((country_count == 0) and (string(goslist_dbf.end_date) == " 1.01.0001"))
      /*ввод новой страны*/
      ClearRecord(country);
      country.codelat2 = goslist_dbf.iso_lat2;
      country.codelat3 = goslist_dbf.iso_lat3;
      country.codenum3 = goslist_dbf.iso_dig;
      country.name = trim(goslist_dbf.short_name);
      country.fullname = trim(goslist_dbf.name_rush);
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Введена новая запись");
      Insert(country);
      isocur_rs = RsdRecordset("SELECT t_num FROM disocur_dbt WHERE t_numbercode = '" + goslist_dbf.iso_curr + "'");
      if(isocur_rs.movenext)
        ClearRecord(cur_cntr);
        cur_cntr.numbercode = goslist_dbf.iso_curr;
        cur_cntr.codenum = country.codenum3;
        if(not GetEQ(cur_cntr))
          ClearRecord(cur_cntr);
          cur_cntr.numbercode = goslist_dbf.iso_curr;
          cur_cntr.codenum = country.codenum3;
          PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Добавлена национальная валюта " + goslist_dbf.iso_curr);
          Insert(cur_cntr);
        end;
      else
        PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Неизвестная национальная валюта " + goslist_dbf.iso_curr);
      end;
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                            " WHERE NOT EXISTS(SELECT 1 FROM goslist_dbf c " +
                                             " WHERE c.t_iso_dig = i.t_codenum3" + 
                                               " AND c.t_iso_lat2 = i.t_codelat2" + 
                                               " AND c.t_iso_lat3 = i.t_codelat3)");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Отсутствует в файле, рекомендуется удалить");
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                           " WHERE i.t_codenum3 IN (SELECT ii.t_codenum3 FROM dcountry_dbt ii " +
                                                  " GROUP BY ii.t_codenum3 " + 
                                                 " HAVING COUNT(*) > 1)" + 
                            " ORDER BY i.t_codenum3");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Цифровой код " + country.codenum3 + " не уникален");
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                           " WHERE i.t_codelat3 IN (SELECT ii.t_codelat3 FROM dcountry_dbt ii " +
                                                  " GROUP BY ii.t_codelat3 " + 
                                                 " HAVING COUNT(*) > 1)" + 
                            " ORDER BY i.t_codenum3");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Трехбуквенный код " + country.codelat3 + " не уникален");
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                           " WHERE i.t_codelat2 IN (SELECT ii.t_codelat2 FROM dcountry_dbt ii " +
                                                  " GROUP BY ii.t_codelat2 " + 
                                                 " HAVING COUNT(*) > 1)" + 
                            " ORDER BY i.t_codenum3");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Двухбуквенный код " + country.codelat2 + " не уникален");
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                           " WHERE " + sql_normalization("i.t_name") + " IN (SELECT " + sql_normalization("ii.t_name") + " FROM dcountry_dbt ii " +
                                                  " GROUP BY " + sql_normalization("ii.t_name") + " " + 
                                                 " HAVING COUNT(*) > 1)" + 
                            " ORDER BY i.t_codenum3");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Короткое наименование не уникально");
    end;
  end;
  country_rs = RsdRecordset("SELECT i.t_countryid FROM dcountry_dbt i " +
                           " WHERE UPPER(i.t_name) = i.t_name");
  while(country_rs.movenext)
    ClearRecord(country);
    country.countryid = country_rs.value(0);
    if(GetEQ(country))
      PrintTableCenter("база", country.codenum3, country.codelat3, country.name, "Название не содержит строчных букв");
    end;
  end;
  PrintTableBottom();  
end;

macro RunImportISO()
  var stat, cmd;
  var TxtPath;
  var ErrText;

  PrintHeader();
    
  GetRegistryValue("CB\\IMPORT\\ISOIMPORTFILEDIR", V_STRING, TxtPath, stat);

  if(stat != 0) 
    println("Ошибка чтения настройки CB\\IMPORT\\ISOIMPORTFILEDIR"); 
  else
    if(SubStr(TxtPath, strlen(TxtPath)) != "\\")
      TxtPath = TxtPath + "\\";
    end;
    
    if((not open(currlist_dbf, TxtPath + "currlist.dbf")) or 
       (not open(currlist_prv, TxtPath + "currlist.dbf")))
      println("Настройка реестра CB\\IMPORT\\ISOIMPORTFILEDIR задана неверно, или файл currlist.dbf не может быть открыт!"); 
    else
      Message("Выполняется импорт справочника валют. Ждите...");
      if(not dbf2ora(TxtPath + "currlist.dbf", "currlist_dbf", "Загрузка файла currlist.dbf в БД", ErrText))
        println("Файл " + TxtPath + "currlist.dbf не загружен, импорт не выполнен. Ошибка: " + ErrText);
      else
        if(CheckCurrlistFields(TxtPath))
          ExecuteImportCurrlist();
        end;
      end;
    end;
    
    if( not open(goslist_dbf, TxtPath + "goslist.dbf"))
      println("Настройка реестра CB\\IMPORT\\ISOIMPORTFILEDIR задана неверно, или файл goslist.dbf не может быть открыт!"); 
    else
      Message("Выполняется импорт справочника стран. Ждите...");
      if(not dbf2ora(TxtPath + "goslist.dbf", "goslist_dbf", "Загрузка файла goslist.dbf в БД", ErrText))
        println("Файл " + TxtPath + "goslist.dbf не загружен, импорт не выполнен. Ошибка: " + ErrText);
      else
        if(CheckGoslistFields(TxtPath))
          ExecuteImportGoslist();
        end;
      end;
    end;
  end;
  Message("");
  return "success";
end;

