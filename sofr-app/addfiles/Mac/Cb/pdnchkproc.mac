/*
 $Name: pdnchkproc.mac
 $Module: Ядро ГКБО 
 $Description: Сервисные функции видов ПДн проекта RS-Core
 */
 /*
	Сервисные функции видов ПДн проекта RS-Core
 */
import "global.mac", ptinter, oralib, RsbDataSet, cb_sql, rsd;
import pdnchklog_lib;
import pdnchklog_xls;

const RESULT_TRUE  = 0;
const RESULT_FALSE = -1;
const RESULT_EXEC_ERR = -2;

 private macro PDN_ExecuteCheckProcessFunc(Type, MacroFile, Func, FuncPrm, PdnTypePartyID, pOutEndDate, pOutOper, pOutComment)
 //begin
    var result = RESULT_TRUE;
    if (Type == PDNKIND_FN_MACRO)
        var outEndDate, outOper, outComment;
        
        if ((MacroFile != "") and (Func != ""))
            result = ExecMacroFile (MacroFile, Func, PdnTypePartyID, FuncPrm, outEndDate, outOper, outComment);
            
            if (ValType(result) == V_UNDEF)
                result = RESULT_FALSE;
            else
                SetParm (5, outEndDate);
                SetParm (6, outOper);
                SetParm (7, outComment);
            end;
        end;
    else
        var sql = "begin ? := " + Func + "(?, ?, ?, ?, ?); end;";
        var cmd = RsdCommand(sql);
        cmd.addParam("result", RSDBP_OUT ,V_INTEGER);
        cmd.addParam("pPdnTypePartyID", RSDBP_IN, PdnTypePartyID);  
        cmd.addParam("FuncPrm", RSDBP_IN, FuncPrm);
        cmd.addParam("outEndDate",  RSDBP_OUT, V_DATE);
        cmd.addParam("outOper", RSDBP_OUT, V_INTEGER );
        cmd.addParam("outComment", RSDBP_OUT, V_STRING );
        SQL_Execute(cmd);
        
        result = cmd.value("result");
        if (result == RESULT_TRUE)
            SetParm (5, cmd.value("outEndDate"));
            SetParm (6, cmd.value("outOper"));
            SetParm (7, cmd.value("outComment"));
        end;
    end;
    return result;
 OnError(err)
    RunError("", "Не удалось выполнить функцию проверки завершения обработки ПДн: " + Func);
    return RESULT_EXEC_ERR;
 end;
 
 private macro Pdn_CreateFinProcParty(PdnTypePartyID, PersonID, CmpltPrcssID, EndDate, PlanDate, Oper, Comment, NeedStopFlag)
 //begin
    record FinProcParty_old("pdnfinprocparty.dbt");
    record FinProcParty_new("pdnfinprocparty.dbt");
    
    var FinProcParty = TbFile("pdnfinprocparty.dbt", "W");
    FinProcParty.KeyNum = 2;
    FinProcParty.rec.PdnTypeParty = PdnTypePartyID;
    FinProcParty.rec.PersonID = PersonID;
    FinProcParty.rec.CmpltPrcssID = CmpltPrcssID;

    if (FinProcParty.GetEq())
        Copy(FinProcParty_old, FinProcParty);
        
        var NeedUpdate = false;
        if (NeedStopFlag == false)
            if ((ZeroValue(V_DATE) != EndDate) and (FinProcParty.rec.EndDate < EndDate))
                FinProcParty.rec.EndDate = EndDate;
                FinProcParty.rec.Oper = Oper;
                FinProcParty.rec.Comment = Comment;
                NeedUpdate = true;
            end;
        else
            if ((ZeroValue(V_DATE) != EndDate) and (FinProcParty.rec.EndDate > EndDate))
                FinProcParty.rec.EndDate = EndDate;
                FinProcParty.rec.Oper = Oper;
                FinProcParty.rec.Comment = Comment;
                NeedUpdate = true;
            end;
        end;
        
        if ((PlanDate != ZeroValue(V_DATE)) and (NeedUpdate))
            FinProcParty.rec.PlanDate = PlanDate;
            FinProcParty.rec.PlanOper = Oper;
            NeedUpdate = true;
        end;
        
        if (NeedUpdate)
            Copy(FinProcParty_new, FinProcParty);
            PDN_AddLogMessage(PersonID, PDNEVENTLOG_MANUAL, PDNEVENTLOG_PDN_FINALY, PDNEVENTLOG_UPDATE, FinProcParty_old, FinProcParty_new, false, true);
        end;
    else
        FinProcParty.rec.PdnTypeParty = PdnTypePartyID;
        FinProcParty.rec.PersonID = PersonID;
        FinProcParty.rec.CmpltPrcssID = CmpltPrcssID;
        
        FinProcParty.rec.EndDate = EndDate;
        FinProcParty.rec.Oper = Oper;
        FinProcParty.rec.Comment = Comment;
        
        if (PlanDate != ZeroValue(V_DATE))
            FinProcParty.rec.PlanDate = PlanDate;
            FinProcParty.rec.PlanOper = Oper;
        end;
        
        Copy(FinProcParty_new, FinProcParty);
        PDN_AddLogMessage(PersonID, PDNEVENTLOG_AUTO, PDNEVENTLOG_PDN_FINALY, PDNEVENTLOG_INSERT, FinProcParty_new, FinProcParty_old, false, true);
    end;
 end;
 
private macro PDN_CheckProcessBody(rs : @RsdRecordset, 
    OpenCheck, 
    CloseCheck,
    PdnPartyKind,
    PartyID,
    CheckFinProc,
    CheckNeedDelete,
    CheckDecor,
    Txt, 
    Xls, 
    Xml
)
//begin
    record FinProcParty_old("pdnfinprocparty.dbt");
    record FinProcParty_old2("pdnfinprocparty.dbt");
    const VALUE_TYPE         = 0; 
    const VALUE_MACROFILE    = 1;
    const VALUE_FUNC         = 2;
    const VALUE_FUNCPRM      = 3;
    const VALUE_FUNCID       = 4;
    
    const VALUE_CMPLTPRCSSID = 0;
    const VALUE_TERMLIMIT    = 1;
    const VALUE_NEEDSTOPFLAG = 2;
    
    while (rs.movenext())
        var params = TArray;
        var sql = "SELECT t1.T_CMPLTPRCSSID, t2.t_TermLimit, T2.T_NEEDSTOPFLAG FROM DPDNPTKINDCMPLT_DBT t1, dpdncmpltprcss_dbt t2 WHERE t1.T_PARTYKIND = ? AND t1.T_CMPLTPRCSSID = t2.t_ID";
        params[params.size] = SQLParam("", rs.value(2));
        var PdnTypePartyID = rs.value(1);
        var PersonID = rs.value(0);
        var AllComplete = true;

        var cmplt = execSQLSelect(sql, params, true);
        while(cmplt.movenext())
            var outEndDateArr = TArray;
            var outOperArr = TArray;
            var outCommentArr = TArray;
            
            var NeedStopFlag = cmplt.value(VALUE_NEEDSTOPFLAG); 
            var TermLimit = cmplt.value(VALUE_TERMLIMIT);
            var CmpltPrcssID = cmplt.value(VALUE_CMPLTPRCSSID);
            var funcsSql = "SELECT t_Type, t_MacroFile, t_Func, t_FuncPrm, t_ID FROM dPDNCMPLTPRCSSFN_dbt WHERE T_PDNCMPLTID = ? and T_NOTUSE <> CHR (88)";
            var funcsSqlParams = TArray; funcsSqlParams[funcsSqlParams.size] = SQLParam("", CmpltPrcssID);
            var funcs = execSQLSelect(funcsSql, funcsSqlParams, true);

            while (funcs.movenext())
                var outEndDate, outOper, outComment, PlanDate;
                var result = PDN_ExecuteCheckProcessFunc(
                    funcs.value(VALUE_TYPE),      // Type
                    funcs.value(VALUE_MACROFILE), // MacroFile
                    funcs.value(VALUE_FUNC),      // Func
                    funcs.value(VALUE_FUNCPRM),   // FuncPrm
                    PdnTypePartyID,               // PdnTypePartyID
                    outEndDate,                   // outEndDate
                    outOper,                      // outOper
                    outComment                    // outComment
                );
                
                if (result == RESULT_FALSE)
                    AllComplete = false;
                elif (result == RESULT_TRUE)
                    if ((ValType(outOper) == V_UNDEF) or (outOper <= 0))
                        outOper = {oper};
                    end;
                    
                    if ((ValType(outEndDate) == V_UNDEF) or (outEndDate == ZeroValue(V_DATE)))
                        outEndDate = Date();
                    end;
                    
                    if (ValType(outComment) == V_UNDEF)
                        outComment = "";
                    end;
                    outEndDateArr[outEndDateArr.size] = outEndDate;
                    outOperArr[outOperArr.size] = outOper;
                    outCommentArr[outCommentArr.size] = outComment;
                else
                    PdnChkLogAddLine(ЗавершениеОбработкиПДнОшибка, rs.value(0), rs.value(1), CmpltPrcssID, ZeroValue(V_DATE), funcs.value(VALUE_FUNCID));
                end;
            end;

            if ((NeedStopFlag == true) or (AllComplete == true))
                var comment = "";
                var ValueEndate, ValueOper = 0, ValueComment = "", ValueTermLimit = "";
                
                if (NeedStopFlag == true)
                    ValueEndate = Date(31, 12, 9999);
                else
                    ValueEndate = ZeroValue(V_DATE);
                end;

                var i = 0;
                while (i < outEndDateArr.size)
                    var dt = outEndDateArr[i];
                    if (NeedStopFlag == true)
                        if (ValueEndate > dt)
                            ValueEndate = dt;
                            ValueOper = outOperArr[i];
                            ValueComment = outCommentArr[i];
                        end;
                    else
                        if (ValueEndate < dt)
                            ValueEndate = dt;
                            ValueOper = outOperArr[i];
                            ValueComment = outCommentArr[i];
                        end;
                    end;
                    i = i + 1;
                end;
                
                if (NeedStopFlag == true)
                    if (ValueEndate == Date(31, 12, 9999))
                        ValueEndate = ZeroValue(V_DATE);
                    end;
                end;
                
                if (outEndDateArr.size)
                PDN_CalculatePlanDate(TermLimit, ValueEndate, PlanDate);
                Pdn_CreateFinProcParty(PdnTypePartyID, PersonID, CmpltPrcssID, ValueEndate, PlanDate, outOper, outComment, NeedStopFlag);
                   
                // Тут должен быть протокол
                PdnChkLogAddLine(ЗавершениеОбработкиПДн, rs.value(0), rs.value(1), CmpltPrcssID, ValueEndate);
                end;
            elif ((NeedStopFlag == false) and (AllComplete == false))
                var FinProcParty = TbFile("pdnfinprocparty.dbt", "W", 2);
                FinProcParty.rec.PdnTypeParty = PdnTypePartyID;
                FinProcParty.rec.PersonID = PersonID;
                FinProcParty.rec.CmpltPrcssID = CmpltPrcssID;
                
                if (FinProcParty.GetEq())
                    Copy(FinProcParty_old, FinProcParty);
                    Copy(FinProcParty_old2, FinProcParty);
                    PDN_AddLogMessage(PersonID, PDNEVENTLOG_MANUAL, PDNEVENTLOG_PDN_FINALY, PDNEVENTLOG_DELETE, FinProcParty_old, FinProcParty_old2, false, true);
                end;
            end;
        end;
    end;
end;

private macro PDN_CheckNeedDeleteDateCondition(PARTYID, PDNTYPEPARTYID, CMPLTPRCSSID, PLANDATE, ENDDATE)
//begin
    var result = false;
    if (((PLANDATE == ZeroValue(V_DATE)) AND (ENDDATE <= Date())) or ((PLANDATE != ZeroValue(V_DATE)) AND (PLANDATE <= Date())))
        result = true;
    end;
    return result;
end;

 macro PDN_CheckProcess(
    OpenCheck, 
    CloseCheck,
    PdnPartyKind,
    PartyID,
    CheckFinProc,
    CheckNeedDelete,
    CheckDecor,
    Txt, 
    Xls, 
    Xml,
    IsScheduler
 )
//begin
    PdnChkLogClear();

    const VALUE_PARTYID        = 0; 
    const VALUE_PDNTYPEPARTYID = 1;
    const VALUE_CMPLTPRCSSID   = 2;
    const VALUE_PLANDATE       = 3;
    const VALUE_ENDDATE        = 4;
    const VALUE_TERMLIMITINFO  = 5;
            
    var params = TArray;
    var sql = "select t.t_partyid, tp.t_ID, tp.t_PartyKind from dparty_dbt t, dpdntypeparty_dbt tp where ";
    var sqlwhere = "tp.t_PersonID = t.t_partyid ";
    var sqlptwhere = "";
    var LogFileName = "";
    
    if (PartyID != 0)
        sqlptwhere = sqlptwhere + " t.t_partyid = ?";
        params[params.size] = SQLParam("", PartyID);
    else
        var fOpenClose = false;
        sqlptwhere = sqlptwhere + " (1 = 0 ";
        
        if (CloseCheck)
            sqlptwhere = sqlptwhere + "or t.T_LOCKED = CHR(88) ";
            fOpenClose = true;
        end;
            
        if (OpenCheck)
            sqlptwhere = sqlptwhere + "or t.T_LOCKED = CHR(0) ";
            fOpenClose = true;
        end;
        
        if (not fOpenClose)
            sqlptwhere = sqlptwhere + "or t.T_LOCKED = CHR(0) ";
        end;
        sqlptwhere = sqlptwhere + ")";
    end;
    
    if (PdnPartyKind != -1)
        sqlwhere = sqlwhere + "AND tp.t_PartyKind = ?";
        params[params.size] = SQLParam("", PdnPartyKind);
    end;
    
    sqlptwhere = sqlptwhere + "AND t.t_PdnDestr = TO_DATE('01/01/0001', 'MM/DD/YYYY')";
    sqlwhere = sqlwhere + "AND tp.t_TermDate = TO_DATE('01/01/0001', 'MM/DD/YYYY')";
    sqlwhere = sqlptwhere + " AND " + sqlwhere;
    
    sql = sql + sqlwhere;
    var rs;
    if (CheckFinProc)
        rs = execSQLSelect(sql, params, true);
        //println("CheckNeedDelete:");  !!!!!!!!!!!!!!!!!!!!!!!
        PDN_CheckProcessBody(rs, OpenCheck, 
            CloseCheck,
            PdnPartyKind,
            PartyID,
            CheckFinProc,
            CheckNeedDelete,
            CheckDecor,
            Txt, 
            Xls, 
            Xml);
    end;

    if (CheckNeedDelete)
        var CheckNeedDeleteSql = "SELECT t.t_partyid, tp.t_ID, FP.T_CMPLTPRCSSID, fp.t_PlanDate, fp.t_EndDate FROM dparty_dbt t, dpdntypeparty_dbt tp, dpdnfinprocparty_dbt fp where ";
        var CheckNeedDeleteWhere = sqlwhere + " and FP.T_PERSONID = tp.t_PersonID ";
        CheckNeedDeleteWhere = CheckNeedDeleteWhere + "union select t1.t_PersonID, 0, T1.T_CMPLTPRCSSID, t1.t_PlanDate, t1.t_EndDate from dpdnfinprocparty_dbt t1, dparty_dbt pt where t1.t_PdnTypeParty = 0 and PT.T_PARTYID = t1.t_PersonID and PT.t_PdnDestr = TO_DATE ('01/01/0001', 'MM/DD/YYYY')";
        
        var CheckNeedDeleteSqlFull = "select distinct * from (" + CheckNeedDeleteSql + " " + CheckNeedDeleteWhere + ")";
        
        rs = execSQLSelect(CheckNeedDeleteSqlFull, params, true);
        
        //println("CheckNeedDelete:");  !!!!!!!!!!!!!!!!!!!!!!!
        while (rs.movenext())
            var rsPARTYID = rs.value(VALUE_PARTYID);
            var PDNTYPEPARTYID = rs.value(VALUE_PDNTYPEPARTYID);
            var CMPLTPRCSSID = rs.value(VALUE_CMPLTPRCSSID);
            var PLANDATE = rs.value(VALUE_PLANDATE);
            var ENDDATE =  rs.value(VALUE_ENDDATE);
            
            if (not PDN_CheckNeedDeleteDateCondition(rsPARTYID, PDNTYPEPARTYID, CMPLTPRCSSID, PLANDATE, ENDDATE))
                // проверить необходимость предупреждения о предстоящем уничтожении ПДн
                if (PLANDATE != ZeroValue(V_DATE))
                    var cmd = RsdCommand("select ? - (select t_TermLimitInfo from dpdncmpltprcss_dbt where t_ID = ?) from dual");
                    cmd.addParam("", RSDBP_IN, PLANDATE);
                    cmd.addParam("", RSDBP_IN, CMPLTPRCSSID);
                    
                    var res = RsdRecordset(cmd);
                    if (res.movenext())
                        if (PDN_CheckNeedDeleteDateCondition(rsPARTYID, PDNTYPEPARTYID, CMPLTPRCSSID, res.value(0), ENDDATE))
                            // предупреждение о предстоящем уничтожении ПДн
                            PdnChkLogAddLine(ПредупреждениеОНеобходимостиУничтоженияПДн, 
                                             rsPARTYID, PDNTYPEPARTYID, CMPLTPRCSSID, res.value(0));
                        end;
                    end;
                end;
            else
                // Тут должен быть протокол
                PdnChkLogAddLine(НеобходимоУничтожитьПДн, 
                                 rsPARTYID, PDNTYPEPARTYID, CMPLTPRCSSID, PLANDATE);
            end;
        end;
    end;
    
    //println("CheckDecor:");  !!!!!!!!!!!!!!!!!!!!!!!
    if (CheckDecor)
        var CheckDecorSql = "SELECT t.t_partyid FROM dparty_dbt t WHERE t.T_LEGALFORM = 2 AND ( ";
        CheckDecorSql = CheckDecorSql + sqlptwhere;
        CheckDecorSql = CheckDecorSql + "AND NOT EXISTS (SELECT 1 FROM dpdntypeparty_dbt tpt where tpt.t_personid = t.t_partyid))";

        var SumParams = TArray;
        var iArr = 0;
        var jArr = 0;

        while(jArr<2)
          iArr = 0;
          while(iArr < params.size)
           var tmp = iArr;
           SumParams[SumParams.size] = params[tmp];
           iArr = iArr + 1;
          end;
          jArr = jArr + 1;
        end;
        // Тут должен быть протокол
        rs = execSQLSelect(CheckDecorSql, SumParams, true);
        while (rs.movenext())
            PdnChkLogAddLine(НеОформленаОбработкаПДн, 
                             rs.value(0));
        end;
    end;
    
    if(Xls)
      PrintLogXls(OpenCheck, 
        CloseCheck,
        PdnPartyKind,
        PartyID,
        CheckFinProc,
        CheckNeedDelete,
        CheckDecor,
        IsScheduler
      );
    end;

    if(Xml)
      var LogXmlFileName = PrintLogXml(OpenCheck, 
        CloseCheck,
        PdnPartyKind,
        PartyID,
        CheckFinProc,
        CheckNeedDelete,
        CheckDecor,
        IsScheduler
      );

      RunViewSystem(LogXmlFileName);
    end;
    
    if(Txt)
      LogFileName = PrintLogTxt(OpenCheck, 
        CloseCheck,
        PdnPartyKind,
        PartyID,
        CheckFinProc,
        CheckNeedDelete,
        CheckDecor,
        IsScheduler
      );
    end;
    
    return LogFileName;
 end;