/*
$Name:          cbRepFun.mac
$Module:        RS-Bank, ПИ
$Description:   Общие функции выпуска отчетов
*/

import rsd, PaymInter, CTInter, BankInter, CurrInter, SPInter, PTInter, FIInter,
       "adress.mac", oprinter, "Календарь", "cb_sql.mac", "or_rep_h.mac", globals;

var HeaderWidth;

private CONST SET_CHAR   = "X";
private const UNDF = 0;

/*UseApp - обернуть день в кавычки , те выводить "10" февраля 2006 г.*/
macro DL_DateToStr(pDate, UseApp)

  var d, m, y, Day, Month, Year;

  DateSplit(pDate, d, m, y);
  Day = String(d);

  if(UseApp)
    Day = "\""+Day+"\"";
  end;

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";
end;

/* Имя операциониста, должность и телефон */
class DepoExecuter()
  var OperId;
  var Name, Post, PhoneNumber = "", Email = null;
  var person  = TBFile("person");
  var officer = TBFile("officer");
  var party   = TrecHandler("party");

  GetParm(1, OperId);
  if(valtype(OperId) != V_INTEGER)
    OperId = {oper};
  end;

  person.rec.oper = OperId;
  if(person.GetEQ)
    Name = person.rec.Name;
  else
    Name = "";
  end;

  officer.rec.PartyID = {OurBank};
  officer.rec.PersonID = person.rec.PartyID;
  if(officer.GetEQ)
    Post = officer.rec.Post;
  else
    Post = "Ответственный сотрудник";
  end;
  Post = "Исполнитель";

  FindAdressContactValue(person.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);

  macro GetEmail()
    if( Email == null )
      Email = "";
      FindAdressContactValue(person.rec.PartyID, PTADDR_LEGAL, CNTADR_E_MAIL, Email);
    end;

    return Email;
  end;
end;

/* Подпись исполнителя */
macro FooterExecuterSign(OperID, Rep, SizeCol, FontStyle)
  var Executer = DepoExecuter(OperID);
  var FS_ = FontStyle;

  if(FS_ == UNDF)
    FS_ = "l:ex_FS(b)";
  end;

  if(Rep != NULL)
    if(SizeCol)
      HeaderWidth = SizeCol;
    else
      HeaderWidth = Rep.GetHeaderWidth();
    end;

    Rep.AddPrintCell(Executer.Post                    +" _____________ "+Executer.Name, HeaderWidth, 0, FS_, REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell(MkStr(" ", strlen(Executer.Post))+"    подпись", HeaderWidth, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddEmptyStr(1, SizeCol);
    Rep.AddStr();
    Rep.AddPrintCell("Телефон: "+Executer.PhoneNumber, HeaderWidth, 0, FS_, REP_ELEM_STR);
    Rep.AddStr();
  else
    [ #](Executer.Post                    +" _____________ "+Executer.Name);
    [ #](MkStr(" ", strlen(Executer.Post))+"    подпись");
    println("");
    [ #]("Телефон: "+Executer.PhoneNumber);
  end;
end;

/* Подпись главного босса - директора банка */
macro FooterMainBossSign(Rep, SizeCol, FontStyle)
  var Post, Name;
  var FS_ = FontStyle;

  if(FS_ == UNDF)
    FS_ = "l:ex_FS(b)";
  end;

  if({Name_Boss} and not {Name_Book})
    Post = {Name_Boss};
  else
    Post = "Директор банка";
  end;

  Name = {FIO_Boss};

  if(Rep != NULL)
    if(SizeCol)
      HeaderWidth = SizeCol;
    else
      HeaderWidth = Rep.GetHeaderWidth();
    end;

    Rep.AddPrintCell(Post                    +" _____________ "+Name, HeaderWidth, 0, FS_, REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell(MkStr(" ", strlen(Post))+"    подпись", HeaderWidth, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  else
    [ #](Post                    +" _____________ "+Name);
    [ #](MkStr(" ", strlen(Post))+"    подпись");
  end;
end;

/* Подпись главного бухгалтера */
macro FooterBookSign(Rep, SizeCol, FontStyle)
  var Post, Name;
  var FS_ = FontStyle;

  if(FS_ == UNDF)
    FS_ = "l:ex_FS(b)";
  end;

  if({Name_Book})
    Post = {Name_Book};
  else
    Post = "Главный бухгалтер банка"
  end;

  Name = {FIO_Book};

  if(Rep != NULL)
    if(SizeCol)
      HeaderWidth = SizeCol;
    else
      HeaderWidth = Rep.GetHeaderWidth();
    end;

    Rep.AddPrintCell( Post                    +" _____________ "+Name, HeaderWidth, 0, FS_, REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( MkStr(" ", strlen(Post))+"    подпись", HeaderWidth, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  else
    [ #](Post                    +" _____________ "+Name);
    [ #](MkStr(" ", strlen(Post))+"    подпись");
  end;
end;

/* Подпись "Начальника отдела" */
macro FooterBossSign(Rep, SizeCol, FontStyle)
  var Post = "", Name = "";
  var officer = TBFile("officer", null, 5); /*для поиска начальника отдела*/
  var party  = TBFile("party");
  var FS_ = FontStyle;

  if(FS_ == UNDF)
    FS_ = "l:ex_FS(b)";
  end;

  officer.Clear;
  officer.rec.PartyID = {OurBank};
  officer.rec.OfficeID = SelfDivision;
  officer.rec.IsFirstOfficePerson = SET_CHAR;
  if(officer.GetEQ)
    if(officer.rec.Post)
      Post = officer.rec.Post;
    else
      Post = "Начальник отдела";
    end;

    party.rec.PartyID = officer.rec.PersonID;
    if(party.GetEQ)
      Name = party.rec.Name;
    else
      Name = "";
    end;
  end;

  if(Post == "")
    Post = "Начальник отдела";
  end;

  if(Rep != NULL)
    if(SizeCol)
      HeaderWidth = SizeCol;
    else
      HeaderWidth = Rep.GetHeaderWidth();
    end;

    Rep.AddPrintCell(Post                    +" _____________ "+Name, HeaderWidth, 0, FS_, REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell(MkStr(" ", strlen(Post))+"    подпись", HeaderWidth, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  else
    [ #](Post                    +" _____________ "+Name);
    [ #](MkStr(" ", strlen(Post))+"    подпись");
  end;
end;

/* Стандартная подошва отчета */
macro after_44_footer(Rep, SizeCol, IsCurDate, NoBook, WithBoss)
  var RepDate = Date();

  if(IsCurDate)
    RepDate = {curdate};
  end;

  if(Rep != null)
    if(SizeCol)
      HeaderWidth = SizeCol;
    else
      HeaderWidth = Rep.GetHeaderWidth();
    end;

    Rep.AddPrintCell(" ", HeaderWidth, 0, "f:l:ex_FS(b)", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddEmptyStr(1, SizeCol);
    FooterMainBossSign(Rep, SizeCol);
    Rep.AddEmptyStr(1, SizeCol);
    if(not NoBook)
      FooterBookSign(Rep, SizeCol);
      Rep.AddEmptyStr(1, SizeCol);
    end;
    if(WithBoss)
      FooterBossSign(Rep, SizeCol);
      Rep.AddEmptyStr(1, SizeCol);
    end;
    FooterExecuterSign({oper}, Rep, SizeCol);
    Rep.AddEmptyStr(1, SizeCol);
    Rep.AddPrintCell(DL_DateToStr(RepDate), HeaderWidth, 0, "l:ex_FS(b)", REP_ELEM_STR);
    Rep.AddStr();
  else
    println("");
    FooterMainBossSign();
    println("");
    if(not NoBook)
      FooterBookSign();
      println("");
    end;
    if(WithBoss)
      FooterBossSign();
      println("");
    end;
    FooterExecuterSign({oper});
    println("");
    [ #](DL_DateToStr(RepDate));
  end;
end;

/* Стандартная подошва отчета без главного бухгалтера */
macro after_44_footer_nobook(Rep, SizeCol, IsCurDate)
  after_44_footer(Rep, SizeCol, IsCurDate, true);
end;

/**********************************************************************************/
private macro GetNameAlg( Type, Number )
  var NameAlg= TBFile("namealg");
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;

const ALG_CB_MONTHS_AND_QUART = 2263;
const ALG_CB_PERIOD_IN_QUART = 2264;

/*******************************Работа с датами******************************************/
/* Получить название месяца или квартала по его номеру и году.
      PeriodCode  - 1..12 - месяца
                    13..16 - кварталы (1-ый .. 4-ый, соответственно)
      YearNum     - номер года
      GrowingPer  - флаг, нарастающий период. Если true, то начало
                    периода - 1-ое января YearNum года.
   Пример работы:
      1) Исходные данные:  PeriodCode = 5, YearNum = 2004
         Результат:        май 2004 года
         Нараст. период:   январь - май 2004 года
      2) Исходные данные:  PeriodCode = 14, YearNum = 2004
         Результат:        II квартал 2004 года
         Нараст. период:   полугодие 2004 года */
macro PeriodName_GetMonthsAndQuart( PeriodCode, YearNum, GrowingPer )
   if( GrowingPer )
      if( PeriodCode == 1 )
         return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
                  + " " + string(YearNum) + " года";
      elif( PeriodCode == 13 )
         return "I квартал " + string(YearNum) + " года";
      elif( PeriodCode == 14 )
         return "полугодие " + string(YearNum) + " года";
      elif( PeriodCode == 15 )
         return "9 месяцев " + string(YearNum) + " года";
      elif( PeriodCode == 16 )
         return string(YearNum) + " год";
      else
         return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, 1)
                  + " - " + GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
                  + " " + string(YearNum) + " года";
      end;
   else
      return   GetNameAlg(ALG_CB_MONTHS_AND_QUART, PeriodCode)
               + " " + string(YearNum) + " года";
   end;
end;

/* Получить начало и конце периода по его номеру и году.
      PeriodCode  - 1..12 - месяца
                    13..16 - кварталы (1-ый .. 4-ый, соответственно)
      YearNum     - номер года
      GrowingPer  - флаг, нарастающий период. Если true, то начало
                    периода - 1-ое января YearNum года
   Пример работы:
      1) Исходные данные:  PeriodCode = 5, YearNum = 2004
         Результат:        BegDate = 01.05.2004, EndDate = 31.05.2004
         Нараст. период:   BegDate = 01.01.2004, EndDate = 31.05.2004
      2) Исходные данные:  PeriodCode = 14, YearNum = 2004
         Результат:        BegDate = 01.04.2004, EndDate = 30.06.2004
         Нараст. период:   BegDate = 01.01.2004, EndDate = 30.06.2004 */
macro Period_GetInterval(  PeriodCode, YearNum, BegDate:@date, EndDate:@date,
                           GrowingPer )
   if( PeriodCode <= 12 )
      /* Указан месяц */
      if( (PeriodCode == 1) and ( YearNum==1 ) )
         BegDate = Date(0, 0, 0);
      else
         BegDate = Date( 1, PeriodCode, YearNum );
         EndDate = DateAfterCalenMonths( BegDate, 1) - 1;
      end;
   else
      /* Указан квартал */
      BegDate = Date( 1, ((PeriodCode - 13)*3 + 1), YearNum );
      EndDate = DateAfterCalenMonths( BegDate, 3 ) - 1;
   end;

   if( GrowingPer )
      BegDate = Date( 1, 1, YearNum );
   end;
end;

/*Остаток по счету accbuf до проводки Carry*/
MACRO ОстатокСчетаДоПроводки( accbuf, Start_Date, OutRest, Carry, IsIncludeCarry:bool )
   file oper( oproper ) key 1;
   var  Rest;

   macro GetCarrySum( WhereCond )
      var Sum = $0;
      var sql, AccTrnData;

      sql = RSDCommand(" select * " +
                       "   from dacctrn_dbt " +
                       "  where " + WhereCond +
                       "    and t_Date_Carry = ? " +
                       "    and t_State = 1 " +
                       "    and t_Result_Carry != "+string(ARHCARRY));

      sql.addParam( "", RSDBP_IN, Carry.Date_Carry );
      sql.execute();

      AccTrnData = TRsbDataSet( sql );

      while( AccTrnData.MoveNext() )
         if( (IsIncludeCarry) and (AccTrnData.AccTrnID <= Carry.AccTrnID) ) /*включая и проводку Carry*/
            if( AccTrnData.Account_Payer == accbuf.Account )
               Sum = Sum + AccTrnData.Sum_Payer;
            else
               Sum = Sum + AccTrnData.Sum_Receiver;
            end;
         elif( AccTrnData.AccTrnID < Carry.AccTrnID ) /*Проводка была раньше*/
            if( AccTrnData.Account_Payer == accbuf.Account )
               Sum = Sum + AccTrnData.Sum_Payer;
            else
               Sum = Sum + AccTrnData.Sum_Receiver;
            end;
         end;
      end;
      return Sum;
   end;

   /*на начало дня*/
   Rest = RestAC( accbuf.Account, accbuf.Code_Currency, Start_Date-1, null, accbuf.Chapter );
   /* Перебираем все проводки по счету за дату операции до 1й проводки операции (до Carry)*/
   Rest = Rest - GetCarrySum("     t_Account_Payer = '"+accbuf.Account+"'"+
                             " and t_FIID_Payer = "+accbuf.Code_Currency+
                             " and t_Chapter = "+accbuf.Chapter); /*по Account_Payer (дебет)*/
   Rest = Rest + GetCarrySum("     t_Account_Receiver = '"+accbuf.Account+"'"+
                             " and t_FIID_Receiver = "+accbuf.Code_Currency+
                             " and t_Chapter = "+accbuf.Chapter);/*по Account_Receiver (кредит)*/
   SetParm( 2, Rest + OutRest );
   return Rest;
END;

/* Поиск в LLVALUE */
MACRO GetLLVALname (list, element)
   var llval  = TBfile ("llvalues");

   llval.KeyNum = 0;
   llval.rec.List = list;
   llval.rec.Element = element;

   if (llval.GetEQ)
       return llval.rec.Name;
   end;

    return "";
END;
