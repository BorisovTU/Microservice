/*
 $Name: xcompl_mass_check.mac
 $Module: RS-Connect
 $Description: Проверка на принадлежность к ПДЛ (массовая)
 */
 /* 
	Проверка на принадлежность к ПДЛ (массовая)
 */
import "cb_sql.mac", "globals.mac", rslx, rcw, rsexts, "ws_xcompl_lib.mac", "common_service_caller.mac";
import BankInter, PTInter, CTInter;

var pProcessed, pNotIgnoredul;

private class (CServiceCallerBase) CMassRejectQueryCaller(_Param)
    private var m_param = _Param;

    private macro SetRequestParameters(request : @TXRRequest)
        var inParams = TArray();
        inParams[inParams.size] = m_param;
        request.Parms = inParams;
    end;
    
    private macro GetSystemURL() : String
     var m_systemURL;
      GetRegistryValue ("RS-CONNECT\\SSUSAGE_SNILS\\URL", V_STRING, m_systemURL);
     return m_systemURL;
    end;    

    InitCServiceCallerBase(CreateGUID(), GetSystemURL(), "ws_xcompl_check_mass", "XCompliance_SearchOperationMass", 300000);
end;

private macro CastMassAnswer(answer : Variant)
    var answerCast = TXCompResponseSearchOperationMass();
    answerCast.AnswPar.SenderID      = answer.AnswPar.SenderID;
    answerCast.AnswPar.MessageID     = answer.AnswPar.MessageID;
    answerCast.AnswPar.SenderPointID = answer.AnswPar.SenderPointID;
    answerCast.AnswPar.ObjectID      = answer.AnswPar.ObjectID;
    answerCast.AnswPar.PackageNum    = answer.AnswPar.PackageNum;
    answerCast.AnswPar.PackageCount  = answer.AnswPar.PackageCount;
    answerCast.ResultSelect = TArray;
      
    for (var info, answer.ResultSelect)
        var infoCast        = TXCompInfoLongOut();
        infoCast.RowID      = info.RowID;
        infoCast.IdentMess  = info.IdentMess;
        infoCast.DateMess   = info.DateMess;
        infoCast.AttrDel    = info.AttrDel;
        infoCast.FullName   = info.FullName;
        infoCast.LastName   = info.LastName;
        infoCast.FirstName  = info.FirstName;
        infoCast.Patronymic = info.Patronymic;
        infoCast.birthdate  = info.birthdate;
        infoCast.NameDoc    = info.NameDoc;
        infoCast.SeriesDoc  = info.SeriesDoc;
        infoCast.NumDoc     = info.NumDoc;
        infoCast.Issue      = info.Issue;
        infoCast.CodeCat    = info.CodeCat;
        infoCast.DescrCat   = info.DescrCat;
        infoCast.Job        = info.Job;
        infoCast.Biography  = info.Biography;

        answerCast.ResultSelect[answerCast.ResultSelect.size] = infoCast;
    end;
    return answerCast;
end;

private macro GetQuestion()
    Array Text;
    Array Buttons;
    Text(0) = "Потеряна связь с RS-Connect. Обработана только часть данных. " ;
    Buttons(0)  = " Связаться повторно ";
    Buttons(1)   = " Завершить процедуру ";
    return ConfWin(Text,Buttons);
end;

private macro FillInformationTable(obj : @variant)
    var stat = true;
    InitProgress( obj.ResultSelect.size, "~Alt-Break~ Прервать", "Заполнение временной таблицы" );
    var pdlserviceCache = RsbSQLInsert("pdlservice.tmp");
    var pdlservice = TRecHandler("pdlservice.tmp");

    var t = 0, j = 0, jt = 0;
    var sizea = obj.ResultSelect.size;
    while (t < sizea)
        var i = t;
        ClearRecord (pdlservice);
        var SelectResult = obj.ResultSelect[i];
        if(ValType(SelectResult.RowID)      != V_UNDEF) pdlservice.rec.RowID           = SelectResult.RowID;     end;
        if(ValType(SelectResult.IdentMess)  != V_UNDEF) pdlservice.rec.SystemId        = SelectResult.IdentMess; end;
        if(ValType(SelectResult.DateMess)   != V_UNDEF) pdlservice.rec.UpDateAt        = SelectResult.DateMess;  end;
        if(ValType(SelectResult.AttrDel)    != V_UNDEF) pdlservice.rec.IsDeletedPeople = SelectResult.AttrDel;   end;
        if(ValType(SelectResult.FullName)   != V_UNDEF) pdlservice.rec.FullName        = SelectResult.FullName;  end;
        if(ValType(SelectResult.birthdate)  != V_UNDEF) pdlservice.rec.BirthDate       = SelectResult.birthdate; end;
        if(ValType(SelectResult.NameDoc)    != V_UNDEF) pdlservice.rec.PaperName       = SelectResult.NameDoc;   end;
        if(ValType(SelectResult.SeriesDoc)  != V_UNDEF) pdlservice.rec.PaperSeries     = SelectResult.SeriesDoc; end;
        if(ValType(SelectResult.NumDoc)     != V_UNDEF) pdlservice.rec.PaperNumber     = SelectResult.NumDoc;    end;
        if(ValType(SelectResult.Issue)      != V_UNDEF) pdlservice.rec.PaperIssue      = SelectResult.Issue;     end;
        if(ValType(SelectResult.CodeCat)    != V_UNDEF) pdlservice.rec.CategoryCode    = SelectResult.CodeCat;   end;
        if(ValType(SelectResult.DescrCat)   != V_UNDEF) pdlservice.rec.CategoryName    = SelectResult.DescrCat;  end;
        if(ValType(SelectResult.Job)        != V_UNDEF) pdlservice.rec.Post            = SelectResult.Job;       end;
        if(ValType(SelectResult.Biography)  != V_UNDEF) pdlservice.rec.Biography       = SelectResult.Biography; end;

        pdlserviceCache.AddRecord(pdlservice);
        t = t + 1;
        UseProgress(t);
    end;

    if(obj.ResultSelect.size > 0)
        if(not pdlserviceCache.Insert())
            MsgBox("Ошибка вставки в pdlservice.tmp");
            stat = false;
        end;
    end;
    RemProgress();

    return stat;
end;

private macro ComparePartyTable()
    var stat = true;
    BegAction(-1, "Идентификация субъектов", false);
    var strQuery = ""; 

    strQuery = "INSERT INTO dpdlsrvptlink_tmp(t_PdlService, t_PartyID) " + 
        "SELECT ms.T_ID, T_PERSONID FROM DPERSN_DBT PS, DPARTY_DBT PT, DPDLSERVICE_TMP ms " + 
        " WHERE PT.T_PARTYID = PS.T_PERSONID " +
        " AND RSI_RSBPARTY.XCOMPL_NormalizeString(PT.T_NAME, 1) = RSI_RSBPARTY.XCOMPL_NormalizeString(ms.t_FullName, 1) " +
        " AND RSI_RSBPARTY.XCOMPL_CheckBirthDate(ms.t_BirthDate, PS.T_BORN) = 0 " +
        " AND ms.t_Complete <> CHR(88) AND ms.t_FullName <> CHR(1) ";

    if (pNotIgnoredul == "X")
        strQuery = strQuery + " AND EXISTS (SELECT 1 FROM DPERSNIDC_DBT dc WHERE " +
            "dc.T_PERSONID = PT.T_PARTYID AND " + 
            "RSI_RSBPARTY.XCOMPL_NormalizeString(T_PAPERSERIES, 1) = RSI_RSBPARTY.XCOMPL_NormalizeString(ms.T_PAPERSERIES, 1) " + 
            "RSI_RSBPARTY.XCOMPL_NormalizeString(T_PAPERNUMBER, 1) = RSI_RSBPARTY.XCOMPL_NormalizeString(ms.T_PAPERNUMBER, 1) " +
            ")";
    end;
    SQL_Execute(strQuery);
    EndAction();

    return stat;
END;

private macro AddNoteParty()
    var stat = true;

    var sString = " SELECT lnk.t_PartyID, srv.t_SystemId, srv.t_UpDateAt  "
        " FROM dpdlservice_tmp srv, dpdlsrvptlink_tmp lnk "
        " WHERE srv.t_Complete <> CHR(88) AND lnk.t_PdlService = srv.t_ID "
        " ORDER BY t_PartyID ";

    var scmd = RsdCommand(sString);
    scmd.NullConversion = true;
    var srs = RsdRecordset(scmd);

    BegAction(-1, "Вставка примечаний субъекта", false);
    while(srs.moveNext())
        var PartyID = srs.value("t_PartyID");
        var SystemId = srs.value("t_SystemId");
        var UpDateAt = Date(srs.value("t_UpDateAt"));
        var AddNote = XCOMPL_MakePartyNoteText(SystemId, UpDateAt);
        var strPartyID = string(PartyID:o:10);
        var FullNote = readNoteForObject(OBJTYPE_PARTY, strPartyID, PARTY_NOTE_KIND_XCOMPL_PDL);

        if(strlen(FullNote) > 0 )
            FullNote = FullNote + ";" + AddNote;
        else
            FullNote = AddNote;
        end;
        AddNoteForObject(OBJTYPE_PARTY, strPartyID, PARTY_NOTE_KIND_XCOMPL_PDL, FullNote);
    end;
    EndAction();

    return stat;
end;

private macro SetComplete()
    var stat = true;
    var strQuery = " UPDATE dpdlservice_tmp ms SET t_Complete = 'X'";
    SQL_Execute(strQuery);
    return stat;
end;

private macro GetPackageInfo(SenderId, url, ObjectID, PackageNum:@integer, CompletedPackageNum)
    var stat = true;
    var mes = TXCompRequestSearchOperationMass;

    mes.QueryPar = TXCompHeaderInOut;
    mes.ParamSelect = TXCompBodySearchOperationMassIn;

    mes.QueryPar.SenderID = SenderId;
    mes.QueryPar.MessageID = ObjectID;
    mes.QueryPar.SenderPointID = "";
        
    mes.ParamSelect.ObjectID = ObjectID;
    PackageNum = PackageNum + 1;
    mes.ParamSelect.PackageNum = PackageNum;
    mes.ParamSelect.CompletedPackageNum = CompletedPackageNum;

    var retstat = false;
    var caller = CMassRejectQueryCaller(mes);
    var answer;

    while((retstat != true) AND (stat == true))
      InitProgress( -1, "~Alt-Break~ Прервать", "Получение данных через транспортную компоненту." ); 
      retstat = caller.CallService(@answer, true);
      RemProgress();

      if (retstat != true)
        if(GetQuestion() != 0)
          stat = false;
        end;
      end;
    end;

    if((retstat == true) AND (stat == true))
        var obj;
        var retv = false;

        while((retv == false) AND (stat == true))
            InitProgress( -1, "~Alt-Break~ Прервать", "Получение данных через транспортную компоненту." );                     
            obj = CastMassAnswer(answer);
            retv = true;
            RemProgress();
        end;

        if((retv == true) AND (stat == true))
            if(PackageNum > 0) // Если не финализирующий запрос
                if (obj.AnswPar.PackageCount == 0)
                    RunError("", "Информация по запрошенным параметрам отсутствует");
                else
                    stat = FillInformationTable(obj);

                    if(stat == true)
                        stat = ComparePartyTable();
                    end;

                    if(stat == true)
                        stat = AddNoteParty();
                    end;

                    if(stat == true)
                        stat = SetComplete();
                    end;
                end;
            end;
        end;
    else
        stat = false;
    end;
    return stat;
end;

private macro PrintHeader(Processed)
[Отчет о результатах проверки субъектов на принадлежность к ПДЛ];
    if (not Processed)
[                     Только новые анкеты                      ];
    end;
[
+-----+----------------+--------------------------------+------------+-------------------------+-----------------+-----+
|  №  + Код 1 Субъекта |          ФИО субъекта          | Дата рожд. |        Категория        |    ID анкеты    | ПУ  |
+-----+----------------+--------------------------------+------------+-------------------------+-----------------+-----+
];
end;

private macro PrintReportLine(id, code, fullname, birthdate, categorycode, systemid, isdeletedpeople)
[|#####+ ############## | ############################## | ########## | ####################### | ############### |  #  |]
(id:r, code, fullname, birthdate, categorycode:w, systemid:r, isdeletedpeople);
end;

private macro PrintReport(Processed)
    var sString = " SELECT count(lnk.t_PartyID) numparty"
        " FROM dpdlservice_tmp srv, dpdlsrvptlink_tmp lnk"
        " WHERE lnk.t_PdlService = srv.t_ID";

    var scmd = RsdCommand(sString);
    scmd.NullConversion = true;
    var srs = RsdRecordset(scmd);

    if(srs.moveNext())
        var Count = int(srs.value("numparty"));       
    end;

    if(Count == 0)
        [ Данных о принадлежности к ПДЛ по субъектам в соответствии с параметрами запроса не найдено];
    else
        PrintHeader(Processed);
    end;

    sString = "SELECT ROWNUM AS FROWNUM, " + 
        "    NVL ((SELECT T_CODE FROM DPARTCODE_DBT WHERE T_PARTYID = lnk.T_PARTYID AND T_CODEKIND = 1), CHR (1)) T_CODE1, " + 
        "    PT.T_NAME, " + 
        "    PS.T_BORN, " + 
        "    SERV.T_CATEGORYCODE, " + 
        "    SERV.T_SYSTEMID, " + 
        "    SERV.T_ISDELETEDPEOPLE " + 
        "FROM DPDLSERVICE_TMP SERV, DPARTY_DBT PT, DPERSN_DBT PS, DPDLSRVPTLINK_TMP lnk " + 
        "WHERE     LNK.T_PDLSERVICE = SERV.T_ID " + 
        "    AND lnk.T_PARTYID = PT.T_PARTYID " + 
        "    AND PS.T_PERSONID = lnk.T_PARTYID"; 

    var fRownum, Code1, Name, Born, CategoryCode, SystemID, IsDeletedPeople;
    var EqCount = 0;
    scmd = RsdCommand(sString);
    scmd.NullConversion = true;
    srs = RsdRecordset(scmd);
    while(srs.moveNext())
        fRownum = Int(srs.value("FROWNUM"));
        Code1 = srs.value("T_CODE1");
        Name = srs.value("T_NAME");
        Born = Date(srs.value("T_BORN"));
        CategoryCode = srs.value("T_CATEGORYCODE");
        SystemID = Int(srs.value("T_CATEGORYCODE"));
        IsDeletedPeople = srs.value("T_ISDELETEDPEOPLE");
        PrintReportLine(fRownum, Code1, Name, Born, CategoryCode, SystemID, IsDeletedPeople);

        EqCount = EqCount + 1;
    end;

    if(Count)
[+-----+----------------+--------------------------------+------------+-------------------------+-----------------+-----+];
[

--------------------------------------------------------------------------
Всего рассмотрено субъектов:         ############
Всего найдено совпадений:            ############
Дата и время проверки: ##########  ########
Исполнитель:           ########  ########################################
](Count, EqCount, Date(), Time(), {oper}:l, {Name_Oper});
    end;
end;

macro XCompliance_SearchOperation(SenderId, url, Processed, NotIgnoredul)
    var stat = true;
    var error : string;
    var paramArray = TArray;
    var errMsg : string;
    var GUID = CreateGUID();
    var MessageID = substr(GUID, 2, strlen(GUID)-2);
    pProcessed = Processed;
    pNotIgnoredul = NotIgnoredul;

    var Param = TXCompRequestSearchOperationMass;
    Param.QueryPar = TXCompHeaderInOut;
    Param.ParamSelect = TXCompBodySearchOperationMassIn;

    Param.QueryPar.SenderID = SenderId;
    Param.QueryPar.MessageID = MessageID;
    Param.QueryPar.SenderPointID = "";

    if(Processed > 0 )
        Param.ParamSelect.NeedOldRecs = "X";  // признак установлен, и новые и обработанные
    else
        Param.ParamSelect.NeedOldRecs = ""; // признак не установлен, только новые.
    end;
    Param.ParamSelect.PackageSize = 0;
    Param.ParamSelect.ObjectID = MessageID;
    Param.ParamSelect.PackageNum = 0;
    Param.ParamSelect.CompletedPackageNum = 0;
    
    var caller = CMassRejectQueryCaller(Param);
    var answer;
    var CallService_stat = caller.CallService(@answer);
    var obj = CastMassAnswer(answer);

    if (obj.AnswPar.PackageCount != 0)
        var PackageCount  = Int(obj.AnswPar.PackageCount);
        var CurPackageNum = Int(obj.AnswPar.PackageNum);
        var CompletedPackageNum = 0;
        InitProgress( PackageCount, "~Alt-Break~ Прервать", "Проверка на принадлежность к ПДЛ" );
        while ((CurPackageNum <= PackageCount) AND (stat == true))
            stat = GetPackageInfo(SenderId, url, MessageID, @CurPackageNum, CompletedPackageNum);

            if(stat == true)
                CompletedPackageNum = CurPackageNum;
            end;

            UseProgress(CurPackageNum);
        end;
        
        // финализирующий запрос
        if(stat == true)
           CurPackageNum = -1;
           stat = GetPackageInfo(SenderId, url, MessageID, @CurPackageNum, CompletedPackageNum);
        end;
        RemProgress();
    end;
    PrintReport(Processed);
    return stat;
OnError(err)
    if (ValType(err.Err) != V_UNDEF)
        MsgBox (err.Err);
    else
        MsgBox (err.Message);
    end;
    return false;
end;

//XCompliance_SearchOperation_Test(1, "http://serpkov:8084/RsConnect/ws/integration?wsdl", "", "X");