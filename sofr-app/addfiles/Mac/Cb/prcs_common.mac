/*
 $Name:        prcs_common.mac
 $Module:      ББ
 $Description: Инициализация параметров для печати кассовых ордеров
*/

import prpm, prcs, bnk_common;

CLASS TInOutOrderPrintData
  var Number               : string,
      DateStr              : string,
      Client               : string,
      PayerAccount         : string,
      ReceiverAccount      : string,
      PayerBankName        : string,
      ReceiverBankName     : string,
      CCY_Code             : string,
      Amount               : string,
      PayAmount            : string,
      ArraySym             : TArray,
      ArrayPartSum         : TArray,
      ArraySym2            : TArray,
      ArrayPartSum2        : TArray,
      SumString            : string, 
      Ground               : string,
      BworkName            : string,
      CworkName            : string,
      CCworkName           : string,
      Status               : bool,
      Document             : string;

  var   RegKey             : string = "COMMON\\WORK_MODE\\WORK_WITH_RETAIL"; 
  var   RegKeyFrmt         : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ФОРМАТ_ДАТЫ",
        format;
  var   RegKeySSum         : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\РАЗДЕЛИТЕЛЬ_СУММЫ",    sepSumm : string;
  var   RegKeyToDigit      : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ",toDigit : bool;

  private macro GetAmountStr( sum:money ):string
    private var str = string( Sum:f ), stat = 0;
    if( ( toDigit == FALSE ) AND ( SubStr( str, strlen( str ) - 1 ) == "00") )
      str = SubStr( str, 1, strlen( str ) - 3 );
    elif( sepSumm )
      var fraction = SubStr( str, strlen( str ) - 1 );
      str = SubStr( str, 1, strlen( str ) - 3 ) + sepSumm + fraction;
    end;
    return str;
  end;

  private macro FillTArrayFromArray( TArr : TArray, Arr )
  
    var i : integer = 0;
    for (i, 0, asize(Arr) - 1, 1)
      TArr.value(i) = Arr(i);
    end;
    
  end;
  
  private macro getCCY( FIID ):string
    file fiinstr(fininstr);
    if( FIID != NATCUR )
      ClearRecord(fiinstr);
      fiinstr.FIID = FIID;
      var res = "";
      if( GetEQ(fiinstr) )
        res = fiinstr.CCY;
      end;
      return res;
    else
      return "";
    end;
  end;  

  /*Заполнение наименования банков*/
  private macro FillBankName(Account,BaseFIID,Chapter):string
    var BankName: string = "";
    file acc ("account.dbt" ) key 0;
    file dp_dep("dp_dep.dbt") key 0;
    file party("party.dbt");
  
    acc.Chapter       = Chapter;
    acc.Account       = Account;
    acc.Code_Currency = BaseFIID;
    if( getEQ( acc ) )
      dp_dep.Code = acc.Department;
      if(getEQ(dp_dep))
        if(dp_dep.PartyID)
          party.PartyID = dp_dep.PartyID;
          if(getEQ(party))
            BankName = party.Name;
          end;
        end;
      end;
      if (acc.Department != acc.Branch)
        dp_dep.Code = acc.Branch;
        if(getEQ(dp_dep))
          if(dp_dep.PartyID)
            party.PartyID = dp_dep.PartyID;
            if(getEQ(party))
              BankName = BankName + "," + party.Name;
            end;
          end;
        end;
      end;
    end;
    return BankName;
  end;


  private macro FillDocument():string

    var d:integer;
    var PaperNameStr:string = "";
    
    if( strlen( pr_pscshdoc.rec.PaperSeries ) or strlen( pr_pscshdoc.rec.PaperNumber ) )
      PaperNameStr = string( GetPaperKindName( pr_pscshdoc.rec.PaperKind ));
    end;
    if( strlen( pr_pscshdoc.rec.PaperSeries ) )
      PaperNameStr = string( PaperNameStr, " серии ", pr_pscshdoc.rec.PaperSeries );
    end;
    if( strlen(pr_pscshdoc.rec.PaperNumber ) )
      PaperNameStr = string( PaperNameStr, " № ", pr_pscshdoc.rec.PaperNumber );
    end;
    
    if( strlen( pr_pscshdoc.rec.PaperIssuer ) )
      PaperNameStr = string( PaperNameStr, " Выдан ", pr_pscshdoc.rec.PaperIssuer );
    end;

    DateSplit( pr_pscshdoc.rec.PaperIssuedDate, d );      
    if(d > 0) 
      PaperNameStr = string(PaperNameStr, 
                             IfThenElse( pr_pscshdoc.rec.PaperIssuer != "", ", ", " Выдан " ),
                             "\"", 
                             SubStr( string(pr_pscshdoc.rec.PaperIssuedDate:m:f), 1, 2 ), 
                             "\"", 
                             SubStr( string(pr_pscshdoc.rec.PaperIssuedDate:m:f), 3, strlen(string(pr_pscshdoc.rec.PaperIssuedDate:m:f) ) - 3 ),
                             "ода"                                
                           );
    end; 
    return PaperNameStr;

  end;;

  /*Заполнение имен кассовых и бухгалтерских работников*/
  /*1 - для бухгалтера, 2 для получающего кассира, 3 для выдающего кассира*/
  private macro GetBCWorkerName(oper,dockind,paymentId,mode):string       

    var cmd, RS;
    var query: string; 
    if (mode == 1) 
      cmd = RSDCommand( "select t_ShortName from dparty_dbt where t_PartyId = (select t_PartyId from dperson_dbt where t_Oper =  ?) " ) ; 
      cmd.addParam( "", RSDBP_IN, oper);
      cmd.execute();
      RS = TRsbDataSet(cmd);
      if (RS.movenext() )
        BworkName = Rs.shortname;
      else
        BworkName = "";
      end;
      return BworkName;
    end;
    if (mode == 2)
      query = "SELECT * "+
               "FROM (  SELECT t_ShortName "+
                         "FROM dparty_dbt pt, "+
                              "dperson_dbt prs, "+
                              "doprstep_dbt os, "+
                              "doproper_dbt oo "+
                         "WHERE     pt.t_PartyId = prs.t_PartyId "+
                              "AND prs.t_Oper = os.t_Oper "+
                              "AND os.t_ID_Operation = oo.t_ID_Operation "+
                              "AND oo.t_Dockind = ? "+
                              "AND oo.t_DocumentID = LPAD ( ? , 34, '0') "+
                     "ORDER BY os.t_ID_Step DESC) "+
              "WHERE ROWNUM < 2 ";    

      cmd = RSDCommand(query);
      cmd.addParam( "DocKind", RSDBP_IN);
      cmd.addParam( "PaymentId", RSDBP_IN);  
      cmd.value("DocKind") = dockind;
      cmd.value("PaymentId")  = paymentId;
  
      cmd.execute();
      RS = TRsbDataSet(cmd);
      if (RS.movenext() )
        CworkName = Rs.shortname;
      else
        CworkName = "";
      end;                   
      return CworkName;     
    end;  
                                         
    if (mode == 3)

      query = "SELECT * "+
               "FROM (  SELECT t_ShortName "+
                         "FROM dparty_dbt pt, "+
                              "dperson_dbt prs, "+
                              "doprstep_dbt os, "+
                              "doproper_dbt oo "+
                         "WHERE     pt.t_PartyId = prs.t_PartyId "+
                              "AND prs.t_Oper = os.t_Oper "+
                              "AND os.t_ID_Operation = oo.t_ID_Operation "+
                              "AND oo.t_Dockind = ? "+
                              "AND oo.t_DocumentID = LPAD ( ? , 34, '0') "+
                              "AND os.t_Symbol IN ('А', 'Г') "+
                     "ORDER BY os.t_ID_Step) "+
              "WHERE ROWNUM < 2 ";
              
      cmd = RSDCommand(query);
      cmd.addParam( "DocKind", RSDBP_IN);
      cmd.addParam( "PaymentId", RSDBP_IN);  
      cmd.value("DocKind") = dockind;
      cmd.value("PaymentId")  = paymentId;
  
      cmd.execute();
      RS = TRsbDataSet(cmd);
      if (RS.movenext() )
        CCworkName = Rs.shortname;
      else
        CCworkName = "";
      end;                   
      return CCworkName;     
    end;                                           
                   

  end;

  private macro FillSymbolsAndSum( ArraySum, ArraySym, SymbolKind: integer )

    var SymbList:TCashSymbolList = TCashSymbolList( pr_pscshdoc, SymbolKind );
    var i: integer = 0;
    ArraySym(i) = "";
    
    while( i < SymbList.Size )
      ArraySym(i) = SymbList.Value(i).code;
      ArraySum(i) = GetAmountStr(SymbList.Value(i).sum);
      i = i + 1;
    end;

  end;
  
  /* -----------------------------------
  Проинициализировать данными кассового ордера
  ----------------------------------- */
  MACRO InitByCashOrder( pmpaym:TRecHandler, pmrmprop:TRecHandler, pscshdoc:TRecHandler )

    GetRegistryValue( RegKeySSum,    V_STRING,  sepSumm,    "" );
    GetRegistryValue( RegKeyToDigit, V_BOOL,    toDigit,    "" );

    format       = 0;
    Number       = pmrmprop.rec.Number;                                                   
    DateStr      = FillDateString();                                                      

    Client = pscshdoc.rec.FIOClient;                                                      
                                                                                          
    PayerBankName = FillBankName(pmpaym.rec.PayerAccount,pmpaym.rec.FIID,pmpaym.rec.Chapter); 
    ReceiverBankName = FillBankName(pmpaym.rec.ReceiverAccount,pmpaym.rec.PayFIID,pmpaym.rec.Chapter);

    Amount = GetAmountStr(pmpaym.rec.Amount);
    PayAmount = GetAmountStr(pmpaym.rec.PayAmount);
    PayerAccount = pmpaym.rec.PayerAccount;                                                       
    ReceiverAccount = pmpaym.rec.ReceiverAccount;                                                 

    var SumRubString,
        SumNameRubString,
        SumKopString,
        SumNameKopString;    
    FillAmountStr3352( @SumRubString,@SumNameRubString,@SumKopString,@SumNameKopString );
    SumString = SumRubString + " " + SumNameRubString + " " + SumKopString + " " + SumNameKopString;

    CCY_Code = getCCY(pmpaym.rec.FIID);
    array aSym, aPartSum;                                                                         
    FillSymbolsAndSum( aPartSum, aSym, SYMB_KIND_INCOME );
    ArraySym = TArray;
    FillTArrayFromArray(ArraySym, aSym);
    ArrayPartSum = TArray;
    FillTArrayFromArray(ArrayPartSum, aPartSum);

    array bSym, bPartSum;                                                                         
    FillSymbolsAndSum( bPartSum, bSym, SYMB_KIND_OUTGO );
    ArraySym2 = TArray;
    FillTArrayFromArray(ArraySym2, bSym);
    ArrayPartSum2 = TArray;
    FillTArrayFromArray(ArrayPartSum2, bPartSum);
    Ground = pmrmprop.rec.Ground;                                                                 
    BWorkName = GetBCWorkerName(pmpaym.rec.Oper,pmpaym.rec.DocKind, pmpaym.rec.paymentId,1);      

    GetRegistryValue( RegKey, V_BOOL, STATUS, "" );
    GetRegistryValue( RegKeyFrmt, V_INTEGER, format, "" );

    if ((pmpaym.rec.Paymstatus == 32000) and (Status == false ) )
      CworkName = GetBCWorkerName(pmpaym.rec.Oper, pmpaym.rec.DocKind, pmpaym.rec.paymentId, 2);  
    else
      CworkName = "";
    end;

    Document = FillDocument();                                                                                
    CCworkName =  GetBCWorkerName(pmpaym.rec.Oper, pmpaym.rec.DocKind, pmpaym.rec.paymentId, 3);  
          
  END;
  
END;
