/*───────────────────────────────────────────────────────────────────────────
  RS-Bank 5.0                                            R-Style Software Lab

  File Name   : firate.mac                                       4.11.1998
  Description : Курсы финансовых инструментов по датам
  Programmer  : Стасевич В.
  Comment     :

└───────────────────────────────────────────────────────────────────────────*/
import FIInter, "globals.mac";

FILE ФИ( fininstr );
FILE Типы( ratetype );
FILE Инф( party );
FILE Перс( person );

record _Курс( ratedef );

/*VAR {oper}, {Name_Bank}, {Post_Addr};*/

/*------------ Имя операциониста --------------------------------------------*/
MACRO ИмяОперациониста( Номер )
 VAR Имя;
   Перс.Oper = Номер;
   if (not GetEQ (Перс)) Имя = "не найден";
   else                  Имя = Перс.Name;
   end;
   return Имя;
END;

/*------------ Определить название финансового инструмента ------------------*/
MACRO НазваниеФИ( ФИИД )
  ФИ.FIID = ФИИД;

  if( getEQ( ФИ ) )
    return ФИ.Name;
  else
    MsgBox("Не найден финансовый инструмент:", ФИИД);
  end;
END;

/*------- Определить название типа курса финансового инструмента ------------*/
MACRO НазваниеТипа( Тип )

  if( Тип == 0 )
    return "Не задан";
  else
    Типы.Type = Тип;

    if( getEQ( Типы ) )
      return Типы.TypeName;
    else
      MsgBox("Не найден тип курса:", Тип);
    end;
  end;
END;

/*------------ Определить имя PARTY -----------------------------------------*/
MACRO ИмяPARTY( ИД )

  if( ИД < 0 )
    /*return "Не задано";*/
    return "";
  else
    Инф.PartyID = ИД;

    if( getEQ( Инф ) )
      return Инф.Name;
    else
      /*MsgBox("Не найден PARTY:", ИД);*/
      return "";
    end;
  end;
END;

/* -----------------------------------------------------------------
              Функция вызывается из системного модуля
 ------------------------------------------------------------------- */
MACRO Отчет( RateID, ДатаНачала, ДатаКонца )
 VAR Значение, контр = TRUE;

   ПолучитьКурс( _Курс, RateID );
  
[ #                                                                            ]({Name_Bank}:l);
[ #                                                                            ]({Post_Addr});
[                                                                              ];
[                     Курсы финансовых инструментов                            ];
[                 за период  с #          по  #                                ]( ДатаНачала, ДатаКонца );
[                                                                              ];
[                                                                              ];
[ Котируемый финансовый инструмент: #                                          ]( НазваниеФИ(_Курс.FIID) );
[ Базовый финансовый инструмент: #                                             ]( НазваниеФИ(_Курс.OtherFI) );
[                                                                              ];
[ Название курса #                                                             ]( _Курс.Name );
[ #                                                                            ]( _Курс.Definition );
[                                                                              ];
[ Вид курса: #                                                                 ]( НазваниеТипа(_Курс.Type) );
[                                                                              ];
[ Признак "основной" #                                                         ]( _Курс.IsDominant );
[ Признак относительной котировки #                                            ]( _Курс.IsRelative );
[                                                                              ];
[ Источник информации о котировке: #                                           ]( ИмяPARTY(_Курс.Informator):l );
[ Торговая площадка: #                                                         ]( ИмяPARTY(_Курс.Market_Place):l );
[                                                                              ];
[───────────┬───────┬─────────┬──────────┬──────────┬────────┬──────────────────];
[  Значение │Масштаб│Обратная │  Начало  │   Дата   │  Время │ Операционист     ];
[           │       │котировка│ действия │   ввода  │  ввода │                  ];
[───────────┴───────┴─────────┴──────────┴──────────┴────────┴──────────────────];


  while(( ДатаНачала <= ДатаКонца ) and
        ( ПолучитьЗначениеКурса( _Курс, ДатаНачала ) != 0 ) )
    ДатаНачала = ДатаНачала + 1;
  end;

  if( ДатаНачала > ДатаКонца )
     return FALSE;
  else
     ПолучитьСтрокуЗначенияКурса( _Курс, 1, Значение ) == 0;
[########### ####### ######### ########## ########## ########    ######         ]
( Значение, _Курс.Scale, _Курс.IsInverse, _Курс.SinceDate,
  _Курс.InputDate, _Курс.InputTime, _Курс.Oper );

  end;

  контр = ( СледующееЗначениеКурса( _Курс ) == 0 ) and
          ( _Курс.SinceDate <= ДатаКонца );
  while( контр )
     ПолучитьСтрокуЗначенияКурса( _Курс, 1, Значение ) == 0;

[########### ####### ######### ########## ########## ########    ######         ]
( Значение, _Курс.Scale, _Курс.IsInverse, _Курс.SinceDate,
  _Курс.InputDate, _Курс.InputTime, _Курс.Oper );

     контр = ( СледующееЗначениеКурса( _Курс ) == 0 ) and
             ( _Курс.SinceDate <= ДатаКонца );
  end;

[                                                                              ];
[                                                                              ];
[         Бухгалтер _________________   / ####################/                ]
(ИмяОперациониста({oper}));

  return TRUE;
END;
