/*
 * Общие функции для отчетов процедур урегулирования
 */
Import FIInter, PTInter;

/* Получить полное наименование узла ТС */
macro GetDprtString( DprtID : integer )

  var DprtString : string;

  record party("party.dbt");
  file dp_dep("dp_dep.dbt") key 0;

  DprtString = "";

  dp_dep.Code = DprtID;

  if( GetEQ(dp_dep) )
    if( ПолучитьСубъекта(dp_dep.PartyID, party) == 0 )
      DprtString = party.Name;
    end;
  end;

  return DprtString;

end;

/* Получить наименование главы */
macro GetChapterString( Chapter : integer ) : string

  var ChapterString : string;
  file obchaptr("obchaptr.dbt") key 0;

  ChapterString = string(Chapter);

  obchaptr.Chapter = Chapter;
  if( GetEQ(obchaptr) )
    ChapterString = ChapterString + " " + obchaptr.Name;
  end;

  return ChapterString;

end;

/* Получить код и наименование ФИ */
macro GetCurrencyString( Currency : integer ) : string

  var CurrencyString : string;

  CurrencyString = GetFICode( Currency );

  CurrencyString = CurrencyString + " ";
  CurrencyString = CurrencyString + GetFIName( Currency );

  return CurrencyString;

end;

/* Получить должности операциониста */
macro GetOperString( Oper : integer, PostString : @string ) : string
  
  var rs : RsdRecordset;
  var Post     : string;
  var OperName : string;

  file person("person.dbt") key 0;

  OperName   = "";
  PostString = "";

  person.Oper = Oper;
  if( GetEQ(person) )
    
    OperName = person.Name;

    if( person.PartyID > 0 )
    
      rs = RsdRecordset( "select t_Post from dofficer_dbt where t_PersonID = " + person.PartyID );

      while( rs.MoveNext() )
       
        Post = Trim(rs.value(0));
        if( (Post != "") and (Post != StrFor(1)) )
         
          if( PostString != "" ) PostString = PostString + ", "; end;
         
          PostString = PostString + Post;
       
        end;
     
     end;
    
    end;

  end;

  return OperName;

end;
