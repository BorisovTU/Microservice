/*
    Информирование о необработанных документах
    Контроль начальных и ответных платежей

            Mikhailov S., 23.08.2001, Th

    
*/

import cbinfcom;


private CONST SET_CHAR   = "X";
private CONST UNSET_CHAR = "";


private const PAYM_STR = "Платежей со статусом ";

private const I_STAT = 0; /*Paymstatus*/
private const I_NAME = 1; /*Name*/
private const I_NUM = 2; /*Num*/
private const I_SUM = 3; /*sum*/

private const I_ALL = 4;/*всего*/



/*
    Заполнить массив статусов только статусами свойств
    Только из-за того, что там номера огромные,
    а самих статусов немного. Памяти не хватит
*/

macro InitArray( statArrayClass )
    file pmstatus( pmstatus ) key 0;
    var index = 0;
    rewind( pmstatus );

    while( next( pmstatus ) )
        if( pmstatus.Type == PAYMSTATUS_MAIN /*PAYMSTATUS_PROP */)
            statArrayClass[index] = TArray(I_ALL);
            statArrayClass[index][I_STAT] = pmstatus.PaymStatus;
            statArrayClass[index][I_NAME] = pmstatus.ShortName;
            statArrayClass[index][I_NUM] = int( 0 );
            statArrayClass[index][I_SUM] = Money( 0 );

            index = index + 1;
        end;
    end;/*while*/

    return CBINF_StatusOk;
end;/*FillArrayStat*/

/*
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
0 Paymstatus
1 Name
2 Num
3 sum
*/
macro AddSumByStat(  statusArrayClass, PaymStatus, Sum, OutType )
    var index = 0;

    CBINF_Result  =1;

    while( index < StatusArrayClass.Size )
        if( StatusArrayClass[index][I_STAT] == PaymStatus )
            StatusArrayClass[index][I_NUM] = StatusArrayClass[index][I_NUM] +1;
            StatusArrayClass[index][I_SUM] = StatusArrayClass[index][I_SUM] + Sum;

            return CBINF_StatusOk;
        end;

        index = index + 1;
    end;

    CBINF_Error( "Не найден статус платежа " + PaymStatus, OutType );
    return CBINF_StatusError;/**/
end;/*GetStatNumByPaymStatus*/



/*
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
    Доступ оперов????????
*/
macro CBINF_Proc( RegPath, OutType/*CBINF_Out...*/ )
    var this_stat, nmbRec = 0;

    var MsgOutFileName ;
    file MsgOutFile()txt;
    
    file pmprop( pmprop ) key 1;
    file pmpaym( pmpaym ) key 0;
/*    file pmstatus( pmstatus ) key 0;*/

    var index;
    
/*  Исходящий */
    var OutPaymNum  = int( 0 ), OutPaymSum = Money( 0 );

/*  Входящий */
    var InPaymNum  = int( 0 ) , InPaymSum = Money( 0 );


    var Inpaym  = tarray;
    var OutPaym = Tarray;

    CBINF_Result = 0;
    CBINF_ErrorStatus = 0;

    this_stat = InitArray( InPaym ) ;
    if( this_stat )
        return this_stat;
    end;

    this_stat = InitArray( OutPaym ) ;
    if( this_stat )
        return this_stat;
    end;
    
/*msgbox("OutPaym size = ", OutPaym.Size );*/

    rewind ( pmprop );
    
        InitProgress( NRecords(pmprop), "Информирование о необработанных документах", "Контроль начальных и ответных платежей" );
        
/*Заполняем массив*/
    while( next( pmprop ) )
    
            nmbRec = nmbRec + 1;
            UseProgress( nmbRec );
            
        pmpaym.PaymentID = pmprop.PaymentID;

        if( not GetEQ( pmpaym ) )
            CBINF_Error( "Не найден платеж " + pmprop.PaymentID, OutType );
            RemProgress( nmbRec );
            return CBINF_StatusError;
        end;

/*Исходящие*/
        if( (pmprop.IsSender == UNSET_CHAR )
            and
/*  Только такие статусы ?? extpaym.mac см. InitArray!*/
            ( (pmpaym.PaymStatus == PM_READIED ) or
              (pmpaym.PaymStatus == PM_READY_TO_SEND ) or
              (pmpaym.PaymStatus == PM_IS_SENDING ) or
              (pmpaym.PaymStatus == PM_FINISHED ) ) )

            this_stat = AddSumByStat( outPaym, pmpaym.PaymStatus/*pmprop.PropStatus*/, pmpaym.Amount, outType );  
            if( this_stat )
                RemProgress( nmbRec );
                return this_stat;
            end;
            
        elif( (pmprop.IsSender == SET_CHAR )
              and
/*  Только такие статусы ?? extpaym.mac см. InitArray!*/
            ( (pmpaym.PaymStatus == PM_IS_RECEIVING ) or
              (pmpaym.PaymStatus == PM_KVITWAIT ) or
              (pmpaym.PaymStatus == PM_FINISHED ) ) )
              
            this_stat = AddSumByStat( InPaym, pmpaym.PaymStatus/*pmprop.PropStatus*/, pmpaym.Amount, outType );  

            if( this_stat )
                RemProgress( nmbRec );
                return this_stat;
            end;
            
        end;
        
    end;/*while*/


    RemProgress( nmbRec );
    
    if( ( OutType == CBINF_OutReport ) or ( OutType == CBINF_OutMsg ) )
[┌───────────────────────Незавершенные платежи───────────────────────────┐];
/*
[││#################### "#################" ######### ##################││]
*/
    end;

/*Распечатать*/

/*    заголовок исх*/
    if( ( OutType == CBINF_OutReport ) or ( OutType == CBINF_OutMsg ) )
[│                                                                       │];
[│┌──────────────────────────Исходящие──────────────────────────────────┐│];
[││ Статус                                 Количество        Сумма      ││];
[│├─────────────────────────────────────────────────────────────────────┤│];
/*
[││#################### "#################" ######### ##################││]
*/
    end;

    index = 0;
    while( index < OutPaym.Size )
        OutPaymNum = OutPaymNum + OutPaym[index][I_NUM];
        OutPaymSum = OutPaymSum + OutPaym[index][I_SUM];

        if( OutPaym[index][I_NUM] )
    
            if(( OutType == CBINF_OutReport )or ( OutType == CBINF_OutMsg  ))
[││#################### "#################" ######### ##################││]
(PAYM_STR, OutPaym[index][I_NAME],OutPaym[index][I_NUM], OutPaym[index][I_SUM]);
            end;
    
        end;
        
        index = index + 1;
    end;

/*    оконч исх*/
    if(( OutType == CBINF_OutReport )or( OutType == CBINF_OutMsg  ))
[│├─────────────────────────────────────────────────────────────────────┤│];
[││Итого начальных платежей                 ######### ##################││]
( OutPaymNum, OutPaymSum  );
[│└─────────────────────────────────────────────────────────────────────┘│];
/*
[││#################### "#################" ######### ##################││]
*/
    end;



/*    заголовок вх*/
    if( ( OutType == CBINF_OutReport )or( OutType == CBINF_OutMsg  ))
[│                                                                       │];
[│┌─────────────────────────────Входящие────────────────────────────────┐│];
[││ Статус                                 Количество        Сумма      ││];
[│├─────────────────────────────────────────────────────────────────────┤│];
/*
[││#################### "#################" ######### ##################││]
*/
    end;

    index = 0;
    while( index < InPaym.Size )
        InPaymNum = InPaymNum + InPaym[index][I_NUM];
        InPaymSum = InPaymSum + InPaym[index][I_SUM];

        if( InPaym[index][I_NUM] )
    
            if(( OutType == CBINF_OutReport) or( OutType == CBINF_OutMsg  ))
[││#################### "#################" ######### ##################││]
(PAYM_STR, OutPaym[index][I_NAME], InPaym[index][I_NUM], InPaym[index][I_SUM]);
/*
[││#################### "#################" ######### ##################││]
*/
            end;
    
        end;
        
        index = index + 1;

    end;/*while*/

/*    оконч вх*/
    if(( OutType == CBINF_OutReport )or( OutType == CBINF_OutMsg  ))
[│├─────────────────────────────────────────────────────────────────────┤│];
[││ Итого ответных платежей                 ######### ##################││]
( InPaymNum, InPaymSum  );
[│└─────────────────────────────────────────────────────────────────────┘│];
/*
[││#################### "#################" ######### ##################││]
*/
    end;



/*Оконч*/
    if( (OutType == CBINF_OutReport )or( OutType == CBINF_OutMsg  ))
[];    
[└───────────────────────────────────────────────────────────────────────┘];
/*
[││#################### "#################" ######### ##################││]
*/
    end;

/*Насильно показать файл вывода, т.к. PlayRep для CBINF_OutMsg не показывает!*/

    if( OutType == CBINF_OutMsg )
        MsgOutFileName = SetOutPut( GetTxtFileName("cbinftmp") );/* Перенаправить вывод, чтобы можно было юзать ральный файл вывода*/
        if(not open( MsgOutFile, MsgOutFileName ) )
            CBINF_Error( "Ошибка при открытии файла " + MsgOutFileName, OutType );
            return CBINF_StatusError;
        end;
/*        ViewFile( MsgOutFile ); 
-- MS 30-10-2001, Tue. Новое ТЗ. Перекачиваем файл в системный буфер */
        rewind( MsgOutFile );
        while( next( MsgOutFile ))
            CBINF_Message( MsgOutFile.Str );
        end;
        
        close( MsgOutFile );
        SetOutPut( null );
        
    end;/*OutType == CBINF_OutMsg */
    
    return CBINF_StatusOk;
    
end;/*CBINF_Proc*/

/*test call
const REGPATH = "COMMON\\CBINFPARMS";
var ret;

ret = CBINF_Proc( REGPATH, CBINF_OutReport );

msgbox( "test : stat =  ", ret , " CBINF_Result = ", CBINF_Result );

*/
