/*
  $Name:         pm064_10.mac
  $Module:       РКО
  $Description:  Исполнение поручения на списание
*/

//-----------------------------------------------------------------------------
// Блок      : 29064 - "Исполнение поручения на списание"
// Шаг       : 10    - "Исполнение поручения на списание"
// Описание  : Макрос шага
//-----------------------------------------------------------------------------

import PaymInter, BankInter, PSInter, CTInter, OprInter, "wltools.mac", cbsttls, pm_common;

var PaymentObj:RsbPayment;

// ----------------------------------------------------------------------------
// Создать рублевый клиентский платеж
// ----------------------------------------------------------------------------
private macro CreatePSPayOrder():object

  var PSPayOrderObj :RsbPSPayOrder = RsbPSPayOrder();

  PSPayOrderObj.DocKind           = PSPOKIND_REQUEST;
  PSPayOrderObj.Kind_Operation    = 0;
  PSPayOrderObj.CurrentState      = PSPO_ST_DEFERRED;
  PSPayOrderObj.Oper              = {Oper};
  PSPayOrderObj.Origin            = PSPO_OR_FNS; 
  /*Автозапуск операции*/
  PSPayOrderObj.LaunchOper =  true;
  /*И пусть он идет на контроль*/
  PSPayOrderObj.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
  return PSPayOrderObj.Payment;
end;

// ----------------------------------------------------------------------------
// Создать ИПВС
// ----------------------------------------------------------------------------

private macro ToAutoDate( str )
  if (StrIsNumber(substr(str, 1, 4)))
    return date( int(substr(str, 9, 2)), 
               int(substr(str, 6, 2)), 
               int(substr(str, 1, 4)) 
             );
  else
    return date( int(substr(str, 1, 2)), 
               int(substr(str, 4, 2)), 
               int(substr(str, 7, 4)) 
             );
  end;
end;

private macro CreateRequestOrder():object

  var RequestOrderObj :RsbRequestOrder = RsbRequestOrder();

  RequestOrderObj.KindOperation     = 0;
  RequestOrderObj.State             = PSINRQ_ST_DEFERRED;
  RequestOrderObj.Oper              = {Oper};
  RequestOrderObj.Origin            = CP_OR_FNS; //PSINRQ_OR_FNS; Для ИПВС использовать теперь константы из CP_ORIGIN
  RequestOrderObj.PSBCNumber = GetFldValue( PaymentObj.DocumentID, "НомПорВал"  );
  RequestOrderObj.PSBCDate   = ToAutoDate( GetFldValue( PaymentObj.DocumentID, "ДатаПорВал" ) );
  /*Автозапуск операции*/
  RequestOrderObj.LaunchOper =  true;
  /*И пусть он идет на контроль*/
  RequestOrderObj.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
  return RequestOrderObj.Payment;
end;

// ----------------------------------------------------------------------------
// Создать рублевый банковский платеж
// ----------------------------------------------------------------------------
private macro CreateBankPayment():object

  var BankPaymentObj :RsbBankPayment = RsbBankPayment();

  BankPaymentObj.Kind_Operation    = 0;
  BankPaymentObj.Status            = 1/*MEMORDER_STATUS_POST*/;
  BankPaymentObj.Oper              = {Oper};
  BankPaymentObj.Origin            = MEMORDER_FDOC_FNS;
  /*Автозапуск операции*/
  BankPaymentObj.LaunchOper =  true;
  /*И пусть он идет на контроль*/
  BankPaymentObj.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
  return BankPaymentObj.Payment;
end;

// ----------------------------------------------------------------------------
// Создать валютный банковский платеж
// ----------------------------------------------------------------------------
private macro CreateBBCPOrder():object

  var BbCpOrderObj :RsbBbCpOrder = RsbBbCpOrder();

  BbCpOrderObj.Kind_Operation    = 0;
  BbCpOrderObj.CurrentState      = 0/*CP_ST_DEFERRED*/;
  BbCpOrderObj.Oper              = {Oper};
  BbCpOrderObj.Origin            = CP_OR_FNS;
  /*Автозапуск операции*/
  BbCpOrderObj.LaunchOper =  true;
  /*И пусть он идет на контроль*/
  BbCpOrderObj.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
  return BbCpOrderObj.Payment;
end;

// ----------------------------------------------------------------------------
// Выполнение шага
// ----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )

  var stat = 0;
  var NewPaymentObj:RsbPayment;
  var AccRec:TRecHandler = TRecHandler( "account.dbt" );
  array FlgRM;
  var i = 0;
  /* Создать дочерний документ */
  if( PM_GetAccountRecord( PaymentObj.PayerAccount, PaymentObj.PayerFIID, PaymentObj.Chapter, AccRec ) )
    if( PaymentObj.PayerFIID == 0/*NATCUR*/ )
      if( Index( AccRec.rec.Type_Account, "К" ) == 0 )
        NewPaymentObj = CreatePSPayOrder();
      else
        NewPaymentObj = CreateBankPayment();
      end;
    else
      if( Index( AccRec.rec.Type_Account, "К" ) == 0 )
        NewPaymentObj = CreateRequestOrder();
      else
        NewPaymentObj = CreateBBCPOrder();
      end;
    end;
    /* Заполнить общие поля платежа */
    NewPaymentObj.ValueDate            = PM_GetDefaultValueDate(NewPaymentObj.Department, NewPaymentObj.PrimDocKind);
    NewPaymentObj.PayDate              = NewPaymentObj.ValueDate;
    NewPaymentObj.PayType = BPT_TAX;

    NewPaymentObj.Number               = PaymentObj.Number;
    NewPaymentObj.PaymStatus           = PM_PREPARING;
    NewPaymentObj.Date                 = PaymentObj.Date;
    NewPaymentObj.PayerBankEnterDate   = PaymentObj.PayerBankEnterDate;
    NewPaymentObj.PrimDocKind          = WL_INDOC;
    NewPaymentObj.Oper                 = {Oper};
    NewPaymentObj.BaseAmount           = PaymentObj.BaseAmount;
    NewPaymentObj.BaseFIID             = PaymentObj.BaseFIID;
    NewPaymentObj.PayerAmount          = PaymentObj.PayerAmount;
    NewPaymentObj.PayerFIID            = PaymentObj.PayerFIID;
    NewPaymentObj.PaymentKind          = PaymentObj.PaymentKind;
    NewPaymentObj.ShifrOper            = PaymentObj.ShifrOper;
    NewPaymentObj.Priority             = PaymentObj.Priority;
    NewPaymentObj.Ground               = PaymentObj.Ground;
    NewPaymentObj.TaxAuthorState       = PaymentObj.TaxAuthorState;
    NewPaymentObj.UIN                  = PaymentObj.UIN;
    NewPaymentObj.BTTTICode            = PaymentObj.BTTTICode;
    NewPaymentObj.OKATOCode            = PaymentObj.OKATOCode;
    NewPaymentObj.TaxPmGround          = PaymentObj.TaxPmGround;
    NewPaymentObj.TaxPmPeriod          = PaymentObj.TaxPmPeriod;
    NewPaymentObj.TaxPmNumber          = PaymentObj.TaxPmNumber;
    NewPaymentObj.TaxPmDate            = PaymentObj.TaxPmDate;
    NewPaymentObj.TaxPmType            = PaymentObj.TaxPmType;
    /* Скопировать ПИ по дебету */
    NewPaymentObj.SetPayerPI( PaymentObj.PayerGroup,                                /*Group*/
                              PaymentObj.PayerBankID,                               /*BankID*/
                              PaymentObj.PayerBankCodeKind,                         /*BankCodeKind*/
                              PaymentObj.PayerBankCode,                             /*BankCode*/
                              PaymentObj.PayerBankName,                             /*BankName*/
                              "",                                                   /*CorrAcc*/
                              PaymentObj.PayerFIID,                                 /*FIID*/
                              PaymentObj.Chapter,                                   /*Chapter*/
                              PaymentObj.PayerAccount,                              /*Account*/
                              PaymentObj.Payer,                                     /*Client*/
                              PaymentObj.PayerName,                                 /*ClientName*/
                              PaymentObj.PayerINN,                                  /*ClientINN*/
                              PaymentObj.PayerCodeKind,                             /*ClientCodeKind*/
                              PaymentObj.PayerCode,                                 /*ClientCode*/
                              -1,                                                   /*Corschem*/
                              0  );                                                 /*CorrPosType*/
    /* Скопировать ПИ по кредиту */
    NewPaymentObj.SetReceiverPI( PaymentObj.ReceiverGroup,                           /*Group*/               
                                 PaymentObj.ReceiverBankID,                          /*BankID*/              
                                 PaymentObj.ReceiverBankCodeKind,                    /*BankCodeKind*/        
                                 PaymentObj.ReceiverBankCode,                        /*BankCode*/            
                                 PaymentObj.ReceiverBankName,                        /*BankName*/            
                                 PaymentObj.ReceiverBankCorrAcc,                     /*CorrAcc*/             
                                 PaymentObj.ReceiverFIID,                            /*FIID*/                
                                 PaymentObj.Chapter,                                 /*Chapter*/             
                                 PaymentObj.ReceiverAccount,                         /*Account*/             
                                 PaymentObj.Receiver,                                /*Client*/              
                                 PaymentObj.ReceiverName,                            /*ClientName*/          
                                 PaymentObj.ReceiverINN,                             /*ClientINN*/           
                                 PaymentObj.ReceiverCodeKind,                        /*ClientCodeKind*/      
                                 PaymentObj.ReceiverCode,                            /*ClientCode*/
                                 -1,                                                 /*Corschem*/
                                 0  );                                               /*CorrPosType*/

    /* Актуализировать новый платеж */
    if( NewPaymentObj.Actuate() )
      msgbox("Ошибка при актуализации платежа");
      return 1;
    end;

    /* Вывести панель созданного платежа на экран */
    if(not IsOprMultiExec())
      while( i < PNRMPM_COUNT )
        FlgRM[i] = 1;
        i = i + 1;
      end;
      stat = PM_ProcessPanel( NewPaymentObj, 1, NULL, FlgRM, NULL, NULL, NULL, NULL, true );
      if( stat )
        if( stat == 4704 )
          msgbox( "Выполнение процедуры прервано пользователем" );
        else
          msgbox( "Ошибка при создании панели редактирования платежа" );
        end;
        return 1;
      end;
    end;

    /* Связать созданный платеж с обрабатываемым по виду связи "Исполнение поручения"*/ 
    if( PaymentObj.LinkPayment( NewPaymentObj, PMLINK_KIND_EXECORDER ) )
      msgbox(GetErrMsg());
      return 1;
    end;
  end;
  return 0;
end;                                                                                         
