/*
$Name:        ws_gmp_sendpayment.mac
$Module:      Web-общее
$Description: Макрос для вызова сервиса "Получение сведений о наличии решений
              о приостановлении операций по счетам" RS-Connect
*/

import XmlRpcInter;
import rcw;

private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");

macro javaToRslTime(javaDateTime)
    if (javaDateTime == null) return null; end;
    return time(javaDateTime.hour, javaDateTime.minute, javaDateTime.second);
end;

macro javaToRslDate(javaDateTime)
    if (javaDateTime == null) return null; end;
    return date(javaDateTime.day, javaDateTime.month, javaDateTime.year);
end;

macro javaToRslDateTime(javaDateTime)
    if (javaDateTime == null) return null; end;
    return dttm(javaToRslDate(javaDateTime), javaToRslTime(javaDateTime));
end;

macro getXMLGregorianCalendarDate(_date)
    if (_date == null) return null; end;

    var year = 0,
        month = 0,
        day = 0;

    DateSplit(_date, day, month, year);
    var _paymentDate = jvm.createJavaObject("com.sun.org.apache.xerces.internal.jaxp.datatype.XMLGregorianCalendarImpl", "<init>");
    _paymentDate.year = year;
    _paymentDate.month = month;
    _paymentDate.day = day;
    return _paymentDate;
end;

/*
 * Метод для конвертации типа Money к строке для конвертации в BigInteger
 * ******** !!!!! ДРОБНАЯ ЧАСТЬ ОТБРАСЫВАЕТСЯ !!!!! ******** *
*/
private macro toBigIntegerStr(value)
    return String(value : 0 : 0);
end;


/*
    GMPPaymentSendRequestType
*/

class AccDocType
    var accDocNo: String = null;
    var accDocDate: Date;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.AccDocType", "<init>");
        if (accDocNo != null)
            javaObj.accDocNo = accDocNo;
        end;
        javaObj.accDocDate = getXMLGregorianCalendarDate(accDocDate);
        return javaObj;
    end;
end;

class AdditionalDataType
    var name: String = "";
    var value: String = "";

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.AdditionalDataType", "<init>");
        javaObj.name = name;
        javaObj.value = value;
        return javaObj;
    end;
end;

class BankType
    var name: String = null;
    var bik: String = "";
    var correspondentBankAccount: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.BankType", "<init>");
        if (name != null)
            javaObj.name = name;
        end;
        javaObj.bik = bik;
        if (correspondentBankAccount != null)
            javaObj.correspondentBankAccount = correspondentBankAccount;
        end;
        return javaObj;
    end;
end;

class BankWithDepType
    var name: String = null;
    var bik: String = "";
    var department: String = "";
    var correspondentBankAccount: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.BankWithDepType", "<init>");
        if (name != null)
            javaObj.name = name;
        end;
        javaObj.bik = bik;
        javaObj.department = department;
        if (correspondentBankAccount != null)
            javaObj.correspondentBankAccount = correspondentBankAccount;
        end;
        return javaObj;
    end;
end;

class BudgetIndexType
    var status: String = "";
    var paytReason: String = "";
    var taxPeriod: String = "";
    var taxDocNumber: String = "";
    var taxDocDate: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.BudgetIndexType", "<init>");
        javaObj.status = status;
        javaObj.paytReason = paytReason;
        javaObj.taxPeriod = taxPeriod;
        javaObj.taxDocNumber = taxDocNumber;
        if (taxDocDate != null)
            javaObj.taxDocDate = taxDocDate;
            // javaObj.taxDocDate = getXMLGregorianCalendarDate(taxDocDate);
        end;
        return javaObj;
    end;
end;

class ChangeStatusType
    var reason: String = null;
    var meaning: String = "";

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.ChangeStatusType", "<init>");
        if (reason != null)
            javaObj.reason = reason;
        end;
        javaObj.meaning = meaning;
        return javaObj;
    end;
end;

class PayeeType
    var bank: BankType;
    var payeeAccount: String = "";
    var payeeName: String = "";
    var payeeINN: String = "";
    var payeeKPP: String = "";
    var payeeOGRN: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.PayeeType", "<init>");
        javaObj.bank = bank.toJava();
        javaObj.payeeAccount = payeeAccount;
        javaObj.payeeName = payeeName;
        javaObj.payeeINN = payeeINN;
        javaObj.payeeKPP = payeeKPP;
        if (payeeOGRN != null)
            javaObj.payeeOGRN = payeeOGRN;
        end;
        return javaObj;
    end;
end;

class PayerType
    var payerIdentifier: String = "";
    var payerName: String = null;
    var payerAccount: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.PayerType", "<init>");
        javaObj.payerIdentifier = payerIdentifier;
        if (payerName != null)
            javaObj.payerName = payerName;
        end;
        if (payerAccount != null)
            javaObj.payerAccount = payerAccount;
        end;
        return javaObj;
    end;
end;

class PaymentOrgType
    var bank = null;
    var ufk: String = null;
    var other: String = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.PaymentOrgType", "<init>");
        if (bank != null)
            javaObj.bank = bank.toJava();
        end;
        if (ufk != null)
            javaObj.ufk = ufk;
        end;
        if (other != null)
            javaObj.other = other;
        end;
        return javaObj;
    end;
end;

class PartialPaytType

    var accDoc = null;
    var transKind: String = null;
    var paytNo: String = null;
    var transContent: String = "";
    var sumResidualPayt = null; // type: Money

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.PartialPaytType", "<init>");
        javaObj.accDoc = accDoc.toJava();
        javaObj.transKind = transKind;

        if (paytNo != null)
            javaObj.paytNo = paytNo;
        end;
        if (transContent != null)
            javaObj.transContent = transContent;
        end;
        if (sumResidualPayt != null)
            javaObj.sumResidualPayt = jvm.createJavaObject("java.math.BigInteger", "<init>@(Ljava/lang/String;)V", toBigIntegerStr(sumResidualPayt));
        end;
        return javaObj;
    end;
end;

class ImportedPaymentType

    var documentID: String = "";
    var paymentID: String = null;
    var changeStatus; // type: changeStatusType
    var supplierBillID: String = null;
    var purpose: String = "";
    var amount; // type: Money
    var paymentDate: Date;
    var receiptDate: Date = null;
    var KBK: String = null;
    var OKTMO: String = null;
    var deliveryDate: Date = null;
    var transKind: String = null;
    var paymentOrg;
    var payer = null;
    var payee;
    var budgetIndex = null;
    var accDoc;
    var partialPayt = null;
    var additionalData = null; // type: TArray()
    var ESIAID = null;
    var noticeID = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.ImportedPaymentType", "<init>");
        javaObj.documentID = documentID;
        if (paymentID != null)        
            javaObj.paymentID = paymentID;
        end;
        javaObj.changeStatus = changeStatus.toJava();
        if (supplierBillID != null)
            javaObj.supplierBillID = supplierBillID;
        end;

        var _paymentDate = jvm.createJavaObject("com.sun.org.apache.xerces.internal.jaxp.datatype.XMLGregorianCalendarImpl", "<init>");

        javaObj.purpose = purpose;
        javaObj.amount = jvm.createJavaObject("java.math.BigInteger", "<init>@(Ljava/lang/String;)V", toBigIntegerStr(amount)); // amount;
        javaObj.paymentDate = getXMLGregorianCalendarDate(paymentDate);
        if (receiptDate != null)
            javaObj.receiptDate = getXMLGregorianCalendarDate(receiptDate);
        end;
        if (kbk != null)
            javaObj.KBK = kbk;
        end;
        if (oktmo != null)
            javaObj.OKTMO = oktmo;
        end;
        if (deliveryDate != null)
            javaObj.deliveryDate = getXMLGregorianCalendarDate(deliveryDate);
        end;
        if (transKind != null)
            javaObj.transKind = transKind;
        end;
        javaObj.paymentOrg = paymentOrg.toJava();
        if (payer != null)
            javaObj.payer = payer.toJava();
        end;
        javaObj.payee = payee.toJava();
        if (budgetIndex != null)
            javaObj.budgetIndex = budgetIndex.toJava();
        end;
        javaObj.accDoc = accDoc.toJava();
        if (partialPayt != null)
            javaObj.partialPayt = partialPayt.toJava();
        end;
        if (additionalData != null)
            // javaObj.additionalData вызывает конструктор java.util.LinkedList
            var jObj = javaObj.additionalData;
            for (var item, additionalData)
                if (item != null)
                    javaObj.additionalData.add(item.toJava());
                end;
            end;
        end;
        if (esiaid != null)
            javaObj.ESIAID = esiaid;
        end;
        if (noticeID != null)
            javaObj.noticeID = noticeID;
        end;
        return javaObj;
    end;

end;

class RequestHeaderType
    var reqID: String = "";
    var senderID: String = "";

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.RequestHeaderType", "<init>");
        javaObj.reqID = reqID;
        javaObj.senderID = senderID;
        return javaObj;
    end;
end;

class PaymentsPackageType
    var importedPayment = TArray();

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.PaymentsPackageType", "<init>");
        // javaObj.importedPayment = jvm.createJavaObject("java.util.LinkedList", "<init>");
        for (var i, 0, importedPayment.Size() - 1, 1)
            if (importedPayment[i] != null)
                javaObj.importedPayment.add(importedPayment[i].toJava());
            end;
        end;
        return javaObj;
    end;

end;

class RequestDataType
    var paymentsPackage;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.SendRequestDataType", "<init>");
        javaObj.paymentsPackage = paymentsPackage.toJava();
        return javaObj;
    end;
end;

class GMPPaymentSendRequestType
    var requestHeader;
    var requestData;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.GMPPaymentSendRequestType", "<init>");
        javaObj.requestHeader = requestHeader.toJava();
        javaObj.requestData = requestData.toJava();
        return javaObj;
    end;
end;

/*
    * GmpSendPaymentResponseType
*/

class ObjectErrorType
    var errorCode: String = "";
    var errorText: TArray = TArray();

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        errorCode = javaObj.errorCode;
        for(var i, 0, javaObj.errorText.size - 1, 1)
            errorText[errorText.Size] = javaObj.errorText.get(i);
        end;
        return this;
    end;

    macro toString()
        return "ObjectErrorType{"
            "errorCode: " + errorCode + ","
            "errorText: " + errorText + "}";
    end;
end;

class ObjectStatusType

    var documentId: String = "";
    var objectId = null;
    var paymentId: String = "";
    var objectError = null;
    var noticeID = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        documentId = javaObj.documentID;
        if (javaObj.objectID != null)
            objectId = javaObj.objectID;
        end;
        if (javaObj.paymentID != null)
            paymentId = javaObj.paymentID;
        end;
        if (javaObj.objectError != null)
            objectError = ObjectErrorType.fromJava(javaObj.objectError);
        end;
        if (javaObj.noticeID != null)
            noticeID = javaObj.noticeID;
        end;
        return this;
    end;

    macro toString()
        var result = "ObjectStatusType {documentId: " + documentId + ","
            "objectId: " + objectId + ","
            "paymentId: " + paymentId + "";
        if (objectError != null)
            result = result + "," + objectError.toString();
        end;
        return result + "}";
    end;

end;

class ObjectStatusListType
    var objectStatus;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        objectStatus = TArray();
        if (javaObj.objectStatus != null)
            for(var i, 0, javaObj.objectStatus.size - 1, 1)
                objectStatus[objectStatus.Size] = ObjectStatusType().fromJava(javaObj.objectStatus.get(i));
            end;
        end;
        return this;
    end;

    macro toString()
        if (objectStatus == null) return null; end;
        var result = "ObjectStatusListType {";
        for (var item, objectStatus)
            result = result + item.toString();
        end;
        return result + "}"
    end;
end;

class ResponseHeaderType
    var reqId: String = "";
    var senderID: String = "";

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        reqId = javaObj.reqID;
        senderID = javaObj.senderID;
        return this;
    end;

    macro toString()
        return "ResponseHeaderType{"
            "reqId: " + reqID + ","
            "senderId: " + senderID + "}";
    end;
end;

class ResponseConnectDataType
    var objectError = null;
    var objectStatusList = null;
    
    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        objectError = ObjectErrorType().fromJava(javaObj.objectError);
        objectStatusList = ObjectStatusListType().fromJava(javaObj.objectStatusList);
        return this;
    end;

    macro toString()
        var result = "ResponseConnectDataType{";
        if (objectError != null)
            result = result + "objectError: " + objectError.toString();
        end;
        if (objectStatusList != null)
            result = result + "objectStatusList: " + objectStatusList.toString();
        end;
        return result + "}";
    end;

end;

class GmpSendPaymentResponseType
    var responseHeader;
    var responseConnectData;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        responseHeader = ResponseHeaderType().fromJava(javaObj.responseHeader);
        responseConnectData = ResponseConnectDataType().fromJava(javaObj.responseConnectData);
        return this;
    end;

    macro toString()
        return "GmpSendPaymentResponseType{"
        "responseHeader: " + responseHeader + ","
        "responseConnectData: " + responseConnectData + "}";
    end;

end;


/*
    GMPPaymentGetAnswerRequestType
*/

class ObjectType
    var objectID = null;
    var noticeID = null;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.ObjectType", "<init>");
        if (objectID != null)
            javaObj.objectID = objectID;
        end;
        if (noticeID != null)
            javaObj.noticeID = noticeID;
        end;
        return javaObj;
    end;
end;

class ObjectIDListType
    var object = TArray(); // type: List<ObjectType>

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.ObjectIDListType", "<init>");
        if (object != null)
            // javaObj.object вызывает создание массива
            var jArr = javaObj.object; // = jvm.createJavaObject("java.util.ArrayList", "<init>");
            for (var item, object)
                javaObj.object.add(item.toJava());
            end;
        end;
        return javaObj;
    end;
end;

class GetAnswerRequestDataType

    var initReqID: String;
    var objectIDList: ObjectIDListType;

    macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.GetAnswerRequestDataType", "<init>");
        if (initReqID != null)
            javaObj.initReqID = initReqID;
        end;
        if (objectIDList != null)
            javaObj.objectIDList = objectIDList.toJava();
        end;
        return javaObj;
    end;

end;

class GMPPaymentGetAnswerRequestType

     var requestHeader: RequestHeaderType;
     var requestData: GetAnswerRequestDataType;

     macro toJava()
        var javaObj = jvm.createJavaObject("xsd.payments.gmp.GMPPaymentGetAnswerRequestType", "<init>");
        javaObj.requestHeader = requestHeader.toJava();
        javaObj.requestData = requestData.toJava();
        return javaObj;
     end;

end;


/*
 * GMPPaymentGetAnswerResponseType
*/

class GetAnswerResponseConnectDataType
    var objectError = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        objectError = ObjectErrorType().fromJava(javaObj.objectError);
        return this;
    end;
end;

class ExternalErrorType
    var externalSource: Integer;
    var externalCode: String;
    var externalText: String;

    macro fromJava(javaObj)
        if (javaObj == null) return javaObj; end;
        externalSource = javaObj.externalSource;
        externalCode = javaObj.externalCode;
        externalText = javaObj.externalText;
        return this;
    end;
end;

class StatusInfoType
    var documentID: String;
    var paymentID: String;
    var changeStatus: String;
    var statusCode: String;
    var statusText: String;
    var timeStamp: String;
    var objectError = null;
    var externalError = null;
    var objectID = null;
    var noticeID = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;

        objectID = javaObj.objectID;
        noticeID = javaObj.noticeID;
        documentID = javaObj.documentID;
        paymentID = javaObj.paymentID;
        changeStatus = javaObj.changeStatus;
        statusCode = javaObj.statusCode;
        statusText = javaObj.statusText;
        timeStamp = javaObj.timeStamp;
        objectError = ObjectErrorType().fromJava(javaObj.objectError);
        externalError = ExternalErrorType().fromJava(javaObj.externalError);
        return this;        
    end;
end;


class StatusInfoListType
    var statusInfo = null; // type: List<StatusInfoType>

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        if ((javaObj.statusInfo != null) AND (javaObj.statusInfo.size > 0))
            statusInfo = TArray();
            for (var i, 0, javaObj.statusInfo.size - 1, 1)
                statusInfo[statusInfo.size] = StatusInfoType().fromJava(javaObj.statusInfo.get(i));
            end;
        end;
        return this;
    end;
end;


class GetAnswerResponseContentType
    var statusInfoList = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
	    if ((javaObj != null) AND (javaObj.statusInfoList == null)) return null; end;
        statusInfoList = StatusInfoListType().fromJava(javaObj.statusInfoList);
        return this;
    end;
end;

class GetAnswerResponseServiceDataType
    var responseContent = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        responseContent = GetAnswerResponseContentType().fromJava(javaObj.responseContent);
        return this;
    end;
end;

class GMPPaymentGetAnswerResponseType

    var responseHeader: ResponseHeaderType;
    var responseConnectData = null;
    var responseServiceData = null;

    macro fromJava(javaObj)
        if (javaObj == null) return null; end;
        responseHeader = ResponseHeaderType().fromJava(javaObj.responseHeader);
        if (javaObj.responseConnectData != null) 
            responseConnectData = GetAnswerResponseConnectDataType().fromJava(javaObj.responseConnectData);
        end;
        if (javaObj.responseServiceData != null)
            responseServiceData = GetAnswerResponseServiceDataType().fromJava(javaObj.responseServiceData);
        end;
        return this;
    end;

end;


/*
    CALLING SERVICES
*/

macro callPaymentSendRequest(source, urlAddress)
    var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V", urlAddress);
    var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
    var port = proxy.getIntegrationWSPort();
    var result = port.wsGMPPaymentSendRequest(source.toJava());
    return GmpSendPaymentResponseType().fromJava(result);
end;

macro callPaymentGetAnswer(source, urlAddress)
    var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V", urlAddress);
    var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
    var port = proxy.getIntegrationWSPort();
    var result = port.wsGMPPaymentGetAnswer(source.toJava());
    return GMPPaymentGetAnswerResponseType().fromJava(result);
end;

/*
    TESTS
*/

private macro paymentSendRequestTest()


    var accDoc = AccDocType();
    accDoc.accDocNo = "123456";
    accDoc.accDocDate = date();

    var ppt = PartialPaytType();
    ppt.accDoc = accDoc;
    ppt.transKind = "2";
    ppt.paytNo = "null123";
    ppt.transContent = "231";
    ppt.sumResidualPayt = Money(123.3); // type: Money

    var jobj = ppt.toJava();

    var requestHeader = RequestHeaderType();
    requestHeader.reqId = SubStr(CreateGUID(), 2, 36); // "518e71c7-5f89-4179-aade-6888086f1006";
    requestHeader.senderId = "GMP";

    var changeStatus = ChangeStatusType();
    changeStatus.reason = "1231";
    changeStatus.meaning = "1";

    var bank = BankType();
    bank.name = "dsafa";
    bank.bik = "123456789";
    bank.correspondentBankAccount = "12345678901234567890";

    var bnk = BankWithDepType();
    bnk.name = "dsafa";
    bnk.bik = "123456789";
    bnk.department = "1234";
    bnk.correspondentBankAccount = "12345678901234567890";

    var paymentOrg = PaymentOrgType(); 
    paymentOrg.bank = bnk;

    var payee = PayeeType();
    payee.bank = bank;
    payee.payeeAccount = "12345678901234567890";
    payee.payeeName = "payeeName";
    payee.payeeINN = "1123456789";
    payee.payeeKPP = "112345666";
    payee.payeeOGRN = "1234567890123";

    var budget = BudgetIndexType();
    budget.status = "09";
    budget.paytReason = "ЗД";
    budget.taxPeriod = "12345678";
    budget.taxDocNumber = "1231";
    budget.taxDocDate = "01.01.2017";


    var importedPayment = ImportedPaymentType();
    importedPayment.documentID = "518e71c7-5f89-4179-aade-6888086f30b0";
    importedPayment.paymentID = "11234567892342340101201800000012";
    importedPayment.changeStatus = changeStatus;
    importedPayment.supplierBillID = "1111111111311111411111111";
    importedPayment.purpose = "purpose";
    importedPayment.amount = Money(100.1001); // type: Money
    importedPayment.paymentDate = date(1,1,2018);
    importedPayment.receiptDate = date();
    importedPayment.kbk = "0123456789abcdef1234";
    importedPayment.oktmo = "12345678";
    importedPayment.deliveryDate = Date();
    importedPayment.transKind = "01";
    importedPayment.paymentOrg = paymentOrg;
    importedPayment.payer = null;
    importedPayment.payee = payee;
    importedPayment.budgetIndex = budget;
    importedPayment.accDoc = accDoc;
    importedPayment.partialPayt = null;
    importedPayment.additionalData = null;
    importedPayment.esiaid = null;
    importedPayment.partialPayt = ppt;

    var paymentsPackage = PaymentsPackageType();
    paymentsPackage.importedPayment = TArray();
    paymentsPackage.importedPayment[0] = importedPayment;
    
    var requestData = RequestDataType();
    requestData.paymentsPackage = paymentsPackage;
    
    var request = GMPPaymentSendRequestType();
    request.requestHeader = requestHeader;
    request.requestData = requestData;


    var obj = callPaymentSendRequest(request, "http://localhost:8080/RsConnect/ws/integration");
	println(obj.toString());

end;

private macro paymentGetAnswerInitReqIdTest()

    var requestHeader = RequestHeaderType();
    requestHeader.reqId = "518e71c7-5f89-4179-aade-6888086f3100";
    requestHeader.senderId = "ESIA007";

    var requestData = GetAnswerRequestDataType();
    requestData.initReqID = "518e71c7-5f89-4179-aade-6888086f30b6";

    var request = GMPPaymentGetAnswerRequestType();
    request.requestHeader = requestHeader;
    request.requestData = requestData;
    var result = callPaymentGetAnswer(request, "http://localhost:8080/RsConnect/ws/integration");
	println(result);
end;

private macro paymentGetAnswerObjectsIdTest()

    var requestHeader = RequestHeaderType();
    requestHeader.reqId = SubStr(CreateGUID(), 2, 36); // "518e71c7-5f89-4179-aade-6888086f3100";
    requestHeader.senderId = "GMP";

    var requestData = GetAnswerRequestDataType();
    requestData.objectIDList = ObjectIDListType();
    var obj = ObjectType();
    obj.objectId = "16";
    requestData.objectIDList.object[0] = obj;
//    obj = ObjectType();
//    obj.objectId = "1252";
//    requestData.objectIDList.object[1] = obj;
//    obj = ObjectType();
//    obj.objectId = "32";
//    requestData.objectIDList.object[2] = obj;
//    obj = ObjectType();
//    obj.noticeId = "f56ef8d9-156d-471c-9a9d-5e122c56fbca";
//    requestData.objectIDList.object[3] = obj;
    // request

    // requestData.initReqID = "518e71c7-5f89-4179-aade-6888086f30b6";

    var request = GMPPaymentGetAnswerRequestType();
    request.requestHeader = requestHeader;
    request.requestData = requestData;
    var result = callPaymentGetAnswer(request, "http://localhost:8080/RsConnect/ws/integration");
	println(result);

end;

//  paymentSendRequestTest();
// paymentGetAnswerInitReqIdTest();

// paymentGetAnswerObjectsIdTest();
