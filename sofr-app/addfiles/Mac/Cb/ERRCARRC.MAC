/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : errcarrc.mac                                     07.07.1998
  Description : Отчет по ошибкам в валютных счетах
  Comment     : Пункт 1802 "Системного сервиса"

                Отчет выводтся на печать на лазерном принтере
                при установке опций печати "Ланшафт + Сжатие по горизонтали"
└───────────────────────────────────────────────────────────────────────────*/
import globals;

 file ec ( "errcarr$" ) key 0; /* файл ошибок валютных счетов баланс.учета */
 file oc ( "obchaptr" ) key 0;  /* файл глав счетов */

/* --------------- Печать заголовка отчета --------------- */
macro Title
[
 #  --------------------------------------------------------------------------------------------------------------------------------------------------  ##########

   ОТЧЕТ ПО ОШИБКАМ В ВАЛЮТНЫХ СЧЕТАХ

   Глава счетов: ##################################################

 ┌────┬─────────────────────────┬────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┐
 │Ста-│  Номер лицевого счета   │ Фин.инструмент │  Дебет за день  │  Кредит за день │ Остаток за день │   Планируемый   │  Дебет за месяц │ Кредит за месяц │      Остаток    │
 │тус │                         │                │                 │                 │                 │     остаток     │                 │                 │     за месяц    │
 │    │                         │                │                 │                 │                 │                 │                 │                 │                 │
 ├────┼─────────────────────────┼────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
 │  1 │           2             │       3        │        4        │        5        │        6        │        7        │        8        │        9        │        10       │
 ├────┼─────────────────────────┼────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
]({Name_Bank}:15, date, oc.Name );
end;

/* ----------------- Получить код фин.инструмента ---------------- */
macro CreateFICode()
  var КодФИ;
  file ФинИнстр("fininstr") key 0;
  ФинИнстр.FIID = ec.Code_Currency;
  if(GetEq(ФинИнстр))
    КодФИ = ФинИнстр.FI_Code;
  else
    КодФИ = "???";
  end;
  return КодФИ;
end;

/* ----------------- Печать строки отчета ---------------- */
macro Line
  var КодФИ;
  КодФИ = CreateFICode(КодФИ);
[│  # │#########################│################│#################│#################│#################│#################│#################│#################│#################│
]( ec.Stat, ec.Account, КодФИ:c, ec.Debet, ec.Kredit, ec.Rest, ec.PlanRest, ec.DebetM, ec.KreditM, ec.RestM );
end;

/* -------------------- Печать отчета -------------------- */
/*                                                         */
/*     Параметр процедуры chapter - номер главы счетов     */
/*                                                         */
macro Report ( chapter )
  /* Поиск названия главы счетов */
  oc.Chapter = chapter;
  if ( not getEQ( oc ) ) oc.Name = "???"; end;  /* Не нашли главу счетов */
  Title;  /* Печать заголовка */
  /* Цикл вывода всех записей по данной главе счетов */
  while ( next( ec ) )
     if ( chapter == ec.Chapter )
        Line;  /* Печать строки отчета */
     end;
  end;
end;
