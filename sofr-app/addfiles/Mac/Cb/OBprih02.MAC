/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*            Copyright (c) R-Style Software Lab 1999                   */
/*                                                                      */
/*  Имя файла   : Obprih02.mac                                          */
/*                                                                      */
/*  Описание    : Операция "Приход по внебалансу"                       */
/*                Шаг 2  "Проводки по внебалансу"                       */
/*                                                                      */
/*  Создан      : 05.04.1999                                            */
/*                                                                      */
/************************************************************************/
IMPORT InsCarryDoc, "ob_lib.mac";

/************************************************************************/
/* Процедура шага                                                       */
/************************************************************************/
MACRO ExecuteStep (CarryDoc, FirstDoc)
  
  var СчетАктивный : bool;
  var ВидСчета : string;
  var CorrespondentAccount : string;
  var accTrn : RsbAccTransaction;

  accTrn = RsbAccTransaction;

  /* Читаем данные из пользовательского файла первичных документов */
  SetBuff (uo_obcur, FirstDoc);

  ВидСчета = ВидЛСчета( Глава, uo_obcur.Code_Currency, uo_obcur.Account_Receiver );
  if( ВидСчета == 0 )
    MsgBox("Неверный номер лицевого счета");
  elif( ВидСчета == "А" )
    СчетАктивный = TRUE;
  else
    СчетАктивный = FALSE;
  end;

  CorrespondentAccount = OB_GetCorrespondentAccount( ВидСчета, uo_obcur.Date_Carry );
  if( CorrespondentAccount == "" )
    MsgBox( "Ошибка при определении счета корреспонденции" );
    return 1;
  end;

  /* Подготовка проводки */
  accTrn.Date_Carry    = uo_obcur.Date_Carry;
  accTrn.Chapter       = Глава;
  accTrn.Numb_Document = uo_obcur.Numb_Document;
  accTrn.Number_Pack   = uo_obcur.Number_Pack;
  accTrn.Ground        = uo_obcur.Ground;
  accTrn.Department    = {OperDprt};

  if( СчетАктивный )
    /* Приход по ПАССИВНОМУ внебалансу*/
    
    /*Кр*/
    accTrn.AccountPayer = uo_obcur.Account_Receiver;
    accTrn.FIIDPayer    = uo_obcur.Code_Currency;
    accTrn.SumPayer     = uo_obcur.Sum;

    /*Дб*/
    accTrn.AccountReceiver = CorrespondentAccount;
    accTrn.FIIDReceiver    = OB_КодВалютыРубли;
    
  else
    /* Приход по АКТИВНОМУ внебалансу*/
    
    /*Кр*/
    accTrn.AccountPayer = CorrespondentAccount;
    accTrn.FIIDPayer    = OB_КодВалютыРубли;
    
    /*Дб*/
    accTrn.AccountReceiver = uo_obcur.Account_Receiver;
    accTrn.FIIDReceiver    = uo_obcur.Code_Currency;
    accTrn.SumReceiver     = uo_obcur.Sum;
  
  end;

  if( not accTrn.Carry() )
    MsgBox( "Ошибка при вставке проводки" );
    return 1;
  end;

  return 0;

END;