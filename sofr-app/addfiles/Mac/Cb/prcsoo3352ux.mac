/*
$Name:             Prcsoo3352ux.mac
$Module:           ББ и РКО
$Description:      Печать расходного кассового ордера 3352-У (xls)
*/

import prcs, BnkTemplReport;
    
private class (BnkTemplReport)PrintOrderReport(PrintOrderData)

  var data = PrintOrderData;

  MACRO Create()
    SetSheetName( data.Number );

    array ArrayClient, ArrayBankName, ArrayGround, ArrayPaperName, ArraySum;
           
    StrSplit( data.PaperNameStr, ArrayPaperName, 80, 40, 3);
    StrSplit( data.Client, ArrayClient, 34, 28, 3);
    StrSplit( data.BankName, ArrayBankName, 47, 34, 2);

    /*Основание платежа*/
    StrSplit(data.Ground, ArrayGround, 85, 65, 2);
    StrSplit(data.SumString, ArraySum, 75, 65, 2);
  
    var isDebL = IfThenElse( data.DebitList.size() > 1, true, false );

    SetValues( "Number_0"          , "Отрывной талон к расходному кассовому ордеру №" + data.Number,
               "Number"            ,  data.Number           ,
               "DateStr  "         ,  data.DateStr          ,
               "ClientStr"         ,  ArrayClient(0)        ,
               "ClientStr_1"       ,  ArrayClient(1)        ,
               "ClientStr_2"       ,  ArrayClient(2)        ,

               "DebAcc"            ,  data.DebitList.value(0).Account   ,
               "FICode"            ,  data.DebitList.value(0).FICode    ,
               "Amount"            ,  data.DebitList.value(0).Amount );

    var index = 1;
    if( isDebL )
      var table = RegisterTable("DEBET");
      while( index < data.DebitList.size())
        table.SetValueCell( "DebAcc"        ,  data.DebitList.value(index).Account );
        table.SetValueCell( "FICode"        ,  data.DebitList.value(index).FICode  );
        table.SetValueCell( "Amount"        ,  data.DebitList.value(index).Amount  );
        table.AddStr();
        index = index + 1;
      end;
      table.EndTable();
    end;

    SetValues(
               "CredAcc"           ,  data.ReceiverAccount              , 
               "CFICode"           ,  data.ReceiverFICode               ,
               "CAmount"           ,  data.ReceiverAmountStr            ,
               "BankName"          ,  ArrayBankName(0)                  ,
               "BankName_1"        ,  ArrayBankName(1)                  ,
               "BIC"               ,  data.BIC                          ,
               "AmountStr"         ,  ArraySum(0)                       ,
               "AmountStr_1"       ,  ArraySum(1)                       ,
               "Symb"              ,  data.ArraySym.value(0)            ,
               "Symb_1"            ,  data.ArraySym.value(1)            ,
               "SAmount"           ,  MakeMoneyStr3352(data.ArrayPartSum.value(0)),
               "SAmount_1"         ,  MakeMoneyStr3352(data.ArrayPartSum.value(1)),
               "Symb_2"            ,  data.ArraySym.value(2),
               "SAmount_2"         ,  MakeMoneyStr3352(data.ArrayPartSum.value(2)),
               "Ground"            ,  ArrayGround(0)
             );
               
    var tableSymList = RegisterTable("SymList");
    index = 3;
    while( (index < data.ArraySym.size) and (data.ArraySym.value(index) != "") )
      tableSymList.SetValueCell("Symb_2Tmp",  data.ArraySym.value(index));  
      tableSymList.SetValueCell("SAmount_2Tmp", MakeMoneyStr3352(data.ArrayPartSum.value(index)));           
      if ( index == 3)
        tableSymList.SetValueCell("GroundTmp", ArrayGround(1));           
      end;
      tableSymList.AddStr();
      index = index + 1;
    end;
    
    if (index == 3)
      SetValues( "Ground_1"          ,  ArrayGround(1) );    
    end;
    
    tableSymList.EndTable();
               
    SetValues( "Sh"                ,  data.ShifrOper                           ,
               "PaperName"         ,  ArrayPaperName(0)                        ,
               "PaperName_1"       ,  ArrayPaperName(1)/*                      ,
               "NameAccount"       ,  "Бухгалтерский работник"                 ,
               "NameControl"       ,  "Контролирующий работник"                ,
               "NameCashier"       ,  "Кассовый работник"*/
             );

    return TRUE;
  END;

  initBnkTemplReport("DebitCashOrder3352-U.xls");
end; 

/* Буфер мультивалютного документа, чтобы печать не ругалась */
var pr_multydoc:TRecHandler = TRecHandler( "multydoc.dbt" );
/* Буфер мемордера, чтобы печать не ругалась */
var pr_cb_doc  :TRecHandler = TRecHandler( "cb_doc.dbt"   ); 

macro PrintDocument(ncopy:integer):bool

  var DocKind:integer = pr_pmpaym.rec.DocKind;
  var data : TOutOrderPrintData3352 = TOutOrderPrintData3352();

  if( ( DocKind == 420/*CASH_PS_OUTORDER*/ ) or ( DocKind == 440/*CASH_BOF_OUTORDER*/ ) )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pscshdoc );  
  elif( ( DocKind == CB_MULTYDOC ) or ( DocKind == DLDOC_MEMORIALORDER ) )
    data.InitByMemorialDocument( pr_pmpaym, pr_pmrmprop );
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;

  return PrintOrderReport(data, ncopy).print();

end;
