/*
 $Name: ws_clientident.mac
 $Module: Ядро ГКБО 
 $Description: Интерфейс "Выполнить идентификацию клиента"
 */
 /*
        Интерфейс "Выполнить идентификацию клиента"
 */
Import BankInter, CTInter, PTInter, FIInter, GateInter, "ws_lib_fun.mac", "globals.mac";
import rsd, oralib, likepy, "ws_client_lib.mac";

// Интерфейс "Выполнить идентификацию клиента" : Параметры запроса
class TClientIdent
    var id : string;
    var ReqID : string;
    var ReqFindClient;
end;

class RSCReqFindClientsListRequest 
    var ReqID : string;
    var SenderID : string;
    var ClientsList;
    var Senders;
end;

private class RSCReqFindClientsListResponse
    var reqId : string = "";
    var resultList/* : TArray = TArray*/;/*<ClientListResponseElement>*/
end;

private macro FillTWsClientIdent(oClientIdent, wsClientIdent : TClientIdent, insMode : bool)
    var ParamN     : integer;
    var ObjectName : string;
    var IsMandatory = insMode;
    ParamN     = 1;
    ObjectName = "";

    WS_SetAttributeValue(@wsClientIdent.ID, ParamN, ObjectName, "ID", oClientIdent, V_STRING, false, null);
    WS_SetAttributeValue(@wsClientIdent.ReqID, ParamN, ObjectName, "ReqID", oClientIdent, V_STRING, false, null);
    WS_SetAttributeValue(@wsClientIdent.ReqFindClient, ParamN, ObjectName, "ReqFindClient", oClientIdent, V_GENOBJ, true, null);
    
    if (wsClientIdent.ReqFindClient != null)
        wsClientIdent.ReqFindClient = TClientRequisites;
        FillTReqFindClient(@wsClientIdent.ReqFindClient, oClientIdent.ReqFindClient);
    end;
end;

macro ClientIdentBody(oClientIdent, wsClientIdent)
    FillTWsClientIdent(oClientIdent, wsClientIdent, false);
    
    var ClientRequisites = wsClientIdent.ReqFindClient;
    var result = TClientIdentSuccess;
    if (ClientRequisites.Type == ReqTypePersn)
        result = IdentClientByReqFindClientPerson(ClientRequisites);
    elif (ClientRequisites.Type == ReqTypeIns)
        result = IdentClientByReqFindClientLegal(ClientRequisites);
    end;
    
    if(GenPropID(oClientIdent, "ReqID") != -1)
        result.ReqID = oClientIdent.ReqID;
    end;

    return result;
end;

private macro _ClientIdentSingle(oClientIdent)
    var wsClientIdent : TClientIdent;
    WS_CheckParameter(1, oClientIdent, true, V_GENOBJ);
    wsClientIdent = TClientIdent;

    return ClientIdentBody(oClientIdent, wsClientIdent);
end;  

private macro LPAD2(val, str)
    return StrSubst(String(val:2), " ", str)
end;

macro LPAD4(val, str)
    return StrSubst(String(val:4), " ", str)
end;

macro FormatDate(DatePrm)
    var str = "";

    var day = 0, mon = 0, year = 0;
    DateSplit(DatePrm, day, mon, year);

    if ((day == 0) and (mon == 0) and (year) == 0)
        day = 1; mon = 1; year = 1;
    end;

    str = String(LPAD2(day, "0"), ".", LPAD2(mon, "0"), ".", LPAD4(year, "0"));
    
    return str;
end;

private macro _ClientIdentMass(oClientIdent)
    var wsClientIdentMass : RSCReqFindClientsListResponse;
    var clientsList;
    var oClientListElem;
    var i = 0;

    // Legal
    var pClientIdentMassLegalPrm = ClientIdentMassLegalPrm();
    var pClientIdentMassLegalPrmCount = 0;
    //Person
    var pClientIdentMassPersonPrm = ClientIdentMassPersonPrm();
    var pClientIdentMassPersonPrmCount = 0;

    if(GenPropID(oClientIdent, "clientsList") != -1) // обращаемся к свойству, если оно существует в объекте
        WS_SetAttributeValue(@clientsList, 1, "", "clientsList", oClientIdent, V_GENOBJ, true, null);
        if(GenPropID(clientsList, "size") != -1)
            wsClientIdentMass.resultList = TArray(false, clientsList.size);

            pClientIdentMassLegalPrm.IdenArray = TArray(false, clientsList.size);
            pClientIdentMassLegalPrm.CodeArray = TArray(false, clientsList.size);
            pClientIdentMassLegalPrm.NameArray = TArray(false, clientsList.size);

            pClientIdentMassPersonPrm.IdenArray = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.NameArray = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.Name1 = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.Name2 = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.Name3 = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.Born = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.PaperKind = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.PaperSeries = TArray(false, clientsList.size);
            pClientIdentMassPersonPrm.PaperNumber = TArray(false, clientsList.size);

            while(i < clientsList.size)
                oClientListElem = clientsList[i];

                var wsClientIdent : TClientIdent;
                WS_CheckParameter(1, oClientListElem, true, V_GENOBJ);
                wsClientIdent = TClientIdent;
                FillTWsClientIdent(oClientListElem, wsClientIdent, false);

                var ClientRequisites = wsClientIdent.ReqFindClient;
                if (ClientRequisites.Type == ReqTypeIns)
                    pClientIdentMassLegalPrm.IdenArray[pClientIdentMassLegalPrmCount] = /*pClientIdentMassLegalPrmCount + 1*/wsClientIdent.id;

                    if (ValType(ClientRequisites.INN) != V_UNDEF)
                        var INN = "";
                        splitINN_KPP(ClientRequisites.FullName, INN);
                        pClientIdentMassLegalPrm.CodeArray[pClientIdentMassLegalPrmCount] = INN;
                    else 
                        pClientIdentMassLegalPrm.CodeArray[pClientIdentMassLegalPrmCount] = "";
                    end;

                    if (ValType(ClientRequisites.FullName) != V_UNDEF)
                        pClientIdentMassLegalPrm.NameArray[pClientIdentMassLegalPrmCount] = NormalizeString(ClientRequisites.FullName);
                    else 
                        pClientIdentMassLegalPrm.NameArray[pClientIdentMassLegalPrmCount] = "";
                    end;
                    pClientIdentMassLegalPrmCount = pClientIdentMassLegalPrmCount + 1;

                    if(GenPropID(oClientIdent, "ReqID") != -1)
                        pClientIdentMassLegalPrm.ReqID = wsClientIdent.ReqID;
                    end;
                elif (ClientRequisites.Type == ReqTypePersn)
                    pClientIdentMassPersonPrm.IdenArray[pClientIdentMassPersonPrmCount] = /*pClientIdentMassPersonPrmCount + 1*/wsClientIdent.id;

                    if (ValType(ClientRequisites.FullName) != V_UNDEF)
                        pClientIdentMassPersonPrm.NameArray[pClientIdentMassPersonPrmCount] = NormalizeString(ClientRequisites.FullName);
                    else
                        pClientIdentMassPersonPrm.NameArray[pClientIdentMassPersonPrmCount] = "";
                    end;

                    if (ValType(ClientRequisites.FIO) != V_UNDEF)
                        var FIO = ClientRequisites.FIO;
                        pClientIdentMassPersonPrm.Name1[pClientIdentMassPersonPrmCount] = NormalizeString(FIO.Surname);
                        pClientIdentMassPersonPrm.Name2[pClientIdentMassPersonPrmCount] = NormalizeString(FIO.FirstName);

                        if (ValType(FIO.Patronymic) != V_UNDEF)
                            pClientIdentMassPersonPrm.Name3[pClientIdentMassPersonPrmCount] = NormalizeString(FIO.Patronymic);
                        else
                            pClientIdentMassPersonPrm.Name3[pClientIdentMassPersonPrmCount] = "";
                        end;
                    else
                        pClientIdentMassPersonPrm.Name1[pClientIdentMassPersonPrmCount] = "";
                        pClientIdentMassPersonPrm.Name2[pClientIdentMassPersonPrmCount] = "";
                        pClientIdentMassPersonPrm.Name3[pClientIdentMassPersonPrmCount] = "";
                    end;

                    if (ValType(ClientRequisites.BirthDate) != V_UNDEF)
                        pClientIdentMassPersonPrm.Born[pClientIdentMassPersonPrmCount] = Date(ClientRequisites.BirthDate);
                    else
                        pClientIdentMassPersonPrm.Born[pClientIdentMassPersonPrmCount] = Date(0,0,0);
                    end;

                    if (ValType(ClientRequisites.IDoc) != V_UNDEF)
                        var IDoc = ClientRequisites.IDoc;
                        pClientIdentMassPersonPrm.PaperKind[pClientIdentMassPersonPrmCount] = Int(IDoc.TypeDoc);
                        pClientIdentMassPersonPrm.PaperSeries[pClientIdentMassPersonPrmCount] = NormalizeIDocSer(IDoc.SerDoc);
                        pClientIdentMassPersonPrm.PaperNumber[pClientIdentMassPersonPrmCount] = IDoc.NumDoc;
                    else
                        pClientIdentMassPersonPrm.PaperKind[pClientIdentMassPersonPrmCount] = -1;
                        pClientIdentMassPersonPrm.PaperSeries[pClientIdentMassPersonPrmCount] = "";
                        pClientIdentMassPersonPrm.PaperNumber[pClientIdentMassPersonPrmCount] = "";
                    end;
                    pClientIdentMassPersonPrmCount = pClientIdentMassPersonPrmCount + 1;

                    if(GenPropID(oClientIdent, "ReqID") != -1)
                        pClientIdentMassPersonPrm.ReqID = wsClientIdent.ReqID;
                    end;
                end;
                i = i + 1;
            end;
        end;
    end;

    FillClientIdentMassTableLegal(pClientIdentMassLegalPrm, wsClientIdentMass.resultList);
    FillClientIdentMassTablePerson(pClientIdentMassPersonPrm, wsClientIdentMass.resultList);
    
    if(GenPropID(oClientIdent, "ReqID") != -1)
        wsClientIdentMass.reqId = oClientIdent.ReqID;
    end;
  
    return wsClientIdentMass;
end;  

// Интерфейс "Выполнить идентификацию клиента"
macro ClientIdent(oClientIdent)
    var resVal;
    var iprop = GenPropID(oClientIdent, "clientsList");

    if(iprop != -1) // обращаемся к свойству, если оно существует в объекте
      resVal = _ClientIdentMass(oClientIdent);
    else
      resVal = _ClientIdentSingle(oClientIdent); 
    end;

    return resVal;
end;