/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    Сокращенный протокол расчета ПЗО

+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfcommon;


file sfrepsim_write( "sfrepsim.tmp" ) write ;

var DepPartyID;

macro GetOperName(op)

   FILE person (person) key 0;

   person.Oper = op;
   if ( GetEQ(person) )
      return person.Name;
   end;
   return  "?" ;

end;

macro GetDepartName(PartyID)
	FILE party (party) key 0;

   party.PartyID = PartyID;
   if (GetEQ(party))
      return party.Name;
   end;
   return  "?" ;
end;

macro ReportPrintCommonHeader(ComCode, ComProp, DateFee)
        
[Сокращенный протокол расчета комиссии];
[Дата формирования  - ##########](date());
[Время формирования - ########](time());
[Операционист: #### ############################################]({oper}, GetOperName({oper}));
[ ];
[Параметры запуска:];
[Филиал - по всем филиалам];
[Признак - ##############################](ComProp);
[Комиссия - ################](ComCode);
[Дата взимания - ##########](DateFee);
[ ];
[Результаты выполнения:];
[ ];
   
    return 0;
end;/*ReportPrintCommonHeader*/


macro ReportPrintHeader( sfrepsim )

    file sfcomiss( sfcomiss )key 0;

    sfcomiss.FeeType = sfrepsim.FeeType;
    sfcomiss.Number  = sfrepsim.CommNumber;

    if( not GetEQ( sfcomiss ) )
        MsgBox( "Не найдена комиссия" );
        return 1;
    end;
    
[Филиал - ####################################](GetDepartName(DepPartyID));
[ ];
[Ведомость расчета комиссии ################]( sfcomiss.Code )    ;
[┌───────────────────────────┬─────────────────────────┬──────────────────────────────────────┐];
[│ Объект начисления         │        Период           │             Сумма                    │];
[│                           │    с       │    по      │                                      │];
[├───────────────────────────┼────────────┼────────────┼──────────────────────────────────────┤];
    
    return 0;
end;/*ReportPrintHeader*/


macro ReportPrintLine( sfrepsim ) 

    file fininstr (fininstr)key 0;
    fininstr.FIID = sfrepsim.FIID_sum;
    if( not GetEQ( fininstr ))
        MsgBox("Не найдена валюта ", sfrepsim.FIID_sum );
        return 1;
    end;

[│ ######################### │ ########## │ ########## │ ################### ################ │]
    ( sfrepsim.Object, sfrepsim.BeginDate, sfrepsim.EndDate, 
    sfrepsim.CommSum + sfrepsim.NDSSum, fininstr.FI_Code
    );

    return 0;
end;

macro ReportPrintFooter( sfrepsim, Sum )

    file fininstr (fininstr)key 0;
    fininstr.FIID = fininstr.FIID = sfrepsim.FIID_sum;
    if( not GetEQ( fininstr ))
        MsgBox("Не найдена валюта ", sfrepsim.FIID_sum );
        return 1;
    end;
    
[├───────────────────────────┴────────────┴────────────┼──────────────────────────────────────┤];
[│ Итого                                               │ ################### ################ │](Sum, fininstr.FI_Code);
[└─────────────────────────────────────────────────────┴──────────────────────────────────────┘];
[  ];
    return 0;
end;


macro ReportBegin(DepartPartyID)
    var stat = OpenReportGlobalFile( sfrepsim_write, true );

    DepPartyID = DepartPartyID;
/*    msgbox( "ReportBegin: ", stat );    */

  return      stat;
end;/*ReportBegin*/



macro ReportEnd()

    file sfrepsim( "sfrepsim.tmp" ) key 0;
    var FeeTypeCurrent = 0;
    var CommNumberCurrent=0;
    var Sum = $0;
    
    if (OpenReportGlobalFile(sfrepsim, false))
        return 1;
    end;
    

    if (NRecords(sfrepsim) == 0)
        println("Филиал - ", GetDepartName(DepPartyID));
        println("С указанными параметрами рассчитываемых комиссий нет");
        println;        
    end;

    while (next(sfrepsim))

        if ((sfrepsim.FeeType == FeeTypeCurrent) and
             (sfrepsim.CommNumber == CommNumberCurrent))

             if (ReportPrintLine(sfrepsim))
                return 1;
             end;
            
        else
             if (FeeTypeCurrent != 0)
                 if (ReportPrintFooter(sfrepsim))
                    return 1;
                 end;
             end;
             
             FeeTypeCurrent = sfrepsim.FeeType;
             CommNumberCurrent = sfrepsim.CommNumber;


             if (ReportPrintHeader(sfrepsim))
                return 1;
             end;
             
             if (ReportPrintLine(sfrepsim))
                return 1;
             end;

        end;
        
        Sum = Sum + sfrepsim.CommSum + sfrepsim.NDSSum;

    end;/*while*/

    if (FeeTypeCurrent != 0)
         if (ReportPrintFooter(sfrepsim, Sum))
            return 1;
         end;
    end;

/*msgbox( "ReportEnd: ", 0 );    */
    return 0;
end;/*ReportEnd*/



macro ReportInsertLine(sfcontr_, sfdefcom_)

    record sfdefcom( sfdefcom );
    record sfcontr( sfcontr );

    SetBuff( sfdefcom, sfdefcom_ );
    SetBuff( sfcontr, sfcontr_ );
    
    ClearRecord( sfrepsim_write );

    sfrepsim_write.FeeType  = sfdefcom.FeeType;
    sfrepsim_write.CommNumber=sfdefcom.CommNumber;

/*    sfrepsim_write.ObjectType=sfcontr.ObjectType;
    sfrepsim_write.FIID     =sfcontr.FIID;*/
    sfrepsim_write.Object   =sfcontr.Object;
    sfrepsim_write.SfcontrID = sfdefcom.conID;

    sfrepsim_write.BeginDate=sfdefcom.datePeriodBegin;
    sfrepsim_write.EndDate  =sfdefcom.dateFee;
    
    sfrepsim_write.baseSum  =sfdefcom.baseSum;
    sfrepsim_write.commSum  =sfdefcom.commSum;
    sfrepsim_write.NDSSum=sfdefcom.NDSSum;
    sfrepsim_write.FIID_sum = sfdefcom.FIID_commSum;

    var stat = Ins( sfrepsim_write );
    
/*    msgbox( "ReportInsertLine: ", stat );    */
    
    return stat;

end;/*ReportInsertLine*/


macro ReportInsertErrorLine( SFErrorMsg,FeeType,CommNumber,CID )

    file sfcomiss( sfcomiss )key 0;
if (FeeType and CommNumber)
    sfcomiss.FeeType = FeeType;
    sfcomiss.Number  = CommNumber;

    if( not GetEQ( sfcomiss ) )
        SFErrorMsg = "Не найдена комиссия";
	[Ведомость расчёта комиссии ################](SFErrorMsg );
    else
	[Ведомость расчёта комиссии ################]( sfcomiss.Code );
    end;


[---------------------------------------------------------------------------------------------------------------------];
[| Объект начисления         | Ошибка										     |];
[|                           |   										     |];
[---------------------------------------------------------------------------------------------------------------------];
[| ######################### | ############################################################## 			      |]
(CID,SFErrorMsg);
[---------------------------------------------------------------------------------------------------------------------];
[  ];
else
 println("С указанными параметрами оплачиваемых комиссий нет");
end; /* end if */
return 0;
end; /*ReportInsertErrorLine*/