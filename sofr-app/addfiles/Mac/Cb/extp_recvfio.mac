/* 
    Реализован в рамках проекта №905
    макрос поиска и сверки ФИО получателя перед визуальным контролем ФИО 
*/
import rsd;

/* Получение следующего слова в строке */
macro GetNextWord( Str, Pos, NextWord )
    var len = strlen(Str), symb;
    var ValidSymbols = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyzАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя";
    var stat: bool;

    NextWord = "";
    stat = true;
    while( (Pos <= len) and (stat) )
        symb = substr(Str, Pos, 1);
        if( index(ValidSymbols, symb) > 0 )
            NextWord = NextWord + symb;
        else
            if( strlen(NextWord) > 0 )
                stat = false;
            end;
        end;
        Pos = Pos + 1;
    end;
         
    SetParm(1,Pos);
    SetParm(2,NextWord);
end;

/* Поиск слова в строке */
macro FindWord( Str, Word, isNeedNextPos: bool )
    var len = strlen(Str), pos, tmpWord;
    var stat: bool;
    
    stat = ( strlen(Word) > 0 );
    if( stat )
        pos = index(Str, Word);
        
        if( (pos > 0) and (isNeedNextPos) )
            stat = false;
            while( (pos <= len) and (not stat) )
                GetNextWord(Str, pos, tmpWord);
                stat = ( tmpWord == Word );
            end;
        end;
    end;

    if( not stat ) 
        pos = 0;
    end;

    return pos;
end;

/* Разбор ФИО на Ф И О */
macro ParseFIO( FIO, Family, Name, Patron )
    var pos = 1;

    GetNextWord(FIO, pos, Family);
    GetNextWord(FIO, pos, Name);
    GetNextWord(FIO, pos, Patron);
        
    SetParm(1,Family);
    SetParm(2,Name);
    SetParm(3,Patron);
end;


/* Поиск совпадения ФИО в полях записи dpmrmprop_dbt */
macro CheckReceiverFIO( PaymentID, FIO )
    var cmd, rs;
    var stat: bool;
    var ReceiverName: string;
    var Ground: string;
    var Family, Name, Patron, tmpWord;
    var pos;

    cmd = RSDCommand( string("select t_ReceiverName, t_Ground from dpmrmprop_dbt where t_PaymentID=", PaymentID) );
    cmd.execute();
    rs = RsdRecordset(cmd);
    stat = rs.moveNext(); 
    if( stat )
        ReceiverName = rs.Value(0);
        Ground = rs.Value(1);
        FIO          = strupr(FIO);
        ReceiverName = strupr(ReceiverName);
        Ground       = strupr(Ground);
        /* Разбор ФИО на Ф И О */
        ParseFIO(FIO, Family, Name, Patron );
        if( (strlen(Family) <= 0) or 
            (strlen(Name)   <= 0))
           stat = false;
        end;
    end;

    if( stat )
        /* Поиск ФИО в ReceiverName в порядке "Family Name Patron" */
        pos = FindWord(ReceiverName, Family, true);
        if( pos > 0 )
            GetNextWord(ReceiverName, pos, tmpWord); /* имя */
            stat = ( tmpWord == Name );
        else
            stat = false;
        end;

        if( stat )
            GetNextWord(ReceiverName, pos, tmpWord); /* отчество */
            stat = ( tmpWord == Patron );
        end;

        /* Поиск ФИО в Ground в порядке "Family Name Patron" */
        if( not stat )
            pos = FindWord(Ground, Family, true);
            if( pos > 0 )
                GetNextWord(Ground, pos, tmpWord); /* имя */
                stat = ( tmpWord == Name );
            end;

            if( stat )
                GetNextWord(Ground, pos, tmpWord); /* отчество */
                stat = ( tmpWord == Patron );
            end;
        end;
    end;

    return stat;
end;

/* Определение ФИО получателя платежа */
macro GetReceiverFIO( PaymentID, DeposFIO )
    var cmd, rs;
    var stat: bool;
    var ReceivFIO: string;
    var upDeposFIO: string;
    var upReceiverName: string, ReceiverName: string;
    var upGround: string, Ground: string;
    var Family, Name, Patron, tmpWord;
    var pos;

    ReceivFIO = "";
    cmd = RSDCommand( string("select t_ReceiverName, t_Ground from dpmrmprop_dbt where t_PaymentID=", PaymentID) );
    cmd.execute();
    rs = RsdRecordset(cmd);
    stat = rs.moveNext(); 
    if( stat )
        ReceiverName = rs.Value(0);
        Ground = rs.Value(1);
        upDeposFIO     = strupr(DeposFIO);
        upReceiverName = strupr(ReceiverName);
        upGround       = strupr(Ground);
        /* Разбор ФИО на Ф И О */
        ParseFIO(upDeposFIO, Family, Name, Patron );
        if( (strlen(Family) <= 0) or 
            (strlen(Name)   <= 0))
           stat = false;
        end;
    else
        return ReceivFIO;
    end;

    if( stat )
        if( not CheckReceiverFIO( PaymentID, DeposFIO ) )
            /* Поиск ФИО в ReceiverName в порядке "Family Name Patron" */
            pos = FindWord(upReceiverName, Family, false); /* false - чтоб узнать только позицию, а затем считать слово в оригинальном регистре */
            if( pos > 0 )
                GetNextWord(ReceiverName, pos, tmpWord); /* фамилия */
                ReceivFIO = tmpWord;
                GetNextWord(ReceiverName, pos, tmpWord); /* имя */
                ReceivFIO = ReceivFIO + " " + tmpWord;
                GetNextWord(ReceiverName, pos, tmpWord); /* отчество */
                ReceivFIO = ReceivFIO + " " + tmpWord;
            else
                stat = false;
            end;

            /* Поиск ФИО в Ground в порядке "Family Name Patron" */
            if( not stat )
                pos = FindWord(upGround, Family, false); /* false - чтоб узнать только позицию, а затем считать слово в оригинальном регистре */
                if( pos > 0 )
                    GetNextWord(Ground, pos, tmpWord); /* фамилия */
                    ReceivFIO = tmpWord;
                    GetNextWord(Ground, pos, tmpWord); /* имя */
                    ReceivFIO = ReceivFIO + " " + tmpWord;
                    GetNextWord(Ground, pos, tmpWord); /* отчество */
                    ReceivFIO = ReceivFIO + " " + tmpWord;
                    stat = true;
                end;
            end;

            if( (not stat) and (strlen(ReceiverName) > 0) )
                ReceivFIO = ReceiverName;
            end;

        else
            ReceivFIO = DeposFIO;
        end;

    end;

    return ReceivFIO;
end;
