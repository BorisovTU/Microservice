/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ОДБ"                                   */
/*                                                                          */
/*                                                                          */
/*  Имя файла: pctoob.CAR                                                   */
/*  Создан: 10.12.1998                                        Стасевич В.   */
/*                                                                          */
/****************************************************************************/
/*****************************************************************************
Изменения:
    15.11.2000, Михайлов С.: исправлено ОпределитьСуммыОднойЗаписи()
    23.11.2000, Михайлов С.: InsertPcCarry
*****************************************************************************/
IMPORT InsCarryDoc, pcproc;
IMPORT pccarry;
import mc_lib;

RECORD din( arhdoc );

FILE pcacc( pcacc ) write;
FILE pcacnt( pcacnt );
FILE pcpaygrh( pcpaygrh ) key 2;
FILE pcaddhis( pcaddhis ) key 1;
FILE pccalc( pccalc ) key 7;
FILE pccalc2( pccalc ) key 1;
FILE pcrestc( pcrestc );


/* Переменные */
VAR СуммаПр = $0, СуммаНач = $0, СуммаНачН = $0, СуммаНачК = $0,
    СуммаПрК = $0, СуммаНаБуд = $0, СуммаНаПолучателе = $0,
    Дата;


/*------------------------------------------------------------------------
  Процедура получения кода валюты по номеру категории учета
--------------------------------------------------------------------------*/
MACRO НайтиВалютуПоКатегории( НомерКатегории )
  pcacnt.AutoPerc = pcacc.AutoKey;
  pcacnt.AcntCode = НомерКатегории;

  if(( GetGE( pcacnt ) )                      and
     ( pcacnt.AutoPerc == pcacc.AutoKey ) and
     ( pcacnt.AcntCode == НомерКатегории    )     )
    return pcacnt.FIID;
  else
    return -1;
  end;
END;

/*------------------------------------------------------------------------*/
MACRO НайтиСчетПоКатегории( НомерКатегории )
  pcacnt.AutoPerc = pcacc.AutoKey;
  pcacnt.AcntCode = НомерКатегории;

  if(( GetGE( pcacnt ) )                and
     ( pcacnt.AutoPerc == pcacc.AutoKey ) and
     ( pcacnt.AcntCode == НомерКатегории    )     )
    return pcacnt.Account;
  else
    if( ( НомерКатегории == НомКатСчетПлательщик           ) or
        ( НомерКатегории == НомКатСчетПолучатель           ) or
        ( ( НомерКатегории == НомКатСчетПросрочки    ) and
          ( pcacc.ObjectType == ОбъектДоговорКредита )     )    )

       return "";
    else
       СтрокаОшибки = string("Не найдена запись по счету для категории учета:",НомерКатегории);
/*       MsgBox(СтрокаОшибки);*/
       return "";
    end;
  end;
END;

/*------------ Остаток счета процентов за дату ---------------------------*/
MACRO ОстатокСчетаПроцентов( ДатаОстатка )
 VAR Остаток;
   pcrestc.AutoPerc = pcacc.AutoKey;
   pcrestc.Date     = ДатаОстатка;

   if( not getEQ(pcrestc) )
      return $0;
   else
      return pcrestc.Rest;
   end;
END;

/*------------ Сумма расчитанных процентов за период ---------------------*/
MACRO СуммаЗаПериод( ДатаНач, ДатаКон )
 VAR СуммаПроцентов = $0,
     контр;

   pccalc2.AutoPerc = pcacc.AutoKey;
   pccalc2.PayType  = 1;
   pccalc2.CalcDate = ДатаНач;

   контр = getGE( pccalc2 ) and
           ( pccalc2.AutoPerc == pcacc.AutoKey ) and
           ( pccalc2.PayType  == 1 ) and
           ( pccalc2.CalcDate <= ДатаКон );

   while( контр )
      СуммаПроцентов = СуммаПроцентов + pccalc2.PercSum;

      контр = next( pccalc2 ) and
              ( pccalc2.AutoPerc == pcacc.AutoKey ) and
              ( pccalc2.PayType  == 1 ) and
              ( pccalc2.CalcDate <= ДатаКон );
   end;

   return СуммаПроцентов;
END;

macro СравнитьДатыПериодов( ДатаНачисления, ДатаОплаты )

   if( pcacc.FormAddByPay == 0 )
      return ДатаНачисления <  ДатаОплаты;
   else
      return ДатаНачисления <= ДатаОплаты;
   end;
end;

/* ---------------------- Определить суммы документов ---------------- */
MACRO ОпределитьСуммыОднойЗаписи( ВСуммаПр, ВСуммаНач, ВСуммаНачН, ВСуммаНачК,
                                  ВСуммаНаПолучателе, ВСуммаНаБуд, ВСуммаПрК,
                                  Дата1, Дата2, ДатаОпл,
                                  pcpaygrhRec )
 VAR ПромСумма = $0, ПромСумма2 = $0, СуммаНачисленных = $0,
     ПромДата1, ДатаН, ВспмгтДата,
     контр;

 /* Определение суммы СуммаПр */
   if( Дата <= ДатаОпл )
      ВСуммаПр = $0;
   else
      ВСуммаПр = СуммаЗаПериод( Дата1, Дата2 );

      pccalc.PayGraphID = pcpaygrh.AutoKey;
      pccalc.PayDate    = Дата1;

      контр = getGE( pccalc ) and
              ( pccalc.PayGraphID == pcpaygrh.AutoKey )/* and
              ( pccalc.PayDate <= Дата2 )*/;

      while( контр )
         if( pccalc.PayDate <= Дата )
            ПромСумма = ПромСумма + pccalc.PercSum;
         end;

         контр = next( pccalc ) and
                 ( pccalc.PayGraphID == pcpaygrh.AutoKey )/* and
                 ( pccalc.PayDate <= Дата2 )*/;
      end;

      ВСуммаПр = ВСуммаПр - ПромСумма;
   end;

 /* Определение суммы СуммаНач */
   ВспмгтДата = Минимум( Дата, ДатаОпл );

   pcaddhis.AutoPerc = pcacc.AutoKey;
   pcaddhis.AddDate  = ВспмгтДата;

   if( getLE( pcaddhis ) and
       ( pcaddhis.AutoPerc == pcacc.AutoKey ) and
       СравнитьДатыПериодов( pcaddhis.AddDate, ВспмгтДата ) )
      ПромДата1 = pcaddhis.EndDate;
   else
      if( prev( pcaddhis ) and
          ( pcaddhis.AutoPerc == pcacc.AutoKey ) and
          СравнитьДатыПериодов( pcaddhis.AddDate, ВспмгтДата ) )
         ПромДата1 = pcaddhis.EndDate;
      else
         ПромДата1 = date(0,0,0);
      end;
   end;

   ДатаН = ПромДата1;

   if( Дата <= pcacc.BeginDate )
      СуммаНачисленных = $0;
   else
      СуммаНачисленных = СуммаЗаПериод( Дата1, ДатаН );

      ПромСумма = $0;
      pccalc.PayGraphID = pcpaygrh.AutoKey;
      pccalc.PayDate    = date(0,0,0);

      контр = getGE( pccalc ) and
              ( pccalc.PayGraphID == pcpaygrh.AutoKey );

      while( контр )
         if( pccalc.PayDate <= Дата )
            ПромСумма = ПромСумма + pccalc.PercSum;
         end;

         контр = next( pccalc ) and
                 ( pccalc.PayGraphID == pcpaygrh.AutoKey );
      end;

      СуммаНачисленных = СуммаНачисленных - ПромСумма;
      СуммаНачисленных = Максимум( $0, СуммаНачисленных );
   end;

   if( not БылЛиВыносНаПросрочкуПоПериоду( pcpaygrhRec ) )
      ВСуммаНач = СуммаНачисленных;
   else
      ВСуммаНач = $0;
   end;

   if( БылЛиВыносНаПросрочкуПоПериоду( pcpaygrhRec ) and
       ПериодПервойПросрочки( pcacc, pcpaygrhRec ) )

      ВСуммаНаПолучателе = СуммаНачисленных;
      ВСуммаНаБуд = ВСуммаПр - СуммаНачисленных;
   end;

   if( БылаПросрочка( pcacc ) )
      if( ПериодПервойПросрочки( pcacc, pcpaygrhRec ) )

         ВСуммаНачН = СуммаНачисленных;
      end;
   else
      ВСуммаНачН = СуммаНачисленных;
   end;

   if( БылаПросрочка( pcacc ) and
       БылЛиВыносНаПросрочкуПоПериоду( pcpaygrhRec ) and
       not ПериодПервойПросрочки( pcacc, pcpaygrhRec ) )

      ВСуммаПрК = ВСуммаПр;
   end;

   if( БылаПросрочка( pcacc ) and
       not БылЛиВыносНаПросрочкуПоПериоду( pcpaygrhRec ) and
       not ПериодПервойПросрочки( pcacc, pcpaygrhRec ) )

      ВСуммаНачК = СуммаНачисленных;
   end;

   SetParm( 0, ВСуммаПр           );
   SetParm( 1, ВСуммаНач          );
   SetParm( 2, ВСуммаНачН         );
   SetParm( 3, ВСуммаНачК         );
   SetParm( 4, ВСуммаНаПолучателе );
   SetParm( 5, ВСуммаНаБуд        );
   SetParm( 6, ВСуммаПрК          );

   return true;
END;

/* ---------------------- Определить суммы документов ---------------- */
MACRO ОпределитьСуммы()
  VAR контр,
      ВСуммаПр, ВСуммаНач, ВСуммаНачН, ВСуммаНачК,
      ВСуммаНаПолучателе, ВСуммаНаБуд, ВСуммаПрК,
      last_date = date(0,0,0);

   pcpaygrh.AutoPerc = pcacc.AutoKey;
   pcpaygrh.EndDate  = date(0,0,0);

   контр = getGE( pcpaygrh ) and
           ( pcpaygrh.AutoPerc == pcacc.AutoKey ) and
           ( pcpaygrh.EndDate  <= Дата );
   while( контр )

      if( pcpaygrh.PaySum < pcpaygrh.CalcSum )
         ВСуммаПр  = $0;
         ВСуммаНач = $0;
         ВСуммаНачН = $0;
         ВСуммаНачК = $0;
         ВСуммаНаПолучателе = $0;
         ВСуммаНаБуд = $0;
         ВСуммаПрК = $0;

         ОпределитьСуммыОднойЗаписи( ВСуммаПр, ВСуммаНач, ВСуммаНачН, ВСуммаНачК,
                                     ВСуммаНаПолучателе, ВСуммаНаБуд, ВСуммаПрК,
                                     pcpaygrh.BeginDate,
                                     pcpaygrh.EndDate,
                                     pcpaygrh.PayDate,
                                     pcpaygrh );

         СуммаПр           = СуммаПр + ВСуммаПр;
         СуммаНач          = СуммаНач + ВСуммаНач;
         СуммаНачН         = СуммаНачН + ВСуммаНачН;
         СуммаНачК         = СуммаНачК + ВСуммаНачК;
         СуммаНаПолучателе = СуммаНаПолучателе + ВСуммаНаПолучателе;
         СуммаНаБуд        = СуммаНаБуд + ВСуммаНаБуд;
         СуммаПрК          = СуммаПрК + ВСуммаПрК;

         last_date = pcpaygrh.EndDate;
      end;
      контр = next( pcpaygrh ) and
              ( pcpaygrh.AutoPerc == pcacc.AutoKey ) and
              ( pcpaygrh.EndDate  <= Дата );
   end;

/*Учесть период, который попадает не полностью*/
   if( last_date<Дата)
   if( ( pcpaygrh.AutoPerc == pcacc.AutoKey ) and ( pcpaygrh.EndDate > Дата ) )
      if( pcpaygrh.PaySum < pcpaygrh.CalcSum )
         ВСуммаПр  = $0;
         ВСуммаНач = $0;
         ВСуммаНачН = $0;
         ВСуммаНачК = $0;
         ВСуммаНаПолучателе = $0;
         ВСуммаНаБуд = $0;
         ВСуммаПрК = $0;

         ОпределитьСуммыОднойЗаписи( ВСуммаПр, ВСуммаНач, ВСуммаНачН, ВСуммаНачК,
                                     ВСуммаНаПолучателе, ВСуммаНаБуд, ВСуммаПрК,
                                     pcpaygrh.BeginDate,
                                     pcpaygrh.EndDate,
                                     pcpaygrh.PayDate,
                                     pcpaygrh );

         СуммаПр  = СуммаПр + ВСуммаПр;
         СуммаНач = СуммаНач + ВСуммаНач;
         СуммаНачН = СуммаНачН + ВСуммаНачН;
         СуммаНачК = СуммаНачК + ВСуммаНачК;
         СуммаНаПолучателе = СуммаНаПолучателе + ВСуммаНаПолучателе;
         СуммаНаБуд        = СуммаНаБуд + ВСуммаНаБуд;
         СуммаПрК          = СуммаПрК + ВСуммаПрК;
      end;
   end;
   end;

   return true;
END;

/*┌──────────────────────────┐*/
/*│ Точка входа в программу  │*/
/*└──────────────────────────┘*/
MACRO ExecuteStep(out, in)
  VAR БылиДокументы = FALSE, ВалютаПлательщика, ВалютаПолучателя, НадежностьСубъекта;
  var СчетОВП ;  /* Номер лицевого счета ОВП (одинаковый для всех
                           глав и валют */

  setbuff(din, in);
  setbuff(mc_out, out);

  pcacc.AutoKey = din.AutoKey;
  getEQ( pcacc );
  din.AutoKey = 0;

  Дата    = din.Date_Carry;

  if( not ОпределитьСуммы() )
     return 1;
  end;


  if( not ПолучитьСчетОВП (СчетОВП, din.chapter, din.code_currency, 4503 ) )
    СчетОВП = "ОВП";
  end;



  /* Проводки - в зависимости от метода и типа остатка */

  if  ( Index( pcacc.UserType, КассовыйМетод ) > 0 )
    /* Оплата процентов - Кассовый метод */

          if( СуммаПр > $0 )
             copy( dout, din ); /*1*/
             dout.Sum = СуммаПр;
             dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетБудПериодов );
             dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетПросрочки );
             InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
             БылиДокументы = TRUE;
          end;

          if( СуммаНач > $0 )
             copy( dout, din ); /*2*/
             dout.Sum = СуммаНач;
             dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетБудПериодов );
             dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетТребованийОбязательств );
             InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
             БылиДокументы = TRUE;
          end;

          if( СуммаПр > $0 )
             copy( dout, din ); /*3*/
             dout.Chapter = ГлаваВ;
             dout.Sum = СуммаПр;
             dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетНаВнебалансеПоПросроч );
             dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетДляКорреспонденции );
             InsertPcCarry( dout.Code_Currency, НайтиВалютуПоКатегории( НомКатСчетДляКорреспонденции ), СчетОВП, СчетОВП );
             БылиДокументы = TRUE;
          end;

          if( СуммаНач > $0 )
             copy( dout, din ); /*4*/
             dout.Chapter = ГлаваВ;
             dout.Sum = СуммаНач;
             dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетНаВнебалансе );
             dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетДляКорреспонденции );
             InsertPcCarry( dout.Code_Currency, НайтиВалютуПоКатегории( НомКатСчетДляКорреспонденции ), СчетОВП, СчетОВП );
             БылиДокументы = TRUE;
          end;
  elif( Index( pcacc.UserType, МетодНачислений ) > 0 )
    /* Перенос на внебаланс - Метод начисления */

           if (NOT ОпределитьНадежностьСубъекта(pcacc.AutoKey, @НадежностьСубъекта, Дата))
               MsgBox("Ошибка определения надежности субъекта: " + ppLastError);
               return 1;
           end;

           // если субъект ненадежный
           if (НадежностьСубъекта == 0)
              // проверим, может он уже на внебалансе
              if( Index( pcacc.UserType, НаВнебалансе ) > 0 )
                  MsgBox("Клиент уже на внебалансе.");
                  return 1;
              end;

              // просто устанавливаем пользовательский тип На внебалансе
              pcacc.UserType = pcacc.UserType + НаВнебалансе;
              Update(pcacc);

           else
              MsgBox("Клиент не является проблемным, перенос на внебаланс не предусмотрен.");
              return 1;
           end;
  end;

  if( БылиДокументы and ( СтрокаОшибки == "" ) )
     return 0;
  else
     if(СтрокаОшибки != "") MsgBox( СтрокаОшибки );end;
     return 1;
  end;
END;
