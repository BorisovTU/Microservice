/*                           
$Name:             prn_cert.mac
$Module:           Депозитарий
$Description:      Макрос печати инвентарных карточек (сертификатов)
      - Векселя 
      - Складского свидетрельства
      - Депозитного и сберегательного сертификатов
      - Эмиссионных ц.б.
      VSH 19.09.01
*/

import DealsInter, FIInter, PTInter, CTInter, Календарь, globals, DPInter, "adress.mac";
import "or_rep_h.mac";
import RsbDataSet;

const DepoChapter = 5;  /*Глава учета ценных бумаг*/

private CONST SET_CHAR   = "X";
private CONST UNSET_CHAR = "";
private const MAXINT = 2147483648;
               
record fininstr("fininstr");
record avoiriss("avoiriss");


var Table1 = "┌──────────────┬────────────────────┬────────────────────┬──────────┐\n"+
             "│Серия         │№ начальный         │№ конечный          │Количество│\n"+
             "├──────────────┼────────────────────┼────────────────────┼──────────┤";

var Table2 = "┌─────────┬────────────────────┬────────────┐\n"+
             "│Роль     │Наименование агента │Дата подписи│\n"+
             "├─────────┼────────────────────┼────────────┤";

var Rep = CMakeReport();
const RepFontStyleTitel =  "ex_FS(b)";
const TableWidth = 80;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/************************************************************/
macro __NameAlg(Type, Number)
  var NameAlg= TBFile("namealg");
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;
/************************************************************/

/*ISO код валюты*/
macro GetISOCode( valFIID )
   file fi_ISO ("fininstr") key 0;

   fi_ISO.FIID = valFIID;
   if ( GetEQ(fi_ISO) )
      return fi_ISO.ISO_Number;
   else
      return 0;
   end;
end;

macro GetLLvaluesName( List, Elem )
   record llv( "llvalues.dbt" );

   if( LL_FindLLVALUES( List, Elem, llv ) )
     return llv.Name;
   end;

   return "";
end;


class АтрибутыИК(_invcard, _vficert, _vsbanner, _dl_leg)

   var ic  = _invcard;
   var vficert  = _vficert;
   var vsbanner = _vsbanner;
   var dl_leg   = _dl_leg;

   var AvrKind = TBFile("avrkinds");
   var Fiwarnts= TBFile("fiwarnts");
   var VsircAgent=TBFile("vsircag", "R", 1);

   var party   = TrecHandler("party");
   var FaceFIID= TrecHandler("fininstr");

/*   var    */
   var
       Price = 0.0,
       FaceValue = $0,
       FaceValueFI= 0;

   if(dl_leg != NULL)
      if (ПолучитьСтрокуЗначенияКурса(dl_leg.Price,dl_leg.Point,Price)!=0)
         MsgBox("Не найдена процентная ставка");
      end;
   end;

   AvrKind.rec.FI_Kind   = fininstr.FI_Kind  ;
   AvrKind.rec.AvoirKind = fininstr.AvoirKind;
   if(not GetEQ(AvrKind))
      MsgBox("Не найден вид финансового инструмента");
      ClearRecord(AvrKind);
   end;

   ClearRecord(FaceFIID);
   FaceValue   = vficert.FaceValue;
   FaceValueFI = vficert.FaceValueFI;

   if( FaceValueFI!=-1 )
      if (ПолучитьФинИн (FaceValueFI, FaceFIID, NULL) != 0)
         MsgBox("Не найдена валюта номинала ценной бумаги");
      end;
   end;

   macro FirstAgent()
      VsircAgent.AddFilter( " t_BCID = " + vficert.CertID + " and t_AvoirKind in (select avr.t_AvoirKind from davrkinds_dbt avr where avr.t_FI_Kind = 2 start with  avr.t_AvoirKind = " + vficert.AvoirKind + " connect by prior avr.t_avoirkind = avr.t_parent )" );
      if(VsircAgent.Next())
         return true;
      end;
      return false;                                            
   end;

   macro NextAgent()
      if(VsircAgent.Next())
         return true;
      end;
      return false;
   end;

   macro Agent()
      return VsircAgent.rec;
   end;

   macro Assigned()
     if(vsbanner != NULL)
        if(vsbanner.Flags)
           return "переуступаемый";
        else
           return "не переуступаемый";
        end;
     end;
     return "";
   end;
   /*Полное название субъекта*/
   macro Name(PartyID)
      if (ПолучитьСубъекта(PartyID, party) == 0)
         return party.rec.Name;
      end;
      return "       ";
   end;
   /*Название региона*/
   macro Регион(Region)
      var ptregion = TBFile("ptregion");
      ptregion.rec.CodeRegion = Region;
      if(getEQ(ptregion))
         return ptregion.rec.NameRegion; 
      end;
      return "";
   end;
   /*Юридический адрес*/
   macro Address(PartyID)
      record RecordAdress ( adress );
      if(НайтиЮридическийАдресСубъекта(PartyID,RecordAdress))
         return RecordAdress.Adress;
      end;
      return "   ";
   end;
   /*Фактический адрес*/
   macro RealAddress(PartyID, Type)
     var adress = TBFile("adress");
     adress.rec.PartyID = PartyID;
     adress.rec.Type    = Type;
     if(getEQ(adress))
        return adress.rec.Adress;
     end;
     return "";
   end;

   /*код вида 1*/
   macro Code(PartyID)
      return ПолучитьКодСубъекта(PartyID, PTCK_CONTR);
   end;

   /*ИНН*/
   macro INN(PartyID)
      return ПолучитьКодСубъекта(PartyID, PTCK_INN);
   end;

   macro invcard()
      return ic;
   end;

   macro cert()
      return vficert;
   end;

   macro FaceFI()
     return FaceFIID.rec;
   end;

   macro Dl()
     return dl_leg;
   end;

   macro Vs()
     return vsbanner;
   end;

   macro Kind()
     return AvrKind.rec;
   end;

   macro Fin()
     return fininstr;
   end;

   macro Avr()
     return avoiriss;
   end;

   macro ActualIssuer()
      var report_date = {curdate};
      return FI_GetIssuerOnDate( Fin, report_date);
   end;

   macro Warrant(_fininstr)
      if(_fininstr.AvoirKind == AVOIRISSKIND_COUPON_BOND)
         Fiwarnts.rec.DrawingDate = {curdate};
         Fiwarnts.rec.FIID = _fininstr.FIID;
         if(not GetEQ(Fiwarnts))
            MsgBox("Не найден текущий купон купонной облигации");
            ClearRecord(Fiwarnts);
         end;
      else
         ClearRecord(Fiwarnts);
      end;
      return Fiwarnts.rec;
   end;

   macro AcntCode(DAcnt)
      var Depoacnt = TBFile("depoacnt");
      Depoacnt.rec.AutoKey = DAcnt;
      if(GetEQ(Depoacnt))
         return Depoacnt.rec.Code;
      end;
      return "";
   end;

   macro Root_Acc(DAccID)
      var Account  = TBFile("account", "R", 1); 
      Account.rec.AccountID       = DAccID;
      if(GetEQ(Account))
         return AcntCode(Account.rec.DepoAcc) +"/"+ AcntCode(Account.rec.DepoRoot);
      end;
      return "";
   end;

   macro Account(AccID)
      var Account  = TBFile("account", "R", 1); 
      Account.rec.AccountID       = AccID;
      if(GetEQ(Account))
         return Account.rec.Account;
      end;
      return "";
   end;

   macro Owner_Name(AccID)
      var depoacnt = TBFILE("depoacnt");
      var account = TBFILE("account", "R", 1);
    
      account.rec.AccountID = AccID;
      if (account.GetEQ())
        depoacnt.rec.AutoKey = account.rec.DepoAcc;
        if (not depoacnt.GetEQ())
          return "       ";
        end;  
      else
        return "       ";
      end;
      
      return Name(depoacnt.rec.Owner);
   end;

   macro Owner_Code(AccID)
      var depoacnt = TBFILE("depoacnt");
      var account = TBFILE("account", "R", 1);
    
      account.rec.AccountID = AccID;
      if (account.GetEQ())
        depoacnt.rec.AutoKey = account.rec.DepoAcc;
        if (not depoacnt.GetEQ())
          return "       ";
        end;  
      else
        return "       ";
      end;
      
      return Code(depoacnt.rec.Owner);
   end;
    
end; /*class АтрибутыИК*/


/*Печатная форма инвентарной карточки векселя*/
macro ИК_Векселя(Bill)
  var celllen;

  celllen = 23;
  Rep.AddPrintCell( "И Н В Е Н Т А Р Н А Я   К А Р Т О Ч К А   В Е К С Е Л Я", 100, 0, "c:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "№ "+Bill.invcard.Number, 100, 0, "c:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddPrintCell( "составлена: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.invcard.RegistrDate/*+"     операцией № <N1>"*/, 0, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "состояние карточки: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( __NameAlg(5211,Bill.invcard.Status) + "   установлено: " + Bill.invcard.StatusDate, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "всего сертификатов: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( 1 + " (" + NumToStr(1) + ") " + " шт.", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "всего на сумму: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.FaceValue + " (" + NumToStr(Bill.FaceValue) + ") " + GetISOCode(Bill.FaceValueFI), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "предъявлены к оплате: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.BCPresentationDate, 0, 0, "r:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "осуществлено погашение: ", celllen, 0, "r:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Dl.Closed, 0, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "---------------------------------------------------------------------------------------------------", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  celllen = 20;
  Rep.AddPrintCell( "ВЕКСЕЛЕДАТЕЛЬ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      код анкеты:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Code(Bill.cert.Issuer), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      наименование:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Name(Bill.cert.Issuer), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      ИНН:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.INN(Bill.cert.Issuer), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      адрес:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Address(Bill.cert.Issuer), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  celllen = 34;
  Rep.AddPrintCell( "ПАРАМЕТРЫ ВЕКСЕЛЯ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      код анкеты:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Fin.FI_Code, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      наименование:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Fin.Name, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      вид:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( __NameAlg(3505,Bill.Vs.IssueKind), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      дата составления:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Dl.Start, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      выписан на сумму:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.FaceValue + " (" + NumToStr(Bill.FaceValue) + ") ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      валюта:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( GetISOCode(Bill.FaceValueFI) + " (" + Bill.FaceFI.Name + ")", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      формулировка срока платежа:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( GetLLvaluesName( 1132, Bill.Vs.BCTermFormula ), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      формулировка срока погашения:", celllen, 0, "l:", REP_ELEM_STR );
  if( (Bill.Vs.BCTermFormula == 15) or (Bill.Vs.BCTermFormula == 30) )
    Rep.AddPrintCell( Bill.Dl.Diff + " дней от " + __NameAlg(3508, IIF(Bill.Vs.BCTermFormula == 15, 25, 30)), 100, 0, "l:", REP_ELEM_STR );
  elif( Bill.Vs.BCTermFormula == 10 )
    Rep.AddPrintCell(  " в дату " + date(Bill.Dl.Start + int(Bill.Dl.Diff)) , 100, 0, "l:", REP_ELEM_STR );
  elif( Bill.Vs.BCTermFormula == 20 )
    if( Bill.Dl.Expiry != date(0,0,0) )
      Rep.AddPrintCell(  " не позднее  " + Bill.Dl.Expiry, 100, 0, "l:", REP_ELEM_STR );
    elif( Bill.Dl.Maturity != date(0,0,0) )
      if( Bill.Vs.BCTermFormat != 9 )
        Rep.AddPrintCell(  " не ранее " + int(Bill.Dl.Duration) + " дней от составления", 100, 0, "l:", REP_ELEM_STR );
      else
        Rep.AddPrintCell(  " не ранее " + Bill.Dl.Maturity, 100, 0, "l:", REP_ELEM_STR );
      end;
    end;
  end;
  Rep.AddStr();
  Rep.AddPrintCell( "      доход:", celllen, 0, "l:", REP_ELEM_STR );
  if( Bill.Dl.Formula == 0 )
    Rep.AddPrintCell( "дисконт", 100, 0, "l:", REP_ELEM_STR );
  elif( (Bill.Dl.Formula == 1) or (Bill.Dl.Formula == 3) )
    Rep.AddPrintCell( "проценты, ставка " + Bill.Dl.Price/10000 + " % годовых, начиная с " + Bill.Dl.InterestStart, 100, 0, "l:", REP_ELEM_STR );
  end;
  Rep.AddStr();

  celllen = 33;
  Rep.AddPrintCell( "МЕСТО СОСТАВЛЕНИЯ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      адрес:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.IssuePlace, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  celllen = 33;
  Rep.AddPrintCell( "МЕСТО ПЛАТЕЖА", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      адрес:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.PaymentPlace, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  celllen = 20;
  Rep.AddPrintCell( "1-й ВЕКСЕЛЕДЕРЖАТЕЛЬ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      код анкеты:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Code(Bill.Vs.Holder), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      наименование:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.HolderName, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      ИНН:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.HolderTaxCode, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      адрес:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Vs.HolderAddress, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "СЕРТИФИКАТЫ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AutoScan( "[\n" + Table1 + "]" );
  Rep.AddPrintCell( Bill.cert.Series, 14, 0, "l:" + "ex_B(rlb)");
  Rep.AddPrintCell( Bill.cert.Number, 20, 0, "l:" + "ex_B(rlb)");
  Rep.AddPrintCell( Bill.cert.Number, 20, 0, "l:" + "ex_B(rlb)");
  Rep.AddPrintCell( Bill.cert.Copies, 10, 0, "l:" + "ex_B(rlb)");
  Rep.AddStr();

  if(Bill.FirstAgent())
     Rep.AddPrintCell( "АГЕНТЫ ОБРАЩЕНИЯ", 100, 0, "l:", REP_ELEM_STR );
     Rep.AutoScan( "[\n" + Table2 + "]" );

     Rep.AddPrintCell( GetLLvaluesName(1109, Bill.Agent.AgentRole), 9, 0, "l:" + "ex_B(rlb)");
     Rep.AddPrintCell( Bill.Agent.AgentName, 20, 0, "l:w:" + "ex_B(rlb)");
     Rep.AddPrintCell( Bill.Agent.DateOfSigning, 12, 0, "l:" + "ex_B(rlb)");
     Rep.AddStr();
     while(Bill.NextAgent())
        Rep.AddPrintCell( GetLLvaluesName(1109, Bill.Agent.AgentRole), 9, 0, "l:" );
        Rep.AddPrintCell( Bill.Agent.AgentName, 20, 0, "l:w:" );
        Rep.AddPrintCell( Bill.Agent.DateOfSigning, 12, 0, "l:" );
        Rep.AddStr();
     end;
     Rep.AddStr();
  end;

  celllen = 30;
  Rep.AddPrintCell( "ТЕКУЩЕЕ ЗАЧИСЛЕНИЕ", 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      Депонент", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            код анкеты:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Owner_Code(Bill.invcard.HAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            наименование:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Owner_Name(Bill.invcard.HAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            счет/раздел:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Root_Acc(Bill.invcard.HAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            лицевой счет:", celllen, celllen, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Account(Bill.invcard.HAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "      Место хранения", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            код анкеты:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Owner_Code(Bill.invcard.LAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            наименование:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Owner_Name(Bill.invcard.LAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            счет/раздел:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Root_Acc(Bill.invcard.LAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            лицевой счет:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.Account(Bill.invcard.LAccountID), 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            секция:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.invcard.Section, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "            ячейка:", celllen, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.invcard.Cell, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "ПРИМЕЧАНИЯ:", 12, 0, "l:", REP_ELEM_STR );
  Rep.AddPrintCell( Bill.invcard.Description, 100, 0, "l:", REP_ELEM_STR );
  Rep.AddStr();
end; /*ИК_Векселя*/


/*Печатная форма инвентарной карточки сертификата эмиссионной ц/б*/
macro ИК_ЭмиссионнойЦБ(Share)

  Rep.AddPrintCell( "ИНВЕНТАРНАЯ КАРТОЧКА СЕРТИФИКАТА ЭМИССИОННОЙ ЦЕННОЙ БУМАГИ", 100, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "№ " + Share.invcard.Number, 100, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddPrintCell( "Составлена:            " + date(Share.invcard.RegistrDate)/*+"     операцией № <N1>"*/, 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Серия ц/б:             " + Share.cert.Series, 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Номера бланков:        " + "с " + Trim(Share.cert.Number)+" по "+Trim(Share.cert.Number), 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Количество в карточке: " + Share.cert.Copies, 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Тип ц/б:               " + Share.Kind.Name+" "+Share.Avr.ISIN+" выпуска", 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Номер государственной регистрации выпуска: " + Share.Avr.LSIN, 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddPrintCell( "ФИНАНСОВЫЕ ПАРАМЕТРЫ ЦЕННОЙ БУМАГИ", 100, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Номинал ц/б: " + Share.Fin.FaceValue, 39, 0, "l:" + RepFontStyleTitel);
  /*Rep.AddPrintCell( Share.Fin.FaceValue, 24, 0, "l:w:" + RepFontStyleTitel);*/
  Rep.AddPrintCell( "Валюта номинала: " + Share.FaceFI.Name, 59, 0, "l:" + RepFontStyleTitel);
  Rep.AddStr();
  Rep.AddPrintCell( "Номинальная стоимость сертификата: " + Share.Fin.FaceValue*Share.cert.Copies, 99, 0, "l:" + RepFontStyleTitel);
  Rep.AddStr();
  Rep.AddPrintCell( "Доходность: " + Share.Avr.IncomeRate+" % годовых"/* с начислением        <A6>"*/, 99, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddPrintCell( "ЭМИТЕНТ", 100, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR);
  Rep.AddStr();                                
  Rep.AddPrintCell( "" + Share.Name(Share.ActualIssuer), 99, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddPrintCell( "Юридический адрес: " + Share.Address(Share.ActualIssuer), 99, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddPrintCell( "Фактический адрес: " + Share.RealAddress(Share.ActualIssuer, PTADDR_REAL), 99, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddPrintCell( "ИНН " + Share.INN(Share.ActualIssuer ), 99, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddPrintCell( "ТЕКУЩЕЕ ЗАЧИСЛЕНИЕ", 100, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();
  Rep.AddPrintCell( "Депонент:", 16, 0, "l:" + RepFontStyleTitel );
  Rep.AddPrintCell( Share.Owner_Name(Share.invcard.HAccountID), 32, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( " счет/раздел " + Share.Root_Acc(Share.invcard.HAccountID), 49, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
  Rep.AddPrintCell( "Место хранения:", 16, 0, "l:" + RepFontStyleTitel );
  Rep.AddPrintCell( Share.Owner_Name(Share.invcard.HAccountID), 32, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( " счет/раздел " + Share.Root_Acc(Share.invcard.LAccountID), 49, 0, "l:" + RepFontStyleTitel );
  Rep.AddStr();
end; /*ИК_ЭмиссионнойЦБ*/


macro Print_Invcard(InvCardBuff)

   record _invcard("invcard.dbt");      /* Структура для доступа к ИК*/
   var  _vficert;
   file _vsbanner("vsbanner");
   file _dl_leg("dl_leg.dbt");
   file _warrant("warrant.dbt");
   file _mortgage("mortgage.dbt");

   var InvKard;

   var Excel;
   var err;
   var NoPrint = 0;

   GetRegistryValue( "DEPO\\DEPOREPTOEXCEL", V_BOOL, Excel, err );

   SetBuff (_invcard, InvCardBuff) ;
   
   _vficert = TRsbDataSet("select crt.*, rsb_deals.GetFicertPlanDrawingDate(crt.t_FiCertID) as DrawingDate from dv_ficert_dbt crt where crt.t_FiCertID = " + _invcard.FiCertID);
   if(not _vficert.moveNext())
     MsgBox("Ненайден сертификат инвентарной карточки ", _invcard.FiCertID);
     return 1;
   end;  

   if (ПолучитьФинИн (_vficert.FIID, fininstr, avoiriss) != 0)
      MsgBox("Ненайден финансовый инструмент сертификата инвентарной карточки ", _vficert.FIID);
      return 1;
   end;

   if(not НеэмиссионныйФинИн(fininstr.FIID))
      InvKard = АтрибутыИК(_invcard, _vficert);
      ИК_ЭмиссионнойЦБ(InvKard);
   elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BILL, fininstr.AvoirKind ))

      _vsbanner.BCID = _vficert.CertID;
      if(not GetEQ(_vsbanner))
        MsgBox("Ненайдена информация о сертификате инвентарной карточки");
        return 1;
      end;

      _dl_leg.LegKind  = LEG_KIND_VSBANNER;
      _dl_leg.DealID   = _vsbanner.BCID;
      _dl_leg.LegID    = 0;
      if(not GetEQ(_dl_leg))
        MsgBox("Ненайдена ценовые условия сертификата инвентарной карточки");
        return 1;
      end;

      InvKard = АтрибутыИК(_invcard, _vficert, _vsbanner, _dl_leg);
      ИК_Векселя(InvKard);
   elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_STORAGE_CERTIFICATE, fininstr.AvoirKind ))
      _warrant.WarrantID = _vficert.CertID;
      if(not GetEQ(_warrant))
        MsgBox("Ненайдена информация о сертификате инвентарной карточки");
        return 1;
      end;
      
      ExecMacroFile("warrcert.mac", "ИК_СкладскогоСвидетельства", _invcard, _vficert, _warrant , Excel/*, STAT*/);
      NoPrint = 1;    
   elif( (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_SAVING_CERTIFICATE, fininstr.AvoirKind )) OR (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_DEPOSIT_CERTIFICATE, fininstr.AvoirKind )) )
      _vsbanner.BCID = _vficert.CertID;
      if(not GetEQ(_vsbanner))
        MsgBox("Ненайдена информация о сертификате инвентарной карточки");
        return 1;
      end;

      _dl_leg.LegKind  = LEG_KIND_VSBANNER;
      _dl_leg.DealID   = _vsbanner.BCID;
      _dl_leg.LegID    = 0;
      if(not GetEQ(_dl_leg))
        MsgBox("Ненайдена ценовые условия сертификата инвентарной карточки");
        return 1;
      end;
      ExecMacroFile("scdpcert.mac", "ИК_ДепозитногоИлиСберегательногоСертификата", _invcard, _vficert, _vsbanner, _dl_leg, Excel/*, STAT*/);
      NoPrint = 1;
   elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_MORTGAGE, fininstr.AvoirKind ) )
      _mortgage.MortgageID = _vficert.CertID;
      if(not GetEQ(_mortgage))
        MsgBox("Ненайдена информация о сертификате инвентарной карточки");
        return 1;
      end;
      ExecMacroFile("mortcert.mac", "ИК_Закладной", _invcard, _vficert, _mortgage, Excel/*, STAT*/);
      NoPrint = 1;  
   else
      println("\n Неизвестный тип инвентарной карточки.");
   end;

   if(not err)
     if( not NoPrint )
       if( Excel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;
     end;
   else
      MsgBox("Не могу найти настройку выпуска отчетов в Excel");
      Rep.PrintRep();
   end;

end; /*Print_Invcard*/
