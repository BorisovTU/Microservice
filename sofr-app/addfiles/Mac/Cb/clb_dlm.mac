/*----------------------------------------------------------------------------\
|                                                                             |
|        Импорт рублевых платжных документов (PS_PAYORDER),                   |
|        поступивших по системе "Клиент-Сбербанк"                             |
|                                                                             |
|  07.07.04 Копачев Д.В.                                                      |
|                                                                             |
\----------------------------------------------------------------------------*/

import iabs, iabsdw, clbdbsgn, Globals, oralib, cryptdlm;

/*---------------- переменные -----------------------------*/
var export:ClbDbfSignCheck;

const ClbSignatoryID = "0099990000", /* Идентификатор абонента криптосистем */
      ClbOTPKContextID = "ИмпортRSBank|Клиент-Сбербанк|DLM", /* Точка проверки значения ключевания документов, импортируемых из системы Клиент-Сбербанк */
      ClbNTPKContextID = "ИмпортRSBank|Клиент-Сбербанк|Макрос", /* Точка импорта значения ключевания документов, импортируемых из системы Клиент-Сбербанк */
      originalSystem = "CLBSB",
      ExportFileName = "\\export.dbf"; /* файл с платежными документами системы клиент-банк */
var   PathExportFileName = "";

var ErrMes:string = " ";
var oDocument;
var oABS   = ABSGetInterface();
var oABSDW = ABSDirectWriteGetInterface();
var oMeta, oDoc, resInsert;

var ISOTxt       = 810;
var oCurrentBank = oABS.GetCurrentBank();
var cur          ;

/* Ошибка создания объекта */
if( IsEqClass("IABSNotFound", oCurrentBank) ) 
  ErrMes = String( "Ошибка конструирования объекта 'IABSCurrentBank': ", oCurrentBank.what() );
  println(ErrMes);
else
  cur = oCurrentBank.GetCurrencyByISO(ISOTxt);
end;

/* Создадим Объект */
MACRO CreateDocument(class_name, sbrfkey)
  oMeta = oABSDW.GetMetaClass(class_name);
  var oDoc = oABSDW.CreateDocument(originalSystem, string(sbrfkey), oMeta);
  return oDoc;
END;

/************************************************************************************/
/* Проверка существования документа, соответсвующего документу системы Клиент Банка */
/*   (с заданными originalSystem и originalId)                                      */
/* Если документа нет, возвращается 0, иначе 1.                                     */
/************************************************************************************/
MACRO IsExistCLSBDoc( originalId:string ):bool
  var rset:object;
  var select:string;
  var params:TArray = TArray();

  select = " SELECT t.t_docid " +
           " FROM dclbdoc_dbt t " +
           " WHERE t.t_originalsystem = :originalSystem" +
           "   AND t.t_originalid = :originalId";
  params[params.size] = SQLParam( "originalSystem", originalSystem );
  params[params.size] = SQLParam( "originalId", originalId );
  rset = execSQLselect( select, params, TRUE );
  
  if( rset and rset.moveNext() )
    return true;
  end;

  return false;
END;

/***********************************************************************/
/* Заполнение некоторых специфичных полей                              */
/***********************************************************************/
/* Номер пачки                                                         */
MACRO ProcImpl_NumberPack(impl, NumberPack:integer):integer
  var stat, rechandler:TRecHandler;

  if( IsEqClass("IABSObjectList",impl) )
    stat = impl.first;
    while( stat )
      if( GenClassName(impl.rec) == "TRecHandler" )
       if( impl.rec.TBlName == "pmpaym.dbt" )
        rechandler = impl.rec;
        rechandler.rec.NumberPack = NumberPack;
        return 0;
       end;
      end;
      stat = impl.next;
    end;
  elif( GenClassName(impl) == "TRecHandler" )
    if( impl.rec.TBlName == "pmpaym.dbt" )
        rechandler = impl.rec;
        rechandler.rec.NumberPack = NumberPack;
        return 0;
    end;
  end;
  return 1;
END;

/* Происхождение                                                       */
MACRO ProcImpl_Origin(impl):integer
  var stat, rechandler:TRecHandler;

  if( IsEqClass("IABSObjectList",impl) )
    stat = impl.first;
    while( stat )
      if( GenClassName(impl.rec) == "TRecHandler" )
       if( impl.rec.TBlName == "pspayord.dbt" )
        rechandler = impl.rec;
        rechandler.rec.Origin = PSPO_OR_CLSB;
        return 0;
       end;
      end;
      stat = impl.next;
    end;
  elif( GenClassName(impl) == "TRecHandler" )
    if( impl.rec.TBlName == "pspayord.dbt" )
        rechandler = impl.rec;
        rechandler.rec.Origin = PSPO_OR_CLSB;
        return 0;
    end;
  end;
  return 1;
END;

/***********************************************************************/
/* Чтение данных                                                       */
/***********************************************************************/
MACRO ReadPaymentData( err, oDoc )
   /* создаем новый документ */
   oDoc.originalId = trim(export.Field("SBRFKEY").Value);
   var ISOTxt       = 810;
   var stat:bool    = true;

   var Kpp_Payer:string = "", Kpp_Receiver:string = "";
   var err_mes:string = "";
   var impl;
      
   var oProtection;
   var oCurrentBank = oABS.GetCurrentBank();

     if( IsEqClass("IABSNotFound", cur) ) 
       stat = false;
       ErrMes = String( "Ошибка конструирования объекта 'IABSInBankCurrency': ", cur.what() );
     else
       
       /* заполняем документ oDoc */
       oDoc.Number          = String(Int(export.Field("N_Document").Value)); /* Номер документа */
       /*oDoc.date            = export.Field("DataDoc").Value;*/ /* Дата */
       oDoc.paymentPriority = Int(export.Field("Order").Value);   /* Очередность платежа */
       oDoc.sum             = MoneyL(export.Field("Sum").Value);  /* Сумма */
       oDoc.Ground          = Trim(export.Field("Ground").Value); /* Основание */
       oDoc.currency        = cur; /* Валюта */

       /* плательщик */
       oDoc.payer.chapter    = 1;
       oDoc.payer.number     = Trim(export.Field("Acc_Payer").Value); /* Счет плательщика */
       oDoc.payer.name       = Trim(export.Field("Payer").Value);     /* Название плательщика */
       oDoc.payer.client.INN = Trim(export.Field("INN_Payer").Value); /* Инн плательщика */
       Kpp_Payer = Trim(export.Field("KPPPLAT").Value); /* КПП плательщика */
       if( strlen(Kpp_Payer) )
         oDoc.payer.client.INN = oDoc.payer.client.INN + "/" + Kpp_Payer;
       end;
       oDoc.payer.currency   = cur;
       oDoc.payer.bank       = oCurrentBank;           /* банк плательщика */

       /* получатель */
       oDoc.receiver.chapter          = 1;
       oDoc.receiver.number           = Trim(export.Field("Acc_Rec").Value);  /* Счет получателя */
       oDoc.receiver.name             = Trim(export.Field("Receiver").Value); /* Название получателя */
       oDoc.receiver.currency         = cur;
       oDoc.receiver.client.INN       = Trim(export.Field("INN_Rec").Value);  /* Инн получателя */
       Kpp_Receiver = Trim(export.Field("KPPPOL").Value); /* КПП получателя */
       if( strlen(Kpp_Receiver) )
         oDoc.receiver.client.INN = oDoc.receiver.client.INN + "/" + Kpp_Receiver;
       end;
       oDoc.receiver.bank.BIC         = Trim(export.Field("MFO").Value);      /* МФО получателя */
       oDoc.receiver.bank.name        = Trim(export.Field("Name_Bank").Value);/* Название банка получателя */
       oDoc.receiver.bank.corrAccount = Trim(export.Field("ACC_BANK").Value); /* Корр. счет банка получ-ля */


       /* добавляем контрольные данные: */
       /* создаем объект класса потомка от IABSObjectProtection
          соответствующий методу защиты информации, например, с помощью ЭЦП */
       oProtection = oABSDW.CreateObjectOfClass("IABSObjectProtectionBySignature");

       oProtection.protectionData = trim(string(export.Field("sbrfkey").Value));

       /* ИКПОО для вставки внешней ЭЦП */
       oProtection.ContextID = ClbNTPKContextID;

       /* добавляем защитную информацию к документу */
       oDoc.AddProtection(oProtection);

       /* Теперь, когда все поля oDoc заполнены, можно заполнить номер пачки */
       impl = oDoc._GetImplementation;
       ProcImpl_NumberPack(impl, int(export.Field("n_pack").Value));
       ProcImpl_Origin(impl);
     end;

//   SetParm( 0, stat );

   return oDoc;
END;

MACRO insertTrn
  resInsert = oDocument.insertToCache();
  if( IsEqClass("IABSError", resInsert) )
    return true;
  else
    return false;
  end;

END;

/***********************************************************************/
/* Запись данных                                                       */
/***********************************************************************/
MACRO WritePaymentData()

  ErrMes = "Документ импортирован успешно";
  return insertTrn();

END;

/***********************************************************************/
/* Проверка формата импортируемого файла                               */
/***********************************************************************/
private macro CheckFields()

   if( (export.Field("N_Document").Name == "") or /* Номер документа */
       (export.Field("Order").Name      == "") or /* Очередность платежа */
       (export.Field("Sum").Name        == "") or /* Сумма */
       (export.Field("Ground").Name     == "") or /* Основание */
       (export.Field("Acc_Payer").Name  == "") or /* Счет плательщика */
       (export.Field("Payer").Name      == "") or /* Название плательщика */
       (export.Field("INN_Payer").Name  == "") or /* Инн плательщика */
       (export.Field("KPPPLAT").Name    == "") or /* КПП плательщика */
       (export.Field("Acc_Rec").Name    == "") or /* Счет получателя */
       (export.Field("Receiver").Name   == "") or /* Название получателя */
       (export.Field("INN_Rec").Name    == "") or /* Инн получателя */
       (export.Field("KPPPOL").Name     == "") or /* КПП получателя */
       (export.Field("MFO").Name        == "") or /* МФО получателя */
       (export.Field("Name_Bank").Name  == "") or /* Название банка получателя */
       (export.Field("ACC_BANK").Name   == "")    /* Корр. счет банка получ-ля */
     )
     return 1;
   end;

   return 0;
end;

/********************************************************************************/
/* Импорт платежных документов из Dbf-файла, полученного из системы "Клиент-СБ" */
/********************************************************************************/
macro ImportPayOrder()
   var count:integer = 0;
   var insID, Value_err, TypeVal;
   var prefix:string;
   var day:integer, mon:integer, year:integer;
   var cur_stat:bool;
   var CryptoAPI = RsCryptoAPI();
   var NeedCheckSign:bool = CryptoAPI.IsCryptoActionNeeded( ClbOTPKContextID, 201 );
   
   TypeVal = GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, PathExportFileName, Value_err);
   if( ( TypeVal == V_UNDEF ) OR ( Value_err != 0) ) 
     MsgBox ("Ошибка чтения настройки BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR"); 
     return false;
   end; 

   PathExportFileName = PathExportFileName + ExportFileName;
  
   export = ClbDbfSignCheck(PathExportFileName);
   if( export.LastError )
      MsgBox(export.LastError);
      return false;
   end;

   if( CheckFields() )
     MsgBox("Файл импорта " + PathExportFileName + " имеет неверный формат");
     return false;
   end;

   oDoc = CreateDocument("IABSDWPaymentOrder", "000000000000");
   InitProgress(export.RecCount, "Пожалуйста подождите...", "Идет импорт платежных документов");
   export.Rewind();

   /* Кэширование пароля */
   if( NeedCheckSign )
     CryptoAPI.BeginKeyCashing();
   end;

   while( export.next() )

      if( count == 0 )
[      Отчет о результатах импорта платежных документов "Клиент-Сбербанк"           ];
[┌─────────┬────────────┬─────────────┬─────────────────────┬────────────────────────────────────────────────────────────┐];
[│  Номер  │    Номер   │     Дата    │ Значение ключевания │                    Статус                                  │];
[│  пачки  │  документа │  документа  │                     │                                                            │];
[├─────────┼────────────┼─────────────┼─────────────────────┼────────────────────────────────────────────────────────────┤];

      end;

      /* Читаем информацию о платежном документе из файла данных */
      oDocument = ReadPaymentData( cur_stat, oDoc );
      if( cur_stat )
        DateSplit(export.Field("DataDoc").Value, day, mon, year);
        prefix = string( day:o:2, mon:o:2, (year-int(year/100)*100):o:2 ) + ClbSignatoryID;

        if( (not NeedCheckSign) OR export.CheckSignature(prefix, ClbOTPKContextID) )

          resInsert = 0;

          if( IsExistCLSBDoc( trim(export.Field("SBRFKEY").Value) ) )

            ErrMes = "В импорте отказано по причине повторного ввода";

          else
            /* Запись данных */
            WritePaymentData();

            if( IsEqClass("IABSError", resInsert) )
              ErrMes = resInsert.what();
            end;
          end
        else
          ErrMes = export.LastError;
        end;
      end;

      count = count + 1;
      UseProgress(count);

      ErrMes = StrSubst(ErrMes, "|", " " );
      [│ ####### │ ########## │ ##########  │ ################### │ ########################################################## │]
      (int(export.Field("N_PACK").Value), String(Int(export.Field("N_Document").Value)), export.Field("DataDoc").Value, trim(export.Field("SBRFKEY").Value), ErrMes:w);

   end;

   /* Сброс режима кэширования пароля */
   if( NeedCheckSign )
     CryptoAPI.EndKeyCashing();
   end;

   if( count != 0 )
[└─────────┴────────────┴─────────────┴─────────────────────┴────────────────────────────────────────────────────────────┘];
   end;
   resInsert = oABSDW.Insert();

   if( IsEqClass("IABSError", resInsert) )
     ErrMes = resInsert.what();
     println(ErrMes);
   end;
   RemProgress();

   println("Обработано записей: " + count);

   export = NULL;

   return true;
end;
