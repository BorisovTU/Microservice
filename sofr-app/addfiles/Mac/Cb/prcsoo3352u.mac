/*
$Name:             Prcsoo3352u.mac
$Module:           ББ и РКО
$Description:      Печать расходного кассового ордера 3352-У
*/

import prcs;
    
/* -----------------------------------------------
   Непосредственно печать по подготовленным данным
   ----------------------------------------------- */

MACRO PrintOutCashOrder(d : TOutOrderPrintData3352, ncopy : integer):bool

  array ArrayClient, ArrayBankName, ArrayGround, ArrayPaperName, ArraySum;
         
  StrSplit( d.PaperNameStr, ArrayPaperName, 69, 24, 3);
  StrSplit( d.Client, ArrayClient, 34, 28, 3);
  StrSplit( d.BankName, ArrayBankName, 45, 44, 2);
  /*Основание платежа*/
  StrSplit(d.Ground, ArrayGround, 60, 40, 3);
  
  StrSplit(d.SumString, ArraySum, 60, 46, 3);

  /*Печать*/
  while(ncopy > 0)
    ncopy = ncopy - 1;
//-------------------------------------------------------------------------------------------  
      [                                        ┌───────────────────┐  ┌ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ┐
                                               │Код формы документа│  │Отрывной талон к расходному│
                                               │  по ОКУД 0402009  │  │кассовому ордеру № ########│
                                               └───────────────────┘  └ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ┘
                                                                                                   
                                   ┌─────────┐                        ┌───────────────────────────┐
        Расходный кассовый ордер N │######## │####################### │    Место для наклейки     │
                                   └─────────┘  ────────────────────  │     отрывного талона      │
                                                       Дата           └───────────────────────────┘
                                                                                                   
                                                                      ┌───────────────────────────┐
                                                       ДЕБЕТ          │      Сумма цифрами        │
        Выдать #########################  ┌───────────────────────────┼─────────────────────┬─────┤
               ───────────────────────────│счет № ####################│### #################│     │    ]
        
       ( d.Number:l, 
         d.Number:c, d.DateStr:c,
         ArrayClient(0), d.DebitList.value(0).Account:l, d.DebitList.value(0).FICode, d.DebitList.value(0).Amount:r );

      if (d.DebitList.size < 2)
      [ (фамилия,имя,отчество(при наличии)└───────────────────────────┤                     │     │
         #################################         КРЕДИТ             │                     │     │    ]
        (ArrayClient(1));
      else
        var i : integer = 1;
        while( i < d.DebitList.size )
          if (d.DebitList.value(i).Account == "") 
            break; 
          end;        
          if ( i == 1 )
      [ (фамилия,имя,отчество(при наличии)│       ####################│### #################│     │    ]
            (d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          end;
          if ((i == 2) and (d.DebitList.size > 2 ))
      [  #################################│       ####################│### #################│     │    ]
            (ArrayClient(1), d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          elif (i == 2)
      [  #################################└───────────────────────────┤                     │     │    
                                                   КРЕДИТ             │                     │     │    ]
            (ArrayClient(1));
          end;
          if ( i > 2 )
      [                                   │       ####################│### #################│     │    ]
         (d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          end;
         i = i + 1;
        end;
        if (i > 2)
      [                                   └───────────────────────────┤                     │     │    
                                                   КРЕДИТ             │                     │     │    ]
        end;                                           
      end;

      [ ──────────────────────────────────┬───────────────────────────┤                     │     │
                                          │счет № ####################│### #################│     │
        ───────────────────────────────── └───────────────────────────┼─────────────────────┴─────┤
         Наименование банка ######################################### │  в том числе по символам  │
                            ──────────────────────────────────────────┼────────┬──────────────────┤ 
         ############################################# БИК ########## │ символ │      сумма       │
        ──────────────────────────────────────────────     ───────────┼────────┼──────────────────┤ 
        Сумма прописью ############################################## │  ####  │   ############## │
                       ───────────────────────────────────────────────┼────────┼──────────────────┤ 
         ############################################################ │  ####  │   ############## │
        ──────────────────────────────────────────────────────────────┼────────┼──────────────────┤ 
         ############################################################ │  ####  │   ############## │    ]
         
       ( d.ReceiverAccount:l, d.ReceiverFICode, d.ReceiverAmountStr:r, 
         ArrayBankName(0),
         ArrayBankName(1), d.BIC, 
         ArraySum(0), d.ArraySym.value(0):c, MakeMoneyStr3352(d.ArrayPartSum.value(0)):r,
         ArraySum(1), d.ArraySym.value(1):c, MakeMoneyStr3352(d.ArrayPartSum.value(1)):r,
         ArraySum(2), d.ArraySym.value(2):c, MakeMoneyStr3352(d.ArrayPartSum.value(2)):r);
    
      var CashSymbCount = 3;
      while( CashSymbCount < (d.ArraySym.size - 1) )   
        if (d.ArraySym.value(CashSymbCount) == "") 
          break; 
        end;        
      
      [ ──────────────────────────────────────────────────────────────┼────────┼──────────────────┤    
                                                                      │  ####  │   ############## │    ]
        (d.ArraySym.value(CashSymbCount):c, MakeMoneyStr3352(d.ArrayPartSum.value(CashSymbCount)):r );
        CashSymbCount = CashSymbCount + 1;
      end;

      [ ──────────────────────────────────────────────────────────────┼────────┼──────────────────┤
        Направление выдачи   ######################################## │  ####  │   ############## │
                             ─────────────────────────────────────────┼────────┴───────────┬──────┤
         ############################################################ │ Шифр документа     │  ##  │
        ──────────────────────────────────────────────────────────────┴────────────────────┴──────┘
         ############################################################
        ───────────────────────────────────────────────────────────────────────────────────────────        
        Предъявлен документ, удостоверяющий личность ########################┌ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─┐
                                                    ─────────────────────────│                    │
        #####################################################################│                    │
        ─────────────────────────────────────────────────────────────────────│      Подпись       │
        #####################################################################└ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─┘
        ─────────────────────────────────────────────────────────────────────
              (наименование документа, серия, номер, кем и когда выдан)                    
           
        Указанную в расходном кассовом ордере сумму получил _______________________
                                                                (личная подпись)

        ____________ ________ _________ ____________ ________ _________ ____________ ________ _________
        (наименование (личная (фамилия,(наименование (личная  (фамилия,(наименование (личная (фамилия,     
          должности)  подпись) инициалы) должности)  подпись)  инициалы) должности)  подпись) инициалы)]
       ( ArrayGround(0), d.ArraySym.value(CashSymbCount):c, MakeMoneyStr3352(d.ArrayPartSum.value(CashSymbCount)):r, 
         ArrayGround(1), d.ShifrOper:c,   
         ArrayGround(2),
         ArrayPaperName(0), 
         ArrayPaperName(1),
         ArrayPaperName(2));
  end;        
//-------------------------------------------------------------------------------------------          ]
  return TRUE;

END;

/* Буфер мультивалютного документа, чтобы печать не ругалась */
var pr_multydoc:TRecHandler = TRecHandler( "multydoc.dbt" );
/* Буфер мемордера, чтобы печать не ругалась */
var pr_cb_doc  :TRecHandler = TRecHandler( "cb_doc.dbt"   ); 

MACRO PrintDocument(ncopy:integer):bool

  var DocKind:integer = pr_pmpaym.rec.DocKind;
  var data : TOutOrderPrintData3352 = TOutOrderPrintData3352();

  if( ( DocKind == 420/*CASH_PS_OUTORDER*/ ) or ( DocKind == 440/*CASH_BOF_OUTORDER*/ ) )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pscshdoc );  
  elif( ( DocKind == CB_MULTYDOC ) or ( DocKind == DLDOC_MEMORIALORDER ) )
    data.InitByMemorialDocument( pr_pmpaym, pr_pmrmprop );
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;
  if( pr_PrintEA )
    PrintEAHeader();
  end;

  return PrintOutCashOrder(data, ncopy);

END;
