/*
 $Name: pm_allowtransfer.mac
 $Module: МБР
 $Description: Проверка платежей, позиционированных на корсхему, в которой  корреспондент corschem.CorrID является ПБР
 */
import oralib, PaymInter, likepy, pm_setst;


private macro IsCorschemPBR(Corschem : integer, FIID : integer) : bool
  var rs = execSQLselectPrm
    ( "Select t_CorrID "
      "  from dcorschem_dbt "
      " where t_Number = :CorschemNumber "
      "   and t_FIID = :FIID "
      "   and t_FI_Kind = " + FIKIND_CURRENCY,
      SQLParam("CorschemNumber", Corschem),
      SQLParam("FIID", FIID)
    );

  if(rs and rs.moveNext())
    if( БанкУБР(rs.value("t_CorrID")) )
      return true;
    end;
  end;

  return false;
end;

macro IsPmPosPSBR(debet : TRecHandler, credit : TRecHandler) : bool
  if( (debet.rec.Group == PAYMENTS_GROUP_EXTERNAL) and
      IsCorschemPBR(debet.rec.Corschem, debet.rec.PayFIID)
      or
      (credit.rec.Group == PAYMENTS_GROUP_EXTERNAL) and
      IsCorschemPBR(credit.rec.Corschem, credit.rec.PayFIID)
    )
    return true;
  end;

  return false;
end;


const CHECKRESULT_SUCCEED    = 0;  //Успех
const CHECKRESULT_NOTALLOWED = 1;  //Ошибка
const CHECKRESULT_NOBESP     = 2;  //Платеж не подключен к БЕСП
const CHECKRESULT_CHANGEKIND = 3;  //Изменить вид платежа на "Срочно"
const CHECKRESULT_WARNING    = 4;  //Порекомендовать изменить вид платежа на "Срочно"
const CHECKRESULT_INSTREJECT = 5;  //Отвергнуть Срочный
const CHECKRESULT_INSTWARN   = 6;  //Предупреждение для Срочного
const CHECKRESULT_NEEDMANUAL = 7;  //Требуется ручная обработка

macro CheckPmPSBR( Payment:RsbPayment )
    
  if( IsPmPosPSBR( Payment.GetDEBET(), Payment.GetCREDIT() ) )
    execSQL( " Begin PM_ALLOWTRANSFER.SingleCheckPmPSBR(:PaymentID); end; ",
               makeArray(SQLParam("PaymentID", Payment.PaymentID)));
    
    
    var rs = execSQLSelect( " Select t.t_ErrorMessage, t.t_Result "
                            " From TABLE(PM_ALLOWTRANSFER.GetTransferTbl()) t "
                            " Where t.t_PaymentID = :PaymentID ",
                            MakeArray(SQLParam("PaymentID", Payment.PaymentID)));
    
    var CheckResult = 0;
    var ErrorMessage = "";
    if( rs and rs.MoveNext())
      CheckResult = rs.value("t_Result");
      ErrorMessage = rs.value("t_ErrorMessage");

      if( (CheckResult == CHECKRESULT_NOTALLOWED ) or (CheckResult == CHECKRESULT_INSTREJECT ) )
        MsgBox( ErrorMessage );
        if( RejectPayment( Payment, ErrorMessage ) )
          return -1;
        end;
        return 1;
      elif( CheckResult == CHECKRESULT_NEEDMANUAL )
        MsgBox( ErrorMessage );
        if( PM_ToManual( Payment, ErrorMessage ) )
          return -1;
        end;
        return 1;
      elif( (CheckResult == CHECKRESULT_NOBESP) or 
            (CheckResult == CHECKRESULT_WARNING) or (CheckResult == CHECKRESULT_INSTWARN))
        MsgBox( ErrorMessage);
      end;
      
      if( CheckResult == CHECKRESULT_CHANGEKIND)
        Payment.PaymentKind = "С";
        Payment.OutTransport = 0;
        Payment.OutTpSchem = 0;
        Payment.OutRlsForm = 0;
        var TpID = 0, TpSchemID = 0, RlsFormID = 0, FormID = 0, SubKindMes = 0;
        if( DefineRlsForm( Payment, TpID, TpSchemID, FormID, RlsFormID, SubKindMes) )
          Payment.OutTransport = TpID;
          Payment.OutTpSchem = TpSchemID;
          Payment.OutRlsForm = RlsFormID;
          MsgBox( ErrorMessage);
        else 
          ErrorMessage = ErrorMessage + "\nНе удалось подобрать релиз для вида платежа \"Срочно\"";
          MsgBox( ErrorMessage);
          if( RejectPayment( Payment, ErrorMessage ) )
            return -1;
          end;
          return 1;
        end;
      end;
    end;
  end;
  return 0;
  
end;