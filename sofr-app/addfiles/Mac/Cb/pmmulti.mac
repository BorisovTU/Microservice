/****************************************************************************/
/*                     R-Style SoftLab, RS-Bank 5.1                         */
/****************************************************************************/
/*               Автоматическая обработка сводных платежей                  */
/*                                                                          */
/*                                                                          */
/*  Имя файла: pmmulti.mac                                                  */
/*  Создан:    16.11.01                                      Алешин А.В.    */
/****************************************************************************/
import "globals.mac", rsd, PaymInter;

RECORD MultiPaym( pmpaym );       /* ─┐                                   */
RECORD MultiRmprop( pmrmprop );   /*  ├─ параметры создаваемого сводного  */
                                  /* ─┘  платежа                          */

const PathUseMulti  = "CB\\PAYMENTS\\MULTI\\AUTOCREATE";
const PathMaxMulti  = "CB\\PAYMENTS\\MULTI\\MAXPAYMENT";
const PathDprtMulti = "CB\\PAYMENTS\\MULTI\\CHECKDPRT";

const RES_MULTI_SKIP = 0,  /* Не вставлять платеж в сводный       */ 
      RES_MULTI_ADD  = 1,  /* Добавить платеж в имеющийся сводный */ 
      RES_MULTI_NEW  = 2;  /* Начать новый сводный платеж         */ 

/* Регистрозависимая переменная, хранит результат обработки платежа */
var RES_MULTI_PAYM = RES_MULTI_SKIP; 

var MaxMultiPaym = 0; /* Максимальное число платежей в сводном */

const OP_MULTI_FIRST = 0,  /* Получить первый сводный платеж */ 
      OP_MULTI_NEXT  = 1,  /* Получить следующий сводный платеж */
      OP_MULTI_CLOSE = 2;  /* Закончить поиск сводных платежей */

record TempPaym( pmpaym );

/* Подсчет количества платежей, составляющих сводный */
macro CalcMultiPayments( ID )
  var Cnt = 0, rs;

    rs = RsdRecordset( string("select count(*) from dpmpaym_dbt where dpmpaym_dbt.t_SubSplittedPayment = ", 
                               ID,
                              " AND dpmpaym_dbt.t_CreatedInSS = 1" ) );
    if( rs.MoveNext() )
      Cnt = int(rs.value(0));
    end;

  return Cnt;
end;

/* Перебор начальных сводных платежей АРМа, для которых не начата операция */
macro FindMultiPaym( PayFIID, CorSchem, ValueDate, Department )
  var op = OP_MULTI_FIRST, MultyID = 0, Count = 1, Num, error, CheckDprt = FALSE;

  GetRegistryValue( PathDprtMulti, V_BOOL, CheckDprt, error );

  while( Count AND (GetMultiPayments( op, PM_PROP_READY, TempPaym, PayFIID, CorSchem ) == TRUE) )
    op = OP_MULTI_NEXT;
    if( TempPaym.ValueDate == ValueDate )
      if( (CheckDprt == FALSE) OR (TempPaym.Department == Department) )    
        /* Если есть ограничение - подсчитываем текущее количество */
        if( MaxMultiPaym != 0 ) 
          Num = CalcMultiPayments( TempPaym.PaymentID );
        end;
        /* Если нет ограничения или вписываемся - то добавляем */
        if( (MaxMultiPaym == 0) OR ((MaxMultiPaym != 0) AND (Num < MaxMultiPaym)) )
          /* Здесь могут быть дополнительные проверки на сведение */
          MultyID = TempPaym.PaymentID;
          Count   = 0;
        end;
      end;
    end;
  end;

  GetMultiPayments( OP_MULTI_CLOSE );

  return MultyID;
end;

/* Процедура автоматической обработки сводного платежа
   Возвращаемое значение (через заполнение переменной RES_MULTI_PAYM): 
      0 - не вставлять платеж в сводные 
      1 - добавить платеж в имеющийся сводный (в поле MultiPaym.PaymentID 
          должен быть записан ID этого сводного платежа)
      2 - начать новый сводный платеж (можно дополнительно заполнить текстовые
          поля структур MultiPaym, MultiRmprop) */
macro ProcessPayment( Cs, PayFIID, ValueDate, Department )
  var ID, result = RES_MULTI_SKIP, RefID = 0, Number = "", UseMulti = FALSE, error;

  RES_MULTI_PAYM = result;

  GetRegistryValue( PathUseMulti, V_BOOL, UseMulti, error );

  if( (error == 0) AND (UseMulti == TRUE) )
    
    /* Считываем максимальное допустимое число платежей в сводном */
    GetRegistryValue( PathMaxMulti, V_INTEGER, MaxMultiPaym, error );

    /* Ищем сводные платежи по схеме расчетов и филиалу текущего платежа */
    ID = FindMultiPaym( PayFIID, Cs, ValueDate , Department);

    if( ID != 0 )               /* Вставляем платеж в имеющийся сводный */
      result = RES_MULTI_ADD;
      MultiPaym.PaymentID  = ID;
      MultiPaym.Department = Department;
    else
      result = RES_MULTI_NEW;    /* Начинаем новый сводный платеж */

      if( (GetReferenceIDByType(
               520, /*OBJTYPE_MULTIPAYMENT*/
               1,                    
               RefID ) == 0) AND (RefID != 0) )
        if( GenerateReference( RefID, Number ) != 0 )
          MsgBox( "Ошибка при генерации номера сводного платежа" );
          return 1;
        end;
      end;

      MultiRmprop.Number = Number;      

    end;

    /* Заполняем результат обработки платежа */
    RES_MULTI_PAYM = result;
  end;

  return 0;
end;
