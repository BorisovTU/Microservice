/************************************************************************
  RS-Bank V.6

  Импорт файлов корректур ЦБ РФ
*************************************************************************/

import mfo_lib, rsexts;

var RecreateTableDBF = false;

macro RunImport(ImportFileName)

  var LastImportFileInfo, LastImportFileName, LastImportFileKor, CurrImportFileKor, DeltaImportFileKor, CloseDeletedBank;
  var OrderControl;
  var err, files, i, nFiles, MaxNum;
  var CurNum, FileNum, rs, cmd, CurDate, Result, ret_val;
  array TextYN;
  array ButtonYN;
  ButtonYN( 0 ) = " Да ";
  ButtonYN( 1 ) = " Нет ";

  if ({curdate} == date(0,0,0))
    CurDate = "01.01.0001";
  else
    CurDate = {curdate};
  end;

  GetRegistryValue("CB\\BNKSIMPORT\\ORDERCONTROL", V_INTEGER, OrderControl, err, false, {oper});

  GetRegistryValue("CB\\BNKSIMPORT\\LASTIMPORTFILEINFO", V_STRING, LastImportFileInfo, err, false, {oper});

  GetRegistryValue("CB\\BNKSIMPORT\\CLOSEDELETEDBANK", V_BOOL, CloseDeletedBank, err, false, {oper});

  /* 
   * Таблица протокола bnkseek.log, 
   * таблица файла bnkseek.dbf, 
   * таблица файла корректировок korrek.dbf, 
   * таблицы логов ошибок err$_ теперь создаются апргейдом 2ой категории 
   */
  ClearImportTempTable();

  /* Таблица korrek.dbf теперь создается апгрейдером 2ой категории, поэтому RecreateTableDBF = false*/
  if (not dbf2ora(ImportFileName, "korrek_dbf", "Загрузка файла " + ImportFileName + " в БД", err, RecreateTableDBF))
    msgbox(err);
    exit;
  end;

  if(not CheckKorrekCorrectStruct())
    msgbox("Ошибка: файл " + ImportFileName + " имеет не поддерживаемую структуру");
    exit;
  end;

  /*Проверим возможность импорта файла корректур*/
  CurrImportFileKor = GetCurrImportFileKor();
  LastImportFileName = GetLastImportFileName(LastImportFileInfo);
  if(LastImportFileName == "bnkseek")
  elif(OrderControl != 0)
    LastImportFileKor = GetLastImportFileKor(LastImportFileInfo);
    if((LastImportFileName == "") or (LastImportFileKor == ""))
      if(OrderControl == 1)
        TextYN(0) = "Отсутствуют или некорректны сведения о предыдущем импорте справочника, результат импорта может быть некорректным. Все равно выполнить импорт?";
        Result = confwin(TextYN, ButtonYN);
        if(Result == 1)
          exit;
        end;
      elif(OrderControl >= 2)
        msgbox("Нет сведения о предыдущем импорте справочника, выполните импорт файла bnkseek.dbf или откорректируйте настройку CB\\BNKSIMPORT\\LASTIMPORTFILEINFO");
        exit;
      end;
    elif(LastImportFileName == "korrek")
      DeltaImportFileKor = int(CurrImportFileKor) - int(LastImportFileKor) - 1;
      if(OrderControl == 1)
        if((LastImportFileKor != "") and (CurrImportFileKor != "") and (strlen(LastImportFileKor) == strlen(CurrImportFileKor)))
          if(LastImportFileKor > CurrImportFileKor)
            TextYN(0) = "Импортируемый файл корректур более ранний, чем последний импортировавшийся файл. Результат импорта может быть некорректным. Все равно выполнить импорт?";
            Result = confwin(TextYN, ButtonYN);
            if(Result == 1)
              exit;
            end;
          elif(LastImportFileKor == CurrImportFileKor)
            TextYN(0) = "Импортируемый файл корректур уже импортирован. Все равно выполнить импорт?";
            Result = confwin(TextYN, ButtonYN);
            if(Result == 1)
              exit;
            end;
          else
            if(DeltaImportFileKor > 0)
              TextYN(0) = "Пропущен импорт " + DeltaImportFileKor + " файлов корректур. Результат импорта может быть некорректным. Все равно выполнить импорт?";
              Result = confwin(TextYN, ButtonYN);
              if(Result == 1)
                exit;
              end;
            end;
          end;
        end;
      elif(OrderControl >= 2)
        if(LastImportFileKor > CurrImportFileKor)
          msgbox("Импортируемый файл корректур более ранний, чем последний импортировавшийся файл. Импорт прерван.");
          exit;
        elif(LastImportFileKor == CurrImportFileKor)
          msgbox("Импортируемый файл корректур уже импортирован. Импорт прерван.");
          exit;
        else
          if(DeltaImportFileKor > 0)
            msgbox("Пропущен импорт " + DeltaImportFileKor + " файлов корректур. Импорт прерван.");
            exit;
          end;
        end;
      end;
    end;
  end;

  if(RecreateTableDBF)
    SQL_Execute("ALTER PACKAGE rsb_convmfo COMPILE");
  end;

  SQL_Execute("begin dbms_stats.gather_table_stats(ownname=>sys_context('USERENV','SESSION_USER'),tabname=>'DOBJCODE_DBT'); end;", "Сбор статистики...");

  ret_val = 0;

  cmd = RsdCommand("begin ? := rsb_convmfo.updtmfo_load(" + {oper} + "); end;");

  cmd.addParam("ret_val", V_INTEGER, RSDBP_OUT);

  SQL_Execute(cmd,"Выполняется импорт файла корректур ЦБ РФ. Ждите...");

  if (ret_val == 1) 
    msgbox("Закончился диапазон ID субъектов для данного филиала.");
    exit;
  end;

  SetDefaultRegistryValue( "CB\\BNKSIMPORT\\LASTIMPORTFILEINFO", "korrek " + date + " " + time + " " + {oper} + " " + CurrImportFileKor);

  if(CloseDeletedBank)
    ProcessCloseDeletedBank();
  end;

  PrintProtocol("файла корректур ЦБ РФ", {oper}, ImportFileName);

  return ImportFileName;

end;
