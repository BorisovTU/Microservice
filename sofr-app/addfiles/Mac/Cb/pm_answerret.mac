/*
$Name:           pm_answerret.mac
$Module:         РКО
$Description:    Заполнение уведомления о возврате (аннулировании) платежа
*/                                                                            

import "wluftool.mac", PaymInter, CTInter, MesInter, "oralib.mac", "likepy.mac", "OprInter";

macro GetMesFormatType(MesID: integer): integer
  var rs = execSQLSelect("select wtf.t_FormatFile from dwltpfrmt_dbt wtf, dwlsess_dbt sess, dwlmes_dbt mes "
                  " where mes.t_MesID = ? "
                  "   and sess.t_SessionID = mes.t_SessionID "
                  "   and wtf.t_TpFrmtID = sess.t_TpFrmtID", makeArray(SQLParam("", MesID)));
  if (rs and rs.MoveNext())
    return rs.value(0);
  end;
  return -1;
end;

// получить PartyID и party.Name филиала
private macro GetDprtPartyIDandName( DepCode   : integer, 
                                     PartyID   : @integer, 
                                     PartyName : @string )

  file dp_dep("dp_dep.dbt") key 0;
  record party("party.dbt");

  dp_dep.Code = DepCode;

  if( GetEQ(dp_dep) )    
    PartyID = dp_dep.PartyID;
    if( ПолучитьСубъекта(PartyID, party) == 0 )
      PartyName = party.Name;
    else
      PartyName = "";
    end;
  else
    PartyID = -1;
    PartyName = "";
  end;

end;

// получить данные из входящего сообщения с референсом wlreq.RelatedRef 
// заполняет wlreq.InitDateMes, wlreq.InitFormIDMes, TpID, TpSchemID, RlsFormID
private macro FillInitMesData(wlreq, TpID: @integer, TpSchemID: @integer, RlsFormID: @integer)

  // Find incoming message
  var select = "Select t_MesID, t_TpSchemID, t_RlsFormID " +
               "  from dwlmes_dbt " +
               " where t_Direct = 'X' " +
               "   and t_Trn = '" + wlreq.RelatedRef + "' ";
  var rs = execSQLselect(select);

  if( rs and rs.moveNext )

    // InitDateMes
    var xml_str = "";
    var xml:object = ActiveX( "MSXML.DOMDocument" );
    if( ПолучитьПоле (rs.value("t_MesID"), XMLField, xml_str) and
        xml.loadXML(xml_str) )
      var EDDate : string = ReadOptinalAttribute(xml, "EDDate");
      if(EDDate)
        wlreq.InitDateMes = ToDate(EDDate); // ToDate из wluftool.mac
      end;
      wlreq.RecipientCode = ReadOptinalAttribute(xml, "EDAuthor");
    end;

    // InitFormIDMes
    select = "Select t_FormID " +
             "  from dwlmesrls_dbt " +
             " where t_RlsFormID = :RlsFormID ";
    var params = makeArray( SQLParam("RlsFormID", rs.value("t_RlsFormID")) );
    var rs_FormID = execSQLselect(select, params);
    if( rs_FormID and rs_FormID.moveNext )
      wlreq.InitFormIDMes = rs_FormID.value("t_FormID");
    end;

    // TpSchemID, RlsFormID    
    TpID = getTpIDofTpSchem(rs.value("t_TpSchemID"));
    TpSchemID = RlsFormID = 0;
    WldDefineRlsForm( OBJTYPE_REQ, 
                      wlreq.RecipientID, 
                      WLD_MES_KIND_ANSWER, 
                      TpID, TpSchemID, RlsFormID, WLD_SUBKIND_ANS_RET );

  end;

end;

private macro GetCodeNum(wlreq) : string
  var CodeNum = "";

  const CodeLabel = "InfoCode:";

  var ind = index(wlreq.Queries, CodeLabel);
  if(ind)
    CodeNum = substr(wlreq.Queries, ind + strlen(CodeLabel), 1);
  end;

  return CodeNum;
end;

private macro YYYYMMDD( dateInit:date )
  
  var year, mon, day;

  DateSplit( date, day, mon, year );
  return StrSubst(string(year:4, "-", mon:2,"-", day:2), " ", "0");
end;


private macro IsInitialPayment( PaymentID : integer ): bool
  var ret:bool = false;
  var s = "  select pmlink.t_InitialPayment                                         "
          "   from dpmlink_dbt pmlink                                               "   
          "  where pmlink.t_PurposePayment  = :PaymentID                            "                                         
          "    and pmlink.t_InitialPayment != pmlink.t_PurposePayment               " 
          "    and pmlink.t_LinkKind        = :LinkKind                             ";
   
  var params : TArray = makeArray( SQLParam("PaymentID", PaymentID),
                                   SQLParam("LinkKind", PMLINK_KIND_ESIDPAYMENT)
                                 );

  var rs = execSQLselect(s, params);
  
  if ( rs and rs.moveNext() )  
    s = "  select 1                                                               "
        "   from dwlreqlnk_dbt wlreqlnk, dwlreq_dbt wlreq,                        "
        "        dwlmeslnk_dbt wlmeslnk, dwlmes_dbt wlmes                         "   
        "  where wlreqlnk.t_ObjID         = :InitialPayment                       "
        "    and wlreqlnk.t_ObjKind       = :ObjType                              "
        "    and wlreq.t_ReqID            = wlreqlnk.t_ReqID                      " 
        "    and wlreq.t_SubKind          = :SubKind                              "
        "    and regexp_like( wlreq.t_Queries, '(InfoCode:)[[:space:]]*[12357]' ) "
        "    and wlmeslnk.t_ObjID         = wlreqlnk.t_ReqID                      " 
        "    and wlmeslnk.t_MesID         = wlmes.t_MesID                         "
        "    and wlmes.t_state           <> :MesState                             ";
    
    params = makeArray( SQLParam("InitialPayment", rs.value(0)),
                        SQLParam("ObjType", OBJTYPE_PMCLAIM), // Распоряжение на оплату
                        SQLParam("SubKind", WLD_SUBKIND_ANS_RET), // Возврат/аннулирование
                        SQLParam("MesState", WLD_STATUS_MES_INVALID) // невалидно 
                      );
    
    if ( not existsSQLselect(s, params))
      ret = true;
    end;
  
  end;
  
  return ret;

end;

private macro IsExistED274ForPurposePayment( PaymentID : integer ): bool
  var ret:bool = false;

  var s = "  select pmlink.t_PurposePayment                                         "
          "   from dpmlink_dbt pmlink                                               "
          "  where pmlink.t_InitialPayment  = :PaymentID                            "
          "    and pmlink.t_InitialPayment != pmlink.t_PurposePayment               "
          "    and pmlink.t_LinkKind        = :LinkKind                             ";
  
  var params : TArray = makeArray( SQLParam("PaymentID", PaymentID),
                                   SQLParam("LinkKind", PMLINK_KIND_ESIDPAYMENT)
                                 );
  var rs = execSQLselect(s, params);
  
  if ( rs and rs.moveNext() )  
                                 
    s = "  select 1                                                               "
        "   from dwlpm_dbt wlpm, dwlreqlnk_dbt wlreqlnk,                          "
        "        dwlreq_dbt wlreq, dwlmeslnk_dbt wlmeslnk, dwlmes_dbt wlmes       "
        "  where wlpm.t_PaymentID = :PurposePayment                               "
        "    and wlpm.t_Direct  = chr(0) /*WLD_MES_OUT*/                          "
        "    and wlpm.t_WlPmId = wlreqlnk.t_ObjID                                 " 
        "    and wlreqlnk.t_ObjKind       = :ObjType                              "
        "    and wlreq.t_ReqID            = wlreqlnk.t_ReqID                      "
        "    and wlreq.t_SubKind          = :SubKind                              "
        "    and regexp_like( wlreq.t_Queries, '(InfoCode:)[[:space:]]*[12357]' ) "
        "    and wlmeslnk.t_ObjID         = wlreqlnk.t_ReqID                      "
        "    and wlmeslnk.t_MesID         = wlmes.t_MesID                         "
        "    and wlmes.t_state           <> :MesState                             ";
    
    params = makeArray( SQLParam("PaymentID", rs.value(0)),
                        SQLParam("ObjType", OBJTYPE_PAYMENT), // Распоряжение на оплату
                        SQLParam("SubKind", WLD_SUBKIND_ANS_RET), // Возврат/аннулирование
                        SQLParam("MesState", WLD_STATUS_MES_INVALID) // невалидно 
                      );
    if ( not existsSQLselect(s, params))
      ret = true;
    end;
  else
    ret = true;
  end;
  
  return ret;
end;

macro CreateED274
( PaymentID : integer,
  Queries : string,
  Narrative : string,
  ID_Oper : integer,
  ID_Step : integer
) : string

  var ObjID:integer   = 0;
  var ObjKind:integer = 0;
  var msg = "";

  record wlreq("wlreq");
  
  var Reference,
      Categories,
      OrderAmount,
      FutureBaseAmount,
      Dockind,
      StartDepartment,
      ReceiverBankID,
      ReceiverBankName,
      OutCorschem,
      OutCorschemFIID,
      PayOrdDockInd,
      PropStatus,
      ValueDate,
      PayOrdOrigin,
      InrqOrigin;
  
  
  var selectVars =
      " select pmrmprop.t_Reference,                                        " +
      "        PM_COMMON.CheckObjAttrPresence(501/*PM_COMMON.OBJTYPE_PAYMENT*/, Lpad(pmpaym.t_PaymentID, 10, '0'), 18, 1), " +
      "        pmpaym.t_OrderAmount,                                        " +
      "        pmpaym.t_FutureBaseAmount,                                   " +
      "        pmpaym.t_Dockind,                                            " +
      "        pmpaym.t_StartDepartment,                                    " +
      "        pmpaym.t_ReceiverBankID,                                     " +
      "        pmrmprop.t_ReceiverBankName,                                 " +
      "        decode(db.t_IsSender, chr(0), db.t_Corschem, cr.t_Corschem), " +
      "        decode(db.t_IsSender, chr(0), db.t_PayFIID, cr.t_PayFIID),   " +
      "        decode(pmpaym.t_DocKind, " + PS_PAYORDER + " ,pspayord.t_DocKind, 0), " +
      "        decode(db.t_IsSender, chr(0), db.t_PropStatus, cr.t_PropStatus),   " +
      "        pmpaym.t_ValueDate,                                          " +
      "        pspayord.t_Origin,                                           " +
      "        psinrq.t_Origin                                              " +
      "  from dpspayord_dbt pspayord, dpmrmprop_dbt pmrmprop,               " +
      "       dpsinrq_dbt psinrq, dpmpaym_dbt pmpaym,                       " +
      "       dpmprop_dbt db, dpmprop_dbt cr                                " +
      " where pmpaym.t_PaymentID = :PaymentID                               " +
      "   and ( ( pmpaym.t_DocKind = " + PS_PAYORDER                          +  
      "           and ( pspayord.t_DocKind = " + PSPOKIND_DEMAND              + 
      "                 or pspayord.t_DocKind = " + PSPOKIND_REQUEST + " )  " + 
      "           and pspayord.t_Origin = " + PSPO_OR_PAYEEBANK               +
      "           and pmpaym.t_PaymentID = pspayord.t_OrderID               " +
      "         )                                                           " +
      "         or                                                          " +
      "         ( pmpaym.t_DocKind = " + PS_INRQ                              +  
      "           and psinrq.t_Origin = " + /*CP_OR_PAYEEBANK*/ 19            +
      "           and pmpaym.t_PaymentID = psinrq.t_PaymentID               " +
      "         )                                                           " +
      "         or                                                          " +
      "           pmpaym.t_DocKind = " + DLDOC_IN_PMCLAIM                     + 
      "         or                                                          " +
      "           pmpaym.t_DocKind = " + DLDOC_IN_PMCOL                       +  
      "       )                                                             " +
      "   and pmrmprop.t_PaymentID = pmpaym.t_PaymentID                     " +
      "   and db.t_PaymentID = pmpaym.t_PaymentID                           " +
      "   and db.t_DebetCredit = 0                                          " +
      "   and cr.t_PaymentID = pmpaym.t_PaymentID                           " +
      "   and cr.t_DebetCredit = 1                                          " +
      "   and pmpaym.t_PaymentID = psinrq.t_PaymentID(+)                    " +
      "   and pmpaym.t_PaymentID = pspayord.t_OrderID(+)                    " ;
      
  var paramsVars = makeArray( SQLParam("PaymentID", PaymentID ) );
  var rsVars = execSQLselect(selectVars, paramsVars);
  
  if ( rsVars and rsVars.moveNext() )  
    Reference = rsVars.value(0);
    Categories = rsVars.value(1);
    OrderAmount = rsVars.value(2);
    FutureBaseAmount = rsVars.value(3);
    Dockind = rsVars.value(4);
    StartDepartment = rsVars.value(5);
    ReceiverBankID = rsVars.value(6);
    ReceiverBankName = rsVars.value(7);
    OutCorschem = rsVars.value(8);
    OutCorschemFIID = rsVars.value(9);
    PayOrdDockInd = rsVars.value(10);
    PropStatus = rsVars.value(11);
    ValueDate = rsVars.value(12);
    PayOrdOrigin = rsVars.value(13);
    InrqOrigin = rsVars.value(14);
    
/*    
    PS_PAYORDER      = 201,  // Рублевый платежный документ клиента
    PS_INRQ          = 203,  // Инкассовое поручение к валютному счету клиента (ИПВС)
    DLDOC_IN_PMCLAIM = 463,  // Предъявленное требование
    DLDOC_IN_PMCOL   = 464,  // Предъявленное инкассовое поручение
    PSPOKIND_DEMAND  = 2,    // Рублевое платежное требование
    PSPOKIND_REQUEST = 3,    // Рублевое инкассовое поручение  
*/    
    var AutoCreateNotice = true;
    GetRegistryValue("PS\\PAYORDER\\DEMAND\\AUTOCREATENOTICE", V_BOOL, AutoCreateNotice);
    
    if ( 
         (
           (
             (
               ( Dockind == PS_PAYORDER ) 
               and
               (
                 ( PayOrdDockInd == PSPOKIND_DEMAND ) 
                  or 
                 ( PayOrdDockInd == PSPOKIND_REQUEST ) 
               ) 
               and
               ( PayOrdOrigin == PSPO_OR_PAYEEBANK ) 
             ) or
             ( 
               ( Dockind == PS_INRQ ) 
               and
               ( InrqOrigin == /*CP_OR_PAYEEBANK*/ 19 ) 
             )            
           )
           and
           ( Reference )         
           and
           ( IsInitialPayment(PaymentID) )         
         )
         or
         (
           (
             ( Dockind == DLDOC_IN_PMCLAIM ) 
             or
             ( Dockind == DLDOC_IN_PMCOL ) 
           )
           and
           ( IsExistED274ForPurposePayment(PaymentID) )
         )
       )
           
      var TpID = 0, TpSchemID = 0, RlsFormID = 0;
    
      if ( (Dockind == PS_PAYORDER) or (Dockind == PS_INRQ) )
        var findWlPmOut;
        var wlpmbuf = TBFile("wlpm.dbt", "W", 1 );
        ClearRecord( wlpmbuf );
        wlpmbuf.rec.PaymentID = PaymentID;
        wlpmbuf.rec.Direct = "";//WLD_MES_OUT
        findWlPmOut = wlpmbuf.GetGE;
        if ( (not findWlPmOut) or (wlpmbuf.rec.PaymentID != PaymentID))
          // CreateWlPmOut
          ClearRecord( wlpmbuf );
          wlpmbuf.rec.WlPmNum    = 0;
          wlpmbuf.rec.PaymentID  = PaymentID;    // Ссылка на платеж
          wlpmbuf.rec.Direct     = "";//WLD_MES_OUT
          wlpmbuf.rec.PropStatus = PropStatus;    // Выгружен во фронт-офис
          wlpmbuf.rec.PayFIID    = OutCorschemFIID;  // Валюта
          wlpmbuf.rec.Corschem   = OutCorschem; // Корсхема
          wlpmbuf.rec.ValueDate  = ValueDate;    // Дата валютирования
          wlpmbuf.Insert();
        end;
        ObjID = wlpmbuf.rec.WlPmID; //исходящей записи wlpm по платежу;
        ObjKind = OBJTYPE_PAYMENT;
      else
        ObjID = PaymentID;
        ObjKind = OBJTYPE_PMCLAIM;
      end;      
    
    
      ClearRecord(wlreq);
      wlreq.RelatedRef = Reference;
      wlreq.Direct = ""; // WLD_MES_OUT
      wlreq.Kind = MESKIND_ANSWER;
      var PartyID = -1, PartyName = "";
      GetDprtPartyIDandName(StartDepartment, @PartyID, @PartyName);
      wlreq.SubKind = WLD_SUBKIND_ANS_RET;
      wlreq.OriginatorID = PartyID;
      wlreq.OriginatorCodeKind = PTCK_BIC;
      wlreq.OriginatorCode = ПолучитьКодСубъекта(wlreq.OriginatorID, wlreq.OriginatorCodeKind);
      wlreq.OriginatorName = PartyName;
      wlreq.RecipientCode = "";
      wlreq.RecipientCodeKind = 47;
      wlreq.RecipientID = ReceiverBankID;
      wlreq.RecipientName = ReceiverBankName;
      TpID = TpSchemID = RlsFormID = 0;
      FillInitMesData(wlreq, @TpID, @TpSchemID, @RlsFormID);
      wlreq.Corschem = OutCorschem;
      wlreq.FIID = OutCorschemFIID;
      
      if( PayOrdDockInd == PSPOKIND_DEMAND )
    
        var InfoCode = int(SubStr( Queries, Index("InfoCode") + 10 ));
        if( (InfoCode == 3) or (InfoCode == 4) or (InfoCode == 5) )
    
          Queries = Queries + "\nSumPT:" + string(int(OrderAmount *100)); 
          if( (FutureBaseAmount > 0) and (FutureBaseAmount < OrderAmount) )
    
            Queries = Queries + "\nAcptSum:" + string(int(FutureBaseAmount *100));
    
            var select = " select step.t_Fact_Date " +
                         "  from doprstep_dbt step, doproper_dbt opr, dpmpaym_dbt pm" +
                         "   where step.t_ID_Operation = opr.t_ID_Operation " +
                         "     and step.t_Kind_Action  = 205 " +  // PM_OPR_KA_ACCEPTI1
                         "     and opr.t_DocKind       = pm.t_PrimDocKind " +
                         "     and opr.t_DocumentID    = lpad(:PAYMENTID, 34, 0) ";
            var params = makeArray( SQLParam("PAYMENTID", PaymentID ) );
            var rs = execSQLselect(select, params);
            if( rs and rs.moveNext )
              var FDate:date;
              DtTmSplit( rs.Value(0), FDate );
              Queries = Queries + "\nAcptDate:" + YYYYMMDD( FDate ); // YYYY-MM-DD
            end;
          end;
        end;
      end;
    
      wlreq.Queries = Queries;
    
      var err = CreateQuery(wlreq, Narrative, "", RlsFormID, TpSchemID, true, ID_Oper, ID_Step, TpID, ObjID, ObjKind);
    
      if( err == 0 )      
        var Trn : string = GetTrnByMesLnk(wlreq.ReqID, OBJTYPE_REQ, "");
        msg = "Сформировано уведомление ED274 о результатах приема к исполнению. " +
              "Референс сообщения " + Trn + ". В сообщение записан код " + GetCodeNum(wlreq) + ", " +
              "документ доступен для редактирования в п/с \"Межбанковские расчеты\"";
      else
        MemoryError( err );                
        msg = "Произошла ошибка создания уведомления ED274 о результатах " +
              "приема к исполнению: " + GetErrMsg();            
      end;
    
      if( not IsOprMultiExec() )
        msgbox(msg);
      end;
    end;
    
  end;

  return msg;
end;

