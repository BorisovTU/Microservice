/*
 $Name: fm4077u_out_acc.mac
 $Module: Ядро ГКБО 
 $Description: ФМ.4077-У: Формирование сообщения об отказах: выполнение операции 
 */
 /*
  ФМ.4077-У: Формирование сообщения об отказах: заключения и расторжения договора счета
 */


import "fm4077u_out_lib.mac";
import rsexts;

private file MessageXMLFile() txt write;

private file rjentcontracc("fmrjentcontracc.dbt") key 1 WRITE ;
private file rjcnclcontracc("fmrjcnclcontracc.dbt") key 1 WRITE ;

private file rjcncloperdoc("fmrjcncloperdoc.dbt") key 1;
private file rjcnclopercode("fmrjcnclopercode.dbt") key 1;
private file rjsrvptoper("fmrjsrvptoper.dbt") key 1;
private record fmrjsrvmsg(fmrjsrvmsg); 
private file rjsrvclnt("fmrjsrvclnt.dbt") key 0;

/*
private macro ОснованиеОп(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("ОснованиеОп");
    node.addChildEx("КодДок", rjcncloperdoc.DocKind);
    
    var DocKindName = Tbfile ("llvalues.dbt");
    DocKindName.keynum = 1;
    DocKindName.rec.List = 3565;
    DocKindName.rec.Code = rjcncloperdoc.DocKind;
    DocKindName.GetEQ();
    node.addChildEx("НаимДок", DocKindName.rec.Name);
    
    if (StrLen(rjcncloperdoc.DocName) > 0)
        node.addChildEx("ИноеНаимДок", rjcncloperdoc.DocName);
    end;
    
    if (rjcncloperdoc.DocDate != ZeroValue(V_DATE))
        node.addChildEx("ДатаДок", rjcncloperdoc.DocDate);
    end;
    
    if (StrLen(rjcncloperdoc.DocNumber) > 0)
        node.addChildEx("НомДок", rjcncloperdoc.DocNumber);
    end;
    node.addChildEx("СодДок", rjcncloperdoc.DocSummary);
end;
*/
private macro Участник(PPrm : TRlBackRjMsg_Out_Prm, node)
//    var node = Parent.addChild("Участник");
    
    node.addChildEx("ТипКлиента", rjsrvclnt.ClientType);
    
    var Sign = "1";
    if (rjsrvclnt.IsNotResident == "X")
        Sign = "0";
    end;
    node.addChildEx("ПризнакКлиента", Sign);
    
    if (rjsrvclnt.ClientType == RJSRVCLNT_CT_LEGAL)
        СведенияЮЛ(PPrm, node, rjsrvclnt.ID);
    elif ( (rjsrvclnt.ClientType == RJSRVCLNT_CT_PERSN) OR  (rjsrvclnt.ClientType == RJSRVCLNT_CT_INDIVID) OR (rjsrvclnt.ClientType == RJSRVCLNT_CT_PERSN_PRA) )
        СведенияФЛ(PPrm, rjsrvclnt.ID, rjsrvclnt.ClientType, node);
    elif (rjsrvclnt.ClientType == RJSRVCLNT_CT_FOREIGN_ENT)
        СведИНБОЮЛ(PPrm, node, rjsrvclnt.ID);
    end;

end;

private macro СведОтказЗаключения(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведОтказ");
    rjentcontracc.RecNumber = FormatRecNumber(rjentcontracc.RecNumber);
    Update(rjentcontracc);
    node.addChildEx("НомерЗаписи", rjentcontracc.RecNumber);
    node.addChildEx("ТипЗаписи", rjentcontracc.RecType);
    
    if (rjentcontracc.UsedCBInfo == "X")
        node.addChildEx("ПризнакИнф", "1");
    else
        node.addChildEx("ПризнакИнф", "0");
    end;
    
    var RejectGround = "03";
    var stat;

    node.addChildEx("КодОтказа", RejectGround);
    
    if (PPrm.MsgBuf.Version != FMRJSRVMSG_VER_1_1)
        var Has0000 = false;
        var FailureCode = Tbfile ("fmrjfailurecode.dbt");
        FailureCode.keynum = 1;
        FailureCode.rec.RjContrAccID = rjentcontracc.ID;
        stat = FailureCode.GetGE();
        while (stat)
            if (FailureCode.rec.RjContrAccID == rjentcontracc.ID)
                node.addChildEx("КодПричинаОтказ", FailureCode.rec.Code);
                
                if (FailureCode.rec.Code == "0000")
                    Has0000 = true;
                end;
            end;
            stat = FailureCode.next();
        end;
        
        if (Has0000)
            node.addChildEx("ИнаяПричинаОтказа", rjentcontracc.OtherReason);
        end;
    end;
    
    node.addChildEx("ДатаОтказа", FormatDate(rjentcontracc.RejectDate));

    if(rjentcontracc.RjSrvClntID > 0)
      ClearRecord(rjsrvclnt);
      rjsrvclnt.ID = rjentcontracc.RjSrvClntID;
      stat = GetEQ(rjsrvclnt);

      if(stat)
        Участник(PPrm, node);
      end;
    end;

    if (rjentcontracc.AddInfo != "")
        node.addChildEx("Коммент", rjentcontracc.AddInfo);
    end;
    
    if ((PPrm.MsgBuf.Version != FMRJSRVMSG_VER_1_1) and 
        ((rjentcontracc.RecType == RJENTCONTRACC_DELETE) or (rjentcontracc.RecType == RJENTCONTRACC_REM_GROUND) or (rjentcontracc.RecType == RJENTCONTRACC_ABS_GROUND))
       )
        node.addChildEx("ПричинаУдаления", rjentcontracc.DeleteReason);
    end;
end;
                                          
private macro СведОтказРасторжения(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведОтказ");
    rjcnclcontracc.RecNumber = FormatRecNumber(rjcnclcontracc.RecNumber);
    Update(rjcnclcontracc);
    node.addChildEx("НомерЗаписи", rjcnclcontracc.RecNumber);
    node.addChildEx("ТипЗаписи", rjcnclcontracc.RecType);
    
    if (rjcnclcontracc.UsedCBInfo == "X")
        node.addChildEx("ПризнакИнф", "1");
    else
        node.addChildEx("ПризнакИнф", "0");
    end;
    
    var RejectGround = "09";

    node.addChildEx("КодОтказа", RejectGround);
    node.addChildEx("ДатаОтказа", FormatDate(rjcnclcontracc.RejectDate));
    //node.addChildEx("ДатаУведомления", FormatDate(rjcnclcontracc.ClientNotifyDate));

    if(rjcnclcontracc.RjSrvClntID > 0)
      ClearRecord(rjsrvclnt);
      rjsrvclnt.ID = rjcnclcontracc.RjSrvClntID;
      var stat = GetEQ(rjsrvclnt);

      if(stat)
        Участник(PPrm, node);
      end;
    end;

    if (rjcnclcontracc.AddInfo != "")
        node.addChildEx("Коммент", rjcnclcontracc.AddInfo);
    end;
    
    if ((PPrm.MsgBuf.Version != FMRJSRVMSG_VER_1_1) and 
        ((rjcnclcontracc.RecType == RJENTCONTRACC_DELETE) or (rjcnclcontracc.RecType == RJENTCONTRACC_REM_GROUND) or (rjcnclcontracc.RecType == RJENTCONTRACC_ABS_GROUND))
       )
        node.addChildEx("ПричинаУдаления", rjcnclcontracc.DeleteReason);
    end;
end;

private macro СведПредстав(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведПредстав");
    
    if (rjsrvmsgidprt.SourceDprt != PPrm.MsgBuf.SourceDprt)
        node.addChildEx("ПризнакПредставСвед", "1");

        ИнфФилиал(PPrm, node);
    else
        node.addChildEx("ПризнакПредставСвед", "0");
    end;
    
    if(PPrm.MsgBuf.MsgKind == RJSRVMSG_MSGKIND_REJ_ACC_CNTR) // отказы от заключения договоров счета
      ClearRecord(rjentcontracc);
      rjentcontracc.RjSrvMsgIDprtID = rjsrvmsgidprt.ID;
      var stat = GetGE(rjentcontracc);
      
      while(stat AND (rjentcontracc.RjSrvMsgIDprtID == rjsrvmsgidprt.ID))
          СведОтказЗаключения(PPrm, node);
          stat = next(rjentcontracc);
      end;
    elif(PPrm.MsgBuf.MsgKind == RJSRVMSG_MSGKIND_TERM_ACC_CNTR) // расторжения договоров счета
      ClearRecord(rjcnclcontracc);
      rjcnclcontracc.RjSrvMsgIDprtID = rjsrvmsgidprt.ID;
      stat = GetGE(rjcnclcontracc);
      
      while(stat AND (rjcnclcontracc.RjSrvMsgIDprtID == rjsrvmsgidprt.ID))
          СведОтказРасторжения(PPrm, node);
          stat = next(rjcnclcontracc);
      end;
    end;
end;
                      
// Процедура формирования сообщения об отказе заключения и расторжения договора счета
macro FM_Out_RjSrvMsgContrAcc(memMsgBuf, sFileName)
    record MsgBuf( "fmrjsrvmsg.dbt" );

    SetBuff( MsgBuf, memMsgBuf );

    var prm = TRlBackRjMsg_Out_Prm();
    prm.MsgBuf = MsgBuf;
    
    prm.xml.createProcessingInstruction("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
    
    if (FM_Out_RjSrvMsgCommonInfo(prm))
        var inform : FmXmlNodeElement;
        inform  = prm.root.addChild("ИнформЧасть");
        ИнфБанк(prm, inform);

        var stat = true;
        ClearRecord(rjsrvmsgidprt);
        rjsrvmsgidprt.RjSrvMsgID = prm.MsgBuf.ID;
        keynum(rjsrvmsgidprt, 1);
        stat = GetGE(rjsrvmsgidprt);
        while(stat AND (rjsrvmsgidprt.RjSrvMsgID == prm.MsgBuf.ID))
            СведПредстав(prm, inform);
            stat = next(rjsrvmsgidprt);
        end;
    end;
    
    return FM_Out_RjSrvMsgSave(MessageXMLFile, sFileName, prm.xml);
end;

