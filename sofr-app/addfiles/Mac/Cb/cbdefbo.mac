/*
$Name:               cbdefbo.mac
$Module:            АРМ Позиционера
$Description:      Разноска принятых закрытых платежей по бэкам-получателям
*/
/* ---------------------------------------------------------------------------
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : rminpm.mac
  Created     : 25.07.00
  Programmer  : Alex V. Mishin
  Description : Проверки ответного платежа
                Функция пользователя  
                Новый_Документ
                Разноска принятых закрытых платежей по бэкам-получателям
----------------------------------------------------------------------------*/
import PaymInter, globals, "rmtools.mac", "likepy.mac", "bnk_common.mac", "rmcmptl.mac";

const ПутьКНастройкеПредобработка = "COMMON\\ПРЕДОБРАБОТКА\\";

private macro СтрокаНенулевая(Строка)
  var i = 1, Символ;
  while(i <= Strlen(Строка))
       Символ = substr(Строка, i, 1);
       if(Символ != "0")
           return true;
       end;
       i = i + 1;
  end;
  return false;
end;

//-----------------------------------------------------------------------------
// Является ли субъект юридическим лицом
//-----------------------------------------------------------------------------
PRIVATE MACRO IsOrganisation( PartyID ):bool

  VAR select:string = " select party.T_LEGALFORM " +
                        " from dparty_dbt party "+
                       " where party.T_PARTYID = :PartyID";
  VAR params:TArray = makeArray( SQLParam( "PartyID", PartyID ) );
  VAR rset:RsdRecordset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    if( rset.value(0) == PTLEGF_INST )
      return true;
    end;
  end;

  return false;

END;

/****************************************************************************/
/*           Разноска принятых закрытых платежей по бэкам-получателям       */
/*                                                                          */
/* Макрофункции передается запись первичного документа, в результате        */
/* возвращается символ бэк-офиса-получателя, который присваивается          */
/* полученному платежу.                                                     */
/*                                                                        */
/* Процедура возвращает:
    * Строковую переменную из 1 символа в случае если ей удалось определить
      бэк-офис-получатель.
    * Пустую строку, если не удалось определить или этот платеж ни для одного
      из нужных бэк-офисов.                                                 */
/****************************************************************************/
macro DefineToBackOffice( PaymentObj:RsbPayment, fromInPmPrePro )
  var stat, СписокБэкофисов = "", err = 0, i = 0, j = 0, 
      Офис = "",  МассивСтрок:TArray, Значения;
  var DKFlag = "", FlagTrans = 0;
  var IsMasksGround = false;
  FlagTrans = PaymentObj.IsTransit();  
  var TypeAccount:string = "";
  var Client:integer = 0;  

  if( FlagTrans )
      return "";
  end;

  GetRegistryValue( ПутьКНастройкеПроводитьЛиБухУчет, V_STRING, СписокБэкофисов, err );

  // Если символ уже есть - то просто его проверим
  if( PaymentObj.ToBackOffice != "" )
    // Если бэк-офис имеется в настройке, то уходим из алгоритма
    if( Index( СписокБэкофисов, PaymentObj.ToBackOffice ) )
      return PaymentObj.ToBackOffice;
    end;
  elif( fromInPmPrePro )
    if( PaymentObj.IsCredit() )
      if( (PaymentObj.ReceiverAccount != "") AND (PaymentObj.PayerFIID != NATCUR)
        AND СчетСуществуетИОткрыт( PaymentObj.ReceiverFIID, PaymentObj.ReceiverAccount, CHAPT1, NULL, TypeAccount, Client)
        AND CчетПолучателяКлиентский( PaymentObj ) AND (Index(TypeAccount,"Ч") OR Index(TypeAccount, "X"))
        AND ( IsOrganisation( Client ) OR IsIndividualEmployer( Client ) )
        )
        PaymentObj.ToBackoffice = "Ч";
        if( CheckPaymReceiverName( PaymentObj.PaymentID, null, fromInPmPrePro ))
          PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, "Указано неверное наименование клиента по счету № " + PaymentObj.ReceiverAccount );
        end;
      end;
    end;
  end;

  while(i < Strlen(СписокБэкофисов))
    /*Проверить инсталляцию*/
    Офис = substr(СписокБэкофисов, i+1, 1);
    /*Сначала смотрим на счет*/
    if((PaymentObj.ReceiverGroup == PAYMENTS_GROUP_INTERNAL) and PaymentObj.ReceiverAccount and (СтрокаНенулевая(PaymentObj.ReceiverAccount)))
        GetRegistryValue( string(ПутьКНастройкеПредобработка, Офис, "\\СЧЕТА"), V_STRING, Значения, err );
        if((not err) and (Значения))
            if( CompareStrWithMasks( Значения, PaymentObj.ReceiverAccount ) == 0 )
                return Офис;
            end;
        end;
    end;
    /*Если по счету БО не определился, смотрим на назначение платежа независимо от заполненности счета.*/ 
    GetRegistryValue( string(ПутьКНастройкеПредобработка, Офис, "\\НАЗНАЧЕНИЯ ПЛАТЕЖА"), V_STRING, Значения, err );
    if((not err) and (Значения))
        
        // Если в настройке находим символы '*' или '?', 
        // то считаем, что вся настройка задана масками
        if(Index(Значения, "*") or Index(Значения, "?"))
          IsMasksGround = true;
        else
          IsMasksGround = false;
        end;

        МассивСтрок = map( split( Значения, ";" ), @Trim );
        МассивСтрок = map( МассивСтрок, @StrUpr );
        j = 0;
        while(j < МассивСтрок.Size)
             if( (IsMasksGround and (CompareStrWithMasks( МассивСтрок[j], StrUpr(PaymentObj.Ground), 257) == 0) ) or
                 ((not IsMasksGround) and index( StrUpr(PaymentObj.Ground), МассивСтрок[j] ))
               )
                 return Офис;
             end;
             j = j + 1;
        end;
    end;     
    i = i + 1;
  end;

  return ""; /* По умолчанию символ бэк-офиса не определен, производим бух.учет в АРМ Позиционра */
end;
