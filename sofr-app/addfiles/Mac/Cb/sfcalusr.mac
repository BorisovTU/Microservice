/*
 $Name: sfcalusr.mac
 $Module: Ядро ГКБО 
 $Description: пример макроса расчета
*/


/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    RS-Bank 5.10

    ПЗО


    Это пример макроса расчета
    для пользовательского алгоритма расчета.

    Здесь приведены образцы макрофункций для расчета как периодических(CalcServiceSum), 
    так и единовременных(CalcCommissionSum) комиссий.

    Для периодических комиссий, здесь же должны быть 3 процедуры для печати подробного протокола о расчете.


                                        Mikhailov S., 13.08.2001, Mon
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/


import CTInter, sfinter, bankinter, rsd;/*InsertSumList*/

/*
    Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение
*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины
enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

private const NATCUR = 0;
    record sfbassum( "sfbassum.str" );

/*
    Расчет для единовременных комиссий
*/
macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);

    var stat:integer; 
    var quont : integer;/*для возврата количества*/
    

/*    msgbox(begindate, "    ", enddate);*/

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );

    ClearRecord(sfbassum);

    sfbassum.baseType = SF_BASETYPE_QUONT;
    sfbassum.baseSum  = 1;

    sfbassum.baseType2 = SF_BASETYPE_SUM;
    sfbassum.baseSum2  = $10;
    sfbassum.FIID_baseSum2 = NATCUR;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы: ", GetErrMsg );
    end;

    sfbassum.baseType = SF_BASETYPE_QUONT;
    sfbassum.baseSum  = 1 ;

    sfbassum.baseType2 = SF_BASETYPE_SUM;
    sfbassum.baseSum2  = $20;
    sfbassum.FIID_baseSum2 = NATCUR;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы: ", GetErrMsg );
    end;

    
    
    /*return integer( 1 ); -- количество*/
    
    /*return $1; -- сумма */
    
end;/*CalcCommissionSum*/




/*
    Расчет для периодических комиссий
*/
macro CalcServiceSum( sfcontr_addr/*Договор обслуживания*/, BeginDate/*Начало периода*/, EndDate/*Конец периода*/ )
    record sfcontr( sfcontr );
    var stat;
    SetBuff( sfcontr, sfcontr_addr );
    
    ClearRecord(sfbassum);

    sfbassum.baseType = SF_BASETYPE_SUM;
    sfbassum.baseSum  = $40000;

    sfbassum.baseType2 = SF_BASETYPE_SUM;
    sfbassum.baseSum2  = $40000;
    sfbassum.FIID_baseSum2 = NATCUR;

    sfbassum.BaseObjectType = OBJTYPE_DOCUMENT; /*Вид документа*/
    sfbassum.BaseObjectID   = 0; /*Doc.DocID;*/   /*ИД документа*/

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/




macro CalcServiceSumBatch()
    var stat = true;

    var cmd = RsdCommand;
    cmd.CmdText = "INSERT INTO dsfbasesum_tmp "
                  "            (t_feetype, t_sfdefid, t_calcalid, t_concomid, t_basetype, "
                  "             t_basesum, t_basequont, t_fiid_basesum, t_basetype2, t_basesum2, "
                  "             t_basequont2, t_fiid_basesum2, t_baseobjecttype, t_baseobjectid,"
                  "             t_commsum, t_docnum, t_docdate, t_docsum, t_docfiid, "
                  "             t_docpayeraccount, t_docreceiveraccount) "
                  "   (SELECT 1, parms.t_sfdefid, parms.t_calcalid, parms.t_concomid, 1, 40000, "
                  "           0, 0, 1, 40000, 0, 0, 1, '0', 0, '0', "
                  "          TO_DATE ('01010001', 'DDMMYYYY'), 0, 0, CHR (0), CHR (0) "
                  "      FROM dsfcontr_dbt contr, dsfbasesumparms_tmp parms "
                  "      WHERE contr.t_id = parms.t_contrid) ";

    cmd.execute();

    return stat;
end;/*CalcServiceSumBatch*/



macro CalcCommissionSumBatch()
    var stat = true;
    var cmd = RsdCommand;
    cmd.CmdText = "INSERT INTO dsfbasesum_tmp "
                  "            (t_feetype, t_sfdefid, t_calcalid, t_concomid, t_basetype, "
                  "             t_basesum, t_basequont, t_fiid_basesum, t_basetype2, t_basesum2, "
                  "             t_basequont2, t_fiid_basesum2, t_baseobjecttype, t_baseobjectid,"
                  "             t_commsum, t_docnum, t_docdate, t_docsum, t_docfiid, "
                  "             t_docpayeraccount, t_docreceiveraccount) "
                  "   (SELECT 1, parms.t_sfdefid, parms.t_calcalid, parms.t_concomid, 1, 40000, "
                  "           0, 0, 1, 40000, 0, 0, 1, '0', 0, '0', "
                  "          TO_DATE ('01010001', 'DDMMYYYY'), 0, 0, CHR (0), CHR (0) "
                  "      FROM dsfcontr_dbt contr, dsfbasesumparms_tmp parms, druledefobjcom_tmp defobj "
                  "      WHERE contr.t_id = parms.t_contrid AND defobj.t_ContrID = contr.t_ID "
                  "        AND parms.t_ID_Operation = defobj.t_ID_Operation AND parms.t_ID_Step = defobj.t_ID_Step)";

    cmd.execute();

    return stat;
end;/*CalcCommissionSumBatch*/


/*
    Печать заголовка подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*
    Печать строчки отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*
    Печать окончания отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

