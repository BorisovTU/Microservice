//FileManager.mac
import "cblogger2.mac", rsexts, oralib;

/*
  Какие-то максимально общие механизмы по работе с файлами.
  Можно ещё добавить коипрование в подпапки done и err
*/
class FileManager()
  private var logger = NewLogger(c_logger.IT_LOG_TYPE, "FileManager");

  //копирует файл на сервер приложений
  //на вход полное имя файла
  //возвращает путь к файлу на сервере приложений
  macro CopyToAppServ(fullNameFile:string):string
    if(isStandAlone()) //Уже на СП, копировать не нужно
      return fullNameFile;
    end;

    var FileName, ExtName;
    var appServPath;

    //Отделяем путь файла от имени
    SplitFile(fullNameFile, FileName, ExtName);
    appServPath = "..\\workfile\\" + FileName + ExtName;
    if (not CopyFile("$" + fullNameFile, appServPath))
      appServPath = "";
      logger.Error("Error copy file to appserv " + fullNameFile);
    end;

    return appServPath;
  end;

  macro RemoveFromAppServ(fullNameFile:string)
    if(not isStandAlone()) // когда мы на терминале, удаляем файл на СП
      if (not RemoveFile(fullNameFile))
        logger.Error("Error remove file " + fullNameFile);
      end;
    end;
  end;

  //сохраняет файл в itt_file
  macro SaveToIttFile(fileDir:string, fileName:string)
    var query = "begin :fileID := it_file.insert_file(:fileDir, :fileName); end;";
    var params = TArray();
    params(params.size()) = SqlParam("fileID", V_INTEGER, RSDBP_OUT);
    params(params.size()) = SqlParam("fileDir", fileDir);
    params(params.size()) = SqlParam("fileName", fileName);

    ExecSql(query, params);
    return params.value(0).value;
  end;

  //Возвращает ИД файла из таблицы itt_file
  //если withSaving = true, то файл сохраняется в itt_file, если не был найден
  macro GetItFileID(fullNameFile:string, withSaving:bool):integer
    var query = "select id_file from itt_file where file_name = :filename order by create_sysdate desc";

    var FileName, ExtName;

    //Отделяем путь файла от имени
    var dirName = SplitFile(fullNameFile, FileName, ExtName);
    var sql = ExecSQLselectPrmDyn(query, FileName + ExtName);
    if (sql.MoveNext())
      return sql.value("id_file");
    end;

    //сюда дошли только если не нашли файл ранее
    var fileID = 0;
    if (withSaving)
      fileID = SaveToIttFile(dirName, FileName + ExtName);
    end;
    
    return fileID;
  end;

end;