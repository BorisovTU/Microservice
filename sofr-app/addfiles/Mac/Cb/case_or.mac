/*
$Name: case_or.mac
$Module: Ядро ГКБО
$Description: Макрос шага "Формирование оценочного резерва"
*/

Import "case_cls.mac", "res_carry.mac";

record AccCaseParm("accasepm.dbt");

/*
    Пользовательская часть шага
*/
macro ExecuteStep( doc, primdoc, DocKind, ID_Operation )

  file   accaseFile("accase.dbt");
  record accasscs(accasscs);
  record accase(accase);
  
  var IncomeReserveAccount : string;
  var OutlayReserveAccount : string;
  var ReserveAccount : string;
  var accasePD : AccCasePrimdoc;
  var query, rs;
  var params : TArray;
  var queryCase, rsCase;
  var paramsCase : TArray;
  var ReserveType : integer;
  var ReserveKind : integer;
  var ClassifReserve : string;
  var IncomeCatCodePrm;
  var OutlayCatCodePrm;

  SetBuff( accasscs, primdoc );

  accaseFile.CaseID = accasscs.CaseID;

  if( GetEQ( accaseFile ) )
    Copy( accase, accaseFile );
  end;
   
  accasePD = AccCasePrimdoc( accase, AccCaseParm, accasscs );

  ReserveAccount = FindReserveAccount(accasePD, AccOprServDoc.Date);
  if(Trim(ReserveAccount) == "")
    return 0;
  end;

  if(((accasscs.DateTo != date(0,0,0)) and (accasscs.DateTo <= AccOprServDoc.Date)) or
     (ReserveEstimated == $0))
    IncomeReserveAccount = FindReserveEstimatedAccountIncome( accasePD, AccOprServDoc.Date );
    OutlayReserveAccount = FindReserveEstimatedAccountOutlay( accasePD, AccOprServDoc.Date );
    if((Trim(IncomeReserveAccount) == "") or (Trim(OutlayReserveAccount) == ""))
      return 0;
    end;
  else
    IncomeReserveAccount = OpenReserveEstimatedAccountIncome( accasePD, AccOprServDoc.Date );
    OutlayReserveAccount = OpenReserveEstimatedAccountOutlay( accasePD, AccOprServDoc.Date );
  end;
  
  if((Trim(IncomeReserveAccount) == "") or (Trim(OutlayReserveAccount) == ""))
    msgbox("Не найден счет оценочного резерва.");
    return 1;
  end;

  query =         "SELECT t_reservetype ";
  query = query + "  FROM daccassub_dbt ";
  query = query + " WHERE t_caseid = :CaseSubID ";
  query = query + "   AND t_datefrom = :DateFrom ";
  params = makeArray(SQLParam("CaseSubID", accasscs.CaseSubID)
                    ,SQLParam("DateFrom", accCaseParm.DateFrom)
                    );
  rs = execSQLselect(query, params, true);
  queryCase =             "SELECT t_reservekind ";
  queryCase = queryCase + "  FROM daccase_dbt ";
  queryCase = queryCase + " WHERE t_caseid = (SELECT t_caseid FROM daccasscs_dbt WHERE t_casesubid = :CaseSubID )";
  queryCase = queryCase + "   AND t_datefrom = :DateFrom ";
  paramsCase = makeArray(SQLParam("CaseSubID", accasscs.CaseSubID)
                    ,SQLParam("DateFrom", accCaseParm.DateFrom)
                    );
  rsCase = execSQLselect(queryCase, paramsCase, true);
  if(rs and rs.moveNext())
    ReserveType = rs.value(0);
    if(rsCase and rsCase.moveNext())
      ReserveKind = rsCase.value(0);
      ClassifReserve = GetCaseReserveClassif(ReserveKind, ReserveType, accase);
      GetIncomeOutlayCatCodeEstimated(ClassifReserve, @IncomeCatCodePrm, @OutlayCatCodePrm, AccOprServDoc.Date);
    end;
  end;
  
  if((IncomeCatCodePrm == "") or (OutlayCatCodePrm == ""))
    if(ReserveKind == 1)
      msgbox("Не задана категория средств для классификации РВП \"", ClassifReserve, "\"");
    elif(ReserveKind == 2)
      msgbox("Не задана категория средств для классификации РВПC \"", ClassifReserve, "\"");
    end;
    return 1;
  else
    if(not CreateReserveEstimatedCarry(accasePD, AccOprServDoc.Date, ReserveAccount, IncomeReserveAccount, OutlayReserveAccount, IncomeCatCodePrm, OutlayCatCodePrm))
      msgbox("Не удалось создать проводку резерва.");
      return 1;
    end;
  end;

  if((accasscs.DateTo != date(0,0,0)) and (accasscs.DateTo <= AccOprServDoc.Date) and (Trim(IncomeReserveAccount) != ""))
    CloseReserveEstimatedAccountIncome(accasePD, AccOprServDoc.Date);
  end;
  if((accasscs.DateTo != date(0,0,0)) and (accasscs.DateTo <= AccOprServDoc.Date) and (Trim(OutlayReserveAccount) != ""))
    CloseReserveEstimatedAccountOutlay(accasePD, AccOprServDoc.Date);
  end;
  
  return 0;

end;
