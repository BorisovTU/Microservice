/*
$Name:             ws_validation.mac
$Module:           АРНУ 
$Description:      Валидация
*/
    import BankInter;
    import "cb_sql.mac";

    class TRsMessageBoxButtons
        const None = 0;
        const Ok = 1;
        const Yes = 2;  
        const No = 4;
        const Cancel = 8;
        const ContinueBtn = 16;
        const YesNo = Yes + No;
        const YesNoCancel = Yes + No + Cancel;
        const OkCancel = Ok + Cancel;
        const OkCancelContinue = Ok + Cancel + ContinueBtn;
        const All = Ok + Yes + No + Cancel + ContinueBtn;
    end;
    const RsMessageBoxButtons = TRsMessageBoxButtons;

    class TMessageSeverity
        const ValidationError = 0;
        const RuntimeError = 1;
        const Warning = 2;
        const Info = 3;
    end;
    const MessageSeverity = TMessageSeverity;

    class TMessageLevel
        const Field = 0;
        const Object = 1;
    end;
    const MessageLevel = TMessageLevel;

    class QuestionBoxInfo(_title, _buttons, _defaultButton)
        var Title : string = "";
        var Buttons : integer = RsMessageBoxButtons.YesNo;
        var DefaultButton : integer = RsMessageBoxButtons.Yes;

        // .ctor
        if (_title != null)
            Title = _title;
        end;
        if (_buttons != null)
            Buttons = _buttons;
        end;
        if (_defaultButton != null)
            DefaultButton = _defaultButton;
        end;
    end;

    // обязательные Severity, (Code или Message)
    class WsErrorMessage
        var Severity : integer = MessageSeverity.RuntimeError;
        var Code : integer = 0;
        var Message : string = "";
        var Help : string = ""; //help page
        var PropertyName : string = "";
    end;

    // обязательные Severity, (Code или Message)
    class WsQuestion
        var Code : integer = 0;
        var Message : string = "";
        var QuestionInfo : QuestionBoxInfo = null; 
        var Help : string = ""; //help page
    end;

    class QuestionBoxResponse
        var Code : integer = 0;
        var ResultButton : integer = 0;
    end;

    class WebServiceRequest(_userObject)
        var userObject = null;
        var errorMessages = TArray(); // of WsErrorMessage
        var questionBoxResponses = TArray(); // of QuestionBoxResponse
        // .ctor
        userObject = _userObject;
    end;

    class WebServiceResponse(_userObject)
        var UserObject = null;
        var ErrorMessages = TArray(); // of WsErrorMessage
        var Questions = TArray(); // of WsQuestion
        // .ctor
        userObject = _userObject;
    end;

    macro WSRunError(
        macroName : String
       ,err : Variant
       ,stat : Integer
    )
        RunError( "Макрос: " + macroName + ", Строка: " + err.Line + " , Сообщение: " + err.Message, stat );
    end;

    macro GetErrorMsgEx( stat : Integer /*, formatParam : Variant*/ ) : String
        var exp : String = "";
        var paramCount : Integer = 1;
        var formatParam : Variant = null;
        var errMsg : String = "";

        DisplayError();

        exp = "CreateErrMsg(" + String( stat );
        while( GetParm( paramCount, formatParam ) )
            exp = exp + ", ";
            if(ValType(formatParam) == V_STRING)
              exp = exp +  String( "\"", StrSubst(formatParam ,"\"",  "'"  ),"\"" );
            else
              exp = exp +  String( formatParam );
            end;
            paramCount = paramCount + 1;
        end;
        exp = exp + ");";

        ExecExp( exp );

        errMsg = GetErrMsg();

        InitError();

        return errMsg;
    end;

    macro GetErrorMsg( Number:integer ):string
        var RetVal:string = "";
        var query = RSDCommand(" SELECT T_CONTENTS FROM DBANK_MSG WHERE T_NUMBER = ? ");

        query.NullConversion = true;
        query.addParam( "", RSDBP_IN, Number );
        query.execute();

        var ds = TRsbDataSet(query);

        if( ds.MoveNext() )
         RetVal = SQL_ConvTypeStr(ds.Contents);
        end;

        return RetVal;
    end;

    class WebResponseBuilder
        private var response = WebServiceResponse;
        private var _hasErrors : bool = false;

        macro AddMessage(error : WsErrorMessage)
            if ((not _hasErrors)
            and ((error.Severity == MessageSeverity.ValidationError)
              or (error.Severity == MessageSeverity.RuntimeError)))
                _hasErrors = true;
            end;
            response.ErrorMessages[response.ErrorMessages.size] = error;
        end;
        macro AddQuestion(question : WsQuestion)
            response.Questions[response.Questions.size] = question;
        end;
        macro SetUserObject(obj)
            response.UserObject = obj;    
        end;
        macro Result()
            return response;
        end;
        macro HasErrors()
            return _hasErrors;
        end;
        macro AddErrorEx(
            Code : Integer
           ,Severity : Integer
           ,Message : String
        )
            var errorMessage = null;

            if( ValType( Code ) == V_INTEGER )
                errorMessage = WsErrorMessage();

                errorMessage.Code = Code;

                if( ( ValType( Message ) == V_STRING )
                and ( Message != "" ) )
                    errorMessage.Message = Message;
                else
                    errorMessage.Message = GetErrorMsg( Code );
                end;

                errorMessage.Message = StrSubst( errorMessage.Message, "|", " " );

                if( ( ValType( Severity ) == V_INTEGER )
                and ( ( Severity == MessageSeverity.ValidationError )
                   or ( Severity == MessageSeverity.RuntimeError )
                   or ( Severity == MessageSeverity.Warning )
                   or ( Severity == MessageSeverity.Info ) ) )
                    errorMessage.Severity = Severity;
                else
                    errorMessage.Severity = MessageSeverity.ValidationError;
                end;

                AddMessage( errorMessage );
            end;
        end;
        macro AddError(
            Code : Integer
        )
            AddErrorEx( Code, MessageSeverity.ValidationError );
        end;
        macro AddWarning(
            Code : Integer
        )
            AddErrorEx( Code, MessageSeverity.Warning );
        end;
    end;
