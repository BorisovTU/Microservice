/*
 * Общие функции для печати отчетов процедур исправления лицевых счетов
 */
Import PTInter, rsd, "globals.mac";

/* получить ФИО операциониста */
private macro GetOperName( Oper : integer ) : string
  
  var OperName : string;

  file person("person.dbt") key 0;

  OperName = "";

  person.Oper = Oper;
  if( GetEQ(person) )
    
    OperName = person.Name;

  end;

  return OperName;

end;

/* получить полное наименование узла ТС */
private macro GetDprtString( DprtID : integer ) : string

  var DprtString : string;

  record party("party.dbt");
  file dp_dep("dp_dep.dbt") key 0;

  DprtString = "";

  dp_dep.Code = DprtID;

  if( GetEQ(dp_dep) )
    
    DprtString = dp_dep.Name;

    if( ПолучитьСубъекта(dp_dep.PartyID, party) == 0 )
      DprtString = DprtString + " " + party.Name;
    end;
  end;

  return DprtString;

end;

/* получить наименование главы учета */
private macro GetChapterString( Chapter : integer ) : string

  var ChapterString : string;

  file obchaptr("obchaptr.dbt") key 0;

  ChapterString = Chapter;

  obchaptr.Chapter = Chapter;

  if( GetEQ(obchaptr) )
    
    ChapterString = ChapterString + " " + obchaptr.Name;
  
  end;

  return ChapterString;

end;


/* Печать заголовка отчета */
macro PrintHeader( Head :string, Count : integer, Chapter : integer, Balance : string, AccountMask : string )
  
  [#] (Head);
  println;
  [Дата:           ########## #########] ( date, time );
  [Операционист:   ##### #] ( {oper}:r, GetOperName({oper}) );
  [Филиал:         #] ( GetDprtString({OperDprt}):w );
  println;

  if( Chapter )
    [Глава плана счетов:        #] ( GetChapterString(Chapter) );
  end;

  if( Balance )
    [Номер балансового счета:   #] ( Balance );
  end;

  if( AccountMask )
    [Номер счета:               #] ( AccountMask );
  end;

  println;

  if( Count == 0 )
    [Процедура не выявила счетов для исправления];
  end;

end;

/* Печать заголовка таблицы */
macro PrintTableHeader()
  
  [+----------------------------+----------------------+--------------------+-------------------+];
  [|    НОМЕР ЛИЦЕВОГО СЧЕТА    | НАЗВАНИЕ АТРИБУТА    | СТАРОЕ ЗНАЧЕНИЕ    | НОВОЕ ЗНАЧЕНИЕ    |];
  [+----------------------------+----------------------+--------------------+-------------------+];

end;

/* Печать низа таблицы */
macro PrintTableBottom( nAccount, nCorrect )
  
  println;
  [Исправлено лицевых счетов: ##########] ( nAccount:r );
  [        Всего исправлений: ##########] ( nCorrect:r );

end;

/* Печать строки таблицы */
macro PrintFirstTableLine( Account : string, NewDateValue : date, OldDateValue : date )

  var strNewDateValue : string;
  var strOldDateValue : string;

  if( NewDateValue == null ) strNewDateValue = "-";
  else                       strNewDateValue = string(NewDateValue:f); end;

  if( OldDateValue == null ) strOldDateValue = "-";
  else                       strOldDateValue = string(OldDateValue:f); end;

  [| ########################## | Дата                 | ################## | ################# |]
   (Account:f, strOldDateValue:l, strNewDateValue:l );
  [+----------------------------+----------------------+--------------------+-------------------+];

end;

/* Печать строки таблицы */
macro PrintTableLine( AttrName : string, NewValue : variant, OldValue : variant )
  
  var strNewValue : string;
  var strOldValue : string;

  if( NewValue == null ) strNewValue = "-";
  else                   strNewValue = string(NewValue); end;

  if( OldValue == null ) strOldValue = "-";
  else                   strOldValue = string(OldValue); end;

  [|                            | #                    | ################## | ################# |]
   (AttrName, strOldValue:r, strNewValue:r );
  [+----------------------------+----------------------+--------------------+-------------------+];

end;

/* Получить значение запроса */
macro ReadSelectValue( rs : RsdRecordset, ValueName : string ) : variant

  var val, IsNull;

  val = rs.value(ValueName, IsNull);

  if( IsNull ) val = null; end;

  return val;

end;
