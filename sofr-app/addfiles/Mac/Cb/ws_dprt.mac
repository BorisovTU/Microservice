/*
$Name:        ws_dprt.mac
$Module:      Главная книга
$Description: Макрос виджета ТС и РС
*/

import RsbDataSet, globals;
import "cb_sql.mac", "web_scroll_lib.mac";

// перечисление EDprtFilterMode
private var LISTALL                 = 0;
private var LISTOPEN                = 1;
private var LISTACTIVE              = 2;
private var LISTCLOSE               = 4;
private var LISTSUPERIOR            = 8;
private var LISTFILIAL              = 16;
private var LISTVSP                 = 32;
private var LIST_ONLINE             = 64;
private var LIST_BRANCH             = 128;
private var LIST_BRANCH_FROM_FILIAL = 256;

private var DPRT_NODETYPE_FILIAL = 1;
private var DPRT_NODETYPE_VSP    = 2;


private var DEPARTMENT_STATUS_OPEN      = 1;  // открыт
private var DEPARTMENT_STATUS_ACTIVE    = 2;
private var DEPARTMENT_STATUS_CLOSED    = 3;
private var DEPARTMENT_STATUS_SUPERIOR  = 4;

private var DEPARTMENT_ACCESS_ONLINE    = 1;  // Online

//класс TWsDepartment предоставляет информацию о узле ТС
class TWsDepartment
  var Code      : integer;
  var Name_     : string;
  var NodeType  : integer;
  var PartyID   : integer;
  var PartyName : string;
end;

macro CB_GetDprtList
(
  DprtID,     // Идентификатор узла ТС
  ParentID,   // Идентификатор родительского узла ТС
  FilterMode, // Режим отбора подчиненных узлов ТС
  Name,       // пользовательский код узла ТС
  Mode,       // Режим работы
  NameLk,     // пользовательский код узла ТС
  listLen     // длина списка
)

  var result;
  
  var query;
  
  var p;

  if(Mode == 0)

    query =
      " SELECT 1, XMLELEMENT(dprt_list, " +
      " (SELECT DBMS_XMLGEN.GETXMLTYPE  " +
      " (DBMS_XMLGEN.NEWCONTEXTFROMHIERARCHY " +
      " ('SELECT level, " +
        " XMLElement( Department, " +
        " XMLElement(Code, dep.t_Code), " +
        " XMLElement(Name, dep.t_Name), " +
        " XMLElement(NodeType, dep.t_NodeType), " +
        " XMLElement(PartyID, dep.t_PartyID), " +
        " XMLElement(PartyName, pt.t_Name)) " +
      " FROM ddp_dep_dbt dep, dparty_dbt pt " +
      " WHERE dep.t_PartyID = pt.t_PartyID ";
    
  else
    
    query =
      " SELECT dep.t_Code Code, dep.t_Name Name, pt.t_Name PartyName, dep.t_NodeType, dep.t_PartyID " +
      " FROM ddp_dep_dbt dep, dparty_dbt pt " +
      " WHERE dep.t_PartyID = pt.t_PartyID ";

  end;

  var OrderBy = "";

  if(DprtID != NULL)
    if (ValType(DprtID) == V_STRING)
      query = query + " AND dep.t_Code in (" + DprtID + ")";
    elif (ValType(DprtID) == V_INTEGER)
      query = query + " AND dep.t_Code = " + DprtID;  
    end;
  end;
  
  if( Name != NULL )
    query = query + " AND dep.t_Name = " + "'" + string(Name) + "'";
  end;
  
  if( NameLk != NULL )
    query = query + " AND LOWER (dep.t_Name) LIKE LOWER ('%" + NameLk + "%') ";
  end; 

  if( listLen != 0 )
    query = query + " AND ROWNUM < " + string(listLen);
  end;  

  if( ParentID != -1 )
  
    query = query + " start with dep.t_Code = " + ParentID;

    if( CheckBits(FilterMode, LISTSUPERIOR) )
    
      query = query + " connect by prior dep.t_ParentCode = dep.t_Code ";
      OrderBy = " DESC";
      
    else
    
      query = query + " connect by prior dep.t_Code = dep.t_ParentCode  ";
      
    end;
    
  else // ParentID не задан
  
    ParentID = 0;
    
    if( (CheckBits(FilterMode, LISTFILIAL)) OR (CheckBits(FilterMode, LISTVSP)) )
    
      ParentID = {OperDprt};
      
    elif( CheckBits(FilterMode, LIST_BRANCH) )
    
      ParentID = {OperDprtNode};
      
    elif(CheckBits(FilterMode, LIST_BRANCH_FROM_FILIAL) )
    
      ParentID = {OperDprt};
      
    end;

    query = query + " start with dep.t_ParentCode = " + ParentID;
    query = query + " connect by prior dep.t_Code = dep.t_ParentCode ";
    
  end;

  var NodeType = 0;
  
  if( CheckBits(FilterMode, LISTFILIAL) )
  
    NodeType = DPRT_NODETYPE_FILIAL;
    
  elif( CheckBits(FilterMode, LISTVSP) )
  
    NodeType = DPRT_NODETYPE_VSP;
    
  end;

  if( NodeType != 0 )
  
    query = query + " and dep.t_NodeType = " + NodeType;
    
  end;

  if( CheckBits(FilterMode, LIST_ONLINE) )
  
    query = query + " AND t_AccessMode = " + DEPARTMENT_ACCESS_ONLINE;
    
  end;

  if( not CheckBits(FilterMode, LISTALL) )
  
    if( CheckBits(FilterMode, LISTACTIVE) )
    
      query = query + " AND t_Status = " +  DEPARTMENT_STATUS_ACTIVE;
      
    end;

    if( CheckBits(FilterMode, LISTOPEN) )
    
      query = query + " AND t_Status = " +  DEPARTMENT_STATUS_OPEN;
      
    end;

    if( CheckBits(FilterMode, LISTCLOSE) )
    
      query = query + " AND t_Status = " +  DEPARTMENT_STATUS_CLOSED;
      
    end;
    
  end;

  query = query + " order siblings by dep.t_Code ";
  query = query + OrderBy;

  if(Mode == 0)
    query = query +  "')) FROM DUAL)).GETCLOBVAL() XMLDOC " +
                    " FROM DUAL";
  end;

  println ( query );

  var ds = TRsbDataSet( query );
  
  if(Mode == 0)
  
    if( ds.MoveNext() )
      result = ds.XMLDOC;
    end;
    
  else
  
    if(Mode == 1)
  
      result = TArray();
    
      while (ds.MoveNext())

        p = TWsDepartment();

        p.Code      = ds.Code;
        p.Name_     = ds.Name;
        p.PartyName = ds.PartyName;
        p.NodeType  = ds.NodeType;
        p.PartyID   = ds.PartyID;

        result.value(result.Size) = p;

      end;
    
    else
    
      if(ds.MoveNext())

        p = TWsDepartment();

        p.Code      = ds.Code;
        p.Name_     = ds.Name;
        p.PartyName = ds.PartyName;
        p.NodeType  = ds.NodeType;
        p.PartyID   = ds.PartyID;

        result = p;

      end; 
      
    end;
    
  end;

  return result;

end;

/*
 * Получить условия для встраивания в SQL-запрос
 */
macro WebDprt_GetQueryParam( select : @string, from : @string, where : @string, alias : string, DepID : string )

  var aliasParty = string( alias, "_party" );
  
  WebScroll_AddSelect( @select, string(alias, ".t_Code"    , " ", alias, "_Code"    ) );
  WebScroll_AddSelect( @select, string(alias, ".t_Name"    , " ", alias, "_Name"    ) );
  WebScroll_AddSelect( @select, string(alias, ".t_NodeType", " ", alias, "_NodeType") );

  WebScroll_AddSelect( @select, string(aliasParty, ".t_Name", " ", aliasParty, "_Name") );

  WebScroll_AddFrom( @from, string("ddp_dep_dbt ", alias     ) );
  WebScroll_AddFrom( @from, string("dparty_dbt " , aliasParty) );

  WebScroll_AddWhere( @where, string(DepID, " = ", alias, ".t_Code(+)") );

  WebScroll_AddWhere( @where, string(alias, ".t_PartyID", " = ", aliasParty, ".t_PartyID(+)") );

end;

/*
 * Получить условие сортировки для встраивания в SQL-запрос
 */
macro WebDprt_GetSortParam( alias : string )

  return string(alias, "_Name");

end;

/*
 * Создать объект TWsDepartment по результатам выполнения SQL-запроса
 */
macro WebDprt_GetObjectFromRecordset( alias : string, rs : RsdRecordset )

  var p = null;

  var aliasParty = string( alias, "_party" );

  var IsCodeNull : bool;

  var Code = rs.value( string(alias, "_Code"), IsCodeNull );

  if( IsCodeNull == false )

    p = TWsDepartment();

    p.Code      = Code;
    p.Name_     = SQL_ConvType( rs.value(string(alias, "_Name"    )) );
    p.NodeType  = SQL_ConvType( rs.value(string(alias, "_NodeType")) );

    p.PartyName = SQL_ConvType( rs.value(string(aliasParty, "_Name")) );

  end;

  return p;

end;

macro GetTWsDepartmentByCode(Code : integer) : TWsDepartment

  return CB_GetDprtList(Code, -1, LISTALL, null, 2, null, 0);

end;

//CodeList - List<int>
macro GetDepartmentsListByIds(DprtIdList)
  
  var DprtIdListAsString = "";
  var i = 0;

  while(i < DprtIdList.size())
    DprtIdListAsString = DprtIdListAsString + DprtIdList[i];
    if (i != DprtIdList.size() - 1)
      DprtIdListAsString = DprtIdListAsString + ", ";
    end;

    i = i + 1;
  end;

  return CB_GetDprtList(DprtIdListAsString, -1, LISTALL, null, 2, null, 0);

end;


macro CB_GetRegList
(
  RegID,      // Идентификатор узла
  ParentID    // Идентификатор родительского узла
)

  var result;
  
  var query;
  
  query =
    " SELECT 1, XMLELEMENT(dp_reg_list, " +
    "   (SELECT DBMS_XMLGEN.GETXMLTYPE " +
    "   (DBMS_XMLGEN.NEWCONTEXTFROMHIERARCHY " +
    "    ('SELECT level, " +
    "      XMLElement( RegionDprt, " +
    "        XMLElement(RegID, rg.t_RegID), " +
    "        XMLElement(ParentRegID, rg.t_ParentRegID), " +
    "        XMLElement(NodeType, rg.t_NodeType), " +
    "        XMLElement(NodeID, rg.t_NodeID), " +
    "        XMLElement(Name_, DECODE(rg.t_Name, chr(1), '''', rg.t_Name)), " +
    "        XMLElement(DepName, dep.t_Name), " +
    "        XMLElement(PartyID, dep.t_PartyID), " +
    "        XMLElement(PartyName, pt.t_Name)) " +
    "       FROM ddp_reg_dbt rg " +
    "       LEFT JOIN ddp_dep_dbt dep ON rg.t_NodeID = dep.t_Code "
    "       LEFT JOIN dparty_dbt pt ON dep.t_PartyID = pt.t_PartyID ";

  if(RegID != NULL)
    if (ValType(RegID) == V_STRING)
      query = query + " WHERE rg.t_RegID in (" + RegID + ")";
    elif ((ValType(RegID) == V_INTEGER) AND (RegID != -1))
      query = query + " WHERE rg.t_RegID = " + RegID;  
    end;
  end;

  if( ParentID != -1 )
    query = query + " start with rg.t_ParentRegID = " + ParentID;
  else 
    query = query + " start with rg.t_ParentRegID = 0 "; 
  end;

  query = query + 
    "  connect by prior rg.t_RegID = rg.t_ParentRegID " +
    "  order siblings by rg.t_RegID ')) " +
    "  FROM DUAL)).GETCLOBVAL() XMLDOC  FROM DUAL " ;

  println ( query );

  var ds = TRsbDataSet( query );
  
  if( ds.MoveNext() )
    result = ds.XMLDOC;
  end;

  return result;

end;

// GetTWsDepartmentByCode(2);
/*
var str = CB_GetRegList(null, -1);
println(str);
str = str;
*/

                                                                     