/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Импорт справочника участников SWIFT из BIC Directory                    */
/*                                                                          */
/*  Имя файла: bicfltin.mac                                                 */
/*  Создан:  21.01.00                                        Васильев Д.В.  */
/****************************************************************************/

import "bicimp.mac";

/* класс Текстовый файл */
class TTextFile( VAR_TXTFILE, _filename );
  var Name:string,
      tf, /* переменная типа FILE txt */
      isOpen = 0;

  macro Str:string
    return tf.Str;
  end;
  macro NextRec:bool
    return Next(tf);
  end;
  macro OpenFile( _filename ):bool
    name = _filename;
    return Open(tf,name);
  end;

  tf = VAR_TXTFILE;
  if(ValType(_filename)==V_STRING)
    isOpen = OpenFile(_filename);
  else
    name = "";
  end;

end; /* class TTextFile */


macro SubStrTrim( str, i, n )
    return Trim( SubStr( str, i, n ) );
end;

macro SubStrTrimRight1( str, i, n )
    return TrimRight1( SubStr( str, i, n ) );
end;

macro SubStrTrimRight( str, i, n )
    return TrimRight( SubStr( str, i, n ) );
end;

/* описатель источника данных для BIC Directory */
class (BICSource) BICDirFlatSource(TxtFile)
  var TBICDir:TTextFile, EOFflag = false;

  TBICDir = TxtFile;

  /* инициализация источника и получение первой записи */
  macro Connect:bool
    Value_Added_Services = TArray( 20 );
    Physical_Address = TArray( 4 );
    CurStr = 0;
    if ( not TBICDir.isOpen ) return FALSE; end;
    /* становимся на первую строку текстового файла */
    NextRec();
    return not EOF();
  end;
  /* закрытие источника */
  macro Disconnect
  end;
  /* макс. значение счетчика указано приблизительно
     - на сентябрь 1998 в базе BIC Directory было 56736 записей */
  macro NumRec:integer
    return 70000;
  end;

  /* проверка на пропуск текущей записи. TRUE - пропустить */
  macro SkipRec:bool
    if ( trim(TBICDir.Str)=="" )
       return true;
    end;
    return false;
/* только росс. банки */
/*    return Substr(TBICDir.Str,  8,   2) != RU_Code;*/
  end;
  /* признак исчерпания источника */
  macro EOF:bool
    return EOFflag;
  end;
  /* название источника - для лога и интерфейса */
  macro SourceName:string
    return "BIC Directory - "+TBICDir.name;
  end;
    /* переход к следующей записи */
  macro NextRec
    EOFflag = not TBICDir.NextRec();
    CurStr = CurStr + 1;
  end;

  /* Прочитать текущую запрись */
  macro GetRec
    var i, str;

    Flag_ABA_Directory = false;
    MFO_Depart = "";
    Tag_Identifier = SubstrTrim( TBICDir.Str,   1,   2 ); /* Идентификатор */
    Modification_Flag = SubstrTrim( TBICDir.Str,   3,   1 ); /* Способ модификации записи */

    if( Modification_Flag == "U" )
       Modification_Flag = "M";
    end;

    /* каждая запись будет только добавляться */
    if( ( Modification_Flag != "M" ) AND ( Modification_Flag != "D" ) )
       Modification_Flag = "A";
    end;
    /* Если добавляется новая запись или модифицируется существующая - считываем необходимую информацию */
    BIC_Key                 = SubstrTrim(TBICDir.Str,   4,   8); /* ISO BIC */
    str                     = SubstrTrim(TBICDir.Str,  12,   3); /* Branch Code */
    if( str != "XXX" )
       BIC_Key = BIC_Key + str;
    end;
    CountryCode = Substr(BIC_Key,5,2); /* код страны по ISO */
    Institution_Name1       = SubstrTrimRight1(TBICDir.Str,  15,  35); /* Наименование участника SWIFT */
    Institution_Name2       = SubstrTrimRight1(TBICDir.Str,  50,  35); /* Наименование участника SWIFT */
    Institution_Name3       = SubstrTrimRight(TBICDir.Str,  85,  35); /* Наименование участника SWIFT */
    Branch_Information1     = SubstrTrimRight1(TBICDir.Str, 120,  35); /*  */
    Branch_Information2     = SubstrTrimRight(TBICDir.Str, 155,  35); /*  */
    City_Heading            = SubstrTrimRight1(TBICDir.Str, 190,  35); /*  */
    Subtype_Indication      = SubstrTrim(TBICDir.Str, 225,   4); /* Тип участника SWIFT */

    i = 0;
    while( i<20 )
       Value_Added_Services(i) = SubstrTrim(TBICDir.Str, 229+3*i,   3); /*  */
       i = i + 1;
    end;

    Extra_Information      = SubstrTrimRight(TBICDir.Str, 289,  35); /* Дополнительная информация */
    i = 0;
    while( i<4 )
        Physical_Address(i)      = SubstrTrimRight1(TBICDir.Str,  324+35*i,  35); /* Физический адрес участника */
        i = i + 1;
    end;

    Location1              = SubstrTrimRight1(TBICDir.Str, 464,   35); /* Местонахождение участника */
    Location2              = SubstrTrimRight1(TBICDir.Str, 499,  35); /* Местонахождение участника */
    Location3              = SubstrTrimRight(TBICDir.Str, 534,  35); /* Местонахождение участника */

    Country_Name1          = SubstrTrimRight1( TBICDir.Str, 569,   35); /* Наименование страны участника */
    Country_Name2          = SubstrTrimRight( TBICDir.Str, 604,  35); /* Наименование страны участника */
    POB_Number             = SubstrTrimRight(TBICDir.Str, 639,  35); /* P ost  O ffice  B ox number */

    POB_Location1          = SubstrTrimRight1(TBICDir.Str, 674,  35); /* Местонахождение почтового отделения участника */
    POB_Location2          = SubstrTrimRight1(TBICDir.Str, 709,  35); /* Местонахождение почтового отделения участника */
    POB_Location3          = SubstrTrimRight(TBICDir.Str, 744,  35); /* Местонахождение почтового отделения участника */

    POB_Country_Name1      = SubstrTrimRight1(TBICDir.Str, 779,  35); /* Наименование страны почтового отделения участника */
    POB_Country_Name2      = SubstrTrimRight(TBICDir.Str, 814,  35); /* Наименование страны почтового отделения участника */
    /* EMPTY_VALUE - "прозрачное" значение, означает сохранить старую величину.
       Применяется эта константа ТОЛЬКО для полей содержащихся в структуре
       BIC+ и не содержащихся в BICDirectory  */
    ZIP_Code             = EMPTY_VALUE;
    POB_ZIP_Code         = EMPTY_VALUE;
    CHIPS_UID            = EMPTY_VALUE;
    National_ID          = EMPTY_VALUE;
    BIC_RUS              = EMPTY_VALUE;
    BIC_Plus_Key         = EMPTY_VALUE;
    Update_Date          = EMPTY_VALUE;
    Activation_Date      = EMPTY_VALUE;
    Deactivation_Date    = EMPTY_VALUE;
    Undo_Merge_CHIPS     = EMPTY_VALUE;
    Undo_Merge_Nation_ID = EMPTY_VALUE;
    ABA                  = EMPTY_VALUE;
  end;
end;

/* описатель источника данных для ABA Routing Number */
class (BICSource) ABAFlatSource(TxtFile)
  var TBICDir:TTextFile, EOFflag = false;

  TBICDir = TxtFile;

  /* инициализация источника и получение первой записи */
  macro Connect:bool
    FindCreatin = GetTrue(true, "Устанавливать связи между банками?");
    Value_Added_Services = TArray( 20 );
    Physical_Address = TArray( 4 );
    CurStr = 0;
    if ( not TBICDir.isOpen ) return FALSE; end;
    /* становимся на первую строку текстового файла */
    NextRec();
    return not EOF();
  end;
  /* закрытие источника */
  macro Disconnect
  end;
  /* макс. значение счетчика указано приблизительно
     - на февраль 2001 в базе Fedware Directory было 11001 запись */
  macro NumRec:integer
    return 15000;
  end;

  /* проверка на пропуск текущей записи. TRUE - пропустить */
  macro SkipRec:bool
    if ( trim(TBICDir.Str)=="" )
       return true;
    end;
    return false;
  end;
  /* признак исчерпания источника */
  macro EOF:bool
    return EOFflag;
  end;
  /* название источника - для лога и интерфейса */
  macro SourceName:string
    return "Fedwire Directory - "+TBICDir.name;
  end;
    /* переход к следующей записи */
  macro NextRec
    EOFflag = not TBICDir.NextRec();    
    if ( (EOFflag==false) AND (trim(TBICDir.Str)!="") )
       CurStr = CurStr + 1;
    end;    
  end;

  /* Прочитать текущую запрись */
  macro GetRec
    var i, str;

    /* При обновлении банка только добавить код ABA и ничего не менять */
    Flag_ABA_Directory        = true;

    MFO_Depart              = EMPTY_VALUE;
    Tag_Identifier          = ""; /* Идентификатор */
    Modification_Flag       = "A"; /* Способ модификации записи */

    ABA                     = SubstrTrim(TBICDir.Str, 1,   9); 
    Institution_Name1       = SubstrTrimRight1(TBICDir.Str,  28,  36); /* Наименование участника SWIFT */
    City_Heading            = SubstrTrim(TBICDir.Str, 66,  25); /*  */

    BIC_Key                 = EMPTY_VALUE; /* ISO BIC */
    str                     = ""; /* Branch Code */
    CountryCode             = "US"; /* код страны по ISO */

    Institution_Name2       = "";
    Institution_Name3       = "";
    Branch_Information1     = "";
    Branch_Information2     = "";
    Subtype_Indication      = "";

    i = 0;
    while( i<20 )
       Value_Added_Services(i) = ""; /*  */
       i = i + 1;
    end;

    Extra_Information       = "";
    i = 0;
    while( i<4 )
        Physical_Address(i) = "";
        i = i + 1;
    end;

    Location1              = "";
    Location2              = "";
    Location3              = "";

    Country_Name1          = "UNITED STATE OF AMERICA";
    Country_Name2          = "";
    POB_Number             = "";

    POB_Location1          = "";
    POB_Location2          = "";
    POB_Location3          = "";

    POB_Country_Name1      = "";
    POB_Country_Name2      = "";
    /* EMPTY_VALUE - "прозрачное" значение, означает сохранить старую величину.
       Применяется эта константа ТОЛЬКО для полей содержащихся в структуре
       BIC+ и не содержащихся в BICDirectory  */
    ZIP_Code             = EMPTY_VALUE;
    POB_ZIP_Code         = EMPTY_VALUE;
    CHIPS_UID            = EMPTY_VALUE;
    National_ID          = EMPTY_VALUE;
    BIC_RUS              = EMPTY_VALUE;
    BIC_Plus_Key         = EMPTY_VALUE;
    Update_Date          = EMPTY_VALUE;
    Activation_Date      = EMPTY_VALUE;
    Deactivation_Date    = EMPTY_VALUE;
    Undo_Merge_CHIPS     = EMPTY_VALUE;
    Undo_Merge_Nation_ID = EMPTY_VALUE;   
  end;
end;
