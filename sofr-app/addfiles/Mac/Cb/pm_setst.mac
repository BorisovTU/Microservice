/*
  $Name:         pm_setst.mac
  $Module:       Banking
  $Description:  Общие функции и переменные для макросов шагов
*/

//-----------------------------------------------------------------------------
// Блок     : Вне блока
// Шаг      : Вне шага
// Описание : Общие функции и переменные для макросов шагов
//-----------------------------------------------------------------------------
IMPORT InsCarryDoc, PaymInter, PSInter, pm_const, BankInter, OprInter, globals, pm_common, "cbsttls.mac";

//-----------------------------------------------------------------------------
// Установить статус первичного документа
//-----------------------------------------------------------------------------
macro PM_SetPrimDocumentState( Payment:RsbPayment, state:integer )

  var obj:object;

  if( Payment.DocKind == DLDOC_MEMORIALORDER )
    obj = GenObject( "RsbMemorialOrder", Payment.DocumentID );
    if( state == DOCUMENT_ST_DEFERRED )
      obj.State = CB_DOC_STATE_DEFERRED;
    elif( state == DOCUMENT_ST_WORKING )
      obj.State = CB_DOC_STATE_WORKING;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.State = CB_DOC_STATE_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.State = CB_DOC_STATE_CLOSED;
    end;
  end;

  if( Payment.DocKind == CB_MULTYDOC )
    obj = GenObject( "RsbMultyDoc", Payment.DocumentID );
    if( state == DOCUMENT_ST_DEFERRED )
      obj.Status = MCDOC_STATUS_POST;
    elif( state == DOCUMENT_ST_WORKING )
      obj.Status = MCDOC_STATUS_OPEN;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.Status = MCDOC_STATUS_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.Status = MCDOC_STATUS_CLOSE;
    end;
  end;

  if( ( Payment.DocKind == PS_CPORDER ) or
      ( Payment.DocKind == BBANK_CPORDER ) )
    if( Payment.DocKind == PS_CPORDER )
      obj = GenObject( "RsbPsCpOrder", Payment.DocumentID );
    else
      obj = GenObject( "RsbBbCpOrder", Payment.DocumentID );
    end;

    if( state == DOCUMENT_ST_DEFERRED )
      obj.CurrentState = CP_ST_DEFERRED;
    elif( state == DOCUMENT_ST_WORKING )
      obj.CurrentState = CP_ST_WORKING;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.CurrentState = CP_ST_REJECTED;
      // Сделать возможное сообщение невалидным
      UnvalidPaymentMessage( Payment.PaymentID);
    elif( state == DOCUMENT_ST_CLOSED )
      obj.CurrentState = CP_ST_CLOSED;
    end;
  end;

  if( Payment.DocKind == PS_PAYORDER )
    obj = GenObject( "RsbPsPayOrder", Payment.DocumentID );
    if( state == DOCUMENT_ST_DEFERRED )
      obj.CurrentState = PSPO_ST_DEFERRED;
    elif( state == DOCUMENT_ST_WORKING )
      obj.CurrentState = PSPO_ST_WORKING;
    elif( state == DOCUMENT_ST_I1 )
      obj.CurrentState = PSPO_ST_I1;
    elif( state == DOCUMENT_ST_I2 )
      obj.CurrentState = PSPO_ST_I2;
    elif( state == DOCUMENT_ST_IWP )
      obj.CurrentState = PSPO_ST_IWP;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.CurrentState = PSPO_ST_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.CurrentState = PSPO_ST_CLOSED;
    end;
  end;

  if( Payment.DocKind == PS_INRQ )
    obj = GenObject( "RsbRequestOrder", Payment.DocumentID );
    if( state == DOCUMENT_ST_DEFERRED )
      obj.State = PSINRQ_ST_DEFERRED;
    elif( state == DOCUMENT_ST_WORKING )
      obj.State = PSINRQ_ST_WORKING;
    elif( state == DOCUMENT_ST_I2 )
      obj.State = PSINRQ_ST_I2;
    elif( state == DOCUMENT_ST_IWP )
      obj.State = PSINRQ_ST_IWP;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.State = PSINRQ_ST_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.State = PSINRQ_ST_CLOSED;
    end;
  end;

  if( ( Payment.DocKind == DLDOC_BANKPAYMENT ) or
      ( Payment.DocKind == DLDOC_BANKCLAIM ) )

    if( Payment.DocKind == DLDOC_BANKPAYMENT )
      obj = GenObject( "RsbBankPayment", Payment.DocumentID );
    else
      obj = GenObject( "RsbBankClaim", Payment.DocumentID );
    end;

    if( state == DOCUMENT_ST_DEFERRED )
      obj.Status = MEMORDER_STATUS_POST;
    elif( state == DOCUMENT_ST_WORKING )
      obj.Status = MEMORDER_STATUS_OPEN;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.Status = MEMORDER_STATUS_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.Status = MEMORDER_STATUS_CLOSE;
    end;
  end;

  if( ( Payment.DocKind == CASH_BOF_ADDORDER ) or
      ( Payment.DocKind == CASH_PS_INCORDER  ) or
      ( Payment.DocKind == CASH_PS_OUTORDER  ) or
      ( Payment.DocKind == CASH_BOF_INCORDER ) or
      ( Payment.DocKind == CASH_BOF_OUTORDER ) or
      ( Payment.DocKind == DLDOC_INOUTORDER ))

    if( Payment.DocKind == CASH_BOF_ADDORDER )
      obj = GenObject( "RsbBBAddCashOrder", Payment.DocumentID );
    elif( Payment.DocKind == CASH_BOF_INCORDER )
      obj = GenObject( "RsbBBIncCashOrder", Payment.DocumentID );
    elif( Payment.DocKind == CASH_BOF_OUTORDER )
      obj = GenObject( "RsbBBOutCashOrder", Payment.DocumentID );
    elif( Payment.DocKind == CASH_PS_INCORDER )
      obj = GenObject( "RsbPSInCashOrder", Payment.DocumentID );
    elif( Payment.DocKind == DLDOC_INOUTORDER ) 
      obj = GenObject( "RsbBBInOutCashOrder", Payment.DocumentID );
    else //CASH_PS_OUTORDER
      obj = GenObject( "RsbPSOutCashOrder", Payment.DocumentID );
    end;

    if( state == DOCUMENT_ST_DEFERRED )
      obj.Status = STAT_CASH_ORDER_POST;
    elif( state == DOCUMENT_ST_WORKING )
      obj.Status = STAT_CASH_ORDER_OPEN;
    elif( state == DOCUMENT_ST_REJECTED )
      obj.Status = STAT_CASH_ORDER_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      obj.Status = STAT_CASH_ORDER_CLOSE;
    end;
  end;

  if( Payment.Dockind == DLDOC_SUMMARY_MEMORDER )

    if( state == DOCUMENT_ST_DEFERRED )
      Payment.PaymStatus = PM_PREPARING;
    elif( state == DOCUMENT_ST_WORKING )
      Payment.PaymStatus = PM_NOTFINISHED;
    elif( state == DOCUMENT_ST_REJECTED )
      Payment.PaymStatus = PM_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      Payment.PaymStatus = PM_FINISHED;
    end;
  
  end;
  
  if( Payment.Dockind == DLDOC_BANKORDER )

    if( state == DOCUMENT_ST_DEFERRED )
      Payment.PaymStatus = PM_PREPARING;
    elif( state == DOCUMENT_ST_WORKING )
      Payment.PaymStatus = PM_NOTFINISHED;
    elif( state == DOCUMENT_ST_REJECTED )
      Payment.PaymStatus = PM_REJECTED;
    elif( state == DOCUMENT_ST_CLOSED )
      Payment.PaymStatus = PM_FINISHED;
    elif( state == DOCUMENT_ST_I2 )
      Payment.PaymStatus = PM_I2PLACED;
    elif( state == DOCUMENT_ST_IWP )
      Payment.PaymStatus = PM_IWPPLACED;
    end;

  end;
end;

macro PM_SetErrorStatus( p_PaymentID:integer, p_ErrorStatus:integer, p_ErrorMessage:string )

  var query:string = "update doprtemp_tmp set t_ErrorStatus = :ErrorStatus";
  var params:TArray = TArray;

  params[0] = SQLParam( "ErrorStatus", p_ErrorStatus );

  if( p_ErrorMessage )
    query = query + " ,t_ErrorMessage = :ErrorMessage";
    params[1] = SQLParam( "ErrorMessage", p_ErrorMessage );
  end;

  query = query + " where t_OrderID = :OrderID";
  params[ params.size ] = SQLParam( "OrderID", p_PaymentID );

  execSQL( query, params );

end;

//Отвергнуть документ с заполнением примечания причины отказа
macro RejectPayment( Payment : RsbPayment, NoteText : string )
  
  Payment.PaymStatus = PM_REJECTED;
  PM_SetPrimDocumentState( Payment, DOCUMENT_ST_REJECTED );

  if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  // Заполнить примечание
  if(NoteText)
    if( Payment.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, NoteText ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;

  //если отвергли, сбросить ручную обработку, чтобы можно было после перезапуска запланировать предобработку
  if (GetOprStatus(OPR_PAYM_MANUAL) == OPR_PAYM_ST_MANUAL_NEED) 
    if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  return 0;
end;

// ----------------------------------------------------------------------------
// На ручную обработку
// ----------------------------------------------------------------------------
macro PM_ToManual( Payment:RsbPayment, note_text:string ):integer

  if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NEED, OPR_PAYM_DO, OPR_PM_ST_ENTER ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;
  if( note_text != "" ) // Заполнить примечание
    if( Payment.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;

  return 0;
end;

// отзыв распоряжения на оплату
macro RevokeEsidOrder(PaymentObj : RsbPayment) : integer
  if( УстановитьСтатусыПлатежа( OPR_CLAIM_STATE, OPR_CL_ST_CLOSE ) ) 
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  PaymentObj.PaymStatus = PM_REJECTED;

  return 0;
end;
