/*
$Name:        fm_ground.mac
$Module:      Финансовый мониторинг
$Description: Дополнительные проверки при отборе операций, подлежащих контролю
*/

Import "fm_util.mac";

private var CheckedGround : string; /* основание, преобразованное для проверки */
private var GroundWordArray : TArray;

private var PayerID    : integer;
private var ReceiverID : integer;

private var PayerBankID    : integer;
private var ReceiverBankID : integer;

private var ClientPayerAccount    : bool;
private var ClientReceiverAccount : bool;

var SetCodeFlag : bool; /* признак того, что операции был задан код */

/* определить параметры участников операции */
private macro InitPartyParameters( FMOperation : RsbFMOperation )

  ClientPayerAccount    = IsClientAccount( FMOperation.OprParty(_FM_PARTY_PAYER   ).Account );
  ClientReceiverAccount = IsClientAccount( FMOperation.OprParty(_FM_PARTY_RECEIVER).Account );

end;


/* установить код операции */
macro SetOperationCode( FMOperation : RsbFMOperation, Code : string, CodeType : integer )
  
  SetCodeFlag = true;
  FMOperation.AddCode( Code, CodeType );

end;

macro SetOperationCodeUO( FMOperation : RsbFMOperation, Code3X : string, Code4X : string )
  
  SetCodeFlag = true;
  FMOperation.AddCodeUO( Code3X, Code4X );

end;



/* разбивет строку на массив слов */
private macro SplitStringByWord( WordArray : TArray, InitialString : string )
  var EndWordPos : integer;
  var CurWord : string, CurString : string;
  var iWord : integer;
           
  CurString = Trim(InitialString);

  iWord = 0;

  while( CurString != "" )
  
    /* ищем, где находится первый пробел в строке */
    EndWordPos = Index( CurString, " " );
    
    if( EndWordPos == 0 )
      
      /* в строке находится целое слово */
      CurWord = CurString;

    else

      /* определяем первое слово в строке */
      CurWord = SubStr( CurString, 1, (EndWordPos - 1) );

    end;
    
    /* заносим слово в массив слов */
    WordArray(iWord) = CurWord;
    iWord = iWord + 1;

    CurString = Trim(SubStr(CurString, EndWordPos));
    
  end;
  
end;

/* заменить символы одинакового начертания */
private macro ReplaceSymbols( InitialString : string ) : string

  InitialString = StrSubst( InitialString, "Ё", "Е" );
  InitialString = StrSubst( InitialString, "A", "А" );
  InitialString = StrSubst( InitialString, "B", "В" );
  InitialString = StrSubst( InitialString, "C", "С" );
  InitialString = StrSubst( InitialString, "E", "Е" );
  InitialString = StrSubst( InitialString, "H", "Н" );
  InitialString = StrSubst( InitialString, "K", "К" );
  InitialString = StrSubst( InitialString, "M", "М" );
  InitialString = StrSubst( InitialString, "O", "О" );
  InitialString = StrSubst( InitialString, "P", "Р" );
  InitialString = StrSubst( InitialString, "T", "Т" );
  InitialString = StrSubst( InitialString, "X", "Х" );

  return InitialString;

end;

/* преобразовать основание для проверки */
private macro ConvertString( InitialString : string ) : string

  var ResultString : string;
  var i : integer, len : integer;
  var DelimiterString = ".,()[]\"&<>{}!?";

  Trim( InitialString );

  /* преобразуем к верхнему регистру */
  ResultString = StrUpr( InitialString );

  /* заменим разделители на пробелы */
  i = 0; len = StrLen( DelimiterString );
  while( i < len )
    i = i + 1;
    ResultString = StrSubst( ResultString, SubStr(DelimiterString, i, 1), " " );
  end;

  ResultString = ReplaceSymbols( ResultString );

  /* добавим пробел в конец строки */
  ResultString = ResultString + " ";

  return ResultString;

end;

/* проверить, есть ли слово в основании */
private macro IsWordInString( CheckedString : string, CheckedWord : string )
  
  CheckedWord = StrUpr( CheckedWord );
  
  if( (Index(CheckedString, " " + CheckedWord)) or (SubStr(CheckedString, 1, strlen(CheckedWord)) == CheckedWord) )
    return true;
  end;

  return false;
end;

/* проверить, являются ли два слова соседними */
private macro IsNeighboringWords( WordArray : TArray, CheckedWord1 : string, CheckedWord2 : string, RightOrder : bool )

  var i : integer, nWords : integer;
  var stat : bool;

  stat = false;

  if( RightOrder == null ) RightOrder = false; end;

  CheckedWord1 = StrUpr( CheckedWord1 );
  CheckedWord2 = StrUpr( CheckedWord2 );

  i = 0;
  nWords = WordArray.Size - 1;

  while( (i < nWords) and (stat == false) )

    if( (IsWordInString(WordArray(i), CheckedWord1)) and IsWordInString(WordArray(i + 1),CheckedWord2) )

      stat = true;

    elif( not RightOrder )

     if( (IsWordInString(WordArray(i), CheckedWord2)) and IsWordInString(WordArray(i + 1),CheckedWord1) )
      
        stat = true; /* слова являются соседними */

     end;
    
    end;

    i = i + 1;

  end;

  return stat;

end;


/* проверить счета операции на принадлжность */
macro CompareOperationAccountsWithMask( FMOperation : RsbFMOperation, AccMask : string ) : bool

  return (CompareStrWithMasks(AccMask, FMOperation.OprParty(_FM_PARTY_PAYER   ).Account) == 0)
      or (CompareStrWithMasks(AccMask, FMOperation.OprParty(_FM_PARTY_RECEIVER).Account) == 0);

end;


/* проверка на код 1008 - внесение денег в уставной капитал */
private macro CheckCode1008( FMOperation : RsbFMOperation )

  if( (ReceiverBankID == {OurBank}) and (ClientReceiverAccount == true) and (FMOperation.OprParty(_FM_PARTY_RECEIVER).PartyType == "Ю") )

    if( ((IsWordInString(CheckedGround, "УСТАВ")) or (IsWordInString(CheckedGround, "СКЛАДОЧН")))
    and ((IsWordInString(CheckedGround, "ФОНД" )) or (IsWordInString(CheckedGround, "КАП"     )))
      )

      SetOperationCode( FMOperation, "1008" );
    
    end;

  end;

end;

/* проверка на код 5001 - помещение ценностей в ломбард */
private macro CheckCode5001( FMOperation : RsbFMOperation )

  var NormPayerName = ConvertString( FMOperation.OprParty(_FM_PARTY_PAYER).Name );

  if( (IsWordInString(NormPayerName, "ЛОМБАРД")) or (IsWordInString(CheckedGround, "ЛОМБАРД")) )
    SetOperationCode( FMOperation, "5001" );
  end;

end;

/* проверка на код 5002 - выплата страхового возмещения */
private macro CheckCode5002( FMOperation : RsbFMOperation );

  if( (IsWordInString(CheckedGround, "АННУИТЕТ"))
   or (IsWordInString(CheckedGround, "АНУИТЕТ" ))
   or ((IsWordInString(CheckedGround, "СТРАХОВ") or IsWordInString(CheckedGround, "ПЕРЕСТР"))
     and
       ( (IsWordInString(CheckedGround, "ЖИЗН"   ))
      or (IsWordInString(CheckedGround, "БОЛЕЗН" ))
      or (IsWordInString(CheckedGround, "МЕДИЦИН"))
      or (IsWordInString(CheckedGround, "ПРЕМИ"  ))
      or (IsWordInString(CheckedGround, "ВОЗМЕЩ" ))
      or (IsWordInString(CheckedGround, "ВЫПЛ" ))
      or ((IsWordInString(CheckedGround, "ЛИЧН")) and (IsWordInString(CheckedGround, "НАКОПИТ")))
      or ((IsWordInString(CheckedGround, "НЕСЧ")) and (IsWordInString(CheckedGround, "СЛУЧ"   )))
       )
      )
    )

    SetOperationCode( FMOperation, "5002" );

  end;

end;

/* проверить на код 5003 */
private macro CheckCode5003( FMOperation : RsbFMOperation ) : integer
  
  if( CompareOperationAccountsWithMask(FMOperation, "47701*, 608*, 61211*, 91506*") )

    SetOperationCode( FMOperation, "5003" );
    
  elif( CompareOperationAccountsWithMask(FMOperation, "401* - 408*, 91501*, 91507*") )
      
    if( (IsWordInString(CheckedGround, "ЛИЗИНГ" ))
     or (IsWordInString(CheckedGround, "ЛИЗ "   ))
     or (IsNeighboringWords(GroundWordArray, "ФИНАНС", "АРЕНД"))
      )
      SetOperationCode( FMOperation, "5003" );
    end;

  end; 

end;

/* проверка на код 5005 - скупка юв. изделий */
private macro CheckCode5005( FMOperation : RsbFMOperation )

  if( (IsWordInString(CheckedGround, "ЗОЛОТ" ))
   or (IsWordInString(CheckedGround, "СЕРЕБР"))
   or (IsWordInString(CheckedGround, "ПЛАТИН"))
   or (IsWordInString(CheckedGround, "ПАЛАД" ))
   or (IsWordInString(CheckedGround, "ПАЛЛАД"))
   or ((IsWordInString(CheckedGround, "ДРАГ")) and (IsWordInString(CheckedGround, "МЕТАЛ")))
   or (IsNeighboringWords(GroundWordArray, "ЮВ", "ИЗД"))
    )

    SetOperationCode( FMOperation, "5005" );
  
  end;

end;

/* проверка на код 5006 - получение выигрыша в лоттерею  */
private macro CheckCode5006( FMOperation : RsbFMOperation )

  if( (IsWordInString(CheckedGround, "ЛОТЕРЕ" ))
   or (IsWordInString(CheckedGround, "ТОТАЛИЗ"))
   or (IsWordInString(CheckedGround, "ПАРИ "  ))
   or (IsWordInString(CheckedGround, "ВЫИГРЫШ"))
   or (IsWordInString(CheckedGround, "ИГОРН"  ))
    )

    SetOperationCode( FMOperation, "5006" );

  end;

end;

/* проверка на принадлежность операции к займам */
private macro CheckCode5007( FMOperation : RsbFMOperation )
  
  if( (IsWordInString(CheckedGround, "ПОМОЩ"      ))
   or (IsWordInString(CheckedGround, "БЛАГОТВОРИТ"))
   or (IsWordInString(CheckedGround, "ПОЖЕРТВ"    ))
   or (IsWordInString(CheckedGround, "СПОНС"      ))
   or (IsWordInString(CheckedGround, "БЕЗВОЗМЕЗД" ))
   or (IsNeighboringWords(GroundWordArray, "БЕЗ", "ПРОЦ"))
   or ((
        (IsWordInString(CheckedGround, "БЕСПРОЦ")) or (IsWordInString(CheckedGround, "БЕСПР "))
    or  (IsWordInString(CheckedGround, "БЕЗПРОЦ")) or (IsWordInString(CheckedGround, "БЕЗПР "))
       )
   and ((IsWordInString(CheckedGround, "КРЕДИТ"))
     or (IsWordInString(CheckedGround, "ССУД"  ))
     or (IsWordInString(CheckedGround, "ЗАЕМ"  ))
     or (IsWordInString(CheckedGround, "ЗАЁМ"  ))
     or (IsWordInString(CheckedGround, "ЗАЙМ"  ))))
    )

     SetOperationCode( FMOperation, "5007" );

  end;

end;


/* Проверка на принадлежность операции сделкам с недвижимостью */
private macro CheckCode8001( FMOperation : RsbFMOperation )
  
  var stat : integer;
  var ImmDealRegVal;

  stat = 0;

  /* проверим на принадлежность операции сделкам с недвижимостью */
  if( (IsWordInString(CheckedGround, "АРЕНД"      ))
   or (IsWordInString(CheckedGround, "ИМУЩ"       ))
   or (IsWordInString(CheckedGround, "НЕДВИЖ"     ))
   or (IsWordInString(CheckedGround, "ЗДАН"       ))
   or (IsWordInString(CheckedGround, "СООРУЖЕН"   ))
   or (IsWordInString(CheckedGround, "ПОМЕЩ"      ))
   or (IsWordInString(CheckedGround, "ЗАЛОГ"      ))
   or (IsWordInString(CheckedGround, "КВАРТИР"    ))
   or (IsWordInString(CheckedGround, "ИПОТЕК"     ))
   or (IsWordInString(CheckedGround, "АЭРОСТАТ"   ))
   or (IsWordInString(CheckedGround, "ДИРИЖАБЛ"   ))
   or (IsWordInString(CheckedGround, "ПЛАНЕР"     ))
   or (IsWordInString(CheckedGround, "ВЕРТОЛЕТ"   ))
   or (IsWordInString(CheckedGround, "САМОЛЕТ"    ))
   or (IsWordInString(CheckedGround, "КОСМИЧЕСК"  ))
   or (IsWordInString(CheckedGround, "ТРУБОПРОВ"  ))
   or (IsWordInString(CheckedGround, "НАСАЖДЕН"   ))
   or (IsWordInString(CheckedGround, "ЛЕСН"       ))
   or (IsWordInString(CheckedGround, "УЧАСТОК"    ))
   or (IsWordInString(CheckedGround, "УЧАСТК"     ))
   or (IsWordInString(CheckedGround, "ОБЪЕКТ"     ))
   or (IsWordInString(CheckedGround, "ЗЕМЛ"       ))
   or (IsWordInString(CheckedGround, "ИНВЕСТ"     ))
   or (IsWordInString(CheckedGround, "ЖИЛЬ"       ))
   or (IsWordInString(CheckedGround, "СТРОИТЕЛЬСТ"))
   or (IsWordInString(CheckedGround, "СМР"        ))
   or (IsWordInString(CheckedGround, "РЕНТ"       ))
   or (IsWordInString(CheckedGround, "ДАЧ"        ))
   or (IsWordInString(CheckedGround, "СТР-В"      ))
   or (IsWordInString(CheckedGround, "ЯХТ"        ))
   or (IsWordInString(CheckedGround, "КАТЕР"      ))
   or (IsNeighboringWords(GroundWordArray, "ЖИЛ"       , "ДОМ"    ))
   or (IsNeighboringWords(GroundWordArray, "ВОЗДУШН"   , "СУД"    ))
   or (IsNeighboringWords(GroundWordArray, "ЖЕЛЕЗНОДОР", "ПУТ"    ))
   or (IsNeighboringWords(GroundWordArray, "АВТОТР"    , "МАГИСТР"))
   or (IsNeighboringWords(GroundWordArray, "ДОЛЕВ"     , "ФИНАНС" ))
    )

    if( FMOperation.SumEquivalent >= $3000000 )
      SetOperationCode( FMOperation, "8001" );
    else
      ImmDealRegVal = GetImmDealRegVal();      
      if( (SetCodeFlag == false) and (ImmDealRegVal == false) )
        stat = 1;
      end;
    end;
  
  end;

  return stat;

end;

/* проверка на код 901 (необычные операции) */
/*
macro CheckUnusualOperations( FMOperation : RsbFMOperation )
  var SetCode = false; 

  CheckedGround = ConvertString( FMOperation.Ground ); // !!!

  if( (IsWordInString(CheckedGround, "В СЧЕТ В/Р"    ))
   or (IsWordInString(CheckedGround, "В СЧЕТ ВЗАИМОР"))
   or (IsWordInString(CheckedGround, "КЛИРИНГ"       ))
    )
   
    SetOperationCodeUO( FMOperation, "901", "1699" );
    SetCode = true;
  end;

  if( (IsWordInString(CheckedGround, "ПОПОЛН"        ))
   or (((IsWordInString(CheckedGround, "ПЕРЕВОД")) or (IsWordInString(CheckedGround, "ПЕРЕЧ")))
    and ( (IsNeighboringWords(GroundWordArray, "СОБСТВ", "СР", true))
       or (IsNeighboringWords(GroundWordArray, "СОБСТВ", "СЧ", true))
       or (IsNeighboringWords(GroundWordArray, "ДЕНЕЖ" , "СР", true))
       or (IsNeighboringWords(GroundWordArray, "ДЕНЕЖ" , "СЧ", true))
        )
      )
    )

    SetOperationCodeUO( FMOperation, "901", "1199" );
    SetCode = true;
  end;

  return SetCode;
end;
*/
/* проверка операций, связанный со странами не участвующими в противодействии легализации */
private macro SetCode3000( FMOperation : RsbFMOperation )

  if( (IsWordInString(CheckedGround, "КРЕДИТ"))
   or (IsWordInString(CheckedGround, "ЗАЙМ"  ))
   or (IsWordInString(CheckedGround, "ЗАЕМ"  ))
   or (IsWordInString(CheckedGround, "ЗАЁМ"  ))
   or (IsWordInString(CheckedGround, "ССУД"  ))
    )
      
      SetOperationCode( FMOperation, "3011" );
    
  elif( (IsWordInString(CheckedGround, "АКЦИ"    ))
     or (IsWordInString(CheckedGround, "ВЕКСЕЛ"  ))
     or (IsWordInString(CheckedGround, "ОБЛИГАЦИ"))
     or (IsWordInString(CheckedGround, "СЕРТИФ"  ))
     or (IsWordInString(CheckedGround, "ПИФ "    ))
     or (IsNeighboringWords(GroundWordArray, "ЦЕННЫ", "БУМАГ"))
     or (CompareStrWithMasks("5*", FMOperation.OprParty(_FM_PARTY_PAYER   ).Account) == 0)
     or (CompareStrWithMasks("5*", FMOperation.OprParty(_FM_PARTY_RECEIVER).Account) == 0)
      )
      
      SetOperationCode( FMOperation, "3021" );
    
  else
      
      SetOperationCode( FMOperation, "3001" );
    
  end;

end;


/*
 * Проверка основания операции на возможность определения кодов
 */
macro FM_CheckGround( FMOperation : RsbFMOperation, CountrySign : bool, Chapter : integer ) : integer

  var stat : integer;

  GroundWordArray = TArray;

  CheckedGround = ConvertString( FMOperation.Ground );

  SplitStringByWord( GroundWordArray, CheckedGround );

  PayerID    = FMOperation.OprParty(_FM_PARTY_PAYER   ).PartyID;
  ReceiverID = FMOperation.OprParty(_FM_PARTY_RECEIVER).PartyID;

  PayerBankID    = FMOperation.OprParty(_FM_PARTY_PAYER   ).BankID;
  ReceiverBankID = FMOperation.OprParty(_FM_PARTY_RECEIVER).BankID;

  InitPartyParameters( FMOperation );

  if( Chapter == 1 )
    
    if( CountrySign )
    
      /* проверка на коды из группы 3000 */
      SetCode3000( FMOperation );

    end;

    /* проверка на код 1008 */
    CheckCode1008( FMOperation );

    /* проверка на код 5001 */
    CheckCode5001( FMOperation );

    /* проверка на код 5002 */
    CheckCode5002( FMOperation );
  
  end;

  /* проверка на код 5003 */
  CheckCode5003( FMOperation );

  if( Chapter == 1 )
    
    /* проверка на код 5005 */
    CheckCode5005( FMOperation );

    /* проверка на код 5006 */
    CheckCode5006( FMOperation );

    /* проверка на код 5007 */
    CheckCode5007( FMOperation );
  
  end;

  /* проверка на код 8001 */
  stat = CheckCode8001( FMOperation );

  if( (stat == 0) and (Chapter == 1) )
    
    /* проверка на код 901 (необычные операции) */
    //CheckUnusualOperations( FMOperation ); теперь они выполняются всегда


  end;

  if( (stat == 0) and (Chapter == 3) and (SetCodeFlag == false) )
    stat = 1; /* для документов главы В создаем записи об операции, только если определили код операции */
  end;

  return stat;

end;
