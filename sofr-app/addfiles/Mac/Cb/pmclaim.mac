/*
  $Name:             pmclaim.mac
  $Module:           РКО юридических лиц
  $Description:      Макрос скроллинга распоряжений на оплату
*/

import pmbuff, pm_chksave, MesInter;

/* Установка подсказки для скролингов из макроса */

private const Hint_ByValueDate:string = "/*+FIRST_ROWS LEADING(t pmprop pmrmprop oproper oprcurst) INDEX(t DPMPAYM_DBT_IDX11) USE_NL(t  pmprop pmrmprop oproper oprcurst)*/";
private const Hint_ByStatus:string = "/*+FIRST_ROWS LEADING(t pmprop pmrmprop oproper oprcurst) INDEX(t DPMPAYM_DBT_IDX16) USE_NL(t  pmprop pmrmprop oproper oprcurst)*/";
private const Hint_ByStep:string = "/*+FIRST_ROWS LEADING(oprstep oproper t pmprop pmrmprop oprcurst) INDEX(oprstep doprstep_dbt_idx10) INDEX(t dpmpaym_dbt_idx0) USE_NL(oprstep oproper t  pmprop pmrmprop oprcurst)*/";


MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
                  
  if(   (ScrolStates == PSSK_DEFER)
     or (ScrolStates == PSSK_OPEN)
     or (ScrolStates == PSSK_MES_SEND) 
     or (ScrolStates == PSSK_MES_DEFECT)
     or (ScrolStates == PSSK_CLOSED)
     /*or ScrolStates == ? Неисполненные*/ )

    return Hint_ByStatus;
  
  elif( ScrolStates == PSSK_ALL )
    
    return Hint_ByValueDate;
 
  elif( ScrolStates == PSSK_STEP )

    return Hint_ByStep;
 
  end;

  return DefaultHint;

END;

macro Новый_Документ( )
  return 0;
end;

macro  Проверить_Документ( Режим )
  
  var CtrlStatus = 0;
  var PanelName = IfThenElse( InList( r_pmpaym.DocKind, DLDOC_OUT_PMCLAIM, DLDOC_IN_PMCLAIM ), "CLMESID", "COLESID" );
  var flds = TPanelFields( PanelName, "ps.lbr" );
  if ( (r_pmpaym.DocKind == DLDOC_IN_PMCLAIM) or (r_pmpaym.DocKind == DLDOC_IN_PMCOL) ) // В панели CLMESID используется одно и то же поле для ReceiverBankMarkDate и PayerBankEnterDate
    flds.PayerBankEnterDate = flds.GetFieldNum( "ReceiverBankMarkDate" );
  end;
  if( (Режим == OBJ_INSERT) or (Режим == OBJ_UPDATE) )
    
    if( InList( r_pmpaym.DocKind, DLDOC_IN_PMCLAIM, DLDOC_IN_PMCOL ) and ( r_credit.Group != PAYMENTS_GROUP_EXTERNAL ) )  
      msgbox("Можно ввести только внешнее предъявленное распоряжение на оплату. Необходимо указать внешний банк получателя");
      return flds.ReceiverBankCode + 1; // Увеличиваем на 1 так как в TPanelFields реальные номера полей, а возвращать надо на 1 больше
    end;

  end;

  if( Режим == OBJ_UPDATE )
    
    if( ПолучитьСообщение( r_pmpaym.PaymentID, OBJTYPE_PAYMENT, 0/*WLD_MES_OUT*/, NULL, NULL ) )
      msgbox("Изменение реквизитов запрещено. На основании распоряжения сформировано исходящее сообщение.");
      return CHANG_NOTKEEP;
    end;
    
    if( existsPurposePayment( r_pmpaym.PaymentID, PMLINK_KIND_ESIDPAYMENT ) )
      msgbox("Изменение реквизитов запрещено. На основании распоряжения сформирован исполняемый платежный документ");
      return CHANG_NOTKEEP;
    end;

    if( r_pmpaym.PrimDocOrigin != PD_OR_MANUAL )
      msgbox("Изменение реквизитов запрещено. Распоряжение сформировано на основании сообщения, выставленного банком получателя");
      return CHANG_NOTKEEP;
    end;

    if( r_pmpaym.PrimDocOrigin == PD_OR_MANUAL )
      PM_GetOprStatus( r_pmpaym.DocKind, r_pmpaym.PaymentID, OPR_CLAIM_CONTROL, @CtrlStatus );
      if( CtrlStatus == OPR_CL_ST_CONTROLLED )
        msgbox("Изменение реквизитов запрещено. Распоряжение проконтролировано");
        return CHANG_NOTKEEP;
      end;
    end;

  end;
  
  
  var stat = 0;
  if( (Режим == OBJ_INSERT) or (Режим == OBJ_UPDATE) )
    stat = CL_ScrolMacroCommonChecks( flds, r_pmpaym, r_debet, r_credit, r_pmrmprop );
  end;

  return stat;
         
end;

macro    Функция_Пользователя( Режим:integer  )
  return 0;
end;
