import FIInter, PTInter, CTInter, "globals.mac", cb_sql;
import EAInter;
import rsd, oralib, likepy;
import "ea_lib.mac";

record RecEDSTUNITLOG(edstunitlog);
file FileEDSTUNITLOG(edstunitlog) key 0;
record RecEDOC(edoc);
file FileEDOC(edoc) key 0;
file FileEDKIND(edkind) key 0;
file FileEDSTUNITREG(edstunitreg) key 0;

private macro _GetRegistryFileNameANSI()
  var stat;
  var textDir;
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, textDir, stat);
  return textDir + "\\ea_regis_ansi." + UserNumber;
end;

private macro _GetRegistryFileNameOEM()
  var stat;
  var textDir;
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, textDir, stat);
  return textDir + "\\ea_regis_oem." + UserNumber;
end;

private macro _EA_GetPathArchDocFileName(edstunitlog)
  var stat;
  var vFileName = "";
  var vImageDir = "";

  stat = EA_GetImageDir(edstunitlog, vImageDir);
  stat = EA_CreateDir(ToANSI(vImageDir, true));
  stat = EA_GetRegistryFileName(edstunitlog, vFileName);

  return vImageDir + vFileName;
end;

private macro _PrintHeaderXML()
  println("<?xml version=\"1.0\" encoding=\"Windows-1251\"?>");
  println("<!-- РЕЕСТР ЕХ -->");
end;

private macro _Print_filial(edstunitlog)
  var name = edstunitlog.Department;
  var partyname = GetDprtPartyName(edstunitlog.Department);

  println("<filial");
  println("     name=\""     , StrSubst(name     , "\"", ""), "\"");
  println("     partyname=\"", StrSubst(partyname, "\"", ""), "\"");
  println("/>");
end;

private macro _Print_type(edstunitlog)
  var name = GetStoreUnitTypeName(edstunitlog.StoreUnitType);
  var media = GetMediaKindName(edstunitlog.StoreUnitType);
  var maxsize = GetMediaKindSize(edstunitlog.StoreUnitType); 

  println("<type");
  println("     name=\""   , StrSubst(name, "\"", ""), "\"");
  println("     media=\""  , media  , "\"");
  println("     maxsize=\"", maxsize, "\"");
  println("/>");
end;

private macro _Print_comlpetionreason(edstunitlog)
  var number = GetStoreUnitTypeCompleteCondition(edstunitlog.StoreUnitType);
  var desc = GetCompleteConditionName(number);

  println("<comlpetionreason");
  println("     number=\"", number, "\"");
  println("     desc=\""  , desc  , "\"");
  println("/>");
end;

private macro _Print_dates(edstunitlog)
  var operday = string(edstunitlog.OperDate:x);
  var created = string(edstunitlog.CreateDate:x) + string(edstunitlog.CreateTime:x);
  var completed = string(edstunitlog.CompleteDate:x) + string(edstunitlog.CompleteTime:x);
  var recorded = string(edstunitlog.RecordDate:x) + string(edstunitlog.RecordTime:x);
  var storagecomplete = string(edstunitlog.StorageCompleteDate:x);
  var periodbegin = string(edstunitlog.PeriodBeginDate:x);
  var periodend = string(edstunitlog.PeriodEndDate:x);

  println("<dates");
  println("     operday=\""        , operday        , "\"");
  println("     created=\""        , created        , "\"");
  println("     completed=\""      , completed      , "\"");
  println("     recorded=\""       , recorded       , "\"");
  println("     storagecomplete=\"", storagecomplete, "\"");
  println("     periodbegin=\""    , periodbegin    , "\"");
  println("     periodend=\""      , periodend      , "\"");
  println("/>");
end;

private macro _Print_creator(edstunitlog)
  var num = edstunitlog.Creator;
  var fio = GetOperName(edstunitlog.Creator);

  println("<creator");
  println("     num=\"", num, "\"");
  println("     fio=\"", fio, "\"");
  println("/>");
end;

private macro _Print_recorder(edstunitlog)
  var num = edstunitlog.Recorder;
  var fio = GetOperName(edstunitlog.Recorder);

  println("<recorder");
  println("     num=\"", num, "\"");
  println("     fio=\"", fio, "\"");
  println("/>");
end;

private macro _Print_receiptor(edstunitlog)
  var num = edstunitlog.Receiptor;
  var fio = GetOperName(edstunitlog.Receiptor);

  println("<receiptor");
  println("     num=\"", num, "\"");
  println("     fio=\"", fio, "\"");
  println("/>");
end;

private macro _Print_storeunit(edstunitlog)
  var number = edstunitlog.Number;
  var headbank = GetDprtPartyName({OperDprt});
  var size = int(edstunitlog.StoreUnitSize);
  var hash = GetHashStringEDSTUNITLOG(edstunitlog);
  var comment = edstunitlog.Comment;
  var edtotal = GetDocTotal(edstunitlog);

  println("<storeunit");
  println("     number=\""  , number  , "\"");
  println("     headbank=\"", StrSubst(headbank, "\"", ""), "\"");
  println("     size=\""    , size    , "\"");
  println("     hash=\""    , hash    , "\"");
  println("     comment=\"" , comment , "\"");
  println("     edtotal=\"" , edtotal , "\"");
  println(">");

  _Print_filial(edstunitlog);
  _Print_type(edstunitlog);
  _Print_comlpetionreason(edstunitlog);
  _Print_dates(edstunitlog);
  _Print_creator(edstunitlog);
  _Print_recorder(edstunitlog);
  _Print_receiptor(edstunitlog);

  println("</storeunit>");
end;

private macro _Print_account(accountid)

  var query;
  var rs;
  var params : TArray;
  var number    = "";
  var chapter   = "";

  CaptureOutput;
[
SELECT t_Account, T_CHAPTER
  FROM daccount_dbt
 WHERE t_AccountID = :AccountID
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("AccountID", AccountID)
                    );
  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    number    = rs.value(0);
    chapter   = rs.value(1);
  end;

  println("<account");
  println("     number=\""     , number   , "\"");
  println("     chapter=\""    , chapter  , "\"");
  println("     accountid=\""  , accountid, "\"");
  println("/>");
end;

private macro _Print_edockind(edkind)
  var num = edkind.EDocKindID;
  var name = edkind.Name;
  var converttopdf;
  if(edkind.ConvertToPdf == "X")
    converttopdf = "да";
  else
    converttopdf = "нет";
  end;

  println("<edockind");
  println("     num=\""         , num         , "\"");
  println("     name=\""        , StrSubst(name, "\"", ""), "\"");
  println("     converttopdf=\"", converttopdf, "\"");
  println("/>");
end;

private macro _Print_branch(edoc)
  var code = edoc.Branch;
  var name = GetDprtName(edoc.Branch);
  var partyname = GetDprtPartyName(edoc.Branch);

  println("<branch");
  println("     code=\""     , code                         , "\"");
  println("     name=\""     , StrSubst(name     , "\"", ""), "\"");
  println("     partyname=\"", StrSubst(partyname, "\"", ""), "\"");
  println("/>");
end;

private macro _Print_oper(edoc)
  var num = edoc.Oper;
  var fio = GetOperName(edoc.Oper);

  println("<oper");
  println("     num=\"", num, "\"");
  println("     fio=\"", fio, "\"");
  println("/>");
end;

private macro _Print_edobject(edoc)
  var type = edoc.ObjectType;
  var id = edoc.ObjectID;

  println("<edobject");
  println("     type=\"", type, "\"");
  println("     id=\""  , id  , "\"");
  println("/>");
end;

private macro _GetFIKindName(FIKindID)
  var FIKindName = "";
  file FileFIKINDS(fikinds) key 0;
  FileFIKINDS.FI_Kind = FIKindID;
  if(GetEQ(FileFIKINDS))
    FIKindName = FileFIKINDS.Name;
  end;
  return FIKindName;
end;

private macro _GetFIKindID(FIID)
  var FIKindID = 0;
  file FileFININSTR(fininstr) key 0;
  FileFININSTR.FIID = FIID;
  if(GetEQ(FileFININSTR))
    FIKindID = FileFININSTR.FI_Kind;
  end;
  return FIKindID;
end;

private macro _Print_debet(edstunitreg)
  var account = edstunitreg.DebetAccount;
  var ficode = ПолучитьКодФинИн(edstunitreg.DebetFIID);
  var SumInFIID = edstunitreg.DebetSum;

  println("<debet");
  println("     account=\""    , account  , "\"");
  println("     ficode=\""     , ficode   , "\"");
  println("     SumInFIID=\""  , SumInFIID, "\"");
  println("/>");
end;

private macro _Print_credit(edstunitreg)
  var account = edstunitreg.CreditAccount;
  var ficode = ПолучитьКодФинИн(edstunitreg.CreditFIID);
  var SumInFIID = edstunitreg.CreditSum;

  println("<credit");
  println("     account=\""    , account  , "\"");
  println("     ficode=\""     , ficode   , "\"");
  println("     SumInFIID=\""  , SumInFIID, "\"");
  println("/>");
end;

private macro _Print_devspravka(edstunitreg)
  var SumInNatcur = edstunitreg.NatCurDocSum;

  println("<devspravka");
  println("     SumInNatcur=\"", SumInNatcur, "\"");
  println(">");

  _Print_debet(edstunitreg);
  _Print_credit(edstunitreg);

  println("</devspravka>");
end;

private macro _GetMaxEDocLogID4EDocID(EDocID : string, EDocKindID : integer)
  var query;
  var rs;
  var params : TArray;
  var EDocLogID = 0;

  CaptureOutput;
[
SELECT NVL(MAX(t_EDocLogID), 0)
  FROM dedoc_dbt
 WHERE t_EDocID = :EDocID 
   AND t_EDocKindID = :EDocKindID
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("EDocID", EDocID)
                    ,SQLParam("EDocKindID", EDocKindID)
                    );
  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    EDocLogID = rs.value(0);
  end;
  return EDocLogID;
end;

private macro _IsUploadWaiting(EDocLogID)
  var query;
  var rs;
  var params : TArray;
  var Waiting = false;

  CaptureOutput;
[
SELECT 1
  FROM dedoc_dbt
 WHERE t_EDocLogID = :EDocLogID
   AND (   t_UploadResult = 0
        OR (    t_UploadResult IS NULL
            AND t_CreateResult = 0
           )
       )
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("EDocLogID", EDocLogID)
                    );
  rs = execSQLselect(query, params, true);

  if(rs and (not rs.moveNext()))
    Waiting = true;
  end;
  return Waiting;
end;

private macro _GetRepeatUpload(edoc)
  var RepeatUpload = "";
  var EDocLogID = _GetMaxEDocLogID4EDocID(edoc.EDocID, edoc.EDocKindID);
  if(EDocLogID != 0)
    if(_IsUploadWaiting(EDocLogID))
      RepeatUpload = "ожидается";
    end;
  end;
  if(edoc.UploadControl == EDOC_UPLOADCONTROL_REQUIRE)
    if(RepeatUpload != "") 
      RepeatUpload = RepeatUpload + ","; 
    end;
    RepeatUpload = RepeatUpload + "требуется";
  end;
  if(edoc.UploadControl == EDOC_UPLOADCONTROL_DENY)
    if(RepeatUpload != "") 
      RepeatUpload = RepeatUpload + ","; 
    end;
    RepeatUpload = RepeatUpload + "запрещена";
  end;
  return RepeatUpload;
end;

private macro _Print_edoc(edoc)
  var edoclogid = edoc.EDocLogID;
  var edocid = edoc.EDocID;
  var formcode = edoc.FormCode;
  var docnumber = edoc.DocNumber;
  var docdate = string(edoc.DocDate:x);
  var createdatetime = string(edoc.CreateDate:x) + string(edoc.CreateTime:x);
  var filename = edoc.FileName;
  var repeatupload = _GetRepeatUpload(edoc);
  var parentedocid = edoc.ParentEDocID;
  var basicdocid = edoc.BasicEDocLogID;
  var filehash = "";
  var iscashdoc = edoc.IsCashDoc;
  var isacctrn = edoc.IsAccTrn;
  var findedEDSTUNITREG = false;

  FileEDSTUNITREG.EDocLogID = edoc.EDocLogID;
  if(GetEQ(FileEDSTUNITREG))
    filehash = GetHashStringEDSTUNITREG(FileEDSTUNITREG);
    findedEDSTUNITREG = true;
  end;

  println("<edoc");
  println("     edoclogid=\""        , edoclogid      , "\"");
  println("     edocid=\""           , edocid         , "\"");
  println("     formcode=\""         , formcode       , "\"");
  println("     docnumber=\""        , docnumber      , "\"");
  println("     docdate=\""          , docdate        , "\"");
  println("     createdatetime=\""  , createdatetime, "\"");
  println("     filename=\""         , filename       , "\"");
  println("     repeatupload=\""     , repeatupload   , "\"");
  println("     parentedocid=\""     , parentedocid   , "\"");
  println("     basicdocid=\""       , basicdocid     , "\"");
  println("     filehash=\""         , filehash       , "\"");
  println("     iscashdoc=\""        , iscashdoc      , "\"");
  println("     isacctrn =\""        , isacctrn       , "\"");
  println(">");

  _Print_account(edoc.AccountID);

  FileEDKIND.EDocKindID = edoc.EDocKindID;
  if(GetEQ(FileEDKIND))
    _Print_edockind(FileEDKIND);
  end;

  _Print_branch(edoc);
  _Print_oper(edoc);
  _Print_edobject(edoc);
  
  if(findedEDSTUNITREG)
    _Print_devspravka(FileEDSTUNITREG);
  end;
  
  println("</edoc>");
end;

private macro _Print_edoc_list(edstunitlog)
  var query;
  var rs;
  var params : TArray;

  CaptureOutput;
[
SELECT t_EDocLogID
  FROM dedoc_dbt
 WHERE t_StoreUnitLogID = :StoreUnitLogID
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("StoreUnitLogID", edstunitlog.StoreUnitLogID)
                    );
  rs = execSQLselect(query, params, true);

  println("<edoc-list>");

  while(rs and rs.moveNext())
    FileEDOC.EDocLogID = rs.value(0);
    if(GetEQ(FileEDOC))
      _Print_edoc(FileEDOC);
    end;
  end;
  
  println("</edoc-list>");
end;

private macro _Print_stunitregistry(edstunitlog)
  println("<stunitregistry>");
  _Print_storeunit(edstunitlog);
  _Print_edoc_list(edstunitlog);
  println("</stunitregistry>");
end;

private macro _PrintBodyXML(edstunitlog)
  _Print_stunitregistry(edstunitlog);
end;

macro EA_CreateRegistry(edstunitlog)

  var RealFileNameOEM : string; /*реальное имя файла документа в OEM*/
  var RealFileNameANSI : string; /*реальное имя файла документа в ANSI*/
  var ArchFileName : string; /*полное имя файла документа в архиве узла ТС*/

  SetBuff(RecEDSTUNITLOG, edstunitlog);

  RealFileNameOEM = _GetRegistryFileNameOEM();
  RealFileNameANSI = _GetRegistryFileNameANSI();
  ArchFileName = _EA_GetPathArchDocFileName(RecEDSTUNITLOG);

  SetOutput(RealFileNameOEM, false);

  _PrintHeaderXML();
  _PrintBodyXML(RecEDSTUNITLOG);

  SetOutput(null, false);

  ConvertToANSI(RealFileNameOEM, RealFileNameANSI);

  if(not CopyFile(RealFileNameANSI, ArchFileName))
    msgbox("Ошибка при копировании файла|" + RealFileNameANSI + "|в каталог архива|" + ArchFileName);
  end;
end;

/*eof*/