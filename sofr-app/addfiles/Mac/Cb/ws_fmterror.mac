Import ws_lib_fun, fm_str, BankInter, GateInter;
Import FMInter, PTInter, FIInter, "globals.mac";
import rsd, oralib;

/*
    Класс TWsGateTerrorInfo 
*/
class TWsGateTerrorInfo

  var Name_	           : string = ""; /* Наименование террориста. */
  var AltNames         : TArray     ; /* Дополнительные имена террориста. Массив строк с именами. */
  var RegAddress       : string = ""; /* Адрес регистрации. Заполняется только по справочнику ФСФМ РФ. */
  var FactAdress       : string = ""; /* Фактический адрес. Заполняется только по справочнику ФСФМ РФ. */
  var INN              : string = ""; /* ИНН. Заполняется только по справочнику ФСФМ РФ. */
  var MemberKind       : string = ""; /* Вид участника. Заполняется только по справочнику ФСФМ РФ. */
                                      /* Возможные значения: */
                                      /* Ю - организация     */
                                      /* Ф - физ. лицо       */
  var BornDate         : date   = date(0,0,0); /* Дата рождения. Заполняется только по справочнику ФСФМ РФ. */
  var CodeDocum        : string = ""; /* УД код. Заполняется только по справочнику ФСФМ РФ. */
  var PaperNumber      : string = ""; /* УД номер. Заполняется только по справочнику ФСФМ РФ. */
  var PaperSeries      : string = ""; /* УД серия. Заполняется только по справочнику ФСФМ РФ. */
  var GroupID          : integer= 0; /* Группа. Возможные значения:          */
                                     /*         1 - Террористы ФСФМ РФ       */
                                     /*         2 - Террористы OFAC/SDN List */
  var DocumentName     : string = ""; /* Название док-та, удост. личность. Заполняется только по справочнику ФСФМ РФ. */
  var RegistrPlaceCode : string = ""; /* Код места регистрации. Заполняется только по справочнику ФСФМ РФ. */
  var StandPlaceCode   : string = ""; /* Код места нахождения. Заполняется только по справочнику ФСФМ РФ. */

  var UpdateDate       : date   = date(0,0,0); /* Дата обновления. */
  var DeleteDate       : date   = date(0,0,0); /* Дата удаления. Заполняется только по справочнику ФСФМ РФ. */
  var Notes            : TArray     ; /* Массив строк с дополнительной информацией. Заполняется только по справочнику OFAC/SDN List. */
end;

private macro GetKFM(result)

  var terrorInfo = null;

  var TerroristID = 0;

  var i = 0;

  var query, rs;
  query = "SELECT kfm.t_terroristID, kfm.t_ChType, kfm.t_CodeDocum, kfm.t_inn, kfm.t_RegDate," +
          "       kfm.t_DateUpdate, kfm.t_DateDelete, kfm.t_CodCR, kfm.t_CodCN, " +
          "       aka.t_Name, " +
          "       info.t_LegalAddress, info.t_Address, info.t_PaperNumber, info.t_PaperSeries, info.t_NameDocum " +
          "  FROM dterr_kfm_dbt kfm,  dterr_kfm_aka_dbt aka, dterrinfo_dbt info " +
          " WHERE kfm.t_terroristID = aka.t_TerroristID  " +
          "   AND info.t_TerroristID = kfm.t_TerroristID " +
          " ORDER BY kfm.t_terroristID, aka.t_NameID;    ";
 
  rs = execSQLselect(query);
  while(rs and rs.moveNext())
      
    if(TerroristID != rs.value("t_terroristID"))
      
      if( terrorInfo != null ) 
	      result[result.size] = terrorInfo;
	    end;
      
      TerroristID = rs.value("t_terroristID");
      terrorInfo = TWsGateTerrorInfo;
      
      terrorInfo.Name_ = rs.value("t_Name");
      terrorInfo.RegAddress = rs.value("t_LegalAddress"); 
      terrorInfo.FactAdress = rs.value("t_Address"); 
      terrorInfo.INN = rs.value("t_INN"); 
      terrorInfo.MemberKind = rs.value("t_ChType"); 
      terrorInfo.BornDate = rs.value("t_RegDate"); 
      terrorInfo.CodeDocum = rs.value("t_CodeDocum"); 
      terrorInfo.PaperNumber = rs.value("t_PaperNumber"); 
      terrorInfo.PaperSeries = rs.value("t_PaperSeries"); 
      terrorInfo.GroupID = 1; 
      terrorInfo.DocumentName = rs.value("t_NameDocum"); 
      terrorInfo.RegistrPlaceCode = rs.value("t_CodCR"); 
      terrorInfo.StandPlaceCode = rs.value("t_CodCN");
      terrorInfo.UpdateDate = rs.value("t_DateUpdate"); 
      terrorInfo.DeleteDate = rs.value("t_DateDelete");

      terrorInfo.AltNames = TArray;
      terrorInfo.Notes = TArray;
    else
      terrorInfo.AltNames[terrorInfo.AltNames.size] = rs.value("t_Name");  
    end;
  end;     

  if( terrorInfo != null ) 
	  result[result.size] = terrorInfo;
	end;
end;

private macro GetOFAC(result)

  var terrorInfo = null;
  var query, rs;
  var EntNum = 0;
  var prevAkaID = 0;
  var prevNoteID = 0;
  query  = "SELECT * FROM " +
           "  (SELECT terr.t_entnum As EntNum, terr.t_name, terr.t_dateupdate, NVL(aka.t_autoinc, 0) As AkaID," +
           "          NVL(aka.t_aka, chr(1)) As AkaName, 0 As NoteID, chr(1) As NoteText" +
           "     FROM dterror_dbt terr " +
           "     LEFT JOIN dter_aka_dbt aka ON terr.t_entnum = aka.t_entnum" +
           "    UNION ALL" +
           "     SELECT terr.t_entnum, terr.t_name, terr.t_dateupdate, 0 As AkaID, chr(1) As AkaName, " +
           "          NVL(note.t_autoinc, 0) As NoteID,   NVL(note.t_Note, chr(1)) As NoteText " +
           "       FROM dterror_dbt terr " +
           "       LEFT JOIN dter_note_dbt note ON terr.t_entnum = note.t_entnum)" +
           " ORDER BY EntNum, AkaID DESC, NoteID DESC;";
 
  rs = execSQLselect(query );
  while(rs and rs.moveNext())
      
    if(EntNum != rs.value("EntNum"))
      
      if( terrorInfo != null ) 
	      result[result.size] = terrorInfo;
	    end;
      
      EntNum = rs.value("EntNum");
      
      terrorInfo = TWsGateTerrorInfo;      
      terrorInfo.Name_ = rs.value("t_Name");
      terrorInfo.GroupID = 2; 
      terrorInfo.UpdateDate = rs.value("t_DateUpdate");

      terrorInfo.AltNames = TArray;
      var AkaID = rs.value("AkaID");      
      if( AkaID != 0 ) 
        terrorInfo.AltNames[terrorInfo.AltNames.size] = rs.value("AkaName");
        prevAkaID = AkaID;
      end;

      terrorInfo.Notes = TArray;
      var NoteID = rs.value("NoteID");
      if( NoteID != 0 )
        terrorInfo.Notes[terrorInfo.Notes.size] = rs.value("NoteText");
	      prevNoteID = NoteID;
      end;

    else
      AkaID = rs.value("AkaID");
      if( (AkaID != 0) AND (prevAkaID != AkaID) )            
        prevAkaID = AkaID;
        terrorInfo.AltNames[terrorInfo.AltNames.size] = rs.value("AkaName");
      end;

      NoteID = rs.value("NoteID");
      if( (NoteID != 0) AND (prevNoteID != NoteID) )
      	prevNoteID = NoteID; 
        terrorInfo.Notes[terrorInfo.Notes.size] = rs.value("NoteText");  
      end;
    end;
  end;
  
  if( terrorInfo != null ) 
	  result[result.size] = terrorInfo;
  end;
end;

macro GetTerrorList()

  var result = TArray();

  GetKFM (result);
  GetOFAC(result);
    
  return result;
end;
