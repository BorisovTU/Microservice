/*
$Name:           sfpay_lib.mac
$Module:         Ядро ГКБО
$Description:    ПЗО: Библиотека классов и функций
*/


import sfcommon, InsCarryDoc, OprInter, PaymInter, SFInter, CurrInter, pm_opr, sfgetcat;

const BankNameLen = 140;

class TSfPayParams
  var payDate; //дата оплаты
  var ground = "";  //основание
  var paySum = $0;  //сумма в валюте FIID
  var taxSum = $0;  //НДС в валюте FIID
  var FIID = -1; 
  var primKind = 0; //тип первички KIND_OF_DOC
  var primID = 0;   //ID первички
  var objectType = 0; //вид объекта
  var objectBuf = null;  //объект
  var payMethod;
  var SfComPD = null;    // для единовременных и период объект класса SfConComPrimDoc, для разовых - SfSingDfPrimDoc
  var IsIncluded = null;
  var isNVPI = false;
  var FacturaID = 0;
  var IsNDSSumReset = false;
  
  var payFIID = -1;
  var feeType = 0;
  var objectID = 0;
  var PlusCalc_Account = null;
  var PlusCalcNDS_Account = null;
  var MinusNdsAcrual_Account = null;
  var MinusNDS_Account = null;
  var PreAcptID = 0;

  var RefPaymNum = null; // Номер платежа по референсу для вида объекта $(Рублевый платежный документ)
  var IsChecked  = false; // признак того, что параметры формирования документа оплаты проверены.

  var ErrorNum:integer = 0; // Номер ошибки
  var ErrorMsg:string = null; // Текст сообщения об ошибке

  macro SetError( ErrNum:integer, ErrMsg:string )
    ErrorNum = ErrNum;
    ErrorMsg = ErrMsg;
  end;

  macro correctSums( sfcomiss )
    var rateType = sfcomiss.Ratetype;
    payFIID = FIID;
    if( objectType == OBJTYPE_SFINVOICE )
      objectID = objectBuf.InvoiceID;
      rateType = objectBuf.PayRateID;
      payFIID = objectBuf.payFIID;
    else
      objectID = objectBuf.ID;
      if( sfcomiss.FIID_paySum != -1 )
        payFIID = sfcomiss.FIID_paySum;
      end;
    end;

    if( FIID != payFIID )
      ConvSum( paySum, paySum, payDate, FIID, payFIID, Ratetype );
      ConvSum( taxSum, taxSum, payDate, FIID, payFIID, Ratetype );
    end;
  end;

  macro getPlusCalcAccounts( sicredit )
    var CalcCatCode:string, CalcNDSCatCode:string;

    if( (IsIncluded != NULL) AND (IsIncluded == "X") )

      if( feetype == SF_FEE_TYPE_INVOICE )

        payFIID = sicredit.Rec.FIID;
        PlusCalc_Account = sicredit.Rec.Account;

        if( taxSum > 0 )
          PlusCalcNDS_Account = sicredit.Rec.ReceiverNDSAccount;
        end;

      else

        if((SfComPD != NULL) AND (isNVPI != NULL))
          if( isNVPI == true )
            CalcCatCode = PlusCalcNVPI_CatCode;
            CalcNDSCatCode = PlusCalcNDS_NVPI_CatCode;
          else
            CalcCatCode = PlusCalc_CatCode;
            CalcNDSCatCode = PlusCalcNDS_CatCode;
          end;

          if((feeType == SF_FEE_TYPE_PERIOD) AND (objectID > 0))
            PlusCalc_Account = SfComPD.GetDefComSfSiAccount( objectID, CALC_SFSI_KIND, CalcCatCode, date, payFIID );
            PlusCalcNDS_Account = SfComPD.GetDefComSfSiAccount( objectID, CALCNDS_SFSI_KIND, CalcNDSCatCode, date, payFIID );
          else
            PlusCalc_Account = SfComPD.FindAndOpenAccount( CalcCatCode, payDate, payFIID );
            PlusCalcNDS_Account = SfComPD.FindAndOpenAccount( CalcNDSCatCode, payDate, payFIID );
          end;
        end;

        /*Если счета начисления НДС "+Расчеты НДС, НВПИ"/"+Расчеты НДС" и комиссии "+Расчеты, НВПИ"/"+Расчеты" совпадают 
        и комиссия облагается НДС (DOPRSFCOM_DBT.T_SUMNDS > 0),*/
        if( (PlusCalcNDS_Account == PlusCalc_Account) AND (taxSum > 0) )
          sicredit.Rec.Account = PlusCalc_Account;
          sicredit.Rec.FIID = payFIID;        
        else
          if( taxSum > 0 )
            sicredit.Rec.Account = PlusCalc_Account;
            sicredit.Rec.FIID = payFIID;
            sicredit.Rec.ReceiverNDSAccount = PlusCalcNDS_Account;
          else
            sicredit.Rec.Account = PlusCalc_Account;
            sicredit.Rec.FIID = payFIID;
          end;
        end;
      
      end;

    end;

  end;

end;
     
class TSfFormPayDocParams
  var sidebet;    // СПИ по дебету УПК - запись таблицы dsfsi_dbt
  var sicredit;   // СПИ по кредиту УПК - запись таблицы dsfsi_dbt
  var sfcomiss;   // буфер вида периодический комиссии -  - запись таблицы dsfcomiss_dbt
  var payParams;  // параметры оплаты - объект класса TSfPayParams 
  var sfdefIndex; // индекс эдемента массива, содержащих данные о УК. Нужен, чтобы внести коррективы в УК после формирования документа оплаты.
end;

private macro getMinusNDSAccount( sfcomiss, payParams:TSfPayParams, DprtID:@integer )

  var MinusNDSAccount:string = "";
  DprtID = {OperDprt};
  record AccountRec( account );
  file SfContr( "sfcontr.dbt") key 0;

  if((payParams.MinusNds_Account == NULL) OR ((payParams.MinusNds_Account == "")) )
    if( (payParams.primKind == SFCONTR_DOC) AND (payParams.primID > 0) )
      SfContr.Id = payParams.primID;
      if( getEQ( SfContr ) )
        DprtID = SfContr.Department;
        MinusNDSAccount = SfGetCatAccount( SfContr, NATCUR, "-НДС", null, true );
      end;
    end;

    if( MinusNDSAccount == "" )
      SfGetCategoryAccount( sfcomiss, OBJROLE_SFCOMISS_TAX_ACC, AccountRec, DprtID );
      MinusNDSAccount = AccountRec.Account;
    end;
  else
    MinusNDSAccount = payParams.MinusNds_Account;
  end;

  return MinusNDSAccount;
end;

private macro SfPayConvSum( SumTo:@money, SumFrom, convDate, FIID_From, FIID_To, payParams )
  if( ConvSum( SumTo, SumFrom, convDate, FIID_From, FIID_To, 0 ) != 0 )
    var msg = "Не найден основной курс "+ПолучитьКодФинИн(FIID_From)+" относительно "+ПолучитьКодФинИн(FIID_To)+" за "+convDate;
    MsgBox( msg );
    payParams.SetError(SFPAY_ERROR_COMMON, msg);
    return 1;
  else
    return 0;
  end;
end;

private macro GetAccountDprtParm( Account, Chapter, FIID, DprtID:@integer, Branch:@integer )
  record AccountRec ("account");
  if( SfCommon_GetAccount(AccountRec, Account, FIID, Chapter) == true )
    DprtID = AccountRec.Department;
    if( Branch )
      Branch = AccountRec.Branch;
    end;
  end;
end;

private macro InsertPmAddPI( Paym, sidebet, sicredit, sfcomiss, payParams:TSfPayParams, paySumInPmFIID, taxSumInPmFIID )
  
  var pmaddpi    = TRecHandler("pmaddpi.dbt");
  var pmaddpiNDS = TRecHandler("pmaddpi.dbt");
  ClearRecord( pmaddpi    );
  ClearRecord( pmaddpiNDS );    

  pmaddpi.Rec.DebetCredit = PRT_Credit;
  pmaddpi.Rec.PMFIID      = sidebet.Rec.FIID;
  pmaddpi.Rec.PMAmount    = paySumInPmFIID;

  pmaddpi.Rec.FIID        = sicredit.rec.FIID;

  if( SfPayConvSum( @pmaddpi.Rec.Amount, payParams.paySum, payParams.PayDate, payParams.payFIID, pmaddpi.Rec.FIID, payParams ) != 0 )
    return 1;
  end;
 
  pmaddpi.Rec.Account     = sicredit.Rec.Account;
  pmaddpi.Rec.Chapter     = 1/*CHAPT1*/;
  pmaddpi.Rec.Ground      = "Оплата комиссии " + sfcomiss.Code;
  pmaddpi.Rec.Kind        = PMADDPI_KIND_SF;

  Paym.Department = sicredit.Rec.Department;

//  GetAccountDprtParm( pmaddpi.Rec.Account, pmaddpi.Rec.Chapter, pmaddpi.Rec.FIID, @Paym.Department );
  pmaddpi.Rec.Department = Paym.StartDepartment = Paym.EndDepartment = Paym.Department;

  pmaddpiNDS.Rec.DebetCredit = PRT_Credit;
  pmaddpiNDS.Rec.PMFIID      = sidebet.Rec.FIID;
  pmaddpiNDS.Rec.PMAmount    = taxSumInPmFIID;

  var MinusNDSAccount:string = getMinusNDSAccount( sfcomiss, payParams, @pmaddpiNDS.Rec.Department );

  if( sicredit.Rec.ReceiverNDSAccount == MinusNDSAccount )
    pmaddpiNDS.Rec.FIID        = NATCUR;
    if( SfPayConvSum(@pmaddpiNDS.Rec.Amount, payParams.taxSum, payParams.PayDate, payParams.payFIID, NATCUR, payParams) != 0 )    
      return 1;
    end;
  else
    pmaddpiNDS.Rec.FIID        = sicredit.rec.FIID;
    if( SfPayConvSum( @pmaddpiNDS.Rec.Amount, payParams.taxSum, payParams.PayDate, payParams.payFIID, pmaddpiNDS.Rec.FIID, payParams) != 0 )
      return 1;
    end;
  end;
  
  pmaddpiNDS.Rec.Account     = sicredit.Rec.ReceiverNDSAccount;
  pmaddpiNDS.Rec.Chapter     = 1/*CHAPT1*/;
  pmaddpiNDS.Rec.Ground      = "НДС по комиссии " + sfcomiss.Code;
  pmaddpiNDS.Rec.Kind        = PMADDPI_KIND_NDS;

  var ErrMsg = "";
  var Err = Paym.PIList( PRT_Credit ).Insert( pmaddpi );
  if( Err != 0 ) 
    ErrMsg = "Ошибка при вставке разноски по счету комиссии " + GetErrMsg();
    MsgBox( ErrMsg );
    payParams.SetError(Err, ErrMsg);
    return 1;
  end;
  
  Err = Paym.PIList( PRT_Credit ).Insert( pmaddpiNDS );
  if( Err != 0 ) 
    ErrMsg = "Ошибка при вставке разноски по счету НДС " + GetErrMsg();
    MsgBox( ErrMsg );
    payParams.SetError(Err, ErrMsg);
    return 1;
  end;

  return 0;
end;


macro SfFillPaymentExt( Paym, sidebet, sicredit, sfcomiss, payParams:TSfPayParams, PmAmount )
  
  var taxSumInPmFIID = $0;
  var paySumInPmFIID = $0;
  var PayerAmount = $0;
  var BankMsg;

  //PmAmount - not used?
  if( sidebet.Rec.FIID != payParams.payFIID )
    if( ConvSum( PayerAmount, payParams.paySum + payParams.taxSum, payParams.PayDate, payParams.payFIID, sidebet.Rec.FIID ) != 0 )
      payParams.SetError(SFPAY_ERROR_COMMON, "Ошибка конвертации суммы");
      MsgBox( "Ошибка конвертации суммы" );
      return 1;
    end;

    if(payParams.taxSum > $0)
      if( SfPayConvSum( @taxSumInPmFIID, payParams.taxSum, payParams.PayDate, payParams.payFIID, sidebet.Rec.FIID, payParams ) != 0 )
        return 1;
      else
        if(taxSumInPmFIID == $0)
          payParams.IsNDSSumReset = true;
        end;
      end;
    end;


    paySumInPmFIID = PayerAmount - taxSumInPmFIID;
    /*
    if( ConvSum( paySumInPmFIID, PmAmount, payParams.PayDate, payParams.payFIID, sidebet.Rec.FIID, 0 ) != 0 )
      MsgBox("Не найден основной курс ",ПолучитьКодФинИн(FIID), " относительно ",ПолучитьКодФинИн(sidebet.Rec.FIID), " за ", PayDate);
      return 1;
    end;
    */
  else
    PayerAmount = payParams.paySum + payParams.taxSum;
    taxSumInPmFIID = payParams.taxSum;
    paySumInPmFIID = payParams.paySum;
  end;


  var ErrMsg = "";
  var Ret = Paym.SetPayerPI
  ( 
    PAYMENTS_GROUP_UNDEF,
    sidebet.Rec.BankID,
    sidebet.Rec.BankCodeKind,
    sidebet.Rec.BankCode,
    "", /*substr( sidebet.Rec.BankName, 1, BankNameLen ),*/
    sidebet.Rec.CorrAcc,
    sidebet.Rec.FIID,
    1/*CHAPT1*/,
    sidebet.Rec.Account,
    sidebet.Rec.PartyID,
    substr( sidebet.Rec.PartyName, 1, BankNameLen ),
    sidebet.Rec.PartyINN,
    sidebet.Rec.PartyCodeKind,
    sidebet.Rec.PartyCode
  );

  if( Ret == false ) 
    BankMsg = AL_GetErrorInfo();
    ErrMsg = "Ошибка при вставке ПИ платежа " + BankMsg.Message;
    payParams.SetError(BankMsg.Stat, ErrMsg);
  end;

  if( (taxSumInPmFIID == 0) 
    OR( (taxSumInPmFIID > 0) 
        AND(payParams.PlusCalc_Account != NULL) AND (payParams.PlusCalcNDS_Account != NULL) 
        AND(payParams.PlusCalc_Account != "") AND (payParams.PlusCalcNDS_Account != "") 
        AND(payParams.PlusCalc_Account == payParams.PlusCalcNDS_Account) 
      ) 
    )
    Ret = Paym.SetReceiverPI
    ( 
      PAYMENTS_GROUP_UNDEF,
      sicredit.Rec.BankID,
      sicredit.Rec.BankCodeKind,
      sicredit.Rec.BankCode,
      "", /*substr( sicredit.Rec.BankName, 1, BankNameLen ),*/
      sicredit.Rec.CorrAcc,
      sicredit.Rec.FIID,
      1/*CHAPT1*/,
      sicredit.Rec.Account);

    if( Ret == false ) 
      BankMsg = AL_GetErrorInfo();
      ErrMsg = "Ошибка при вставке ПИ платежа " + BankMsg.Message;
      payParams.SetError(BankMsg.Stat, ErrMsg);
    end;

    Ret = Paym.SetReceiverAccount( sicredit.Rec.FIID, 1, sicredit.Rec.Account);
    if( Ret == false ) 
      BankMsg = AL_GetErrorInfo();
      ErrMsg = "Ошибка установки реквизитов получателя " + BankMsg.Message;
      payParams.SetError(BankMsg.Stat, ErrMsg);
    end;
  else
    if( InsertPmAddPI(Paym, sidebet, sicredit, sfcomiss, payParams, paySumInPmFIID, taxSumInPmFIID) != 0 )
      return 1;
    end;
  end;

  Paym.CryptoAction( string("Автоматическое_формирование_платежей") ); 
  Paym.BaseFIID       = payParams.payFIID;

  Paym.PayerAmount    = PayerAmount;
  if( sicredit.Rec.FIID == payParams.payFIID )
    Paym.ReceiverAmount = payParams.paySum + payParams.taxSum;
  else
    if( ConvSum( Paym.ReceiverAmount, payParams.paySum + payParams.taxSum, payParams.PayDate, payParams.payFIID, sicredit.Rec.FIID ) != 0 )
      MsgBox( "Ошибка конвертации суммы" );
      payParams.SetError(SFPAY_ERROR_COMMON, "Ошибка конвертации суммы");
      return 1;
    end;
  end;
  
  Paym.BaseAmount     = payParams.paySum + payParams.taxSum;
  Paym.ValueDate      = 
  Paym.Date           = payParams.PayDate;
  Paym.PayerBankEnterDate = 
  Paym.ClientDate         = {curdate};
  Paym.IsPlanPaym     = "X";
  Paym.PaymentKind    = "Э";

  return 0;
end;

macro SfFillPayment( Paym, sidebet, sicredit, sfcomiss, PayDate, paySum, taxSum, payFIID, 
                     PlusCalc_Account, PlusCalcNDS_Account, PmAmount )
  var payParams = TSfPayParams;
  payParams.payDate = payDate;
  payParams.paySum = paySum; 
  payParams.taxSum = taxSum;
  payParams.payFIID = payFIID;

  payParams.PlusCalc_Account = PlusCalc_Account;
  payParams.PlusCalcNDS_Account = PlusCalcNDS_Account;

  return SfFillPaymentExt( Paym, sidebet, sicredit, sfcomiss, payParams, PmAmount );
end;

macro SfPayDocConnectToOperation( PayDoc, IsBatchMode, oprchild, payParams )
  if( (IsBatchMode != null) AND (IsBatchMode == true) )

    PayDoc.ConnectToOperation( oprchild.ID_Operation, oprchild.ID_Step );

    var Err = PayDoc.Update();
    if( Err != 0 ) 
      var ErrMsg = "Ошибка обновления платежного документа "  + GetErrMsg();
      payParams.SetError(Err, ErrMsg);
      return 1;
    end;

    oprchild.Child_DocKind    = PayDoc.Payment.DocKind;
    oprchild.Child_DocumentID = PM_MakeDocumentID( PayDoc.Payment.DocKind, PayDoc.Payment.DocumentID );
  end;

  return 0;
end;


macro SfPayDocGetRefNumber( RefNumber:@string, payParams )

  if((payParams.RefPaymNum != NULL) AND (payParams.RefPaymNum != ""))
     RefNumber = payParams.RefPaymNum;
  else
    var RefID;
    if( GetReferenceIDByType( OBJTYPE_PSPAYORD, /*Рублевый платежный документ*/
                              REFOBJ_SVORDER,   /*ObjectType = 500; номер требования банка к клиентам (РКО)*/
                              RefID ) != 0 )
      MsgBox( "Ошибка при генерации номера платежного документа" );
      return 1;
    end;
      
    if( GenerateReference( RefID, RefNumber ) != 0 )
      MsgBox( "Ошибка при генерации номера платежного документа" );
      return 1;
    end;
  end;

  return 0;
end;

