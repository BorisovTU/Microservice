/*
$Name:           d_inter.mac
$Module:         
$Description:    Общие функции о групповых действий над документами
*/  

import OprInter, PaymInter;
IMPORT  "gstname.mac";
private RECORD  cb_doc  (cb_doc);
private RECORD  oproper (oproper);
private FILE    pm      (pmpaym) key 1;
private FILE    rm      (pmrmprop);
private RECORD  pspayord(pspayord);
private RECORD  sfpaym  (pmpaym);
private FILE    cashdoc_f(pscshdoc);
private FILE    cb_doc_f (cb_doc);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2,
        ReportLineWidth = 78;

/* Выводим длинную строку Str в поле длиной LineWidth - StrLen( EmptyFlds ) */
/* EmptyFlds - предыдущие пустые колонки                                    */
/* LineWidth - общая длина строки отчета                                    */
macro WriteLongStr( Str, EmptyFlds, LineWidth, EndSymb )
  var i, FieldWidth = LineWidth - StrLen( EmptyFlds ) - 1, NumItems;
  array Strokes;

  macro AppendSpace( Str, Len, EndSymb )
    var slen = strlen( Str ), rstr;
    return Str + MkStr( " ", Len-slen ) + EndSymb;
  end;

  StrSplit( Str, Strokes, FieldWidth );
  println( AppendSpace( Strokes( 0 ), FieldWidth, EndSymb ) );
  i = 1;
  NumItems = Asize( Strokes );
  while( i < NumItems )
    println( AppendSpace( EmptyFlds + Strokes( i ), LineWidth - 1, EndSymb ) );
    i = i + 1;
  end;
end;

macro ПечатьПолногоОтчетаДляДокумента ( Вызов, Статус, Сообщение, УспСообщение, ЗаголовокОтчета, PaymentID)
  record pm_rmprop( pmrmprop );
  record pm_paym  ( pmpaym );
  file pm_addpi ( pmaddpi )  key 1;
  var arrWar, i = 0;  
  var strPayerAccount = "";
  var strReceiverAccount = "";
  var AmountCName1 = "Сумма по";
  var AmountCName2 = "дебету";
  var PayAmountCName1 = "Сумма по";
  var PayAmountCName2 = "кредиту";
  var СтандУспСообщение = "Действие выполнено успешно";
  var ПрУспСообщение = "";

  if( УспСообщение == "" )
    ПрУспСообщение = СтандУспСообщение;
  else
    ПрУспСообщение = УспСообщение;  
  end;

  
  if( Вызов == Первый )


[##########################################################################################################################################](ЗаголовокОтчета:c);
[┌─────────┬────────────────────┬─────────────┬────────────────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │        Счет        │#############│        Счет        │#############│   Дата   │ Рез- │                Сообщение             │]( AmountCName1:c, PayAmountCname1:c );
[│документа│    плательщика     │#############│     получателя     │#############│          │ тат  │                                      │]( AmountCName2:c, PayAmountCname2:c );;
[├─────────┼────────────────────┼─────────────┼────────────────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

  elif( Вызов==Ошибка )
    if( (Статус == 0) and (Сообщение == "") )
      Сообщение = ПрУспСообщение;
    end;

    if ( FindPayment( PaymentID, 
                      0,    
                      0,    
                      0,    
                      0,
                      true,   
                      pm_paym,
                      null,
                      null,
                      pm_rmprop
                      ) != 0 )
      Статус = 1;
      Сообщение = "Не найден платеж";
    elif ( Статус != 10103 ) 
      if (pm_paym.DocKind==322)
        pm_paym.DocKind=320;
      end;        
      Сообщение = Сообщение+". "+GetStepNameExecuteRead(pm_paym.PaymentID, pm_paym.DocKind, pm_paym.PaymStatus);
    end;


    arrWar = GetWarnings(true);
    while( arrWar.size > i )
      Сообщение = Сообщение + "\n" + arrWar[i].Message;
      i = i + 1;
    end;

    strPayerAccount = pm_paym.PayerAccount;
    strReceiverAccount = pm_paym.ReceiverAccount;

    if( strPayerAccount == "" )
      ClearRecord( pm_addpi );
      pm_addpi.PaymentID = PaymentID;
      pm_addpi.DebetCredit = 0;
      if( GetEQ( pm_addpi ) )
        strPayerAccount = "Несколько счетов";
      end;
    end;

    if( strReceiverAccount == "" )
      ClearRecord( pm_addpi );
      pm_addpi.PaymentID = PaymentID;
      pm_addpi.DebetCredit = 1;
      if( GetEQ( pm_addpi ) )
        strReceiverAccount = "Несколько счетов";
      end;
    end;
  
                
[│#########│####################│#############│####################│#############│##########│######│######################################│]
( pm_rmprop.Number, strPayerAccount:c, pm_paym.Amount,  strReceiverAccount:c, pm_paym.PayAmount, pm_rmprop.Date, Статус, StrSubst(Сообщение, "|", " " ):w);
                
        elif (Вызов==Последний)
[└─────────┴────────────────────┴─────────────┴────────────────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
        end;
end;


MACRO ПечатьОтчетаДляДокумента (Вызов,НомерДокумента,Статус,Сообщение,ЗаголовокОтчета)

var stat;

        if (Вызов==Первый)
[##############################################################################](ЗаголовокОтчета);
[┌─────────┬──────┬───────────────────────────────────────────────────────────┐];
[│  Номер  │Номер │                Сообщение                                  │];
[│документа│ошибки│                                                           │];
[├─────────┼──────┼───────────────────────────────────────────────────────────┤];
        elif (Вызов==Ошибка)

      print("│", НомерДокумента:9, "│", Статус:6, "│");

      /* Длинную строку Сообщения разбиваем по пробелам */
      WriteLongStr( StrSubst( Сообщение, "|", " " ), "│         │      │", ReportLineWidth, "│" );

        elif (Вызов==Последний)
[└─────────┴──────┴───────────────────────────────────────────────────────────┘];
        end;
END;

/* Печать отчета для мемориального ордера */
MACRO ПечатьОтчета (Вызов,firstdoc,Статус,Сообщение,ЗаголовокОтчета, stat)
      var УспСообщение = "Действие выполнено успешно";

      setbuff (cb_doc,firstdoc);
      if(stat)
        ПечатьПолногоОтчетаДляДокумента (Вызов,Статус,Сообщение,УспСообщение,ЗаголовокОтчета,cb_doc.DocumentId);
      else
        ПечатьОтчетаДляДокумента (Вызов,GetLastPaymentsNumber(),Статус,Сообщение,ЗаголовокОтчета);
      end;
END;

MACRO ПечатьОтчетаОбобщенногоСписка (Вызов,firstdoc,Статус,Сообщение,ЗаголовокОтчета)
      setbuff (oproper,firstdoc);
      record rmprop(pmrmprop);
      var НомерДокумента:string = "";
      var id:integer = int(oproper.DocumentID),
          PaymentID:integer = 0;
      var err:bool = true;

      if(id > 0)
        if( (oproper.DocKind == DLDOC_BANKPAYMENT)    or
            (oproper.DocKind == DLDOC_BANKCLAIM)      or
            (oproper.DocKind == PS_CPORDER)           or
            (oproper.DocKind == BBANK_CPORDER)        or
            (oproper.DocKind == DLDOC_PAYORDER)
          )
          pm.DocKind    = oproper.DocKind;
          pm.DocumentID = id;

          err = GetGE(pm);
          while( (err) and (PaymentID == 0) and (pm.DocKind == oproper.DocKind) and (pm.DocumentID = id) )
            if(pm.SubPurpose == 0) PaymentID = pm.PaymentID;
            else err = next(pm);
            end;
          end;
          if(PaymentID)
            rm.PaymentID = PaymentID;
            if(GetEQ(rm))
              НомерДокумента = rm.Number;
            end;
          end;

        elif( (oproper.DocKind == CASH_PS_INCORDER)     or
              (oproper.DocKind == CASH_PS_OUTORDER)     or
              (oproper.DocKind == CASH_BOF_INCORDER)    or
              (oproper.DocKind == CASH_BOF_OUTORDER)    or
              (oproper.DocKind == CASH_BOF_ADDORDER)
            )
          cashdoc_f.AutoKey = id;
          if(GetEQ(cashdoc_f))
            НомерДокумента = cashdoc_f.Numb_Document;
          end;

        elif(oproper.DocKind == DLDOC_MEMORIALORDER)
          cb_doc_f.DocumentID = id;
          if(GetEQ(cb_doc_f))
            if( FindPayment( 0, PM_PURP_MEMORDER, 0, 70, cb_doc.DocumentID, true, null, null, null, rmprop ) != 0 )
              Статус = 1;
              Сообщение = "Не найден платеж";
            end;
            НомерДокумента = rmprop.Number;
          end;
        end;
      end;
      ПечатьОтчетаДляДокумента (Вызов,НомерДокумента,Статус,Сообщение,ЗаголовокОтчета);
END;

MACRO ПечатьОтчетаСпискаОплатПЗО (Вызов,firstdoc,Статус,Сообщение,ЗаголовокОтчета)
  setbuff (sfpaym,firstdoc);
  var НомерДокумента:string = "";
  rm.PaymentID = sfpaym.PaymentID;
  if(GetEQ(rm))
    НомерДокумента = rm.Number;
  end;
  ПечатьОтчетаДляДокумента (Вызов,НомерДокумента,Статус,Сообщение,ЗаголовокОтчета);
END;
