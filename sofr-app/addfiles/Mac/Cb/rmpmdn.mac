//-----------------------------------------------------------------------------
// Блок     : Вне блока
// Шаг      : Вне шага
// Описание : Печатная форма отказа от платежа
//-----------------------------------------------------------------------------

import PaymInter, globals;
import oralib, likepy;

var pr_pmdenial:TRecHandler = TRecHandler( "pmdenial.dbt", "bank.def" );

MACRO PrintDocument(ncopy:integer):bool

  var DenialKind:string   = "";
  var DocumentKind:string = "";
  var PayerName:string = "";
  var PayerAccount:string = "";
  var PayerAccountTop:string = "сч. ";
  var DocNumber:string = "";
  var DocDate:date = {curdate};
  var PaymAmount = pr_pmdenial.rec.Amount;
  var PaymAmountStr:string = "";
  var DenAmountStr:string = "";
  array PaymAmountArr;
  array DenAmountArr;
  var BodyStr:string = "";
  var Payer:integer = 0;

  var select:string;
  var params:TArray;
  var rs:object;

  // Ищем платеж
  select = " select pm.t_Amount, pm.t_PayerAccount, pm.t_Payer, rm.t_PayerName, rm.t_Number, rm.t_Date " +
           " from DPMPAYM_DBT pm, DPMRMPROP_DBT rm " +
           " where pm.T_PAYMENTID = :PaymentID     " + 
           "   and rm.T_PAYMENTID = pm.T_PAYMENTID ";

  params = makeArray( SQLParam("PaymentID", pr_pmdenial.rec.PaymentID) );
  rs     = execSQLselect( select, params, FALSE );
    
  if( rs AND rs.moveNext() ) 
    PaymAmount   = rs.value(0);
    PayerAccount = rs.value(1);
    Payer        = rs.value(2);
    PayerName    = rs.value(3);
    DocNumber    = rs.value(4);
    DocDate      = rs.value(5);
  else
    msgbox("Не найден платеж");
    return false;
  end;

  // Ищем вид документа
  select = " select llv.T_NAME           " +
           " from   DLLVALUES_DBT llv    " +
           " where  llv.T_LIST    = 1681 " +
           "   and  llv.T_ELEMENT = :DocumentKind ";

  params = makeArray( SQLParam("DocumentKind", pr_pmdenial.rec.DocumentKind) );
  rs     = execSQLselect( select, params, FALSE );

  if( rs AND rs.moveNext() ) 
    DocumentKind = rs.value(0);
  else
    msgbox("Не найден вид нормативного документа");
    return false;
  end;

  // Ищем название плательщика
  select = " select party.t_Name      " +
           " from   DPARTY_DBT party  " +
           " where  party.t_PartyID = :PartyID ";

  params = makeArray( SQLParam("PartyID", Payer) );
  rs     = execSQLselect( select, params, FALSE );

  if( rs AND rs.moveNext() ) 
    PayerName = rs.value(0);
  else
    msgbox("Не найден плательщик");
    return false;
  end;

  if( pr_pmdenial.rec.Amount == PaymAmount )
    DenialKind = "полностью";
  else
    DenialKind = "частично";
  end;

  PayerAccountTop = PayerAccountTop + PayerAccount;

  strsplit( RubToStrAlt(PaymAmount), PaymAmountArr, 100, 100, 1 );
  PaymAmountStr = PaymAmount + " (" + PaymAmountArr(0) + ")";

  strsplit( RubToStrAlt(pr_pmdenial.rec.Amount), DenAmountArr, 100, 100, 1 );
  DenAmountStr = pr_pmdenial.rec.Amount + " (" + DenAmountArr(0) + ")";

  BodyStr = string( "Документ N ", DocNumber, " от ", DocDate:m:c, " на сумму ", StrSubst( PaymAmountStr, ".", "-") ) + 
  string( " к счёту ", PayerAccount, " отозван (", DenialKind, ") в сумме ", StrSubst( DenAmountStr, ".", "-"), "." );

  while( ncopy != 0 )
[
 ###############################################################################
 ######################################         ################################
───────────────────────────────────────         ────────────────────────────────

Отказ от платежа

################################################################################

Основание отзыва:

################################################################################

################################################################################
]
( PayerName:w,
  PayerAccountTop, {Name_Bank}:w,
  BodyStr:w,
  string( DocumentKind, " N ", pr_pmdenial.rec.Number, " от ", pr_pmdenial.rec.Date:m:c ),
  pr_pmdenial.rec.Ground:w 
);

    ncopy = ncopy - 1;
  end;

  return true;

END;
