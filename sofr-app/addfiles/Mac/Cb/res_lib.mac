/*
$Name: res_lib.mac
$Module: Ядро ГКБО
$Description: Общие функции, необходимые для формирования резерва
*/

Import BankInter, CTInter, FIInter, "res_fun.mac", "sf_lib.mac";

/* Получение даты ввода МСФО 9 */
macro DateBeginMSFO9() : date
  var DateBegin;
  var day,mon,year,pos;
  GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА", V_STRING, DateBegin, null);
  if(DateBegin != "")
    pos = Index(DateBegin, ".");
    day = SubStr(DateBegin, 1, pos-1);
    DateBegin = SubStr(DateBegin, pos+1);
    pos = Index(DateBegin, ".");
    mon = SubStr(DateBegin, 1, pos-1);
    DateBegin = SubStr(DateBegin, pos+1);
    year = SubStr(DateBegin, 1);
    DateBegin = date(Int(day),Int(mon),Int(year));
  else
    DateBegin = date(31,12,9999);
  end;

  return DateBegin;
end;

/* получить буфер счета */
macro GetAccountRecord( accRec, Chapter : integer, Account : string, FIID : integer ) : bool
  file acc ("account.dbt" ) key 0;
  acc.Chapter       = Chapter;
  acc.Account       = Account;
  acc.Code_Currency = FIID;
  if( getEQ( acc ) )
    Copy( accRec, acc );
    return true;
  end;
  return false;
end;

/* получить массив номеров связанных объектов - счета обеспечения */
private macro GetObjRoleSecurAccount( ObjRoleArray : TArray )
  var i : integer;
  var ObjRole : integer;

  /* счета обеспечения - это связанные объекты с номерами 34-43 */

  ObjRole = 34;
  while( ObjRole <= 43 )
    ObjRoleArray[ObjRoleArray.Size()] = ObjRole;

    ObjRole = ObjRole + 1;
  end;

end;


/* получить строковый идентификатор счета */
private macro GetAccountObjectID( accRec )
  var ObjectID : string;
  
  return UniID( accRec, OBJTYPE_ACCOUNT );
end;

/* получить связанный счет для лицевого счета */
private macro GetAccountLinkedToAccount( accLinked, accRec, ObjRole : integer, DateLinked : integer ) : bool
  
  var retval : bool;
  var ObjectID : string;

  ObjectID = GetAccountObjectID( accRec );

  retval = false;
  if( GetLinkedObject(ObjRole, OBJTYPE_ACCOUNT, ObjectID, OBJTYPE_ACCOUNT, accLinked, DateLinked) == 0 )
    retval = true;
  end;

  return retval;

end;

/* получить значение категории для счета */
private macro GetAccountCategory( accRec, GroupID : integer, DateCategory : date, ValidFromDate : @date ) : string
  var Num : string;
  Num = null;
  GetMainObjAttr( null, OBJTYPE_ACCOUNT, UniID(accRec, OBJTYPE_ACCOUNT), GroupID, null, null, Num, DateCategory, ValidFromDate );
  return Num;
end;

/* получить значение категории для субъекта */
private macro GetPartyCategory( partyRec, GroupID : integer, DateCategory : date, ValidFromDate : @date ) : string
  var Num : string;
  Num = null;
  GetMainObjAttr( null, OBJTYPE_PARTY, UniID(partyRec, OBJTYPE_PARTY), GroupID, null, null, Num, DateCategory, ValidFromDate );
  return Num;
end;

/* получить значение примечания субъекта */
private macro GetPartyNote( partyRec, NoteKind : integer, DateNote : date, DateFrom : @date )
  var Note = readNoteForObject( OBJTYPE_PARTY, UniID(partyRec, OBJTYPE_PARTY), NoteKind, DateNote, DateFrom );
  if( (DateFrom == null) or (DateFrom == Date(0, 0, 0)) )
    Note = null;
  end;
  return Note;
end;

/* установить значение категории для субъекта*/
private macro SetPartyCategory( partyRec, GroupID : integer, Num : string, DateCategory : date ) : bool
  return ConnectObjAttr( null, OBJTYPE_PARTY, UniID(partyRec, OBJTYPE_PARTY), GroupID, null, "", Num, DateCategory );
end;

macro SetPartyClassifyInd( partyRec, ClassifyInd : integer, DateCategory : date ) : bool
  return SetPartyCategory( partyRec, PtCategory_ClassifyInd, string(ClassifyInd), DateCategory );
end;

macro GetPartyClassifyInd( partyRec, DateCategory : date ) : string
  return GetPartyCategory(partyRec, PtCategory_ClassifyInd, DateCategory);
end;

macro GetPartyTaxGroup( partyRec, DateCategory : date ) : string
  return GetPartyCategory(partyRec, PtCategory_TaxGroup, DateCategory);
end;

/* установить значение категории для счета */
private macro SetAccountCategory( accRec, GroupID : integer, Num : string, DateCategory : date ) : bool

  return ConnectObjAttr( null, OBJTYPE_ACCOUNT, UniID(accRec, OBJTYPE_ACCOUNT), GroupID, null, "", Num, DateCategory );

end;

/* Установить значение примечания счета */
private macro SetAccountNote( accRec, NoteKind : integer, Note : variant, DateNote : date ) : bool

  return addNoteForObject( OBJTYPE_ACCOUNT, UniID(accRec, OBJTYPE_ACCOUNT), NoteKind, Note, DateNote ) == 0;

end;


/*
 * Получить парный счет
 */                                      
macro GetPairAccount( accCover, Chapter : integer, FIID : integer, Account : string )
  record accRec("account.dbt");

  if( GetAccountRecord( accRec, Chapter, Account, FIID) )

      return GetAccountRecord( accCover, accRec.Chapter, accRec.Code_Currency, accRec.PairAccount );

  end;

  return false;
end;

/* 
 * Получить счет покрытия
 */
macro GetConnectAccount( accCover, Chapter : integer, FIID : integer, Account : string )
  record accRec("account.dbt");

  if( GetAccountRecord( accRec, Chapter, Account, FIID) )

    if( accRec.Code_Currency == NATCUR )
      Copy( accCover, accRec );
      return true;
    else
      return GetConnectAccount( accCover, accRec.Connect_Chapter, accRec.Connect_Currency, accRec.Connect_Account );
    end;

  end;

  return false;

end;

/* 
 * Получить счет покрытия парного счета
 */
macro GetConnPairAccount( accCover, Chapter : integer, FIID : integer, Account : string )
  record accRec("account.dbt");

  if( GetPairAccount( accRec, Chapter, FIID, Account ))

    return GetConnectAccount( accCover, accRec.Connect_Chapter, accRec.Connect_Currency, accRec.Connect_Account );

  end;

  return false;
end;


/* получить остаток на субсчете */
macro GetAbsRestSubAccount( SubAccountCode : string, ReserveAccount : string, DateRest : date ) : money
  var RestSubAccount : money;
  var AccAnaliticsID : integer;
  var SubAccountID   : integer;

  RestSubAccount = $0;

  AccAnaliticsID = GetAccAnaliticsID( SYS_ANL_ACCRESERVE, ReserveAccount, NATCUR, BalanceChapter );
  SubAccountID   = GetSubAccID( SYS_ANL_ACCRESERVE, ReserveAccount, NATCUR, BalanceChapter, SubAccountCode );

  if( SubAccountID )
    RestSubAccount = RestSubAcc( AccAnaliticsID, SubAccountID, DateRest );
    if( RestSubAccount < $0 ) RestSubAccount = -RestSubAccount; end;
  end;

  return RestSubAccount;

end;

/*
 * Получить дату последнего изменения остатка
 */
macro GetLastRestDate(Account : string, DateRest : date, Chapter : integer, Code_Currency : integer) : date
  var query, rs;
  var params : TArray;
  record accRec("account.dbt");
  if( GetAccountRecord( accRec, Chapter, Account, Code_Currency) )
    query =         "SELECT t_RestDate ";
    query = query + "  FROM drestdate_dbt ";
    query = query + " WHERE t_AccountID = :AccountID ";
    query = query + "   AND t_RestCurrency = :RestCurrency ";
    query = query + "   AND t_RestDate <= :RestDate";
    query = query + " ORDER BY t_RestDate DESC";
    params = makeArray(SQLParam("AccountID", accRec.AccountID)
                      ,SQLParam("RestCurrency", accRec.Code_Currency)
                      ,SQLParam("RestDate", DateRest)
                      );
    rs = execSQLselect(query, params, true);
    if(rs and rs.moveNext())
      return date(rs.value(0));
    end;                         
  end;                         
  return date(0, 0, 0);
end;

/*
 * Получить сумму просроченных требований
 */
macro GetAccOverdueSum(accRec, DateSum : date, DateFrom : @date) : money
  var ObjectID : string;
  var NoteSum : money;
  ObjectID = GetAccountObjectID(accRec);
  NoteSum = readNoteForObject(OBJTYPE_ACCOUNT, ObjectID, AccNoteKind_OverdueSum, DateSum, DateFrom);
  return NoteSum;
end;

macro GetAccSumReserveCalcUser(accRec, DateSum : date, DateFrom : @date) : money
  var ObjectID : string;
  var NoteSum : money;
  ObjectID = GetAccountObjectID(accRec);
  NoteSum = readNoteForObject(OBJTYPE_ACCOUNT, ObjectID, AccNoteKind_SumReserveCalcUser, DateSum, DateFrom);
  return NoteSum;
end;

macro GetAccFreeReserveSumRateDate( accRec, DateReserve : date, DateRate : date) : money
  var ObjectID : string;
  var FreeReserveSum, SumR : money;
  var DateFrom : date;
  ObjectID = GetAccountObjectID( accRec );
  FreeReserveSum = readNoteForObject(OBJTYPE_ACCOUNT, ObjectID, AccNoteKind_FreeReserveSum, DateReserve, DateFrom);
  if((DateFrom == null) or (DateFrom == Date(0, 0, 0)) )
    SumR = $0;
  else
    if(accRec.Code_Currency != NATCUR)
      ConvSum(SumR, 
              FreeReserveSum, 
              DateRate, 
              accRec.Code_Currency);
    else
      SumR = FreeReserveSum;
    end;
  end;
  return SumR;
end;

/*
 *  Получить остаток по модулю на счете за дату
 */
macro GetAbsAccRest( Account : string, DateRest : date, Chapter : integer, Code_Currency : integer ) : money
  var RestAcc = $0;
  RestAcc = RestA( Account, DateRest, null, Chapter, Code_Currency, NATCUR );
  if( RestAcc < $0 )
    RestAcc = -RestAcc;
  end;
  return RestAcc;
end;

/*
 *  Получить остаток по модулю на счете за дату
 */
macro GetAbsAccRestRate( Account : string, DateRest : date, DateRate : date, Chapter : integer, Code_Currency : integer ) : money
  var RestAcc = $0;
  if(Code_Currency != NATCUR)
    RestAcc = RestAC( Account, Code_Currency, DateRest, null, Chapter );
    if(ConvSum(RestAcc, 
               RestAcc, 
               DateRate, 
               Code_Currency, 
               NATCUR))
      RunError("Ошибка при конвертации суммы");
    end;
  else
    RestAcc = RestA( Account, DateRest, null, Chapter );
  end;
  if( RestAcc < $0 )
    RestAcc = -RestAcc;
  end;
  return RestAcc;
end;

/*
 * Получить категорию УОНХ
 */
macro GetAccCategoryUONH( accRec, DateReserve : date ) : integer
  var CategoryUONH : integer;
  var CategoryUONHCode : string;
  CategoryUONHCode = GetAccountCategory( accRec, AccCategory_CategoryUONH, DateReserve );
  if( CategoryUONHCode != null )
    CategoryUONH = int(CategoryUONHCode);
  end;
  return CategoryUONH;
end;

private macro _GetCategoryContragent(Balance, Chapter) : integer
  var CategoryContragent : integer;

  var query = "";
  var rs;
  var params : TArray;

  query = query + "SELECT t_Contragent ";
  query = query + "  FROM dprcblncat_dbt ";
  query = query + " WHERE t_Balance = :Balance ";
  query = query + "   AND t_Chapter = :Chapter ";

  params = makeArray(SQLParam("Balance", Balance)
                    ,SQLParam("Chapter", Chapter)
                    );

  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    CategoryContragent = int(rs.value(0));
  end;
  
  return CategoryContragent;
end;


macro GetCategoryContragent(Balance, Chapter) : integer
  var CategoryContragent : integer;
  if((CategoryContragent == null) and (strlen(Balance) >= 5))
    CategoryContragent = _GetCategoryContragent(substr(Balance, 1, 5), Chapter);
  end;
  if((CategoryContragent == null) and (strlen(Balance) >= 3))
    CategoryContragent = _GetCategoryContragent(substr(Balance, 1, 3), Chapter);
  end;
  return CategoryContragent;
end;

/*
 * Получить группу риска для счета
 */
macro GetAccGroupRisk( accRec, DateReserve : date ) : integer
  
  var GroupRisk : integer;
  var GroupRiskCode : string;
  
  GroupRiskCode = GetAccountCategory( accRec, AccCategory_GroupRisk, DateReserve );
  if( GroupRiskCode != null )
    GroupRisk = int(GroupRiskCode);
  end;
  return GroupRisk;

end;

/*
 * Получить признак Производить расчет просроченных требований
 */
macro GetAccCalcOverdueSum( accRec, DateReserve : date ) : integer
  
  var CalcOverdueSum : integer;
  var CalcOverdueSumCode : string;

  CalcOverdueSum = CalcOverdueSum_Undef;
  
  CalcOverdueSumCode = GetAccountCategory( accRec, AccCategory_CalcOverdueSum, DateReserve );
  if( CalcOverdueSumCode != null )
    CalcOverdueSum = int(CalcOverdueSumCode);
  end;
  return CalcOverdueSum;

end;

/*
 * Получить признак Производить расчет просроченных требований
 */
macro GetAccBackOffice( accRec, DateReserve : date ) : integer
  
  var BackOffice : integer;
  var BackOfficeCode : string;

  BackOffice = BackOffice_Undef;
  
  BackOfficeCode = GetAccountCategory( accRec, AccCategory_BackOffice, DateReserve );
  if( BackOfficeCode != null )
    BackOffice = int(BackOfficeCode);
  end;
  return BackOffice;

end;

/*Получить вид резерва*/
macro GetAccKindReserve( accRec, DateReserve : date ) : integer
  
  var KindReserve : integer;
  var KindReserveCode : string;
  
  KindReserveCode = GetAccountCategory( accRec, AccCategory_KindReserve, DateReserve );
  if( KindReserveCode != null )
    KindReserve = int(KindReserveCode);
  end;
  return KindReserve;

end;

/* получить вид резерва для счета */
macro GetAccountReserveKind( RsvParm, accRec, DateReserve : date ) : integer
  var ReserveKind = KindReserveNone;

  if( RsvParm.RsvClass and RsvParm.RsvClassLoans )
    if( GetAccKindReserve( accRec, DateReserve ) == 1 )
      ReserveKind = KindReserveLoss;
    else
      ReserveKind = KindReserveLoans;
    end;
  else
    if( RsvParm.RsvClass ) 
      ReserveKind = KindReserveLoss;
    elif( RsvParm.RsvClassLoans ) 
      ReserveKind = KindReserveLoans;
    elif( RsvParm.RsvClassOffshore )
      ReserveKind = KindReserveOffshore;
    end;
  end;

  return ReserveKind;
end;

/*Получить права на ц/б учитываются в надежном депозитарии*/
macro GetAccSolidDeposit( accRec, DateReserve : date ) : integer
  var SolidDeposit : integer;
  var SolidDepositCode : string;
  SolidDeposit = SolidDeposit_Undef;
  SolidDepositCode = GetAccountCategory( accRec, AccCategory_SolidDeposit, DateReserve );
  if( SolidDepositCode != null )
    SolidDeposit = int(SolidDepositCode);
  end;
  return SolidDeposit;
end;

/*
 * Определение наличия просроченных требований на счетах клиента
 */
macro ClientHasAccOverdueSum( Client : integer, DateSum : date, MaskOverdue : string )
  record accRec("account.dbt");
  var query = "";
  var rs;
  var params : TArray;
  var ConvertMask : string;

  ConvertMask = ConvertMaskToSQLFormat( MaskOverdue, "t_Balance" );
  
  if( ConvertMask == "" )
    ConvertMask = "1 = 1";
  end;

  query = query + "SELECT t_Account, t_Chapter, t_Code_Currency ";
  query = query + "  FROM daccount_dbt ";
  query = query + " WHERE t_Client = :Client ";
  query = query + "   AND t_Open_Close = CHR(0) ";
  query = query + "   AND (" + ConvertMask + ") ";
  query = query + "   AND t_Open_Date <= :Open_Date ";
  query = query + "   AND rsb_account.restac(t_Account, t_Code_Currency, :RestDate, t_Chapter, 0) != 0 ";
  query = query + "   AND EXISTS(SELECT 1 ";
  query = query + "                FROM dobjatcor_dbt ";
  query = query + "               WHERE t_ObjectType = 4 ";
  query = query + "                 AND t_GroupID = 4 ";
  query = query + "                 AND t_AttrID = 1 ";
  query = query + "                 AND t_Object = TO_CHAR(t_Chapter, 'FM0x') || TO_CHAR(t_Code_Currency, 'FM0xxxxxx') || t_Account) ";

  params = makeArray(SQLParam("Client", Client)
                    ,SQLParam("Open_Date", DateSum)
                    ,SQLParam("RestDate", DateSum)
                    );

  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    if( GetAccountRecord( accRec, rs.value(1), rs.value(0), rs.value(2)) )
      if( GetAccOverdueSum( accRec, DateSum ) > $0 )
        return true;
      end;
    end;
  end;
  return false;
end;

/*
 * Установить сумму просроченных требований
 */
macro SetAccOverdueSum( accRec, OverdueSum : money, DateSum : date )
  var NoteSum : money;
  var DateFrom : date;
  NoteSum = GetAccOverdueSum(accRec, DateSum);
  if(NoteSum != OverdueSum)
    return SetAccountNote( accRec, AccNoteKind_OverdueSum, OverdueSum, DateSum );
  end;
  return 0;
end;

/*
 * Установить группу риска для счета
 */
macro SetAccGroupRisk( accRec, GroupRisk : integer, DateCategory : date ) : bool
  return SetAccountCategory( accRec, AccCategory_GroupRisk, string(GroupRisk), DateCategory );
end;

/*
 * Получить процент резервирования РВП (РВПС) для счета
 */
macro SetProcentOfReserve( accRec, ReserveProcent : double, DateProcent : date )
  return SetAccountNote( accRec, AccNoteKind_ReserveProcent, ReserveProcent, DateProcent );
end;

/*
 * Получить процент резервирования РОФШ для счета
 */
macro SetProcentOfReserveOffshore( accRec, ReserveProcent : double, DateProcent : date )
  return SetAccountNote( accRec, AccNoteKind_ReserveProcentOffshore, ReserveProcent, DateProcent );
end;

/*
 * Получить процент резервирования РВП для субъекта
 */
macro GetPartyProcentOfReserve( partyRec, DateProcent : date ) : double
  return GetPartyNote( partyRec, PtNoteKind_ReserveProcent, DateProcent );
end;

/*
 * Получить процент резервирования РОФШ для субъекта
 */
macro GetPartyProcentOfReserveOffshore( partyRec, DateProcent : date ) : double
  return GetPartyNote( partyRec, PtNoteKind_ReserveProcentOffshore, DateProcent );
end;

/*
 * Получить размер оценочного резерва для субъекта
 */
macro GetPartyProcentOfReserveEstimated( partyRec, DateProcent : date ) : double
  return GetPartyNote( partyRec, PtNoteKind_ReserveEstimated, DateProcent );
end;



/*
 * Получить группу риска для субъекта
 */
macro GetPtGroupRisk( partyRec, DateReserve : date ) : integer
  
  var GroupRisk : integer;
  var GroupRiskCode : string;
  
  GroupRiskCode = GetPartyCategory( partyRec, PtCategory_GroupRisk, DateReserve );
  if( GroupRiskCode != null )
    GroupRisk = int(GroupRiskCode);
  end;
  return GroupRisk;

end;

/*
 * Получить текущую рыночную стоимость
 */
macro GetAccMarketCost( accRec, DateReserve : date ) : money
  var ObjectID : string;
  var DateFrom = null;
  var MarketCost : money;
  ObjectID = GetAccountObjectID( accRec );
  MarketCost = readNoteForObject( OBJTYPE_ACCOUNT, ObjectID, AccNoteKind_MarketCost, DateReserve, DateFrom);
  if((DateFrom == null) or (DateFrom == Date(0, 0, 0)))
    MarketCost = $0;
  end;
  return MarketCost;
end;

/*
 * Получить остаток на счете обязательств
 */
macro GetAccObligationRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountObligation, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

/*
 * Получить остаток на счете амортизации
 */
macro GetAccAmortizationRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountAmortization, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

macro GetAccProcentRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountProcent_1, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  elif( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountProcent_2, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  elif( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountProcent_3, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

/* Получить остаток на счете положительной переоценки */
macro GetAccPosRevalueRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountPosRevalue, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

/* Получить остаток на счете отрицательной переоценки */
macro GetAccNegRevalueRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountNegRevalue, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

/*
 * Получить остаток на счете ссудной задолженности
 */
macro GetAccLoanOverdraftRest( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountLoanOverdraft, DateReserve) )
    Rest = GetAbsAccRest( accLinked.Account, DateReserve, accLinked.Chapter, accLinked.Code_Currency );
  end;
  return Rest;
end;

/*
 * Получить остаток на счете покрытия парного счета
 */
macro GetAccCurrencyRest( accRec, DateReserve : date ) : money
  record accPair("account.dbt");
  var Rest : money;
  Rest = $0;
  if( GetPairAccount( accPair, accRec.Chapter, accRec.Code_Currency, accRec.Account ))
    Rest = GetAbsAccRest( accPair.Account, DateReserve, accPair.Chapter, accPair.Code_Currency );
  end;
  return Rest;
end;

/* получить текущую рыночную стоимость на одном счете обеспечения */
private macro _GetMarketCostAccountSecurity( accRec, SecurIndex : integer, DateReserve : date ) : money

  record accLinked("account.dbt");
  var EnsuringCategory : string;
  var MarketCost : money;

  MarketCost = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, SecurIndex, DateReserve) )
    
    EnsuringCategory = GetCategoryOfEnsuring( accLinked.Chapter, accLinked.Code_Currency, accLinked.Account, DateReserve );
    if( EnsuringCategory == "1" )
      MarketCost = GetAccMarketCost( accLinked, DateReserve );
    end;

  end;
  
  return MarketCost;

end;

/*
 * Получить текущую рыночную стоимость на счетах обеспечения
 */
macro GetMarketCostAccountSecurity( accRec, DateReserve : date ) : money

  var MarketCost : money;
  var ObjRoleArray : TArray;
  var i : integer, n : integer;

  ObjRoleArray = TArray;
  GetObjRoleSecurAccount( ObjRoleArray );

  MarketCost = $0;
  
  i = 0;
  n = ObjRoleArray.Size();

  while( i < n )
    MarketCost = MarketCost + _GetMarketCostAccountSecurity( accRec, ObjRoleArray[i], DateReserve );

    i = i + 1;
  end;
  
  return MarketCost;

end;

/* получить текущую рыночную стоимость на одном счете обеспечения c учетом категории обеспечения качества */
private macro _GetMarketCostAccountSecurityByCategory( accRec, SecurIndex : integer, DateReserve : date ) : money
  record accLinked("account.dbt");
  var EnsuringCategory : string;
  var PawnCollectionDate : date;
  var Coefficient : double;
  var MarketCost : money;

  MarketCost = $0;
  if( GetAccountLinkedToAccount(accLinked, accRec, SecurIndex, DateReserve) )
    
    EnsuringCategory = GetCategoryOfEnsuring( accLinked.Chapter, accLinked.Code_Currency, accLinked.Account, DateReserve );
    if( EnsuringCategory == null ) EnsuringCategory = ""; end;
      
    PawnCollectionDate = GetPawnCollectionDate( accLinked.Chapter, accLinked.Code_Currency, accLinked.Account );
    if( PawnCollectionDate == null ) PawnCollectionDate = DateReserve; end;

    Coefficient = GetCoefficientOfEnsuring( EnsuringCategory, PawnCollectionDate, DateReserve );

    if( Coefficient )
      MarketCost = Coefficient * GetAccMarketCost(accLinked, DateReserve);
    end;

  end;
  
  return MarketCost;

end;

/*
 * Получить текущую рыночную стоимость на счетах обеспечения c учетом категории обеспечения качества
 */
macro GetMarketCostSecurityByCategory( accRec, DateReserve : date ) : money

  var MarketCost : money;
  var ObjRoleArray : TArray;
  var i : integer, n : integer;

  ObjRoleArray = TArray;
  GetObjRoleSecurAccount( ObjRoleArray );

  MarketCost = $0;
  
  i = 0;
  n = ObjRoleArray.Size();

  while( i < n )
    
    MarketCost = MarketCost + _GetMarketCostAccountSecurityByCategory( accRec, ObjRoleArray[i], DateReserve );

    i = i + 1;
  end;
  
  return MarketCost;
end;

/*
 * Получить текущую рыночную стоимость на счетах обеспечения c учетом категории обеспечения качества для связанного счета ссудной задолженности
 */
macro GetMarketCostSecurityByCategoryLoanOverdraft( accRec, DateReserve : date ) : money
  record accLinked("account.dbt");
  var MarketCost : money;
  var ObjRoleArray : TArray;
  var i : integer, n : integer;

  ObjRoleArray = TArray;
  GetObjRoleSecurAccount( ObjRoleArray );

  MarketCost = $0;
  
  i = 0;
  n = ObjRoleArray.Size();

  if( GetAccountLinkedToAccount(accLinked, accRec, AccObjRole_AccountLoanOverdraft, DateReserve) )
    while( i < n )

      MarketCost = MarketCost + _GetMarketCostAccountSecurityByCategory( accLinked, ObjRoleArray[i], DateReserve );

      i = i + 1;
    end;
  end;

  return MarketCost;
end;

/* Получить счета просрочки */
macro GetExpirationAccounts()

  var Accounts = "";

  GetRegistryValue( "CB\\РЕЗЕРВЫ\\СЧЕТА ПРОСРОЧКИ", V_STRING, Accounts, null );

  return Accounts;

end;

/*
 * Класс - элемент массива счетов портфеля
 */
class CaseAccount

  var Chapter      ;
  var Code_Currency;
  var Account      ;
  var ReserveType  ;

  var DateInclude;

end;

/*
 * Заполнить массив счетов портфеля
 */
macro CreateCaseAccountArray( accasscs, accArray : TArray, DateReserve : date, ReserveType : integer )

  var stat;
  file caseit("accaseit.dbt") key 0;
  var i = 0;

  ClearRecord( caseit );
  caseit.CaseID = accasscs.CaseSubID;

  stat = getGE( caseit );

  while( (stat) and (caseit.CaseID == accasscs.CaseSubID) )

    if((caseit.ReserveType == ReserveType) and (caseit.DateFrom <= DateReserve) and ((caseit.DateTo == Date(0,0,0)) or (caseit.DateTo >= DateReserve)) )

      i = accArray.Size();      

      accArray(i) = CaseAccount;

      accArray(i).Chapter       = caseit.AccountChapter;
      accArray(i).Code_Currency = caseit.AccountFI;
      accArray(i).Account       = caseit.Account;
      accArray(i).ReserveType   = caseit.ReserveType;
      accArray(i).DateInclude   = caseit.DateFrom;

    end;

    stat = next( caseit );

  end;

end;

macro AccCalcOverdueSum( accRec, DateReserve : date ) : money
  var СуммаПросроченныхТребований : money;
  var BackOffice = GetAccBackOffice( accRec, DateReserve );
  var OverdueTerm : integer;
  var Sum : money;
  var AccMask : string;

  Sum = $0;
  СуммаПросроченныхТребований = $0;

  GetRegistryValue( "CB\\РЕЗЕРВЫ\\СРОК ПРОСРОЧКИ", V_INTEGER, OverdueTerm, null );

  AccMask = "";
  GetRegistryValue( "CB\\РЕЗЕРВЫ\\СЧЕТ ПОЛУЧАТЕЛЯ В ТРЕБОВАНИЯХ", V_STRING, AccMask, null );

  if( BackOffice == BackOffice_Undef )
    if( not GetOverdueClaimSum( accRec.Code_Currency, accRec.Chapter, accRec.Account, DateReserve, OverdueTerm, AccMask, @Sum ))
      СуммаПросроченныхТребований = СуммаПросроченныхТребований + Sum;
    end;

    Sum = getExpireAmountPZO( accRec.Account, accRec.Code_Currency, accRec.Chapter, DateReserve );

    СуммаПросроченныхТребований = СуммаПросроченныхТребований + Sum;

  elif( BackOffice == BackOffice_Retail )

/*    if( GetOverDueSumOnIndebt( accRec.Account, accRec.Chapter, accRec.Code_Currency, DateReserve, OverdueTerm, Sum ))*/
    if( not ExecMacroFile("overdue.mac", "GetOverDueSumOnIndebt", accRec.Account, accRec.Chapter, accRec.Code_Currency, DateReserve, OverdueTerm, Sum ))
      СуммаПросроченныхТребований = СуммаПросроченныхТребований + Sum;
    end;

  end;

  return СуммаПросроченныхТребований;
end;

/*
 * Получить дату курса для счета
 */
macro GetDateRateForAccount(accRec, DateReserve : date, RestReserve : @money) : date
  var mon0, year0; DateSplit(DateReserve, null, mon0, year0);
  var NextWorkDay = GetDateAfterWorkDays(DateReserve, 1);
  var mon1, year1; DateSplit(NextWorkDay, null, mon1, year1);
  var AccountReserve : string;
  var PrevDateReserve : date;
  RestReserve = $0;
  /*
  если это рублевый счет, то дата курса равна дате резервирования
  */
  if(accRec.Code_Currency == NATCUR)
    return DateReserve;
  end;
  /*
  если следующий за датой резерва рабочий день в следующем месяце, 
  то дата курса и есть последний день текущего месяца
  */
  if((mon0 != mon1) or (year0 != year1))
    return DateReserve;
  end;
  /*
  ищем счет резерва и день предшествующей проводки по этому счету
  */
  AccountReserve = CB_GetAccountReserve(accRec);
  if(Trim(AccountReserve) != "")
    RestReserve = GetAbsAccRest(AccountReserve, DateReserve, 1, NATCUR);
    PrevDateReserve = GetLastRestDate(AccountReserve, DateReserve, 1, NATCUR);
    if(PrevDateReserve != Date(0, 0, 0))
      return PrevDateReserve;
    end;
  end;
  return DateReserve;
end;

/*
 * Получить дату курса для субпортфеля
 */
macro GetDateRateForCase(accaseRec, accasepmRec, accasscsRec, DateReserve : date, RestReserve : @money) : date
  var mon0, year0; DateSplit(DateReserve, null, mon0, year0);
  var NextWorkDay = GetDateAfterWorkDays(DateReserve, 1);
  var mon1, year1; DateSplit(NextWorkDay, null, mon1, year1);
  var AccountReserve : string;
  var PrevDateReserve : date;
  RestReserve = $0;
  /*
  если в субпортфеле только рублевые счета, то дата курса равна дате резервирования
  */
  var query, rs;
  var params : TArray;
  query =         "SELECT COUNT(*) ";
  query = query + "  FROM daccaseit_dbt ";
  query = query + " WHERE t_AccountChapter = :Chapter ";
  query = query + "   AND t_AccountFI != :FI ";
  query = query + "   AND t_CaseID = :CaseSubID ";
  params = makeArray(SQLParam("Chapter", accasepmRec.AccountChapter),
                     SQLParam("FI", NATCUR),
                     SQLParam("CaseSubID", accasscsRec.CaseSubID));
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    if(rs.value(0) == 0)
      return DateReserve;
    end;
  end;
  /*
  если следующий за датой резерва рабочий день в следующем месяце, 
  то дата курса и есть последний день текущего месяца
  */
  if((mon0 != mon1) or (year0 != year1))
    return DateReserve;
  end;
  /*
  ищем счет резерва и день предшествующей проводки по этому счету
  */
  AccountReserve = GetAccountReserveForCase(accaseRec, accasscsRec, DateReserve, RestReserve);
  if(Trim(AccountReserve) != "")
    RestReserve = GetAbsAccRest(AccountReserve, DateReserve, 1, NATCUR);
    PrevDateReserve = GetLastRestDate(AccountReserve, DateReserve, 1, NATCUR);
    if(PrevDateReserve != Date(0, 0, 0))
      return PrevDateReserve;
    end;
  end;
  return DateReserve;
end;

/*Получить дату предыдущего расчета резерва*/
private macro _GetLastDateCalcReserve(Symbol, DocKind, DocumentID)
  var LastDateCalcReserve : date;
  var query, rs;
  var params : TArray;
  query =         "SELECT   aos.t_date, os.t_fact_date ";
  query = query + "    FROM doprstep_dbt os, daccopsrv_dbt aos ";
  query = query + "   WHERE os.t_isexecute = 'X' ";
  query = query + "     AND os.t_servdockind = :ServDocKind ";
  query = query + "     AND os.t_blockid IN ( ";
  query = query + "            SELECT t_blockid ";
  query = query + "              FROM doproblck_dbt ";
  query = query + "             WHERE t_symbol = :Symbol ";
  query = query + "               AND t_notinuse = CHR (0) ";
  query = query + "               AND t_kind_operation IN ( ";
  query = query + "                      SELECT t_kind_operation ";
  query = query + "                        FROM doproper_dbt ";
  query = query + "                       WHERE t_dockind = :DocKind ";
  query = query + "                         AND t_documentid = :DocumentID))";
  query = query + "     AND os.t_id_operation IN ( ";
  query = query + "                       SELECT t_id_operation ";
  query = query + "                         FROM doproper_dbt ";
  query = query + "                        WHERE t_dockind = :DocKind ";
  query = query + "                              AND t_documentid = :DocumentID) ";
  query = query + "     AND os.t_servdocid = aos.t_srvdocid ";
  query = query + "ORDER BY os.t_countnum DESC ";
  params = makeArray(SQLParam("ServDocKind", 46)
                    ,SQLParam("Symbol", Symbol)
                    ,SQLParam("DocKind", DocKind)
                    ,SQLParam("DocumentID", DocumentID)
                    );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    LastDateCalcReserve = rs.value(0);
  end;                         
  return LastDateCalcReserve;
end;

macro GetAccLastDateCalcReserveLoss(AccountID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveLoss, DLDOC_ACCOUNT, AccountID);
end;

macro GetAccLastDateCalcReserveLoans(AccountID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveLoans, DLDOC_ACCOUNT, AccountID);
end;

macro GetAccLastDateCalcReserveOffshore(AccountID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveOffshore, DLDOC_ACCOUNT, AccountID);
end;

macro GetAccLastDateCalcReserveEstimated(AccountID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveEstimated, DLDOC_ACCOUNT, AccountID);
end;

macro GetCaseLastDateCalcReserveLoss(CaseSubID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveLoss, DOCKIND_CASE, CaseSubID);
end;

macro GetCaseLastDateCalcReserveLoans(CaseSubID)
  return _GetLastDateCalcReserve(AccOpServMaintananceSymbReserveLoans, DOCKIND_CASE, CaseSubID);
end;

/* получить классификация */
private macro _GetElementByRsvClass( ListClassif : integer, Classif : integer ) : string
  record llvalues( "llvalues.dbt" );
  ClearRecord( llvalues );
  if( Classif )
    LL_FindLLVALUES( ListClassif, Classif, llvalues );
    return llvalues.Code;
  end;
  return null;
end;

/* получить классификацию счета */
macro GetAccountReserveClassif( ReserveKind : integer, ReserveType : integer, AccountReserveKind : integer ) : string
  var ReserveClassif : string;

    if((AccountReserveKind == ReserveKind) and (ReserveKind == KindReserveLoss )) ReserveClassif = _GetElementByRsvClass( 1030, ReserveType );
  elif((AccountReserveKind == ReserveKind) and (ReserveKind == KindReserveLoans)) ReserveClassif = _GetElementByRsvClass( 1033, ReserveType );
  elif(ReserveKind == KindReserveOffshore)                                        ReserveClassif = _GetElementByRsvClass( 1031, ReserveType );
  end;

  return ReserveClassif;
end;

/* получить классификацию портфеля */
macro GetCaseReserveClassif( ReserveKind : integer, ReserveType : integer, accase ) : string
  var ReserveClassif : string;

    if((accase.ReserveKind == ReserveKind) and (ReserveKind == KindReserveLoss )) ReserveClassif = _GetElementByRsvClass( 1030, ReserveType );
  elif((accase.ReserveKind == ReserveKind) and (ReserveKind == KindReserveLoans)) ReserveClassif = _GetElementByRsvClass( 1033, ReserveType );
  end;

  return ReserveClassif;
end;

private macro _GetAccRestSumForAccSecurityRole
(
  Account : string
 ,Chapter : integer
 ,Code_Currency : integer
 ,DateSum : date
 ,GroupID : integer
) : money

  var query = "";
  var rs;
  var params : TArray;
  var AccRestSum : money;
  var RestAcc : money;
  record accRec("account.dbt");

  AccRestSum = $0;

  query = query + "SELECT ";
  query = query + "  TO_NUMBER(SUBSTR(t_ObjectID, 1, 2), 'FM0x') /*Chapter*/ ";
  query = query + " ,TO_NUMBER(SUBSTR(t_ObjectID, 3, 7), 'FM0xxxxxx') /*Code_Currency*/ ";
  query = query + " ,TRIM(SUBSTR(t_ObjectID, 10)) /*Account*/ ";
  query = query + "  FROM dobjlink_dbt ";
  query = query + " WHERE t_ObjectType = :ObjectType ";
  query = query + "   AND t_AttrType = :AttrType ";
  query = query + "   AND t_AttrID = TO_CHAR(:Chapter, 'FM0x') || TO_CHAR(:Code_Currency, 'FM0xxxxxx') || :Account ";
  query = query + "   AND :DateSum BETWEEN t_ValidFromDate AND t_ValidToDate ";
  query = query + "   AND t_GroupID = :GroupID ";

  params = makeArray(SQLParam("ObjectType", OBJTYPE_ACCOUNT)
                    ,SQLParam("AttrType", OBJTYPE_ACCOUNT)
                    ,SQLParam("Chapter", Chapter)
                    ,SQLParam("Code_Currency", Code_Currency)
                    ,SQLParam("Account", Account)
                    ,SQLParam("DateSum", DateSum)
                    ,SQLParam("GroupID", GroupID)
                    );

  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    RestAcc = $0;
      
    if(int(rs.value(1)) != NATCUR)
      RestAcc = RestAC(rs.value(2), int(rs.value(1)), DateSum, null, int(rs.value(0)));
      if(ConvSum(RestAcc, RestAcc, DateSum, rs.value(1), NATCUR))
        RunError("Ошибка при конвертации суммы");
      end;
    else
      RestAcc = RestA(rs.value(2), DateSum, null, int(rs.value(0)));
    end;
      
    AccRestSum = AccRestSum + RestAcc;  
  end;

  return AccRestSum;
end;

/*
 * Определить сумму остатков по счетам, для которого заданный счет является счетом обеспечения
 */
macro GetAccRestSumForAccSecurity
(
  Account : string
 ,Chapter : integer
 ,Code_Currency : integer
 ,DateSum : date
) : money

  var i : integer;
  var ObjRoleArray : TArray;
  var AccRestSum : money;

  AccRestSum = $0;
  ObjRoleArray = TArray;
  GetObjRoleSecurAccount(ObjRoleArray);

  i = 0;
  while(i < ObjRoleArray.Size())
    AccRestSum = AccRestSum + _GetAccRestSumForAccSecurityRole(Account, Chapter, Code_Currency, DateSum, ObjRoleArray[i]);
    i = i + 1;
  end;
  
  return AccRestSum;
end;

macro GetIncomeOutlayCatCodeLoss(ClassifReserveLoss : string, IncomeCatCodePrm : @string, OutlayCatCodePrm : @string, ReserveDate : date)
  if(ReserveDate >= DateBegin446P)
    IncomeCatCodePrm = "";
    OutlayCatCodePrm = "";
    
    if(   (SubStr(ClassifReserveLoss, 1, 9) == "2.2 (301)")
       or (SubStr(ClassifReserveLoss, 1, 9) == "2.2 (302)")
       or (SubStr(ClassifReserveLoss, 1, 9) == "2.2 (304)")
      )
      IncomeCatCodePrm = IncomeCatCodeDSAccPrc;
      OutlayCatCodePrm = OutlayCatCodeDSAccPrc;
    elif(   (SubStr(ClassifReserveLoss, 1, 9) == "2.3 (474)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.4 (603)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.4 (604)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.5 (602)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (479)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (604)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (610)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (619)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (620)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.7 (621)")
         or (SubStr(ClassifReserveLoss, 1, 9) == "2.9 (202)")
        )
      IncomeCatCodePrm = IncomeCatCodeDS;
      OutlayCatCodePrm = OutlayCatCodeDS;
    elif(   ((SubStr(ClassifReserveLoss, 1, 5) == "3.1.1") and (SubStr(ClassifReserveLoss, 1, 13) != "3.1.1 (91318)"))
         or (SubStr(ClassifReserveLoss, 1, 11) == "3.1.1 (913)")
         or (SubStr(ClassifReserveLoss, 1, 11) == "3.1.1 (909)")
        )
      IncomeCatCodePrm = IncomeCatCodeRUOKHOR;
      OutlayCatCodePrm = OutlayCatCodeRUOKHOR;
    elif(   (SubStr(ClassifReserveLoss, 1, 13) == "3.1.1 (91318)")
        )
      IncomeCatCodePrm = IncomeCatCodeRUONH;
      OutlayCatCodePrm = OutlayCatCodeRUONH;
    elif(   (SubStr(ClassifReserveLoss, 1, 11) == "5.1.1 (203)")
        )
      IncomeCatCodePrm = IncomeCatCodeDSPrc;
      OutlayCatCodePrm = OutlayCatCodeDSPrc;
    elif(   ((SubStr(ClassifReserveLoss, 1, 5) == "5.1.1") and (SubStr(ClassifReserveLoss, 1, 11) != "5.1.1 (203)"))
        )
      IncomeCatCodePrm = IncomeCatCodeCrdPrc;
      OutlayCatCodePrm = OutlayCatCodeCrdPrc;
    end;
  else
    IncomeCatCodePrm = IncomeCatCode;
    OutlayCatCodePrm = OutlayCatCode;

    if(SubStr(ClassifReserveLoss, 1, 13) == "3.1.1 (91318)")
      IncomeCatCodePrm = IncomeCatCodeUONH;
      OutlayCatCodePrm = OutlayCatCodeUONH;
    end;
  end;
end;

macro GetIncomeOutlayCatCodeLoans(ClassifReserveLoans : string, IncomeCatCodePrm : @string, OutlayCatCodePrm : @string, ReserveDate : date)
  if(ReserveDate >= DateBegin446P)
    IncomeCatCodePrm = "";
    OutlayCatCodePrm = "";
    
    if(   (SubStr(ClassifReserveLoans, 1, 7) == "1 (403)")
       or (SubStr(ClassifReserveLoans, 1, 5) == "1 (44")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (450)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (451)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (452)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (453)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (454)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (455)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (456)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (457)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "1 (458)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "3 (474)")
       or (SubStr(ClassifReserveLoans, 1, 7) == "3 (603)")
      )
      IncomeCatCodePrm = IncomeCatCodeCrdPrc;
      OutlayCatCodePrm = OutlayCatCodeCrdPrc;
    elif(   (SubStr(ClassifReserveLoans, 1, 7) == "1 (203)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (322)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (323)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (460)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (461)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (462)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (463)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (464)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (465)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (466)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (467)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (468)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (469)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (470)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (471)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (472)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (473)")
        )
      IncomeCatCodePrm = IncomeCatCodeDSPrc;
      OutlayCatCodePrm = OutlayCatCodeDSPrc;
    elif(   (SubStr(ClassifReserveLoans, 1, 7) == "4 (478)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "5 (478)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "6 (478)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "7 (474)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "8 (474)")
        )
      IncomeCatCodePrm = IncomeCatCodeDS;
      OutlayCatCodePrm = OutlayCatCodeDS;
    elif(   (SubStr(ClassifReserveLoans, 1, 7) == "1 (320)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (321)")
         or (SubStr(ClassifReserveLoans, 1, 7) == "1 (324)")
        )
      IncomeCatCodePrm = IncomeCatCodeDPPrc;
      OutlayCatCodePrm = OutlayCatCodeDPPrc;
    elif(   (SubStr(ClassifReserveLoans, 1, 8) == "10 (477)")
        )
      IncomeCatCodePrm = IncomeCatCodeLease;
      OutlayCatCodePrm = OutlayCatCodeLease;
    end;
  else
    IncomeCatCodePrm = IncomeCatCode;
    OutlayCatCodePrm = OutlayCatCode;
  end;
end;

macro GetIncomeOutlayCatCodeOffshore(ClassifReserveOffshore : string, IncomeCatCodePrm : @string, OutlayCatCodePrm : @string, ReserveDate : date)
  if(ReserveDate >= DateBegin446P)
    IncomeCatCodePrm = "";
    OutlayCatCodePrm = "";
    
    if(   (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (301)")
       or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (302)")
      )
      IncomeCatCodePrm = IncomeCatCodeDSAccPrc;
      OutlayCatCodePrm = OutlayCatCodeDSAccPrc;
    elif(   (SubStr(ClassifReserveOffshore, 1, 11) == "2.1 (47417)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (603)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (602)")
         or (SubStr(ClassifReserveOffshore, 1, 7) == "2 (474)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (604)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (478)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (479)")
        )
      IncomeCatCodePrm = IncomeCatCodeDS;
      OutlayCatCodePrm = OutlayCatCodeDS;
    elif(   (SubStr(ClassifReserveOffshore, 1, 11) == "2.1 (91315)")
         or (SubStr(ClassifReserveOffshore, 1, 11) == "2.1 (91414)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (909)")
        )
      IncomeCatCodePrm = IncomeCatCodeRUOKHOR;
      OutlayCatCodePrm = OutlayCatCodeRUOKHOR;
    elif(   (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (325)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (474)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (459)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (403)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (913)")
         or (SubStr(ClassifReserveOffshore, 1, 7) == "2.3 (32")
         or (SubStr(ClassifReserveOffshore, 1, 7) == "2.3 (45")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (603)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (474)")
        )
      IncomeCatCodePrm = IncomeCatCodeCrdPrc;
      OutlayCatCodePrm = OutlayCatCodeCrdPrc;
    elif(   (SubStr(ClassifReserveOffshore, 1, 9) == "2.1 (203)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (203)")
         or (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (473)")
        )
      IncomeCatCodePrm = IncomeCatCodeDSPrc;
      OutlayCatCodePrm = OutlayCatCodeDSPrc;
    elif(   (SubStr(ClassifReserveOffshore, 1, 9) == "2.3 (477)")
        )
      IncomeCatCodePrm = IncomeCatCodeLease;
      OutlayCatCodePrm = OutlayCatCodeLease;
    end;
  else
    IncomeCatCodePrm = IncomeCatCode;
    OutlayCatCodePrm = OutlayCatCode;
  end;
end;

macro GetIncomeOutlayCatCodeEstimated(ClassifReserve : string, IncomeCatCodePrm : @string, OutlayCatCodePrm : @string, ReserveDate : date)
  IncomeCatCodePrm = "";
  OutlayCatCodePrm = "";

  if(   (SubStr(ClassifReserve, 1, 9) == "2.2 (301)")
     or (SubStr(ClassifReserve, 1, 9) == "2.2 (302)")
     or (SubStr(ClassifReserve, 1, 9) == "2.2 (304)")
    )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDSAccPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDSAccPrc;
  elif(   (SubStr(ClassifReserve, 1, 9) == "2.3 (474)")
       or (SubStr(ClassifReserve, 1, 9) == "2.4 (603)")
       or (SubStr(ClassifReserve, 1, 9) == "2.5 (602)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDS;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDS;
  elif(   ((SubStr(ClassifReserve, 1, 5) == "3.1.1") and (SubStr(ClassifReserve, 1, 13) != "3.1.1 (91318)"))
       or (SubStr(ClassifReserve, 1, 11) == "3.1.1 (913)")
       or (SubStr(ClassifReserve, 1, 11) == "3.1.1 (909)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedRUOKHOR;
    OutlayCatCodePrm = OutlayCatCodeEstimatedRUOKHOR;
  elif(   ((SubStr(ClassifReserve, 1, 5) == "5.1.1") and (SubStr(ClassifReserve, 1, 11) != "5.1.1 (203)"))
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedCrdPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedCrdPrc;
  elif(   (SubStr(ClassifReserve, 1, 7) == "1 (403)")
       or (SubStr(ClassifReserve, 1, 5) == "1 (44")
       or (SubStr(ClassifReserve, 1, 7) == "1 (450)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (451)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (452)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (453)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (454)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (455)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (456)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (457)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (458)")
       or (SubStr(ClassifReserve, 1, 7) == "3 (474)")
       or (SubStr(ClassifReserve, 1, 7) == "3 (603)")
      )
      IncomeCatCodePrm = IncomeCatCodeEstimatedCrdPrc;
      OutlayCatCodePrm = OutlayCatCodeEstimatedCrdPrc;
  elif(   (SubStr(ClassifReserve, 1, 7) == "1 (322)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (323)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (460)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (461)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (462)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (463)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (464)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (465)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (466)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (467)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (468)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (469)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (470)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (471)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (472)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (473)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDSPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDSPrc;
  elif(   (SubStr(ClassifReserve, 1, 7) == "4 (478)")
       or (SubStr(ClassifReserve, 1, 7) == "5 (478)")
       or (SubStr(ClassifReserve, 1, 7) == "6 (478)")
       or (SubStr(ClassifReserve, 1, 7) == "7 (474)")
       or (SubStr(ClassifReserve, 1, 7) == "8 (474)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDS;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDS;
  elif(   (SubStr(ClassifReserve, 1, 7) == "1 (320)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (321)")
       or (SubStr(ClassifReserve, 1, 7) == "1 (324)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDPPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDPPrc;
  elif(   (SubStr(ClassifReserve, 1, 8) == "10 (477)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedLease;
    OutlayCatCodePrm = OutlayCatCodeEstimatedLease;
  elif(   (SubStr(ClassifReserve, 1, 9) == "2.3 (301)")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (302)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDSAccPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDSAccPrc;
  elif(   (SubStr(ClassifReserve, 1, 11) == "2.1 (47417)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (603)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (602)")
       or (SubStr(ClassifReserve, 1, 7) == "2 (474)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (604)")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (478)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDS;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDS;
  elif(   (SubStr(ClassifReserve, 1, 11) == "2.1 (91315)")
       or (SubStr(ClassifReserve, 1, 11) == "2.1 (91414)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (909)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedRUOKHOR;
    OutlayCatCodePrm = OutlayCatCodeEstimatedRUOKHOR;
  elif(   (SubStr(ClassifReserve, 1, 9) == "2.1 (325)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (474)")
       or (SubStr(ClassifReserve, 1, 9) == "2.1 (459)")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (403)")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (913)")
       or (SubStr(ClassifReserve, 1, 7) == "2.3 (32")
       or (SubStr(ClassifReserve, 1, 7) == "2.3 (45")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (603)")
       or (SubStr(ClassifReserve, 1, 9) == "2.3 (474)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedCrdPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedCrdPrc;
  elif(   (SubStr(ClassifReserve, 1, 9) == "2.3 (473)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedDSPrc;
    OutlayCatCodePrm = OutlayCatCodeEstimatedDSPrc;
  elif(   (SubStr(ClassifReserve, 1, 9) == "2.3 (477)")
      )
    IncomeCatCodePrm = IncomeCatCodeEstimatedLease;
    OutlayCatCodePrm = OutlayCatCodeEstimatedLease;
  end;
end;