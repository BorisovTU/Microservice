 /*
 $Name:        pmchangeprop.mac
 $Module: 
 $Description: Общая функция изменения реквизитов из скроллинга
 */

import globals, CTInter, OprInter, PaymInter, BankInter, likepy;

private const ESC_ITEM = -2;        // Нажал Esc
private const PRECEDENCE_ITEM = 0;  // Приоритет
private const PAYMENTKIND_ITEM = 1; // Вид платежа

private class ChangesPaymentProperties

  var Precedence:string = "";
  var PaymentKind:string = "";
  var ItemKind:integer = ESC_ITEM;
  var NameProperty:string = "";
  
end;

                                                  
private var g_changesPaymentProperties:ChangesPaymentProperties = null;


macro ChoicePaymentPropertiesFromScrol()
  
  g_changesPaymentProperties = ChangesPaymentProperties();
  
  array MenuItems;
  MenuItems[PRECEDENCE_ITEM] = "Приоритет платежа                  ";  //Пробелы поставил, чтобы заголовок был полностью виден
  MenuItems[PAYMENTKIND_ITEM] = "Вид платежа";
  
  g_changesPaymentProperties.ItemKind = Menu(MenuItems, "Enter Выбор  Esc Отмена", "Какой реквизит необходимо изменить?", -1, -1);

  if( g_changesPaymentProperties.ItemKind == PRECEDENCE_ITEM )
    
    if( not GetString(g_changesPaymentProperties.Precedence, "Приоритета платежа:", 2, false, 0, "Введите приоритета платежа", "Enter Ввод  Esc Отмена" ) )
      msgbox("Пользователь прервал выполнение операции");
      return 1;
    end;

    if( strlen(g_changesPaymentProperties.Precedence) != 2 )
      msgbox("Приоритет платежа должен содержать две цифры");
      return 1;
    end;

    g_changesPaymentProperties.NameProperty = "Приоритет платежа";

  elif( g_changesPaymentProperties.ItemKind == PAYMENTKIND_ITEM )

    g_changesPaymentProperties.PaymentKind = PM_ListPMPOPKND();
    if( g_changesPaymentProperties.PaymentKind == "" )
      msgbox("Пользователь прервал выполнение операции");
      return 1;
    end;

    g_changesPaymentProperties.NameProperty = "Вид платежа";

  elif( g_changesPaymentProperties.ItemKind == ESC_ITEM )
    msgbox("Пользователь прервал выполнение операции");
    return 1;
  end;

  return 0;
end;

macro ChangePaymentPropertiesFromScrol( PaymentID:integer )

  var Payment = RsbPayment( PaymentID );

  if( g_changesPaymentProperties )
    if( InList( Payment.DocKind, DLDOC_BANKPAYMENT, DLDOC_BANKCLAIM, PS_PAYORDER, PS_INRQ, RM_PMOUTDOC, DLDOC_MULTYPM, DOC_BO_PAYMENT ) )

      if( g_changesPaymentProperties.ItemKind == PRECEDENCE_ITEM )
        Payment.Precedence = g_changesPaymentProperties.Precedence;
      elif( g_changesPaymentProperties.ItemKind == PAYMENTKIND_ITEM )
        Payment.PaymentKind = g_changesPaymentProperties.PaymentKind;
      end;

    else
      msgbox("Согласно заданным в функции УстановитьРеквизиты условиям|для документа " + Payment.Number + " от " + Payment.Date + 
             " установка нового значения|реквизита \"" + g_changesPaymentProperties.NameProperty + "\" невозможна");
      return 1;
    end;
  else
    msgbox("Не определены данные для установки реквизитов");
    return 1;
  end;

  return 0;
end;
