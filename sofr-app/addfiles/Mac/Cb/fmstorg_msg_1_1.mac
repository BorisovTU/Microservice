/*
 $Name: fmstorg_msg_1_1.mac
 $Module: Ядро ГКБО 
 $Description: Сообщения в ФСФМ по стратегическим организациям
 */
 /*
	Сообщения в ФСФМ по стратегическим организациям
 */
import BankInter, PTInter, CTInter, rsd, oralib, likepy, globals;
import FMInter, "fmreqi_lib.mac", "FmXmlWriter.mac";

class SidStruct
	var First;
	var Second;
end;

private record fmstorgmsg(fmstorgmsg);
private record fmstorgakkred(fmstorgakkred);
private file MessageXMLFile() txt write;
private var InfoSIDMap = TArray;

private macro FormatDateToName(indate)
	var cmd = RsdCommand("SELECT TO_CHAR(?,'YYYYMMDD') FROM dual");
	cmd.AddParam ("", RSDBP_IN, indate);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(0);
end;

private macro ExtractYear(indate)
	var cmd = RsdCommand("SELECT TO_CHAR(EXTRACT(YEAR FROM ?)) FROM dual");
	cmd.AddParam ("", RSDBP_IN, indate);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(0);
end;

private macro FormatDate(indate)
	var cmd = RsdCommand("SELECT TO_CHAR(?,'DD.MM.YYYY') FROM dual");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, indate);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(0);
end;

private macro GetNNNComponent(msg)
	var cmd = RsdCommand("SELECT TO_NUMBER (SUBSTR(T_MSGFILENAME, 30, 3)) AS NNN, T_MSGFILENAME " + 
				"FROM dfmstorgmsg_dbt WHERE T_SENDBANKBIC = ? AND T_EMSGDATE = ? AND " +
				"(LENGTH (T_MSGFILENAME) > 0 AND T_MSGFILENAME IS NOT NULL AND T_MSGFILENAME <> CHR (1)) " + 
				"ORDER BY NNN DESC ");
	cmd.NullConversion = true;
				
	cmd.AddParam ("", RSDBP_IN, msg.SENDBANKBIC);
	cmd.AddParam ("", RSDBP_IN, msg.EMsgDate);
	var rs = RsdRecordset(cmd);
	
	var nnn = 1;
	if (rs.MoveNext())
		if (rs.value(0) != NULLVAL)
			nnn = Int(rs.value(0)) + 1;
		end;
	end;
	
	return nnn;
end;

// Определение текущего порядкового номера для идентификатора сообщения
private macro ComputeMsgNumber(fmstorgmsg)
	var n1 = 0, n2 = 0;
	
	var cmd = RsdCommand("SELECT to_number(substr (AK.t_InfoSID,16,8)) As MMM FROM " + 
		"DFMSTORGMSG_DBT msg, DFMSTORG_DBT st, DFMSTORGAKKRED_DBT ak " +
		"WHERE EXTRACT(YEAR FROM MSG.T_EMSGDATE) = EXTRACT(YEAR FROM ?) " +
		"AND MSG.T_SENDBANKBIC = ? AND MSG.T_ID = ST.T_MSGID  " +
		"AND AK.T_STORGID = st.T_ID AND ROWNUM = 1 ORDER BY MMM DESC");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.EMsgDate);	
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.SENDBANKBIC);
	
	var rs = RsdRecordset(cmd);
	if (rs.MoveNext())
		if (rs.value(0) != NULLVAL)
			n1 = Int(rs.value(0));
		end;
	end;
	
	var cmd2 = RsdCommand("SELECT to_number(substr (AK.t_InfoSID,16,8)) As MMM FROM " + 
		"DFMSTORGMSG_DBT msg, DFMSTORG_DBT st, DFMSTORGEMITENT_DBT ak " +
		"WHERE EXTRACT(YEAR FROM MSG.T_EMSGDATE) = EXTRACT(YEAR FROM ?) " +
		"AND MSG.T_SENDBANKBIC = ? AND MSG.T_ID = ST.T_MSGID  " +
		"AND AK.T_STORGID = st.T_ID AND ROWNUM = 1 ORDER BY MMM DESC");
	cmd2.NullConversion = true;
	cmd2.AddParam ("", RSDBP_IN, fmstorgmsg.EMsgDate);	
	cmd2.AddParam ("", RSDBP_IN, fmstorgmsg.SENDBANKBIC);
	
	var rs2 = RsdRecordset(cmd2);
	if (rs2.MoveNext())
		if (rs2.value(0) != NULLVAL)
			n2 = Int(rs2.value(0));
		end;
	end;
	
	return Max(n1,n2);
end;

private macro FillLeaderZero(str, needlen)
	var s = str;
	
	if (strlen(s) < needlen)
		var i = 0;
		while (i < needlen - strlen(s))
			s = "0" + s;
		end;
	end;
	
	return s;
end;

// Формирование идентификатора электронного сообщения
private macro ComputeMsgId(fmstorgmsg, n)
	// gggg_nReg_nnnF_mmmmmmmm
	var gggg = ExtractYear(fmstorgmsg.EMsgDate);
	var nReg = FillLeaderZero(fmstorgmsg.KGRKO_BPtNumber, 4);
	var nnnF = FillLeaderZero(fmstorgmsg.BPtFilialNumber, 4);
	var mmmm = FillLeaderZero(String(n + 1), 8);
	
	return String(gggg + "_" + nReg + "_" + nnnF + "_" + mmmm);
end;

macro Clear3ZerosInString(str)
	var f = true;
	var i : integer = 1;
	
	var tmp : string = str;
	
	while (f != false)
		var sub = SubStr(tmp, 1, 1);
		if (sub != "0")
			f = false;
		else
			tmp = SubStr(tmp, 2);
		end;
		
		if (i < 3)
			i = i + 1;
		else
			f = false;
		end;
	end;
	
	return tmp;
end;

macro СлужЧасть(StOrgMsg, node : FmXmlNodeElement)
	node.addChildEx("ВерсФорм", "1.1");
	node.addChildEx("ВерсПрог", "RS-Bank " + _GetDBVersion());
	node.addChildEx("ТипИнф",     "ИНФРФМ");
	node.addChildEx("БИККОНапрв", StOrgMsg.SendBankBIC);
	node.addChildEx("ДатаСооб", FormatDate(StOrgMsg.EMsgDate));
	node.addChildEx("УполнСотрудн", StOrgMsg.ProxyPost);
	
	var fio = node.addChild("ФИОУполнСотрудн", StOrgMsg.ProxyPost);
	fio.addChildEx("Фам", StOrgMsg.ProxyName1);
	fio.addChildEx("Имя", StOrgMsg.ProxyName2);
	fio.addChildEx("Отч", StOrgMsg.ProxyName3);
	
	node.addChildEx("ТелУполнСотрудн", StOrgMsg.ProxyPhone);
end;

macro СведИспБанк(fmstorgmsg, node : @FmXmlNodeElement)
	var НаимКО : string = fmstorgmsg.KGRKO_BPtShortName;
	if (strlen(fmstorgmsg.BPtFilialShortName) > 0)
		НаимКО = НаимКО + ", филиал " + fmstorgmsg.BPtFilialShortName;
	end;
	
	node.addChildEx("НаимКО", НаимКО);
	node.addChildEx("РегНомКО", Clear3ZerosInString(fmstorgmsg.KGRKO_BPtNumber));
	node.addChildEx("НомФл", Clear3ZerosInString(fmstorgmsg.BPtFilialNumber));
	node.addChildEx("БИККО", fmstorgmsg.BPtBIC);
end;

macro ИнфАккредитив(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
	var n = 0;
	
	if (strlen(fmstorgmsg.MsgFileName) <= 0)
		n = ComputeMsgNumber(fmstorgmsg);
	end;
	
	var cmd = RsdCommand("SELECT * FROM DFMSTORGAKKRED_DBT WHERE T_STORGID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
		var ИнфАккредитив = node.addChild("ИнфАккредитив");
		
		if (strlen(rs.value("T_InfoSID")) <= 0)
			var sid = ComputeMsgId(fmstorgmsg, n);
			ИнфАккредитив.addChildEx("ИдентифИнфКО", sid);
			
			var size = pInfoSID.size;
			pInfoSID[size] = SidStruct;
			pInfoSID[size].First  = rs.value("T_ID");
			pInfoSID[size].Second = sid;
			
			n = n + 1;
		else
			ИнфАккредитив.addChildEx("ИдентифИнфКО", rs.value("T_InfoSID"));
		end;
		
		ИнфАккредитив.addChildEx("ПризнакИнф", rs.value("T_InfoDirection"));
		
		// Элементы XML-узла <ИнфАккр> и их значения
		var ИнфАккр = ИнфАккредитив.addChild("ИнфАккр");
		ИнфАккр.addChildEx("ДатаАккредитив", FormatDate(rs.value("T_AkkredDate")));
		ИнфАккр.addChildEx("НомАккредитив", rs.value("T_Number"));
		ИнфАккр.addChildEx("КодВал", rs.value("T_ISO_Number"));
		ИнфАккр.addChildEx("СумАккредитив", rs.value("T_Sum"));
		ИнфАккр.addChildEx("СрокАккредитив", FormatDate(rs.value("T_EndDate")));
		ИнфАккр.addChildEx("СумРубАккредитив", rs.value("T_NatCurSum"));
		
		var СвИспБанк = ИнфАккредитив.addChild("СведИспБанк");
		
		fmstorgakkred.KGRKO_BPtShortName = rs.value("T_KGRKO_BPtShortName");
		fmstorgakkred.BPtFilialShortName = rs.value("T_BPtFilialShortName");
		fmstorgakkred.KGRKO_BPtNumber = rs.value("T_KGRKO_BPtNumber");
		fmstorgakkred.BPtFilialNumber = rs.value("T_BPtFilialNumber");
		fmstorgakkred.BPtBIC = rs.value("T_BPtBIC");
		СведИспБанк(fmstorgakkred, СвИспБанк);
		
		var ПолучАккр = ИнфАккредитив.addChild("ПолучАккр");
		ПолучАккр.addChildEx("ТипПолучатель", rs.value("T_ReceiverType"));
		ПолучАккр.addChildEx("ПризнРез", rs.value("T_ResidentSign"));
		
		var str = String(rs.value("T_ReceiverName"));
		
		if (strlen(rs.value("T_ReceiverFilialName")) > 0)
			str = str + ", филиал " + rs.value("T_ReceiverFilialName");
		end;
		
		ПолучАккр.addChildEx("НаимПолучатель", str);
		ПолучАккр.addChildEx("ИННПолучатель", rs.value("T_ReceiverINN"));
	end;
end;

macro ПолучКодЦенБумаг(rs)
	var cmd = RsdCommand("select T_CODE from DLLVALUES_DBT where T_LIST = 1212 and T_ELEMENT = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, rs.value("T_FMSecurityType"));
	
	var code = "";
	var lrs = RsdRecordset(cmd);
	
	if (lrs.moveNext())
		code = lrs.value("T_CODE");
	end;
	
	return code;
End;

macro ИнфЦенБумаг(EmitentID, node : FmXmlNodeElement) 
	var cmd = RsdCommand("SELECT * FROM DFMSTORGEMITSECUR_DBT WHERE T_EMITENTID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, EmitentID);
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
		var ИЦенБумаг = node.addChild("ИнфЦенБумаг");
		
		ИЦенБумаг.addChildEx("КодЦенБумаг", ПолучКодЦенБумаг(rs));
		ИЦенБумаг.addChildEx("НомГосРегЦенБумаг", rs.value("T_IssueRegNumber"));
		ИЦенБумаг.addChildEx("КодВал", rs.value("T_NominalISO_Number"));
		ИЦенБумаг.addChildEx("КолЦенныхБумаг", rs.value("T_Quantity"));
		ИЦенБумаг.addChildEx("КодВалЦена", rs.value("T_PurchaseISO_Number"));
		ИЦенБумаг.addChildEx("ЦенаПриобретения", rs.value("T_Sum"));
		ИЦенБумаг.addChildEx("ЦенаРубПриобретения", rs.value("T_NatCurSum"));
	end;
end;

macro ИнфЭмитент(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
	var n = 0;
	
	if (strlen(fmstorgmsg.MsgFileName) <= 0)
		n = ComputeMsgNumber(fmstorgmsg);
	end;
	
	var cmd = RsdCommand("SELECT * FROM DFMSTORGEMITENT_DBT WHERE T_STORGID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
		var ИнфЭмитент = node.addChild("ИнфЭмитент");
		
		if (strlen(rs.value("T_InfoSID")) <= 0)
			var sid = ComputeMsgId(fmstorgmsg, n);
			ИнфЭмитент.addChildEx("ИдентифИнфКО", sid);
			
			var size = pInfoSID.size;
			pInfoSID[size] = SidStruct;
			pInfoSID[size].First  = rs.value("T_ID");
			pInfoSID[size].Second = sid;
			
			n = n + 1;
		else
			ИнфЭмитент.addChildEx("ИдентифИнфКО", rs.value("T_InfoSID"));
		end;
		
		ИнфЭмитент.addChildEx("ПризнакИнф", rs.value("T_InfoDirection"));
		ИнфЭмитент.addChildEx("ДатаПриобретения", FormatDate(rs.value("T_PurchaseDate")));
		
		var ЦенБумагиСвед = ИнфЭмитент.addChild("ЦенБумагиСвед");
		ЦенБумагиСвед.addChildEx("ТипЭмитент", rs.value("T_Type"));
		ЦенБумагиСвед.addChildEx("НаимЭмитент", rs.value("T_Name"));
		ЦенБумагиСвед.addChildEx("КодЭмитент", rs.value("T_Number"));
		
		ИнфЦенБумаг(rs.value("T_ID"), ЦенБумагиСвед);
	end;
end;

private macro СоздатьНаимОбщ(fmstorg : @RsdRecordset)
	var str = fmstorg.value("T_OrgName");
	
	if (strlen(fmstorg.value("T_FilialName")) > 0)
		str = str + ", филиал " + fmstorg.value("T_FilialName") + " ";
	end;
	
	return str;
end;

macro ИнфОбщество(fmstorgmsg, fmstorg : @RsdRecordset, node : FmXmlNodeElement)
	var ИнфОбщ = node.addChild("ИнфОбщество");
	
	var СведОбщ = ИнфОбщ.addChild("ИнфОбщ");
	СведОбщ.addChildEx("НаимОбщ", СоздатьНаимОбщ(fmstorg));
	СведОбщ.addChildEx("ОГРНОбщ", fmstorg.value("T_OGRN"));
	СведОбщ.addChildEx("ИННОбщ", fmstorg.value("T_INN"));
	СведОбщ.addChildEx("КППОбщ", fmstorg.value("T_KPP"));
	СведОбщ.addChildEx("ДатаРегОбщ", FormatDate(fmstorg.value("T_RegDate")));
	
	var АдрОбщ = ИнфОбщ.addChild("АдрОбщ");
	АдрОбщ.addChildEx("КодСубъектаПоОКАТО", fmstorg.value("T_OKATO"));
	АдрОбщ.addChildEx("Район", fmstorg.value("T_Rayon"));
	АдрОбщ.addChildEx("Пункт", fmstorg.value("T_Place"));
	АдрОбщ.addChildEx("Улица", fmstorg.value("T_Street"));
	АдрОбщ.addChildEx("Дом", fmstorg.value("T_House"));
	АдрОбщ.addChildEx("Корп", fmstorg.value("T_Corpus"));
	АдрОбщ.addChildEx("Оф", fmstorg.value("T_Office"));
	
	// Аккредитив
	if (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR)
		ИнфАккредитив(fmstorgmsg, fmstorg, InfoSIDMap, ИнфОбщ);
	elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_SECUR)
		ИнфЭмитент(fmstorgmsg, fmstorg, InfoSIDMap, ИнфОбщ);
	end;
end;

macro ИнформЧасть(fmstorgmsg, node : @FmXmlNodeElement)
	// Сформировать XML-узел "СведКО"
	var СведКО = node.addChild("СведКО");
	
	СведИспБанк(fmstorgmsg, node);
	
	var cmd = RsdCommand("SELECT * FROM DFMSTORG_DBT WHERE T_MSGID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.ID);
	var rs = RsdRecordset(cmd);
	
	while (rs.MoveNext())
		ИнфОбщество(fmstorgmsg, rs, node);
	end;
end;

macro GetFileName(StOrgMsg)
	var fname : string;
	setbuff(fmstorgmsg, StOrgMsg);
	
	if (strlen(fmstorgmsg.MsgFileName) <= 0)
		fname = String("SKO443_", fmstorgmsg.MessageKind:o:2:r, "_",
			fmstorgmsg.SendBankBIC:t:9:r,
			"_",
			FormatDateToName(fmstorgmsg.EMsgDate),
			"_",
			GetNNNComponent(fmstorgmsg):o:3:r,
			".xml");
	else 
		fname = fmstorgmsg.MsgFileName;
	end;
	
	return fname;
end;

private macro UpdateStOrgMsg(StOrgMsgID, FileName)
	var fname = "";
	SplitFile(FileName, fname);
	fname = fname + ".xml";
	
	var cmd = RsdCommand("UPDATE DFMSTORGMSG_DBT SET T_MSGFILENAME = ?, " +
		"T_UNLOADDATE = SYSDATE, " +
		"T_UNLOADTIME = SYSDATE " +
		"WHERE T_ID = ?");
	cmd.NullConversion = true;
	
	cmd.AddParam ("", RSDBP_IN, fname);
	cmd.AddParam ("", RSDBP_IN, StOrgMsgID);
	
	return cmd.Execute();
end;

private macro UpdateAkkred(Id, InfoSID)
	var cmd = RsdCommand("UPDATE DFMSTORGAKKRED_DBT SET T_INFOSID = ? WHERE T_ID = ?");
	cmd.NullConversion = true;
	
	cmd.AddParam ("", RSDBP_IN, InfoSID);
	cmd.AddParam ("", RSDBP_IN, Id);
	
	return cmd.Execute();
end;

private macro UpdateEmitent(Id, InfoSID)
	var cmd = RsdCommand("UPDATE DFMSTORGEMITENT_DBT SET T_INFOSID = ? WHERE T_ID = ?");
	
	cmd.AddParam ("", RSDBP_IN, InfoSID);
	cmd.AddParam ("", RSDBP_IN, Id);
	
	return cmd.Execute();
end;

macro FM_UnloadStOrgMsg_1_1(StOrgMsg, FileName)
	setbuff(fmstorgmsg, StOrgMsg);
	
	var xmlWriter = FmXmlDocumentWriter;
	xmlWriter.createProcessingInstruction("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
	var root : FmXmlNodeElement;
	root = xmlWriter.getRootElement("СообщОткрКО", root);
	
	var service : FmXmlNodeElement;
	var inform : FmXmlNodeElement;
	
	service = root.addChild("СлужЧасть");
	inform  = root.addChild("ИнформЧасть");
	
	СлужЧасть(fmstorgmsg, service);
	ИнформЧасть(fmstorgmsg, inform);
	
	var stat = 0;
	var FullFileName = FileName;
	close(MessageXMLFile);
	DelFile(MessageXMLFile);
	if(open(MessageXMLFile, FullFileName, "utf8"))
		xmlWriter.saveToFile(MessageXMLFile);
		close(MessageXMLFile);
		
		UpdateStOrgMsg(fmstorgmsg.ID, FullFileName);
		
		var i = 0;
		if (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR)
			while (i < InfoSIDMap.size)
				UpdateAkkred (InfoSIDMap[i].First, InfoSIDMap[i].Second);
				i = i + 1;
			end;
		elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_SECUR)
			while (i < InfoSIDMap.size)
				UpdateEmitent (InfoSIDMap[i].First, InfoSIDMap[i].Second);
				i = i + 1;
			end;
		end;
	else
		// Ошибка записи файла.
		stat = 3510;
	end;
end;