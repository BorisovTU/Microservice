/*
 $Name: common_service_caller.mac
 $Module: RS-Connect
 $Description: Сервис
 */
 /*
	Сервис
 */
import "serviceaction.mac";

const eXR_LOG_EVENT_KIND_SOAP_REQUEST = 50;
const eXR_LOG_EVENT_KIND_SOAP_RESPONSE = 51;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

class CServiceCallerBase(_requestID : string, _urlAddress : String, _MacroName : String, _MacroFuncName : String, _Timeout : Integer, _Priority : Integer)
    private var m_requestID     : String = _requestID; 
    private var m_urlAddress    : String = _urlAddress;
    private var m_macroName     : String = _MacroName;
    private var m_macroFuncName : String = _MacroFuncName;
    private var m_Timeout       : Integer = IIF(_Timeout == NULL, 0, _Timeout);
    private var m_Priority      : Integer = IIF(_Priority == NULL, 0, _Priority);
    
    private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");
    
    private macro getJavaPort()
        var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V",  m_urlAddress);
        var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
        var port = proxy.getIntegrationWSPort();
        
        return port;
    end;

    private macro SetRequestParameters(request : @TXRRequest)
        RunError("", "Вызов метода абстрактного класса");
    end;
   
    private macro GetRequest() : TXRRequest
        var request : TXRRequest = TXRRequest();
        request.ReqId = m_requestID;
        request.MacroName = m_macroName;  
        request.MacroFunc = m_macroFuncName;
        request.Direction = "1";
        SetRequestParameters(@request);
        return request;
    end;

    macro CallService(outResponseRsl : @variant, throwOnFault) : bool
        var mes = GetRequest().GenerateMessage();
        
        var port = getJavaPort();
        
        WriteXrLog(eXR_LOG_EVENT_KIND_SOAP_REQUEST, m_requestID, mes, m_urlAddress, "XMLRPCCall", "");
        
        var outResponse = port.xmlrpcCall(mes, m_Timeout, m_Priority);
        
        WriteXrLog(eXR_LOG_EVENT_KIND_SOAP_RESPONSE, m_requestID, outResponse, m_urlAddress, "XMLRPCCall", "");
        
        convertToRsl(outResponse, outResponseRsl);
        
        if ((throwOnFault == NULL) and (genPropId(outResponseRsl, "faultString") != -1))
          RunError("", outResponseRsl.faultString);
        end;
        
        return true;
      OnError(error)
        RunError("", error.err);
        return false;
      end;
    end;
    
end;
