/*
        ПЗО

        Подробный протокол о расчете периодических комиссий

                            Mikhailov S., 06.08.2001
*/

import sfcommon;
import PTInter;//ПолучитьСубъекта


private const SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const SF_BASETYPE_QUONT   = 2;       /* Количество*/


file sfrepdet_write( "sfrepdet.tmp" ) write ;


/*
    Для распечатки окончания
*/
record prev_sfrepdet( "sfrepdet.tmp" ) ;

/*
*/
var BaseBeginDate;
var TotalSum;


/*
*/
record sfbastrn_get( "sfbastrn.str" );
record sfbascsh_get( "sfbascsh.str" );


private file fininstr( fininstr )Key 0;

/*private var FIID_CalcSum;*/

var FeeTypeCurrent ;
var CommNumberCurrent;

/*var ObjectTypeCurrent;
var FIIDCurrent;
var ObjectCurrent ;*/
var sfcontrID;

var AlgKindCurrent;
var AlgNumberCurrent ;


private var FICode_calc:string;

private const USERPRINTHEADER   = "CalcReportPrintHeader";
private const USERPRINTLINE   = "CalcReportPrintLine";
private const USERPRINTFOOTER   = "CalcReportPrintFooter";


macro GetMacro( name, sfrepdet )
        file sfcalcal(sfcalcal)key 0;
        record sfcalcal_usr( "sfclusr.str" );
        
        sfcalcal.feetype= sfrepdet.FeeType;
        sfcalcal.CommNumber = sfrepdet.CommNumber;
        sfcalcal.Kind = sfrepdet.AlgKind;
        sfcalcal.Number = sfrepdet.AlgNumber;

        if( not GetEQ( sfcalcal ))
            MsgBox("Не найден алгоритм");
            return 1;
        end;

        SetRecordAddr( sfcalcal_usr, sfcalcal, 0, 0, false );
        SetParm ( 0,sfcalcal_usr.CalcMacroName );

        return 0;

end;


/*
*/
macro DropNewAlg()
    FeeTypeCurrent = 0;
    CommNumberCurrent=0;

/*    ObjectTypeCurrent = 0;
    FIIDCurrent     =-1;
    ObjectCurrent   ="";*/
    sfcontrID = 0;

    AlgKindCurrent   =0;
    AlgNumberCurrent =0;

end;/*DropNewAlg*/


/*
*/
macro CheckNewAlg( sfrepdet )
        if( ( sfrepdet.FeeType == FeeTypeCurrent ) and
             ( sfrepdet.CommNumber == CommNumberCurrent )and
             
/*             ( sfrepdet.ObjectType == ObjectTypeCurrent )and
             ( sfrepdet.FIID_contr == FIIDCurrent )and
             ( sfrepdet.Object == ObjectCurrent )and*/
             (sfrepdet.SfContrID == sfcontrID)and
             
             ( sfrepdet.AlgKind == AlgKindCurrent )and
             ( sfrepdet.AlgNumber == AlgNumberCurrent ) )

            return false;
        else
            return true;
        end;

end;/*CheckNewAlg*/


/*
*/
macro SetNewAlg( sfrepdet )

             FeeTypeCurrent = sfrepdet.FeeType;
             CommNumberCurrent = sfrepdet.CommNumber;

/*             ObjectTypeCurrent = sfrepdet.ObjectType;
             FIIDCurrent = sfrepdet.FIID_contr;
             ObjectCurrent = sfrepdet.Object;*/
             sfcontrID = sfrepdet.sfcontrID;
             
             AlgKindCurrent = sfrepdet.AlgKind;
             AlgNumberCurrent = sfrepdet.AlgNumber;

end;/*SetNewAlg*/

/*
*/

macro DropAlgParameters()
    BaseBeginDate = Date( 0, 0, 0 );
    TotalSum = MoneyL( 0 );
    FICode_calc = "";

    return 0;
end;/*DropAlgParameters*/

/*
*/
macro MakeAlgParameters( sfrepdet )
    TotalSum = TotalSum + sfrepdet.CalcSum;
end;/*MakeAlgParameters*/


/*
*/
macro InsertVarLen(file_handle, RecLen )
 if(Not Insert(file_handle, RecLen ) )
  MsgBox("Не вставлена запись в файл ",file_handle);
  
 end;
end;/*InsertVarLen*/

/*
    Даже если информации нет, мы вставляем пустую переменную часть
*/
macro SetBaseInfo( recLen , baseinfo_, sfrepdet )
    record sfbastrn_rept( "sfbastrn.str" );
    record sfbastrn_info( "sfbastrn.str" );

    record sfbascsh_rept( "sfbascsh.str" );
    record sfbascsh_info( "sfbascsh.str" );


/**/
    if( sfrepdet.Algkind == SF_CALCAL_TRN_KIND )
        SetParm( 0, getRecordSize( sfbastrn_rept ) );
        SetRecordAddr( sfbastrn_rept, sfrepdet_write, 0, 0, false );

        ClearRecord( sfbastrn_rept );

        if( baseinfo_ != null )
            SetBuff( sfbastrn_info, baseinfo_ );

            copy( sfbastrn_rept, sfbastrn_info );
        end;/*if*/
/**/
    elif(  sfrepdet.Algkind == SF_CALCAL_CASH_KIND )
        SetParm( 0, getRecordSize( sfbascsh_rept ) );
        SetRecordAddr( sfbascsh_rept, sfrepdet_write, 0, 0, false );

        ClearRecord( sfbascsh_rept );

        if( baseinfo_ != null )
            SetBuff( sfbascsh_info, baseinfo_ );

            copy( sfbascsh_rept, sfbascsh_info );
        end;/*if*/
    end;/*if sfrepdet.Algkind*/

    return 0;
end;/*SetBaseInfo*/



/*
*/
macro GetBaseInfo( /*recLen , baseinfo_, sfcalcal,*/ sfrepdet )
    record sfbastrn_info( "sfbastrn.str" );
    record sfbascsh_info( "sfbascsh.str" );
    
    if( sfrepdet.AlgKind == SF_CALCAL_TRN_KIND )
/*
    Здесь всегда что-то есть, хотя бы пустота
*/
        SetRecordAddr( sfbastrn_info, sfrepdet, 0, 0, false );


        copy( sfbastrn_get, sfbastrn_info );
        
    elif( sfrepdet.AlgKind == SF_CALCAL_CASH_KIND )
/*
    Здесь всегда что-то есть, хотя бы пустота
*/
        SetRecordAddr( sfbascsh_info, sfrepdet, 0, 0, false );


        copy( sfbascsh_get, sfbascsh_info );
        
    end;/*if sfrepdet.AlgKind */

    return 0;
end;/*GetBaseInfo*/


private macro GetPartyName(PartyID)
    var party = TRecHandler("party.dbt");
    if( ПолучитьСубъекта( PartyID, party ) == 0 )
        return party.rec.ShortName;
    else
        return "<Не задан>";
    end;
end;


macro ReportPrintHeader( sfrepdet )
    file sfcomiss( sfcomiss )key 0;
    file sfcontr(sfcontr)key 0;
    
    var MacroName:string;

    sfcomiss.FeeType = sfrepdet.FeeType;
    sfcomiss.Number  = sfrepdet.CommNumber;

    if( not GetEQ( sfcomiss ) )
        MsgBox( "Не найдена комиссия" );
        return 1;
    end;

    sfcontr.ID = sfrepdet.sfcontrID;
    if( not GetEQ( sfcontr ) )
        MsgBox( "Не найден договор" );
        return 1;
    end;

[Объект начисления -- счет #########################](sfcontr.Object);
[Договор обслуживания N ##############################. Плательщик ####################](sfcontr.Number:r, GetPartyName( sfcontr.PartyID ));
[Дата начала ##########. Контрагент ####################](sfcontr.DateBegin, GetPartyName( sfcontr.ContractorID ));
[Комиссия ################]( sfcomiss.Code );
[За период с ########## по ##########]( sfrepdet.BeginDate, sfrepdet.EndDate );

    if( sfrepdet.AlgKind == SF_CALCAL_TRN_KIND )
[---------------------------------------------------------------------------------------------------------------------------------------------];
[|        Период           |             Сумма                   |             Ставка                  |             Итого                   |];
[|    с       |    по      |                                     |                                     |                                     |];
[---------------------------------------------------------------------------------------------------------------------------------------------];
    elif( sfrepdet.AlgKind == SF_CALCAL_CASH_KIND )
[---------------------------------------------------------------------------------------------------------------------------------------------------------];
[|   Дата     |     Номер       |Символ|             Сумма                   |             Ставка                  |            Итого                    |];
[|            |   документа     |      |                                     |                                     |                                     |];
[---------------------------------------------------------------------------------------------------------------------------------------------------------];
    elif( sfrepdet.AlgKind == SF_CALCAL_USR_KIND )

        if( GetMacro(MacroName, sfrepdet)) return 1;end;
        if( ExecMacroFile( MacroName, USERPRINTHEADER, sfrepdet ) )
            MsgBox( "Ошибка при выполнении функции ", USERPRINTHEADER, " в файле ", MacroName );
            return 1;
        end;
    end;/* if sfrepdet.AlgKind */
    
    return DropAlgParameters();
end;/*ReportPrintHeader*/


macro ReportPrintLine( sfrepdet ) 

    var MacroName:string;
    var FICode_base, FICOde_tariff:string;

    if( GetBaseInfo( sfrepdet ) )
        return 1;
    end;

    MakeAlgParameters( sfrepdet );

    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;

    if( sfrepdet.AlgKind == SF_CALCAL_TRN_KIND )
    
[| ########## | ########## | ################### ############### | ################### ############### | ################### ############### |]

    ( sfrepdet.BeginDate, sfrepdet.EndDate,
    sfrepdet.BaseSum, FICode_base,
    sfrepdet.tariff, FICOde_tariff,
    sfrepdet.CalcSum, FICode_calc );

    elif( sfrepdet.AlgKind == SF_CALCAL_CASH_KIND )
    
[| ########## | ############### | #### | ################### ############### | ################### ############### | ################### ############### |]

    ( sfbascsh_get.Date_Carry, sfbascsh_get.Numb_Document, sfbascsh_get.Symbol_Cash, 
    sfrepdet.BaseSum, FICode_base,
    sfrepdet.tariff, FICOde_tariff,
    sfrepdet.CalcSum, FICode_calc );

    elif( sfrepdet.AlgKind == SF_CALCAL_USR_KIND )

        if( GetMacro(MacroName, sfrepdet)) return 1;end;
        
        if( ExecMacroFile( MacroName, USERPRINTLINE, sfrepdet ) )
            MsgBox( "Ошибка при выполнении функции ", USERPRINTLINE, " в файле ", MacroName );
            return 1;
        end;
    end;/*if sfrepdet.AlgKind */


    return 0;
end;/*ReportPrintLine*/


/*
*/
macro ReportPrintFooter( sfrepdet )
    var MacroName:string;
    
    if( sfrepdet.AlgKind == SF_CALCAL_TRN_KIND )
[---------------------------------------------------------------------------------------------------------------------------------------------];
[|Итого за период                                                                                      | ################### ############### |]
(TotalSum, FICode_calc);/*Осталась заполненной с последнего раза*/
[---------------------------------------------------------------------------------------------------------------------------------------------];
    elif( sfrepdet.AlgKind == SF_CALCAL_CASH_KIND )
[---------------------------------------------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                                                                   | ################### ################ |]
(TotalSum, FICode_calc);
[---------------------------------------------------------------------------------------------------------------------------------------------------------];
    elif( sfrepdet.AlgKind == SF_CALCAL_USR_KIND )
        if( GetMacro(MacroName, sfrepdet)) return 1;end;
        
        if( ExecMacroFile( MacroName, USERPRINTFOOTER, sfrepdet, TotalSum ) )
            MsgBox( "Ошибка при выполнении функции ", USERPRINTFOOTER, " в файле ", MacroName );
            return 1;
        end;
    end;

[  ];
[  ];


    return 0;
end;/*ReportPrintFooter*/


macro ReportBegin()
  return OpenReportGlobalFile( sfrepdet_write, true );
end;/*ReportBegin*/


/*
*/
macro ReportEnd()

    file sfrepdet( "sfrepdet.tmp" ) key 0;

    DropNewAlg();
    
    if( OpenReportGlobalFile( sfrepdet, false ) )
        return 1;
    end;
    
    Clearrecord( prev_sfrepdet );
    
    if( NRecords(sfrepdet) == 0 )
        println("С указанными параметрами рассчитываемых комиссий  с алгоритмами нет");
    end;
    
    while( next( sfrepdet ) )


        if( not CheckNewAlg( sfrepdet ) )

             if( ReportPrintLine( sfrepdet ) )
                return 1;
             end;
            
        else
             if( FeeTypeCurrent != 0 )/* Значит, уже что-то прошли */
                 if( ReportPrintFooter( prev_sfrepdet ) )
                    return 1;
                 end;
             end;
             
             SetNewAlg( sfrepdet );

             if ( ReportPrintHeader( sfrepdet ) )
                return 1;
             end;
             
             if( ReportPrintLine( sfrepdet ) )
                return 1;
             end;

        end;

        copy ( prev_sfrepdet, sfrepdet );

    end;/*while*/


    if( FeeTypeCurrent != 0 )/* Значит, уже что-то прошли */
         if( ReportPrintFooter( prev_sfrepdet ) )
            return 1;
         end;
    end;

    
    return 0;
end;/*ReportEnd*/



macro ReportInsertLine(
        sfrepdet_,
                         
        baseinfo_

            )

    var RecLen;



    record sfrepdet( "sfrepdet.tmp");


    SetBuff( sfrepdet, sfrepdet_ );
        

    Copy( sfrepdet_write, sfrepdet );

    if( SetBaseInfo( RecLen, baseinfo_, sfrepdet) )
        return 1;
    end;

    return InsertVarLen( sfrepdet_write, RecLen );
            
end;/*ReportInsertLine*/

macro ReportInsertErrorLine( SFErrorMsg,FeeType,CommNumber,CID )

    file sfcomiss( sfcomiss )key 0;
if (FeeType and CommNumber)
    sfcomiss.FeeType = FeeType;
    sfcomiss.Number  = CommNumber;

    if( not GetEQ( sfcomiss ) )
        SFErrorMsg = "Не найдена комиссия";
	[Ведомость расчёта комиссии ################](SFErrorMsg );
    else
	[Ведомость расчёта комиссии ################]( sfcomiss.Code );
    end;


[---------------------------------------------------------------------------------------------------------------------];
[| Объект начисления         | Ошибка										     |];
[|                           |   										     |];
[---------------------------------------------------------------------------------------------------------------------];
[| ######################### | ############################################################## 			      |]
(CID,SFErrorMsg);
[---------------------------------------------------------------------------------------------------------------------];
[  ];
else
 println("С указанными параметрами оплачиваемых комиссий нет");
end; /* end if */
return 0;
end; /*ReportInsertErrorLine*/