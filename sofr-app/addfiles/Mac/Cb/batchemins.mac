/* -----------------------------------------------------------------------------
   Пакетная вставка записей в таблицу
   ----------------------------------------------------------------------------- */
import BankInter, likepy;

private macro algMaxStrLen( result:integer, val:string ):integer
  return max( strlen(val), result );
end;

/* -----------------------------------------------------------------------------
   RSL-класс для пакетной вставки записей таблицы
   параметры конструктора:
     TableName - имя таблицы в формате эмулятора Pervasive (например "pmpaym.dbt")
     InitialSize - начальный размер массива записей (примерный, исключительно для быстродействия TArray)
     SizeIncrement - размер страницы приращения массива записей
   методы:
     AddRecort( Rec:TRecHandler ) - добавить запись для вставки
     Clear() - очистить список записей
     Transfer():bool - вставить записи в БД
   ----------------------------------------------------------------------------- */
CLASS RsbBatchEmuInsert( TableName:string, InitialSize:integer, SizeIncrement:integer )

  private
  var m_TableName:string = TableName,
      m_Records:TArray = TArray(InitialSize,  SizeIncrement),
      m_SqlStatement:string = "";

  /* добавить запись для вставки */
  MACRO AddRecord( Rec:TRecHandler )
    if( strupr(Rec.TblName) != strupr(m_TableName) )
      RunError( "Вставляемая запись имеет неверную структуру" );
    end;
    m_Records.value( m_Records.size ) = TRecHandler( m_TableName );
    Copy( m_Records.value( m_Records.size - 1 ), Rec );
  END;

  /* очистить список записей */
  MACRO Clear()
    m_Records.size = 0;
  END;

  private macro CreateSQLStatement()
    var rec:TRecHandler = TRecHandler( m_TableName ),
        i:integer = 0,
        names:TArray = TArray,
        params:TArray = TArray;
    for( i, 0, fldNumber( rec ) - 1 )
      names.value( i ) = string( "t_", fldName( rec, i ) );
      params.value( i ) = "?";
    end;
    return string( "insert into d", strsubst( m_TableName, ".", "_" ), 
           " ( ", join( names, ", " ) , " )",
           " values ( ", join( params, ", " ), " )" );
  end;

  private macro AddColumn( Executor:RsbSQLInsert, ColNum:integer )
    var rec:TRecHandler,
        strBuffSize:integer = 0,
        values:TArray = TArray( m_Records.size );
    for( rec, m_Records )
      values.value( values.size ) = rec.item( ColNum );
    end;
    if( valtype( values.value( 0 ) ) == V_STRING )
      strBuffSize = reduce( values, @algMaxStrLen, 0 ) + 1;
    end;
    Executor.AddParam( valtype( values.value( 0 ) ), values, strBuffSize );
  end;
  
  /* вставить записи в БД */
  MACRO Transfer():bool
    if( not m_Records.size )
      return true;
    end;
    if( not m_SqlStatement )
      m_SqlStatement = CreateSQLStatement();
    end;
    var rec:TRecHandler = m_Records.value(0);
    var executor:RsbSQLInsert = RsbSQLInsert( m_SqlStatement, fldNumber( rec ), m_Records.size ),
        i:integer = 0;
    for( i, 0, fldNumber( rec ) - 1 )
      AddColumn( executor, i );
    end;
    return executor.Insert();
  END;
  
END;
