/*
$Name:        fm_filerecs.mac
$Module:      ФМ
$Description: Контроль данных в скроллиенге "Записи файла ОЭС"
*/


Import Bankinter, FMInter, rsd, rcw, cb_sql, RslScr, rsexts, "fm_outex.mac"; 


/***********************************************************************
   Копирует текущее содержимое Recordset'а в буфер файла или структуры

   fbuff          - буфер файла или структуры
   rset           - Recordset или TRsbDataSet
   start_rset_pos - позиция Recordset'a, начиная с которой будут копи-
                    роваться значения.  

   Возвращаемое значение: НЕТ
***********************************************************************/
macro _CopyRSetToFBuff( fbuff, rset )
  var idx   = 0,
      nflds = FldNumber( fbuff ),
      val,
      NullConv;

  ClearRecord(fbuff);

  if( IsEqClass( "RsdRecordset", rset ))
    NullConv = rset.Command.NullConversion;
    rset.Command.NullConversion = true;
  end;

  while( idx < nflds )
    val = rset.value( "t_" + FldName( fbuff, idx ) );
    if( ValType( fbuff( idx ) ) == V_STRING )
      fbuff( idx ) = SQL_ConvTypeStr( val );
    elif( val != NULL )
      fbuff( idx ) = rset.value( "t_" + FldName( fbuff, idx ), null, ValType( fbuff( idx ) ) );
    end;
    idx = idx + 1;
  end;

  if( IsEqClass( "RsdRecordset", rset ))
    rset.Command.NullConversion = NullConv;
  end;
end;

/* Вывод отчета */
macro ПоказОтчета(TxtFileName)
  file outfile() txt;

  if( not open(outfile, TxtFileName))
    msgbox("Не найден файл отчета");
  else  
    ViewFile(outfile);
    close(outfile);
  end;
end;

private file f1     ( "fz115.dbf" ) dbf ;
private file filesr ( "fz115.dbf" ) dbf write;
private var TmpFileName;

macro CreateTmpFile(  )
 
  var Path, stat;
  var DIR_ORIG;
  DIR_ORIG = FM_GetDirOrigUnloadFile();
 
  if( not Open( f1, DIR_ORIG ) )
    MsgBox( "Невозможно открыть эталонный файл " + DIR_ORIG + ", заданный в настройках подсистемы" );
    exit(1);
  end;

  GetRegistryValue("ФИНМОНИТОРИНГ\\DIR_IMPWORK", V_STRING, Path, stat);
  if( stat == 0 )
    if( SubStr( Path, strlen( Path )) != "\\" )
      Path =  Path + "\\";
    end;   
    TmpFileName = Path + "fm_sel.dbf"; 
    if( not clone( f1, TmpFileName ) )
      MsgBox( "Невозможно создать файл ", TmpFileName );
      Exit(1);
    end;            
  end;
end;


macro DelTmpFile( )   
  if( Open( filesr, TmpFileName ) )
    while( next(filesr) )
       Delete( filesr );
    end;
    Close( filesr );
  end;
  DelFile( TmpFileName );
  RemoveFile(TmpFileName);  
end;



macro ExecControl( FmFileName, sysdate, systime, DprtName,  OperID, OperName, fimp ) //FM_FileRecordsScrol
  var i = 0, stat = 0, DIR_Path, nflds;   
  file FileOes ( ) dbf;
  record FileImport( fm_fileimport );
  setbuff( FileImport, fimp );

  GetRegistryValue("ФИНМОНИТОРИНГ\\DIR_IMPWORK", V_STRING, DIR_Path, stat);
  if( stat != 0 ) 
    MsgBox ("Ошибка чтения настройки ФИНМОНИТОРИНГ\\DIR_IMPWORK"); 
  end;   
  
  if( SubStr( DIR_Path, strlen( DIR_Path )) != "\\" )
    DIR_Path = DIR_Path + "\\";
  end;  
 
  if( not open( FileOes, DIR_Path + FmFileName ))
    msgbox("настройка реестра ФИНМОНИТОРИНГ\\DIR_IMPWORK задана неверно,|| или файл ОЭС не может быть открыт!!!"); 
    return 1;
  end;

  SetOutput(DIR_Path + "control.txt");
  ExecMacroFile( "fm_fimpctrl.mac", "PrintFileOesFillingControl", sysdate, systime, FmFileName, DprtName,  OperID, OperName, FileImport.Type );              
  SetOutput();                           
  ПоказОтчета( DIR_Path + "control.txt");                
  return 0;
end;









