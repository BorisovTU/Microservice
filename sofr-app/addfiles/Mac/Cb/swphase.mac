/*
 * Формирование протокола выполнения процедуры переключения группы счетов
 */

Import rsd, "globals.mac";

var FirstDepartment : bool;
var ErrorProcedure : bool;

/* получить название филиала */
private macro GetDepartmentName( Department : integer ) : string

  var DepartmentName : string;

  if( Department == 0 )

    DepartmentName = "по всем филиалам";

  else

    CB_GetDepartmentCodeAndName( Department, null, null, DepartmentName );

  end;

  return DepartmentName;

end;


/* получить название фазы операционного дня */
private macro GetPhaseName( Phase : integer ) : string

  var PhaseName : string;

  file phases("phases.dbt") key 0;

  PhaseName = "";

  if( Phase != 0 )

    phases.Phase = Phase;
    if( GetEQ(phases) )
      PhaseName = string(Phase, " ", phases.Name );
    end;

  end;

  return PhaseName;

end;

/* получить название вида группы */
private macro GetGroupKindName( GroupKind : integer ) : string

  var GroupKindName : string;

    if( GroupKind == 1 ) GroupKindName = "группа открытия";
  elif( GroupKind == 2 ) GroupKindName = "группа закрытия";
  end;

  return GroupKindName;

end;

/* получить название переключаемых групп */
private macro GetGroupName( AllGroups : bool ) : string

  var GroupName : string;

  var rs : RsdRecordset;

  GroupName = "";

  if( AllGroups )

    GroupName = "все группы";

  else

    rs = RsdRecordset( "SELECT d.t_Name FROM dopdgrp_tmp t, dopdgroup_dbt d WHERE t.t_GroupID = d.t_GroupID" );

    while( rs.moveNext() )

      if( GroupName != "" )
        GroupName = GroupName + ", ";
      end;

      GroupName = GroupName + string(rs.value(0));

    end;

  end;

  return GroupName;

end;


/*
 * Печать заголовка протокола
 */
private macro PrintTitle()

[ Протокол  выполнения процедуры переключения группы счетов                    ];
[                                                                              ];
[  Дата формирования: ##########                                               ] (date:f:l);
[ Время формирования: ##########                                               ] (time:f:l);
[       Операционист: ##### #                                                  ] ({oper}:l, {Name_Oper});
[                                                                              ];

end;

/*
 * Печать параметров процедуры переключения фазы
 */
private macro PrintParam( Department : integer, OperDate : date, Phase : integer, GroupKind : integer, AllGroups : bool )

[ Параметры запуска:                                                           ];
[   ####### ################################################################## ] ( "Филиал:", GetDepartmentName(Department):w );
[   ####################### ##########                                         ] ( "Дата операционного дня:", OperDate:f );
[   ####################### ################################################## ] ( "Фаза операционного дня:", GetPhaseName(Phase):w );
[   ########### #                                                              ] ( "Вид группы:", GetGroupKindName(GroupKind) );
[   ####### ################################################################## ] ( "Группа:", GetGroupName(AllGroups):w );
[                                                                              ];
[                                                                              ];

end;

/*
 * Начало обработки групп филиала
 */
private macro StartProcessDepartment( Department : integer )

  if( FirstDepartment )
[ Результаты выполнения:                                                       ];
[                                                                              ];
  end;

[   ######## ################################################################# ] ( "Филиал -", GetDepartmentName(Department):w );

  FirstDepartment = false;

end;

/*
 * Завершение обработки групп филиала
 */
private macro EndProcessDepartment( IsError : bool, ErrMessage : string )

  var PrintMessage : string;

  PrintMessage = "группы переключены";

  if( IsError )
    
    ErrorProcedure = true;
    PrintMessage = "группы не переключены";

    if( ErrMessage != "" ) PrintMessage = PrintMessage + ": " + ErrMessage; end;

  end;
    
[   ########### ############################################################## ] ( "Результат - ", PrintMessage:w );
[                                                                              ];

end;

/*
 * Ошибка переключения группы
 */
private macro ErrorSwitchGroup( GroupID : integer, GroupName : string, ErrMessage : string )

[     ################################    #################################### ] ( ("Группа " + GroupID + " " + GroupName):w, ErrMessage:w );

end;

/*
 * Заверщение раобты процедуры
 */
private macro EndProcedure()

[                                                                              ];  
  if( ErrorProcedure )
[ Результат выполнения: группы переключены не во всех филиалах                 ];
  else
[ Результат выполнения: группы переключены во всех филиалах                    ];
  end;

end;


/*
 * Начало работы макроса
 */
ErrorProcedure  = false;
FirstDepartment = true;
