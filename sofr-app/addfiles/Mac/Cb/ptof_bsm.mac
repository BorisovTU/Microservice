/*
 ------------------------------------------------------------------------------
                            Для п/с ОФ

 Макрос "Процедура проверки наличия инвентарных карточек или документов 
 движения и инвентаризации с данным номером подразделения
 ------------------------------------------------------------------------------
*/

file bsm      ("basemean.dbt", "bsmeans.def" ) key 0; 
file mvm      ("movemean.dbt", "bsmeans.def" ) key 0; 
file bsm_inv  ("bsminve.dbt",  "bsmeans.def" ) key 0; 

var bsm_ercode;
var bsm_ertext;

/*
 ------------------------------------------------------------------------------
 Проверка списка инвентарных карточек
 ------------------------------------------------------------------------------
*/
Macro Check_BsmMean_Ptoffice( rec_ptof )

  var rc = True;

  KeyNum( bsm, 3 );                  /* PartyID+OfficeID+Group+InvNumb */
  bsm.PartyID    = rec_ptof.PartyID;
  bsm.OfficeID   = rec_ptof.OfficeID;
  bsm.Group      = -1;
  bsm.InvNumb    = "";
  rc = GetGE( bsm );
  if( rc == True ) 
    if( (bsm.PartyID  == rec_ptof.PartyID ) and
        (bsm.OfficeID == rec_ptof.OfficeID) )
      MsgBox("По данному подразделению, в п/с ОФ| - есть инв.карточка номер ",bsm.InvNumb," !" );
      return 1;
    else
      return 0;
    end;
  else
    bsm_ercode = status( bsm_ertext );
    if( (bsm_ercode == 4) or (bsm_ercode == 9))
      return 0;
    else
      MsgBox("Ошибка (",bsm_ercode,"):",bsm_ertext);
      return 1;
    end;
  end;
end;

/*
 ------------------------------------------------------------------------------
 Проверка списка актов "движения"
 ------------------------------------------------------------------------------
*/
Macro Check_MoveMean_Ptoffice( rec_ptof )

  var rc = True;

  KeyNum( mvm, 5 );                  /* PartyID+OfficeID+Group+A_DateStmt */
  mvm.PartyID    = rec_ptof.PartyID;
  mvm.OfficeID   = rec_ptof.OfficeID;
  mvm.Group      = -1;
  mvm.A_DateStmt = Date(0,0,0);
  rc = GetGE( mvm );
  if( rc == True )
    if( (mvm.PartyID  == rec_ptof.PartyID ) and
        (mvm.OfficeID == rec_ptof.OfficeID) )
      MsgBox("По данному подразделению, в п/с ОФ| - есть акт номер ",mvm.A_Nstmt," !" );
      return 1;
    else
      return 0;
    end;
  else
    bsm_ercode = status( bsm_ertext );
    if( (bsm_ercode == 4) or (bsm_ercode == 9))
      return 0;
    else
      MsgBox("Ошибка (",bsm_ercode,"):",bsm_ertext);
      return 1;
    end;
  end;
end;

/*
 ------------------------------------------------------------------------------
 Проверка списка док-тов на инвентаризацию
 ------------------------------------------------------------------------------
*/
Macro Check_BsmInve_Ptoffice( rec_ptof )

  KeyNum( bsm_inv, 0 );              
  ReWind( bsm_inv );
  while( next(bsm_inv) == True )
    if( (bsm_inv.Party_ID  == rec_ptof.PartyID ) and
        (bsm_inv.Office_ID == rec_ptof.OfficeID) )
      MsgBox("По данному подразделению, в п/с ОФ| - есть док-т инвентаризации номер ",bsm_inv.Num_Order," !" );
      return 1;
    end;
  end;
  bsm_ercode = status( bsm_ertext );
  if((bsm_ercode == 4) or (bsm_ercode == 9))
    return 0;
  else
    MsgBox("Ошибка (",bsm_ercode,"):",bsm_ertext);
    return 1;
  end;
end;
