/*
 $Name: VedParty.mac
 $Module: Ядро ГКБО 
 $Description: ФМ. Перечень подозрительных участников ВЭД
 */
 /*
	ФМ. Перечень подозрительных участников ВЭД
 */
import rsexts;
import "globals.mac", rsd, oralib, FIInter;
Import FMInter, PaymInter;
Import CTInter, cb_sql;
Import BankInter;

file fmved(fmvedparty) write;

Class VedPartyInserterWraper
    var T_NAME    = TArray;
    var T_OGRN    = TArray;
    var T_INN     = TArray;
    var T_REGDATE = TArray;
    var T_GROUP   = TArray;
    
    macro Clear()
        T_NAME.size    = 0;
        T_OGRN.size    = 0;
        T_INN.size     = 0;
        T_REGDATE.size = 0;
        T_GROUP.size   = 0;
    end;
    
    macro Size()
        return T_NAME.size;
    end;
    
    macro ClearTmp()
        var cmd = RsdCommand("delete from dfmvedparty_tmp");
        cmd.execute();
    end;
    
    macro ClearDbt()
        var cmd = RsdCommand("delete from dfmvedparty_dbt");
        cmd.execute();
    end;
    
    macro AddToCache(Name : string, ORGN : string, INN : string, RegDate : date, Group : integer)
        var i = Index(ORGN, ".");
        if (i != 0)
            ORGN = SubStr(ORGN, 1, i - 1);
        end;
        i = Index(INN, ".");
        if (i != 0)
            INN = SubStr(INN, 1, i - 1);
        end;
        
        T_NAME[T_NAME.size]       = Name;
        T_OGRN[T_OGRN.size]       = ORGN ;
        T_INN[T_INN.size]         = INN;
        T_REGDATE[T_REGDATE.size] = RegDate;
        T_GROUP[T_GROUP.size]     = Group;
    end;
    
    private macro InsertToVedTable(TableName : string)
        var query = "insert into " + TableName + "(T_NAME,T_OGRN,T_INN,T_REGDATE,T_GROUP) values(?,?,?,?,?)";
        var inserter = RsbSQLInsert(query, 5, T_NAME.size);
        inserter.AddParam(V_STRING, T_NAME, 71);
        inserter.AddParam(V_STRING, T_OGRN, 14);
        inserter.AddParam(V_STRING, T_INN, 11);
        inserter.AddParam(V_DATE, T_REGDATE);
        inserter.AddParam(V_INTEGER, T_GROUP);
        inserter.Insert();
    end;
    
    macro InsertToVedTempTable()
        InsertToVedTable("dfmvedparty_tmp");
    end;
    
    macro InsertToVedDbtTable()
        InsertToVedTable("dfmvedparty_dbt");
    end;
End;

var fmvedpartyInserter : VedPartyInserterWraper = VedPartyInserterWraper();

private var FlagUpdate = false;
private var FlagDelete = false;

private macro VedPartyImportTrn()
    var cmd;
    if ((not FlagUpdate) and (not FlagDelete))
        fmvedpartyInserter.ClearDbt();
        fmvedpartyInserter.InsertToVedDbtTable();
    elif ((FlagUpdate == true) and (not FlagDelete))
        fmvedpartyInserter.ClearTmp();
        fmvedpartyInserter.InsertToVedTempTable();
        
        cmd = RsdCommand(
        "MERGE INTO DFMVEDPARTY_DBT T1 " + 
        "     USING (SELECT T_NAME, " + 
        "                   T_REGDATE, " + 
        "                   T_GROUP, " + 
        "                   T_INN, " + 
        "                   T_OGRN " + 
        "              FROM DFMVEDPARTY_TMP) T2 " + 
        "        ON (T2.T_INN = T1.T_INN AND T2.T_OGRN = T1.T_OGRN) " + 
        "WHEN MATCHED " + 
        "THEN " + 
        "   UPDATE SET " + 
        "      T1.T_NAME = T2.T_NAME, " + 
        "      T1.T_REGDATE = T2.T_REGDATE, " + 
        "      T1.T_GROUP = T2.T_GROUP " + 
        "WHEN NOT MATCHED " + 
        "THEN " + 
        "   INSERT     (T1.T_NAME, T1.T_REGDATE, T1.T_GROUP, T1.T_INN, T1.T_OGRN) " + 
        "       VALUES (T2.T_NAME, T2.T_REGDATE, T2.T_GROUP, T2.T_INN, T2.T_OGRN)");
        cmd.execute();
    elif ((not FlagUpdate) and (FlagDelete == true))
        fmvedpartyInserter.ClearTmp();
        fmvedpartyInserter.InsertToVedTempTable();
        
        cmd = RsdCommand(
        "DELETE FROM DFMVEDPARTY_DBT T1 " + 
        "  WHERE (T1.T_OGRN, T1.T_INN) IN " + 
        "           (SELECT T_OGRN, T_INN FROM DFMVEDPARTY_TMP)");
        cmd.execute();
    end;
end;

private macro WriteVedPartyImportProtocol(ImportFilePath,FileDate,RenewDate,bAfterCheck)
[Протокол процедуры.
];
    if ((not FlagUpdate) and (not FlagDelete))
[Импорт Перечня подозрительных участников ВЭД

];
    elif ((FlagUpdate == true) and (not FlagDelete))
[Импорт списка дополнений к ранее направленным Перечням

];
    elif ((not FlagUpdate) and (FlagDelete == true))
[Импорт списка исключений из ранее направленных Перечней

];
    end;
[Дата, время:	##########, ########
Пользователь:   ##########  ########################################################

Дата загружаемого файла: ##########
Дата последнего обновления: ##########
Файл импорта: #
](Date(), Time(), {oper}, {Name_Oper}, FileDate, RenewDate, ImportFilePath);//импорта Перечня подозрительных участников ВЭД

	if (bAfterCheck)
		[
		Проверка субъектов после обновления выполнялась.
		];
	else
		[
		Проверка субъектов не выполнялась.
		];
	end;
[Процедура импорта завершена успешно.];

end;

private macro GetQueryValue(sql, column)
	var cmd = RsdCommand(sql);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(column);
end;

private macro WriteVedPartyCheckProtocolBody(bOpenOnly, PartyKinds, PartyKindSign, ServDateFrom, PartyID, sql)
[Дата, время:	##########, ########
Пользователь:   ############  ########################################################

](Date(), Time(), {oper}, {Name_Oper});

	var check_open : string = "Нет";
	if (bOpenOnly)
		check_open = "Да";
	end;
	
	if ((ValType(PartyID) != V_UNDEF) and (PartyID > 0))
		var val1 = GetQueryValue("SELECT T_CODE from DPARTCODE_DBT WHERE T_CODEKIND = 1 AND T_PARTYID = " + PartyID, 0);
		var val2 = GetQueryValue("SELECT T_NAME from DPARTY_DBT WHERE T_PARTYID = " + PartyID, 0);
	
[Субъект: ########### ########################################################
](val1, val2);
	else
		var objs : string = "Все виды";
		if ((ValType(PartyKinds) == V_GENOBJ) and (PartyKinds.size > 0))
			if (PartyKinds[0] == -1)
				objs = "Все виды";
			else
				objs = "Cписок";
			end;
		end;
		
		if (ValType(PartyKindSign) == V_UNDEF)
			PartyKindSign = "=";
		end;
[Вид субъекта: # ########################################################
Проверять только открытых субъектов: #####
](SubStr(PartyKindSign, 1, 1), objs, check_open);
	end;
	
	if (ValType(ServDateFrom) != V_UNDEF)
[Приняты на обслуживание с:  ###########
](ServDateFrom);
	end;
	
	var cmd = RsdCommand("SELECT " + "t3.T_NAME as PNAME, T2.T_NUMBER, T2.T_NAME, T2.T_GROUP, T1.T_MATCHEDBY, T1.T_INN, T1.T_OGRN " + sql);
	cmd.execute();	
	var rs = RsdRecordset(cmd);
[
+-----------------------------------+---------------------------------------------+--------+-------------------------+
|                                   |         Подозрительный участник ВЭД         |        |                         |
|              Субъект              +---------+-----------------------------------+ Группа | Информация о совпадении |
|                                   |  Номер  |           Наименование            |        |                         |
+-----------------------------------+---------+-----------------------------------+--------+-------------------------+
];
	while (rs.MoveNext())
		var match_by : integer = rs.value("T_MATCHEDBY");
		var buf_str : string = "";
		
		if (match_by == 1)
			buf_str = "ИНН " + rs.value("T_INN");
		else
			buf_str = "ОГРН  " + rs.value("T_OGRN");
		end;
[| ################################# | ####### | ################################# | ###### | ####################### |]
(rs.value("PNAME"):w, rs.value("T_NUMBER"):r, rs.value("T_NAME"):w, rs.value("T_GROUP"):r, buf_str);
[+-----------------------------------+---------+-----------------------------------+--------+-------------------------+];
	end;
end;

private macro WriteVedPartyCheckProtocol(bOpenOnly, PartyKinds, PartyKindSign, ServDateFrom, PartyID)
[Протокол Процедуры проверки субъектов по Перечню подозрительных участников ВЭД

];
	var sql = "FROM DFMVEDPTCHECK_TMP t1, DFMVEDPARTY_DBT t2, DPARTY_DBT t3 WHERE T1.T_MATCHEDBY <> 0 " + 
		"AND (T2.T_INN = T1.T_INN OR T2.T_OGRN = T1.T_OGRN) AND T1.T_PARTYID = T3.T_PARTYID";
	
	var match_count : integer = GetQueryValue("SELECT COUNT(*) " + sql, 0);
	
	if (match_count > 0)
		WriteVedPartyCheckProtocolBody(bOpenOnly, PartyKinds, PartyKindSign, ServDateFrom, PartyID, sql);
[Процедура проверки субъектов успешно завершена.];
[Найдено совпадений: ########
](match_count);
	else 
[Процедура проверки субъектов успешно завершена.];
[Не найдено совпадение субъектов с данными Перечня подозрительных участников ВЭД.
];
	end;
end;

private macro AddCondition(sql, condition)
	var out;
	
	if (Index(sql, "WHERE") == 0)
		out = " WHERE " + condition;
	else
		out = sql + " AND " + condition;
	end;
	
	return out;
end;

macro VedPartyCheck(
	bOpenOnly,       //признак проверки открытых субъектов
	PartyKinds,      //массив видов субъекта
	PartyKindSign,   //логическое условие для PartyKinds, по умолчанию имеет значение "=".
	ServDateFrom,    //дата принятия на обслуживание
	PartyID          //идентификатор субъекта экономики
	)
	var tables = "DPARTY_DBT pt";
	var sql = "";
	var cmd2 = RsdCommand("delete from dfmvedptcheck_tmp");
	cmd2.execute();

	if ((ValType(PartyID) != V_UNDEF) and (PartyID > 0))
		sql = AddCondition(sql, "pt.T_PARTYID = " + PartyID);
	else
		if (bOpenOnly)
			sql = AddCondition(sql, "pt.T_LOCKED = chr(0)");
		end;

		if ((ValType(ServDateFrom) != V_UNDEF) and (ServDateFrom != ""))
			tables = tables + ", DCLIENT_DBT cl";
			sql = AddCondition(sql, "cl.T_PARTYID (+)= pt.T_PARTYID");
			
			sql = AddCondition(sql, "(cl.T_STARTDATE >= '" + String(ServDateFrom) + "' OR cl.T_STARTDATE = '01.01.0001')");
		end;
		
		if ((ValType(PartyKinds) == V_GENOBJ) and (PartyKinds.size > 0))
			if (PartyKinds[0] > 0)
				var index = 0;
				tables = tables + ", DPARTYOWN_DBT ptow";

				sql = AddCondition(sql, "pt.T_PARTYID = ptow.T_PARTYID");
				
				var condition = "ptow.T_PARTYKIND ";
				
				if (SubStr(PartyKindSign, 1, 1) == "!")
					condition = condition + "NOT ";
				end;
				
				condition = condition + "IN(";
				
				while(index < PartyKinds.size)
					if (index != 0)
						condition = condition + ",";
					end;
					
					condition = condition + PartyKinds[index];
					index = index + 1;
				end;
				
				condition = condition + ")";
				sql = AddCondition(sql, condition);
			else 
				if (SubStr(PartyKindSign, 1, 1) == "!")//'!' все виды
					WriteVedPartyCheckProtocol(bOpenOnly, PartyKinds, PartyKindSign, ServDateFrom, PartyID);
					return; 
				end;
			end;
		end;
	end;
	sql = "SELECT DISTINCT pt.T_PARTYID, RSI_RSBPARTY.PT_GetPartyCode (pt.T_PARTYID, 16), " +
		"RSI_RSBPARTY.PT_GetPartyCode (pt.T_PARTYID, 27), 0, 0 FROM " + tables + " " + sql;
	var cmd = RsdCommand(sql);
	
	var not_empty : BOOL = false;
	
	/*
		Перенести полученный список идентификаторов во временную таблицу fmvedptcheck.tmp: заполнить поле PartyID,
		получить ИНН и ОГРН для каждого отобранного субъекта, использую функцию RSI_RSBPARTY.PT_GetPartyCode
	*/
	cmd2.CmdText = "INSERT INTO DFMVEDPTCHECK_TMP(T_PARTYID,T_INN,T_OGRN,T_MATCHEDBY,T_VEDPTNUMBER)" + sql;
	cmd2.execute();
	
	/*
		Проверить, есть ли совпадения среди отобранных субъектов (записи fmvedptcheck.tmp) 
		с подозрительными участниками  (ПУ) ВЭД (записи таблицы fmvedparty.dbt) по ИНН
	*/
	cmd2.CmdText = "merge into DFMVEDPTCHECK_TMP t1 " +
		"using (select * from DFMVEDPARTY_DBT) t2 " +
		"ON (t1.T_INN = t2.T_INN) " +
		"WHEN MATCHED THEN " +
		"UPDATE SET t1.T_VEDPTNUMBER = t2.T_NUMBER, t1.T_MATCHEDBY = 1";
	cmd2.execute();
	
	/*
		Проверить, есть ли совпадения среди отобранных субъектов, для которых нет совпадения по ИНН, 
		(записи fmvedptcheck.tmp, у которых значение MatchedBy равно 0) с подозрительными участниками ВЭД по ОГРН
	*/
	cmd2.CmdText = "MERGE INTO DFMVEDPTCHECK_TMP t1 " + 
		"USING (SELECT * FROM DFMVEDPARTY_DBT) t2 " + 
		"ON (t1.T_OGRN = t2.T_OGRN) " + 
		"WHEN MATCHED THEN " +
		"UPDATE SET t1.T_VEDPTNUMBER = t2.T_NUMBER, t1.T_MATCHEDBY = 2 " + 
		"WHERE t1.T_MATCHEDBY = 0";
	cmd2.execute();
	
	//Получить список отобранных субъектов, для которых не было найдено совпадений с ППУ ВЭД и у которых задана категория "Подозрительный участник ВЭД"
        cmd2.CmdText = "SELECT DISTINCT t1.T_PARTYID FROM DFMVEDPTCHECK_TMP t1, DOBJATCOR_DBT t2 where T1.T_MATCHEDBY = 0 AND T2.T_OBJECTTYPE = 3 AND T2.T_OBJECT = TRIM(TO_CHAR(T1.T_PARTYID, '0000000000')) AND T2.T_GROUPID = 67";
	cmd2.execute();
	var rs = RsdRecordset(cmd2);
	
	var Error  : integer = 0;
	var stat : bool = false;
	var attr : integer = 3;
	record party(party);
	
	while (rs.MoveNext())
		party.PartyID = rs.value(0);
                stat = GetMainObjAttr(Error, OBJTYPE_PARTY, party, 67, attr);
		
		//Если значение категории равно 1 или 2 (<Да>), то, установить значение 3 (<Нет>)
		if (stat and ((attr == 1) or (attr == 2)))
                        ConnectObjAttr(Error, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 67, 3);
		end;
	end;
	
	//Получить список отобранных субъектов - записей fmvedptcheck.tmp, для которых найдены совпадения с ППУ ВЭД
	cmd2.CmdText = "SELECT * FROM DFMVEDPTCHECK_TMP t1, DFMVEDPARTY_DBT t2 WHERE T1.T_MATCHEDBY <> 0 AND (T2.T_INN = T1.T_INN OR T2.T_OGRN = T1.T_OGRN)";
	cmd2.execute();
	rs = RsdRecordset(cmd2);
	
	while (rs.MoveNext())
		var d : Date = ServDateFrom;
		party.PartyID = rs.value(0);
                stat = GetMainObjAttr(Error, OBJTYPE_PARTY, party, 67, attr, 0, 0, d);
		
		//Если такое значение категории у субъекта отсутствует или оно равно 3 (<Нет>), то
		if ((not stat) or (attr == 3))
			/*
			если ПУ ВЭД fmvedptcheck.VedPtNumber относится к группе 1 (fmvedparty.Group = 1), 
			то задать для субъекта новый действующий с даты опер. дня филиала признак категории 3.2.1 
			со значением = 1 (<Да (группа 1)>)
			*/
			if (rs.value("T_GROUP") == 1)
                                ConnectObjAttr(Error, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 67, 1);
			else
				/*
				иначе если ПУ ВЭД fmvedptcheck.VedPtNumber относится к группе 2 (fmvedparty.Group = 2), 
				то задать для субъекта новый действующий с даты опер. дня филиала признак категории 3.2.1 
				со значением = 2 (<Да (группа 2)>)
				*/
                                ConnectObjAttr(Error, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 67, 2);
			end;
		elif (stat)//Иначе (значение категории задано и оно не равно 3)
			/*
			Если значение категории не соответствует номеру групп ПУ ВЭД (т.е. а) значение категории равно 1, 
			а номер группы fmvedparty.Group = 2 или б) значение категории равно 2, а номер группы fmvedparty.Group = 1), 
			то изменить значение категории на соответствующее группе
			*/
			if (attr != rs.value("T_GROUP"))
                                ConnectObjAttr(Error, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 67, rs.value("T_GROUP"));
			end;
		end;
	end;
	
	WriteVedPartyCheckProtocol(bOpenOnly, PartyKinds, PartyKindSign, ServDateFrom, PartyID);
end;

/*
	Проверить соответствуют ли названия столбцов таблицы (значения ячеек A2:F2) 
	названиям столбцов таблицы из шаблонного файла
*/
private macro CheckMainVedListHeaders(ActiveSheet)
    var index = 1;
	var sample_heads = TArray(6, 1);
	sample_heads[0] = "№ п/п";
	sample_heads[1] = "Наименование резидента";
	sample_heads[2] = "ОГРН";
	sample_heads[3] = "ИНН";
	sample_heads[4] = "Дата регистрации";
	sample_heads[5] = "Группа";
	while(index <= 6)
		var value = ActiveSheet.Cells(2, index).Value;
		if (value != sample_heads[index - 1])
			return 1;
		end;
		index = index + 1;
	end;
    
    return 0;
end;

private macro CheckAdditionsVedListHeaders(ActiveSheet)
    var index = 1;
	var sample_heads = TArray(6, 1);
	sample_heads[0] = "№ п/п";
	sample_heads[1] = "Наименование резидента";
	sample_heads[2] = "ОГРН";
	sample_heads[3] = "ИНН";
	sample_heads[4] = "Дата регистрации";
	sample_heads[5] = "Группа";
	while(index <= 6)
		var value = ActiveSheet.Cells(2, index).Value;
		if (value != sample_heads[index - 1])
			return 1;
		end;
		index = index + 1;
	end;
    
    return 0;
end;

private macro CheckExceptionsVedListHeaders(ActiveSheet)
    var index = 1;
	var sample_heads = TArray(7, 1);
	sample_heads[0] = "№ п/п";
	sample_heads[1] = "Наименование резидента";
	sample_heads[2] = "ОГРН резидента";
	sample_heads[3] = "ИНН резидента";
	sample_heads[4] = "Дата направления Перечня";
	sample_heads[5] = "Наименование файла Перечня";
    sample_heads[6] = "Дата направления Списка исключений";
    
	while(index <= 7)
		var value = ActiveSheet.Cells(2, index).Value;
		if (value != sample_heads[index - 1])
			return 1;
		end;
		index = index + 1;
	end;
    
    return 0;
end;

private macro ProcessVedPartyFile(ActiveSheet)
    var index = 3;
    // две строки на заголовки
	InitProgress(ActiveSheet.UsedRange.Rows.Count - 2, "", "Чтение перечня подозрительных участников ВЭД");
    
    while (index <= ActiveSheet.UsedRange.Rows.Count)
        var stat = 0;
		if ((ValType(ActiveSheet.Cells(index, 1).Value) == V_UNDEF) or ((ValType(ActiveSheet.Cells(index, 2).Value) == V_UNDEF)  AND (ValType(ActiveSheet.Cells(index, 2).Value) == V_UNDEF )))
			stat = 1;
		end;
		
		if (stat == 0)
            fmvedpartyInserter.AddToCache
            (
                ActiveSheet.Cells(index, 2).Value,
                String(ActiveSheet.Cells(index, 3).Value),
                String(ActiveSheet.Cells(index, 4).Value),
                Date(ActiveSheet.Cells(index, 5).Value),
                Int(ActiveSheet.Cells(index, 6).Value)
            );
		end;
        
        UseProgress(index);
		index = index + 1;
    end;
    
    if (ProcessTrn(0,"VedPartyImportTrn",fmved))
	end;
end;

macro VedPartyImport(ImportFilePath,FileDate,RenewDate,bUpdate,bDelete,bAfterCheck)
  var ExcelApplication;
  var startAX;
  var strFile, strExt;

  SplitFile(ImportFilePath, strFile, strExt);

  if (isStandAlone())
    ExcelApplication = ActiveX("Excel.Application", null, true);
  else
    if (startAX == null)
      startAX = CreateObject("rsax", "TRsAxServer", "FmAxServer", isStandalone());
    end;
    ExcelApplication = startAX.CreateComObject("Excel.Application");
  end;
      
  var stat : integer = 0;
  FlagUpdate = bUpdate;
  FlagDelete = bDelete;
  var file_path = ImportFilePath;

  if(not IsStandAlone) /*Трехзвенка*/
    file_path = CallRemoteRsl("dlwrept.mac", "DLREPTL_GetTxtFileDir") + strFile + UserNumber + strExt;

    if (not CopyFile(ImportFilePath, "$" + file_path))
      msgbox ("Ошибка при передаче файла на терминал");
      return false;
    end;
  end;

  var ExcelDoc = ExcelApplication.Workbooks.Open(file_path);
  ExcelDoc.Sheets(1).Activate();
  var ActiveSheet = ExcelApplication.ActiveSheet;
  
  if ((bUpdate == false) and (bDelete == false))
      stat = CheckMainVedListHeaders(ActiveSheet);
  elif ((bUpdate == true) and (bDelete == false))
      stat = CheckAdditionsVedListHeaders(ActiveSheet);
  elif ((bUpdate == false) and (bDelete == true))
      stat = CheckExceptionsVedListHeaders(ActiveSheet);
  end;
  
  if (not stat)
      fmvedpartyInserter.Clear();
      ProcessVedPartyFile(ActiveSheet);
  else
      MsgBox ("Процедура импорта не выполнена: неверный формат данных");
      return 1;
  end;
      
  WriteVedPartyImportProtocol(ImportFilePath,FileDate,RenewDate,bAfterCheck);
      
  if (bAfterCheck)
    VedPartyCheck(true);
  end;
  
  ExcelApplication.Quit();
  return stat;
end;