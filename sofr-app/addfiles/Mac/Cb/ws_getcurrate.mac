/*
 $Name: ws_getcurrate.mac
 $Module: Главная книга
 $Description: Сервис получения текущего курса валют
*/

import rsd;
import globals;
import "ws_lib_fun.mac";

class TWsGetCurRateRequest
  var isoCurrencyFrom:String;    // Валюта, из которой переводим (базовая). Символьный ISO код валюты, например: "USD".
  var isoCurrencyTo:String;	 // Валюта, в которую переводим (котируемая). Символьный ISO код валюты.
  var onDate:Date;               // Дата, на которую действует курс.
  var type:Integer;              // Тип внешнего курса.
end;

class TWsGetCurRateResponse
  var valueRate:Money;                 // Значение курса, полученное расчетом из системных значений
  var Rate:Money;                      // Системное значение курса
  var Scale:Integer;                   // Системный масштаб курса
  var Point:Integer;                   // Системная точность курса
  var IsInverse:Bool;                  // Системный признак обратного курса
  
  valueRate = 0;
  Rate = 0;
  Scale = 0;
  Point = 0;
  IsInverse = 0;
end;

private macro FillRequest (oRequest, wsRequest : TWsGetCurRateRequest)
  
  var ParamN     : integer;
  var ObjectName : string;

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue(@wsRequest.isoCurrencyFrom, ParamN, ObjectName, "isoCurrencyFrom", oRequest, V_STRING,  false, "");
  WS_SetAttributeValue(@wsRequest.isoCurrencyTo,   ParamN, ObjectName, "isoCurrencyTo",   oRequest, V_STRING,  false, "");
  WS_SetAttributeValue(@wsRequest.onDate,          ParamN, ObjectName, "onDate",          oRequest, V_DATE,    false, null);
  WS_SetAttributeValue(@wsRequest.type,            ParamN, ObjectName, "type",            oRequest, V_INTEGER, false, null);

end;

/* Сервис "Получение текущего курса валюты" */
macro GetCurRate( Request )
  
  var wsRequest : TWsGetCurRateRequest;
  var result : TArray = TArray();
  var cmd, rs;
  var quotedFIID : Integer;
  var baseFIID : TArray = TArray();
  var i, currencyCount : Integer;
  var iso_number;
  var stat : Integer = 0;

  record fininstr("fininstr.dbt");

  WS_CheckParameter( 1, Request, false, V_GENOBJ );  
  wsRequest = TWsGetCurRateRequest;
  FillRequest( Request, wsRequest );
  
  if ( ( valtype (wsRequest.type) == V_INTEGER ) and (wsRequest.type != 0) )
    cmd = RsdCommand( " select * from dratetype_dbt " +     
                      " where " +
                      " t_type = :type " );
    rs = RsdRecordset( cmd );
    cmd.addParam( "type", RSDBP_IN, wsRequest.type );
    cmd.execute;
    if ( not rs.moveNext() )
      RunError("Вид курса не найден", -1);  
    end;
  end;

  if ( ( not (valtype (wsRequest.isoCurrencyTo) == V_STRING ) ) or (wsRequest.isoCurrencyTo == "" ) )
    quotedFIID = NATCUR;
  else
    if ( not ПолучитьФинИнПоISO( wsRequest.isoCurrencyTo, fininstr) )
      RunError("Котируемая валюта не найдена", -1);   
    else
      quotedFIID = fininstr.FIID;
    end;
  end; 

  if ( not (valtype ( wsRequest.isoCurrencyFrom ) == V_STRING ) or ( wsRequest.isoCurrencyFrom == "" ) )
    cmd = RsdCommand( " select t_fiid from dfininstr_dbt " +     
                      " where " +
                      " t_fiid <> :fiid " +
                      " and " +
                      " t_fi_kind = :fi_kind" );
    rs = RsdRecordset( cmd );
    cmd.addParam( "fiid", RSDBP_IN, quotedFIID );
    cmd.addParam( "fi_kind", RSDBP_IN, FIKIND_CURRENCY );
    cmd.execute;  
    currencyCount = 0;
    while ( rs.moveNext() )
      iso_number = rs.value("t_fiid");
      baseFIID[currencyCount] = iso_number;
      currencyCount = currencyCount + 1;
    end;
  else
    if ( not ПолучитьФинИнПоISO( wsRequest.isoCurrencyFrom, fininstr ) )
      RunError("Базовая валюта не найдена", -1);   
    else
      baseFIID[0] = fininstr.FIID;
      currencyCount = 1;
    end;
  end;

  if ( valtype(wsRequest.onDate) != V_DATE )
    wsRequest.onDate = {curdate};
  end;    
  
  i = 0;
  while ( i < currencyCount )
    var Responce : TWsGetCurRateResponse = TWsGetCurRateResponse;
    if ( ( valtype ( wsRequest.type ) == V_INTEGER ) and ( wsRequest.type != 0 ) )
      stat = FI_GetRateOnDate(quotedFIID,baseFIID[i],wsRequest.onDate,wsRequest.type,Responce.Rate,Responce.Scale,Responce.Point,Responce.IsInverse);
    else
      stat = FI_GetRateOnDate(quotedFIID,baseFIID[i],wsRequest.onDate,0,Responce.Rate,Responce.Scale,Responce.Point,Responce.IsInverse);
    end;
    if ( stat == 0 )
      if (Responce.IsInverse)
        Responce.valueRate = (Responce.Scale * (Pow (10, Responce.Point))) / Responce.Rate;
      else
        Responce.valueRate = (Responce.Rate / Responce.Scale) * Pow(0.1, Responce.Point);
      end;
      result[result.size] = Responce;
    end;
    i = i + 1;
  end;

  if ( result.size == 0 ) 
    result = NULL;
  end;

  return result;

end;

