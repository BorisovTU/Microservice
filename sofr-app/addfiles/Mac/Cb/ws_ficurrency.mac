/*
        $Name: ws_ficurrency.mac
        $Module: WEB - Общее
        $Description: Макрос для реализации работы Web-интерфейса.
*/
/*
        
*/

import rcw;
import rsd, cb_sql;
import rsbdataset;
import PTInter;
import FIInter;
import "ws_validation.mac";
import "ws_ficlass.mac";
import "globals.mac";
import "fi.mac";

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

macro GetISOCurrencyList(ISONumCode:string, ISONumCodeLk:string, listLen)

   var p;
   var result = TArray();

   var query = "SELECT t_NameCurr, t_NumberCode, t_ISOCode, t_IsRevoked  FROM disocur_dbt WHERE 1=1 ";
     
   if(ISONumCode != NULL)
     query = query + " AND t_NumberCode = " + "'" + string(ISONumCode) + "'";
   end;
   
   if( ISONumCodeLk != NULL )
     query = query + " AND LOWER (t_NumberCode) LIKE LOWER ('%" + ISONumCodeLk + "%') ";
   end; 

   query = query + " ORDER BY t_isocode, t_num"; 
      
   if( listLen != 0 )
     query = "select * from ( " + query + " ) where ROWNUM < " + string(listLen);
   end;    
   
   println(query);
   
   var Revoked;

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

   while (ds.MoveNext())
      if(ds.IsRevoked == "X")
        Revoked = true;
      else
        Revoked = false;
      end;

      p = TWsIsoCur(ds.NameCurr, ds.NumberCode, ds.ISOCode, Revoked) ;

      result.value(result.Size) = p;
   end;

   return result;
end;

macro GetTWsIsoCurByNumCode(ISONumCode:string) : TWsIsoCur

  var result;

  result = GetISOCurrencyList(ISONumCode, NULL, 0);
  
  return result[0];

end;


macro getServiceError(errorCode, errorMessage, errorHelpPage, userObject)
    var responseBuilder = WebResponseBuilder;
    var error           = WsErrorMessage;

    if( errorCode != 0 )
      error.Severity  = MessageSeverity.ValidationError;
      error.Code      = errorCode;
      error.Message   = errorMessage;
      error.Help      = errorHelpPage;

      responseBuilder.AddMessage(error); 
    end;

    responseBuilder.SetUserObject(userObject); 

    return responseBuilder.Result;
end;

private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                          DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                          CodeKind:integer       /* Название таблицы c . */
                        ):String
                        
  var SortStr:string = "";

  if( (OrderItems == null) or (OrderItems.Size() <= 0) )
  
     SortStr = " " + DefaultSortStr + " ";
     
  else
  
     var i:integer = 0;
     
     while( i < OrderItems.size() )
     
        if(OrderItems[i].Column == "GetCode")
        
          if(CodeKind == 1)
            OrderItems[i].Column = "ficode";
          else
            if(CodeKind == 2)
              OrderItems[i].Column = "fiisonum";
            else
              if(CodeKind == 3)
                OrderItems[i].Column = "ficcy";
              else
                if(CodeKind == 4)
                  OrderItems[i].Column = "lsin";
                else
                  if(CodeKind == 5)
                    OrderItems[i].Column = "isin";
                  else
                    if(CodeKind == 6)
                      OrderItems[i].Column = "ficodeinacc";
                    else
                      OrderItems[i].Column = "code";                     
                    end;                   
                  end;                  
                end;               
              end;             
            end;          
          end;
          
        else
          if(OrderItems[i].Column == "Fi.Name")
            OrderItems[i].Column = "finame";
          else
            if(OrderItems[i].Column == "Fi.Fiid")
              OrderItems[i].Column = "fiid";
            else
              if(OrderItems[i].Column == "StrRateValue")
                OrderItems[i].Column = "rate";
              else
                if(OrderItems[i].Column == "StrRateSinceDate")
                  OrderItems[i].Column = "sincedate";
                else
                  OrderItems[i].Column = "fiid";
                end;                 
              end;               
            end;            
          end;
        end;
  
        i = i + 1;
     end;  
  
     SortStr = " " + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");
     i = 1;
     while( i < OrderItems.size() )
        SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
        i = i + 1;
     end;
     SortStr = SortStr + " ";
     
  end;

  return SortStr;
  
end;

MACRO GetFIList(pagesize            : integer,
                pagenum             : integer,
                codekind            : integer,
                fiid                : integer,
                orderitems )
   var p;
  
   var result = TArray();
   var query = " SELECT fi.t_fiid AS fiid, " +
               "        fi.t_fi_code AS ficode, " +
               "        fi.t_fi_kind AS fikind, " +
               "        fi.t_name AS finame, " +
               "        fi.t_iso_number AS fiisonum, " +
               "        fi.t_ccy AS ficcy, " +
               "        fi.t_codeinaccount AS ficodeinacc, " +
               "        fi.t_definition AS fidef, " +
               "        av.t_isin AS isin, " +
               "        av.t_lsin AS lsin, " +
               "        NVL(rd.t_rate/power(10,rd.t_point), 0) AS rate, " +
               "        NVL(rd.t_sincedate, TO_DATE('01010001')) AS sincedate, " +
               "        NVL (rd.t_isDominant, CHR(0)) AS IsRateDominant, " +
               "        (SELECT t_Code " +
               "           FROM dobjcode_dbt oc1 " +
               "          WHERE     oc1.t_objecttype = 9 " +
               "                AND oc1.t_objectid = fi.t_fiid " +
               "                AND oc1.t_bankclosedate = TO_DATE ('01.01.0001', 'dd.mm.yyyy') " +
               "                AND oc1.t_autokey IN " +
               "                       (SELECT MIN (oc2.t_autokey) " +
               "                          FROM dobjcode_dbt oc2 " +
               "                         WHERE     oc2.t_objecttype = oc1.t_objecttype " +
               "                               AND oc2.t_codekind = oc1.t_codekind " +
               "                               AND oc2.t_objectid = oc1.t_objectid) ";

  if(ValType(codekind) == V_INTEGER)
    query = query + " AND t_CodeKind = " + string(codekind);
  end;

  query = query + "          ) code " +
               "   FROM dfininstr_dbt fi " +
               "        LEFT JOIN davoiriss_dbt av " +
               "           ON fi.t_fiid = av.t_fiid " +
               "        LEFT JOIN dratedef_dbt rd " +
               "           ON  (( fi.t_fiid = rd.t_otherfi AND rd.t_fiid = 0) " +
               "    OR (fi.t_fiid = rd.t_fiid AND rd.t_otherfi = 0)) AND t_IsDominant = 'X'" +
               "  WHERE fi.t_FIID >= 0 AND fi.t_fi_kind = 1 AND fi.t_IsClosed <> 'X' ";
               
  if((ValType(fiid) == V_INTEGER) AND (fiid >= 0))
    query = query + " AND fi.t_FIID = " + string(fiid);
  end;

  if(ValType(orderitems) != V_UNDEF)
   query = query + " ORDER BY " + GetSortStr(orderitems, "ficode", codekind);
  end;

  if((ValType(pagesize) == V_INTEGER) AND (pagesize >= 0))  
    query = SQL_WrapSelect( query, pagenum, pagesize);
  else
    query = SQL_WrapSelect( query, 0, 0);
  end;

   println("*** GetFIList ***");
   println(query);               

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

   var queryc;
   var dsc ;

   while (ds.MoveNext())
      p = TWsCurrency();

      p.Fi = TWsFinInstr(ds.fiid, ds.ficode, ds.ficode, ds.fikind, 0, ds.finame, ds.fiisonum, ds.ficcy, ds.ficodeinacc, ds.fidef);
      p.Lsin = ds.Lsin; 
      p.Isin = ds.Isin;
      
      var IsRateDominant = false;

      if (ds.IsRateDominant == "X")
        IsRateDominant = true;
      end;

      p.Rate = TWsFIActualRate(ds.rate, ds.sincedate, IsRateDominant);

      p.IsoCur = GetTWsIsoCurByNumCode(ds.fiisonum);
      
      p.CodeKind = codekind;
      
      if( ds.code != NULL )
        p.ObjCodes = TArray(); 

        p.ObjCodes.value(p.ObjCodes.Size) = TWsCurObjCode(codekind, ds.code);
      end;

      result.value(result.Size) = p;
   end;

   return result;
END;

macro GetFICount():integer

  var result : integer;

   var query = " SELECT COUNT(*) C"
               " FROM dfininstr_dbt fi, davoiriss_dbt av "
               "  WHERE fi.t_fiid = av.t_fiid(+) "
               "     AND fi.t_FIID >=0  AND fi.t_fi_kind = 1 AND fi.t_IsClosed <> 'X' ";
  
  println("*** GetFICount ***");
  println(query);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())

    result = ds.c;

  end;

  return result;

end;

macro GetCountryList(NumberCode:string)
   var p;
   var result = TArray();
   var query = "SELECT cur_cntr.t_numbercode as numcode, cur_cntr.t_codenum as codenum, cntr.t_name as cname" +
               "  FROM dcountry_dbt cntr, dcur_cntr_dbt cur_cntr " +
               " WHERE cntr.t_codenum3 = cur_cntr.t_codenum " +
               "   AND cur_cntr.t_numbercode =  '" + NumberCode + "'" +
               " ORDER BY cur_cntr.t_codenum";

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

   while (ds.MoveNext())
      p = TWsCurCountry(ds.numcode, ds.codenum, ds.cname) ;

      result.value(result.Size) = p;
   end;

   return result;
end;




macro FI_SaveCurrency
(
  newCurrency,  // объект класса TWsFinInstr
  savedCurrency // 
)
  var errmsg : string;
  var stat = 0;
  file fininstr ("fininstr.dbt") write key 0; // fininstr
  ClearRecord(fininstr);

  file fininstr_save ("fininstr.dbt") write key 0; // fininstr
  ClearRecord(fininstr_save);

  if(newCurrency.Fiid == -1 )
     fininstr.fiid          = newCurrency.Fiid         ;
     fininstr.fi_kind       = newCurrency.Kind         ;

     if(ValType(newCurrency.CodeWidget) != V_UNDEF)
     fininstr.fi_code       = newCurrency.CodeWidget   ;
     else     
       fininstr.fi_code = "";
     end;
     
     if(ValType(newCurrency.ISO_Number) != V_UNDEF)
     fininstr.iso_number    = newCurrency.ISO_Number   ;
     else     
       fininstr.iso_number = "";
     end;

     if(ValType(newCurrency.CCY) != V_UNDEF)
     fininstr.ccy           = newCurrency.CCY          ;
     else     
       fininstr.ccy = "";
     end;

     if(ValType(newCurrency.Name) != V_UNDEF)
       fininstr.name = newCurrency.Name;
     else     
       fininstr.name = "";
     end;

     if(ValType(newCurrency.CodeInAccount) != V_UNDEF)
     fininstr.codeinaccount = newCurrency.CodeInAccount;
     else     
       fininstr.codeinaccount = "";
     end;

     if(ValType(newCurrency.Definition) != V_UNDEF)
     fininstr.definition    = newCurrency.Definition   ;
     else     
       fininstr.definition = "";
     end;
    
     stat = FI_Insert(fininstr);
  else
     fininstr.FIID = newCurrency.Fiid;
     if(not GetEQ( fininstr ))
       stat = 70;
     else
       Copy(fininstr_save, fininstr);
     end;

     fininstr.fiid          = newCurrency.Fiid         ;
     fininstr.fi_code       = newCurrency.CodeWidget   ;
     fininstr.fi_kind       = newCurrency.Kind         ;
     fininstr.name          = newCurrency.Name         ;
     fininstr.iso_number    = newCurrency.ISO_Number   ;
     fininstr.ccy           = newCurrency.CCY          ;
     fininstr.codeinaccount = newCurrency.CodeInAccount;
     fininstr.definition    = newCurrency.Definition   ;

     fininstr_save.fiid          = savedCurrency.Fiid         ;
     fininstr_save.fi_code       = savedCurrency.CodeWidget   ;
     fininstr_save.fi_kind       = savedCurrency.Kind         ;
     fininstr_save.name          = savedCurrency.Name         ;
     fininstr_save.iso_number    = savedCurrency.ISO_Number   ;
     fininstr_save.ccy           = savedCurrency.CCY          ;
     fininstr_save.codeinaccount = savedCurrency.CodeInAccount;
     fininstr_save.definition    = savedCurrency.Definition   ;

     if(stat == 0)
       stat = FI_Update(fininstr, fininstr_save);
     end;
  end;

  errmsg = "";

  if(stat != 0)
    if(stat == 70)
      errmsg = "Конфликт. Документ изменен другим операционистом";
    else
      errmsg = GetErrMsg();
    end;
    stat = 1;
  end;

  return getServiceError(stat, errmsg, 0, newCurrency);

end;

macro FI_CloseFinInstr
(
  Currency  // объект класса TWsFinInstr
)
  var stat = 0;
  file fininstr ("fininstr.dbt") write key 0; // fininstr
  ClearRecord(fininstr);

  fininstr.FIID = Currency.Fiid;
  if(not GetEQ( fininstr ))
    stat = 70;
  end;

  if(stat == 0)
    stat = FI_CloseCurrency(fininstr);
  end;

  if(stat != 0)
    if(stat == 70)
      RunError("Конфликт. Документ изменен другим операционистом");
    else
      var errmsg = GetErrMsg();
      if(errmsg != "");
        RunError(errmsg);
      end;
    end;
  end;

  return stat;
end;


macro FI_DeleteFinInstr
(
  Currency  // объект класса TWsFinInstr
)
  var stat = 0;
  file fininstr ("fininstr.dbt") write key 0; // fininstr
  ClearRecord(fininstr);

  fininstr.FIID = Currency.Fiid;
  if(not GetEQ( fininstr ))
    stat = 70;
  end;

  if(stat == 0)
    stat = FI_DeleteCurrency(fininstr);
  end;
  var errmsg = "";
  if(stat != 0)
    if(stat == 70)
      errmsg = "Конфликт. Документ изменен другим операционистом";
    else
      errmsg = GetErrMsg();
    end;
  end;

  return getServiceError(stat, errmsg, 0, Currency);
end;

class TWsFiUserData
  var UserString : string;
  var UserInt : integer;
end;

macro FI_GetUserData(fiid)

   var res = TWsFiUserData();

   res.UserString = "╤ЄЁюър фы  " + fiid;
   res.UserInt = fiid;

   return res;

end;

macro FI_SaveUserData(ud)

   return "─рээ√х єёях°эю ёюїЁрэхэ√";

end;

// var obj = GetFIList(0,0,0,9, NULL);

// GetTWsIsoCurByNumCode(810);