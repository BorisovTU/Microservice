/*
$Name:           res_acc.mac
$Module:         Ядро ГКБО
$Description:    Расчет резерва для лицевого счета
*/

Import "res_calc.mac", "res_lib.mac";

class (CalcReserve) CalcReserveAccount
(
  _accRec
 ,_DateReserve      : date
 ,_RsvClass         : string
 ,_RsvClassLoans    : string
 ,_RsvClassOffshore : string
 ,OffshoreGroup     : @integer
 ,PtRiskGroup              : integer
 ,PtReserveProcent         : double
 ,PtReserveProcentOffshore : double
 ,PtReserveProcentEstimated : double
 ,AccountReserveKind: integer
 ,_ConsiderMinPercent : bool
)

  private var OverdueMask : string; /*Маска счетов просроченных требований*/
  private var accRec; /* буфер лицевого счета */
  private record clientRec("party.dbt");

/* получить процент */
private macro GetProcent( procent : double ) : double
  if( procent == null )
    procent = 0;
  end;
  return procent;
end;

private macro GetSumReserveCalcUser()
  SumReserveCalcUser_DateReserve = GetAccSumReserveCalcUser( accRec, DateReserve );
  
  if(accRec.Code_Currency != NATCUR)
    if(ConvSum(SumReserveCalcUser_DateReserve, 
               SumReserveCalcUser_DateReserve, 
               DateReserve, 
               accRec.Code_Currency, 
               NATCUR))
      RunError("Ошибка при конвертации суммы");
    end;
  end;
  if(DateReserve != DateRate)
    SumReserveCalcUser_DateRate = GetAccSumReserveCalcUser( accRec, DateRate );
    if(accRec.Code_Currency != NATCUR)
      if(ConvSum(SumReserveCalcUser_DateRate, 
                 SumReserveCalcUser_DateRate, 
                 DateRate, 
                 accRec.Code_Currency, 
                 NATCUR))
        RunError("Ошибка при конвертации суммы");
      end;
    end;
  else
    SumReserveCalcUser_DateRate = SumReserveCalcUser_DateReserve;
  end;
end;

/*** Конструктор ***/
  InitCalcReserve();

  accRec = _accRec;
  
  /* классификация РВП */
  RsvClass         = _RsvClass;
  RsvClassLoans    = _RsvClassLoans;
  
  if( (OffshoreGroup == 2) or (OffshoreGroup == 3) )
    RsvClassOffshore = _RsvClassOffshore;
  else
    OffshoreGroup = 0;
  end;

  OverdueMask = "";
  /*Маска балансовых счетов просроченных требований*/      
  GetRegistryValue( "CB\\РЕЗЕРВЫ\\СЧЕТА ДЛЯ РАСЧ. ПРОСРОЧ. ТРЕБ.", V_STRING, OverdueMask, null );
        
  /* дата формирования резерва и дата курса*/
  DateReserve = _DateReserve;
  DateRate = GetDateRateForAccount(accRec, DateReserve, @RestReserve);

  if( RsvClassOffshore )
    /* процент РОФШ */
    if( PtReserveProcentOffshore == null )
      ProcentReserveOffshore = GetProcent( GetProcentOfReserveOffshore(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateReserve) );
    else
      ProcentReserveOffshore = PtReserveProcentOffshore;
    end;

    /* Максимальный остаток объекта, по которому создается резерв, за отчетный период */
    MaxRest = GetMaxAbsAccRest( accRec, DateReserve );

    /* среднедневной расчетный дебетовый оборот */
    DebetTurn = GetAverageAccDebetTurn( accRec, DateReserve );

    /* максимальный остаток по счету в течение последних 30 календарных дней (согласно 1584-У) */
    MaxRest30WorkDay = GetMaxAbsAcc30DayRest( accRec, DateReserve );

    /* среднедневной расчетный дебетовый оборот за последние 30 календарных дней (согласно 1584-У)*/
    DebetTurn30Day = GetAverageAccDebetTurn30Day( accRec, DateReserve, accRec.Open_Date );

  end;

  /*Для классификации "3.1.1 (91318)" резерв не рассчитывается, а берется из примечания к счету*/
  if(SubStr(RsvClass, 1, 13) == "3.1.1 (91318)")
    GetSumReserveCalcUser();
    return;
  end;

  /* остаток на лицевом счете */
  Rest_DateReserve = GetAbsAccRest( accRec.Account, DateReserve, accRec.Chapter, accRec.Code_Currency );
  if(DateReserve != DateRate)
    Rest_DateRate = GetAbsAccRestRate( accRec.Account, DateReserve, DateRate, accRec.Chapter, accRec.Code_Currency );
  else
    Rest_DateRate = Rest_DateReserve;
  end;

  /* процент РВП (РВПС) */
  if( PtReserveProcent == null )
    ProcentReserve = GetProcent( GetProcentOfReserve(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateReserve) );
  else
    ProcentReserve = PtReserveProcent;
  end;

  /* процент ОР */
  if(PtReserveProcentEstimated == null)
    ProcentReserveEstimated = GetProcentOfReserveEstimated(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateReserve);
  else
    ProcentReserveEstimated = PtReserveProcentEstimated;
  end;

  /* остаток на счете процентов */
  RestAccountProcent = GetAccProcentRest( accRec, DateReserve );

  if(DateReserve < DateBegin446P)
    if(   (SubStr(RsvClass, 1, 9) == "2.1 (502)")
       or (SubStr(RsvClass, 1, 9) == "2.1 (507)")
       or (SubStr(RsvClass, 1, 9) == "2.5 (601)")
       or (SubStr(RsvClass, 1, 12) == "2732-У (501)")
       or (SubStr(RsvClass, 1, 12) == "2732-У (506)")
       or (SubStr(RsvClass, 1, 12) == "2732-У (502)")
       or (SubStr(RsvClass, 1, 12) == "2732-У (507)")
      )
      GetSumReserveCalcUser();
    
      SolidDeposit = GetAccSolidDeposit( accRec, DateReserve );
      if(SolidDeposit != SolidDeposit_Yes)
        /* остаток на счете положительной переоценки */
        RestPosRevalue = GetAccPosRevalueRest( accRec, DateReserve );
    
        /* остаток на счете отрицательной переоценки */
        RestNegRevalue = GetAccNegRevalueRest( accRec, DateReserve );
      end;
    
      return;
    end;
  end;

  if(   (SubStr(RsvClass, 1, 3) == "2.8")
    )
    GetSumReserveCalcUser();
  
    GetRegistryValue( "CB\\РЕЗЕРВЫ\\РВП ПО ПР. РЕПО", V_INTEGER, RsvRepoCostCB, null );

    /* остаток на счете обязательств */
    RestObligation = GetAccObligationRest( accRec, DateReserve );

    if(DateReserve < DateBegin446P)
      ProcentReserveContr = GetProcent( GetProcentOfReserveContr(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateReserve) );
    
      SolidDeposit = GetAccSolidDeposit( accRec, DateReserve );
    
      /* остаток на счете положительной переоценки */
      RestPosRevalue = GetAccPosRevalueRest( accRec, DateReserve );
    
      /* остаток на счете отрицательной переоценки */
      RestNegRevalue = GetAccNegRevalueRest( accRec, DateReserve );
    end;

    return;
  end;
  
  if(DateReserve < DateBegin446P)
    if(   (SubStr(RsvClass, 1, 9) == "2.6 (479)")
       or (SubStr(RsvClass, 1, 11) == "2.6 (60106)")
       or (SubStr(RsvClass, 1, 14) == "2732-У (91311)")
      )
      GetSumReserveCalcUser();
    
      SolidDeposit = GetAccSolidDeposit( accRec, DateReserve );
    
      if(SubStr(RsvClass, 1, 14) == "2732-У (91311)")
        СуммаОстатковСчетовРазмещенныхСредств = Abs(GetAccRestSumForAccSecurity(accRec.Account, accRec.Chapter, accRec.Code_Currency, DateReserve));
      end;
      
      return;
    end;
    
    if(   (SubStr(RsvClass, 1, 12) == "2732-У (601)")
       or (SubStr(RsvClass, 1, 14) == "2732-У (91314)")
      )
      SolidDeposit = GetAccSolidDeposit( accRec, DateReserve );
      return;
    end;
  end;

  /* Группа риска */
  if( PtRiskGroup == null )
    GroupRisk = GetAccGroupRisk( accRec, DateReserve );
  else
    GroupRisk = PtRiskGroup;
  end;
  if( GroupRisk == null ) GroupRisk = 0; end;

  /* текущая рыночная стоимость */
  MarketCost = GetAccMarketCost( accRec, DateReserve );

  /* нерезервируемая сумма */
  FreeReserveSum_DateReserve = GetAccFreeReserveSumRateDate( accRec, DateReserve, DateReserve );
  if(DateReserve != DateRate)
    FreeReserveSum_DateRate = GetAccFreeReserveSumRateDate( accRec, DateReserve, DateRate );
  else
    FreeReserveSum_DateRate = FreeReserveSum_DateReserve;
  end;

  /*Минимальный процент резерва*/
  ConsiderMinPercent = _ConsiderMinPercent;
  if(ConsiderMinPercent)
    MinPercentReserve_DateReserve = GetAccountMinPercentReserve(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateReserve);
    if(DateReserve != DateRate)
      MinPercentReserve_DateRate = GetAccountMinPercentReserve(accRec.Chapter, accRec.Code_Currency, accRec.Account, DateRate);
    else
      MinPercentReserve_DateRate = MinPercentReserve_DateReserve;
    end;
  end;

  /* остаток на счете амортизации */
  RestAmortization = GetAccAmortizationRest( accRec, DateReserve );

  /* остаток на счете ссудной задолженности */
  RestLoanOverdraft = GetAccLoanOverdraftRest( accRec, DateReserve );

  /* сумма текущих рыночная стоимостей для всех счетов обеспечения */
  MarketCostSecurity = GetMarketCostAccountSecurity( accRec, DateReserve );

  /* сумма текущих рыночная стоимостей для всех счетов обеспечения с учетом качества обеспечения */
  MarketCostSecurityByCategory = GetMarketCostSecurityByCategory( accRec, DateReserve );
  
  /* сумма текущих рыночная стоимостей для всех счетов обеспечения с учетом качества обеспечения для связанного счета ссудной задолженности */
  MarketCostSecurityByCategoryLoanOverdraft = GetMarketCostSecurityByCategoryLoanOverdraft( accRec, DateReserve );

  if((StrLen(RsvClass) > 0) and (CompareStrWithMasks(OverdueMask, accRec.Balance, 7) == 0))

    РасчетРезерваПросроченныхТребований = true;
    КлиентСчета = accRec.Client;

    GetRegistryValue( "CB\\РЕЗЕРВЫ\\МИН. ПРОЦЕНТ РЕЗЕРВИРОВАНИЯ\\РВП\\5", V_DOUBLE, ПроцентРезерваПросроченныхТребований, null );

    СуммаПросроченныхТребований_DateReserve = GetAccOverdueSum( accRec, DateReserve );
    СуммаПросроченныхТребований_DateRate    = GetAccOverdueSum( accRec, DateReserve );

    if(accRec.Code_Currency != NATCUR)
      if(ConvSum(СуммаПросроченныхТребований_DateReserve, 
                 СуммаПросроченныхТребований_DateReserve, 
                 DateReserve, 
                 accRec.Code_Currency, 
                 NATCUR))
        RunError("Ошибка при конвертации суммы");
      end;
      if(DateReserve != DateRate)
        if(ConvSum(СуммаПросроченныхТребований_DateRate, 
                   СуммаПросроченныхТребований_DateRate, 
                   DateRate, 
                   accRec.Code_Currency, 
                   NATCUR))
          RunError("Ошибка при конвертации суммы");
        end;
      else
        СуммаПросроченныхТребований_DateRate = СуммаПросроченныхТребований_DateReserve;
      end;
    end;

    GetRegistryValue( "CB\\РЕЗЕРВЫ\\РЕЗЕРВ ПО НЕПРОСРОЧ. ТРЕБ-ЯМ", V_BOOL, РасчетРезерваНепросроченныхТребований, null );
    
    if( РасчетРезерваНепросроченныхТребований )

      if((СуммаПросроченныхТребований_DateReserve > $0) or (ClientHasAccOverdueSum( accRec.Client, DateReserve, OverdueMask )))

        ProcentReserve = ПроцентРезерваПросроченныхТребований;

      end;

    end;

  end;

  if(StrLen(RsvClassLoans) > 0)
    if(not ПолучитьСубъекта(accRec.Client, clientRec))
      PartyTaxGroup = GetPartyTaxGroup(clientRec, DateReserve);
    end;
  end;

  /*Вид резерва, используется при рассчете РОФШ*/
  ReserveKind = AccountReserveKind;

  /* установить в качестве исходных данных для расчета резерва величины на дату резервирования */
  SetVariables_DateReserve();

end;
