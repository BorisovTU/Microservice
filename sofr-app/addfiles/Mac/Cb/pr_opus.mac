/*
 $Name: pr_opus.mac
 $Module: Главная книга
 $Description: Отчет соответствия символов ОФР
 */
 /*
	Отчет соответствия символов ОФР
 */
import BankInter, CurrInter, FIInter, rsd, "globals.mac", oralib, likepy;

file opusymb("opusymb.dbt");
var SqlOutP3 : string = "";

macro PrintTitle()
  file person("person.dbt");
  file dp_dep("dp_dep.dbt");
  file party("party.dbt");
  var operName = "";
  var depName = "";
  var partyName = "";

  person.Oper = {oper};
  if(getEQ(person))
    operName = person.Name;
  end;
  dp_dep.Code = {OperDprt};
  if(getEQ(dp_dep))
    depName = dp_dep.Name;
    if(dp_dep.PartyID)
      party.PartyID = dp_dep.PartyID;
      if(getEQ(party))
        partyName = party.Name;
      end;
    end;
  end;
[
                         Отчет о соответствии символов ОФР

  Дата операции: ##########
  Операционист:  ##### #
  Подразделение: ############ #


]
({curdate}, {oper}, operName, depName, partyName);
end;

macro PrintHeaderAbsent()
[1. Счета, у которых символ ОФР отсутствует в справочнике.              ];
[-------------------------+------------+-----------------+--------------];
[ Лицевой счет            | Символ ОФР | Балансовый счет | Код валюты   ];
[-------------------------+------------+-----------------+--------------];
end;

macro PrintAccountAbsent(Account, OpuCode, Balance, FICode)
[ ########################    #####           #####        #] (Account, OpuCode, Balance, FICode);
end;

macro PrintHeaderHasRest()
[2. Счета с остатком, у которых символ ОФР закрыт с признаком "без остатка"];
[-------------------------+------------+-----------------+--------------];
[ Лицевой счет            | Символ ОФР | Остаток         | Код валюты   ];
[-------------------------+------------+-----------------+--------------];
end;

macro PrintHeaderSymPart3(StartPeriod,EndPeriod)
[3. Проводки с ошибочным символом ОФР.                                  ];
[   Начало периода:    ##########](StartPeriod);
[   Окончание периода: ##########](EndPeriod);
[                                                                                               ];
[---------+-----------+------------------------+------------------------+--------------+----------------------------------------------------------------];
[ Номер   | Дата      | Счет дебета            | Счет кредита           | Символ ОФР   |                         Ошибка                                 ];
[         | проводки  |                        |                        |              |                                                                ];
[---------+-----------+------------------------+------------------------+--------------+----------------------------------------------------------------];
end;

macro PrintHeaderSymPart4(StartPeriod,EndPeriod)
[4. Проводки с символом ОФР, не изменяющие прочий совокупный доход.                     ];
[   Начало периода:    ##########](StartPeriod);
[   Окончание периода: ##########](EndPeriod);
[                                                                                       ];
[---------+-----------+------------------------+------------------------+--------------+];
[ Номер   | Дата      | Счет дебета            | Счет кредита           | Символ ОФР   |];
[         | проводки  |                        |                        |              |];
[---------+-----------+------------------------+------------------------+--------------+];
end;

macro PrintAccountHasRest(Account, OpuCode, Rest, FICode)
[ ########################    #####                    #   #            ] (Account, OpuCode, Rest, FICode);
end;

macro PrintFooter()
[-----------------------------------------------------------------------


];
end;

macro PrintFooterForPart3()
[---------+-----------+------------------------+------------------------+--------------+----------------------------------------------------------------


];
end;

macro PrintFooterForPart4()
[---------+-----------+------------------------+------------------------+--------------+


];
end;

macro ReportAccountAbsent()
  var strSql = "";
  var rs : RsdRecordset;
  var params : TArray;
  var IsNull : bool;
  var RegMask : string;
  var SqlMask : string;

  SqlMask = "1 = 1";
  GetRegistryValue("PS\\REQOPENACC\\СЧЕТА ДОХОДОВ_РАСХОДОВ", V_STRING, RegMask, null);
  if(RegMask != "")
    SqlMask = ConvertMaskToSQLFormat(RegMask, "ac.t_balance");
  end;

  strSql = strSql + "SELECT ac.t_account, ac.t_opucode, ac.t_balance, fi.t_fi_code, op.t_balance ";
  strSql = strSql + "  FROM daccount_dbt ac";
  strSql = strSql + "  JOIN dfininstr_dbt fi ON (ac.t_code_currency = fi.t_fiid) ";
  strSql = strSql + "  FULL OUTER JOIN dopusymb_dbt op ON (op.t_recId = ac.T_OFRRECID) ";
  strSql = strSql + " WHERE ac.t_opucode != CHR(1) ";
  strSql = strSql + "   AND ac.t_department = :OperDprt ";
  strSql = strSql + "   AND ac.t_open_date <= :OpenDate ";
  strSql = strSql + "   AND DECODE(ac.t_close_date, TO_DATE('01.01.0001', 'DD.MM.YYYY'), TO_DATE('31.12.9999', 'DD.MM.YYYY'), ac.t_close_date) > :CloseDate ";
  strSql = strSql + "   AND ac.t_type_account NOT LIKE '%П%' ";
  strSql = strSql + "   AND (" + SqlMask + ") ";
  strSql = strSql + " ORDER BY ac.t_account ";

  params = makeArray(SQLParam("OperDprt", {OperDprt})
                    ,SQLParam("OpenDate", {curdate})
                    ,SQLParam("CloseDate", {curdate})
                    );
  rs = execSQLselect(strSql, params, true);

  if(rs)
    while(rs.MoveNext())
      rs.value(4, IsNull);
      if((IsNull) or (CompareStrWithMasks(rs.value(4), rs.value(2)) != 0))
        PrintAccountAbsent(rs.value(0), rs.value(1), rs.value(2), rs.value(3));
      end;
    end;
  end;
end;

macro ReportAccountHasRest()
  var strSql = "";
  var rs : RsdRecordset;
  var params : TArray;
  var RestAcc = $0;
  var RestNat = $0;
  var RegMask : string;
  var SqlMask : string;
  var NatCode = ПолучитьКодФинИн(NATCUR);

  SqlMask = "1 = 1";
  GetRegistryValue("PS\\REQOPENACC\\СЧЕТА ДОХОДОВ_РАСХОДОВ", V_STRING, RegMask, null);
  if(RegMask != "")
    SqlMask = ConvertMaskToSQLFormat(RegMask, "ac.t_balance");
  end;
  strSql = strSql + "SELECT ac.t_chapter, ac.t_code_currency, ac.t_account, ac.t_opucode, ac.t_balance, fi.t_fi_code, op.t_balance, op.t_closedate ";
  strSql = strSql + "  FROM daccount_dbt ac ";
  strSql = strSql + "  JOIN dfininstr_dbt fi ON (ac.t_code_currency = fi.t_fiid) ";
  strSql = strSql + "  JOIN dopusymb_dbt op ON (op.t_recId = ac.T_OFRRECID) ";
  strSql = strSql + " WHERE op.t_closedate != TO_DATE('01.01.0001', 'DD.MM.YYYY') ";
  strSql = strSql + "   AND op.t_closedate < :CurDate ";
  strSql = strSql + "   AND op.t_principle = CHR(0) ";
  strSql = strSql + "   AND ac.t_department = :OperDprt ";
  strSql = strSql + "   AND ac.t_open_date <= :OpenDate ";
  strSql = strSql + "   AND DECODE(ac.t_close_date, TO_DATE('01.01.0001', 'DD.MM.YYYY'), TO_DATE('31.12.9999', 'DD.MM.YYYY'), ac.t_close_date) > :CloseDate ";
  strSql = strSql + "   AND (" + SqlMask + ") ";
  strSql = strSql + " ORDER BY ac.t_account ";

  params = makeArray(SQLParam("CurDate", {curdate})
                    ,SQLParam("OperDprt", {OperDprt})
                    ,SQLParam("OpenDate", {curdate})
                    ,SQLParam("CloseDate", {curdate})
                    );
  rs = execSQLselect(strSql, params, true);
  if(rs)
    while(rs.MoveNext())
      if(CompareStrWithMasks(rs.value(6), rs.value(4)) == 0)
        RestAcc = $0;
        RestNat = $0;
        if(rs.value(1) != NATCUR)
          RestAcc = RestAC(rs.value(2), rs.value(1), rs.value(7), null, rs.value(0));
          RestNat = RestA(rs.value(2), rs.value(7), null, rs.value(0), rs.value(1), 0);
        else
          RestAcc = RestA(rs.value(2), rs.value(7), null, rs.value(0));
        end;
        
        if(RestAcc != $0)
          PrintAccountHasRest(rs.value(2), rs.value(3), RestAcc, rs.value(5));
        end;
        if(RestNat != $0)
          PrintAccountHasRest(rs.value(2), rs.value(3), RestNat, NatCode);
        end;
      end;
    end;
  end;
end;

macro PrintSymPart3(fNUMB_DOCUMENT,fDATE_CARRY,fACCOUNT_PAYER,fACCOUNT_RECEIVER, fCODE, isClose)
    var vCode = "";
    if (fCODE != NullVal)
        vCode = fCODE;
    end;

    [ #######  ############ ######################   ######################   ############   ##############################################################   ](fNUMB_DOCUMENT:c, date(fDATE_CARRY):c,fACCOUNT_PAYER:c,fACCOUNT_RECEIVER:c,vCode:c,isClose:c);
end;

macro PrintSymPart4(fNUMB_DOCUMENT,fDATE_CARRY,fACCOUNT_PAYER,fACCOUNT_RECEIVER, fCODE)
    [ #######  ############ ######################   ######################   ############](fNUMB_DOCUMENT:c, date(fDATE_CARRY):c,fACCOUNT_PAYER:c,fACCOUNT_RECEIVER:c,fCODE:c);
end;

macro Part3ErrorMsg(acctrnID, recID)
    var strSql = "";
    var rs : RsdRecordset;
    var params : TArray;
    
    params = makeArray(SQLParam("acctrnID", acctrnID)
                    ,SQLParam("recID", recID));
                    
    rs = execSQLselect(SqlOutP3, params, true);
    
    var ret = "";
    if(rs)
        if(rs.MoveNext())
            ret = rs.value(0);
        end;
    end;
    
    return ret;
end;

macro ReportErrSym(StartPeriod,EndPeriod)
    var strSql = "";
    var rs : RsdRecordset;
    var params : TArray;
    
    strSql = strSql + "SELECT T1.T_NUMB_DOCUMENT, ";
    strSql = strSql + "T1.T_DATE_CARRY, ";
    strSql = strSql + "T1.T_ACCOUNT_PAYER, ";
    strSql = strSql + "T1.T_ACCOUNT_RECEIVER, ";
    strSql = strSql + "ops.t_code, ";
    strSql = strSql + "T1.T_ACCTRNID, ";
    strSql = strSql + "ops.t_recid ";
    strSql = strSql + "FROM dacctrn_dbt t1, ";
    strSql = strSql + "dopusymb_dbt ops ";
    strSql = strSql + "WHERE(SUBSTR (T1.T_ACCOUNT_PAYER, 1, 3) = '106' ";
    strSql = strSql + "OR SUBSTR (T1.T_ACCOUNT_RECEIVER, 1, 3) = '106') ";
    strSql = strSql + "AND ops.t_recid (+) = t1.t_OFRRecID ";
    strSql = strSql + "AND (t1.t_OFRRecID = 0 ";
    strSql = strSql + "OR t1.t_OFRRecID NOT IN (SELECT t_recid FROM dopusymb_dbt) ";
    strSql = strSql + "OR EXISTS ";
    strSql = strSql + "(SELECT 1 ";
    strSql = strSql + "FROM daccount_dbt acct, dopusymb_dbt sym ";
    strSql = strSql + "WHERE ACCT.T_BALANCE LIKE '106%' ";
    strSql = strSql + "AND (acct.T_ACCOUNTID = t1.T_ACCOUNTID_PAYER ";
    strSql = strSql + "OR acct.T_ACCOUNTID = t1.T_ACCOUNTID_RECEIVER) ";
    strSql = strSql + "AND sym.T_BALANCE NOT LIKE ";
    strSql = strSql + "'%' || ACCT.T_BALANCE || '%' ";
    strSql = strSql + "AND sym.t_recid = t1.t_OFRRecID) ";
    strSql = strSql + "OR EXISTS ";
    strSql = strSql + "(SELECT t_recid ";
    strSql = strSql + "FROM dopusymb_dbt sym2 ";
    strSql = strSql + "WHERE sym2.t_closedate <= t1.T_DATE_CARRY ";
    strSql = strSql + "AND sym2.T_RECID = ops.T_RECID ";
    strSql = strSql + "AND sym2.t_closedate <> TO_DATE ('01.01.0001', 'DD.MM.YYYY')) ";
    
    strSql = strSql + "OR EXISTS ";
    strSql = strSql + "(SELECT t_recid ";
    strSql = strSql + "FROM dopusymb_dbt sym3 ";
    strSql = strSql + "WHERE     sym3.t_opendate > t1.T_DATE_CARRY ";
    strSql = strSql + "AND sym3.T_RECID = ops.T_RECID)) ";
    
    strSql = strSql + "AND t1.T_DATE_CARRY >= :StartPeriod ";
    strSql = strSql + "AND t1.T_DATE_CARRY <= :EndPeriod ";
    
    params = makeArray(SQLParam("StartPeriod", StartPeriod)
                    ,SQLParam("EndPeriod", EndPeriod));
     
    rs = execSQLselect(strSql, params, true);

    if(rs)
        while(rs.MoveNext())
            var ErrMsg = "";
            ErrMsg = Part3ErrorMsg(rs.value(5), rs.value(6));
            PrintSymPart3(rs.value(0),rs.value(1),rs.value(2), rs.value(3), rs.value(4), ErrMsg);
        end;
    end;
end;

macro ReportSymPart4(StartPeriod,EndPeriod)
    var strSql = "";
    var rs : RsdRecordset;
    var params : TArray;
    
    strSql = strSql + "SELECT T1.T_NUMB_DOCUMENT, ";
    strSql = strSql + "T1.T_DATE_CARRY, ";
    strSql = strSql + "T1.T_ACCOUNT_PAYER, ";
    strSql = strSql + "T1.T_ACCOUNT_RECEIVER,";
    strSql = strSql + "ops.t_code ";
    strSql = strSql + "FROM dacctrn_dbt t1, dopusymb_dbt ops ";
    strSql = strSql + "WHERE     (    SUBSTR (T1.T_ACCOUNT_PAYER, 1, 3) <> '106' ";
    strSql = strSql + "AND SUBSTR (T1.T_ACCOUNT_RECEIVER, 1, 3) <> '106') ";
    strSql = strSql + "AND ops.t_recid = t1.t_OFRRecID ";
    strSql = strSql + "AND t1.T_DATE_CARRY >= :StartPeriod ";
    strSql = strSql + "AND t1.T_DATE_CARRY <= :EndPeriod ";
    
    params = makeArray(SQLParam("StartPeriod", StartPeriod)
                    ,SQLParam("EndPeriod", EndPeriod));
                    
    rs = execSQLselect(strSql, params, true);
    if(rs)
        while(rs.MoveNext())
            PrintSymPart4(rs.value(0),rs.value(1),rs.value(2), rs.value(3), rs.value(4));
        end;
    end;
end;

macro SqlOutPart3OutHandler(str)
    SqlOutP3 = SqlOutP3 + str;
end;

macro InitSqlOutPart3()
    SqlOutP3 = "";
    SetOutHandler(@SqlOutPart3OutHandler);
    [
    SELECT CASE
          WHEN EXISTS
                  (SELECT 1
                     FROM DACCTRN_DBT trn, dopusymb_dbt sym
                    WHERE     SYM.T_RECID = TRN.T_OFRRECID
                          AND TRN.T_ACCTRNID = :acctrnID
                          AND SYM.T_RECID = :recID
                          AND (    sym.t_CloseDate <>
                                      TO_DATE ('01.01.0001', 'DD.MM.YYYY')
                               AND sym.t_CloseDate < trn.t_Date_Carry))
          THEN
             'Закрыт'
          WHEN EXISTS
                  (SELECT 1
                     FROM DACCTRN_DBT trn, dopusymb_dbt sym
                    WHERE     SYM.T_RECID = TRN.T_OFRRECID
                          AND TRN.T_ACCTRNID = :acctrnID
                          AND SYM.T_RECID = :recID
                          AND sym.t_OpenDate > trn.t_Date_Carry)
          THEN
             'Не открыт'
          WHEN EXISTS
                  (SELECT 1
                     FROM DACCTRN_DBT trn
                    WHERE     TRN.T_ACCTRNID = :acctrnID
                          AND (   SUBSTR (TRN.T_ACCOUNT_PAYER, 1, 3) = '106'
                               OR SUBSTR (TRN.T_ACCOUNT_RECEIVER, 1, 3) =
                                     '106')
                          AND (trn.T_OFRRECID = 0 OR trn.T_OFRRECID IS NULL))
          THEN
             'Символ ОФР не задан'
          WHEN EXISTS
                  (SELECT 1
                     FROM DACCTRN_DBT trn
                    WHERE     TRN.T_ACCTRNID = :acctrnID
                          AND trn.T_OFRRECID NOT IN
                                 (SELECT t_recid FROM dopusymb_dbt))
          THEN
             'Некорректный символ ОФР'
          WHEN EXISTS
                  (SELECT 1
                     FROM daccount_dbt acct,
                          dopusymb_dbt sym,
                          DACCTRN_DBT trn
                    WHERE     ACCT.T_BALANCE LIKE '106%'
                          AND TRN.T_ACCTRNID = :acctrnID
                          AND (   acct.T_ACCOUNTID = trn.T_ACCOUNTID_PAYER
                               OR acct.T_ACCOUNTID = trn.T_ACCOUNTID_RECEIVER)
                          AND sym.T_BALANCE NOT LIKE
                                 '%' || ACCT.T_BALANCE || '%'
                          AND sym.t_recid = trn.t_OFRRecID)
          THEN
                'Лицевой счет '
             || (SELECT DECODE (SUBSTR (TRN.T_ACCOUNT_PAYER, 1, 3),
                                '106', TRN.T_ACCOUNT_PAYER,
                                trn.T_ACCOUNT_RECEIVER)
                   FROM DACCTRN_DBT trn
                  WHERE TRN.T_ACCTRNID = :acctrnID)
             || ' не соответствует символу ОФР'
          ELSE
             CHR (1)
       END
          AS errmsg
    FROM DUAL
    ];
    SetOutHandler();
end;

macro ProcessReport(CheckAccount,CheckSymbPeriod,StartPeriod,EndPeriod)
  PrintTitle();
  
  if (CheckAccount)
    PrintHeaderAbsent();
    ReportAccountAbsent();
    PrintFooter();
    
    PrintHeaderHasRest();
    ReportAccountHasRest();
    PrintFooter();
  end;
  
  if (CheckSymbPeriod)
    InitSqlOutPart3();
    
    PrintHeaderSymPart3(StartPeriod,EndPeriod);
    ReportErrSym(StartPeriod,EndPeriod);
    PrintFooterForPart3();
    
    PrintHeaderSymPart4(StartPeriod,EndPeriod);
    ReportSymPart4(StartPeriod,EndPeriod);
    PrintFooterForPart4();
  end;
  
end;
