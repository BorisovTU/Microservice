//-----------------------------------------------------------------------------
// Блок     : 29016  - "Предобработка клиентского платежа"
//            29017  - "Предобработка кассового документа"
//            
// Шаг      : "Единовременная комиссия - исполнение"
//-----------------------------------------------------------------------------

import pm_common, pm_setst, cbsttls, oralib, likepy;

var PaymentObj:RsbPayment;

//Возможные исходы событий
PRIVATE CONST PMSF_ACTION_OK    :integer = 0, //Ok
              PMSF_ACTION_REJECT:integer = 1, //Отвергнуть
              PMSF_ACTION_STOP  :integer = 2, //Приостановить до оплаты комиссий
              PMSF_ACTION_ERROR :integer = 3; //Возникла ошибка

PRIVATE VAR DocKindList:TArray = TArray();

//------------------------------------------------------------------------------
// Получить ID предыдущего шага операции
//------------------------------------------------------------------------------
PRIVATE MACRO Opr_GetPrevStep( ID_Operation:integer, ID_Step:integer, Prev_Step:@integer ):bool

  var query :string = "SELECT OS.T_PREVIOUS_STEP FROM DOPRSTEP_DBT OS WHERE OS.T_ID_OPERATION = :ID_OPERATION AND OS.T_ID_STEP = :ID_STEP";
  var params:TArray = makeArray( SQLParam( "ID_OPERATION", ID_Operation ),
                                 SQLParam( "ID_STEP",      ID_Step      ) );
  var rs:RsdRecordset = execSQLselect( query, params, true );

  if( rs and rs.moveNext() )
    Prev_Step = rs.Value(0);
    return true;
  else
    MsgBox( "Не найден предыдущий шаг операции" );
    return false;
  end;

ONERROR(x)

  MsgBox( "Ошибка получения предыдущего шага операции|" + x.Message );
  return false;

END;

//------------------------------------------------------------------------------
// Проверка документа оплаты комиссии
//------------------------------------------------------------------------------
PRIVATE MACRO CheckFee( action:integer, oprdocs:TRecHandler /*oprchilddoc.rec*/ ):integer
  
  record pmpaym(pmpaym);
  ClearRecord(pmpaym);
  pmpaym.PaymentID = PaymentObj.PaymentID;

  if( action != PMSF_ACTION_OK )
    return action;
  end;

  var Status:integer = -1;
  var notetext:string = "";

  if( not PM_GetOprStatus( oprdocs.rec.DocKind, oprdocs.rec.DocumentID, OPR_PAYM_STATE, @Status ) )
    return PMSF_ACTION_ERROR;
  end;
    
  //оплата зачислена - Ок
  if  ( Status == OPR_PM_ST_CLOSE  )
    return PMSF_ACTION_OK;

  //оплата отвергнута - отвергаем и основной документ
  elif( Status == OPR_PM_ST_REJECT )

    PaymentObj.PaymStatus = PM_REJECTED;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
      MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
      return PMSF_ACTION_ERROR;
    end;
    
    notetext = "Комиссия. " + ReadNoteForObject( OBJTYPE_PAYMENT, makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym), PM_NOTEKIND_DENIALGROUND );
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, notetext ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return PMSF_ACTION_ERROR;
    end;
    return PMSF_ACTION_REJECT;

  //оплата еще обрабатывается - ждем
  else
    MsgBox( "Не оплачена комиссия" );
    return PMSF_ACTION_STOP;
  end;

END;

//------------------------------------------------------------------------------
// Получить список DocKind
//------------------------------------------------------------------------------
PRIVATE MACRO GetOprkDoc():TArray

  var OprkDocList:TArray = TArray();
  var query:string =  "select kd.t_DocKind " +
                      " from doprkdoc_dbt kd " +
                      " connect by prior kd.t_DocKind = kd.t_ParentDocKind " +
                      " start with kd.t_DocKind = 29 ";

  var rs:RsdRecordset = execSQLselect( query, null, true );
  if( rs )
    while( rs.moveNext() )
      OprkDocList[OprkDocList.size] = rs.value(0);
    end;
  end;
  return OprkDocList;

ONERROR(x)
  
  return OprkDocList;

END;

//------------------------------------------------------------------------------
// Фильтрик на порожденные документы
//------------------------------------------------------------------------------
PRIVATE MACRO CheckDocKind( oprdocs:TRecHandler /*oprchilddoc.rec*/ )
  return ( find( DocKindList, oprdocs.rec.DocKind ) >= 0 );
END;

//------------------------------------------------------------------------------
// Макрос шага
//------------------------------------------------------------------------------
MACRO ExecuteStep( doc, paymDoc, DocKind:integer, ID_Operation:integer, ID_Step:integer )
  
  //находим предыдущий шаг
  var Prev_Step:integer = 0;
  var stat     :bool    = Opr_GetPrevStep( ID_Operation, ID_Step, @Prev_Step );

  DocKindList = GetOprkDoc();

  //находим документы, порожденные на предыдущем шаге
  var ChildDocList:TArray;
  if( stat )
    ChildDocList = filter( OprGetChildDocs( ID_Operation, Prev_Step, true ), @CheckDocKind );
  end;

  //проверяем состояние документов, порожденных на предыдущем шаге
  var Action:integer;
  if( stat )
    Action = reduce( ChildDocList, @CheckFee, PMSF_ACTION_OK );
    if( ( Action == PMSF_ACTION_STOP  ) OR
        ( Action == PMSF_ACTION_ERROR ) )
      stat = false;
    end;
  end;

  if( stat )
    return 0;
  else
    return 1;
  end;

END;
