/*───────────────────────────────────────────────────────────────────────────
  RS-Bank 5.0                                            R-Style Software Lab

  File Name   : corrarh.mac                                      12.11.1998
  Description : Протокол процедуры исправления ошибок в архиве
  Programmer  : Стасевич В.
  Comment     :

└───────────────────────────────────────────────────────────────────────────*/
import globals;

FILE Главы( obchaptr );

record СтарыйСчет( account );
record НовыйСчет ( account );

record СтарыеОстатки( restdate );
record НовыеОстатки ( restdate );

/*------------------ Получить название главы --------------------------*/
MACRO НазваниеГлавы( Глава )
  Главы.Chapter = Глава;

  if( getEQ( Главы ) )
    return Главы.ShortName;
  else
    return "Не найдена";
  end;
END;

/* --------------------- Название процедуры -------------------------- */
MACRO ИмяПроцедуры( Тип )
  if  ( Тип == 0 )
    return "- остатки назад";
  elif( Тип == 1 )
    return "- остатки вперед";
  else
    return "- обороты по месяцам";
  end;
END;

/*------------------ Заголовок отчета ---------------------------------*/
MACRO Шапка( Тип, ДатаНачала, Глава, ФинИнстр,
             НомерБалансового, НомерЛицевого )

[──────────────────────────────────────────────────────────────────────────────];
[ Протокол исправлений ошибок по счетам #                                      ]( ИмяПроцедуры(Тип):l );
[                                                                              ];
[ Дата: #                                                                      ]( {curdate}:l );
[ Операционист: #                                                              ]( {oper}:l );
[                                                                              ];
if  ( Тип == 1 )
[ Дата начала периода: #                                                       ]( ДатаНачала:l );
[ Дата  конца периода: #                                                       ]( ({curdate}-1):l );
end;
[                                                                              ];
[ Глава: #                                                                     ]( НазваниеГлавы(Глава):l );
[ Фин.инструмент: #                                                                    ]( ФинИнстр:l );
[                                                                              ];

if  ( НомерЛицевого != "" )
[ По лицевому счету #                                                          ]( НомерЛицевого:l );
elif( НомерБалансового != "" )
[ По балансовому счету #                                                       ]( НомерБалансового:l );
else
[ По всем счетам                                                               ];
end;

[──────────────────────────┬──────────┬─────────────────────┬──────────────────];
[   Номер лицевого счета   │ Название │      Значение       │     Значение     ];
[                          │   поля   │    до изменения     │ после изменения  ];

END;

/*---------------------------------------------------------------------*/
MACRO НомерЛицевого( Номер )
[──────────────────────────────────────────────────────────────────────────────];
[                         #                                                    ]
( Номер:r );
[                                                                              ];
END;


/* ----------------- Обновление restdate ------------------------------*/
MACRO ОбновлениеФайлаОстатков( ИмяФайла, СтароеЗначение, НовоеЗначение )
  SetBuff( СтарыеОстатки, СтароеЗначение );
  SetBuff( НовыеОстатки, НовоеЗначение  );
[                                  Обновление записи файла #                   ]
( ИмяФайла:l );

[                           Date_Value                     #                  #]
( СтарыеОстатки.Date_Value:r, НовыеОстатки.Date_Value:r );

[                           Rest                           #                  #]
( СтарыеОстатки.Rest, НовыеОстатки.Rest );
[                                                                              ];
END;

/* ----------------- Добавление новой записи в restdate -----------------*/
MACRO ДобавлениеВФайлОстатков( ИмяФайла, НовоеЗначение )
  SetBuff( НовыеОстатки, НовоеЗначение  );
[                              Добавление новой записи в файл #                ]
( ИмяФайла:l );

[                           Date_Value                                        #]
( НовыеОстатки.Date_Value:r );

[                           Rest                                              #]
( НовыеОстатки.Rest );
[                                                                              ];
END;

/* ----------------- Удаление записи из restdate --------------------------*/
MACRO УдалениеИзФайлаОстатков( ИмяФайла, СтароеЗначение )
  SetBuff( СтарыеОстатки, СтароеЗначение );
[                                  Удаление записи из файла #                  ]
( ИмяФайла:l );

[                           Date_Value                     #                   ]
( СтарыеОстатки.Date_Value:r );

[                           Rest                           #                   ]
( СтарыеОстатки.Rest );
[                                                                              ];
END;

/*------------------ Обновление account -------------------------------*/
MACRO ОбновлениеФайлаCчетов( ИмяФайла, СтароеЗначение, НовоеЗначение )
 VAR i, НазвПоля;

  SetBuff( СтарыйСчет, СтароеЗначение );
  SetBuff( НовыйСчет,  НовоеЗначение  );

[                                  Обновление записи файла #                   ]
( ИмяФайла:l );
[                                                                              ];

  i = 26;
  while( i <= 68 )

    if( СтарыйСчет(i) != НовыйСчет(i) )
      НазвПоля = string( "R", (i-26)/3 );

[                                 #                        #                  #]
( НазвПоля, СтарыйСчет(i), НовыйСчет(i) );
    end;
    i = i + 3;
  end;

  i = 24;
  while( i <= 66 )

    if( СтарыйСчет(i) != НовыйСчет(i) )
      НазвПоля = string( "D", (i-24)/3 );

[                                 #                        #                  #]
( НазвПоля, СтарыйСчет(i), НовыйСчет(i) );
    end;
    i = i + 3;
  end;

  i = 25;
  while( i <= 67 )

    if( СтарыйСчет(i) != НовыйСчет(i) )
      НазвПоля = string( "K", (i-25)/3 );

[                                 #                        #                  #]
( НазвПоля, СтарыйСчет(i), НовыйСчет(i) );
    end;
    i = i + 3;
  end;

  if( СтарыйСчет.PlanRest != НовыйСчет.PlanRest )
[                           PlanRest                       #                  #]
( СтарыйСчет.PlanRest, НовыйСчет.PlanRest );
  end;

  if( СтарыйСчет.Final_Date != НовыйСчет.Final_Date )
[                           Final_Date                     #                  #]
( СтарыйСчет.Final_Date, НовыйСчет.Final_Date );
  end;
[                                                                              ];

END;
