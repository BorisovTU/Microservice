/*
$Name:        ws_objlinks.mac
$Module:      Главная книга
$Description: Макрос списка связанных объектов
*/

import BankInter, rsd, RSD, cb_sql, CTInter, FIInter, "ws_validation.mac";

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private const NONE = 0, CREATE = 1, MODIFY = 2, REMOVE = 3;

// класс "Вид связи (роль) объекта"
class TWsObjRole

  var ObjectType : integer;    // Тип объекта №1
  var GroupID : integer;       // Номер вида связи
  var LinkedObjType : integer; // Тип объекта №2
  var RoleName : string;       // Роль объекта №1 - наименование связи со стороны объекта №2
  var KeepOldValues : bool;    // Признак "Хранить историю значений"
  var LinkedRoleName : string; // Роль объекта №2 - наименование связи со стороны объекта №1
  var BiDirection : bool;      // Признак "Двунаправленная связь"
  var IsPeer : bool;           // Признак "Равноправная связь"
  //var MacroName : string;    // Имя макроса для пользовательских проверок доп. поля

end;

class TWsObChapter
  var Chapter : integer;
  var Name_ : string;
end;

class TWsFIKind
  var Fi_Kind : integer;
  var Name_   : string;
end;

class TWsFi
  var FIID   : integer;
  var FIKind;
  var FICode : string;
  var FIName : string;
end;

// класс "связанный лицевой счет"
class TWsLinkedAccount
  var ObChapter;
  var Fi;
  var AccountID;
  var Account;
  var AccountName;
end;

// класс "Связанный объект"
class TWsLinkedObj
  var Type_       : integer;
  var ID          : integer;
  var Code_       : string;
  var Name_       : string;
  var Account;  //: TWsLinkedAccount
end;

// класс "Связи объекта"
class TWsObjLink
  var LinkID : integer;        // Идентификатор связи
  var ObjRole;                 //: TWsObjRole
  var ObjectID : integer;       // Объект связи №1
  var LinkedObject;            //: TWsLinkedObj  // Объект связи №2
  var ValidFromDate : date;    // Дата начала действия связи
  var ValidToDate: date;       // Дата окончания действия связи
  var OperNum;                 // Номер операциониста
  var OperFIO;                 // ФИО операциониста
end;

class TWsObjLinks
  var ObjectType : integer;
  var ObjLinks;
end;

class TWsObjRoles
  var ObjectType : integer;
  var ObjRoles;
end;

class TWsObjectLinkInfo
  var ObjectTypeName : string;
  var ObjLinkTypes;
  var ObjLinksCollection;
  var ObjRolesCollection;
  var ObChaptersCollection;
  var FIKindsCollection;
end;

class TWsObjLinkOut
  var linkid;
  var groupid;
  var objecttype;
  var objectid;
  var attrtype;
  var attrid;
  var validfromdate;
  var validtodate;
  var oper;
  var action;
end;

/*
 Копирование значений полей текущей записи Recordset'а по таблице
 в свойства объекта TRecHandler по одноименной таблице.
 Требование к полям Recordset'а:
   - имена полей Recordset'а должны совпадать с именами полей TRecHandler
     и иметь префикс "t_": значение поля Recordset'а с именем t_partyid
     в TRecHandler будет скопировано в поле partyid
*/
macro _CopyRecordsetToRecHandler(rs, rh)
  var ifld = 0;
  var nfld = FldNumber(rh);
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    val = rs.value("t_" + FldName(rh, ifld));
    if(ValType(GenGetProp(rh.rec, FldName(rh, ifld))) == V_STRING)
      GenSetProp(rh.rec, FldName(rh, ifld), SQL_ConvTypeStr(val));
    elif((val != null) and (val != nullval))
      GenSetProp(rh.rec, FldName(rh, ifld), rs.value("t_" + FldName(rh, ifld), null, ValType(GenGetProp(rh.rec, FldName(rh, ifld)))));
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro CB_GetObjLinkTypes( ObjectType )

  var Query = " SELECT t.t_objecttype FROM dobjects_dbt t "
              " WHERE t.t_ObjectType IN (select objrole.t_ObjectType "
              "  FROM dobjrole_dbt objrole WHERE objrole.t_AttrType = ? "
              "  AND objrole.t_IsPeer = chr(0) AND objrole.t_BiDirection = chr(88)) "
              " OR t.t_ObjectType IN (select objrole.t_AttrType "
              "  FROM dobjrole_dbt objrole WHERE objrole.t_ObjectType = ? ) "
              " ORDER BY t.t_objecttype ";

  var cmd = RsdCommand( Query );

  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, ObjectType );

  var rs = rsdRecordSet( cmd );

  var result = TArray();

  while( rs.moveNext() )
    result.value(result.Size) = rs.value("t_objecttype");
  end;

  return result;

end;

private macro CB_GetObChapters()

  var Query = " SELECT * FROM dobchaptr_dbt ";
  var cmd = RsdCommand( Query );
  var rs = rsdRecordSet( cmd );

  var result = TArray();

  rs.Command.NullConversion = true;

  while( rs.moveNext() )
    var chapter = TWsObChapter;

    chapter.Chapter = rs.value("t_chapter");
    chapter.Name_   = rs.value("t_name");

    result.value(result.Size) = chapter;

  end;

  return result;

end;

private macro CB_GetFIKinds()

  var Query = " SELECT * FROM dfikinds_dbt ";
  var cmd = RsdCommand( Query );
  var rs = rsdRecordSet( cmd );

  var result = TArray();

  rs.Command.NullConversion = true;

  while( rs.moveNext() )
    var fikind = TWsFIKind;

    fikind.Fi_Kind = rs.value("t_fi_kind");
    fikind.Name_   = rs.value("t_name");

    result.value(result.Size) = fikind;

  end;

  return result;

end;

private macro GetObjectTypeName(ObjectType)

  var ObjectTypeName = "";

  var query = " SELECT t_name " +
              "   FROM DOBJECTS_DBT " +
              "  WHERE t_objecttype = " + string(ObjectType);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())

    ObjectTypeName = ds.name;

  end;

  return ObjectTypeName;

end;

private macro GetObjLinkTypes( ObjectType )

  var Query = " SELECT t.t_objecttype FROM dobjects_dbt t "
              " WHERE t.t_ObjectType IN (select objrole.t_ObjectType "
              "  FROM dobjrole_dbt objrole WHERE objrole.t_AttrType = ? "
              "  AND objrole.t_IsPeer = chr(0) AND objrole.t_BiDirection = chr(88)) "
              " OR t.t_ObjectType IN (select objrole.t_AttrType "
              "  FROM dobjrole_dbt objrole WHERE objrole.t_ObjectType = ? ) "
              " ORDER BY t.t_objecttype ";

  var cmd = RsdCommand( Query );

  cmd.NullConversion = true;

  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, ObjectType );

  var rs = rsdRecordSet( cmd );

  var result = TArray();

  while( rs.moveNext() )
    result.value(result.Size) = rs.value("t_objecttype");
  end;

  return result;

end;

private macro CB_GetObjLinks( ObjectType, LinkedObjType, ObjectID )
  var ValidToDate = date(31,12,9999);
  
  var Query1 = "";
  var Query1a = "";
  var Query1b = "";
  var Query2a = "";
  var Query2b = "";
  var Query3 = "";

  var Query = " SELECT * FROM ( ";

  Query1 =  " SELECT t.*, NVL(role.t_Name, chr(1)) As RoleName, "
            "   NVL(role.t_NameAttr, chr(1)) As NameAttr,  "
            "   NVL(role.t_KeepOldValues, chr(0)) As KeepOldValues, "
            "   NVL(role.t_BiDirection, chr(0)) As BiDirection, "
            "   NVL(role.t_IsPeer, chr(0)) As IsPeer, "
            "   person.t_Name As OperName ";

  // дополнительные поля по связанному объекту
  if(    (LinkedObjType == OBJTYPE_CURRENCY)  // валюта
      OR (LinkedObjType == OBJTYPE_AVOIRISS)  // ценная бумага
      OR (LinkedObjType == OBJTYPE_METAL)     // драгметаллы
    )
    Query1 = Query1 + ", fi.t_FI_Code As FICode, fi.t_Name As FIName, fi.t_FI_Kind ";

  elif( LinkedObjType == OBJTYPE_PARTY )
    Query1 = Query1 + ", RSI_RSBPARTY.GetPartyCode(pt.t_PartyID, 1) As PTCode, pt.t_Name As PTName ";

  elif( LinkedObjType == OBJTYPE_ACCOUNT )
    Query1 = Query1 + ", acc.t_Chapter, acc.t_Code_Currency, acc.t_AccountID, acc.t_Account, acc.t_NameAccount, "
                    " fi.t_FI_Code, fi.t_Name As FiName, fi.t_FI_Kind, chapt.t_Name As ChaptName ";
  end;

  Query1 = Query1 + " FROM dobjlink_dbt t, dobjrole_dbt role, dperson_dbt person ";


  // связанные таблицы
  if(    (LinkedObjType == OBJTYPE_CURRENCY)  // валюта
      OR (LinkedObjType == OBJTYPE_AVOIRISS)  // ценная бумага
      OR (LinkedObjType == OBJTYPE_METAL)     // драгметаллы
    )
    Query1 = Query1 + ", dfininstr_dbt fi ";

  elif( LinkedObjType == OBJTYPE_PARTY )
    Query1 = Query1 + ", dparty_dbt pt ";

  elif( LinkedObjType == OBJTYPE_ACCOUNT )
    Query1 = Query1 + ", daccount_dbt acc, dfininstr_dbt fi, dobchaptr_dbt chapt ";
  end;
  
  Query1 = Query1 + 
            " WHERE t.t_ObjectType = role.t_ObjectType(+) "
            "   AND t.t_GroupID = role.t_GroupID(+) "
            "   AND t.t_Oper = person.t_Oper ";
            
  Query1a = "   AND t.t_AttrType = ? ";
  Query1b = "   AND t.t_ObjectType = ? ";

  // соединение со связанными таблицами
  if(    (LinkedObjType == OBJTYPE_CURRENCY)  // валюта
      OR (LinkedObjType == OBJTYPE_AVOIRISS)  // ценная бумага
      OR (LinkedObjType == OBJTYPE_METAL)     // драгметаллы
    )
    Query1 = Query1 + " AND fi.t_FIID = to_number(t.t_AttrID) AND fi.t_FI_Kind = ? ";

  elif( LinkedObjType == OBJTYPE_PARTY )
    Query1 = Query1 + " AND pt.t_PartyID = to_number(t.t_AttrID) ";

  elif( LinkedObjType == OBJTYPE_ACCOUNT )
    
    Query2a = " AND LENGTH(t.t_AttrID) > 10 "
              " AND acc.t_Chapter = to_number(substr( t.t_AttrID, 1, 2)) "
              " AND acc.t_Code_Currency = to_number(substr( t.t_AttrID, 3, 7), 'xxxxxxx') "
              " AND acc.t_Account = substr( t.t_AttrID, 10) "

              " AND fi.t_FIID = acc.t_Code_Currency "
              " AND chapt.t_Chapter = acc.t_Chapter ";

    
    Query2b = " AND LENGTH(t.t_ObjectID) > 10 "
              " AND acc.t_Chapter = to_number(substr( t.t_ObjectID, 1, 2)) "
              " AND acc.t_Code_Currency = to_number(substr( t.t_ObjectID, 3, 7), 'xxxxxxx') "
              " AND acc.t_Account = substr( t.t_ObjectID, 10) "

              " AND fi.t_FIID = acc.t_Code_Currency "
              " AND chapt.t_Chapter = acc.t_Chapter ";
  end;
  
  Query3 = "  AND (t.t_AttrType = ?      "
           "  AND t.t_ObjectType = ?     "
           "  AND t.t_ObjectID = ?       "
           "  OR t.t_GroupID IN          "
           "     (SELECT objrole.t_GroupID "
           "        FROM dobjrole_dbt objrole "
           "        WHERE objrole.t_AttrType = ? "
           "          AND objrole.t_ObjectType = ? "
           "          AND objrole.t_BiDirection = CHR (88)) "
           "   AND t.t_AttrID = ? )    "
           "   AND t.t_ValidToDate = ? ";

  if( (ObjectType == OBJTYPE_PARTY) AND (LinkedObjType == OBJTYPE_ACCOUNT) )
  
    Query = Query + Query1 + Query1a + Query2a + Query3 + " UNION ALL " + Query1 + Query1b + Query2b + Query3 + " ) tt ORDER BY tt.t_linkid";
  
  else
  
    Query = Query1 + Query1a + Query2a + Query3 + " ORDER BY t.t_linkid ";
  
  end;

  println("CB_GetObjLinks");
  println(Query);

  var cmd = RsdCommand( Query );

  cmd.NullConversion = true;
  
  if( LinkedObjType == OBJTYPE_CURRENCY )
    cmd.AddParam( "", RSDBP_IN, FIKIND_CURRENCY );
  elif( LinkedObjType == OBJTYPE_AVOIRISS )
    cmd.AddParam( "", RSDBP_IN, FIKIND_AVOIRISS );
  elif( LinkedObjType == OBJTYPE_METAL )
    cmd.AddParam( "", RSDBP_IN, FIKIND_METAL );
  end;

  cmd.AddParam( "", RSDBP_IN, LinkedObjType );

  cmd.AddParam( "", RSDBP_IN, LinkedObjType );
  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, ObjectID );

  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, LinkedObjType );
  cmd.AddParam( "", RSDBP_IN, ObjectID );

  cmd.AddParam( "", RSDBP_IN, ValidToDate );
  

  if( LinkedObjType == OBJTYPE_CURRENCY )
    cmd.AddParam( "", RSDBP_IN, FIKIND_CURRENCY );
  elif( LinkedObjType == OBJTYPE_AVOIRISS )
    cmd.AddParam( "", RSDBP_IN, FIKIND_AVOIRISS );
  elif( LinkedObjType == OBJTYPE_METAL )
    cmd.AddParam( "", RSDBP_IN, FIKIND_METAL );
  end;

  cmd.AddParam( "", RSDBP_IN, LinkedObjType );

  cmd.AddParam( "", RSDBP_IN, LinkedObjType );
  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, ObjectID );

  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, LinkedObjType );
  cmd.AddParam( "", RSDBP_IN, ObjectID );

  cmd.AddParam( "", RSDBP_IN, ValidToDate );

  var rs = rsdRecordSet( cmd );

  var result = TArray();
  
  while( rs.moveNext() )

    var objLink = TWsObjLink;

    objLink.LinkID = rs.value("t_linkid");
    objLink.ObjectID = rs.value("t_objectid");
    objLink.ValidFromDate = rs.value("t_validfromdate");
    objLink.ValidToDate = rs.value("t_validtodate");
    objLink.OperNum = rs.value("t_oper");
    objLink.OperFIO = rs.value("OperName");


    objLink.LinkedObject = TWsLinkedObj;
    objLink.LinkedObject.Type_ = rs.value("t_attrtype");
    objLink.LinkedObject.ID   = rs.value("t_attrid");

  if(    (LinkedObjType == OBJTYPE_CURRENCY)  // валюта
      OR (LinkedObjType == OBJTYPE_AVOIRISS)  // ценная бумага
      OR (LinkedObjType == OBJTYPE_METAL)     // драгметаллы
    )
      objLink.LinkedObject.Code_ = rs.value("FICode");
      objLink.LinkedObject.Name_ = rs.value("FIName");

      objLink.LinkedObject.Account = TWsLinkedAccount;
      objLink.LinkedObject.Account.Fi = TWsFi;
      objLink.LinkedObject.Account.Fi.FIKind = TWsFIKind;
      objLink.LinkedObject.Account.Fi.FIKind.Fi_Kind = rs.value("t_FI_Kind");
      objLink.LinkedObject.Account.Fi.FIID = rs.value("t_attrid");
      objLink.LinkedObject.Account.Fi.FICode = rs.value("FICode");
      objLink.LinkedObject.Account.Fi.FIName = rs.value("FIName");

    elif( LinkedObjType == OBJTYPE_PARTY )
      objLink.LinkedObject.Code_ = rs.value("PTCode");
      objLink.LinkedObject.Name_ = rs.value("PTName");

    elif( LinkedObjType == OBJTYPE_ACCOUNT )
      objLink.LinkedObject.Account = TWsLinkedAccount;

      objLink.LinkedObject.Account.ObChapter = TWsObChapter;
      objLink.LinkedObject.Account.ObChapter.Chapter = rs.value("t_Chapter");
      objLink.LinkedObject.Account.ObChapter.Name_ = rs.value("ChaptName");

      objLink.LinkedObject.Account.Fi = TWsFi;
      objLink.LinkedObject.Account.Fi.FIID = rs.value("t_Code_Currency");
      objLink.LinkedObject.Account.Fi.FICode = rs.value("t_FI_Code");
      objLink.LinkedObject.Account.Fi.FIName = rs.value("FiName");

      objLink.LinkedObject.Account.Fi.FIKind = TWsFIKind;
      objLink.LinkedObject.Account.Fi.FIKind.Fi_Kind = rs.value("t_FI_Kind");

      objLink.LinkedObject.Account.AccountID = rs.value("t_AccountID");
      objLink.LinkedObject.Account.Account = rs.value("t_Account");
      objLink.LinkedObject.Account.AccountName = rs.value("t_NameAccount");
      
      objLink.LinkedObject.ID = objLink.LinkedObject.Account.AccountID;
    end;

    objLink.objRole = TWsObjRole;
    objLink.objRole.ObjectType      = rs.value("t_objecttype");
    objLink.objRole.GroupID         = rs.value("t_groupid");
    objLink.objRole.LinkedObjType   = rs.value("t_attrtype");
    objLink.objRole.RoleName        = rs.value("RoleName");
    objLink.objRole.KeepOldValues   = SQL_ConvTypeBool(rs.value("KeepOldValues"));
    objLink.objRole.LinkedRoleName  = rs.value("NameAttr");
    objLink.objRole.BiDirection     = SQL_ConvTypeBool(rs.value("BiDirection"));
    objLink.objRole.IsPeer          = SQL_ConvTypeBool(rs.value("IsPeer"));

    result.value(result.Size) = objLink;

  end;

  return result;

end;

/// Получение ролей объекта
/// <param name="ObjectType">Тип объекта №1</param>
/// <param name="LinkedObjType">Тип объекта №2</param>
private macro CB_GetObjRoles( ObjectType, LinkedObjType )

  var Query = " SELECT * FROM dobjrole_dbt t1                   "
              " WHERE t1.t_AttrType = ? AND t1.t_ObjectType = ? "
              "   OR t1.t_GroupID IN (SELECT t2.t_GroupID       "
              "     FROM dobjrole_dbt t2             "
              "     WHERE     t2.t_AttrType = ?   "
              "           AND t2.t_ObjectType = ?   "
              "           AND t2.t_IsPeer = chr(0)   "
              "           AND t2.t_BiDirection = chr(88)) "
              " ORDER BY t1.t_objecttype, t1.t_groupid    ";

  var cmd = RsdCommand( Query );

  cmd.NullConversion = true;

  cmd.AddParam( "", RSDBP_IN, LinkedObjType );
  cmd.AddParam( "", RSDBP_IN, ObjectType );

  cmd.AddParam( "", RSDBP_IN, ObjectType );
  cmd.AddParam( "", RSDBP_IN, LinkedObjType );

  var rs = rsdRecordSet( cmd );

  var result = TArray();

  while( rs.moveNext() )
    var objRole = TWsObjRole;

    objRole.ObjectType = rs.value("t_objecttype");
    objRole.GroupID    = rs.value("t_groupid");
    objRole.LinkedObjType = rs.value("t_attrtype");
    objRole.RoleName      = rs.value("t_name");
    objRole.KeepOldValues = SQL_ConvTypeBool(rs.value("t_keepoldvalues"));
    objRole.LinkedRoleName = rs.value("t_nameattr");
    objRole.BiDirection    = SQL_ConvTypeBool(rs.value("t_bidirection"));
    objRole.IsPeer         = SQL_ConvTypeBool(rs.value("t_ispeer"));

    result.value(result.Size) = objRole;

  end;

  return result;

end;

private macro GetRecFIByID(FIID : integer);
  var cmd, rs;

  var Rec = TRecHandler("fininstr");

  var query = "select * from dfininstr_dbt where t_fiid = " + string(FIID);

  cmd = RsdCommand(query);
  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro GetRecPartyByID(PartyID : integer);
  var cmd, rs;

  var Rec = TRecHandler("party");

  var query = "select * from dparty_dbt where t_partyid = " + string(PartyID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro GetRecAccountByID(AccountID : integer);
  var cmd, rs;

  var Rec = TRecHandler("account");

  var query = "select * from daccount_dbt where t_accountid = " + string(AccountID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro GetRecDealByID(DealID : integer);
  var cmd, rs;

  var Rec = TRecHandler("dl_leg");

  var query = "select * from ddl_leg_dbt where t_id = " + string(DealID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

macro GetObjectIDStr(ObjectType, ObjectID) : string

  var ObjectIDStr = "";

  if( (ObjectType == OBJTYPE_CURRENCY) OR (ObjectType == OBJTYPE_AVOIRISS) OR (ObjectType == OBJTYPE_METAL) )

    var RecFi = GetRecFIByID(ObjectID);

    ObjectIDStr = UniID(RecFi, ObjectType);

  end;

  if(ObjectType == OBJTYPE_PARTY)

    var RecParty = GetRecPartyByID(ObjectID);

    ObjectIDStr = UniID(RecParty, ObjectType);

  end;

  if(ObjectType == OBJTYPE_ACCOUNT)

    var RecAccount = GetRecAccountByID(ObjectID);

    ObjectIDStr = UniID(RecAccount, ObjectType);

  end;

  if(ObjectType == 101)

    var RecDeal = GetRecDealByID(ObjectID);

    ObjectIDStr = UniID(RecDeal, ObjectType);

  end;

  return ObjectIDStr;

end;

macro GetObjectLinkInfo(ObjectType : integer, ObjectID : integer)
  var responseBuilder = WebResponseBuilder;
  var error           = WsErrorMessage;
  var question        = WsQuestion;

  var ObjectIDStr = GetObjectIDStr(ObjectType, ObjectID);

  var p = TWsObjectLinkInfo();

  p.ObjectTypeName      = GetObjectTypeName(ObjectType);
  p.ObjLinkTypes        = GetObjLinkTypes(ObjectType);

  p.ObjLinksCollection  = TArray;
  p.ObjLinksCollection.size = 5;

  var i:integer = 0;

  while( i < p.ObjLinkTypes.size() )

    var OL = TWsObjLinks();

    OL.ObjectType = p.ObjLinkTypes[i];
    OL.ObjLinks = CB_GetObjLinks(ObjectType, p.ObjLinkTypes[i], ObjectIDStr);

    if(OL.ObjectType == OBJTYPE_CURRENCY)
      p.ObjLinksCollection[0] = OL;
    end;

    if(OL.ObjectType == OBJTYPE_PARTY)
      p.ObjLinksCollection[1] = OL;
    end;

    if(OL.ObjectType == OBJTYPE_ACCOUNT)
      p.ObjLinksCollection[2] = OL;
    end;

    if(OL.ObjectType == OBJTYPE_AVOIRISS)
      p.ObjLinksCollection[3] = OL;
    end;

    if(OL.ObjectType == OBJTYPE_METAL)
      p.ObjLinksCollection[4] = OL;
    end;

    i = i + 1;

  end;

  p.ObjRolesCollection = TArray;
  p.ObjRolesCollection.size = 5;

  i = 0;

  while( i < p.ObjLinkTypes.size() )

    var ObjR = TWsObjRoles();

    ObjR.ObjectType = p.ObjLinkTypes[i];
    ObjR.ObjRoles = CB_GetObjRoles(ObjectType, p.ObjLinkTypes[i]);

    if(ObjR.ObjectType == OBJTYPE_CURRENCY)
      p.ObjRolesCollection[0] = ObjR;
    end;

    if(ObjR.ObjectType == OBJTYPE_PARTY)
      p.ObjRolesCollection[1] = ObjR;
    end;

    if(ObjR.ObjectType == OBJTYPE_ACCOUNT)
      p.ObjRolesCollection[2] = ObjR;
    end;

    if(ObjR.ObjectType == OBJTYPE_AVOIRISS)
      p.ObjRolesCollection[3] = ObjR;
    end;

    if(ObjR.ObjectType == OBJTYPE_METAL)
      p.ObjRolesCollection[4] = ObjR;
    end;

    i = i + 1;

  end;

  p.ObChaptersCollection = CB_GetObChapters();
  p.FIKindsCollection    = CB_GetFIKinds();

  responseBuilder.SetUserObject(p);

  return responseBuilder.Result;

end;

macro InsUpdOBJLINK( ObjLink, ObjLinkSave )

  var stat = 0;

  var ol = Tbfile ("objlink", "W", 0);
  ol.Clear();

  var ols = Tbfile ("objlink", "W", 0);
  ols.Clear();

  ol.rec.groupid       = ObjLink.groupid;
  ol.rec.objecttype    = ObjLink.objecttype;
  ol.rec.objectid      = GetObjectIDStr(ol.rec.objecttype, ObjLink.objectid);
  ol.rec.attrtype      = ObjLink.attrtype;
  ol.rec.attrid        = GetObjectIDStr(ol.rec.attrtype, ObjLink.attrid);
  ol.rec.validfromdate = ObjLink.validfromdate;
  ol.rec.validtodate   = ObjLink.validtodate;
  ol.rec.oper          = ObjLink.oper;

  ols.rec.groupid       = ObjLinkSave.groupid;
  ols.rec.objecttype    = ObjLinkSave.objecttype;
  ols.rec.objectid      = GetObjectIDStr(ols.rec.objecttype, ObjLinkSave.objectid);
  ols.rec.attrtype      = ObjLinkSave.attrtype;
  ols.rec.attrid        = GetObjectIDStr(ols.rec.attrtype, ObjLinkSave.attrid);
  ols.rec.validfromdate = ObjLinkSave.validfromdate;
  ols.rec.validtodate   = ObjLinkSave.validtodate;
  ols.rec.oper          = ObjLinkSave.oper;

  stat = InsertUpdateOBJLINK( ol.rec, ols.rec );

  if(stat != 0)
    RunError("Ошибка сохранения - " + string(stat));
  end;

  return stat;

end;

macro SaveObjLinkList( ObjLinkOutList, ObjLinkOutListSave )

  var responseBuilder = WebResponseBuilder;
  
  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var stat = 0;

  file olf("objlink.dbt");
  file olsf("objlink.dbt");

  var objlinkvalue = Tbfile ("objlink", "W", 0);
  objlinkvalue.Clear();

  var objlinkvaluesave = Tbfile ("objlink", "W", 0);
  objlinkvaluesave.Clear();

  var i = 0;

  while(i < ObjLinkOutList.size)

    if(ObjLinkOutList[i].action == CREATE)

      objlinkvalue.rec.groupid       = ObjLinkOutList[i].groupid;
      objlinkvalue.rec.objecttype    = ObjLinkOutList[i].objecttype;
      objlinkvalue.rec.objectid      = GetObjectIDStr(objlinkvalue.rec.objecttype, ObjLinkOutList[i].objectid);
      objlinkvalue.rec.attrtype      = ObjLinkOutList[i].attrtype;
      objlinkvalue.rec.attrid        = GetObjectIDStr(objlinkvalue.rec.attrtype, ObjLinkOutList[i].attrid);
      objlinkvalue.rec.validfromdate = ObjLinkOutList[i].validfromdate;
      objlinkvalue.rec.validtodate   = ObjLinkOutList[i].validtodate;
      objlinkvalue.rec.oper          = ObjLinkOutList[i].oper;

      Copy(olf, objlinkvalue);

      stat = InsertUpdateOBJLINK(olf, null);

      if(stat != 0)
        break;
      else
        objlinkvalue.Clear();
      end;

    end;

    if(ObjLinkOutList[i].action == MODIFY)

      objlinkvalue.rec.linkid   = ObjLinkOutList[i].linkid;

      stat = GetEQ( objlinkvalue );

      if(stat == true)

        objlinkvalue.rec.groupid       = ObjLinkOutList[i].groupid;
        objlinkvalue.rec.objecttype    = ObjLinkOutList[i].objecttype;
        objlinkvalue.rec.objectid      = GetObjectIDStr(objlinkvalue.rec.objecttype, ObjLinkOutList[i].objectid);
        objlinkvalue.rec.attrtype      = ObjLinkOutList[i].attrtype;
        objlinkvalue.rec.attrid        = GetObjectIDStr(objlinkvalue.rec.attrtype, ObjLinkOutList[i].attrid);
        objlinkvalue.rec.validfromdate = ObjLinkOutList[i].validfromdate;
        objlinkvalue.rec.validtodate   = ObjLinkOutList[i].validtodate;
        objlinkvalue.rec.oper          = ObjLinkOutList[i].oper;

        Copy(olf, objlinkvalue);

        var k = 0;

        while(k < ObjLinkOutListSave.size)

          if(ObjLinkOutList[i].linkid == ObjLinkOutListSave[k].linkid)

            objlinkvaluesave.rec.linkid        = ObjLinkOutListSave[k].linkid;
            objlinkvaluesave.rec.groupid       = ObjLinkOutListSave[k].groupid;
            objlinkvaluesave.rec.objecttype    = ObjLinkOutListSave[k].objecttype;
            objlinkvaluesave.rec.objectid      = GetObjectIDStr(objlinkvaluesave.rec.objecttype, ObjLinkOutListSave[k].objectid);
            objlinkvaluesave.rec.attrtype      = ObjLinkOutListSave[k].attrtype;
            objlinkvaluesave.rec.attrid        = GetObjectIDStr(objlinkvaluesave.rec.attrtype, ObjLinkOutListSave[k].attrid);
            objlinkvaluesave.rec.validfromdate = ObjLinkOutListSave[k].validfromdate;
            objlinkvaluesave.rec.validtodate   = ObjLinkOutListSave[k].validtodate;
            objlinkvaluesave.rec.oper          = ObjLinkOutListSave[k].oper;

            Copy(olsf, objlinkvaluesave);

            break;

          end;

          k = k + 1;

        end;

        stat = InsertUpdateOBJLINK(olf, olsf);

        if(stat != 0)
          break;
        else
          objlinkvalue.Clear();
        end;

      else
        break;
      end;

    end;

    if(ObjLinkOutList[i].action == REMOVE)

      olf.linkid   = ObjLinkOutList[i].linkid;

      stat = GetEQ( olf );

      if(stat == true)

        stat = DeleteOBJLINK(olf);

        if(stat != 0)

          break;

        end;

      else
        stat = 1;
        break;
      end;

    end;

    i = i + 1;

  end;

  if( stat != 0 )

    ErrorMessage.Code     = 1;
    ErrorMessage.Message  = "Ошибка сохранения в БД.";
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    responseBuilder.AddMessage( ErrorMessage );

  end;

  responseBuilder.SetUserObject(stat);

  return responseBuilder.Result;  

end;

macro CheckObjLinkForSave( ObjectType, AccountID, PartyID, FininstrID, ObjLink, ObjLinkSave )

  var er = true;;
  var stat = 0;

  file acc("account.dbt") key 1;
  file pt("party.dbt");
  file fi("fininstr.dbt");

  file olf("objlink.dbt");
  file olsf("objlink.dbt");

  var ol = Tbfile ("objlink", "W", 0);
  ol.Clear();

  var ols = Tbfile ("objlink", "W", 0);
  ols.Clear();

  if(ObjLink != null)
    ol.rec.groupid       = ObjLink.groupid;
    ol.rec.objecttype    = ObjLink.objecttype;
    ol.rec.objectid      = GetObjectIDStr(ol.rec.objecttype, ObjLink.objectid);
    ol.rec.attrtype      = ObjLink.attrtype;
    ol.rec.attrid        = GetObjectIDStr(ol.rec.attrtype, ObjLink.attrid);
    ol.rec.validfromdate = ObjLink.validfromdate;
    ol.rec.validtodate   = ObjLink.validtodate;
    ol.rec.oper          = ObjLink.oper;

    Copy(olf, ol);
  end;

  if(ObjLinkSave != null)
    ols.rec.groupid       = ObjLinkSave.groupid;
    ols.rec.objecttype    = ObjLinkSave.objecttype;
    ols.rec.objectid      = GetObjectIDStr(ols.rec.objecttype, ObjLinkSave.objectid);
    ols.rec.attrtype      = ObjLinkSave.attrtype;
    ols.rec.attrid        = GetObjectIDStr(ols.rec.attrtype, ObjLinkSave.attrid);
    ols.rec.validfromdate = ObjLinkSave.validfromdate;
    ols.rec.validtodate   = ObjLinkSave.validtodate;
    ols.rec.oper          = ObjLinkSave.oper;

    Copy(olsf, ols);
  end;

  if(AccountID != -1)

    acc.accountid = AccountID;
    er = GetEQ( acc );

    if(er == true)

      stat = CheckObjLinkIE(ObjectType, acc, null, null, olf, olsf);

    else

      stat = -1;

    end;

  end;

  if(PartyID != -1)

    pt.partyid = PartyID;
    er = GetEQ( pt );

    if(er == true)

      stat = CheckObjLinkIE(ObjectType, null, pt, null, olf, olsf);

    else

      stat = -1;

    end;

  end;

  if(FininstrID != -1)

    fi.fiid = FininstrID;
    er = GetEQ( fi );

    if(er == true)

      stat = CheckObjLinkIE(ObjectType, null, null, fi, olf, olsf);

    else

      stat = -1;

    end;

  end;

  if(stat != 0)
    RunError("Ошибка проверки - " + string(stat));
  end;

  return stat;

end;

macro CheckObjLinkForDelete( ObjLinkID )

  var er = true;;
  var stat = 0;

  file ol("objlink.dbt") key 0;

  if(ObjLinkID != -1)

    ol.linkid = ObjLinkID;
    er = GetEQ( ol );

    if(er == true)

      stat = CheckObjLinkD(ol);

    else

      stat = -1;

    end;

  end;

  if(stat != 0)
    RunError("Ошибка проверки - " + string(stat));
  end;

  return stat;

end;