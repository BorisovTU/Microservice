
import PaymInter, pm_tools;

// Класс для передачи в функцию ExecFunForEachPIInList
// с параметром-функцией GetPIAmount или GetPIPmAmount
// поле SumPI для сохранения суммы по всем уточю записям

private class ( TExecFunPIParm ) SumPIParm
  var SumPI:money = 0;
  InitTExecFunPIParm();
end;

  
MACRO IsSummaryPayment( Payment:RsbPayment ):bool
  if( ( Payment.PIList(0).Size > 0 ) or ( Payment.PIList(1).Size > 0 ) )
    return true;
  else
    return false;
  end;
END;

// Определить сторону разноски платежа
MACRO SidePaymentDeliver( Payment:RsbPayment ):integer
  if( Payment.PIList(0).Size > 0 )
    return 0;
  elif( ( Payment.PIList(1).Size > 0 ) )
    return 1;
  end;
  return -1;
END;

// Класс для копирование уточ. записей из одного платежа в другой
private class ( TExecFunPIParm ) CopyPIParm( _IntoPaym:RsbPayment, _DC:integer )
  var DC      :integer    = _DC;    // ДебетКредит
  var IntoPaym:RsbPayment = _IntoPaym; // платеж в который копировать
  InitTExecFunPIParm();
end;

private macro pmsummo_CopyAddPI( parm : CopyPIParm ):integer
   parm.pi.rec.PmAddPIID = 0;
   parm.pi.rec.PaymentID = parm.IntoPaym.PaymentID;
   return parm.IntoPaym.PIList( parm.DC ).Insert( parm.pi );
end;  


MACRO CopyPIFromPaymIntoOthePaym( FromPaym:RsbPayment, IntoPaym:RsbPayment, DC:integer ): integer
   var parm:CopyPIParm = CopyPIParm( IntoPaym, DC );
   ExecFunForEachPIInList( FromPaym, DC, null, @pmsummo_CopyAddPI, parm );
   return parm.stat;
END;

private macro pmsummo_GetPIAmount( parm : SumPIParm ):money
  parm.SumPI = ( parm.SumPI + parm.pi.rec.Amount );
  return 0;
end;

private macro pmsummo_GetPIPmAmount( parm : SumPIParm ):money
  parm.SumPI = ( parm.SumPI + parm.pi.rec.PmAmount );
  return 0;
end;

MACRO GetSumPIList( Payment:RsbPayment, DC:integer, IsPmAmount:bool ): money
   var Sum : SumPIParm = SumPIParm();
   if( IsPmAmount == true )
     ExecFunForEachPIInList( Payment, DC, null, @pmsummo_GetPIPmAmount, Sum );
   else
     ExecFunForEachPIInList( Payment, DC, null, @pmsummo_GetPIAmount,   Sum );
   end;
   return Sum.SumPI;
END;

macro pmsummo_IsAddPiInCurDep( parm : TExecFunPIParm ):integer
  var Payment:RsbPayment = RsbPayment( parm.pi.rec.PaymentID );
  
  if( parm.pi.rec.Department == Payment.Department )
    return 1;
  else
    return 0;
  end;
end;  

// по платежу есть разноска в текущем филиале платежа 
macro IsExistAddPiInCurDep( Payment : RsbPayment, DC ):bool
  var parm : TExecFunPIParm;

  if( ValType(DC) == V_UNDEF )
    if( ExecFunForEachPIInList(Payment, PRT_Debet , null, @pmsummo_IsAddPiInCurDep, parm) or
        ExecFunForEachPIInList(Payment, PRT_Credit, null, @pmsummo_IsAddPiInCurDep, parm)
      )
      return true;
    end;
  else
    if( ExecFunForEachPIInList(Payment, DC , null, @pmsummo_IsAddPiInCurDep, parm) )
      return true;
    end;
  end;

  return false;
end;


