/*
 *     RS-Bank 5.1
 *
 *     Функции печати отчета по журналу операций.
 *
 *     18.01.2000 AS : Создан
 *     25.01.2001 YM : Добавил печать заголовка фискального журнала
 */

import BankInter;


CONST
   OLinsrec = 1,                /* Ввод записи         */
   OLdelrec = 2,                /* Удаление записи     */
   OLchgrec = 3,                /* Обновление записи   */
   OLproc   = 4;                /* Процедура           */

CONST
   iOLentry   = 1000,              /* Вход в программу                */
   iOLexit    = 1001,              /* Выход из программы              */
   iOLstrproc = 1002,              /* Начало выполнения процедуры     */
   iOLfinproc = 1003,              /* Окончание выполнения процедуры  */
   iOLbadpwd  = 1100,              /* Неверный пароль */
   iOLblock   = 1101,              /* Блокировка пользователя */
   iOLsysmsg  = 1102;              /* Системное сообщение */



/*
 *  Печать заголовка отчета.
 *   - Параметры функции:
 *      SingleRecord:  0 - отчет по всем записям списка
 *                     1 - отчет по выбранной записи
 *   - Возвращаемое значение:
 *      TRUE:  нормальное выполнение
 *      FALSE: прервать выпуск отчета
 */
macro PrintHeader( SingleRecord )

 [Отчет по журналу аудита за ########## ]( Date() );
 [──────────────────────────────────────────────────────────────────────────];
 [ ];

 return TRUE;

end;


/*  YM 25.01.01
 *  Печать заголовка отчета для фискального журнала.
 *   - Параметры функции:
 *      SingleRecord:  0 - отчет по всем записям списка
 *                     1 - отчет по выбранной записи
 *   - Возвращаемое значение:
 *      TRUE:  нормальное выполнение
 *      FALSE: прервать выпуск отчета
 */
macro PrintHeaderFisc( SingleRecord )

 [Отчет по фискальному журналу за ########## ]( Date() );
 [──────────────────────────────────────────────────────────────────────────];
 [ ];

 return TRUE;

end;



/*
 *  Печать итоговой части отчета.
 *   - Параметры функции:
 *      SingleRecord:  0 - отчет по всем записям списка
 *                     1 - отчет по выбранной записи
 *   - Возвращаемое значение: не имеет значения
 */
macro PrintFooting( SingleRecord )

 [ ];
 [──────────────────────────────────────────────────────────────────────────];
 [Конец отчета ];
 return TRUE;

end;






/*
 *  Печать отчета по конкретной записи в журнале операций
 *   - Параметры функции:
 *      SingleRecord:  0 - отчет по всем записям списка
 *                     1 - отчет по выбранной записи
 *      BufferPtr   :  адрес буфера с копией записи в журнале операций
 *                     (структура operlog.dbt)
 *      RecordType  :  одно из значений OLinsrec, OLdelrec, OLchgrec, OLproc
 *                     (соответственно ввод, удаление, обновление записи в
 *                     btrieve-файле или запись о выполнении некой процедуры)
 *      VarRecord   :  адрес буфера с переменной частью записи журнала
 *      ProcName    :  строка с названием выполненной процедуры
 *      rec1        :  Либо объект TRecHandler, содержащий структуру введенной, удаленной
 *                     записи или записи до изменения, либо V_UNDEF
 *      rec2        :  Либо объект TRecHandler, содержащий структуру записи после изменения,
 *                     либо V_UNDEF.
 *
 *   - Возвращаемое значение:
 *      TRUE:  нормальное выполнение
 *      FALSE: прервать выпуск отчета
 */
macro PrintRecord( SingleRecord, BufferPtr , RecordType, VarRecord, ProcName, rec1, rec2 )
   
   RECORD oplog (operlog);
   RECORD iolog ("tLogIO.str");
   VAR i = 0;
   VAR FieldChanged, Field1, Field2;

   SetBuff( oplog, BufferPtr );

   [ ];
   [ ];

   if ( (RecordType == OLinsrec) or (RecordType == OLdelrec) or (RecordType == OLchgrec) )

      /* удаление, ввод или обновление записи */
      [--- ########## ########  Операционист: ##### Файл: ############ ]
      (oplog.bdDateRec, oplog.btTimeRec, oplog.iOper, oplog.szFileName);
      [ ];

      if ( ValType( rec1 ) != V_UNDEF )
   
         [ ┌────────────────────┬──┬─────────────────────────┬─────────────────────────┐];
         [ │       Поле         │Из│     Старое значение     │     Новое значение      │];
         [ ├────────────────────┼──┼─────────────────────────┼─────────────────────────┤];
   
         while ( i < rec1.FldNumber )
   
            if ( RecordType == OLinsrec )
               FieldChanged = " ";
               Field1 = "";
               Field2 = rec1.(i);
            elif ( RecordType == OLdelrec )
               FieldChanged = " ";
               Field1 = rec1.(i);
               Field2 = "";
            elif ( ValType( rec2 ) != V_UNDEF )
               Field1 = rec1.(i);
               Field2 = rec2.(i);
               if ( Field2 != Field1 )
                  FieldChanged = "X";
               else
                  FieldChanged = " ";
               end;
            else
               /* Неверная запись? */
               FieldChanged = "?";
               Field1 = rec1.(i);
               Field2 = "?";
            end;
   
            [ │####################│##│#########################│#########################│]
            ( rec1.FldName(i) , FieldChanged, Field1, Field2 );
   
            i = i+1;
   
         end;
   
         [ └────────────────────┴──┴─────────────────────────┴─────────────────────────┘];
      else
         [ * Неверная запись в журнале или ошибка формирования структуры ];
      end;
   else
      /* Запись о выполнении той или иной процедуры */
      [--- ########## ########  Операционист: ##### Операция: #### ]
      (oplog.bdDateRec, oplog.btTimeRec, oplog.iOper, oplog.iOperation);
      [ ];

      if ( (oplog.iOperation == iOLentry) or
           (oplog.iOperation == iOLexit)  or
           (oplog.iOperation == iOLbadpwd)  or
           (oplog.iOperation == iOLblock) 
         )

         if (oplog.iOperation == iOLentry) 
            println("  Действие: вход в программу");
         elif (oplog.iOperation == iOLbadpwd)
            println("  Действие: неверный ввод пароля");
         elif (oplog.iOperation == iOLblock)
            println("  Действие: блокировка пользователя");
         else
            println("  Действие: выход из программы");
         end;

         SetBuff( iolog, VarRecord );
         if ( ( iolog.Mark == "\x01" ) or (iolog.Mark == "\x02") )

            println("  Путь к исполняемому модулю: ", iolog.ExePath );
            println("  Название исполняемого модуля: ", iolog.ExeName );
            println("  Размер исполняемого модуля: ", iolog.ExeLen );
            println("  Дата и время исполняемого модуля: ", iolog.ExeDate, ", ", iolog.ExeTime  );
            if ( iolog.Mark == "\x01")
             println("  Версия программы: " , iolog.verHi,".", iolog.verLo, ".", iolog.Build  );
            elif ( iolog.Mark == "\x02")
             println("  Версия программы: " , iolog.verHi,".", iolog.verLo, ".", iolog.Build,".", iolog.subBuild  );
            end;
            println("  Статус: ", iolog.Name_Type );

         end;

      elif ( oplog.iOperation == iOLstrproc )

         println("  Начало выполнения процедуры '", ProcName, "'." );

      elif ( oplog.iOperation == iOLfinproc )

         println("  Завершение выполнения процедуры '", ProcName, "'." );

      elif ( oplog.iOperation == iOLsysmsg )

         println("  Системное сообщение: ", ProcName );

      end;

   end;

   return TRUE;

end;
