/*
$Name: QueueWSLib.mac
$Module: Ядро ГКБО
$Description: API RSL Web Sphere
*/
import "oralib.mac", "globals.mac", "XmlValidator.mac", "email_notifyfun.mac";
import rcw;

const SRV_XR_KIND_IN   = 1; // "Вх.";
const SRV_XR_KIND_OUT  = 3; // "Исх.";

CONST TYPE_QUEUE_IN  = 1; // Входящая
CONST TYPE_QUEUE_OUT = 0; // Исходящая

const DirectionPreffix = "bons2:";
const DirectionAnswer = "ANSWER";
const DirectionRequest = "REQUEST";

const E_MailTitle = "Интеграция с Web Sphere";

private var CNum;

private var GWaitingSyn = -2;

private var SS_RESPONSE_END = 3;


class WSCoverData
    var FromSystem = "";
    var ToSystem = "";
    var Direction = "";
    var FromBranch = "";
    var ToBranch = "";
    var Pass = "";
    var Interaction = "";
    var Service = "";
    var Operation = "";
    var ADUserId = "";

    // блок результата
    var Status = "";
    var Description = "";
    var Code = "";

    //блок Метки целостности
    var HasMAC_info = false;
    var signerId = "";
    var secretKeyId = "";
    var algorithmName = "";
    var macDatetime = "";
    var signer = "";
    var hmac = "";

    var XMLMes = "";
    var XMLTrEn = "";

    var Extention = TArray;
    var SenderID = "";
end;

private class CoverDataExtention
    var name = "";
    var value = "";
end;


private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;



macro GetWaitingSynWS()


  var error = 0;
  if(GWaitingSyn == -2)
    GetRegistryValue( "COMMON\\QUEUE\\WAITING SYNC", V_INTEGER, GWaitingSyn, error );
  end;

  return GWaitingSyn;
end; 


private macro WSWriteXrLog(LogType, Kind, ReqId, Mes, ErrorCode)
  var stat = 0;
  var OutId = 0;
  var cmd;

  if(CNum == NULL)
    var rs = execSQLselect("SELECT RSBSESSIONDATA.CNum FROM dual", null, true);
    if(rs and rs.moveNext())
      CNum = rs.value(0);
    else
      CNum = 0;
    end;
  end;

  /*
  WriteXrLog( p_LogType IN NUMBER, p_Kind IN NUMBER, p_ReqId IN VARCHAR2, p_SessionId IN NUMBER, p_Oper IN NUMBER, p_Date IN DATE, p_Message IN CLOB, 
                         p_ErrorCode IN NUMBER, p_OfflineFile IN VARCHAR, p_OfflineNum IN NUMBER, p_Macro IN VARCHAR2, p_FUNC IN VARCHAR2,
                         p_LogicalId IN VARCHAR2, p_Source IN VARCHAR2,p_Destination IN VARCHAR2, p_Direction IN NUMBER,
                         p_Url IN VARCHAR2, p_Login IN VARCHAR2, p_Method IN VARCHAR2, p_OutId OUT NUMBER) RETURN NUMBER
  */
  cmd = RSDCommand( "{ ? = CALL WriteXrLog(?, ?, ?, ?, ?, ?, ?, ?, NULL, NULL, NULL, NULL, NULL, NULL, NULL, ?, NULL, NULL, NULL, ?) }" );

  cmd.addParam( "retv", RSDBP_OUT, V_INTEGER );
  cmd.addParam( "", RSDBP_IN, LogType   );
  cmd.addParam( "", RSDBP_IN, Kind      );
  cmd.addParam( "", RSDBP_IN, ReqId     );
  cmd.addParam( "", RSDBP_IN, CNum      ); 
  cmd.addParam( "", RSDBP_IN, {oper}    );
  cmd.addParam( "", RSDBP_IN, {curdate} );
  cmd.addParam( "", RSDBP_IN, Mes       );
  cmd.addParam( "", RSDBP_IN, ErrorCode );
//  cmd.addParam( "", RSDBP_IN, ""        );  // OfflineFile
//  cmd.addParam( "", RSDBP_IN, ""        );  // OfflineNum
//  cmd.addParam( "", RSDBP_IN, ""        );  // Macro
//  cmd.addParam( "", RSDBP_IN, ""        );  // FUNC
//  cmd.addParam( "", RSDBP_IN, ""        );  // LogicalId
//  cmd.addParam( "", RSDBP_IN, ""        );  // Source
//  cmd.addParam( "", RSDBP_IN, ""        );  // Destination
  cmd.addParam( "", RSDBP_IN, 0         );  // Direction 
//  cmd.addParam( "", RSDBP_IN, ""        );  // Url
//  cmd.addParam( "", RSDBP_IN, ""        );  // Login
//  cmd.addParam( "", RSDBP_IN, ""        );  // Method
  cmd.addParam( "OutId", RSDBP_OUT, V_INTEGER );  // OutId

  cmd.execute();

  if (cmd.value("retv") != 0)
    stat = cmd.value("retv");
  else
    OutId = cmd.value("OutId");
  end;


  return stat;
end;

macro WriteXrLogQueueWS(Kind, ReqId, Mes, ErrorCode)
  var stat = 0 ;
  var flag = true, err;
  var LogType = 3;  // XR_LOG_TYPE_QUEUEWS  = 3   // Очереди

/*
  GetRegistryValue( "COMMON\\RSXMLRPC\\ЖУРНАЛ ОЧЕРЕДЕЙ", V_BOOL, flag, err );
  if( err != 0 )
    flag = true;
  end;
*/

  if(flag == true)
    stat = WSWriteXrLog(LogType, Kind, ReqId, Mes, ErrorCode);
  end;

  return stat;
end;

macro TransXmlQueueWS( strInit )
   private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");
   return jvm.callStaticMethod("org.apache.commons.lang3.StringEscapeUtils", "escapeXml", strInit);
end;

/*macro TransXmlQueueWS( strInit )
   var str="", count, len = strlen(strInit), symb;
   count = 1;
   while( count<=len )
      symb = substr(strInit, count, 1);

      if ( symb=="<" )
         symb = "&lt;";
      elif ( symb==">" ) // ?? В примере: &lt;DocNumber>92 07 244936&lt;/DocNumber> 
         symb = "&gt;";
      elif ( symb=="&" )
         symb = "&amp;";
      elif ( symb=="\"" )
         symb = "&quot;"; 
      end;

      str = str + symb;
      count = count + 1;
   end;

   return str;
end;*/

macro ValidateXmlXsd(RequestText, FullPathXSD, parser:@string)
    var result = true;

    var validator = XmlValidator();
    validator.loadXML(RequestText);
    validator.addXsd(FullPathXSD);

    var objErr = validator.validate();

    if (objErr.errorCode != 0)
        result = false;
        parser = objErr.reason;
    end;

    return result;
end;


macro MonTrEnWSTimeOutProc()
    var v_result_sheduler;
    var cmd = RsdCommand("SELECT t_ID,t_SysDate,t_SysTime,t_TimeOut FROM dMonTrEnWS_dbt WHERE t_IsTimeOut = CHR (0) AND t_YesAnswer = CHR (0)");
    cmd.execute();

    var QueueDate = Date();
    var QueueTime = Time();
    var rs = RsdRecordset(cmd);
    while (rs.MoveNext())
        var timeduration = WS_GetDateTimeIntervalMsec(Date(rs.value("t_SysDate")), Time(rs.value("t_SysTime")), QueueDate, QueueTime);
        var TimeOut = Int(rs.value("t_TimeOut"));

        if (timeduration > TimeOut)
            var upd = RsdCommand("update dMonTrEnWS_dbt set t_IsTimeOut = chr(88) WHERE t_ID = ?");
            upd.addParam("", RSDBP_IN, rs.value("t_ID"));
            upd.execute();
        end;
    end;

    v_result_sheduler = c_ssRunResult(SS_RESPONSE_END);
    return v_result_sheduler;

end;


// ------------------------------------------------------------------------------------------------------
private macro ReadCoverType1MAC_info(Node : @variant, cdat : @WSCoverData)
    var i = 0;
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (element.nodeName == "se:_hmac")
            cdat.hmac = element.text;
        elif (element.nodeName == "se:_signerId")
            cdat.signerId = element.text;
        elif (element.nodeName == "se:_secretKeyId")
            cdat.secretKeyId = element.text;
        elif (element.nodeName == "se:algorithmName")
            cdat.algorithmName = element.text;
        elif (element.nodeName == "se:macDatetime")
            cdat.macDatetime = element.text;
        elif (element.nodeName == "se:signer")
            cdat.signer = element.text;
        end;
        i = i + 1;
    end;

    cdat.HasMAC_info = true;
end;

private macro ReadCoverType1Parameters(Node : @variant, cdat : @WSCoverData)
    var i = 0;
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        var j = 0;
        var tmpData = CoverDataExtention;

        while (j < element.childNodes.length)
            var paramElement = element.childNodes.item(j);
            if (Index(paramElement.nodeName, "name"))
                tmpData.name = paramElement.text;
            elif (Index(paramElement.nodeName, "name"))
                tmpData.value = paramElement.text;
            end;
            j = j + 1;
        end;

        if (tmpData.name == "DATA")
            cdat.XMLMes = tmpData.value;
        elif (tmpData.name == "SenderID")
            cdat.SenderID = tmpData.value;
        else
            cdat.Extention[cdat.Extention.size] = tmpData;
        end;

        i = i + 1;
    end;
end;

private macro ReadCoverType1(Node : @variant)
    var cdat = WSCoverData();

    var i = 0;
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (element.nodeName == "input")
            cdat.FromSystem = element.getAttribute("fromSystem");
            cdat.ToSystem   = element.getAttribute("toSystem");
            cdat.Direction = element.getAttribute("userDirect");
            cdat.FromBranch = element.getAttribute("fromBranch");
            cdat.ToBranch = element.getAttribute("toBranch");
            cdat.Pass = element.getAttribute("pass");
            cdat.Interaction = element.getAttribute("Interaction");
            cdat.Service = element.getAttribute("service");
            cdat.Operation = element.getAttribute("operation");
            cdat.ADUserId = element.getAttribute("ADUserId");

            var j = 0;
            while (j < element.childNodes.length)
                var inputElement = element.childNodes.item(j);

                if (Index(inputElement.nodeName, "MAC_info"))
                    ReadCoverType1MAC_info(inputElement, @cdat);
                elif (Index(inputElement.nodeName, "parameters"))
                    ReadCoverType1Parameters(inputElement, @cdat);
                end;
                j = j + 1;
            end;
        end;
        i = i + 1;
    end;

    return cdat;
end;

// ------------------------------------------------------------------------------------------------------
private macro ReadCoverType2Header(Node : @variant, cdat : @WSCoverData)
    var i = 0;

    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (Index(element.nodeName, "FromSystem"))
            cdat.FromSystem = element.text;
        elif (Index(element.nodeName, "ToSystem"))
            cdat.ToSystem = element.text;
        elif (Index(element.nodeName, "Direction"))
            cdat.Direction = element.text;
        elif (Index(element.nodeName, "FromBranch"))
            cdat.FromBranch = element.text;
        elif (Index(element.nodeName, "ToBranch"))
            cdat.ToBranch = element.text;
        elif (Index(element.nodeName, "pass"))
            cdat.pass = element.text;
        elif (Index(element.nodeName, "Interaction"))
            cdat.Interaction = element.text;
        elif (Index(element.nodeName, "Service"))
            cdat.Service = element.text;
        elif (Index(element.nodeName, "operation"))
            cdat.operation = element.text;
        elif (Index(element.nodeName, "ADUserId"))
            cdat.ADUserId = element.text;
        end;
        i = i + 1;
    end;
end;

private macro ReadCoverType2MAC_info(Node : @variant, cdat : @WSCoverData)
    var i = 0;
    
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (Index(element.nodeName, "_hmac"))
            cdat.hmac = element.text;
        elif (Index(element.nodeName, "_signerId"))
            cdat.signerId = element.text;
        elif (Index(element.nodeName, "_secretKeyId"))
            cdat.secretKeyId = element.text;
        elif (Index(element.nodeName, "algorithmName"))
            cdat.algorithmName = element.text;
        elif (Index(element.nodeName, "macDatetime"))
            cdat.macDatetime = element.text;
        elif (Index(element.nodeName, "signer"))
            cdat.signer = element.text;
        end;
        i = i + 1;
    end;
    cdat.HasMAC_info = true;
end;

private macro ReadCoverType2Result(Node : @variant, cdat : @WSCoverData)
    var i = 0;
    
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (Index(element.nodeName, "Description"))
            cdat.Description = element.text;
        elif (Index(element.nodeName, "Code"))
            cdat.Code = element.text;
        end;
        i = i + 1;
    end;
end;

private macro ReadCoverType2Extention(Node : @variant, cdat : @WSCoverData)
    var i = 0;
    
    var ext = CoverDataExtention();
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (Index(element.nodeName, "name"))
            ext.name = element.text;
        elif (Index(element.nodeName, "value"))
            ext.value = element.text;
        end;
        i = i + 1;
    end;

    if (ext.name == "SenderID")
        cdat.SenderID = ext.value;
    else
        cdat.Extention[cdat.Extention.size] = ext;
    end;
end;

private macro ReadCoverType2(Node : @variant)
    var cdat = WSCoverData();

    var i = 0;
    while (i < node.childNodes.length)
        var element = node.childNodes.item(i);
        if (element.nodeName == "input")
            var j = 0;
            while (j < element.childNodes.length)
                var inputElement = element.childNodes.item(j);
                if (Index(inputElement.nodeName, "Header"))
                    ReadCoverType2Header(inputElement, @cdat);
                elif (Index(inputElement.nodeName, "MacInfo"))
                    ReadCoverType2MAC_info(inputElement, @cdat);
                elif (Index(inputElement.nodeName, "Result"))
                    cdat.Status = inputElement.getAttribute("Status");
                    ReadCoverType2Result(inputElement, @cdat);
                elif (Index(inputElement.nodeName, "Extention"))
                    ReadCoverType2Extention(inputElement, @cdat);
                elif (Index(inputElement.nodeName, "Message"))
                    cdat.XMLMes = inputElement.text;
                end;
                j = j + 1;
            end;
        end;
        i = i + 1;
    end;

    return cdat;
end;

// ------------------------------------------------------------------------------------------------------

macro WSReadCover(xml : @variant, TypeMsg)
    var Node = xml.documentElement;
    var cdat;

    if (TypeMsg == 1)
        cdat = ReadCoverType1(Node);
    else
        cdat = ReadCoverType2(Node);
    end;

    return cdat;
end;


macro WSInsertQueueLinks(ReqIDTrEn, ReqIDMes, ParentReqIDTrEn, JMSMessageID, QueueID, Type)
    var QueueLinks = TBfile("QueueLinks.dbt", "W", 3);
    QueueLinks.rec.ReqIDTrEn = ReqIDTrEn;
    QueueLinks.rec.ReqIDMes = ReqIDMes;
    QueueLinks.rec.ParentReqIDTrEn = ParentReqIDTrEn;
    QueueLinks.rec.JMSMessageID = JMSMessageID;
    QueueLinks.rec.QueueID = QueueID;
    QueueLinks.rec.Type = Type;

    return QueueLinks.insert();
end;

macro GetQueueBO(Service, Id : @integer)
    var ParmBO = "";
    var cmd = RsdCommand("select T_ID, t_ParmBO from dDataTypeWS_dbt where t_ParmIP = ?");
    cmd.addParam("", RSDBP_IN, Service);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        ParmBO = rs.value("t_ParmBO");
        Id = rs.value("T_ID");
    end;

    return ParmBO;
end;

macro SendEmail(title, content)
    var EmailList;
    GetRegistryValue("COMMON\\QUEUE\\E-MAIL", V_STRING, EmailList);

    AddNotifyToDbt(title, content, EmailList);
    SendNotyfyToEmail(false, false);
end;


macro WSValidateMessageByXSD(Message, Service, errorstring : @string)
    var result = true;
    var XsdDir = "";
    GetRegistryValue("COMMON\\QUEUE\\ПУТЬ", V_STRING, XsdDir);

    var cmd = RsdCommand("select t_FileNameXSD from dDataTypeWS_dbt where t_ParmIP = ?");
    cmd.addParam("", RSDBP_IN, Service);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        var FileNameXSD = rs.value("t_FileNameXSD");

        if (strlen(FileNameXSD) and GetFileInfo(FileNameXSD))
            var validator = XmlValidator();
            validator.loadXML(Message);
            validator.addXsd(XsdDir + "\\" + rs.value("t_FileNameXSD"));

            var objErr = validator.validate();

            if (objErr.errorCode != 0)
                result = false;
                errorstring = "Контроль по XSD схеме " + rs.value("t_FileNameXSD") + " не пройден.";
                errorstring = errorstring + objErr.reason;

                SendEmail(E_MailTitle, errorstring);
                WriteXrLogQueueWS(SRV_XR_KIND_OUT, null, errorstring, "1001");
            end;
        end;
    end;

    return result;
OnError(error)
    return true;
end;
