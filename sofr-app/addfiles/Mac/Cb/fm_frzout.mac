 /*
 $Name: fm_frzout.mac
 $Module: fm_frzout.mac
 $Description: Выгрузка в транспортный файл замороженых объектов
 */
 /*
 Any text.
 */
Import BankInter, FMInter, PTInter, FIInter, CTInter, "fm_out_util", "globals.mac", likepy;
/* Код терр. учреждения по ОКАТО */
private var KTU : string;

/* Коды нашего банка */
private var BANK_INN : string;
private var REGN     : string;
private var NUMBF_S  : string;

/* Путь к эталонному транспортному файлу */
private var DIR_ORIG : string;

/* Название транспортного файла */
private var OutFileName : string;

private var OutFileType : string;

record FreezAction("fmfreezeaction.dbt" );


private macro FormatDate(dt:date)
  var day, mon, year;
  var str = "";   
  if(dt != date(0,0,0))
    DateSplit ( dt, day, mon, year );
    str = string(day:2:o)+ string(mon:2:o)+string(year:4:0);
  else
    str = "01012099";
  end;
  return str;
end;

/*
private macro FormatTime(tm:time)
  var hour, min, sec;
  var str = "";   

  TimeSplit ( dt, hour, min, sec );
  str = string(hour:2:o)+ string(min:2:o)+substr(string(sec:2:0),3,2) ;

  return str;
end;
*/


/*
 * Определить тип транспортного файла,
 * в который необходимо выгрузить данную операцию
 */
private macro DefineFileType( Repeat )
    
  if((FreezAction.MessageType == FMFREEZ_MESSAGE_TYPE_ADD))
    return "d";
  else
    return "f";
  end;
end;


/*
 * Сформировать название файла выгрузки
 * Возвращаемое значение: название файла
 */
private macro CreateOutFileName( Date_Seance, Seance, Repeat, FileKind )
  
  var day, mon, year, fname, ext;
  var OutFileName;

  var error;
  /* вид кода филиала */
  var BankID, DprtCodeParty, DprtCodeKind;
  OutFileType = DefineFileType( Repeat );

  DateSplit( Date_Seance, day , mon, year );

  /* имя файла для КФМ*/
//  if( FileKind == 0 )
    fname = SubStr( {MFO_Bank}, 5, 5 ) + string(OutFileType);
    if( day < 10 ) 
       fname = fname + "0" ;
    end;
    fname = fname + String(day);
    
    /* расширение */
    ext = "";
    if( Seance < 10 ) 
       ext =  "0";
    end;

    ext = ext + Seance + "8" ;
//  end;

  /* имя файла для ВФ*/
/*
  if( FileKind == 1 )

  end; 
*/
  /* формируем файл */
  OutFileName = MergeFile( FM_GetOutDir(), fname, ext );
  
  return OutFileName;

end;

/* Подкорректировать незаполненные поля в транспортном файле */
private macro CorrectTransportFile(TranspFile)

  var i = 0;

  while( i < FldNumber(TranspFile) )

    if( (ValType(TranspFile(i)) == V_STRING) and ((Trim(TranspFile(i)) == "") or (Trim(TranspFile(i) ) == ",")) )
         
      TranspFile(i) = "0";
      
    elif( (ValType(TranspFile(i)) == V_DATE ) and (TranspFile(i) == Date(0,0,0)) )
         
      TranspFile(i) = Date(1, 1, 2099) ;
      
    end;

    i = i + 1;

  end;

end;


/* Макрофункция вызывается в начале выгрузки
 *
 *   Возвращаемые значения:
 *      0 - продолжить выгрузку
 *  not 0 - остановить выгрузку
 *
 */
macro FrzOutStart()
  /* определим код территориального учреждения по ОКАТО */
  KTU = FM_GetKTU();
  if( Trim(KTU) == "" )
    msgbox( "Не определен код территории учреждения по ОКАТО" );
    return 1;
  end;
  
  var cmd = RsdCommand("delete from DFMFRZOUTLOG_tmp;");
  cmd.execute;
  
  BANK_INN = GetCodeParty( {OurBank}, PTCK_INN );

  ПолучитьНомерРегистрацииБанка( @REGN, @NUMBF_S );

  return 0;

end;


/* Макрофункция вызывается по завершении выгрузки
 *
 *   Возвращаемое значение не анализируется
 *
 */
macro FrzOutFinish()
end;

private macro FreezOutToDBF( Date_Seance, Seance, First, Repeat, FileKind )

   file SampleFile("fz134.dbf") dbf;
   file TranspFile("fz134.dbf") dbf write;

   if( not Open(SampleFile, DIR_ORIG) )
     MsgBox( "Невозможно открыть эталонный файл " + DIR_ORIG + ", заданный в настройках подсистемы" );
     return 1;
   end;

   if( First ) /* если выгрузка первой записи - создаем файл */
     
     OutFileName = CreateOutFileName( Date_Seance, Seance, Repeat, FileKind );

     if( not clone(SampleFile, OutFileName) )
       MsgBox( "Невозможно создать файл выгрузки " + OutFileName );
       return 1;
     end;

   else

     if( OutFileType != DefineFileType(Repeat) )
       return -1;
     end;

   end;

   if( not open(TranspFile, OutFileName) )
     MsgBox( "Невозможно открыть файл выгрузки " + OutFileName );
     return 1;
   end;

   ClearRecord( TranspFile );

   /* Служебная информация */
   TranspFile.VERSION    = "8";
   TranspFile.ACTION     = FreezAction.MessageType;
   TranspFile.NUMB_P     = FreezAction.MessageNumber;
   TranspFile.DATE_P     = FreezAction.MessageDate;
   TranspFile.REGN       = FreezAction.BankKGRKO;
   TranspFile.KTU_S      = FreezAction.OKATO;

   
   TranspFile.BIK_S      = FreezAction.BIC;
   TranspFile.NUMBF_S    = FreezAction.DprtKGRKO;

   if(FreezAction.SourceDprtSign == "X")
     TranspFile.BRANCH     = "1";
     TranspFile.KTU_SS     = FreezAction.SourceDprtOKATO;
     TranspFile.BIK_SS     = FreezAction.SourceDprtBIC;
     TranspFile.NUMBF_SS   = FreezAction.SourceDprtKGRKO;
   else
     TranspFile.BRANCH     = "0"; 
     TranspFile.KTU_SS     = "0";
     TranspFile.BIK_SS     = "0";
     TranspFile.NUMBF_SS   = "0";
   end;

   TranspFile.RESRV_S    = "0";

   TranspFile.TIPINF    = "БЛОКИРОВКА";
   TranspFile.PART      = FreezAction.FreezeGround;
   if(FreezAction.FreezeGround == FREEZING_BY_TERRLIST)
     TranspFile.ID_COD  = FormatDate(FreezAction.TerrListDate) + "_" + FreezAction.TerrListNumber + "_" +FreezAction.TerrListRecNo;
   elif(FreezAction.FreezeGround == FREEZING_BY_DECISION)
     TranspFile.ID_COD  = FormatDate(FreezAction.FreezeDocDate) + "_" + FreezAction.FreezeDocNumber;
   end;

   TranspFile.DATA      = FreezAction.FreezeDate;
   TranspFile.TIME      = string(FreezAction.FreezeTime);
   TranspFile.VIDIM     = FreezAction.FrozenObjKind;
   TranspFile.SCHET     = FreezAction.FrozenObjNumber;
   TranspFile.CURREN    = FreezAction.CurrencyISO;
   TranspFile.SUM       = FreezAction.SumInFIID;
   TranspFile.SUME      = FreezAction.SumInNatCur;
   
   if(FreezAction.FrozenObjKind == FM_VIDIM_SECURITIES)
     TranspFile.FORMA     = FreezAction.SecurityKind;
     TranspFile.SUM_41    = FreezAction.SecurityNominal;
     TranspFile.SUM_42    = FreezAction.SecurityCalcValue;
   end;

   TranspFile.DESCR     = FreezAction.Comment;
   TranspFile.RESRV_P   = "0";
   TranspFile.RESRV_PP  = "0";

   
   
   /* Подкорректируем некоторые поля */
   CorrectTransportFile(TranspFile);

   if ( not Insert( TranspFile ) )
      MsgBox( "Ошибка вставки записи в транспортный файл" );
      return 1;
   end;
   
   return OutFileName; /* Запись успешно выгружена*/

end;




/*
 *  2. Выгрузка в транспортный файл 
 *
 *   Параметры:
 *      Date_Seance : date     - дата выгрузки
 *      Seance      : integer  - номер сеанса выгрузки
 *      First       : bool     - флаг выгрузки первой записи
 *      Repeat      : bool     - признак выгрузки в случае ошбики структурного контроля первичного файла
 *
 *   Перед вызовом макрофункции заполняются структуры:
 *      OpContr       - запись об операции, подлежащей контролю
 *
 *   Возвращаемые значения:
 *      
 *      0    - запись успешно обработана
 *    < 0    - операция должна быть выгружена другим сеансом
 *    > 0    - прерывание выполнения
 *
 */
 
macro FrzOutToDBF( Date_Seance, Seance, First, Repeat, FileKind )

  var stat = 0;
  /* определим название файла-эталона */
  DIR_ORIG = "..\\mac\\cb\\fz134.dbf";//FM_GetDirOrigUnloadFile();
   
  if( Trim(DIR_ORIG) == "" )
    msgbox( "В настройках подсистемы не задан эталонный файл выгрузки" );
    return 1;
  end;
  
  return FreezOutToDBF( Date_Seance, Seance, First, Repeat, FileKind );
end;

//===================================================================================

private macro GetOperfio (opernum)
	var cmd, rs;
	cmd = RsdCommand("select T_NAME from DPERSON_DBT WHERE T_OPER = :id");
	rs = RsdRecordset(cmd);
	cmd.addParam("id", RSDBP_IN, opernum);
	cmd.execute;
	
	if (rs.moveNext())
		return rs.value(0);
	end;
	
	return "";
end;

private macro GetDpName (dpId)
	var cmd, rs;
	cmd = RsdCommand("select T_NAME from DDP_DEP_DBT where T_CODE = :id");
	rs = RsdRecordset(cmd);
	cmd.addParam("id", RSDBP_IN, dpId);
	cmd.execute;
	
	if (rs.moveNext())
		return rs.value(0);
	end;
	
	return "";
end;

macro FrzOutProtocol(operId, dp)
	var cur_date = Date();
[Протокол Процедуры выгрузки сообщений в ФСФМ о замораживании средств и имущества клиентов
Дата:	      ########## г.
Время:	      ##########
Пользователь: ##### #############################

+----------+--------+--------------------+---------------------+-----------------------+---------------+
| Действие | Филиал |       Клиент       |      Сообщение      | Замороженные средства |   Результат   |
+----------+--------+--------------------+---------------------+-----------------------+---------------+]
(cur_date:l, Time():l, operId:l, GetOperfio(operId):l);

	var cmd, rs;
	cmd = RsdCommand(
		"SELECT "
		"LOGS.T_ACTIONID AS ACTIONID, "
		"DDP_DEP_DBT.T_NAME AS DPRT, "
		"DPARTCODE_DBT.T_CODE AS CLCODE, "
		"DPARTY_DBT.T_NAME AS CLNAME, "
		"DFMFREEZEACTION_DBT.T_MESSAGETYPE AS MSGTYPE, "
		"DFMFREEZEACTION_DBT.T_MESSAGENUMBER AS MSGNUM, "
		"DFMFREEZEACTION_DBT.T_MESSAGEDATE AS MSGDATE, "
		"DLLVALUES_DBT.T_NAME AS FRZKIND, "
		"DFMFREEZEACTION_DBT.T_FROZENOBJNUMBER AS FRZOBJ, "
		"DFMFREEZEACTION_DBT.T_SECURITYKIND AS SECKIND, "
		"DFMFREEZEACTION_DBT.T_SUMINNATCUR AS SUM, "
		"DFMFREEZEACTION_DBT.T_SECURITYNOMINAL AS NOMINAL, "
		"LOGS.T_RESULT AS RESULT, "
		"LOGS.T_OUTFILENAME AS OUTFILENAME, "
		"DFMFREEZEACTION_DBT.T_FROZENOBJKIND AS FROZENOBJKIND "
	"FROM "
		"DFMFRZOUTLOG_TMP LOGS "
	"INNER JOIN DFMFREEZEACTION_DBT ON DFMFREEZEACTION_DBT.T_ID = LOGS.T_ACTIONID "
	"INNER JOIN DDP_DEP_DBT ON DDP_DEP_DBT.T_CODE = DFMFREEZEACTION_DBT.T_FREEZEDPRT "
	"INNER JOIN DPARTCODE_DBT ON DPARTCODE_DBT.T_PARTYID = DFMFREEZEACTION_DBT.T_PARTYID "
	"INNER JOIN DPARTY_DBT ON DPARTY_DBT.T_PARTYID = DFMFREEZEACTION_DBT.T_PARTYID "
	"INNER JOIN DLLVALUES_DBT ON DLLVALUES_DBT.T_ELEMENT = DFMFREEZEACTION_DBT.T_FROZENOBJKIND "
	"WHERE "
		"DPARTCODE_DBT.T_CODEKIND = 1 "
	"AND DLLVALUES_DBT.T_LIST = 1697 "
	"ORDER BY "
		"LOGS.T_ACTIONID");
	
	cmd.NullConversion = true;
	cmd.execute;
	rs = RsdRecordset(cmd);

	while (rs.moveNext())
		var actionID,dprt,msg,frzkind,result, FroozenObjKind ;
		actionID = rs.value("actionID");
		dprt = rs.value("dprt");
		FroozenObjKind = rs.value("FROZENOBJKIND");

		msg = "тип: ";
		if (rs.value("msgtype") == 1)
			msg = msg + "добавление новой записи";
		elif (rs.value("msgtype") ==  2)
			msg = msg + "исправление записи";
		elif (rs.value("msgtype") ==  3)
			msg = msg + "запрос замены и представления дополнительной записи";
		else
			msg = msg + "запрос удаления записи";
		end;
		
		var val, datVal, IsNull;
		val = rs.value("msgdate", IsNull);
		if(trim(string(val)) != "")
			datVal = Date(val);
		else
			datVal = "00.00.0000";
		end;
		msg = msg + "\nномер: " + rs.value("msgnum") + "\nдата: " + datVal;
		
		frzkind = rs.value("frzkind");
		if ((FroozenObjKind == 1) or (FroozenObjKind == 2) or 
			(FroozenObjKind == 3) or (FroozenObjKind == 5) or 
			(FroozenObjKind == 6))
				frzkind = frzkind + "\n" + rs.value("frzobj");
		elif (FroozenObjKind == 4)
				frzkind = frzkind + "\n" + rs.value("seckind");
		end;
		
		if ((FroozenObjKind == 1) or (FroozenObjKind == 2) or 
			(FroozenObjKind == 3) or (FroozenObjKind == 5) or 
			(FroozenObjKind == 6) or (FroozenObjKind == 7))
				frzkind = frzkind + "\nсумма " + rs.value("sum");
		end;
		
		if (FroozenObjKind == 4)
			frzkind = frzkind + "\nстоимость " + rs.value("nominal");
		end;
		
		if (rs.value("result") == 0)
			result = "исключено: нулевая сумма";
		else
			result = "выгружено:\n" + rs.value("OUTFILENAME");
		end;
		
[| ######## | ###### | ################## | ################### | ##################### | ############# |]
(actionID:c,dprt:c, string(rs.value("clcode") + "\n" + rs.value("clname")):w, msg:w, frzkind:w, result:w);
[+----------+--------+--------------------+---------------------+-----------------------+---------------+]
	end;
end;






