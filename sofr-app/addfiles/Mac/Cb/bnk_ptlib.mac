 /*
 $Name: bnk_ptlib.mac
 $Module: Ядро Banking
 $Description: Функции для работы с субъектами в макросах Расчетного банка
 */

import oralib, likepy, CTInter, PTInter, "globals.mac", FIInter;

macro ПринадлежностьБанкаНашейТС( BankID )
    const DEP_TYPE_FILIAL = 1; /* Когда проэкспортят, тогда заменить на нее */
    var rs:object;
    var select:string;
    var params:TArray;

    select = "select 1 from ddp_dep_dbt dep where "+
                           "dep.t_PartyID =:BankID and dep.t_NodeType = :Dep and dep.t_status <> 3";
    params = makeArray( SQLParam("BankID", BankID),
                        SQLParam("Dep", DEP_TYPE_FILIAL));
    rs = execSQLselect( select, params );

    if ( rs and rs.moveNext() )
       return true;
    end;
    return false;
end;

macro Bnk_GetBankDprt( BankID )
  var bankdprt_rec:TRecHandler = TRecHandler("bankdprt.dbt");
  var bankdprt:TBFile = TBFile("bankdprt.dbt");
  bankdprt.rec.PartyID = BankID;
  if( GetEQ(bankdprt) )
    Copy(bankdprt_rec, bankdprt);
  end;
  return bankdprt_rec;

end;

//------------------------------------------------------------------------------
// Банк в ТС?
//------------------------------------------------------------------------------
MACRO PM_IsBankInTS( BankID:integer, OurCABS:bool ):bool

  var CABSstr:string = "";
  var select :string = "select 1 "
                     +  " from ddp_dep_dbt dp "
                     + " where dp.t_PartyID = :BankID "
                     +   " and dp.t_Status <> 3 ";

  var params:TArray = TArray();
  params[params.size] = SQLParam( "BankID" , BankID );
  var rset:RsdRecordset;

  if( OurCABS == true )
    CABSstr = " and dp.t_ACCESSMODE <> 3 ";
  else
    CABSstr = " and dp.t_ACCESSMODE =  3 ";
  end;

  select = select + CABSstr;

  rset = execSQLselect( select, params, false );
  return ( rset AND rset.moveNext() );
END;

//------------------------------------------------------------------------------
// Банк является Учреждением Банка России?
//------------------------------------------------------------------------------
macro БанкУБР( BankID: integer): bool
  return execStoredFunc( "PM_COMMON.IsBankUBR_Num", 
                         V_INTEGER,
                         makeArray( SQLParam("p_BankID", BankID) ) ) == 1;
end;


//------------------------------------------------------------------------------
// Банк является СБРФ?
//------------------------------------------------------------------------------
MACRO BankIsSB( BankID:integer ):bool
  return IsBankType( BankID, PT_KIND_SAVINGS_BANK );
END;

//------------------------------------------------------------------------------
// Получить код субъекта с учетом иерархии
//  PartyID     - субъект
//  CodeKind    - вид кода
//  Code        - возвращаемый код
//  CodeOwnerID - возвращаемый владелец кода
// Возвращает 0 в случае успеха.
//------------------------------------------------------------------------------
MACRO GetPartyCodeEx( PartyID:integer, CodeKind:integer, Code:@string, CodeOwnerID:@integer ):integer
  var params:TArray = TArray();
  params[0] = SQLParam( "p_PartyID"    , PartyID              );
  params[1] = SQLParam( "p_CodeKind"   , CodeKind             );
  params[2] = SQLParam( "p_Code"       , V_STRING , RSDBP_OUT );
  params[3] = SQLParam( "p_CodeOwnerID", V_INTEGER, RSDBP_OUT );
  var retval:integer = execStoredFunc( "RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, params );
  if( not retval )
    Code        = params.Value(2).value;
    CodeOwnerID = params.Value(3).value;
  end;
  return retval;
END;

MACRO ПервыйЗаданныйКодСубъекта(PartyID:integer/*, CodeKind1, CodeKind2, ...*/) : string
  var Code : string = "";

  var CodeKind : integer, i : integer = 1;
  var stat : integer = 0;
  
  while(GetParm(i, CodeKind))
    if( (GetPartyCodeEx(PartyID, CodeKind, @Code) == 0) and Code )
      break;
    end;
    i = i + 1;
  end;

  return Code;
END;

//------------------------------------------------------------------------------
// Получить название субъекта
//------------------------------------------------------------------------------
MACRO Bnk_GetPartyName( PartyID:integer ):string
  var select:string = "select t_Name from dparty_dbt where t_PartyID=:PartyID";
  var params:TArray = TArray();
  params[params.size] = SQLParam( "PartyID", PartyID );
  var rset:object = execSQLselect( select, params, false );
  if( rset AND rset.moveNext() )
    return rset.Value( 0 );
  else
    return "";
  end;
END;

//------------------------------------------------------------------------------
// Получить краткое название субъекта
//------------------------------------------------------------------------------
MACRO Bnk_GetPartyShortName( PartyID : integer ) : string
  var select : string = "select t_ShortName "
                        "  from dparty_dbt "
                        " where t_PartyID = :PartyID";

  var params : TArray = TArray();
  params[params.size] = SQLParam( "PartyID", PartyID );

  var rset : RsdRecordset = execSQLselect( select, params );
  if( rset AND rset.moveNext() )
    return rset.Value( 0 );
  else
    return "";
  end;
END;

//------------------------------------------------------------------------------
// Получить ФИО субъекта-физ.лица
//------------------------------------------------------------------------------
MACRO GetPersonNames
( PersonID : integer,
  NameF : @string,
  NameI : @string,
  NameO : @string

) : bool

  var q : string = "select t_Name1, t_Name2, t_Name3 "
                   "  from dpersn_dbt "
                   " where t_PersonID = :PersonID ";
  var rs : RsdRecordset = execSQLselect( q, makeArray( SQLParam("PersonID", PersonID) ) );

  if(rs and rs.moveNext())
    NameF = rs.value("t_Name1");
    NameI = rs.value("t_Name2");
    NameO = rs.value("t_Name3");
    return TRUE;
  end;

  return FALSE;
END;

//------------------------------------------------------------------------------
// Получить ID субъекта-владельца кода
// Если код не задан, то это OurBank или ближайший вышестоящий, имеющий код заданного вида
//------------------------------------------------------------------------------
MACRO GetCodeOwnerID( CodeKind:integer, Code:string ):integer
  var select:string = "",
      nested:string = "";
  var params:TArray = TArray();
  var rset:RsdRecordset;
  var CodeOwnerID:integer = -1;

  //Код задан - ищем в partcode.dbt
  if( Code )
    select = "select NVL(t_PartyID, -1) from dpartcode_dbt where t_CodeKind=:CodeKind and t_Code=:Code";
    params[0] = SQLParam( "CodeKind", CodeKind );
    params[1] = SQLParam( "Code"    , Code     );
    rset = execSQLselect( select, params, false );
    if( rset AND rset.moveNext() )
      CodeOwnerID =  rset.Value( 0 );
    end;
  //Код не задан - более тяжелый случай
  else
    GetPartyCodeEx( {OurBank}, CodeKind, @Code, @CodeOwnerID );
  end;

  return CodeOwnerID;
END;

// Получить ИД головного банка в иерархии ТС (т.е. ИД головного филиала)
// ChildPartyID - филиал, для которого определяем головной банк
MACRO GetTSHeadPartyID(ChildPartyID : integer) : integer
  var query : string = 
              "select dp.t_PartyID " +
              "from ddp_dep_dbt dp " +
              "where dp.t_ParentCode = 0 " +
              "connect by dp.t_Code = prior dp.t_ParentCode " +
              "start with dp.t_PartyID = :ChildPartyID ";

  var params : TArray = makeArray( SQLParam("ChildPartyID", ChildPartyID) );

  var rs : RsdRecordset = execSQLselect(query, params);
  if(rs and rs.moveNext())
    return rs.value("t_PartyID");
  end;

  return 0;
END;

// Получить ИД субъекта, связанного с филиалом DprtID (dp_dep.Code)
macro GetDprtPartyID( DprtID : integer ) : integer
  
  var PartyID : integer = -1;

  var rs = execSQLselect( "SELECT t_PartyID        "
                          "  FROM ddp_dep_dbt      "
                          " WHERE t_Code = :DprtID ",
                          makeArray( SQLParam("DprtID", DprtID) )
                        );
  if(rs and rs.moveNext())
    PartyID = rs.value("t_PartyID");
  end;

  return PartyID;

end;

macro GetDepIdByPartyId(PartyID : integer) : integer

  var DepID : integer = -1;

  var rs = execSQLselect( "SELECT t_Code        "
                          "  FROM ddp_dep_dbt      "
                          " WHERE t_PartyID = :PartyID ",
                          makeArray( SQLParam("PartyID", PartyID) )
                        );
  if(rs and rs.moveNext())
    DepID = rs.value("t_Code");
  end;

  return DepID;

end;

macro GetHeadDprtPartyID : integer
  var HeadPartyID : integer = 0;

  var select : string = 
    " select t_PartyID " +
    "   from ddp_dep_dbt " +
    "  where t_ParentCode = 0 ";

  var rs = execSQLselect( select );
  if(rs and rs.moveNext())
    HeadPartyID = rs.value(0);
  end;

  return HeadPartyID;
end;

// Является ли филиал головным?
macro IsHeadDprt( DprtID : integer ) : bool
  
  var IsHead : bool = false;

  var rs = execSQLselect( "SELECT t_ParentCode     "
                          "  FROM ddp_dep_dbt      "
                          " WHERE t_Code = :DprtID ",
                          makeArray( SQLParam("DprtID", DprtID) )
                        );
  if(rs and rs.moveNext())
    if( rs.value("t_ParentCode") == 0 )
      IsHead = true;
    end;
  end;

  return IsHead;

end;

//-----------------------------------------------------------------------------
// Является ли субъект физическим лицом
//-----------------------------------------------------------------------------
MACRO IsPerson( PartyID ):bool

  VAR select:string = " select party.T_LEGALFORM " +
                        " from dparty_dbt party "+
                       " where party.T_PARTYID = :PartyID";
  VAR params:TArray = makeArray( SQLParam( "PartyID", PartyID ) );
  VAR rset:RsdRecordset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    if( rset.value(0) == PTLEGF_PERSN )
      return true;
    end;
  end;

  return false;

END;

// Субъект является индивидуальным предпринимателем?
macro IsIndividualEmployer(PartyID : integer) : bool
  var qEmp = "select 1 "
             "  from dpersn_dbt "
             " where t_PersonID = :PartyID "
             "   and t_IsEmployer = 'X' ";

  var params : TArray = makeArray( SQLParam("PartyID", PartyID) );

  var rs : RsdRecordset = execSQLselect( qEmp, params );
  if( rs and rs.moveNext() )
    return TRUE;
  end;

  return FALSE;
end;

// Получить для субъекта PartyID основное значение категории "Тип субъекта" 
// или 0, если категория не установлена
macro GetPartyMainAttr_PartyType(PartyID : integer) : integer
  var AttrID : integer = 0;

  record party(party);
  party.PartyID = PartyID;

  var Error : integer = 0;
  GetMainObjAttr(Error, OBJTYPE_PARTY, party, PARTY_ATTR_GROUP_PTTYPE, AttrID);
  if(Error)
    AttrID = 0;
  end;

  return AttrID;
end;


/* Если ИНН == ИНН/КПП, то оставляем только ИНН */
macro RemoveKPP(INN)
   var ind_slash = Index(INN,"/");
   if ( ind_slash != 0 ) return SubStr(INN, 1, ind_slash-1); end;
   return INN;
end;

macro RemoveINN(INN)
   var ind_slash = Index(INN,"/");
   if ( ind_slash != 0 ) return SubStr(INN, ind_slash+1); end;
   return "";
end;

/* Составить INN/КПП */
macro ConstructINN( INN, KPP )
    if ( KPP == "" )
       return Trim(INN);
    end;
    return ( Trim(INN) + "/" + KPP );
end;

macro GetOnlyINN(PartyID : integer) : string
  var OnlyINN : string = "";

  var FullINN : string = ПолучитьКодСубъекта(PartyID, PTCK_INN);
  if(FullINN)
    OnlyINN = RemoveKPP(FullINN);
  end;

  return OnlyINN;
end;

macro GetINN_KPP(PartyID : integer, INN : @string, KPP : @string, recursive : Integer)
  INN = KPP = "";

  var FullINN : string = ПолучитьКодСубъекта(PartyID, PTCK_INN, null, recursive);
  if(FullINN)
    SplitFullINN(FullINN, INN, KPP);
  end;
end;

/**
 *  Проверка есть ли у субъекта код заданного вида с учетом иерархии и признака активности
 */
macro Bnk_IsExistsPartyCode( PartyID:integer, CodeKind:integer, Code:string,  permissibleClose:bool ):bool
    var rs:object;
    var select:string;
    var params:TArray;



    select = "select 1 "+
               "from dobjcode_dbt oc " +
               "where oc.t_ObjectType = 3 " +
                 "and oc.t_CodeKind = :CodeKind " +
                 "and oc.t_Code = :Code "+ 
                 IfThenElse( permissibleClose, "", "and oc.t_State = 0 ") +
                 "and oc.t_ObjectID in ( select pt.t_PartyID " + 
                                          "from dparty_dbt pt "+
                                          "connect by pt.t_PartyID =  prior pt.t_Superior "+
                                            "start with pt.t_PartyID = :PartyID ) ";
                  
    params = makeArray( SQLParam("CodeKind", CodeKind),
                        SQLParam("Code", Code),
                        SQLParam("PartyID", PartyID)
                      );
    rs = execSQLselect( select, params );

    if ( rs and rs.moveNext() )
       return true;
    end;
    return false;
end;

macro GetPartyTaxInstitution(PartyID : integer, TaxInstName : @string) : integer
  var TaxInst : integer = 0;
  TaxInstName = "";

  var q : string = "select pt.t_TaxInstitution, pttax.t_Name "
                   "  from dparty_dbt pt, dparty_dbt pttax "
                   " where pt.t_PartyID = :PartyID "
                   "   and pttax.t_PartyID = pt.t_TaxInstitution ";
  var rs : RsdRecordset = execSQLselect(q, makeArray( SQLParam("PartyID", PartyID) ));
  if(rs and rs.moveNext())
    TaxInst = rs.value("t_TaxInstitution");
    TaxInstName = rs.value("t_Name");
  end;

  return TaxInst;
end;

MACRO GetNameOper(OperID : integer)
  var NameOper : string = "";

  var rs : RsdRecordset = execSQLselectPrm
    ( "SELECT t_Name FROM dperson_dbt WHERE t_Oper = :OperID",
      SQLParam("OperID", OperID)
    );

  if( rs and rs.moveNext() )
    NameOper = rs.value("t_Name");
  end;

  return NameOper;
END;

macro IsPartyOwned(PartyID : integer, PartyKind : integer) : bool
  var rs : RsdRecordset = execSQLselectPrm
    ( "select 1 "
      "  from dpartyown_dbt "
      " where t_PartyID = :PartyID "
      "   and t_PartyKind = :PartyKind ",
      SQLParam("PartyID", PartyID),
      SQLParam("PartyKind", PartyKind)
    );

  if(rs and rs.moveNext())
    return true;
  end;

  return false;
end;

macro GetDepListStr(ParentDepID : integer) : string
  var DepListStr : string = "";

  var rs : RsdRecordset = execSQLselectPrm
    ( "select dp.t_Code "
      "  from ddp_dep_dbt dp "
      " start with dp.t_Code = :ParentDepID "
      " connect by prior dp.t_Code = dp.t_ParentCode ",
      SQLParam("ParentDepID", ParentDepID)
    );

  while(rs and rs.moveNext())
    DepListStr = DepListStr + rs.value("t_Code") + ",";
  end;

  // Обрежем последнюю запятую
  DepListStr = SubStr(DepListStr, 1, StrLen(DepListStr) - 1);

  return DepListStr;
end;

// Функция получения ID субъекта по ИНН
// Параметры: INN - строка с ИНН
// Возвращаемое значение: ID субъекта или 0 если не найден
macro GetPartyIDByINN( INN: string )
  var rs:object;
  var select:string;
  var params:TArray;
    
  select = " select pt.t_PartyID"+
             " from dpartcode_dbt code, dparty_dbt pt"+
            " where code.t_PartyID = pt.t_PartyID"+
              " and pt.t_LegalForm = :legalForm"+
              " and PM_SCRHLP.GetINN(code.t_Code ) =  PM_SCRHLP.GetINN( :Code )" +
              " and code.t_CodeKind = :CodeKind";
  params = makeArray( SQLParam("legalForm", PTLEGF_INST),
                      SQLParam("Code", INN ),
                      SQLParam("CodeKind", PTCK_INN));
  
  rs = execSQLselect( select, params, FALSE );
  
  if(rs.moveNext())
    return rs.value(0);
  end;     

  return 0;
end;

macro GetBirthInfo
( PersonID : integer, 
  BirthDate : @date, 
  BirthPlace : @string
) : bool

  var q : string = "select t_Born, t_BirsPlase "
                   "  from dpersn_dbt "
                   " where t_PersonID = :PersonID ";

  var rs : RsdRecordset = execSQLselectPrm(q, SQLParam("PersonID", PersonID));
  if(rs and rs.moveNext())
    BirthDate = date( rs.value("t_Born") );

    BirthPlace = rs.value("t_BirsPlase");

    return TRUE;
  end;

  return FALSE;
end;

macro Bnk_GetDeptPartyID(DeptCode:integer):integer
  return GetDprtPartyID(DeptCode);
end;

