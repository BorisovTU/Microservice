/*
$Name:           ptimfias.mac
$Module:         Ядро ГКБО
$Description:    Реализация процедуры загрузки справочников ФИАС
*/

import rsexts;
import PtInter;
import rsd, cb_sql, globals, likepy, oralib;

private const RegPathDateOfActuality = "CB\\PARTY\\ФИАС\\ДАТА_АКТУАЛЬНОСТИ";

private macro _ClearTempTableForDelta() : bool
  execSQL("TRUNCATE TABLE das_addrobj_tmp", null, false);
  execSQL("TRUNCATE TABLE das_stead_tmp", null, false);
  execSQL("TRUNCATE TABLE das_house_tmp", null, false);
  execSQL("TRUNCATE TABLE das_room_tmp", null, false);
end;

private macro _ClearTablesForFull() : bool
  execSQL("TRUNCATE TABLE das_addrobj_dbt", null, false);
  execSQL("TRUNCATE TABLE das_stead_dbt", null, false);
  execSQL("TRUNCATE TABLE das_house_dbt", null, false);
  execSQL("TRUNCATE TABLE das_room_dbt", null, false);
  execSQL("TRUNCATE TABLE das_socrbase_dbt", null, false);
  execSQL("TRUNCATE TABLE das_curentst_dbt", null, false);
  execSQL("TRUNCATE TABLE das_operstat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_centerst_dbt", null, false);
  execSQL("TRUNCATE TABLE das_hststat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_eststat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_strstat_dbt", null, false);
end;

private macro _ClearTablesForDelta() : bool
  execSQL("TRUNCATE TABLE das_socrbase_dbt", null, false);
  execSQL("TRUNCATE TABLE das_curentst_dbt", null, false);
  execSQL("TRUNCATE TABLE das_operstat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_centerst_dbt", null, false);
  execSQL("TRUNCATE TABLE das_hststat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_eststat_dbt", null, false);
  execSQL("TRUNCATE TABLE das_strstat_dbt", null, false);
end;

private macro _IsFileNameAddrobj(_FileName)
  return index(strlwr(_FileName), "as_addrobj_") > 0; 
end;

private macro _IsFileNameStead(_FileName)
  return index(strlwr(_FileName), "as_stead_") > 0; 
end;

private macro _IsFileNameHouse(_FileName)
  return index(strlwr(_FileName), "as_house_") > 0; 
end;

private macro _IsFileNameRoom(_FileName)
  return index(strlwr(_FileName), "as_room_") > 0; 
end;

private macro _IsFileNameSocrbase(_FileName)
  return index(strlwr(_FileName), "as_socrbase_") > 0; 
end;

private macro _IsFileNameCurentst(_FileName)
  return index(strlwr(_FileName), "as_curentst_") > 0; 
end;

private macro _IsFileNameOperstat(_FileName)
  return index(strlwr(_FileName), "as_operstat_") > 0; 
end;

private macro _IsFileNameCenterst(_FileName)
  return index(strlwr(_FileName), "as_centerst_") > 0; 
end;

private macro _IsFileNameHststat(_FileName)
  return index(strlwr(_FileName), "as_hststat_") > 0; 
end;

private macro _IsFileNameEststat(_FileName)
  return index(strlwr(_FileName), "as_eststat_") > 0; 
end;

private macro _IsFileNameStrstat(_FileName)
  return index(strlwr(_FileName), "as_strstat_") > 0; 
end;

private macro _GetFileNameDate(_FileName)
  var FileNameDate = "";

  if(_IsFileNameAddrobj(_FileName))
    FileNameDate = substr(_FileName, 12, 8);
  elif(_IsFileNameStead(_FileName))
    FileNameDate = substr(_FileName, 10, 8);
  elif(_IsFileNameHouse(_FileName))
    FileNameDate = substr(_FileName, 10, 8);
  elif(_IsFileNameRoom(_FileName))
    FileNameDate = substr(_FileName, 9, 8);
  elif(_IsFileNameSocrbase(_FileName))
    FileNameDate = substr(_FileName, 13, 8);
  elif(_IsFileNameCurentst(_FileName))
    FileNameDate = substr(_FileName, 13, 8);
  elif(_IsFileNameOperstat(_FileName))
    FileNameDate = substr(_FileName, 13, 8);
  elif(_IsFileNameCenterst(_FileName))
    FileNameDate = substr(_FileName, 13, 8);
  elif(_IsFileNameHststat(_FileName))
    FileNameDate = substr(_FileName, 12, 8);
  elif(_IsFileNameEststat(_FileName))
    FileNameDate = substr(_FileName, 12, 8);
  elif(_IsFileNameStrstat(_FileName))
    FileNameDate = substr(_FileName, 12, 8);
  end;

  /*
  println("DEBUG _GetFileNameDate: _FileName=", _FileName, "\tFileNameDate=", FileNameDate);
  */

  return FileNameDate;
end;

private macro _GetFileNameWithMaxDate(_FileName1, _FileName2)
  var FileNameDate1 = _GetFileNameDate(_FileName1);
  var FileNameDate2 = _GetFileNameDate(_FileName2);
  var retval = _FileName1;

  if(FileNameDate1 < FileNameDate2)
    retval = _FileName2;
  end;
  
  return retval;
end;

private macro _GetFileNamesFromPath
(
  PathFIAS,
  FileNameAddrobj : @string,
  FileNameStead : @string,
  FileNameHouse : @string,
  FileNameRoom : @string,
  FileNameSocrbase : @string,
  FileNameCurentst : @string,
  FileNameOperstat : @string,
  FileNameCenterst : @string,
  FileNameHststat : @string,
  FileNameEststat : @string,
  FileNameStrstat : @string
)
  var DirList = TDirList;
  var i = 0;

  DirList.List(PathFIAS + "\\*.xml", "F");
  while(i < DirList.Count)

    if(_IsFileNameAddrobj(DirList.Name(i)))
      FileNameAddrobj = _GetFileNameWithMaxDate(FileNameAddrobj, DirList.Name(i));
    elif(_IsFileNameStead(DirList.Name(i)))
      FileNameStead = _GetFileNameWithMaxDate(FileNameStead, DirList.Name(i));
    elif(_IsFileNameHouse(DirList.Name(i)))
      FileNameHouse = _GetFileNameWithMaxDate(FileNameHouse, DirList.Name(i));
    elif(_IsFileNameRoom(DirList.Name(i)))
      FileNameRoom = _GetFileNameWithMaxDate(FileNameRoom, DirList.Name(i));
    elif(_IsFileNameSocrbase(DirList.Name(i)))
      FileNameSocrbase = _GetFileNameWithMaxDate(FileNameSocrbase, DirList.Name(i));
    elif(_IsFileNameCurentst(DirList.Name(i)))
      FileNameCurentst = _GetFileNameWithMaxDate(FileNameCurentst, DirList.Name(i));
    elif(_IsFileNameOperstat(DirList.Name(i)))
      FileNameOperstat = _GetFileNameWithMaxDate(FileNameOperstat, DirList.Name(i));
    elif(_IsFileNameCenterst(DirList.Name(i)))
      FileNameCenterst = _GetFileNameWithMaxDate(FileNameCenterst, DirList.Name(i));
    elif(_IsFileNameHststat(DirList.Name(i)))
      FileNameHststat = _GetFileNameWithMaxDate(FileNameHststat, DirList.Name(i));
    elif(_IsFileNameEststat(DirList.Name(i)))
      FileNameEststat = _GetFileNameWithMaxDate(FileNameEststat, DirList.Name(i));
    elif(_IsFileNameStrstat(DirList.Name(i)))
      FileNameStrstat = _GetFileNameWithMaxDate(FileNameStrstat, DirList.Name(i));
    end;

    i = i + 1;
  end;
end;

private macro _IsFileNamesDateEqual
(
  FileNameAddrobj : string,
  FileNameStead : string,
  FileNameHouse : string,
  FileNameRoom : string,
  FileNameSocrbase : string,
  FileNameCurentst : string,
  FileNameOperstat : string,
  FileNameCenterst : string,
  FileNameHststat : string,
  FileNameEststat : string,
  FileNameStrstat : string
)
  var retval = false;

  if(    (_GetFileNameDate(FileNameAddrobj ) == _GetFileNameDate(FileNameStead   ))
     and (_GetFileNameDate(FileNameStead   ) == _GetFileNameDate(FileNameHouse   ))
     and (_GetFileNameDate(FileNameHouse   ) == _GetFileNameDate(FileNameRoom    ))
     and (_GetFileNameDate(FileNameRoom    ) == _GetFileNameDate(FileNameSocrbase))
     and (_GetFileNameDate(FileNameSocrbase) == _GetFileNameDate(FileNameCurentst))
     and (_GetFileNameDate(FileNameCurentst) == _GetFileNameDate(FileNameOperstat))
     and (_GetFileNameDate(FileNameOperstat) == _GetFileNameDate(FileNameCenterst))
     and (_GetFileNameDate(FileNameCenterst) == _GetFileNameDate(FileNameHststat ))
     and (_GetFileNameDate(FileNameHststat ) == _GetFileNameDate(FileNameEststat ))
     and (_GetFileNameDate(FileNameEststat ) == _GetFileNameDate(FileNameStrstat ))
    )
    retval = true;
  end;

  return retval;
end;

private macro _IsFileNameExisting
(
  FileNameAddrobj : string,
  FileNameStead : string,
  FileNameHouse : string,
  FileNameRoom : string,
  FileNameSocrbase : string,
  FileNameCurentst : string,
  FileNameOperstat : string,
  FileNameCenterst : string,
  FileNameHststat : string,
  FileNameEststat : string,
  FileNameStrstat : string
)
  var retval = false;

  if(    (FileNameAddrobj  != "")
     and (FileNameStead    != "")
     and (FileNameHouse    != "")
     and (FileNameRoom     != "")
     and (FileNameSocrbase != "")
     and (FileNameCurentst != "")
     and (FileNameOperstat != "")
     and (FileNameCenterst != "")
     and (FileNameHststat  != "")
     and (FileNameEststat  != "")
    )
    retval = true;
  end;

  return retval;
end;

private macro _GetRegValueDateOfActuality()
  var regval : string;
  var error = 0;

  GetRegistryValue(RegPathDateOfActuality, V_STRING, regval, error);
    
  if(error != 0)
    regval = "00000000";
  end;

  return regval;
end;

private macro _SetRegValueDateOfActuality(regval : string)
  var error = SetDefaultRegistryValue(RegPathDateOfActuality, regval);
    
  return error;
end;

private macro _YesNoWin(msg : string)
  Array Text;
  Array Button;
  Button(0) = "  Нет  ";
  Button(1) = "  Да   ";
  var but, ind = 0;

  Text(ind) = msg;
  but = ConfWin(Text, Button);
  
  return but;
end;

private macro _MergeTempTablesTrn()
  var params:TArray;

  RsBeginAction(100, "Внимание!!! Выполняется обработка новых записей справочника. Ждите...", 0);

  params = makeArray(SQLParam( "", "das_addrobj_tmp"), 
                     SQLParam( "", "das_addrobj_dbt"),
                     SQLParam( "", "das_addrobj_tmp.t_aoid = das_addrobj_dbt.t_aoid"),
                     SQLParam( "", ""),
                     SQLParam( "", "t_aoid"),
                     SQLParam( "", 1)
                    );

  execStoredFunc( "rsb_tools.merge_tables", V_UNDEF, params);

  /*
  params = makeArray(SQLParam( "", "das_stead_tmp"), 
                     SQLParam( "", "das_stead_dbt")
                    );

  execStoredFunc( "rsb_tools.copy_table", V_UNDEF, params);
  */

  params = makeArray(SQLParam( "", "das_house_tmp"), 
                     SQLParam( "", "das_house_dbt"),
                     SQLParam( "", "das_house_tmp.t_houseid = das_house_dbt.t_houseid"),
                     SQLParam( "", ""),
                     SQLParam( "", "t_houseid"),
                     SQLParam( "", 1)
                    );

  execStoredFunc( "rsb_tools.merge_tables", V_UNDEF, params);

  /*
  params = makeArray(SQLParam( "", "das_room_tmp"), 
                     SQLParam( "", "das_room_dbt")
                    );

  execStoredFunc( "rsb_tools.copy_table", V_UNDEF, params);
  */

  execSQL("DELETE /*+ PARALLEL */ FROM das_addrobj_dbt WHERE t_nextid > CHR(1)", null, false);

  execSQL("DELETE /*+ PARALLEL */ FROM das_house_dbt WHERE t_enddate < SYSDATE", null, false);

  RsEndAction();

  return true;
OnError(err)

  RemProgress();
  return false;
end;

private macro _ImportFIAS_DELTA
(
  FileNameAddrobj : string,
  FileNameStead : string,
  FileNameHouse : string,
  FileNameRoom : string,
  FileNameSocrbase : string,
  FileNameCurentst : string,
  FileNameOperstat : string,
  FileNameCenterst : string,
  FileNameHststat : string,
  FileNameEststat : string,
  FileNameStrstat : string
)
  var stat = true;

  _ClearTempTableForDelta();
  _ClearTablesForDelta();

  UploadFiasXMLFileIntoDBFile(FileNameAddrobj , T_AS_ADDROBJ_TMP );
  /*
  UploadFiasXMLFileIntoDBFile(FileNameStead   , T_AS_STEAD_TMP   );
  */
  UploadFiasXMLFileIntoDBFile(FileNameHouse   , T_AS_HOUSE_TMP   );
  /*
  UploadFiasXMLFileIntoDBFile(FileNameRoom    , T_AS_ROOM_TMP    );
  */

  /*Запустить перенос в постоянные таблицы в транзакции*/
  stat = ProcessTrn(null, "_MergeTempTablesTrn");

  /*stat = _MergeTempTablesTrn();*/
  
  if(stat)
    UploadFiasXMLFileIntoDBFile(FileNameSocrbase, T_AS_SOCRBASE);
    UploadFiasXMLFileIntoDBFile(FileNameCurentst, T_AS_CURENTST);
    UploadFiasXMLFileIntoDBFile(FileNameOperstat, T_AS_OPERSTAT);
    UploadFiasXMLFileIntoDBFile(FileNameCenterst, T_AS_CENTERST);
    UploadFiasXMLFileIntoDBFile(FileNameHststat , T_AS_HSTSTAT );
    UploadFiasXMLFileIntoDBFile(FileNameEststat , T_AS_ESTSTAT );
    UploadFiasXMLFileIntoDBFile(FileNameStrstat , T_AS_STRSTAT );
  end;

  return stat;
end;

private macro _ImportFIAS_FULL
(
  FileNameAddrobj : string,
  FileNameStead : string,
  FileNameHouse : string,
  FileNameRoom : string,
  FileNameSocrbase : string,
  FileNameCurentst : string,
  FileNameOperstat : string,
  FileNameCenterst : string,
  FileNameHststat : string,
  FileNameEststat : string,
  FileNameStrstat : string
)
  var stat = true;

  _ClearTablesForFull();

  UploadFiasXMLFileIntoDBFile(FileNameAddrobj , T_AS_ADDROBJ );
  /*
  UploadFiasXMLFileIntoDBFile(FileNameStead   , T_AS_STEAD   );
  */
  UploadFiasXMLFileIntoDBFile(FileNameHouse   , T_AS_HOUSE   );
  /*
  UploadFiasXMLFileIntoDBFile(FileNameRoom    , T_AS_ROOM    );
  */
  UploadFiasXMLFileIntoDBFile(FileNameSocrbase, T_AS_SOCRBASE);
  UploadFiasXMLFileIntoDBFile(FileNameCurentst, T_AS_CURENTST);
  UploadFiasXMLFileIntoDBFile(FileNameOperstat, T_AS_OPERSTAT);
  UploadFiasXMLFileIntoDBFile(FileNameCenterst, T_AS_CENTERST);
  UploadFiasXMLFileIntoDBFile(FileNameHststat , T_AS_HSTSTAT );
  UploadFiasXMLFileIntoDBFile(FileNameEststat , T_AS_ESTSTAT );
  UploadFiasXMLFileIntoDBFile(FileNameStrstat , T_AS_STRSTAT );

  return stat;
end;

private macro _FillAdmterr()
  SQL_Execute("DELETE FROM dadmterr_dbt WHERE t_Kladr = CHR(88);", "Обновление справочников...");
  SQL_Execute("INSERT INTO dadmterr_dbt (t_Place, t_NamePlace, t_Kladr) SELECT place, placename, chr(88) FROM " +
              "  (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)) AS place, TRIM(CHR(32) FROM T_SOCRNAME) AS placename " + 
              "  FROM das_socrbase_dbt WHERE t_level IN (1, 3, 4, 6, 65, 7) AND " +
              "    (NOT TRIM(CHR(32) FROM t_scname) IN (SELECT t_place FROM dadmterr_dbt)));", "Обновление справочников...");

  SQL_Execute("UPDATE dadmterr_dbt SET t_Kladr = CHR(88) " + 
              "WHERE t_Place IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)) FROM das_socrbase_dbt);", "Обновление справочников...");

  SQL_Execute("UPDATE dadmterr_dbt SET t_RegionLevel = CHR(0), t_ProvinceLevel = CHR(0), " + 
              "  t_DistrictLevel = CHR(0), t_PlaceLevel = CHR(0), t_StreetLevel = CHR(0) " + 
              "WHERE t_Kladr = CHR(88); ", "Обновление справочников...");

  SQL_Execute("UPDATE dadmterr_dbt SET t_RegionLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 1);", "Обновление справочников...");
  SQL_Execute("UPDATE dadmterr_dbt SET t_ProvinceLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 3);", "Обновление справочников...");
  SQL_Execute("UPDATE dadmterr_dbt SET t_DistrictLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 4);", "Обновление справочников...");
  SQL_Execute("UPDATE dadmterr_dbt SET t_PlaceLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 6);", "Обновление справочников...");
  SQL_Execute("UPDATE dadmterr_dbt SET t_PlanLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 65);", "Обновление справочников...");
  SQL_Execute("UPDATE dadmterr_dbt SET t_StreetLevel = CHR(88) WHERE t_Kladr = CHR(88) AND (t_Place, t_NamePlace) IN (SELECT DISTINCT(TRIM(CHR(32) FROM t_scname)), TRIM(CHR(32) FROM t_socrname) FROM das_socrbase_dbt WHERE t_level = 7);", "Обновление справочников...");
end;

private macro _ConvertYYYYMMDD_To_StrDate(yyyymmdd)
  var retval;
  retval = substr(yyyymmdd, 7, 2) + "." + substr(yyyymmdd, 5, 2) + "." + substr(yyyymmdd, 1, 4);
  return retval;
end;

private macro _CountRecsInTable(TableName)
  var Nrec = 0;
  var rs = execSQLselect("SELECT COUNT(*) FROM " + TableName, null, false);

  if(rs.moveNext())
    Nrec = int(rs.value(0));
  end;
  return Nrec;
end;

private macro _GetDateTimeStamp()
  return string(date:f) + " " + string(time:f);
end;

private macro _PrintProtocol
(
  BeforeDateOfActuality,
  AfterDateOfActuality,
  StartDateTime
)
  var FinishDateTime;

  RsBeginAction(100, "Внимание!!! Выполняется формирование протокола импорта. Ждите...", 0);

[ПРОТОКОЛ
выполнения Процедуры импорта базы данных ФИАС

Дата актуальности БД:
- до начала Процедуры:          ##########
- после выполнения Процедуры:   ##########

Пользователь: ###### ###########################################################

================================================================================
] (_ConvertYYYYMMDD_To_StrDate(BeforeDateOfActuality), _ConvertYYYYMMDD_To_StrDate(AfterDateOfActuality), {oper}, {Name_Oper});

[
Количество записей в БД:];
[- ADDROBJ        ###############] (_CountRecsInTable("das_addrobj_dbt"));
[- CENTERST       ###############] (_CountRecsInTable("das_centerst_dbt"));
[- CURENTST       ###############] (_CountRecsInTable("das_curentst_dbt"));
[- ESTSTAT        ###############] (_CountRecsInTable("das_eststat_dbt"));
[- HOUSE          ###############] (_CountRecsInTable("das_house_dbt"));
[- HSTSTAT        ###############] (_CountRecsInTable("das_hststat_dbt"));
[- OPERSTAT       ###############] (_CountRecsInTable("das_operstat_dbt"));
[- ROOM           ###############] (_CountRecsInTable("das_room_dbt"));
[- SOCRBASE       ###############] (_CountRecsInTable("das_socrbase_dbt"));
[- STEAD          ###############] (_CountRecsInTable("das_stead_dbt"));
[- STRSTAT        ###############] (_CountRecsInTable("das_strstat_dbt"));
[
================================================================================
];
[Ошибки:];

  FinishDateTime = _GetDateTimeStamp();
[
================================================================================
Процедура начата:       ###################
Процедура завершена:    ###################
] (StartDateTime, FinishDateTime);

  RsEndAction();

end;

/*Функция проверяет, что файлы имеют размер менее половины размера полного справочника*/
private macro _IsFileTooLarge
(
  FileNameAddrobj : string,
  FileNameStead : string,
  FileNameHouse : string,
  FileNameRoom : string
)
  var stat = false;
  var AddrobjSize64 : bigint;
  var SteadSize64 : bigint;
  var HouseSize64 : bigint;
  var RoomSize64 : bigint;

  /*размеры полных справочников по состоянию на 24.09.2017*/
  const FullAddrobjSize64 =  2465014857;
  const FullSteadSize64   =   791481637; 
  const FullHouseSize64   = 21837723397;
  const FullRoomSize64    =  6768347921; 
  
  GetFileSize64(FileNameAddrobj, @AddrobjSize64);
  GetFileSize64(FileNameStead, @SteadSize64);
  GetFileSize64(FileNameHouse, @HouseSize64);
  GetFileSize64(FileNameRoom, @RoomSize64);

  if(AddrobjSize64 > FullAddrobjSize64 / 2)
    stat = true;
  elif(SteadSize64 > FullSteadSize64 / 2)
    stat = true;
  elif(HouseSize64 > FullHouseSize64 / 2)
    stat = true;
  elif(RoomSize64 > FullRoomSize64 / 2)
    stat = true;
  end;

  return stat;
end;

macro ImportFIAS(FiasDELTA, PathFIAS)
  var FileNameAddrobj = "";
  var FileNameStead = "";
  var FileNameHouse = "";
  var FileNameRoom = "";
  var FileNameSocrbase = "";
  var FileNameCurentst = "";
  var FileNameOperstat = "";
  var FileNameCenterst = "";
  var FileNameHststat = "";
  var FileNameEststat = "";
  var FileNameStrstat = "";
  var IsExisting;
  var IsDateEqual;
  var IsTooLarge;
  var RegDateOfActuality;
  var ImpDateOfActuality;
  var stat = true;
  var StartDateTime = _GetDateTimeStamp();

  _GetFileNamesFromPath
    (
      PathFIAS,
      @FileNameAddrobj,
      @FileNameStead,
      @FileNameHouse,
      @FileNameRoom,
      @FileNameSocrbase,
      @FileNameCurentst,
      @FileNameOperstat,
      @FileNameCenterst,
      @FileNameHststat,
      @FileNameEststat,
      @FileNameStrstat
    );

  IsExisting = _IsFileNameExisting
    (
      FileNameAddrobj,
      FileNameStead,
      FileNameHouse,
      FileNameRoom,
      FileNameSocrbase,
      FileNameCurentst,
      FileNameOperstat,
      FileNameCenterst,
      FileNameHststat,
      FileNameEststat,
      FileNameStrstat
    );

  if(not IsExisting)
    msgbox("Импорт невозможен: один или более файлов БД ФИАС отсутствуют в каталоге импорта");
    stat = false;
  end;

  if(stat)
    IsDateEqual = _IsFileNamesDateEqual
      (
        FileNameAddrobj,
        FileNameStead,
        FileNameHouse,
        FileNameRoom,
        FileNameSocrbase,
        FileNameCurentst,
        FileNameOperstat,
        FileNameCenterst,
        FileNameHststat,
        FileNameEststat,
        FileNameStrstat
      );
    
    if(not IsDateEqual)
      msgbox("Импорт невозможен: импортируемые файлы относятся к разным датам актуальности БД ФИАС");
      stat = false;
    end;
  end;

  if(stat)
    RegDateOfActuality = _GetRegValueDateOfActuality();
    ImpDateOfActuality = _GetFileNameDate(FileNameAddrobj);
    if(RegDateOfActuality > ImpDateOfActuality)
      if(_YesNoWin("Импорт невозможен: дата актуальности импортируемой БД ФИАС предшествует дате уже импортированной БД. Все равно импортировать?") == 0)
        stat = false;
      end;
    end;
  end;

  if(stat)
    if(FiasDELTA)
      
      IsTooLarge = _IsFileTooLarge
      (
        PathFIAS + "\\" + FileNameAddrobj,
        PathFIAS + "\\" + FileNameStead,
        PathFIAS + "\\" + FileNameHouse,
        PathFIAS + "\\" + FileNameRoom
      );
      
      if(IsTooLarge)
        msgbox("Импорт обновлений невозможен: импортируемые файлы предположительно содержат данные полного справочника БД ФИАС");
        stat = false;
      else
        stat = _ImportFIAS_DELTA
        (
          PathFIAS + "\\" + FileNameAddrobj,
          PathFIAS + "\\" + FileNameStead,
          PathFIAS + "\\" + FileNameHouse,
          PathFIAS + "\\" + FileNameRoom,
          PathFIAS + "\\" + FileNameSocrbase,
          PathFIAS + "\\" + FileNameCurentst,
          PathFIAS + "\\" + FileNameOperstat,
          PathFIAS + "\\" + FileNameCenterst,
          PathFIAS + "\\" + FileNameHststat,
          PathFIAS + "\\" + FileNameEststat,
          PathFIAS + "\\" + FileNameStrstat
        );
      end;
    else
      stat = _ImportFIAS_FULL
      (
        PathFIAS + "\\" + FileNameAddrobj,
        PathFIAS + "\\" + FileNameStead,
        PathFIAS + "\\" + FileNameHouse,
        PathFIAS + "\\" + FileNameRoom,
        PathFIAS + "\\" + FileNameSocrbase,
        PathFIAS + "\\" + FileNameCurentst,
        PathFIAS + "\\" + FileNameOperstat,
        PathFIAS + "\\" + FileNameCenterst,
        PathFIAS + "\\" + FileNameHststat,
        PathFIAS + "\\" + FileNameEststat,
        PathFIAS + "\\" + FileNameStrstat
      );
    end;
  end;

  if(stat)
    _FillAdmterr();
  end;

  if(stat)
    _SetRegValueDateOfActuality(ImpDateOfActuality);
  end;

  if(stat)
    _PrintProtocol
    (
      RegDateOfActuality,
      ImpDateOfActuality,
      StartDateTime
    );
  end;

  return stat;
end;
