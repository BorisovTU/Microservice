/*
$Name:             Prcsio3352ux.mac
$Module:           ББ и РКО
$Description:      Печать приходного кассового ордера 3352-У (xls)
*/


import prcs, "BnkTemplReport.mac";

 
class (BnkTemplReport)  Prcsio3352uxReport( data : TIncOrderPrintData3352)

  var d = data;
  
  macro Create()
   
    array ArrayClient, ArrayPayerBankName, ArrayReceiverBankName, ArrayGround, ArrayReceiverName, ArraySum;
    StrSplit (d.Client, ArrayClient, 40, 33, 2);
    StrSplit(d.ReceiverName, ArrayReceiverName, 40, 33, 2);
    StrSplit(d.PayerBankName, ArrayPayerBankName, 60, 45, 2);
    StrSplit(d.ReceiverBankName, ArrayReceiverBankName, 60, 45, 2);
    StrSplit(d.Ground, ArrayGround, 73, 91, 2);
    StrSplit(d.SumString, ArraySum, 85, 70, 2);
   
     //Заполнение отчета
     SetValues( "Number1", "Отрывной талон к приходному кассовому ордеру № " + d.Number,
                "Number2",d.Number:c,
                "DateStr", d.DateStr, 
                "Client1", ArrayClient(0),
                "Client2", ArrayClient(1),
                "PayerAccount", d.PayerAccount,
                "FICode", d.FICode,
                "AmountStr", d.AmountStr,
                "ReceiverName1", ArrayReceiverName(0),
                "ReceiverINN", d.ReceiverINN,
                "PayerBankName1", ArrayPayerBankName(0),
                "PayerBankName2", ArrayPayerBankName(1),
                "ReceiverBankName1", ArrayReceiverBankName(0),
                "ReceiverBankName2", ArrayReceiverBankName(1),
                "BIC1", d.BIC1,
                "BIC2", d.BIC2,
                "ShifrOper", d.ShifrOper,
                "Ground1", ArrayGround(0),
                "Ground2", ArrayGround(1),
                "ReceiverName1",  ArrayReceiverName(0),
                "CreditListAccount",  d.CreditList.value(0).Account,
                "CreditListFICode", d.CreditList.value(0).FICode,
                "CreditListAmount", d.CreditList.value(0).Amount,
                "CreditList2Account",d.CreditList2.value(0).Account
              );

    var tableCreditList = RegisterTable("CreditList");

    if ( d.CreditList.value(1).Account == "")
      SetValues("ReceiverName2", ArrayReceiverName(1));
    end;
            
    var index = 1;    
    while( (index < d.CreditList.size) and (d.CreditList.value(index).Account != "") )
      if (index == 1) 
        tableCreditList.SetValueCell("ReceiverNameTmp",  ArrayReceiverName(1));  
      end;
      tableCreditList.SetValueCell("CreditListAccountTmp",  d.CreditList.value(index).Account);  
      tableCreditList.SetValueCell("CreditListFICodeTmp", d.CreditList.value(index).FICode);           
      tableCreditList.SetValueCell("CreditListAmountTmp", d.CreditList.value(index).Amount);
      tableCreditList.AddStr();
      index = index + 1;
    end;
    tableCreditList.EndTable();

    if (d.CreditList2.value(1).Account == "" )
      SetValues("SymbolTitle", "в том числе по символам:");
    end;
    index = 1;
    var tableCreditList2 = RegisterTable("CreditList2");
    while( (index < d.CreditList2.size) and (d.CreditList2.value(index).Account != "") )
      tableCreditList2.SetValueCell("CreditList2AccountTmp",  d.CreditList2.value(index).Account);  
      if ( ((index + 1 ) == d.CreditList2.size) or 
           (((index + 1 ) < d.CreditList2.size) and (d.CreditList2.value(index + 1).Account == ""))) 
        tableCreditList2.SetValueCell("SymbolTitleTmp", "в том числе по символам:");
      else
        tableCreditList2.SetValueCell("SymbolTitleTmp", "");
      end;

      tableCreditList2.AddStr();
      index = index + 1;
    end;
    
    tableCreditList2.EndTable();
   
    SetValues("ArraySym", d.ArraySym.value(0),
              "ArraySym2", d.ArraySym.value(1),
              "ArraySym3", d.ArraySym.value(2),
              "ArraySym4", d.ArraySym.value(3),
              "ArrayPartSum", MakeMoneyStr3352(d.ArrayPartSum.value(0)),
              "ArrayPartSum2", MakeMoneyStr3352(d.ArrayPartSum.value(1)),
              "ArrayPartSum3", MakeMoneyStr3352(d.ArrayPartSum.value(2)),
              "ArrayPartSum4", MakeMoneyStr3352(d.ArrayPartSum.value(3)),
              "ArraySum1", ArraySum(0)
             );
             
    var tableSymList = RegisterTable("SymList");
    index = 4;
    while( (index < d.ArraySym.size) and (d.ArraySym.value(index) != "") )
      tableSymList.SetValueCell("ArraySym4Tmp",  d.ArraySym.value(index));  
      tableSymList.SetValueCell("ArrayPartSum4Tmp", MakeMoneyStr3352(d.ArrayPartSum.value(index)));           
      if ( index == 4)
        tableSymList.SetValueCell("ArraySum2Tmp", ArraySum(1));           
      end;
      tableSymList.AddStr();
      index = index + 1;
    end;
    tableSymList.EndTable();
    
    if (index == 4)
      SetValues( "ArraySum2" ,  ArraySum(1));    
    end;    
    
   end;
   
   initBnkTemplReport("CreditCashOrder3352-U.xls");

end;

/* Буфер мультивалютного документа, чтобы печать не ругалась */
var pr_multydoc:TRecHandler = TRecHandler( "multydoc.dbt" );
/* Буфер мемордера, чтобы печать не ругалась */
var pr_cb_doc  :TRecHandler = TRecHandler( "cb_doc.dbt"   ); 

macro PrintDocument(ncopy:integer):bool

  var DocKind:integer = pr_pmpaym.rec.DocKind;
  var data : TIncOrderPrintData3352 = TIncOrderPrintData3352();

  if( ( DocKind == 410/*CASH_PS_INCORDER*/ ) or ( DocKind == 430/*CASH_BOF_INCORDER*/ ) )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pscshdoc );  
  elif( ( DocKind == CB_MULTYDOC ) or ( DocKind == DLDOC_MEMORIALORDER ) )
    data.InitByMemorialDocument( pr_pmpaym, pr_pmrmprop );
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;

  return Prcsio3352uxReport(data).Print();
end;

