/*
$Name:           sfbatch_lib.mac
$Module:         Ядро ГКБО
$Description:    ПЗО: Основные библиотечные функции
*/

Import "globals.mac";
Import RsbDataSet, BilFacturaInter, "sfcommon.mac", "sfcataccbatch.mac", "sfdefopr.mac";


private const PlusCalc_CatCode             = "+Расчеты"; 
private const PlusCalc_CatCode1            = "+РасчетыКомисс1"/*"+Расчеты"*/; 

private const PlusCalcNDS_CatCode          = "+Расчеты НДС"; 
private const MinusNDSAccrual_CatCode      = "-НДС начисленный"; 

private const PlusCalcNVPI_CatCode         = "+Расчеты,НВПИ"; 
private const PlusCalcNDS_NVPI_CatCode     = "+Расчеты НДС,НВПИ";
private const MinusNDSAccrual_NVPI_CatCode = "-НДС начисленный, НВПИ";

private const MinusNdsCatCode = "-НДС"; 
private const PlusNdsCatCode = "+НДС"; 

private const BILTYPE_BILFAC = 1;
private const SFTYPE_DISTRIBUTED = 1;
private const SFTYPE_OBTAINED = 2;


private macro RegCreateSf()
  var Regval = false;
  var error = 0;

  GetRegistryValue( "CB\\BILLFACT\\ФОРМИРОВАТЬ СФ АВТОМАТИЧЕСКИ", V_BOOL, RegVal, error );
    
  if( error != 0)
    RegVal = false;
  end;

  return RegVal;
end;


macro SfDefOprSetOprStatus_Bilf()
  
  var  stat = 0;
  var strSql, cmd;

  if( RegCreateSf() == true )
  
    strSql = " INSERT INTO doprstval_tmp (t_id_operation, t_statuskindid, t_dockind, t_id_step, t_numvalue) " +
             "       SELECT oprtmp.t_id_operation, ?, oprtmp.t_dockind, " +
             "              oprtmp.t_id_step, ? " +
             "         FROM doprtemp_tmp oprtmp, " +
             "              dsfdef_dbt sfdef, " +
             "              dsfcomiss_dbt comiss, " +
             "              dsfcontr_dbt contr " +
             "        WHERE sfdef.t_feetype = ? " +
             "          AND sfdef.t_id = TO_NUMBER (oprtmp.t_documentid) " +
             "          AND (oprtmp.t_errorstatus = 0 OR oprtmp.t_errorstatus IS NULL)"+
             "          AND comiss.t_paynds != ? " +
             "          AND comiss.t_InstantPayment = 'X' " +
             " AND comiss.t_feetype = sfdef.t_feetype " +
             " AND comiss.t_number = sfdef.t_commnumber " +
             " AND contr.t_id = sfdef.t_sfcontrid " +
             " AND (   comiss.t_receiverid IN (SELECT t_partyid FROM ddp_dep_dbt WHERE t_accessmode <> ?) " +
             "      OR contr.t_partyid IN (SELECT t_partyid FROM ddp_dep_dbt WHERE t_accessmode <> ?) " +
             "     ) " +
             " AND NOT EXISTS (SELECT 1 FROM doprstval_tmp stval WHERE stval.t_id_operation = oprtmp.t_id_operation) "; 


    cmd = RsdCommand( strSql );

    cmd.addParam( "", RSDBP_IN, SFDEF_OPRDOCCUR_STATUS     ); // :<512 (Документооборот)>
    cmd.addParam( "", RSDBP_IN, SFDEF_STATUS_NUM_CREARE_SF ); // :<4 (Формирование СФ)>
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD         ); // 1 Периодически
    cmd.addParam( "", RSDBP_IN, SF_NOT_TAX                 );
    cmd.addParam( "", RSDBP_IN, 3                          ); // DEPARTMENT_ACCESS_NOTCABS = 3 "Другая ЦАБС"
    cmd.addParam( "", RSDBP_IN, 3                          ); // DEPARTMENT_ACCESS_NOTCABS = 3 "Другая ЦАБС"

    cmd.execute();   
  end;

  return stat;

end;


macro SfDefOprSetOprStatus_Pay()
  var  stat = 0;
  var strSql, cmd;

  strSql = " INSERT INTO doprstval_tmp (t_id_operation, t_statuskindid, t_dockind, t_id_step, t_numvalue) " +
             " SELECT oprtmp.t_id_operation, ?, oprtmp.t_dockind, " +
             "        oprtmp.t_id_step, ? " +
             "   FROM doprtemp_tmp oprtmp " +
             "  WHERE NOT EXISTS (SELECT 1 FROM doprstval_tmp stval WHERE stval.t_id_operation = oprtmp.t_id_operation) "
             "    AND ( oprtmp.t_errorstatus = 0 OR oprtmp.t_errorstatus IS NULL)";

  cmd = RsdCommand( strSql );

  cmd.addParam( "", RSDBP_IN, SFDEF_OPRDOCCUR_STATUS ); // <521 (Документооборот)>
  cmd.addParam( "", RSDBP_IN, SFDEF_STATUS_NUM_PAY   ); // <5 (Оплата)>
  
  cmd.execute();

  return stat;
end;


private macro SetOprStatusForPay()

  var  stat = 0;
  var strSql, cmd;

  var SfAccrueRegVal = SfComAccrueGetRegValue();

  if((stat == 0) AND (SfAccrueRegVal == 0 /*SFCOM_ACCRUE_ALWAYS*/))

    strSql = " INSERT INTO doprstval_tmp (t_ID_Operation, t_StatusKindID, t_DocKind, t_ID_Step, t_NumValue) " +
             " SELECT oprtmp.t_ID_Operation, ?, oprtmp.t_DocKind, oprtmp.t_ID_Step, ?" +
             "   FROM doprtemp_tmp oprtmp ";
     
    cmd = RsdCommand( strSql );

    cmd.addParam( "", RSDBP_IN, SFDEF_OPRDOCCUR_STATUS  ); // :<512 (Документооборот)>
    cmd.addParam( "", RSDBP_IN, SFDEF_STATUS_NUM_ACCRUE ); // :<3 (Доначисление)>
    
    cmd.execute();   
  elif(stat == 0)

   strSql = "INSERT INTO doprstval_tmp (t_id_operation, t_statuskindid, t_dockind, t_id_step, t_numvalue) " +
            " SELECT oprtmp.t_id_operation, ?, oprtmp.t_dockind, " +
            "       oprtmp.t_id_step, ? " +
            "  FROM doprtemp_tmp oprtmp, dsfdef_dbt sfdef " +
            " WHERE sfdef.t_feetype = ? " +
            "   AND sfdef.t_id = TO_NUMBER (oprtmp.t_documentid) " +
            "   AND ( (? = ? AND sfdef.t_sumnds != 0  ) " +
            "         OR " +
            "         ( EXISTS (SELECT 1 FROM dsfaccrue_dbt accrue WHERE accrue.t_sfdefcomid = sfdef.t_id))" +
            "       )";
  

    cmd = RsdCommand( strSql );

    cmd.addParam( "", RSDBP_IN, SFDEF_OPRDOCCUR_STATUS  ); // :<512 (Документооборот)>
    cmd.addParam( "", RSDBP_IN, SFDEF_STATUS_NUM_ACCRUE ); // :<3 (Доначисление)>
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD      ); // 1 Периодически
    cmd.addParam( "", RSDBP_IN, SfAccrueRegVal          ); // 
    cmd.addParam( "", RSDBP_IN, 2                       ); // SFCOM_ACCRUE_CARD2_NDS
    
    
    cmd.execute();   
  end;

  if( stat == 0 )
    stat = SfDefOprSetOprStatus_Bilf();
  end;

  if(stat == 0)
    stat = SfDefOprSetOprStatus_Pay();
  end;

  return stat;

end;

private macro SetOprStatusForAccrue()
  
  var strSql = " INSERT INTO doprstval_tmp (t_ID_Operation, t_StatusKindID, t_DocKind, t_ID_Step, t_NumValue) " +
               " SELECT oprtmp.t_ID_Operation, ?, oprtmp.t_DocKind, oprtmp.t_ID_Step, ?" +
               " FROM doprtemp_tmp oprtmp "
               " WHERE t_errorstatus = 0 OR t_errorstatus IS NULL ";
     
  var cmd = RsdCommand( strSql );

  cmd.addParam( "", RSDBP_IN, SFDEF_OPRDOCCUR_STATUS  ); // :<512 (Документооборот)>
  cmd.addParam( "", RSDBP_IN, SFDEF_STATUS_NUM_WAIT_EXEC ); // :<1 (Ожидание исполнения)>
    
  cmd.execute();

  return 0;
end;

  
macro SfPercomSetOprStatus( SfSrvDocRec )
  var  stat = 0;
  var strSql, cmd;

  if(stat == 0)  
    strSql = " DELETE FROM doprstval_tmp "; 
     
    cmd = RsdCommand( strSql );
    cmd.execute();   
  end;


  if( stat == 0 )
    if( SfSrvDocRec.Kind == 1 ) // начисление
      stat = SetOprStatusForAccrue();
    else
      stat = SetOprStatusForPay();
    end;

    if(stat == 0)
      strSql = " { CALL RSI_RsbOperation.procSetOprStatusValue() } ";    
      
      cmd = RsdCommand( strSql );
     
      cmd.execute();
    end;
  end;

  return stat;
end;

/**
 @brief Заполнение в doprsetdates_tmp дат выноса комиссий на просрочку
 @return результат работы процедуры
*/
private macro SetOprDatesForOverdue():integer  
  var strSql = " INSERT INTO doprsetdates_tmp (t_ID_Operation, t_ID_Step, t_DateKindID, t_DateNew) " +
               " SELECT oprtmp.t_ID_Operation, oprtmp.t_ID_Step, ?, sfdef.t_PlanPayDate " +
               "   FROM doprtemp_tmp oprtmp, dsfdef_dbt sfdef " +
               "  WHERE (oprtmp.t_errorstatus = 0 OR oprtmp.t_errorstatus IS NULL) " +
               "    AND oprtmp.t_DocKind = 51 " +
               "    AND sfdef.t_ID = TO_NUMBER(oprtmp.t_DocumentID) " +
               "    AND sfdef.t_FeeType = " + SF_FEE_TYPE_PERIOD;
     
  var cmd = RsdCommand(strSql);
  cmd.addParam("", RSDBP_IN, SF_OPR_DK_DEBTOVERDUE); //Вынос на просрочку
  cmd.execute();

  return 0;
end;

/**
 @brief Массовая установка дат шагов на комиссиях по doprsetdates_tmp
 @return результат работы процедуры
*/
private macro MassSetOprDates():integer
  SQL_Execute("DECLARE "+
              " v_Error INTEGER; "+
              "BEGIN "+
              " v_Error := RSI_RsbOperation.procMassSetOprDates( true ); "+
              " IF( v_Error != 0 ) THEN "+
              "   RAISE_APPLICATION_ERROR( -20101, 'Error procMassSetOprDates' ); "+
              " END IF; "+
              "END; " );
  return 0;
onerror
  return 1;
end;

/**
 @brief Установка дат шагов на комиссиях 
 @param[in]  SfSrvDocRec  структура сервисной операции 
 @return результат обработки
*/
macro SfPercomSetOprDates(SfSrvDocRec):integer
  var stat = 0;
  var strSql, cmd;

  if(stat == 0)  
    strSql = " DELETE FROM doprsetdates_tmp "; 
     
    cmd = RsdCommand( strSql );
    cmd.execute();   
  end;

  if( (stat == 0) and (SfSrvDocRec.Kind == 1) ) // начисление
    stat = SetOprDatesForOverdue(); //Заполнение дат выноса на просрочку

    if(stat == 0)
      stat = MassSetOprDates();
    end;
  end;

  return stat;
end;

private macro GetSfDefRec(sfdef, rs)
  ClearRecord(sfdef);

  sfdef.FEETYPE          = rs.value("T_FEETYPE");      
  sfdef.ID               = rs.value("T_ID");
  sfdef.COMMNUMBER       = rs.value("T_COMMNUMBER");
  sfdef.STATUS           = rs.value("T_STATUS");
  sfdef.FIID_SUM         = rs.value("T_FIID_SUM");      
  sfdef.SUM              = rs.value("T_SUM");
  sfdef.SUMNDS           = rs.value("T_SUMNDS");
  sfdef.SFCONTRID        = rs.value("T_SFCONTRID");
  sfdef.DEPARTMENT       = rs.value("T_DEPARTMENT");      
  sfdef.NDSRATEVALUE     = rs.value("T_NDSRATEVALUE");
  sfdef.ISINCLUDED       = rs.value("T_ISINCLUDED");
  sfdef.FACTURAID        = rs.value("T_FACTURAID");
  sfdef.DATEFEE          = rs.value("T_DATEFEE");      
  sfdef.BASESUM          = rs.value("T_BASESUM");
  sfdef.FIID_BASESUM     = rs.value("T_FIID_BASESUM");
  sfdef.PERCENT          = rs.value("T_PERCENT");
  sfdef.SUM_PER_UNIT     = rs.value("T_SUM_PER_UNIT");      
  sfdef.FIID_TARSCL      = rs.value("T_FIID_TARSCL");
  sfdef.INVOICEID        = rs.value("T_INVOICEID");
  sfdef.BASEQUANT        = rs.value("T_BASEQUANT");
  sfdef.DATEPAY          = rs.value("T_DATEPAY");      
  sfdef.DATEPERIODBEGIN  = rs.value("T_DATEPERIODBEGIN");
  sfdef.DATEPERIODEND    = rs.value("T_DATEPERIODEND");
  sfdef.SPGROUNDID       = rs.value("T_SPGROUNDID");
  sfdef.EXTCOMID         = rs.value("T_EXTCOMID");      
  sfdef.COMMENT          = rs.value("T_COMMENT");
  sfdef.SKIPEDBYMACRO    = rs.value("T_SKIPEDBYMACRO");
  sfdef.ISHANDMADE       = rs.value("T_ISHANDMADE");
  sfdef.ID_OPERATION     = rs.value("T_ID_OPERATION");
  sfdef.ID_STEP          = rs.value("T_ID_STEP");
  sfdef.ID_PAYSTEP       = rs.value("T_ID_PAYSTEP");
  sfdef.OPRCOMMNUMBER    = rs.value("T_OPRCOMMNUMBER");
  sfdef.ISDOC            = rs.value("T_ISDOC");
  sfdef.FIIDPAYER        = rs.value("T_FIIDPAYER");
  sfdef.ACCOUNTPAYER     = rs.value("T_ACCOUNTPAYER");
  sfdef.OPER             = rs.value("T_OPER");
  sfdef.CODE             = rs.value("T_CODE");
  sfdef.USERFIELD1       = rs.value("T_USERFIELD1");
  sfdef.USERFIELD2       = rs.value("T_USERFIELD2");
  sfdef.USERFIELD3       = rs.value("T_USERFIELD3");
  sfdef.USERFIELD4       = rs.value("T_USERFIELD4");
  sfdef.CLOSEDATE        = rs.value("T_CLOSEDATE");
  sfdef.FIID_PAYSUM      = rs.value("T_FIID_PAYSUM");
  sfdef.CALCCOMISSSUMALG = rs.value("T_CALCCOMISSSUMALG");
  sfdef.CONCOMID         = rs.value("T_CONCOMID");
  sfdef.DISCOUNTID       = rs.value("T_DISCOUNTID");
  sfdef.PLANPAYDATE      = rs.value("T_PLANPAYDATE");
  sfdef.ISDEBTOVERDUE    = rs.value("T_ISDEBTOVERDUE");

  return sfdef;
end;

private macro GetSfContrRec(contr, rs)
  ClearRecord(contr);

  contr.ID              = rs.value("SfContrID");
  contr.OBJECTTYPE      = rs.value("T_OBJECTTYPE");
  contr.FIID            = rs.value("T_FIID");
  contr.OBJECT          = rs.value("T_OBJECT");
  contr.PARTYID         = rs.value("T_PARTYID");
  contr.NUMBER          = rs.value("T_NUMBER");
  contr.DATEBEGIN       = rs.value("T_DATEBEGIN");
  contr.DATEPROLONG     = rs.value("T_DATEPROLONG");
  contr.DATECLOSE       = rs.value("T_DATECLOSE");
  contr.PAYMETHOD       = rs.value("T_PAYMETHOD");
  contr.NAME            = rs.value("T_NAME");
  contr.DATECONC        = rs.value("T_DATECONC");
  contr.CONTRACTORID    = rs.value("T_CONTRACTORID");
  contr.SERVKIND        = rs.value("T_SERVKIND");
  contr.SERVKINDSUB     = rs.value("T_SERVKINDSUB");
  contr.SETACCSEARCHALG = rs.value("T_SETACCSEARCHALG");
  contr.INVMETHOD       = rs.value("T_INVMETHOD");
  contr.PAYFIID         = rs.value("T_PAYFIID");
  contr.PAYRATEID       = rs.value("T_PAYRATEID");
  contr.PAYRATEPERCENT  = rs.value("T_PAYRATEPERCENT");
  contr.PAYRATEDATEKIND = rs.value("T_PAYRATEDATEKIND");
  contr.INVOICEDURATION = rs.value("T_INVOICEDURATION");
  contr.DEPARTMENT      = rs.value("T_DEPARTMENT");
  contr.ACCCODE         = rs.value("T_ACCCODE");
  contr.BRANCH          = rs.value("T_BRANCH");
  contr.UNIONCONTRID    = rs.value("T_UNIONCONTRID");
  contr.PREACPTID       = rs.value("T_PREACPTID");
                                    
  return contr;
end;

private macro GetSfDefFacturaLines( sfdef, sfcomiss, bilOprCodeID, TaxSchema )
  var facturaLineArray = TArray;

  facturaLineArray[0] = TRecHandler("bilfacturaline.dbt");
  facturaLineArray[0].Clear();    
    
  facturaLineArray[0].rec.FacturaLineID = 0; 
  facturaLineArray[0].rec.LineNo  = 1;
                                              
  facturaLineArray[0].rec.ProductID     = sfcomiss.ProductID;
  facturaLineArray[0].rec.ProductName   = sfcomiss.name + sfdef.DateFee;

  if( TaxSchema != NULLVAL )
    facturaLineArray[0].rec.TaxSchema     = TaxSchema;
  end;

  facturaLineArray[0].rec.Amount        = sfdef.Sum;          
  facturaLineArray[0].rec.AmountWithNDS = sfdef.Sum + sfdef.SumNDS;
  facturaLineArray[0].rec.NDSAmount     = sfdef.SumNDS;  
  facturaLineArray[0].rec.NDSRate       = sfdef.NDSRateValue; 

  facturaLineArray[0].rec.ExciseAmount  = $0;

  facturaLineArray[0].rec.ComisID       = sfdef.ID;  
  facturaLineArray[0].rec.ComisType     = sfdef.FeeType; 
  facturaLineArray[0].rec.OprCodeID     = bilOprCodeID; 

  return facturaLineArray;
end;

/// sfdef - буфер УПК - записи таблицы dsfdef_dbt;
/// sfcomiss - буфер вида комиссии - записи таблицы dsfcomiss_dbt;
/// sfcontr - буфер ДО - записи таблицы dsfcontr_dbt;
/// factura - буфер СФ - записи таблицы dbilfactura_dbt - выходной параметр.
private macro SfDefFillBillFactura( sfdef, sfcomiss, sfcontr, factura, BankID, OperID, HeadBankID )

  ClearRecord(factura);
  factura.FacturaID = 0;

  if(sfcomiss.ReceiverID == BankID)
     factura.Direction = SFTYPE_DISTRIBUTED; // Направление СФ - выданный
  else
     factura.Direction = SFTYPE_OBTAINED;   // Направление СФ - полученный
  end;

  factura.SfTypeID = BILTYPE_BILFAC; // Вид счета-фактуры
  factura.Department = sfdef.Department; // Филиал удержанной комиссии
  factura.Branch = sfcontr.Branch; // Подразделение, в котором взимается комиссия
  if(factura.Branch == 0)
    factura.Branch = factura.Department;
  end;

  factura.BankDate = sfdef.DateFee; // Операционный день создания СФ
  factura.SysDate = date(); // Системная дата создания СФ
  factura.SysTime = time(); // Системное время  создания СФ
  factura.Oper = OperID;    // Операционист, выполняющий создание СФ

  factura.CreationDate = // Дата составления
  factura.AcquisitionDate = sfdef.DateFee;  // Дата получения
  factura.RegDate = sfdef.DateFee; // Дата регистрации
  factura.FIID = sfdef.FIID_Sum; // Валюта СФ
  factura.Status = 1; // Статус - введен
  factura.Assignment = 1; // Назначение - оплата

  factura.TotalAmount  = sfdef.Sum;          
  factura.TotalNDS     = sfdef.SumNDS;  
  factura.TotalWithNDS = sfdef.Sum + sfdef.SumNDS;

  if( factura.Direction == SFTYPE_DISTRIBUTED )
     factura.SupplierID = HeadBankID; // Поставщик
     factura.ReceiverID = sfcontr.PartyID;  // Получатель
  else
     factura.SupplierID = sfcontr.PartyID; // Поставщик
     factura.ReceiverID = HeadBankID; // Получатель
     factura.FacturaNumber = string( sfdef.ID ); // Номер СФ
     factura.IsHanded = "X"; // Выдан
  end;
end;

// Функция формирования СФ по УК
// вызывается из сишника и из макроса
macro SFCreateBilFacturaBatch( FeeType )
  var stat:integer = 0;
  var strSql, cmd, rs;
  var bilOprCodeID = "";
  var DefIDArray = TArray;
  var i = 0, indx = 0;

  record sfdef ("sfdef.dbt");
  record factura ("bilfactura.dbt");
  record sfcomiss ("sfcomiss.dbt");
  record contr ("sfcontr.dbt");

  ClearRecord(sfcomiss);
  ClearRecord(sfcomiss);
  ClearRecord(sfcomiss);
  ClearRecord(sfcomiss);
  var bfBatch = RslBilFacturaBatchCharger;

  cmd = RsdCommand( "SELECT t_id FROM DBILOPRCODE_DBT WHERE t_code = '01'" );
  cmd.NullConversion = true;

  rs = RsdRecordset( cmd ); 
  if( rs.moveNext() )
    bilOprCodeID = rs.value("t_id");     
  end;

  var BankID = {OurBank};
  var OperID = {Oper};
  var HeadBankID = {HeadBankID};

  cmd = RsdCommand();
  cmd.NullConversion = true;

  if(FeeType == SF_FEE_TYPE_PERIOD)
    strSql = " SELECT sfdef.*, opr.t_ID_Operation as ID_Operation, opr.t_ID_Step as ID_Step, " +
             " sfcomiss.t_Name As ComissName, sfcomiss.t_ProductID as ProductID, sfcomiss.t_ReceiverID as ComissReceiverID, bilproduct.t_TaxSchema as TaxSchema, contr.*, contr.t_ID as SfContrID " +
             " FROM doprtemp_tmp opr, dsfdef_dbt sfdef, dsfcomiss_dbt sfcomiss, dbilproduct_dbt bilproduct, dsfcontr_dbt contr" +
             " WHERE sfdef.t_FeeType = ? " +
             " AND sfdef.t_ID = to_number(opr.t_documentID) " +
             " AND sfcomiss.t_feetype = sfdef.t_feetype AND sfcomiss.t_number = sfdef.t_commnumber " +
             " AND bilproduct.t_productID(+) = sfcomiss.t_ProductID " +
             " AND contr.t_ID = sfdef.t_SfContrID " ;

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD ); 
  elif(FeeType == SF_FEE_TYPE_SINGLE)
    strSql = " SELECT sfdef.*, tmp.t_ID_Operation as ID_Operation, tmp.t_ID_Step as ID_Step, " +
             " sfcomiss.t_Name As ComissName, sfcomiss.t_ProductID as ProductID, sfcomiss.t_ReceiverID as ComissReceiverID, bilproduct.t_TaxSchema as TaxSchema, contr.*, contr.t_ID as SfContrID" +
             " FROM  dsfdeftmp_tmp tmp, dsfdef_dbt sfdef, dsfcomiss_dbt sfcomiss, dbilproduct_dbt bilproduct, dsfcontr_dbt contr " +
             " WHERE sfdef.t_FeeType = ? " +
             " AND sfdef.t_ID = tmp.t_SfDefID " +
             " AND sfcomiss.t_feetype = sfdef.t_feetype AND sfcomiss.t_number = sfdef.t_commnumber " +
             " AND bilproduct.t_productID(+) = sfcomiss.t_ProductID " +
             " AND contr.t_ID = sfdef.t_SfContrID " +
             " AND sfcomiss.t_paynds != ? " ;

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE ); 
    cmd.addParam( "", RSDBP_IN, SF_NOT_TAX         );
  end;

  cmd.CmdText = strSql;

  i = 0;
  rs = RsdRecordset( cmd ); 
  while( rs.moveNext() )
    GetSfDefRec(sfdef, rs);
    GetSfContrRec(contr, rs);
    sfcomiss.ReceiverID = rs.value("ComissReceiverID");
    sfcomiss.ProductID  = rs.value("ProductID");
    sfcomiss.Name = rs.value("ComissName");

    if((sfcomiss.ReceiverID == BankID) OR (contr.PartyID == BankID))
      var facturaLineArray = GetSfDefFacturaLines( sfdef, sfcomiss, bilOprCodeID, rs.value("TaxSchema"));
      SfDefFillBillFactura( sfdef, sfcomiss, contr, factura, BankID, OperID, HeadBankID );

      bfBatch.add( factura, facturaLineArray, rs.value("ID_Operation"), rs.value("ID_Step"));
      DefIDArray[i] = sfdef.ID;
      i = i + 1;
     end;
  end;

  stat = bfBatch.run();
  stat = stat;

  var ErrCode : TArray; 
  var ErrMessage : TArray;
  var FacturaIDArray : TArray;
  var FeeTypeArray : TArray = TArray;

  var RecCount = bfBatch.GetResult( ErrCode, ErrMessage, FacturaIDArray );

  indx = 0; i = 0;
  while(indx<RecCount)
    i = indx;
    FeeTypeArray[i] = FeeType;

    if(FeeType == SF_FEE_TYPE_PERIOD)
      if(ErrCode[i] != 0)
        // Добавить в лог сообщение ErrMessage[i]
      end;
    end;
    indx = i + 1;
  end;

  if((stat == 0) AND (DefIDArray.Size > 0))
    var upd = RsbSQLInsert("UPDATE dsfdef_dbt SET t_facturaid = ? WHERE t_id = ? AND t_feetype = ?", 3, DefIDArray.Size );
    upd.AddParam( V_INTEGER, FacturaIDArray);
    upd.AddParam( V_INTEGER, DefIDArray );
    upd.AddParam( V_INTEGER, FeeTypeArray );
    if(not upd.Insert())
      stat = 1;
    end;
  end;

  return stat;
end;


//  Функция формирования счетов по КУ для УК

macro SFCreateCatBatch( feetype,        // тип взимания
                        needAccrue,     // признак необходимости открытия счетов для выполнения проводок по начислению.
                        SfAccrueRegVal, // значение настройки "COMMON\\КОМИССИИ\\НАЧИСЛЕНИЕ"
                        ActionDate      // Дата действия
                      )

  var stat:integer = 0;
  var RetVal:bool = true; 
  var strSql, cmd, rs;
  var catAccCharger = TSfDefCatAccCharger(ActionDate, feetype);
  var isNVPI = false;
  var catAccParam;
  var oprID_Operation = 0; 
  var oprID_Step      = 0; 

  record SfContr(sfcontr);
  record ConCom(sfconcom);
  record SfDef(sfdef);

  if((feetype != SF_FEE_TYPE_PERIOD) AND (feetype != SF_FEE_TYPE_SINGLE))
    return stat;
  end;


  cmd = RsdCommand();
  cmd.NullConversion = true;


  strSql = " SELECT tmp.t_ID_Operation As oprID_Operation, tmp.t_ID_Step As oprID_Step, " +

             // УПК
           " sfdef.t_ID As sfdefID, sfdef.t_FIID_Sum As sfdefFIID_Sum, sfdef.t_Sum, sfdef.t_SumNDS, sfdef.t_DateFee, " +

             // ДО
           " contr.t_ID As contrID, contr.t_ServKind As contrServKind, " +
           " contr.t_PartyID As contrPartyID, contr.t_Department As contrDepartment," +
           " contr.t_PayRateID As contrPayRateID, contr.t_PayRateDateKind As contrPayRateDateKind, " +
           " contr.t_PayRatePercent As contrPayRatePercent, " +
           " contr.t_PayFIID As contrPayFIID, contr.t_Number As contrNumber, contr.t_DateConc contrDateConc," +
           " contr.t_Branch As contrBranch, " +
           // код КУ для оплаты НДС (+/-НДС)
           " NVL(categ.t_Code, chr(1)) As CategNDSCode, " +
// валюта оплаты, в ней открываются счета по КУ
           " DECODE(comiss.t_FIID_paySum, -1, sfdef.t_FIID_Sum, comiss.t_FIID_paySum) As payFIID, " +
             // комиссия ДО
           " concom.t_ID As concomID, " +
           " concom.t_feeType As concomFeeType, concom.t_CommNumber As concomNumber ";
  if(feetype == SF_FEE_TYPE_PERIOD)
    strSql = strSql + " FROM doprtemp_tmp tmp, dsfdef_dbt sfdef, dsfcontr_dbt contr, dsfconcom_dbt concom, dsfcomiss_dbt comiss, dmccateg_dbt categ "+
                      " WHERE sfdef.t_FeeType = ? " +
                      " AND sfdef.t_Sum <> 0 " +
                      " AND sfdef.t_ID = to_number(tmp.t_documentID) ";
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD); 

  elif(feetype == SF_FEE_TYPE_SINGLE)
    strSql = strSql + "   FROM dsfdeftmp_tmp tmp, dsfdef_dbt sfdef, dsfcontr_dbt contr, dsfconcom_dbt concom, dsfcomiss_dbt comiss, dmccateg_dbt categ " +
                      " WHERE sfdef.t_FeeType = ? " +
                      " AND sfdef.t_Sum <> 0 " +
                      " AND sfdef.t_ID = tmp.t_SfDefID " +
                      " AND tmp.t_DateFee = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE); 
    cmd.addParam( "", RSDBP_IN, ActionDate); 
  end;
                       
  strSql = strSql + " AND contr.t_ID = sfdef.t_SfContrID " +
                    " AND concom.t_ID = sfdef.t_ConComID " + 
                    " AND comiss.t_feeType = sfdef.t_feeType " + 
                    " AND comiss.t_Number = sfdef.t_CommNumber " + 
                    " AND categ.t_LevelType(+) = ? " + 
                    " AND categ.t_Number(+) = comiss.t_NDSCateg " ; 
     
  cmd.addParam( "", RSDBP_IN, 1 ); // MCCATEG_LEVEL_TYPE_CATEGORY  

  cmd.cmdText = strSql;
  rs = RsdRecordset( cmd ); 
  while( rs.moveNext() )
    ClearRecord(SfContr);
    SfContr.ID              = rs.value("contrID");     
    SfContr.ServKind        = rs.value("contrServKind");
    SfContr.PartyID         = rs.value("contrPartyID");
    SfContr.PayRateID       = rs.value("contrPayRateID");
    SfContr.PayRateDateKind = rs.value("contrPayRateDateKind");
    SfContr.PayRatePercent  = rs.value("contrPayRatePercent");
    SfContr.PayFIID         = rs.value("contrPayFIID");
    SfContr.Number          = rs.value("contrNumber");
    SfContr.DateConc        = rs.value("contrDateConc");
    SfContr.Department      = rs.value("contrDepartment");
    SfContr.Branch          = rs.value("contrBranch");
    if(SfContr.Branch == 0)
      SfContr.Branch = SfContr.Department;
    end;

    ClearRecord(ConCom);
    ConCom.ID         = rs.value("concomID");
    ConCom.feeType    = rs.value("concomFeeType");
    ConCom.CommNumber = rs.value("concomNumber");

    ClearRecord(SfDef);
    SfDef.ID       = rs.value("sfdefID");
    SfDef.FIID_Sum = rs.value("sfdefFIID_Sum");
    SfDef.Sum      = rs.value("t_Sum");
    SfDef.SumNDS   = rs.value("t_SumNDS");
    SfDef.DateFee  = rs.value("t_DateFee");

    oprID_Operation = rs.value("oprID_Operation");
    oprID_Step      = rs.value("oprID_Step");

    if((SfContr.PayFIID != SfDef.FIID_Sum) AND (SfContr.PayRateDateKind != SFINV_RATEDATEKIND_DRAWN))
      isNVPI = true;
    else
      isNVPI = false;
    end;

    if(needAccrue == true)
      catAccParam = TSfDefCatAccParam;
      catAccParam.SfDefID      = SfDef.ID;
      catAccParam.ID_Operation = oprID_Operation;
      catAccParam.ID_Step      = oprID_Step;     
      catAccParam.Department   = SfContr.Department;
      catAccParam.Branch       = SfContr.Branch;
      catAccParam.FIID         = rs.value("payFIID"); 
      
      catAccCharger.add(CALC_SFSI_KIND, isNVPI, catAccParam);

      if(sfdef.SumNDS != $0)
        // Сформировать параметры для поиска/открытия счета по КУ "+Расчеты НДС"/ "+Расчеты НДС,НВПИ":
        catAccParam = TSfDefCatAccParam;
        catAccParam.SfDefID      = SfDef.ID;
        catAccParam.ID_Operation = oprID_Operation;
        catAccParam.ID_Step      = oprID_Step;     
        catAccParam.Department   = SfContr.Department;
        catAccParam.Branch       = SfContr.Branch;
        catAccParam.FIID         = rs.value("payFIID"); 

        catAccCharger.add(CALCNDS_SFSI_KIND, isNVPI, catAccParam);

        // Сформировать параметры для поиска/открытия счета по КУ "-НДС начисленный"/ "-НДС начисленный, НВПИ"
        catAccParam = TSfDefCatAccParam;
        catAccParam.SfDefID      = SfDef.ID;
        catAccParam.ID_Operation = oprID_Operation;
        catAccParam.ID_Step      = oprID_Step;
        catAccParam.Department   = SfContr.Department;
        catAccParam.Branch       = SfContr.Branch;
        catAccParam.FIID         = rs.value("payFIID"); 

        catAccCharger.add(NDSACRUAL_SFSI_KIND, isNVPI, catAccParam);
      end;

    end;

    if(sfdef.SumNDS != $0)
      catAccParam = TSfDefCatAccParam;
      catAccParam.SfDefID      = SfDef.ID;
      catAccParam.ID_Operation = oprID_Operation;
      catAccParam.ID_Step      = oprID_Step;     
      catAccParam.Department   = SfContr.Department;
      catAccParam.Branch       = SfContr.Branch;
      catAccParam.FIID         = rs.value("payFIID"); 

      var CategNDSCode = rs.value("CategNDSCode");

      if(CategNDSCode == "+НДС")
        catAccCharger.add(NDS_SFSI_KIND, true, catAccParam);
      elif(CategNDSCode == "-НДС")
        catAccCharger.add(NDS_SFSI_KIND, false, catAccParam);
      end;
    end;
  end;

  if(stat == 0)
    if(needAccrue == true)
      // Пакетно получить счета по КУ "+Расчеты"/ "+Расчеты,НВПИ":
      if(RetVal == true)
         If(StrFor(GetIdentProgram()) == "S") // ценные бумаги
            RetVal = catAccCharger.FindAndOpenAccountBatch(PlusCalc_CatCode1);
         else
        RetVal = catAccCharger.FindAndOpenAccountBatch(PlusCalc_CatCode);
      end;
      end;
      if(RetVal == true)
        RetVal = catAccCharger.FindAndOpenAccountBatch(PlusCalcNVPI_CatCode);
      end;

      // Пакетно получить счета по КУ "+Расчеты НДС"/ "+Расчеты НДС,НВПИ":
      if(RetVal == true)
        RetVal = catAccCharger.FindAndOpenAccountBatch(PlusCalcNDS_CatCode);
      end;
      if(RetVal == true)
        RetVal = catAccCharger.FindAndOpenAccountBatch(PlusCalcNDS_NVPI_CatCode);
      end;

      // Пакетно получить счета по КУ "-НДС начисленный" / "-НДС начисленный, НВПИ":
      if(RetVal == true)
        RetVal = catAccCharger.FindAndOpenAccountBatch(MinusNDSAccrual_CatCode);
      end;
      if(RetVal == true)
        RetVal = catAccCharger.FindAndOpenAccountBatch(MinusNDSAccrual_NVPI_CatCode);
      end;
    end;

    // Пакетно получить счета по КУ "-НДС":  
    if(RetVal == true)
      RetVal = catAccCharger.FindAndOpenAccountBatch(MinusNdsCatCode);
    end;

    // Пакетно получить счета по КУ "+НДС":  
    if(RetVal == true)
      RetVal = catAccCharger.FindAndOpenAccountBatch(PlusNdsCatCode);
    end;
  end;

  return stat;
end;


macro SFUpdateSfDefStatusIncBatch(FeeType, sfdefArray)
  var stat = 0;

  // из-за проблем в RSL с пакетным обновлением полей с типом char приходится выполнять
  // обновление поля IsIncluded  2 вариантами
  var StatusArray    = TArray;
  var StatusArrayInc = TArray;

  var DefIDArray     = TArray;
  var DefIDArrayInc  = TArray;

  var i = 0;     
  var sizea = 0, sizeInc = 0;
  var sizeSfDefArray = sfdefArray.size;
  while(i < sizeSfDefArray)
    if(sfdefArray[i] != NULL)
      if((sfdefArray[i].Error == 0))
        if( sfdefArray[i].SfDef.rec.IsIncluded == "" )
          StatusArray[sizea]     = sfdefArray[i].SfDef.rec.Status;
          DefIDArray[sizea]      = sfdefArray[i].SfDef.rec.ID;
          sizea = sizea + 1;
        else
          StatusArrayInc[sizeInc]     = sfdefArray[i].SfDef.rec.Status;
          DefIDArrayInc[sizeInc]      = sfdefArray[i].SfDef.rec.ID;
          sizeInc = sizeInc + 1;
        end;         

        if(FeeType == SF_FEE_TYPE_PERIOD)
          if(sfdefArray[i].SfDef.rec.Status != sfdefArray[i].OldStatus)
            var StatDocID = String(sfdefArray[i].SfDef.rec.ID:10)+String(sfdefArray[i].OldStatus:5)+String(sfdefArray[i].oprID_Operation:10)+String(sfdefArray[i].oprID_Step:5)+String(FeeType:2);
            StatDocID = StrSubst( StatDocID, " ", "0" );
            AddDocumentToStepMass(2001, StatDocID, sfdefArray[i].oprID_Operation, sfdefArray[i].oprID_Step); // SFDDEFCOM_CHANGE_STATUS = 2001
          end;

          if(sfdefArray[i].SfDef.rec.IsIncluded != sfdefArray[i].OldIsIncluded)
            // sprintf( DocID, "%05d%010d", (int)InclPrm->feeType, (int)InclPrm->DefComID );
            var IncDocID = String(FeeType:5)+String(sfdefArray[i].SfDef.rec.ID:10);
            IncDocID = StrSubst( IncDocID, " ", "0" );
            AddDocumentToStepMass(2005, IncDocID, sfdefArray[i].oprID_Operation, sfdefArray[i].oprID_Step); // SFDEF_SET_ISINCLUDED = 2005
          end;
        end;
      end;
    end;
    i = i + 1;
  end;
  
  if( sizea > 0 )
    var upd = RsbSQLInsert("UPDATE dsfdef_dbt SET t_Status = ?, t_IsIncluded = chr(0) WHERE t_FeeType = " + FeeType + " AND t_id = ?", 2, sizea );
    upd.AddParam( V_INTEGER, StatusArray     );
    upd.AddParam( V_INTEGER, DefIDArray      );
    if(not upd.Insert())
      stat = 1;
    end;
  elif( sizeInc > 0 )
    var updInc = RsbSQLInsert("UPDATE dsfdef_dbt SET t_Status = ?, t_IsIncluded = 'X' WHERE t_FeeType = " + FeeType + " AND t_id = ?", 2, sizeInc );
    updInc.AddParam( V_INTEGER, StatusArrayInc  );
    updInc.AddParam( V_INTEGER, DefIDArrayInc   );
    if(not updInc.Insert())
      stat = 1;
    end;
  end;
    
  return stat;
end;
