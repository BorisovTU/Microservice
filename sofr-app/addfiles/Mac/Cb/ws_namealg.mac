/*
$Name:        ws_namealg.mac
$Module:      Ядро ГКБО
$Description: Макрос реализации сервисов получения данных из справочника namealg
*/
import rcw;
import rsd;
import rsbdataset;

class TWsNamealg(_numberAlg : integer, _nameAlg : string, _typeAlg : integer)
  var NumberAlg : integer = _numberAlg;
  var NameAlg   : string  = _nameAlg;
  var TypeAlg   : integer = _typeAlg;
end;

macro CreateNameAlgFromParam(NumberAlg : integer, NameAlg : string, TypeAlg : integer)
  var nameAlgItem = TWsNamealg(NumberAlg, NameAlg, TypeAlg);

  return nameAlgItem;
end;
macro CreateNameAlgFromAppServ(TypeAlg : integer, NumberAlg : integer)
  var sqlText = "select t_sznamealg from dnamealg_dbt where t_iTypeAlg = ? AND t_iNumberAlg = ?";

  var cmd : RsdCommand, dataSet, tempObject = null;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, TypeAlg);
  cmd.AddParam("", RSDBP_IN, NumberAlg);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    tempObject = TWsNameAlg();
  
    tempObject.NameAlg    = dataSet.value(0);
    tempObject.NumberAlg  = NumberAlg;
    tempObject.TypeAlg    = TypeAlg;
  end;

  return tempObject;
end;

MACRO GetListNamealg(TypeAlg : integer, NumberAlgList)
   var p;

   if(TypeAlg <= 0)
     RunError("Не задан тип алгоритма");
   end;
   
   var result = TArray();
   var query = "select * from dnamealg_dbt where t_iTypeAlg = " + string(TypeAlg);

   if (NumberAlgList != NULL)
    var i = 0;
    var appendString = "";

    while(i < NumberAlgList.size)
      appendString = appendString + NumberAlgList[i];

      if (i != NumberAlgList.size - 1)
        appendString = appendString + ", ";
      end;

      i = i + 1;
    end;

    query = query + " AND t_iNumberAlg IN (" + appendString + ")";
   end;

   query = query + " ORDER BY t_iNumberAlg";

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

   while (ds.MoveNext())
      p = TWsNamealg(ds.iNumberAlg, ds.szNameAlg, ds.itypealg);

      result.value(result.Size) = p;
   end;

   return result;
END;
