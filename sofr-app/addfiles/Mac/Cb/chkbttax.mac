import BankInter;

record bt_pmpaym( pmpaym   ); /* Структуры */
record bt_rm    ( pmrmprop );
record bt_debet ( pmprop   );
record bt_credit( pmprop   );

/* Процедура контроля значения реквизита КБК                           */
/* проверка кода подпрограммы (14-17 символы поля 104) на соответствие */
/* значению поля 110 "Тип налогового платежа"                          */
macro ПроверитьКБК( rmprop ): bool
  var result:bool = true;
   
  var masks1 = "182101*,182102*,182103*,182104*,182105*,182106*,182107*,182108*,182109*,"+
               "100101*,100102*,100103*,100104*,100105*,100106*,100107*,100108*,100109*,"+
               "???1100101001????180,???1100102101????180,"+
               "???1100102201????180,???1100102301????180,???1100102401????180,"+
               "???1100200001????180,???1120203001????120,???1120208001????120";

  if   ( CompareStrWithMasks (masks1, rmprop.BttTICode)== 0)

    if(CompareStrWithMasks("01,03,04,05,06,07,08,15", rmprop.TaxAuthorState)==0)

      if  ( CompareStrWithMasks("НС,АВ,ПЛ,ГП,ВЗ", rmprop.TaxPmType)== 0)
        if( SubStr( rmprop.BttTICode, 14, 4) != "1000")
          result = false;
        end;
      elif( CompareStrWithMasks( "СА,АШ,ИШ", rmprop.TaxPmType)== 0) 
        if( SubStr( rmprop.BttTICode, 14, 4) != "3000")
          result = false;
        end;
      elif( rmprop.TaxPmType == "0" ) 
        if( SubStr( rmprop.BttTICode, 14, 4) != "4000")
          result = false;
        end;
      end;           
    end;

    if( CompareStrWithMasks( "ПЕ,ПЦ", rmprop.TaxPmType)== 0)
      if( SubStr( rmprop.BttTICode, 14, 4) != "2000")
        result = false;
      end;
    end;
  end;

   return result;
end;

/* -------------------------------------------------*/
/* Процедура контроля значения реквизита КБК:       */
/* -------------------------------------------------*/
MACRO CheckBttax():bool
   if(not ПроверитьКБК( bt_rm ))
      MemoryError( 2426 ); // "Неверный код бюджетной классификации"
      return false;
   end;

   return true;
END;
