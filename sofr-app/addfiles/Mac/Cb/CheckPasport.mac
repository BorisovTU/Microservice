/*
 $Name: CheckPasport.mac
 $Module: Ядро ГКБО 
 $Description: предупреждение о недействительности паспорта клиента по возрасту 
 */
 /*
	предупреждение о недействительности паспорта клиента по возрасту 
 */
import BankInter, "globals.mac";
Import oralib, rsexts, PTInter;

private const CHECKED_AGE_45 = 45;
private const CHECKED_AGE_20 = 20;

private var MsgMode    : integer = CHKPASSMSG_ALL;
private var AskMsg     : bool    = false;
private var UserSelect : integer = 0;
private var InvalidAge : integer = 0;

record persondic(persnidc);
file fpersondic(persnidc);
file person(persn) key 0;

private macro MsgWrapper(Error, Age)
    var MsgString = "";
    var AsMsg = AskMsg;
    var NeedMsg = false;
    
    if ((MsgMode == CHKPASSMSG_ALL) or (MsgMode == CHKPASSMSG_NOT_EXPIRED))
        NeedMsg = true;
    end;
        
    if (Error == CHKPASSRES_NO_CONTROL)
        MsgString = "Проверка срока действия удостоверения личности не выполнена: документ не является паспортом гражданина РФ";
    elif (Error == CHKPASSRES_NO_ISSUDATE)
        MsgString = "Проверка срока действия паспорта не выполнена: не указана дата выдачи";
    elif (Error == CHKPASSRES_NOT_EXPIRED)
        AsMsg = true;
        if (MsgMode == CHKPASSMSG_ALL)
            NeedMsg = true;
        else
            NeedMsg = false;
        end;
        AsMsg = false;
        MsgString = "Срок действия паспорта РФ не истек";
    elif (Error == CHKPASSRES_NO_BIRTHDATE)
        MsgString = "Проверка срока действия паспорта РФ не выполнена: не указана дата рождения владельца паспорта";
    elif (Error == CHKPASSRES_EXPIRED)
        NeedMsg = false;
        if ((MsgMode == CHKPASSMSG_ALL) or (MsgMode == CHKPASSMSG_NOT_EXPIRED) or (MsgMode == CHKPASSMSG_EXPIRED))
           NeedMsg = true; 
        end;
        MsgString = "ВНИМАНИЕ! Срок действия паспорта РФ истек по достижении владельцем возраста " + Age + " лет. Паспорт подлежит замене и не может быть использован для удостоверения личности владельца";
    else
        MsgString = "Срок действия паспорта РФ не истек.";
    end;
    
    if (NeedMsg)
        if (AsMsg)
            UserSelect = MsgBoxEx (MsgString, MB_OK + MB_CANCEL);
        else
            MsgBox  (MsgString);
        end;
    end;
    
    return Error;
end;

private macro CalculatePersonAge(Person)
    var sqlString = "SELECT MONTHS_BETWEEN (SYSDATE, ?) / 12 AS age FROM DUAL";
    var cmd = RSDCommand( sqlString );
    var rs;
    var age = -1;
    
    cmd.addParam("", RSDBP_IN, Person.BORN);
    cmd.execute();
    rs = RsdRecordset(cmd);
    
    if (rs.moveNext())
        age = Int(rs.value("AGE"));
    end;
    
    return age;
end;

/*
    Вычислить календарную дату наступления Age-летнего возраста
*/
private macro CalculateAgeDate(BirthDate, Age)
    var sqlString = "SELECT ? + INTERVAL '" + Age + "' YEAR AS AGE FROM DUAL";
    var cmd = RSDCommand( sqlString );
    var rs;
    var ret = ZeroValue (V_DATE);
    
    cmd.addParam("", RSDBP_IN, BirthDate);
    cmd.addParam("", RSDBP_IN, Age);
    cmd.execute();
    rs = RsdRecordset(cmd);
    
    if (rs.moveNext())
        ret = Date(rs.value("AGE"));
    end;
    
    return ret;
end;

/*
    Проверить истечение срока действия паспорта
    возвращаемые значения:
    -1:                 перейти к проврке следующего возраста
     0:                 паспорт выдан по наступлении n-летия владельца и поэтому действителен
    CHKPASSRES_EXPIRED: срок действия истёк (завершить работу функции)
*/
private macro CheckPassportForAge(CheckForAge, PersonAge)
    var result = 0;
    if (PersonAge < CheckForAge)
        result = -1;
    else
        var d = CalculateAgeDate(person.BORN, CheckForAge);
        
        if (persondic.PAPERISSUEDDATE < d)
            result = CHKPASSRES_EXPIRED;
        end;
    end;
    
    return result;
end;

private macro HR(result)
    var res = false;
    
    if (result == CHKPASSRES_NOT_EXPIRED)
        res = true;
    end;
    
    return res;
end;

/*
    Контроль срока действия паспорта гражданина РФ
*/
macro CheckPaperRecord(
    Paper                  ,
    PMsgMode      : integer,
    PAskMsg       : bool   ,
 
    OUT_UserAge   :@integer,
    OUT_AskResult :@integer
)
    SetBuff(persondic, Paper);
    var result = CHKPASSRES_NOT_EXPIRED;
    MsgMode = PMsgMode;

    if (MsgMode != 0)
        var prm;
        getparm(2, prm);
        
        if (ValType(prm) != V_UNDEF)
            AskMsg = PAskMsg;
        end;
    end;
    
    if (persondic.PAPERKIND != 0)
        result = MsgWrapper(CHKPASSRES_NO_CONTROL);
    end;
    
    if (HR(result))
        person.PERSONID = persondic.PERSONID;
        
        if (not GetEQ(person))
            result = MsgWrapper(CHKPASSRES_NO_CONTROL);
        end;
    end;
    
    if (HR(result))
        if (persondic.PAPERISSUEDDATE == ZeroValue (V_DATE))
            result = MsgWrapper(CHKPASSRES_NO_ISSUDATE);
        end;
    end;
    
    if (HR(result))
        if (person.BORN == ZeroValue (V_DATE))
            result = MsgWrapper(CHKPASSRES_NO_BIRTHDATE);
        else
            var age = CalculatePersonAge(person);
            var chkres = -1;
            
            // Проверить истечение срока действия паспорта в 45 лет
            chkres = CheckPassportForAge(CHECKED_AGE_45, age);
            
            if (chkres == CHKPASSRES_EXPIRED)
                result = MsgWrapper(chkres, CHECKED_AGE_45);
                InvalidAge = CHECKED_AGE_45;
            end;
            
            // Проверить истечение срока действия паспорта в 20 лет
            if (chkres == -1)
                chkres = CheckPassportForAge(CHECKED_AGE_20, age);
                if (chkres == CHKPASSRES_EXPIRED)
                    result = MsgWrapper(chkres, CHECKED_AGE_20);
                    InvalidAge = CHECKED_AGE_20;
                end;
            end;
        end;
    end;
    
    if (result == CHKPASSRES_NOT_EXPIRED)
        result = MsgWrapper(chkres);
    end;
    
    OUT_UserAge = InvalidAge;
    OUT_AskResult = UserSelect;
    
    return result;
end;

macro CheckPaper(
 PersonID      : integer, // Идентификатор физического лица
 PaperKind     : integer, // Вид удостоверения личности
 PMsgMode      : integer, // Признак необходимости выдачи результата проверки сообщением в пользовательском интерфейсе
 PAskMsg       : bool   , // Признак выдачи сообщений в виде запросов
 
 OUT_UserAge   :@integer, // Возраст владельца для замены удостоверения личности (лет)
 OUT_AskResult :@integer  // Признак ответа пользователя на запрос: "да" или "отмена" (2 - "Да", 3 - "Отмена")
)
    var result = CHKPASSRES_NOT_EXPIRED;

    fpersondic.PERSONID = PersonID;
    fpersondic.PAPERKIND = PaperKind;

    if (not GetEQ(fpersondic))
        result = MsgWrapper(CHKPASSRES_NO_CONTROL);
    end;
    
    if (HR(result))
        Copy(persondic, fpersondic);
        result = CheckPaperRecord (persondic, PMsgMode, PAskMsg, @OUT_UserAge, @OUT_AskResult);
    end;
    
    return result;
end;

/*
    Процедура массового контроля срока действия паспортов гражданина РФ
*/
private macro GetDBDateString()
    var cmd = RSDCommand("select TO_CHAR(sysdate, 'dd.mm.yyyy hh24:mi') as d from dual");
    var d = "";
    cmd.execute();
    var rs = RsdRecordset(cmd);
    
    if (rs.moveNext())
        d = rs.value("d");
    end;
    
    return d;
end;

private macro PrintProtocolLine(Age, Result)
    var PersonString = String(person.PERSONID, " ", person.NAME1, " ", person.NAME2, " ", person.NAME3);
    var AgeString = "";
    var PassportString = "";
    var ResultString = "";
    
    if (person.BORN != ZeroValue (V_DATE))
        AgeString = String(person.BORN, ", ", CalculatePersonAge(person), " лет");
    end;
    
    if (persondic.PAPERSERIES != ZeroValue (V_STRING))
        PassportString = PassportString + persondic.PAPERSERIES + " ";
    end;
    
    if (persondic.PAPERNUMBER != ZeroValue (V_STRING))
        PassportString = PassportString + persondic.PAPERNUMBER + " ";
    end;
    
    if (persondic.PAPERISSUEDDATE != ZeroValue (V_DATE))
        PassportString = PassportString + String("выдан ", persondic.PAPERISSUEDDATE);
    end;

    if (Result == CHKPASSRES_EXPIRED)
        ResultString = String("Срок действия истёк ", CalculateAgeDate(person.BORN, Age));
    elif (Result == CHKPASSRES_NO_BIRTHDATE)
        ResultString = "Не задана дата рождения физлица";
    else
        ResultString = "Не задана дата выдачи паспорта";
    end;
    [| ######################## | ################### | ############################## | ############################## |]
    (PersonString, AgeString, PassportString, ResultString);
    [+--------------------------+---------------------+--------------------------------+--------------------------------+];

end;

private macro MergeProtocols(File1, File2)
    FILE std() txt append;
    FILE tmp() txt;

    if (open(std, File1) and open(tmp, File2))
        while (next (tmp))
            insert(std, tmp.str);
        end;
        close (tmp);
    end;
end;

private macro CountPrivatePersons()
    var count = 0;
    var cmd = RSDCommand(
        "SELECT COUNT (T_PARTYID) FROM DCHKPSPRT_TMP TMP WHERE EXISTS " + 
        "(SELECT T_PERSONID FROM DPERSN_DBT PRSN WHERE TMP.T_PARTYID = PRSN.T_PERSONID)");
        
    cmd.execute();
    var rs = RsdRecordset(cmd);
    
    while(rs.moveNext())
        count = Int(rs.value(0));
    end;
    
    return count;
end;

macro CheckPassportBatch()
    var oldFile = SetOutput (GetTxtFileName("CheckPassportHeader"));
    [Протокол проверки срока действия паспортов гражданина РФ
    
    Дата, время:	################
    Пользователь:	##### #########################################################
    
    ]
    (GetDBDateString(), {oper}, {Name_Oper});

    var HeaderFile = SetOutput (GetTxtFileName("CheckPassportTable"));
    [Результаты:
    ];
    [+--------------------------+---------------------+--------------------------------+--------------------------------+];
    [| Субъект	                | Возраст             | Паспорт                        | Результат проверки             |];
    [+--------------------------+---------------------+--------------------------------+--------------------------------+];
    
    var cmd = RSDCommand(
        "SELECT * FROM DCHKPSPRT_TMP TMP " + 
        "WHERE     TMP.T_PAPERKIND = 0 AND EXISTS " + 
        "(SELECT T_PERSONID FROM DPERSN_DBT PRSN " + 
        "WHERE TMP.T_PARTYID = PRSN.T_PERSONID) ORDER BY TMP.T_PARTYID ASC");
        
    cmd.execute();
    var rs = RsdRecordset(cmd);
    
    var nres = 0;
    var npas = 0;
    var nsub = CountPrivatePersons();
    while (rs.moveNext())
        fpersondic.PERSONID = Int(rs.value(0));
        fpersondic.PAPERKIND = Int(rs.value(1));
        
        person.PERSONID = fpersondic.PERSONID;
        if (GetEQ(fpersondic) and GetEQ(person) and (fpersondic.PAPERKIND == 0))
            Copy(persondic, fpersondic);
            var Age = 0;
            var hr = CheckPaperRecord(persondic, CHKPASSMSG_OFF, false, @Age);
            npas = npas + 1;
            
            if (hr > 0)
                PrintProtocolLine(Age, hr);
                nres = nres + 1;
            end;
        end;
    end;
    var TmpProtocol = SetOutput(HeaderFile, true);
    
    [Проверено:	    #################################
    Выявлено:		######
    
    ](String("субъектов: ", nsub, "  паспортов: ", npas):l, nres);
    
    SetOutput(NULL, true);
    
    MergeProtocols(oldFile, HeaderFile);
    MergeProtocols(oldFile, TmpProtocol);

    RemoveFile(HeaderFile);
    RemoveFile(TmpProtocol); 
end;