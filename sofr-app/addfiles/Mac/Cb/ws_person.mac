/*
$Name: ws_person.mac
$Module: Web-общее
$Description: Макрос виджета выбора пользователя системы
*/

import RsbDataSet, cb_sql, globals, BankInter;
import "web_scroll_lib.mac";


class TWsPerson
  var Number    : integer;
  var Name_     : string;
  var DprtNode  : integer;
  var DprtName  : string;
  var HasDeputy : bool;
  var SpecValue;
end;

macro CB_GetUserList(OperNum, DprtNode, FilterMode, PrivId, sub, listLen)
  var result = TArray();

  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsPerson(), dataSet;

  var sqlText = "SELECT t.t_Oper As Num, t.t_Name As Name_, t.t_CodeDepart As DprtNode, " +
                "NVL (dp_dep.t_Name, chr(1)) As DprtName, " +
                "DECODE (NVL (persondeputy.t_Oper, 0), 0, CHR (0), 'X') As HasDeputy " +
                "FROM dperson_dbt t, ddp_dep_dbt dp_dep, dpersondeputy_dbt persondeputy " +
                "WHERE t.t_CodeDepart = dp_dep.t_Code(+) " +
                "AND t.t_Oper = persondeputy.t_Oper(+) ";

  var filterConditionString = SetOperFilterCondition(DprtNode, Filtermode, PrivId);

  if(filterConditionString != "")
    sqlText = sqlText + " AND " + filterConditionString;
  end;
  
  if(ValType(OperNum) != V_UNDEF)
    sqlText = sqlText + " AND t.t_Oper = ?";

    cmd.AddParam("", RSDBP_IN, OperNum);
  end;

  if(sub != "")

    sqlText = sqlText + " AND LOWER(t.t_Oper) LIKE LOWER ('%' || ? || '%') ";

  end;

  if( listLen != 0 )

    sqlText = sqlText + " AND ROWNUM < " + string(listLen);

  end;  

  cmd.cmdText = sqlText + " ORDER BY t.t_Oper";
  
  if(sub != "")

    cmd.addParam( "", RSDBP_IN, sub );

  end;
  
  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj                     = TWsPerson();

      tempObj.Number    = dataSet.value("Num");
      tempObj.Name_     = SQL_ConvTypeStr(dataSet.value("Name_"));
      tempObj.DprtNode  = SQL_ConvTypeInteger(dataSet.value("DprtNode"));
      tempObj.DprtName  = SQL_ConvTypeStr(dataSet.value("DprtName"));
      tempObj.HasDeputy = SQL_ConvTypeBool(dataSet.value("HasDeputy"));

    outArray[outArray.size]     = tempObj;
  end;

  return outArray;
end;

macro CreatePersonFromParam(Number, Name_, DprtNode, DprtName, HasDeputy, SpecValue)  
  var personObject = TWsPerson();

  personObject.Number    = Number;
  personObject.Name_     = SQL_ConvTypeStr(Name_);
  personObject.DprtNode  = SQL_ConvTypeInteger(DprtNode);
  personObject.DprtName  = SQL_ConvTypeStr(DprtName);
  personObject.HasDeputy = SQL_ConvTypeBool(HasDeputy);

  return personObject;
end;

macro FindPersonByNumber(OperNum)
  var personObject = NULL;

  var cmd : RsdCommand, dataSet;

  var sqlText = "SELECT t.t_Oper As Num, t.t_Name As Name_, t.t_CodeDepart As DprtNode, " +
                "NVL (dp_dep.t_Name, chr(1)) As DprtName, " +
                "DECODE (NVL (persondeputy.t_Oper, 0), 0, CHR (0), 'X') As HasDeputy " +
                "FROM dperson_dbt t, ddp_dep_dbt dp_dep, dpersondeputy_dbt persondeputy " +
                "WHERE t.t_CodeDepart = dp_dep.t_Code(+) " +
                "AND t.t_Oper = persondeputy.t_Oper(+) " +
                "AND t.t_Oper = ?";
  
  cmd.cmdText = sqlText;

  cmd.addParam("", RSDBP_IN, OperNum);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    personObject = TWsPerson();

      personObject.Number    = dataSet.value("Num");
      personObject.Name_     = SQL_ConvTypeStr(dataSet.value("Name_"));
      personObject.DprtNode  = SQL_ConvTypeInteger(dataSet.value("DprtNode"));
      personObject.DprtName  = SQL_ConvTypeStr(dataSet.value("DprtName"));
      personObject.HasDeputy = SQL_ConvTypeBool(dataSet.value("HasDeputy"));
  end;

  return personObject;
end;

/* Получить условия для встраивания в SQL-запрос */
macro WebPerson_GetQueryParam( select : @string, from : @string, where : @string, alias : string, Oper : string )

  WebScroll_AddSelect( @select, string(alias, ".t_Oper", " ", alias, "_Oper") );
  WebScroll_AddSelect( @select, string(alias, ".t_Name", " ", alias, "_Name") );

  WebScroll_AddFrom( @from, string("dperson_dbt ", alias) );

  WebScroll_AddWhere( @where, string(Oper, " = ", alias, ".t_Oper(+)") );

end;


/* Получить условие сортировки для встраивания в SQL-запрос */
macro WebPerson_GetSortParam( alias : string )

  return string(alias, "_Oper");

end;

/* Создать объект TWsDepartment по результатам выполнения SQL-запроса */
macro WebPerson_GetObjectFromRecordset( alias : string, rs : RsdRecordset )

  var p = null;

  var IsOperNull : bool;

  var Oper = rs.value( string(alias, "_Oper"), IsOperNull );

  if( IsOperNull == false )

    p = TWsPerson();

    p.Number = Oper;
    p.Name_  =  SQL_ConvType( rs.value(string(alias, "_Name")) );

  end;

  return p;

end;
