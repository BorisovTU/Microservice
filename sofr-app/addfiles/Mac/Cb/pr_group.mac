/*
Отчет о правах доступа пользователя
*/

import BankInter, "globals.mac", RSD;

private macro PrintHeader()
  [  Отчет о правах доступа группы пользователей];
end;

private macro PrintGroupH(GroupID)
  var strSql : string;
  var rs : RsdRecordset;
  var GroupName : string;

  strSql = "select t_Name from dacsgroup_dbt where t_GroupID = " + GroupID;

  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    GroupName = rs.value( 0 );
  end;
  [
     Группа: ######### #################################################] ( GroupID,  GroupName);
end;

private macro PrintDprt(GroupID)
  var strSql : string;
  var rs : RsdRecordset;
  var Dprt : string;

  strSql = " SELECT dp.t_Name, pt.t_ShortName                    " +
           " FROM ddp_dep_dbt dp, dacsgroup_dbt gr, dparty_dbt pt" +
           " WHERE dp.t_Code = gr.t_Department                   " +
           "   AND pt.t_PartyID = dp.t_PartyID                   " +
           "   AND gr.t_GroupID = " + GroupID ;

  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    Dprt = rs.value( 0 ) + " " + rs.value( 1 );
  end;
  [  Подразделение: ##########################################################] ( Dprt );
end;

private macro PrintUser( userNum )
  var strSql : string;
  var rs : RsdRecordset;
  var User : string;
  array _User;
  var j : integer;

  strSql = "SELECT t1.t_oper, t2.t_name_type, t1.t_name, dp.t_Name, pt.t_ShortName " +
           "FROM dperson_dbt t1, dtypeac_dbt t2, ddp_dep_dbt dp, dparty_dbt pt     " +
           "WHERE                                                                  " +
           "  t1.t_oper = " + userNum + " AND                                      " +
           "  t2.t_inumtype = 13 AND                                               " +
           "  t2.t_type_account = t1.t_ctypeperson AND                             " +
           "  dp.t_Code = t1.t_codedepart AND                                      " +
           "  pt.t_PartyID = dp.t_PartyID                                          ";
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    User = rs.value( 0 ) + ", " + rs.value( 1 ) + ", " + rs.value( 2 ) + ", " + rs.value( 3 ) + " " + rs.value( 4 );
    StrSplit( User, _User, 58, 58, 2 );
    j = 0;
    while( StrLen(_User( j )) > 0 )
      if( j == 0 )
        [  Сформирован: ##########################################################] ( _User( j ));
      else
        [               ##########################################################] ( _User( j ));
      end;
      j = j + 1;
    end;
  end;
end;

private macro PrintDate()
  [  Дата/время: ########## ########] ( date, time );
end;

private macro PrintTop( GroupID )
  PrintGroupH( GroupID );
  PrintDprt( GroupID );
  PrintUser( {oper} );
  PrintDate(); 
end;

private macro PrintGroup( GroupID )
  var strSql : string;
  var rs : RsdRecordset;
  var Group : string;
  array _Group;
  var j : integer;
  var i : integer;
  var ParentList = TArray;
  var Num = 0;
  var IsParent;


  strSql = " SELECT t_ParentID     " +
           " FROM dacsgrouplink_dbt" +
           " WHERE t_GroupID = " + GroupID +
           " ORDER BY t_ParentID";

  rs = RsdRecordset( strSql );

  while( rs.MoveNext())
    ParentList[Num] = rs.value( 0 );

    Num = Num + 1;
  end;

  strSql = " SELECT t_GroupID, t_Name                                     " +
           " FROM dacsgroup_dbt                                           " +
           " WHERE t_GroupID IN (SELECT distinct t_ParentID               " + 
           "                     FROM dacsgrouplink_dbt                   " + 
           "                     START WITH t_GroupID = " + GroupID         + 
           "                     CONNECT BY PRIOR t_ParentID = t_GroupID) "  ;


  rs = RsdRecordset( strSql );

  j = 0;
  Group = "";
  while( rs.MoveNext())
    if( j > 0 )
      Group = Group + ", ";
    end;
 
    i = 0;
    IsParent = false;
    while( i < Num)
      if( ParentList[i] == rs.value( 0 ) )
        IsParent = true;
      end;
      i = i + 1;
    end;

    if(IsParent == true)
      Group = Group + rs.value( 0 ) +" "+ rs.value( 1 );
    else
      Group = Group + "[" + rs.value( 0 ) + " " + rs.value( 1 ) + "]";
    end;
    j = j + 1;
  end;
  StrSplit( Group, _Group, 70, 70, 10 );
  j = 0;
  while( StrLen(_Group( j )) > 0 )
    if( j == 0 )
      [  Группы: ######################################################################] ( _Group( j ));
    else
      [          ######################################################################] ( _Group( j ));
    end;
    j = j + 1;
  end;
end;


private macro PrintRole( GroupID )
  var strSql : string;
  var rs : RsdRecordset;
  var Role : string;
  array _Role;
  var j : integer;

  strSql = "SELECT t1.t_name                                    " +
           "FROM dacsroletree_dbt t1, dacsgrouprole_dbt t2         " +
           "WHERE t1.t_roleid = t2.t_roleid AND t2.t_groupid = " + GroupID +
           "ORDER BY t1.t_name                                  ";

  rs = RsdRecordset( strSql );

  j = 0;
  Role = "";
  while( rs.MoveNext())
    if( j > 0 )
      Role = Role + ", ";
    end;
    Role = Role + rs.value( 0 );
    j = j + 1;
  end;
  StrSplit( Role, _Role, 72, 72, 10 );
  j = 0;
  while( StrLen(_Role( j )) > 0 )
    if( j == 0 )
      [
         Роли: ########################################################################] ( _Role( j ));
    else
      [        ########################################################################] ( _Role( j ));
    end;
    j = j + 1;
  end;
end;

private macro PrintRestrict( PrivID, PrivName, GroupID )
  var IsExist : bool;
  var RestValue : string;
  array _RestValue;
  var j : integer;
  var stat = false;
  RestValue = "";

  var strSql : string;
  var rs : RsdRecordset;

  strSql = " SELECT t_RestKind, t_RestValue                 " +
           " FROM dacsrestrict_dbt rt, dacsrestgroup_dbt rg " +
           " WHERE  rt.t_RestID = rg.t_RestID               " +
           "   AND t_PrivID = " + PrivID                      +
           "   AND t_ItemLevel = 2                          " +
           "   AND t_ItemID = "+ GroupID                      +
           " ORDER BY t_RestKind                            " ;
           " " ;

  rs = RsdRecordset( strSql );

  while( rs.MoveNext())
    if(RestValue != "")
      RestValue = RestValue + "; ";
    end;

    if(rs.value( 0 ) == "-") 
      RestValue = RestValue + "^";
    end;
   
    RestValue = RestValue + rs.value( 1 );
  end ;

  if(( not stat ) and strlen( RestValue ))
    [
    {####} ########################################################################] ( PrivID, PrivName );
    StrSplit( RestValue, _RestValue, 71, 71, 10 );
    j = 0;
    while( StrLen(_RestValue( j )) > 0 )
      [       #######################################################################] ( _RestValue( j ));
      j = j + 1;
    end;
  end;
end;

private macro PrintPrivilege( GroupID )
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t.t_nodeid, t.t_name FROM dacsprivtree_dbt t "+ 
           " WHERE t.t_nodeid > 0 " +
           "   AND EXISTS ( SELECT 1 FROM dacsRestGroup_dbt rg"
           "                        WHERE rg.t_PrivID = t.t_nodeid " +
           "                          AND rg.t_itemLevel = 2 " +
           "                          AND rg.t_itemID =  " + GroupID +
           "              )            " +
           " ORDER BY t.t_nodeid ";

  rs = RsdRecordset( strSql );

  [
  Привилегии:];
  while( rs.MoveNext())
    PrintRestrict( rs.value( 0 ), rs.value( 1 ), GroupID );
  end;
end;

private macro PrintContext( GroupID )
  PrintRole( GroupID );
  PrintGroup( GroupID );
  PrintPrivilege( GroupID );
end;

private macro PrintBottom( curOper )
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t2.t_post, t1.t_name                      " +
           "FROM dperson_dbt t1 FULL JOIN dofficer_dbt t2 ON " +
           "( t2.t_personid = t1.t_partyid ) WHERE t1.t_oper = " + curOper;
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    if( StrLen(rs.value( 0 )) > 0 )
      [
      #####################________________________#################################] ( rs.value( 0 ), rs.value( 1 ));
    else
      [
                           ________________________#################################] ( rs.value( 1 ));
    end;
  end;
end;

macro ПечатьОтчетаСУД( GroupID )
  PrintHeader();

  PrintTop( GroupID );

  PrintContext( GroupID );

  PrintBottom( {oper} );

end;

//ПечатьОтчетаСУД( 11 );