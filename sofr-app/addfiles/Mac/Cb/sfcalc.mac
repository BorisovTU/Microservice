/*
 * Макрос выполнения шага "Расчет" операции по обработке периодической комиссии
 */

Import FIInter, "sf_lib.mac", "cb_sql.mac";

record SfAccrueRec("sfaccrue.dbt");
record SfDefComRec("sfdefcom.dbt");
record SfContrRec("sfcontr.dbt");
record SfConComRec("sfconcom.dbt");


macro InsertSfRepAcc(SfDefCom, BeginDate, EndDate, TransactionDate, Amount, 
                     InvoiceID, Debit, Credit, Comment )

  var strBeginDate      = "to_date( '"+ BeginDate       +"', 'dd.mm.yyyy')";
  var strEndDate        = "to_date( '"+ EndDate         +"', 'dd.mm.yyyy')";
  var strTransactionDate= "to_date( '"+ TransactionDate +"', 'dd.mm.yyyy')";
  var strQuery = "";  
  var cmd;

  if(BeginDate == date(0,0,0))
    strBeginDate = "to_date( '01.01.0001', 'dd.mm.yyyy')";
  end;

  if(EndDate == date(0,0,0))
    strEndDate = "to_date( '01.01.0001', 'dd.mm.yyyy')";
  end;

  if(TransactionDate == date(0,0,0))
    strTransactionDate = "to_date( '01.01.0001', 'dd.mm.yyyy')";
  end;
  

  strQuery = "INSERT INTO DSFREPACC_TMP (T_SFDEFCOMID,T_BEGINDATE,T_ENDDATE,T_TRANSACTIONDATE,T_AMOUNT, " +
             "T_INVOICEID,T_DEBIT,T_CREDIT,T_COMMENT, T_CONTRID, T_FeeType, T_ComissNumber, T_Department) " +
             "VALUES(" + SfDefCom.ID        +","+   
                         strBeginDate      +","+   
                         strEndDate        +","+   
                         strTransactionDate+","+   
                         Amount            +","+   
                         InvoiceID         +","+                              
               "'"+     Debit             +"',"+   
               "'"+     Credit            +"',"+   
               "'"+     Comment           +"',"+
                        SfDefCom.ConID           +","+
                        SfDefCom.FeeType         +","+
                        SfDefCom.CommNumber      +","+
                        SfDefCom.Department      +
             ")";
             

   SQL_Execute(strQuery);
end;



macro UpdateSfRepAcc(SfDefComID, Comment)

  var strQuery = "";  

  strQuery = "UPDATE DSFREPACC_TMP SET T_COMMENT = '" + Comment  +"'" +
             " WHERE T_SFDEFCOMID  =" + SfDefComID             ;
             
             
   SQL_Execute(strQuery);
end;



macro ExecuteStep( outBuff, primDoc )

  var stat = 0;
  var AccDebit = "", AccCredit= "", Comment= "", strAmount = "";

  SetBuff( SfDefComRec, primDoc );

  if( SfAccrueRec.Amount > 0 )

    stat = SfCarryAccrue( SfDefComRec, SfAccrueRec, SfContrRec, SfConComRec, @AccDebit, @AccCredit );
    InsertSfRepAcc(SfDefComRec, SfAccrueRec.BeginDate, SfAccrueRec.EndDate, SfAccrueRec.TransactionDate, SfAccrueRec.Amount, 
                 0, AccDebit, AccCredit, Comment);

  end;

  if(SfAccrueRec.Amount <0 )
    Comment = "Для комиссии " + SfDefComRec.COMMNUMBER + " по договору" 
    + SfContrRec.Number+  " остаток на счете \"+Расчеты\" "+ SfAccrueRec.Amount + ПолучитьКодФинИн(SfDefComRec.FIID_CommSum); 

    InsertSfRepAcc(SfDefComRec, SfAccrueRec.BeginDate, SfAccrueRec.EndDate, SfAccrueRec.TransactionDate, SfAccrueRec.Amount, 
                 0, AccDebit, AccCredit, Comment);
  end;

  return 0;

end;

macro PostStepAction( message,     /* данный параметр может принимать следующие */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Number_Step, /* номер шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* идентификатор шага операции                     */

  setbuff (SfDefComRec, FirstDoc);

  /* Происходит откат */
  if ((message==OP_EXECUTE_STEP) AND errTrn) 
    UpdateSfRepAcc(SfDefComRec.ID, "Ошибка создания документа доначисления");
    return 0;    
  end;

  return 0;
end;
