/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.20
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpccarry.mac

  Библиотека       : 

  Описание         : получение счета, проводки

  Программист      : Kirakozov Michael (KMB)

  Создан           : 02.02.2009
-------------------------------------------------------------------------*/
import InsCarryDoc, BankInter, PaymInter, mccatacf, globals, mpclb;


/************************************************************************/
/* Проводка документа                           */
/************************************************************************/
MACRO Prc_DocCarry(  Chapter, 
                     DbFIID, 
                     CrFIID, 
                     DbAccount, 
                     CrAccount, 
                     DbAmount, 
                     CrAmount, 
                     ValDate, 
                     Shifr, 
                     Status, 
                     CarryResult, 
                     Ground, 
                     PaymentID, 
                     Method, 
                     accTrn )

   var   tr, 
         PaymentObj = null,
         dprt = 0,
         stat = 0;

   if (ValType(ValDate) == V_UNDEF)     
      ValDate = {curdate}; 
   end;
   if (ValType(CarryResult) == V_UNDEF) 
      CarryResult = 1;     
   end;
   if (ValType(Ground) == V_UNDEF)      
      Ground = "";         
   end;

   if ( DbAmount != null )
      if( Chapter != 5 )
         DbAmount = money(round(DbAmount, 2));
      end;
      if( DbAmount < $0 )  
         return 1;
      end;
   else
      return 1;
   end;

   if ( CrAmount != null )
      if( Chapter != 5 )
         CrAmount = money(round(CrAmount, 2));
      end;
      if( CrAmount < $0 )  
         return 1;
      end;
   else
      return 1;
   end;

   if (ValType(Method) == V_UNDEF) 
      Method = PMMET_ID; 
   end;

   if( (PaymentID != NULL) AND (PaymentID > 0) )
      PaymentObj = RSBPayment( PaymentID );
      tr = PaymentObj.MakeTransaction();
   else
      //проводку формируем без платежа
      tr = RsbAccTransaction();
   end;

   if(tr == NULL)
      return 1;
   end;

   GetAccountDepartment(DbAccount, Chapter, DbFIID, @Dprt);

   tr.Chapter         = Chapter;
   tr.Date_Carry      = ValDate;

   if( PaymentObj != null )
      tr.Number_Pack   = PaymentObj.NumberPack;
      tr.Numb_Document = PaymentObj.Number;
   end;

   tr.ResultCarry     = CarryResult;
   tr.Ground          = Ground;
   tr.Department      = Dprt;
   tr.FIIDPayer       = DbFIID;
   tr.FIIDReceiver    = CrFIID;
   tr.SumPayer        = MoneyL(DbAmount);
   tr.SumReceiver     = MoneyL(CrAmount);
   tr.AccountPayer    = DbAccount;
   tr.AccountReceiver = CrAccount;
   tr.shifr_oper      = Shifr;

   if (Status != NULL)
      tr.Status_After = Status;
   end;

   if(Chapter == 5)
      tr.NoEquivalentCarry = true;
   end;

   if( not tr.Carry() )
      return 1;
   end;      

   if( NOT stat )
      SetParm( 14, tr );
   end;

   return stat;
   
END;


/*Получить счет для документа*/
MACRO MPC_GetDocAccount(CatCode,     /*категория (код)*/
                       fd,          /*документ*/
                       Account, /*здесь будет номер счета*/
                       OpenMode,    /*(MC_OPENACC_ACT, MC_OPENACC_CREATE, MC_OPENACC_RECREATE,
                                       MC_OPENACC_CHECKPERIOD, MC_OPENACC_CHECKEXIST*/
                       ActionDate,  /*дата нахождения, открытия*/
                       Currency,    /*валюта счета*/
                       IsMass,      /*режим массовой работы*/
                       ORScheme,    /*схема переоценки*/
                       AccBuf,      /*буфер для найденного/открытого счета (Account)*/
                       FIRole,      /* роль финансового инструмента */
                       ActivateDate)/* дата начала действия счета */

  var ResultAccount = "";

  if (ValType(OpenMode) != V_INTEGER)
    OpenMode = MC_OPENACC_CREATE;
  end;
  if ((ValType(IsMass) != V_INTEGER) AND (ValType(IsMass) != V_BOOL))
    IsMass = 0;
  end;
  if (ValType(IsMass) == V_BOOL)
    if (IsMass)
      IsMass = 1;
    else
      IsMass = 0;
    end;
  end;
  ResultAccount = MC_FindAndOpenAccountEx(CatCode,
                                        fd,
                                        ActionDate,
                                        IsMass,
                                        OpenMode,
                                        AccBuf,
                                        Currency,
                                        ORScheme,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        FIRole,
                                        null, null, null, null,
                                        ActivateDate            /* дата начала действия счета */
                                       );      

  if (ValType(ResultAccount) != V_UNDEF)
    Account = ResultAccount;
  end;
  if (ResultAccount == "")
    return false;
  end;
  SetParm(2, ResultAccount);
  return true;
END;


