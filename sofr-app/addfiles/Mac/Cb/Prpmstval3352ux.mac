/*
 $Name:        Prpmstval3352ux.mac
 $Module:      ББ
 $Description: Печать мемордера 0402102 по 3352-У в XML
*/

var pr_pmvaltr:TRecHandler = TRecHandler( "pmvaltr.dbt" );
var pr_pmstval:TRecHandler = TRecHandler( "pmstval.dbt" );

// Программист: TDV 2014
IMPORT BnkTemplReport, prpm_common;

private class (BnkTemplReport)PrintOrderReport(PrintOrderData)

  var data = PrintOrderData;

  MACRO Create()

    SetSheetName( data.Number );

    array ArrayValuableNameValue;
    
    var valuables:TArray = data.order.ValuableList.AsTArray(),
        valuable:TRecHandler;

    var piListDeb :TArray = data.order.PIList(PRT_Debet).AsTArray(),
        piListCred:TArray = data.order.PIList(PRT_Credit).AsTArray();

    var isDebL = IfThenElse( piListDeb.size()  > 0, true, false ),
        isCredL= IfThenElse( piListCred.size() > 0, true, false );

    array ArrayBankName, ArrayOwnerName, ArrayContent, ArraySumPropis, ArrayRegDoc;
    StrSplit(data.BankName, ArrayBankName, 39, 39, 3);    
    StrSplit(data.OwnerName, ArrayOwnerName, 39, 39, 3);    
    StrSplit(data.Content, ArrayContent, 97, 77, 2);    
    IfThenElse( StrSplit(CurToStrAlt( IfThenElse( isDebL, pr_pmpaym.rec.PayAmount, pr_pmpaym.rec.Amount), null, null, 
             int( GetFICode( IfThenElse( isDebL, pr_pmpaym.rec.PayFIID, pr_pmpaym.rec.FIID ), FICK_ISONUMBER ) ) ), ArraySumPropis, 97, 82, 2),
                StrSplit(CurToStrAlt( pr_pmpaym.rec.Amount, null, null, 
             GetFICode( pr_pmpaym.rec.FIID, FICK_ISONUMBER ) ), ArraySumPropis, 97, 82, 2));  
    StrSplit(data.RegDoc, ArrayRegDoc, 97, 51, 3);    
      
    SetValues( "NumDoc"          , data.Number,
               "DocDate"         , data.DateStr,
               "Sh"              , data.Sh,
               "Content0"        , ArrayContent(0),
               "Content1"        , ArrayContent(1),
               "Propis0"         , ArraySumPropis(0),
               "Propis1"         , ArraySumPropis(1),
               "FIO"             , data.FIO,
               "RegDoc0"         , ArrayRegDoc(0),
               "RegDoc1"         , ArrayRegDoc(1)
             );

    var table:object = RegisterTable("ValuableList");
    var index = 0;

    if( isDebL == false )
      SetValues( "BankName0"  , ArrayBankName(0),
                 "BankName1"  , ArrayBankName(1),
                 "BankName2"  , ArrayBankName(2),
                 "DebAcc",   data.DebAcc,
                 "DAm",      data.DAm,
                 "DFI",      IfThenElse(pr_pmpaym.rec.Chapter == 5/*CHAPT5*/, "", getFIINameOrCYY(data.DFI, data.NameVal==1) ) 
               );
    else
      table = RegisterTable("DEBET");
      while( index < piListDeb.size() )
        table.SetValueCell( "BankName0" , IfThenElse(index == 0, ArrayBankName(0), IfThenElse(index == 1 , ArrayBankName(1), IfThenElse(index == 2 , ArrayBankName(2), "" ) )));
        table.SetValueCell( "NAccDeb"   , "счет N" );
        table.SetValueCell( "DebAcc"    , piListDeb.value(index).rec.Account);

        table.SetValueCell( "DAm"       , data.SplitAmount(piListDeb.value(index).rec.Amount, pr_pmpaym.rec.Chapter, pr_pmpaym.rec.FIID) );
        table.SetValueCell( "DFI"       , IfThenElse(pr_pmpaym.rec.Chapter == 5/*CHAPT5*/, "", getFIINameOrCYY(piListDeb.value(index).rec.FIID, data.NameVal==1) ) );  
        table.AddStr();
        index = index + 1;
      end;
      table.EndTable();
      if ( piListDeb.size() == 2 )   
        SetValues("BankName1"  , ArrayBankName(2) );
      end;
    end;

    index = 0;
    if( isCredL == false )
      SetValues( "OwnerName0"  , ArrayOwnerName(0),
                 "OwnerName1"  , ArrayOwnerName(1),
                 "OwnerName2"  , ArrayOwnerName(2),      
                 "CredAcc",    data.CredAcc,
                 "CredAmount", data.CredAmount,
                 "CFI",        getFIINameOrCYY(data.CFI, data.NameVal==1)
               );
    else
      table = RegisterTable("CREDIT");
      while( index < piListCred.size() )
        table.SetValueCell( "OwnerName0" , IfThenElse(index == 0, ArrayOwnerName(0), IfThenElse(index == 1 , ArrayOwnerName(1), IfThenElse(index == 2 , ArrayOwnerName(2), "" ) )));
        table.SetValueCell( "NAccCred"  , "счет N" );
        table.SetValueCell( "CredAcc"   , piListCred.value(index).rec.Account );                               
        table.SetValueCell( "CredAmount", IfThenElse(pr_pmpaym.rec.Chapter == 5, "", data.SplitAmount(piListCred.value(index).rec.Amount) ) );
        table.SetValueCell( "CFI"       , IfThenElse(pr_pmpaym.rec.Chapter == 5, "", getFIINameOrCYY(piListCred.value(index).rec.FIID, data.NameVal==1) ) );
        table.AddStr();
        index = index + 1;
      end;
      table.EndTable();
      if ( piListCred.size() == 2 )   
        SetValues("OwnerName1"  , ArrayOwnerName(2) );
      end;
    end;
    table = RegisterTable("ValuableList");
    
    if( (valuables.size == 0) and (pr_pmpaym.rec.Chapter == 5) )      

      if( pr_pmpaym.rec.InDoorStorage == 1 )

        var select =  " SELECT fininstr.t_Name AS ValName, " +
                      " dv_ficert.t_facevaluefi, " +
                      " COUNT (dv_ficert.t_facevaluefi) AS ValCount, " +
                      " SUM (dv_ficert.t_FaceValue) AS ValSum " +
                      " FROM dv_ficert_dbt dv_ficert, " +
                      " dinvcard_dbt invcard, " +
                      " dinvlist_dbt invlist, " +
                      " dfininstr_dbt fininstr " +
                      " WHERE dv_ficert.t_FICertID = invcard.t_FicertID " +
                      " AND invcard.t_invcardID = invlist.t_invcardID " +
                      " AND invlist.t_PaymentID = :paymentID " +
                      " AND fininstr.t_FIID = :FIID " +
                      " GROUP BY dv_ficert.t_facevaluefi, fininstr.t_Name;";

        var prm = makeArray(SQLParam("paymentID", pr_pmpaym.rec.PaymentID),
                            SQLParam("FIID", pr_pmpaym.rec.FIID));

        var rs = execSQLselect( select, prm, false );

        while( rs.MoveNext() )
       
          table.SetValueCell( "ValName0"  , rs.value("ValName"));
          table.SetValueCell( "ValName1"  , "");
          table.SetValueCell( "ValName2"  , "");
          table.SetValueCell( "ValCount"  , rs.value("ValCount"));
          table.SetValueCell( "ValSum"    , rs.value("ValSum"));
          table.AddStr();

        end;

        table.EndTable();

      end;

    elif( pr_pmpaym.rec.Chapter == 3 )

      for( valuable, valuables )
        StrSplit(valuable.rec.NameValue, ArrayValuableNameValue, 45, 45, 3);    
        table.SetValueCell( "ValName0"  , ArrayValuableNameValue(0));
        table.SetValueCell( "ValName1"  , ArrayValuableNameValue(1));
        table.SetValueCell( "ValName2"  , ArrayValuableNameValue(2));
        table.SetValueCell( "ValCount" , valuable.rec.CountValue);
        table.SetValueCell( "ValSum", data.SplitAmount(valuable.rec.AmountValue));
        table.AddStr();
      end;

      table.EndTable();      

    end;
    table.EndTable();

    return TRUE;
  END;

  initBnkTemplReport("Ордер по передаче ценностей.xls");
end; 

macro PrintDocument( ncopy:integer ):bool
  var stat : bool = TRUE,
      data : TInOutOrderPrintData = TInOutOrderPrintData();

  if( pr_pmpaym.rec.DocKind == 288 )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pmstval, pr_pmvaltr );
    stat = PrintOrderReport( data ).print();
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    stat = FALSE;
  end;

  return stat;
end;
