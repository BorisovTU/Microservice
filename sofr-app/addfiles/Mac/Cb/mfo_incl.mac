/* ************************************************************************

  RS-Bank 5.0

  Библиотека общих функций, используемых при работе
  со справочника банков файла bnkseek.dbf


************************************************************************ */
Import PTInter, CTInter;

/* название файла справочника банков ЦБ */
CONST NAME_REFERENCE_FILE = "bnkseek.dbf";

CONST SET_CHAR   = "X";
CONST UNSET_CHAR = "";

file DprtKind  ( dprtkind ) key 0;
file DprtPlac  ( dprtplac ) key 0;
file PartyReg  ( partyreg ) key 0 write;

var LicenseDocID;

/*
        Возвращает подстроку входной строки без ведущих пробелов
*/
MACRO Убрать_пробелы_слева( Строка )
  while( index( Строка, " " ) == 1 )
    Строка = substr( Строка, 2 );
  end;
  return Строка;
END;



/*
        Возвращает подстроку входной строки без пробелов в конце
*/
MACRO Убрать_пробелы_справа( Строка )
  var Длина = strlen( Строка );
  while( index( substr( Строка, Длина ), " " ) == 1 )
    Длина = Длина - 1;
  end;
  return substr( Строка, 1, Длина );
END;

/*
        Преобразование счета при приеме файлов
*/
MACRO Преобразовать_к_счету( Строка )
  var s = Убрать_пробелы_справа( Строка );
  s = Убрать_пробелы_слева(s) ;
  return s ;
END;

MACRO Тип_Банка(NumberKind)

   rewind(DprtKind);

   while( next(DprtKind) )
      if(DprtKind.NumberKind == NumberKind) return DprtKind.Kind_Depart; end;
   end;

   return "";
end;

MACRO Тип_Населенного_Пункта(NumberPlace)
   rewind(DprtPlac);

   while( next(DprtPlac) )
      if(DprtPlac.NumberPlace == NumberPlace) return DprtPlac.szPlace; end;
   end;

   return "";
end;

MACRO СоздатьНазваниеБанка(Источник)
   return trim(Источник.NAMEP);
end;

MACRO Lock_Bank(REAL)
  return UNSET_CHAR;
END;

MACRO Lock_Bankdprt(REAL)
  if( (trim (REAL) == "БЛОК" ) )
    return SET_CHAR;
  end;
  return UNSET_CHAR;
END;



/* Получить вид платежа */
macro GetPaymentKind( region, UER )
    const RGN_MOSCOW        = "45",
          RGN_MOSCOWREGION  = "46",
          PAYMENT_KIND_TELG = "T",
          PAYMENT_KIND_ELEC = "Э";

    if ( UER == 0 )
        return PAYMENT_KIND_TELG;
    elif ( UER == 3 )
        return PAYMENT_KIND_ELEC;
    else
       region = trim( region );
       if ( (region==RGN_MOSCOW) OR (region==RGN_MOSCOWREGION) )
           if ( UER == 2 )
              return PAYMENT_KIND_ELEC;
           end;
       else
          if ( UER == 1 )
              return PAYMENT_KIND_ELEC;
           end;
       end;
    end;
    return PAYMENT_KIND_TELG;
end;

/* Получть тип банка по виду банка ЦБ */
macro GetBankTypeByKindDepart( KindDepart )
  if( (KindDepart == "ГРКЦ") or (KindDepart == "РКЦ") or (KindDepart == "ПРС") or (KindDepart == "МРХ") )
    return PT_KIND_PAYM_CASH_CENTRE;
  elif( (KindDepart == "ПУ") or (KindDepart == "ПУГ") )
    return PT_KIND_FIELDOFFICE_CENTRALBANK;
  elif( (KindDepart == "СБ") or (KindDepart == "ФСБ") or (KindDepart == "ТУСБ") or ((KindDepart == "УСБ")) )
    return PT_KIND_SAVINGS_BANK;
  end;
  return 0;
end;

/* Задать региональность */
macro SetRegionalAttr( Party, BankDprt, Uer )
  var stat = DisconnectObjAttr( OBJTYPE_PARTY, UniID(Party, OBJTYPE_PARTY), PT_ATTR_GROUP_REGIONKIND );
  if( (BankDprt.UERType == PT_KIND_PAYM_CASH_CENTRE) or (BankDprt.UERType == PT_KIND_FIELDOFFICE_CENTRALBANK) )
    if( BankDprt.Uer == 1 )
      ConnectObjAttr( null, OBJTYPE_PARTY, UniID(Party, OBJTYPE_PARTY), PT_ATTR_GROUP_REGIONKIND, null, "", "2" );
    elif( BankDprt.Uer == 2 )
      ConnectObjAttr( null, OBJTYPE_PARTY, UniID(Party, OBJTYPE_PARTY), PT_ATTR_GROUP_REGIONKIND, null, "", "1" );
    elif( BankDprt.Uer == 3 )
      ConnectObjAttr( null, OBJTYPE_PARTY, UniID(Party, OBJTYPE_PARTY), PT_ATTR_GROUP_REGIONKIND, null, "", "1" );
      ConnectObjAttr( null, OBJTYPE_PARTY, UniID(Party, OBJTYPE_PARTY), PT_ATTR_GROUP_REGIONKIND, null, "", "2" );
    end
  end;
  return true;
end;

/* Задать правопреемника  */
macro SetSuccessor( Party, VKEYDEL )
  file Successor(party) key 0;
  file SuccessorBankDprt( bankdprt ) key 4;

  if( trim(VKEYDEL) == "" )
    UnlinkObject( OBJROLE_PARTY_ASSIGNEE, OBJTYPE_PARTY, Party );
  else
    SuccessorBankDprt.Vkey = trim(VKEYDEL);
    if( GetEQ(SuccessorBankDprt) )
      Successor.PartyID = SuccessorBankDprt.PartyID;
      if( GetEQ(Successor) )
        SetLinkedObject( OBJROLE_PARTY_ASSIGNEE, OBJTYPE_PARTY, Party, OBJTYPE_PARTY, Successor, true );
      end;
    end;
  end;
  
  return true;
end;

/* Определить ИД вида документа регистрации */
macro GetLicenseDocID()
  file dk_reg("dk_reg.dbt") key 1;

  dk_reg.ListKind_Ref = PTLIST_CENTRALBANK;
  dk_reg.DocKindName  = "Лицензия ЦБ РФ";
  if( GetEQ(dk_reg) )
    LicenseDocID = dk_reg.DocKindID;
  else
    LicenseDocID = 0;
    println( "Не найден документ регистрации \"Лицензия ЦБ РФ\" для регистрирующего органа \"ЦБ РФ\"" );
    println();
  end;
end;

/* Найти регистрацию по данному виду документа */
macro FindRegistry( PartyID, PartyKind, DocKindID, RegNumber )
  var stat;

  ClearRecord( PartyReg );
  PartyReg.PartyID   = PartyID;
  PartyReg.OpenClose = UNSET_CHAR;
  PartyReg.PartyKind = PartyKind;

  stat = getGE( PartyReg );
  while( (stat) and ((PartyReg.PartyID   == PartyID   )
                and  (PartyReg.OpenClose == UNSET_CHAR)
                and  (PartyReg.PartyKind == PartyKind )) )
     if((PartyReg.DocKindID == DocKindID) and ((valtype(RegNumber) != V_STRING) or (RegNumber != Trim(PartyReg.RegNumber))))
       return true;
     end;

     stat = next( PartyReg );
  end;
  return false;
end;

/* Импорт номера регистрации банка */
macro ImportRegNumber( ErrorText, PartyID, REGN, REAL, DATE_CH )
  
  var RegNumber;
  var PartyKind = PTLIST_CENTRALBANK;
  var IsNewRegNumber = true;
  
  if( LicenseDocID != 0 )
    
    /* сначал перебираем все данные о регистрации */
    RegNumber = null;
    while( FindRegistry( PartyID, PartyKind, LicenseDocID, RegNumber ) )
    
      if( (IsNewRegNumber) and (Trim(PartyReg.RegNumber) == Trim(REGN)) )
        IsNewRegNumber = false;
        RegNumber = Trim(REGN);
      else
        /* если старые номер регистрация не совпадает с новым, закрываем старую регистрацию */
        PartyReg.OpenClose = SET_CHAR;
        if ( not Update ( PartyReg ) )
          SetParm( 0, "Ошибка при закрытии регистрации" );
          return false;
        end;
      end;
  
    end;

    /* если регистрации с таким номером не было, то вставляем новую регистрацию */
    if( (IsNewRegNumber) and (Trim(REGN) != "") )
      ClearRecord( PartyReg );
      PartyReg.PartyID    = PartyID;
      PartyReg.PartyKind  = PTLIST_CENTRALBANK;
      PartyReg.OpenClose  = UNSET_CHAR;
      PartyReg.DocKindID  = LicenseDocID;
      PartyReg.PtRegParty = PTID_UNKNOWNPARTY;
      PartyReg.RegNumber  = Trim(REGN);
      if( Trim(REAL) == "ОТЗВ" )
        Partyreg.RegDateEnd = DATE_CH;
      end;
    
      if( not Insert( PartyReg ) )
        SetParm( 0, "Ошибка при вставке данных о регистрации" );
        return false;
      end;
    end;

  end;

  return true;

end;

MACRO compare_tail (str1, str2)
  var tail, ustr2;
  if (strlen (str1) < strlen (str2))
    return false;
  end;
  tail = strupr (substr (str1, strlen (str1) - strlen (str2) + 1));
  ustr2 = strupr (str2);
  return tail == ustr2;
END;
