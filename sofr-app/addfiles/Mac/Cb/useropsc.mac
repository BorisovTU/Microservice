/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                                                                      */
/*  Имя файла   : useropsc.mac                                          */
/*                                                                      */
/*  Описание    : Отчет о выполнении массовых действий                  */
/*                в списке пользовательских операций                    */
/*                                                                      */
/*  Создан      : 22.09.2003                                            */
/*                                                                      */
/************************************************************************/

import likePY;

PRIVATE CONST EVENT_ERROR:integer = 0,
              EVENT_FIRST:integer = 1,
              EVENT_LAST :integer = 2;

PRIVATE CONST KEY_F8:integer = 322;

//запись в таблице экземпляра операции
private var   hndl_oproper:TRecHandler = TRecHandler("oproper.dbt", "bank.def");
private record rec_oproper("oproper.dbt", "bank.def");

//таблицы пользовательских первичек
private var uo_obcur:TBFile = TBFile("uo_obcur.dbt", "R", 0, "uo_obcur.dbt", "userop.def");
private var uo_mem_o:TBFile = TBFile("uo_mem_o.dbt", "R", 0, "uo_mem_o.dbt", "userop.def");
private var dr_docs :TBFile = TBFile("dr_docs.dbt" , "R", 0, "dr_docs.dbt",  "userop.def");

//------------------------------------------------------------------------------
//базовый класс отчета сканирования
//------------------------------------------------------------------------------
PRIVATE CLASS TBaseScanReport(oproper:TRecHandler)
  
  private var m_oproper:TRecHandler = oproper;

  //получение номера документа
  MACRO GetDocNumber():string
    return string(int(m_oproper.rec.DocumentID));
  END;
  
  //заголовок отчета сканирования
  MACRO PrintHeader(key:integer)
    var header:string = "Отчет о сканировании пользовательских документов";

    if(key == KEY_F8)
      header = "Отчет о групповом удалении пользовательских документов";
    end;
    
    [###############################################################################
     +---------+------+------------------------------------------------------------+
     |#########|Номер |                 Сообщение                                  |
     |документа|ошибки|                                                            |
     +---------+------+------------------------------------------------------------+]
    ( header:c:w, IfThenElse(key!=KEY_F8, "Номер", "ID"):c );
  END;

  //подвал отчета сканирования
  MACRO PrintFooter(key:integer)
    [+---------+------+------------------------------------------------------------+];
  END;

  //строка отчета сканирования
  MACRO PrintLine(key:integer, stat:integer, message:string)
    var number:string;
    if(key != KEY_F8)
      number = GetDocNumber();
    else
      number = string(int(m_oproper.rec.DocumentID));
    end;
    if(not stat)
      if(key == KEY_F8)
        message = "Документ успешно удален";
      end;
    end;
    [|#########|######|############################################################|]
    (number:r, stat:r, message:l:w);
  END;
  
END;

//------------------------------------------------------------------------------
//класс отчета сканирования пользовательских внебалансовых документов
//------------------------------------------------------------------------------
PRIVATE CLASS (TBaseScanReport) TOBScanReport(oproper:TRecHandler)
  InitTBaseScanReport(oproper);
  //получение номера документа
  MACRO GetDocNumber():string
    uo_obcur.rec.AutoKey = int(m_oproper.rec.DocumentID);
    if(uo_obcur.GetEQ())
      return uo_obcur.rec.Numb_Document;
    else
      return "???";
    end;
  END;
END;

//------------------------------------------------------------------------------
//класс отчета сканирования пользовательских мемордеров
//------------------------------------------------------------------------------
PRIVATE CLASS (TBaseScanReport) TMOScanReport(oproper:TRecHandler)
  InitTBaseScanReport(oproper);
  //получение номера документа
  MACRO GetDocNumber():string
    uo_mem_o.rec.AutoKey = int(m_oproper.rec.DocumentID);
    if(uo_mem_o.GetEQ())
      return uo_mem_o.rec.Numb_Document;
    else
      return "???";
    end;
  END;
END;

//------------------------------------------------------------------------------
//класс отчета сканирования списаний доходов/расходов
//------------------------------------------------------------------------------
PRIVATE CLASS (TBaseScanReport) TIncomeOutgoWriteOffScanReport(oproper:TRecHandler)
  InitTBaseScanReport(oproper);
  //получение номера документа
  MACRO GetDocNumber():string
    string(int(m_oproper.rec.DocumentID));
  END;
END;

//------------------------------------------------------------------------------
//макрос, вызываемый приложением
//------------------------------------------------------------------------------
MACRO Печать_ОтчетСканирования(event:integer, buff, stat:integer, message:string, key:integer)

  var rep:TBaseScanReport;

  //такое извращение вместо SetBuff(hndl_oproper, buff),
  //потому что SetBuff неправильно работает с TRecHandler
  SetBuff(rec_oproper, buff);
  copy(hndl_oproper, rec_oproper);
  
  //пользовательский внебалансовый документ
  if  (hndl_oproper.rec.DocKind == 5000)
    rep = TOBScanReport(hndl_oproper);

  //пользовательский мемордер
  elif(hndl_oproper.rec.DocKind == 5001)
    rep = TMOScanReport(hndl_oproper);
    
  //списание доходов/расходов
  elif(hndl_oproper.rec.DocKind == 5010)
    rep = TIncomeOutgoWriteOffScanReport(hndl_oproper);
    
  //таинственный незнакомец
  else
    rep = TBaseScanReport(hndl_oproper);

  end;

  
  if  (event == EVENT_FIRST)
    rep.PrintHeader(key);
    
  elif(event == EVENT_ERROR)
    rep.PrintLine(key, stat, message);
  
  elif(event == EVENT_LAST)
    rep.PrintFooter(key);
    
  end;
  
END;
