/*
$Name:        party.mac
$Module:      Главная книга
$Description: Для справочника субъектов
*/

/*---------------------------------------------------------------------------\
|                       Файл    party.mac                                   |
|                     Для справочника субъектов                             |
\---------------------------------------------------------------------------*/

import Global, BankInter, PTInter, "pt_lib.mac";
import CTInter;

record address( adress );
record persnidc( persnidc );

record СтарыйСубъект(party);
record Старый_Код(objcode);
record Новый_Код(objcode);

record Старая_Категория(objatcor);//Заполняется только при работе с категориями
record Новая_Категория(objatcor);//Заполняется только при работе с категориями

record Категория_CDI(objatcor);
record Категория_OKOPF(objatcor);

private file attr("objattr.dbt") key 0;
private const C_CATEGORY_CDI_BLOCKED = 175;
private const C_CATEGORY_CDI_BLOCKED_YES = 1;

private macro PartyInspection(Субъект:RsbParty, Вид_Панели:integer)
/*
0 - панель субъекта
1 - панель адреса
2 - панель значения кода субъекта
3 - панель документа регистрации субъекта экономики.
4 - панель удостоверения личности физического лица.
5 - панель примечания субъекта экономики.
6 - панель примечания физического лица.
7 - список признаков категории субъекта экономики.
8 - кроме этого данную функцию вызываем при единичном закрытии одного субъекта по [F8]
*/
  //msgbox("Проверка входа в PartyInspection!!! Субъект.LastName = ", Субъект.LastName, ", Вид_Панели = ", Вид_Панели);
  return 0;
end;

/*Пример функции для проверки необходимости синхронизации
 */
private macro CheckSync(PartyInit:RsbParty, Party:RsbParty)
  //msgbox("Проверка входа в CheckSync!!! PartyInit.LastName = ", PartyInit.LastName, ", Party.LastName = ", Party.LastName);
  return 1;
end;

/*******************************************/
PRIVATE MACRO Новый_Субъект(Субъект:RsbParty, Вид_Кода:integer)

        return 0;
END;

/* Показать панель субъекта? */
private macro Показать_Панель_Субъекта(Субъект:RsbParty, Вид_Кода:integer)
  return PT_EXIT_SUCCESS;
end;

/*
private macro DialogWhatDoYouWantToReturn()
  array Text;
  array Button;
  Text(0) = "Что вернуть из макрофункции?";
  Text(1) = " ";
  Text(2) = "0 - имитируется ситуация, когда ошибка обработана и пользователю не нужно выдавать системное сообщение о возникшей ошибке в триггере.";
  Text(3) = "1 - имитируется ситуация, когда пользователю необходимо выдать системное сообщение о возникшей ошибке.";
  Text(4) = " ";
  Button(0) = "   0   ";
  Button(1) = "   1   ";
  return confwin(Text, Button); 
end;
*/
/* 
Обработать исключение пользовательского триггера 
Возвращаемое значение:
  0 - если ошибка обработана и пользователю не нужно выдавать системное сообщение о возникшей ошибке.
  не 0 - (или макрофункция не реализована) если пользователю необходимо выдать системное сообщение о возникшей ошибке.
*/
/*
private macro Обработать_Исключение(ТекстИсключения : string)
  var retVal = 1;
  msgbox("В макрофункцию Обработать_Исключение пришел текст сообщения из триггера: |", ТекстИсключения);
  retVal = DialogWhatDoYouWantToReturn();
  if(retVal == 1)
    msgbox("Из макрофункции Обработать_Исключение возвращается значение ", retVal, ".|Далее будет показана системное сообщение о возникшей ошибке.");
  else
    msgbox("Из макрофункции Обработать_Исключение возвращается значение ", retVal, ".");
  end;
  return retVal;
end;
*/

/*******************************************/
/**
 @brief Проверить субъекта
 @param[in] Режим Режим проверки
 @param[in] Субъект Объект RsbParty проверяемого субъекта
 @param[in] Вид_Кода Вид кода/категории
 @param[in] RunFromScroll запускается из скроллинга
 @return 0 если операция с субъектом разрешена
 @return >0 если операция с субъектом запрещена

 Пользовательская проверка при операциях с субъектом
*/
PRIVATE MACRO Проверить_Субъекта(Режим:integer, Субъект:RsbParty, Вид_Кода:integer, RunFromScroll:bool)
VAR stat = false, error;
VAR PT_DELCAT = 11;

/* Режимы проверки:
 PT_DEL      - удаление субъекта;       ┐ При этих значениях "Вид_Кода"
 PT_INS      - ввод субъекта;           ├ инициализируется видом кода, показываемого
 PT_EDIT     - корректировка субъекта;          │ в панели субъекта
 PT_OPEN     - открытие субъекта (из закрытых); │
 PT_CLOSE    - закрытие субъекта;               ┘

 PT_DELCODE  - удаление кода субъекта;    ┐ При этих значениях "Вид_Кода"
 PT_INSCODE  - ввод кода субъекта;        ├ инициализируется видом меняемого(вводимого\удаляемого)
 PT_EDITCODE -корректировка кода субъекта.┘ кода субъекта
 
 PT_INSCAT   - ввод категории субъекта         ┐ При этих значениях "Вид_Кода"
 PT_EDITCAT  - корректировка категории субъекта┘ инициализируется видом группы категории субъекта
*/

  if (Режим == PT_EDIT)
    if (Субъект.LegalForm == 2)
       if (Субъект.LastName+" "+Субъект.FirstName+" "+Субъект.Patronymic != СтарыйСубъект.name)
          execmacrofile("dlcontrfunc", "ChangeNameAccount", Субъект.partyid);
       end;
    end;
  end;
  stat = true;
  
  if((Режим == PT_EDITCAT) and (Новая_Категория.OBJECT == "")) //Отдельного режима для удаления категории нет
    Режим = PT_DELCAT;
  end;
  
  if (Режим == PT_EDITCAT) //При вызове из категорий инициализируем субъекта
    var PartyBuf = TRecHandler("party.dbt");
    RestoreFromUniID(Новая_Категория.OBJECT, PartyBuf, OBJTYPE_PARTY);
    Субъект = RsbParty(PartyBuf.rec.PartyID);
  end;
  
  var EnableCDI = false;
  
  GetRegistryValue( "РСХБ\\ИНТЕГРАЦИЯ\\ВЗАИМОДЕЙСТВИЕ С AC CDI\\АКТИВНО", V_BOOL, EnableCDI, error );
 
  if (error !=0 ) EnableCDI = false; end;
  
  var IsBlockedInCDI = false;
  if (EnableCDI)
    if ( Субъект.IsOwned(PTK_BANK) or Субъект.IsOwned(PTK_MARKETPLASE) )
       IsBlockedInCDI = true;
    else 
       //IsBlockedInCDI = Субъект.Category.IsAttrPresense(C_CATEGORY_CDI_BLOCKED, C_CATEGORY_CDI_BLOCKED_YES, null, null, false, date());  
       var sql_category  = " select count(*) cnt from dobjatcor_dbt objcor "+
                           "  where objcor.t_objecttype = :objtype and objcor.t_object = lpad(:partyid,10,'0') and objcor.t_validtodate = to_date('31.12.9999','dd.mm.yyyy') "+
                           "    and objcor.t_groupid = :groupid and objcor.t_attrid = :attrid ";
       var cmd_category = RSDCommand(sql_category);
       cmd_category.Addparam("objtype", RSDBP_IN, OBJTYPE_PARTY);
       cmd_category.Addparam("partyid", RSDBP_IN, String(Субъект.partyid:o:10));
       cmd_category.Addparam("groupid", RSDBP_IN, C_CATEGORY_CDI_BLOCKED);
       cmd_category.Addparam("attrid", RSDBP_IN, C_CATEGORY_CDI_BLOCKED_YES);
       var rs_category = RSDRecordSet(cmd_category);
       if (rs_category.movenext())
          if (rs_category.value("cnt") == 1)
             IsBlockedInCDI = true;
          end;
       end;

    end;   
  end;

  if (EnableCDI and (Режим != PT_DELCAT))

    if (Режим == PT_INS) //Проверка отсутствия дубликатов CDI
      var PartyDubID = 0;
      if (Субъект.LegalForm == 1)//ЮЛ
        if (Субъект.IsNotResident()) //Дублем считается совпадение ИНН или КИО
          stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1203/*ИНН*/, PartyDubID );
          if (stat == false)
            stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1205/*КИО*/, PartyDubID );
          end;
        else //Дублем считается, если у клиентов совпал ИНН или ОГРН. 
          stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1203/*ИНН*/, PartyDubID );
          if (stat == false)
            stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1200/*ОГРН*/, PartyDubID );
          end;
        end;
      else
        if(Категория_CDI.AttrID == 3/*ИП*/)
          stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1207/*ИНН и ОГРН*/, PartyDubID );
        elif(Категория_CDI.AttrID == 4/*ФЛЧП*/)
          stat = ExecMacroFile("checkDubParty.mac", "checkDubPartyOnCDI", Субъект, 1206/*ИНН и ОКОПФ*/, PartyDubID, Категория_OKOPF.AttrID);
        end;
      end;
      if (PartyDubID != 0)
        msgbox("Создаваемая карточка субъекта является дублем субъекта с идентификатором " + PartyDubID + " ");
      end;
    end;
  
    if ( (Режим == PT_EDITCODE) and (Вид_Кода == PTCK_INN) and (not IsBlockedInCDI) )
      stat = false;
      msgbox("Редактирование кода ИНН запрещено");
      return 1;
    end;

    if( (Режим == PT_EDIT) and (
        (Субъект.FullName != СтарыйСубъект.Name) or 
        (Субъект.SHORTNAME != СтарыйСубъект.SHORTNAME) or 
        (Субъект.NRCOUNTRY != СтарыйСубъект.NRCOUNTRY) or
        (Субъект.OKPO != СтарыйСубъект.OKPO) or 
        (Субъект.LatName != СтарыйСубъект.LatName) )
         and (not IsBlockedInCDI)
      )
      stat = false;
      msgbox("Редактирование параметров субъекта запрещено");
      return 1;
    end;
    
    if ( (Режим == PT_EDITCODE) and ( 
         (Вид_Кода == PTCK_INN)  or
         (Вид_Кода == PTCK_OGRN)  or
         (Вид_Кода == PTCK_STATEREGNUM) )
         and (not IsBlockedInCDI)
       )
      stat = false;
      var NameCode = "";
      
      if (Вид_Кода == PTCK_INN) NameCode = "ИНН";
      elif (Вид_Кода == PTCK_OGRN) NameCode = "ОГРН";
      elif (Вид_Кода == PTCK_STATEREGNUM) NameCode = "Рег.номер в стране регистрации";
      end;
      
      msgbox("Редактирование кода " + NameCode + " запрещено");
      return 1;
    end;
    
    if ( (Режим == PT_EDITCAT) and ( 
         (Вид_Кода == 12 )  or //Справочник ОКАТО
         (Вид_Кода == 17 )  or //Код ОКВЭД
         (Вид_Кода == 27 )  or //Код ОКФС
         (Вид_Кода == 48 )  or //Код ОКОГУ ОК 006-2011
         (Вид_Кода == 53 )  or //Код ОКОПФ ОК 028-2012
         (Вид_Кода == 79 )  or //Код ОКВЭД2 для Ф302
         (Вид_Кода == 64 )  or //Код ОКВЭД2
         (Вид_Кода == 88 )  or //Код ОКВЭД нерезидента
         (Вид_Кода == 108 ) or //Признак FATCA
         (Вид_Кода == 109 ) or //Признак CRS
         (Вид_Кода == 63 )     //Хозяйственное общество, имеющее стратегическое значение
         )
         and (not IsBlockedInCDI)
       )
      stat = false;
      var NameCat = "";
      var cmd, DS;

      cmd = RSDCommand( "SELECT * FROM DOBJGROUP_DBT WHERE T_GROUPID = ? ");
      
      cmd.addParam( "", RSDBP_IN, Вид_Кода );
      cmd.execute();

      DS = TRsbDataSet(cmd);

      if( DS.MoveNext() )
         NameCat = DS.Name;
      end;
        
      if(Вид_Кода == 64) //на момент проверки у ОКВЭД2 и ОКВЭД2 для Ф302 одинаковые номера
        msgbox("Редактирование категории Код ОКВЭД2 / Код ОКВЭД2 для Ф302 запрещено");
      else
        msgbox("Редактирование категории " + NameCat + " запрещено");
      end;
        
      return 1;
    end;

    if ( (Режим == PT_EDIT) and (not IsBlockedInCDI) )
      //BIQ-10268 отправка обновления в CDI, если включена настройка
      InstLoadModule("func_lib.mac");
      var needcdi = ExecMacro2("NeedSendCDI",Субъект.partyid, date());
      if (needcdi)
         ExecMacro("PutInProcess",5055, Субъект.partyid, 1, "4,5,6", null, 11); // создать событие 5055 c типом 11
      end;

    end;

  end;
 

if(stat == true) return 0;
else             return 1;
end;

END;

//def-53147
/*******************************************/
/**
 @brief Проверка на наличие у клиента пароля для входа в торговые терминалы
 @param[in] Partid клиента
 @return true если если выгрузка данных по клиенту разрешена
 @return false если если выгрузка данных по клиенту запрещена

 Проверка возможности выгрузки клиента в quik и РСХБ-Брокер
*/
PRIVATE MACRO GetTermPWD(Partyid)

 var sql_str, cmd, rs;

 sql_str = "select 1 from utableprocessevent_dbt where t_objecttype = 5024 and t_objectid = :p_client_id and t_status = 4";
 cmd = RSDCommand(sql_str);
 cmd.AddParam("p_client_id", RSDBP_IN, Partyid);
 rs = RSDRecordSet(cmd);
 if (rs.movenext)
    return true;
 else
    return false;
 end;

end;

/**
 @brief После_Вставки_Субъекта
 @param[in] Субъект Объект RsbParty проверяемого субъекта

 Выполнение пользовательских действий после вставки субъекта
*/
PRIVATE MACRO После_Вставки_Субъекта(Субъект:RsbParty)
  //BIQ-10268 отправка субъекта в CDI, если включена настройка
  var cdi_isactive, needcdi=false, error;
  InstLoadModule("func_lib.mac");
  GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ВЗАИМОДЕЙСТВИЕ С AC CDI\\АКТИВНО", V_BOOL, cdi_isactive, error);
  if( error > 0 )
    cdi_isactive = false; // не нашли настройку, значит не включено
  end;
  if (cdi_isactive)
    needcdi = ExecMacro2("NeedSendCDI",Субъект.partyid, date());
    if (needcdi)
      ExecMacro("PutInProcess",5053, Субъект.partyid, 1, null, "-1"); // создать событие FindOrg 
    end;
    var stat = 0;
    var PartyCateg = RsbObjCategories( 3, String(Субъект.partyid:o:10) );
    if(Субъект.legalform == 1)
      var OGRN = ПолучитьКодСубъекта(Субъект.partyid, 27, null, 0);
      var INN = ПолучитьКодСубъекта(Субъект.partyid, PTCK_INN);
      if( (StrLen(INN) > 10) and (StrLen(OGRN) > 13) )
           PartyCateg.ConnectAttr( 150, 3, null, null,{curdate});
           PartyCateg.Save( String(Субъект.partyid:o:10) );
      else 
        if( (StrLen(INN) > 10) and (StrLen(OGRN) == 0) )
           PartyCateg.ConnectAttr( 150, 4, null, null,{curdate});
           PartyCateg.Save( String(Субъект.partyid:o:10) );
        else
          PartyCateg.ConnectAttr( 150, 2, null, null,{curdate});
          PartyCateg.Save( String(Субъект.partyid:o:10) );
        end;
      end;
    end;

    if(Субъект.legalform == 2)
      var sqlIE = "SELECT * " +
                  "  FROM (SELECT COUNT (1)     AS cat " +
                  "          FROM dobjatcor_dbt  c " +
                  "               INNER JOIN DOBJATTR_DBT a " +
                  "                   ON     a.t_groupid = c.t_groupid " +
                  "                      AND a.t_attrid = c.t_attrid " +
                  "                      AND a.t_objecttype = c.t_objecttype " +
                  "         WHERE     LPAD (" + Субъект.partyid + ", 10, '0') = c.t_object " +
                  "               AND a.t_objecttype = 3 " +
                  "               AND a.t_groupid = 53 " +
                  "               AND a.t_nameobject LIKE '5 01%'), " +
                  "       (SELECT COUNT (1)     AS employer " +
                  "          FROM dpersn_dbt pers " +
                  "         WHERE pers.t_personid = " + Субъект.partyid + " AND pers.t_isemployer = 'X')";

      var sqlPP = "SELECT COUNT (1) AS cat " +
                  "  FROM dobjatcor_dbt  c " +
                  "       INNER JOIN DOBJATTR_DBT a " +
                  "           ON     a.t_groupid = c.t_groupid " +
                  "              AND a.t_attrid = c.t_attrid " +
                  "              AND a.t_objecttype = c.t_objecttype " +
                  " WHERE     LPAD (" + Субъект.partyid + ", 10, '0') = c.t_object " +
                  "       AND a.t_objecttype = 3 " +
                  "       AND a.t_groupid = 53 " +
                  "       AND a.t_nameobject LIKE '5 02%' ";
      var setCat = false;
      var cmd = rsdCommand( sqlIE );
      var data = TRsbDataSet(cmd);
      if (data.moveNext())
        if ((data.cat > 0) or (data.employer > 0))
          PartyCateg.ConnectAttr( 150, 3, null, null,{curdate});
          PartyCateg.Save( String(Субъект.partyid:o:10) );
          setCat = true;
        end;    
      end;
      
      cmd = rsdCommand( sqlPP );
      data = TRsbDataSet(cmd);
      if (data.moveNext())
        if (data.cat > 0)
          PartyCateg.ConnectAttr( 150, 4, null, null,{curdate});
          PartyCateg.Save( String(Субъект.partyid:o:10) );
          setCat = true;
        end;    
      end;
      if (setCat == false)
        PartyCateg.ConnectAttr( 150, 1, null, null,{curdate});
        PartyCateg.Save( String(Субъект.partyid:o:10) );
      end;
    end;
  end;

END;

 private var NumColumns_doc = 0;
 private Macro AddColumn_doc( ar,ind, fld, head, wdth, edit )
  ar.value( ind * 6 + 0 ) = fld;  
  ar.value( ind * 6 + 1 ) = head;
  ar.value (ind * 6 + 2) = wdth;
  ar.value (ind * 6 + 3 ) = 0;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
  NumColumns_doc = NumColumns_doc + 1;
 end;

/*******************************************/

PRIVATE MACRO Функция_Пользователя(Субъект:RsbParty, Режим:integer)
   private var v_result = true;
   private var v_choice;
   private var v_acc_trn_menu = TArray;
   private var ObjID = String(Субъект.partyid:o:10);
   private var ObjType = 3;
   private var ObjCat;

   v_acc_trn_menu.value(0) = "Основания валютных платежей МБК";
   v_acc_trn_menu.value(1) = "Генерация кода субъекта в номере лицевого счета";
   v_acc_trn_menu.value(2) = "Кодовые слова для Брокерского обслуживания";
   v_acc_trn_menu.value(3) = "Ручная отправка временного пароля для входа в торговые терминалы";
   v_acc_trn_menu.value(4) = "Повторная выгрузка в РСХБ-Брокер";
   v_acc_trn_menu.value(5) = "Повторная выгрузка в Quik";
   v_acc_trn_menu.value(6) = "Кафка. Повторная выгрузка клиента в Свои инвестиции";
   v_acc_trn_menu.value(7) = "Поиск клиента по контактным данным";  
   v_choice = menu(v_acc_trn_menu, "Выберите действие", "Выберите действие");
   if(v_choice == 0)
         execmacrofile("dl_ground", "Kground",Субъект.partyid) ;
   elif (v_choice == 1 )
     var st = execmacrofile("Add_code_20.mac", "code_add",Субъект) ;
     Субъект.Code.SetCode(20,st);
     Субъект.update();
   elif(v_choice == 2)
      execmacrofile("int_code_word_brk_serv", "m_run_main_scroll", 2, Субъект.partyid);
   elif(v_choice == 3)
      if(gettrue(true, "Вы уверены, что хотите выполнить повторную отправку СМС\nс временным паролем для входа в торговые терминалы?"))
         v_result = execmacrofile("submit_trade_term_temp_pwd", "m_make_submit_trm_pwd", Субъект.partyid);
         if(not v_result)
            msgbox("Отправка временного пароля не выполнена.\nПроверьте позволяют ли условия выполнить повторную отправку или обратитесь в поддержку");
         end;
      end;
   elif (v_choice == 4)  
     // да, отдельный объект
     ObjCat = RsbObjCategories(ObjType, ObjID);
     ClearRecord(attr);
     var cc = ObjCat.GetMainAttr(170, date(), attr); 
     if ( (ValType(attr.attrid) != V_UNDEF) and (attr.attrid != 1) )
        // есть ли хоть один открытый
        var  sql_str = "SELECT count(*) "+
                       " FROM dsfcontr_dbt cntr, "+
                       "      ddlcontr_dbt dl "+
                       "WHERE dl.t_sfcontrid = cntr.t_id "+
                       "  AND cntr.t_partyid = :p_client_id "+
                       "  AND cntr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') ";
        var cmd = RSDCommand(sql_str);
        cmd.AddParam("p_client_id", RSDBP_IN, Субъект.partyid);
        var rs = RSDRecordSet(cmd);
        var type = 11; // спец.код для ручной выгрузки
        if (rs.movenext)
           if (rs.value(0) == 0)
              type = 3;
           end;
        end;
//def-53147
        if(GetTermPWD(Субъект.partyid))
           var  v_out_obj = ExecMacroFile("usp_Import.mac", "usr_sendrequest", Субъект.partyid, false, type);
           if (not v_out_obj)
              msgbox("Выгрузка завершилась с ошибкой");
           else
              msgbox("OK!");
           end;
        else
           msgbox("Клиент не прошел процедуру получения пароля к торговым терминалам");
        end;
     else
        msgbox("Не подлежит выгрузке");
     end;
   elif (v_choice == 5)  
     // да, отдельный объект
//def-53147
     if(GetTermPWD(Субъект.partyid))
        execMacrofile("global_utils_intgr.mac","m_make_event_to_process",5025, Субъект.partyid);
     msgbox("Задание на выгрузку создано");
   else
        msgbox("Клиент не прошел процедуру получения пароля к торговым терминалам");
     end;

   elif (v_choice == 6)  

      var sql; cmd = NULL;
      var ErrorCode, ErrorDesc;
      
      sql = "DECLARE "+
            "   v_ErrorCode number; "+
            "   v_ErrorDesc varchar2(2000); "+
            "   XML varchar(100) := '<XML action = \"UPD\" tab = \"DPARTY_DBT\" pkcol = \"T_PARTYID\" pk = \""+Субъект.partyid+"\"/>'; "+
            "BEGIN                       "+
            "   it_broker.Client_info(XML, v_ErrorCode, v_ErrorDesc); "+
            "   :p_ErrorCode := v_ErrorCode; "+
            "   :p_ErrorDesc := v_ErrorDesc; "+
            "END;  ";
       
      cmd = RSDCommand(sql);
      cmd.addParam("p_ErrorCode", RSDBP_OUT, V_INTEGER);
      cmd.addParam("p_ErrorDesc", RSDBP_OUT, V_STRING);
      cmd.execute();
   
      ErrorCode = cmd.value("p_ErrorCode");
      ErrorDesc = cmd.value("p_ErrorDesc");
   
      cmd.close(); cmd = NULL; sql = NULL;

      if(ErrorCode == 0)
         MsgBox("ИД клиента = "+Субъект.partyid+" Формирование и размещение JSON-сообщения в очередь QManager выполнено успешно");
      else
         MsgBox("ИД клиента = "+Субъект.partyid+" Ошибка при формирование JSON-сообщения");
      end;
   elif (v_choice == 7) 
      var email = "";
      var  Columns_doc = TArray();
	  NumColumns_doc = 0;
      

       sql = 
            "select  " +
            "(select t_code from dobjcode_dbt o where O.T_OBJECTID = p.t_partyid and o.t_objecttype = 3 and t_codekind = 101 and t_state = 0) code," +
            "p.t_shortname FIO,                                                                                                                           " +
            "(select t_name from dcontacttype_dbt where C.T_CONTACTTYPE  = t_type and t_kind = c.t_contactkind) ComType,                                     " +
            "(select t_name from dcontactkind_dbt where C.T_CONTACTkind  = t_kind ) Type,                                                                   " +
            "c.t_value value                                                                                                                                         " +
           " from dcontact_dbt c join dparty_dbt p on p.t_partyid = c.t_partyid where ((length(trim(:email)) <> 0) and t_value like '%'||:email||'%' ) ";	

            AddColumn_doc( Columns_doc, NumColumns_doc, "code",     "Код_клиента"  , 15, 2 );
            AddColumn_doc( Columns_doc, NumColumns_doc, "FIO",      "ФИО"          , 15, 2 );
            AddColumn_doc( Columns_doc, NumColumns_doc, "ComType",  "Тип_связи"    , 15, 2 );
            AddColumn_doc( Columns_doc, NumColumns_doc, "Type",     "Вид_связи"    , 15, 2 );
            AddColumn_doc( Columns_doc, NumColumns_doc, "value",    "Значение"   , 15, 2 );
 
      cmd = RsdCommand(sql);
      if ( GetString(email,"Введите контактные атрибуты") )
         cmd.addParam("email", RSDBP_IN, email);
         rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
          RunScroll (rs, NumColumns_doc, Columns_doc, "SearchByContact" ); 
      end;

   else
      msgbox("Выбор не подтвержден");
   end;
        return 0;

END;

/*******************************************/
macro ПроверитьАдресСубъекта(Адрес)

  var stat = 0;
  var ind = 0;
  var but;
  var regparm = false;
  var err = 0;
  Array Text;
  Array Button1;
  Array Button2;
  var okato = false;
  var kladr = false;
  var OkatoByKladr = "";
  var OktmoByKladr = "";

  GetRegistryValue("CB\\PARTY\\КЛАДР\\СВЕРКА ОКАТО", V_BOOL, okato, err);
  if ( err != 0 )
    okato = false;
  end; 
  GetRegistryValue("CB\\PARTY\\КЛАДР\\АДРЕС КЛАДР", V_BOOL, regparm, err );
  if ( err != 0 )
    regparm = false;
  end; 
  GetRegistryValue("CB\\PARTY\\КЛАДР\\КЛАДР", V_BOOL, kladr, err );
  if ( err != 0 )
    kladr = false;
  end; 

  Button1(0) = " Исправить ";
  Button1(1) = " Сохранить ";
  Button2(0) = " Исправить ";

  if(( Адрес.Country == "" ) or ( Адрес.Territory == "" ))
    stat = 1;
    Text(ind) = "Не указана страна или территория.";
    ind = ind + 1;
  end;
  if( Адрес.Country == "RUS" )
    if(( Адрес.Region == "" ) or ( Адрес.PostIndex == "" ))
      stat = 1;
      Text(ind) = "Не указан регион или индекс.";
      ind = ind + 1;
    end;
  end;

  if(( Адрес.Country == "RUS" ) AND ( regparm ) AND ( Адрес.Fias == "" )) 
    stat = 2;
    Text(ind) = "Не заполнен код ФИАС.";
    ind = ind + 1;
  end;
  
  if((Адрес.Country == "RUS") and (Адрес.Fias != "") and (regparm or okato))
    GetOkatoByKladr(Адрес.Fias, @OkatoByKladr, @OktmoByKladr);
    if(regparm and (OkatoByKladr == "noKladr"))
      stat = 1;
      Text(ind) = "Код ФИАС не найден в справочнике ФИАС.";
      ind = ind + 1;
    else
      if(kladr and okato and (Адрес.Okato != "") and (OkatoByKladr != "noKladr") and (OkatoByKladr != Адрес.Okato))
        stat = 1;
        Text(ind) = "Код ОКАТО не соответствует коду ФИАС.";
        ind = ind + 1;
      end;
      if(kladr and okato and (Адрес.Oktmo != "") and (OktmoByKladr != "noKladr") and (OktmoByKladr != Адрес.Oktmo))
        stat = 1;
        Text(ind) = "Код ОКТМО не соответствует коду ФИАС.";
        ind = ind + 1;
      end;
    end;
  end;

  if((index(Адрес.Region  , "*") != 0) or (index(Адрес.Region  , "?") != 0) or
     (index(Адрес.Province, "*") != 0) or (index(Адрес.Province, "?") != 0) or
     (index(Адрес.District, "*") != 0) or (index(Адрес.District, "?") != 0) or
     (index(Адрес.Place   , "*") != 0) or (index(Адрес.Place   , "?") != 0) or
     (index(Адрес.Plan    , "*") != 0) or (index(Адрес.Plan    , "?") != 0) or
     (index(Адрес.Street  , "*") != 0) or (index(Адрес.Street  , "?") != 0) )
    stat = 2;
    Text(ind) = "Недопустимый символ '*' или '?' в наименовании адресного объекта.";
    ind = ind + 1;
  end;

  if(stat != 0)
    if( stat == 1 )
      but = ConfWin(Text,Button1);
    else
      but = ConfWin(Text,Button2);
    end;
    if(but == 1)
      stat = 0;
    end;
  end;

  return stat;
end;

private macro ЗаполнитьРегион( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьРайон( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьГород( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьПункт( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьУлица( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьПланирСтрукт( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ПроверитьУдостоверениеСубъекта( Документ, Субъект:RsbParty )
  var stat = false;
  setbuff( persnidc, Документ );
  
  stat = true;

  if(stat == true) return 0;
  else             return 1;
  end;
end;