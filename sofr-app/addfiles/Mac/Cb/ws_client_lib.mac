/*
 $Name: ws_client_lib.mac
 $Module: Ядро ГКБО 
 $Description: Интерфейс "Выполнить идентификацию клиента", Интерфейс "Запрос о наличии счетов"
 */
 /*
    Интерфейс "Выполнить идентификацию клиента"
    Интерфейс "Запрос о наличии счетов"
 */
import BankInter, FIInter, "ws_lib_fun.mac", rsd, "likepy.mac", CurrInter;

const ReqTypeIns = "1";
const ReqTypePersn = "2";

class TClientFIO
    var Surname : string;     // Фамилия
    var FirstName : string;   // Имя
    var Patronymic : string;  // Отчество (при наличии)
end;

class TClientDoc             // Документ удостоверения личности (ДУЛ)
    var TypeDoc : string;    // Справочник кодов ДУЛ
    var SerDoc  : string;
    var NumDoc  : string;
end;

class TClientRequisites
    var Type : string;
    var FullName : string;   // Для ЦИК
    var FIO;                 // ФИО ФЛ, раздельно (при наличии)
    var INN : string;        // Не используется
    var KPP : string;        // Не используется
    var OGRN : string;       // Не используется
    var BirthDate : date;    // Не используется
    var Snils : string;      // СНИЛС. Не используется
    var IDoc;                // Документ удостоверения личности (ДУЛ)
end;

// Интерфейс "Выполнить идентификацию клиента" : Параметры ответа
class TClientIdentSuccess
    var id : string;
    var ReqID : string;
    var ClientFullName : string;
    var CountRezult    : integer;
    var ClientID       : string;
end;

class TContractor       // Исполнитель
    var FIO   : string; // ФИО
    var Phone : string; // Телефон
end;

class ClientIdentMassLegalPrm
    var IdenArray;
    var CodeArray;
    var NameArray;
    var ReqID;
end;

class ClientIdentMassPersonPrm
    var IdenArray;
    var NameArray;
    var Name1, Name2, Name3;
    var Born;
    var PaperKind;
    var PaperSeries;
    var PaperNumber;
    var ReqID;
end;

macro FillTWsFIO(ClientFIO : @variant, wsClientFIO, ParamN)
    WS_SetAttributeValue(@ClientFIO.Surname, ParamN, "ReqFindClient.FIO", "Surname", wsClientFIO, V_STRING, true, null);
    WS_SetAttributeValue(@ClientFIO.FirstName, ParamN, "ReqFindClient.FIO", "FirstName", wsClientFIO, V_STRING, true, null);
    WS_SetAttributeValue(@ClientFIO.Patronymic, ParamN, "ReqFindClient.FIO", "Patronymic", wsClientFIO, V_STRING, false, null);
end;

macro FillTWsIDOC(ClientIDOC : @variant, wsClientIDOC, ParamN)
    WS_SetAttributeValue(@ClientIDOC.TypeDoc, ParamN, "ReqFindClient.IDoc", "TypeDoc", wsClientIDOC, V_STRING, true, null);
    WS_SetAttributeValue(@ClientIDOC.SerDoc, ParamN, "ReqFindClient.IDoc", "SerDoc", wsClientIDOC, V_STRING, true, null);
    WS_SetAttributeValue(@ClientIDOC.NumDoc, ParamN, "ReqFindClient.IDoc", "NumDoc", wsClientIDOC, V_STRING, true, null);
end;

macro FillTReqFindClient(oReqFindClient : @variant, wsReqFindClient, ParamN)

    WS_SetAttributeValue(@oReqFindClient.Type, ParamN, "ReqFindClient", "Type", wsReqFindClient, V_STRING, true, null);
    WS_SetAttributeValue(@oReqFindClient.FullName, ParamN, "ReqFindClient", "FullName", wsReqFindClient, V_STRING, false, "");
    WS_SetAttributeValue(@oReqFindClient.INN, ParamN, "ReqFindClient", "INN", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.KPP, ParamN, "ReqFindClient", "KPP", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.OGRN, ParamN, "ReqFindClient", "OGRN", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.BirthDate, ParamN, "ReqFindClient", "BirthDate", wsReqFindClient, V_DATE, false, null);
    WS_SetAttributeValue(@oReqFindClient.Snils, ParamN, "ReqFindClient", "Snils", wsReqFindClient, V_STRING, false, null);
    
    if (oReqFindClient.FullName == null)
        WS_SetAttributeValue(@oReqFindClient.FIO, ParamN, "ReqFindClient", "FIO", wsReqFindClient, V_GENOBJ, true, null);
        if (oReqFindClient.FIO != null)
            oReqFindClient.FIO = TClientFIO;
            FillTWsFIO(@oReqFindClient.FIO, wsReqFindClient.FIO, ParamN);
        end;
    end;
    
    WS_SetAttributeValue(@oReqFindClient.IDoc, ParamN, "ReqFindClient", "IDoc", wsReqFindClient, V_GENOBJ, true, null);
    if (oReqFindClient.IDoc != null)
        oReqFindClient.IDoc = TClientDoc;
        FillTWsIDOC(@oReqFindClient.IDoc, wsReqFindClient.IDoc, ParamN);
    end;
end;

macro NormalizeString(Str)
    var result : string = Trim(RsRegexReplace(Str, "[[:space:]]+", " "));
    result = StrUpr(result);
    result = RsRegexReplace(result, "\\-", "");
    result = RsRegexReplace(result, "Ё", "Е");
    
    return result;
end;

macro NormalizeIDocSer(Str)
    var result : string = Str;
    result = StrSubst (result, "х", "X");
    result = StrSubst (result, "с", "С");
    result = StrSubst (result, "Х", "X");
    result = StrSubst (result, "С", "С");

    result = StrSubst (result, "x", "X");
    result = StrSubst (result, "i", "I");
    result = StrSubst (result, "v", "V");
    result = StrSubst (result, "l", "L");
    result = StrSubst (result, "c", "C");
    result = NormalizeString(str);
    result = RsRegexReplace(result, "(\\-|\\s+)", "");
    
    return result;
end;

macro splitINN_KPP(fullINN:string, INN:@string, KPP:@string)
    var parts:TArray;
    INN = "";
    KPP = "";
    parts = split(fullINN, "/");
        
    if(parts.Size > 0)
        INN = parts.Value(0);
        if(parts.Size > 1)
            KPP = join(subArray(parts, 1), "/");
        end;
    end;
    SetParm(1, INN);
    SetParm(2, KPP);
end;

private macro ClearIdentList()
    var cmd = RsdCommand("truncate table DCLIDENTLIST_TMP");
    cmd.execute();
end;

private macro ReadMassResultList(resultList : @TArray, ReqID)
    var cmd = RsdCommand("select * from dclidentlist_tmp order by t_id ");
    cmd.NullConversion = true;
    var rs = RsdRecordSet(cmd);
    var lastId = 0;
    var result;
    while (rs.moveNext())
        if (rs.value("t_Id") != lastId)
            result = TClientIdentSuccess;
            result.ID = rs.value("t_Id");
            result.ReqID = ReqID;
            result.CountRezult = 0;
            result.ClientID = 0;
            result.ClientFullName = "";

            if (lastId != 0)
                resultList[resultList.size] = result;
            end;
                lastId = rs.value("t_Id");
        end;
            result.ClientID = rs.value("t_ClientID");
            result.ClientFullName = rs.value("t_ClientFullName");
            result.CountRezult = result.CountRezult + 1;
    end;

    if (GenClassName(result) == StrUpr("TClientIdentSuccess"))
        if (result.ClientFullName != "")
            resultList[resultList.size] = result;
        end;
    end;
end;

var FillClientIdentMassPersonSqlStr = "";
private macro FillClientIdentMassPersonSql(str)
    FillClientIdentMassPersonSqlStr = FillClientIdentMassPersonSqlStr + str;
end;

macro FillClientIdentMassTablePerson(pClientIdentMassPrm : @ClientIdentMassPersonPrm, resultList : @TArray)
    ClearIdentList();
    FillClientIdentMassPersonSqlStr = "";
    SetOutHandler (@FillClientIdentMassPersonSql);
[insert into dclidentlist_tmp(t_Id,t_ClientFullName,t_ClientID)
with pParams as
(
    select ? IdenArray, ? NameArray, ? Name1, ? Name2, ? Name3, ? BirthDate, ? TypeDoc, ? SerDoc, ? NumDoc
    from dual
)
select pParams.IdenArray, pt.T_NAME, PT.T_PARTYID
from DPERSN_DBT ps, DPARTY_DBT pt, pParams
where PS.T_PERSONID = PT.T_PARTYID
AND 
(
    (Length(pParams.NameArray) is not null AND pParams.NameArray <> CHR(1) AND PT.T_NORMALIZEDNAME = pParams.NameArray)
    or
    ((Length(pParams.NameArray) is null OR pParams.NameArray = CHR(1)) AND (Length(pParams.Name1) is not null AND Length(pParams.Name2) is not null
        AND ps.T_NORMALIDENTNAME1 = pParams.Name1 AND ps.T_NORMALIDENTNAME2 = pParams.Name2 OR (Length(pParams.Name3) = 0 OR pParams.Name3 = CHR(1) OR ps.T_NORMALIDENTNAME3 = pParams.Name3)))
)
AND
(
    pParams.BirthDate is null or Trim(pParams.BirthDate) = TRIM(TO_DATE('01.01.0001', 'DD.MM.YYYY'))
    OR TRIM(PS.T_BORN) = TRIM(pParams.BirthDate)
)
AND
(
    (
        ((Length(pParams.SerDoc) is not null AND pParams.SerDoc <> CHR(1)) OR (Length(pParams.NumDoc) is not null AND pParams.NumDoc <> CHR(1))) AND pParams.TypeDoc >= 0
        AND EXISTS 
        (
            SELECT 1 FROM DPERSNIDC_DBT pd where PS.T_PERSONID = PD.T_PERSONID AND PD.T_PAPERKIND = pParams.TypeDoc
            AND 
            (
                (
                    Length(pParams.SerDoc) is not null AND pParams.SerDoc <> CHR(1)
                        AND T_NORMALIDENTSERIES = pParams.SerDoc
                )
                OR 
                (
                    Length(pParams.SerDoc) is null OR pParams.SerDoc = CHR(1)
                )
            )
            AND 
            (
                (
                    Length(pParams.NumDoc) is not null AND pParams.NumDoc <> CHR(1) AND PD.T_PAPERNUMBER = pParams.NumDoc
                )
                OR 
                (
                    Length(pParams.NumDoc) is null OR pParams.NumDoc = CHR(1)
                )
            )
        )
    )
    OR
    (
       NOT ((Length(pParams.SerDoc) is not null AND pParams.SerDoc <> CHR(1)) OR (Length(pParams.NumDoc) is not null AND pParams.NumDoc <> CHR(1))) OR pParams.TypeDoc < 0
    )
)
];
    SetOutHandler ();
    var ins = RsbSQLInsert(FillClientIdentMassPersonSqlStr, 9, pClientIdentMassPrm.IdenArray.size);
    ins.AddParam(V_STRING, pClientIdentMassPrm.IdenArray, 37);
    ins.AddParam(V_STRING, pClientIdentMassPrm.NameArray, 321);
    ins.AddParam(V_STRING, pClientIdentMassPrm.Name1, 25);
    ins.AddParam(V_STRING, pClientIdentMassPrm.Name2, 25);
    ins.AddParam(V_STRING, pClientIdentMassPrm.Name3, 25);
    ins.AddParam(V_DATE, pClientIdentMassPrm.Born);
    ins.AddParam(V_INTEGER, pClientIdentMassPrm.PaperKind);
    ins.AddParam(V_STRING, pClientIdentMassPrm.PaperSeries, 13);
    ins.AddParam(V_STRING, pClientIdentMassPrm.PaperNumber, 26);

    if (ins.Insert())
        ReadMassResultList(resultList, pClientIdentMassPrm.ReqID);
    end;
end;

macro FillClientIdentMassTableLegal(pClientIdentMassPrm : @ClientIdentMassLegalPrm, resultList : @TArray)
    ClearIdentList();
    FillClientIdentMassPersonSqlStr = "";
    SetOutHandler (@FillClientIdentMassPersonSql);
[INSERT INTO dclidentlist_tmp (t_Id, t_ClientFullName, t_ClientID)
WITH pParams AS 
(
    SELECT ? IdenArray, ? CodeArray, ? Names from dual
)
SELECT pParams.IdenArray, PT.T_NAME, PT.T_PARTYID
    FROM dparty_dbt pt, pParams,
        (SELECT * FROM DOBJCODE_DBT code WHERE CODE.T_CODEKIND = 16 AND CODE.T_OBJECTTYPE = 3) code
    WHERE CODE.T_OBJECTID(+) = PT.T_PARTYID
        AND 
        (   
            LENGTH (pParams.CodeArray) IS NULL
            OR pParams.CodeArray = CHR(1)
            OR CODE.T_CODE = CHR (1)
            OR CODE.T_CODE = pParams.CodeArray
            OR CODE.T_CODE IS NULL
       )
       AND (PT.T_NORMALIZEDNAME = Names)
];
    SetOutHandler ();
    var ins = RsbSQLInsert(FillClientIdentMassPersonSqlStr, 3, pClientIdentMassPrm.IdenArray.size);
    
    ins.AddParam(V_STRING, pClientIdentMassPrm.IdenArray, 37);
    ins.AddParam(V_STRING, pClientIdentMassPrm.CodeArray, 36);
    ins.AddParam(V_STRING, pClientIdentMassPrm.NameArray, 321);

    if (ins.Insert())
        ReadMassResultList(resultList, pClientIdentMassPrm.ReqID);
    end;
end;

class TClientIdentFinder
    private var Condition : string = "";
    private var RezultCount : integer = 0;
    private var Command = RsdCommand();
    private var pClientFullName = "";
    private var pClientID = 0;
    private var Params = TArray;
        
    const COND_AND = 0;
    const COND_OR = 1;
    
    macro ClearCondition()
        Condition = "";
    end;
    
    macro CountRezult()
        return RezultCount;
    end;
    
    macro ClientFullName()
        return pClientFullName;
    end;
    
    macro ClientID()
        return pClientID;
    end;
    
    macro AddCondition(logic,cnd)
        var SkipLogic = false;
        if (strlen(Condition) == 0)
            Condition = Condition + " where ";
            SkipLogic = true;
        end;
        
        if (not SkipLogic)
            if (logic == COND_AND)
                Condition = Condition + " and ";
            else
                Condition = Condition + " or ";
            end;
        end;
        
        Condition = Condition + cnd;
    end;
    
    macro AddParam(value)
        Params[Params.size] = value;
    end;
    
    macro Find(Type)
        RezultCount = 0;
        var i = 0;
        var sql;
        if ((ValType(Type) == V_UNDEF) or (Type == ReqTypePersn))
            sql = "select PT.T_PARTYID, pt.T_NAME from DPERSN_DBT ps, DPERSNIDC_DBT pd, DPARTY_DBT pt";
        else
            sql = "select PT.T_PARTYID, pt.T_NAME from DPARTY_DBT pt, (select * from DOBJCODE_DBT code where CODE.T_CODEKIND = 16 AND CODE.T_OBJECTTYPE = 3) code ";
        end;
        sql = sql + Condition;
        
        Command.CmdText = sql;
        
        while (i < Params.size)
            Command.addParam("", RSDBP_IN, Params[i]);
            i = i + 1;
        end;
        
        Command.execute();
        
        var rs = RsdRecordset(Command);
        while (rs.MoveNext())
            pClientFullName = rs.value(1);
            pClientID = rs.value(0);
            RezultCount = RezultCount + 1;
        end;
        
        if (RezultCount != 1)
            pClientID = 0;
        end;
    end;
end;

class TAccountsAvailabilityHelper
    private var Sql : string;
    private var DataReq : date, TypeCloseAcc;
    private var Command;
    private var Recordset = RsdRecordset(Command);
    private var ClientID = 0;
    private var pCountAcc = 0;

    macro SetDataReq(v)
        DataReq = v;
    end;
    
    macro CountAcc()
        return pCountAcc;
    end;
    
    macro SetTypeCloseAcc(v)
        TypeCloseAcc = v;
    end;
    
    macro SetClientID(Client)
        ClientID = Client;
    end;
    
    macro GetContructor()
        var contractor = null;
        var str = "SELECT PERS.T_NAME, OFFICER.T_PHONENUMBER "
            "  FROM DACCOUNT_DBT ACC, "
            "       DDP_DEP_DBT DEP, "
            "       DOFFICER_DBT OFFICER, "
            "       DPERSON_DBT PERS "
            " WHERE     ACC.T_DEPARTMENT = DEP.T_CODE "
            "       AND ACC.T_OPER = PERS.T_OPER "
            "       AND PERS.T_PARTYID = OFFICER.T_PERSONID "
            "       AND OFFICER.T_PARTYID = DEP.T_PARTYID "
            "       AND ACC.T_CLIENT = ?";
            
        var cmd = RsdCommand(str);
        cmd.addParam("", RSDBP_IN, ClientID);
        cmd.NullConversion = true;
        var rs = RsdRecordset(cmd);
        
        if (rs.moveNext())
            contractor = TContractor;
            contractor.FIO = rs.value(0);
            contractor.Phone = rs.value(1);
        end;
        
        return contractor;
    end;
    
    macro Next()
        var result = Recordset.moveNext();
        
        if (result)
            pCountAcc = pCountAcc + 1;
        end;
        
        return result;
    end;
    
    macro Value(id)
        return Recordset.value(id);
    end;

    private macro GetDepCode(code)
        var str = "SELECT CODE.T_CODE FROM DDP_DEP_DBT DEP, DOBJCODE_DBT CODE "
            "WHERE DEP.T_CODE = ? "
            "AND DEP.T_PARTYID = CODE.T_OBJECTID "
            "AND CODE.T_OBJECTTYPE = 3 "
            "AND CODE.T_CODEKIND = ? ";
            
        var cmd = RsdCommand(str);
        cmd.addParam("", RSDBP_IN, int(Recordset.value("T_DEPARTMENT")));
        cmd.addParam("", RSDBP_IN, code);
        cmd.NullConversion = true;
        
        var out = "";
        var rs = RsdRecordset(cmd);
        if (rs.moveNext())
            out = rs.value(0);
        end;
        
        return out;
    end;
    
    macro BicBank()
        return GetDepCode(PTCK_BIC);
    end;
    
    macro BankOGRN()
        return GetDepCode(27);
    end;
    
    macro BankINN_KPP(inn:@string, kpp:@string)
        var fullINN = GetDepCode(PTCK_INN);
        splitINN_KPP(fullINN, inn, kpp);
    end;
    
    macro BankRegionCode()
        var RegionCode;

        RegionCode = Recordset.value("POST_REGIONNUM");

        if( RegionCode == nullval )
          RegionCode = Recordset.value("JUR_REGIONNUM");
        end;

        if( RegionCode == nullval )
          RegionCode = "";
        end;
        
        return RegionCode;
    end;
    
    macro BankAddress()
        var Address;

        Address = Recordset.value("POST_ADRESS");

        if( Address == nullval )
          Address = Recordset.value("JUR_ADRESS");
        end;

        if( Address == nullval )
          Address = "";
        end;
        
        return Address;
    end;
    
    macro DeptCode()
        return GetDepCode(PTCK_BANKREGNUM);
    end;
    
    macro AccountType()
        var type = "";
        var balance = Recordset.value("T_BALANCE");
        var type_account = Recordset.value("T_TYPE_ACCOUNT");

        if ((balance == "40817") or (balance == "40820"))
            type = "W";
        elif ((balance == "20309") or (balance == "20310"))
            type = "О";
        elif (not CompareStrWithMasks("421-426", balance))
            type = "Д";
        elif (balance == "40823")
            type = "М";
        elif (balance == "40824")
            type = "Э";
        elif ((not CompareStrWithMasks("45101-45109", balance)) or
              (not CompareStrWithMasks("45201-45209", balance)) or
              (not CompareStrWithMasks("45303-45309", balance)) or
              (not CompareStrWithMasks("45404-45410", balance)) or
              (not CompareStrWithMasks("45501-45510", balance)) or
              (not CompareStrWithMasks("45601-45608", balance)) or
              (not CompareStrWithMasks("45701-45709", balance)))
              type = "С";
        elif (Index(type_account, "Ч") != 0)
            type = "Ч";
        elif (Index(type_account, "Я") != 0)
            type = "Я";
        end;
        
        return type;
    end;
    
    macro DateMove()
        var LastCarryDate;
        if (GetLastCarryDate(
            Int(Recordset.value("T_CHAPTER")),
            Int(Recordset.value("T_CODE_CURRENCY")),
            string(Recordset.value("T_ACCOUNT")),
            LastCarryDate, DataReq) == false)
            LastCarryDate = date;
        end;
        
        if (LastCarryDate == date(0,0,0))
            LastCarryDate = Recordset.value("T_OPEN_DATE");
        end;
        
        return LastCarryDate;
    end;
    
    macro GetRest( Account, Chapter, Code_Currency )
      if(Code_Currency)
        return RestAC(Account, Code_Currency, NULL, NULL, Chapter);
      else
        return RestA(Account, NULL, NULL, Chapter);
      end;
    end;

    macro CurrencyCode()
        return Recordset.value("CUR_SHORT_NAME");
    end;
    
    macro SummaVal()
        var value;
        
        return GetRest(
            string(Recordset.value("T_ACCOUNT")),
            Int(Recordset.value("T_CHAPTER")),
            Int(Recordset.value("T_CODE_CURRENCY")));
    end;
    
    macro SummaRUB()
        var SumConv : MoneyL;
        var Sum = SummaVal();
        ConvSum(SumConv, Sum, DataReq, Int(Recordset.value("T_CODE_CURRENCY")));
        
        return SumConv;
    end;
    
    macro PrepareSql()
        var Params = TArray;
        Sql = 
            "SELECT ACC.T_ACCOUNTID, "
            "       DECODE (acc.T_TYPE_ACCOUNT, "
            "               CHR (1), CHR (1), "
            "               SUBSTR (acc.T_TYPE_ACCOUNT, 1, 1)) "
            "          AS T_TYPE_ACCOUNT, "
            "       ACC.T_ACCOUNT, "
            "       ACC.T_CODE_CURRENCY, "
            "       ACC.T_CHAPTER, "
            "       ACC.T_OPEN_DATE, "
            "       ACC.T_PAIRACCOUNT, "
            "       ACC.T_NAMEACCOUNT, "
            "       ACC.T_DEPARTMENT, "
            "       ACC.T_BALANCE, "
            "       ADR_JUR.T_REGIONNUM  AS JUR_REGIONNUM, "
            "       ADR_JUR.T_ADRESS     AS JUR_ADRESS, "
            "       ADR_POST.T_ADRESS    AS POST_ADRESS, "
            "       ADR_POST.T_REGIONNUM AS POST_REGIONNUM, "
            "       CUR.T_ISO_NUMBER     AS CUR_SHORT_NAME "
            "  FROM DACCOUNT_DBT ACC, "
            "       DDP_DEP_DBT DEP, "
            "       DADRESS_DBT ADR_JUR, "
            "       DADRESS_DBT ADR_POST, "
            "       DFININSTR_DBT CUR "
            " WHERE ACC.T_CHAPTER = 1 "    
            "   AND ACC.T_DEPARTMENT = DEP.T_CODE "
            "   AND ADR_JUR.T_PARTYID(+) = DEP.T_PARTYID "
            "   AND ADR_JUR.T_TYPE(+)    = 1 "
            "   AND ADR_POST.T_PARTYID(+) = DEP.T_PARTYID "
            "   AND ADR_POST.T_TYPE(+)    = 3 "
            "   AND CUR.T_FIID = ACC.T_CODE_CURRENCY "
            "   AND ACC.T_CLIENT = ?"
            "   AND ACC.T_OPEN_DATE <= ? ";

        Params[Params.size] = ClientID;
        Params[Params.size] = DataReq;
        
        if (TypeCloseAcc != "A")
            if (TypeCloseAcc == "O")
                Sql = Sql + "AND (   acc.T_CLOSE_DATE = TO_DATE ('01.01.0001', 'dd.mm.yyyy') ";
                Sql = Sql + "OR acc.T_CLOSE_DATE > ?) ";
                Params[Params.size] = DataReq;
            elif (TypeCloseAcc == "C")
                Sql = Sql + "AND acc.T_CLOSE_DATE <= ? AND acc.T_CLOSE_DATE <> TO_DATE ('01.01.0001', 'dd.mm.yyyy')";
                Params[Params.size] = DataReq;
            else
                var ErrNum = 31406;
                CreateErrMsg(ErrNum, 1, "TypeCloseAcc");
                RunError(GetErrMsg(), ErrNum);
            end;
        end;
         
        var i = 0;
        Command = RsdCommand(Sql);
        Command.NullConversion = true;
        while (i < Params.size)
            Command.addParam("", RSDBP_IN, Params[i]);
            i = i + 1;
        end;

        Command.execute();
        Recordset = RsdRecordset(Command);
    end;
end;

macro GetCurrDate()
    var cmd = RsdCommand("select trim(TO_CHAR(sysdate, 'dd.mm.yyyy')) from dual;");
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        return date(rs.value(0));
    end;
    
    return date;
end;

macro GetCurrDateTime()
    var cmd = RsdCommand("select sysdate from dual;");
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        return rs.value(0);
    end;
    
    return date;
end;

macro IdentClientByReqFindClientLegal(ClientRequisites : @variant)
    var result = TClientIdentSuccess;
    var finder = TClientIdentFinder;
    
    result.ReqID = "";
    result.ClientFullName = "";
    result.CountRezult = 0;
    result.ClientID = 0;
    
    finder.AddCondition(finder.COND_AND, "CODE.T_OBJECTID (+)= PT.T_PARTYID");
    if (ClientRequisites.INN != NULL)
        finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(CODE.T_CODE) = ?");
        finder.AddParam(NormalizeString(ClientRequisites.INN));
    end;
    
    if (ClientRequisites.FullName != NULL)
        finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(PT.T_NAME) = ?");
        finder.AddParam(NormalizeString(ClientRequisites.FullName));
    else 
        finder.AddCondition(finder.COND_AND, "1 = 0");
    end;
    
    finder.Find(ReqTypeIns);
        
    result.CountRezult = finder.CountRezult();
    result.ClientID = finder.ClientID();
    result.ClientFullName = finder.ClientFullName();
        
    return result;
end;

macro IdentClientByReqFindClientPerson(ClientRequisites : @variant)
    var result = TClientIdentSuccess;
    var finder = TClientIdentFinder;
    
    result.ReqID = "";
    result.ClientFullName = "";
    result.CountRezult = 0;
    result.ClientID = 0;
        
    finder.AddCondition(finder.COND_AND, "PS.T_PERSONID = PT.T_PARTYID");
    
    if (ClientRequisites.FullName != null)
        finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(PT.T_NAME) = ?");
        finder.AddParam(NormalizeString(ClientRequisites.FullName));
    else
        if (ClientRequisites.FIO != null)
            var FIO = ClientRequisites.FIO;
            finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(PS.T_NAME1) = ?");
            finder.AddParam(NormalizeString(FIO.Surname));
                
            finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(PS.T_NAME2) = ?");
            finder.AddParam(NormalizeString(FIO.FirstName));
                
            if (FIO.Patronymic != null)
                finder.AddCondition(finder.COND_AND, "RSI_RSB_STRING.NormalizeString2(PS.T_NAME3) = ?");
                finder.AddParam(NormalizeString(FIO.Patronymic));
            end;
        end;
    end;
    
    if (ClientRequisites.BirthDate != null)
        finder.AddCondition(finder.COND_AND, "PS.T_BORN = ?");
        finder.AddParam(ClientRequisites.BirthDate);
    end;
        
    if (ClientRequisites.IDoc != null)
        var IDoc = ClientRequisites.IDoc;
        if(((IDoc.SerDoc != "") or (IDoc.NumDoc != "")) and (IDoc.TypeDoc != null))
            finder.AddCondition(finder.COND_AND, "PS.T_PERSONID = PD.T_PERSONID");
            finder.AddCondition(finder.COND_AND, "PD.T_PAPERKIND = ?");
            finder.AddParam(Int(IDoc.TypeDoc));

            if(IDoc.SerDoc != "") 
                finder.AddCondition(finder.COND_AND, 
                "REGEXP_REPLACE(RSI_RSB_STRING.NormalizeIDocSer(PD.T_PAPERSERIES), '(\\-|\\s+)') = REGEXP_REPLACE(?, '(\\-|\\s+)')");
                finder.AddParam(NormalizeIDocSer(IDoc.SerDoc));
            end;

            if(IDoc.NumDoc != "") 
                finder.AddCondition(finder.COND_AND, "PD.T_PAPERNUMBER = ?");
                finder.AddParam(IDoc.NumDoc);
            end;
        else
            finder.AddCondition(finder.COND_AND, "PS.T_PERSONID = PD.T_PERSONID(+)");
            finder.AddCondition(finder.COND_AND, "PD.T_PAPERKIND(+) = 0");
        end;
    else
        finder.AddCondition(finder.COND_AND, "PS.T_PERSONID = PD.T_PERSONID(+)");
        finder.AddCondition(finder.COND_AND, "PD.T_PAPERKIND(+) = 0");
    end;
 
    finder.Find(ReqTypePersn);
        
    result.CountRezult = finder.CountRezult();
    result.ClientID = finder.ClientID();
    result.ClientFullName = finder.ClientFullName();
        
    return result;
end;