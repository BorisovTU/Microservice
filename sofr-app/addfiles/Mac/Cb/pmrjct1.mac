 /*
 $Name: pmrjct1.mac
 $Module: Ядро Банкинг
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок     : 29008 - "Обработка отвергнутого"
// Шаг      : 10    - "Обработка отвергнутого"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import pm_setst, cbsttls, pm_common, likepy, "pm_answerret.mac";
var PaymentObj:RsbPayment;

private macro ValueDateForProcessRejectedPayment(Paym : RsbPayment) : date
  var ValueDate : date = {curdate};
  var DocKind = Paym.PrimDocKind;

  if( GetParentOrEqualDocKindFromList( DocKind, 
        PMDOC_BANKPAYMENT, PMDOC_BANKCLAIM, CASH_BOF_ADDORDER, CASH_BOF_INCORDER,
        CASH_BOF_OUTORDER, PMDOC_MEMORIALDOCUMENT, DLDOC_BANKORDER, WL_INDOC )
    )
    ValueDate = PM_GetOperDay_Balance(Paym.Department);
  elif( GetParentOrEqualDocKindFromList( DocKind,
          PMDOC_CLIENTPAYMENT, CASH_PS_INCORDER, CASH_PS_OUTORDER )
      )
    ValueDate = PM_GetOperDay_BankServiceBalance(Paym.Department);
  end;

  return ValueDate;
end;

MACRO ExecuteStep( doc, paymDoc )

  var ProcKind : integer = PM_GetRejectProcessKind();

  if( ( PaymentObj.DemandIsESID == NOT_ESID_PAYMENT ) and ( ProcKind == PM_REJECT_PROCKIND_RESTART ) )
    
    PaymentObj.ValueDate = ValueDateForProcessRejectedPayment(PaymentObj);

    if( (PaymentObj.DocKind == WL_INDOC) OR
        (PaymentObj.DocKind == WL_WIPM) ) 
      PaymentObj.PaymStatus = PM_IS_RECEIVING;
      PaymentObj.PropStatus = PM_PROP_RECEIVED;
    elif( (PaymentObj.PropStatus == PM_PROP_REJECTED) or (PaymentObj.PropStatus == PM_PROP_CORREJECTED) )
      PaymentObj.PaymStatus = PM_READIED;
      PaymentObj.PropStatus = PM_PROP_PREPARING;
    else
      PaymentObj.PaymStatus = PM_READIED;
    end;

    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_WORKING );

    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_OPEN ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    if( GetOprStatus(OPR_PAYM_CONTROL) == OPR_PAYM_ST_CTRL_CONTROL)
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;    
    end;
          
    if((PaymentObj.PayerGroup == PAYMENTS_GROUP_INTERNAL) and (PaymentObj.ReceiverGroup == PAYMENTS_GROUP_INTERNAL))
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_INTERNAL ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
    elif( (PaymentObj.PayerGroup == PAYMENTS_GROUP_EXTERNAL) and (PaymentObj.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL))
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_TRANZIT ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
    elif( (PaymentObj.PayerGroup == PAYMENTS_GROUP_EXTERNAL)    and ( PaymentObj.IsCredit ) or
          (PaymentObj.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL) and ( not PaymentObj.IsCredit ) )
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_IN ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
    elif( (PaymentObj.PayerGroup == PAYMENTS_GROUP_EXTERNAL)    and ( not PaymentObj.IsCredit ) or
          (PaymentObj.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL) and ( PaymentObj.IsCredit ) )
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_OUT ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
    end;

    // Установить дату начала операции равной дате операционного дня пользователя
    SetOprDate(29000000, {curdate});


    if( not IsChildTranzitOrigin(PaymentObj) )
      PaymentObj.FreeReserve( PaymentObj.PayerAccount, CHAPT1, PaymentObj.PayerFIID );
    end;
  else
    if( ( PaymentObj.PrimDocKind == WL_INDOC ) )
      if( ProcKind == PM_REJECT_PROCKIND_EXECUTE ) /*Исполнить*/
        if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_OPEN, OPR_PAYM_DO, OPR_PM_ST_EXEC_ORDER ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        else 
          return 0;
        end;
      elif( ProcKind == PM_REJECT_PROCKIND_REFUSE ) /*Отказать*/
        var DenialGround = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
        if( DenialGround == "" )
          msgbox( "Причина отказа не заполнена.|Формирования подтверждения с отказом невозможно.|Запишите причину отказа в примечания платежа" );
          return 1;
        elif( ( Index( DenialGround, ";" ) <= 1 ) or 
              ( IsDigitalNumber( SubStr( DenialGround, 1, Index( DenialGround, ";" ) - 1 ) ) != 0 ) or
              ( StrLen( SubStr( DenialGround, Index( DenialGround, ";" ) + 1 ) ) == 0 ) )
          msgbox("В причине отказа, указанной в примечании платежа, невозможно выделить код и текстовое пояснение.|Введите сведения в формате <код>;<текстовое пояснение>");
          return 1;
        end;
        if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_OPEN, OPR_PAYM_DO, OPR_PM_ST_REFUSAL_ORDER ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        else 
          return 0;
        end;
      elif( ProcKind == PM_REJECT_PROCKIND_FINISH ) /*Завершить*/
        if( not IsOprMultiExec() )
          var ExecRejectESIDDemand = "Обработка поручения на списание будет завершена без формирования инкассового поручения или подтверждения для ФНС.";
          if( ConfWin( makeArray( string( ExecRejectESIDDemand + " Продолжить?" ) ),
                       makeArray( " Да", " Нет" ) ) == 1/*Нет*/ )
            return 1;
          end;
        end;
      end;
    end;

    if( PaymentObj.PrimDocKind == PS_PAYORDER )
      var obj = GenObject( "RsbPSPayOrder", PaymentObj.PaymentID );
      if( obj.PreacptID )
        PM_UnLinkPreAcpt( PaymentObj.PaymentID );
      end;
    end;

    if( PaymentObj.PrimDocKind == DOC_BO_PAYMENT )
      msgbox("Документ из бэк-офиса должен быть обработан");
      return 1;
    end;
    
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    PaymentObj.FreeReserve( PaymentObj.PayerAccount, CHAPT1, PaymentObj.PayerFIID );

  end;
  
  if(OV_GetOverdraftProcMode() == RBOV_MODE_INTEGRATED)
    if(not RejectLimitUse())
      msgbox("Ошибка при отвержении использования лимита");
      return 1;
    end;
    
    if(not RejectLimitRestore())
      msgbox("Ошибка при отвержении востановления лимита");
      return 1;
    end;
  end;

  if( PaymentObj.PrimDocKind == PS_PAYORDER )

    var PsOrder : RsbPsPayOrder = RsbPsPayOrder(PaymentObj.PaymentID);
    
    if ( (PM_GetRejectProcessKind() == PM_REJECT_PROCKIND_FINISH) // Если действие - завершить
          and
         (PsOrder.Origin == PSPO_OR_PAYEEBANK))
      var PaymStatusClaim, DocKindClaim, PaymentIDClaim;
      if ( GetESIDPrimPaym(PaymentObj.PaymentID,PaymStatusClaim, DocKindClaim, PaymentIDClaim ))
         if (ChoiseESIDProcesDoc(PaymStatusClaim, DocKindClaim, PaymentIDClaim))
           return 1;
         end;
      end;    
    end;
  end;

  
  if( ( ProcKind == PM_REJECT_PROCKIND_FINISH ) and ( existsSqlSelect(  " SELECT 1 " +
                        "   FROM DPMLINK_DBT PMLINK " +
                        "  WHERE     PMLINK.T_PURPOSEPAYMENT = :PAYMENTID " +
                        "        AND PMLINK.T_LINKKIND = :LINKKIND ",
                        makeArray
                        (
                          SQLParam("PAYMENTID", PaymentObj.PaymentID ),
                          SQLParam("LINKKIND", PMLINK_KIND_PARTPAYMENT)
                        )                          
                      ) ) )
                      
    if( IsOprMultiExec() )
      msgbox( "Документ создан для частичного исполнения входящего платежа. Выполните обработку документа в индивидуальном режиме" );
      return 1;
    elif( ConfWin( makeArray( "Документ создан для частичного исполнения входящего платежа|Поступившая сумма должна быть зачислена получателю, на счет невыясненных, либо возвращена отправителю" ),
                   makeArray( "Завершить платеж без исполнения", "Отмена" ) ) )
                   
            msgbox("Пользователь прервал выполнение операции.");
            return 1;
    end;
  
  end;


  return 0;
END;

// на случай, если системный PostTrn не отработает из-за какой-то ошибки
private macro PostTrn(IsExecStep : bool)
  if( IsExecStep and not IsOprMultiExec() )
    PM_SetRejectProcessKind(PM_REJECT_PROCKIND_NONE);
  end;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if( (PM_GetRejectProcessKind() == PM_REJECT_PROCKIND_FINISH) // Если действие - завершить
        and 
        ((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ))
      )
      var DocKind, Origin;
      var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                             " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                             makeArray(SQLParam("", PaymentObj.PaymentID)));
      if (rs and rs.MoveNext())
        DocKind = rs.value(0);
        Origin = rs.value(1);

        if (Origin == PSPO_OR_PAYEEBANK)
          // Queries, Narrative
          var Queries : string = "InfoCode:7";
          var Narrative : string = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );

          var ind = Index(Narrative,";");
          if (ind) 
            var NumQueries: string = Trim(SubStr(Narrative,1,ind-1));
            if (StrIsNumber(NumQueries))
              Queries = "InfoCode:" + NumQueries;
            end;

            Narrative = Trim(SubStr(Narrative,ind+1));
          end;

          Narrative = 
            "Уведомляем об аннулировании " + 
            IfThenElse( DocKind == PSPOKIND_REQUEST, 
                        "инкассового поручения", "платежного требования" ) + ". " + Narrative;
         
          CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
        end;
      end;
    end;
  end;

  PostTrn(message == 1);
  return stat;

OnError(er)
  PostTrn(message == 1);
  ExeptionMessage( er );
  return 1;
end;
