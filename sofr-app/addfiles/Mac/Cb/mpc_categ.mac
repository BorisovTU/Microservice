/*
$Name: mpc_categ.mac
$Module: Ядро ГКБО
$Description: Макрос для определения счетов по категориям учета в механизме процентов
*/

/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpc_categ.mac

  Библиотека       : 

  Описание         : 

  Программист      : Kirakozov Michael (KMV)

  Создан           : 02.02.2009
-------------------------------------------------------------------------*/
Import BankInter, CTInter, FIInter, RsbDataSet, globals;
Import "rsb_const.mac";

//  Реализация класса первичного документа
CLASS PrcContractFD( parm1:variant )

   PRIVATE VAR
      ParmA = TArray,
      OwnerID = 0,
      DepCode = 0,
      FIRoleBArray = TArray;

   var 
      PrcContract = TBfile("prccontract.dbt"),
      Error = 0,       /* $$$ Номер ошибки. Используется для анализа ошибок в ф-ях категорий*/
      Kind  = 0,       /* $$$ вид первичного документа*/
      ID    = 0;       /* $$$ ID первичного документа*/


   PRIVATE MACRO InitParmArray

      ParmA[MC_TYPE_PARAMETR_FIID] = PrcContract.rec.FIID;
      ParmA[MC_TYPE_PARAMETR_CURRENCY] = PrcContract.rec.FIID;
      ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = PrcContract.rec.FIID;
      ParmA[MC_TYPE_PARAMETR_VALDATE] = PrcContract.rec.BeginDate;
      ParmA[MC_TYPE_PARAMETR_BEGDATE] = PrcContract.rec.BeginDate;
      ParmA[MC_TYPE_PARAMETR_FINDATE] = PrcContract.rec.EndDate;
      ParmA[MC_TYPE_PARAMETR_DEPARTMENT] = DepCode;
      ParmA[MC_TYPE_PARAMETR_CLIENT] = OwnerID;
      ParmA[MC_TYPE_PARAMETR_CONTRACTOR] = OwnerID;      
      ParmA[MC_TYPE_PARAMETR_DOCKIND] = 1010;
      ParmA[MC_TYPE_PARAMETR_DOCID] = PrcContract.rec.ContractID;
      ParmA[MC_TYPE_PARAMETR_NUMBER] = PrcContract.rec.Number;
      FIRoleBArray[0] = FIROLE_FIDOC;

    END;


  /* корректировка счета */
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    return true;
  end;

  /* определение FIRole */
  macro GetBasisFIRole( FIRole : integer ) : integer
    return FIRole;
  end;
   
   // определение владельца счета
  /* macro GetOwnerID(account:string, chapter:integer, FIID:integer):integer
      var cmd,rs;
   
      cmd = RSDCommand( "select T_CLIENT from DACCOUNTS_DBT "
                        +"where T_ACCOUNT = ? and T_CHAPTER = ? and T_CODE_CURRENCY = ?");
      cmd.addParam( "", RSDBP_IN, account );
      cmd.addParam( "", RSDBP_IN, chapter );
      cmd.addParam( "", RSDBP_IN, FIID );
      cmd.execute();

      rs = TRsbDataSet(cmd);
      
      if ( rs.movenext() )
         return rs.client;
      end;

      return 0;
  
   end;*/

   //  определение узла ТС счета
   macro GetDepCode(account:string, chapter:integer, FIID:integer):integer
      var   cmd,rs;

      cmd = RSDCommand( "select T_BRANCH from DACCOUNT_DBT "
                        +"where T_ACCOUNT = ? and T_CHAPTER = ? and T_CODE_CURRENCY = ?");
      cmd.addParam( "", RSDBP_IN, account );
      cmd.addParam( "", RSDBP_IN, chapter );
      cmd.addParam( "", RSDBP_IN, FIID );
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      
      if ( rs.movenext() )
         return rs.branch;
      end;

      return 0;
  END;

  /* определение параметров второго рода */
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole ) : integer
   
   VAR Parametr = ParmA[ParmKind];

   if (ValType(OperDate) != V_DATE)
      OperDate = {curdate};
   end;

            if (Parametr==null) 
            /*владелец*/
            if( ParmKind == MC_TYPE_PARAMETR_OWNER ) 
               Parametr = OwnerID;

            /*размещение*/
            elif( ParmKind == MC_TYPE_PARAMETR_PLACE ) 
               Parametr = {OurBank};
            elif (ParmKind == MC_TYPE_PARAMETR_BEGDATE)
               Parametr = PrcContract.rec.BeginDate;
            elif (ParmKind == MC_TYPE_PARAMETR_VALDATE)
               Parametr = PrcContract.rec.BeginDate;
            elif (ParmKind == MC_TYPE_PARAMETR_FINDATE)
               Parametr = PrcContract.rec.EndDate;
            elif (ParmKind == MC_TYPE_PARAMETR_CURRENCY)
               Parametr = PrcContract.rec.FIID;
            elif (ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY)
               Parametr = PrcContract.rec.FIID;
            elif (ParmKind == MC_TYPE_PARAMETR_FIID)
               Parametr = PrcContract.rec.FIID;
            elif (ParmKind == MC_TYPE_PARAMETR_NUMBER)
               Parametr = PrcContract.rec.Number;
            else
               Parametr = -1;
            end;
        end;

    return Parametr;

  end;

   /* определение параметров первого рода */
   macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole ) : integer
    
      var Parametr = -1;

      if( Classificator == LLCLASS_CHAPTER_OCP )

         /* глава учета */
         Parametr = 1;

      elif ((ObjectID == OBJTYPE_KINDRAZM) AND (Classificator == LLCLASS_KIND_RAZM))
         
         Parametr = PrcContract.rec.ResType;

      elif ((ObjectID == OBJTYPE_KINDPRIVL) AND (Classificator == LLCLASS_KIND_PRIV))
      
         Parametr = PrcContract.rec.ResType;

      elif ((ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_KONTRMM))

         Parametr = PrcContract.rec.Contragent;

      elif ((ObjectID == OBJTYPE_SYMBOLNUMBERS) AND (Classificator == LLCLASS_SYMBOLNUMBERS))
      
         if( OperDate >= DateBegin446P )
           Parametr = 0;
         else
           Parametr = 5;
         end;
      
      elif ((ObjectID == OBJTYPE_DIRECTION) AND (Classificator == 565))
      
         Parametr = PrcContract.rec.ResKind;

      elif ((ObjectID == OBJTYPE_PARTY_TYPE) AND (Classificator == LLCLASS_PARTY_TYPE_FU))
      
         Parametr = PrcContract.rec.LegalForm;

      elif ((ObjectID == OBJTYPE_BOOLEAN) AND (Classificator == LLCLASS_CONTRCREDIT_ORG))
      
         Parametr = PrcContract.rec.CreditOrg;
      
      elif( (ObjectID==OBJTYPE_BACKOFFICE) AND (Classificator==LLCLASS_BACKOFFICE) )

         Parametr = PrcContract.rec.BackOffice;

      elif( ( OBJTYPE_SIDEBALANCE) AND (LLCLASS_SIDEBALANCE_CORACCKIND) )

         Parametr = 1;

      end;

    return Parametr;

  end;

  
    if ((ValType(parm1)==V_INTEGER))

    /*Конструктор из ContractID*/

      if (parm1 == 0)
         RunError ("Не указан ID процентного договора");
      end;
      
      PrcContract.KeyNum = 0;
      PrcContract.rec.ContractID = parm1;
      if (not PrcContract.GetEQ)
         RunError ("Не найден процентный договор");
         return null;
      end;
    else
       RunError ("Неверный тип параметра");
    end;

    //OwnerID = GetOwnerID(PrcContract.rec.account,PrcContract.rec.chapter, PrcContract.rec.FIID);
    OwnerID = PrcContract.rec.Client;
    if (OwnerID == 0)
      RunError ("Не найден владелец счета процентного договора");
      return null;
    end;
    
    DepCode = GetDepCode(PrcContract.rec.account,PrcContract.rec.chapter, PrcContract.rec.FIID);
    if (DepCode == 0)
      RunError ("Не найден узел ТС счета процентного договора");
      return null;
    end;
    InitParmArray ();

    Kind = ParmA[MC_TYPE_PARAMETR_DOCKIND];
    Id = ParmA[MC_TYPE_PARAMETR_DOCID];

  end;
  

