/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : calcestm.mac                                           */
/*                                                                      */
/*  Описание   : Процедура расчета времени исполнения платежа           */
/*                                                                      */
/* -------------------------------------------------------------------- */

import BankInter, Календарь, globals, PaymInter, PTInter;

var paym:TRecHandler = TRecHandler( "pmpaym.dbt", "bank.def" );
var rm:TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );

//Категории платежа (для расчета времени исполнения платежа)
private var CATEG_PM_INSIDEBANK     = 1, //внутри одного банка
            CATEG_PM_INSIDEPROVINCE = 2, //внутри одной области
            CATEG_PM_INSIDEREGION   = 3, //внутри одного региона
            CATEG_PM_INTERREGIONAL  = 4; //межрегиональный


private var strDiffTimeZone = "DIFFTIMEZONE", //Часовая разница со временем межрегионального РЦ
            strOperateBegin = "OPERATEBEGIN", //Время операционной обработки: от               
            strOperateEnd   = "OPERATEEND",   //Время операционной обработки: до               
            strTime1        = "TIME1",        //Время на перевод внутри одного банка           
            strTime2        = "TIME2",        //Время на перевод внутри одной области          
            strTime3        = "TIME3",        //Время на перевод внутри одного региона         
            strTime4        = "TIME4";        //Время на межрегиональный перевод               

private const PathEstimateTimePrm = "CB\\PAYMENTS\\ESTIMATETIME\\";

private macro GetEstimateTimePrm(Parm, Value)
  
  var err:integer = 0;
  var locale:string = "";
  var TypeVar;

  if((Parm == strDiffTimeZone) or
     (Parm == strTime1) or
     (Parm == strTime2) or
     (Parm == strTime3) or
     (Parm == strTime4))
    TypeVar = V_INTEGER;
  elif((Parm == strOperateBegin) or (Parm == strOperateEnd))
    TypeVar = V_STRING;
  else
    err = 1;
  end;

  if(not err)
    
    GetRegistryValue(PathEstimateTimePrm + Parm, TypeVar, Value, err);
    
    if(not err)
      SetParm(1, Value);
    else
      msgbox("Ошибка получения значения настройки \"" + PathEstimateTimePrm + Parm +"\"");
    end;

  end;
  
  return err;
end; 


//Сравнить две строки в указанных позициях
//str1, str2 - строки для сравнения
//BeginPos, EndPos - начальная и конечная позиция сравнения включительно
private macro StrCmpPos(str1, str2, BeginPos, EndPos)
  if((BeginPos <= EndPos) and 
     (SubStr(str1, BeginPos, EndPos - BeginPos + 1) == SubStr(str2, BeginPos, EndPos - BeginPos + 1)))
    return true;
  end;
  return false;
end;

// Перевести время в минуты 
private macro TimeTransferMin(t:time):integer
  var hour, min, retval;
  TimeSplit(t, hour, min);
  return hour*60 + min;
end;

// Перевести минуты в время с часами и минутами
// Предполагается, что min < 60*24
private macro MinTransferTime(min):time
  return time(min/60, min-(min/60)*60);
end;

// Учесть часовую разницу со временем межрегионального РЦ
private macro AddTimeZone(Время, RegVal):integer
  if(RegVal >= 0)
    Время = Время - TimeTransferMin(time(RegVal, 0));
  else
    Время = Время + TimeTransferMin(time(-RegVal, 0));
  end;
  return Время;
end;

macro РасчетВремениИсполненияПлатежа()
  var err = 0,
      Дата = {curdate},
      ТипВремени = TYPE_TIME_LOCALTIME,
      Категория,
      Время,
      RegVal,
      RegVal1,
      PayerCodeCliring,
      ReceiverCodeCliring,
      min,hour;

  if(not rm.rec.Instancy)
    msgbox("Платеж не объявлен срочным в СМФР, время исполнения не определяется");
    err = 1;
  end;
  
  if(not err)
    if((StrCmpPos(paym.rec.PayerAccount, paym.rec.ReceiverAccount, 10, 13) and (paym.rec.PayerBankID == paym.rec.ReceiverBankID)) 
       or MFR_BankInCABS(paym.rec.ReceiverBankID))
      
      Категория = CATEG_PM_INSIDEBANK; //внутри одного банка
      if(not (err = GetEstimateTimePrm(strTime1, RegVal)))
        Время = TimeTransferMin(Time()) + RegVal;
      end;

    else
      
      PayerCodeCliring = GetCodeParty(paym.rec.PayerBankID, PTCK_CLIRING, err);
      
      if(not err)
        ReceiverCodeCliring = GetCodeParty(paym.rec.ReceiverBankID, PTCK_CLIRING, err);
        if(err)
          msgbox("Ошибка получения кода клиринга банка получателя.");
        end;
      else
        msgbox("Ошибка получения кода клиринга банка плательшика.");
      end;
      
      if(not err)
        if(StrCmpPos(PayerCodeCliring, ReceiverCodeCliring, 1, 2))
          
          if(StrCmpPos(paym.rec.PayerAccount, paym.rec.ReceiverAccount, 10, 11))
            
            Категория = CATEG_PM_INSIDEPROVINCE; // внутри одной области
          
            if(not (err = GetEstimateTimePrm(strTime2, RegVal)))
              Время = TimeTransferMin(Time()) + RegVal;
            end;
            
          else
            
            Категория = CATEG_PM_INSIDEREGION; // внутри одного региона
          
            if(not (err = GetEstimateTimePrm(strTime3, RegVal)) and 
               not (err = GetEstimateTimePrm(strDiffTimeZone, RegVal1)))
              
              Время = TimeTransferMin(Time()) + RegVal;
              // Учтём часовую разницу со временем межрегионального РЦ
              Время = AddTimeZone(Время, RegVal1);

            end;
            
            ТипВремени = TYPE_TIME_MOSCOWTIME;
          end;

        else
          
          Категория = CATEG_PM_INTERREGIONAL; // межрегиональный
              
          if(not (err = GetEstimateTimePrm(strTime4, RegVal)) and 
             not (err = GetEstimateTimePrm(strDiffTimeZone, RegVal1)))
            
            Время = TimeTransferMin(Time()) + RegVal;
            // Учтём часовую разницу со временем межрегионального РЦ
            Время = AddTimeZone(Время, RegVal1);
            
            ТипВремени = TYPE_TIME_MOSCOWTIME;
          end;

        end;
      end;
    end;
  end;
    
  if((not err) and not (err = GetEstimateTimePrm(strOperateEnd, RegVal)))
    
    if(Время >= TimeTransferMin(time(RegVal)))
      
      if(not (err = GetEstimateTimePrm(strOperateBegin, RegVal1)))
        Время = TimeTransferMin(time(RegVal1));
        Дата = GetDateAfterWorkDays(Дата, 1);
        if((Категория == CATEG_PM_INTERREGIONAL) or (Категория == CATEG_PM_INSIDEREGION))
          ТипВремени = TYPE_TIME_LOCALTIME;
        end;
      end;

    end;
  end;

  if(not err)
    Время = MinTransferTime(Время);
    err = ShowPanelTimeExecInstancyPM(Дата, Время, ТипВремени);
  end;

  return err;
end;
