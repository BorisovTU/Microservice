/*
 $Name: mpcpod20.mac
 $Module: RSACCOUNT
 $Description: Операция выноса на просрочку. Шаг "Балансовый учет".
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcpod20.mac

  Библиотека       : RSACCOUNT

  Описание         : Операция выноса на просрочку: Балансовый учет

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,CTInter, PTInter,globals, CurrInter, mpcactacc, mpccarry,
       mpclb, mpckvit;


record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    ContractID = 0, DocID = 0,
    FIID, OperDate, DocDate, 
    TypeDocs = 0, OldTypeDocs = 0,
    AccountB = "", AccountNB = "",
    AccRestB = $0, AccRestNB = $0,
    SumPrc = $0, SumNOD = $0,
    TrnMsg = "";

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);

PRIVATE MACRO GetSchedPayRecordsNotPaid(ContractID, OperDate):bool
   var   cmd,rs,
         stat = false;

   cmd = RSDCommand ("select T_CNTSCHEDID from DPRCCNTSCHED_DBT where T_DATEREAL IS NULL and " +
                     "T_CONTRACTID = ? and T_SCHEDULEKIND = 3 and T_DELAYDATE <= ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, OperDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      stat = true;
   end;

   return stat;
END;

PRIVATE MACRO GetMergeDate(ContractID, OperDate, MergeDate: @date)
   var   cmd,rs,
         stat = 1;

   cmd = RSDCommand ("select max(T_PERIODENDDATE) as MAXDATE from DPRCCNTSCHED_DBT " +
                     "where T_CONTRACTID = ? and T_SCHEDULEKIND = 3 and T_DELAYDATE <= ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, OperDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      MergeDate = rs.maxdate;
      return stat = 0;
   end;

   return stat;
END;

PRIVATE MACRO GetNonOverduePrcSum(ContractID, MergeDate, Sum: @money)
   var   cmd,rs,
         stat = 1;

   cmd = RSDCommand ("select sum(T_SUMFACT) as SUMMA from DPRCCNTSCHED_DBT " +
                     "where T_CONTRACTID = ? and T_SCHEDULEKIND = 2 and T_PERIODBEGINDATE > ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, MergeDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      Sum = rs.summa;
      return stat = 0;
   end;

   return stat;
END;



PRIVATE MACRO ActuateAcc(CatID, Account:@string, FIID:@integer, Chapter:@integer)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID, CatCode, @CodeCurr, @Chapt);
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;
   
   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;


// Транзакционная часть
PRIVATE MACRO DoOperationCarry()
   var   Debet = "",Credit = "",
         Debet2 = "",Credit2 = "",
         ContrFIID,
         DebetFIID, Debet2FIID,
         CreditFIID, Credit2FIID,
         DebetCatID = 0, CreditCatID = 0,
         Debet2CatID = 0, Credit2CatID = 0,
         DebetTypeID = 0, CreditTypeID = 0,
         Debet2TypeID = 0, Credit2TypeID = 0,
         DebetDep  = 0, CreditDep  = 0,
         Debet2Dep = 0, Credit2Dep = 0,
         CarrySum = $0, CarrySum2 = $0,
         DebetCarrySum, CreditCarrySum, Debet2CarrySum, Credit2CarrySum,
         Chapter = 0, Chapter2 = 0, 
         НазначениеПроводки = "",
         stat = 0;

   НазначениеПроводки = "Перенос начисленных, но неоплаченных по сроку процентов для договора # " + 
                       prccontract.rec.Number + " по счету " + prccontract.rec.Account;

   
   // размещение.баланс
   if (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL)
      DebetTypeID = 2;
      CreditTypeID = 1;
      Chapter = 1;
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);
      GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID);

      if ({curdate} > ДАТА_ВВОДА)
            CarrySum = money(AccRestB - abs(SumNOD));
      else
            CarrySum = SumPrc;
      end;
   end;
   
   // размещение.внебаланс
   if ((TypeDocs == PRC_ACCSCHEMA_RAZM_NONBAL) and ({curdate} < ДАТА_ВВОДА))
      DebetTypeID = 6;
      CreditTypeID = 5;
      Chapter = 3;
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
      GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID);  
      CarrySum = AccRestNB - SumNOD;
   end;
   
   // размещение.баланс и внебаланс
   if ((TypeDocs == PRC_ACCSCHEMA_RAZM_BAL_NONBAL)  and ({curdate} < ДАТА_ВВОДА))
      DebetTypeID = 2;
      CreditTypeID = 1;
      Chapter = 1;      
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);
      GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID);
      CarrySum = AccRestB;
      
      Debet2TypeID = 6;
      Credit2TypeID = 5;
      Chapter2 = 3;
      GetAccountFromList(ContractID, Debet2TypeID, @Debet2, @Debet2CatID, @Debet2FIID, @Chapter2);  
      GetAccountFromList(ContractID, Credit2TypeID, @Credit2, @Credit2CatID, @Credit2FIID);
      CarrySum2 = AccRestNB - SumNOD;
   end;

   if ((Debet == "") and (DebetCatID == 0)) 
      TrnMsg = "Не определен счет плательщика и не задана соотв.КУ для договора";
      return 1;
   else
      if (Debet == "")
         ActuateAcc(DebetCatID, @Debet, @DebetFIID, @Chapter);
         if (Debet == "")
            TrnMsg = "Не удалось актуализировать счет плательщика";
            return 1;
         else
            PrcSetCntAcc(ContractID, DebetTypeID, DebetFIID, Chapter, Debet);
         end;   
      end;
   end;

   if ((Credit == "") and (CreditCatID == 0))
      TrnMsg = "Не определен счет получателя и не задана соотв.КУ для договора";
      return 1;
   else
      if (Credit == "")
         ActuateAcc(CreditCatID, @Credit, @CreditFIID, @Chapter);
         if (Credit == "")
            TrnMsg = "Не удалось актуализировать счет получателя";
            return 1;
         else
            PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
         end;
      end;
   end;

   
   if (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL_NONBAL)
      if ((Debet2 == "") and (Debet2CatID == 0)) 
         TrnMsg = "Не определен счет плательщика и не задана соотв.КУ для договора";
         return 1;
      else
         if (Debet2 == "")
            ActuateAcc(Debet2CatID, @Debet2, @Debet2FIID, @Chapter2);
            if (Debet2 == "")
               TrnMsg = "Не удалось актуализировать счет плательщика";
               return 1;
            else
               PrcSetCntAcc(ContractID, Debet2TypeID, Debet2FIID, Chapter2, Debet2);
            end;   
         end;
      end;

      if ((Credit2 == "") and (Credit2CatID == 0))
         TrnMsg = "Не определен счет получателя и не задана соотв.КУ для договора";
         return 1;
      else
         if (Credit2 == "")
            ActuateAcc(Credit2CatID, @Credit2, @Credit2FIID, @Chapter2);
            if (Credit2 == "")
               TrnMsg = "Не удалось актуализировать счет получателя";
               return 1;
            else
               PrcSetCntAcc(ContractID, Credit2TypeID, Credit2FIID, Chapter2, Credit2);
            end;
         end;
      end;
   end;

   if (CarrySum == 0)
      TrnMsg = "Сумма проводки не должна быть равной 0";
      return 1;
   end;

   ContrFIID = prccontract.rec.FIID;
   
   if (DebetFIID != ContrFIID)
      ConvSum(DebetCarrySum, CarrySum, DocDate, ContrFIID, DebetFIID );
   else
      DebetCarrySum  = CarrySum;
   end;
   if (CreditFIID != ContrFIID)
      ConvSum(CreditCarrySum, CarrySum, DocDate, ContrFIID, CreditFIID );
   else
      CreditCarrySum = CarrySum; 
   end;

   
   if (stat == 0)
      GetAccountDepartment(Debet, Chapter,DebetFIID, @DebetDep);
      GetAccountDepartment(Credit,Chapter,CreditFIID,  @CreditDep);
      if (DebetDep == 0)
         DebetDep = {OperDprt};
      end;
      if (CreditDep == 0)
         CreditDep = {OperDprt};
      end;
   
      stat = Prc_DocCarry( 
                           Chapter,                  // Глава
                           DebetFIID,                // Дебет Валюта
                           CreditFIID,               // Кредит Валюта
                           Debet,                    // Дебет
                           Credit,                   // Кредит
                           abs(DebetCarrySum), 
                           abs(CreditCarrySum),     // сколько
                           DocDate,                  // За какое число
                           "09",
                           NULL,
                           0,                         // CarryResult
                           НазначениеПроводки
                        );
   end;

   if (stat == 0)
      PrcSaveOperDoc(
                     DocID, 
                     1,
                     Chapter,
                     Debet, 
                     DebetDep,
                     DebetFIID, 
                     money(DebetCarrySum),
                     Credit,
                     CreditDep,
                     CreditFIID,
                     money(CreditCarrySum),
                     НазначениеПроводки);
   end;   


   if (PrcSetOperSum(DocID, money(CarrySum)) != 0)
     return 1;
   end;

   if ((stat == 0) and (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL_NONBAL))

      if (Debet2FIID != ContrFIID)
         ConvSum(Debet2CarrySum, CarrySum2, DocDate, ContrFIID, Debet2FIID );
      else
         Debet2CarrySum  = CarrySum2;
      end;
      if (Credit2FIID != ContrFIID)
         ConvSum(Credit2CarrySum, CarrySum2, DocDate, ContrFIID, Credit2FIID );
      else
         Credit2CarrySum = CarrySum2; 
      end;

      GetAccountDepartment(Debet2,  Chapter2, Debet2FIID,  @Debet2Dep);
      GetAccountDepartment(Credit2, Chapter2, Credit2FIID, @Credit2Dep);
      if (Debet2Dep == 0)
         Debet2Dep = {OperDprt};
      end;
      if (Credit2Dep == 0)
         Credit2Dep = {OperDprt};
      end;
      
      stat = Prc_DocCarry(
                           Chapter2,               // Глава
                           Debet2FIID,            // Дебет Валюта
                           Credit2FIID,           // Кредит Валюта
                           Debet2,                // Дебет
                           Credit2,               // Кредит
                           abs(Debet2CarrySum), 
                           abs(Credit2CarrySum),   // сколько
                           DocDate,               // За какое число
                           "09",
                           NULL,
                           0,                      // CarryResult
                           НазначениеПроводки
                        );
      if (stat == 0)
         PrcSaveOperDoc(
                     DocID, 
                     2,
                     Chapter2,
                     Debet2, 
                     Debet2Dep,
                     Debet2FIID, 
                     money(Debet2CarrySum),
                     Credit2,
                     Credit2Dep,
                     Credit2FIID,
                     money(Credit2CarrySum),
                     НазначениеПроводки);
      end;
      
      if (PrcSetOperSum(DocID, money(CarrySum)) != 0)
        return 1;
      end;
      
   end;   

   if (stat)
      TrnMsg = "Ошибка формирования проводки";
      return 1;
   end; 


   return 0;
   
END;


MACRO ExecuteStep(Buffer, Document)
   var   ЗначениеНастройки,
         stat = 0,
         MaxDepDate,
         MergeDate;

   SetBuff( cntopr_buf, Document );

   ContractID = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;	
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      msgbox ("Не могу найти договор с ID = " + ContractID);
      return 1;
   end;

   prccntopr.KeyNum = 0;	
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      msgbox ("Не найдена операция");
      return 1;
   end;

   FIID        = prccontract.rec.FIID;   
   OperDate    = prccntopr.rec.OperDate;
   DocDate     = prccntopr.rec.DocDate;
   OldTypeDocs = prccntopr.rec.TypeDocs;

   // 1.0
   if (GetSchedPayRecordsNotPaid(ContractID, OperDate))
      msgbox ("Не по всем периодам оплаты, отсрочка по которым |закончилась, проведена операция оплаты");
      return 0;
   end;

   MaxDepDate = GetMaxCurDate();
    
   // 2.0
   GetAccountFromList(ContractID, 1, @AccountB, NULL);
   if (AccountB != "")
      if ( FIID == NATCUR )
         AccRestB = abs(RestA( AccountB, MaxDepDate, MaxDepDate, 1/*CHAPT1*/ ));
      else
         AccRestB = abs(RestAC( AccountB, FIID,  MaxDepDate, MaxDepDate, 1/*CHAPT1*/ ));
      end;   
   end;

   GetAccountFromList(ContractID, 5, @AccountNB, NULL);
   if (AccountNB != "")
      if ( FIID == NATCUR )
         AccRestNB = abs(RestA( AccountNB, MaxDepDate, MaxDepDate, 3/*CHAPT3*/ ));
      else
         AccRestNB = abs(RestAC( AccountNB, FIID,  MaxDepDate, MaxDepDate, 3/*CHAPT3*/ ));
      end;   
   end;

   // 2.2
   if ((AccRestB == $0) and (AccRestNB == $0))
      msgbox ("По договору нет начисленных, но неоплаченных срочных процентов");
      return 0;
   end;

   // 3.1
   GetMergeDate(ContractID, OperDate, @MergeDate);
   // 3.2
   GetNonOverduePrcSum(ContractID, MergeDate, @SumNOD);
   // 3.3

   if (ValType(AccRestB) == V_UNDEF)
      AccRestB = $0;
   end;

   if (ValType(AccRestNB) == V_UNDEF)
      AccRestNB = $0;
   end;

   if (ValType(SumNOD) == V_UNDEF)
      SumNOD = $0;
   end;
   
   // Sпр = Sб + Sвб - Sнепр
   SumPrc = money(AccRestB + AccRestNB - abs(SumNOD));
   
   // 3.4
   if (SumPrc == $0)
      msgbox ("По договору нет просроченных процентов");
      return 0;
   end;
   
   // 3.5
   if (SumPrc < $0)
      msgbox ("Ошибка в учете. В учете не отражены| начисленные, но неоплаченные срочные проценты");
      return 0;
   end;  
   
   // Размещение
   if (prccontract.rec.reskind == PRC_RESKIND_RAZM)
      if ({curdate} < ДАТА_ВВОДА)
            if ((AccRestB != 0) and (AccRestNB == 0))
                  TypeDocs = PRC_ACCSCHEMA_RAZM_BAL;
            elif ((AccRestB == 0) and (AccRestNB != 0))
                  TypeDocs = PRC_ACCSCHEMA_RAZM_NONBAL;
            elif (AccRestB >= SumPrc)
                  TypeDocs = PRC_ACCSCHEMA_RAZM_BAL;
            elif (AccRestB < SumPrc)
                  TypeDocs = PRC_ACCSCHEMA_RAZM_BAL_NONBAL;
            end;
      else
            TypeDocs = PRC_ACCSCHEMA_RAZM_BAL;
      end;       
   end;

   if (TypeDocs == 0)
      msgbox("Схема проводок не определена");
      return 1;
   end;

   PrcSetOperTypeDocs(DocID, TypeDocs, OldTypeDocs);
   
   
   if (DoOperationCarry() != 0)
      if (TrnMsg != "")
         msgbox(TrnMsg);
      end;
      return 1;
   else
      if (PrcReceiverAccountChange(ContractID, DocID, OperDate) != 0)
         return 1;
      end;      
   end;

   if (TrnMsg != "")
      msgbox(TrnMsg);
   end;
   
   return 0;
   
END;



MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

