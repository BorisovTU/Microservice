/*
Отчет о правах доступа пользователя
*/

import BankInter, "globals.mac", RSD;

private macro PrintHeader()
  [  Отчет о правах доступа роли СУД];
end;

private macro PrintRoleH(RoleID)
  var strSql : string;
  var rs : RsdRecordset;
  var RoleName : string;

  strSql = "select t_Name from dacsroletree_dbt where t_RoleID = " + RoleID;

  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    RoleName = rs.value( 0 );
  end;
  [
     Роль: ######### #################################################] ( RoleID,  RoleName);
end;

private macro PrintUser( userNum )
  var strSql : string;
  var rs : RsdRecordset;
  var User : string;
  array _User;
  var j : integer;

  strSql = "SELECT t1.t_oper, t2.t_name_type, t1.t_name, dp.t_Name, pt.t_ShortName " +
           "FROM dperson_dbt t1, dtypeac_dbt t2, ddp_dep_dbt dp, dparty_dbt pt     " +
           "WHERE                                                                  " +
           "  t1.t_oper = " + userNum + " AND                                      " +
           "  t2.t_inumtype = 13 AND                                               " +
           "  t2.t_type_account = t1.t_ctypeperson AND                             " +
           "  dp.t_Code = t1.t_codedepart AND                                      " +
           "  pt.t_PartyID = dp.t_PartyID                                          ";
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    User = rs.value( 0 ) + ", " + rs.value( 1 ) + ", " + rs.value( 2 ) + ", " + rs.value( 3 ) + " " + rs.value( 4 );
    StrSplit( User, _User, 58, 58, 2 );
    j = 0;
    while( StrLen(_User( j )) > 0 )
      if( j == 0 )
        [  Сформирован: ##########################################################] ( _User( j ));
      else
        [               ##########################################################] ( _User( j ));
      end;
      j = j + 1;
    end;
  end;
end;

private macro PrintDate()
  [  Дата/время: ########## ########] ( date, time );
end;

private macro PrintTop( RoleID )
  PrintRoleH( RoleID );
  PrintUser( {oper} );
  PrintDate(); 
end;

private macro PrintGroup( RoleID )
  var strSql : string;
  var rs : RsdRecordset;
  var Group : string;
  array _Group;
  var j : integer;

  strSql = "SELECT DISTINCT t1.t_name                             " +
           "FROM dacsgroup_dbt t1, dacsgrouprole_dbt t2           " +
           "WHERE t1.t_groupid = t2.t_groupid AND t2.t_roleid = " + RoleID +
           "ORDER BY t1.t_name                                    ";

  rs = RsdRecordset( strSql );

  j = 0;
  Group = "";
  while( rs.MoveNext())
    if( j > 0 )
      Group = Group + ", ";
    end;
    Group = Group + rs.value( 0 );
    j = j + 1;
  end;
  StrSplit( Group, _Group, 70, 70, 10 );
  j = 0;
  while( StrLen(_Group( j )) > 0 )
    if( j == 0 )
      [
         Группы: ######################################################################] ( _Group( j ));
    else
      [          ######################################################################] ( _Group( j ));
    end;
    j = j + 1;
  end;
end;

/*
private macro PrintRole( repOper )
  var strSql : string;
  var rs : RsdRecordset;
  var Role : string;
  array _Role;
  var j : integer;

  strSql = "SELECT t1.t_name                                    " +
           "FROM dacsroletree_dbt t1, dacsoprole_dbt t2         " +
           "WHERE t1.t_roleid = t2.t_roleid AND t2.t_oper = " + repOper +
           "ORDER BY t1.t_name                                  ";

  rs = RsdRecordset( strSql );

  j = 0;
  Role = "";
  while( rs.MoveNext())
    if( j > 0 )
      Role = Role + ", ";
    end;
    Role = Role + rs.value( 0 );
    j = j + 1;
  end;
  StrSplit( Role, _Role, 72, 72, 10 );
  j = 0;
  while( StrLen(_Role( j )) > 0 )
    if( j == 0 )
      [
        Роли: ########################################################################] ( _Role( j ));
    else
      [       ########################################################################] ( _Role( j ));
    end;
    j = j + 1;
  end;
end;
*/
private macro PrintRestrict( PrivID, PrivName, RoleID )
  var IsExist : bool;
  var RestValue : string;
  array _RestValue;
  var j : integer;
  var stat = false;
  RestValue = "";

  var strSql : string;
  var rs : RsdRecordset;

  strSql = " SELECT t_RestKind, t_RestValue                 " +
           " FROM dacsrestrict_dbt rt, dacsrestgroup_dbt rg " +
           " WHERE  rt.t_RestID = rg.t_RestID               " +
           "   AND t_PrivID = " + PrivID                      +
           "   AND t_ItemLevel = 1                          " +
           "   AND t_ItemID = "+ RoleID                       +
           " ORDER BY t_RestKind                            " ;
           " " ;

  rs = RsdRecordset( strSql );

  while( rs.MoveNext())
    if(RestValue != "")
      RestValue = RestValue + "; ";
    end;

    if(rs.value( 0 ) == "-") 
      RestValue = RestValue + "^";
    end;
   
    RestValue = RestValue + rs.value( 1 );
  end ;

  if(( not stat ) and strlen( RestValue ))
    [
    {####} ########################################################################] ( PrivID, PrivName );
    StrSplit( RestValue, _RestValue, 71, 71, 10 );
    j = 0;
    while( StrLen(_RestValue( j )) > 0 )
      [       #######################################################################] ( _RestValue( j ));
      j = j + 1;
    end;
  end;
end;

private macro PrintPrivilege( RoleID )
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t.t_nodeid, t.t_name FROM dacsprivtree_dbt t "+ 
           " WHERE t.t_nodeid > 0 " +
           "   AND EXISTS ( SELECT 1 FROM dacsRestGroup_dbt rg"
           "                        WHERE rg.t_PrivID = t.t_nodeid " +
           "                          AND rg.t_itemLevel = 1 " +
           "                          AND rg.t_itemID =  " + RoleID +
           "              )            " +
           " ORDER BY t.t_nodeid ";

  rs = RsdRecordset( strSql );

  [
  Привилегии:];
  while( rs.MoveNext())
    PrintRestrict( rs.value( 0 ), rs.value( 1 ), RoleID );
  end;
end;

private macro PrintContext( RoleID )
  PrintGroup( RoleID );

  PrintPrivilege( RoleID );
end;

private macro PrintBottom( curOper )
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t2.t_post, t1.t_name                      " +
           "FROM dperson_dbt t1 FULL JOIN dofficer_dbt t2 ON " +
           "( t2.t_personid = t1.t_partyid ) WHERE t1.t_oper = " + curOper;
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    if( StrLen(rs.value( 0 )) > 0 )
      [
      #####################________________________#################################] ( rs.value( 0 ), rs.value( 1 ));
    else
      [
                           ________________________#################################] ( rs.value( 1 ));
    end;
  end;
end;

macro ПечатьОтчетаСУД( RoleID )
  PrintHeader();

  PrintTop( RoleID );

  PrintContext( RoleID );

  PrintBottom( {oper} );

end;

//ПечатьОтчетаСУД( 5 );