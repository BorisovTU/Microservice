/*
 $Name: fmreqi_lib.mac
 $Module: Финансовый мониторинг
 $Description: Библиотека функций
*/

import BankInter, PTInter, CTInter, rsd, oralib, likepy, globals;
import serviceaction;
import FMInter;

private const english_abc = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
private const russian_abc = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЬЫЪЭЮЯ";
private const digit_abc   = "0123456789";

macro _islower(symbol) : bool
  if((index(strlwr(english_abc), symbol) != 0) or (index(strlwr(russian_abc), symbol) != 0))
    return true;
  end;
  return false;
end;

macro _isupper(symbol) : bool
  if((index(strupr(english_abc), symbol) != 0) or (index(strupr(russian_abc), symbol) != 0))
    return true;
  end;
  return false;
end;

macro _isdigit(symbol) : bool
  if(index(digit_abc, symbol) != 0)
    return true;
  end;
  return false;
end;

macro _isalpha(symbol) : bool
  if(_islower(symbol) or _isupper(symbol))
    return true;
  end;
  return false;
end;

macro _CorrectAddrFieldValue(AddrFieldValue)
  var retValue = "";
  var i = 1;
  var symbol;
  while(i <= strlen(AddrFieldValue))
    symbol = substr(AddrFieldValue, i, 1);
    if((symbol == "-") or (symbol == "/") or (_isalpha(symbol)) or (_isdigit(symbol)))
      retValue = retValue + symbol;
    end;
    i = i + 1;
  end;
  return retValue;
end;

macro _ПолучитьВременноеИмяФайла(fmreqiattach, ext)
  var tmpFileName = string(date:x);
  var query = "";
  var rs;
  query = "SELECT TO_CHAR(SYSDATE, 'YYYYMMDD_HH24MISS') || TRIM(TO_CHAR(RSBSESSIONDATA.CNum, '00000')) FROM dual";
  rs = execSQLselect(query, null, true);

  if(rs and rs.moveNext())
    tmpFileName = rs.value(0);
  end;
  tmpFileName = tmpFileName + "." + ext;
  return tmpFileName;
end;

macro _GetSchemaFormatDate(dateValue)
  var strValue = string(dateValue:f);
  var retValue = "";
  var i = 1;
  var symbol;
  while(i <= strlen(strValue))
    symbol = substr(strValue, i, 1);
    if(symbol == ".")
      retValue = retValue + "/";
    else
      retValue = retValue + symbol;
    end;
    i = i + 1;
  end;
  if(retValue == "")
    retValue = "00/00/0000";
  end;
  return retValue;
end;

macro _GetSchemaFormatTime(timeValue)
  var retValue = "0" + trim(string(timeValue));
  retValue = substr(retValue, strlen(retValue) - 7);
  return retValue;
end;

macro _GetDBVersion()
  var query = "";
  var rs;
  var retValue = "";

  query = query + "SELECT                                                                     ";
  query = query + "  TRIM(TO_CHAR(t_Ver, '0')) || '.' ||                                      ";
  query = query + "  TRIM(TO_CHAR(t_Rel, '00')) || '.' ||                                     ";
  query = query + "  TRIM(TO_CHAR(t_Build, '000')) || '.' ||                                  ";
  query = query + "  TRIM(TO_CHAR(t_Patch, '000')) || '.' ||                                  ";
  query = query + "  TRIM(TO_CHAR(t_Update, '00'))                                            ";
  query = query + "FROM dsetupdat_dbt                                                         ";
  query = query + "ORDER BY t_Ver DESC, t_Rel DESC, t_Build DESC, t_Patch DESC, t_Update DESC ";

  rs = execSQLselect(query, null, true);
  if(rs and rs.moveNext())
    retValue = rs.value(0);
  end;
  return retValue;
end;

