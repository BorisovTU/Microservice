/*
 $Name: ResidencyAccordance.mac
 $Module: Ядро ГКБО 
 $Description: Процедура проверки условий соответствия резидентности субъекта
 */
 /*
	Процедура проверки условий соответствия резидентности субъекта
 */
import PTInter, Rsd, BankInter;

class ChkRsdntAcc
    macro ClearTempTable()
        var cmd : RsdCommand = RsdCommand("delete from dchkrsdntacc_tmp;");
        cmd.execute();
    end;
    
    macro Count()
        var c = 0;
        var cmd : RsdCommand = RsdCommand("select count(*) from dchkrsdntacc_tmp");
        cmd.Execute();
        
        var rs : RsdRecordset = RsdRecordset(cmd);
        if ( rs.MoveNext())
            c = Int(rs.Value(0));
        end;
        
        return c;
    end;
    
    macro AddValue(Client,Account,ForResident)
        var cmd : RsdCommand = RsdCommand("insert into dchkrsdntacc_tmp values(:Client,:Account,:ForResident)");
        cmd.AddParam("Client", RSDBP_IN, Client);
        cmd.AddParam("Account", RSDBP_IN, Account);
        cmd.AddParam("ForResident", RSDBP_IN, ForResident);
        cmd.Execute();
    end;
end;

private macro FindPartyCode1(PartyId)
    var sql = "SELECT CD.T_CODE FROM DPARTY_DBT PT, DOBJCODE_DBT CD ";
    sql = sql + "WHERE CD.T_OBJECTID = PT.T_PARTYID AND CD.T_OBJECTTYPE = 3 ";
    sql = sql + "AND PT.T_PARTYID = :PartyId";
    
    var code = "";
    var cmd : RsdCommand = RsdCommand(sql);
    cmd.AddParam("PartyId", RSDBP_IN, PartyId);
    cmd.Execute();
    
    var rs : RsdRecordset = RsdRecordset(cmd);
    if (rs.MoveNext())
        code = rs.Value(0);
    end;
    return code;
end;

private macro AddPtWithAccH(PartyID, chk : ChkRsdntAcc, Residency : Bool)
    var stat = 0;
    var sql = "SELECT t_Client, t_Account, ";
    if (not Residency)
        sql = sql + "CHR(0) ";
    else
        sql = sql + "CHR(88) ";
    end;
    sql = sql + "t_ForResident "
        "FROM daccount_dbt "
        "WHERE t_Client = :PartyId "
        "    AND t_Open_Close = CHR(0) "
        "    AND (t_Balance, t_Chapter) IN (SELECT t_Balance, t_Chapter "
        "        FROM dbalance_dbt "
        "         WHERE t_Type_Balance ";
    if (Residency)
        sql = sql + "NOT ";
    end;
    sql = sql + "LIKE '%Н%')";
    var cmd : RsdCommand = RsdCommand(sql);
    cmd.AddParam("PartyId", RSDBP_IN, PartyID);
    cmd.execute();	
	var rs : RsdRecordset = RsdRecordset(cmd);
    while ( rs.MoveNext() )
        chk.AddValue( rs.Value(0), rs.Value(1), rs.Value(2));
    end;
    
    return stat;
end;

macro GetAllMasks()
    var AllMasks = "";
    var arr;
    GetRegValueChilds("PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ", false, arr);
    GetRegValueChilds("PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИД С ОГРАНИЧ", true, arr);
    
    var i = 0;
    while (i < arr.size())
        if ((i != 0) and (StrLen(arr[i]) > 0))
            AllMasks = AllMasks + ",";
        end;
        AllMasks = AllMasks + arr[i];
        i = i + 1;
    end;
    
    return AllMasks;
end;

private macro AddPtWithBalanceMask(PartyID, chk : ChkRsdntAcc, Residency : Bool)
    var i = 0;
    var AllMasks = GetAllMasks();
    
    var sql = "SELECT t_Client, t_Account, ";
    if (not Residency)
        sql = sql + "CHR(0) ";
    else 
        sql = sql + "CHR(88) ";
    end;
    sql = sql + " t_ForResident "
            "    FROM daccount_dbt  "
            "    WHERE t_Client = :PartyID  "
            "    AND t_Open_Close = CHR(0) "
            "AND ";
    if (Residency)
        sql = sql + "NOT ";
    end;
    sql = sql + ConvertMaskToSQLFormat(AllMasks, "t_Account");
            
    var cmd : RsdCommand = RsdCommand(sql);
    cmd.AddParam("PartyId", RSDBP_IN, PartyID);
    cmd.Execute();
        
    var rs : RsdRecordset = RsdRecordset(cmd);
        
    while (rs.MoveNext())
        chk.AddValue( rs.Value(0), rs.Value(1), rs.Value(2));
    end;
end;

private macro MakeRecordSet(Batch : Bool)
    var sql : string = "";
    if (not Batch)
        sql = "SELECT DISTINCT T_ACCOUNT, T_FORRESIDENT FROM DCHKRSDNTACC_TMP";
    else
        sql = "SELECT DISTINCT T.T_ACCOUNT, "
           "     T.T_FORRESIDENT, T.T_CLIENT, "
           "     CD.T_CODE, "
           "     PT.T_NAME "
           "FROM DCHKRSDNTACC_TMP T, DPARTY_DBT PT, (select * from DOBJCODE_DBT CD "
           "   WHERE CD.T_OBJECTTYPE = 3 AND T_CODEKIND = 1) CD "
           "   WHERE     T.T_CLIENT = PT.T_PARTYID "
           "   AND CD.T_OBJECTID(+) = PT.T_PARTYID "
           "ORDER BY T.T_CLIENT, T.T_ACCOUNT"
    end;
    return RsdRecordset(sql);
end;

private macro PrintBlockHeader()
[+-----------------------------------------------------+-----------------------------------------------+
| Условие                                             | Открытые объекты                              |
+-----------------------------------------------------+-----------------------------------------------+];
end;

private macro PrintLineFooter()
[+-----------------------------------------------------+-----------------------------------------------+];
end;

private macro PrintPartyHeaderBlock(ClientCode, ClientName)
[+-----------------------------------------------------------------------------------------------------+
| ################################################################################################### |
](ClientCode + " " + ClientName);
end;

private macro PrintLine(ForResident, account)
    // ForResident - Признак счета, открываемого для резидента
    var msg : string;
    if (ForResident == "X")
        msg = "У субъекта-нерезидента есть открытые лицевые счета, открываемые для резидентов.";
    else
        msg = "У субъекта-резидента есть открытые лицевые счета, открываемые для нерезидентов.";
    end;
[| ################################################### | ############################################# |
](msg:w, account);
end;

macro PrintResidencyAccordanceReport()
    var PartyID = 0;
    var Party : RsbParty;
    var Batch : Bool = true;
    
    if (GetParm(0, PartyID))
        Party = RsbParty(PartyID);
        Batch = false;
    end;
[Протокол процедуры проверки условий соответствия резидентности субъекта
];
    var chk : ChkRsdntAcc = ChkRsdntAcc();
    var cCount = chk.Count();
    var rs : RsdRecordset;
    var HasLinesInPrint = false;

    // Если проверка вызвана в отношении одного субъекта и показала выполнение условий соответствия
    if (not Batch)
        var msg = "Для субъекта \"" + FindPartyCode1(PartyID) + " " + Party.FullName + "\" ";
        if (cCount == 0)
            // alt1
            msg = msg + "выполняются условия соответствия резидентности.";
            PrintLn(msg);
        else
            // alt3
            msg = msg + "не выполняются условия соответствия резидентности";
            PrintLn(msg);
            PrintBlockHeader();
            
            rs = MakeRecordSet(Batch);
            while (rs.MoveNext())
                HasLinesInPrint = true;
                PrintLine(rs.Value(1), rs.Value(0));
            end;
        end;
    else
        if (cCount == 0)
            // alt2
            PrintLn("Субъектов, для которых не выполняются условия соответствия резидентности, не обнаружено.");
        else
            // alt4
            [Обнаружены субъекты, для которых  не выполняются условия соответствия резидентности: 
            ];
            rs = MakeRecordSet(Batch);
            var LastClient = -1;
            while (rs.MoveNext())
                if (LastClient != rs.Value(2))
                    LastClient = rs.Value(2);
                    PrintPartyHeaderBlock(rs.Value(3), rs.Value(4));
                    PrintBlockHeader();
                end;
                HasLinesInPrint = true;
                PrintLine(rs.Value(1), rs.Value(0));
            end;
        end;
    end;
    
    if(HasLinesInPrint)
        PrintLineFooter();
    end;
end;

// 0 - выполняются условия соответствия резидентности или был сформирован протокол проверки
macro CheckResidencyAccordance(PartyID : integer, NotResident : string, FormReport : Bool) : integer
    var party : RsbParty = RsbParty(PartyID);
    var chk : ChkRsdntAcc;
    chk.ClearTempTable();

    var Mask  = "";
    var Mask2 = "";
    if (NotResident == "")
        AddPtWithAccH(PartyID, chk, false);
        if ((chk.Count() != 0)  and (not FormReport))
            return 1;
        end;
        
        // 173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ, 173-ФЗ СЧЕТА НЕРЕЗИД С ОГРАНИЧ
        AddPtWithBalanceMask(PartyID, chk, false); 
        if ((chk.Count() != 0) and (not FormReport))
            return 1;
        end;
    else
        AddPtWithAccH(PartyID, chk, true);
        if ((chk.Count() != 0) and (not FormReport))
            return 1;
        end;
        
        // 173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ, 173-ФЗ СЧЕТА НЕРЕЗИД С ОГРАНИЧ
        AddPtWithBalanceMask(PartyID, chk, true); 
        if ((chk.Count() != 0) and (not FormReport))
            return 1;
        end;
    end;
    
    if (FormReport)
        PrintResidencyAccordanceReport(PartyID);
    end;
    
    return 0;
end;