/*
 *  Отчет процедуры исправления остатков лицевых счетов
 */
Import rsd, "corraccrep.mac";

macro PrintReport( Chapter : integer, Balance : string, AccountMask : string, CurrSign : bool )
  
  var Head : string;
  var Count : integer, AccountCount : integer;
  var Account : string;
  var NewDateValue : date, OldDateValue : date;
  var NewRest : money, OldRest : money;
  var sqlStr : string;
  
  var rs : RsdRecordset;

  rs = RsdRecordset( "SELECT count(1) FROM drestdate_tmp" );

  rs.moveNext();

  Count = rs.value(0);

  Head = "Протокол исправления остатков ";

  if( CurrSign ) Head = Head + "по валютным счетам";
  else           Head = Head + "по счетам в национальной валюте";
  end;

  /* печать заголовка отчета */
  PrintHeader( Head, Count, Chapter, Balance, AccountMask );

  if( Count )
    
    /* печать заголовка таблицы */
    PrintTableHeader();
    
    sqlStr =          "SELECT ";
    sqlStr = sqlStr + "   acc.t_Chapter t_Chapter ";
    sqlStr = sqlStr + "  ,acc.t_Code_Currency t_RestCurrency";
    sqlStr = sqlStr + "  ,acc.t_Account t_Account";
    sqlStr = sqlStr + "  ,rest.t_RestDate t_RestDate";
    sqlStr = sqlStr + "  ,rest.t_Rest t_Rest";
    sqlStr = sqlStr + "  ,rest.t_OldRest t_OldRest";
    sqlStr = sqlStr + "  ,rest.t_PlanRest t_PlanRest";
    sqlStr = sqlStr + "  ,rest.t_OldPlanRest t_OldPlanRest";
    sqlStr = sqlStr + "  ,rest.t_Debet t_Debet";
    sqlStr = sqlStr + "  ,rest.t_OldDebet t_OldDebet";
    sqlStr = sqlStr + "  ,rest.t_Credit t_Credit";
    sqlStr = sqlStr + "  ,rest.t_OldCredit t_OldCredit";
    sqlStr = sqlStr + "  ,rest.t_DebetSPOD t_DebetSPOD";
    sqlStr = sqlStr + "  ,rest.t_OldDebetSPOD t_OldDebetSPOD";
    sqlStr = sqlStr + "  ,rest.t_CreditSPOD t_CreditSPOD";
    sqlStr = sqlStr + "  ,rest.t_OldCreditSPOD t_OldCreditSPOD";
    sqlStr = sqlStr + "  FROM drestdate_tmp rest, dcorrect_account_tmp acc ";
    sqlStr = sqlStr + " WHERE rest.t_AccountID = acc.t_AccountID ";
    sqlStr = sqlStr + "   AND rest.t_RestCurrency = acc.t_Code_Currency ";
    sqlStr = sqlStr + " ORDER BY acc.t_Chapter, acc.t_Code_Currency, acc.t_Account, rest.t_RestDate DESC";

    rs = RsdRecordset( sqlStr );

    while( rs.moveNext() )
      
      Account      = rs.value("t_Account"  );
      NewDateValue = OldDateValue = rs.value("t_RestDate");

      NewRest = ReadSelectValue( rs, "t_Rest"    );
      OldRest = ReadSelectValue( rs, "t_OldRest" );

      if( NewRest == null ) NewDateValue = null; end;
      if( OldRest == null ) OldDateValue = null; end;
      
      PrintFirstTableLine( Account, NewDateValue, OldDateValue );

      if( NewRest != OldRest )
        PrintTableLine( "Остаток", NewRest, OldRest );
      end;

      NewRest = ReadSelectValue( rs, "t_PlanRest"    );
      OldRest = ReadSelectValue( rs, "t_OldPlanRest" );
      if( NewRest != OldRest )
        PrintTableLine( "Планируемый остаток", NewRest, OldRest );
      end;

      NewRest = ReadSelectValue( rs, "t_Debet"    );
      OldRest = ReadSelectValue( rs, "t_OldDebet" );
      if( NewRest != OldRest )
        PrintTableLine( "Дебет", NewRest, OldRest );
      end;

      NewRest = ReadSelectValue( rs, "t_Credit"    );
      OldRest = ReadSelectValue( rs, "t_OldCredit" );
      if( NewRest != OldRest )
        PrintTableLine( "Кредит", NewRest, OldRest );
      end;
      
      NewRest = ReadSelectValue( rs, "t_DebetSPOD"    );
      OldRest = ReadSelectValue( rs, "t_OldDebetSPOD" );
      if( NewRest != OldRest )
        PrintTableLine( "Дебет СПОД", NewRest, OldRest );
      end;

      NewRest = ReadSelectValue( rs, "t_CreditSPOD"    );
      OldRest = ReadSelectValue( rs, "t_OldCreditSPOD" );
      if( NewRest != OldRest )
        PrintTableLine( "Кредит СПОД", NewRest, OldRest );
      end;

    end;

    rs = RsdRecordset( "SELECT count(1) FROM (SELECT 1 FROM drestdate_tmp GROUP BY t_AccountID)" );

    rs.moveNext();

    AccountCount = rs.value(0);

    /* печать низа таблицы */
    PrintTableBottom( AccountCount, Count );

  end;

end;
