/*
$Name:           pttax_pan.mac
$Module:         Ядро ГКБО
$Description:    Процедура импорта справочника СОУН. Диалог с пользователем, вызов ХП импорта и печать протокола.
*/

/************************************************************************
  RS-Bank V.6
  
  Импорт справочника СОУН
  Заполнение справочника банков из файла soun1.dbf и soun2.dbf
*************************************************************************/

import BankInter, globals, CTInter, InsCarryDoc, PTInter, FIInter, dbf2ora, cb_sql;
import oralib, likepy, mfo_lib;

record dr(E_ISOUN ,"bank.def");
record dpan(E_IMSOUN ,"bank.lbr") dialog;

file Опер( person );

var NewOper = true;//, Depart = 0, Date_Carry = Date(0,0,0);

var RecreateTableDBF = false;
var ImportFileDir = "";

var stat;
var ParmDelParty = 0;
GetRegistryValue( "CB\\PARTY\\ИМПОРТ СПРАВОЧНИКОВ\\ФАЙЛЫ ИМПОРТА СОУН", V_STRING, ImportFileDir, stat );
 
if( stat != 0 ) 
  MsgBox ("Ошибка чтения настройки CB\\PARTY\\ИМПОРТ СПРАВОЧНИКОВ\\ФАЙЛЫ ИМПОРТА СОУН"); 
end;

FILE soun_ver_dbf ("soun_ver.dbf") dbf;
FILE soun_v_dbf   ("soun_v.dbf"  ) dbf;
 
if( SubStr(ImportFileDir, strlen(ImportFileDir)) != "\\" )
  ImportFileDir = ImportFileDir + "\\";
end;

if( not OPEN(soun_ver_dbf, ImportFileDir+"soun_ver.dbf"))
  MsgBox ("Процедура импорта прервана. Ошибка загрузки файла soun_ver.dbf"); 
  return 1;
end;

if( not OPEN(soun_v_dbf, ImportFileDir+"soun_v.dbf"))
  MsgBox ("Процедура импорта прервана. Ошибка загрузки файла soun_v.dbf"); 
  return 1;
end;


/* Вывод протокола */
macro ПоказОтчета(TxtFileName)
  file outfile() txt;

  if(open(outfile, TxtFileName))
    ViewFile(outfile);
    close(outfile);
  end;
end;

/* Получение данных для протокола. Действующие НО: самостоятельные */
private macro GetParmDS()
  var rs : RsdRecordset;
  var DSCount = 0;
  rs = RsdRecordset("SELECT COUNT(*)  FROM dpttaxautor_dbt AUTOR,  soun1_dbf SOUN" + 
                    " WHERE AUTOR.T_PARTYID = SOUN.T_PARTYID" + 
                    "   AND ( AUTOR.T_DATETILL IS NULL OR AUTOR.T_DATETILL = TO_DATE('01-01-0001', 'DD-MM-YYYY'))" + 
                    "   AND not exists   (SELECT *  FROM dpttaxlink_dbt PTLINK WHERE  PTLINK.T_PARTYID =  SOUN.T_PARTYID )");
  if( rs.MoveNext() )
    DSCount = int(rs.value(0));
  end;
  return DSCount;
end;

/* Получение данных для протокола. Действующие НО: участки */
private macro GetParmDY()
  var rs : RsdRecordset;
  var DYCount = 0;

  rs = RsdRecordset("SELECT COUNT(*)  FROM dpttaxautor_dbt AUTOR,  soun1_dbf SOUN" + 
                    " WHERE AUTOR.T_PARTYID = SOUN.T_PARTYID" + 
                    "   AND ( AUTOR.T_DATETILL IS NULL OR AUTOR.T_DATETILL = TO_DATE('01-01-0001'   ,'DD-MM-YYYY'))" + 
                    "   AND exists   (SELECT *  FROM dpttaxlink_dbt PTLINK WHERE  PTLINK.T_PARTYID =  SOUN.T_PARTYID )");
  if( rs.MoveNext() )
    DYCount = int(rs.value(0));
  end;
  return DYCount;
end;
/* Получение данных для протокола. Исторические записи */
private macro GetParmH()
  var rs : RsdRecordset;
  var HCount = 0;
  rs = RsdRecordset("SELECT COUNT(*)  FROM dpttaxautor_dbt AUTOR,  soun1_dbf SOUN" + 
                    " WHERE AUTOR.T_PARTYID = SOUN.T_PARTYID" + 
                    "   AND ( AUTOR.T_DATETILL IS NOT NULL OR AUTOR.T_DATETILL != TO_DATE('01-01-0001'   ,'DD-MM-YYYY'))" + 
                    "   AND not exists   (SELECT *  FROM dpttaxlink_dbt PTLINK WHERE  PTLINK.T_PARTYID =  SOUN.T_PARTYID )");
  if( rs.MoveNext() )
    HCount = int(rs.value(0));
  end;
  return HCount;
end;

/* Получение данных для протокола. Закрытые */
private macro GetParmC()
  var rs : RsdRecordset;
  var CCount = 0;
  rs = RsdRecordset("SELECT COUNT(*)  FROM dpttaxautor_dbt AUTOR,  soun1_dbf SOUN" + 
                    " WHERE AUTOR.T_PARTYID = SOUN.T_PARTYID" + 
                    "   AND ( AUTOR.T_DATETILL IS NOT NULL OR AUTOR.T_DATETILL != TO_DATE('01-01-0001'   ,'DD-MM-YYYY'))" + 
                    "   AND exists   (SELECT *  FROM dpttaxlink_dbt PTLINK WHERE  PTLINK.T_PARTYID =  SOUN.T_PARTYID )");
  if( rs.MoveNext() )
    CCount = int(rs.value(0));
  end;
  return CCount;
end;

/* Получение данных для протокола. Пункты учета налогоплательщиков */
private macro GetParmPY()
  var rs : RsdRecordset;
  var PYCount = 0;
  rs = RsdRecordset("SELECT COUNT(*) FROM dpttaxpoint_dbt"); 
  if( rs.MoveNext() )
    PYCount = int(rs.value(0));
  end;
  return PYCount;
end;

/* Получение данных для протокола. Предупреждения */
private macro GetParmW()
  var rs : RsdRecordset;
  var WCount = 0;
  rs = RsdRecordset("SELECT COUNT(*) FROM soun1_log WHERE t_Type = 0"); 
  if( rs.MoveNext() )
    WCount = int(rs.value(0));
  end;
  return WCount;
end;

/* Получение данных для протокола. Ошибки */
private macro GetParmErr()
  var rs : RsdRecordset;
  var ErrCount = 0;
  rs = RsdRecordset("SELECT COUNT(*) FROM soun1_log WHERE t_Type = 1"); 
  if( rs.MoveNext() )
    ErrCount = int(rs.value(0));
  end;
  return ErrCount;
end;

private macro DeleteParty(PartyID : integer)
  var party : RsbParty;
  party = RsbParty(PartyID);
  party.Delete();
OnError(err)
  return;
end;

private macro ProcessDeleteParty()
  var rs;
  rs = SQL_ExecuteAndGetRs("SELECT t_PartyID FROM soun1_log WHERE t_fordelete = 1");
  while(rs.MoveNext)
    DeleteParty(rs.value(0));
    ParmDelParty = ParmDelParty + 1;
  end;
end;

/* Печать протокола */
private macro PrintProtocol( StartTime : time, EndTime : time, ImpText, cSENOFound, cSECreated, cNOCreated, cNODelete )
  var TypeName : STRING;
      TypeName = "";
[Протокол работы процедуры импорта справочника налоговых органов СОУН              ];
[                                                                                  ];
[Дата:         ########## г.                                                       ] (date:f);                
[Время:        ######## - ########                                                 ] (StartTime:f, EndTime:f);
[Пользователь: ##### #                                                             ] ({oper}, {Name_Oper});   
[                                                                                  ];
next(soun_ver_dbf);
[Версия: ### от ########## (SOUN1: #####, SOUN2: ##, SOUN V: ## )                   ](soun_ver_dbf.NizV, soun_ver_dbf.DateVD, soun_ver_dbf.QSoun1, soun_ver_dbf.QSoun2, soun_ver_dbf.QSoun_V );//(GetNizV():l, GetDateVD():l, GetQSoun1():l, GetQSoun2():l, GetQSounV():l);
[                                                                                  ];
[Импортировать: #                                                                  ](ImpText);
[                                                                                  ];
[Результат импорта:                                                                ];
[----------------------------------------------------------------------------------];
[ субъекты экономики:          найдено  ######, создано  ######, всего  ######     ](cSENOFound:l, cSECreated:l, (cSENOFound + cSECreated):l );
[                              удалено  ######                                     ](ParmDelParty:l);
[ налоговые органы:            найдено  ######, создано  ######, всего  ######     ](cSENOFound:l, cNOCreated:l, (cSENOFound + cNOCreated):l);
[   действующие:       самостоятельные  ######, участки  ######, всего  ######     ](GetParmDS():l, GetParmDY():l, (GetParmDS() + GetParmDY()):l);
[   исторические записи:                ######                                     ](GetParmH():l);
[   закрытые:                           ######                                     ](GetParmC():l);
[   удалено:                            ######                                     ](cNODelete():l);
[                                                                                  ];
[ пункты учета налогоплательщиков:      ######                                     ](GetParmPY():l);
[----------------------------------------------------------------------------------];
[ предупреждения:        #                                                         ](GetParmW():l);
[ ошибки:                #                                                         ](GetParmErr():l);
[----------------------------------------------------------------------------------];
[                                                                                  ];
[Сообщения процедуры импорта:                                                      ];
[+--------------+-----+------+---------+------------------------------------------+];
[|      Тип     | Код |  Код |    ID   |                Описание                  |];
[|              |  НО | ППУНП| субъекта|                                          |]; 
[+--------------+-----+------+---------+------------------------------------------+]; 
  var rs : RsdRecordset;
  array _RecKey;
  var j;
  rs = RsdRecordset("SELECT t_Type, t_AutorCode, t_CodePPNUP, t_PartyID, t_Text" +
                    "  FROM soun1_log ORDER BY t_AutorCode, t_PartyID, t_CodePPNUP, t_Type"); 
  while( rs.MoveNext() )
    if(rs.value(0) == 0)
      TypeName = "Предупреждение";
   elif(rs.value(0) == 1)
      TypeName = "Ошибка";
   end;

   StrSplit(rs.value(4), _RecKey, 42, 42, 10 );
   j = 0;
   while(StrLen(_RecKey(j)) > 0)
     if(j == 0)
[|##############|#####|######|#########|##########################################|] (TypeName, rs.value(1), rs.value(2), rs.value(3), _RecKey(j));
     else
[|              |     |      |         |##########################################|] (_RecKey(j));
     end;
    j = j + 1;
   end;   

[+--------------+-----+------+---------+------------------------------------------+];
  end;
[                                                                                  ];
end;

macro RunImport(F1, F2, F3, F4)
  var i = 0;
  var ErrText;
  var NotExists : string;
  var Result = 0;
  var rs, cmd, ret_val, CountIMP, CountDPL, CountDLT, stat;
  var StartTime : time;
  var EndTime   : time;
  var txtDir, TxtFileName;
  var retval = 0;
  var cSENOFound = 0, cSECreated = 0, cNOCreated = 0, cNODelete = 0;
  var ImpText : string;
      ImpText = "";
  var F1_text = "действующие налоговые органы";
  var F2_text = "пункты учета налогоплательщиков";
  var F3_text = "исторические записи налоговых органов";
  var F4_text = "закрытые налоговые органы";

  if(F1 == "X")
    ImpText = F1_text;
  end;
  if(F2 == "X")
    ImpText = ImpText + ", " + F2_text;
  end;
  if(F3 == "X")
    ImpText = ImpText + ", " + F3_text;
  end;
  if(F4 == "X")
    ImpText = ImpText + ", " + F4_text ;
  end;

  StartTime = time;
    
  /* 
   * Таблица протокола soun1.log, 
   * таблица файла soun1.dbf, 
   */
 // ClearImportTempTable();

    SQL_Execute("TRUNCATE TABLE soun1_log");
    SQL_Execute("TRUNCATE TABLE soun1_dbf");
    SQL_Execute("TRUNCATE TABLE soun2_dbf");

  /* Таблица soun1.dbf теперь создается апгрейдером 2ой категории, поэтому RecreateTableDBF = false*/
 
  if(not dbf2ora(ImportFileDir + "soun1.dbf", "soun1_dbf", "Загрузка файла soun1.dbf в БД", ErrText, RecreateTableDBF))
      MsgBox("Процедура импорта прервана. Ошибка загрузки файла soun1.dbf:" + ErrText +".");
      return 1;
    end;
 
  if(F2 == "X")
    if(not dbf2ora(ImportFileDir + "soun2.dbf", "soun2_dbf", "Загрузка файла soun2.dbf в БД", ErrText, RecreateTableDBF))
      MsgBox("Процедура импорта прервана. Ошибка загрузки файла soun2.dbf:" + ErrText +".");
    return 1;
  end;
  end;

  if(RecreateTableDBF)
    SQL_Execute("ALTER PACKAGE rsi_rsb_pttaximp COMPILE");
    SQL_Execute("ALTER PACKAGE rsb_pttaximp COMPILE");
  end;

  SQL_Execute("begin dbms_stats.gather_table_stats(ownname=>sys_context('USERENV','SESSION_USER'),tabname=>'SOUN1_DBF'); end;", "Сбор статистики...");

  cmd = RsdCommand("begin ? := rsb_pttaximp.pttaximp_load(?, ?, ?, ?, ?, ?, ?, ?, ?); end;");

  cmd.addParam("ret_val", RSDBP_OUT, V_INTEGER);
  cmd.addParam("F1", RSDBP_IN);
  cmd.addParam("F2", RSDBP_IN);
  cmd.addParam("F3", RSDBP_IN);
  cmd.addParam("F4", RSDBP_IN);
  cmd.addParam("Oper", RSDBP_IN);

  cmd.addParam("cSENOFound", RSDBP_OUT, V_INTEGER);
  cmd.addParam("cSECreated", RSDBP_OUT, V_INTEGER);
  cmd.addParam("cNOCreated", RSDBP_OUT, V_INTEGER);
  cmd.addParam("cNODelete" , RSDBP_OUT, V_INTEGER);

  cmd.value("F1") = F1;
  cmd.value("F2") = F2;
  cmd.value("F3") = F3;
  cmd.value("F4") = F4;
  cmd.value("Oper") = {oper};

  SQL_Execute(cmd, "Выполняется импорт файлов справочника СОУН. Ждите...");

  cSENOFound = cmd.value("cSENOFound"); 
  cSECreated = cmd.value("cSECreated");
  cNOCreated = cmd.value("cNOCreated");
  cNODelete  = cmd.value("cNODelete");

/* 13 */

    ProcessDeleteParty();

    EndTime = time;

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtDir, ErrText);
    TxtFileName = txtDir + "\\impsoun." + String(UserNumber);

    SetOutput(TxtFileName);
    PrintProtocol( StartTime, EndTime, ImpText, cSENOFound, cSECreated, cNOCreated, cNODelete );
    SetOutput();

      ПоказОтчета(TxtFileName);
  
    return "soun1.dbf";
end;

  



/************************************************************************/
/*  Проверки перед сохранением данных при выходе                        */
/************************************************************************/
macro CheckPanel(  dlg, cmd, id, key  )
var stat = true;

  return stat;

end;


macro Init(dlg_rec)
  var stat;
  ClearRecord(dlg_rec);
  dlg_rec.F1  = "X";
  dlg_rec.F2  = "X";
  dlg_rec.F3  = "";
  dlg_rec.F4  = "";
end;

macro Read(temp_rec, rec, read_panel)
  ClearRecord(temp_rec);

  temp_rec.F1 = rec.F1;
  temp_rec.F2 = rec.F2;
  temp_rec.F3 = rec.F3;
  temp_rec.F4 = rec.F4

end;

MACRO EventPan(dlg, cmd, id, key)
  var itemsel;

  if(cmd == DLG_INIT)
    if(NewOper)
      Init(dlg);
    else
      Read(dlg, dpan);
    end;

    UpdateFields(dlg);


  elif(cmd == DLG_KEY) 
    if (key == 32) /*K_SPACE*/
        if(trim(fldname(dlg, id)) == "F1")
          if(dlg.F1 == "")
            dlg.F1 = "X";
          else
            dlg.F1 = "";
          end;
        elif(trim(fldname(dlg, id)) == "F2")
          if(dlg.F2 == "")
            dlg.F2 = "X";
          else
            dlg.F2 = "";
          end;
        elif(trim(fldname(dlg, id)) == "F3")
          if(dlg.F3 == "")
          dlg.F3 = "X";
        else
          dlg.F3 = "";
        end;
        elif(trim(fldname(dlg, id)) == "F4")
          if(dlg.F4 == "")
            dlg.F4 = "X";
          else
            dlg.F4 = "";
          end;
      
        UpdateFields(dlg);
        end;
    elif (key == 316)  /* Клавиша F2 */
      //Main();//
      Message("Выполняется импорт файлов справочника СОУН. Ждите...");
      
      RunImport(dlg.F1, dlg.F2, dlg.F3, dlg.F4); 
      UpdateFields (dlg); //???

      Message("Импорт выполнен");
    end;

  elif(CMD == DLG_SAVE)
    if(NewOper)
      Read(dpan, dlg, true);
    end;
  else
    return CM_DEFAULT;
  end;

END;

Опер.Oper = {oper};
getEQ( Опер );
if( Опер.CTypePerson == "У")
  msgbox("Вам запрещено вызывать эту функцию");
  exit(1);
end;

RunDialog(dpan, "EventPan");
exit (1);
