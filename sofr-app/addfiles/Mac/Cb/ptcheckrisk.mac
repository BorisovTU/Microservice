/*
$Name:           ptcheckrisk.mac
$Module:         Ядро ГКБО
$Description:    Печать протокола проверки уровня риска
*/

Import BankInter;
Import RSD;
Import "ptrsklvl.mac";
Import "globals.mac";

/*
   Формирование строки с рекомендациями
*/
private macro GetRecommendationString( Recommendation : integer )

  var RecommendationString : string;

  RecommendationString = "";

    if( Recommendation == 1 ) RecommendationString = "\n(Рекомендуется назначить)";
  elif( Recommendation == 2 ) RecommendationString = "\n(Рекомендуется изъять)";
  end;

  return RecommendationString;

end;

/*
   Печать заголовка протокола
*/
private macro PrintHeader( LogFormatName : string, TotalParty : integer, ErrorString : string )

[Протокол проверки уровня риска    ];
[                                  ];
[Дата, время: ########## ##########] (Date:f, Time:f);
[Пользователь: ##### #             ] ({oper}, {Name_Oper});
[                                  ];

  if( ErrorString != "" )

[#](ErrorString);

  else

[Формат протокола: #               ] (Trim(LogFormatName));
[Проверено субъектов: #            ] (string(TotalParty));
[                                  ];
[Результаты:                       ];

  end;

end;

/*
   Печать сокращенного протокола для субъекта
*/
private macro PrintShortLog( CurrentRiskLevel : string )

[+---------------+-----------------------------------------------------------------------------------------------------+];
[| Уровень риска | ################################################################################################### |] ( CurrentRiskLevel );
[+---------------+-----------------------------------------------------------------------------------------------------+];

end;

/*
   Печать полного протокола для субъекта
*/
private macro PrintFullLog( PartyID : integer, CurrentRiskLevel : string, FactRiskLevel : string, RecommendedRiskLevel : string )

  var i : integer;

  var cmd : RsdCommand;
  var rs : RsdRecordset;

  cmd = RsdCommand( "SELECT kind.t_ShortName FactorName, t.t_Ground Ground,t.t_Recommendation Recommendation "
                    "FROM dptriskfactorcheck_tmp t, dptriskfactorkind_dbt kind                               "
                    "WHERE t.t_PartyID = ? AND (t.t_IsOn = 'X' OR t.t_Recommendation <> 0)                   "
                    "  AND t.t_FactorKind = kind.t_Number                                                    "
                    "ORDER BY t_ArrayIndex                                                                   "
                  );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, PartyID );

  rs = RsdRecordset( cmd );

  i = 0;

  while( rs.moveNext() )

    var Header : string;
    var FactorName : string;
    var Ground : string;

    if( i > 0 ) Header = "";
    else        Header = "Факторы риска";
    end;

    FactorName = rs.value("FactorName");
    
    Ground = rs.value("Ground");

[+---------------+-----------------------------------------+-----------------------------------------------------------+];
[| ############# | ####################################### | ######################################################### |]
(Header, (FactorName + GetRecommendationString(rs.value("Recommendation"))):w, Ground:w );


    i = i + 1;

  end;


  if( i > 0 )
[+---------------+---------------+-------------------------+-----------------------------------------------------------+];
  else
[+---------------+---------------+-------------------------------------------------------------------------------------+];
  end;

[| Уровень риска | текущий       | ################################################################################### |] ( CurrentRiskLevel );
[|               +---------------+-------------------------------------------------------------------------------------+];
[|               | фактический   | ################################################################################### |] ( FactRiskLevel );
[|               +---------------+-------------------------------------------------------------------------------------+];
[|               | рекомендуемый | ################################################################################### |] ( RecommendedRiskLevel );
[+---------------+---------------+-------------------------------------------------------------------------------------+];


end;


/*
   Печать протокола процедуры
*/
private macro PrintProtokol( FullLogFormat : bool, PartyID : integer, PartyName : string, CurrentRiskLevel : string, FactRiskLevel : string, RecommendedRiskLevel : string )

[+---------------+-----------------------------------------------------------------------------------------------------+];
[| ############# | ################################################################################################### |] ("Субъект", string(PartyID, " ", PartyName):w);

  if( FullLogFormat == false )

    PrintShortLog( CurrentRiskLevel );

  else

    PrintFullLog( PartyID, CurrentRiskLevel, FactRiskLevel, RecommendedRiskLevel );
  
  end;

[                                                                                                                       ];

end;

/*
   Получить фактический уровень риска субъекта
*/
private macro GetFactRiskLevel( PartyID : integer, onDate : date ) : integer
  
  var FactRiskLevel : integer;

  var stat = GetPtRiskLevelByFactors( PartyID, onDate, @FactRiskLevel );
  
  if( stat != 0 )
    CreateErrMsg();
    RunError( GetErrMsg() );
  end;

  return FactRiskLevel;

end;
