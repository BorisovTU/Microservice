/*
 $Name: rs_json.mac
 $Module: Инструмент
 $Description: api для работы с JSON
*/

/*
 Объект RsJson предназначен для сериализации\десериализации RSL-объектов в\из строку Json формата

 class RsJson
    Методы:
       ClassToStr( ObjSource:object ) - публичный метод для сериализации объекта ObjSource в Json-строку. Возвращает строку в Json формате
       JsonDoc   ( strJSON  :string ) - публичный метод для сериализации Json-строки strJSON в объект. Возвращает объект RSL
       GetProperty      (JsonObject:object, propertyName:string) - публичный метод для получения значение поля с именем propertyName нативного типа. Возвращает значение
       GetObjectProperty(JsonObject:object, propertyName:string) - публичный метод для получения значение поля с именем propertyName типа "Объект" . Возвращает значение (объект)
       GetKeys ( JsonObject:object ) : tarray                    - публичный метод для получения индексов(key) массива с данными типа объект
       GetValue( JsonObject:object ) : tarray                    - публичный метод для получения данных массива с данными типа объект
 end;

 Пример формата:
                Формат:
                {
                   "firstName": "Иван",
                   "lastName": "Иванов",
                   "address": {
                       "streetAddress": "Московское ш., 101, кв.101",
                       "city": "Ленинград",
                       "postalCode": 101101
                   },
                   "phoneNumbers": [
                       "812 123-1234",
                       "916 123-4567"
                   ]
                }
*/

Import rcw;
Import dynamicobj;

class RsJson 

   private var ScriptEngine:object;

   //*** Метод для сериализации объекта ObjSource в Json-строку. Возвращает строку в Json формате
   macro ClassToStr( ObjSource:object, SkipUndefProp : bool ) : string
      if( ValType(ObjSource)      == V_UNDEF ) return "{}"; end;

        // TODO: почему на undefined нужно возвращить пустой объект, а не null?
        // Для чего пропускать SkipUndefProp? Если очень нужно, то позже недо доделать.
      
      return ValueToJson(ObjSource);
   end;


   //*** Получить объект
   macro JsonDoc( strJSON:String ) : object
    
      return JsonToValue(strJSON);
   end;

   //*** Получить значение поля с обычным типом
   macro GetProperty(JsonObject:object, propertyName:string) : variant         
       if( ValType(JsonObject) == V_UNDEF )
           MsgBox( "Ошибка!\nНулевой объект JsonObject!" );
           return "";
       end;

       return GenGetProp(JsonObject, propertyName);
       
       onError
          MsgBox( "Ошибка получения значения поля <", propertyName, ">!" );
          return "";
   end;

   //*** Получить значение поля типа "Объект"
   macro GetObjectProperty(JsonObject:object, propertyName:string) : object
       if( ValType(JsonObject) == V_UNDEF )
           MsgBox( "Ошибка!\nНулевой объект JsonObject!" );
           return "";
       end;

       return GenGetProp(JsonObject, propertyName);

       onError
          MsgBox( "Ошибка получения значения поля <", propertyName, ">!" );
          return NULL;
   end;

    //*** Получить значение поля типа "массив"
   macro GetKeys( JsonObject:object ) : tarray
        var sz = JsonObject.size;
        var KeysArray :Tarray  = tarray;
       var i         :integer = 0;

       For( var _val, 0, sz )
           KeysArray[i] = _val;
           i = i + 1;
       end;
       return KeysArray;

       onError
          MsgBox( "Ошибка получения значения поля типа массив!" );
          return NULL;
   end;

   //*** Получить значение поля типа "массив"
   macro GetValue( JsonObject:object ) : tarray
       return JsonObject;
   end;

end;

//*** Создадим экземпляр класса
const jsn:RsJSON = RsJSON;

/*
// Пример использования 
class CAccount(Num: String, opcl: bool, am: money, gr: string)
    var Number  :string  = Num;
    var FlagOpen:bool    = opcl;
    var Amount  :money   = am;
    var Ground  :string  = gr;
end;

class CAddress()
   var streetAddress:string = "Московское ш., 101, кв.101";
   var city         :string = "Ленинград";
   var postalCode   :string = "101101";
end;

class CTest
   var Code   :string  = "Привет";
   var Name   :string  = "Имя";
   var Price  :money   = $111.34;
   var Count  :integer = 100;
   var Flag   :bool    = true;
   var OperDay:date    = Date();
   var lsPhone:tarray  = TArray;
   var Address:CAddress= Address;

   lsPhone[0] = "812 123-1234";
   lsPhone[1] = "916 123-4567";
   lsPhone[2] = "100 123-4567";

   var Accounts    :tarray = Tarray;
       Accounts[0] = CAccount(302011080000001, true , $111.34, "Balance account"    );
       Accounts[1] = CAccount(202021080000001, false, $151.00, "Non-balance account");
end;

var Obj:CTest = CTest;

debugbreak;
// Получить по объекту строку в формате JSON
var strJson:string = jsn.ClassToStr(Obj);
println( strJson );

// Получить объект по строке
var JsonObject = jsn.JsonDoc(strJson);

// Получаем свойства объекта по имени
if( JsonObject != NULL )
   println( jsn.GetProperty(JsonObject, "Code"   ) );
   println( JsonObject.CODE );
   println( jsn.GetProperty(JsonObject, "Price"  ) );
   println( jsn.GetProperty(JsonObject, "Count"  ) );
   println( jsn.GetProperty(JsonObject, "OperDay") );
end;

var lskeys = jsn.GetKeys(jsn.GetObjectProperty(JsonObject, "ACCOUNTS"));
// Массив объектов
var ls = jsn.GetValue(jsn.GetObjectProperty(JsonObject, "ACCOUNTS"));

for( var val, ls )
   println( jsn.GetProperty(val, "Number") );
   println( jsn.GetProperty(val, "Ground") );
end;

//Проверить JSON-строку на корректность можно здесь
//http://json.parser.online.fr/
*/

/**/
