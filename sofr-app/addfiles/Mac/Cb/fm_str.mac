/*
    Общие функции для отчетов подсистемы "Финансовый мониторинг"
 */

private macro AddSpace( StrSource : string, Length : integer ) : string
  var i = 0;
  while( i < Length )
    StrSource = StrSource + " ";
    i = i + 1;
  end;
  return StrSource;
end;

/*
 * Добавить к строке разделителю Length - символов
 */
macro FM_AddToSplitLine( Line : string , Length : integer ) : string
  var i = 0;
  if( Length > 0 )
    if( SubStr(Line, StrLen(Line)) != "+" ) Line = Line + "+"; end;
    while( i < Length )
      i = i + 1;
      Line = Line + "-";
    end;
    Line = Line + "+";
  end;
  return Line;
end;

/*
 * Добавить к строке колонку
 */
macro FM_AddToTabLine( Line : string, Content : string, Length : integer, IndentMode : string, NoTabMode : bool ) : string
  var LeftIndent : integer, RightIndent : integer;
  var TabSymb : string;
  var i = 0;

  if( Length > 0 )
    TabSymb = "|";
    if( valtype(NoTabMode) != V_BOOL )
      NoTabMode = false;
    end;
    
    LeftIndent  = 0;
    RightIndent = Length - StrLen(Content);
    if( (valtype(IndentMode) == V_STRING) )
      if( IndentMode == "r" )
        LeftIndent  = Length - StrLen(Content);
        RightIndent = 0;
      elif( IndentMode == "c" )
        LeftIndent  = (Length - StrLen(Content)) / 2;
        RightIndent = (Length - StrLen(Content)) - LeftIndent;
      end;
    end;

    if( (not NoTabMode) and (SubStr(Line, StrLen(Line)) != TabSymb) ) Line = Line + TabSymb; end;
    Line = AddSpace( Line, LeftIndent );
    Line = Line + Content;
    Line = AddSpace( Line, RightIndent );
  
    if( (not NoTabMode) ) Line = Line + TabSymb; end;
  end;
  return Line;
end;


/*
 * Класс - ячейка печатной формы
 */
class FM_RepCage
( _CageContent     : string
 ,_CageLength      : integer
 ,_LeftIndentSize  : integer
 ,_RightIndentSize : integer
 ,_TypeIndent      : string
)
  private var CageContent : string;
  private var CageLength  : integer;

  private var LeftIndentSize  : integer,
              RightIndentSize : integer;
  private var TypeIndent : string;

  private var SplitContent : TArray;

  /* Конструктор */
  CageContent = _CageContent;
  CageLength  = _CageLength;

  LeftIndentSize  = 0; if( valtype(_LeftIndentSize ) == V_INTEGER ) LeftIndentSize  = _LeftIndentSize ; end;
  RightIndentSize = 0; if( valtype(_RightIndentSize) == V_INTEGER ) RightIndentSize = _RightIndentSize; end;
  
  TypeIndent = "l"; if( valtype(_TypeIndent) == V_STRING ) TypeIndent = _TypeIndent; end;

  /*************** МЕТОДЫ КЛАССА ********************/

  /* получить длину ячейки */
  macro GetCageLength() : string
    return CageLength;
  end;

  /* получить режим форматирования */
  macro GetTypeIndent() : string
    return TypeIndent;
  end;

  /* разбить содержимое ячейки на строки */
  macro Split() : integer
    var i : integer;
    array _SplitContent;

    strsplit( CageContent, _SplitContent, CageLength - LeftIndentSize - RightIndentSize );
    SplitContent = TArray( asize(_SplitContent) );
    i = 0;
    while( i < asize(_SplitContent) )
      SplitContent(i) = _SplitContent(i);
      i = i + 1;
    end;
    return SplitContent.Size();
  end;

  /* получить строку из массива по индексу */
  macro GetLine( nLine : integer )
    var i : integer;
    var Indent : string;
    if( nLine < SplitContent.Size() )
      Indent = ""; i = 0;
      while( i < LeftIndentSize )
        Indent = Indent + " ";
        i = i + 1;
      end;
      return Indent + SplitContent(nLine);
    end;
    return "";
  end;

end;
