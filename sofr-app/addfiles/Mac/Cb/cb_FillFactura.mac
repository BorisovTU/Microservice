import BankInter, BilFacturaInter;

/** 
 * Получение актива платежа в зависимости от вида СФ
 * @param pmpaym Платёж
 * @param bfType Вид СФ
 * @param sum    Возвращаемая сумма
 * @param fiid   Возвращаемая валюта
 */
private macro GetPaymentActiveForBF( pmpaym, bfType:integer, sum:@money, fiid:@integer )
  if( bfType == 1 ) // Выданный
    sum  = pmpaym.PayAmount;
    fiid = pmpaym.PayFIID;
  else // Полученный
    sum  = pmpaym.Amount;
    fiid = pmpaym.FIID;
  end;
  if( ( fiid == -1 ) or ( not sum ) )
    sum  = pmpaym.BaseAmount;
    fiid = pmpaym.BaseFIID;
  end;
end;

/**
 * Пользовательское дозаполнение счёта фактуры по платежу.
 * Вызывается при нажатии Ctrl-F на платеже
 * @param pm_paym_addr          Адрес структуры dpmpaym_dbt
 * @param pm_rmprop_addr        Адрес структуры dpmrmprop_dbt
 * @param debet_addr            Адрес структуры dpmprop_dbt (дебетовое свойство платежа)
 * @param credit_addr           Адрес структуры dpmprop_dbt (кредитовое свойство платежа)
 * @param bil_factura_addr      Адрес структуры DBilFactura_dbt (СФ)
 * @param bil_factura_line_addr Адрес структуры dbilfacturaline_dbt (строка СФ)
 */
macro ЗаполнитьСчетФактуру( pm_paym_addr, pm_rmprop_addr, debet_addr, credit_addr, bil_factura_addr, bil_factura_line_addr )

  RECORD  pm_paym   (pmpaym);
  RECORD  pm_rmprop (pmrmprop);  
  RECORD  debet     (pmprop);  
  RECORD  credit    (pmprop);  
  RECORD  bil_factura      (bilfactura);
  RECORD  bil_factura_line (bilfacturaline);  

  SetBuff(pm_paym  , pm_paym_addr  );
  SetBuff(pm_rmprop, pm_rmprop_addr);
  SetBuff(debet    , debet_addr    );
  SetBuff(credit   , credit_addr   );

  SetBuff(bil_factura     , bil_factura_addr     ); 
  SetBuff(bil_factura_line, bil_factura_line_addr); 

  //------------------------------------------------------

  bil_factura.SFTYPEID = BILTYPE_BILFAC;

  var nds_rate:double = 0;
  GetNDSRateByDate(nds_rate);

  bil_factura_line.NDSRATE = nds_rate;

  if( ( not bil_factura_line.AmountWithNDS ) or ( bil_factura.FIID == -1 ) )
    var sum :money   = 0,
        fiid:integer = -1;
    GetPaymentActiveForBF( pm_paym, bil_factura.Direction, @sum, @fiid );
    bil_factura.FIID = fiid;
    bil_factura_line.AmountWithNDS = sum;
  end;

end;
