/*
 Печатная форма "Остатки по портфелю за даты изменений"
*/
import BankInter, globals;
var pr_accase:TRecHandler = TRecHandler( "accase.dbt", "bank.def" );
file accasscs(accasscs) key 1;

record accasepm(accasepm);
file   accaseit1(accaseit);
file   accaseit2(accaseit);

/* Проверка, что все привязки для портфеля на дату date1 существуют и на дату date2 */
MACRO CheckIncludeAccCaseIt(CaseID:integer, date1:date, date2:date):bool

 accaseit1.CaseID   = CaseID;
 accaseit1.DateFrom = date1;

//msgbox(date1, date2);
//msgbox(accaseit1.CaseID," ! ",accaseit1.DateFrom);

 var result = getGE(accaseit1);
 while( result and (accaseit1.CaseID == CaseID) and (accaseit1.DateFrom == date1) )

   accaseit2.CaseID         = accaseit1.CaseID;
   accaseit2.DateFrom       = date2;
   accaseit2.AccountChapter = accaseit1.AccountChapter;
   accaseit2.AccountFI      = accaseit1.AccountFI;
   accaseit2.Account        = accaseit1.Account;

   if(not getEQ(accaseit2))
     return false;
   end;

   result = next(accaseit1);
 end;

 return true;
END;

MACRO PrintDocument(ncopy:integer):bool
 var accase = pr_accase.rec;

 var DateFrom:date = {curdate};
 var DateTo:date   = {curdate};

 if(not SelectDate(@DateFrom,@DateTo))
   exit;
 end;

 var PeriodStr:string = "";
 if(DateFrom == DateTo)
   PeriodStr = string("на дату ", DateFrom);
 else
   PeriodStr = string("за период с ", DateFrom, " по ", DateTo);
 end;

[Портфель ########## ##################################################
 Список остатков за даты изменений параметров
 ####################################
 ┌──────────┬───────────┬────────────────────────────┐
 │ Дата     │Субпортфель│ Остаток                    │
 ├──────────┼───────────┼────────────────────────────┤
](accase.CaseID, accase.Name, PeriodStr);

 /* Остатки выводим за даты периода, когда менялись следующие параметры портфеля:
    группа риска, процент резерва, состав (список элементов).
    Берутся даты <DateTo+1> соответствующих записей в accasepm.dbt и accaseit.dbt.
 */
 var existRest:bool = false;
 var dateCur:date = DateFrom;
 var result:bool = true;
 var subresult:bool = true;
 var prevRiskGroup:string = "";
 var prevDateFrom:date = date(0,0,0);
 var prevReservePercent:double = 0;
 var IsFirstParm:bool = true;
 var NeedPrint:bool = true; /* Первый раз печатаем несмотря ни на что */
 var rest:money;

 while( (dateCur <= DateTo) and result)
   result = GetACCASEPM_on_Date(accase.CaseID, dateCur, accasepm);

   /* Проверяем, что сменились параметры портфеля(по DateFrom) */
   if( result and (not NeedPrint) and (prevDateFrom != accasepm.DateFrom) )
     /* Проверяем изменение группы риска или процента резерва */
     if((not NeedPrint) and (prevRiskGroup != accasepm.RiskGroup) or (prevReservePercent != accasepm.ReservePercent) )
       NeedPrint = true;
     end;
     /* Проверяем изменение состава портфеля */
     /* Состав не изменился, если все привязки на предыдущю дату существуют и в текущую, и наооброт, все привязки на текущую дату есть и в предудущей */
     if( (not NeedPrint) and (not CheckIncludeAccCaseIt(accase.CaseID, prevDateFrom,      accasepm.DateFrom))) NeedPrint = true; end;

     if( (not NeedPrint) and (not CheckIncludeAccCaseIt(accase.CaseID, accasepm.DateFrom, prevDateFrom)))      NeedPrint = true; end;
   end;

   if(result and NeedPrint)
     ClearRecord(accasscs);
     result = CalcACCASERest(pr_accase, accasscs, dateCur, rest);
     if(result)
[ ##########             ############################](dateCur, rest:f);

       accasscs.CaseID = accase.CaseID;
       subresult = getGE(accasscs);

       /* Печатаем строки об остатках субпортфелей*/
       while(subresult)
         subresult = CalcACCASERest( pr_accase, accasscs, dateCur, rest );
[ ########## ########### ############################]( dateCur, accasscs.Number, rest:f );
         subresult = next(accasscs);
         if(subresult) subresult = (accasscs.CaseID == accase.CaseID); end;
       end;
     end;
   end;

   prevDateFrom       = accasepm.DateFrom;
   prevRiskGroup      = accasepm.RiskGroup;
   prevReservePercent = accasepm.ReservePercent;

   NeedPrint = false;
   dateCur   = dateCur + 1;
 end;

return true;

END;