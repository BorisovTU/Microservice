/*
 * Общие функции обработки комиссий
 */

Import CTInter, "sfcomcat.mac","cb_sql.mac", BankInter, PaymInter;

macro PrintSfPeriodBody(SfComissCode, BeginDate, Amount, FIID, Account, SfInvName, DocID, DocKind, Comment, Type, MacrotxtName)
  var StrQuery, cmd, rs, Name;
  var FIIDName = "";
  var DocName =  "";


  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;

  FIIDName = ПолучитьКодФинИн(FIID);

  if(DocKind > 0)
    StrQuery =  "SELECT T_NAME FROM DOPRKDOC_DBT WHERE T_DOCKIND =" + DocKind;

    cmd = RsdCommand( StrQuery );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if(rs.moveNext())
      Name  = rs.value(0);
    end;
    DocName = Name + " №" + string(DocID);
  end;

  if (Type == 1)
[│######## │##############│#####│######│################│################################│](SfComissCode:w, BeginDate, Amount, FIIDName, Account, Comment:w);
  else
[│######## │##############│#####│######│################│#################│###########│################################│](SfComissCode:w, BeginDate, Amount, FIIDName, Account, SfInvName:w, DocName:w, Comment:w);
end;

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;
end;


macro PrintSfPeriodBodyComment(SfInvName, DocName, Comment, Type,  MacrotxtName)
  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;

  if (Type == 1)
[│######## │##############│#####│######│################│################################│]("", "", "", "", "", Comment:w);
  else
[│######## │##############│#####│######│################│#################│###########│################################│]("", "", "", "", "", SfInvName:w,  DocName:w, Comment:w);
  end;

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;
end;



macro PrintSfSingleBody(SfComissCode, CalcDate, ID_Operation,  Amount, FIID, Account, DocID, DocKind, Comment, MacrotxtName)
  var StrQuery, cmd, rs, Name;
  var FIIDName = "";
  var DocName =  "";

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;

  FIIDName = ПолучитьКодФинИн(FIID);

  if(DocKind > 0)
    StrQuery =  "SELECT T_NAME FROM DOPRKDOC_DBT WHERE T_DOCKIND =" + DocKind;

    cmd = RsdCommand( StrQuery );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if(rs.moveNext())
      Name  = rs.value(0);
    end;
    DocName = Name + " №" + string(DocID);
  end;

[│######## │##############│##########│#####│######│################│######################│################################│](SfComissCode, CalcDate, ID_Operation, Amount, FIIDName, Account, DocName:w, Comment:w);

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;
end;

macro InsertCarry(DateCarry, FIID, debetAccount, creditAccount, Sum, Ground, ID_Operation, ID_Step, TypeDocument)
  var stat = 0;
  var doc = RsbAccTransaction(); // RsbAccTransactionData

  if(Sum > $0)
    doc.ID_Operation     = ID_Operation;
    doc.ID_Step          = ID_Step;     
    doc.Chapter          = 1;
    doc.FIID             = FIID;
    doc.Sum              = Sum;
    doc.Date_Carry       = DateCarry;
    doc.ResultCarry      = 1;
    doc.Number_Pack      = 0;
    doc.Kind_Oper        = " 1" ;
    doc.Shifr_Oper       = "09" ;
    doc.Numb_Document    = "";
    doc.Ground           = Ground;
    doc.AccountPayer     = debetAccount;
    doc.AccountReceiver  = creditAccount;
    if(TypeDocument != NULL)
      doc.TypeDocument = TypeDocument;
    end;

    stat = doc.Carry(ACCTRN_STATUS_DOCUMENT);
  end;
  return stat;
end;
  

private const RashetCatCode = "+Расчеты"; 
private const RashetCatCodeXO = "+Расчеты,ХО"; 

var SfContrRec_    ;
var SfDefComRec_   ;
var ProfitAccount  ;
var LossAccount    ;
var Amount         ;
var TransactionDate;
var PaymentID      ;
var MacrotxtName   ;
var OprSfComRec_   ;
var InvoiceID      ;
////////////////////
var sfcomissFile_Code, ReportDate;
var RashetAccountXO = "";
var DocID = 0, DocKind = 0;
var ID_Operation = 0, ID_Step = 0;     

private var Comment = "";


macro SetBeneAccountComiss( _SfContrRec_, _SfDefComRec_, _TransactionDate, _InvoiceID, _MacrotxtName)
  var stat = 0;
  var COMMENT = "";

  SfContrRec_    =   _SfContrRec_     ;
  SfDefComRec_   =   _SfDefComRec_    ;
  TransactionDate=   _TransactionDate ;
  InvoiceID      =   _InvoiceID       ;
  MacrotxtName   =   _MacrotxtName    ;

  if (not ProcessTrn(0,"SetBeneAccountComissTrn"))
      Comment = "Ошибка при подмене счета";
      PrintSfPeriodBodyComment("", "", Comment, 2, MacrotxtName);
      stat = 1;
  end;
 
  return stat;
end;

macro SetBeneAccountComissTrn()
                                                  
  var stat = 0;
  var ProfitAmount = $0;
  var LossAmount = $0;
  var RashetAccount = "";
  var Ground = "";
  var SfComPD : SfComPrimDoc;
  var strQuery = "";
  var strSubQuery = "";
  var cmd, rs, cmd2, rs2;
  var Comment = "";
  var rsbpaym;
  PaymentID = 0;
  var SfInvName = "", DocName = "", Name = "";

  record SfDefComRec("sfdefcom.dbt");
  record SfContrRec ("sfcontr.dbt" );


  SetBuff( SfDefComRec, SfDefComRec_ );
  SetBuff( SfContrRec, SfContrRec_ );


  SfComPD = SfComPrimDoc( 87 /*SFDOC_INV*/, SfContrRec );   
  RashetAccount = SfComPD.FindAndOpenAccount( RashetCatCode, TransactionDate, SfDefComRec.FIID_commSum );

  if((stat == 0) AND (RashetAccount != ""))
    strSubQuery = " UPDATE DSFINV_DBT SET T_BENEACCOUNT = '"+ RashetAccount +"'," + 
                  " T_ISINCLUDED = 'X'                     " +
                  " WHERE T_INVOICEID = " +  InvoiceID;

    SQL_Execute(strSubQuery);

    Comment =  Comment + "Счет получателя в ТО заменен на \""+RashetAccount+"\"";
  else
    if(RashetAccount == "")
      Comment =  "Ошибка создания счета по категории \"" + RashetCatCode + "\"";
    end;
  end;

  if(InvoiceID > 0)
    StrQuery =  "SELECT T_DOCNUMBER  FROM DSFINV_DBT WHERE T_INVOICEID =" + InvoiceID;

    cmd = RsdCommand( StrQuery );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if(rs.moveNext())
      SfInvName  = rs.value(0);
    end;
  end;

  if(InvoiceID > 0)
    StrQuery =  "SELECT T_PAYMENTID  FROM DSFINVLNK_DBT WHERE t_InvoiceID = " + InvoiceID;

    cmd = RsdCommand( StrQuery );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if(rs.moveNext())
       PaymentID  = rs.value(0);

       rsbpaym = RSBPAYMENT( PaymentID );
       if( rsbpaym == NULL )
         Comment = "Ошибка создания платежа";
         stat = 1;
       end;  

       rsbpaym.SetReceiverAccount(SfDefComRec.FIID_commSum, 1, RashetAccount);

       rsbpaym.Update();

       strSubQuery =  "SELECT T_NAME FROM DOPRKDOC_DBT WHERE T_DOCKIND =" + rsbpaym.DocKind;

       cmd2 = RsdCommand( strSubQuery );
       cmd2.NullConversion = true;
       rs2 = RsdRecordset( cmd2 );

       if(rs2.moveNext())
         Name  = rs2.value(0);
         DocName = DocName + Name + " №" + string(PaymentID ) + " ";
       end;
    end;
  end;

/* 
  if(stat == 0)
    strQuery = "UPDATE DSFDEFCOM_DBT SET T_ISINCLUDED = CHR(88) WHERE T_ID = "+ SfDefComRec.ID ;  

    SQL_Execute(strQuery);
    SfDefComRec.IsIncluded = "X";
  end;
*/

  if((MacrotxtName != NULL) AND (MacrotxtName != ""))
    if ((stat == 0))
      PrintSfPeriodBodyComment(SfInvName, DocName, Comment, 2, MacrotxtName);
    else
      Comment = "Ошибка создания проводки";
      PrintSfPeriodBodyComment("", "", Comment, 2, MacrotxtName);
    end
  end;

  if(stat != 0 )
    AbortTrn;
  end;
  
  return stat; 

end;


macro CarrySingleComiss( _SfContrRec_, _OprSfComRec_, _ProfitAccount, _LossAccount, _Amount, _TransactionDate, _PaymentID, _MacrotxtName)
  var stat = 0;

  SfContrRec_    =   _SfContrRec_     ;
  OprSfComRec_   =   _OprSfComRec_    ;
  ProfitAccount  =   _ProfitAccount   ;
  LossAccount    =   _LossAccount     ;
  Amount         =   _Amount          ;
  TransactionDate=   _TransactionDate ;
  PaymentID      =   _PaymentID       ;
  MacrotxtName   =   _MacrotxtName    ;

  Comment == "";

  if (not ProcessTrn(0,"CarrySingleComissTrn"))
      if(Comment == "")
        Comment = "Ошибка создания проводки";
      end;
      PrintSfSingleBody(sfcomissFile_Code, TransactionDate, ID_Operation,  Amount, 0, RashetAccountXO, DocID, DocKind, Comment, MacrotxtName);
      stat = 1;
  end;

  return stat;
end;

macro CarrySingleComissTrn()
                                                  
  var stat = 0;
  var ProfitAmount = $0;
  var LossAmount = $0;
  var RashetAccount = "";
  var Ground = "";
  var SfComPD : SfComPrimDoc;
  var strQuery = "";
  var cmd, rs;
  Comment = "";
  
  DocID = 0; DocKind = 0;
  ID_Operation = 0; ID_Step = 0;     


  file sfcomissFile( "sfcomiss.dbt"  ) key 0;

  record OprSfComRec("oprsfcom.dbt");
  record SfContrRec ("sfcontr.dbt" );


  SetBuff( OprSfComRec, OprSfComRec_ );
  SetBuff( SfContrRec, SfContrRec_ );

  sfcomissFile.FeeType = OprSfComRec.FeeType;
  sfcomissFile.Number  = OprSfComRec.CommNumber;

  if( not getEQ( sfcomissFile ) )
     MsgBox("Не найдена комиссия" );
     AbortTrn;
     return 1;
  end;

  sfcomissFile_Code =  sfcomissFile.Code;

  ID_Step      = OprSfComRec.ID_Step;
  ID_Operation = OprSfComRec.ID_Operation;

  if(ID_Step == 0)
    ID_Operation = 0;
  end;


  if(PaymentID > 0)
    StrQuery =  "SELECT T_DOCKIND, T_DOCUMENTID FROM DPMPAYM_DBT WHERE T_PAYMENTID = " + PaymentID;

    cmd = RsdCommand( StrQuery );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if(rs.moveNext())
      DocKind  = rs.value(0);
      DocID    = rs.value(1);
    end;
  end;


  Ground = "Начисление комиссии " + sfcomissFile.Code + " по договору " + SfContrRec.Number;

  SfComPD = SfComPrimDoc( 87 /*DLDOC_SFCONTRACT*/, SfContrRec );   
  RashetAccountXO = SfComPD.FindAndOpenAccount( RashetCatCodeXO, TransactionDate, OprSfComRec.FIID_Sum );

  RashetAccount = SfComPD.FindAndOpenAccount( RashetCatCode, TransactionDate, OprSfComRec.FIID_Sum );


  ProfitAmount = RestA(ProfitAccount,null,null,1);

  if((RashetAccount == "") OR (RashetAccountXO == ""))
    Comment = "";

    if(RashetAccount == "")
      Comment =  "Ошибка создания счета по категории \"" + RashetCatCode + "\" ";
    end;

    if(RashetAccountXO == "")
      Comment = Comment +  "Ошибка создания счета по категории \"" + RashetCatCodeXO + "\"";
    end;
    stat = 1;
  else
    if(Amount > 0)
      if(ProfitAmount != $0)
        stat = InsertCarry(TransactionDate, 0, RashetAccountXO, ProfitAccount, Amount, Ground, ID_Operation, ID_Step, "З");
      else
        LossAmount = RestA(LossAccount,null,null,1);

        if(LossAmount < $0)
          LossAmount =  -LossAmount;
        end;

        if(LossAmount >= Amount)
           stat = InsertCarry(TransactionDate, 0, RashetAccountXO, LossAccount, Amount, Ground, ID_Operation, ID_Step, "З");
        else
           stat = InsertCarry(TransactionDate, 0, RashetAccountXO, LossAccount, LossAmount, Ground, ID_Operation, ID_Step, "З");

           if(not stat)
             stat = InsertCarry(TransactionDate, 0, RashetAccountXO, ProfitAccount, Amount - LossAmount, Ground, ID_Operation, ID_Step, "З");
           end;
        end;
      end;

      Ground = "Перенос начисленной комиссии " + sfcomissFile.Code + " по договору " + SfContrRec.Number;

      if(stat == 0)
        stat = InsertCarry(TransactionDate, 0, RashetAccount, RashetAccountXO, Amount, Ground, ID_Operation, ID_Step);
      end;
    end;


    if((stat == 0) AND (PaymentID != 0))

      strQuery = " UPDATE DPMPAYM_DBT SET T_RECEIVERACCOUNT = '"+ RashetAccount +"'," + 
                 " T_FUTURERECEIVERACCOUNT = '" + RashetAccount +"'"                 +
                 " WHERE T_PAYMENTID = " +  PaymentID;



     SQL_Execute(strQuery);
    end;

    if(stat == 0)
      strQuery = "UPDATE DOPRSFCOM_DBT SET T_ISINCLUDED = CHR(88) WHERE T_ID = "+ OprSfComRec.ID ;  

      SQL_Execute(strQuery);
      OprSfComRec.IsIncluded = "X";
    end;
  end;


  if((MacrotxtName != NULL) AND (MacrotxtName != ""))
    if ((stat == 0))
      if(PaymentID)
        Comment = "Счет получателя заменен на \""+RashetAccount+"\"";
      else
        Comment = "";
      end;

      if(ID_Operation != OprSfComRec.ID_Operation)
        Comment= Comment + "Комиссия рассчитана при инициализации операции, поэтому проводки начисления не привязаны к операции";
      end;

      PrintSfSingleBody(sfcomissFile.Code, TransactionDate, ID_Operation,  Amount, 0, RashetAccountXO, DocID, DocKind, Comment, MacrotxtName);
    else
//      PrintSfSingleBody(sfcomissFile.Code, TransactionDate, ID_Operation,  Amount, 0, RashetAccountXO, DocID, DocKind, Comment, MacrotxtName);
    end
  end;

  if(stat != 0 )
    if(Comment == "")
      Comment = "Ошибка создания проводки";
    end;
    AbortTrn;
  end;

  return stat; 

end;


macro PrintHead(DateFee, ProfitAccount, LossAccount, MacrotxtName)
  var strSql : string;
  var rs : RsdRecordset;

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;

  strSql = "SELECT t_oper, t_name " +
           "FROM dperson_dbt      " +
           "WHERE                 " +
           "  t_oper = " + {oper}  ;
           
  rs = RsdRecordset( strSql );
  rs.MoveNext();

[  Протокол начисления комиссий за 2007 г.                           ];
[                                                                    ];
[ Дата формирования: ##########                                      ](date);
[ Время формирования:########                                        ](time);
[ Операционист: ##### ############################################   ](rs.value( 0 ), rs.value( 1 ));
[                                                                    ];
[ Параметры запуска операции начисления:                             ];
[ Дата начисления - ##########                                       ](DateFee);
[ Лицевой счет прибыли предшествующих лет - #######################  ](ProfitAccount);
[ Лицевой счет убытка предшествующих лет  - #######################  ](LossAccount);
[ Дата окончания периода начисления - 31.12.2007                     ];
[                                                                    ];
[ Результаты выполнения:                                             ];
[                                                                    ];

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;
end;

macro PrintSfPeriodHead(SFContrCode, SFContrAccount, ClientID, Type, MacrotxtName )
  file client  ("party.dbt");
  var ClientName = "";

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;


  if( not ПолучитьСубъекта(ClientID, client) )
    ClientName = client.ShortName;
  end;

[ Договор обслуживания № ##############################              ](SFContrCode);
[ Объект договора: #######################                           ](SFContrAccount);
[ Клиент:                                                            ](ClientName);

  if(Type == 1)
[┌─────────┬──────────────┬─────┬──────┬────────────────┬────────────────────────────────┐] ;
[│Комиссия │ Дата начала  │Сумма│Валюта│ Счет начисления│          Комментарий           │] ;
[│         │ периода      │     │      │                │                                │] ;
[│         │ начисления   │     │      │                │                                │] ;
[├─────────┼──────────────┼─────┼──────┼────────────────┼────────────────────────────────┤] ;
//[│######## │##############│#####│######│################│################################│](:w);
//[└─────────┴──────────────┴─────┴──────┴────────────────┴────────────────────────────────┘]    ;
  else
[┌─────────┬──────────────┬─────┬──────┬────────────────┬─────────────────┬───────────┬────────────────────────────────┐] ;
[│Комиссия │ Дата начала  │Сумма│Валюта│ Счет начисления│    № изм. ТО    │ Измененные│          Комментарий           │] ;
[│         │ периода      │     │      │                │                 │ документы │                                │] ;
[│         │ начисления   │     │      │                │                 │ оплаты    │                                │] ;
[├─────────┼──────────────┼─────┼──────┼────────────────┼─────────────────┼───────────┼────────────────────────────────┤] ;
//[│######## │##############│#####│######│################│#################│###########│################################│](:w);
//[└─────────┴──────────────┴─────┴──────┴────────────────┴─────────────────┴───────────┴────────────────────────────────┘]    ;
  end;




 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;

end;



macro PrintSfPeriodTail(Type, MacrotxtName)

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;

  if(Type == 1)
[└─────────┴──────────────┴─────┴──────┴────────────────┴────────────────────────────────┘]    ;
  else
[└─────────┴──────────────┴─────┴──────┴────────────────┴─────────────────┴───────────┴────────────────────────────────┘]    ;    ;
  end;


 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;

end;
////////////////////////////////////////////////////////////////////////

macro PrintSfSingleHead(SFContrCode, SFContrAccount, ClientID, MacrotxtName )
  file client  ("party.dbt");
  var ClientName = "";

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;


  if( not ПолучитьСубъекта(ClientID, client) )
    ClientName = client.ShortName;
  end;

[ Договор обслуживания № ###############                             ](SFContrCode);
[ Объект договора: #######################                           ](SFContrAccount);
[ Клиент:                                                            ](ClientName);
[┌─────────┬──────────────┬──────────┬─────┬──────┬────────────────┬──────────────────────┬────────────────────────────────┐] ;
[│Комиссия │ Дата расчета │ Ид.      │Сумма│Валюта│ Счет начисления│ Измененные документы │          Комментарий           │] ;
[│         │              │ Операции │     │      │                │ оплаты               │                                │] ;
[│         │              │          │     │      │                │                      │                                │] ;
[├─────────┼──────────────┼──────────┼─────┼──────┼────────────────┼──────────────────────┼────────────────────────────────┤] ;
//[│######## │##############│##########│#####│######│################│######################│################################│](:w);
//[└─────────┴──────────────┴──────────┴─────┴──────┴────────────────┴──────────────────────┴────────────────────────────────┘]    ;

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;

end;

macro PrintSfSingleTail(MacrotxtName)

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;


[└─────────┴──────────────┴──────────┴─────┴──────┴────────────────┴──────────────────────┴────────────────────────────────┘]    ;

 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;

end;


macro PrintSfComissHeadByType(Type, MacrotxtName)

  if(MacrotxtName != "")
    SetOutput(MacroTxtName, True);
  end;
[                                                                                                     ]    ;
  if(Type == 1) // 
[ПЕРИОДИЧЕСКИЕ КОМИССИИ - НАЧИСЛЕНИЕ                                                                  ]    ;
 else 
  if (Type == 2)
[ПЕРИОДИЧЕСКИЕ КОМИССИИ - ИЗМЕНЕНИЕ ПЛАТЕЖНЫХ ДОКУМЕНТОВ                                              ]    ;
  else
[ЕДИНОВРЕМЕННЫЕ КОМИССИИ                                                                              ]    ;
  end;
 end;
[                                                                                                     ]    ;
 if(MacrotxtName != "")
   SetOutput(NULL, True);
 end;

end;
