
IMPORT CTInter, FIInter, "globals.mac", rsd;
IMPORT SFInter, sfcomcat, sf_lib;


/*Для единовременных комиссий проводки по учету начисленной комиссии в доход выполняются на шаге 
начисления. */

private const OBJTYPE_OPRSFCOM  = 665; /*Удержанная единовременная комиссия*/

private const PlusCalc_CatCode = "+Расчеты"; 
private const PlusCalcNDS_CatCode = "+Расчеты НДС"; 
private const MinusNDSAccrual_CatCode = "-НДС начисленный"; 

private const PlusCalcNVPI_CatCode = "+Расчеты,НВПИ"; 
private const PlusCalcNDS_NVPI_CatCode = "+Расчеты НДС,НВПИ";
private const MinusNDSAccrual_NVPI_CatCode = "-НДС начисленный, НВПИ";
 
macro CountOprSfComAsIncome( OprSfCom_buff, SfContr_buff, sidebet_buff, sicredit_buff, 
                             BlockID, Number_step, DateKindID, CatAccount:@string )
  
  var stat = 0;

  var SfConComPD : SfConComPrimDoc;

  var RashetAccount : string;
  var NDSPayerAccount:string, NDSReceiverAccount : string;
  var Ground : string, strDate: string;
  var sqlString, rs, count, cmd;
  var opdate;

  var SumPayer = $0, SumNDS = $0;
  
  var PayerCatCode:string, NDSPayerCatCode:string, NDSReceiverCatCode:string;

  var SfAccrueRegVal = SfComAccrueGetRegValue();

  var isNVPI : bool;  

  record OprSfCom( sfdef );
  record SfContr( sfcontr );  
  
  SetBuff( OprSfCom, OprSfCom_buff );
  SetBuff( SfContr, SfContr_buff );

  record sidebet(sfsi)  ;
  record sicredit(sfsi) ;

  SetBuff( sidebet , sidebet_buff );
  SetBuff( sicredit, sicredit_buff);

  file sfcomiss( "sfcomiss.dbt"  ) key 0;
  sfcomiss.FeeType = OprSfCom.FeeType;
  sfcomiss.Number  = OprSfCom.CommNumber;

  if( not getEQ( sfcomiss ) )
    MsgBox("Не найдена комиссия");
    return 1;
  end;

  var payFIID = OprSfCom.FIID_Sum;
  if( sfcomiss.FIID_paySum != -1 )
    payFIID = sfcomiss.FIID_paySum; 
  end;

  if( DateKindID > 0 )
      GetOprDate_Kind( DateKindID, opdate );      
      if( opdate == Date(0,0,0) )
        opdate = {curdate};
      end;      
    else
      opdate = {curdate};
    end;

  /*в 2029.3X изменился индекс TSfConComKey0: добавлены поля SfPlanID и dateBegin.
  При SfPlanID = -1 в функции проверяется наличие ТП у SfContr действующего на dateBegin: 
  если ТП есть, то SfConCom ищется с учетом его идентификаторa, если нет - SfPlanID = 0*/
  var SfConCom = TBFile( "sfconcom.dbt" );
  if( FindSfConCom_OnDate(SfContr.ID, OprSfCom.FeeType, OprSfCom.CommNumber, 659, -1, opdate, SfConCom) )
    MsgBox("Не найдена комиссия ДО");
    return 1;
  end;

  sqlString = " SELECT COUNT(1) FROM DOPRKSFCM_DBT " +
              " WHERE T_BLOCKID  = " + BlockID +
              " AND T_NUMBER_STEP = " + Number_step +
              " AND T_ACTION = 2  "   +                            /* OPRKSFCM_ACTION_PAY */
              " AND T_FEETYPE = " + OprSfCom.FeeType  +
              " AND T_COMMNUMBER = " +OprSfCom.CommNumber ;


  cmd = RsdCommand( sqlString );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );
  rs.moveNext();
  count = rs.value(0);

  if( ((SfIsOurFilial(sicredit.PartyID) == true ) OR (sicredit.BankCode == "")) 
    AND ( (SfAccrueRegVal == 0) OR ((SfAccrueRegVal == 2) AND (sfcomiss.PayNDS != SF_NOT_TAX)) )    
    )

    if( isDefComNVPI( OBJTYPE_OPRSFCOM, OprSfCom, isNVPI ) != 0 )
      return 1;
    end;
    
    SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConCom.rec, SfContr, OprSfCom.FIID_Sum );

    if( isNVPI == false )
      PayerCatCode = PlusCalc_CatCode;
      /*Если по комиссии был рассчитан НДС (DOPRSFCOM_DBT. T_SUMNDS > 0),*/
      if( OprSfCom.SumNDS > 0 )
        NDSPayerCatCode = PlusCalcNDS_CatCode;
        NDSReceiverCatCode = MinusNDSAccrual_CatCode;        
      end;
    else/*Иначе (нужно использовать НВПИ)*/
      PayerCatCode = PlusCalcNVPI_CatCode;

      if( OprSfCom.SumNDS > 0 )
        NDSPayerCatCode = PlusCalcNDS_NVPI_CatCode;
        NDSReceiverCatCode = MinusNDSAccrual_NVPI_CatCode;
      end;
    end;

    RashetAccount = SfConComPD.GetDefComSfSiAccount( OprSfCom.ID, CALC_SFSI_KIND, PayerCatCode, opdate, PayFIID );

    if( RashetAccount == "" )
      msgbox( "Ошибка определения счета по категории учета = " + PayerCatCode);
      return 1;      
    end;

    CatAccount = RashetAccount;
    if( ConvSum( SumPayer, OprSfCom.Sum, opdate, OprSfCom.FIID_Sum, PayFIID, sfcomiss.Ratetype ) != 0 )
      SumPayer = $0;
    end;
    Ground = "За услуги по договору \"" +SfContr.Number + "\"  от " + string(SfContr.DATECONC:f);

    stat = CountComissAsIncome( opdate, PayFIID, RashetAccount, sicredit.FIID, 
                                sicredit.Account, SumPayer, Ground );    


    if( (stat == 0) AND (OprSfCom.SumNDS > 0) )
      NDSPayerAccount = SfConComPD.GetDefComSfSiAccount( OprSfCom.ID, CALCNDS_SFSI_KIND, NDSPayerCatCode, opdate, PayFIID );
    
      NDSReceiverAccount
        = SfConComPD.GetDefComSfSiAccount( OprSfCom.ID, NDSACRUAL_SFSI_KIND, NDSReceiverCatCode, opdate, PayFIID );

      if( NDSPayerAccount == "" )
        msgbox( "Ошибка определения счета по категории учета = " + NDSPayerAccount);
        return 1;
      end;
      if( NDSReceiverAccount == "" )
        msgbox( "Ошибка определения счета по категории учета = " + NDSReceiverAccount);
        return 1;
      end;

      if( ConvSum( SumNDS, OprSfCom.SumNDS, opdate, OprSfCom.FIID_Sum, PayFIID, sfcomiss.Ratetype ) != 0 )
        SumNDS = $0;
      end;
      Ground = "НДС за услуги по договору \"" + SfContr.Number + "\"  от " + string(SfContr.DATECONC:f);
      
      stat = CountComissAsIncome( opdate, PayFIID, NDSPayerAccount, PayFIID, 
                                  NDSReceiverAccount, SumNDS, Ground );
    end;

    if( stat == 0 )
      OprSfCom.IsIncluded = "X";
      OprSfCom.Status = SFDEFCOM_STATUS_CALCULATED;
    end;

  end;
    
  return stat;

end;

