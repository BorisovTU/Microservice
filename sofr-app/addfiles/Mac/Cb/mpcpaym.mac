/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.20
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcpaym.mac

  Библиотека       : RSACCOUNT

  Описание         : .

  Программист      : Kirakozov Michael (KMB)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/

import PaymInter, PSInter, OprInter, BankInter, psbccomn, CurrInter, FIInter, CTInter,
       InsCarryDoc, pm_common, RsbDataSet;


macro Prc_FillPayment( Paym, PayerFIID, ReceiverFIID,  PayDate, PaySum,  FIID, flagPostDoc)

   var stat;
   var PayerAmount = $0;
   var ReceiverAmount = $0;

   Paym.BaseFIID       = FIID;
   if( PayerFIID != FIID )
      if( not ConvSumCross( PayerAmount, PaySum, PayDate, FIID, PayerFIID ))
         MsgBox( "Ошибка конвертации суммы" );
         return 1;
      end;
   else
      PayerAmount =PaySum;
   end;

   if( ReceiverFIID != FIID )
      if( not ConvSumCross( ReceiverAmount, PaySum, PayDate, FIID, ReceiverFIID ))
         MsgBox( "Ошибка конвертации суммы" );
         return 1;
      end;
   else
      ReceiverAmount =PaySum;
   end;

   if (flagPostDoc)
      Paym.Origin = PAYMENT_OR_ELECTR;
   else
      Paym.Origin = PAYMENT_OR_AUTO;
      Paym.CryptoAction( string("Автоматическое_формирование_платежей") );
   end;
  
   Paym.PayerAmount    = PayerAmount;
   Paym.ReceiverAmount = ReceiverAmount;
   Paym.BaseAmount     = PaySum;
   Paym.ValueDate      = 
   Paym.Date           = PayDate;
   Paym.PayerBankEnterDate = 
   Paym.ClientDate         = {curdate};
   Paym.IsPlanPaym     = "X";
   Paym.PaymentKind    = "Э";

   return 0;
end;


macro Prc_FillBankPayment( Paym, PayDate )

  Paym.Purpose            = PM_PURP_BANKPAYMENT;
  Paym.NumberPack         = 995;
  Paym.PayerBankEnterDate = PayDate;
  Paym.ToBackOffice       = "";

  Paym.Date               = PayDate;
  Paym.PayDate            = PayDate;
  Paym.ClientDate         = PayDate;
  Paym.PaymentKind        = "Э";
  Paym.ComissCharges      = 0;
  
  if( Paym.DocKind == DLDOC_BANKCLAIM )
    Paym.ShifrOper        = "02";
  else
    Paym.ShifrOper        = "01";
  end;

  return 0;
end;

//-----------------------------------------------------------------------------
// Оплата платежом
//-----------------------------------------------------------------------------
macro Prc_FormBankPayment(PayerFIID, ReceiverFIID, PayerAccount, ReceiverAccount, PayerDep, ReceiverDep,
                          PayDate, PaySum, FIID, PaymStatus, Ground, CntSchedID, CntOprDocID, PayerSettAcc, ReceiverSettAcc, flagPostDoc)
                         

   var Payment     : RsbPayment;
   var BankPaym    : object; //RsbPSPayOrder
   var curBankPaym : object; //RsbCpOrder
   var Number      : string;
   var RefID;
   var cmdRec, rsRec, cmdPay, rsPay;
   var PayerBankPartyID, PayerBankName, PayerBankCode;
   var ReceiverBankPartyID, ReceiverBankName, ReceiverBankCode;

   var PmAmount = PaySum;

   if( (FIID == NATCUR) AND (PayerFIID == NATCUR) AND (ReceiverFIID == NATCUR) )
      BankPaym = RsbBankPayment(0);//BankPaym = GenObject( "RsbPSPayOrder", 0 ); 
      Payment = BankPaym.Payment;

      BankPaym.Origin     = MEMORDER_FDOC_PERCENT;
      BankPaym.Oper       = {oper}; 
      BankPaym.Status     = 1 /*MEMORDER_STATUS_POST*/;
      BankPaym.LaunchOper  = not(flagPostDoc);

      Payment.DocKind     = DLDOC_BANKPAYMENT;
      Payment.Purpose     = PM_PURP_BANKPAYMENT;     
   else
      curBankPaym = GenObject( "RsbBbCpOrder", 0 );//curBankPaym = GenObject( "RsbPSPayOrder", 0 );
      Payment = curBankPaym.Payment;

      curbankPaym.Origin       = CP_OR_PERCENT;
      curbankPaym.Oper         = {oper}; 
      curbankPaym.CurrentState = 0 /*CP_ST_DEFERRED*/;
      curbankPaym.LaunchOper  = not(flagPostDoc);

      Payment.PayerFIID   = PayerFIID; 
      Payment.DocKind     = BBANK_CPORDER;
      Payment.Purpose     = PM_PURP_BANKPAYMENT;    
   end;

   if (ReceiverDep == 0)
      ReceiverDep = PayerDep;
   end;
   
   Payment.Department      = PayerDep;
   Payment.StartDepartment = PayerDep;
   Payment.EndDepartment   = ReceiverDep;   
      
    
   if( Prc_FillPayment( Payment, PayerFIID, ReceiverFIID,  PayDate, PaySum,  FIID, flagPostDoc)) 
      return 1; 
   end;
   if( Prc_FillBankPayment( Payment, PayDate ))
      return 1; 
   end;   

   if (ValType(PayerSettAcc) == V_UNDEF)

      cmdPay = RSDCommand ("select party.T_PARTYID, party.T_NAME, code.T_CODE " +
                        "from DPARTY_DBT party, DDP_DEP_DBT dep, DPARTCODE_DBT code " + 
                        "where dep.T_CODE = ? and dep.T_PARTYID = party.T_PARTYID " +
                        "and code.T_PARTYID = dep.T_PARTYID and code.T_CODEKIND=1");
      cmdPay.addParam( "", RSDBP_IN, PayerDep );
      cmdPay.execute();
   
      rsPay = TRsbDataSet(cmdPay);
      if (rsPay.movenext())
         PayerBankPartyID = rsPay.partyid;  
         PayerBankName    = rsPay.name;
         PayerBankCode    = rsPay.code;
      end;
      
      Payment.SetPayerPI(  PAYMENTS_GROUP_UNDEF,
                           PayerBankPartyID,
                           1,
                           PayerBankCode,
                           PayerBankName, 
                           "",
                           PayerFIID,
                           1 /*CHAPT1*/,
                           PayerAccount);   
   else // using SPI   
      Payment.SetPayerPI(  PAYMENTS_GROUP_UNDEF,
                           PayerSettAcc.BankID,
                           PayerSettAcc.BankCodeKind,
                           PayerSettAcc.BankCode,
                           "", 
                           PayerSettAcc.CorrAcc,
                           PayerSettAcc.FIID,
                           PayerSettAcc.Chapter,
                           PayerSettAcc.Account,
                           PayerSettAcc.PartyID,
                           PayerSettAcc.RecName,
                           PayerSettAcc.INN,
                           PayerSettAcc.CodeKind,
                           PayerSettAcc.Code                           
                           );      
   end;

   if (ValType(ReceiverSettAcc) == V_UNDEF)

      cmdRec = RSDCommand ("select party.T_PARTYID, party.T_NAME, code.T_CODE " +
                           "from DPARTY_DBT party, DDP_DEP_DBT dep, DPARTCODE_DBT code " + 
                           "where dep.T_CODE = ? and dep.T_PARTYID = party.T_PARTYID " +
                           "and code.T_PARTYID = dep.T_PARTYID and code.T_CODEKIND=1");
      cmdRec.addParam( "", RSDBP_IN, ReceiverDep );
      cmdRec.execute();
      
      rsRec = TRsbDataSet(cmdRec);
      if (rsRec.movenext())
         ReceiverBankPartyID = rsRec.partyid;  
         ReceiverBankName    = rsRec.name;
         ReceiverBankCode    = rsRec.code;
      end;
      
      Payment.SetReceiverPI(  PAYMENTS_GROUP_UNDEF,
                              ReceiverBankPartyID,
                              1,
                              ReceiverBankCode,
                              ReceiverBankName, 
                              "",
                              ReceiverFIID,
                              1 /*CHAPT1*/,
                              ReceiverAccount);
   else  // using SPI   
      Payment.SetReceiverPI(  PAYMENTS_GROUP_UNDEF,
                              ReceiverSettAcc.BankID,
                              ReceiverSettAcc.BankCodeKind,
                              ReceiverSettAcc.BankCode,
                              "",
                              ReceiverSettAcc.CorrAcc,
                              ReceiverSettAcc.FIID,
                              ReceiverSettAcc.Chapter,
                              ReceiverSettAcc.Account,
                              ReceiverSettAcc.PartyID,
                              ReceiverSettAcc.RecName,
                              ReceiverSettAcc.INN,
                              ReceiverSettAcc.CodeKind,
                              ReceiverSettAcc.Code                           
                              );      
   
   end;

   Payment.Ground = Ground;

   if (GetReferenceIDByType( 
                              OBJTYPE_PSPAYORD, /*Рублевый платежный документ*/
                              REFOBJ_SVORDER,   /*ObjectType = 500; номер требования банка к клиентам (РКО)*/
                              RefID) != 0 )

      MsgBox( "Ошибка при генерации номера требования" );
      return 1;
   end;

   if (GenerateReference( RefID, Number ) != 0)
      MsgBox( "Ошибка при генерации номера требования" );
      return 1;
   else
      Payment.Number = Number;
   end;

   PrcSetQuitLink(CntSchedID, Payment, CntOprDocID);

   return 0;   
end;

//-----------------------------------------------------------------------------
// Создать банковский ордер
//-----------------------------------------------------------------------------
MACRO Prc_CreateBankOrder (PayerFIID, ReceiverFIID, PayerAccount, ReceiverAccount, 
                           DocDate, DocSum, FIID,Ground, CntSchedID, CntOprDocID,PayerSettAcc, ReceiverSettAcc, flagPostDoc)
   
   var BankOrder:object;
   var RefID, Number;

   BankOrder = GenObject( "RsbBankOrder", 0 );
      
   BankOrder.Oper    = {oper};
   BankOrder.PrimDocOrigin = PD_OR_PERCENT;
   BankOrder.LaunchOper = not(flagPostDoc);    
   BankOrder.DocKind = DLDOC_BANKORDER;


   Prc_FillPayment(BankOrder, PayerFIID, ReceiverFIID,  DocDate, DocSum,  FIID, flagPostDoc);


   if (ValType(PayerSettAcc) == V_UNDEF)
      BankOrder.SetPayerAccount (PayerFIID, 
                                 1, //Chapter 
                                 PayerAccount);   
   else // using SPI   
      BankOrder.SetPayerPI(  PAYMENTS_GROUP_UNDEF,
                           PayerSettAcc.BankID,
                           PayerSettAcc.BankCodeKind,
                           PayerSettAcc.BankCode,
                           "", 
                           PayerSettAcc.CorrAcc,
                           PayerSettAcc.FIID,
                           PayerSettAcc.Chapter,
                           PayerSettAcc.Account,
                           PayerSettAcc.PartyID,
                           PayerSettAcc.RecName,
                           PayerSettAcc.INN,
                           PayerSettAcc.CodeKind,
                           PayerSettAcc.Code                          
                           );      
   end;

   if (ValType(ReceiverSettAcc) == V_UNDEF)
      BankOrder.SetReceiverAccount (ReceiverFIID, 
                                    1, //Chapter 
                                    ReceiverAccount);
   else  // using SPI   
      BankOrder.SetReceiverPI(  PAYMENTS_GROUP_UNDEF,
                              ReceiverSettAcc.BankID,
                              ReceiverSettAcc.BankCodeKind,
                              ReceiverSettAcc.BankCode,
                              "", 
                              ReceiverSettAcc.CorrAcc,
                              ReceiverSettAcc.FIID,
                              ReceiverSettAcc.Chapter,
                              ReceiverSettAcc.Account,
                              ReceiverSettAcc.PartyID,
                              ReceiverSettAcc.RecName,
                              ReceiverSettAcc.INN,
                              ReceiverSettAcc.CodeKind,
                              ReceiverSettAcc.Code                          
                              );      
   
   end;
   
                   
   BankOrder.Ground = Ground;

   if (GetReferenceIDByType( 
                              OBJTYPE_PSPAYORD, //Рублевый платежный документ
                              REFOBJ_SVORDER,   //ObjectType = 500; номер требования банка к клиентам (РКО)
                              RefID) != 0 )

      MsgBox( "Ошибка при генерации номера требования" );
      return 1;
   end;
  
   if( GenerateReference( RefID, Number ) != 0 )
      MsgBox( "Ошибка при генерации номера " );
      return 1;
   else
      BankOrder.Number = Number;
   end;

   PrcSetQuitLink(CntSchedID, BankOrder, CntOprDocID);

END;
  
