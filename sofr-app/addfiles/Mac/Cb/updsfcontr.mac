import BankInter, PTInter, PSInter;

record PrdClient ( "prdclient.dbt" );
record OldPrdClient ( "prdclient.dbt" );

// Все функции должны возвращать 
// 0 - в случае успеха 
// Или строку с ошибкой 
// Или номер ошибки 

// Функция вызывается при создании ДО 
macro CreateSfContr(SfContr)
  var err = 0;
  var ErrMsg = "";
  record SfContrB( "sfcontr.dbt" );
  SetBuff( SfContrB, SfContr );


  if(SfContrB.ServKind == PTSK_DEPOS)
    err = ExecMacroFile("dpproduct.mac", "AddDepoAcntObjPRDbyCode", PrdClient.ClientProductID, SfContrB.Object);
    return err;
  end;

  if(not err)
    // Создание объектов БП Расчетного Банка
    if( SfContrB.ServKind == PTSK_PAY )
      ErrMsg = ExecMacroFile( "rb_updsf.mac", "RB_CreateSfContr", SfContrB, PrdClient );
      if( ErrMsg != "" )
        return ErrMsg;
      end;
    end;
  end;
  return err;
end;



// Функция вызывается при обновлении ДО 
macro UpdateSfContr(SfContr, OldSfContr)
  var err = 0;
  var ErrMsg = "";
  record SfContrB( "sfcontr.dbt" );
  record SfContrO( "sfcontr.dbt" );
  SetBuff( SfContrB, SfContr );
  SetBuff( SfContrO, OldSfContr );
  var ZeroDate = date(0,0,0);
  var preacc = null;

 
  if(PrdClient.ProductID != OldPrdClient.ProductID)
    // При смене банковского продукта нужно создать новые объекты БП.
    if(SfContrB.ServKind == PTSK_DEPOS)
      err = ExecMacroFile("dpproduct.mac", "AddDepoAcntObjPRDbyCode", PrdClient.ClientProductID, SfContrB.Object);
      return err;
    end;
  else
    if(SfContrB.ServKind == PTSK_DEPOS)
      if(SfContrB.Object != SfContrO.Object)
        err = ExecMacroFile("dpproduct.mac", "ChangeSfDepoAcntObjPRDbyCode", SfContrO.ID, PrdClient.ClientProductID, SfContrB.Object, SfContrO.Object);
        return err;
      end;
    end;
  end;

  if(not err)
    // Обновление объектов БП Расчетного Банка
    if( SfContrB.ServKind == PTSK_PAY )
      ErrMsg = ExecMacroFile( "rb_updsf.mac", "RB_UpdateSfContr", SfContrB, SfContrO, PrdClient, OldPrdClient );
      if( ErrMsg != "" )
        return ErrMsg;
      end;
    end;
  end;

  if(not err)
    // При закрытии ДО, то установить ЗДА автоматически статус "Завершен".
    if((SfContrB.DateClose != SfContrO.DateClose) AND (SfContrB.DateClose != ZeroDate) AND (SfContrB.PreAcptID > 0)) 
      preacc = GenObject( "RsbPreAccept", SfContrB.PreAcptID );  
      preacc.State = 2; //PA_ST_CLOSE;
      err = preacc.SaveChanges();
    end;
  end;

  if(not err)
    if((SfContrB.DateBegin != SfContrO.DateBegin) OR (SfContrB.DateClose != SfContrO.DateClose) )
      preacc = GenObject( "RsbPreAccept", SfContrB.PreAcptID );  
      preacc.DateFrom = SfContrB.DateBegin;
      preacc.DateTo   = SfContrB.DateClose;
      err = preacc.SaveChanges();
    end;
  end;

  return err;
end; 



// Функция вызывается перед удалением ДО 
macro DeleteSfContr(SfContr)
  record SfContrB( "sfcontr.dbt" );
  SetBuff( SfContrB, SfContr );
  var ErrMsg = "";

  // Удаление объектов БП Расчетного Банка
  if( SfContrB.ServKind == PTSK_PAY )
    ErrMsg = ExecMacroFile( "rb_updsf.mac", "RB_DeleteSfContr", SfContrB );
    if( ErrMsg != "" )
      return ErrMsg;
    end;
  end;

  return 0;
end; 