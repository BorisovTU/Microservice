/*
$Name:             fi.mac
$Module:           Ценные бумаги 
$Description:      Для справочника ФИ
*/

IMPORT FIInter, BankInter, PTInter, RsbDataSet, OprInter, PaymInter, "globals.mac", SPInter;
import CTInter;
import "loadRUData.mac";
import "nptxсirchist_scroll.mac";

record Курс( ratedef );
record СтарыйКурс( ratedef );

record ФинИнстр             ( fininstr );
record СтарыйФинИнстр       ( fininstr );
record ЦеннаяБумага         ( avoiriss );
record СтараяЦеннаяБумага   ( avoiriss );
record ЦеннаяБумагаПай      ( avrinvst );
record СтараяЦеннаяБумагаПай( avrinvst );

PRIVATE CONST FI_DERIVATIVE_FUTURES  = 1, /*Фьючерс*/
              FI_DERIVATIVE_OPTION   = 2, /*Опцион*/
              FI_DERIVATIVE_FORWARD  = 3, /*Форвард*/
              FI_DVSTYLE_AMERICAN     = 1, /* Опцион американский */
              FI_DVSTYLE_EUROPE       = 2, /* Опцион европейский */
              FI_DVTYPE_PUT          = 1, /* Опцион put*/
              FI_DVTYPE_CALL         = 2; /* Опцион call*/              

PRIVATE CONST BEFORE_INSERT = 0, /*Режим - перед вводом*/
              BEFORE_EDIT   = 1; /*Режим - перед редактированием*/

PRIVATE CONST FIPREVIEW   = 0,   /* Только просмотр */
              FIPEDIT     = 1;   /* Редактирование  */

PRIVATE CONST OBJTYPE_FININSTR  = 9; /* Финансовый инструмент */
PRIVATE CONST REG_PARTYKIND_REGISTRATOR = 15; /*Регистрирующий субъект для ФИ*/
PRIVATE CONST REG_DOCKIND_CONTR = 1; /* Код ФИ в кодировке контрагента  */
PRIVATE CONST REG_DOCKIND_FOREX = 2; /* Код у контраг. для срочн. контр */

PRIVATE const RTS_CODE           = "РТС";
PRIVATE const MAX_MARKETCODE_LEN = 35; /*Максимальная длина биржевого кода (OBJCODE.Code)*/
PRIVATE const MAX_FICODE_LEN     = 15; /*Максимальная длина кода ФИ (FININSTR.FI_Code)*/

private var GroupMode = false; // групповая процедура

macro ПроверитьДоступДляДПОФР(rate_id) /*BIQ-8979*/
  var sel, DSet, dostup, role;
  sel="select * from dobjatcor_dbt t, dratedef_dbt r where  t.t_objecttype = 2 ";/*валюта*/ 
                                   sel = sel + " and   t.t_object = r.t_otherfi ";  /*fiid*/ 
                                   sel = sel + " and   t.t_groupid = 102 and t.t_attrid=1 ";/*категория ДоступДляДПОФР и установленное значение ДА*/
                                   sel = sel + " and   r.t_rateid = ? ";
  Sel = RSDCommand(sel);
  Sel.AddParam("", RSDBP_IN, rate_id);
  Sel.Execute();
  DSet = TRsbDataSet(Sel);
  if( DSet.moveNext()) 
     dostup = true;
  else    
     dostup = false;
  end;    

  sel="select * from dacsoprole_dbt t where t_oper =? and t_roleid = 10035 " ; /*10035 = [35] Курсы валют для ДПОФР*/
  Sel = RSDCommand(sel);
  Sel.AddParam("", RSDBP_IN, {oper});
  Sel.Execute();
  DSet = TRsbDataSet(Sel);
  if( DSet.moveNext())                                 
    role = true;
  else    
    role = false;
  end;  

  if ((role == true) and (dostup == true))   /*у пользователь роль [35] Курсы валют для ДПОФР* и у валюты есть признак спецдоступа */
     return true;
  elif ((role == true) and (dostup == false)) /*у пользователь роль [35] Курсы валют для ДПОФР* и у валюты НЕТ признака спецдоступа */
     return false;
  elif (role == false) /*у пользователь НЕТ роли [35] Курсы валют для ДПОФР*/
     return true;
  end;
end;

/* Установить групповой режим */
macro SetGroupModeFI(mode:bool)
  GroupMode = mode;
end;

macro ПроверитьКурс(Режим, Вид)
record fin             ( fininstr );
  if (Вид == 1)       /*корректировка параметров курса*/
    if (Режим == 1)   /*Удаление*/
     if (not ПроверитьДоступДляДПОФР (Курс.rateid))
      MsgBox("Удаление курса запрещено.");
      return 1;
     end;
    end;
    if (Режим == 2)   /*Ввод*/
      if (not ПроверитьДоступДляДПОФР (Курс.rateid))
        MsgBox("Сохранение курса запрещено.");
        return 1;
      end;
       ПолучитьФинИН(Курс.otherfi, fin);
       if( FI_IsAvrKindBond( fin.AvoirKind ))
         If((Курс.isrelative != "X") and (Курс.type != 12 ) and (Курс.type != 15 ))
             MsgBox("Для облигаций курс должен быть в процентах");
             return 1;
         end;
       end;
    end;
    if (Режим == 3)   /*Редактирование*/
     if (not ПроверитьДоступДляДПОФР (Курс.rateid))
      MsgBox("Сохранение курса запрещено.");
      return 1;
     end;
    end;
  elif (Вид == 2)     /*корректировка значения курса*/
/*GAA: Def-14240*/
      if ((Курс.rate == 0)/* or (Курс.rate < 0)*/) /*PNV 535932 по кредитным индексам возможен отрицательный курс*/
             MsgBox("Введите правильно курс");
             return 1;
     end;/**/
     if (not ПроверитьДоступДляДПОФР (Курс.rateid))
      MsgBox("Сохранение курса запрещено.");
      return 1;
     end;
  end;
  return 0;
end;

macro ПроверитьСтарыйКурс(Режим)

  if (Режим == 1)   /*Удаление*/
    if (not ПроверитьДоступДляДПОФР (Курс.rateid))
      MsgBox("Удаление курса запрещено.");
      return 1;
    end;
  end;
  if (Режим == 2)   /*Ввод*/
      msgbox("новый курс");
  end;
  if (Режим == 3)   /*Редактирование*/
     if (not ПроверитьДоступДляДПОФР (Курс.rateid))
      MsgBox("Сохранение курса запрещено.");
      return 1;
     end;
  end;
  return 0;
end;

/*Для инициализации заполняется структура "ФинИнстр" */
private macro    НовыйФинансовыйИнструмент()
  return 0;
end;

/*Возможность для пользователя разрешать/не разрешать редактировать панель фин. инструмента.
  Если возвращает 0, - редактирование панели разрешено. Иначе - нет.
  Режимы вызова: перед вводом, перед редактированием.
*/
private macro    ВызватьПанельФинансовогоИнструмента( Режим_проверки, _Режим_доступа )
   var stat = 0, Режим_доступа = -1;

/* Режимы проверки:
 BEFORE_INSERT = 0 - перед вводом
 BEFORE_EDIT   = 1 - перед редактированием

   Режимы доступа:
 FIPREVIEW   = 0,   // Только просмотр
 FIPEDIT     = 1,   // Редактирование
 (по умолчанию = -1)
*/

  SetParm(1, Режим_доступа);
  return stat;
end;

PRIVATE MACRO GetObjCode( FIID:INTEGER, ObjType:INTEGER, RegPartyKind:INTEGER, RegDocKind:INTEGER, PartyID:INTEGER )
   var Code = "", ObjCode,
       ObjAlReg = TBFile("objalreg.dbt", "R", 1 );

   ObjAlReg.Clear();
   ObjAlReg.rec.ObjectType   = ObjType;
   ObjAlReg.rec.RegPartyKind = RegPartyKind;
   ObjAlReg.rec.RegDocKind   = RegDocKind;
   ObjAlReg.rec.PartyID      = PartyID;

   if( ObjAlReg.GetEQ() )
      ObjCode = TBFile("objcode.dbt", "R", 0 );
      ObjCode.Clear();
      ObjCode.rec.ObjectType = ObjType;
      ObjCode.rec.CodeKind   = ObjAlReg.rec.CodeKind;
      ObjCode.rec.ObjectID   = FIID;
      if( ObjCode.GetEQ() )
         Code = ObjCode.rec.Code;
      end;
   end;

   return Code;
END;

// Есть ли лоты в БОЦБ или ДУ по FIID
macro SP_IsAnyWrtSum( FIID, IsTrust:bool )
  var query, DataSet;
  var Select;
  var AddWhere:string = "";

  if( IsTrust )
     AddWhere = " AND t_Trust = 'X' "; 
  else
     AddWhere = " AND t_Trust != 'X' "; 
  end;

  query = " select count(1) NumDeal FROM ( " + 
          " select 1 "
          "   from dpmwrtsum_dbt " +
          "  where t_FIID = ? " +
          AddWhere +
          " UNION ALL " +
          " select 1 " + 
          "   FROM dpmwrtcl_dbt " + 
          "  WHERE t_FIID = ? )";

  Select = RSDCommand(query);

  Select.AddParam("", RSDBP_IN, FIID);
  Select.AddParam("", RSDBP_IN, FIID);

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  if( (DataSet.moveNext()) and (DataSet.NumDeal > 0) )
     return true;
  end;

  return false;
end;

private macro SP_AvoirissExistWrtSum( FIID, IsTrust:bool )
  return (WRTGetPortfolioAmount(-1, FIID, 0, -1, -1, -1, {curdate}, true, true, true, IsTrust) > 0);
end;

// Есть ли отложенные сделки в БОЦБ или ДУ по FIID
macro SP_IsAnyDelayedDeal( FIID, IsTrust:bool )
  var query, DataSet;
  var Select;

  query = " SELECT count(1) NumDeal " +
          "   FROM ddl_leg_dbt leg, ddl_tick_dbt tick " +
          "  WHERE leg.t_LegKind = 0 " +
          "    AND leg.t_PFI     = ? /*1*/ " +
          "    AND leg.t_DealID  = tick.t_DealID " +
          "    AND tick.t_DealStatus = 0 ";

  if(IsTrust)
     query = query + " AND tick.t_BofficeKind in(?,?,?) "; 
  else
     query = query + " AND tick.t_BofficeKind not in(?,?,?) "; 
  end;

  Select = RSDCommand( query );

  Select.AddParam("", RSDBP_IN, FIID );  /*1*/
  Select.AddParam("", RSDBP_IN, DL_TRUSTDOC );  /*1*/
  Select.AddParam("", RSDBP_IN, DL_TRUSTRETIREMENT );  /*1*/
  Select.AddParam("", RSDBP_IN, DL_TRUSTAVRWRT );  /*1*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if( (DataSet.moveNext()) and (DataSet.NumDeal > 0) )
    return TRUE;
  else
    return FALSE;
  end;
end;

// по выпуску в БО Ц/Б есть сделки в БОЦБ или ДУ на внебалансе (т.е. открытые и по которым не выполнены ни поставка, ни аванс/оплата) 
macro SP_IsAnyReadyDeal( FIID, IsTrust )
  var Select, Query, DataSet;

  Query = " Select count(1) NumDeal "+
          "   From ddl_tick_dbt deal,  ddl_leg_dbt leg, dpmpaym_dbt paym " + 
          "  Where leg.t_PFI = ? " +
          "    and leg.t_LegKind = 0 " + 
          "    and leg.t_DealID = deal.t_DealID " + 
          "    and paym.t_Purpose IN (1,2,42) " + 
          "    and paym.t_DocumentID = deal.t_DealID " + 
          "    and paym.t_PaymStatus not in (150, 32000) " + 
          "    and deal.t_DealStatus = 10 "/*DL_READIED*/;

  if(IsTrust)
     query = query + " AND deal.t_BofficeKind in(?,?,?) "; 
  else
     query = query + " AND deal.t_BofficeKind not in(?,?,?) "; 
  end;
          
  Select = RSDCommand(query);

  Select.addParam( "", RSDBP_IN, FIID );
  Select.AddParam("", RSDBP_IN, DL_TRUSTDOC );  /*1*/
  Select.AddParam("", RSDBP_IN, DL_TRUSTRETIREMENT );  /*1*/
  Select.AddParam("", RSDBP_IN, DL_TRUSTAVRWRT );  /*1*/

  Select.execute();
  DataSet = TRsbDataSet(Select);

  if (DataSet.MoveNext() AND (DataSet.NumDeal > 0) )
    return TRUE;
  else
    return FALSE;
  end;
end;

macro SP_IsExistAvoirSrv( FIID )
  var query, DataSet;
  var Select;

  query = " SELECT t_FIID " + 
          "   FROM davoirsrv_dbt " +
          "  WHERE t_FIID = ? " +
          "    AND t_ServiceState <> 0 ";

  Select = RSDCommand( query );

  Select.AddParam("FIID", RSDBP_IN, FIID);

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    return TRUE;
  else
    return FALSE;
  end;
end;

private macro SP_GetMinDrawingDate( FIID ):date
  var RetVal:date = date(0,0,0);
  var Query;
  var select = RSDCommand(" select NVL(min(t_DrawingDate),'01.01.0001') DrawingDate " +
                          "   from dfiwarnts_dbt " +
                          "  where t_FIID = ? " + 
                          "    and t_IsPartial = 'X' "
                          "    and t_IsClosed = 'X' "
                         );

  select.addParam("", RSDBP_IN, FIID);
  select.execute();

  Query = TRsbDataSet(select);
  if( Query.MoveNext() )
     RetVal = Query.DrawingDate;
  end;

  return RetVal;
end;

macro ПроверитьФинансовыйИнструмент( Режим )
/* Режимы проверки:
 OBJ_DELETE - удаление финансового инструмента;
 OBJ_INSERT - ввод финансового инструмента;
 OBJ_UPDATE - корректировка финансового инструмента;
*/
  var stat = 0, error;
  if( ФинИнстр.FI_KIND == FIKIND_AVOIRISS ) // Ценная бумага
     if( FI_IsAvrKindBond( ФинИнстр.AvoirKind ) AND (ЦеннаяБумага.IndexNom != "X") AND
         ( ((Режим == OBJ_INSERT) AND (ФинИнстр.FaceValue == 0)) OR
           ((Режим == OBJ_UPDATE) AND (ФинИнстр.FaceValue != СтарыйФинИнстр.FaceValue) AND (ФинИнстр.FaceValue == 0))
         )
       )
        MsgBox("Не задан номинал ц/б");
        return 1;
     end;

     if( ( GroupMode == false ) AND  // Для группового режима не спрашиваем (например, при импорте справочников НРД)
         ( FI_IsAvrKindInvestmentShare(ФинИнстр.AvoirKind) ) AND
         ( (Режим == OBJ_INSERT) OR (Режим == OBJ_UPDATE) ) AND
         ( ФинИнстр.SumPrecision > AVOIRISS_SUM_PRECISION /* 6 */)
       )
        if( MsgBoxEx( "Учет дробных частей ценных бумаг осуществляется депозитарием в десятичных дробях с 6 знаками после запятой.|Сохранить?", MB_YES+MB_NO, IND_NO, "Предупреждение", "") != IND_YES )
           return 1;
        end;
     end;
  end;

  /* проверки при изменении номинала */
  if( (Режим == OBJ_UPDATE) AND
      (ФинИнстр.FI_KIND == FIKIND_AVOIRISS) AND FI_IsAvrKindBond(ФинИнстр.AvoirKind) AND /*облигация*/
      (ФинИнстр.FaceValue != СтарыйФинИнстр.FaceValue) AND /*изменение номинала*/
      (ЦеннаяБумага.IndexNom != "X")
    )
     if( SP_AvoirissExistWrtSum(ФинИнстр.FIID, false) )
        MsgBox("По данному выпуску существуют остатки в БО ЦБ, номинал не может быть изменен");
        return 1;
     end;

     if( SP_AvoirissExistWrtSum(ФинИнстр.FIID, true) )
        MsgBox("Номинал ц/б в анкете не может быть изменен, так как выпуск учитывается в ДУ");
        return 1;
     end;

     if( SP_IsExistAvoirSrv(ФинИнстр.FIID) )
        MsgBox("Номинал ц/б в анкете не может быть изменен, так как выпуск учитывается в Депозитарии");
        return 1;
     end;

     var MinDrawingDate:date = SP_GetMinDrawingDate(ФинИнстр.FIID);
     if( (MinDrawingDate != date(0,0,0)) AND (MinDrawingDate <= {curdate}) )
        MsgBox("По данному выпуску ц/б были частичные погашения, номинал не может быть изменен");
        return 1;
     end;
  end;

  /*нельзя менять ВАЛЮТУ НОМИНАЛА ц\б, если есть сделки с этой ц\б или в БОЦБ или в ДУ или ц\б на обслуживании в ДЕПОЗИТАРИИ*/
  if( (ФинИнстр.FI_KIND == FIKIND_AVOIRISS) AND (FI_IsAvrKindNotEmissive(ФинИнстр.AvoirKind) == false) AND
      (Режим == OBJ_UPDATE) AND (ФинИнстр.FaceValueFI != СтарыйФинИнстр.FaceValueFI)
    )
     if(SP_IsAnyWrtSum( ФинИнстр.FIID, false ))
        MsgBox("Данный выпуск учитывается в БО ЦБ,|валюта номинала не может быть изменена.");
        return 1;
     end;

     /*если лотов нет - проверим, есть ли в БОЦБ отложенные сделки с этим выпуском*/
     if(SP_IsAnyDelayedDeal( ФинИнстр.FIID, false ))
        MsgBox("С данным выпуском в БО ЦБ есть введенные сделки,|по которым еще не выполнялись учетные действия.|Данные сделки необходимо удалить и ввести новые после изменения валюты номинала ц/б.");
        return 1;
     end;

     /*если нет отложенных, проверим, есть ли в БОЦБ открытые на внебалансе*/
     if( SP_IsAnyReadyDeal( ФинИнстр.FIID ) )
        msgbox( "С данным выпуском в БО ЦБ есть сделки на внебалансе.|Данные сделки необходимо откатить и ввести новые после изменения валюты номинала ц/б." );
        return 1;
     end;

     if(SP_IsAnyWrtSum( ФинИнстр.FIID, true ))
        MsgBox( "Данный выпуск учитывается в ДУ,|валюта номинала не может быть изменена." );   
        return 1;
     end;

     /*если лотов нет - проверим, есть ли в ДУ отложенные сделки с этим выпуском*/
     if(SP_IsAnyDelayedDeal( ФинИнстр.FIID, true ))
        MsgBox("С данным выпуском есть введенные сделки ДУ,|по которым еще не выполнялись учетные действия.|Данные сделки необходимо удалить и ввести новые после изменения валюты номинала ц/б.");
        return 1;
     end;

     /*если нет отложенных, проверим, есть ли в ДУ открытые на внебалансе*/
     if( SP_IsAnyReadyDeal( ФинИнстр.FIID, true ) )
        msgbox( "С данным выпуском есть срочные сделки ДУ на внебалансе.|Данные сделки необходимо откатить и ввести новые после изменения валюты номинала ц/б." );
        return 1;
     end;

     if( not FI_AvrKindsEQ( FIKIND_AVOIRISS, ФинИнстр.AvoirKind, AVOIRISSKIND_INVESTMENT_SHARE   ) AND // не инвестиционный пай 
         not FI_AvrKindsEQ( FIKIND_AVOIRISS, ФинИнстр.AvoirKind, AVOIRISSKIND_DEPOSITORY_RECEIPT ) AND // не депозитарная расписка
         SP_IsExistAvoirSrv(ФинИнстр.FIID) )
        MsgBox( "Валюта номинала ц/б не может быть изменена, так как выпуск находится на обслуживании в депозитарии." );   
        return 1;
     end;
  end;

  return 0;
end;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string):string

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dfininstr_dbt_idx0)*/";

  return DefaultHint;

END;


private macro    ФункцияПользователя(Режим)

   PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
      ar.value (ind * 6)     = fld;
      ar.value (ind * 6 + 1) = head;
      ar.value (ind * 6 + 2) = width;
      ar.value (ind * 6 + 3 ) = rdonly;   // fldType
      ar.value (ind * 6 + 4 ) = dp;//   decPoint
      ar.value (ind * 6 + 5 ) = 0;   // reserv
   END;

   macro EvProc(rs, cmd, id, key)
     var  cmd1, rs1, col1, arnum, CM_DEFAULT = null;
     if ((cmd == DLG_KEY)and(key == 13/*K_ENTER*/))
       /* if (rs.RecCount > 0)
          return CM_SELECT;
        end;*/
        var m = TArray();
        m(m.Size) = "Купоны";
        m(m.Size) = "Биржевые коды";
        var choice = menu(m);
        if (choice == 0)
           cmd1 = RsdCommand(" select t.* from SOFR_BOND_COUPONS t where t.id_fintool =  :1 order by begin_period");
            cmd1.addParam(":1"  , RSDBP_IN, rs.Value("fintoolid"));
           rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
        if (RunScroll(rs1/*, arnum+1, col1, "RU DATA" ,null," Купоны "*/))
           end;
        elif  (choice == 2)
           col1 = TArray;   
           arnum = -1;
           AddCol (col1, arnum=arnum+1, "name",     "name",     10,  0,0);
           AddCol (col1, arnum=arnum+1, "shortname_rus",     "shortname_rus",     15,  0,0);
           AddCol (col1, arnum=arnum+1, "seccode",    "seccode",    12,  0);
           AddCol (col1, arnum=arnum+1, "cfi_name","cfi_name",16, 0);
           AddCol (col1, arnum=arnum+1, "exchange","exchange",16, 0);
           cmd1 = RsdCommand(" select t.* from SOFR_INFO_INSTRUMENTS t where t.isin =  :1 ");
               cmd1.addParam(":1"  , RSDBP_IN, rs.Value("isincode"));
           rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
           if (RunScroll(rs1, arnum+1, col1, "" ,null," Коды на бирже "))
           end;
        end;

     end;
     return CM_DEFAULT;
      //elif ((cmd == DLG_KEY)and(key == K_F3))
   end;

   macro EvProc1(rs, cmd, id, key)
     var  sql, cmd1, rs1,CM_DEFAULT = null;
     if ((cmd == DLG_KEY)and(key == 13/*K_ENTER*/))
        sql = 
               "SELECT t_changedate \"Date\", Rest, Rest - RestPrev Delta " +
               "  FROM (SELECT t_changeDate, Rest, LAG (rest, 1, 0) OVER (ORDER BY t_changedate) restprev " +
               "          FROM (  SELECT d.dat t_changedate, SUM (t_amount) Rest " +
               "                    FROM    v_scwrthistex v " +
               "                         LEFT JOIN " +
               "                            dparty_dbt p " +
               "                         ON v.t_party = p.t_partyid, " +
               "                         (  SELECT t_changedate dat " +
               "                              FROM v_scwrthistex " +
               "                             WHERE t_fiid = :1 AND t_party = :2 and t_portfolio = :3 and t_contract = :4" +
               "                          GROUP BY t_changedate) d " +
               "                   WHERE t_state IN (1, 3) AND t_amount > 0 " +
               "                         AND t_instance = " +
               "                                (SELECT MAX (t_instance) " +
               "                                   FROM v_scwrthistex " +
               "                                  WHERE v.t_sumid = t_sumid " +
               "                                        AND v.t_changedate = t_changedate) " +
               "                         AND t_changedate = " +
               "                                (SELECT MAX (t_changedate) " +
               "                                   FROM v_scwrthistex " +
               "                                  WHERE t_sumid = v.t_sumid " +
               "                                        AND t_changedate <= d.dat) " +
               "                         AND t_fiid = :5 " +
               "                         AND t_party = :6 " +
               "                and t_portfolio = :7 " +
               "                AND t_contract = :8 " +
               "                GROUP BY d.dat " +
               "                ORDER BY d.dat DESC)) " +
               " WHERE rest <> restprev order by t_changedate desc" ;

            cmd1 = RsdCommand(sql);
            cmd1.addParam(":1"  , RSDBP_IN, ФинИнстр.Fiid);
            cmd1.addParam(":2"  , RSDBP_IN, rs.Value("t_party"));
            cmd1.addParam(":3"  , RSDBP_IN, rs.Value("portf"));
            cmd1.addParam(":4"  , RSDBP_IN, rs.Value("t_contract"));
            cmd1.addParam(":5"  , RSDBP_IN, ФинИнстр.Fiid);
            cmd1.addParam(":6"  , RSDBP_IN, rs.Value("t_party"));
            cmd1.addParam(":7"  , RSDBP_IN, rs.Value("portf"));
            cmd1.addParam(":8"  , RSDBP_IN, rs.Value("t_contract"));
        rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
        if (RunScroll(rs1/*, arnum+1, col1, "Остатки по датам" ,null," Купоны "*/))
        end;
     end;
     return CM_DEFAULT;
      //elif ((cmd == DLG_KEY)and(key == K_F3))
   end;
/* Режимы:
 UFN_PANEL_INPUT - вызов функции пользователя из панели ввода;
 UFN_PANEL_EDIT  - вызов функции пользователя из панели редактирования;
 UFN_SCROL       - вызов функции пользователя из скроллинга;
*/
  var sql, cmd,rs, col1, arnum, d = {curdate};
  Var NkdDate;

  
  if (ФинИнстр.Fi_Kind == 2)
     var m = TArray();
     var OpenSubordAccMenuItem = -1;
     var ChangeEmitentMenuItem = -1;
     var BondIssueMenuItem = -1;

     m(m.Size) = "Посмотреть на выпуск в RU DATA";
     m(m.Size) = "Загрузить ИН из RuData";
     m(m.Size) = "Список лотов";
     m(m.Size) = "Счета по бумаге";
     m(m.Size) = "Счета по сделкам";
     m(m.Size) = "Сменить признак субординированной облигации";
     m(m.Size) = "Посчитать НКД на дату";
     m(m.Size) = "Выпуск в регистре КДУ";
     if ((ФинИнстр.Issuer == {OurBank}) and (ФинИнстр.Avoirkind == 42))
       BondIssueMenuItem = m.Size;
       m(BondIssueMenuItem) = "Операции с эмиссионным счетом";
     end;
     if ((ФинИнстр.Issuer == {OurBank}) and (ЦеннаяБумага.Subordinated == "X") and (ФинИнстр.Fiid > 0))
       m(m.Size) = "Открытие л/с по суборд.ОЭБ";
       OpenSubordAccMenuItem = m.Size-1;
     end;
     ChangeEmitentMenuItem = m.Size;
     m(ChangeEmitentMenuItem) = "Смена эмитента";
     
     var choice = menu(m);
     if (choice == 0)

        col1 = TArray;   
        arnum = -1;
        AddCol (col1, arnum=arnum+1, "fintooltype",     "fintooltype",   8 ,  0,0);
        AddCol (col1, arnum=arnum+1, "cfi",  "cfi",  6  ,  0,0);
        AddCol (col1, arnum=arnum+1, "isincode",  "isin",  8  ,  0,0);
        AddCol (col1, arnum=arnum+1, "basis",  "basis",  8  ,  0,0);
        AddCol (col1, arnum=arnum+1, "status",  "status",    8, 0);
        AddCol (col1, arnum=arnum+1, "regdate",  "regdate",    8, 0);
        AddCol (col1, arnum=arnum+1, "regdistdate",  "regdistdate",    8, 0);
        AddCol (col1, arnum=arnum+1, "begdistdate",  "begdistdate",    8, 0);
        AddCol (col1, arnum=arnum+1, "endmtydate",  "drawdate",    8, 0);
        AddCol (col1, arnum=arnum+1, "country","country",    6, 0);
        AddCol (col1, arnum=arnum+1, "nickname",      "nickname",    18, 0);
        AddCol (col1, arnum=arnum+1, "fullname",     "fullname",    15, 0);
        AddCol (col1, arnum=arnum+1, "regcode",   "regcode",    15, 0);
        AddCol (col1, arnum=arnum+1, "issueruid",  "issueruid",    10, 0);
        AddCol (col1, arnum=arnum+1, "issuerinn",  "issuerinn",    10, 0);
        AddCol (col1, arnum=arnum+1, "issuername",         "issuername",    50, 0);
        AddCol (col1, arnum=arnum+1, "facevalue",     "facevalue",    8, 0);
        AddCol (col1, arnum=arnum+1, "updatetime",    "updatetime",    10, 0);
        AddCol (col1, arnum=arnum+1, "fintoolid",    "fintoolid",    10, 0);


         cmd = RsdCommand(" select t.* from SOFR_INFO_FINTOOLREFERENCEDATA t where t.fintoolid =  "
                             "     ( select c.t_code from dobjcode_dbt c where c.t_objecttype = 9 and c.t_codekind = 104 " + 
                             "        and c.t_objectid = :1 and c.t_state = 0) or isincode = :2");
            cmd.addParam(":1"  , RSDBP_IN, ФинИнстр.Fiid);
            cmd.addParam(":2"  , RSDBP_IN, ЦеннаяБумага.isin);
         rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        if (RunScroll(rs, arnum+1, col1, "RU DATA" ,"EvProc","RU DATA", "      Enter - меню  "))
        end;
     elif (choice == 1)        
        sql = "declare  "
                + "  out_result_code   NUMBER; "               
                + "  out_result_text   VARCHAR2(4000); "  
                + "  begin" 
                + "  it_rudata.DateOptionsTableWrapper_Rq( p_fiid        => :fiid "
                + "                                      ,p_result_code  => out_result_code  "
                + "                                      ,p_result_text  => out_result_text ) ;"
                + "   ?:= out_result_code; " 
                + "   ?:= out_result_text; "  
                + " end;";  

         cmd = RSDCommand(sql);
         cmd.AddParam("fiid",              RSDBP_IN, ФинИнстр.Fiid);
         cmd.addParam("out_result_code",   RSDBP_OUT, V_INTEGER);
         cmd.addParam("out_result_text",   RSDBP_OUT, V_STRING, 1000);
         cmd.Execute(); 

         var errorStatus = cmd.value("out_result_code");
         var errorText   = cmd.value("out_result_text");
         

         if(errorStatus)
            msgbox(errorText);
         else
            msgbox("Успешно выгружено");
         end;
     elif (choice == 2)
        col1 = TArray;   
        arnum = -1;
        AddCol (col1, arnum=arnum+1, "Date",     "Date",     8,  0,0);
        AddCol (col1, arnum=arnum+1, "Rest",     "Rest",     15,  0,0);
        AddCol (col1, arnum=arnum+1, "Owner",    "Owner",    15,  0);
        AddCol (col1, arnum=arnum+1, "numb",    "number",    15,  0);
        AddCol (col1, arnum=arnum+1, "Portfolio","Portfolio",17, 0);
        AddCol (col1, arnum=arnum+1, "t_state","state",3, 0);

        sql = 
                   "  SELECT :1 \"Date\",                                     " 
                   " SUM (t_amount) " +
                   "   \"Rest\",  " +  
                   "         NVL (p.t_shortname, 'Наш банк') \"Owner\",                        " 
                    "        ( select t_number from dsfcontr_dbt where t_id = v.t_contract) numb, "
                   "         (SELECT t_name                                                       " +
                   "            FROM dllvalues_dbt                                                " 
                   "           WHERE t_list = 1100 AND t_element = v.t_portfolio)                 " 
                   "             \"Portfolio\", decode(t_state,3,'БПП') t_state, v.t_Portfolio portf, v.t_party, v.t_contract      " 
                   "    FROM v_scwrthistex v LEFT JOIN dparty_dbt p ON v.t_party = p.t_partyid    " +
                   "   WHERE t_state IN (1, 3, 20, 23) AND t_amount > 0                                   " 
                   "         AND t_instance =                                                     " 
                   "                (SELECT MAX (t_instance)                                      " 
                   "                   FROM v_scwrthistex                                         " +
                   "                  WHERE v.t_sumid = t_sumid AND v.t_changedate = t_changedate)" 
                   "         AND t_fiid = :2                                                      " 
                   "         AND v.t_changedate =                                                 " 
                   "                (SELECT MAX (t.t_changedate)                                    " +
                   "                   FROM v_scwrthistex t                                        " 
                   "                  WHERE v.t_sumid = t_sumid AND t.t_changedate <= :3)         " 
                   " GROUP BY v.t_portfolio, p.t_shortname, v.t_Portfolio, v.t_state, v.t_party, v.t_contract, v.t_fiid " 
                   " ORDER BY t_portfolio DESC, p.t_shortname                     "; 
//                msgbox(sql);
            cmd = RsdCommand(sql);
            GetDate(d,"Дата остатков");
//            cmd.addParam(":d"  , RSDBP_IN, d);
            cmd.addParam(":1"  , RSDBP_IN, d);
            cmd.addParam(":2"  , RSDBP_IN, ФинИнстр.Fiid);
            cmd.addParam(":3"  , RSDBP_IN, d);
            rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        while (RunScroll(rs, arnum+1, col1, "Остатки" ,"EvProc1"," Остатки","Enter - история остатков", true, 20,10,70,60))
        end;
     elif (choice == 3)
        col1 = TArray;   
        arnum = -1;
        AddCol (col1, arnum=arnum+1, "t_account","account",  18,  0,0);
        AddCol (col1, arnum=arnum+1, "Code",     "Code",     20,  0,0);
        AddCol (col1, arnum=arnum+1, "t_iscommon",     "iscommon",     1,  0,0);
        AddCol (col1, arnum=arnum+1, "t_dockind",     "dockind",     5,  0,0);
        AddCol (col1, arnum=arnum+1, "t_docid",     "docid",     5,  0,0);
        AddCol (col1, arnum=arnum+1, "Rest",     "Rest",     12,  0,0);
        AddCol (col1, arnum=arnum+1, "cur",     "cur",     3,  0,0);
        AddCol (col1, arnum=arnum+1, "restnat",  "restnat",   12,  0,0);
        AddCol (col1, arnum=arnum+1, "t_nameaccount",  "name",   30,  0,0);
        sql = 
                   "  SELECT rsb_account.restall (m.t_account,                                          " 
                   "                              m.t_chapter,                                          " 
                   "                              m.t_currency,                                         " +
                   "                              :d )                                                  " 
                   "            rest,                                                                   " +
                   "            (select max(t_code) from dmccateg_dbt where t_number = m.t_catnum) code," 
                   "            (select max(t_ccy) from dfininstr_dbt where t_fiid = m.t_currency) cur," 
                   "   RSB_FIINSTR.CONVSUM(rsb_account.restall (m.t_account,                                         "
                   "           m.t_chapter,                                                                          " +
                   "           m.t_currency,                                                                         "
                   "           :d1), m.t_currency, 0,:d2) restnat, " 
                   "           a.t_nameaccount,    "
                   "         m.*                                                                        " 
                   "    FROM dmcaccdoc_dbt m inner join daccount_dbt a on m.t_account = a.t_account and m.t_chapter = a.t_chapter " +
                   "   WHERE t_fiid = :f AND m.t_chapter NOT IN (22, 21, 5) /*and t_dockind <> 101*/        " 
                   " ORDER BY t_catnum                                                                  "; 
            cmd = RsdCommand(sql);
            GetDate(d,"Дата остатков");
            cmd.addParam(":d"  , RSDBP_IN, d);
            cmd.addParam(":d1"  , RSDBP_IN, d);
            cmd.addParam(":d2"  , RSDBP_IN, d);
            cmd.addParam(":f"  , RSDBP_IN, ФинИнстр.Fiid);
            rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        while (RunScroll(rs, arnum+1, col1, "Счета " + string(d:f) ,"EvProc2","Счета " + string(d:f),"Счета " + string(d:f), true, 20,10,100,60))
        end;
     elif (choice == 4)
        col1 = TArray;   
        arnum = -1;
        AddCol (col1, arnum=arnum+1, "t_account","account",  18,  0,0);
        AddCol (col1, arnum=arnum+1, "Code",     "Code",     12,  0,0);
        AddCol (col1, arnum=arnum+1, "t_iscommon",     "iscommon",     1,  0,0);
        AddCol (col1, arnum=arnum+1, "t_dockind",     "dockind",     5,  0,0);
        AddCol (col1, arnum=arnum+1, "t_docid",     "docid",     5,  0,0);
        AddCol (col1, arnum=arnum+1, "Rest",     "Rest",     12,  0,0);
        AddCol (col1, arnum=arnum+1, "cur",     "cur",     3,  0,0);
        AddCol (col1, arnum=arnum+1, "restnat",  "restnat",   12,  0,0);
        sql = 
                   "  SELECT rsb_account.restall (m.t_account,                                          " 
                   "                              m.t_chapter,                                          " 
                   "                              m.t_currency,                                         " +
                   "                              :d )                                                  " 
                   "            rest,                                                                   " +
                   "            (select max(t_code) from dmccateg_dbt where t_number = m.t_catnum) code," 
                   "            (select max(t_ccy) from dfininstr_dbt where t_fiid = m.t_currency) cur," 
                   "   RSB_FIINSTR.CONVSUM(rsb_account.restall (m.t_account,                                         "
                   "           m.t_chapter,                                                                          " +
                   "           m.t_currency,                                                                         "
                   "           :d1), m.t_currency, 0,:d2) restnat, " 
                   "         m.*                                                                        " 
                   "    FROM dmcaccdoc_dbt m                                                            " +
                   "   WHERE m.t_chapter NOT IN (22, 21, 5) and m.t_dockind in (101, 176)         " 
                   "   and (( (select distinct t_pfi from ddl_tick_dbt where t_dealid = m.t_docid ) = :f  ) or  "
                   "        ( (select distinct t_pfi from ddl_leg_dbt where t_id = m.t_docid  ) = :f1  ))         " 
                   " ORDER BY t_catnum                                                                  "; 
            cmd = RsdCommand(sql);
            GetDate(d,"Дата остатков");
            cmd.addParam(":d"  , RSDBP_IN, d);
            cmd.addParam(":d1"  , RSDBP_IN, d);
            cmd.addParam(":d2"  , RSDBP_IN, d);
            cmd.addParam(":f"  , RSDBP_IN, ФинИнстр.Fiid);
            cmd.addParam(":f1"  , RSDBP_IN, ФинИнстр.Fiid);
            rs = RsdRecordSet(cmd, rsdval_client, rsdval_static);
        while (RunScroll(rs, arnum+1, col1, "Счета " + string(d:f) ,"EvProc2","Счета " + string(d:f),"Счета " + string(d:f), true, 20,10,100,60))
        end;
     elif (choice == 5)
         cmd = RsdCommand ( " update davoiriss_dbt set T_SUBORDINATED = ? where t_fiid = ? ");

         if (ЦеннаяБумага.SUBORDINATED == StrFor(88))
            cmd.addParam(":d"  , RSDBP_IN, StrFor(0));
         else
            cmd.addParam(":d"  , RSDBP_IN, StrFor(88));
         end;
         cmd.addParam(":d1"  , RSDBP_IN, ЦеннаяБумага.fiid);
         cmd.execute();
/*         if (ЦеннаяБумага.SUBORDINATED == StrFor(88))
            ЦеннаяБумага.SUBORDINATED = StrFor(0);
         else
            ЦеннаяБумага.SUBORDINATED = StrFor(88);
         end;*/
     elif (choice == 6)
        If(GetDate(NkdDate, "Введите дату расчета НКД на 1 бумагу "))
           MsgBox("НКД = " + НКД(ЦеннаяБумага.fiid, 1, NkdDate));
        end;
     elif ((choice == 7))
       ExecMacroFile ("QRRegister.mac","exec_qr_register", ФинИнстр.Fiid);
     elif((OpenSubordAccMenuItem >= 0) and (OpenSubordAccMenuItem == choice))
       ExecMacroFile ("OwnSubordBondOpenAcc.mac","OpenAccountsForOwnSubordBond", ФинИнстр.Fiid);
     elif ((choice == BondIssueMenuItem) and (ЦеннаяБумага.EndPlacementDate  >= date(1,6,2019)))
       ExecMacroFile ("Bond_Issue.mac","exec_bond_issue", ФинИнстр.Fiid);
     elif ((choice == BondIssueMenuItem) and (ЦеннаяБумага.EndPlacementDate < date(1,6,2019)))   
       msgbox("Операции с эмиссионным счетом для данного выпуска не требуются");
     elif (choice == ChangeEmitentMenuItem)
       ExecMacroFile("ChangeEmitent.mac", "exec_change_emitent", ФинИнстр.Fiid);
     end;
  end;

  return 0;
END;

/*Отрезать ведущие нули*/
macro TrimZeroStr( str:STRING )
  while( substr( str, 1, 1 ) == "0" )
     str = substr( str, 2 );
  end;
  return str;
end;

/*Сумма в строку длиной Len без нулей в дробной части*/
macro SummaToStr( Summa:MONEY, Point:INTEGER, Len:INTEGER )
  var TmpStr, Symbol, _Continue = true; 

  TmpStr = ExecExp( "String( (double(Summa) ):z:0" + ":" + string(Point) + ")" );

  /*Отрезать незначащие нули в дробной части. Если после точки все нули, то точку тоже вырезать */
  while( _Continue )
     Symbol = substr( TmpStr, strlen(TmpStr), 1 );
     if( (Symbol == "0") OR (Symbol == ".") )
        TmpStr = substr( TmpStr, 1, strlen(TmpStr)-1 );
     else
        _Continue = false;
     end;
  end;

  if( strlen(TmpStr) > Len )
     TmpStr = substr( TmpStr, 1, Len );
  end;

  return TmpStr;
end;

/*Сумма в строку длиной Len с нулями в дробной части*/
macro SummaToStrZero( Summa:MONEY, Point:INTEGER, Len:INTEGER )
  return substr( ExecExp( "String( (double(Summa) ):0" + ":" + string(Point) + ")" ), 1, Len );
end;


/*Получить код для срочных контрактов в кодировке эмитента*/
PRIVATE MACRO GetFiCodeByIssuer( FI:TRecHandler, Len:INTEGER )
   var Code = "";
   Code = GetObjCode( FI.rec.FIID, OBJTYPE_FININSTR, REG_PARTYKIND_REGISTRATOR, REG_DOCKIND_FOREX, FI.rec.Issuer );
   if( Code == "" )
      Code = GetObjCode( FI.rec.FIID, OBJTYPE_FININSTR, REG_PARTYKIND_REGISTRATOR, REG_DOCKIND_CONTR, FI.rec.Issuer );
      if( (Code == "") AND (FI.rec.FI_Kind == FIKIND_CURRENCY) )
         Code = FI.rec.Ccy;
      end;
      if( Code == "" )
         Code = FI.rec.FI_Code;
      end;
   end;

   return substr( Code, 1, Len );
END;

/*Получить код фьючерса в кодировке эмитента*/
PRIVATE MACRO GetFutersCodeByIssuer( FI:TRecHandler, Len:INTEGER )
   var Code = "";

   Code = GetObjCode( FI.rec.FIID, OBJTYPE_FININSTR, REG_PARTYKIND_REGISTRATOR, REG_DOCKIND_FOREX, FI.rec.Issuer );

   return substr( TrimZeroStr(Code), 1, Len );
END;

PRIVATE MACRO DV_CheckFI( FI:TRecHandler, FIDeriv:TRecHandler )

   if( FI.rec.FaceValueFI < 0 )
      return false;
   end;

   if( (FI.rec.AvoirKind != FI_DERIVATIVE_FORWARD) and (FI.rec.DrawingDate == Date(0,0,0)) )
      return false;
   end;

   if( FI.rec.AvoirKind == FI_DERIVATIVE_OPTION )
      if( (FIDeriv.rec.OptionStyle <= 0)   OR 
          (FIDeriv.rec.OptionType <= 0)    OR
          (FIDeriv.rec.Strike == $0)
      )
         return false; 
      end;
   end;

   return true;
END;

/*Генерация биржевого кода производного инструмента
  RefID - сюда должен записываться референс, если он используется для генерации номера.
          Необходимо для отката сгенеренного референса в случае отказа от ввода или 
          повторной генерации*/
PRIVATE MACRO DV_GenerateMarketCode( FI:TRecHandler, FIDeriv:TRecHandler, RefID:INTEGER )
  VAR Day, Month, MonthBA, Year, FaceValueFI, DateStr, Code = "",
      OptionStyle, OptionType;

  if( DV_CheckFI( FI, FIDeriv ) == false )
     MsgBox("Недостаточно данных для формирования кода");
     return "";
  end;
                    
  if( ПолучитьКодСубъекта( FI.rec.Issuer, PTCK_CONTR ) != "ММВБ" )
     return "";
  end;

  FaceValueFI = TRecHandler( "fininstr" ); /*Базовый актив*/

  if( FI.rec.FaceValueFI < 0 ) 
     MsgBox( "Не задан финансовый инструмент базового актива." );
     return "";
  elif( ПолучитьФинИн( FI.rec.FaceValueFI, FaceValueFI ) ) 
     MsgBox( "Не найден финансовый инструмент базового актива." );
     return "";
  end;

  DateSplit( FI.rec.DrawingDate, Day, Month, Year );

  if( FI.rec.AvoirKind   == FI_DERIVATIVE_OPTION )
     if( FIDeriv.rec.OptionType == FI_DVTYPE_PUT )
        OptionType = "P";
     elif( FIDeriv.rec.OptionType == FI_DVTYPE_CALL )
        OptionType = "C";
     else
        MsgBox( "Не задан вид опциона." );
        return "";
     end;

     if( FIDeriv.rec.OptionStyle == FI_DVSTYLE_AMERICAN )
        OptionStyle = "A";
     elif( FIDeriv.rec.OptionStyle == FI_DVSTYLE_EUROPE )
        OptionStyle = "E";
     else
        MsgBox( "Не задан тип опциона." );
        return "";
     end;
  end;   

  if( FI.rec.AvoirKind == FI_DERIVATIVE_FUTURES )
     Code = "-" + string(Month:2:o) + string(mod(Year, 100):2:o);
     Code = substr( GetFiCodeByIssuer(FaceValueFI, 4), 1, MAX_MARKETCODE_LEN-strlen(Code) ) + Code;

  elif( (FI.rec.AvoirKind ==  FI_DERIVATIVE_OPTION) AND 
        (FaceValueFI.rec.FI_Kind   == FIKIND_DERIVATIVE ) AND
        (FaceValueFI.rec.AvoirKind == FI_DERIVATIVE_FUTURES )
      )
     DateSplit( FaceValueFI.rec.DrawingDate, NULL, MonthBA, NULL );

     if( Month == MonthBA )
        Code =  "..." + OptionType + OptionStyle + "..." + SummaToStrZero( FIDeriv.rec.Strike, FI.rec.Point, 7 );
     else
        Code =  "_" + string(Day:2:o) + string(Month:2:o) + string(mod(Year, 100):2:o) + 
                OptionType + OptionStyle + " " + 
                SummaToStr( FIDeriv.rec.Strike, FI.rec.Point, 4 );
     end;

     Code = substr( GetFutersCodeByIssuer(FaceValueFI,9), 1, MAX_MARKETCODE_LEN-strlen(Code) ) + Code;
  elif( FI.rec.AvoirKind ==  FI_DERIVATIVE_OPTION )
     Code =  "-" + string(Month:2:o) + string(mod(Year, 100):2:o) + 
             "..." + OptionType + OptionStyle + "..." + SummaToStrZero( FIDeriv.rec.Strike, FI.rec.Point, 7 );

     Code = substr( GetFiCodeByIssuer(FaceValueFI,4), 1, MAX_MARKETCODE_LEN-strlen(Code) ) + Code;
  elif( FI.rec.AvoirKind == FI_DERIVATIVE_FORWARD )
     MsgBox( "Для форварда биржевой код не генерируется." );
     return "";
  else
     MsgBox( "Не задан вид производного инструмента." );
     return "";
  end;

  SetParm( 2, 0 ); /*RefID*/
  return Code;
END;

/*Генерация внутреннего кода производного инструмента
  RefID - сюда должен записываться референс, если он используется для генерации номера.
          Необходимо для отката сгенеренного референса в случае отказа от ввода или 
          повторной генерации*/
PRIVATE MACRO DV_GenerateFICode( FI:TRecHandler, FIDeriv:TRecHandler, RefID:INTEGER )
  VAR BaseFiCode, Month, Year, DateStr, OptionStyle, OptionType,
      Code = "";
  var FaceValueFI = TRecHandler( "fininstr" ); /*Базовый актив*/

  if( DV_CheckFI( FI, FIDeriv ) == false )
     MsgBox("Недостаточно данных для формирования кода");
     return "";
  end;

  if( FI.rec.FaceValueFI < 0 ) 
     MsgBox( "Не задан финансовый инструмент базового актива." );
     return "";
  elif( ПолучитьФинИн( FI.rec.FaceValueFI, FaceValueFI ) ) 
     MsgBox( "Не найден финансовый инструмент базового актива." );
     return "";
  end;

  if( FaceValueFI.rec.FI_Kind == FIKIND_CURRENCY )
     BaseFiCode = FaceValueFI.rec.CCY;
  else
     BaseFiCode = TrimZeroStr( FaceValueFI.rec.FI_Code );
  end;

  DateSplit( FI.rec.DrawingDate, NULL, Month, Year );
  DateStr = string(Month:2:o) + string(mod(Year, 100):2:o);

  if( FI.rec.AvoirKind == FI_DERIVATIVE_OPTION )
     if( FIDeriv.rec.OptionType == FI_DVTYPE_PUT )
        OptionType = "P";
     elif( FIDeriv.rec.OptionType == FI_DVTYPE_CALL )
        OptionType = "C";
     else
        MsgBox( "Не задан вид опциона." );
        return "";
     end;

     if( FIDeriv.rec.OptionStyle == FI_DVSTYLE_AMERICAN )
        OptionStyle = "A";
     elif( FIDeriv.rec.OptionStyle == FI_DVSTYLE_EUROPE )
        OptionStyle = "E";
     else
        MsgBox( "Не задан тип опциона." );
        return "";
     end;
     
     Code =  "-" + DateStr + OptionType + OptionStyle + SummaToStr( FIDeriv.rec.Strike, FI.rec.Point, 4 );

  elif( FI.rec.AvoirKind == FI_DERIVATIVE_FUTURES )
     Code = "-" + DateStr;
  elif( FI.rec.AvoirKind == FI_DERIVATIVE_FORWARD )
     Code = "-" + DateStr;
  else
     MsgBox( "Не задан вид производного инструмента." );
     return "";
  end;                     

  SetParm( 2, 0 ); /*RefID*/
  return substr( BaseFiCode, 1, MAX_FICODE_LEN-strlen(Code) ) + Code;
END;

/*Генерация кода в номере счета производного инструмента
  RefID - сюда должен записываться референс, если он используется для генерации номера.
          Необходимо для отката сгенеренного референса в случае отказа от ввода или 
          повторной генерации*/
PRIVATE MACRO DV_GenerateAccountCode( FI:TRecHandler, FIDeriv:TRecHandler, RefID:INTEGER )
  VAR ds, i, _Continue = true, Code = "";
  VAR RefAlreadyUsed = TArray(); 
  if( (GetReferenceIDByType(
           18, /*OBJTYPE_DV*/
           1,  /*код Фи в номере счета*/                  
           RefID ) == 0
      ) AND 
      (RefID != 0) 
    )
     while( _Continue )

        if( GenerateReference( RefID, Code ) != 0 )
           MsgBox( "Ошибка при генерации референса по описателю " + string(RefID) );
           return "";
        end;

        /*Проверить, возможно уже есть ФИ с таким кодом в номере счета*/
        ds = TRsbDataSet( "select count(1) as NumRecord from dfininstr_dbt where t_CodeInAccount = '" + Code +"';");
        if( ds.moveNext() AND (ds.NumRecord > 0) )
           RefAlreadyUsed[RefAlreadyUsed.Size] = Code;
        else
           _Continue = false;
        end;
     end;

     i = 0;
     while( i < RefAlreadyUsed.Size )
        RestoreReference( RefID, RefAlreadyUsed[i] );
        i = i + 1;
     end;

  else
     MsgBox( "Не найден описатель референса." );
     return "";
  end;

  SetParm( 2, RefID ); /*RefID*/
  return Code;
END;

/*Определение ГНУ (группа налогового учета) по данным анкеты ц/б*/
MACRO ОпределитьГНУ( fininstr, avoiriss, avrinvst ):INTEGER
  VAR ГНУ = 0;
  var AvrKind, IncomeType, NominalFIID, IndexNom, BegDate = Date(0,0,0);
  
  AvrKind    = fininstr.rec.AvoirKind;
  IncomeType = avoiriss.rec.IncomeType;
  IndexNom   = avoiriss.rec.IndexNom;

  NominalFIID = fininstr.rec.FaceValueFI;
  if(FI_IsISU(fininstr))
    NominalFIID = avrinvst.rec.FormValueFIID;
  end;

  //минимальная из дат выпуска,  ввода в обращение и начала размещения
  if ((fininstr.rec.Issued != null) AND (fininstr.rec.Issued != Date(0,0,0)))
     BegDate = fininstr.rec.Issued;
  end;
  if ((avoiriss.rec.InCirculationDate != null) AND (avoiriss.rec.InCirculationDate != Date(0,0,0)))
     if (avoiriss.rec.InCirculationDate < BegDate)
        BegDate = avoiriss.rec.InCirculationDate;
     end;
  end;
  if ((avoiriss.rec.BegPlacementDate != null) AND (avoiriss.rec.BegPlacementDate != Date(0,0,0)))
     if (avoiriss.rec.BegPlacementDate < BegDate)
        BegDate = avoiriss.rec.BegPlacementDate;
     end;
  end;

  if( (IndexNom == "") AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OFZ, AvrKind ) )
     ГНУ = 1  ;/*ОФП   */ 
  elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_GKO, AvrKind ) )
     ГНУ = 2  ;/*ОФД   */ 
  elif( (IncomeType == FI_INCOME_TYPE_PERCENT) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY_BOND, AvrKind ) )                                
     ГНУ = 3  ;/*ОСФП  */ 
  elif( (IncomeType ==  FI_INCOME_TYPE_DISCOUNT) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY_BOND, AvrKind ) )                                
     ГНУ = 4  ;/*ОСФД  */ 
  elif( (IncomeType == FI_INCOME_TYPE_PERCENT) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL_BOND, AvrKind ) )                                
     ГНУ = 5  ;/*ОМП15 */ 
  elif( (IncomeType ==  FI_INCOME_TYPE_DISCOUNT) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL_BOND, AvrKind ) )
     ГНУ = 6  ;/*ОМД15 */ 
  elif( (IndexNom == "X") AND (IncomeType == FI_INCOME_TYPE_PERCENT) AND
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_BOND, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OBR, AvrKind )
        )
      )
     ГНУ = 25  ;/*ОИН */  
  elif( (NominalFIID == NATCUR) AND 
        (IncomeType == FI_INCOME_TYPE_PERCENT) AND 
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_BOND, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OBR, AvrKind )
        ) AND ((avoiriss.rec.BegPlacementDate >= date(1, 1, 2017)) and (avoiriss.rec.BegPlacementDate <= date(31, 12, 2021)))
      )
     ГНУ = 17  ;/*ОКЛПД */
  elif( (IncomeType == FI_INCOME_TYPE_PERCENT) AND
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_BOND, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OBR, AvrKind )
        )
      )
     ГНУ = 9  ;/*ОИН */ 
  elif( (IncomeType ==  FI_INCOME_TYPE_DISCOUNT) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_BOND, AvrKind ) )
     ГНУ = 10 ;/*ОКД24 */ 
  elif( (NominalFIID == NATCUR) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_SHARE, AvrKind ) )                               
     ГНУ = 12 ;/*АР    */ 
  elif( (NominalFIID == NATCUR) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_INVESTMENT_SHARE, AvrKind ) )
     ГНУ = 13 ;/*ИПР   */ 
  elif((IndexNom == "X") AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OFZ, AvrKind ))
     ГНУ = 15 ;/*ОФЗИН */
  elif( (NominalFIID == NATCUR) AND FI_IsDeposReceipt(fininstr) )
     ГНУ = 22 ;/*АРИ   */
  elif( (IncomeType ==  FI_INCOME_TYPE_PERCENT) AND
        (NominalFIID == NATCUR) AND
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_FOREIGN_FED, AvrKind )
        )
      )
     ГНУ = 29 ;/*ОКП29   */
  elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OVVZ, AvrKind ) )                               
     ГНУ = 31 ;/*ОВВЗ  */ 
  elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_OGVZ, AvrKind ) )                               
     ГНУ = 32 ;/*ОГВЗ  */
  elif( (IncomeType ==  FI_INCOME_TYPE_PERCENT) AND
        (NominalFIID != NATCUR) AND (NominalFIID != ALLFININSTR) AND
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_FOREIGN_FED, AvrKind )
        )
      )
     ГНУ = 33 ;/*ОПП */ 
  elif( (IncomeType ==  FI_INCOME_TYPE_DISCOUNT) AND
        (NominalFIID != NATCUR) AND (NominalFIID != ALLFININSTR) AND
        ( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE_EVRO, AvrKind ) OR
          FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_FOREIGN_FED, AvrKind )
        )
      )
     ГНУ = 34 ;/*ОПД */
  elif( (NominalFIID != NATCUR) AND (NominalFIID != ALLFININSTR) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_INVESTMENT_SHARE, AvrKind ) )                               
     ГНУ = 35 ;/*ИПВ   */ 
  elif( (NominalFIID != NATCUR) AND (NominalFIID != ALLFININSTR) AND FI_IsDeposReceipt(fininstr) )
     ГНУ = 36 ;/*ДР    */ 
  elif( (NominalFIID != NATCUR) AND (NominalFIID != ALLFININSTR) AND FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_SHARE, AvrKind ) )                               
     ГНУ = 37 ;/*АВ    */ 
  elif(FI_IsKSU(fininstr))
     ГНУ = 888 ;/*КСУ   */
  elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BASKET, AvrKind ))
     ГНУ = 999;/*Корзина ц/б   */
  end;

  return ГНУ;
END;

/*Определение номера ГНУДЮН (Группа налогового учета для юрлиц-нерезидентов) по данным анкеты ц/б*/
MACRO ОпределитьГНУДЮН( fininstr, avoiriss, avrinvst ):STRING
  record issuer("party.dbt");
  VAR ГНУДЮН = "10"; //необлагаемые ц/б
  if((FI_IsBond(fininstr)) and (ПолучитьСубъекта(fininstr.rec.Issuer, issuer) == 0) and (issuer.NotResident == ""))
    if((avoiriss.rec.BegPlacementDate >= date(1,1,2017)) and
       (avoiriss.rec.BegPlacementDate <= date(31,12,2021)) 
      )
      ГНУДЮН = "15"; //льгота по 58-ФЗ (15% с купона) 
    elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE, fininstr.rec.AvoirKind ))
      ГНУДЮН = "20"; //облагаемые облигации
    end;
  end;

  return ГНУДЮН;
END;


/* Макрос установки редактируемости полей в форме ввода/корректировки ценной бумаги или ПИ
   Вызывается после системной установки доступа к полям, если только панель открыта не в режиме просмотра.
   Параметры:
      FI:TRecHandler   - ФИ, если FI.rec.FIID == 0, то ввод записи, 
      FldNames:TArray  - массив имен полей 
      FldAccess:TArray - массив, определяющий доступ к полю, 1 - разрешить, 0 - запретить
      Индексы массивов FldNames и FldAccess должны соответствоать друг другу. 
      Имя поля задается в ресурсе панели, в дистрибутиве имена заданы для 
      полей "Номинал" = "pNominal" и "Объем выпуска" = "pQuant" ц/б.

   Пример использования:
      FldNames[FldNames.Size] = "pQuant";    FldAccess[FldAccess.Size]=0; -- закрыть поле "Объем выпуска" для редактирования
      FldNames[FldNames.Size] = "pNominal";  FldAccess[FldAccess.Size]=1; -- разрешить редактировать поле "Номинал" 
*/
MACRO CorrectFieldAccess( FI:TRecHandler, FldNames:TArray, FldAccess:TArray )
END;


macro ПослеСохраненияФинансовогоИнструмента( Режим, error )
/* Режимы проверки:
 OBJ_DELETE - удаление финансового инструмента;
 OBJ_INSERT - ввод финансового инструмента;
 OBJ_UPDATE - корректировка финансового инструмента;
*/

/* error - при удачном выполнении транзакции 0, иначе номер ошибки */

  var Эмитент, stat;
  //msgbox("Режим = ", Режим, "   error = ", error);
  //msgbox("ФинИнстр.FIID = ", ФинИнстр.FIID, "  ФинИнстр.Definition = ", ФинИнстр.Definition);

  //Chesnokov D.S. 16.02.2019 Убираем установку категории КИ для евробондов
  //Chesnokov D.S. 22.02.2019 для эмитентов нерезидентов
  if ((Режим == OBJ_INSERT) )
//    Эмитент = RsbParty(ФинИнстр.Issuer); //в разы медленнee работает RsbParty
    Эмитент = TRsbDataSet(" select * from dparty_dbt where t_partyid = " + ФинИнстр.Issuer);
    if (Эмитент.movenext())
       if (Эмитент.NotResident == "X")
         //DisconnectObjAttr(OBJTYPE_AVOIRISS, UniID(ЦеннаяБумага, OBJTYPE_AVOIRISS), 34, 0);
         //bpv 20191001 получалось, что категория устанавливалась и убиралась в одну дату и система в эту считала категорию действующей, хотя в интерфейсе ее нет
         //             поэтому удаляем ее совсем
         var cmd = RsdCommand("delete from dobjatcor_dbt where t_groupid = 34 and t_objecttype = 12 and t_object =  lpad("+ФинИнстр.fiid+",10,'0')");
             cmd.execute();
       end;  
    
       if((Эмитент.PartyID == {OurBank}) and (fi_isbond(ФинИнстр)))
         //DAN Для облигаций нашего Банка устанавливаем значение "Да" в категории "Наблюдаемые исходные данные" 
         if( not ConnectObjAttr(stat, OBJTYPE_AVOIRISS, UniID( ЦеннаяБумага, OBJTYPE_AVOIRISS), 60, 1, null, null, {curdate}))
           msgbox("Ошибка при установке категории Наблюдаемые исходные данные");
         end;
         
         //msgbox("Для введенной ЦБ требуется в панели обслуживания выпуска(CTRL+U) \nустановить платежного агента(ПА) и договор с ним. ");  
         //Вместо предупреждения временный костыль, добавить определение ПА по выпуску и договора с ним
         var  cmd_PA =RSDCommand( "UPDATE davrserv_dbt SET T_PARTYP = 4,T_PARTYCONTRP = 25641 WHERE T_FIID= ?");
         cmd_PA.AddParam("FIID", RSDBP_IN, ФинИнстр.FIID); 
         cmd_PA.execute();
         
         //BIQ-13907 Для субординированных облигаций с установленным признаком "Право отказа от выплаты"
         // устанавливается значение категории "Квалификационный признак суборд.ОЭБ" (=70) = "Долевой инструмент" (=2)
         if ((ЦеннаяБумага.Subordinated == "X") and (ЦеннаяБумага.CouponRefuseRight == "X"))
           if( not ConnectObjAttr(stat, OBJTYPE_AVOIRISS, UniID( ЦеннаяБумага, OBJTYPE_AVOIRISS), 70, 2, null, null, {curdate}))
             msgbox("Ошибка при установке категории 'Квалификационный признак суборд.ОЭБ'");
           end;
         end;
         
       end;
    end;
  end;

  if ((Режим == OBJ_INSERT) and ( ФинИнстр.FI_KIND == FIKIND_AVOIRISS ))
      BegAction(2000, "Получение данных из RU DATA...");
      var StrLSIN_ISIN ="" ;
      if (ЦеннаяБумага.ISIN != "")
           StrLSIN_ISIN = ЦеннаяБумага.ISIN ;
      elif (ЦеннаяБумага.LSIN != "")
           StrLSIN_ISIN = ЦеннаяБумага.LSIN ;
      end;
      if ((StrLSIN_ISIN == "") or (not SOFR_LoadRuDataByID(StrLSIN_ISIN)))
        MsgBox("Выпуск не найден в RU DATA. Для корректной работы системы необходимо вручную добавить на выпуск Биржевые коды бумаги\n"+
               "1. Код вида 11 \"Код на ММВБ\"\n"+
               "2. Код вида 22 \"Код на СПБ\"\n"
        );
      end;
     EndAction();
  end;
  return 0;
end;

macro ВызватьПанельИсторииОбращаемЦБдляНДФЛ(fiid)
  RunCircHistScroll(fiid);
end;