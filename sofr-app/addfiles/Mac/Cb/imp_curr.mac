/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank v 4.31                                     R-Style Software Lab Ltd

  Импорт курсов валют.

CREATED      : 13.12.99 LA
FILE_NAME    : imp_curr.MAC
MODIFICATIONS:
NOTES        :

└───────────────────────────────────────────────────────────────────────────*/

IMPORT FIInter, BankInter, "globals.mac";

/*                             Настройка                                    */

/* Если флаг ПоВсемВалютам установлен в TRUE, то импорт будет произведен
   для всех валют, записи по которым имеются в fininstr.dbt, иначе -
   для валют, заданных в нижеследующем списке */
var ПоВсемВалютам = TRUE;

var ПутьК_DBF = ""; /* Директория, где находится DBF файл. Если не задана,
                       поиск производится в каталоге Dbfile */

/* Список кодов ISO валют, курсы по которым будут импортированы */
var СписокВалют = TArray;

СписокВалют(0)  = "840";
СписокВалют(1)  = "280";
СписокВалют(2)  = "978";

var Value_err;

/* ------------------------------------------------------------------------ */

FILE src() DBF;
FILE fininstr( fininstr ) key 12;

/* ------------------------------------------------------------------------ */

var l_rurCode;

var not_found = "NOT FOUND";

/* ------------------------------------------------------------------------ */

/* Надо ли импортировать валюту с заданным кодом (INT) */
MACRO importRate( p_code )
 
 var i, n;

 p_code = int( p_code );

 i = 0;
 n = СписокВалют.size;

 while( i < n )
  if( int( СписокВалют(i) ) == p_code )
   return TRUE;
  end;
  i = i+1;
 end;
 return FALSE;
END;

/* ......................................................................... */

/* Получить польз.код ФИ по коду ISO */
MACRO getCodeByISO( p_ISOCode )
 fininstr.ISO_Number = string(p_ISOCode);
 if( not getEQ( fininstr ) )

  if( p_ISOCode == 810 )
   println( "!!! Ошибка: в справочнике не найдена базовая валюта (код ISO = 810)" );
   exit;
  end;

  return not_found;
 end;
 return fininstr.FI_Code;
END;

/* ......................................................................... */

/* Установить курс по текущей валюте */
MACRO processRate
 RECORD _rec( ratedef );
 RECORD wrk( rateparm );

 var err_code;
 var l_curCode;

 l_curCode = getCodeByISO( int( src.ISO_DIG ) );

 if( l_curCode == not_found )
  return;
 elif( ( not ПоВсемВалютам ) and
       ( not importRate( src.ISO_DIG ) ) )
  return;
 end;

 /* Получить курс валюты */
 if( ( ПолучитьКурс( _rec, l_rurCode, l_curCode ) != 0 ) or
     ( ПолучитьЗначениеКурса( _rec, src.DAT ) != 0 ) )

  println( "................................................................" );
  println;
  println( "!!! Ошибка получения данных по курсу валюты ",
           src.ISO_DIG, "  ", trim( src.NAME_RUSH ) );
  println;
  return;
 end;

 if( _rec.SinceDate == src.DAT )

  if( not getTrue( TRUE, "По валюте " + trim( src.ISO_DIG ) +
                         " уже задан курс за текущий день. Обновить?" ) )
   return;
  end;

 elif( _rec.SinceDate > src.DAT )
  return;
 end;

 /* проверка на соотвествие даты */
 while( src.DAT > {curdate} )
   
  src.DAT = {curdate};
  if( not getTrue( FALSE, "Дата устанавливаемого курса больше текущего\n" +
                          "операционного дня. Установить?" ) )
   return;
  elif( not getDate( src.DAT, "Введите дату начала действия курса" ) )
   return;
  end;
 
 end;

 wrk.FI_Code   = l_rurCode;
 wrk.OtherFI   = l_curCode;
 wrk.Type      = _rec.Type;
 wrk.IsInverse = _rec.IsInverse;
 wrk.Rate      = moneyL( src.CURSE * 10000 );
 wrk.Scale     = int( src.SCALE );
 wrk.Point     = _rec.Point;
 wrk.SinceDate = src.DAT;
 wrk.IsDominant = "X"; /*18 January 2000, VSH*/

 err_code = УстановитьКурс( wrk );

 println( "................................................................" );
 println;
 println( ">> Валюта ", src.ISO_DIG, "  ", trim( src.NAME_RUSH ) );
 println;

 if( err_code != 0 )
  print( "> !!! Ошибка обновления курса: " );

  if  ( err_code == 1 )
   println( " Не найдена котируемая валюта." );
  elif( err_code == 2 )
   println( " Не найдена базовая валюта." );
  elif( err_code == 3 )
   println( " Не найден вид курса для текущей валюты." );
  elif( err_code > 100 )
   println( " Ошибка Btrieve № ", err_code - 100 );
  else
   println( " Описание ошибки отсутствует" );
  end;

  return;
 end;

 println( "> Курс валюты был обновлен. Установлены следующие значения:" );
 println;
 println( "> Дата курса       ", wrk.SinceDate );
 println( "> Текущий курс     ", wrk.Rate/10000:0:4, ", масштаб ", wrk.Scale );
 println;
END;

/* ---------------------------- Entry point -------------------------------- */

if( trim( ПутьК_DBF ) == "" )
 GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, ПутьК_DBF, Value_err);
end;

if( not open( src,  ПутьК_DBF + "\\curcours.dbf" ) )
 msgbox( "Невозможно открыть файл с курсами валют:|",
         ПутьК_DBF + "\\curcours.dbf" );
 exit(1);
end;

l_rurCode = getCodeByISO( 810 );

println;
println( "Протокол импорта валют ", date, "  ", time );
println;

while( next( src ) )
 processRate;
end;
