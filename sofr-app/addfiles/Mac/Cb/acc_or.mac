/*
$Name: acc_or.mac
$Module: Ядро ГКБО
$Description: Макрос шага "Формирование оценочного резерва"
*/

Import InsCarryDoc, "res_carry.mac", "acc_cls.mac";

private record RsvParm("rsvprm.dbt");

var OffshoreGroup = 0;

/*
    Пользовательская часть шага
*/
macro ExecuteStep( doc, primdoc, DocKind, ID_Operation )

  record acc(account);
  
  var IncomeReserveAccount : string;
  var OutlayReserveAccount : string;
  var ReserveAccount : string;
  var accPD : AccountPrimdoc;
  var ClassifReserve : string;
  var KindReserve;
  var IncomeCatCodePrm;
  var OutlayCatCodePrm;

  SetBuff( acc, primdoc );

  accPD = AccountPrimdoc( acc, ID_Operation, RsvParm );

  ReserveAccount = FindReserveAccount(accPD, AccOprServDoc.Date);
  if(Trim(ReserveAccount) == "")
    return 0;
  end;

  KindReserve = GetAccountReserveKind(RsvParm, acc, AccOprServDoc.Date);

  if(KindReserve == KindReserveLoss)
    ClassifReserve = GetAccountReserveClassif(KindReserve, RsvParm.RsvClass, KindReserve);
  elif(KindReserve == KindReserveLoans)
    ClassifReserve = GetAccountReserveClassif(KindReserve, RsvParm.RsvClassLoans, KindReserve);
  elif(KindReserve == KindReserveOffshore)
    ClassifReserve = GetAccountReserveClassif(KindReserve, RsvParm.RsvClassOffshore, KindReserve);
  end;

  GetIncomeOutlayCatCodeEstimated(ClassifReserve, @IncomeCatCodePrm, @OutlayCatCodePrm, AccOprServDoc.Date);

  if((IncomeCatCodePrm == "") or (OutlayCatCodePrm == ""))
    if(KindReserve == KindReserveLoss)
      msgbox("Не задана категория средств корректировки резерва для классификации РВП \"", ClassifReserve, "\"");
    elif(KindReserve == KindReserveLoans)
      msgbox("Не задана категория средств корректировки резерва для классификации РВПC \"", ClassifReserve, "\"");
    elif(KindReserve == KindReserveOffshore)
      msgbox("Не задана категория средств корректировки резерва для классификации РОФШ \"", ClassifReserve, "\"");
    end;
    return 1;
  else

    if((acc.Open_Close != "") or (ReserveEstimated == $0))
      IncomeReserveAccount = FindReserveEstimatedAccountIncome( accPD, AccOprServDoc.Date );
      OutlayReserveAccount = FindReserveEstimatedAccountOutlay( accPD, AccOprServDoc.Date );
      if((Trim(IncomeReserveAccount) == "") or (Trim(OutlayReserveAccount) == ""))
        return 0;
      end;
    else
      IncomeReserveAccount = OpenReserveEstimatedAccountIncome( accPD, AccOprServDoc.Date );
      OutlayReserveAccount = OpenReserveEstimatedAccountOutlay( accPD, AccOprServDoc.Date );
    end;

    if((Trim(IncomeReserveAccount) == "") or (Trim(OutlayReserveAccount) == ""))
      msgbox("Не найден счет оценочного резерва.");
      return 1;
    end;

    if(not CreateReserveEstimatedCarry(accPD, AccOprServDoc.Date, ReserveAccount, IncomeReserveAccount, OutlayReserveAccount, IncomeCatCodePrm, OutlayCatCodePrm))
      msgbox("Не удалось создать проводку резерва.");
      return 1;
    end;
  end;

  if((acc.Open_Close != "") and (Trim(IncomeReserveAccount) != ""))
    CloseReserveEstimatedAccountIncome(accPD, AccOprServDoc.Date);
  end;
  if((acc.Open_Close != "") and (Trim(OutlayReserveAccount) != ""))
    CloseReserveEstimatedAccountOutlay(accPD, AccOprServDoc.Date);
  end;

  return 0;

end;
