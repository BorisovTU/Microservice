/*
$Name:           sfpmbtch.mac
$Module:         Ядро ГКБО
$Description:    RSL-классы для обработки ПЗО в РКО в пакетном режиме
*/



import RSD, BilFacturaInter, SfInter, BankInter, CTInter, PTInter;
import "globals.mac", "sfcommon.mac";

macro AfterSfPayBatch(afterSfPayDataArray:TArray)
end;

class TSfPaymBatchCharger( accTrn:RsbBatchPaymTrn ) 
  var m_AccTrn = accTrn; // ссылка на передаваемый в конструктор параметр - объект класса RsbBatchAccTrn или его наследника;
  
  // Заполнение временной таблицы dsfpaymbatch_params_tmp данными, необходимыми для выполнения проводок по учету НДС или по начислению ТО и/или УК.
  macro fillSfPaymParams( IsK2 )  // Признак постановки платежа в картотеку 2
    var stat:integer = 0;
    var cmd, rs, strSql;


    // Отбор данных платежей по ТО. 
    cmd = RsdCommand(  );
    cmd.NullConversion = true;
    strSql = " INSERT INTO DSFPAYMBATCH_PARAMS_TMP " +
             "  (T_PAYMENTID, T_PMVALUEDATE, " +
             "   T_OPERATIONID, T_STEPID, T_INVOICEID, T_SFCONTRID, " +
             "  T_FEETYPE, T_COMMNUMBER, T_SFDEFID, T_STATUS, T_DATEPAY, T_CLOSEDATE, " +
             "  T_ONDATE,  " +
             "  T_SFCONCOMID, T_SFPLANID, " +
             "  T_CalcAccount, T_CalcNDSAccount, T_NDSACRUALAccount, T_MinusNDSAccount, T_ComissAccount ) " +
             "SELECT pm.t_PaymentID, pm.t_ValueDate, " +
             "opr.t_ID_Operation, opr.t_ID_Step, inv.t_InvoiceID, inv.t_ContractID, " +
             "  sfdef.t_feetype, sfdef.t_commnumber, sfdef.t_ID, sfdef.t_Status, sfdef.t_DatePay, sfdef.t_CloseDate, " +
             "  sfdef.t_dateperiodend, " +
             "  sfdef.t_concomid, 0, " +
             "  NVL(calcsi.t_Account, chr(0)), NVL(calcndssi.t_Account, chr(0)), " +
             "  NVL(ndsacrualsi.t_Account, chr(0)), NVL(minusndssi.t_Account, chr(0)), " +
             "  chr(0) " +
             "FROM dsfinv_dbt inv, dpmpaym_dbt pm, doprtemp_tmp opr, dsfinvlnk_dbt lnk, dsfdef_dbt sfdef, " +
             "  dsfsi_dbt calcsi, " +
             "  dsfsi_dbt calcndssi, " +
             "  dsfsi_dbt ndsacrualsi, " +
             "  dsfsi_dbt minusndssi " +
             "WHERE pm.t_PaymentID = opr.t_OrderID AND opr.t_ErrorStatus = 0 " +
             "  AND lnk.t_PaymentID = pm.t_PaymentID AND lnk.t_InvoiceID = inv.t_InvoiceID " +
             "  AND sfdef.t_InvoiceID = inv.t_InvoiceID ";



    if(IsK2)
      strSql = strSql + " AND pm.t_Payer != ? AND inv.t_IsIncluded = chr(0) ";

      cmd.addParam( "", RSDBP_IN, {HeadBankID});
    end;  

    // ПИ учета комиссии. Источник КУ: "+ Расчеты" или "+ Расчеты, НВПИ"
    strSql = strSql + " AND calcsi.t_ObjectType(+)= DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND calcsi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND calcsi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, CALC_SFSI_KIND);
    
    // ПИ учета НДС. Источник КУ: "+Расчеты НДС" или "+Расчеты НДС, НВПИ"
    strSql = strSql + " AND calcndssi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND calcndssi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND calcndssi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, CALCNDS_SFSI_KIND);


    // ПИ начисленного НДС. Источник КУ: "-НДС начисленный", "+НДС начисленный", "-НДС начисленный, НВПИ", "+НДС начисленный, НВПИ"
    strSql = strSql + " AND ndsacrualsi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND ndsacrualsi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND ndsacrualsi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, NDSACRUAL_SFSI_KIND);

    // ПИ НДС. Источник КУ: "+ НДС" или "- НДС".
    strSql = strSql + " AND minusndssi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND minusndssi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND minusndssi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, NDS_SFSI_KIND);


    cmd.CmdText = strSql;
    cmd.execute();   

    cmd = NULL;

    // Отбор данных платежей по УК.
    cmd = RsdCommand(  );
    cmd.NullConversion = true;

    strSql = "INSERT INTO DSFPAYMBATCH_PARAMS_TMP " +
             "  (T_PAYMENTID, T_PMVALUEDATE, " +
             "  T_OPERATIONID, T_STEPID, T_INVOICEID, " +
             "  T_FEETYPE, T_COMMNUMBER, T_SFDEFID, T_STATUS, T_DATEPAY, T_CLOSEDATE, " +
             "  T_ONDATE, T_CalcAccount, T_CalcNDSAccount, T_NDSACRUALAccount, T_MinusNDSAccount, T_ComissAccount ) " +
             "SELECT pm.t_PaymentID, pm.t_ValueDate, " +
             "  opr.t_ID_Operation, opr.t_ID_Step, 0, sfdef.t_feetype, sfdef.t_commnumber, " +
             "  sfdef.t_ID, sfdef.t_Status, sfdef.t_DatePay, sfdef.t_CloseDate, " +
             "  sfdef.t_dateperiodend, NVL(calcsi.t_Account, chr(0)), NVL(calcndssi.t_Account, chr(0)), " +
             "  NVL(ndsacrualsi.t_Account, chr(0)), NVL(minusndssi.t_Account, chr(0)),";

    if(IsK2)
      strSql =  strSql + " NVL(comisssi.t_Account, chr(0)) "; 
    else
      strSql =  strSql + " chr(0) "; 
    end;

    strSql =  strSql + "FROM doprtemp_tmp opr, dpmpaym_dbt pm, dsfdef_dbt sfdef, " +
                       "  dsfsi_dbt calcsi, " +
                       "  dsfsi_dbt calcndssi, " +
                       "  dsfsi_dbt ndsacrualsi, " +
                       "  dsfsi_dbt minusndssi " ;
    if(IsK2)
      strSql =  strSql + " , dsfsi_dbt comisssi " ;
    end;

    strSql =  strSql + "WHERE opr.t_OrderID = pm.t_PaymentID  " +
                       "  AND pm.t_FeeType IN ( ?, ?, ? ) " +
                       "  AND pm.t_DefComID > 0 AND opr.t_ErrorStatus = 0 " +
                       "  AND sfdef.t_feetype = pm.t_feetype AND sfdef.t_ID = pm.t_DefComID ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE  );

    if(IsK2)
      strSql = strSql + " AND sfdef.t_IsIncluded = chr(0) AND pm.t_Payer != ? ";
      cmd.addParam( "", RSDBP_IN, {HeadBankID});
    end;

    // ПИ учета комиссии. Источник КУ: "+ Расчеты" или "+ Расчеты, НВПИ"
    strSql = strSql + " AND calcsi.t_ObjectType(+)= DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND calcsi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND calcsi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, CALC_SFSI_KIND);
                      
    // ПИ учета НДС. Источник КУ: "+Расчеты НДС" или "+Расчеты НДС, НВПИ"
    strSql = strSql + " AND calcndssi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND calcndssi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND calcndssi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, CALCNDS_SFSI_KIND);

    // ПИ начисленного НДС. Источник КУ: "-НДС начисленный", "+НДС начисленный", "-НДС начисленный, НВПИ", "+НДС начисленный, НВПИ"
    strSql = strSql + " AND ndsacrualsi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND ndsacrualsi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND ndsacrualsi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, NDSACRUAL_SFSI_KIND);
                      
    // ПИ НДС. Источник КУ: "+ НДС" или "- НДС".
    strSql = strSql + " AND minusndssi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                      " AND minusndssi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND minusndssi.t_DebetCredit(+) = ? ";

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
    cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
    cmd.addParam( "", RSDBP_IN, NDS_SFSI_KIND);

    // ПИ вида комиссии. Источник СПИ комиссии
    if(IsK2)
      strSql = strSql + " AND comisssi.t_ObjectType(+) = DECODE(sfdef.t_feeType, ?, ?, ?, ?, ?) " +
                        " AND comisssi.t_ObjectID(+) = LPAD(to_char(sfdef.t_ID), 10, '0') AND comisssi.t_DebetCredit(+) = ? ";

      cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);
      cmd.addParam( "", RSDBP_IN, OBJTYPE_OPRSFCOM);
      cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE);
      cmd.addParam( "", RSDBP_IN, OBJTYPE_SFSINGDF);
      cmd.addParam( "", RSDBP_IN, OBJTYPE_SFDEFCOM);
      cmd.addParam( "", RSDBP_IN, COMISS_SFSI_KIND);
    end;

    cmd.CmdText = strSql;
    cmd.execute();   

    cmd = NULL;

    // Скорректировать дату onDate для единовременных комиссий
    strSql = "UPDATE DSFPAYMBATCH_PARAMS_TMP tmp " +
             "SET tmp.t_OnDate = " +
             "  (SELECT T_Fact_Date FROM doprstep_dbt step " +
             "   WHERE step.t_ID_Operation = tmp.t_OperationID AND step.t_ID_Step = tmp.t_StepID) " +
             "WHERE tmp.t_feetype = ?";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;

    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE);

    cmd.execute();
  end;

  
  // Получение набора записей параметров по комиссиям ТО
  macro getRecordSetForSfInv()
    var cmd, rs, strSql;   

    var SFInvRec = TRecHandler("sfinv.dbt");
    var SFInvStr = GetRecHandlerFields(SFInvRec, "inv");

    var PmPaymRec = TRecHandler("pmpaym.dbt");
    var PmPaymStr = GetRecHandlerFields(PmPaymRec, "paym");

    var SFDefRec = TRecHandler("sfdef.dbt");
    var SFDefStr = GetRecHandlerFields(SFDefRec, "sfdef");

    var SFComissRec = TRecHandler("sfcomiss.dbt");
    var SFComissStr = GetRecHandlerFields(SFComissRec, "sfcomiss");
    
    strSql = "SELECT " + SFInvStr +", " +
                         PmPaymStr + ", " +
                         SFDefStr +", " +
                         SFComissStr + ", " +
             "  sfpaym.t_OperationID As sfpaymOperationID, sfpaym.t_StepID As sfpaymStepID, " +
             "  sfpaym.t_CalcAccount As sfpaymCalcAccount, sfpaym.t_CalcNdsAccount As sfpaymCalcNdsAccount, " +
             "sfpaym.t_NdsAcrualAccount As sfpaymNdsAcrualAccount, sfpaym.t_MinusNdsAccount As sfpaymMinusNdsAccount, sfpaym.t_ComissAccount As sfpaymComissAccount " +
             "FROM DSFPAYMBATCH_PARAMS_TMP sfpaym, dsfinv_dbt inv, dpmpaym_dbt paym, " +
             "  dsfdef_dbt sfdef, dsfcomiss_dbt sfcomiss " +
             "WHERE inv.t_InvoiceID = sfpaym.t_InvoiceID " +
             "  AND paym.t_PaymentID = sfpaym.t_PaymentID " +
             "  AND sfdef.t_feetype = sfpaym.t_feetype AND sfdef.t_ID = sfpaym.t_SfDefID " +
             "  AND sfcomiss.t_feetype = sfpaym.t_feetype AND sfcomiss.t_number = sfpaym.t_commNumber " +
             "ORDER BY sfpaym.t_InvoiceID "; 

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.execute();   

    rs = RsdRecordset( cmd ); 

    return rs;
  end; 

  // Получение набора записей параметров по комиссиям
  macro getRecordSetForSfDef()
    var cmd, rs, strSql;   

    var PmPaymRec = TRecHandler("pmpaym.dbt");
    var PmPaymStr = GetRecHandlerFields(PmPaymRec, "paym");

    var SFDefRec = TRecHandler("sfdef.dbt");
    var SFDefStr = GetRecHandlerFields(SFDefRec, "sfdef");

    var SFComissRec = TRecHandler("sfcomiss.dbt");
    var SFComissStr = GetRecHandlerFields(SFComissRec, "sfcomiss");

    // paym.t_PaymentID, paym.t_ValueDate, paym.t_FIID as PayerFIID, paym.t_PayFIID as ReceiverFIID, paym.t_FuturePayerAmount, paym.t_FutureReceiverAmount,
    strSql = " SELECT  " +
                 PmPaymStr + ", " +
                 SFDefStr +", " +
                 SFComissStr + ", " +
             "   sfpaym.t_OperationID As sfpaymOperationID, sfpaym.t_StepID As sfpaymStepID, " +
             "   sfpaym.t_CalcAccount As sfpaymCalcAccount, sfpaym.t_CalcNdsAccount As sfpaymCalcNdsAccount, " +
             "  sfpaym.t_NdsAcrualAccount As sfpaymNdsAcrualAccount, sfpaym.t_MinusNdsAccount As sfpaymMinusNdsAccount, sfpaym.t_ComissAccount As sfpaymComissAccount " +
             " FROM DSFPAYMBATCH_PARAMS_TMP sfpaym, dpmpaym_dbt paym, " +
             "   dsfdef_dbt sfdef, dsfcomiss_dbt sfcomiss  " +
             " WHERE paym.t_PaymentID = sfpaym.t_PaymentID  " +
             "   AND sfdef.t_feetype = sfpaym.t_feetype AND sfdef.t_ID = sfpaym.t_SfDefID " +
             "   AND sfcomiss.t_feetype = sfpaym.t_feetype AND sfcomiss.t_number = sfpaym.t_commNumber " +
             "   AND sfpaym.t_InvoiceID = 0 " +
             " ORDER BY sfpaym.t_feeType, sfpaym.t_SfDefID ";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.execute();   

    rs = RsdRecordset( cmd ); 

    return rs;
  end;

  // Получение вида объекта по типу взимания ПЗО
  macro getObjectType( feeType ) // тип взимания
    if(feeType == SF_FEE_TYPE_PERIOD)
      return OBJTYPE_SFDEFCOM;
    elif(feeType == SF_FEE_TYPE_SINGLE)
      return OBJTYPE_OPRSFCOM;
    elif(feeType == SF_FEE_TYPE_ONCE)  
      return OBJTYPE_SFSINGDF;
    end;
  end; 


  macro IsSfDefNVPI( sfdef   , // буфер УК
                     sfcomiss, // буфер вида комиссии
                     PayFIID:@integer   // валюта оплаты - выходной параметр
                   )

    var IsNVPI = false;
    var retPayFIID = sfdef.FIID_Sum;

    if(sfdef.FeeType == SF_FEE_TYPE_ONCE)
      IsNVPI = false;
      if(sfdef.FIID_paySum != ALLFININSTR) 
        retPayFIID = sfdef.FIID_paySum;
      end;
    else
     if(sfcomiss.FIID_paySum != ALLFININSTR) 
       retPayFIID = sfcomiss.FIID_paySum;
     end;

     if((sfdef.FIID_Sum != payFIID) AND (SfComiss.CalcComissSumAlg != 1)) // SFCOMISS_CALCCOMISSSUMALG_CALC
       IsNVPI = true;
     end;
    end;

    PayFIID = retPayFIID;
//    SetParm ( 3, retPayFIID );
    return IsNVPI;
  end;


  // Определение валюты оплаты УК
  macro GetSfDefPayFIID(sfdef, sfcomiss) 
    var payFIID = sfdef.rec.FIID_Sum;

    if(sfdef.rec.FeeType == SF_FEE_TYPE_ONCE)
      if(sfdef.rec.FIID_paySum != ALLFININSTR) 
        payFIID = sfdef.rec.FIID_paySum;
      end;
    else
      if(sfcomiss.rec.FIID_paySum != ALLFININSTR )
        payFIID = sfcomiss.rec.FIID_paySum;
      end;
    end;
    return payFIID;
  end;
end;

class TAfterSfPayData()
  var sfDefComs:TArray;
  var depoPaidAmount;
  var depoPaidNDS;
  var depoPayFIID;
  var PaymentID;
end;

class TSfPaymBatchData
  var PaymentID = 0;
  var ValueDate = DATE(0,0,0);
  var FacturaID = 0;
  var feeType   = 0;
  var SfDefID = 0;
  var SfDefStatus = 0;
  var trnData = null;        // :RsbAccTransactionData
  var sfdocs = null;         // буфер записи таблицы sfdocs.dbt : TRecHandler
  var ID_Operation = 0;      // идентификатор экземпляра операции
  var ID_Step = 0;           // идентификатор шага операции
  var iCarry  = 0;
  var Error = 0;
  var ErrorMesage = "";
  var afterSfPayData = null; // : TAfterSfPayData
end;

// Класс служит для пакетного изменения статуса УК.
class TSfDefStatusChanger // Класс служит для пакетного изменения статуса УК.
  var StatusArray       = TArray;
  var OldStatusArray    = TArray;
  var DatePayArray      = TArray;
  var FeeTypeArray      = TArray;
  var DefIDArray        = TArray;
  var ID_OperationArray = TArray;
  var ID_StepArray      = TArray;

  // Метод добавляет в массивы-атрибуты класса данные, необходимые для изменения статуса УК в пакетном режиме, и привязывает изменение статуса к шагам операции.
  macro connectOprDocs( paymData: TSfPaymBatchData) // ссылка на объект TSfPaymBatchData
    FeeTypeArray[FeeTypeArray.size()] = paymData.FeeType;
    DefIDArray[DefIDArray.size()] = paymData.SfDefID;
    StatusArray[StatusArray.size()] = SFDEFCOM_STATUS_PAYED;
    DatePayArray[DatePayArray.size()] = paymData.ValueDate;
    ID_OperationArray[ID_OperationArray.size()] = paymData.ID_Operation;
    ID_StepArray[ID_StepArray.size()] = paymData.ID_Step;
    OldStatusArray[OldStatusArray.size()] = paymData.SfDefStatus;

  end;
  
  // Пакетно изменить статус  и дату оплаты УК по данным массивов-атрибутов класса
  macro doChange()  
    var stat = 0;
    // изменение статуса УК
    var sizea = DefIDArray.size();
    var strQuery = "UPDATE dsfdef_dbt " +
                   "SET t_Status = ?, t_DatePay = ? "  +
                   " WHERE t_FeeType = ? AND t_id = ? ";

    var upd = RsbSQLInsert(strQuery, 4, sizea );
    upd.AddParam( V_INTEGER, StatusArray  );
    upd.AddParam( V_DATE   , DatePayArray );
    upd.AddParam( V_INTEGER, FeeTypeArray );
    upd.AddParam( V_INTEGER, DefIDArray   );

    if(not upd.Insert())
      stat = 1;
    end;

    // Привязка изменений статусов к шагу операции
    var indx = 0, i = 0;
    while(indx<sizea)
      i = indx;
      var StatDocID = String(DefIDArray[i]:10)+String(OldStatusArray[i]:5)+String(ID_OperationArray[i]:10)+String(ID_StepArray[i]:5)+String(FeeTypeArray[i]:2);
      StatDocID = StrSubst( StatDocID, " ", "0" );

      AddDocumentToStepMass(2001, StatDocID, ID_OperationArray[i], ID_StepArray[i]); //  SFDDEFCOM_CHANGE_STATUS = 2001
      indx = indx + 1;
    end;
    return stat;
  end;
end;


// Класс служит для управления пакетной вставкой записей в таблицу dsfdocs_dbt и привязки вставленных записей к шагам операции.
class (RsbSQLInsert) TSfDocsInserter 
 InitRsbSQLInsert("sfdocs.dbt");

 macro connectOprDocs( paymData: TSfPaymBatchData )
   AddRecord(paymData.sfdocs);
   
   var DocID = String(paymData.sfdocs.rec.LinkKind:5)+String(paymData.sfdocs.rec.ObjectType:5)+String(paymData.sfdocs.rec.ObjectID:10);
   DocID = StrSubst( DocID, " ", "0" );

   AddDocumentToStepMass(2020, DocID, paymData.ID_Operation, paymData.ID_Step); // SFPAY_CONNECT_SFDOCS = 2020
 end;
end;


class (BilBookEntryBatchCharger) TSfPaymBBEntryCharger

  // Формирование параметров ЗК СФ по ТО/УК
  macro addBilBookEntryParam( facturaID    , // идентификатор СФ
                              paym         , // буфер платежа
                              ID_Operation ,
                              ID_Step 
                            )

    var bilfDocArray = TArray;
    bilfDocArray[0] = TRecHandler("bilf_doc.rec");
    bilfDocArray[0].Clear();
    bilfDocArray[0].rec.DocID   = string( paym.rec.PaymentID() );
    bilfDocArray[0].rec.DocKind = paym.rec.DocKind;
    bilfDocArray[0].rec.FIID    =  paym.rec.BaseFIID;
    bilfDocArray[0].rec.Amount  = paym.rec.BaseAmount;

    add(FacturaID, bilfDocArray, paym.rec.ValueDate, ID_Operation, ID_Step);
  end;

end;                            

class (TSfPaymBatchCharger) TSfPaymBatchCompletion ( accTrn:RsbBatchPaymTrn )
  private var m_PaymDataArray       = TArray;                             // массив объектов класса TSfPaymBatchData;
  private var m_bbeBatchCharger     = TSfPaymBBEntryCharger();            //объект класса BilBookEntryBatchCharger для управления пакетной обработки ЗК СФ. В конструктор класса передается в качестве параметра размер кэша, равный 0.
  private var m_DscntNDSSumInserter = RsbSQLInsert("sfdscntnds_sum.tmp"); //класс для управления пакетной вставкой записей во временную таблицу sfdscntnds_sum.tmp
  private var m_DscntNDSSumIndex = 0;
  private var m_CarryPaymDataArray  = TArray;                             // массив индексов объектов класса TSfPaymBatchData по проводкам;

  // Формирование параметров для определения суммы НДС
  private macro addDscntNDsSum( ServKind, InvoiceID, sfdef, paym, CalcAccount, CalcNDSAccount ) 
    var dscntnds_sum = TRecHandler("sfdscntnds_sum.tmp");

    ClearRecord(dscntnds_sum);
    dscntnds_sum.rec.ArrayIndex = m_DscntNDSSumIndex;
    dscntnds_sum.rec.InvoiceID = InvoiceID;
    dscntnds_sum.rec.paymentid = paym.rec.PaymentID;
    dscntnds_sum.rec.PmPayerFIID = paym.rec.FIID;
    dscntnds_sum.rec.PmReceiverFIID = paym.rec.PayFIID;
    dscntnds_sum.rec.PmValueDate = paym.rec.ValueDate;
    dscntnds_sum.rec.NDSRateValue = sfdef.rec.NDSRateValue;
    dscntnds_sum.rec.CalcAccount = CalcAccount;
    dscntnds_sum.rec.CalcNDSAccount = CalcNDSAccount;

    if(InvoiceID != 0)
      dscntnds_sum.rec.FeeType = SF_FEE_TYPE_INVOICE;
    else
      dscntnds_sum.rec.FeeType = sfdef.rec.FeeType;
    end;

    if(paym.rec.PayFIID == paym.rec.FIID)
      dscntnds_sum.rec.PaidNDS_In = dscntnds_sum.rec.PaidNDS_Out = Paym.rec.FuturePayerAmount;
    else
      dscntnds_sum.rec.PaidNDS_In = dscntnds_sum.rec.PaidNDS_Out = Paym.rec.FutureReceiverAmount;
    end;

    if(ServKind == PTSK_DEPOS)
      dscntnds_sum.IsDepoData = "X";
    end;

    m_dscntNDSSumInserter.addRecord(dscntnds_sum);
    m_DscntNDSSumIndex = m_DscntNDSSumIndex + 1;
  end;

  // Формирование параметров для учета НДС по ТО
  private macro sfInvDiscountNDSParams() 
    var prevInvoiceID = 0;
    
    var rs = getRecordSetForSfInv();

    while(rs.moveNext())
      var paymData = TSfPaymBatchData;

      var SFInv    = TRecHandler("sfinv.dbt");
      var PmPaym   = TRecHandler("pmpaym.dbt");
      var SFDef    = TRecHandler("sfdef.dbt");
      var SFComiss = TRecHandler("sfcomiss.dbt");

      GetRecHandlerValueByRecordset(rs, SFInv, "inv");
      GetRecHandlerValueByRecordset(rs, PmPaym,"paym");
      GetRecHandlerValueByRecordset(rs, SFDef, "sfdef");
      GetRecHandlerValueByRecordset(rs, SFComiss, "sfcomiss");

      paymData.ID_Operation = rs.value("sfpaymOperationID");
      paymData.ID_Step = rs.value("sfpaymStepID");

      paymData.PaymentID = PmPaym.rec.PaymentID;
      paymData.ValueDate = PmPaym.rec.ValueDate;

      paymData.FeeType = SFDef.rec.FeeType;
      paymData.SfDefID = SFDef.rec.ID;
      paymData.SfDefStatus = SFDef.rec.Status;

      if(prevInvoiceID == SFInv.rec.InvoiceID)
        if(SFComiss.rec.ServiceKind == PTSK_DEPOS)
          paymData.afterSfPayData.sfDefComs[paymData.afterSfPayData.sfDefComs.size()] = SFDef;
        end;

        m_PaymDataArray[m_PaymDataArray.size()] = paymData;
      else
        prevInvoiceID == SFInv.rec.InvoiceID;

        if(SFInv.rec.FacturaID > 0)
          paymData.FacturaID = SFInv.rec.FacturaID;
          m_bbeBatchCharger.addBilBookEntryParam( SFInv.rec.FacturaID, PmPaym, paymData.ID_Operation, paymData.ID_Step);
        end;

        if((SFInv.rec.NDSAmount != $0) AND (SfInv.rec.IsIncluded == "X"))
          // сформировать параметры проводки по учету НДС:
          paymData.trnData = RsbAccTransactionData;
          paymData.trnData.Chapter         = 1;
          paymData.trnData.Date_Carry      = PmPaym.rec.ValueDate; // Дата проводки
          paymData.trnData.FIIDPayer       = SFInv.rec.PayFIID;
          paymData.trnData.AccountPayer    = rs.value("sfpaymNdsAcrualAccount");
          paymData.trnData.FIIDReceiver    = NATCUR;
          paymData.trnData.AccountReceiver = rs.value("sfpaymMinusNdsAccount");
          paymData.trnData.SumReceiver     = 0; // дозаполняется позже
          paymData.trnData.Department      = {OperDprt};
          paymData.trnData.Ground          = "Оплата НДС комиссии " + SFComiss.rec.Code;

          // Заполнить параметры привязки проводки к ТО paymData.sfdocs:
          paymData.sfdocs = TRecHandler("sfdocs.dbt");
          paymData.sfdocs.Clear();
          paymData.sfdocs.rec.LinkKind   = SFDOCS_LINKKIND_NDSDISCOUNT;
          paymData.sfdocs.rec.ObjectType = OBJTYPE_SFINVOICE;
          paymData.sfdocs.rec.ObjectID   = SFInv.rec.InvoiceID;
          paymData.sfdocs.rec.AccTrnID   = 0; // заполняется в CommitFunction

          // Если вид обслуживания комиссии sfcomiss.ServKind равен "Депозитарное обслуживание", то сформировать параметры для учета НДС в депозитарии:
          if(SFComiss.rec.ServiceKind == PTSK_DEPOS)
            paymData.afterSfPayData = TAfterSfPayData;
            paymData.afterSfPayData.sfDefComs = TArray;
            paymData.afterSfPayData.sfDefComs[0] = SfDef;
            paymData.afterSfPayData.PaymentID = PmPaym.rec.PaymentID;
          end;
        end;
    
        // Сформировать параметры расчеты суммы проводки по учету НДС:
        AddDscntNDsSum (SFComiss.rec.ServiceKind, SFInv.rec.InvoiceID, SFDef, PmPaym, rs.value("sfpaymCalcAccount"), rs.value("sfpaymCalcNdsAccount") );

        m_PaymDataArray[m_PaymDataArray.size()] = paymData;
      end;

    end;
  end;

  // Формирование параметров по учету НДС по УК
  private macro sfDefDiscountNDSParams() 
    var rs = getRecordSetForSfDef();

   
    while(rs.moveNext())
      var paymData = TSfPaymBatchData;

      var PmPaym   = TRecHandler("pmpaym.dbt");
      var SFDef    = TRecHandler("sfdef.dbt");
      var SFComiss = TRecHandler("sfcomiss.dbt");

      GetRecHandlerValueByRecordset(rs, PmPaym,"paym");
      GetRecHandlerValueByRecordset(rs, SFDef, "sfdef");
      GetRecHandlerValueByRecordset(rs, SFComiss, "sfcomiss");

      paymData.ID_Operation = rs.value("sfpaymOperationID");
      paymData.ID_Step = rs.value("sfpaymStepID");

      paymData.PaymentID = PmPaym.rec.PaymentID;
      paymData.ValueDate = PmPaym.rec.ValueDate;

      paymData.FeeType = SFDef.rec.FeeType;
      paymData.SfDefID = SFDef.rec.ID;

      if(SFDef.rec.FacturaID > 0)
        paymData.FacturaID = SFDef.rec.FacturaID;
        m_bbeBatchCharger.addBilBookEntryParam( SFDef.rec.FacturaID, PmPaym, paymData.ID_Operation, paymData.ID_Step);
      end;

      if((SFDef.rec.SumNDS != $0) AND (SFDef.rec.IsIncluded == "X"))
        // Сформировать параметры проводки по учету НДС:
        paymData.trnData = RsbAccTransactionData;
        paymData.trnData.Chapter         = 1;
        paymData.trnData.Date_Carry       = PmPaym.rec.ValueDate; // Дата проводки
        paymData.trnData.FIIDPayer       = GetSfDefPayFIID(sfdef, sfcomiss);
        paymData.trnData.AccountPayer    = rs.value("sfpaymNdsAcrualAccount");
        paymData.trnData.FIIDReceiver    = NATCUR;
        paymData.trnData.AccountReceiver = rs.value("sfpaymMinusNdsAccount");
        paymData.trnData.SumReceiver     = 0; // дозаполняется позже
        paymData.trnData.Department      = {OperDprt};
        paymData.trnData.Ground          = "Оплата НДС комиссии " + SFComiss.rec.Code;

        // Заполнить параметры привязки проводки к УК paymData.sfdocs:
        paymData.sfdocs = TRecHandler("sfdocs.dbt");
        paymData.sfdocs.Clear();
        paymData.sfdocs.rec.LinkKind   = SFDOCS_LINKKIND_NDSDISCOUNT;
        paymData.sfdocs.rec.ObjectType = getObjectType(sfdef.rec.feeType);
        paymData.sfdocs.rec.ObjectID   = sfdef.rec.ID;
        paymData.sfdocs.rec.AccTrnID   = 0; // заполняется в CommitFunction

       //  Если вид обслуживания комиссии sfcomiss.ServKind равен "Депозитарное обслуживание", то сформировать параметры для учета НДС в депозитарии:
       if(SFComiss.rec.ServiceKind == PTSK_DEPOS)
         paymData.afterSfPayData = TAfterSfPayData;
         paymData.afterSfPayData.sfDefComs = TArray;
         paymData.afterSfPayData.sfDefComs[0] = SfDef;
         paymData.afterSfPayData.PaymentID = PmPaym.rec.PaymentID;
       end;

       AddDscntNDsSum (SFComiss.rec.ServiceKind, 0, SfDef, PmPaym, rs.value("sfpaymCalcAccount"), rs.value("sfpaymCalcNdsAccount") );
      end;

      m_PaymDataArray[m_PaymDataArray.size()] = paymData;
    end;
  end;

  // Расчет суммы НДС
  private macro getPaidNDSSum() 
    var strSql, cmd;

    // установка признак частичный/полный платеж
    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             " SET tmp.t_IsPaymFull = 'X' " +
             " WHERE tmp.t_InvoiceID <> 0 " +
             " AND NOT EXISTS " +
             "   (SELECT *  FROM dsfinvlnk_dbt WHERE t_InvoiceID = tmp.t_InvoiceID AND t_PaymentID <> tmp.t_PaymentID " +
             "    AND (t_PayAmount <> 0 OR t_PaidNDS <> 0) AND t_LinkStatus = 0)"; 
 
    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.execute();   

    //  Если счета t_CalcAccount и t_CalcNdsAccount совпадают, то сумма НДС t_PaidNDS_Out определяется из данных самого платежа
    // 1) Получить сумму НДС путем пересчета t_PaidNDS_In в НацВал по основному курсу на t_PmValueDate.
    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP " +
             "   SET t_PaidNDS_Out = RSI_RSB_FIInstr.ConvSum(t_PaidNDS_In, t_PmReceiverFIID, ?, t_PmValueDate, 0) "+
             "     WHERE t_CalcAccount = t_CalcNdsAccount";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, NATCUR );
    cmd.execute();   


    // 2) Для УК рассчитать сумму НДС как t_PaidNDS_Out * $(ставку НДС)/ (100 + $(ставка НДС)), 
    // где $(Ставка НДС) - это sfdef.NDSRateValue.
    strSql = "UPDATE DSFDSCNTNDS_SUM_TMP " +
             "  SET " +
             "    t_PaidNDS_Out = t_PaidNDS_Out * t_NDSRateValue / (100 + t_NDSRateValue), " +
             "    t_depoPaidNDS = t_PaidNDS_In * t_NDSRateValue / (100 + t_NDSRateValue), " +
             "    t_depoPaidAmount = t_PaidNDS_In - t_PaidNDS_In * t_NDSRateValue / (100 + t_NDSRateValue), " +
             "    t_depoPayFIID = t_PmReceiverFIID " +
             "  WHERE t_CalcAccount = t_CalcNdsAccount " +
             "  AND t_feeType IN (?, ?, ?)";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE );
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE   );
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
    cmd.execute();   

    // Для полного платежа по TO определяем сумму НДС как t_InvNDSAmount - t_InvPaidNDS
    strSql = "UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             "  SET (t_PaidNDS_Out, t_depoPaidNDS, t_depoPaidAmount, t_depoPayFIID) = " +
             "    (SELECT inv.t_NDSAmount - inv.t_PaidNDS, " +
             "      inv.t_NDSAmount - inv.t_PaidNDS, " +
             "      inv.t_TotalAmount - inv.t_PaidAmount - inv.t_NDSAmount - inv.t_PaidNDS, " +
             "      inv.t_PayFIID FROM dsfinv_dbt inv WHERE inv.t_InvoiceID = tmp.t_InvoiceID) " +
             "  WHERE t_CalcAccount = t_CalcNdsAccount  AND t_InvoiceID <> 0 AND t_IsPaymFull = 'X'";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.execute();   

    // Перевести PaidNDS из валюты ТО в валюту оплаты передав в функцию дату валютирования платежа t_PmValueDate
    // Требуется реализации функции RSI_RSB_FIInstr.FI_CalcSumTypeEx, аналогичную RSI_RSB_FIInstr.FI_CalcSumType,
    // но которая возвращала бы сумму и не принимала выходных параметров
    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             "    SET t_PaidNDS_Out = " +
             "      (SELECT DECODE (inv.t_IsFixedRate, 'X', " +
             "        t_PaidNDS_Out * inv.t_FixedRateValue, " +
             "        RSI_RSB_FIInstr.FI_CalcSumTypeEx(t_PaidNDS_Out, inv.t_InvoiceFIID, inv.t_PayFIID, inv.t_PayRateID, " +
             "          DECODE(inv.t_PayRateDateKind, ?, " +
             "                 t_PmValueDate - 1, t_PmValueDate))) " +
             "       * (1 + inv.t_PayRatePercent/100.0) " + 
             "        FROM dsfinv_dbt inv WHERE inv.t_InvoiceID = tmp.t_InvoiceID) " +
             "       WHERE t_CalcAccount = t_CalcNdsAccount AND t_InvoiceID <> 0 AND t_IsPaymFull = 'X'";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, SFINV_RATEDATEKIND_PAYDAY );
    cmd.execute();   

    // ... Затем в НацВал по основному курсу на t_PmValueDate.
    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             "   SET t_PaidNDS_Out = " +
             "     (SELECT RSI_RSB_FIInstr.ConvSum(t_PaidNDS_Out, inv.t_PayFIID, ?, t_PmValueDate, 0) " +
             "       FROM  dsfinv_dbt inv WHERE inv.t_InvoiceID = tmp.t_InvoiceID) " +
             "   WHERE t_CalcAccount = t_CalcNdsAccount AND t_InvoiceID <> 0 AND t_IsPaymFull = 'X'";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, NATCUR );
    cmd.execute();   

    // Для частичного платежа по ТО
    strSql = "UPDATE DSFDSCNTNDS_SUM_TMP tmp" +
             "  SET (t_PaidNDS_Out, " +
             "      t_depoPaidNDS , " +
             "    t_depoPaidAmount , " +
             "    t_depoPayFIID) =  " +
             "    (SELECT inv.t_NDSAmount / inv.t_TotalAmount, " +
             "       tmp.t_PaidNDS_In * tmp.t_NDSRateValue / (100 + tmp.t_NDSRateValue), " +
             "       tmp.t_PaidNDS_In - tmp.t_PaidNDS_In * tmp.t_NDSRateValue / (100 + tmp.t_NDSRateValue), " +
             "       tmp.t_PmReceiverFIID " +
             "     FROM  dsfinv_dbt inv WHERE inv.t_InvoiceID = tmp.t_InvoiceID) " +
             "    WHERE t_CalcAccount = t_CalcNdsAccount  AND t_InvoiceID <> 0 AND t_IsPaymFull = 'X'"; 

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.execute();   

    // если счета t_CalcAccount и t_CalcNdsAccount не совпадают, то сумму определям из разносок по платежу
    strSql = "UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             "  SET (tmp.t_PaidNDS_Out, tmp.t_PmAddPI_FIID, " +
             "      tmp.t_depoPaidNDS, tmp.t_depoPayFIID ) = " +
             "    (SELECT pi.t_Amount, pi.t_FIID, pi.t_Amount, pi.t_FIID " + 
             "    FROM dpmaddpi_dbt pi " +
             "    WHERE pi.t_PaymentID = tmp.t_PaymentID AND pi.t_DebetCredit = ? " +  
             "      AND pi.t_Account = tmp.t_CalcNdsAccount AND rownum <= 1) " +
             "  WHERE t_CalcAccount != t_CalcNdsAccount "; 

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PRT_Credit );
    cmd.execute();   

    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP tmp " +
             "   SET tmp.t_depoPaidAmount =" +
             "     (SELECT pi.t_Amount " +
             "     FROM dpmaddpi_dbt pi " +
             "     WHERE pi.t_PaymentID = tmp.t_PaymentID AND pi.t_DebetCredit = ?  " +
             "       AND pi.t_Account = tmp.t_CalcAccount AND rownum <= 1)" +
             "   WHERE t_CalcAccount != t_CalcNdsAccount ";

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PRT_Credit );
    cmd.execute();   
             
     // Если pmaddpi.FIID не НацВал, то перевести PaidNDS из pmaddpi.FIID в НацВал по основному курсу на дату Paym.ValueDate
    strSql = " UPDATE DSFDSCNTNDS_SUM_TMP tmp "
             "   SET t_PaidNDS_Out = RSI_RSB_FIInstr.ConvSum(t_PaidNDS_Out, t_PmAddPI_FIID, ?, t_PmValueDate, 0) "
             "   WHERE t_CalcAccount != t_CalcNdsAccount AND t_PmAddPI_FIID != ? "; 

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, NATCUR );
    cmd.addParam( "", RSDBP_IN, NATCUR );
    cmd.execute();   

  end;

  // Корректировка сумм проводок
  private macro CorrectAccTrn() 
    var strSql, cmd, rs; 
    // Пакетно перенести записи во временную таблицу
    m_dscntNDSSumInserter.Insert();

    // Определить сумму НДС 
    getPaidNDSSum();

    
    strSql = " SELECT * " +
             " FROM dsfdscntnds_sum_tmp" + 
             " ORDER BY t_ArrayIndex"; 

    cmd = RsdCommand( strSql ); 
    cmd.NullConversion = true;

    cmd.execute();   
    var i = 0;
    rs = RsdRecordset( cmd ); 
    while( rs.moveNext() )
      i = rs.value("t_ArrayIndex");

      // Дозаполнить сумму проводки
      m_PaymDataArray[i].TrnData.SumReceiver = rs.value("t_PaidNDS_Out");

      var IsDepoData = rs.value("t_IsDepoData");
      if(IsDepoData == "X")
        m_PaymDataArray[i].afterSfPayData.depoPaidAmount = rs.value("t_depoPaidAmount");
        m_PaymDataArray[i].afterSfPayData.depoPaidNDS = rs.value("t_depoPaidNDS");
        m_PaymDataArray[i].afterSfPayData.depoPayFIID = rs.value("t_depoPayFIID");
      end;
    end;
  end;

  // Формирование параметров ЗК СФ по ТО/УК
  private macro addBilBookEntryParam() 
  end;

  // Выполнение действий, необходимых для получения данных для формирования ЗК СФ по РДД и проводок по учету НДС
  private macro PreExecute() : integer 
    ClearTempTable("DSFPAYMBATCH_PARAMS_TMP");
    ClearTempTable("DSFDSCNTNDS_SUM_TMP");

    // На основании записей в doprtemp_tmp заполнить временную таблицу dsfpaymbatch_params_tmp данными, необходимыми для выполнения проводок по учету в доход ТО и УК:
    fillSfPaymParams(false); // ??

    sfInvDiscountNDSParams();

    sfDefDiscountNDSParams();

    CorrectAccTrn();

    m_bbeBatchCharger.preexecute();
  end;

  private macro getFindPaymDataIndex( PaymentID : integer )
    var indx = 0, i = 0, FindPaymData = null;
    var sizea = m_PaymDataArray.size();
    
    while( indx < sizea ) 
      i = indx;
      if( m_PaymDataArray[i].PaymentID == PaymentID )
        FindPaymData = i;
        indx = sizea;
      end;
      indx = indx + 1;
    end;

    return FindPaymData;
  end;

  // Добавление в кэш проводок по учету НДС на основе платежа PayementID
  macro AddTrnData( PaymentID : integer ) 
    var indx = 0, i = 0, FindPaymData = -1;
    var sizea = m_PaymDataArray.size();
    while(indx<sizea) 
      i = indx;
      if(m_PaymDataArray[i].PaymentID == PaymentID)
        FindPaymData = i;
        indx = sizea;
      end;
      indx = indx + 1;
    end;

    if(FindPaymData != -1)
      if(m_PaymDataArray[FindPaymData].Error == 0 )
        if(m_PaymDataArray[FindPaymData].FacturaID != 0)
          m_PaymDataArray[FindPaymData].Error = m_bbeBatchCharger.get_error(m_PaymDataArray[FindPaymData].FacturaID, m_PaymDataArray[FindPaymData].ValueDate, m_PaymDataArray[FindPaymData].ID_Operation, m_PaymDataArray[FindPaymData].ID_Step );
        end;
        if((m_PaymDataArray[FindPaymData].Error == 0) AND (m_PaymDataArray[FindPaymData].trnData != NULL))
           // Сохранить связь между формируемой проводкой и платежом:
           m_PaymDataArray[FindPaymData].iCarry = m_AccTrn.AddTransaction( PaymentID, m_PaymDataArray[FindPaymData].trnData, 2/*ACCTRN_STATUS_DOCUMENT*/, 0, 0, m_PaymDataArray[FindPaymData].ID_Operation, m_PaymDataArray[FindPaymData].ID_Step, false );
           m_CarryPaymDataArray[m_PaymDataArray[FindPaymData].iCarry]= FindPaymData; // для быстрого поиска
        end;
      end;
    end;
  end;

  macro CommitFunction( PaymentID : integer, iPackStartRec : integer, iCarry : integer, ID_Operation : integer, ID_Step : integer ) : integer
    var stat = 0;

    var FindPaymData = null;

    FindPaymData = m_CarryPaymDataArray[iCarry];

    if((FindPaymData != null) AND (m_PaymDataArray[FindPaymData].PaymentID == PaymentID) AND (m_PaymDataArray[FindPaymData].iCarry == iCarry))
      if(m_PaymDataArray[FindPaymData].sfdocs != null)
        m_PaymDataArray[FindPaymData].sfdocs.rec.AccTrnID = m_AccTrn.GetTransaction(iCarry).AccTrnID;
      end;
    end;

    if( FindPaymData == null )
      FindPaymData = getFindPaymDataIndex(PaymentID);
    end;

    if((FindPaymData != null) AND (m_PaymDataArray[FindPaymData].PaymentID == PaymentID))
      stat = m_PaymDataArray[FindPaymData].Error;
    end;

    return stat;
  end;

  macro RollbackFunction( PaymentID : integer, iPackStartRec : integer, iCarry : integer, ID_Operation : integer, ID_Step : integer )
    var FindPaymData = null;

    FindPaymData = m_CarryPaymDataArray[iCarry];

    if( FindPaymData == null )
      FindPaymData = getFindPaymDataIndex(PaymentID);
    end;

    if((FindPaymData != null) AND (m_PaymDataArray[FindPaymData].PaymentID == PaymentID) AND (m_PaymDataArray[FindPaymData].Error == 0))
       // Сделать невалидной ЗК СФ по платежу:
      m_bbeBatchCharger.set_error(m_PaymDataArray[FindPaymData].FacturaID, m_PaymDataArray[FindPaymData].ValueDate, m_PaymDataArray[FindPaymData].ID_Operation, m_PaymDataArray[FindPaymData].ID_Step, 1);
      m_PaymDataArray[FindPaymData].Error = 1;
      m_PaymDataArray[FindPaymData].ErrorMesage = "Ошибка выполнения проводки";
    end;
  end;

  macro AppRunFunction() : integer
    // Перенести ЗК в постоянную таблицу БД:

    m_bbeBatchCharger.execute();

    var statusChanger = TSfDefStatusChanger;
    var sfDocsCache = TSfDocsInserter();

    var afterSfPayDataArray = TArray();
  
    var indx = 0, i = 0;
    var sizea = m_PaymDataArray.size();
    while(indx<sizea) 
      i = indx;
      if(m_PaymDataArray[i].Error == 0)
        // Привязать изменения статуса обработанных УК на "ОПЛАЧЕНО" к шагам операций
        statusChanger.connectOprDocs(m_PaymDataArray[i]);

        if(m_PaymDataArray[i].sfdocs != null)
          if(m_PaymDataArray[i].sfdocs.rec.AccTrnID != 0)
            sfDocsCache.connectOprDocs(m_PaymDataArray[i]);
          end;
        end;

        if(m_PaymDataArray[i].afterSfPayData != null)
          afterSfPayDataArray[afterSfPayDataArray.size()] = m_PaymDataArray[i].afterSfPayData;
        end;

      end;
      indx = indx + 1;
    end;

    // Пакетно изменить статус  и дату оплаты УК:
    statusChanger.doChange();

    // Пакетно вставить записи в dsfdocs_dbt:
    sfDocsCache.Insert();


    AfterSfPayBatch(afterSfPayDataArray);

    return 0;
  end;

  macro GetErrorPayment(PaymentID, ErrorMsg:@string):integer
    var Error = 0;
    var FindPaymData = null;
    
    FindPaymData = getFindPaymDataIndex(PaymentID);

    if((FindPaymData != null) AND (m_PaymDataArray[FindPaymData].PaymentID == PaymentID))
      Error    = m_PaymDataArray[FindPaymData].Error;
      ErrorMsg = m_PaymDataArray[FindPaymData].ErrorMesage;
    end;

    return Error;
  end;

  initTSfPaymBatchCharger(accTrn);
  PreExecute();
end;
