/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : cpscmscr.mac
  Created     : 15.05.99
  Programmer  : Зайцев А.В.
  Description : Печать отчета сканирования отложенных платежных документов

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter;

RECORD pscp_firstdoc (pscpord);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2,
        ReportLineWidth = 78;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   var stat, Purpose, i = 1;
   array StringArray;

        record rmprop (pmrmprop);

        setbuff (pscp_firstdoc,firstdoc);

        if (Вызов==Первый)
[    Отчет о групповом создании операций для отложенных платежных документов.    ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Операция создана успешно";
                end;

                if ( FindPayment(
                         0,      /* Индентификатор неизвестен    */
                         PM_PURP_BANKPAYMENT,      
                         0,      /* Subpurpose                   */
                         28, /* Вид первичного документа */ 
                         pscp_firstdoc.OrderID,
                         true,   /* Поиск по реальной базе       */
                         null,
                         null,
                         null,
                         null,
                         rmprop ) != 0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                end;

         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);
[|#########|######|############################################################|](rmprop.Number,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;