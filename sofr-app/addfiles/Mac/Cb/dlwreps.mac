/*
$Name:        dlwreps.mac
$Module:      Файл подсистемы "Векселя"
$Description: Печать отчета из файла меток.
*/

/* ───────────────────────────────────────────────────────────────────────────┐
    RS-Bank                                        R-Style Software Lab Ltd
    Файл подсистемы "Векселя"

    Печать отчета из файла меток.

    SRR 24.04.2001
    IR 21.10.02 Перенес из векселей
└─────────────────────────────────────────────────────────────────────────── */
Import rsexts, dlwrept, dlreptls;

PRIVATE FILE servtags() txt;
PRIVATE FILE termtags() txt write;

private macro GetUsePoiMode():bool
   var usePoiMode:bool = false;
   var StrErr:string = "";
   var RegKey:string = "BANK_INI\\WINDOWS REPORT\\ИСПОЛЬЗОВАТЬ APACHE POI";
  
   if( NOT GetRegistryValue(RegKey, V_UNDEF, usePoiMode, StrErr) )
       MsgBox("В реестре не найден ключ ["+RegKey+"]!|", StrErr);
   end; 
   
   return usePoiMode;
end;

// Печать отчета из файла меток
// FullServerTagFileName - имя текстового файла с метками
// RealFileName          - реальное имя файла документа, полученное при формировании документа Word
// CloseDoc              - признак необходимости закрытия документа
// SaveDocTwice          - признак необходимости сохранения документа дважды (для возможности копирования первого экземпляра)
// UsePoiMode            - флаг печати через POI. Если указан, то используем его, если нет - то значение настройки "BANK_INI\\WINDOWS REPORT\\ИСПОЛЬЗОВАТЬ APACHE POI"
// FileNamePDF           - имя файла PDF. Если не задано, то PDF файл не будет формироваться
// FileNameHTML          - имя файла HTML. Если не задано, то HTML файл не будет формироваться
// ShowDoc:bool          - признак отвечающий за показ документа Word на экран. По умолчанию - показывать
MACRO DLWREPS_PrintReportFromTagFile(FullServerTagFileName:string, RealFileName:@string, CloseDoc:bool, SaveDocTwice:bool, UsePoiMode:bool, FileNamePDF:string, FileNameHTML:string, ShowDoc:bool)
   var stat = false;
   var FullTermTagFileName;

   if (not (ValType(UsePoiMode) == V_BOOL))
       UsePoiMode = GetUsePoiMode();
   end;
   if (not (ValType(ShowDoc) == V_BOOL))
       ShowDoc = True;
   end;


   if (UsePoiMode)
       if (DLREPTLS_FillTemplsPaths())
         MsgBox("Ошибка при получении путей к шаблонам из настроек банка");
       end;
       stat = POIDLWREPT_PrintReportFromTagFile(FullServerTagFileName, false, @RealFileName, CloseDoc, SaveDocTwice, FileNamePdf, FileNameHtml, ShowDoc);
   elif (IsStandAlone) /*Двухзвенка*/
       if (DLREPTLS_FillTemplsPaths())
         MsgBox("Ошибка при получении путей к шаблонам из настроек банка");
       end;
       stat = DLWREPT_PrintReportFromTagFile (FullServerTagFileName, false, @RealFileName, CloseDoc, SaveDocTwice);   
   else /*Трехзвенка*/
       FullTermTagFileName = CallRemoteRsl("dlwrept.mac", "DLREPTL_GetTxtFileDir") + "wrep." + UserNumber;        
       if (not CopyFile (FullServerTagFileName, "$" + FullTermTagFileName))
           MsgBox ("Ошибка при передаче файла на терминал");
       else
           RealFileName = CallRemoteRsl ("dlwrept.mac" ,"DLWREPT_PrintReportFromTagFileEx", FullTermTagFileName, true, CloseDoc, SaveDocTwice);
           if(RealFileName == "")
             stat = false;
           else
             stat = true;
           end;
       end;
   end;

   return stat;
end;

/*Слить в один отчет список отчетов из файла меток*/
MACRO DLWREPS_MergeReportFromTagFile(FullServerTagFileName)
  var stat = false;
  var FullServerRepFileName;
  var RepFileName, RepFileExt;
  var FullTermRepFileName;
  var FullTermTagFileName;
  var TermTxtFileDir;

  if (IsStandAlone) /*Двухзвенка*/
    if (DLREPTLS_FillTemplsPaths())
      msgbox("Ошибка при получении путей к шаблонам из настроек банка");
    end;
    stat = DLWREPT_MergeReportFromTagFile(FullServerTagFileName, false);
  else /*Трехзвенка*/

    TermTxtFileDir = CallRemoteRsl("dlwrept.mac", "DLREPTL_GetTxtFileDir");
    /* Скопировать все файлы с отчетами на терминал */
    if (not open(servtags, FullServerTagFileName))
      msgbox ("Источник данных не открыт");
      stat = false;
    else
      stat = true;
      /* Перенести имена файлов отчетов в tag-файл с учетом их нового расположения на терминале */
      FullServerTagFileName = FullServerTagFileName + "_term." + UserNumber;
      if (not open(termtags, FullServerTagFileName))
        msgbox ("Источник данных не открыт");
        stat = false;
      else

        stat = true;
        while(stat and next(servtags))
          /* Сформировать новый путь к файлу отчета на терминале */
          FullServerRepFileName = trim(servtags.str);
          SplitFile(FullServerRepFileName, RepFileName, RepFileExt);
          FullTermRepFileName = TermTxtFileDir + RepFileName + "." + UserNumber + RepFileExt;
          /* Вставить новый путь к файлу на терминале в tag-файл */
          if(not CopyFile(FullServerRepFileName, "$" + FullTermRepFileName))
            msgbox ("Ошибка при передаче файла \"", FullServerRepFileName, "\" на терминал");
          else
            termtags.str = FullTermRepFileName;
            Insert(termtags);
          end;
        end;

        close(termtags);
      end;

      close(servtags);
    end;
    
    if (stat)
      FullTermTagFileName = TermTxtFileDir + "wrep." + UserNumber;        
      if (not CopyFile (FullServerTagFileName, "$" + FullTermTagFileName))
          MsgBox ("Ошибка при передаче файла на терминал");
      else
          stat = CallRemoteRsl ("dlwrept.mac" ,"DLWREPT_MergeReportFromTagFile", FullTermTagFileName, true);
      end;
    end;
  end;

  return stat;
end;
