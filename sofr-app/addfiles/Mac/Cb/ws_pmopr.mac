/*
$Name:               ws_pmopr.mac
$Module:            РКО, ББ
$Description:      ТК массовое выполнение платежей
*/
import BankInter, PaymInter, WldInter, oralib, likepy, pmwsutils;

class TWsPmOpr(ID, _State, Msg)
  var PaymentID = ID;
  var State = _State;
  var Message = Msg;
end;

macro PmExecuteOperation( payments, Symbol:string )
  var i;
  execSQL("truncate table dpmrunopr_tmp");
  var ins = RsbSQLInsert(string("insert into dpmrunopr_tmp (t_PaymentID, t_DocKind, t_State) ",
                            "(select NVL(t_PaymentID, ?), NVL(t_PrimDocKind, 0), 0  from dpmpaym_dbt right join dual on t_PaymentID = ?)"), 2, payments.size);
  var id = TArray(payments.size);

  for(i, payments)
    id[id.size] = i.PaymentID;
  end;
  var old = SetDialogFlag(0);
  ins.AddParam(V_INTEGER, id);
  ins.AddParam(V_INTEGER, id);

  if (not ins.Insert())
    SetDialogFlag(old);
    RunError("Ошибка при вставке платежей в таблицу dpmrunopr_tmp");
  end;
  SetDialogFlag(old);

  GenerateError(PM_MassExecuteOperation(true, Symbol));

  var rs = execSQLselect("select t_PaymentID, t_State, t_ErrorMessage from dpmrunopr_tmp");
  var res = TArray();
  while (rs.moveNext())
    res[res.Size] = TWsPmOpr(rs.value("t_PaymentID"), rs.value("t_State"), rs.value("t_ErrorMessage"));
  end;
  execSQL("truncate table dpmrunopr_tmp");
  return res;
end;
