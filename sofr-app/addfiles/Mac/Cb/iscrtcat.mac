/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : iscrtcat.mac
  Description : Заполнение списка категорий сертификата и их значений

└───────────────────────────────────────────────────────────────────────────*/

import FIInter, PTInter, CTInter, "MoCommon.mac","adress.mac", RsbDataSet, "cb_sql.mac";

const MAXINT32 = 2147483648;

const OBJTYPE_ISCERTIF   = 106; /* Тип объекта - сертификат ц/б */
const OBJTYPE_ISFIS      = 12; /* Тип объекта - ц/б */
const CodeForm711        = 1; /* Вид категории - код типа ц/б для формы 711 */
const STATE_BODY         = 1;
const STATE_BODY_SUBJECT = 2;
const BANK               = 3;
const RESIDENT           = 4;
const STATE_BODY_FOREIGN = 5;
const BANK_NOTRESIDENT   = 6;
const CLIENT_NOTRESIDENT = 7;

//значения (номера) категории "Код для 711 формы"
const OBJATTR_711_BON1  = 1;  //облигации федеральных органов исполнительной власти и облигации Банка России
const OBJATTR_711_BON2  = 2;  //облигации органов исполнительной власти субъектов Российской Федерации и муниципальных образований
const OBJATTR_711_BON3  = 3;  //облигации кредитных организаций-резидентов
const OBJATTR_711_BON4  = 4;  //облигации прочих резидентов
const OBJATTR_711_BON5  = 5;  //облигации иностранных государств и облигации иностранных Центральных Банков
const OBJATTR_711_BON6  = 6;  //облигации банков-нерезидентов
const OBJATTR_711_BON61 = 7;  //облигации банков-нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_BON7  = 8;  //облигации прочих нерезидентов
const OBJATTR_711_BON71 = 9;  //облигации прочих нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_DS1   = 10; //депозитные сертификаты кредитных организаций-резидентов
const OBJATTR_711_DS2   = 11; //депозитные сертификаты банков-нерезидентов
const OBJATTR_711_DS21  = 12; //депозитные сертификаты банков-нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_SS1   = 13; //сберегательные сертификаты кредитных организаций-резидентов
const OBJATTR_711_SS2   = 14; //сберегательные сертификаты банков-нерезидентов
const OBJATTR_711_SS21  = 15; //сберегательные сертификаты банков-нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_SHS1  = 16; //акции кредитных организаций-резидентов (обыкновенные)
const OBJATTR_711_SHS2  = 17; //акции кредитных организаций-резидентов (привилегированные)
const OBJATTR_711_SHS3  = 18; //акции прочих резидентов (обыкновенные)
const OBJATTR_711_SHS4  = 19; //акции прочих резидентов (привилегированные)
const OBJATTR_711_SHS5  = 20; //акции банков-нерезидентов
const OBJATTR_711_SHS51 = 21; //акции банков-нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_SHS6  = 22; //прочие акции нерезидентов
const OBJATTR_711_SHS61 = 23; //прочие акции нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_SHS7  = 24; //паи, доли инвестиционных фондов-нерезидентов
const OBJATTR_711_SHS71 = 25; //паи, доли инвестиционных фондов-нерезидентов, не квалифицированные в качестве ценных бумаг
const OBJATTR_711_SHS8  = 26; //паи, доли инвестиционных фондов-резидентов
const OBJATTR_711_BIL1  = 27; //векселя федеральных органов исполнительной власти
const OBJATTR_711_BIL2  = 28; //векселя органов исполнительной власти субъектов Российской Федерации и муниципальных образований
const OBJATTR_711_BIL3  = 29; //векселя кредитных организаций-резидентов
const OBJATTR_711_BIL4  = 30; //векселя прочих резидентов
const OBJATTR_711_BIL5  = 31; //векселя иностранного государства
const OBJATTR_711_BIL6  = 32; //векселя банков-нерезидентов
const OBJATTR_711_BIL7  = 33; //векселя прочих нерезидентов
const OBJATTR_711_DR    = 34; //депозитарные расписки
const OBJATTR_711_CON   = 35; //складское свидетельство
const OBJATTR_711_WTS   = 36; //варранты
const OBJATTR_711_OPN   = 37; //опционы
const OBJATTR_711_ENC   = 38; //закладные
const OBJATTR_711_OTHER = 39; //иное


/* Глобальные массивы для возврата списка категорий и их значений*/
var ListCategory    = TArray;
var ListValue       = TArray;

/*************************************************************************
  получение вида субъекта                                               
  вернет:
    1 - орган гос власти РФ
    2 - орган гос власти суб РФ, орган местной власти 
    3 - банк резедент 
    4 - резидент 
    5 - ИнострГос 
    6 - банк нерезидент
    7 - нерезидент
*************************************************************************/
MACRO GetKindParty( PartyID, Country, NotResident )
   if  (ВидСубъекта(PartyID, PTK_STATE_BODY))/*орган гос власти*/
       if( (Country == "") OR (Country == "RUS") )
          return STATE_BODY;
       elif( (Country != "") AND (Country != "RUS") )
          return STATE_BODY_FOREIGN;/*иностранное государство*/
       end;
   elif( ВидСубъекта(PartyID, PTK_STATE_BODY_SUBJECT) OR /*орган гос власти суб*/
         ВидСубъекта(PartyID, PTK_LOCAL_BODY) )/*орган местной власти*/
       if( (Country == "") OR (Country == "RUS") )
          return STATE_BODY_SUBJECT;
       end;
   elif(ВидСубъекта(PartyID, PTK_BANK))
       if(NotResident!="X")
          return BANK;/*банк резедент*/
       elif(NotResident=="X")
          return BANK_NOTRESIDENT;/*банк нерезедент*/
       end;
   elif(NotResident!="X")
          return RESIDENT;/*резедент*/
   elif(NotResident=="X")
          return CLIENT_NOTRESIDENT;/*нерезедент*/
  end;
  return 0;
END;


macro MakeDocumentID( FIID )
  return  L_Z(FIID,10);
end;

private macro GetAttrID(ObjectType, GroupID, CatVal)
  var query, DataSet;
  var AttrID = 0;

  query =   " select attr.t_AttrID from dobjattr_dbt attr, dobjgroup_dbt agroup "
          + "  where agroup.t_ObjectType = " + ObjectType
          + "    and agroup.t_GroupID = " + GroupID
          + "    and attr.t_ObjectType = (case when agroup.t_AttrObjectType > 0 and agroup.t_AttrGroupID > 0 then agroup.t_AttrObjectType else agroup.t_ObjectType end) "
          + "    and attr.t_GroupID = (case when agroup.t_AttrObjectType > 0 and agroup.t_AttrGroupID > 0 then agroup.t_AttrGroupID else agroup.t_GroupID end) "
          + "    and attr.t_CodeList = CHR(1) "
          + "    and attr.t_NumInList = " + CatVal;
  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    AttrID = DataSet.AttrID;
  end;                                                 
  return AttrID;
end;

private macro IsQualifiedFI(FIID, CheckDate)
   var query, DataSet;
   var isQualifiedBaseFI = 0;

   query =   " select rsb_fiinstr.FI_IsQualified("+FIID+", "+ GetSQLDate(CheckDate)+ ") as isQualifiedBaseFI from dual ";
   DataSet = TRsbDataSet(query);
   if(DataSet.moveNext())
     isQualifiedBaseFI = DataSet.isQualifiedBaseFI;
   end;
   return isQualifiedBaseFI;
end;


macro MakeCategory_711(Certif)
   var CategoryValue = 0, AttrID = 0;
   var CERTObjectID = MakeDocumentID( Certif.FIID );
   var Objatcor = TBFile("objatcor");
   var Objgroup = TBFile("objgroup");
   var Fininstr = TBFile("fininstr");
   var Party    = TBFile("party");

   record RecordAdress ( adress  );

   var Kind;
   
   Objatcor.rec.ObjectType  = OBJTYPE_ISFIS;
   Objatcor.rec.Object      = CERTObjectID;
   Objatcor.rec.GroupID     = CodeForm711;
   Objatcor.rec.ValidToDate = date(31,12,9999);
   Objatcor.rec.AttrID      = MAXINT32;
   if(GetLE(Objatcor) and (Objatcor.rec.ObjectType == OBJTYPE_ISFIS) and (Objatcor.rec.Object == CERTObjectID) and (Objatcor.rec.GroupID == CodeForm711) and (Objatcor.rec.ValidToDate >= {curdate}) )
      AttrID = Objatcor.rec.AttrID;
   else
      Fininstr.rec.FIID   = Certif.FIID;
      if(GetEQ(Fininstr))
         Party.rec.PartyID = Certif.Issuer;         
         if(GetEQ(Party)) /*Party.rec.NotResident*/
            if( (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_PREFERENCE_SHARE, Fininstr.rec.AvoirKind )) OR /*Акц прив*/
                  (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_PREFERENCE_CONV_SHARE, Fininstr.rec.AvoirKind )) )/*Акц прив с правом конверсии*/
                if(ВидСубъекта(Party.rec.PartyID, PTK_BANK))
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_SHS2;
                   elif(Party.rec.NotResident=="X")
                      if(not IsQualifiedFI(Certif.FIID, Certif.RegistrDate))
                        CategoryValue = OBJATTR_711_SHS51;
                      else
                        CategoryValue = OBJATTR_711_SHS5;
                      end;
                   end;
                else
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_SHS4;
                   elif(Party.rec.NotResident=="X")
                      if(not IsQualifiedFI(Certif.FIID, Certif.RegistrDate))
                        CategoryValue = OBJATTR_711_SHS61;
                      else
                        CategoryValue = OBJATTR_711_SHS6;
                      end;
                   end;
                end;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_EQUITY_SHARE, Fininstr.rec.AvoirKind ))/*Акц обыкн*/
                if(ВидСубъекта(Party.rec.PartyID, PTK_BANK))
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_SHS1;
                   elif(Party.rec.NotResident=="X")
                      CategoryValue = OBJATTR_711_SHS5;
                   end;
                else
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_SHS3;
                   elif(Party.rec.NotResident=="X")
                      CategoryValue = OBJATTR_711_SHS6;
                   end;
                end;

            elif( (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_ORDINARY_BOND, Fininstr.rec.AvoirKind ) ) OR /*облиг безкуп*/
                  (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_COUPON_BOND, Fininstr.rec.AvoirKind ) ) OR/*облиг куп*/
                  (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_CONV_BOND, Fininstr.rec.AvoirKind ) ) )/*Облиг с правом конверсии*/
                ClearRecord(RecordAdress);
                НайтиЮридическийАдресСубъекта(Party.rec.PartyID,RecordAdress);
                Kind = GetKindParty( Party.rec.PartyID, RecordAdress.Country, Party.rec.NotResident );
                if  (Kind == STATE_BODY        )    CategoryValue = OBJATTR_711_BON1;
                elif(Kind == STATE_BODY_SUBJECT)    CategoryValue = OBJATTR_711_BON2;
                elif(Kind == BANK              )    CategoryValue = OBJATTR_711_BON3;
                elif(Kind == RESIDENT          )    CategoryValue = OBJATTR_711_BON4;
                elif(Kind == STATE_BODY_FOREIGN)    CategoryValue = OBJATTR_711_BON5;
                elif((Kind == BANK_NOTRESIDENT) and ( not IsQualifiedFI(Certif.FIID, Certif.RegistrDate))) CategoryValue = OBJATTR_711_BON61;
                elif(Kind == BANK_NOTRESIDENT  )    CategoryValue = OBJATTR_711_BON6;
                elif((Kind == CLIENT_NOTRESIDENT) and ( not IsQualifiedFI(Certif.FIID, Certif.RegistrDate))) CategoryValue = OBJATTR_711_BON71;
                elif(Kind == CLIENT_NOTRESIDENT)    CategoryValue = OBJATTR_711_BON7;
                end;
     
            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_DEPOSIT_CERTIFICATE, Fininstr.rec.AvoirKind ))/*Депозит сертиф*/
                if(ВидСубъекта(Party.rec.PartyID, PTK_BANK))
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_DS1;
                   elif(Party.rec.NotResident=="X")
                      CategoryValue = OBJATTR_711_DS2;
                   end;
                end;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_SAVING_CERTIFICATE, Fininstr.rec.AvoirKind ) )/*Сберег сертиф*/
                if(ВидСубъекта(Party.rec.PartyID, PTK_BANK))
                   if(Party.rec.NotResident!="X")
                      CategoryValue = OBJATTR_711_SS1;
                   elif(Party.rec.NotResident=="X")
                      CategoryValue = OBJATTR_711_SS2;
                   end;
                end;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BILL, Fininstr.rec.AvoirKind ) )/*вексель*/
                ClearRecord(RecordAdress);
                НайтиЮридическийАдресСубъекта(Party.rec.PartyID,RecordAdress);
                Kind = GetKindParty( Party.rec.PartyID, RecordAdress.Country, Party.rec.NotResident );
                if  (Kind == STATE_BODY        )    CategoryValue = OBJATTR_711_BIL1;
                elif(Kind == STATE_BODY_SUBJECT)    CategoryValue = OBJATTR_711_BIL2;
                elif(Kind == BANK              )    CategoryValue = OBJATTR_711_BIL3;
                elif(Kind == RESIDENT          )    CategoryValue = OBJATTR_711_BIL4;
                elif(Kind == STATE_BODY_FOREIGN)    CategoryValue = OBJATTR_711_BIL5;
                elif(Kind == BANK_NOTRESIDENT  )    CategoryValue = OBJATTR_711_BIL6;
                elif(Kind == CLIENT_NOTRESIDENT)    CategoryValue = OBJATTR_711_BIL7;
                end;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_DEPOSITORY_RECEIPT, Fininstr.rec.AvoirKind ) )/*Депоз расписка*/
                CategoryValue = OBJATTR_711_DR;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_STORAGE_CERTIFICATE, Fininstr.rec.AvoirKind ) )/*Складское свидетел*/
                CategoryValue = OBJATTR_711_CON;

            elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_MORTGAGE, Fininstr.rec.AvoirKind ) )/*Закладная*/
                CategoryValue = OBJATTR_711_ENC;

            else
                CategoryValue = OBJATTR_711_OTHER;
            end;

            AttrID = GetAttrID(OBJTYPE_ISCERTIF, CodeForm711, CategoryValue);
         else
            MsgBox("Не найден субъект PartyID = ", Certif.Issuer);            
         end;/*if(GetEQ(Party))*/
      else
         MsgBox("Ненайден финансовый инструмент инвентарной карточки ", Certif.FIID);
      end;
   end;
  return AttrID;
end;


/* Точка входа в макрос */
macro Category_Certif(СertifBuff)
   var AttrID = 0;
   record Certif("certif");      /* Структура для доступа к сертификату, он же "инвентарная карточка"*/

   ListCategory.size = 0;
   ListValue.size    = 0;

   /* СertifBuff - буфер структуры CERT */
   SetBuff(Certif, СertifBuff) ;
   AttrID = MakeCategory_711(Certif);
   if(AttrID)
     ListCategory[ListCategory.size] = CodeForm711;
     ListValue[ListValue.size] = AttrID;
   end;

end; /*Category_Certif*/                                                       
