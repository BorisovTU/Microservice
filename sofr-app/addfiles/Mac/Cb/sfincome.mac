/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ГКБО"                                  */
/*                                                                          */
/*                             ПЗО -- новая модель                          */
/*  Макрос шага "Учет комиссии в доход"                                     */
/*  операции оплаты периодического взимания                                 */
/*                                                                          */
/*  Имя файла: sfincome.mac                                                 */
/*  Создан: 24.07.2007                                               СолАН  */
/*                                                                          */
/****************************************************************************/
import sfcomcat, sfpay;

private const SFDOC_DEF_PERIOD = 51;

private const RashetCatCode = "+Расчеты"; 
private const NdsCatCode = "-НДС"; 

macro ExecuteStep( outBuff, primIn )

   var ClaimAccountUse;
   var isCarryPossible:bool;
   var stat;
   var Sum = $0;
   var IsIncluded = false;

   file sfcontrFile( "sfcontr.dbt") key 0;
   file sfdefcomFile( "sfdefcom.dbt"  ) key 0;
   file sfcomissFile( "sfcomiss.dbt"  ) key 0;

   record sfdefcomRec( "sfdefcom.dbt" );   
   
   setbuff( sfdefcomRec, primIn  );      

   sfcontrFile.Id = sfdefcomRec.ConId;

   if( not getEQ( sfcontrFile ) )
      MsgBox("Не найден договор обслуживания: ", sfdefcomRec.ConId);
      return 1;
   end;

   sfcomissFile.FeeType = sfdefcomRec.FeeType;
   sfcomissFile.Number  = sfdefcomRec.CommNumber;

   if( not getEQ( sfcomissFile ) )
      MsgBox("Не найдена комиссия");
      return 1;
   end;
   
   /*Получить платежные инструкции дебета (sidebet) и кредита (sicredit):*/
   var sidebet  = TRecHandler("sfsi.dbt");
   var sicredit = TRecHandler("sfsi.dbt");
   sidebet.Clear;
   sicredit.Clear;   
   SfGetSI( 663/*OBJTYPE_SFDEFCOM*/, sfdefcomRec, sidebet, sicredit );


   if(sidebet.Rec.FIID == sfdefcomRec.FIID_CommSum)
     Sum = sfdefcomRec.commSum;
   else
     ConvSumCross( Sum, sfdefcomRec.commSum, {curdate}, sfdefcomRec.FIID_CommSum, sidebet.Rec.FIID );
   end;
  
   /*определить НаличиеСредствНаСчете (счет плательщика комиссии определяем по sidebet, 
   сумма комиссии sfdefcomRec.commSum)*/
   var IsMoneyAvailable = IsMoneyAvailableOnAccount( sidebet.Rec.FIID, sidebet.Rec.Account, Sum );  

   /*Получить значение настройки _настройка = "COMMON\Комиссии\Не использовать счет требования"*/
   GetRegistryValue( "COMMON\\КОМИССИИ\\НЕ ИСПОЛЬЗОВАТЬ СЧЕТ ТРЕБОВАНИЯ", V_BOOL, ClaimAccountUse, stat );
   if(stat != 0)
     msgbox("Ошибка при определении настройки: |COMMON\\КОМИССИИ\\НЕ ИСПОЛЬЗОВАТЬ СЧЕТ ТРЕБОВАНИЯ");
     return 1;
   end;      
   /*дата_оплаты     = sfdefcomRec.DatePay 
     дата_начисления = sfdefcomRec.dateFEE*/
   
   /*Условия выполнения проводки
     Комиссия должна быть учтена в доход если выполняется хотя бы одно из условий приведенных ниже.
     Таблица Условия учета комиссии в доход      
     ----------------------------|---------------------|-------------------------------|---------
  Значения настройки "Не         | Наличие средств     | Дата оплаты                   | Выполнять 
  использовать  счет требования" | на счете-плательщика|                               | проводку
     ----------------------------------------------------------------------------------------------
      No                           есть                 Совпадает с датой начисления       Да
      Не смотрим на настройку      есть                 Не совпадает с датой начисления    Да
      Не смотрим на настройку      нет                  Совпадает с датой начисления       Да
      Не смотрим на настройку      нет                  Не совпадает с датой начисления    Да
  -------------------------------------------------------------------------------------------------- */
   isCarryPossible = false;
   if( IsMoneyAvailable )
     if( sfdefcomRec.DatePay != sfdefcomRec.dateFee ) 
       isCarryPossible = true;
     else
       if( ClaimAccountUse == false) 
         isCarryPossible = true;
       end;
     end;
   else
     isCarryPossible = true;
   end;   

   var SfComPD : SfComPrimDoc;
   var RashetAccount : string;
   var SumWithoutNDS;
   var settacc = TRecHandler("settacc.dbt");   
   var Ground : string, strDate : string;

    record AccountReceiverRec( account );

   /*Если получатель комиссии филиал "нашего банка"  или "Наш банк" 
     и выполняется условие учета комиссии в доход*/     
   if( ((SfIsOurFilial(sfcomissFile.ReceiverID) == true ) OR (sicredit.Rec.BankCode == "")) 
     AND (isCarryPossible == true) )
     /*
     1. Проводка Дт. "+Расчеты" по договору обслуживания (значение параметра БэкОфисКУ выбирается по 
                      виду обслуживания договора, контрагентом является контрагент по договору) 
                Кт. СПИ договора обслуживания 
                 на сумму комиссии (sfdefcomRec.commSum) 
                 текущим операционным днем филиала. 
        Основание проводки "За услуги по договору <"№"> от <дата> за период 
                           с <дата начала периода> по <дата окончания периода>".
     */ 
     SfComPD = SfComPrimDoc( 87 /*DLDOC_SFCONTRACT*/, sfcontrFile );     
     RashetAccount = SfComPD.FindAndOpenAccount( RashetCatCode, {curdate}, sfdefcomRec.FIID_CommSum );
          
     if (sfcontrFile.DATECLOSE != Date(0,0,0)) 
       Ground =Ground + " по " + string(sfcontrFile.DATECLOSE:f);
     end;

     if (sfcontrFile.DATECLOSE != Date(0,0,0)) 
       strDate = string(sfcontrFile.DATECLOSE:f);
     else
       strDate = "01.01.0001";
     end;
      
     Ground = "За услуги по договору \"" +sfcontrFile.Number + "\"  от " + string(sfcontrFile.DATECONC:f) + " за период с " + string(sfcontrFile.DATEBEGIN:f)+ " по " + strDate;

     CountComissAsIncome(sfdefcomRec.FIID_CommSum, RashetAccount, sicredit.Rec.FIID, sicredit.Rec.Account, sfdefcomRec.commSum, Ground );  

     /*Если у ТО есть сумма НДС sfdefcomRec.NDSSum > 0*/
     if( sfdefcomRec.NDSSum > $0 )

       /*Проводка 2: Дт. "+Расчеты" по договору обслуживания (значение параметра БэкОфисКУ выбирается 
                         по виду обслуживания договора, контрагентом является контрагент по договору) 
                     Кт. счет НДС комиссии 
                         (SfGetCategoryAccount( comiss, OBJROLE_SFCOMISS_TAX_ACC, &chapterID, &FIID, &account )) 
                     на сумму НДС Комиссии (sfdefcomRec.NDSSum) 
                     текущим операционным днем. 
                     Основание проводки "НДС по комиссии <DSFCOMISS.T_CODE>".
       */
       SfGetCategoryAccount( sfcomissFile, /*OBJROLE_SFCOMISS_TAX_ACC*/ 2, AccountReceiverRec );

       Ground = "НДС по комиссии "+sfcomissFile.Code+ ".";
       CountComissAsIncome(sfdefcomRec.FIID_CommSum, RashetAccount, AccountReceiverRec.Code_Currency, AccountReceiverRec.account, sfdefcomRec.NDSSum, Ground);       
     end;

     /*Выставить признак "Учтена в доход" (DSFDEFCOM_DBT.T_ISINCLUDED) комиссии*/
     IsIncluded = true;
     SfDefSetFlagIsInc();
     
   end;      
   
   if( IsIncluded )     
     sfdefcomRec.commSum = sfdefcomRec.commSum + sfdefcomRec.NDSSum;
     sfdefcomRec.NDSSum = $0;
   end;

   println("RashetAccount = ", RashetAccount);
   println("sfdefcomRec.FIID_commSum = ", sfdefcomRec.FIID_commSum);
   /*Сформировать документ оплаты SfFormDocs ()*/ 
   return SfFormDocs( outBuff/*documentRec*/, /*sfcontrFile, */sfcomissFile,
                      sfdefcomRec.DatePay,
                      Ground,
                      sfdefcomRec.commSum, sfdefcomRec.NDSSum/*com_sum, NDS_sum*/, 
                      sfdefcomRec.FIID_commSum,
                      SFDOC_DEF_PERIOD, sfdefcomRec.ID, sfdefcomRec.feeType, 
                      sfdefcomRec, sfcontrFile.PayMethod, RashetAccount); 
      
end;

