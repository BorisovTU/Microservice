Import "globals.mac",rsd,RsbDataSet;

class KeyOwner (ИдВладельцаКлюча,ИдПодразделения)
var KeyOwnerId,PartyId;
KeyOwnerId = ИдВладельцаКлюча;
PartyId= ИдПодразделения;
End;

/* Размер отступа */
const Indent = "  ";
/**/
const SGN_ALG_TYPE_ACTION = 3414;
const SGN_ALG_ROLE_ACTION = 3413;

const SGN_KEY_ACTIV = 1; /* активный ключ */


/* Печать разделителя */
macro PrintSplit()
  var prefix = "";
  GetParm( 0, prefix );

  println( prefix + "===================================================== "
         + {oper} + " " + string({curdate}:f) + " " + string(time:f) );
end;

/* Получить имя контрагента */
macro GetPartyName( PartyID )
  file party("party.dbt");
  party.PartyID = PartyID;
  if( GetEQ(party) )
    return party.ShortName;
  end;
  return "";
end;

/* Получить имя первичного документа */
macro GetOprkdocName( DocKind )
  file file_oprkdoc("oprkdoc.dbt");
  file_oprkdoc.DocKind = DocKind;
  if( GetEQ(file_oprkdoc) )
    return file_oprkdoc.Name;
  end;
  return "";
end;

/* Получить имя вида роли */
macro GetSignKindName( DocKind, SignKind )
  file file_signkind("sgnkind.dbt");
  file_signkind.DocKind  = DocKind;
  file_signkind.SignKind = SignKind;
  if( GetEQ(file_signkind) )
    return file_signkind.Name;      
  end;
  return "";
end;

/* Получить имя шага операции */
macro GetStepName( Kind_Operation, Number_Step )
  file file_oprostep("oprostep.dbt");
  file_oprostep.Kind_Operation = Kind_Operation;
  file_oprostep.Number_Step    = Number_Step;
  if( GetEQ(file_oprostep) )
    return file_oprostep.Name;
  end;
  return "";
end;

/* Получить название криптосистемы */
macro GetCryptoSysName( CryptoSysID )
  file crypto("sgnsys.dbt");
  crypto.CryptoSysID = CryptoSysID;
  if( GetEQ(crypto) )
    return crypto.Name;
  end;
  return "";
end;

/* Получить название алгоритма */
macro GetNameAlg( iTypeAlg, iNumberAlg )
  file file_namealg("namealg.dbt");
  file_namealg.iTypeAlg   = iTypeAlg;
  file_namealg.iNumberAlg = iNumberAlg;
  if( GetEQ(file_namealg) )
    return file_namealg.szNameAlg;
  end;
  return "";
end;

/* Получить название действия над ЭЦП */
macro GetTypeActionName( TypeAction )
  return GetNameAlg( SGN_ALG_TYPE_ACTION, TypeAction );
end;

/* Получить название действия в списке */
/*macro GetRoleActionName( RoleAction )
  return GetNameAlg( SGN_ALG_ROLE_ACTION, RoleAction );
end;*/

/* Получить название филиала */
macro PrintDepartmentName( Department )
  file dp_dep("dp_dep.dbt");
  if( Department )
    dp_dep.Code = Department;
    if( GetEQ(dp_dep) )
      println( "Филиал:" + Indent + dp_dep.Name + Indent + dp_dep.Comment );
    end;
  end;
end;

/* Получить список владельцев криптоключа по его прикладному идентификатору */
macro GetKeyOwner(KeyCode:String, KeyOwner_Arr:TArray) : Integer
var env,sql,rsd,Obj,i=0,isnull=false,ercode;
sql = RSDCommand("select dsgnkown_dbt.t_keyownerid,dsgnkown_dbt.t_partyid "
+ " from dsgnkown_dbt,dsgnckko_dbt,dsgnkey_dbt "
+ " where dsgnkown_dbt.T_KEYOWNERID = dsgnckko_dbt.T_KEYOWNERID " 
+	" and dsgnkey_dbt.T_KEYID = dsgnckko_dbt.T_KEYID "
+	" and dsgnkey_dbt.T_KEYCODE = "
+ KeyCode);
sql.execute();
env=sql.connection.environment;
if (env.ErrorCount) 
		return env.error(0).code;
end;
rsd = TRsbDataSet(sql);
if(rsd.MoveNext())
       Obj=KeyOwner(rsd.t_keyownerid,rsd.t_partyid);
       KeyOwner_Arr[i]=Obj;
       rsd.Value("t_partyid",isnull);
	if (isnull)
            KeyOwner_Arr[i].PartyId=null;
	end;
       i=i+1;
	while(rsd.MoveNext())
		       Obj=KeyOwner(rsd.t_keyownerid,rsd.t_partyid);
			KeyOwner_Arr[i]=Obj;
			rsd.Value("t_partyid",isnull);
			if (isnull)
      	                    KeyOwner_Arr[i].PartyId=null;
			end;
                     i=i+1;
       end;
 	SetParm(2,KeyOwner_Arr);
	return 0;
else 
       if (env.ErrorCount) 
		return env.error(0).code;
	end;
	ercode = status();
	if (ercode)
       return ercode;
	end;
	SetParm(2,null);
	return -1;
end
end; 	
