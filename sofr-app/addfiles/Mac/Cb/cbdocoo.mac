/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 2000              */
/*                                                                      */
/*  Имя файла        : cbdocoo.mac                                      */
/*                                                                      */
/*  Описание         : Выполнение шага проводка мемордера               */
/*  Программист      : Копачев Д.В.                                     */
/*                                                                      */
/*  Создан           : 31.01.2004                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */

import InsCarryDoc, BankInter, PaymInter, pm_common;
var Memorial:RsbMemorialOrder;

FILE cbaccount( account ) key 0;
FILE cbpartcode( "partcode.dbt" ) key 0;

/* Определяет код плательщика/получателя по счету */
macro GetClientCodeByAccount( FIID, acc, CodeKind )
   var ClientCode = "";
   ClearRecord(cbaccount);
   cbaccount.Chapter = CodeKind;
   cbaccount.Code_Currency = FIID;
   cbaccount.Account = Trim(acc);         
   if ( getEQ(cbaccount) )            
     cbpartcode.PartyID = cbaccount.Client;
     cbpartcode.CodeKind = 1;
     return cbaccount.Client;
     if ( GetEQ(cbpartcode) )
        ClientCode = cbpartcode.Code;
     end;
   end;
   return ClientCode;
end;

macro   ExecuteStep( doc, primdoc )
        var DKFlag = 1;
        RECORD  rec_wlconf( wlconf );

        var Payment:RsbMOPayment = Memorial.Payment;
        var paymtr:RsbPaymTransaction = Payment.MakeTransaction();
        if( paymtr == NULL )
          MsgBox("Ошибка при создании проводки по платежу");
          return 1;
        end;

        paymtr.Date_Carry       = Memorial.Date_Carry;
        paymtr.FIIDPayer        = Payment.PayerFIID;
        paymtr.FIIDReceiver     = Payment.ReceiverFIID;
        paymtr.Number_Pack      = Payment.NumberPack;
        paymtr.Department       = Payment.Department;
        paymtr.Shifr_Oper       = Payment.ShifrOper;
        paymtr.Kind_Oper        = Memorial.Kind_Oper;
        paymtr.Chapter          = Memorial.Chapter;
        paymtr.Numb_Document    = Payment.Number;
        paymtr.Ground           = Payment.Ground;
        paymtr.TypeDocument     = Memorial.TypeDocument;
        paymtr.UserTypeDocument = Memorial.UserTypeDocument;
        paymtr.UserField1       = Memorial.UserField1;
        paymtr.UserField2       = Memorial.UserField2;
        paymtr.UserField3       = Memorial.UserField3;
        paymtr.UserField4       = Memorial.UserField4;
        paymtr.ResultCarry      = 1;

        // Добавим символы 
        paymtr.AddCashSymbol(Payment.CashSymbolDebet, Payment.PayerAmount, CASHSYMB_TYPE_DEBET);
        paymtr.AddCashSymbol(Payment.CashSymbolCredit, Payment.PayerAmount, CASHSYMB_TYPE_CREDIT);
        paymtr.AddCashSymbol(Payment.SymbNotBalDebet, Payment.PayerAmount, CASHSYMB_TYPE_NOTB);
        
        if( not paymtr.Carry )
          MsgBox("Ошибка при актуализации платежа");
          return 1;
        end;
        
        // Статус платежа меняем на "Завершенный платеж"
        Payment.PaymStatus = PM_FINISHED;
        // Изменяем дату списания со счета плательщика
        Payment.PayerChargeOffDate = Memorial.Date_Carry;

        /* Создание подтверждения о проводке */
        /* вызываем последовательно для подтверждений дебета и кредита по документу */
        while( DKFlag <= 2 )
          rec_wlconf.FIID          = Memorial.Code_Currency;
          if( DKFlag == 1 )
            rec_wlconf.Account     = Payment.PayerAccount;
          else
            rec_wlconf.Account     = Payment.ReceiverAccount;
          end;
          rec_wlconf.Corschem      = -1; /*ALLCORSCHEMNUMBER*/
          rec_wlconf.DocumentID    = Memorial.DocumentID;
          rec_wlconf.DocKind       = 70;//DLDOC_MEMORIALORDER;
          rec_wlconf.DateValue     = Payment.ValueDate;
          rec_wlconf.Number        = Payment.Number;
          rec_wlconf.Sum           = Payment.PayerAmount;
          rec_wlconf.DKFlag        = DKFlag;
          rec_wlconf.BankID        = GetClientCodeByAccount(rec_wlconf.FIID, rec_wlconf.Account, 1 /*PTCK_CONTR*/);
          rec_wlconf.CodeKind      = 1 /*PTCK_CONTR*/;
          //rec_wlconf.CodeName      = Payment.CodeName; /*!!!!!*/
          rec_wlconf.ShifrOper     = Payment.ShifrOper;
          rec_wlconf.Description   = Payment.Ground; 

          if(CreateConfFlagForCorschem(rec_wlconf))
          if( CreateConfirmation(rec_wlconf) == FALSE )
            msgbox("Ошибка при создании подтверждения по проводке");
            return 1;
          end;
          end;

          DKFlag = DKFlag + 1;
        end;
        /*************************************/

        return 0;
end;