/*
$Name: acc_rof.mac
$Module: Ядро ГКБО
$Description: Макрос выполнения шага "Формирование резерва по операциям с резидентами офшорных зон"
*/

Import InsCarryDoc, "res_carry.mac", "acc_cls.mac";

private record RsvParm("rsvprm.dbt");

var OffshoreGroup = 0;

/*
    Пользовательская часть шага
*/
macro ExecuteStep( doc, primdoc, DocKind, ID_Operation )

  record acc(account);
  
  var ReserveAccount : string;
  var accPD : AccountPrimdoc;
  var ClassifReserveOffshore : string;
  var IncomeCatCodePrm;
  var OutlayCatCodePrm;

  SetBuff( acc, primdoc );

  accPD = AccountPrimdoc( acc, ID_Operation, RsvParm );

  if( OffshoreGroup )
    
    if((acc.Open_Close != "") or
       ((ReserveLoss == $0) and
        (ReserveLoans == $0) and
        (ReserveOffshore == $0)))
      ReserveAccount = FindReserveAccount( accPD, AccOprServDoc.Date );
      if( Trim(ReserveAccount) == "" )
        return 0;
      end;
    else
      ReserveAccount = OpenReserveAccount( accPD, AccOprServDoc.Date );
    end;

    if( Trim(ReserveAccount) == "" )
      msgbox("Не найден счет резерва.");
      return 1;
    end;

    ClassifReserveOffshore = GetAccountReserveClassif(KindReserveOffshore, RsvParm.RsvClassOffshore, KindReserveOffshore);
    
    GetIncomeOutlayCatCodeOffshore(ClassifReserveOffshore, @IncomeCatCodePrm, @OutlayCatCodePrm, AccOprServDoc.Date);

    if((IncomeCatCodePrm == "") or (OutlayCatCodePrm == ""))
      msgbox("Не задана категория средств для классификации РОФШ \"", ClassifReserveOffshore, "\"");
      return 1;
    else
      if(((OffshoreGroup == 2) or (OffshoreGroup == 3)) and
         (GetProcentOfReserveOffshore(acc.Chapter, acc.Code_Currency, acc.Account, AccOprServDoc.Date) != null))
        if( not OpenSubAccounts(ReserveAccount, RsvParm.RsvClass, RsvParm.RsvClassLoans) )
          msgbox("Не удалось открыть субсчета по счету резерва.");
          return 1;
        end;
      end;
    
      if( not CreateReserveOffshoreCarryAcc(accPD, AccOprServDoc.Date, ReserveAccount, IncomeCatCodePrm, OutlayCatCodePrm) )
        msgbox("Не удалось создать проводку резерва.");
        return 1;
      end;
    end;
  
  end;

  if((acc.Open_Close != "") and (Trim(ReserveAccount) != ""))
    CloseReserveAccount( accPD, AccOprServDoc.Date );
  end;

  return 0;

end;
