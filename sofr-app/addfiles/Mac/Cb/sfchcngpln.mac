/*
$Name: sfchcngpln.mac
$Module: Ядро ГКБО          
$Description: Макрос пользовательской проверки перед изменением ТП по договору
*/

import BankInter, CtInter, PtInter;
import rsd, "SendNotifITP.mac";

/*
Макрофункция должна возвращать код:
0 - проверки успешны, смена ТП разрешена
1 - смена ТП запрещена

Принципы реализации оповещения пользователя:
 
 - В случае возникновения ошибки при безинтерфейсных проверках, 
   ее необходимо генерировать (MemoryError) и при установленном признаке ShowErr 
   ошибку необходимо отобразить пользователю (DisplayError).

 - Если запрет смены ТП исходит от пользователя, то ошибку можно не отображать, 
   а ограничится только генерацией (MemoryError), чтобы при необходимости 
   вызывающий код мог получить ее текст.
*/

private macro L_Z( num, len )
    var str1, len1;
    str1 = trim( string( num ) );
    len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else  return  mkstr("0", len-len1 ) + str1;
    end;
end;

private macro getGeneralMainObjAttr(p_objectType,p_object,p_groupid, p_date)
   var v_object = L_Z(p_object,10);
   var v_result:integer;
   var query = "select RSB_SECUR.GetGeneralMainObjAttr( ?, ?, ?, ? ) t from dual;";
   var cmd = RSDCommand(query); 
   cmd.AddParam("", RSDBP_IN, p_objectType);
   cmd.AddParam("", RSDBP_IN, v_object);
   cmd.AddParam("", RSDBP_IN, p_groupid);
   cmd.AddParam("", RSDBP_IN, p_date);
   var rs = RSDRecordSet(cmd);
   if (rs.movenext)
     v_result = rs.value("t");
   end;
   return v_result;
end;

macro CheckChangeSfContrPlan(ObjSfContr, ObjSfPlan, StartDate, ShowErr)

  record SfContr(sfcontr);
  record sfplan(sfplan);
  var stat = 0;
  var PlanAttr:integer = 0, SubAttr:integer = 0;
  var ITPAttr = "", DopDogNum = "", DOpDogDate;
  var cmd, rs;

  const CATEG_PARTY_SEGM  = 115; // DEF-28868, пересечение категорий с категориями тестов НКИ
  const CATEG_TARIFF_SEGM = 102; // Категория 'Сегмент корпоративного бизнеса' на тарифе  

  SetBuff(SfContr, ObjSfContr);
  SetBuff(SfPlan, ObjSfPlan);
  record Client(party);
  ПолучитьСубъекта(SfContr.partyid, Client);

  cmd = RSDCommand("select RSB_SECUR.GetMainObjAttrNoDate("+OBJTYPE_SFPLAN+", LPAD(:ObjID, 10, '0'), "+CATEG_TARIFF_SEGM+") from dual");
  cmd.AddParam("ObjID", RSDBP_IN, SfPlan.SfPlanID);
  rs = RSDRecordSet(cmd);
  if(rs.moveNext())
    PlanAttr = rs.value(0);
  end;

  cmd = RSDCommand("select RSB_SECUR.GetMainObjAttrNoDate("+OBJTYPE_PARTY+", LPAD(:ObjID, 10, '0'), "+CATEG_PARTY_SEGM+") from dual");
  cmd.AddParam("ObjID", RSDBP_IN, SfContr.partyid);
  rs = RSDRecordSet(cmd);
  if(rs.moveNext())
    SubAttr = rs.value(0);
  end;

  If( (PlanAttr != 0) and (SubAttr == 0) and ( Client.legalform == 1)) 
     MsgBox("Для клиента " + Client.name + " не заполнена категория 'Сегмент корпоративного бизнеса'. Тарифный план не может быть установлен");
     stat = 1;
  elIf( (PlanAttr == 0) and ( Client.legalform == 1)) 
     MsgBox("Тарифы с незаполненной категорией 'Сегмент корпоративного рынка' недоступны для установки клиентам, для которых Форма субъекта = Организация");
     stat = 1;
  elIf( (PlanAttr != 0) and (SubAttr != 0) and (SubAttr != PlanAttr) ) 
     MsgBox("Значение категории 'Сегмент корпоративного бизнеса' для клиента не соответствует значению категории 'Сегмент корпоративного рынка' для Тарифного плана");
     stat = 1;
  end;

  ITPAttr = string(getGeneralMainObjAttr(OBJTYPE_SFPLAN, SfPlan.SfPlanID, 101, {curdate})); //DEF-38803

  If(Client.legalform == 2)
     If(ITPAttr != "1")
        MsgBox("По клиентам физическим лицам запрещено менять тариф на НЕ индивидуальный.\n Тариф не может быть изменен ");
        stat = 1;
     end;
  end;

  // BIQ-8786 тарифные планы
  // при смене тарифного плана отправляем данные
  if(stat == 0)
    cmd = RSDCommand("select t_dlcontrid from ddlcontr_dbt where t_sfcontrid = :sf_id");
    cmd.AddParam("sf_id", RSDBP_IN, SfContr.ID);
    rs = RSDRecordSet(cmd);
    if (rs.movenext)
       ExecMacroFile("func_lib.mac", "InsEvent_UpdateContract", rs.value(0), 2);                       
    end;
    If(stat == 0)
       IF(ITPAttr == "1")
          cmd = RSDCommand("select t_dlcontrid from ddlcontr_dbt where t_sfcontrid = :sf_id");
          cmd.AddParam("sf_id", RSDBP_IN, SfContr.ID);
          rs = RSDRecordSet(cmd);
          if (rs.movenext)
             DopDOgNum = ReadNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(rs.value(0),34), 140);
             DOpDogDate = ReadNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(rs.value(0),34), 141);
             SendNotification_ITP(rs.value(0), Client.Partyid, Client.name, DopDOgNum, DOpDogDate, SfContr.number, SfContr.datebegin);
          end;
       end;
    end;
  end;

  return stat;
end;