 /*
 $Name: commonutil.mac
 $Module: Общее
 $Description: Инструментальные функции
 */

/*начало механизма многосимвольных строк*/
Import "cb_sql.mac";

private var _outString;

private Macro _collectOutProc (str)
   if (_outString)
      _outString = _outString + " " + trim (str);
   else
      _outString = trim (str);
      _outString = StrSubst(_outString,"\n","");
   end;
end;

macro CaptureOutput
   _outString = null;
   SetOutHandler (@_collectOutProc);
end;

macro StopCaptureOutput
   var retVal = _outString;
   _outString = null;
   SetOutHandler;
   return retVal;
end;
/*окончание механизма многосимвольных строк*/

macro CU_IIF(condition, valueTrue, valueFalse)
    if (condition)
        return valueTrue;
    else 
        return valueFalse;
    end;
end;

macro CU_YesNoWin(msg : string)
  Array Text;
  Array Button;
  Button(0) = "  Да   ";
  Button(1) = "  Нет  ";
  var but, ind = 0;

  Text(ind) = msg;
  but = ConfWin(Text, Button);
  
  return (but == 0);
end;

macro CU_CopyRSetToFBuff( fbuff, rset )
  var idx   = 0,
      nflds = FldNumber( fbuff ),
      val,
      NullConv;

  ClearRecord(fbuff);

  if( IsEqClass( "RsdRecordset", rset ))
    NullConv = rset.Command.NullConversion;
    rset.Command.NullConversion = true;
  end;

  while( idx < nflds )
    val = rset.value( "t_" + FldName( fbuff, idx ) );
    if( ValType( fbuff( idx ) ) == V_STRING )
      fbuff( idx ) = SQL_ConvTypeStr( val );
    elif( val != NULL )
      fbuff( idx ) = rset.value( "t_" + FldName( fbuff, idx ), null, ValType( fbuff( idx ) ) );
    end;
    idx = idx + 1;
  end;

  if( IsEqClass( "RsdRecordset", rset ))
    rset.Command.NullConversion = NullConv;
  end;
end;

/* 
 Копирование значений полей текущей записи Recordset'а по таблице 
 в свойства объекта TRecHandler по одноименной таблице.
 Требование к полям Recordset'а:
   - имена полей Recordset'а должны совпадать с именами полей TRecHandler
     и иметь префикс "t_": значение поля Recordset'а с именем t_partyid 
     в TRecHandler будет скопировано в поле partyid
*/
macro CU_CopyRecordsetToRecHandler(rs, rh)
  var ifld = 0;
  var nfld = FldNumber(rh);
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    val = rs.value("t_" + FldName(rh, ifld));
    if(ValType(GenGetProp(rh.rec, FldName(rh, ifld))) == V_STRING)
      GenSetProp(rh.rec, FldName(rh, ifld), SQL_ConvTypeStr(val));
    elif(val != null)
      GenSetProp(rh.rec, FldName(rh, ifld), rs.value("t_" + FldName(rh, ifld), null, ValType(GenGetProp(rh.rec, FldName(rh, ifld)))));
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;

/* 
 Копирование значений полей текущей записи Recordset'а по таблице 
 в одноименные свойства объекта пользовательского класса
 Требование к полям Recordset'а:
   - имена полей Recordset'а должны совпадать с именами свойств класса
     и иметь префикс "t_": значение поля Recordset'а с именем t_partyid 
     в классе будет скопировано в поле partyid
*/
macro CU_CopyRecordsetToClassObj(rs, cls)
  var iprop = 0;
  var ifld = 0;
  var nfld = rs.fldCount;
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    iprop = GenPropID(cls, substr(rs.fld(ifld).name, 3));
    if(iprop != -1)
      val = rs.value(ifld);
      if(ValType(cls(iprop)) == V_STRING)
        cls(iprop) = SQL_ConvTypeStr(val);
      elif(val != null)
        cls(iprop) = rs.value(ifld, null, ValType(cls(iprop)));
      end;
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;
