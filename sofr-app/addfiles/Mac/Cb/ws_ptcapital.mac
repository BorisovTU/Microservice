/*
$Name:        ws_ptcapital.mac
$Module:      Ядро ГКБО
$Description: Макрос реализации сервиса получения капитала головного подразделения терр. структуры.
*/

import cb_sql, rsd, globals, oralib, commonutil, likepy;

class TWsPortCapital

  var FromDate : date;  /*Дата начала актуальности значения*/
  var Capital  : money; /*Собственные средства (капитал)*/

end;

private macro GetFromDate(FromDate, ListPortCapital : TArray)
  var query;
  var rs;
  var params : TArray;

  var wsPortCapital : TWsPortCapital;                                

  CaptureOutput;
[
SELECT t_Date, t_SumOfCapital
  FROM dcapital_dbt
 WHERE t_Date <= :FromDate
 ORDER BY t_Date DESC
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("FromDate" , FromDate )  
                    );
  rs = execSQLselect(query, params, true);
 
  if(rs and rs.moveNext())
    wsPortCapital = TWsPortCapital;                                

    wsPortCapital.FromDate = rs.value(0);
    wsPortCapital.Capital = money(rs.value(1));
    
    ListPortCapital[ListPortCapital.size] = wsPortCapital;
  end;
end;

/* Сервис GetCapital - сервис возвращает капитал головного подразделения терр. структуры.

   Параметры:
       FromDate - Дата, начиная с которой требуется вернуть историю изменения капитала.
*/

macro GetCapital(FromDate)

  var ListPortCapital : TArray; 
  ListPortCapital = TArray;
 
  GetFromDate(FromDate, ListPortCapital);

  return ListPortCapital;
end; 