/*
 $Name:        prcc_common.mac
 $Module:      РКО, ББ
 $Description: Инициализация параметров для печати валютных кассовых ордеров по 3352-У
*/

// Программист: TDV 2014

import prcs;

private const TS_DEBET         = 1; /*приход*/
private const TS_KREDIT        = 2; /*расход*/
private const DPRT_TYPE_FILIAL = 1; /*филиал*/
private const DPRT_TYPE_VSP    = 2; /*ВСП*/
private const CURSDIFF_COMING  = 1; /*Курсовая разница ДОХОД*/
private const CURSDIFF_CONSUMPTION = 2; /*Курсовая разница РАСХОД*/

CLASS TCurOrderPrintData
  var Number               : string,
      DateStr              : string,
      BankName             : string,
      OpCode               : string,
      AmountRecStr         : string,
      DebAcc               : string,
      DFI                  : string,
      Amount               : string,
      //Symbol = sDeb+sCred
      AmountSendStr        : string,
      CredAcc              : string,
      CFI                  : string,
      PayAmount            : string,
      NameCashier          : string,
      NameAccount          : string,
      NameControl          : string,
      AmountD17            : string,
      AmountD19            : string,
      AmountD20            : string,
      AmountD22            : string,
      
      AccTrnPayDeb         : string, // Курсовая разница ДОХОД
      AccTrnRecDeb         : string,
      AccTrnRecSumDeb      : string,

      AccTrnPayCred        : string, // Курсовая разница РАСХОД
      AccTrnRecCred        : string,
      AccTrnRecSumCred     : string;

  var AccTrn               : TRecHandler;

  var TypeFIID             : integer,
      sDeb                 : TArray = TArray(),
      sCred                : TArray = TArray(),
      sumDeb               : TArray = TArray(),
      sumCred              : TArray = TArray();

  var RegKeyFDate          : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ФОРМАТ_ДАТЫ",          dateFormat : integer,
      nameVal : integer,
      RegKeySSum           : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\РАЗДЕЛИТЕЛЬ_СУММЫ",    sepSumm : string,
      RegKeyToDigit        : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ",toDigit : bool;


  macro getFIINameOrCYY( FIID, isName ):string
    file fiinstr(fininstr);
    ClearRecord(fiinstr);
    fiinstr.FIID = FIID;
    var res = "";
    if( GetEQ(fiinstr) )
      if( isName==1 )
        res = fiinstr.Name;
      elif( isName==0 )
        res = fiinstr.CCY;
      end;
    end;
    return res;
  end;

  /*Функция для оформления с = или копейками через -*/
  private macro MoneyStyle(sum:money):string          
    var newsum:string;
    if (sum !=0)
      newsum = sum;
      newsum = StrSubSt(newsum, ".00", "=");
      newsum = StrSubSt(newsum, ".", "-"); 
    else
      newsum = "";
    end;
    return newsum;
  end;

  private macro getRegVal()
    GetRegistryValue( RegKeyFDate,   V_INTEGER, dateFormat, "" );
    nameVal = 0;
    GetRegistryValue( RegKeySSum,    V_STRING,  sepSumm,    "" );
    GetRegistryValue( RegKeyToDigit, V_BOOL,    toDigit,    "" );
  end;

  private macro isAccCash( acc:string ):bool
    var cmd, RS;
 
    cmd = RSDCommand( " SELECT t_type_account "
                      "  FROM daccount_dbt "
                      " WHERE t_account = ? ");
    cmd.addParam( "", RSDBP_IN, acc );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
      if( index(RS.Type_Account, "А"/*касса*/) > 0 )
        return TRUE;
      end;
    end;
    return FALSE;
  end;

  private macro GetStrMoney( pmpaym : TRecHandler, isPayer ):string
    var acc:string = IfThenElse( isPayer, pmpaym.rec.PayerAccount, pmpaym.rec.ReceiverAccount );
    var sum:money  = IfThenElse( isPayer, pmpaym.rec.Amount,       pmpaym.rec.PayAmount );
    var FIID:integer=IfThenElse( isPayer, pmpaym.rec.FIID,         pmpaym.rec.PayFIID );
    var CURtext = "",
        SUMstr  = "",
        KOPstr  = "";

    if( isAccCash( acc ) == true )
      CURtext = CurToStrAlt(sum, SUMstr, KOPstr, GetISOCode(FIID));
  
      if( FIID == 0 )
        CURtext = SUMstr + " руб. " + KOPstr + " коп.";
      end;
    end;

    if (StrLen == null)
      StrLen = 65;
    end;

    return CURtext;
  end;

  private macro FillFIID( acc:string, FIID:integer ):string
    if( isAccCash( acc ) == true )
      return getFIINameOrCYY( FIID, nameVal );
    else 
      return "";
    end;
  end;

  private macro FillBankName(Department,OperNode):string
    var cmd, RS;
    var BankNameFIL: string = "",
        BankNameVSP:  string = "";

    cmd = RSDCommand( "SELECT prt.t_Name, prt.t_ShortName, dp.t_NodeType "
                      "  FROM dparty_dbt prt, ddp_dep_dbt dp "
                      " WHERE prt.t_PartyID = dp.t_PartyID "
                      "     AND dp.t_Code IN (?,?) " ) ; 
    cmd.addParam( "", RSDBP_IN, Department );
    cmd.addParam( "", RSDBP_IN, OperNode );
    cmd.execute();
    RS = TRsbDataSet(cmd);

    while( RS.movenext() )
      if( Rs.NodeType == DPRT_TYPE_FILIAL )
        if( (Rs.Name != "") AND (strlen(Rs.Name) <= 73) )
          BankNameFIL = Rs.Name;
        else
          BankNameFIL = Rs.ShortName;
        end;
      elif (Rs.NodeType == DPRT_TYPE_VSP )
        if( (Rs.Name != "") AND (strlen(Rs.Name) <= 73) )
          BankNameVSP = Rs.Name;
        else
          BankNameVSP = Rs.ShortName;
        end;
      end;
    end;
    return (BankNameFIL + IfThenElse( BankNameVSP, ", " + BankNameVSP, "" ));
  end;

  private macro SplitAmount( sum:money ):string
    private var str = string( Sum:f ), stat = 0;
    if( ( toDigit == FALSE ) AND ( SubStr( str, strlen( str ) - 1 ) == "00") )
      str = SubStr( str, 1, strlen( str ) - 3 );
    elif( sepSumm )
      var fraction = SubStr( str, strlen( str ) - 1 );
      str = SubStr( str, 1, strlen( str ) - 3 ) + sepSumm + fraction;
    end;
    return str;
  end;

  private macro GetAmount( sum, acc )
    if( acc )
      if( isAccCash(acc) )
        return SplitAmount(sum);
      else
        return "";
      end;
    else
      return SplitAmount(sum);
    end;
  end;

  private macro GetAmountNatCur( sum:money, FIID:integer, Date:date ):string
    var AmountRub = $0;
    var m_err = IfThenElse( ConvSumCross( AmountRub, sum, Date, FIID, 0/*NATCUR*/ ), 0, 1 );
    if( m_err )
      AmountRub = $0;
      DisplayError();
    end;
    return SplitAmount(AmountRub);
  end;

  private macro GetAccTrn( payid:integer, payacc:string, recacc:string, acctype:integer ):TRecHandler
    var cmd, RS;
    var BankName: string = "",
        EndSymb:  string = "";

    cmd = RSDCommand( " SELECT acctrn1.* "
                      "   FROM dacctrn_dbt acctrn, dacctrn_dbt acctrn1, dpmdocs_dbt pmdocs "
                      "  WHERE pmdocs.t_PaymentID = ? "
                      "    AND pmdocs.t_AcctrnID = acctrn.t_AcctrnID "
                      "    AND acctrn.t_State = 1 "
                      "    AND acctrn.t_exrateacctrnID = acctrn1.t_acctrnID "
                      "    AND acctrn1.t_" + IfThenElse( acctype == CURSDIFF_COMING, "Account_Payer", "Account_Receiver" ) +
                      "        in (?, ?) " );

    cmd.addParam( "", RSDBP_IN, payid );
    cmd.addParam( "", RSDBP_IN, payacc );
    cmd.addParam( "", RSDBP_IN, recacc );
    cmd.execute();

    RS = TRsbDataSet(cmd);

    var obj:TRecHandler = TRecHandler("acctrn.dbt");

    ClearRecord(obj);

    if( RS.movenext() )
      copy( RS, obj );
    end;

    return obj;
  end;

  private macro FillCashSymbolsAndSum( ArraySum, ArraySym, pscshdoc, pmpaym, pmrmprop, SymbolKind: integer )

    var i: integer = 0;
    var count:integer = IfThenElse( SymbolKind == SYMB_KIND_INCOME, 6, 5 );

    if ( InList( pmpaym.rec.DocKind, 410, 420, 430, 440, 445 ) )
      var SymbList:TCashSymbolList = TCashSymbolList( pscshdoc, SymbolKind );
      while( i < SymbList.Size )
        ArraySym(i) = SymbList.Value(i).code;
        ArraySum(i) = string(SymbList.Value(i).sum:f);
        i = i + 1;
      end;
    else
      if (SymbolKind == SYMB_KIND_INCOME)
        ArraySym(i) = pmrmprop.rec.cashSymbolDebet;       
        ArraySum(i) = pmpaym.rec.Amount;  
      else
        ArraySym(i) = pmrmprop.rec.cashSymbolCredit;       
        ArraySum(i) = pmpaym.rec.PayAmount;        
      end;
      i = i + 1;
    end;
    /* если символов меньше, чем на них отведено строк в таблице, недостающие заполним пробелами */
    while( i < count )
      ArraySym(i) = 
      ArraySum(i) = "";
      i = i + 1;
    end;

  end;

  private macro FillCashSymbols( pscshdoc, pmpaym, pmrmprop, SymbolKind: integer )

    array aSym, aSum;
    FillCashSymbolsAndSum( aSum, aSym, pscshdoc, pmpaym, pmrmprop, SymbolKind );

    var syms:TArray = TArray();
    FillTArrayFromArray( syms, aSym);
    var sums:TArray = TArray();
    FillTArrayFromArray( sums, aSum);

    if( SymbolKind == SYMB_KIND_INCOME )
      for( var elem, syms )
        if( elem )
          sDeb(sDeb.size()) = elem;
        end;
      end;
      for( elem, sums )
        if( elem )
          sumDeb(sumDeb.size()) = GetAmount(elem);
        end;
      end;
    elif( SymbolKind == SYMB_KIND_OUTGO )
      for( elem, syms )
        if( elem )
          sCred(sCred.size())   = elem;
        end;
      end;
      for( elem, sums )
        if( elem )
          sumCred(sumCred.size()) = GetAmount(elem);
        end;
      end;
    end;

  end;

  /* -----------------------------------
  Проинициализировать данными валютного ордера
  ----------------------------------- */
  MACRO InitByCashOrder( pmpaym:TRecHandler, pmrmprop:TRecHandler, pscshdoc:TRecHandler )

    Number        = pmrmprop.rec.Number;
    DateStr       = IfThenElse( dateFormat==0, string(pmpaym.rec.ValueDate:f), FillDateString( pmpaym.rec.ValueDate ) );
    BankName      = FillBankName( pmpaym.rec.Department, pmpaym.rec.OperNode );
    OpCode        = pscshdoc.rec.Kind_Operation;
    AmountRecStr  = GetStrMoney( pmpaym, true ); // true - payer, false - receiver
    DebAcc        = pmpaym.rec.PayerAccount;
    DFI           = FillFIID( pmpaym.rec.PayerAccount, pmpaym.rec.FIID );
    Amount        = GetAmount( pmpaym.rec.Amount, pmpaym.rec.PayerAccount );

    FillCashSymbols( pscshdoc, pmpaym, pmrmprop, SYMB_KIND_INCOME );
    FillCashSymbols( pscshdoc, pmpaym, pmrmprop, SYMB_KIND_OUTGO );

    AmountSendStr = GetStrMoney( pmpaym, false ); // true - payer, false - receiver
    CredAcc       = pmpaym.rec.ReceiverAccount;
    CFI           = FillFIID( pmpaym.rec.ReceiverAccount, pmpaym.rec.PayFIID );
    PayAmount     = GetAmount( pmpaym.rec.PayAmount, pmpaym.rec.ReceiverAccount ); // проверка по счёту кассы
    NameCashier   = "";//"Кассовый работник";
    NameAccount   = "";//"Бухгалтерский работник";
    NameControl   = "";//"Контролирующий работник";

    AmountD17     = IfThenElse( TypeFIID == TS_DEBET,
                                Amount,
                                IfThenElse( TypeFIID == TS_KREDIT, PayAmount, GetAmountNatCur(pmpaym.rec.Amount,
                                                                                              pmpaym.rec.FIID,
                                                                                              pmpaym.rec.ValueDate) )
                              );
    AmountD19     = IfThenElse( pmpaym.rec.FIID != NATCUR, GetAmount(pmpaym.rec.Amount), "" ); // без проверки кассы
    AmountD20     = IfThenElse( TypeFIID == TS_DEBET,
                                Amount,
                                IfThenElse( TypeFIID == TS_KREDIT, Amount, GetAmountNatCur(pmpaym.rec.PayAmount,
                                                                                           pmpaym.rec.PayFIID,
                                                                                           pmpaym.rec.ValueDate) )
                              );
    AmountD22     = IfThenElse( pmpaym.rec.PayFIID != NATCUR, GetAmount(pmpaym.rec.PayAmount), "" ); // без проверки кассы

    var accTrnData = RsbAccTransactionData();

    accTrnData.Chapter         = pmpaym.rec.Chapter;
    accTrnData.Date_Carry      = pmpaym.rec.ValueDate;
    accTrnData.FIIDPayer       = pmpaym.rec.FIID;
    accTrnData.FIIDReceiver    = pmpaym.rec.PayFIID;
    accTrnData.AccountPayer    = pmpaym.rec.PayerAccount;
    accTrnData.AccountReceiver = pmpaym.rec.ReceiverAccount;
    accTrnData.SumPayer        = pmpaym.rec.Amount;
    accTrnData.SumReceiver     = pmpaym.rec.PayAmount;

    var exRateSum:money = $0;
    var ExRateAccTrnRec:TRecHandler = TRecHandler("acctrn.dbt");
    var errcode = 0;

    errcode = accTrnData.GetExRateAccTrnParam( exRateSum, ExRateAccTrnRec );

    /*ДОХОД*/
    AccTrn           = GetAccTrn( pmpaym.rec.PaymentID, pmpaym.rec.PayerAccount, pmpaym.rec.ReceiverAccount, CURSDIFF_COMING );
    if( AccTrn.rec.AccTrnID > 0 )
      AccTrnPayDeb     = AccTrn.rec.Account_Payer;
      AccTrnRecDeb     = AccTrn.rec.Account_Receiver;
      AccTrnRecSumDeb  = GetAmount( AccTrn.rec.Sum_Receiver ); // без проверки кассы
    else
      AccTrnPayDeb     = "";
      AccTrnRecDeb     = "";
      AccTrnRecSumDeb  = 0;
    end;

    /*РАСХОД*/
    AccTrn           = GetAccTrn( pmpaym.rec.PaymentID, pmpaym.rec.PayerAccount, pmpaym.rec.ReceiverAccount, CURSDIFF_CONSUMPTION );
    if( AccTrn.rec.AccTrnID > 0 )
      AccTrnPayCred    = AccTrn.rec.Account_Payer;
      AccTrnRecCred    = AccTrn.rec.Account_Receiver;
      AccTrnRecSumCred = GetAmount( AccTrn.rec.Sum_Receiver ); // без проверки кассы
    else
      AccTrnPayCred    = "";
      AccTrnRecCred    = "";
      AccTrnRecSumCred = 0;
    end;
    if( (AccTrnRecSumDeb == 0) AND (AccTrnRecSumCred == 0) )
      if( errcode == 0 )
        if( exRateSum >= 0 )
          AccTrnPayDeb     = ExRateAccTrnRec.rec.Account_Payer;
          AccTrnRecDeb     = ExRateAccTrnRec.rec.Account_Receiver;
          AccTrnRecSumDeb  = GetAmount( exRateSum ); // без проверки кассы
        elif( exRateSum < 0 )
          exRateSum = exRateSum*(-1);
          AccTrnPayCred     = ExRateAccTrnRec.rec.Account_Payer;
          AccTrnRecCred     = ExRateAccTrnRec.rec.Account_Receiver;
          AccTrnRecSumCred  = GetAmount( exRateSum ); // без проверки кассы
        end;
      end;
    end;
  END;
  getRegVal();
  
END;
