/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 2000              */
/*                                                                      */
/*  Имя файла        : bbcpsend.mac                                     */
/*                                                                      */
/*  Описание         : Выполнение шага зачисления                       */
/*                     средств по валютному платежу                     */
/*                     на корсчет                                       */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 17.01.2000                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import InsCarryDoc, BankInter, PTInter, "cppmconv.mac", "pmmulti.mac";

RECORD  PrimPmpaym( pmpaym );     /* ─┐                                                     */
RECORD  PrimPmprop( pmprop );     /*  ├─ параметры платежа по клиентской платежке */
RECORD  PrimPmrmprop( pmrmprop ); /* ─┘                                                     */

//Символ пустой? (пустая строка или только нули)
private MACRO isEmptySymbol( symbol ):bool
    return ( int(trim(symbol)) == 0 );
ONERROR(err)
    return TRUE;
END;

/**********************************/
/* Выполнение шага                */
/**********************************/
MACRO ExecuteStep( doc, payorder )
  var d = RsbAccTransaction(); // RsbAccTransactionData

  record  cporder(bbcpord);

if( PrimPmprop.BankCode )
  //Платеж внешний - дальше 
  
  if( PrimPmpaym.FIID == PrimPmpaym.PayFIID )
     /*****************************************************
     Зачисление на корсчет
     *****************************************************/
     d.Chapter          = 1;
     d.Date_Carry       = PrimPmpaym.ValueDate;
     d.Number_Pack      = PrimPmpaym.NumberPack;
     d.AccountReceiver  = GetCorAcc( PrimPmprop.PayFIID, PrimPmprop.Corschem, CORS_ACC_BOPENPMOUT );
     d.AccountPayer     = PrimPmpaym.PayerAccount;
     d.Sum              = PrimPmpaym.Amount;
     d.Ground           = PrimPmrmprop.Ground;
     d.FIID             = PrimPmpaym.PayFIID;
     d.Numb_Document    = PrimPmrmprop.Number;
     d.Department       = PrimPmpaym.Department;
     d.ResultCarry      = 1;
     d.Kind_Oper        = " 1";

     if(d.AccountReceiver == "")
       msgbox("В схеме расчетов не задан счет \"Незавершенные расчеты банка (списание)\"");
       return 1;
     end;

     if(PrimPmpaym.PayFIID) 
       if( not d.Carry() ) 
          msgbox("Ошибка при вставке валютного документа");
          return 1;
       end;
     else  
       if( not d.Carry() )
          msgbox("Ошибка при вставке рублевого документа");
          return 1;
       end;
     end;
  else
     /*****************************************************
     Зачисление на корсчет с конверсией
     *****************************************************/

     if( ПолучитьДополнительныеСчетаПроводокКонверсии() )
       msgbox( "Ошибка при установке счетов ОВП" );
       return 1;
     end;

     d.MCMethod        = 1;
     d.FIIDPayer       = PrimPmpaym.FIID;
     d.FIIDReceiver    = PrimPmpaym.PayFIID;
     d.SumPayer        = PrimPmpaym.Amount;
     d.SumReceiver     = PrimPmpaym.PayAmount;
     d.AccountPayer    = PrimPmpaym.PayerAccount;
     d.AccountReceiver = GetCorAcc( PrimPmprop.PayFIID, PrimPmprop.Corschem, CORS_ACC_BOPENPMOUT );
     d.Chapter         = 1;
     d.Ground          = PrimPmrmprop.Ground;
     d.Numb_Document   = PrimPmrmprop.Number;
     d.Date_Carry      = PrimPmpaym.ValueDate;
     d.Number_Pack     = PrimPmpaym.NumberPack;
     d.Department      = PrimPmpaym.Department;
     d.ERAccountPlus   = СчетДоходов;
     d.ERAccountMinus  = СчетРасходов;


     if(d.AccountReceiver == "")
       msgbox("В схеме расчетов не задан счет \"Незавершенные расчеты банка (списание)\"");
       return 1;
     end;

     if( not d.Carry() )
       MsgBox( "Ошибка при вставке мультивалютного документа" );
       return 1; 
     end;
  end;

  /*****************************************************
  Здесь можно вставлять дополнительные проводки для шага
  *****************************************************/

  /* Обработка сводных платежей */
  if( ProcessPayment( PrimPmprop.Corschem, PrimPmprop.PayFIID, PrimPmpaym.ValueDate, PrimPmpaym.Department ) )
    return 1;
  end;

  return 0;

else
  //Платеж внутренний - дальше проводка
  
  setbuff(cporder,payorder);
  

  /*****************************************************
   Оплата комиссии
  *****************************************************/
  if( cporder.Origin == CP_OR_SF )

    if( ПолучитьДополнительныеСчетаПроводокКонверсии() )
      msgbox( "Ошибка при установке счетов ОВП" );
      return 1;
    end;

    if( ExecMacroFile( "sfinsdoc.mac", "ВставитьПлатуЗаОбслуживание", 
                       PrimPmpaym, doc,
                       PrimPmrmprop.Number, PrimPmpaym.ValueDate,
                       PrimPmpaym.Department, PrimPmpaym.NumberPack, 
                       СчетДоходов, СчетРасходов) )
      return 1;
    end;

  /*****************************************************
   Зачисление на клиентский счет
  *****************************************************/
  else

    /* Проводка без конверсии*/
    if( PrimPmpaym.FIID == PrimPmpaym.PayFIID )

      d.Chapter          = 1;
      d.Date_Carry       = PrimPmpaym.ValueDate;
      d.Number_Pack      = PrimPmpaym.NumberPack;
      d.AccountReceiver  = PrimPmpaym.ReceiverAccount;
      d.AccountPayer     = PrimPmpaym.PayerAccount;
      d.Sum              = PrimPmpaym.Amount;
      d.Ground           = PrimPmrmprop.Ground;
      d.FIID             = PrimPmpaym.PayFIID;
      d.Numb_Document    = PrimPmrmprop.Number;
      d.Department       = PrimPmpaym.Department;
      d.ResultCarry      = 1;
      d.Kind_Oper        = " 1";
      
      /* Получение массива символов */
      if( not isEmptySymbol(PrimPmrmprop.SymbNotBalDebet) )
        d.AddCashSymbol(PrimPmrmprop.SymbNotBalDebet, d.Sum);
      end;

      if( PrimPmpaym.FIID )
        if( not d.Carry() )
           msgbox("Ошибка при вставке валютного документа");
           return 1;
        end;
      else
        if( not d.Carry() )
           msgbox("Ошибка при вставке рублевого документа");
           return 1;
        end;
      end;

    /* Проводка с конверсией*/
    else
      
      if( ПолучитьДополнительныеСчетаПроводокКонверсии() )
        msgbox( "Ошибка при установке счетов ОВП" );
        return 1;
      end;

      d.MCMethod        = 1;
      d.FIIDPayer       = PrimPmpaym.FIID;
      d.FIIDReceiver    = PrimPmpaym.PayFIID;
      d.SumPayer        = PrimPmpaym.Amount;
      d.SumReceiver     = PrimPmpaym.PayAmount;
      d.AccountPayer    = PrimPmpaym.PayerAccount;
      d.AccountReceiver = PrimPmpaym.ReceiverAccount;
      d.Chapter         = 1;
      d.Ground          = PrimPmrmprop.Ground;
      d.Numb_Document   = PrimPmrmprop.Number;
      d.Date_Carry      = PrimPmpaym.ValueDate;
      d.Number_Pack     = PrimPmpaym.NumberPack;
      d.Department      = PrimPmpaym.Department;
      d.ERAccountPlus   = СчетДоходов;
      d.ERAccountMinus  = СчетРасходов;

      if( not d.Carry() )
        MsgBox( "Ошибка при вставке мультивалютного документа" );
        return 1; 
      end;
    end;
  end;

  /*      Установить статус платежа - завершенный платеж  */
  if( not InsertPaymentStat( PrimPmpaym.PaymentID, 32000 /*PM_FINISHED*/ ) )
    msgbox("Ошибка при вставке статуса платежа");
    return 1;
  end;

  return 0;

end;

end;
