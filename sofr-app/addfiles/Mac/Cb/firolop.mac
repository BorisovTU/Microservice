/*
$Name:        firolop.mac
$Module:      Ядро Securities
$Description: Отчет об откате операций
*/
import oprinter, SPInter;

const Ошибка    = 0,
      Первый    = 1,
      Последний = 2;

RECORD oprstep(oprstep);
file oper(oproper);

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )
   array StringArray;
   var i = 1;

   setbuff(oprstep, firstdoc);

   if( Вызов == Первый )
[             Отчет о групповом выполнении.];
[┌───────────────────────────────────┬────────┬─────────────────────────────────────────────────────────┐];
[│               Код                 │   №    │                 Сообщение                               │];
[│     финансового инструмента       │ ошибки │                                                         │];
[├───────────────────────────────────┼────────┼─────────────────────────────────────────────────────────┤];
   elif( Вызов == Ошибка )
      if( Статус == 0 )
         Сообщение = "Операция выполнена успешно";
      end;

      var Code:string = "";
      ClearRecord(oper);
      oper.ID_Operation = oprstep.ID_Operation;
      if( GetEQ(oper) )
         if( oper.DocKind == DLDOC_ISSUE )
            var FDoc   = TRecHandler("avoiriss");
            var FDocFI = TRecHandler("fininstr");
            if( not ПолучитьПервичныйДокументСервОпер(oper, FDoc, FDocFI) )
               Code = FDocFI.rec.FI_Code;
            end;
         end;
      end;

      StrSplit(StrSubst(Сообщение, "│", " " ), StringArray, 56);
[│###################################│########│#########################################################│](Code, Статус, StringArray(0));

      while( StrLen(StringArray(i)) > 0 )
[│                                   │        │#########################################################│](StringArray(i));
         i = i + 1;
      end;

   elif( Вызов == Последний )
[└───────────────────────────────────┴────────┴─────────────────────────────────────────────────────────┘];
   end;
END;
