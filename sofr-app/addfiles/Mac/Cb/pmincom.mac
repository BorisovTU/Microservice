/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 1999              */
/*                                                                      */
/*  Имя файла        : pmincom.mac                                      */
/*                                                                      */
/*  Описание         : Единовременные комиссии входящего платежа        */
/*  Программист      : Бабин А.П.                                       */
/*                                                                      */
/*  Создан           : 02.04.02                                         */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import InsCarryDoc, PaymInter, OprInter, "pmtlscom.mac";

/*                              КОМИССИЯ НАШЕГО                               */
/*         сумма платежа = сумма платежа - сумма комиссии                     */
/*               (сумма платежа)                                              */
/*         Корсчет        ->      клиент (невыясненные)  (проводка)           */
/*               (сумма комиссии)                                             */
/*         Корсчет        ->      Счет доходов банка  (платеж комиссии)       */
/*                                                                            */

/*  Макрос обработки комиссий */     

/* После перехода на класс платежа удалить */
macro ExecuteSingCommission( ID_Operation, /* Операция */ 
                         ps,           /* Буфер первички (должен быть уже установлен) */
                         Kind,         /* Вид первички */
                         PrimPmpaym,   /* Буфер платежа */
                         PrimPmprop,   /* Буфер свойств платежа */
                         ChargesOfBank, 
                         CorrespondentCharges,                                                   

                         /*    Следующие параметры будут расчитаны здесь    */
                         PaySumPaym, /*  Сумма платежа в валюте платежа     */
                         PaySumDoc,  /*  Сумма проводки в валюте платажа    */
                         SumPaym,    /*  Сумма платежа в валюте актива      */
                         SumDoc,     /*  Сумма проводки в валюте актива     */

                         /* Необязательные параметры */
                         //ABP 4.02.04 PayerAccount и FIID_Payer более не используются
                         PayerAccount, /* Счет плательщика комиссии (не обязателен) */
                         FIID_Payer,   /* Валюта плательщика (не обязятелен) */
                         ChargesSum,   /* Зарезервированные расходы (не обязателен) */
                         FIID_Charges )

  RECORD combuf( oprsfcom );  
  var AddSum = $0, b_continue, koefNDS;

  PaySumPaym = PrimPmpaym.PayAmount;
  PaySumDoc  = PaySumPaym;
  SumPaym    = PrimPmpaym.Amount;
  SumDoc     = SumPaym;

  if ( (valtype(ChargesSum)!=V_UNDEF) AND (ChargesSum>$0) )
     /* Вычисляем добавочную сумму (было зарезервировано больше денег, 
        чем нам надо, но мы их, врагу :), не отдадим)                         */
     AddSum = ChargesSum;
     ClearRecord(combuf); 
     /* Перебираем комиссии нашего банка */
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
     while( b_continue )
        if ( FIID_Charges==combuf.FIID_Sum )
           AddSum = AddSum - combuf.Sum - combuf.SumNDS;
        else
           AddSum = AddSum - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, FIID_Charges);
        end;
        b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
     end;
     if ( AddSum<$0 )
        AddSum = $0;
     end;
  end;

  /*                    КОМИССИЯ НАШЕГО БАНКА                               */
  ClearRecord(combuf); 
  /* Перебираем комиссии нашего банка */
  b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  while( b_continue )
     /* Просто вставляем платежи комиссий, проводки пойдут своим чередом  */
     /*
     if ( valType(PayerAccount)!=V_UNDEF )
        if ( valType(FIID_Payer)!=V_UNDEF )
           combuf.FIID_Payer = FIID_Payer;
        else
           combuf.FIID_Payer = PrimPmpaym.PayFIID;
        end;
        combuf.Account_Payer = PayerAccount;
     end;
     */
     if ( (AddSum>$0) AND (combuf.sum>$0) )
         koefNDS = double(combuf.sumNDS/combuf.sum);
         if ( FIID_Charges==combuf.FIID_Sum )            
            combuf.sumNDS = combuf.sumNDS + moneyl(AddSum*koefNDS);
            combuf.sum = combuf.sum + AddSum - moneyl(AddSum*koefNDS);
         else
            combuf.sumNDS = combuf.sumNDS + GetSumConvert(moneyl(AddSum*koefNDS), FIID_Charges, combuf.FIID_Sum);
            combuf.sum = combuf.sum + GetSumConvert(AddSum-moneyl(AddSum*koefNDS), FIID_Charges, combuf.FIID_Sum);
         end;
         AddSum = $0;
     end;
     if ( not InsertSingComisPayment(Kind, ps, combuf) )
        MsgBox( "Ошибка при вставке платежа комиссии" );
        return 1;
     end;

     if ( PrimPmpaym.PayFIID == combuf.FIID_Sum )
         PaySumPaym = PaySumPaym - combuf.Sum - combuf.SumNDS;
         PaySumDoc  = PaySumDoc  - combuf.Sum - combuf.SumNDS;
     else
         /* Пересчитываем сумму */
         PaySumPaym = PaySumPaym - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
         PaySumDoc  = PaySumDoc  - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
     end;
     if ( PrimPmpaym.FIID == combuf.FIID_Sum )
         SumPaym = SumPaym - combuf.Sum - combuf.SumNDS;
         SumDoc  = SumDoc  - combuf.Sum - combuf.SumNDS;
     else
         /* Пересчитываем сумму */
         SumPaym = SumPaym - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
         SumDoc  = SumDoc  - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
     end;
     
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  end;

  if ( (PaySumPaym<$0) OR (SumPaym<$0) )
     msgBox("Сумма комиссии больше суммы платежа");
     return 1;
  end;
  
  /*                  Обновляем сумму платежа в реальной базе                */
  if( (PrimPmpaym.Amount    != SumPaym) or
      (PrimPmpaym.PayAmount != PaySumPaym) )
    if ( not UpdateSumRealPayment( PrimPmpaym.PaymentID, PaySumPaym, SumPaym ) )
      msgBox( "Ошибка обновления платежа" );
      return 1;
    end;
  end;

  SetParm( 7,  PaySumPaym );
  SetParm( 8,  PaySumDoc );
  SetParm( 9,  SumPaym );
  SetParm( 10, SumDoc );

  return 0;

  OnError(er) /* Обработка ошибок времени выполнения */
    MsgBox(String(er.Message, " Строка: ", er.Line));
    return 1;
end;

macro ExecuteSingCommissionObj( ID_Operation, /* Операция */ 
                         ps,           /* Буфер первички (должен быть уже установлен) */
                         Kind,         /* Вид первички */
                         PaymentObj:RsbPayment,
                         ChargesOfBank, 
                         CorrespondentCharges,                                                   

                         /*    Следующие параметры будут расчитаны здесь    */
                         PaySumPaym, /*  Сумма платежа в валюте платежа     */
                         PaySumDoc,  /*  Сумма проводки в валюте платажа    */
                         SumPaym,    /*  Сумма платежа в валюте актива      */
                         SumDoc,     /*  Сумма проводки в валюте актива     */

                         /* Необязательные параметры */
                         //ABP 4.02.04 PayerAccount и FIID_Payer более не используются
                         PayerAccount, /* Счет плательщика комиссии (не обязателен) */
                         FIID_Payer,   /* Валюта плательщика (не обязятелен) */
                         ChargesSum,   /* Зарезервированные расходы (не обязателен) */
                         FIID_Charges )

  RECORD combuf( oprsfcom );  
  var AddSum = $0, b_continue, koefNDS;

  PaySumPaym = PaymentObj.ReceiverAmount;
  PaySumDoc  = PaySumPaym;
  SumPaym    = PaymentObj.PayerAmount;
  SumDoc     = SumPaym;

  if ( (valtype(ChargesSum)!=V_UNDEF) AND (ChargesSum>$0) )
     /* Вычисляем добавочную сумму (было зарезервировано больше денег, 
        чем нам надо, но мы их, врагу :), не отдадим)                         */
     AddSum = ChargesSum;
     ClearRecord(combuf); 
     /* Перебираем комиссии нашего банка */
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
     while( b_continue )
        if ( FIID_Charges==combuf.FIID_Sum )
           AddSum = AddSum - combuf.Sum - combuf.SumNDS;
        else
           AddSum = AddSum - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, FIID_Charges);
        end;
        b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
     end;
     if ( AddSum<$0 )
        AddSum = $0;
     end;
  end;

  /*                    КОМИССИЯ НАШЕГО БАНКА                               */
  ClearRecord(combuf); 
  /* Перебираем комиссии нашего банка */
  b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  while( b_continue )
     /* Просто вставляем платежи комиссий, проводки пойдут своим чередом  */
     /*
     if ( valType(PayerAccount)!=V_UNDEF )
        if ( valType(FIID_Payer)!=V_UNDEF )
           combuf.FIID_Payer = FIID_Payer;
        else
           combuf.FIID_Payer = PrimPmpaym.PayFIID;
        end;
        combuf.Account_Payer = PayerAccount;
     end;
     */
     if ( (AddSum>$0) AND (combuf.sum>$0) )
         koefNDS = double(combuf.sumNDS/combuf.sum);
         if ( FIID_Charges==combuf.FIID_Sum )            
            combuf.sumNDS = combuf.sumNDS + moneyl(AddSum*koefNDS);
            combuf.sum = combuf.sum + AddSum - moneyl(AddSum*koefNDS);
         else
            combuf.sumNDS = combuf.sumNDS + GetSumConvert(moneyl(AddSum*koefNDS), FIID_Charges, combuf.FIID_Sum);
            combuf.sum = combuf.sum + GetSumConvert(AddSum-moneyl(AddSum*koefNDS), FIID_Charges, combuf.FIID_Sum);
         end;
         AddSum = $0;
     end;
     if ( not InsertSingComisPayment(Kind, ps, combuf) )
        MsgBox( "Ошибка при вставке платежа комиссии" );
        return 1;
     end;

     if ( PaymentObj.ReceiverFIID == combuf.FIID_Sum )
         PaySumPaym = PaySumPaym - combuf.Sum - combuf.SumNDS;
         PaySumDoc  = PaySumDoc  - combuf.Sum - combuf.SumNDS;
     else
         /* Пересчитываем сумму */
         PaySumPaym = PaySumPaym - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PaymentObj.ReceiverFIID);
         PaySumDoc  = PaySumDoc  - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PaymentObj.ReceiverFIID);
     end;
     if ( PaymentObj.PayerFIID == combuf.FIID_Sum )
         SumPaym = SumPaym - combuf.Sum - combuf.SumNDS;
         SumDoc  = SumDoc  - combuf.Sum - combuf.SumNDS;
     else
         /* Пересчитываем сумму */
         SumPaym = SumPaym - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PaymentObj.PayerFIID);
         SumDoc  = SumDoc  - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, PaymentObj.PayerFIID);
     end;
     
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  end;

  if ( (PaySumPaym<$0) OR (SumPaym<$0) )
     msgBox("Сумма комиссии больше суммы платежа");
     return 1;
  end;
  
  /*                  Обновляем сумму платежа в реальной базе                */
  if( (PaymentObj.PayerAmount    != SumPaym) or
      (PaymentObj.ReceiverAmount != PaySumPaym) )
    PaymentObj.PayerAmount = SumPaym;
    PaymentObj.ReceiverAmount = PaySumPaym;
  end;

  SetParm( 6,  PaySumPaym );
  SetParm( 7,  PaySumDoc );
  SetParm( 8,  SumPaym );
  SetParm( 9, SumDoc );

  return 0;

  OnError(er) /* Обработка ошибок времени выполнения */
    MsgBox(String(er.Message, " Строка: ", er.Line));
    return 1;
end;