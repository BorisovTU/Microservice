/*
 *  Операция 202 "Требование банка".
 *  Макрос шага 1 "Проводка".
 */

import InsCarryDoc;    //InsertPaymentStat
import PaymInter, BankInter;
import "pmoutcom.mac"; //ExecuteSingCommission
import "paym_rec.mac";

MACRO ExecuteStep( doc, primdoc, DocKind, ID_Operation )

  var PaySumPaym:moneyl, PaySumDoc:moneyl,
         SumPaym:moneyl,    SumDoc:moneyl, TransferDate;

  record order("memorder.dbt", "bank.def");
  var d = RsbAccTransaction(); // RsbAccTransactionData

  SetBuff(order, primdoc);

  /* Проверяем ДПП платежа */
  if( (Pm_debet.PaymentID == Pm_paym.PaymentID) and (Pm_debet.Group == PAYMENTS_GROUP_EXTERNAL) ) 

    if( not PaymOutChangeDPP(Pm_paym, Pm_debet, NULL, 0, TransferDate ) )
      return 1;
    end;

    if( TransferDate > {curdate} )
      MsgBox("Дата перечисления платежа для собственного платежа банка|не может отличаться от текущей" );
      return 1;
    end;
  else /* Внутренний платеж после проводки закрываем */
    if( not InsertPaymentStat( 0, PM_FINISHED, PMMET_DPS, 0, 0, Pm_paym.Purpose, Pm_paym.SubPurpose ) )
      MsgBox("Ошибка при вставке статуса платежа");
      return 1;
    end;
  end;

  /* Платеж является платой за обслуживание  */
  /* Значит судьба его определяется не здесь */
  if(order.Origin == MEMORDER_FDOC_SF)
    /* Актуализировать проводки МФР */
    if( not CarryPlanDocuments( Pm_paym.PaymentID ) )
      MsgBox("Ошибка при помещении планируемых проводок в проведенные");
      return 1;
    end;

    //Чтобы не компилировать всегда sfinsdoc.mac, зовем комиссии через ExecMacroFile
    if( ExecMacroFile( "sfinsdoc.mac", "ВставитьПлатуЗаОбслуживание",
                        Pm_paym, doc, 
                        Pm_rmprop.Number, Pm_paym.ValueDate, 
                        Pm_paym.Department, Pm_paym.NumberPack) )
      return 1; 
    else
      return 0;
    end;
  end;

  /* Платеж не является платой за обслуживание */
  /* Значит, возможно, с него берутся комиссии */
  if( ExecuteSingCommission( ID_Operation, NULL, DocKind, Pm_paym, Pm_debet, OUR, BEN,
                             PaySumPaym, PaySumDoc, SumPaym, SumDoc ) )
    return 1;
  end;

  /* Заполняем структуру проводки */
  d.Chapter          = 1;
  d.Kind_Oper        = " 1";
  d.ResultCarry      = 1;
  d.Date_Carry       = Pm_paym.ValueDate;
  if(Pm_paym.PaymentID != 0)
    d.Sum            = PaySumDoc;
  else
    d.Sum            = SumDoc;
  end;
  d.FIID             = Pm_paym.FIID;
  d.Ground           = Pm_rmprop.Ground;
  d.Number_Pack      = Pm_paym.NumberPack;
  d.Numb_Document    = Pm_rmprop.Number;
  d.Department       = Pm_paym.Department;
  d.Shifr_Oper       = Pm_rmprop.ShifrOper;

  /* Из-за МФР проводки теперь делаются только по Future-счетам */
  d.AccountPayer     = Pm_paym.FuturePayerAccount;
  d.AccountReceiver  = Pm_paym.FutureReceiverAccount;
  
  if(d.AccountPayer == "")
    MsgBox("В схеме расчетов не задан счет \"Незавершенные расчеты банка (зачисление)\"");
    return 1;
  end;

  /* Делаем проводку */
  if ( ( d.AccountPayer != "" ) or ( Pm_debet.Corschem == 0 ) )
    if( (Pm_debet.PaymentID != Pm_paym.PaymentID) or (Pm_debet.Group != PAYMENTS_GROUP_EXTERNAL) ) // платеж внутренний -> в текущий день
      /* Актуализировать проводки МФР */
      if( not CarryPlanDocuments( Pm_paym.PaymentID ) )
        MsgBox("Ошибка при помещении планируемых проводок в проведенные");
        return 1;
      end;

      if( not d.Carry() )
        MsgBox("Ошибка при вставке рублевого документа");
        return 1;
      end;
    else //иначе -> в планируемые
      if( not d.Carry(ACCTRN_STATUS_PLAN) )
        MsgBox("Ошибка при вставке рублевого документа");
        return 1;
      end;
    end;
  end;

  return 0;

END;
