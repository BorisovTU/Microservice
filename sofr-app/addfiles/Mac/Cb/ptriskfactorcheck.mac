/*
$Name:           ptriskfactorcheck.mac
$Module:         Ядро ГКБО
$Description:    Дистрибутивные функции проверки фактора риска
*/

/*
   В макросе реализованы дистрибутивные функции проверки фактора риска,
   заданные в справочнике "Виды факторов риска субъектов"
*/




Import PTInter, CTInter, FMInter, rsd, "commonutil.mac";

/*
 *  Константы для видов факторов риска, обрабываемых данным макросом
 */

const RFK_FACE_SPECIAL_CATEGORY    =  2; /* Лицо особой категории                        */
const RFK_EXISTS_DENIAL_OF_SERVICE =  4; /* Имеются отказы в обслуживании                */
const RFK_PARTY_TERRORIST          =  9; /* Включен в Перечень террористов               */
const RFK_PARTY_FREEZING           = 10; /* Применены меры по замораживанию              */
const RFK_PARTY_NO_EXECUTIVE_BODY  = 24; /* Отсутствует исполнительный орган по адресу */
const RFK_COUNTRY_13               = 13; /* Страна под международными санкциями          */
const RFK_COUNTRY_14               = 14; /* Страна под специальными экономическими мерами*/
const RFK_COUNTRY_15               = 15; /* Страна не выполняет рекомендации ФАТФ        */
const RFK_COUNTRY_16               = 16; /* Страна поддерживает терроризм                */
const RFK_COUNTRY_17               = 17; /* Страна с повышенным уровнем коррупции        */
const RFK_COUNTRY_25               = 25; /* В стране произ-водятся и пере-правляются наркотики*/
const RFK_COUNTRY_26               = 26; /* В стране разре-шен оборот наркотиков         */
const RFK_EXISTS_FINMON_OPERATION  = 22; /* Совершение операций повышенного уровня риска */

private macro _GetRegBenefOwnerMinPercent()
  var stat;
  var Percent;
  GetRegistryValue("CB\\PARTY\\BENEFOWNER_MIN_PERCENT", V_INTEGER, Percent, stat);
  return Percent;
end;

/*
 * Проверка фактора риска "Лицо особой категории"
 */
private macro checkFaceSpecialCategory( PartyID : integer, IsOn : @bool, Ground : @string )

  var ptName   : string;
  var attrName : string;

  var strSQL : string;

  var rs : RsdRecordset;
  var cmd : RsdCommand;

  strSQL = "SELECT pt.ptName, (SELECT oa.t_Name FROM dobjattr_dbt oa WHERE oa.t_ObjectType = oac.t_ObjectType AND oa.t_GroupID = oac.t_GroupID AND oa.t_AttrID = oac.t_AttrID) attrName "
           "FROM "
           "("
           " SELECT 0 Num, pt.t_PartyID PartyID, pt.t_Name ptName "
           " FROM dparty_dbt pt "
           " WHERE pt.t_PartyID = ? "
           "UNION"
           " SELECT ROWNUM Num, bo.t_BeneOwnerID PartyID, pt.t_Name ptName "
           " FROM dptbeneowner_dbt bo, dparty_dbt pt "
           " WHERE bo.t_OwnPrc = ? "
           "   AND bo.t_PartyID = ? "
           "   AND bo.t_BeneOwnerID = pt.t_PartyID "
           "   AND bo.t_BeginDate <= SYSDATE AND DECODE(bo.t_EndDate, ?, ?, bo.t_EndDate) >= SYSDATE "
           ") pt, dobjatcor_dbt oac "
           "WHERE oac.t_ObjectType = ? AND oac.t_GroupID = ? AND oac.t_Object = LPAD(TO_CHAR(pt.PartyID), 10, '0') AND oac.t_General = 'X' AND SYSDATE BETWEEN oac.t_ValidFromDate AND oac.t_ValidToDate "
           "ORDER BY Num";


  cmd = RsdCommand( strSQL );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, _GetRegBenefOwnerMinPercent());
  cmd.addParam( "", RSDBP_IN, PartyID );               /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, PartyID );               /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, date(0, 0, 0) );         /* Нулевая дата - незаданная дата окончания действия бенефициарного владельца */
  cmd.addParam( "", RSDBP_IN, date(31, 12, 9999) );    /* Максимальная дата */
  cmd.addParam( "", RSDBP_IN, OBJTYPE_PARTY );         /* Вид объекта СУБЪЕКТ ЭКОНОМИКИ */
  cmd.addParam( "", RSDBP_IN, PARTY_ATTR_GROUP_IPDL ); /* Номер группы атрибутов категории "Принадлежность к некоторым категориям лиц по 115-ФЗ" */

  rs = RsdRecordset( cmd );

  IsOn = false;
  Ground = "";

  while( rs.moveNext() )

    IsOn = true;

    ptName   = rs.value( "ptName"   );
    attrName = rs.value( "attrName" );

    if( Ground != "" ) Ground = Ground + "\n"; end;

    Ground = Ground + string( "Субъект ", ptName, " принадлежит к особой категории \"", attrName, "\".");

  end;

end;

/*
 * Проверка фактора риска "Имеются отказы в обслуживании"
 */
private macro checkExistsDenialOfService( PartyID : integer, IsOn : @bool, Ground : @string )

  var OprType : integer;
  var OprDate : date;

  var strSQL : string;

  var rs : RsdRecordset;
  var cmd : RsdCommand;

  strSQL = "SELECT DISTINCT op.t_OperationID, op.t_Type, op.t_Date "
           "FROM dopcntrpt_dbt pt, dopcontr_dbt op "
           "WHERE pt.t_ClientID = ? AND op.t_OperationID = pt.t_OperationID "
           "  AND op.t_Type IN (?, ?) AND op.t_Status <> ? AND ROWNUM <= 10";

  cmd = RsdCommand( strSQL );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, PartyID );                 /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, FM_OPER_TYPE_REJ_ACC );    /* Тип записи в ФМ "" */
  cmd.addParam( "", RSDBP_IN, FM_OPER_TYPE_REJ_OPR );    /* Тип записи в ФМ "" */
  cmd.addParam( "", RSDBP_IN, OPRCONTR_STATUS_REJECT );  /* Статус записи в ФМ "отвергнута" */

  rs = RsdRecordset( cmd );

  IsOn = false;
  Ground = "";

  while( rs.moveNext() )

    IsOn = true;

    OprType = rs.value( "t_Type" );
    OprDate = rs.value( "t_Date" );

    if( Ground != "" ) Ground = Ground + "\n"; end;

    Ground = Ground + string( "Для субъекта зарегистрирован отказ от обслуживания: ", GetOpContrTypeName(OprType), " от ", OprDate:f, "." );

  end;
  
  var cmd2 : RsdCommand;
  cmd2 = RsdCommand("SELECT DISTINCT TRUNC(T_DATE) FROM DNOTETEXT_DBT WHERE T_DOCUMENTID = RCB_OBJATTR.MAKEPARTYID (?) AND T_NOTEKIND = 71 AND T_OBJECTTYPE = 3 AND T_VALIDTODATE = TO_DATE('31.12.9999', 'dd.mm.yyyy')");
  cmd2.addParam("", RSDBP_IN, PartyID); // ИД проверяемого субъекта 
  var rs2 = RsdRecordset(cmd2);
  
  while(rs2.moveNext())
    IsOn = true;
    if( Ground != "" ) Ground = Ground + "\n"; end;
    
    var RejectDate : date = rs2.value(0);
    Ground = Ground + string( "У субъекта есть примечание вида \"Имеются отказы в обслуживании\" от ", RejectDate:f, ".");
  end;
end;

/*
 * Проверка фактора риска "Включен в Перечень террористов"
 */
private macro checkPartyTerrorist( PartyID : integer, IsOn : @bool, Ground : @string )

  var ptName : string;

  var strSQL : string;

  var rs : RsdRecordset;
  var cmd : RsdCommand;

  strSQL = "SELECT pt.ptName "
           "FROM "
           "("
           " SELECT 0 Num, pt.t_PartyID PartyID, pt.t_Name ptName "
           " FROM dparty_dbt pt "
           " WHERE pt.t_PartyID = ? "
           "UNION"
           " SELECT ROWNUM Num, bo.t_BeneOwnerID PartyID, pt.t_Name ptName "
           " FROM dptbeneowner_dbt bo, dparty_dbt pt "
           " WHERE bo.t_PartyID = ? AND bo.t_BeneOwnerID = pt.t_PartyID AND bo.t_BeginDate <= SYSDATE AND (t_EndDate = ? OR t_EndDate >= SYSDATE)"
           ") pt, dpartyown_dbt pown "
           "WHERE pown.t_PartyID = pt.PartyID AND pown.t_PartyKind = ? "
           "ORDER BY Num";


  cmd = RsdCommand( strSQL );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, PartyID );        /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, PartyID );        /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, date(0, 0, 0) );  /* Нулевая дата - незаданная дата окончания действия бенефициарного владельца */
  cmd.addParam( "", RSDBP_IN, PTK_FSFM );       /* Вид субъекта "Включен в Перечень ФСФМ" */

  rs = RsdRecordset( cmd );

  IsOn = false;
  Ground = "";

  while( rs.moveNext() )

    IsOn = true;

    ptName = rs.value( "ptName" );

    if( Ground != "" ) Ground = Ground + "\n"; end;

    Ground = Ground + string( "Субъект ", ptName, " включен в Перечень ФСФМ.");

  end;

end;

/*
 * Проверка фактора риска "Применены меры по замораживанию"
 */
private macro checkPartyFreezing( PartyID : integer, IsOn : @bool, Ground : @string )

  var ptName : string;

  var strSQL : string;

  var rs : RsdRecordset;
  var cmd : RsdCommand;

  strSQL = "SELECT pt.ptName "
           "FROM "
           "("
           " SELECT 0 Num, pt.t_PartyID PartyID, pt.t_Name ptName "
           " FROM dparty_dbt pt "
           " WHERE pt.t_PartyID = ? "
           "UNION"
           " SELECT ROWNUM Num, bo.t_BeneOwnerID PartyID, pt.t_Name ptName "
           " FROM dptbeneowner_dbt bo, dparty_dbt pt "
           " WHERE bo.t_PartyID = ? AND bo.t_BeneOwnerID = pt.t_PartyID AND bo.t_BeginDate <= SYSDATE AND (t_EndDate = ? OR t_EndDate >= SYSDATE)"
           ") pt, dptfrzng_dbt frz "
           "WHERE frz.t_PartyID = pt.PartyID "
           "ORDER BY Num";


  cmd = RsdCommand( strSQL );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, PartyID );        /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, PartyID );        /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, date(0, 0, 0) );  /* Нулевая дата - незаданная дата окончания действия бенефициарного владельца */

  rs = RsdRecordset( cmd );

  IsOn = false;
  Ground = "";

  while( rs.moveNext() )

    IsOn = true;

    ptName = rs.value( "ptName" );

    if( Ground != "" ) Ground = Ground + "\n"; end;

    Ground = Ground + string( "Средства и имущество субъекта ", ptName, " заморожены по решению уполномоченного органа.");

  end;

end;

private macro _FM_GetCountryCodesForFactor( RiskFactorKind : integer )
  var strSQL : string;
  var rs : RsdRecordset;
  var cmd : RsdCommand;
  var ResidentCountry, err;
  var CountryCodes = "";

  GetRegistryValue( "COMMON\\ПЕРЕМЕННЫЕ\\СТРАНА РЕЗИДЕНТНОСТИ", V_STRING, ResidentCountry, err );

  CaptureOutput;
[  
  SELECT NVL(LISTAGG (t_CodeNum3, ';') WITHIN GROUP (ORDER BY t_CodeNum3), CHR(1))
    FROM dcountry_dbt
   WHERE t_CodeLat3 IN
         (SELECT t_Object
            FROM dobjatcor_dbt
           WHERE t_ObjectType = 5 AND t_GroupID = 1 
             AND t_AttrID IN 
                   (SELECT t_AttrID FROM dobjattr_dbt 
                     WHERE t_ObjectType = 5 AND t_GroupID = 1 AND t_IntAttr = ?
                   )
         )
     AND t_CodeLat3 <> ?
];
  strSQL = StopCaptureOutput;
  cmd = RsdCommand( strSQL );
  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, RiskFactorKind );
  cmd.addParam( "", RSDBP_IN, ResidentCountry );

  rs = RsdRecordset( cmd );

  if(rs.moveNext())
    CountryCodes = rs.value(0);
  end;

  return CountryCodes;    
end;


/*
 * Проверка факторов странового риска
 */
private macro checkCountryCodes( PartyID : integer, IsOn : @bool, Ground : @string, RiskFactorKind : integer )

  var CountryCodes : string;
  var PrevPartyID = -1;
  var CheckPartyID : integer;
  var Relation : string;
  var Reason : string;

  IsOn = false;
  Ground = "";

  CountryCodes = _FM_GetCountryCodesForFactor(RiskFactorKind);

  if( (CountryCodes != null) and (CountryCodes != "") )
  
    var strSQL : string;

    var rs : RsdRecordset;
    var cmd : RsdCommand;

    CaptureOutput;
[
SELECT 0 Num, 0 SubNum, pt.t_PartyID PartyID, CHR(1) Relation                                                                                 
  FROM dparty_dbt pt                                                                                                                  
 WHERE pt.t_PartyID = ?/*PartyID*/                                                                                                              
UNION                                                                                                                              
SELECT 1 Num, 0 SubNum, bo.t_BeneOwnerID PartyID, ' (бенефициарный владелец)' Relation                                                                                              
  FROM dptbeneowner_dbt bo, dparty_dbt pt                                                                                             
 WHERE bo.t_PartyID = ?/*PartyID*/ AND bo.t_BeneOwnerID = pt.t_PartyID 
   AND bo.t_BeginDate <= SYSDATE AND (bo.t_EndDate = ?/*ZeroDate*/ OR bo.t_EndDate >= SYSDATE) 
UNION                                                                                                                              
SELECT Num, SubNum, PartyID, Relation 
  FROM
(
  SELECT DISTINCT 2 Num, 0 SubNum, PartyID, ' (контрагент по платежам)' Relation 
    FROM
    (
      SELECT DISTINCT t_Receiver PartyID FROM dpmpaym_dbt WHERE t_Payer = ?/*PartyID*/
      UNION
      SELECT DISTINCT t_Payer PartyID FROM dpmpaym_dbt WHERE t_Receiver = ?/*PartyID*/
    )
   GROUP BY PartyID
  UNION
  SELECT DISTINCT 2 Num, 1 SubNum, PartyID, ' (банк контрагента по платежам)' Relation 
    FROM
    (
      SELECT DISTINCT t_ReceiverBankID PartyID FROM dpmpaym_dbt WHERE t_Payer = ?/*PartyID*/
      UNION
      SELECT DISTINCT t_PayerBankID PartyID FROM dpmpaym_dbt WHERE t_Payer = ?/*PartyID*/
      UNION
      SELECT DISTINCT t_ReceiverBankID PartyID FROM dpmpaym_dbt WHERE t_Receiver = ?/*PartyID*/
      UNION
      SELECT DISTINCT t_PayerBankID PartyID FROM dpmpaym_dbt WHERE t_Receiver = ?/*PartyID*/
    )
   GROUP BY PartyID
)
WHERE PartyID > -1
ORDER BY Num, PartyID, SubNum                                                                                                              
];
    strSQL = StopCaptureOutput;

    cmd = RsdCommand( strSQL );

    cmd.nullConversion = true;

    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, date(0, 0, 0) ); /* Нулевая дата - незаданная дата окончания действия бенефициарного владельца */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */
    cmd.addParam( "", RSDBP_IN, PartyID );       /* ИД проверяемого субъекта */

    rs = RsdRecordset( cmd );

    while( rs.moveNext() )


      var strSQL2 : string;

      var rs2 : RsdRecordset;
      var cmd2 : RsdCommand;

      CheckPartyID = rs.value( "PartyID" );
      Relation = rs.value( "Relation" );

      if(PrevPartyID == CheckPartyID) 
        continue;
      else
        PrevPartyID = CheckPartyID;
      end;

      strSQL2 = "SELECT pt.t_Name, pt.t_Type, cntr.t_CodeNum3 "
                "FROM "
                "("
                " SELECT pt.t_PartyID, pt.t_Name t_Name, 0 t_Type, pt.t_NRCountry t_Country "
                " FROM dparty_dbt pt"
                " WHERE pt.t_PartyID = ? "
                "UNION "
                " SELECT pt.t_PartyID, pt.t_Name t_Name, addr.t_Type, addr.t_Country t_Country "
                " FROM dparty_dbt pt, dadress_dbt addr"
                " WHERE pt.t_PartyID = ? AND addr.t_PartyID = pt.t_PartyID AND addr.t_Type IN (?, ?) "
                ") pt, dcountry_dbt cntr "
                "WHERE cntr.t_CodeLat3 = pt.t_Country AND pt.t_Country <> ?"
                "ORDER BY t_Type ";

      cmd2 = RsdCommand( strSQL2 );

      cmd2.nullConversion = true;

      cmd2.addParam( "", RSDBP_IN, CheckPartyID ); /* ИД субъекта, адрес которого проверяем */
      cmd2.addParam( "", RSDBP_IN, CheckPartyID ); /* ИД субъекта, адрес которого проверяем */
      cmd2.addParam( "", RSDBP_IN, PTADDR_LEGAL ); /* Вид адреса "Юридический адрес" */
      cmd2.addParam( "", RSDBP_IN, PTADDR_REAL );  /* Вид адреса "Фактический адрес" */
      cmd2.addParam( "", RSDBP_IN, "" );           /* Пустая строка - незаданный код страны для адреса субъекта */

      rs2 = RsdRecordset( cmd2 );
     
       while( rs2.moveNext() )

         var Country : string;

         Country = rs2.value( "t_CodeNum3" );

         if( (Country != "") and (Index(CountryCodes, Country) != 0) )
         
           var addrType : integer;
           var addrTypeName : string;

           var ptName : string;

           IsOn = true;

           ptName = rs2.value( "t_Name" );

           addrType = rs2.value( "t_Type" );

             if( addrType == PTADDR_LEGAL ) addrTypeName = "юридического адреса ";
           elif( addrType == PTADDR_REAL  ) addrTypeName = "фактического адреса ";
           else                             addrTypeName = "";
           end;

           Reason = "";
           
           if  (RiskFactorKind == RFK_COUNTRY_13)
             Reason = " под международными санкциями";
           elif(RiskFactorKind == RFK_COUNTRY_14)
             Reason = " под специальными экономическими мерами";
           elif(RiskFactorKind == RFK_COUNTRY_15)
             Reason = " не выполняет рекомендации ФАТФ";
           elif(RiskFactorKind == RFK_COUNTRY_16)
             Reason = " поддерживает терроризм";
           elif(RiskFactorKind == RFK_COUNTRY_17)
             Reason = " с повышенным уровнем коррупции";
           end;

           if(Reason != "")
             if( Ground != "" ) Ground = Ground + "\n"; end;
  
             Ground = Ground + string( "Страна ", addrTypeName, "субъекта ", CheckPartyID, Relation, Reason);
           end;
         end;

       end;
     
     end;

  end;

end;

/*
 * Проверка фактора риска "Совершение операций повышенного уровня риска"
 */
private macro checkExistsFinMonOperation( PartyID : integer, IsOn : @bool, Ground : @string )

  var OprType : integer;
  var OprDate : date;

  var strSQL : string;

  var rs : RsdRecordset;
  var cmd : RsdCommand;

  strSQL = "SELECT DISTINCT op.t_OperationID, op.t_Type, op.t_Date "
           "FROM dopcntrpt_dbt pt, dopcontr_dbt op "
           "WHERE pt.t_ClientID = ? AND pt.t_Kind = ? AND op.t_OperationID = pt.t_OperationID "
           "  AND op.t_Type = ? AND op.t_Status <> ? AND ROWNUM <= 10";

  cmd = RsdCommand( strSQL );

  cmd.nullConversion = true;

  cmd.addParam( "", RSDBP_IN, PartyID );                /* ИД проверяемого субъекта */
  cmd.addParam( "", RSDBP_IN, _FM_PARTY_PAYER );        /* Вид участника операции "Плательщик" */
  cmd.addParam( "", RSDBP_IN, FM_OPER_TYPE_LEGAL );     /* Тип записи в ФМ "" */
  cmd.addParam( "", RSDBP_IN, OPRCONTR_STATUS_REJECT ); /* Статус записи в ФМ "отвергнута" */

  rs = RsdRecordset( cmd );

  IsOn = false;
  Ground = "";

  while( rs.moveNext() )

    IsOn = true;

    OprType = rs.value( "t_Type" );
    OprDate = rs.value( "t_Date" );

    if( Ground != "" ) Ground = Ground + "\n"; end;

    Ground = Ground + string( "Для субъекта зарегистрирована операция повышенного уровня риска типа ", GetOpContrTypeName(OprType), " от ", OprDate:f, "." );

  end;

end;

private macro checkNoExecutiveBody(PartyID : integer, IsOn : @bool, Ground : @string)
    var party = TBfile("party.dbt", "R", 0);
    party.rec.PartyID = PartyID;
    
    Ground = "";
    if (party.getEQ == true)
        CheckObjAttrPresence(IsOn, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 56, null, null, "0", false);
        
        if (IsOn)
            Ground = "Для субъекта " + party.rec.name + " установлен признак 'отсутствует' категории 'Нахождение органа управления по юридическому адресу'";
        end;
    end;
end;

/*
 * Функция проверки фактора риска
 */
macro CheckPtRiskFactor( PartyID : integer, RiskFactorKind : integer, IsOn : @bool, Ground : @string )

  IsOn = false;
  Ground = "";

  if( RiskFactorKind == RFK_FACE_SPECIAL_CATEGORY )

    /* Лицо особой категории */
    checkFaceSpecialCategory( PartyID, @IsOn, @Ground );

  elif( RiskFactorKind == RFK_EXISTS_DENIAL_OF_SERVICE )

    /* Имеются отказы в обслуживании */
    checkExistsDenialOfService( PartyID, @IsOn, @Ground );

  elif( RiskFactorKind == RFK_PARTY_TERRORIST )

    /* Включен в Перечень террористов */
    checkPartyTerrorist( PartyID, @IsOn, @Ground );

  elif( RiskFactorKind == RFK_PARTY_FREEZING )

    /* Применены меры по замораживанию */
    checkPartyFreezing( PartyID, @IsOn, @Ground );

  elif((RFK_COUNTRY_13 <= RiskFactorKind) and (RiskFactorKind <= RFK_COUNTRY_17))

    /* Страна не выполняет рекомендации ФАТФ */
    checkCountryCodes( PartyID , @IsOn, @Ground, RiskFactorKind );
  elif( (RiskFactorKind == RFK_COUNTRY_25) or (RiskFactorKind == RFK_COUNTRY_26))
    /*
        В стране произ-водятся и пере-правляются наркотики
        В стране разре-шен оборот наркотиков
    */
    checkCountryCodes( PartyID , @IsOn, @Ground, RiskFactorKind );
  elif( RiskFactorKind == RFK_EXISTS_FINMON_OPERATION )

    /* Совершение операций повышенного уровня риска */
    checkExistsFinMonOperation( PartyID , @IsOn, @Ground );
  
  elif (RiskFactorKind = RFK_PARTY_NO_EXECUTIVE_BODY)
    /* Отсутствует исполнительный орган по адресу */
    checkNoExecutiveBody(PartyID , @IsOn, @Ground);
  end;

  return 0;

end;
