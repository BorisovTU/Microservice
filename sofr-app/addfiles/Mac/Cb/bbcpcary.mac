/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 2000              */
/*                                                                      */
/*  Имя файла        : bbcpcary.mac                                     */
/*                                                                      */
/*  Описание         : Выполнение шага зачисления                       */
/*                     средств по банковскому валютному платежу         */
/*  Программист      : Бурак В.Ю.                                       */
/*                                                                      */
/*  Создан           : 17.01.2000                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import InsCarryDoc, BankInter, "cppmconv.mac", "paym_rec.mac";

//Символ пустой? (пустая строка или только нули)
PRIVATE MACRO isEmptySymbol( symbol ):bool
  return ( int(trim(symbol)) == 0 );
ONERROR(err)
  return TRUE;
END;

/**********************************/
/* Выполнение шага                */
/**********************************/
MACRO ExecuteStep( doc, payorder )     
  var d = RsbAccTransaction(); // RsbAccTransactionData

  record  cporder(bbcpord);
  setbuff(cporder,payorder);

  /* Актуализировать проводки МФР */
  if( not CarryPlanDocuments( Pm_paym.PaymentID ) )
    MsgBox("Ошибка при помещении планируемых проводок в проведенные");
    return 1;
  end;

  /*****************************************************
   Оплата комиссии
  *****************************************************/
  if( cporder.Origin == CP_OR_SF )

    if( ПолучитьДополнительныеСчетаПроводокКонверсии() )
      msgbox( "Ошибка при установке счетов ОВП" );
      return 1;
    end;

    if( ExecMacroFile( "sfinsdoc.mac", "ВставитьПлатуЗаОбслуживание", 
                       Pm_paym, doc,
                       Pm_rmprop.Number, Pm_paym.ValueDate,
                       Pm_paym.Department, Pm_paym.NumberPack, 
                       СчетДоходов, СчетРасходов) )
      return 1;
    end;

  /*****************************************************
   Зачисление на клиентский счет
  *****************************************************/
  else

    /* Проводка без конверсии*/
    if( Pm_paym.FIID == Pm_paym.PayFIID )

      d.Chapter          = 1;
      d.Date_Carry       = Pm_paym.ValueDate;
      d.Number_Pack      = Pm_paym.NumberPack;
      d.AccountPayer     = Pm_paym.FuturePayerAccount;
      d.AccountReceiver  = Pm_paym.FutureReceiverAccount;
      d.Sum              = Pm_paym.Amount;
      d.Ground           = Pm_rmprop.Ground;
      d.FIID             = Pm_paym.PayFIID;
      d.Numb_Document    = Pm_rmprop.Number;
      d.Department       = Pm_paym.Department;
      d.Result_Carry     = 1;
      d.Kind_Oper        = " 1";
      
      /* Получение массива символов */
      if( not isEmptySymbol(Pm_rmprop.SymbNotBalDebet) )
        d.AddCashSymbol(Pm_rmprop.SymbNotBalDebet, d.Sum);
      end;

      if( Pm_paym.FIID )
        if( not d.Carry() )
           msgbox("Ошибка при вставке валютного документа");
           return 1;
        end;
      else
        if( not d.Carry() )
           msgbox("Ошибка при вставке рублевого документа");
           return 1;
        end;
      end;

    /* Проводка с конверсией*/
    else
      
      if( ПолучитьДополнительныеСчетаПроводокКонверсии() )
        msgbox( "Ошибка при установке счетов ОВП" );
        return 1;
      end;

      d.MCMethod        = 1;
      d.FIIDPayer       = Pm_paym.FIID;
      d.FIIDReceiver    = Pm_paym.PayFIID;
      d.SumPayer        = Pm_paym.Amount;
      d.SumReceiver     = Pm_paym.PayAmount;
      /* Из-за МФР проводки теперь делаются только по Future-счетам */
      d.AccountPayer    = Pm_paym.FuturePayerAccount;
      d.AccountReceiver = Pm_paym.FutureReceiverAccount;
      d.Chapter         = 1;
      d.Ground          = Pm_rmprop.Ground;
      d.Numb_Document   = Pm_rmprop.Number;
      d.Date_Carry      = Pm_paym.ValueDate;
      d.Number_Pack     = Pm_paym.NumberPack;
      d.Department      = Pm_paym.Department;
      d.ERAccountPlus   = СчетДоходов;
      d.ERAccountMinus  = СчетРасходов;

      if( not d.Carry() )
        MsgBox( "Ошибка при вставке мультивалютного документа" );
        return 1; 
      end;
    end;
  end;

  /*      Установить статус платежа - завершенный платеж  */
  if( not InsertPaymentStat( Pm_paym.PaymentID, 32000 /*PM_FINISHED*/ ) )
    msgbox("Ошибка при вставке статуса платежа");
    return 1;
  end;

  return 0;

END;
