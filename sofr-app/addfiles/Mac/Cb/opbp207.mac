/*
 *  Операция 207 "Платеж банка".
 *  Макрос шага 20 "Проводка".
 */

import InsCarryDoc;    
import PaymInter, BankInter;
import "pmoutcom.mac"; 
import "paym_rec";

/* Символ пустой? (пустая строка или только нули) */
PRIVATE MACRO isEmptySymbol( symbol ):bool
  return ( int(trim(symbol)) == 0 );
ONERROR(err)
  return TRUE;
END;

/* Перед выполнением транзакции шага */
MACRO ExecuteStep( doc, primdoc, DocKind, ID_Operation )
  var d = RsbAccTransaction(); // RsbAccTransactionData

  /* Актуализировать проводки МФР */
  if( not CarryPlanDocuments( Pm_paym.PaymentID ) )
    MsgBox("Ошибка при помещении планируемых проводок в проведенные");
    return 1;
  end;

  /* Заполняем структуру проводки */
  d.Chapter          = 1;
  d.Kind_Oper        = " 1";
  d.ResultCarry      = 1;
  d.Date_Carry       = Pm_paym.ValueDate;
  d.Sum              = Pm_paym.Amount;
  d.FIID             = Pm_paym.FIID;
  d.Number_Pack      = Pm_paym.NumberPack;
  d.Numb_Document    = Pm_rmprop.Number;
  d.Department       = Pm_paym.Department;
  d.Shifr_Oper       = Pm_rmprop.ShifrOper;
  d.Ground           = Pm_rmprop.Ground;

  /* Из-за МФР проводки теперь делаются только по Future-счетам */
  d.AccountPayer     = Pm_paym.FuturePayerAccount;
  d.AccountReceiver  = Pm_paym.FutureReceiverAccount;
  
  if( d.AccountReceiver == "" )
    MsgBox("В схеме расчетов не задан счет \"Незавершенные расчеты банка (списание)\"");
    return 1;
  end;

  /* Получение массива символов */
  if( not isEmptySymbol(Pm_rmprop.SymbNotBal) )
    d.AddCashSymbol(Pm_rmprop.SymbNotBal, d.Sum);
  end;

  /* Выполним проводку */
  if( not d.Carry() )
    MsgBox("Ошибка при вставке рублевого документа");
    return 1;
  end;

  // Внутренний платеж
    
  /* Внутренний платеж после проводки закрываем */
  if( not InsertPaymentStat( 0, PM_FINISHED, PMMET_DPS, 0, 0, Pm_paym.Purpose, Pm_paym.SubPurpose ) )
    MsgBox("Ошибка при вставке статуса платежа");
    return 1;
  end;

  if( not PM_IsInternalPayerAccount( Pm_paym ) )

    /* Изменяем дату списания со счета плательщика */
    if( not InsertPayerChargeOffDate( Pm_paym, Pm_rmprop, Pm_paym.ValueDate ) )
      msgbox("Ошибка при изменении даты списания со счета плательщика");
      return 1;
    end;

    /* Изменяем дату отметки банка плательщика */
    if( not InsertPayerBankMarkDate( Pm_paym, Pm_rmprop, Pm_paym.ValueDate ) )
      msgbox("Ошибка при изменении даты отметки банка плательщика");
      return 1;
    end;

  end;
    
  return 0;

END;
