/*
$Name: ic_getacctrn.mac
$Module: Ядро Banking
$Description: Сервис "Запрос выписки по счету клиента"
*/

import BankInter, "bnk_common.mac", "ic_getaccclient.mac";

// Определение реквизитов операции по счету

private macro GetOperation(AccTrn:RSDRecordSet, TType:integer, BIC:@string, Account:@string, Ground:@string )

  var IsPaymentExists = false;

  if(AccTrn.value("PaymentID") != 0)
    IsPaymentExists = true;
  end;

  var FSSPCorrBank = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\FSSPCorrBank", V_BOOL, false);

  if((not IsPaymentExists) and ((TType == 1) or (TType == 2)))
    BIC = GetPartCode(AccTrn.value("Department"), PTCK_BIC);  
  elif((TType == 1) and (AccTrn.value("DebitGroup") == PAYMENTS_GROUP_INTERNAL))
    BIC = GetPartCode(AccTrn.value("StartDepartment"), PTCK_BIC);
  elif((TType == 2) and (AccTrn.value("CreditGroup") == PAYMENTS_GROUP_INTERNAL))
    BIC = GetPartCode(AccTrn.value("EndDepartment"), PTCK_BIC);
  elif((TType == 1) and (AccTrn.value("DebitGroup") == PAYMENTS_GROUP_EXTERNAL))
    BIC = GetPartCode(IfThenElse(FSSPCorrBank, AccTrn.value("PayerBankID"), AccTrn.value("DebitCorrID")), PTCK_BIC);

    if(BIC == "")
      BIC = Substr(GetPartCode(IfThenElse(FSSPCorrBank, AccTrn.value("PayerBankID"), AccTrn.value("DebitCorrID")), PTCK_SWIFT), 1, 8);
    end;

    if(BIC == "")
      BIC = "000000000";
    end;
  elif((TType == 2) and (AccTrn.value("CreditGroup") == PAYMENTS_GROUP_EXTERNAL))
    BIC = GetPartCode(IfThenElse(FSSPCorrBank, AccTrn.value("ReceiverBankID"), AccTrn.value("CreditCorrID")), PTCK_BIC);

    if(BIC == "")
      BIC = Substr(GetPartCode(IfThenElse(FSSPCorrBank, AccTrn.value("ReceiverBankID"), AccTrn.value("CreditCorrID")), PTCK_SWIFT), 1, 8);
    end;

    if(BIC == "")
      BIC = "000000000";
    end;    
  end;

  var FSSPCorrAccount = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\BANKING\\FSSPCorrAccount", V_BOOL, false);

  if(not IsPaymentExists)
    if(TType == 1)
      Account = AccTrn.value("Account_Payer");
    elif(TType == 2)
      Account = AccTrn.value("Account_Receiver");
    end;
  elif(not FSSPCorrAccount)
    if(TType == 1)
      Account = AccTrn.value("PayerCorrAccNostro");
    elif(TType == 2)
      Account = AccTrn.value("ReceiverCorrAccNostro");
    end;
  else
    if(TType ==1)
      Account = AccTrn.value("PayerAccount");
    elif(TType == 2)
      Account = AccTrn.value("ReceiverAccount");
    end;
  end;

  if(Account == "")
    Account = "00000000000000000000";
  end;

  if(IsPaymentExists)
    Ground = AccTrn.value("PGround");
  else
    Ground = AccTrn.value("Ground");
  end;

end;

class AccountTransactionRequest                                  // Входящие параметры
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var          OrigReq :       ReqOrganisation;                  // Определяет параметры исходного Запроса от внешней АС
  var              BIC :                string =            "";  // БИК банка/филиала, где открыт счет
  var          Account :                string =            "";  // Номер счета
  var  PersonalAccount :                string =            "";  // Субсчет, карта
  var        DateReqIn :                  date = date(0, 0, 0);  // Дата начала выписки
  var       DateReqEnd :                  date = date(0, 0, 0);  // Дата окончания выписки  
end;

class      AccountTransaction                                    // Описание операции
  var            TType :               integer =            "";  // Тип операции
  var             Date :                  date;                  // Дата операции
  var           Amount :                 money =            $0;  // Сумма операции
  var              BIC :                string =            "";  // БИК банка-корреспондента
  var          Account :                string =            "";  // Счет корреспондента
  var          Grounds :                string =            "";  // Основание
end;

class AccountTransactionResponse
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var         SenderID :                string =            "";  // Идентификатор (код) АС, источник сведений
  var     RealDateTime :              DateTime = date(0, 0, 0);  // Дата, время актуальности сведений
  var              BIC :                string =            "";  // БИК Банка
  var          Account :                string =            "";  // Номер счета
  var         Currency :                string =            "";  // Код валюты счета
  var   ClientFullName :                string =            "";  // Наименование владельца счета
  var              INN :                string =            "";  // ИНН владельца счета
  var AccountTransactions :             TArray =        TArray;  // Список операций
end;



macro GetAccTrnIC(searchParameters)

  if(ValType(searchParameters.DateReqIn) == V_UNDEF)
    searchParameters.DateReqIn = date(0,0,0);
  end;
  
  if(ValType(searchParameters.DateReqEnd) == V_UNDEF)
    searchParameters.DateReqEnd = date(0,0,0);
  end;
  
  var query = 
  " SELECT accoun.t_AccountID AS AccountID, " +
  " accoun.t_Code_Currency AS FIID, "
  " dp_dep.t_PartyID AS Department, " +
  "        accoun.t_Open_Date AS OpenDate, " +
  "        CASE " +
  "           WHEN accoun.t_Close_Date = :ZeroDate1 " +
  "           THEN " +
  "              RSBSESSIONDATA.CURDATE () " +
  "           ELSE " +
  "              accoun.t_Close_Date " +
  "        END " +
  "           AS CloseDate, " +
  "      NVL(fininstr.t_Iso_Number, CHR(1)) AS CurrencyCode, " +
  "      CASE " +
  "        WHEN party.t_LegalForm = :PTLEGF_INST THEN party.t_Name " +
  "        ELSE persn.t_Name1 || ' ' || persn.t_Name2 || ' ' || persn.t_Name3 " +
  "      END AS ClientFullName, " +
  " NVL(REGEXP_SUBSTR (objcodeINN.t_Code, '[^/]+'), CHR(1)) AS INN " +
  "   FROM daccount_dbt accoun " +
  "        INNER JOIN ddp_dep_dbt dp_dep ON dp_dep.t_Code = accoun.t_Department " +
  "   LEFT OUTER JOIN dfininstr_dbt fininstr " +
  "       ON fininstr.t_FIID = accoun.t_code_Currency  " +
  "   LEFT OUTER JOIN dparty_dbt party ON party.t_PartyID = accoun.t_Client " +
  "   LEFT OUTER JOIN dpersn_dbt persn ON persn.t_PersonID = party.t_PartyID " +
  "   LEFT OUTER JOIN dobjcode_dbt objcodeINN  " +
  "       ON     objcodeINN.t_ObjectID = dp_dep.t_PartyID  " +
  "       AND objcodeINN.t_CodeKInD = :PTCK_INN  " +
  "       AND objcodeINN.t_State = 0  " +
  "  WHERE     accoun.t_Account = :sAccount ";
  
  var queryParams = TArray;
  
  queryParams[queryParams.Size] = SQLParam("ZeroDate1", date(0,0,0));
  queryParams[queryParams.Size] = SQLParam("PTLEGF_INST", PTLEGF_INST);
  queryParams[queryParams.Size] = SQLParam("PTCK_INN", PTCK_INN);
  queryParams[queryParams.Size] = SQLParam("sAccount", searchParameters.Account);
  
  if((searchParameters.DateReqIn != date(0,0,0)) and (searchParameters.DateReqEnd != date(0,0,0))) 
    query = query +
    "          AND accoun.t_Open_Date >= :DateReqIn2 " +
    "      AND (   accoun.t_Close_Date <= :DateReqEnd2 " +
    "           OR accoun.t_Close_Date = :ZeroDate2) " +
    " ORDER BY   LEAST ( " +
    "           CASE " +
    "              WHEN accoun.t_Close_Date = :ZeroDate3 " +
    "              THEN " +
    "                 RSBSESSIONDATA.CURDATE () " +
    "              ELSE " +
    "                 accoun.t_Close_Date " +
    "           END, " +
    "           :DateReqEnd1) " +
    "      - GREATEST (accoun.t_Open_Date, :DateReqIn1) DESC, accoun.t_Open_Date ASC ";
    
    queryParams[queryParams.Size] = SQLParam("DateReqIn2", searchParameters.DateReqIn);
    queryParams[queryParams.Size] = SQLParam("DateReqEnd2", searchParameters.DateReqEnd);
    queryParams[queryParams.Size] = SQLParam("ZeroDate2", date(0,0,0));
    queryParams[queryParams.Size] = SQLParam("ZeroDate3", date(0,0,0));
    queryParams[queryParams.Size] = SQLParam("DateReqEnd1", searchParameters.DateReqEnd);
    queryParams[queryParams.Size] = SQLParam("DateReqIn1", searchParameters.DateReqIn);
  else
    query = query +
    " ORDER BY accoun.t_Open_Date ASC ";
  end;
  
  var rs = execSQLselect(query, queryParams);
  
  var found = false;
      
  while(rs and rs.MoveNext())
    if(GetPartCode(rs.value("Department"), PTCK_BIC) != searchParameters.BIC)
      continue;
    else
      found = true;
      break;
    end;
  end;
  
  if(not found)
    RunError("Не найден счет " + searchParameters.Account );
  else
  
    var StartDate = IfThenElse(searchParameters.DateReqIn == date(0,0,0), rs.value("OpenDate"), searchParameters.DateReqIn);
    var EndDate = IfThenElse(searchParameters.DateReqEnd == date(0,0,0), rs.value("CloseDate"), searchParameters.DateReqEnd);
    
    var queryTransaction =
    " SELECT CASE " +    
    "         WHEN acctrn.t_AccountID_Payer = :AccountIDP1 THEN 1 " +
    "         WHEN acctrn.t_AccountID_Receiver = :AccountIDR1 THEN 2 " +
    "      END " +
    "         AS TType, " +
    "      acctrn.t_Date_carry AS CarryDate, " +
    "      CASE " +
    "         WHEN acctrn.t_AccountID_Payer = :AccountIDP2 " +
    "         THEN " +
    "            acctrn.t_Sum_Receiver " +
    "         WHEN acctrn.t_AccountID_Receiver = :AccountIDR2 " +
    "         THEN " +
    "            acctrn.t_Sum_Payer " +
    "      END " +
    "         AS Amount, " +
    "      acctrn.t_AccTrnID AS AccTrnID, "
    "      acctrn.t_Ground AS Ground, "
    "      acctrn.t_Account_Payer AS Account_Payer, "
    "      acctrn.t_Account_Receiver AS Account_Receiver, "
    "      pmprop_d.t_Group                 AS DebitGroup,  " +
    "      pmprop_c.t_Group                 AS CreditGroup, " +
    "      dp_dep_s.t_PartyID               AS StartDepartment, " +
    "      dp_dep_e.t_PartyID               AS EndDepartment, " +
    "      pmpaym.t_EndDepartment           AS EndDepartment, " +
    "      corschem_d.t_CorrID              AS DebitCorrID, " +
    "      corschem_c.t_CorrID              AS CreditCorrID, " +
    "      pmpaym.t_PayerBankID             AS PayerBankID, " +
    "      pmpaym.t_ReceiverBankID          AS ReceiverBankID, " +
    "      pmrmprop.t_PayerCorrAccNostro    AS PayerCorrAccNostro, " +
    "      pmrmprop.t_ReceiverCorrAccNostro AS ReceiverCorrAccNostro, " +
    "      pmpaym.t_PayerAccount            AS PayerAccount, " +
    "      pmpaym.t_ReceiverAccount         AS ReceiverAccount, " +
    "      pmrmprop.t_Ground                AS PGround, " +
    "      NVL (pmpaym.t_PaymentID, 0)      AS PaymentID, " +
    "      dp_dep.t_PartyID AS Department " +
    " FROM dacctrn_dbt acctrn " +
    "    LEFT OUTER JOIN dpmdocs_dbt pmdocs " +
    "       ON pmdocs.t_AcctrnID = acctrn.t_AcctrnID " +
    "    LEFT OUTER JOIN dpmpaym_dbt pmpaym " +
    "       ON pmdocs.t_PaymentID = pmpaym.t_PaymentID " +
    "    LEFT OUTER JOIN dpmprop_dbt pmprop_d " +
    "       ON     pmprop_d.t_PaymentID = pmpaym.t_PaymentID " +
    "          AND pmprop_d.t_DebetCredit = :PRT_Debit " +
    "    LEFT OUTER JOIN dpmprop_dbt pmprop_c " +
    "       ON     pmprop_c.t_PaymentID = pmpaym.t_PaymentID " +
    "          AND pmprop_c.t_DebetCredit = :PRT_Credit " +
    "    LEFT OUTER JOIN dcorschem_dbt corschem_d " +
    "       ON pmprop_d.t_Corschem = corschem_d.t_Number " +
    "    LEFT OUTER JOIN dcorschem_dbt corschem_c " +
    "       ON pmprop_c.t_Corschem = corschem_c.t_Number " +
    "    LEFT OUTER JOIN dpmrmprop_dbt pmrmprop " +
    "       ON pmrmprop.t_PaymentID = pmpaym.t_PaymentID " +
    "   LEFT OUTER JOIN ddp_dep_dbt dp_dep_s ON dp_dep_s.t_Code = pmpaym.t_StartDepartment " +
    "   LEFT OUTER JOIN ddp_dep_dbt dp_dep_e ON dp_dep_e.t_Code = pmpaym.t_EndDepartment " +
    "   LEFT OUTER JOIN ddp_dep_dbt dp_dep ON dp_dep.t_Code = acctrn.t_Department " +    
   " WHERE        acctrn.t_State = :TRN_STATE_FACT " +
    "         AND acctrn.t_AccountID_Payer = :AccountIDP3 " +
    "      OR     acctrn.t_AccountID_Receiver = :AccountIDR3 " +
    "         AND acctrn.t_Date_Carry BETWEEN :DateStart AND :DateEnd " +
    "         AND acctrn.t_Result_Carry NOT IN (:DELTARATE, " +
    "                                           :DELTARATE_ADD, " +
    "                                           :DELTARATE_MCD, " +
    "                                           :REGRESTOUTCARRY) ";
    
    var queryTransactionParams = TArray;
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDP1", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDR1", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDP2", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDR2", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("PRT_Debit", PRT_Debet);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("PRT_Credit", PRT_Credit);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("TRN_STATE_FACT", TRN_STATE_FACT);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDP3", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("AccountIDR3", rs.value("AccountID"));
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("DateStart", StartDate);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("DateEnd", EndDate);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("DELTARATE", DELTARATE);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("DELTARATE_ADD", DELTARATE_ADD);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("DELTARATE_MCD", DELTARATE_MCD);
    queryTransactionParams[queryTransactionParams.Size] = SQLParam("REGRESTOUTCARRY", REGRESTOUTCARRY);
    
    var rst = execSQLselect(queryTransaction, queryTransactionParams);
    
    var response:AccountTransactionResponse;
    
    response.SenderID = Bnk_GetRegistryValue("RS-CONNECT\\SENDERID\\Banking", V_STRING, "");
    response.ReqID = searchParameters.ReqID;
    response.BIC = GetPartCode(rs.value("Department"), PTCK_BIC);
    response.Account = searchParameters.Account;
    response.Currency = rs.value("CurrencyCode");
    response.ClientFullName = rs.value("ClientFullName");
    response.INN = rs.value("INN");
    
    while(rst and rst.moveNext())
      var trnData = AccountTransaction;
      
      trnData.TType = rst.value("TType");
      
      GetOperation(rst, trnData.TType, @trnData.BIC, @trnData.Account, @trnData.Grounds );
      
      trnData.Date = rst.value("CarryDate");
      trnData.Amount = rst.value("Amount");
      
      response.AccountTransactions[response.AccountTransactions.Size] = trnData;
    end;
   
    trnData = AccountTransaction;
      
    trnData.TType = 3;   
    trnData.Date = StartDate;
    trnData.Amount = GetRestAByID(rs.value("AccountID"), date(StartDate), rs.value("FIID"));
      
    response.AccountTransactions[response.AccountTransactions.Size] = trnData;
    
    trnData = AccountTransaction;
      
    trnData.TType = 4;  
    trnData.Date = EndDate;
    trnData.Amount = GetRestAByID(rs.value("AccountID"), date(EndDate), rs.value("FIID"));
      
    response.AccountTransactions[response.AccountTransactions.Size] = trnData;
    
    response.RealDateTime = DtTm(Date(), Time());
    
    return response;

  end;
  
end;
