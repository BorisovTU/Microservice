/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                    Заполнение строки по шаблону                          */
/*                                                                          */
/*  Имя файла: cbtplstr.mac                                                 */
/*  Создан:  04.03.11                                                  .    */
/****************************************************************************/

import prnfrm;
import PaymInter;
import FIInter;
import BankInter;

var МассивПолей = TArray;


macro ПолучитьСтрокуССуммой(sum:money, FIID)
   var Error = 0, code = 0;
   if(FIID)
      code = int(ПолучитьКодФинИн(FIID, Error, FICK_ISONUMBER)); 
      return string(sum, "(", CurToStrAlt(sum, NULL, NULL, code), ")");
   else
      return string(sum, "(", RubToStrAlt(sum),  ")");
   end;
end;

macro ЗапросОтзывМФР(Платеж:RsbPayment)
   МассивПолей = TArray;
   file dp_dep("dp_dep.dbt");
   file party("party.dbt");
   var  depName = "", partyName = "";


   МассивПолей[0] = Платеж.Number;
   МассивПолей[1] = Платеж.Date;
   МассивПолей[2] = ПолучитьСтрокуССуммой(Платеж.BaseAmount, Платеж.BaseFIID);;
   МассивПолей[3] = Платеж.ReceiverBankName;
   МассивПолей[4] = Платеж.ReceiverAccount;

   KeyNum(dp_dep, 0);
   dp_dep.Code = Платеж.StartDepartment;
   if(getEQ(dp_dep))
     depName = dp_dep.Name;
     if(dp_dep.PartyID)
       party.PartyID = dp_dep.PartyID;
       if(getEQ(party))
         partyName = party.Name;
       end;
     end;
   end;
   МассивПолей[5] = depName;  
   МассивПолей[6] = partyName;
end;



macro СформироватьСтрокуПоШаблону(Объект, ИмяФайлаШаблона)

   var МакросЗаполненияПолей = "";
   var ob:TRepForm;
   var i = 0;
   if(ИмяФайлаШаблона == "reqmfr.tpl")
     МакросЗаполненияПолей = "ЗапросОтзывМФР";
   end;

   if((ИмяФайлаШаблона == "") or (МакросЗаполненияПолей == ""))
     return "";
   end;

   ob = TRepForm (ИмяФайлаШаблона, true );
   ExecMacro2 ( МакросЗаполненияПолей, Объект );

   While(i < МассивПолей.size)
      ob.Value(i) = МассивПолей[i];
      i = i + 1;
   end;
   return ob.ToString();

OnError(er) /* Обработка ошибок времени выполнения */
    msgbox(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return "";
end;

