import globals;
import RsbDataSet;  
import rsd;

import "ws_party_lib.mac";

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по частичному
// совпадению по имени subName
///////////////////////////////////////////////////////////
MACRO LookupPartyByName(subName:STRING, listLen:INTEGER):TArray
  var result = TArray();
  var i;
  var p;
  var query = "SELECT * FROM DPARTY_DBT WHERE LOWER(T_NAME) LIKE LOWER('%" + subName + "%') AND ROWNUM < " + string(listLen);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while (ds.MoveNext())
      p = TWsGenericParty();
      p.Name = ds.NAME;
      p.ShortName = ds.SHORTNAME;
      p.ID = ds.PartyID;
      p.LegalForm = ds.LEGALFORM;
              
    result.value(result.Size) = p;
  end;

  return result;

END;

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по 
// частичному совпадению значения кода subCode вида кода codeKind
///////////////////////////////////////////////////////////
MACRO LookupPartyByCode(codeKind:INTEGER, subCode:STRING, namekind:INTEGER, listLen:INTEGER):TArray
  var result = TArray();
  var i;
  var p;
  var query =  "SELECT O.T_CODE AS CODE," +
                "       P.T_PARTYID AS PARTYID," +
                "       P.T_NAME AS FULLNAME," +
                "       P.T_LEGALFORM AS LEGALFORM," +
                "       N.T_NAME AS NAME," +
                "       P.T_SHORTNAME AS SHORTNAME" +
                "  FROM DPARTY_DBT P" +
                "       LEFT OUTER JOIN DOBJCODE_DBT O" +
                "          ON     P.T_PARTYID = O.T_OBJECTID" +
                "             AND O.T_OBJECTTYPE = 3" +
                "             AND O.T_CODEKIND = " + string(codeKind) +
                "       LEFT OUTER JOIN DPARTYNAME_DBT N" +
                "          ON N.T_PARTYID = P.T_PARTYID AND N.T_NAMETYPEID = " + string(namekind) +
                " WHERE LOWER (O.T_CODE) LIKE LOWER ('%" + subCode + "%') AND ROWNUM < " + string(listLen);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while (ds.MoveNext())
      p = TWsGenericParty();

      p.Code      = ds.CODE;
      p.ID        = ds.PARTYID;
      p.Name      = ds.FULLNAME;
      p.LegalForm = ds.LEGALFORM;
      p.Name_     = ds.NAME;
      p.ShortName = ds.SHORTNAME;
                
      result.value(result.Size) = p;
  end;

  return result;

END;
