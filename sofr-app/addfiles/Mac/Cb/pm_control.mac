 /*
 $Name:        pm_control.mac
 $Module: 
 $Description: Макрос шага(29007 - "Контроль"/10    - "Контроль")
 */
//-----------------------------------------------------------------------------
// Блок     : 29007 - "Контроль"
// Шаг      : 10    - "Контроль"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import pm_setst, cbsttls, PTInter, CashInter, "pm_answerret.mac", "pmfm.mac";

var PaymentObj:RsbPayment;
/*раскомментировать, чтобы при контроле двойным вводом в эту переменную передавался ID дубликата  */
/*var DuplID:integer;*/

PRIVATE const STR_ACTION_STEP_QUESTION  = "правомерность выставления инкассового поручения";
PRIVATE const STR_ACTION_STEP_SET       = "Установить";
PRIVATE const STR_ACTION_STEP_ADD_NOTE  = "Обосновать";
PRIVATE const STR_ACTION_STEP_REJECT    = "Отвергнуть";
PRIVATE const STR_ACTION_STEP_ABORT     = "Прервать";

PRIVATE const PO_REQ_PF_RELEVANCY      = 29;
PRIVATE const PO_REQ_PF_RELEVANCY_NOTE  = 30;

PRIVATE const INRQ_PF_RELEVANCY      = 28;
PRIVATE const INRQ_PF_RELEVANCY_NOTE  = 39;

PRIVATE const CTRL_REL_ACTION_STEP_ACCEPT = 0;
PRIVATE const CTRL_REL_ACTION_STEP_REJECT = 1;
PRIVATE const CTRL_REL_ACTION_STEP_ABORT  = 2;

MACRO WorkWithRetail():bool
  var Work_Retail:bool = false;
  var err:integer = 0;

  GetRegistryValue( "COMMON\\WORK_MODE\\WORK_WITH_RETAIL", V_BOOL, Work_Retail, err );
  if( err != 0 )
    msgbox(" Ошибка чтения настройки COMMON\\WORK_MODE\\WORK_WITH_RETAIL ");
    return false;
  end;

  return Work_Retail;
END;
                                                    
private macro AskForRelevancyOrNote( message:string, Legality:integer ):integer
  Array Text;
  Array Buttons;

  Text(0) = message;                                                       
  if( Legality == PSRQ_REL_NOTSETTED )
    Buttons( CTRL_REL_ACTION_STEP_ACCEPT ) = STR_ACTION_STEP_SET;
  elif( Legality == PSRQ_REL_BYCONTRACT ) 
    Buttons( CTRL_REL_ACTION_STEP_ACCEPT ) = STR_ACTION_STEP_ADD_NOTE;
  end;
  Buttons( CTRL_REL_ACTION_STEP_REJECT ) = STR_ACTION_STEP_REJECT;
  Buttons( CTRL_REL_ACTION_STEP_ABORT  ) = STR_ACTION_STEP_ABORT;

  return ConfWin( Text, Buttons, 0 );
end;


MACRO ExecuteStep( doc, paymDoc )

  var note_text:string = "";
  var Direct = GetOprStatus(OPR_PAYM_DIRECT);
  // Для установки статусов сегментов операции
  var CTRL_segment:integer = 0;

  if( PM_NeedRejectControl() == true )

    if( not GetMultNoteReject( note_text ))
      SetNoteReject( note_text );
      if(not strlen(note_text))
        note_text = "Контроль не выполнен";
      end;
    end;
    
    PaymentObj.PaymStatus = PM_REJECTED;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );

    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    // Заполнить примечание
    if( strlen( trim( note_text ) ) > 0 )
      if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text, {curdate} ) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;
    end;

  else

    if( ( ( PaymentObj.DocKind == PS_PAYORDER ) or ( PaymentObj.DocKind == PS_INRQ ) ) and ( not needUseKZpm() ) )
      var sDocKind = 0;
      if( PaymentObj.DocKind == PS_PAYORDER )
        var ps_payord = RsbPSPayOrder( PaymentObj.PaymentID );
        sDocKind = ps_payord.DocKind;
      end;
      var message;      

      if( ( ( PaymentObj.DocKind == PS_PAYORDER ) and ( sDocKind == PSPOKIND_REQUEST ) ) or ( PaymentObj.DocKind == PS_INRQ ) )
      
        if( ( ( PaymentObj.Legality == PSRQ_REL_NOTSETTED ) or ( ( PaymentObj.Legality == PSRQ_REL_BYCONTRACT ) and ( PaymentObj.LegalityReason == "" ) ) ) )
        
          if( PaymentObj.Legality == PSRQ_REL_NOTSETTED )          
            message = "Не установлена ";
          elif( PaymentObj.Legality == PSRQ_REL_BYCONTRACT )
            message = "Не обоснована ";    
          end;
          
          message = message + STR_ACTION_STEP_QUESTION;
          
          if( not IsOprMultiExec() )
            var choice = AskForRelevancyOrNote( message, PaymentObj.Legality );

            if( choice == CTRL_REL_ACTION_STEP_ACCEPT )
              if( ( PaymentObj.DocKind == PS_PAYORDER ) and ( sDocKind == PSPOKIND_REQUEST ) ) // Панель инкассового поручения
                if( not PS_PayorderPanel( PaymentObj, IfThenElse( PaymentObj.Legality == PSRQ_REL_NOTSETTED, PO_REQ_PF_RELEVANCY, PO_REQ_PF_RELEVANCY_NOTE ) ) )
                  msgbox( "Пользователь прервал выполнение шага операции" );
                  return 1;
                end;
              elif( PaymentObj.DocKind == PS_INRQ ) // Панель инкассового поручения к валютному счету
                if( not PS_RequestOrderPanel( PaymentObj, IfThenElse( PaymentObj.Legality == PSRQ_REL_NOTSETTED, INRQ_PF_RELEVANCY, INRQ_PF_RELEVANCY_NOTE ) ) )
                  msgbox( "Пользователь прервал выполнение шага операции" );
                  return 1;
                end;
              end;
            elif( choice == CTRL_REL_ACTION_STEP_REJECT )
              if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, message ) )
                msgbox( "Ошибка при вставке примечания платежа" );
                return 1;
              end;
              
              if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
                msgbox("Ошибка при установке сегментов статуса экземпляра операции");
                return 1;
              end;

              PaymentObj.PaymStatus = PM_REJECTED;  
              PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );

              return 0;
                          
            elif( ( choice == CTRL_REL_ACTION_STEP_ABORT ) or ( choice == -2 ) )
              msgbox( "Пользователь прервал выполнение шага операции" );
              return 1;
            end;
            
          else
            msgbox( message );
            return 1;
          end;                                                                      
        
        end;
        
      end;
    
    end;
 
    if( PaymentObj.Notes.DelNote( PM_NOTEKIND_DENIALGROUND ) )
      msgbox( "Ошибка при удалении примечания платежа" );
      return 1;
    end;
                 
    if ( not InList( PaymentObj.DocKind, CASH_BOF_OUTORDER, CASH_PS_OUTORDER ) )
      CTRL_segment = OPR_PAYM_ST_CTRL_CONTROL;
    elif( WorkWithRetail() )
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTACCEPTR;     
    else
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTINCABSR;     
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, CTRL_segment ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    // Проверка на возможность отказа по 115-ФЗ
    if( IsPaymentDeniableFM(PaymentObj) )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NEED ) )
        MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
      return 0;
    end;

    if( GetOprStatus(OPR_PAYM_DOCKIND) == WL_INDOC )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;

    if( GetOprStatus(OPR_PAYM_INDEX) > OPR_PAYM_ST_INDEX_NO )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;

    if ( PaymentObj.DocKind == PS_PAYORDER )
      var obj = RsbPSPayOrder(PaymentObj.PaymentID);
    end;
    if( IsDocKindChild(PaymentObj.DocKind, 283/*PMDOC_CLIENTPAYMENT*/) // платеж клиентский
        and ( (IsExistsClaimReserve(PaymentObj.DocKind, PaymentObj.PaymentID) == 0 ) // есть претензия
                 or ( GetOprStatus( OPR_PAYM_INDEX ) == OPR_PAYM_ST_INDEX_2) ) //К2
        and ( (PaymentObj.DocKind != PS_PAYORDER) or (obj.DocKind != 2) //не платежное требование
            or (obj.AcceptTerm == PSPAYDEM_TERM_WITHOUTACCEPT) or (obj.Accept == PSPAYDEM_ST_ACCEPT) or (obj.Accept == PSPAYDEM_ST_AUTOACCEPT) ) //акцепт есть или не нужен
        and (PaymentObj.Notes.ReadNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, {curdate}) == "") ) //примечание не заполнено
      if (PaymentObj.Notes.AddNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, GetCurrentDateTimeUTC({curdate})))
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;
    end;
  end;
  
  if( not PM_NeedRejectControl() and (PaymentObj.DocKind == DLDOC_BANKCLAIM) AND ( 
                                                                                   ( PaymentObj.DemandAcceptTerm == PM_DEMAND_TERM_ACCEPT ) or 
                                                                                   ( GetOprStatus( OPR_PAYM_DO ) == OPR_PM_ST_DISCHARGE ) ) )
    if( Direct == OPR_PM_ST_DIR_OUT )
      // Исходящее требование с акцептом после контроля попадает в АРМ
      PaymentObj.PaymStatus = PM_READY_TO_SEND;
      PaymentObj.PropStatus = PM_PROP_READY;
    end;
  end;

  return 0;
END;

private macro GetNarrativeED274(PsDocKind : integer) : string
  var Narrative : string = 
    "Положительные результаты всех процедур приема к исполнению " +
    "(включая положительный результат проверки достаточности денежных средств " +
    "на счете плательщика), уведомляем о приеме " + 
    IfThenElse( PsDocKind == PSPOKIND_REQUEST,
                "инкассового поручения", "платежного требования" ) + 
    " к исполнению";

  return Narrative;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if (((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ)) and (PaymentObj.paymstatus != PM_REJECTED))
      var DocKind, Origin;
      var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                             " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                             makeArray(SQLParam("", PaymentObj.PaymentID)));
      if (rs and rs.MoveNext())
        DocKind = rs.value(0);
        Origin = rs.value(1);
        var Narrative : string = GetNarrativeED274(DocKind);
        var Queries : string = "InfoCode:8";
        CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
      end;
    end;
  end;

  return stat;
end;


/* Массовое выполнение шага "Контроль" */


/* Простановка примечания "Дата приема к исполнению" для клиентских платежей.
   Живёт здесь, пока на PL/SQL нет функциональности для работы с примечаниями.
   Внутри транзакции. */

private macro PM_MassSetNotesForClientPayment()
  
  var query:string = "select t.t_OrderID "
                           ",t.t_ID_Operation "
                           ",t.t_ID_Step "
                      "from V_PMMASSOPFOREXE t, "
                            "dpspayord_dbt pspo, "
                            "dpmdemand_dbt pmd "
                      "where pspo.t_OrderID(+) = t.t_PaymentID "
                        "and pmd.t_PaymentID(+) = t.t_PaymentID "
                        "and exists ( SELECT * FROM (SELECT t_DocKind FROM doprkdoc_dbt "
                           "CONNECT BY PRIOR t_DocKind = t_ParentDocKind " 
                           "START WITH t_DocKind = 283 /*PMDOC_CLIENTPAYMENT*/ ) " // платеж клиентский
                           "WHERE t_DocKind = t.t_DocKind) "
                        "and ( PM_RESTFUN.IsExistsClaimReserve(t.t_DocKind, t.t_OrderID ) = 0 "
                              "or (select t_numvalue "
                                   "from doprcurst_dbt dc "
                                   "where dc.t_ID_Operation = t.t_ID_Operation "
                                     "and dc.t_statuskindid = 304/*OPR_PAYM_INDEX*/ ) = 3 /*OPR_PAYM_ST_INDEX_2*/ ) " // Картотека 2
                        "and ( (t.t_DocKind != 201 /*PS_PAYORDER*/) "     
                             "or (pspo.t_DocKind != 2) " //не платежное требование 
                             "or (pmd.t_AcceptTerm = 1 /*PSPAYDEM_TERM_WITHOUTACCEPT*/ ) "
                             "or (pmd.t_Accept in(1 /*PSPAYDEM_ST_ACCEPT*/, 5 /*PSPAYDEM_ST_AUTOACCEPT*/ ) ) ) "
                        "and RSI_RSB_KERNEL.GetNote(501 /*OBJTYPE_PAYMENT*/, t.t_PaymentID, 50/*PM_NOTEKIND_PAYM_ACCEPTUATEDATE*/, PM_COMMON.CURDATE) is NULL ";

  var rs:RsdRecordset = execSQLselect( query );

  while( rs.moveNext() )
    
    if( AddNoteForObject( OBJTYPE_PAYMENT, 
                          string( rs.Value(0):o:10 ),
                          PM_NOTEKIND_PAYM_ACCEPTUATEDATE,             
                          GetCurrentDateTimeUTC({curdate}),
                          {curdate},
                          rs.Value(1),
                          rs.Value(2) ) )
      PM_SetErrorStatus( rs.Value(0), 1, "Ошибка при вставке примечания \"Дата приема к исполнению\"" );
    end;

  end;

  return 0;
end;


/* Удаление примечания "Причина отказа" для успешно прошедших контроль платежей.
   Живёт здесь, пока на PL/SQL нет функциональности для работы с примечаниями.
   Внутри транзакции. */

private macro PM_MassDeleteDenialGroundNotesForPayment()

  var query: string = "select t.t_OrderID,t.t_ID_Operation, t.t_ID_Step, nt.t_text "
                      "from V_PMMASSOPFOREXE t, dnotetext_dbt nt "
                      "where nt.t_DocumentID  = LPAD(t.t_PaymentID, 10,'0') "
                        "and nt.t_ObjectType  = :OBJTYPE "
                        "and nt.t_NoteKind    = :PM_NOTEKIND "
                        "and RSI_RSB_KERNEL.ExtractText( nt.t_text ) != chr(1) "
                        "and RSI_RSB_KERNEL.ExtractText( nt.t_text ) is not NULL";    

  var params = makeArray( SQLParam( "OBJTYPE", OBJTYPE_PAYMENT ),  
                          SQLParam( "PM_NOTEKIND", PM_NOTEKIND_DENIALGROUND ) );      

  var rs: RsdRecordset = execSQLselect( query, params );          
  
  while( rs and rs.moveNext() )
    if( AddNoteForObject( OBJTYPE_PAYMENT, 
                          string( rs.Value(0):o:10 ),
                          PM_NOTEKIND_DENIALGROUND,             
                          "",
                          {curdate},
                          rs.Value(1),
                          rs.Value(2) ) )
      PM_SetErrorStatus( rs.Value(0), 1, "Ошибка при удалении примечания \"Дата приема к исполнению\"" );
    end;
  end;
  
  return 0;

end;



macro PrepMassExecuteStep() 
  return execStoredFunc( "PM_CONTROL.MassControlStepPrepare", V_INTEGER );
end;

macro MassExecuteStep()
  
  var stat = execStoredFunc( "PM_CONTROL.MassControlStepExecute", V_INTEGER );
 
  if(stat == 0)
    //Простановка примечания "Дата приема к исполнению" для клиентских платежей
    stat = PM_MassSetNotesForClientPayment();
  end;
         
  if( stat == 0 )
    //Удаление примечания "Причина отказа"
    stat = PM_MassDeleteDenialGroundNotesForPayment();
  end;
          
  return stat;
end;

macro PostMassExecuteStep()

  var stat = 0;

  var AutoCreateNotice = true;
  GetRegistryValue("PS\\PAYORDER\\DEMAND\\AUTOCREATENOTICE", V_BOOL, AutoCreateNotice);

  if( not stat and AutoCreateNotice )
    var Payment : RsbPayment = null;
    var Queries : string = "InfoCode:8";
    var msg : string = "";

    var rs : Rsdrecordset = execSQLselect
      ( "select oprtemp.t_OrderID, oprtemp.t_ID_Operation, oprtemp.t_ID_Step, NVL(pspayord.t_DocKind, " + PSPOKIND_REQUEST + ") t_DocKind " +
        "  from doprtemp_tmp oprtemp, dpspayord_dbt pspayord, dpsinrq_dbt inrq, dpmrmprop_dbt rm " +
        " where oprtemp.t_DocKind in (" + PS_PAYORDER + ", " + PS_INRQ + ") " +
        "   and oprtemp.t_ErrorStatus = 0 " +
        "   and pspayord.t_OrderID(+) = oprtemp.t_OrderID " +
        "   and (pspayord.t_DocKind(+) = " + PSPOKIND_DEMAND +
        "       or " +
        "        pspayord.t_DocKind(+) = " + PSPOKIND_REQUEST + " ) " +
        "   and pspayord.t_Origin(+) = " + PSPO_OR_PAYEEBANK +
        "   and inrq.t_PaymentID(+) = oprtemp.t_OrderID " +
        "   and inrq.t_Origin(+) = " + CP_OR_PAYEEBANK +
        "   and rm.t_PaymentID = oprtemp.t_OrderID " +
        "   and rm.t_Reference != chr(1) " +
        "   and rm.t_Reference is not null "
      );

    while( rs and rs.moveNext() )
      Payment = RsbPayment(rs.value("t_OrderID"));
      var Narrative : string = GetNarrativeED274(rs.value("t_DocKind"));
      msg = CreateED274(rs.value("t_OrderID"), Queries, Narrative, rs.value("t_ID_Operation"), rs.value("t_ID_Step"));

      if(msg)
        execSQL( "insert into dpmoprlog_tmp " +
                 "( t_PaymentID, t_ID_Operation, t_ID_Step, t_Message ) " +
                 "values " +
                 "( :PaymentID, :ID_Operation, :ID_Step, :Msg ) ",

                 makeArray( SQLParam("PaymentID", rs.value("t_OrderID")),
                            SQLParam("ID_Operation", rs.value("t_ID_Operation")),
                            SQLParam("ID_Step", rs.value("t_ID_Step")),
                            SQLParam("Msg", msg)
                          )
               );
      end;
    end;

  end;

  return stat;

end;
