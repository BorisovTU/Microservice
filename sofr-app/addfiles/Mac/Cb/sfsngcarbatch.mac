IMPORT rsd, SFInter;
IMPORT "globals.mac", "sfbatch_lib.mac", "sfacctrnbatch.mac", "sfdefopr.mac";


// Выполнение проводок по начислению УЕК в пакетном режиме

private const OBJTYPE_OPRSFCOM  = 665; /*Удержанная единовременная комиссия*/

private const PlusCalc_CatCode = "+Расчеты"; 
private const PlusCalcNDS_CatCode = "+Расчеты НДС"; 
private const MinusNDSAccrual_CatCode = "-НДС начисленный"; 

private const PlusCalcNVPI_CatCode = "+Расчеты,НВПИ"; 
private const PlusCalcNDS_NVPI_CatCode = "+Расчеты НДС,НВПИ";
private const MinusNDSAccrual_NVPI_CatCode = "-НДС начисленный, НВПИ";

// Выполнение проводок по начислению УЕК
macro SfExecuteAccrueTrn()
  var stat:integer = 0;

  var strSql, cmd, rs;
  var oprID_Operation = 0; 
  var oprID_Step      = 0; 
 
  var sfdefArray      = TArray;

  record SfContr(sfcontr);
  record SfDef(sfdef);

  var OperDprt = {OperDprt};


  cmd = RsdCommand( "SELECT RsbSessionData.OperDprt As OprDprt FROM DUAL" );
  cmd.NullConversion = true;

  rs = RsdRecordset( cmd ); 
  if( rs.moveNext() )
    OperDprt = int(rs.value("OprDprt"));     
  end;



  var sfdefAccTrnCharger = TSfDefAccTrnDataCharger();

  strSql = " SELECT tmp.t_ID_Operation As oprID_Operation, tmp.t_ID_Step As oprID_Step, " +

             // УПК
           " sfdef.t_ID As sfdefID, sfdef.t_FIID_Sum As sfdefFIID_Sum, sfdef.t_Status AS sfdefStatus, sfdef.t_CommNumber AS sfdefCommNumber, " +
           " sfdef.t_DateFee As sfdefDateFee, sfdef.t_Sum As sfdefSum, sfdef.t_SumNDS As sfdefSumNDS, " +

             // ДО
           " contr.t_ID As contrID, contr.t_ServKind As contrServKind, " +
           " contr.t_PartyID As contrPartyID, contr.t_Department As contrDepartment," +
           " contr.t_PayRateID As contrPayRateID, contr.t_PayRateDateKind As contrPayRateDateKind, " +
           " contr.t_PayRatePercent As contrPayRatePercent, " +
           " contr.t_PayFIID As contrPayFIID, contr.t_Number As contrNumber, contr.t_DateConc contrDateConc," +
           " contr.t_Branch As contrBranch, " +

// СПИ получателя УПК
            " sicredit.t_account AS sicreditaccount, sicredit.t_fiid AS sicreditfiid, " +
// счета по КУ +Расчеты
            " NVL (pluscalc.t_account, CHR (0)) AS pluscalcaccount, " +
            " NVL (pluscalc.t_fiid, -1) AS pluscalcfiid, " +
// счета по КУ +Расчеты, НДС
            " NVL (pluscalcnds.t_account, CHR (0)) AS pluscalcndsaccount, " +
            " NVL (pluscalcnds.t_fiid, -1) AS pluscalcndsfiid, " +
// счета по КУ -НДС начисленный
            " NVL (minusndsacr.t_account, CHR (0)) AS minusndsacraccount, " +
            " NVL (minusndsacr.t_fiid, -1) AS minusndsacrfiid " +
       " FROM dsfdeftmp_tmp tmp, " +
            " dsfdef_dbt sfdef, " +
            " dsfcontr_dbt contr, " +
            " dsfsi_dbt sicredit, " +
            " dsfsi_dbt pluscalc, " +
            " dsfsi_dbt pluscalcnds, " +
            " dsfsi_dbt minusndsacr " +
       " WHERE sfdef.t_feetype = ? " +
        " AND sfdef.t_id = tmp.t_SfDefID " +
        " AND contr.t_id = sfdef.t_sfcontrid " +
        " AND sicredit.t_objecttype = ? " +
        " AND sicredit.t_debetcredit = ? " +
        " AND sicredit.t_objectid = LPAD (TO_CHAR (sfdef.t_id), 10, '0') " +
        " AND pluscalc.t_objecttype(+) = ? " +
        " AND pluscalc.t_debetcredit(+) = ? " +
        " AND pluscalc.t_objectid(+) = LPAD (TO_CHAR (sfdef.t_id), 10, '0') " +
        " AND pluscalcnds.t_objecttype(+) = ? " +
        " AND pluscalcnds.t_debetcredit(+) = ? " +
        " AND pluscalcnds.t_objectid(+) = LPAD (TO_CHAR (sfdef.t_id), 10, '0') " +
        " AND minusndsacr.t_objecttype(+) = ? " +
        " AND minusndsacr.t_debetcredit(+) = ? " +
        " AND minusndsacr.t_objectid(+) = LPAD (TO_CHAR (sfdef.t_id), 10, '0') ";

     
  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;

  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE ); 
  cmd.addParam( "", RSDBP_IN, 665                ); // OBJTYPE_OPRSFCOM Вид объекта
  cmd.addParam( "", RSDBP_IN, BANK_CREDIT        );  
  cmd.addParam( "", RSDBP_IN, 665                ); // OBJTYPE_OPRSFCOM Вид объекта
  cmd.addParam( "", RSDBP_IN, CALC_SFSI_KIND     );  
  cmd.addParam( "", RSDBP_IN, 665                ); // OBJTYPE_OPRSFCOM Вид объекта
  cmd.addParam( "", RSDBP_IN, CALC_SFSI_KIND     );  
  cmd.addParam( "", RSDBP_IN, 665                ); // OBJTYPE_OPRSFCOM Вид объекта
  cmd.addParam( "", RSDBP_IN, NDSACRUAL_SFSI_KIND);  


  var i = 0;
  rs = RsdRecordset( cmd ); 
  while( rs.moveNext() )
    ClearRecord(SfContr);
    SfContr.ID              = rs.value("contrID");     
    SfContr.ServKind        = rs.value("contrServKind");
    SfContr.PartyID         = rs.value("contrPartyID");
    SfContr.PayRateID       = rs.value("contrPayRateID");
    SfContr.PayRateDateKind = rs.value("contrPayRateDateKind");
    SfContr.PayRatePercent  = rs.value("contrPayRatePercent");
    SfContr.PayFIID         = rs.value("contrPayFIID");
    SfContr.Number          = rs.value("contrNumber");
    SfContr.DateConc        = rs.value("contrDateConc");
    SfContr.Department      = rs.value("contrDepartment");
    SfContr.Branch          = rs.value("contrBranch");
    if(SfContr.Branch == 0)
      SfContr.Branch = SfContr.Department;
    end;

    ClearRecord(SfDef);
    SfDef.ID         = rs.value("sfdefID");
    SfDef.FIID_Sum   = rs.value("sfdefFIID_Sum");
    SfDef.Status     = rs.value("sfdefStatus"); 
    SfDef.CommNumber = rs.value("sfdefCommNumber");
    SfDef.DateFee    = rs.value("sfdefDateFee");
    SfDef.Sum        = rs.value("sfdefSum");
    SfDef.SumNDS     = rs.value("sfdefSumNDS");

    oprID_Operation = rs.value("oprID_Operation");
    oprID_Step      = rs.value("oprID_Step");

    var DefOpr = SfDefOpr();
    ClearRecord(DefOpr.SfDef);

    Copy(DefOpr.SfDef, SfDef);
    DefOpr.OldStatus       = DefOpr.SfDef.rec.Status;
    DefOpr.OldIsIncluded   = DefOpr.SfDef.rec.IsIncluded;
    DefOpr.oprID_Operation = oprID_Operation;
    DefOpr.oprID_Step      = oprID_Step;

    if(SfDef.Sum != $0)

      var accTrnData = RsbAccTransactionData();

      accTrnData.Chapter         = 1;
      accTrnData.Date_Carry      = SfDef.DateFee; // Дата проводки
      accTrnData.SumPayer        = SfDef.Sum;
      accTrnData.Department      = OperDprt;
      accTrnData.Ground          = string( "За услуги по договору \"", SfContr.Number, "\" от ", SfContr.DateConc:f);
      accTrnData.FIIDPayer       = rs.value("plusCalcFIID");
      accTrnData.AccountPayer    = rs.value("plusCalcAccount");
      accTrnData.FIIDReceiver    = rs.value("sicreditFIID");
      accTrnData.AccountReceiver = rs.value("sicreditAccount");

      sfdefAccTrnCharger.SetPoint();

      sfdefAccTrnCharger.AddDocsTrn(accTrnData, SfDef.ID, NULL, i, oprID_Operation, oprID_Step);


      if(SfDef.SumNDS != $0)

        var accTrnDataNDS = RsbAccTransactionData();

        accTrnData.Chapter         = 1;
        accTrnData.DateCarry       = SfDef.DateFee; // Дата проводки
        accTrnData.SumPayer        = SfDef.SumNDS;
        accTrnData.Department      = OperDprt;
        accTrnData.Ground          = string( "НДС за услуги по договору \"", SfContr.Number, "\" от ", SfContr.DateConc:f);
        accTrnData.FIIDPayer       = rs.value("plusCalcNDSFIID");
        accTrnData.AccountPayer    = rs.value("plusCalcNDSAccount");
        accTrnData.FIIDReceiver    = rs.value("minusNDSAcrFIID");
        accTrnData.AccountReceiver = rs.value("minusNDSAcrAccount");

        sfdefAccTrnCharger.AddDocsTrn(accTrnDataNDS, SfDef.ID, NULL, i, oprID_Operation, oprID_Step);
      end;

      sfdefArray[sfdefArray.size()] = DefOpr;
      i = i + 1;    
    end;
  end;

  if(stat == 0)
    stat = sfdefAccTrnCharger.ExecuteOneTimeCom(sfdefArray);
  end;

  if(stat == 0)
    stat = SFUpdateSfDefStatusIncBatch(SF_FEE_TYPE_SINGLE, sfdefArray);
  end;

  return stat;
end;

// Выполнение проводок по начислению УЕК в пакетном режиме
macro SFCreateAccrueTrnBatch()
  var stat = 0;
  var SfAccrueRegVal = SfComAccrueGetRegValue();
  var cmd, rs;
  var DateFee:date;

  if((SfAccrueRegVal == 0 /*SFCOM_ACCRUE_ALWAYS*/) OR (SfAccrueRegVal == 2/*SFCOM_ACCRUE_CARD2_NDS*/))
    cmd = RsdCommand( "SELECT DISTINCT t_DateFee FROM dsfdeftmp_tmp ORDER BY t_DateFee" );
    cmd.NullConversion = true;
    cmd.execute();   

    rs = RsdRecordset( cmd ); 

    while((rs.moveNext()) AND (stat == 0))
      DateFee = rs.value("t_DateFee");

      stat = SFCreateCatBatch( SF_FEE_TYPE_SINGLE, true, SfAccrueRegVal, DateFee );
    end;

    if(stat == 0)
      stat = SfExecuteAccrueTrn();
    end;
  end;


  return stat;
end;



