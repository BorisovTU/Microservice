/*
$Name:             prpersnidcchk.mac
$Module:           Главная книга
$Description:      Макрос печати протокола проверки паспортов РФ
*/
import BankInter, globals, oralib, rsd, likepy;

private macro _PrintRecs(SessionID)
  var sqlStr = "";
  var params : TArray;
  var rs;

  var PartyID;
  var PersnIDChkID;
  var PartyName;
  var DocSerie;
  var DocNumber;
  var DocIssueDate;
  var MessageStr;
  array _MessageStr;
  var j;

  sqlStr = sqlStr + "SELECT                                                               ";
  sqlStr = sqlStr + "  pcm.t_PartyID,                                                     ";
  sqlStr = sqlStr + "  pcm.t_PersnIDChkID,                                                ";
  sqlStr = sqlStr + "  pt.t_Name,                                                         ";
  sqlStr = sqlStr + "  pc.t_DOC_SERIE,                                                    ";
  sqlStr = sqlStr + "  pc.t_DOC_NUMBER,                                                   ";
  sqlStr = sqlStr + "  pc.t_DOC_ISSUEDATE,                                                ";
  sqlStr = sqlStr + "  pcm.t_Message                                                      ";
  sqlStr = sqlStr + "  FROM dpersnidcchkmass_dbt pcm                                      ";
  sqlStr = sqlStr + "  LEFT JOIN dpersnidcchk_dbt pc ON (pc.t_RecID = pcm.t_PersnIDChkID) ";
  sqlStr = sqlStr + "  LEFT JOIN dparty_dbt pt ON (pt.t_PartyID = pcm.t_PartyID)          ";
  sqlStr = sqlStr + " WHERE pcm.t_SessionID = :SessionID                                  ";
  sqlStr = sqlStr + " ORDER BY pcm.t_PartyID                                              ";

  params = makeArray(SQLParam("SessionID", SessionID)
                    );

  rs = execSQLselect(sqlStr, params, true);

  while(rs and rs.moveNext())

    PartyID = rs.value(0);
    PersnIDChkID = rs.value(1);
    PartyName = rs.value(2);
    DocSerie = rs.value(3);
    DocNumber = rs.value(4);
    DocIssueDate = rs.value(5);
    MessageStr = rs.value(6);

[
 #                                                                              ] (string(PartyID) + " " + PartyName);


    if(PersnIDChkID <= 0)
      StrSplit(MessageStr, _MessageStr, 72, 72, 10 );
      j = 0;
      while(StrLen(_MessageStr(j)) > 0)
[
        ########################################################################] (_MessageStr(j));
        j = j + 1;
      end;
    else

[
        серия: #### номер: ###### дата выдачи: ##########                       ] (DocSerie, DocNumber, date(DocIssueDate):f);
      
      StrSplit(MessageStr, _MessageStr, 63, 63, 10 );
      j = 0;
      while(StrLen(_MessageStr(j)) > 0)
[                ###############################################################] (_MessageStr(j));
        j = j + 1;
      end;
    end;
  end;
end;

macro PrintReportMassCheck(SessionID)
[Протокол проверки действительности паспортов гражданина РФ сервисом ФМС

];
[Дата:  ##########                                                                 ] (date:f);                
[Время: ########                                                                   ] (time:f);                
[Пользователь: ##### #                                                             ] ({oper}, {Name_Oper});   

  _PrintRecs(SessionID);
end;