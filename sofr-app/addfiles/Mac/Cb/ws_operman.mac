/*
$Name:        ws_operman.mac
$Module:      Ядро ГКБО
$Description: Макрос реализации сервисов создания/обновления/удаления операциониста.
*/

Import BankInter, GateInter;
Import ws_lib_fun;

/*
    Структура TWsGateOperman - атрибуты операциониста
 */
class TWsGateOperman

  var Oper        : integer; /* Номер операциониста */
  var Name_Oper   : string ; /* ФИО операциониста */
  var cTypePerson : string ; /* Уровень доступа операциониста */
  var UserBlocked : string ; /* Признак временной блокировки операциониста */
  var UserClosed  : string ; /* Признак закрытия операциониста */

end;

/*  Сервис InsertOperman - создание операциониста в v6 по запросу от 5.50

    Параметры.
      oOpermanParm - TWsGateOperman Атрибуты операциониста
      sObjectCode  - Ид. операциониста во внешней системе.
      iAppID       - Ид. внешней системы.
 */
macro InsertOperman( oOpermanParm, sObjectCode : string, iAppID : integer )

  record operman("person.dbt");

  var stat       : integer;
  var ErrorCode  : string;

  var ObjectID   : integer;
  var ObjectCode : string;   
  
  var wsGateOperman : TWsGateOperman;
  var wsGateOperman_buff : TWsGateOperman;
  
  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, oOpermanParm, true, V_GENOBJ );

  WS_CheckParameter( 2, sObjectCode, false, V_STRING  );
  WS_CheckParameter( 3, iAppID     , true, V_INTEGER );

  
  /* создаем объект класса TWsGateOperman и заполняем его атрибуты в соответствии с атрибутами 
                 параметра oOpermanParm */

  wsGateOperman = TWsGateOperman;
  wsGateOperman_buff = TWsGateOperman;

  ObjectCode = SubStr( "00000", strlen(string(oOpermanParm.Oper)) + 1 ) + string(oOpermanParm.Oper);

  if( oOpermanParm.UserBlocked == null ) oOpermanParm.UserBlocked = ""; end;
  if( oOpermanParm.UserClosed  == null ) oOpermanParm.UserClosed  = ""; end;

  stat = Insert_Operman( oOpermanParm.Oper       ,
                         oOpermanParm.Name_Oper  ,
                         oOpermanParm.cTypePerson,
                         oOpermanParm.UserBlocked,
                         oOpermanParm.UserClosed ,
                         operman );

  if(stat == 0)
 
    stat = InsertObjectWithCode(RG_OPER_OIM, oOpermanParm.Name_Oper, ObjectID, 1, ObjectCode, ErrorCode );
    if( stat != 0 )
      RunError( ErrorCode, stat );
    end;

    if((sObjectCode != null) and (sObjectCode != ""))
      stat = InsertObjectCode(ObjectID, iAppID, sObjectCode, ErrorCode );
    end;
    
    if(stat == 0)
      return oOpermanParm.Oper;
    else
      RunError( ErrorCode, stat );
    end;

  elif( stat != 0 )
    if( stat == 1 )
      wsGateOperman_buff.Oper        = operman.Oper;
      wsGateOperman_buff.Name_Oper   = operman.Name;
      wsGateOperman_buff.cTypePerson = operman.cTypePerson;
      wsGateOperman_buff.UserBlocked = operman.UserBlocked;
      wsGateOperman_buff.UserClosed  = operman.UserClosed;
      return wsGateOperman_buff;
    else
      RunError( GetErrMsg(), stat );
    end;
  end;
end;

/*  Сервис UpdateOperman - редактирование операциониста в v6 по запросу от 5.50

    Параметры.
        oOpermanParm - атрибуты операциониста
        sObjectCode  - Ид. операциониста во внешней системе
        iAppID       - ИД внешней системы
 */
macro UpdateOperman( oOpermanParm, sObjectCode : string, iAppID : integer )

  var CodeiAppID = false;
 
  var stat       : integer;
  var ErrorCode  : string;

  var ObjectID   : integer;
  var ObjectCode : string;   
  
  var wsGateOperman : TWsGateOperman;
 
  wsGateOperman = TWsGateOperman;

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, oOpermanParm, true, V_GENOBJ );

  WS_CheckParameter( 2, sObjectCode, false, V_STRING  );
  WS_CheckParameter( 3, iAppID     , true , V_INTEGER );


  ObjectCode = SubStr( "00000", strlen(string(oOpermanParm.Oper)) + 1 ) + string(oOpermanParm.Oper);

  if ( (sObjectCode != NULL) AND (sObjectCode != ""))
     stat = FindObject( iAppID, RG_OPER_OIM, sObjectCode, ObjectID, ErrorCode );
     
    if( stat == 0 )
      CodeiAppID = true;
      stat = FindObjectCode( ObjectID, 1, ObjectCode, ErrorCode  );
      if(stat == 0)
        oOpermanParm.Oper = int(ObjectCode);
      elif(oOpermanParm.Oper == null)
        RunError( ErrorCode, stat ); 
      end; 
    elif(oOpermanParm.Oper == null)
      RunError( ErrorCode, stat ); 
    end;
  end;

  if( oOpermanParm.UserBlocked == null ) oOpermanParm.UserBlocked = ""; end;
  if( oOpermanParm.UserClosed  == null ) oOpermanParm.UserClosed  = ""; end;

  stat = Update_Operman( oOpermanParm.Oper       ,
                         oOpermanParm.Name_Oper  ,
                         oOpermanParm.cTypePerson,
                         oOpermanParm.UserBlocked,
                         oOpermanParm.UserClosed  );
  if( stat == 0 )

    stat = FindObject( 1, RG_OPER_OIM, ObjectCode, ObjectID, ErrorCode );
     
    if( stat != 0 )
      stat = InsertObjectWithCode(RG_OPER_OIM, oOpermanParm.Name_Oper, ObjectID, 1, ObjectCode, ErrorCode );
      if( stat != 0 ) 
        RunError( ErrorCode, stat ); 
      end; 
    end;

    if((not(CodeiAppID)) AND ((sObjectCode != NULL) AND (sObjectCode != "")) )
      stat = InsertObjectCode(ObjectID, iAppID, sObjectCode, ErrorCode );
      if(( stat != 0 ) and (stat != 30312/*Такой код уже задан*/))
        RunError( ErrorCode, stat ); 
      end;

    end;

  else
      RunError( GetErrMsg(), stat ); 
  end;

end;

/*
    Сервис DeleteOperman - удаление операциониста в v6 по запросу от 5.50

    Параметры.
        Oper         - Ид. операциониста
        sObjectCode  - Ид. операциониста во внешней системе
        iAppID       - ИД внешней системы
 */
macro DeleteOperman( Oper : integer, sObjectCode : string, iAppID : integer )

  var stat       : integer;
  var flag_Oper  : bool;
  var flag_Code  : bool;
  var ErrorCode  : string;

  var ObjectID   : integer;
  var ObjectCode : string;   
  
  var wsGateOperman : TWsGateOperman;

  wsGateOperman = TWsGateOperman;

  if( (Oper == NULL) OR (Oper == 0) ) 
    flag_Oper = false;
    flag_Code = true; 
  end;
    
  if( (sObjectCode == NULL) OR (sObjectCode == "") ) 
    flag_Oper = true;
    flag_Code = false; 
  end;

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, Oper, flag_Oper, V_INTEGER );

  WS_CheckParameter( 2, sObjectCode, flag_Code, V_STRING  );
  WS_CheckParameter( 3, iAppID     , true, V_INTEGER );
  
  if ( (sObjectCode != NULL) AND (sObjectCode != "") )
     stat = FindObject( iAppID, RG_OPER_OIM, sObjectCode, ObjectID, ErrorCode );
     
    if( stat == 0 )
      stat = FindObjectCode( ObjectID, 1, ObjectCode, ErrorCode  );
       Oper = int(ObjectCode);
      if( stat != 0 ) 
        RunError( ErrorCode, stat ); 
      end; 
    end;
  end;

  stat = Delete_Operman( Oper );

  if( stat == 0 )

    ObjectCode = SubStr( "00000", strlen(string(Oper)) + 1 ) + string(Oper);

    stat = FindObject( 1, RG_OPER_OIM, ObjectCode, ObjectID, ErrorCode );
     
    if( stat == 0 )
      stat = DeleteObject(ObjectID, ErrorCode, RG_OPER_OIM );
      if( stat != 0 ) 
        RunError( ErrorCode, stat ); 
    end;
    else
      RunError( ErrorCode, stat ); 
  end;
  else
    RunError( GetErrMsg(), stat );
  end; 
end;