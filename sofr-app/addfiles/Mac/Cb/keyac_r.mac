/*
                       Расчет контрольного ключа

                         RS-BANK 4.31  (Россия)

               В соответствии с письмом ЦБ №515 от 08.09.97




    08.01.98 AS   Установка признака РКЦ по справочнику банков
    21.08.98 AS   Переделан для RSB 5.0

*/
Import PTInter;


file PartCode  ( partcode ) key 1 ;

macro GetDigit ( d1 , d2 )
 var k;

  /*  При использовании клиринговой валюты возможно алфавитное    */
  /*  значение в 6м разряде лицевого счета                        */

  if   ( Index ( "AaАа" , d1 ) != 0 )  d1 = "0";
  elif ( Index ( "BbБб" , d1 ) != 0 )  d1 = "1";
  elif ( Index ( "CcСс" , d1 ) != 0 )  d1 = "2";
  elif ( Index ( "EeЕе" , d1 ) != 0 )  d1 = "3";
  elif ( Index ( "HhНн" , d1 ) != 0 )  d1 = "4";
  elif ( Index ( "KkКк" , d1 ) != 0 )  d1 = "5";
  elif ( Index ( "MmМм" , d1 ) != 0 )  d1 = "6";
  elif ( Index ( "PpРр" , d1 ) != 0 )  d1 = "7";
  elif ( Index ( "TtТт" , d1 ) != 0 )  d1 = "8";
  elif ( Index ( "XxХх" , d1 ) != 0 )  d1 = "9";
  end;

  k = int( d1 ) * int ( d2 );
  return ( k - 10*( k/10 ) );

end;



macro GetAccountKey( ac, mfo )

  VAR
    Maket   = "71371371371371371371371", /* Макет для расчета ключа         */
    Sum     = int( 0 ),                /* Контрольный разряд                */
    KeyPos  = int( 9 ),                /* Позиция ключевого разряда в счете */
    i       = int( 1 ),                /* Вспомогательная переменная        */
    lenm    = strlen( Maket ),         /* Длина строки макета               */
    lena    = strlen( ac ),            /* Дина счета                        */
    lend    = strlen( mfo ),           /* Длина кода МФО                    */
    newac   = ac,                      /* Счет с рассчитанным ключом        */
    rcc     = int( 0 );                /* 1 - Расчет для РКЦ,               */
                                       /* 0 - расчет для кредитных орг-ций  */


  /*  Определим признак РКЦ по справочнику банков   */

  PartCode.CodeKind = 3;     /* Ищем по БИК */
  PartCode.Code = mfo;

  if ( GetEQ( PartCode ) )

    if( IsBankType(PartCode.PartyID, PT_KIND_PAYM_CASH_CENTRE ) or
        IsBankType(PartCode.PartyID, PT_KIND_FIELDOFFICE_CENTRALBANK) )
      
      rcc = 1;
    
    end;
 
  end;


  while ( i <= lenm )

    if (i <= 3)

      /*   Цикл по позициям в БИК   */

      if ( rcc == 0 )
         if ( i+6 <= lend )
            Sum = Sum + GetDigit( SubStr( mfo, i+6, 1 ) , SubStr( maket, i, 1 ) );
         end;
      else
         if ( ( i > 1 ) and ( i + 3 <= lend ) )
            Sum = Sum + GetDigit( SubStr( mfo, i+3, 1 ) , SubStr( maket, i, 1 ) );
         end;
      end;

    /*     Цикл по позициям в счете    */

    elif ( ( i  !=  KeyPos+3 ) and ( i > 3 ) )

      Sum = Sum + GetDigit( SubStr( newac, i-3, 1) , SubStr( maket, i, 1 ) );

    end;
    
    i = i + 1;

  end;

  Sum = Sum - 10*( Sum / 10 );
  Sum = Sum * 3;
  Sum = Sum - 10*( Sum / 10 );

  StrSet ( newac , KeyPos , MkStr( Sum + "0"  ,  1 ) );

  return newac;

end;

