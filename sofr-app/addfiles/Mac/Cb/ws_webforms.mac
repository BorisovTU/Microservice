 /*
 $Name: ws_webforms.mac
 $Module: Инструмент Web
 $Description: Классы описания динамической панели Web
 */

class TWebFieldType
    const Label = 1;        // Метка
    const Text = 2;         // Текстовое поле
    const Numeric = 3;      // Число
    const UpDown = 4;       // Счетчик
    const Money = 5;        // Деньги
    const Date = 6;         // Дата
    const Time = 7;         // Время
    const CheckBox = 8;     // Чекбокс
    const ComboBox = 9;     // Список выбора
end;
const WebFieldType = TWebFieldType;

class WebPanel
    var Title : string; // Наименование панели
    var Width : integer; // Ширина панели при открытии
    var Height : integer; // Высота панели при открытии
    var IsResizing : bool = true; // Позволяет растягивать панель с помощью мыши
    var IsModal : bool = false; // Окно будет располагаться поверх остальных окон без доступа к другим панелям
    var OkButtonName : string = "Выполнить"; // Название кнопки <ОК>
    var CancelButtonName : string = "Отмена"; // Название кнопки <Отмена>
    var MacroService : string; // Наименование макроса сервиса, который будет вызван по кнопке <Выполнить>.
    var ProcService : string; // Наименование процедуры макроса, которая будет вызвана по кнопке <Выполнить>.
    var Fields : TArray = TArray;  // array of WebField. Набор атрибутов панели.
    var InitialValues; // WebPanelModel Первоначальные значения при открытии панели

    macro HasFieldName(name)
        for (var fld, Fields)
            if (fld.Name == name)
                return true;
            end;
        end;
        return false;
    end;

    macro AddField(field)
        if (field == null)
            RunError("field cannot be null");
        end;
        if ((field.Name == null) or (field.Name == ""))
            RunError("Имя поля должно быть задано");
        end;
        if (HasFieldName(field.Name))
            RunError("Поле с таким именем уже существует имя: " + field.Name);
        end;

        Fields[Fields.size] = field;
    end;
end;

class WebField (n,t,l)
    var Name : string = n; // Имя поля панели
    var Type : integer = t; // Тип поля ввода, контрола.
    var Label : string = l; // Метка перед полем.
    var IsRequired : bool = false; // Признак обязательного поля.
    var MinWidth : integer; // Минимальная ширина поля ввода.
    var HasClearButton : bool; // Наличие кнопки <Очистить> внутри поля ввода.
    var Mask : string; // Маска внутри поля ввода.
    var Watermark : string; // Текст, который выводится в поле, когда в нем нет никакого значения.
    var ItemsSource; // array список значений для выбора, например в ComboBox
end;

class WebPanelResult
    var Code : integer;
    var Message : string;
end;

class WebModelItem(n, v)
    var Name : string = n; // Имя поля панели
    var Value : variant = v; // Значение поля
end;

class WebPanelModel(tkmodel)
    var Items : TArray = TArray; // array of WebModelItem

    // constructor
    if (tkmodel != null)
        if (tkmodel.Items != null)
            for (var item, tkmodel.Items)
                Items[Items.size] = WebModelItem(item.Name, item.Value);
            end;
        end;
    end;

    macro FindItem(name : string)
        for (var val, Items)
            if (val.Name == name)
                return val;
            end;
        end;
        return null;
    end;

    macro SetValue(name : string, value : variant)
        var item = FindItem(name);

        if (item == null)
            item = WebModelItem(name);
            Items[Items.size] = item;
        end;
        item.Value = value
    end;

    macro GetValue(name : string) : variant
        var item = FindItem(name);
        
        if (item != null)
            return item.Value;
        end;

        return null;
    end;
end;

/*
var model = WebPanelModel();
model.SetValue("sum", $45);
model.SetValue("sum", $0);
model.SetValue("comission", $0);
model.SetValue("opendate", date(3,5,2007));
var val = model.GetValue("sum");
val = model.GetValue("comission");
val = model.GetValue("opendate");
val = model.GetValue("sum");
*/
/*
macro HandleTestWebPanelResult(tkmodel)
    var model = WebPanelModel(tkmodel);
    var result = WebPanelResult();
    result.Code = 0;

    result.Message = "Ошибка выполнения "
        + "  ComboBox = " + model.GetValue("ComboBox") 
        + "  text = " + model.GetValue("Text") 
        + "  date = " + model.GetValue("Date")  
        + "  time = " + model.GetValue("Time")
        + "  Numeric = " + model.GetValue("Numeric")
        + "  UpDown = " + model.GetValue("UpDown")
        + "  Money = " + model.GetValue("Money")
        + "  CheckBox = " + string(model.GetValue("CheckBox"));

    return result;
end;


macro GetTestWebPanel()

    var panel = WebPanel();
    with (panel)
        Title = "Веб панель";
        Width = 800;
        Height = 300;
        IsResizing = true;
        IsModal = false;
        OkButtonName = "Генерировать";
        CancelButtonName = "Закрыть";
        MacroService = "ws_webforms";
        ProcService = "HandleTestWebPanelResult";
    end;

    var field = WebField("ComboBox", WebFieldType.ComboBox, "ComboBox");
    var itms = TArray();
    itms[0] = "Элемент 0";
    itms[1] = "Элемент 1";
    itms[2] = "Элемент 2";
    with (field)
        ItemsSource = itms; Watermark = "Не выбрано"; HasClearButton = true; IsRequired = true;
    end;
    panel.AddField(field);

    field = WebField("Text", WebFieldType.Text, "Text");
    with (field)
        //IsRequired = true; MinWidth = 500; HasClearButton = true; Mask = "##-##"; Watermark = "Введите сообщение";
        IsRequired = true; MinWidth = 500; HasClearButton = true; Mask = ""; Watermark = "Введите сообщение";
    end;
    panel.AddField(field);

    field = WebField("Date", WebFieldType.Date, "Date");
    with (field)
        IsRequired = true; MinWidth = 500; HasClearButton = true;
    end;
    panel.AddField(field);

    field = WebField("Time", WebFieldType.Time, "Time");
    with (field)
        IsRequired = true; MinWidth = 500; HasClearButton = true; Mask = "";
    end;
    panel.AddField(field);

    panel.AddField(WebField("Numeric", WebFieldType.Numeric, "Numeric"));
    panel.AddField(WebField("UpDown", WebFieldType.UpDown, "UpDown"));
    panel.AddField(WebField("Money", WebFieldType.Money, "Money"));
    panel.AddField(WebField("CheckBox", WebFieldType.CheckBox, "CheckBox"));

    panel.AddField(WebField("Numeric2", WebFieldType.Numeric, "Numeric"));
    panel.AddField(WebField("UpDown2", WebFieldType.UpDown, "UpDown"));
    panel.AddField(WebField("Money2", WebFieldType.Money, "Money"));
    panel.AddField(WebField("CheckBox2", WebFieldType.CheckBox, "CheckBox"));

    var initial = WebPanelModel();
    panel.InitialValues = initial;
    initial.SetValue("Text", "1234");
    initial.SetValue("Date", date(3,5,2010));
    initial.SetValue("Money", 44);
    initial.SetValue("Money2", 66.77);
    initial.SetValue("CheckBox", true);
    initial.SetValue("Numeric", 334);
    //initial.SetValue("Time", dttm(date(2,2,2), time(10,10,10)));
    initial.SetValue("Time", time(10,10,10));
    //initial.SetValue("Time", dttm(null, time(10,10,10)));
    initial.SetValue("Numeric2", 334.66);
    initial.SetValue("CheckBox2", true);
    initial.SetValue("UpDown2", 77);
    initial.SetValue("UpDown", 66);
    initial.SetValue("ComboBox", 2);

    return panel;
end;
*/