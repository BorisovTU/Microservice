/* Файл с макросами процедуры "Обработка счетов с овердрафтом"*/

IMPORT BankInter, CarryDoc, CurrInter, globals;


RECORD Acc     (account); /* Расчетный счет */
RECORD OvAcc   (account); /* Связанный счет овердрафта (глава А) */
RECORD ObAcc   (account); /* Связанный счет неиспользованного лимита по овердрафту (глава B)*/
RECORD CorrAcc (account); /* Связанный счет корреспонденции по учету неиспользованного лимита овердрафта(глава B) */

RECORD Acc_val     ("account$"); /* Расчетный счет */
RECORD OvAcc_val   ("account$"); /* Связанный счет овердрафта (глава А) */
RECORD ObAcc_val   ("account$"); /* Связанный счет неиспользованного лимита по овердрафту(глава B) */
RECORD CorrAcc_val ("account$"); /* Связанный счет корреспонденции по учету неиспользованного лимита овердрафта(глава B) */

RECORD  d     (document);
RECORD  d_val ("documnt$");
RECORD  c     (carryprm);

FILE a    (account) write;
FILE a_val("account$") write;

FILE oba    (account);
FILE oba_val("account$");

VAR ret;

VAR Code_Currency; /* код валюты счета            */
VAR Rest;          /* остаток на рассчетном счете */
VAR OverRest;      /* остаток на счете овердрафта */

CONST TYPEACCOUNT_OVER = "О";  /* тип счета - овердрафт */
CONST KIND_OPER        = " 4"; /* мемориальный ордер */

MACRO СнятьПризнакОвердрафта(ТипСчета)

  VAR НовыйТипСчета;
  VAR pos = Index(ТипСчета,TYPEACCOUNT_OVER);
  if(pos == 0) return ТипСчета;end;
  НовыйТипСчета = SubStr(ТипСчета,1,pos-1);
  НовыйТипСчета = НовыйТипСчета + SubStr(ТипСчета,pos+1);
  return НовыйТипСчета;

END;

MACRO УстановитьПризнакОвердрафта(ТипСчета)

  VAR pos = Index(ТипСчета,TYPEACCOUNT_OVER);
  if(pos == 0) ТипСчета = ТипСчета + TYPEACCOUNT_OVER;end;
  return ТипСчета;

END;

MACRO УстановитьЛимит(счет,Лимит);

   счет.Limit = Лимит;
   if(счет.Limit <= $0) /* Снимаем признак овердрафта */
     счет.Type_Account = СнятьПризнакОвердрафта(счет.Type_Account);
   else                 /* Восстанавливаем признак овердрафта */
     счет.Type_Account = УстановитьПризнакОвердрафта(счет.Type_Account);
   end;
   if(not Update(счет))
     ret = status(c.ErrMsg);
     c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
     AbortTrn;
   end;
END;

MACRO ИзменитьЛимит(Счет,ВнебСчет)

   /* Размер лимита по расчетному счету устанавливается равным значению
      остатка по внебалансовому счету неиспользованного лимита овердрафта */

   VAR ВнебалансовыйОстаток;

   if(Code_Currency == 0)
     a.Chapter = Счет.Chapter;
     a.Account = Счет.Account;
     if(not getEQ(a))
       ret = status(c.ErrMsg);
       c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
       AbortTrn;
     end;
     oba.Chapter = ВнебСчет.Chapter;
     oba.Account = ВнебСчет.Account;
     if(not getEQ(oba))
       ret = status(c.ErrMsg);
       c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
       AbortTrn;
     end;
     ВнебалансовыйОстаток = RestA(oba.Account,null,null,oba.Chapter);
     УстановитьЛимит(a, abs(ВнебалансовыйОстаток));
   else
     a_val.Chapter = Счет.Chapter;
     a_val.Account = Счет.Account;
     a_val.Code_Currency = Code_Currency;
     if(not getEQ(a_val))
       ret = status(c.ErrMsg);
       c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
       AbortTrn;
     end;
     oba_val.Chapter = ВнебСчет.Chapter;
     oba_val.Account = ВнебСчет.Account;
     oba_val.Code_Currency = Code_Currency;
     if(not getEQ(oba_val))
       ret = status(c.ErrMsg);
       c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
       AbortTrn;
     end;
     ВнебалансовыйОстаток = RestAC(oba_val.Account,oba_val.Code_Currency,null,null,oba_val.Chapter);
     УстановитьЛимит(a_val, abs(ВнебалансовыйОстаток));
   end;
END;

MACRO InitDoc(Документ)
   ClearRecord(Документ);
END;

/* Выдача овердрафтного кредита */
MACRO ВыдачаКредита(Документ,
                    Счет,           /* Расчетный счет */
                    СчетОвердрафта, /* Связанный счет овердрафта (глава А) */
                    ВнебСчет,       /* Связанный счет неиспользованного лимита по овердрафту (глава B)*/
                    КоррСчет        /* Связанный счет корреспонденции по учету неиспользованного лимита овердрафта(глава B) */
                   )

   VAR Sum = abs(Rest);

   /* Балансовая проводка */
   InitDoc(Документ);
   Документ.Chapter = 1;
   Документ.Date_Carry = {curdate};
   Документ.Account_Payer    = СчетОвердрафта.Account;
   Документ.Account_Receiver = Счет.Account;
   Документ.Sum = Sum;
   Документ.Code_Currency = Code_Currency;

   Документ.Kind_Oper = KIND_OPER;
   Документ.Result_Carry = PAYOVERCARRY; /* Выдача овердрафтного кредита */
   Документ.Ground = "Выдача овердрафтного кредита";
   ret = RunCarry( c, Документ ); /* Проводка документа */
   if(ret != 0) AbortTrn; end;

   /* Внебалансовая проводка */
   InitDoc(Документ);
   Документ.Chapter = 3;
   Документ.Date_Carry = {curdate};
   Документ.Account_Payer    = КоррСчет.Account;
   Документ.Account_Receiver = ВнебСчет.Account;
   Документ.Sum = Sum;
   Документ.Code_Currency = Code_Currency;

   Документ.Kind_Oper = KIND_OPER;
   Документ.Result_Carry = PAYOVERCARRY; /* Выдача овердрафтного кредита */
   Документ.Ground = "Выдача овердрафтного кредита";
   ret = RunCarry( c, Документ ); /* Проводка документа */
   if(ret != 0) AbortTrn; end;

   /* Производим уменьшение размера лимита по расчетному счету */
   ИзменитьЛимит(Счет,ВнебСчет);

END;

/* Погашение овердрафтного кредита */
MACRO ПогашениеКредита(Документ,
                       Счет,           /* Расчетный счет */
                       СчетОвердрафта, /* Связанный счет овердрафта (глава А) */
                       ВнебСчет,       /* Связанный счет неиспользованного лимита по овердрафту (глава B)*/
                       КоррСчет        /* Связанный счет корреспонденции по учету неиспользованного лимита овердрафта(глава B) */
                      )

   VAR Sum;

   /* Погашение необходимо, только если остаток на счете овердрафта ненулевой */
   if(OverRest == $0)
     ИзменитьЛимит(Счет,ВнебСчет);
     return;
   end;

   if(Rest >= OverRest) Sum = OverRest;
   else                 Sum = Rest;
   end;

   /* Балансовая проводка */
   InitDoc(Документ);
   Документ.Chapter = 1;
   Документ.Date_Carry = {curdate};
   Документ.Account_Payer    = Счет.Account;
   Документ.Account_Receiver = СчетОвердрафта.Account;
   Документ.Sum = Sum;
   Документ.Code_Currency = Code_Currency;

   Документ.Kind_Oper = KIND_OPER;
   Документ.Result_Carry = REPAYOVERCARRY; /* Погашение овердрафтного кредита */
   Документ.Ground = "Погашение овердрафтного кредита";
   ret = RunCarry( c, Документ ); /* Проводка документа */
   if(ret != 0) AbortTrn; end;

   /* Внебалансовая проводка */
   InitDoc(Документ);
   Документ.Chapter = 3;
   Документ.Date_Carry = {curdate};
   Документ.Account_Payer    = ВнебСчет.Account;
   Документ.Account_Receiver = КоррСчет.Account;
   Документ.Sum = Sum;
   Документ.Code_Currency = Code_Currency;

   Документ.Kind_Oper = KIND_OPER;
   Документ.Result_Carry = REPAYOVERCARRY; /* Погашение овердрафтного кредита */
   Документ.Ground = "Погашение овердрафтного кредита";
   ret = RunCarry( c, Документ ); /* Проводка документа */
   if(ret != 0) AbortTrn; end;

   /* Производим увеличение размера лимита по расчетному счету */
   ИзменитьЛимит(Счет,ВнебСчет);

END;

MACRO УстановитьПризнакОвердрафтаПередОткатом(Счет)
  VAR pos = Index(Счет.Type_Account,TYPEACCOUNT_OVER);
  if(pos == 0)
    Счет.Type_Account = УстановитьПризнакОвердрафта(Счет.Type_Account);
    if(not Update(Счет))
      ret = status(c.ErrMsg);
      c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
      AbortTrn;
    end;
  end;
END;

MACRO ОткатитьДокументы(Глава,НомерСчетаПлательщика,НомерСчетаПолучателя,Result_Carry)

  FILE doc(document) key 2;
  FILE doc_val("documnt$") key 2;
  VAR flag, NumDoc = 0;

  if(Code_Currency == 0)

    doc.Chapter    = Глава;
    doc.Account_Payer = НомерСчетаПлательщика;

    flag = (GetGE(doc)) and (doc.Chapter==Глава) and
           (doc.Account_Payer==НомерСчетаПлательщика);

    while(flag)

      if(doc.Result_Carry == Result_Carry)
        if(doc.Account_Receiver != НомерСчетаПолучателя)
          c.ErrMsg = "Неверный счет получателя";
          AbortTrn;
        end;
        Copy(d,doc);
        d.Sum = $0; /* Взамен ничего не вставляем */
        c.ApplicationKind = doc.iApplicationKind;
        c.ApplicationKey  = doc.ApplicationKey;

        flag = (next(doc)); /* следующую запись берем до отката */

        ret = RunBackout(c,d);
        if(ret != 0) AbortTrn; end;
        NumDoc = NumDoc + 1;
      else
        flag = (next(doc));
      end;

      if(flag) flag = (doc.Chapter==Глава) and (doc.Account_Payer==НомерСчетаПлательщика);end;
    end;
  else
    doc_val.Chapter    = Глава;
    doc_val.Account_Payer = НомерСчетаПлательщика;
    doc_val.Code_Currency = Code_Currency;

    flag = (GetGE(doc_val)) and (doc_val.Chapter==Глава) and
           (doc_val.Account_Payer==НомерСчетаПлательщика) and
           (doc_val.Code_Currency == Code_Currency);

    while(flag)

      if(doc_val.Result_Carry == Result_Carry)
        if(doc_val.Account_Receiver != НомерСчетаПолучателя)
          c.ErrMsg = "Неверный счет получателя";
          AbortTrn;
        end;
        Copy(d_val,doc_val);
        d_val.Sum = $0; /* Взамен ничего не вставляем */
        c.ApplicationKind = doc_val.iApplicationKind;
        c.ApplicationKey  = doc_val.ApplicationKey;

        flag = (next(doc_val)); /* следующую запись берем до отката */

        ret = RunBackout(c,d_val);
        if(ret != 0) AbortTrn; end;
        NumDoc = NumDoc + 1;
      else
        flag = (next(doc_val));
      end;

      if(flag)
        flag = (doc_val.Chapter==Глава) and
               (doc_val.Account_Payer==НомерСчетаПлательщика) and
               (doc_val.Code_Currency == Code_Currency);
      end;
    end;
  end;

  return NumDoc;
END;

MACRO BackoutDoc

     VAR NumDoc_Chapt1 = 0,  /* Число найденных документов в главе 1 */
         NumDoc_Chapt3 = 0,  /* Число найденных документов в главе 3 */
         ПересчитатьОстатки = 0; /* Признак, что документы по выдаче(погашению)
                                    сегодня уже были - необходим пересчет */

     /* Для успешного отката ставим признак овердрафта на расчетный счет */
     if(Code_Currency == 0)
       a.Chapter = Acc.Chapter;
       a.Account = Acc.Account;
       if(not getEQ(a))
         ret = status(c.ErrMsg);
         c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
         AbortTrn;
       end;
       УстановитьПризнакОвердрафтаПередОткатом(a);
     else
       a_val.Chapter = Acc_val.Chapter;
       a_val.Account = Acc_val.Account;
       a_val.Code_Currency = Code_Currency;
       if(not getEQ(a_val))
         ret = status(c.ErrMsg);
         c.ErrMsg = "Ошибка(" + ret +"):" + c.ErrMsg;
         AbortTrn;
       end;
       УстановитьПризнакОвердрафтаПередОткатом(a_val);
     end;

     if(Code_Currency == 0)
       /* Удаляем документы по погашению кредита */
       NumDoc_Chapt1 = ОткатитьДокументы(1,Acc.Account,OvAcc.Account,REPAYOVERCARRY);
       NumDoc_Chapt3 = ОткатитьДокументы(3,ObAcc.Account,CorrAcc.Account,REPAYOVERCARRY);
       if(NumDoc_Chapt1 != NumDoc_Chapt3)
         c.ErrMsg = "Не совпадает число документов по балансу и внебалансу";
         AbortTrn;
       end;
       if(NumDoc_Chapt1)ПересчитатьОстатки = 1;end;
       /* Удаляем документы по выдаче кредита */
       NumDoc_Chapt1 = ОткатитьДокументы(1,OvAcc.Account,Acc.Account,PAYOVERCARRY);
       NumDoc_Chapt3 = ОткатитьДокументы(3,CorrAcc.Account,ObAcc.Account,PAYOVERCARRY);
       if(NumDoc_Chapt1 != NumDoc_Chapt3)
         c.ErrMsg = "Не совпадает число документов по балансу и внебалансу";
         AbortTrn;
       end;
       if(NumDoc_Chapt1)ПересчитатьОстатки = 1;end;
     else
       /* Удаляем документы по погашению кредита */
       NumDoc_Chapt1 = ОткатитьДокументы(1,Acc_val.Account,OvAcc_val.Account,REPAYOVERCARRY);
       NumDoc_Chapt3 = ОткатитьДокументы(3,ObAcc_val.Account,CorrAcc_val.Account,REPAYOVERCARRY);
       if(NumDoc_Chapt1 != NumDoc_Chapt3)
         c.ErrMsg = "Не совпадает число документов по балансу и внебалансу";
         AbortTrn;
       end;
       if(NumDoc_Chapt1)ПересчитатьОстатки = 1;end;
       /* Удаляем документы по выдаче кредита */
       NumDoc_Chapt1 = ОткатитьДокументы(1,OvAcc_val.Account,Acc_val.Account,PAYOVERCARRY);
       NumDoc_Chapt3 = ОткатитьДокументы(3,CorrAcc_val.Account,ObAcc_val.Account,PAYOVERCARRY);
       if(NumDoc_Chapt1 != NumDoc_Chapt3)
         c.ErrMsg = "Не совпадает число документов по балансу и внебалансу";
         AbortTrn;
       end;
       if(NumDoc_Chapt1)ПересчитатьОстатки = 1;end;
     end;

     if(ПересчитатьОстатки)
       if(Code_Currency == 0)
         Rest = RestA(Acc.Account,null,null,Acc.Chapter); /* остаток на рассчетном счете */
         OverRest = abs(RestA(OvAcc.Account,null,null,OvAcc.Chapter)); /* остаток на счете овердрафта */
       else
         Rest = RestAC(Acc_val.Account,Acc_val.Code_Currency,null,null,Acc_val.Chapter);
         OverRest = abs(RestAC(OvAcc_val.Account,OvAcc_val.Code_Currency,null,null,OvAcc_val.Chapter));
       end;
     end;

END;

MACRO CarryTrn

   /* На случай повторного запуска процедуры, сначала откатываем все документы
      по выдаче и погашению овердрафтного кредита */
   BackoutDoc();

   if(Rest > $0) /* Погашение овердрафтного кредита */
     if(Code_Currency == 0)
       ПогашениеКредита(d,Acc,OvAcc,ObAcc,CorrAcc);
     else
       ПогашениеКредита(d_val,Acc_val,OvAcc_val,ObAcc_val,CorrAcc_val);
     end;
   elif(Rest < $0) /* Выдача овердрафтного кредита */
     if(Code_Currency == 0)
       ВыдачаКредита(d,Acc,OvAcc,ObAcc,CorrAcc);
     else
       ВыдачаКредита(d_val,Acc_val,OvAcc_val,ObAcc_val,CorrAcc_val);
     end;
   else
     /* После отката нет необходимости по выдаче(погашению кредита) */
     if(Code_Currency == 0)
       ИзменитьЛимит(Acc,ObAcc);
     else
       ИзменитьЛимит(Acc_val,ObAcc_val);
     end;
   end;

END;

/* Макрос вставок проводок */
MACRO CarryDocs(aa,bb,cc)

   Code_Currency = aa; /* код валюты счета            */
   Rest          = bb; /* остаток на рассчетном счете */
   OverRest      = abs(cc); /* остаток на счете овердрафта */

   VAR ОстатокДоОтката = Rest;

   if(Rest == $0) return "Нулевой остаток на расчетном счете";end;

   ClearRecord(c);

   if(Code_Currency == 0)
     c.CarryKind = 1;         /* 1-рубли */
   else
     c.CarryKind = 3;         /* 3-проводка валютного документа с покрытием */
   end;
   c.left=0;              /* Учитывать ограничения на счетах */
   c.dateF={curdate};     /* Проводка за текущий день */
   c.dateV={curdate};     /* Дата валютирования - текущий день */

   ret = 0;

   ProcessTrn( c.CarryKind,"CarryTrn" );  /* Процесс выполения транзакции */

   if((ValType(ret) == V_INTEGER) and (ret==0) and (c.ErrMsg == "") )
     /* Проверяем ситуацию, когда планировалась обработка остатка
       по расчетному счету одного знака, но из-за повторного запуска
       процедуры обработался остаток противоположного знака */
     if( (ОстатокДоОтката != Rest) and
         ( ((ОстатокДоОтката > $0) and (Rest < $0)) or
           ((ОстатокДоОтката < $0) and (Rest > $0))
         )
       )
       return -100;
     elif((Rest == $0) or ((Rest > $0) and (OverRest == $0)) ) return 5813; /* После отката нет необходимости по выдаче(погашению кредита) */
     end;
     return "";
   elif(c.ErrMsg == "")
     return "Ошибка";
   else
     return c.ErrMsg;
   end;

END;

MACRO BackoutTrn
   BackoutDoc();

   if(Code_Currency == 0)
     ИзменитьЛимит(Acc,ObAcc);
   else
     ИзменитьЛимит(Acc_val,ObAcc_val);
   end;
END;

/* Макрос удаления проводок */
MACRO BackoutDocs(aa)

   Code_Currency = aa; /* код валюты счета            */

   ClearRecord(c);

   if(Code_Currency == 0)
     c.CarryKind = 1;         /* 1-рубли */
   else
     c.CarryKind = 3;         /* 3-проводка валютного документа с покрытием */
   end;
   c.left=0;              /* Учитывать ограничения на счетах */
   c.dateF={curdate};     /* Проводка за текущий день */
   c.dateV={curdate};     /* Дата валютирования - текущий день */

   ret = 0;

   ProcessTrn( c.CarryKind,"BackoutTrn" );  /* Процесс выполения транзакции */

   if((ValType(ret) == V_INTEGER) and (ret==0) and (c.ErrMsg == "") )
     return "";
   elif(c.ErrMsg == "")
     c.ErrMsg = "Ошибка";
   end;

   if(c.ErrMsg != "") MsgBox(c.ErrMsg); end;
   return c.ErrMsg;

END;

/* Печать заголовка отчета */
MACRO PrintHeader(Дата)
[                                       Обработка счетов с овердрафтом
                                                за #

](Дата);
[┌─┬───────────────────────┬────────────────────────────────────────────────────┐];
[│О│      Номер счета      │ Результат                                          │];
[├─┼───────────────────────┼────────────────────────────────────────────────────┤];
END;

MACRO PrintAccount(IsErr, RepMsg)
 VAR success = "", i = 1;
 array StringArray;

 if(IsErr) success = "X"; end;

         StrSplit(StrSubst(RepMsg, "|", " " ), StringArray, 52);
[│#│#######################│####################################################│](success,Acc.Account:f,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[│ │                       │####################################################│](StringArray(i));
           i = i + 1;
         end;

[└─┴───────────────────────┴────────────────────────────────────────────────────┘];
END;

