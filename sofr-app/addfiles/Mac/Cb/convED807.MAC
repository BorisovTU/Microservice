/************************************************************************
  RS-Bank V.6

  Заполнение справочника банков из файла xml
*************************************************************************/

import ServiceAction;

import mfo_lib;
import rsexts;

// Импорт справочника банков ЦБ РФ из текста
macro RunImportED807Text(XMLText : string, InputFileName : string, MBRMessID : integer, IniMBRMessID : integer)
  PRIVATE CONST V_SPECVAL = 26;
  var Retval = 0;
  var CloseDeletedBank;
  var err;
  var party : RsbParty;
  var PartyID = 0;
  var CheckData;
  var stat = true;
  var strHeading;
  var pos;
  var rs, cmd, ret_val,
      rsSub, cmdSub,
      rsCount, cmdCount;
  var ImpID = -1;
  var str;

  var BICDirectoryEntry;
  var PartInfo, InitialED;
  var PartAggregateID, PartQuantity, PartNo;
  var EDNo, EDDate, EDAuthor, EDReceiver, CreationReason, CreationDateTime, CreationDate, CreationTime, InfoTypeCode, IniEDNo, IniEDDate, IniEDAuthor;
  var BusinessDay, DirectoryVersion;
  var FileContent;

  file LogFile() txt write;

  var xml = CreateXMLParser();  // создаем MS-парсер

  xml.loadXML(XMLText);

  /*Нельзя TRUNCATE т.к. м.б. вызвана в транзакции*/
  SQL_Execute("DELETE FROM dimpbiclog_tmp");
  SQL_Execute("DELETE FROM err$_dparty_dbt");
  SQL_Execute("DELETE FROM err$_dbankdprt_dbt");
  SQL_Execute("DELETE FROM err$_dobjcode_dbt");
  SQL_Execute("DELETE FROM err$_dpartyown_dbt");
  SQL_Execute("DELETE FROM err$_dpartyname_dbt");
  SQL_Execute("DELETE FROM err$_dobjlink_dbt");
  SQL_Execute("DELETE FROM err$_dadress_dbt");
  SQL_Execute("DELETE FROM err$_dobjatcor_dbt");

  var prefix = "";
  var ED807;
  
  ED807 = xml.getElementsByTagName(prefix + "ED807");
  if(ED807.length == 0)
    prefix = "ed:";
    ED807 = xml.getElementsByTagName(prefix + "ED807");
  end;

  if(ED807.length != 0)
    BICDirectoryEntry = ED807.item(0).getElementsByTagName(prefix + "BICDirectoryEntry");

    EDNo = ED807.item(0).getAttribute("EDNo");
    if(ValType(EDNo) == V_SPECVAL)
      EDNo = 0;
    elif(StrLen(EDNo) > 10)
      msgbox("Ошибка загрузки справочника, атрибут EDNo тега ED807 содержит больше максимального количества символов");
      return 1;
    end;

    EDDate = ED807.item(0).getAttribute("EDDate");
    if(ValType(EDDate) == V_SPECVAL)
      EDDate = "TO_DATE ('0001-01-01', 'YYYY-MM-DD')";
    elif(StrLen(EDDate) != 10)
      msgbox("Ошибка загрузки справочника, атрибут EDDate тега ED807 неверного формата");
      return 1;
    else
      EDDate = "TO_DATE ('"+ED807.item(0).getAttribute("EDDate")+"', 'YYYY-MM-DD')";
    end;

    EDAuthor = ED807.item(0).getAttribute("EDAuthor");
    if(ValType(EDAuthor) == V_SPECVAL)
      EDAuthor = "CHR(1)";
    elif(StrLen(EDAuthor) > 10)
      msgbox("Ошибка загрузки справочника, атрибут EDAuthor тега ED807 содержит больше максимального количества символов");
      return 1;
    end;

    EDReceiver = ED807.item(0).getAttribute("EDReceiver");
    if(ValType(EDReceiver) == V_SPECVAL)
      EDReceiver = "CHR(1)";
    elif(StrLen(EDReceiver) > 10)
      msgbox("Ошибка загрузки справочника, атрибут EDReceiver тега ED807 содержит больше максимального количества символов");
      return 1;
    end;

    BusinessDay = ED807.item(0).getAttribute("BusinessDay");
    if(ValType(BusinessDay) == V_SPECVAL)
      BusinessDay = "TO_DATE ('0001-01-01', 'YYYY-MM-DD')";
    else
      BusinessDay = "TO_DATE('" + SubStr(BusinessDay, 9, 2) + "-" + SubStr(BusinessDay, 6, 2) + "-" + SubStr(BusinessDay, 1, 4) + "', 'DD-MM-YYYY')";;
    end;

    DirectoryVersion = ED807.item(0).getAttribute("DirectoryVersion");
    if(ValType(DirectoryVersion) == V_SPECVAL)
      DirectoryVersion = "CHR(1)";
    else
      DirectoryVersion = SubStr(DirectoryVersion, 1, 2);
    end;

    CreationReason = ED807.item(0).getAttribute("CreationReason");
    if(ValType(CreationReason) == V_SPECVAL)
      CreationReason = "CHR(1)";
    elif(StrLen(CreationReason) > 4)
      msgbox("Ошибка загрузки справочника, атрибут CreationReason тега ED807 содержит больше максимального количества символов");
      return 1;
    else
      CreationReason = "'"+ED807.item(0).getAttribute("CreationReason")+"'";
    end;

    CreationDateTime = ED807.item(0).getAttribute("CreationDateTime");
    if(ValType(CreationDateTime) == V_SPECVAL)
      CreationDate = "TO_DATE ('0001-01-01', 'YYYY-MM-DD')";
      CreationTime = "TO_DATE ('0001-01-01 00-00-00', 'YYYY-MM-DD HH24-MI-SS')";
    elif(StrLen(CreationDateTime) != 20)
      msgbox("Ошибка загрузки справочника, атрибут CreationDateTime тега ED807 неверного формата");
      return 1;
    else
      CreationDate = "TO_DATE('"+ SubStr(CreationDateTime, 1, 10) +"', 'YYYY-MM-DD')";
      CreationTime = "TO_DATE('0001-01-01 "+ SubStr(CreationDateTime, 12, 8) +"', 'YYYY-MM-DD HH24-MI-SS')";
    end;

    InfoTypeCode = ED807.item(0).getAttribute("InfoTypeCode");
    if(ValType(InfoTypeCode) == V_SPECVAL)
      InfoTypeCode = -1;
    elif(StrLen(InfoTypeCode) > 4)
      msgbox("Ошибка загрузки справочника, атрибут InfoTypeCode тега ED807 содержит больше максимального количества символов");
      return 1;
    end;

    InitialED = ED807.item(0).getElementsByTagName(prefix + "InitialED");
    if(InitialED.length != 0)
      IniEDNo = InitialED.item(0).getAttribute("EDNo");
      if(ValType(IniEDNo) == V_SPECVAL)
        IniEDNo = 0;
      elif(StrLen(IniEDNo) > 10)
        msgbox("Ошибка загрузки справочника, атрибут EDNo тега InitialED содержит больше максимального количества символов");
        return 1;
      end;

      IniEDDate = InitialED.item(0).getAttribute("EDDate");
      if(ValType(IniEDDate) == V_SPECVAL)
        IniEDDate = "TO_DATE ('0001-01-01', 'YYYY-MM-DD')";
      elif(StrLen(IniEDDate) != 10)
        msgbox("Ошибка загрузки справочника, атрибут EDDate тега InitialED неверного формата");
        return 1;
      else
        IniEDDate = "TO_DATE ('"+ED807.item(0).getElementsByTagName(prefix + "InitialED").item(0).getAttribute("EDDate")+"', 'YYYY-MM-DD')";
      end;

      IniEDAuthor = InitialED.item(0).getAttribute("EDAuthor");
      if(ValType(IniEDAuthor) == V_SPECVAL)
        IniEDAuthor = 0;
      elif(StrLen(IniEDAuthor) > 10)
        msgbox("Ошибка загрузки справочника, атрибут EDAuthor тега InitialED содержит больше максимального количества символов");
        return 1;
      end;
    else
      IniEDNo = 0;
      IniEDDate = "TO_DATE ('0001-01-01', 'YYYY-MM-DD')";
      IniEDAuthor = 0;
    end;

    PartInfo = ED807.item(0).getElementsByTagName(prefix + "PartInfo");
    if(PartInfo.length != 0)
      PartAggregateID = PartInfo.item(0).getAttribute("PartAggregateID");
      if(ValType(PartAggregateID) == V_SPECVAL)/* Передан XML'ый NULL*/
        PartAggregateID = "CHR(1)";
      elif(StrLen(PartAggregateID) > 27)
        msgbox("Ошибка загрузки справочника, атрибут PartAggregateID тега PartInfo содержит больше максимального количества символов");
        return 1;
      end;

      PartQuantity = PartInfo.item(0).getAttribute("PartQuantity");
      if(ValType(PartQuantity) == V_SPECVAL)
        PartQuantity = 1;
      elif(StrLen(PartQuantity) > 7)
        msgbox("Ошибка загрузки справочника, атрибут PartQuantity тега PartInfo содержит больше максимального количества символов");
        return 1;
      end;

      PartNo = PartInfo.item(0).getAttribute("PartNo");
      if(ValType(PartNo) == V_SPECVAL)
        PartNo = 1;
      elif(StrLen(PartNo) > 7)
        msgbox("Ошибка загрузки справочника, атрибут PartNo тега PartInfo содержит больше максимального количества символов");
        return 1;
      end;

      cmd = RsdCommand("SELECT t_ImpID FROM dimpbic_dbt WHERE t_PartAggregateID = ?");
      cmd.addParam("", RSDBP_IN, PartAggregateID);
      cmd.execute;
      rs = RsdRecordset(cmd);
      if(rs.moveNext)
        ImpID = rs.value(0);
      end;

      if(ImpID != -1)
        SQL_Execute("UPDATE dimpbic_dbt SET t_PartQuantity = " + PartQuantity + " WHERE t_ImpID = " + ImpID);
      else
        cmd = RsdCommand("INSERT INTO dimpbic_dbt (t_ImpID, t_EDNo, t_EDDate, t_EDAuthor, t_EDReceiver, t_CreationReason, t_CreationDate, t_CreationTime, t_InfoTypeCode, t_PartQuantity, t_PartAggregateID, t_IniEDNo, t_IniEDDate, t_IniEDAuthor, t_IniMBRMessID, t_BusinessDay, t_DirectoryVersion)" +
                                  "VALUES ("+ 0 +","+ EDNo +","+ EDDate +","+ EDAuthor +","+ EDReceiver +","+ CreationReason +","+ CreationDate +","+ CreationTime +", '"+ InfoTypeCode +"',"+ PartQuantity +","+ PartAggregateID +","+ IniEDNo +","+ IniEDDate +","+ IniEDAuthor +","+ IniMBRMessID + "," + BusinessDay + "," + DirectoryVersion + ") RETURNING t_ImpID INTO ?");
        cmd.addParam("ImpID", RSDBP_OUT, V_INTEGER);
        cmd.execute;
        ImpID = cmd.value("ImpID");
      end;

      cmd = RsdCommand("SELECT * FROM dimpbicpart_dbt WHERE t_ImpID = " + ImpID + " AND t_PartNo = " + PartNo);
      cmd.execute;
      rs = RsdRecordset(cmd);
      if(rs.moveNext)
        SQL_Execute("UPDATE dimpbicpart_dbt SET t_ReceptionDate = TRUNC(SYSDATE), t_ReceptionTime = TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), t_MBRMessID = " + MBRMessID + " WHERE t_ImpID = " + ImpID + " AND t_PartNo = " + PartNo);
      else
        SQL_Execute("INSERT INTO dimpbicpart_dbt (t_ImpID, t_PartNo, t_ReceptionDate, t_ReceptionTime, t_MBRMessID) VALUES ("+ ImpID +","+ PartNo +", TRUNC(SYSDATE) , TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS') ,"+ MBRMessID +")");
      end;

      // Занести в фискальный журнал стандартную запись 
      str = "Начат импорт Справочника БИК ЦБ РФ: InfoTypeCode="+InfoTypeCode+", PartAggregateID="+PartAggregateID+", PartNo="+PartNo;
      WriteFiscLog(OLstrproc, str);        

      strHeading = "PartAggregateID="+PartAggregateID+", PartQuantity="+PartQuantity+", PartNo="+PartNo+", кол-во записей="+BICDirectoryEntry.length;
      SQL_Execute("INSERT INTO dimpbiclog_tmp (t_SysDate, t_SysTime, t_PartyID, t_Message) "+
                                      "VALUES (TRUNC(SYSDATE), "+
                                               "TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), "+
                                               PartyID+","+
                                               "'"+ strHeading +"')");
      
    else
      PartAggregateID = "CHR(1)";
      PartQuantity = 1;
      PartNo = 1;
      cmd = RsdCommand("INSERT INTO dimpbic_dbt (t_ImpID, t_EDNo, t_EDDate, t_EDAuthor, t_EDReceiver, t_CreationReason, t_CreationDate, t_CreationTime, t_InfoTypeCode, t_PartQuantity, t_PartAggregateID, t_IniEDNo, t_IniEDDate, t_IniEDAuthor, t_IniMBRMessID, t_BusinessDay, t_DirectoryVersion)" +
                                      "VALUES ("+ 0 +","+ EDNo +","+ EDDate +","+ EDAuthor +","+ EDReceiver +","+ CreationReason +","+ CreationDate +","+ CreationTime +", '"+ InfoTypeCode +"',"+ PartQuantity +","+ PartAggregateID +","+ IniEDNo +","+ IniEDDate +","+ IniEDAuthor +","+ 0 + "," + BusinessDay + "," + DirectoryVersion + ") RETURNING t_ImpID INTO ?");
      cmd.addParam("ImpID", RSDBP_OUT, V_INTEGER);
      cmd.execute;
      ImpID = cmd.value("ImpID");
      SQL_Execute("INSERT INTO dimpbicpart_dbt (t_ImpID, t_PartNo, t_ReceptionDate, t_ReceptionTime, t_MBRMessID) "+
                                    "VALUES ("+ ImpID +","+ PartNo +", TRUNC(SYSDATE) , TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS') ,"+ 0 +")");
      // Занести в фискальный журнал стандартную запись 
      str = "Начат импорт Справочника БИК ЦБ РФ: InfoTypeCode="+InfoTypeCode+", файл="+InputFileName;
      WriteFiscLog(OLstrproc, str); 
    end;
      
    FileContent = xml.xml;

    SQL_Execute("DELETE FROM DXMLTEXTED807_TMP");

    cmd = RsdCommand("INSERT INTO dXMLTextED807_tmp (T_XMLTEXT) VALUES (?)");
    cmd.AddParam("", RSDBP_IN, FileContent);
    cmd.execute();

    cmd = RsdCommand("begin ? := rsb_convmfo.convED807_load(" + {oper} + ", " + ImpID + "); end;");
    cmd.addParam("ret_val", RSDBP_OUT, V_INTEGER);
    SQL_Execute(cmd,"Выполняется импорт файла справочника банков ЦБ РФ. Ждите...");

    GetRegistryValue("CB\\BNKSIMPORT\\CLOSEDELETEDBANK", V_BOOL, CloseDeletedBank, err, false, {oper}); // Закрытие субъекта закрываемого банка
    if(CloseDeletedBank)
      cmd = RsdCommand("SELECT DISTINCT t_PartyID FROM dimpbiclog_tmp WHERE t_PartyID <> 0 ");
      cmd.execute;
      rs = RsdRecordset(cmd);
      while(rs.moveNext)
        PartyID = rs.value(0);
        cmdSub =RsdCommand("SELECT t_PartyID, t_CheckData FROM dbankdprt_dbt WHERE t_PartyID = ? "+
                           "AND t_ChangeType = 'DLTD' "+
                           "AND NOT EXISTS (SELECT 1 FROM ddp_dep_dbt WHERE ddp_dep_dbt.t_PartyID = dbankdprt_dbt.t_PartyID) "+
                           "AND t_PZN = (SELECT t_NumberKind FROM ddprtkind_dbt WHERE t_ClosePtOnDel = 'X' AND ddprtkind_dbt.t_NumberKind = dbankdprt_dbt.t_PZN) "+
                           "AND NOT EXISTS (SELECT 1 FROM dpartyown_dbt WHERE dpartyown_dbt.t_PartyID = dbankdprt_dbt.t_PartyID AND dpartyown_dbt.t_PartyKind <> 2 AND dpartyown_dbt.t_PartyKind <> 85)"); 
        cmdSub.addParam("", RSDBP_IN, PartyID);
        cmdSub.execute;
        rsSub = RsdRecordset(cmdSub);
        if(rsSub.moveNext)
          party = RsbParty(PartyID);
          err = party.Lock(false);
          if(err)
            SQL_Execute("UPDATE dobjcode_dbt SET t_BankCloseDate = TO_DATE('" + {curdate} + "', 'DD-MM-YYYY'), t_State = 1 WHERE (t_State = 0 OR t_BankCloseDate = TO_DATE('01-01-0001', 'DD-MM-YYYY')) AND t_ObjectType = 3 AND t_CodeKind = 1 AND t_ObjectID = " + PartyID);
            CheckData = rsSub.value(1);
            SQL_Execute("INSERT INTO dimpbiclog_tmp (t_SysDate, t_SysTime, t_PartyID, t_Message) "+
                                            "VALUES (TRUNC(SYSDATE), "+
                                                    "TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), "+
                                                     PartyID+","+
                                                    "'Субъект ID=" + PartyID + ", БИК=" + CheckData + " закрыт: ChangeType=DLTD')");
          end;
          party = null;
        end;
      end;
    end;

    cmd = RsdCommand("SELECT t_ImpID FROM dimpbic_dbt " +
                                   "WHERE t_InfoTypeCode = 'FIRR' " + 
                                     "AND t_PartQuantity = (SELECT COUNT(*) FROM dimpbicpart_dbt WHERE dimpbicpart_dbt.t_ImpID = " + ImpID + ") " +
                                     "AND t_ImpID = " + ImpID); 
    cmd.execute;
    rs = RsdRecordset(cmd);
    if(rs.moveNext)
      if(CloseDeletedBank)

        cmdSub = RsdCommand("SELECT t1.t_PartyID                                                                                               " +
                            "  FROM dparty_dbt t1                                                                                              " +
                            " WHERE EXISTS(SELECT 1 FROM dbankdprt_dbt t3 WHERE t3.t_PartyID = t1.t_PartyID AND t3.t_ImpID > 0 AND t3.t_ImpID <> " + ImpID + ") " +
                            "   AND t1.t_NotUpdateOnImport = CHR(0)                                                                            " +
                            "   AND t1.t_Locked = CHR(0)                                                                                       " +
                            "   AND t1.t_PartyID NOT IN (SELECT t4.t_PartyID FROM dimpbiclog_tmp t4)                                           " +
                            "   AND t1.t_Superior NOT IN (SELECT t5.t_PartyID FROM dpartyown_dbt t5 WHERE t5.t_PartyKind IN (2, 85))           ");
                             
        cmdSub.execute;
        rsSub = RsdRecordset(cmdSub);
        while(rsSub.moveNext)
          PartyID = rsSub.value(0);

          cmdCount = RsdCommand("SELECT t_CheckData FROM dbankdprt_dbt WHERE t_PartyID = " + PartyID);
          cmdCount.execute;
          rsCount = RsdRecordset(cmdCount);
          if(rsCount.moveNext)
            CheckData = rsCount.value(0);
          end;
          //SQL_Execute("DELETE FROM dbankdprt_dbt WHERE t_PartyID = " + PartyID);
          //SQL_Execute("DELETE FROM dbankdprtrstr_dbt WHERE t_PartyID = " + PartyID);
          //SQL_Execute("DELETE FROM dbankdprtswbics_dbt WHERE t_PartyID = " + PartyID);
          //SQL_Execute("DELETE FROM dbankdprtaccnt_dbt WHERE t_PartyID = " + PartyID);
          //SQL_Execute("DELETE FROM dbankdprtaccrstr_dbt WHERE t_PartyID = " + PartyID);

          SQL_Execute("UPDATE dobjcode_dbt SET t_BankCloseDate = TO_DATE('" + {curdate} + "', 'DD-MM-YYYY'), t_State = 1 " +
                                        "WHERE t_ObjectID = " + PartyID +
                                         " AND t_ObjectType = 3 " +
                                          "AND (t_CodeKind = 3 OR t_CodeKind = 13 OR t_CodeKind = 47) " +
                                          "AND t_State = 0");

          SQL_Execute("DELETE FROM dpartyown_dbt WHERE t_PartyID = " + PartyID +
                                                 " AND (t_PartyKind = 2 OR t_PartyKind = 85)");

          cmdCount = RsdCommand("SELECT COUNT(*) FROM dpartyown_dbt WHERE t_PartyID = " + PartyID);
          cmdCount.execute;
          rsCount = RsdRecordset(cmdCount);
          if(rsCount.moveNext)
            if(rsCount.value(0) == 0)
              party = RsbParty(PartyID);
              err = party.Lock(false);
              if(err)
                SQL_Execute("UPDATE dobjcode_dbt SET t_BankCloseDate = TO_DATE('" + {curdate} + "', 'DD-MM-YYYY'), t_State = 1 WHERE (t_State = 0 OR t_BankCloseDate = TO_DATE('01-01-0001', 'DD-MM-YYYY')) AND t_ObjectType = 3 AND t_CodeKind = 1 AND t_ObjectID = " + PartyID);
                SQL_Execute("INSERT INTO dimpbiclog_tmp (t_SysDate, t_SysTime, t_PartyID, t_Message) "+
                                                "VALUES (TRUNC(SYSDATE), "+
                                                        "TO_DATE('01.01.0001' || TO_CHAR(SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), "+
                                                         PartyID+","+
                                                        "'Субъект ID=" + PartyID + ", БИК=" + CheckData + " закрыт: отсутствует в Справочнике БИК ЦБ РФ')");
              end;
              party = null;
            end;
          end;
        end;
      end;   
    end;

    if(PartInfo.length != 0)
      //Занести в фискальный журнал стандартную запись 
      str = "Завершен импорт Справочника БИК ЦБ РФ: InfoTypeCode="+InfoTypeCode+", PartAggregateID="+PartAggregateID+", PartNo="+PartNo;
      WriteFiscLog(OLfinproc, str);
    else
      //Занести в фискальный журнал стандартную запись 
      str = "Завершен импорт Справочника БИК ЦБ РФ: InfoTypeCode="+InfoTypeCode+", файл="+InputFileName;
      WriteFiscLog(OLfinproc, str);
    end;
    

    str = InfoTypeCode + date + " " + time + " " + {oper};
    SetDefaultRegistryValue("CB\\BNKSIMPORT\\LASTIMPORTFILEINFO", InfoTypeCode + " " + date + " " + time + " " + {oper});

    var WrkDir;
    var NameLogFile,
        NameLogFileFull;
    var d,mon,y,
        h,m,s;
    DateSplit(Date(),d,mon,y);
    TimeSplit(Time(),h,m,s);

    NameLogFile = String(y:4) + "-" + String(mon:2) + "-" + String(d:2) + "_" + String(h:2) + "-" + String(m:2) + "-" + String(s:2) + ".txt";
    NameLogFile = "biclog_" + StrSubst(NameLogFile, " ", "0");
    GetRegistryValue("CB\\BNKSIMPORT\\LOGDIR", V_STRING, WrkDir, err); // Каталог лог-файлов импорта обновлений Справочника БИК ЦБ РФ
    if(WrkDir == "")
      WrkDir = "..\\TxtFile\\";
    end;

    NameLogFileFull = WrkDir + NameLogFile;

    MakeDir(WrkDir);

    SetOutput(NameLogFileFull);
    if(ExistFile(NameLogFileFull))
      if(Open(LogFile, NameLogFileFull))
        cmd = RsdCommand("SELECT t_SysDate, t_SysTime, t_Message FROM dimpbiclog_tmp WHERE t_Message = '" + strHeading + "'"); 
        cmd.execute;
        rs = RsdRecordset(cmd);
        if(rs.moveNext)
          d = rs.value(0);
          h = rs.value(1);
          d = String(d);
          h = String(h);
          pos = StrBrk(d, "(");
          d = SubStr(d, 1, pos-2);
          pos = StrBrk(h, "(");
          h = SubStr(h, pos+1);
          pos = StrBrk(h, ".");
          h = SubStr(h, 1, pos-1);
          println(d+" "+h+ "\t" + rs.value(2));
        end;
        cmd = RsdCommand("SELECT t_SysDate, t_SysTime, t_Message FROM dimpbiclog_tmp ORDER BY t_SysDate ASC, t_SysTime ASC "); 
        cmd.execute;
        rs = RsdRecordset(cmd);
        while(rs.moveNext)
          d = rs.value(0);
          h = rs.value(1);
          d = String(d);
          h = String(h);
          pos = StrBrk(d, "(");
          d = SubStr(d, 1, pos-2);
          pos = StrBrk(h, "(");
          h = SubStr(h, pos+1);
          pos = StrBrk(h, ".");
          h = SubStr(h, 1, pos-1);
          if(rs.value(2) != strHeading)
            println(d+" "+h+ "\t" + rs.value(2));
          end;
        end;

        PrintProtocolErrED807();

        Close(LogFile);
      end;       
    end;
    SetOutput(NULL, TRUE);

  else
    RunError("Неверный формат xml файла");
    Retval = 1;
  end;

  return Retval;
end;
  
// Импорт справочника банков ЦБ РФ из файла
macro RunImportED807(InputFileName : string, MBRMessID : integer, IniMBRMessID : integer)
  var stat = true;
  var Retval = 0;
  var xml = CreateXMLParser();  // создаем MS-парсер

  stat = xml.load(InputFileName);

  if(stat)
    Retval = RunImportED807Text(xml.xml, InputFileName, MBRMessID, IniMBRMessID);
  else
    RunError("Ошибка загрузки xml файла");
    Retval = 1;
  end;

  return Retval;
end;
