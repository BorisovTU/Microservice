/*╔═══════════════════════════════════════════════════════════════════════════╗*/
/*║           Автоматизированная банковская система RS-Bank v5.1              ║*/
/*║               Copyright (c) R-Style Software Lab 2001-2004                ║*/
/*║                                                                           ║*/
/*║ Имя файла     or_dps_h.mac                                                ║*/
/*║                                                                           ║*/
/*║ Описание      Классы для работы с диапазонами заданными строкой           ║*/
/*║                                                                           ║*/
/*║ Программист   Зуев С.В.                                                   ║*/
/*║                                                                           ║*/
/*║ Создан        20.02.2002                                                  ║*/
/*║                                                                           ║*/
/*╚═══════════════════════════════════════════════════════════════════════════╝*/
/* Список классов:
  *CParamDiapason - Вспомогательный класс для заполнения данных по одному диапазону
   CDiapason      - Универсальный класс для формирования массива диапазонов нужного типа по переданной строке 
*/
/* Вспомогательный класс для заполнения данных по одной одному диапазону */ 
CLASS CParamDiapason(_BegDiapason:variant, _EndDiapason:variant)

   VAR BegDiapason:variant; /* Значение начала диапазона */
   VAR EndDiapason:variant; /* Значение конца диапазона */

   /* Инициализация класса */
   Macro Init(l_BegDiapason:variant, l_EndDiapason:variant)   
     
      if( ValType(l_BegDiapason) )   BegDiapason = l_BegDiapason;   end;
      if( ValType(l_EndDiapason) )   EndDiapason = l_EndDiapason;   end;

   End; /* Init */

   /* Конструктор */
   Init(_BegDiapason, _EndDiapason);

END; /* CParamDiapason */

/* Универсальный класс для формирования массива диапазонов нужного типа по переданной строке */
CLASS CDiapason(_Str:string, _TypeName:integer)

   VAR Str      :string  = "";        /* Обрабатываемая строка */
   VAR TypeName :integer = V_INTEGER; /* Тип границ получаемых диапазонов */
   VAR RetValue :bool    = True;      /* Возвращаемое значение */
   VAR ArrResult:TArray  = TArray;    /* Результирующий массив объектов класса CParamDiapason */

   /* Инициализация класса */
   Macro Init(l_Str:string, l_TypeName:integer)

      var TempStr:string  = ""; /* Вспомогательная строка */
      var Sz     :integer = 0;  /* Размер строки */
      var k      :integer = 0;  /* Счетчик циклов */
      var CurChar:string  = ""; /* Текущий символ */ 

      if(ValType(l_Str))
           Str = Trim(l_Str);
           Sz = StrLen(Str);
           /* Удаляем все пробелы из середины */
           while(k <= Sz)
                CurChar = SubStr(Str, k, 1);            
                if (CurChar != " ")
                     TempStr = TempStr + CurChar; 
                end;
                k = k + 1;
           end;
           Str = TempStr;
      end;
      if(ValType(l_TypeName))
           TypeName = l_TypeName;
      end;

   End; /* Init */

   /* Метод разбирает строку перечисления типа <1, 2, 4, 6-15, 30> и формирует массив диапазонов заданного вида.
      В случае успеха возвращает True */
   Macro AnalysisStr()

      var Len    :integer = StrLen(Str); /* Длина строки */
      var FlagEnd:bool    = False;       /* Флаг окончания обработки строки */
      var i      :integer = 1;           /* Счетчик циклов */
      var Pos1   :integer = 0;           /* Номер позиции искомого символа "," */ 

      Macro CheckDiapason(_BegDiapason:variant, _EndDiapason:variant)

          var j :integer = 0;                /* Счетчик циклов */
          var Sz:integer = ArrResult.Size(); /* Размер результирующего массива */
          var ArrParamDiapason:CParamDiapason;

              while( j < Sz )
                   if(ArrResult[j].BegDiapason == _BegDiapason)
                        if(ArrResult[j].EndDiapason == _EndDiapason)
                             Return False;
                        end;
                   end;
                   j = j + 1;
              end;
              ArrParamDiapason = CParamDiapason(_BegDiapason, _EndDiapason);
              ArrResult[Sz] = ArrParamDiapason;
              Return True;

      End; /* CheckDiapason */

      /* Метод анализа лексем */
      Macro AnalysisDiapason(l_Str:string)

         var FLAGERR    :bool    = TRUE;               /* Флаг наличия ошибки в лексеме */
         var LenStr     :integer = StrLen(l_Str);      /* Количество символов в лексеме */
         var Pos2       :integer = Index( l_Str, "-");/* Номер позиции искомого символа "-" */
         var BegDiapason:variant;                      /* Значения начала диапазона */
         var EndDiapason:variant;                      /* Значения конца диапазона */ 

         /* Проверка на повторение запятых*/
         if(NOT LenStr) 
              return True;
         end;
         if(Pos2)
               /* Проверка на повторение дефисов */
              while(FlagErr AND (Pos2<LenStr))
                   if(NOT StrBrk(Trim(SubStr(l_Str, Pos2+1, 1)), "1234567890."))
                        Pos2 = Pos2 + 1;
                   else FlagErr = FALSE;
                   end;
              end;
               /* Проверка на тип значения */
              if(TypeName == V_DOUBLE)
                   BegDiapason = Double(SubStr(l_Str, 1, Pos2-1));
                   EndDiapason = Double(SubStr(l_Str, Pos2+1));
              else BegDiapason = Int(Double(SubStr(l_Str, 1, Pos2-1))+0.5);
                   EndDiapason = Int(Double(SubStr(l_Str, Pos2+1))+0.5);
              end;
              if(BegDiapason >= EndDiapason)
                   MsgBox("Значение начала диапазона должно быть меньше значения конца диапазона!");
                   return FALSE;
              else CheckDiapason(BegDiapason, EndDiapason); 
              end;
         else if(TypeName == V_DOUBLE)
                    BegDiapason = Double(l_Str);
                    EndDiapason = Double(l_Str);
               else BegDiapason = Int(Double(l_Str)+0.5);
                    EndDiapason = Int(Double(l_Str)+0.5);
               end;
               CheckDiapason(BegDiapason, EndDiapason); 
         end;
         return TRUE;

      End; /* AnalysisDiapason */

      /* Проверяем на наличие в строки "левых" символов */
      while( i <= Len );
           if(NOT Index("1234567890.,-", SubStr( Str, i, 1)))
                MsgBox( "В строке диапазонов присутствуют недопустимые символы!");
                return False;
           end;
           i = i + 1;
      end;
      /* Анализируем лексемы строки, разделенные запятыми */
      while(Not FlagEnd)
           if(Pos1 = Index(Str, ","))
                if(Not AnalysisDiapason(Trim(SubStr(Str, 1, Pos1-1))))
                     Return False;
                end;  
                Str = SubStr(Str, Pos1 + 1 );
           else if(Not AnalysisDiapason(Trim(SubStr(Str, 1))))
                     Return False;
                end;
                FlagEnd = True;
           end;
      end;
      Return True;

   End; /* AnalysisStr */

   /* Конструктор */
   Init(_Str, _TypeName);

END; /* CDiapason */