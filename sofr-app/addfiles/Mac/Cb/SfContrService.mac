// SfContrService.mac
import oralib;

class SfSubContrStruct
    var id:integer;
    var contrId:integer;
    var number:string;
    var clientId:integer;
    var dateBegin:date;
    var servKind:integer;
    var servKindSub:integer;
    var marketPlaceCode:string;
    var marketId:integer;

    macro ToString()
        return String("SfSubContrStruct{",
            "id:", id,
            ",contrId:", contrId,
            ",number:", number,
            ",clientId:", clientId,
            ",dateBegin:", dateBegin,
            ",servKind:", servKind,
            ",servKindSub:", servKindSub,
            ",marketPlaceCode:", marketPlaceCode,
            ",marketId:", marketId,
        "}");
    end;
end;

class SfContrStruct
    var id:integer;
    var number:string;
    var clientId:integer;
    var dateBegin:date;

    macro ToString()
        return String("SfContrStruct{",
            "id:", id,
            ",number:", number,
            ",clientId:", clientId,
            ",dateBegin:", dateBegin,
        "}");
    end;
end;

class SfContrService

    macro GetSubContrById(subContrId:integer):SfSubContrStruct
        var subContr:SfSubContrStruct;
        var query = 
                  "select c.t_id, "
                + "       d.t_sfcontrid contr_id, "
                + "       c.t_number, "
                + "       c.t_partyid, "
                + "       c.t_datebegin, "
                + "       c.t_servkind, "
                + "       c.t_servkindsub, "
                + "       m.t_mpcode, "
                + "       m.t_marketid "
                + "  from dsfcontr_dbt c "
                + "  join ddlcontrmp_dbt m on m.t_sfcontrid = c.t_id "
                + "  join ddlcontr_dbt d on d.t_dlcontrid = m.t_dlcontrid "
                + " where c.t_id = :id "
                + "   and m.t_mpregdate <= sysdate "
                + "   and (sysdate < m.t_mpclosedate or m.t_mpclosedate = to_date('01.01.0001', 'dd.mm.yyyy')) "
                ;
        var sql = ExecSQLselectPrmDyn(query, subContrId);

        if (sql.MoveNext())
            subContr.id = sql.value("t_id");
            subContr.contrId = sql.value("contr_id");
            subContr.number = sql.value("t_number");
            subContr.clientId = sql.value("t_partyid");
            subContr.dateBegin = sql.value("t_datebegin");
            subContr.servKind = sql.value("t_servkind");
            subContr.servKindSub = sql.value("t_servkindsub");
            subContr.marketPlaceCode = sql.value("t_mpcode");
            subContr.marketId = sql.value("t_marketid");
        end;

        return subContr;
    end;

    macro GetContrById(contrId:integer):SfContrStruct
        var contr:SfContrStruct;
        var query = 
                  "select c.t_id, "
                + "       c.t_partyid, "
                + "       c.t_number, "
                + "       c.t_datebegin "
                + "  from dsfcontr_dbt c "
                + "  join ddlcontr_dbt dc on dc.t_sfcontrid = c.t_id "
                + " where c.t_id = :contrId ";
        var sql = ExecSQLselectPrmDyn(query, contrId);

        if (sql.MoveNext())
            contr.id = sql.value("t_id");
            contr.number = sql.value("t_partyid");
            contr.clientId = sql.value("t_number");
            contr.dateBegin = sql.value("t_datebegin");
        end;

        return contr;
    end;
end;