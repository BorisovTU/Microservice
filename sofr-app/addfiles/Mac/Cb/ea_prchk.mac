import FIInter, PTInter, CTInter, "globals.mac", cb_sql;
import EAInter;
import rsd, oralib, likepy;
import "ea_lib.mac";

file FileEDSTUNITCHECK("edstunitcheck.tmp") key 0;
file FileEDSTUNITLOG(edstunitlog) key 0;

var PrevStoreUnitLogID = -1;
var PrevEDocLogID = -1;
var PrevResult = -1;
var ProtocolMode = 0;

private macro _GetRegHashMethod()
  var stat;
  var HashMethod = 0;
  GetRegistryValue("EA\\EDTODEV\\HASHMETHOD", V_INTEGER, HashMethod, stat);
  return HashMethod;
end;

private macro _PrintProtocolHeader()

[#                                                                           ] (GetDprtPartyName({NumDprt}));
  if(ProtocolMode == 0)
[ПРОТОКОЛ проверки ЕХ на носителе                                            ];
  else
[ПРОТОКОЛ проверки ЕХ в хранилище образов ЕХ                                 ];
  end;
[# #                                                                         ] (GetDprtName({OperDprt}), GetDprtPartyName({OperDprt}));
[============================================================================];
[Пользователь: ##### #                                                       ] ({oper}, {Name_Oper});
[Дата и время: ########## ########                                           ] (date(), time());
[Метод вычисления хэш-функции: #                                             ] (_GetRegHashMethod());
[============================================================================];


end;

private macro _PrintProtocolFooter()

[
Проверку выполнил: _________________________ #                               ] ({Name_Oper});

end;

private macro _PrintEdStUnitLogFooter()
  if(PrevResult == 0)
[|Результат проверки: | Ошибок нет                                          |];
  end;
  if(PrevResult >= 0)
[----------------------------------------------------------------------------];
  end;
end;

private macro _PrintEdStUnitLogHeader(edstunitlog)
  _PrintEdStUnitLogFooter();
[
];
[----------------------------------------------------------------------------];
[|Единица хранения:   | ##########                                          |] (edstunitlog.StoreUnitLogID);
[|Период ДЭВ:         | с ########## по ##########                          |] (edstunitlog.PeriodBeginDate, edstunitlog.PeriodEndDate);
[|Дата записи:        | ########## ########                                 |] (edstunitlog.RecordDate, edstunitlog.RecordTime);
end;

private macro _PrintEdStUnitCheckLine(edstunitcheck)
  array _ErrMsg;
  var j = 0;

  var LogHashValue;
  var CalcHashValueStorage;
  var CalcHashValueMedia;
  var RegHashValueStorage;
  var RegHashValueMedia;
  
  if(PrevStoreUnitLogID == edstunitcheck.StoreUnitLogID)
    /*Если предыдущая запись была с Result = 0, то с текущей с Result != 0 нужно начать описывать ошибки ЕХ*/
    if((edstunitcheck.EDocLogID == 0) and (edstunitcheck.Result != 0))
      StrSplit(edstunitcheck.ErrorMessage, _ErrMsg, 51, 51, 10 );
      while(StrLen(_ErrMsg(j)) > 0)
        if((j == 0)  and (PrevResult == 0))
[|Результат проверки: | ################################################### |] (_ErrMsg(j));
        else
[|                    | ################################################### |] (_ErrMsg(j));
        end;
        j = j + 1;
      end;

      if((ProtocolMode == 0) and (edstunitcheck.Result == 11))

        CalcHashValueMedia   = GetHashStringEDSTUNITCHECK(edstunitcheck, 2);
        CalcHashValueStorage = GetHashStringEDSTUNITCHECK(edstunitcheck, 1);
        LogHashValue         = GetHashStringEDSTUNITCHECK(edstunitcheck, 0);
        RegHashValueMedia    = GetHashStringEDSTUNITCHECK(edstunitcheck, 4);
        RegHashValueStorage  = GetHashStringEDSTUNITCHECK(edstunitcheck, 3);
[|                    | вычисленное  #                                      |] (substr(CalcHashValueMedia  , 1, 32));
[|                    | на носителе  #                                      |] (substr(CalcHashValueMedia  , 33   ));
        if(strlen(strsubst(CalcHashValueStorage, "0", "")) > 0)
[|                    | вычисленное  #                                      |] (substr(CalcHashValueStorage, 1, 32));
[|                    | в хранилище  #                                      |] (substr(CalcHashValueStorage, 33   ));
        end;
[|                    | в журнале    #                                      |] (substr(LogHashValue        , 1, 32));
[|                    |              #                                      |] (substr(LogHashValue        , 33   ));
[|                    | в реестре ЕХ #                                      |] (substr(RegHashValueMedia   , 1, 32));
[|                    | на носителе  #                                      |] (substr(RegHashValueMedia   , 33   ));
        if(strlen(strsubst(RegHashValueStorage, "0", "")) > 0)
[|                    | в реестре ЕХ #                                      |] (substr(RegHashValueStorage , 1, 32));
[|                    | в хранилище  #                                      |] (substr(RegHashValueStorage , 33   ));
        end;
      end;

      if((ProtocolMode == 1) and (edstunitcheck.Result == 7))
        CalcHashValueStorage = GetHashStringEDSTUNITCHECK(edstunitcheck, 1);
        LogHashValue         = GetHashStringEDSTUNITCHECK(edstunitcheck, 0);
        RegHashValueStorage  = GetHashStringEDSTUNITCHECK(edstunitcheck, 3);
[|                    | вычисленное  #                                      |] (substr(CalcHashValueStorage, 1, 32));
[|                    |              #                                      |] (substr(CalcHashValueStorage, 33   ));
[|                    | в журнале    #                                      |] (substr(LogHashValue        , 1, 32));
[|                    |              #                                      |] (substr(LogHashValue        , 33   ));
[|                    | в реестре ЕХ #                                      |] (substr(RegHashValueStorage , 1, 32));
[|                    |              #                                      |] (substr(RegHashValueStorage , 33   ));
      end;

    elif(edstunitcheck.EDocLogID != 0)

      if(PrevEDocLogID == 0)
[----------------------------------------------------------------------------];
[|Ошибки ДЭВ:                                                               |];
[----------------------------------------------------------------------------];
      end;

      StrSplit(edstunitcheck.ErrorMessage, _ErrMsg, 46, 46, 10 );
      while(StrLen(_ErrMsg(j)) > 0)
        if(j == 0)
[| ####################### | ############################################## |] (edstunitcheck.FileName, _ErrMsg(j));
        else
[|                         | ############################################## |] (_ErrMsg(j));
        end;
        j = j + 1;
      end;
      if((ProtocolMode == 0) and ((edstunitcheck.Result == 101) or (edstunitcheck.Result == 102) or (edstunitcheck.Result == 103) or (edstunitcheck.Result == 104)))
[|                         | номер ##########                               |] (edstunitcheck.EDocLogID);
[|                         | файл  #                                        |] (edstunitcheck.FileName);
      end;
      if((ProtocolMode == 0) and (edstunitcheck.Result == 107))
        CalcHashValueMedia   = GetHashStringEDSTUNITCHECK(edstunitcheck, 2);
        CalcHashValueStorage = GetHashStringEDSTUNITCHECK(edstunitcheck, 1);
        RegHashValueMedia    = GetHashStringEDSTUNITCHECK(edstunitcheck, 4);
        RegHashValueStorage  = GetHashStringEDSTUNITCHECK(edstunitcheck, 3);
[|                         | вычисленное  #                                 |] (substr(CalcHashValueMedia  , 1, 32));
[|                         | на носителе  #                                 |] (substr(CalcHashValueMedia  , 33   ));
        if(strlen(strsubst(CalcHashValueStorage, "0", "")) > 0)
[|                         | вычисленное  #                                 |] (substr(CalcHashValueStorage, 1, 32));
[|                         | в хранилище  #                                 |] (substr(CalcHashValueStorage, 33   ));
        end;
[|                         | в реестре ЕХ #                                 |] (substr(RegHashValueMedia   , 1, 32));
[|                         | на носителе  #                                 |] (substr(RegHashValueMedia   , 33   ));
        if(strlen(strsubst(RegHashValueStorage, "0", "")) > 0)
[|                         | в реестре ЕХ #                                 |] (substr(RegHashValueStorage , 1, 32));
[|                         | в хранилище  #                                 |] (substr(RegHashValueStorage , 33   ));
        end;
      end;
      if((ProtocolMode == 1) and ((edstunitcheck.Result == 101) or (edstunitcheck.Result == 102)))
[|                         | номер ##########                               |] (edstunitcheck.EDocLogID);
[|                         | файл  #                                        |] (edstunitcheck.FileName);
      end;
      if((ProtocolMode == 1) and (edstunitcheck.Result == 104))
        CalcHashValueStorage = GetHashStringEDSTUNITCHECK(edstunitcheck, 1);
        LogHashValue         = GetHashStringEDSTUNITCHECK(edstunitcheck, 0);
        RegHashValueStorage  = GetHashStringEDSTUNITCHECK(edstunitcheck, 3);
[|                         | вычисленное  #                                 |] (substr(CalcHashValueStorage, 1, 32));
[|                         |              #                                 |] (substr(CalcHashValueStorage, 33   ));
[|                         | в реестре ЕХ #                                 |] (substr(RegHashValueStorage , 1, 32));
[|                         |              #                                 |] (substr(RegHashValueStorage , 33   ));
      end;
    end;
  end;
end;

private macro _PrintEdStUnitCheck(edstunitcheck)
  if(PrevStoreUnitLogID != edstunitcheck.StoreUnitLogID)
    FileEDSTUNITLOG.StoreUnitLogID = edstunitcheck.StoreUnitLogID;
    if(GetEQ(FileEDSTUNITLOG))
      _PrintEdStUnitLogHeader(FileEDSTUNITLOG);
    end;
  end;

  _PrintEdStUnitCheckLine(edstunitcheck);

  PrevStoreUnitLogID = edstunitcheck.StoreUnitLogID;
  PrevEDocLogID = edstunitcheck.EDocLogID;
  PrevResult = edstunitcheck.Result;
end;

macro PrintProtocol(Mode)

  ProtocolMode = Mode;
  
  _PrintProtocolHeader();
  
  while(Next(FileEDSTUNITCHECK))
    //println(FileEDSTUNITCHECK.StoreUnitLogID, ", ", FileEDSTUNITCHECK.EDocLogID, ", ", FileEDSTUNITCHECK.FileName, ", ", FileEDSTUNITCHECK.ErrorMessage);
    _PrintEdStUnitCheck(FileEDSTUNITCHECK);
  end;

  _PrintEdStUnitLogFooter();

  _PrintProtocolFooter();
end;