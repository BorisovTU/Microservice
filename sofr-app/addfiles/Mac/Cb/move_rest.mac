/*
$Name:        move_rest.mac
$Module:      Главная книга
$Description: Процедура переноса остатков
*/

Import BankInter, OprInter, CurrInter, FIInter, CTInter, "mc_lib.mac", Календарь, rsd;

private file MoveRestParm("c302parm.dbt") write;

private var nStep : integer;

private var RateDate : date;

private var ConvID : integer;
private var SourceBalance : string;
private var TargetBalance : string;

private var OpenSign : bool;

/* глобальные переменные по обработке одного счета */
private record OldAcc("account.dbt");
private record NewAcc("account.dbt");

private var TargetAccount : string;

private var ErrDescription : string;
private var RestAccount : money;
private var ErrorInCarry : bool;

/*
 *  Первичный документ для категорий (лицевой счет)
 */
class PrimDocClass( _accRecord, ParmID )

/* данные класса ---------------------------------------------------- */
  private var accRecord;
  private var ReservePeriodParm;

  private var FIRoleBArray = TArray;

  var Error = 0, Kind, ID;

/* ------------------------------------------------------------------ */

/* вернуть параметр второго рода ------------------------------------ */

  macro GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = accRecord.Department;
    end;

    return Parametr;
  end;

/* вернуть параметр первого рода ------------------------------------ */

  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole )
    var Parametr = -1;

    if( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND )
        if( Trim(StrUpr(accRecord.Kind_Account)) == "А" ) Parametr = 1;
      elif( Trim(StrUpr(accRecord.Kind_Account)) == "П" ) Parametr = 2; end;
    end;

    return Parametr;
  end;

/* остновная роль фИ ------------------------------------------------ */
  macro GetBasisFIRole(FIRole)
    return FIROLE_UNDEF;
  end;

/* вернуть весь базис ролей ----------------------------------------- */
  macro GetFIRoleBArray()
    return FIRoleBArray;
  end;

/* найти/открыть счет ---------------------------------------------- */
  macro FindAndOpenAccount( CatCode : string, DateCarry : date )
    return MC_FindAndOpenCommonAccByFD( CatCode, this, DateCarry, 1, MC_OPENACC_CREATE, null, null, null, null, null, null );
  end;

/* Корректировка счета ---------------------------------------------- */
   MACRO CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
      return true;
   END;

/* Конструктор */
   Kind = DLDOC_ACCOUNT;
   Id   = ParmID;

   accRecord = _accRecord;

   FIRoleBArray[0] = 0;
/* ------------------------------------------------------------------ */

end;

/* инициалазиация параметров обработки одного счета */
private macro InitProccessAccount( account : TBFile )

  ErrDescription = "";
  
  RestAccount = $0;

  ErrorInCarry = false;

  TargetAccount = "";

  Copy( OldAcc, account );

end;

/* определение даты пересчета сумм */
private macro InitRateDate( BankDate : date )

  var RetDate : date;

  if( MoveRestParm.BankDate > date(31,12,2007) )
    RetDate = GetDateAfterWorkDays( date(1,1,2008), -1 );
  else
    RetDate = BankDate;
  end;

  return RetDate;

end;

/* увеличить счетчик на прогресс-баре */
private macro _UseProgress()
  nStep = nStep + 1;
  UseProgress( nStep );
end;


/* сохранить запись в журнале выполнения процедуры */
private macro SaveLog( accRec, TargetAccount : string, Sum : money, Success : bool, ErrorMsg : string )

  file MoveRestLogFile("c302log.dbt") write;

  ClearRecord(MoveRestLogFile);

  MoveRestLogFile.ParmID = MoveRestParm.ParmID;
  MoveRestLogFile.ConvID = ConvID;

  MoveRestLogFile.Department = accRec.Department;

  MoveRestLogFile.SourceAccount = accRec.Account;

  MoveRestLogFile.Chapter = accRec.Chapter;
  MoveRestLogFile.FIID    = accRec.Code_Currency;

  if( not Success )
    
    MoveRestLogFile.ErrorMsg = ErrorMsg;

  else

    MoveRestLogFile.Success = "X";

    MoveRestLogFile.Rest = Sum;

    MoveRestLogFile.TargetAccount = TargetAccount;
    
  end;

  if( not ((Success) and (Index(OldAcc.Type_Account, "П") != 0)) )
    Insert( MoveRestLogFile );
  end;

end;

/* сохранить запись в журнале выполнения процедуры */
private macro SaveLogBalance( Department : integer, Balance : string, ErrorMsg : string )

  file MoveRestLogFile("c302log.dbt") write;

  ClearRecord(MoveRestLogFile);

  MoveRestLogFile.ParmID = MoveRestParm.ParmID;
  MoveRestLogFile.ConvID = ConvID;

  MoveRestLogFile.SourceAccount = Balance;

  MoveRestLogFile.Department = Department;

  MoveRestLogFile.ErrorMsg = ErrorMsg;

  Insert( MoveRestLogFile );

end;

/* сохранить запись в журнале выполнения проводок */
private macro SaveLogCarry( ParmID : integer, accTrn : RsbAccTransaction )

  record MoveRestLog("c302log.dbt");

  file MoveRestLogCarry("c302car.dbt") write;

  ClearRecord( MoveRestLogCarry );

  MoveRestLogCarry.ParmID = ParmID;

  MoveRestLogCarry.AccTrnID = accTrn.AccTrnID;

  Insert( MoveRestLogCarry );

end;

/* получить счет корреспонденции */
private macro _GetCorrespondAccount( accRec, CatCode : string, DateCarry : date, ParmID : integer )

  var PD = PrimDocClass( accRec, ParmID );

  return PD.FindAndOpenAccount( CatCode, DateCarry );

end;

/* получить счета корреспонденции */
private macro GetCorrespondAccounts( ActiveAcc, PassiveAcc, CorrAccountA : @string, CorrAccountP : @string )

  var Success = true;

  if( Success )
    
    CorrAccountA = _GetCorrespondAccount( ActiveAcc, "ВнебалСчетКорресп", MoveRestParm.BankDate, MoveRestParm.ParmID );
    if( CorrAccountA == "" )
      Success = false;
     ErrDescription = "Не определен номер счета для корреспонденции с активными счетами через категорию \"ВнебалСчетКорресп\"";
    end;

  end;

  if( Success )

    CorrAccountP = _GetCorrespondAccount( PassiveAcc, "ВнебалСчетКорресп", MoveRestParm.BankDate, MoveRestParm.ParmID );
    if( CorrAccountP == "" )
      Success = false;
      ErrDescription = "Не определен номер счета для корреспонденции с пассивными счетами через категорию \"ВнебалСчетКорресп\"";
    end;

  end;

  return Success;

end;




/* выполнить проводку */
private macro RunAccTransaction( Chapter : integer, FIID : integer, AccountPayer : string,  AccountReceiver : string, Sum : money, IsFinal : bool )

  var ErrMsg : string;

  var Success = true;

  var SavePayTypeAccount : string, SaveRecTypeAccount : string;

  var accTrn = RsbAccTransaction;

  accTrn.Chapter     = Chapter;
  accTrn.Date_Carry  = MoveRestParm.BankDate;
  
  accTrn.FIID = FIID;
  accTrn.Sum  = Sum;

  accTrn.AccountPayer    = AccountPayer;
  accTrn.AccountReceiver = AccountReceiver;

  accTrn.Ground  = "Проводка переноса остатков";

  /*accTrn.NoEquivalentCarry = true;*/

  accTrn.Department = OldAcc.Department;

  if( IsFinal )
    accTrn.TypeDocument = "З";
  end;

  if( not accTrn.Carry(null, ErrMsg) )
    Success = false;
    ErrorInCarry = true;
    ErrDescription = "Ошибка при выполнении проводки переноса остатка " + string(AccountPayer:f) + " - " + string(AccountReceiver:f) + ": " + ErrMsg;
  end;

  if( Success )

    SaveLogCarry( MoveRestParm.ParmID, accTrn );

  end;

  return Success;

end;

/* выполнить мультивалютную проводку */
private macro RunMCAccTransaction( Chapter : integer
                                  ,FIIDPayer    : integer, AccountPayer    : string, SumPayer    : money
                                  ,FIIDReceiver : integer, AccountReceiver : string, SumReceiver : money
                                  ,ERAccountPlus : string, ERAccountMinus : string, IsFinal : bool )

  var ErrMsg : string;

  var OCP_Account : string;

  var Success = true;

  var PD : PrimDocClass;

  var accTrn = RsbAccTransaction;

  accTrn.Chapter     = Chapter;
  accTrn.Date_Carry  = MoveRestParm.BankDate;
  
  accTrn.FIIDPayer    = FIIDPayer;
  accTrn.AccountPayer = AccountPayer;
  accTrn.SumPayer     = SumPayer;

  accTrn.FIIDReceiver    = FIIDReceiver;
  accTrn.AccountReceiver = AccountReceiver;
  accTrn.SumReceiver     = SumReceiver;

  accTrn.Ground  = "Проводка переноса остатков";

  accTrn.Department = OldAcc.Department;

  if( IsFinal )
    accTrn.TypeDocument = "З";
  end;

  if( Success )

    if( not accTrn.Carry(null, ErrMsg) )
      Success = false;
      ErrorInCarry = true;
      ErrDescription = "Ошибка при выполнении проводки переноса остатка " + string(AccountPayer:f) + " - " + string(AccountReceiver:f) + ": " + ErrMsg;
    end;

    if( Success )

      SaveLogCarry( MoveRestParm.ParmID, accTrn );

    end;

  end;

  return Success;

end;


/* получить разноску балансовых для лицевого счета */
private macro FindACCBLNC( accblncRec, Chapter : integer, Code_Currency : integer, Account : string )

  file accblnc ("accblnc.dbt") key 1;

  ClearRecord( accblnc );

  accblnc.Chapter       = Chapter;
  accblnc.Code_Currency = Code_Currency;
  accblnc.Account       = Account;

  GetEQ( accblnc );

  Copy( accblncRec, accblnc );

end;

/* выполнить корректировку системных типов лицевых счетов */
private macro CorrectTypeAccount( OldTypeAccount : string ) : string

  var NewTypeAccount : string;

  var Symb : string;
  var i : integer, N : integer;

  NewTypeAccount = "";

  i = 0; N = StrLen(OldTypeAccount);
  
  while( i < N )

    i = i + 1;

    Symb = SubStr(OldTypeAccount, i, 1);

    if( Symb != "Ш" )
      NewTypeAccount = NewTypeAccount + Symb;
    end;
    
  end;

  return NewTypeAccount;

end;

/* открыть новый лицевой счет */
private macro OpenTargetAccount()

  record accblnc("accblnc.dbt");

  var Success = true;
  var Note;
  var DateFrom = null;

  ClearRecord( NewAcc );

  NewAcc.Chapter         = OldAcc.Chapter;
  NewAcc.Code_Currency   = OldAcc.Code_Currency;
  NewAcc.Account         = TargetAccount;
  NewAcc.Balance         = TargetBalance;
  NewAcc.Client          = OldAcc.Client;
  NewAcc.Oper            = OldAcc.Oper;
  NewAcc.Type_Account    = CorrectTypeAccount(OldAcc.Type_Account);
  NewAcc.UserTypeAccount = OldAcc.UserTypeAccount;
  NewAcc.Symbol          = OldAcc.Symbol;
  NewAcc.NameAccount     = OldAcc.NameAccount;
  NewAcc.Department      = OldAcc.Department;
  NewAcc.Branch          = OldAcc.Branch;
  NewAcc.ORScheme        = OldAcc.ORScheme;
  NewAcc.ControlOper     = OldAcc.ControlOper;
  NewAcc.Kind_Account    = "";
  NewAcc.Open_Date       = MoveRestParm.BankDate;

  /*перенести параметры НВПИ*/
  if(index(NewAcc.Type_Account, "В") != 0)
    NewAcc.CurrencyEq           = OldAcc.CurrencyEq;
    NewAcc.CurrencyEq_RateDate  = OldAcc.CurrencyEq_RateDate;
    NewAcc.CurrencyEq_RateType  = OldAcc.CurrencyEq_RateType;
    NewAcc.CurrencyEq_RateExtra = OldAcc.CurrencyEq_RateExtra;
  end;
  
  ClearRecord( accblnc );
  
  if( MoveRestParm.BindPlan )
    FindACCBLNC( accblnc, OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account );
  end;

  accblnc.Chapter       = NewAcc.Chapter;
  accblnc.Code_Currency = NewAcc.Code_Currency;
  accblnc.Account       = NewAcc.Account;
  accblnc.Balance0      = NewAcc.Balance;

  if( Create_Account(NewAcc, accblnc, ErrDescription) )
    Success = false;
  else
    /*перенести примечание НВПИ*/
    if(index(NewAcc.Type_Account, "В") != 0)
      Note = readNoteForObject( OBJTYPE_ACCOUNT, UniID(OldAcc, OBJTYPE_ACCOUNT), 31, NewAcc.Open_Date, DateFrom );
      if(Note != null)
        addNoteForObject( OBJTYPE_ACCOUNT, UniID(NewAcc, OBJTYPE_ACCOUNT), 31, Note, NewAcc.Open_Date )
      end;
    end;
  end;

  return Success;

end;

/* изменение привязки к категориям учета */
private macro GetTemplNum( TemplNum : @integer, CatID : integer, SourceTemplNum : integer ) : bool

  var strSQL : string;
  var rs : RsdRecordset;

  strSQL = "SELECT t_Number FROM dmctempl_dbt WHERE t_CatID = " + string(CatID) + " AND t_IsClose = chr(0) AND t_Balance = '" + TargetBalance + "'" +
                                                   " AND (t_Value1, t_Value2, t_Value3, t_Value4, t_Value5, t_Value6, t_Value7, t_Value8) IN " +
                                                   "(SELECT t_Value1, t_Value2, t_Value3, t_Value4, t_Value5, t_Value6, t_Value7, t_Value8" +
                                                   " FROM dmctempl_dbt WHERE t_CatID = " + string(CatID) + " AND t_Number = " + string(SourceTemplNum) + ")";

  rs = RsdRecordset( strSQL );

  if( rs.moveNext() )
    TemplNum = rs.value("t_Number");
    return true;
  end;
  
  return false;

end;

/* изменение привязки к категориям учета */
private macro ProcessMCACCDOC( Chapter : integer, Currency : integer, Account : string ) : bool

  file mccateg("mccateg.dbt") write;
  file mcaccdoc("mcaccdoc.dbt") write;

  var TemplNum : integer;

  var strSQL : string;
  var rs : RsdRecordset;

  var Success = true;

  strSQL = "SELECT t_ID FROM dmcaccdoc_dbt WHERE t_IsUsable = 'X' AND t_Chapter = "  + string(Chapter ) +
                                                                " AND t_Currency = " + string(Currency) +
                                                                " AND t_Account = '" + Account + "'";

  rs = RsdRecordset( strSQL );

  while( (Success) and (rs.moveNext()) )

    ClearRecord( mcaccdoc );

    mcaccdoc.ID = rs.value("t_ID");
    
    if( GetEQ(mcaccdoc) )

     ClearRecord( mccateg );

     mccateg.ID = mcaccdoc.CatID;

     if( GetEQ(mccateg) )
     
       /* определим номер нового шаблона */
       TemplNum = mcaccdoc.TemplNum;
       Success = GetTemplNum( @TemplNum, mcaccdoc.CatID, mcaccdoc.TemplNum );
     
       if( not Success )
         ErrDescription = "В категории учета \"" + mccateg.Code + "\" отсутствует шаблон для балансового счета " + TargetBalance;
       else

         mcaccdoc.IsUsable      = "";
         mcaccdoc.DisablingDate = MoveRestParm.BankDate;

         if( Update(mcaccdoc) )

           mcaccdoc.ID            = 0;
           mcaccdoc.TemplNum      = TemplNum;
           mcaccdoc.Account       = TargetAccount;
           mcaccdoc.IsUsable      = "X";
           mcaccdoc.ActivateDate  = MoveRestParm.BankDate;
           mcaccdoc.DisablingDate = date(0, 0, 0);

           Insert(mcaccdoc);

         end;

       end;

     end;

    end;

  end;

  return Success;

end;

/* обработка счетов 70701 */
private macro ProcessLossProfitAccount()

  var RestProfitAccount : money, RestLossAccount : money;
  var NatCurRest : money, Sum : money, SumAdd : money, OriginalSum : money;

  var Success = true;

  if( (MoveRestParm.ProfitAccount == "") and (MoveRestParm.LossAccount == "") )
    
    Success = false;
    ErrDescription = "Не заданы лицевые счета прибылей и убытков";
  
  else

    RestProfitAccount = $0;
    RestLossAccount   = $0;

    if( MoveRestParm.ProfitAccount != "" )
      RestProfitAccount = RestA( MoveRestParm.ProfitAccount, MoveRestParm.BankDate );
    end;

    if( MoveRestParm.LossAccount != "" )
      RestLossAccount = -RestA( MoveRestParm.LossAccount, MoveRestParm.BankDate );
    end;

    if( OldAcc.Code_Currency == NATCUR )
      NatCurRest = RestAccount;
    else
      if( ConvSum(NatCurRest, RestAccount, RateDate, OldAcc.Code_Currency) )
        Success = false;
        ErrDescription = "Ошибка пересчета остатка исходного лицевого счета в национальную валюту";
      end;
    end;

    if( Success )

      if( RestProfitAccount != $0 )
    
        TargetAccount = MoveRestParm.ProfitAccount;

        if( OldAcc.Kind_Account == "П" )

          if( OldAcc.Code_Currency == NATCUR )
            /* делаем обычную проводку */
            Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account, MoveRestParm.ProfitAccount, RestAccount, true );
          else
            /* делаем мультивалютную проводку */
            Success = RunMCAccTransaction( OldAcc.Chapter
                                          ,OldAcc.Code_Currency, OldAcc.Account, RestAccount
                                          ,NATCUR, MoveRestParm.ProfitAccount, NatCurRest
                                          ,null, null, true );
          end;

          SaveLog( OldAcc, MoveRestParm.ProfitAccount, RestAccount, Success, ErrDescription );

        elif( OldAcc.Kind_Account == "А" )

          if( RestProfitAccount >= NatCurRest )
            Sum    = NatCurRest;
            SumAdd = $0;
          else
            Sum    = RestProfitAccount;
            SumAdd = NatCurRest - RestProfitAccount;
          end;

          if( Sum > $0 )
            
            if( OldAcc.Code_Currency == NATCUR )
              OriginalSum = Sum;
              Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, MoveRestParm.ProfitAccount, OldAcc.Account, Sum, true );
            else
              if( ConvSum(OriginalSum, Sum, MoveRestParm.BankDate, NATCUR, OldAcc.Code_Currency) )
                Success = false;
                ErrDescription = "Ошибка пересчета суммы проводки в национальную валюту за " + string(MoveRestParm.BankDate:f);
              else
                /* делаем мультивалютную проводку */
                Success = RunMCAccTransaction( OldAcc.Chapter
                                              ,NATCUR, MoveRestParm.ProfitAccount, Sum
                                              ,OldAcc.Code_Currency, OldAcc.Account, OriginalSum
                                              ,null, null, true );
              end;
            end;

            SaveLog( OldAcc, MoveRestParm.ProfitAccount, OriginalSum, Success, ErrDescription );

            if( (Success) and (SumAdd > $0) )

              if( MoveRestParm.LossAccount == "" )
            
                Success = false;
                ErrDescription = "Исчерпан остаток по лицевому счету прибылей, лицевой счет убытков не задан";

              else

                if( OldAcc.Code_Currency == NATCUR )
                  OriginalSum = SumAdd;
                  Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, MoveRestParm.LossAccount, OldAcc.Account, SumAdd, true );
                else
                  /* делаем мультивалютную проводку */
                  OriginalSum = -RestAC( OldAcc.Account, OldAcc.Code_Currency, MoveRestParm.BankDate );

                  Success = RunMCAccTransaction( OldAcc.Chapter
                                                ,NATCUR, MoveRestParm.LossAccount, SumAdd
                                                ,OldAcc.Code_Currency, OldAcc.Account, OriginalSum
                                                ,null, null, true );

                end;

                SaveLog( OldAcc, MoveRestParm.LossAccount, OriginalSum, Success, ErrDescription );
              
              end;
          
            end;
        
          end;
        
        end;

      elif( RestLossAccount != $0 )

        TargetAccount = MoveRestParm.LossAccount;

        if( OldAcc.Kind_Account == "А" )

          if( OldAcc.Code_Currency == NATCUR )
            /* делаем обычную проводку */
            Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, MoveRestParm.LossAccount, OldAcc.Account, RestAccount, true );
          else
            /* делаем мультивалютную проводку */
            Success = RunMCAccTransaction( OldAcc.Chapter
                                          ,NATCUR, MoveRestParm.LossAccount, NatCurRest
                                          ,OldAcc.Code_Currency, OldAcc.Account, RestAccount
                                          ,null, null, true );
          end;

          SaveLog( OldAcc, MoveRestParm.LossAccount, RestAccount, Success, ErrDescription );

        elif( OldAcc.Kind_Account == "П" )

          if( RestLossAccount >= NatCurRest )
            Sum    = NatCurRest;
            SumAdd = $0;
          else
            Sum    = RestLossAccount;
            SumAdd = NatCurRest - RestLossAccount;
          end;

          if( Sum > $0 )
            
            if( OldAcc.Code_Currency == NATCUR )
              OriginalSum = Sum;
              /* делаем обычную проводку */
              Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account, MoveRestParm.LossAccount, Sum, true );
            else
              if( ConvSum(OriginalSum, Sum, MoveRestParm.BankDate, NATCUR, OldAcc.Code_Currency) )
                Success = false;
                ErrDescription = "Ошибка пересчета суммы проводки в национальную валюту за " + string(MoveRestParm.BankDate:f);
              else
                /* делаем мультивалютную проводку */
                Success = RunMCAccTransaction( OldAcc.Chapter
                                              ,OldAcc.Code_Currency, OldAcc.Account, OriginalSum
                                              ,NATCUR, MoveRestParm.LossAccount, Sum
                                              ,null, null, true );
              end;
            end;

            SaveLog( OldAcc, MoveRestParm.LossAccount, OriginalSum, Success, ErrDescription );

            if( (Success) and (SumAdd > $0) )
      
              if( MoveRestParm.ProfitAccount == "" )
            
                Success = false;
                ErrDescription = "Исчерпан остаток по лицевому счету убытков, лицевой счет прибылей не задан";

              else

                if( OldAcc.Code_Currency == NATCUR )
                  OriginalSum = SumAdd;
                  Success = RunAccTransaction( OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account, MoveRestParm.ProfitAccount, SumAdd, true );
                else
                  OriginalSum = RestAC( OldAcc.Account, OldAcc.Code_Currency, MoveRestParm.BankDate );
                  /* делаем мультивалютную проводку */
                  Success = RunMCAccTransaction( OldAcc.Chapter
                                                ,OldAcc.Code_Currency, OldAcc.Account, OriginalSum
                                                ,NATCUR, MoveRestParm.ProfitAccount, SumAdd
                                                ,null, null, true );
                end;

                SaveLog( OldAcc, MoveRestParm.ProfitAccount, OriginalSum, Success, ErrDescription );

              end;
              
            end;
          
          end;

        end;

      end;

    end;
  
  end;

  return Success;

end;


/* обработка одного счета */
private macro _ProcessAccount()

  var Success = true;

  var CorrAccountA : string;
  var CorrAccountP : string;

  TargetAccount = "";

  /* определяем остаток счета */
  if( OldAcc.Code_Currency == NATCUR )
    RestAccount = RestA( OldAcc.Account, MoveRestParm.BankDate, null, OldAcc.Chapter );
  else
    RestAccount = RestAC( OldAcc.Account, OldAcc.Code_Currency, MoveRestParm.BankDate, null, OldAcc.Chapter );
  end;

  /* если счет пассивный, то подкорректируем остаток */
  if( OldAcc.Kind_Account == "А") RestAccount = -RestAccount; end;

  if( TargetBalance == "70701" )

    if( RestAccount != $0 )
      if( Index(OldAcc.Type_Account, "П") == 0 )
        Success = ProcessLossProfitAccount( MoveRestParm );
      end;
    end;

  else

    if( (OpenSign) or (MoveRestParm.MoveZeroRest) or (RestAccount != $0) )

      TargetAccount = TargetBalance + SubStr(OldAcc.Account, 6);

      TargetAccount = GetKey(TargetAccount);

      Success = OpenTargetAccount();

      if( Success ) Success = ProcessMCACCDOC( OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account ); end;

    end;

    if( Success )

      if( RestAccount != $0 )

        if( (OldAcc.Kind_Account == "А") AND (NewAcc.Kind_Account == "А") )

          Success = RunAccTransaction( NewAcc.Chapter, NewAcc.Code_Currency, NewAcc.Account, OldAcc.Account, RestAccount );

        elif( (OldAcc.Kind_Account == "П") AND (NewAcc.Kind_Account == "П") )

          Success = RunAccTransaction( NewAcc.Chapter, NewAcc.Code_Currency, OldAcc.Account, NewAcc.Account, RestAccount );

        elif( (OldAcc.Kind_Account == "А") AND (NewAcc.Kind_Account == "П") )

          Success = GetCorrespondAccounts( OldAcc, NewAcc, @CorrAccountA, @CorrAccountP );

          if( Success )

            if( OldAcc.Code_Currency == NATCUR )

              if( Index(OldAcc.Type_Account, "П") == 0 )
                
                Success = RunAccTransaction( OldAcc.Chapter, NATCUR, CorrAccountA, OldAcc.Account, RestAccount );

                if( Success )
              
                  Success = RunAccTransaction( NewAcc.Chapter, NATCUR, CorrAccountP, NewAcc.Account, RestAccount );

                end;

              end;
        
            else

              Success = RunMCAccTransaction( OldAcc.Chapter
                                            ,NATCUR, CorrAccountA, $0
                                            ,OldAcc.Code_Currency, OldAcc.Account, RestAccount
                                            ,CorrAccountA, CorrAccountA );
              if( Success )
              
                Success = RunMCAccTransaction( NewAcc.Chapter
                                              ,NATCUR, CorrAccountP, $0
                                              ,NewAcc.Code_Currency, NewAcc.Account, RestAccount
                                              ,CorrAccountP, CorrAccountP );

              end;

            end;

          end;

        elif( (OldAcc.Kind_Account == "П") AND (NewAcc.Kind_Account == "А") )

          Success = GetCorrespondAccounts( NewAcc, OldAcc, @CorrAccountA, @CorrAccountP );

          if( Success )

            if( OldAcc.Code_Currency == NATCUR )

              if( Index(OldAcc.Type_Account, "П") == 0 )
                
                Success = RunAccTransaction( OldAcc.Chapter, NATCUR, OldAcc.Account, CorrAccountP, RestAccount );

                if( Success )

                  Success = RunAccTransaction( NewAcc.Chapter, NATCUR, NewAcc.Account, CorrAccountA, RestAccount );

                end;

              end;
        
            else

              Success = RunMCAccTransaction( OldAcc.Chapter
                                            ,OldAcc.Code_Currency, OldAcc.Account, RestAccount
                                            ,NATCUR, CorrAccountP, $0
                                            ,CorrAccountP, CorrAccountP );

              if( Success )

                Success = RunMCAccTransaction( NewAcc.Chapter
                                              ,NewAcc.Code_Currency, NewAcc.Account, RestAccount
                                              ,NATCUR, CorrAccountA, $0
                                              ,CorrAccountA, CorrAccountA );
              end;

            end;

          end;

        end;

        SaveLog( OldAcc, NewAcc.Account, RestAccount, Success, ErrDescription );

      end;
    
    end;

  end;

  if( not Success )
    AbortTrn;
  end;

  return Success;

end;

/* обработка одного счета */
private macro ProcessAccount( account : TBFile )

  private record SaveAcc("account.dbt");

  var accountC : TBFile;

  var Connect_Account : string;

  var Success = true;

  InitProccessAccount( account );

  Copy( SaveAcc, account );

  /* запускаем процедуру конвертации */
  Success = ProcessTrn( null, "_ProcessAccount" );

  if( not Success )
  
    SaveLog( SaveAcc, null, null, false, ErrDescription );

  else

    /* закрываем старый лицевой счет, если это не счет покрытия */
      
    if( CB_CloseAccount(OldAcc.Chapter, OldAcc.Code_Currency, OldAcc.Account, MoveRestParm.BankDate, ErrDescription) == 0 )
      if( RestAccount == $0 )
        SaveLog( OldAcc, TargetAccount, RestAccount, true, "" );
      end;
    else
      Success = false;
      SaveLog( SaveAcc, null, null, false, ErrDescription );
    end;
    
  end;

  return Success;

end;

/* обработка одной записи из таблицы соответствия */
private macro ProcessConvRecord( MoveRestParm, rs : RsdRecordset, Department : integer )

  var account  : TBFile;

  var Success : bool;

  var addSQL : string;

  ConvID = rs.value("t_ConvID");
  SourceBalance = rs.value("t_SourceBalance");
  TargetBalance = rs.value("t_TargetBalance");

  if( TargetBalance == "" )
  
    SaveLogBalance( Department, SourceBalance, "Не задан целевой балансовый счет" );

  else

    addSQL = "t_Balance = '" + SourceBalance + "' AND t_Open_Close = chr(0) AND t_Department = " + string(Department);

    /* обрабатываем рублевые счета */
    account = TBFile("account.dbt" );

    account.AddFilter( addSQL );
  
    account.Clear();

    while( account.Next() )

      OpenSign = false;
      Success = ProcessAccount( account, false );

    end;

    account.DropFilter();

  end;

end;

/* обработка записей таблицы соответствия */
private macro ProcessTableRecords( addSQL : string, Department : integer )

  var strSQL : string;
  var rs : RsdRecordset;

  strSQL = "SELECT t_ConvID, t_SourceBalance, t_TargetBalance FROM dc302acc_dbt WHERE t_Active = 'X'";

  if( MoveRestParm.Oper )

    strSQL = strSQL + " AND (t_Oper = 0 OR t_Oper = " + string(MoveRestParm.Oper) + ")";

  end;

  if( Department )

    strSQL = strSQL + " AND (t_Department = 0 OR t_Department = " + string(Department) + ")";

  end;

  if( addSQL != "" )
    strSQL = strSQL + " AND " + addSQL;
  end;

  rs = RsdRecordset( strSQL );

  while( rs.moveNext() )

    _UseProgress();

    rs.Command.NullConversion = true;

    ProcessConvRecord( MoveRestParm, rs, Department );

  end;

end;

/* обработка записец одного филиала
 */
private macro RunMoveAccountRestDepartment( Department : integer )

  var strSQL : string;

  strSQL = "t_TargetBalance <> '70701'";
  ProcessTableRecords( strSQL, Department );

  if( Department == {NumDprt} )

    strSQL = "t_TargetBalance = '70701' " +
             "AND EXISTS (SELECT * FROM dbalance_dbt WHERE t_iNumPlan = 0 AND t_Balance = t_TargetBalance AND t_Kind_Account = 'A')";
    ProcessTableRecords( strSQL, Department );

    strSQL = "t_TargetBalance = '70701' " +
             "AND NOT EXISTS (SELECT * FROM dbalance_dbt WHERE t_iNumPlan = 0 AND t_Balance = t_TargetBalance AND t_Kind_Account = 'A')";
    ProcessTableRecords( strSQL, Department );
  
  end;

end;

/*
 * 
 */
private macro RunMoveAccountRest( _MoveRestParm )

  record MoveRestParmRec("c302parm.dbt");

  var rs : RsdRecordset;

  SetBuff( MoveRestParmRec, _MoveRestParm );

  Copy( MoveRestParm, MoveRestParmRec );

  MoveRestParm.ParmID = 0;
  
  if( not Insert(MoveRestParm) )

    msgbox( "Ошибка вставки записей в таблицу протоколов выполненных переносов" );

  else

    nStep = 0;

    Copy( MoveRestParmRec, MoveRestParm );

    RateDate = InitRateDate( MoveRestParm.BankDate );

    InitProgress( -1, " ~Alt-Break~ Прервать", "Перенос остатков лицевых счетов" );

    if( (MoveRestParmRec.Department != {NumDprt}) or (MoveRestParmRec.Subordinate == "") )

      RunMoveAccountRestDepartment( MoveRestParmRec.Department );

    else

      rs = RsdRecordset( "SELECT t_Code FROM ddp_dep_dbt WHERE t_Status = 2 " +
                         "START WITH t_Code = " + {NumDprt} + " " +
                         "CONNECT BY t_ParentCode = PRIOR t_Code AND t_NodeType = 1"
                       );

      while( rs.moveNext() )
        RunMoveAccountRestDepartment( rs.value("t_Code") );
      end;

    end;

  end;

end;
