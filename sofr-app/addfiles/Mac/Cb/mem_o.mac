import BankInter, FIInter, OprInter, globals, Календарь;

RECORD mo_pan (MEM_O, "userop.lbr") dialog;
FILE     mo_file( "uo_mem_o", "USEROP.DEF" ) write;  /* Пользовательский первичный документ */
file Опер( person );
record svmo_file( "uo_mem_o", "USEROP.DEF" );

private FILE    accR ("account");       /* рублевые лицевые счета */
private FILE    accC ("account$");      /* валютные лицевые счета */
file   oprkoper( oprkoper );
file   ch( obchaptr );
file   dep( dp_dep );
record FI( fininstr );
record AC( account );

VAR
/*        {curdate}, {oper}, {OperDprt},*/
        КодВалюты       = 0,

        KeyEsc = 0;
/*
        Глава           = 1,
        НомерДокумента  = "",
        ДатаПроводки    = {curdate},
        Операция        = 5010,
        Сумма           = $0.00,
        СчетДебет       = "",
        СчетКредит      = "",
        Основание       = "",
        Пачка           = 0;
*/

const
        ПервДокМемордер = 5001;

/************************************************************************/
/*   Функция проверяет наличие лиц. счета                               */
/************************************************************************/
private MACRO   ЛСчетСуществует (Глава, КодВалюты, ЛСчет)
        if (КодВалюты)
                accC.Chapter        = Глава;
                accC.Account        = ЛСчет;
                accC.Code_Currency  = КодВалюты;
                if (GetEQ(accC))
                        Return TRUE;
                else
                        Return FALSE;   
                end;
        else
                accR.Chapter        = Глава;
                accR.Account        = ЛСчет;
                if (GetEQ(accR))
                        Return TRUE;
                else
                        Return FALSE;
                end;
        end;
END;


/*
/************************************************************************/
/*   Функция возвращает ВИД лиц. счета  ()                               */
/************************************************************************/
MACRO   ВидЛСчета (Chapter, FIID, Account)
        if (FIID)
                accC.Chapter        = Chapter;
                accC.Account        = Account;
                accC.Code_Currency  = FIID;
                if (not GetEQ(accC) )
                        Return 0;
                else
                        Return accC.Kind_Account;   
                end;
        else
                accR.Chapter        = Chapter;
                accR.Account        = Account;
                if (not GetEQ(accR))
                        Return 0;
                else
                        Return accR.Kind_Account;
                end;
        end;
END;
*/

/************************************************************************/
/*  Функция проверяет допустимость вида операции для документа данного вида */
/************************************************************************/
macro ПроверитьОперацию( KindOper )
var stat = false;

   oprkoper.Kind_Operation = KindOper;
   keynum( oprkoper, 0 );
   if( getEQ( oprkoper ) )
      if( oprkoper.DocKind == ПервДокМемордер )
         stat = true;
      end;
   end;

   return stat;
end;

/************************************************************************/
/*  Инициализация полей панели по полям первичного документа            */
/************************************************************************/
macro UpdateOtherFields( pn, CMD, ID, KEY )
var  CurrentField = FldName (pn, ID),
     ChangesMade = false;

   if( (CMD == DLG_INIT) or (CurrentField == "Account_Payer") )
      ChangesMade = true;
      if( ЛСчетСуществует( pn.Chapter, КодВалюты, pn.Account_Payer ) )
         if( КодВалюты )
            pn.NameAP = accC.NameAccount;
            pn.Department =  accC.Department;
         else
            pn.NameAP = accR.NameAccount;
            pn.Department =  accR.Department;
         end;
      else
         pn.NameAP = "";
      end;
   end;
   if( (CMD == DLG_INIT) or (CurrentField == "Account_Receiver") )
      ChangesMade = true;
      if( ЛСчетСуществует( pn.Chapter, КодВалюты, pn.Account_Receiver ) )
         if( КодВалюты )
            pn.NameAR = accC.NameAccount;
         else
            pn.NameAR = accR.NameAccount;
         end;
      else
         pn.NameAR = "";
      end;
   end;
   if( (CMD == DLG_INIT) or (CurrentField == "Chapter") )
      ChangesMade = true;
      ch.Chapter = pn.Chapter;
      if( getEQ( ch ) )
         pn.ChapterName = ch.ShortName;
      else
         pn.ChapterName = "";
      end;
   end;
/*
   /* филиал больше не используем */
   if( (CMD == DLG_INIT) or (CurrentField == "Department") )
      ChangesMade = true;
      dep.Code = pn.Department;
      if( getEQ( dep ) )
         pn.NameDepartment = dep.Name;
      else
         pn.NameDepartment = "";
      end;
   end;
*/
   if( CMD == DLG_INIT )
      ChangesMade = true;
      pn.Code_Currency = ПолучитьКодФинИн ( КодВалюты );
   elif( CurrentField == "Code_Currency" )
      ChangesMade = true;
      КодВалюты = ПолучитьКодФинИн ( pn.Code_Currency );
   end;

   return ChangesMade;
end;


/************************************************************************/
/*  Инициализация полей панели по полям первичного документа            */
/************************************************************************/
macro InitPanel( pn, CMD, ID, KEY )
   
   КодВалюты               = mo_file.Code_Currency;
   pn.Numb_Document        = mo_file.Numb_Document;
   pn.Chapter              = mo_file.Chapter;
   pn.Date_Carry           = mo_file.Date_Carry;
   pn.Sum                  = mo_file.Sum;
   pn.Account_Payer        = mo_file.Account_Payer;
   pn.Account_Receiver     = mo_file.Account_Receiver;
   pn.Ground               = mo_file.Ground;
   pn.Number_Pack          = mo_file.Number_Pack;
   pn.Kind_Operation       = mo_file.Kind_Operation;
   pn.Department           = mo_file.Department;

   UpdateOtherFields( pn, CMD, ID, KEY );

   UpdateFields( pn );

   return CM_IGNORE;

end;

/************************************************************************/
/*  Различные листалки (обработка кливиши F3)            */
/************************************************************************/
macro ListPanel( pn, CMD, ID, KEY )
var  CurrentField = FldName (pn, ID);

   /* вызов "листалки" валют */
   if( CurrentField == "Code_Currency" )
      if (ListFI (FIKIND_ALLFI, 0, FI))
         КодВалюты = FI.FIID;
         pn.Code_Currency = FI.FI_Code; /* польз. код валюты (не ID) */
      end;

   /* вызов "листалки" счетов */
   elif( CurrentField == "Account_Payer" )
      if (ListAccount (AC, pn.Chapter, КодВалюты, pn.Account_Payer))
         pn.Account_Payer = AC.Account;
         pn.NameAP = AC.NameAccount;
      end;
   elif( CurrentField == "Account_Receiver" )
      if (ListAccount (AC, pn.Chapter, КодВалюты, pn.Account_Receiver))
         pn.Account_Receiver = AC.Account;
         pn.NameAR           = AC.NameAccount;
      end;

/*
   /* филиал больше не используем */
   elif( CurrentField == "Department" )
      if( ListDepartment( dep ) )
         pn.Department     = dep.Code;
         pn.NameDepartment = dep.Name;
      end;
*/

   elif( CurrentField == "Kind_Operation" )
      oprkoper(0) = 0;
      if( ListKindOper( oprkoper, ПервДокМемордер, false ) )
         pn.Kind_Operation = oprkoper.Kind_Operation;
      end;
   elif( CurrentField == "Chapter" )
      if( ListChapter( ch ) )
         pn.Chapter     = ch.Chapter;
         pn.ChapterName = ch.ShortName;
      end;
   /* вызов календаря */
   elif (CurrentField == "Date_Carry" )
      pn.Date_Carry = GetDateByCalendar( pn.Date_Carry );
   end;
end;

/************************************************************************/
/*  Проверки перед сохранением данных при выходе                        */
/************************************************************************/
macro CheckPanel( pn, CMD, ID, KEY )
var stat = true;

/*
                        /* сохраняем введенные данные */
                        КодВалюты = ПолучитьКодФинИн (mo_pan.Code_Currency);
                        НомерДокумента   = pn.Numb_Document;
                        ДатаПроводки     = pn.Date_Carry   ;
                        Сумма            = pn.Sum          ;
                        СчетДебет        = pn.Account_Payer;
                        СчетКредит       = pn.Account_Receiver;
                        Основание        = pn.Ground       ;
                        Пачка            = pn.Number_Pack  ;
*/
                        /* проверяем введенные данные */
   if (КодВалюты == -1)
      MsgBox("Неверный код валюты");
      SetFocus(pn, FldIndex(pn,"Code_Currency"));
      stat = false;

   elif (pn.Sum <= $0.00)
      MsgBox("Неверная сумма");
      SetFocus(pn, FldIndex(pn,"Sum"));
      stat = false;

   /* Проверки для счета по дебету */
   elif (not ЛСчетСуществует (pn.Chapter, КодВалюты, pn.Account_Payer))
      MsgBox("Счет не найден");
      SetFocus(pn, FldIndex(pn,"Account_Payer"));
      stat = false;

   /* проверяем, что счет НЕ запрещен к дебетованию */
   elif ( ( КодВалюты ) AND ( index (accC.Type_Account, "Т" ) > 0 ) )
         MsgBox("Запрещено дебетование счета плательщика");
         SetFocus(pn, FldIndex(pn,"Account_Payer"));
         stat = false;

   elif ( ( КодВалюты == 0 ) AND ( index (accR.Type_Account, "Т" ) > 0 ) )
       MsgBox("Запрещено дебетование счета плательщика");
       SetFocus(pn, FldIndex(pn,"Account_Payer"));
       stat = false;

/*
   /* проверяем, что счет НЕ покрытие */
   elif (КодВалюты == 0)
      if( index (accR.Type_Account, "П" ) > 0 )
         MsgBox("Проводки по покрытию запрещены");
         SetFocus(pn, FldIndex(pn,"Account_Payer"));
         stat = false;
      end;
*/

   /* Проверки для счета по кредиту */
   elif (not ЛСчетСуществует (pn.Chapter, КодВалюты, pn.Account_Receiver))
      MsgBox("Счет не найден");
      SetFocus(pn, FldIndex(pn,"Account_Receiver"));
      stat = false;

   /* проверяем, что счет НЕ запрещен к кредитованию */
   elif ( ( КодВалюты ) AND ( index (accC.Type_Account, "У" ) > 0 ) )
         MsgBox("Запрещено кредитование счета получателя");
         SetFocus(pn, FldIndex(pn,"Account_Receiver"));
         stat = false;

   elif ( ( КодВалюты == 0 ) AND ( index (accR.Type_Account, "У" ) > 0 ) )
       MsgBox("Запрещено кредитование счета получателя");
       SetFocus(pn, FldIndex(pn,"Account_Receiver"));
       stat = false;

/*
   /* проверяем, что счет НЕ покрытие */
   elif (КодВалюты == 0)
      if ( index (accR.Type_Account, "П" ) > 0)
         MsgBox("Проводки по покрытию запрещены");
         SetFocus(pn, FldIndex(pn,"Account_Receiver"));
         stat = false;
      end;
*/

   elif( not ПроверитьОперацию( pn.Kind_Operation ) )
      MsgBox("Недопустимая операция");
      SetFocus(pn, FldIndex(pn,"Kind_Operation"));
      stat = false;

   end;

   return stat;

end;

/************************************************************************/
/*  Панель ввода первичного пользовательского документа                 */
/************************************************************************/
MACRO МемордерПанель( pn, CMD, ID, KEY )
        VAR
                CurrentField;

        /* начало обработки */
        CurrentField = FldName (pn, ID);
        if( CMD == DLG_INIT )
           return InitPanel( pn, CMD, ID, KEY );

        elif (CMD == DLG_REMFOCUS)
           if( UpdateOtherFields( pn, CMD, ID, KEY ) )
              UpdateFields (pn);
              if( not ПолучитьКодФинИн ( КодВалюты ))
                if(KeyEsc != 1)
                   msgbox("Неверный код валюты");
                end;
                return CM_CANCEL;
              end;
           end;
           return CM_DEFAULT;

        elif (CMD == DLG_KEY)
                if (KEY == 27) 
                        KeyEsc = 1;
                        return CM_CANCEL; 

                elif (KEY == 323)  /* Клавиша F9 */
                        return CM_IGNORE; 

                elif ((KEY == 13) and (ID == FldNumber(pn) - 1)) 
                        SetFocus(pn, 0);
                        return CM_IGNORE; 

                elif (KEY == 317)  /* Клавиша F3 */
                        ListPanel( pn, CMD, ID, KEY );
                        UpdateFields( pn );
                        return CM_IGNORE;
                elif (KEY == 316)  /* Клавиша F2 */
                        if( UpdateOtherFields( pn, CMD, ID, KEY ) )
                           UpdateFields (pn);
                        end;
                        if( CheckPanel( pn, CMD, ID, KEY ) )
                           mo_file.Code_Currency       = КодВалюты;
                           mo_file.Numb_Document      = pn.Numb_Document;
                           mo_file.Chapter            = pn.Chapter;
                           mo_file.Date_Carry         = pn.Date_Carry;
                           mo_file.Sum                = pn.Sum;
                           mo_file.Account_Payer      = pn.Account_Payer;
                           mo_file.Account_Receiver   = pn.Account_Receiver;
                           mo_file.Ground             = pn.Ground;
                           mo_file.Number_Pack        = pn.Number_Pack;
                           mo_file.Kind_Operation     = pn.Kind_Operation;
                           mo_file.Department         = pn.Department;
                           return CM_SAVE;
                        else
                           return CM_IGNORE;
                        end;
                end;
        else
                return CM_DEFAULT;
/*                return CM_IGNORE; */
        end; /* if*/
END;


/************************************************************************/
/* Начало операции                                                      */
/************************************************************************/

Опер.Oper = {oper};
getEQ( Опер );
if ( Опер.CTypePerson == "У")
 MSGBOX("Вам запрещено вызывать эту функцию");
 exit(1);
end;


ClearRecord( svmo_file );
svmo_file.Code_Currency = 0;
svmo_file.Date_Carry = {curdate};
svmo_file.Oper = {oper};
svmo_file.Department = {OperDprt};
svmo_file.Chapter = 1;
svmo_file.Kind_Operation = 5010;

while (true)

    ClearRecord( mo_file );
    mo_file.Code_Currency     = svmo_file.Code_Currency;
    mo_file.Chapter           = svmo_file.Chapter;
    mo_file.Date_Carry        = svmo_file.Date_Carry;
    mo_file.Number_Pack       = svmo_file.Number_Pack;
    mo_file.Kind_Operation    = svmo_file.Kind_Operation;
    if( (strlen(svmo_file.Numb_Document) < 10)
         or ( (strlen(svmo_file.Numb_Document) == 10) and (svmo_file.Numb_Document < "2147483647")) )
       mo_file.Numb_Document  = String(int(svmo_file.Numb_Document) + 1);
    else
       mo_file.Numb_Document  = "1";
    end;

    mo_file.Department  = {OperDprt};
    mo_file.Oper        = {oper};


    /* запускаем панель ввода первичного документа */
    if (RunDialog (mo_pan,"МемордерПанель"))

        /* Запускаем операцию на выполнение */
        RunOperation( ПервДокМемордер, mo_file, TRUE, mo_file.Kind_Operation );
    else
        Exit (1);
    end;

    Copy( svmo_file, mo_file );
end;
END;
