/*
 $Name: mpcchg40.mac
 $Module: RSACCOUNT
 $Description: Балансовый учет
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcchg40.mac

  Библиотека       : RSACCOUNT

  Описание         : Балансовый учет

  Программист      : Kirakozov Michael (KMB)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,CTInter, PTInter,globals, CurrInter, mpcactacc, mpccarry,
       mpclb, "globals.mac";


record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    ContractID = 0,
    DocID = 0,
    TypeDocs = 0, 
    OldTypeDocs = 0,
    CntSchedID = 0;
var TrnMsg = "";

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);


PRIVATE MACRO GetNewNextDateCharge(ContractID, PeriodDate,NewDate:@date):bool
   var   cmd,rs,
         stat = true;

   cmd = RSDCommand ("select  min(T_DATE) as MINDATE from DPRCCNTSCHED_DBT " + 
          "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and T_DATEREAL IS NULL " +
          "and T_DATE <> ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, PeriodDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   
   if (rs.movenext())
      NewDate = rs.mindate;
   else 
      stat = false;
   end;

   if (ValType(NewDate) == V_UNDEF)
      stat = false;
   end;

   return stat;
END;


MACRO ActuateAcc(CatID, Account:@string, FIID:@integer, Chapter:@integer)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID, CatCode, @CodeCurr, @Chapt);
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;
   
   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;


// Транзакционная часть
PRIVATE MACRO DoOperationCarry()
   var Debet="",Credit="",
       DebetCatID = 0, CreditCatID = 0,
       DebetTypeID = 0, CreditTypeID = 0,
       CreditFIID, DebetFIID, ContrFIID,
       DebetDep = 0, CreditDep = 0,
       CarrySum = $0, DebetCarrySum = $0, CreditCarrySum = $0,
       Chapter = 0,
       НазначениеПроводки = "",
       PeriodBeginDate, PeriodEndDate,
       stat = 0,
       TrnMsg1, TrnMsg2, TrnMsg3,
       NewDateCharge, flagNewDateCharge,
       fd;

   record acc_buf (account);

   ContrFIID = prccontract.rec.fiid; 
   GetDatesFromPeriod(ContractID, 2, prccntopr.rec.OperDate, @PeriodBeginDate, @PeriodEndDate);
   
   if (TypeDocs == PRC_ACCSCHEMA_PRIV)
      Chapter = 1;
      DebetTypeID = 3;
      CreditTypeID = 1;
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
      GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
      НазначениеПроводки = "Отнесение на расходы начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
   end;

   if (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL)
      Chapter = 1;
      DebetTypeID = 1;
      CreditTypeID = 3;
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
      GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
      НазначениеПроводки = "Отнесение на доходы начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
   end;
   if ((TypeDocs == PRC_ACCSCHEMA_RAZM_NONBAL) and ({curdate} < ДАТА_ВВОДА))
      Chapter = 3;
      DebetTypeID = 5;
      GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  

      fd = PrcContractFD(ContractID);
      Credit = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        acc_buf,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            /* дата начала действия счета */
                                       );

      CreditFIID = acc_buf.code_currency;                                  
      НазначениеПроводки = "Учет начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;                                 
   end;

   CarrySum = cntsched.rec.SumCalc;
   if (CarrySum == 0)
      return 0;
   end;  

   if ((Debet == "") and (DebetCatID == 0)) 
      TrnMsg = "Не определен счет дебета и не задана соотв.КУ для договора";
      return 1;
   else
      if (Debet == "")
         ActuateAcc(DebetCatID, @Debet, @DebetFIID, @Chapter);
         if (Debet == "")
            TrnMsg = "Не удалось актуализировать счет дебета";
            return 1;
         else
            PrcSetCntAcc(ContractID, DebetTypeID, DebetFIID, Chapter, Debet);
         end;
      end;
   end;

   if ((Credit == "") and (CreditCatID == 0))
      TrnMsg = "Не определен счет кредита и не задана соотв.КУ для договора";
      return 1;
   else
      if (Credit == "")
         ActuateAcc(CreditCatID, @Credit, @CreditFIID, @Chapter);
         if (Credit == "")
               TrnMsg = "Не удалось актуализировать счет кредита";
               return 1;
         else
            PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
         end;
      end;
   end;
   
        

   if (ValType(DebetFIID) == V_UNDEF)
      DebetFIID = ContrFIID;
   end;
   if (ValType(CreditFIID) == V_UNDEF)
      CreditFIID = ContrFIID;
   end;

   if (ValType(DebetFIID) == V_UNDEF)
      TrnMsg = "Не опередлен код валюты дебета";
      return 1;
   end;
   if (ValType(CreditFIID) == V_UNDEF)
      TrnMsg = "Не определен код валюты кредита";
      return 1;
   end;
   
   if (DebetFIID != ContrFIID)
      ConvSum(DebetCarrySum, CarrySum, prccntopr.rec.DocDate, ContrFIID, DebetFIID );
   else
      DebetCarrySum  = CarrySum;
   end;
   if (CreditFIID != ContrFIID)
      ConvSum(CreditCarrySum, CarrySum, prccntopr.rec.DocDate, ContrFIID, CreditFIID );
   else
      CreditCarrySum = CarrySum; 
   end;     

   GetAccountDepartment(Debet, Chapter,DebetFIID, @DebetDep);
   GetAccountDepartment(Credit,Chapter,CreditFIID,  @CreditDep);
   if (DebetDep == 0)
      DebetDep = {OperDprt};
   end;
   if (CreditDep == 0)
      CreditDep = {OperDprt};
   end;
   
   stat = Prc_DocCarry(  
                        Chapter,                // Глава
                        DebetFIID,              // Валюта Дебет
                        CreditFIID,             // Валюта Кредит
                        Debet,                  // Дебет
                        Credit,                 // Кредит
                        money(DebetCarrySum),           // сколько
                        money(CreditCarrySum),
                        prccntopr.rec.DocDate,      // За какое число
                        "09",
                        NULL,
                        0,                       // CarryResult
                        НазначениеПроводки
                       );

      PrcSaveOperDoc(
                        DocID, 
                        1,
                        Chapter,
                        Debet, 
                        DebetDep,
                        DebetFIID, 
                        money(DebetCarrySum),
                        Credit,
                        CreditDep,
                        CreditFIID,
                        money(CreditCarrySum),
                        НазначениеПроводки);

   // ищем дату следующего начисления
   flagNewDateCharge = GetNewNextDateCharge(ContractID, cntsched.rec.date, @NewDateCharge);

   TrnMsg1 = "Рассчитанная сумма начисленных процентов за период " + cntsched.rec.PeriodBeginDate + 
         "-" + cntsched.rec.PeriodEndDate + " - " + CarrySum + ". ";
   TrnMsg2 = "Ошибка отражения в учете. ";
   if (flagNewDateCharge)
      TrnMsg3 = "Дата следующей операции - " + NewDateCharge + "."
   else
      TrnMsg3 = "";
   end;

   // если ошибка, то выведем сообщение прямо сейчас
   TrnMsg = TrnMsg1 + TrnMsg2 + TrnMsg3;


   if (stat != 0)       
       return 1;
   end;      

   if (PrcSetCntSchedFactSumByID(CntSchedID, money(CarrySum), {curdate}, cntsched.rec.DateReal) != 0)
      return 1;
   end;      
  
   if (PrcSetOperSum(DocID, money(CarrySum)) != 0)
      return 1;
   end;

   // если все отработало нормально, то сообщение выведем в макросе постобработки
   TrnMsg = "";
   
   return 0;   
END;



MACRO ExecuteStep(Buffer, Document)
    var ЗначениеНастройки,
    ClientQuality = 0,
    stat = 0;

    SetBuff( cntopr_buf, Document );

    ClearRecord( contract_buf );

    prccntopr.KeyNum = 0;  
    prccntopr.rec.DocID = cntopr_buf.DocID;
    prccntopr.GetEQ;

    DocID       = prccntopr.rec.DocID;
    ContractID  = prccntopr.rec.ContractID;  
    TypeDocs    = prccntopr.rec.TypeDocs;
    OldTypeDocs = TypeDocs;

    prccontract.KeyNum = 0;  
    prccontract.rec.contractid = ContractID;
    if (not prccontract.GetEQ)
        PrcSendMessage(ContractID, "Не могу найти договор с ID = " + ContractID);
        return 1;
    end;

    if ((prccntopr.rec.DocKind == 1050) and (prccntopr.rec.NewCntSchedID > 0))      
        CntSchedID = prccntopr.rec.NewCntSchedID;
    else
        CntSchedID = GetCntSchedID(ContractID, 2, prccntopr.rec.OperDate);
        if (CntSchedID == 0)
            PrcSendMessage(ContractID, "Не найден период начисления");
            return 1;
        end;
    end;

    cntsched.KeyNum = 0;  
    cntsched.rec.CntSchedID = CntSchedID;
    if (not cntsched.GetEQ)
        PrcSendMessage(ContractID, "Не найден период начисления");
        return 1;
    end;
      
    if (prccontract.rec.reskind == PRC_RESKIND_PRIV)    // привлечение
        TypeDocs = PRC_ACCSCHEMA_PRIV; 
    elif (prccontract.rec.reskind == PRC_RESKIND_RAZM)  // размещение
        if ({curdate} < ДАТА_ВВОДА)    
            if (PrcGetClientQualityOnDate(ContractID, cntsched.rec.PeriodEndDate, @ClientQuality) != 0)
                MemoryError(6829);
                DisplayError();
                return 1;
            end;
        
            if ((ClientQuality == 1) or (ClientQuality == 2))         
                TypeDocs = PRC_ACCSCHEMA_RAZM_BAL;      
            elif ((ClientQuality == 4) or (ClientQuality == 5))         
                TypeDocs = PRC_ACCSCHEMA_RAZM_NONBAL;      
            elif (ClientQuality == 3)
                if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CATEG3, @ЗначениеНастройки) != 0)
                    return 1;
                end;
                if (ЗначениеНастройки)
                    TypeDocs = PRC_ACCSCHEMA_RAZM_BAL;
                else
                    TypeDocs = PRC_ACCSCHEMA_RAZM_NONBAL;
                end;
            else
                MemoryError(6829);
                DisplayError();
                return 1;
            end;
        else
            TypeDocs = PRC_ACCSCHEMA_RAZM_BAL; // Размещение.Баланс
        end;
    else  // направление средств не определено
        PrcSendMessage(ContractID, "Для договора не определен доп.параметр 'Направление средств'");
        return 1;
    end;

    PrcSetOperTypeDocs(DocID, TypeDocs, OldTypeDocs);
   
    if (DoOperationCarry() == 1)
        if (TrnMsg != "")
            PrcSendMessage(ContractID, TrnMsg);
        end;
        return 1;
    end;
  
    if (TrnMsg != "")
        PrcSendMessage(ContractID, TrnMsg);
    end;
   
    return 0;
END;


MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

   var TrnMsg1, TrnMsg2, TrnMsg3,
       NewDateCharge, flagNewDateCharge,
       CarrySum = $0;
  
   if (errTrn OR (CommitOrRollback==2)) 
       // Произошла ошибка или происходит откат
      SetBuff( cntopr_buf, FirstDoc );     
      return;
   end;

   SetBuff( cntopr_buf, FirstDoc );

   ClearRecord( contract_buf );

   prccntopr.KeyNum = 0;  
   prccntopr.rec.DocID = cntopr_buf.DocID;
   prccntopr.GetEQ;

   DocID       = prccntopr.rec.DocID;
   ContractID  = prccntopr.rec.ContractID;

   prccontract.KeyNum = 0;	
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      PrcSendMessage(ContractID, "Не найден договор с ID = " + ContractID);
      return;
   end; 

   if ((prccntopr.rec.DocKind == 1050) and (prccntopr.rec.NewCntSchedID > 0))      
      CntSchedID = prccntopr.rec.NewCntSchedID;
   else
      CntSchedID = GetCntSchedID(ContractID, 2, prccntopr.rec.OperDate);
      if (CntSchedID == 0)
         PrcSendMessage(ContractID, "Не найден период начисления");
         return 1;
      end;
   end;

   cntsched.KeyNum = 0;  
   cntsched.rec.CntSchedID = CntSchedID;
   if (not cntsched.GetEQ)
      PrcSendMessage(ContractID, "Не найден период начисления");
      return;
   end;
   
   CarrySum = cntsched.rec.SumCalc;

   // ищем дату следующего начисления
   flagNewDateCharge = GetNewNextDateCharge(ContractID, cntsched.rec.date, @NewDateCharge);

   TrnMsg1 = "Рассчитанная сумма начисленных процентов за период " + cntsched.rec.PeriodBeginDate + 
         "-" + cntsched.rec.PeriodEndDate + " - " + CarrySum + ". ";
   
   TrnMsg2 = "Отражение в учете проведено. ";
   
   if (flagNewDateCharge)
      TrnMsg3 = "Дата следующей операции - " + NewDateCharge + "."
   else
      TrnMsg3 = "";
   end;
   
   TrnMsg = TrnMsg1 + TrnMsg2 + TrnMsg3;
     
    PrcSendMessage(ContractID, TrnMsg);
  
END;


MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

