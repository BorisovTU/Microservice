 /*
 $Name:        cbsumdoc.mac
 $Module: 
 $Description: Макрос скроллинга сводных мемордеров в ББ
 */

import BankInter, PaymInter, PSInter, likepy, CashInter, pmchoper;
import pm_common, pmbuff;

private var P_SUMMER_NUMBER:integer     = 1;
private var P_SUMMER_DATE:integer       = 2;
private var P_SUMMER_GROUND:integer     = 23;
private var P_SUMMER_OPER:integer       = 24;
private var P_SUMMER_DATE_ZNACH:integer = 29;
private var P_SUMMER_D_FI_CODE:integer  = 17;
private var P_SUMMER_C_FI_CODE:integer  = 19;

PRIVATE CONST NOTHING = -1;
PRIVATE CONST YES     =  0; // " Да";          
PRIVATE CONST NO      =  1; // " Нет";         

private CONST SET_CHAR = "X", 
              UNSET_CHAR = "";


// ----------------------------------------------------------------------------
// Обертка для вызова вертикального меню
// ----------------------------------------------------------------------------
PRIVATE MACRO ShowMenu( DebetCredit:integer ):integer
  
  Array Msg;

  if( DebetCredit == 0 )
    Msg(0) = "Ошибка в распределении суммы по дебету. Сохранить?";
  else
    Msg(0) = "Ошибка в распределении суммы по кредиту. Сохранить?";
  end;

  Array Box;
  Box(0) = " Да";
  Box(1) = " Нет";

  return ConfWin( Msg, Box );

END;


private macro CheckOnePI( pi:TRecHandler ):integer
   
   var stat:integer = 0;

   var Department     :integer = 0;
   var Type_Account   :string  = "";
   var Client_Account :integer = -1;
   var DateNoChange   :date = date(0,0,0);
   

   if( СчетСуществуетИОткрыт( pi.rec.FIID, pi.rec.Account, pi.rec.Chapter, Department, Type_Account, Client_Account, DateNoChange ) )
     if(not (Index(Type_Account,"Н") and Index(Type_Account,"П")))  
       if (Index(Type_Account,"Н") or 
           Index(Type_Account,"П") or 
           Index(Type_Account,"М") or 
           Index(Type_Account,"U"))
         msgbox( "Счет " + IfThenElse( pi.rec.DebetCredit == 1, "получателя", "плательщика" ) + " не должен быть счетом покрытия" );
         return 1
       end;
     end;
   end;

   if( PM_FindBalanceInReg_117( "PS\\REQOPENACC\\Счета клиентов", pi.rec.Account, 1 ) )
     msgbox( "Счет " + IfThenElse( pi.rec.DebetCredit == 1, "кредита ", "дебета " ) + pi.rec.Account + " не является внутрибанковским" );
     return 1;
   end;
   if( pi.rec.Amount <= 0 )
     msgbox( "Не задана сумма уточняющей записи к счету " + pi.rec.Account );
   end;

   return stat;

end;

private macro CheckPIList( PIList:RsbPIPayment, ListAccount:string, SumPmAmount:money ):integer
  
  var stat:integer = 0;
  var stat_get:integer = 0;
  var pi  :TRecHandler = TRecHandler( "pmaddpi.dbt", "bank.def");

  var LA  :string = "";
  var Sum :money  = 0.0;
  
  if( ( PIList.Size() > 0 ) and ( PIList.First() == 0 ) )

    stat_get = PIList.Current( pi );
    if( stat_get == 0 )
      
      LA = LA + pi.rec.Account;
      Sum = Sum + pi.rec.PmAmount;

      stat = CheckOnePI( pi );
    end;

    while( ( PIList.Next() == 0 ) and ( stat == 0 ) and ( stat_get == 0 ) )
       stat_get = PIList.Current( pi );
       if( stat_get == 0 )

         LA = LA + " " + pi.rec.Account;
         Sum = Sum + pi.rec.PmAmount;

         stat = CheckOnePI( pi );
       end;
    end;

  end;

  if( stat == 0 )
    SetParm( 1, LA );
    SetParm( 2, Sum );
  end;
  
  return stat;

end;

PRIVATE MACRO Проверить_Документ( Режим ):integer
  
  var ZeroDate = date( 0, 0, 0 );
  
  var stat:integer = 0;
  var Choice:integer = NOTHING;

  var D_ListAccount:string = "";
  var C_ListAccount:string = "";
  var SumDebet:money       = 0.0;
  var SumCredit:money      = 0.0;

  var RsbSumMemorder : RsbSummaryMemorialOrder = RsbSummaryMemorialOrder(r_pmpaym.PaymentID);

  var Debet_PI  : RsbPIPayment = RsbSumMemorder.PIList( 0 );
  var Credit_PI : RsbPIPayment = RsbSumMemorder.PIList( 1 );

  var DLeng:integer = Debet_PI.Size(), CLeng:integer = Credit_PI.Size();


  if( ( Режим == 3 ) and ( r_pmpaym.PaymStatus != PM_PREPARING ) and ( r_pmpaym.PaymStatus != PM_REJECTED ) )        
    msgbox( "Редактирование разрешено только для отложенных и отвергнутых документов." );
    return 1;
  end;

  if( ( Режим == 3 ) and ( ( not FindSfInvLnkByPaymentID( r_pmpaym.PaymentID ) ) or ( ( r_pmpaym.FeeType != 0 ) and ( r_pmpaym.DefComID != 0 ) ) ) )
    msgbox( "Запрещена корректировка реквизитов документа, являющегося оплатой ПЗО" );
    return 1;
  end;


  if( ((Режим == 2 ) or (Режим == 3) or (Режим == 8)) )

  if( r_pmrmprop.Number == "" )
    msgbox( "Не заполнено поле ""Номер документа""" );
    return P_SUMMER_NUMBER;
  end;
  
  if( r_pmrmprop.Date == ZeroDate )
    msgbox( "Не заполнено поле ""Дата""" );
    return P_SUMMER_DATE;
  end;

  if( r_pmrmprop.Ground == "" )
    msgbox( "Не заполнено поле ""Основание""" );
    return P_SUMMER_GROUND;
  end;

  if( r_pmpaym.Oper == 0 )
    msgbox( "Не заполнено поле ""Операционист""" );
    return P_SUMMER_OPER;
  end;

  if( r_pmpaym.ValueDate == ZeroDate )
    msgbox( "Не заполнено поле ""Дата значения""" );
    return P_SUMMER_DATE_ZNACH;
  end;

  if( r_pmpaym.FIID == -1 )
    msgbox( "Не заполнено поле ""Валюта дебета""" );
    return P_SUMMER_D_FI_CODE;
  end;

  if( r_pmpaym.PayFIID == -1 )
    msgbox( "Не заполнено поле ""Валюта кредита""" );
    return P_SUMMER_C_FI_CODE;
  end;

  if( ( DLeng > 1 ) and ( Cleng > 1 ) )
    msgbox( "Документ не может быть сводным по дебету и кредиту одновременно" );
    return 1;
  end;

  if( ( DLeng <= 1 ) and ( CLeng <= 1 ) )
    msgbox( "Документ не является сводным" );
    return 1;
  end;


  stat = CheckPIList( Debet_PI, D_ListAccount, SumDebet );

  if( stat == 0 )
    stat = CheckPIList( Credit_PI, C_ListAccount, SumCredit );
  end;

  if( PM_FindBalanceInReg_117( "PS\\REQOPENACC\\Счета клиентов", IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, r_pmpaym.PayerAccount, r_pmpaym.ReceiverAccount ), 1 ) )
    msgbox( "Счет " + IfThenElse( r_pmpaym.IsFixAmount == UNSET_CHAR, "кредита ", "дебета " ) + IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, r_pmpaym.PayerAccount, r_pmpaym.ReceiverAccount ) + " не является внутрибанковским" );
    return 1;
  end;

  if( stat == 0 )
    if( index( IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, C_ListAccount, D_ListAccount ),
               IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, r_pmpaym.PayerAccount, r_pmpaym.ReceiverAccount )
             )
      )
      msgbox( IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, 
                         "Счет по дебету найден в списке счетов по кредиту", 
                         "Счет по кредиту найден в списке счетов по дебету"  ) );
      return 1;
    end;
  end;

  if( stat == 0 )
    
    if( ( ( r_pmpaym.IsFixAmount == SET_CHAR   ) and ( r_pmpaym.Amount    != SumCredit  ) ) or 
        ( ( r_pmpaym.IsFixAmount == UNSET_CHAR ) and ( r_pmpaym.PayAmount != SumDebet   ) )
      )
       
       Choice = ShowMenu( IfThenElse( r_pmpaym.IsFixAmount == SET_CHAR, 0, 1 ) );
       
       if( Choice == NO )
         stat = 1;
       end;

    end;
  end;

  end;

  if( Режим == 1 )
    if(CheckDeletePayment(r_pmpaym.PaymentID))
      return 1;
    end;
  end;

  if(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperSummaryMemorialOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old);
  end;

  return stat;

END;
 
PRIVATE MACRO Новый_Документ():integer
  var stat:integer = 0;
  return stat;
END;

PRIVATE MACRO  Функция_Пользователя( Режим:integer ):integer
  var stat:integer = 0;
  return stat;
END;

/* Установка подсказки оптимизатору ORACLE */

private const Hint_ByPaymStatus:string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx16) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByValueDate :string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx11) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByCloseDate :string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx15) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByStep      :string = 
"/*+FIRST_ROWS LEADING(t oproper pmpaym pmrmprop) INDEX(t doprstep_dbt_idx10) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper pmpaym pmrmprop)*/";

PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolState:integer ):string
  
  /* Возможные значения ScrolState
        -1 - все
         0 - отложенные
       100 - отвергнутые
      1000 - открытые, подготовленные
     32000 - закрытые
  */

  /* Отложенные, отвергнутые, открытые */
  if( ( ScrolState == 0 ) or 
      ( ScrolState == 100 ) or 
      ( ( ScrolState == 1000 ) and ( StrUpr( TableName ) == "DPMPAYM_DBT" ) ) )

    return Hint_ByPaymStatus;

  /* Все, закрытые */
  elif( ( ScrolState ==    -1 ) or 
        ( ScrolState == 32000 ) )

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByPaymStatus;
    end;

  /* Подготовленные */
  elif( ( ScrolState == 1000 ) and ( StrUpr( TableName ) == "DOPRSTEP_DBT" ) )

    return Hint_ByStep;
  
  end;

  return DefaultHint;

END;
