 /*
 $Name: ws_persnplacework_panel.mac
 $Module: ws_persnplacework_panel.mac
 $Description: Запись места работы
 */

import oralib;
import PTInter;
import "commonutil.mac";
import "ws_datafilter.mac";
import "ws_validation.mac";
import "ws_person.mac";
import "ws_llvalues.mac";
import "cb_sql.mac";



class TWebPersnPlaceWorkGridItem
  var ID       :Integer;
  var PartyID  :Integer;
  var PlaceWork:String;  
  var IsMain   :bool;
  var Post     :string;
end;

class TWebPersnPlaceWork

  var ID           :Integer;
  var PartyID      :Integer;
  var PlaceWork    :string;
  var IsMain       :Bool;
  var Post         :string;
  var OGRN         :string;
  var INN          :string;
  var PostTypeList :TArray;
  var Date         :Date;
  
  PostTypeList = TArray();

end;

private const NullDate = Date("0001-01-01Z");

private macro LoadRsbErrors(responseBuilder : WebResponseBuilder)

  var objErr = AL_GetErrorInfo();
  responseBuilder.AddErrorEx(objErr.stat, MessageSeverity.RuntimeError, objErr.Message);
  
end;

private macro SeekPosition(rsbppw : RsbPersonPlaceWork, pos : integer) : bool

  if(not rsbPpw.First()) return false; end;
  
  if(rsbppw.id == pos)   return true;  end;  
  
  while(rsbppw.Next())
    if(rsbppw.ID == pos) return true;  end;
  end;
  
  return false;
  
end;


macro WebCore_GetWebPersnPlaceWorkGridItemList(PartyID : integer) // TWebPersnPlaceWorkGridItem[]

  var result = TArray();
  
  CaptureOutput;
  [
    SELECT (CASE
               WHEN t.t_post IS NULL
               THEN
                  (SELECT d.t_name
                     FROM dllvalues_dbt d
                    WHERE d.T_ELEMENT = t.t_PostType AND d.t_list = 4043)
               ELSE
                  t.t_post
            END)
              AS PST,
           t.*
      FROM dpersnplacework_dbt t
     WHERE t.t_partyid = :PartyId; 
  ];
  var query = StopCaptureOutput;
  
  var params = TArray;
  
  params[params.size] = SQLParam("PartyId", PartyID);
  
  var rs = execSQLSelect(query, params);
  
  while(rs.movenext)
    var ppwGItm = TWebPersnPlaceWorkGridItem();
    
    ppwGItm.ID        = SQL_ConvType(rs.value("t_ID"));
    ppwGItm.PartyID   = SQL_ConvType(rs.value("t_PartyID"));
    ppwGItm.PlaceWork = SQL_ConvType(rs.value("t_PlaceWork"));
    ppwGItm.IsMain    = SQL_ConvTypeBool(rs.value("t_IsMain"));
	ppwGItm.Post      = SQL_ConvType(rs.value("PST"));
   
    result[result.size] = ppwGItm;
  end;
  
  return result;

end;

macro WebCore_GetWebPersnPlaceWork(PartyID :integer, RecID :integer) // TWebPersnPlaceWork

  CaptureOutput();
  [
    SELECT *
      FROM (SELECT t.T_ID,
                   t.T_PARTYID,
                   t.T_PLACEWORK,
                   t.T_ISMAIN,                   
                   t.T_OGRN,
                   t.T_INN,
                   t.T_POST,
                   t.T_POSTTYPE,
                   t.T_DATE
              FROM dpersnplacework_dbt t
             WHERE t.t_PartyID = :PartyID AND t.t_ID = :RecID) tt,
           (SELECT d.T_LIST,
                   d.T_ELEMENT,
                   d.T_CODE,
                   d.T_NAME
              FROM dllvalues_dbt d
             WHERE d.t_list = 4043) dd
     WHERE dd.t_element(+) = tt.T_POSTTYPE;
  ];
  var query = StopCaptureOutput();
  
  var params = TArray();
  params[params.size] = SQLParam("PartyID", PartyID);
  params[params.size] = SQLParam("RecID", RecID);
  
  var rs = execSQLSelect(query, params);
  
  var ppwGItm : TWebPersnPlaceWork;
  
  if(rs.movenext)
    
    ppwGItm = TWebPersnPlaceWork();
    
    ppwGItm.ID        = SQL_ConvType(rs.value("t_ID"));
    ppwGItm.PartyID   = SQL_ConvType(rs.value("T_PARTYID"));
    ppwGItm.PlaceWork = SQL_ConvType(rs.value("T_PLACEWORK"));
    ppwGItm.IsMain    = SQL_ConvType(rs.value("T_ISMAIN"));
    ppwGItm.OGRN      = SQL_ConvType(rs.value("T_OGRN"));
    ppwGItm.INN       = SQL_ConvType(rs.value("T_INN"));
    ppwGItm.Post      = SQL_ConvType(rs.value("T_POST"));
    ppwGItm.Date     = SQL_ConvTypeDate(rs.value("T_DATE"));
    
    var llv = CreateLLValueFromParam(
                SQL_ConvType(rs.value("T_ELEMENT")),
                SQL_ConvType(rs.value("T_CODE")),
                SQL_ConvType(rs.value("T_NAME")),
                SQL_ConvType(rs.value("T_LIST"))
              );    

    ppwGItm.PostTypeList[ppwGItm.PostTypeList.Size] = llv;
    
  end;
  
  return ppwGItm;
end;


macro WebCore_PT_DeletePersnPlaceWork(PartyID : integer, RecID):WebServiceResponse


  var responseBuilder = WebResponseBuilder();

  var party  = RsbParty(PartyID);   
  var rsbPpw = party.PlaceWorkObj();
  
  if(not SeekPosition(rsbPpw, RecID))
    LoadRsbErrors(responseBuilder);
    return responseBuilder.Result();
  end;
  
  var stat = RsbPpw.Delete();
  
  var stat2 = party.Update();
  
  if(not (stat or stat2))
    LoadRsbErrors(responseBuilder);
  end;
  
  responseBuilder.SetUserObject((stat or stat2));
  
  return responseBuilder.Result();
  
end;

/// .Partyid - обязательный параметр
macro WebCore_PT_InsUpdPersnPlaceWork (PersnPlaceWork ):WebServiceResponse

  var responseBuilder = WebResponseBuilder();
  
  if(PersnPlaceWork.PartyID == null)
    responseBuilder.AddErrorEx(-1, MessageSeverity.RuntimeError, "Не указан PartyID" );
    return responseBuilder.Result();
  end; 
  
  var party = RsbParty(PersnPlaceWork.PartyId);
  var rsbPpw = party.PlaceWorkObj();
  
  if((PersnPlaceWork.ID == null) or (PersnPlaceWork.ID <= 0)) 
    /* insert */
    var sts = rsbPpw.Add();
    
    if(not sts)
      LoadRsbErrors(responseBuilder);
      return responseBuilder.Result();
    end;
  else 
    /* update */
    SeekPosition(rsbPpw, PersnPlaceWork.ID);
  end;
  
  rsbPpw.PlaceWork = CU_IIF(PersnPlaceWork.PlaceWork == null, "", PersnPlaceWork.PlaceWork);
  rsbPpw.OGRN      = CU_IIF(PersnPlaceWork.OGRN      == null, "", PersnPlaceWork.OGRN);
  rsbPpw.INN       = CU_IIF(PersnPlaceWork.INN       == null, "", PersnPlaceWork.INN);
  rsbPpw.Post      = CU_IIF(PersnPlaceWork.Post      == null, "", PersnPlaceWork.Post);
  if(PersnPlaceWork.PostTypeList == null)
    rsbPpw.PostType  = 0;
  else
    rsbPpw.PostType = PersnPlaceWork.PostTypeList[0].Element;
  end;

  if(not (PersnPlaceWork.Date == NullDate))
    rsbPpw.Date      = PersnPlaceWork.Date;
  end;
  
  if(PersnPlaceWork.IsMain)
    rsbPpw.SetIsMain(true);
  end;
  
  var stat = party.Update();
  
  if(not stat)
    LoadRsbErrors(responseBuilder);
  end;
  
  responseBuilder.SetUserObject(stat);
  
  return responseBuilder.Result();

end;


macro WebCore_PT_SetIsMainPlaceWork(PersnPlaceWork) : WebServiceResponse
  
  var responseBuilder = WebResponseBuilder();
  
  var party = RsbParty(PersnPlaceWork.PartyID);
  var rsbPpw = party.PlaceWorkObj();  
  
  if(SeekPosition(rsbPpw, PersnPlaceWork.id))
    
    var stat:integer = rsbppw.SetIsMain(true);
    
    if(stat != 0)
      LoadRsbErrors(responseBuilder);
    end;
    
    responseBuilder.SetUserObject(stat == 0);
  else
    responseBuilder.AddErrorEx(-1, MessageSeverity.RuntimeError, "Объект не найден id=" + PersnPlaceWork.Id + " party " + PersnPlaceWork.PartyID);
    responseBuilder.SetUserObject(false);
  end;
  
  return responseBuilder.Result();
  
end;

macro WebCore_PT_CheckObjCode(codekind, code, objectid):WebServiceResponse
  
  var responseBuilder = WebResponseBuilder();
  var stat = CB_CheckObjCode(3, objectid, codekind, code, 1);
  
  if(stat != 0)
    LoadRsbErrors(responseBuilder);
  end;
  
  responseBuilder.SetUserObject(stat == 0);
  return responseBuilder.Result();

end;










/* **** TESTs **** */
// 


// Insert;
// var wppw = TWebPersnPlaceWork();
// wppw.partyID = 11;

// var r1 = WebCore_PT_InsUpdPersnPlaceWork(wppw);



/* Update*/
// var party =  RsbParty(11);
// var ppw = party.PlaceWorkObj();

// ppw.last();

// wppw.PartyId   = party.partyId;
// wppw.id        = ppw.id;
// wppw.PartyId   = 11;
// wppw.id        = 24;
// wppw.PlaceWork = "tst_plcWrk";
// wppw.Post      = "iiiiiiiiiiiiii";
// wppw.IsMain = true;
// wppw.OGRN = 11354564;
// wppw.INN = 5466546;

// var r2 = WebCore_PT_InsUpdPersnPlaceWork(wppw);



// /* Delete*/
// var party2 =  RsbParty(11);
// var ppw2 = party2.PlaceWorkObj();

// ppw2.last();

// var r3 =  WebCore_PT_DeletePersnPlaceWork(party2.PartyID, ppw2.ID);


////////////////////////////////////////

// var r4 = WebCore_GetWebPersnPlaceWork(11, 22);


// var r5 = WebCore_GetWebPersnPlaceWorkGridItemList(11);


// [##]("nope");
// var _c = "7";

/* ***TESTs*** */
