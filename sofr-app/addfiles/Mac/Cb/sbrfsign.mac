/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*     Формирование блока целевой информации по сообщению RS-Bank           */
/*                  Криптосистема: Ключевания СБ РФ                         */
/*                                                                          */
/*  Имя файла: sbrfsign.mac                                                 */
/*  Создан:    29.08.05                                      Алешин А.В.    */
/****************************************************************************/

import MesInter, "cbsbsign.mac", "wlglobal.mac", rsd, oralib, likepy;

/* Формирование строки сообщения в формате СБРФ3 */
macro CreateSBRF3Str( MesID:integer, SBRF3Str:string  )
  var rs;
  var tmpstr = SBRF3Str;
  var select:string;
  var params:TArray;

  select = "select wltpfld.t_Name, wlmesval.t_Value"+ 
                           " from dwltpfld_dbt wltpfld, dwlmesval_dbt wlmesval, dwlmes_dbt wlmes"+
                           " where wlmes.t_MesID =:MesID"+
                           " AND wlmes.t_MesID = wlmesval.t_MesID"+
                           " AND wlmesval.t_TpFieldID = wltpfld.t_TpFieldID"+
                           " order by wlmesval.t_Index";
  params = makeArray( SQLParam("MesID", MesID) );
  rs = execSQLselect( select, params, FALSE );

  while( rs.MoveNext() )
     if( rs.value(0) == SB_Tag_PIB ) /* Хранение всех полей сообщения в одном тэге */
       tmpstr = tmpstr + rs.value(1);
     else
       tmpstr = tmpstr + MakeFieldSBRF3( rs.value(0), rs.value(1), false, false );
     end;
  end;

  SetParm( 1, tmpstr );

  return true;
end;

/* Функция формирования блока целевой информации по сообщению RS-Bank'а
   RsMs        - сконструированный объект класса RsbMessage 
   SourceAttrs - набор атрибутов, участвующих в формированиии БЦИ 
   Возвращает блок данных для формата SBRF3 */
macro GenPIB( RsMs:RsbMessage, SourceAttrs:string )
  var SBRF3Str:string = "", PIB:string = "";

  /* Определяем форму сообщения */
  if( not WlDefineForm( RsMs.RlsFormID ) )
    MsgBox( String("Не найдена форма сообщения, RlsFormID = ", RsMs.RlsFormID) );
    return 1;
  end;

  /* Формируем строку собщения в формате SBRF3 */
  SBRF3Str = f_wlmesfrm.Name + RsMs.TRN;

  /* Последовательно считываем поля сообщения */
  if( not CreateSBRF3Str( RsMs.MesID, SBRF3Str ) )
    return 1;
  end;

  /* Формируем блок данных для ключевания - это и есть БЦИ */
  if( not CB_FormingDataInit( RsMs.OutsideAbonentID, SBRF3Str, PIB ) )
    return 1;
  end;

  return PIB; /* Успешное завершение */
end;

// Функция формирования БЦИ в массовом режиме
macro MassSmfrSource()
  var fsgn:TBFile = TBFile( "sgnmass.tmp", "w", 0 );
  var SBRF3Str:string = "", PIB;
  var rsms, err, text;

  fsgn.AddFilter( "t.t_Macro='sbrfsign.mac' and t.t_MacroFunc='SmfrSource'" );

  ClearRecord( fsgn );

  var stat:bool = fsgn.GetGE();

  while( stat )
    /* Конструируем объект сообщение */
    rsms = RsbMessage( int(fsgn.rec.DocID) );
    
    PIB = GenPIB( rsms );
    
    if( PIB != 1 )
      fsgn.rec.PBI = PIB;
      stat = fsgn.Update();
      err = Status(text);
      if( not stat )
        MsgBox( text );
      end;
    end;
    stat = fsgn.Next();
  end;
  
  fsgn.DropFilter();
  return 1; // без ошибок
end;

/* Функция формирования блока целевой информации 
   Работает как в обычном так и в массовом режиме в зависимости от IsMass */
macro SmfrSource( RsMs:RsbMessage, SourceAttrs:string )
  var IsMass:bool = false;

  if( getparm(2,IsMass) and (IsMass == true) ) // массовый режим
    MassSmfrSource();
    return 1; // без ошибок
  else
    return GenPIB( RsMs, SourceAttrs );
  end;

  OnError(er) /* обработка ошибок */
    SB_ExeptionMessage(er);
    if(IsMass == true)
      return 0;
    else
      return 1;
    end;
end;
