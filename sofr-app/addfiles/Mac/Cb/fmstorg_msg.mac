/*
 $Name: fmstorg_msg.mac
 $Module: Ядро ГКБО 
 $Description: Сообщения в ФСФМ по стратегическим организациям
 */
 /*
	Сообщения в ФСФМ по стратегическим организациям
 */
import BankInter, PTInter, CTInter, rsd, oralib, likepy, globals;
import FMInter, "fmreqi_lib.mac", "FmXmlWriter.mac", "serviceaction.mac";

class SidStruct
	var First;
	var Second;
end;
   
private record fmstorgmsg(fmstorgmsg);
private record fmstorgakkred(fmstorgakkred);
private file MessageXMLFile() txt write;
private var InfoSIDMap = TArray;
private var FormatDateResult;

private macro FormatDateToName(indate)
	var cmd = RsdCommand("SELECT TO_CHAR(?,'YYYYMMDD') FROM dual");
	cmd.AddParam ("", RSDBP_IN, indate);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(0);
end;

private macro ExtractYear(indate)
	var cmd = RsdCommand("SELECT TO_CHAR(EXTRACT(YEAR FROM ?)) FROM dual");
	cmd.AddParam ("", RSDBP_IN, indate);
	var rs = RsdRecordset(cmd);
	rs.MoveNext();
	
	return rs.value(0);
end;

private macro FormatDateHandler(str)
    FormatDateResult = FormatDateResult + str;
end;

private macro FillLeaderZero(str, needlen)
	var s = str;
	
	if (strlen(s) < needlen)
		var i = 0;
		while (i < needlen - strlen(s))
			s = "0" + s;
		end;
	end;
	
	return s;
end;

private macro Clear3ZerosInString(str)
	var f = true;
	var i : integer = 1;
	
	var tmp : string = str;
	
	while (f != false)
		var sub = SubStr(tmp, 1, 1);
		if (sub != "0")
			f = false;
		else
			tmp = SubStr(tmp, 2);
		end;
		
		if (i < 3)
			i = i + 1;
		else
			f = false;
		end;
	end;
	
	return tmp;
end;

// Формирование идентификатора электронного сообщения
private macro ComputeMsgId(fmstorgmsg, n)
	// gggg_nReg_nnnF_mmmmmmmm
	var gggg = ExtractYear(fmstorgmsg.EMsgDate);
	var nReg = FillLeaderZero(fmstorgmsg.KGRKO_BPtNumber, 4);
	var nnnF = FillLeaderZero(fmstorgmsg.BPtFilialNumber, 4);
	var mmmm = FillLeaderZero(String(n + 1), 8);
	
	return String(gggg + "_" + nReg + "_" + nnnF + "_" + mmmm);
end;

private macro FormatDate(indate, splch)
    var day, mon, year, ch = "/";
    DateSplit (Date(indate), day, mon, year);
    
    if (ValType(splch) == V_STRING)
        ch = splch;
    end;
    
    if (ValType(day) == V_UNDEF)
        day = 0;
    end;
    
    if (ValType(mon) == V_UNDEF)
        mon = 0;
    end;
    
    if (ValType(year) == V_UNDEF)
        year = 0;
    end;
    
    FormatDateResult = "";
    SetOutHandler (@FormatDateHandler);
    
    print(day:2:o);
    print(ch);
    print(mon:2:o);
    print(ch);
    print(year:4:o);
    
    SetOutHandler();
    return FormatDateResult;
end;

// Определение текущего порядкового номера для идентификатора сообщения
private macro ComputeMsgNumber(fmstorgmsg)
	var n1 = 0, n2 = 0;
	
	var cmd = RsdCommand("SELECT to_number(substr (AK.t_InfoSID,16,8)) As MMM FROM " + 
		"DFMSTORGMSG_DBT msg, DFMSTORG_DBT st, DFMSTORGAKKRED_DBT ak " +
		"WHERE EXTRACT(YEAR FROM MSG.T_EMSGDATE) = EXTRACT(YEAR FROM ?) " +
		"AND MSG.T_SENDBANKBIC = ? AND MSG.T_ID = ST.T_MSGID  " +
		"AND AK.T_STORGID = st.T_ID AND ROWNUM = 1 ORDER BY MMM DESC");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.EMsgDate);	
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.SENDBANKBIC);
	
	var rs = RsdRecordset(cmd);
	if (rs.MoveNext())
		if (rs.value(0) != NULLVAL)
			n1 = Int(rs.value(0));
		end;
	end;
	
	var cmd2 = RsdCommand("SELECT to_number(substr (AK.t_InfoSID,16,8)) As MMM FROM " + 
		"DFMSTORGMSG_DBT msg, DFMSTORG_DBT st, DFMSTORGEMITENT_DBT ak " +
		"WHERE EXTRACT(YEAR FROM MSG.T_EMSGDATE) = EXTRACT(YEAR FROM ?) " +
		"AND MSG.T_SENDBANKBIC = ? AND MSG.T_ID = ST.T_MSGID  " +
		"AND AK.T_STORGID = st.T_ID AND ROWNUM = 1 ORDER BY MMM DESC");
	cmd2.NullConversion = true;
	cmd2.AddParam ("", RSDBP_IN, fmstorgmsg.EMsgDate);	
	cmd2.AddParam ("", RSDBP_IN, fmstorgmsg.SENDBANKBIC);
	
	var rs2 = RsdRecordset(cmd2);
	if (rs2.MoveNext())
		if (rs2.value(0) != NULLVAL)
			n2 = Int(rs2.value(0));
		end;
	end;
	
	return Max(n1,n2);
end;

private macro ИдентифИнфКОTeg(ID, InfoSID, pInfoSID : @TArray, ИнфАккредитив, fmstorgmsg)
    var n = 0;
	
	if (strlen(fmstorgmsg.MsgFileName) <= 0)
		n = ComputeMsgNumber(fmstorgmsg);
	end;
    
    if (strlen(InfoSID) <= 0)
		var sid = ComputeMsgId(fmstorgmsg, n);
		ИнфАккредитив.addChildEx("ИдентифИнфКО", sid);
			
		var size = pInfoSID.size;
		pInfoSID[size] = SidStruct;
		pInfoSID[size].First  = ID;
		pInfoSID[size].Second = sid;
			
		n = n + 1;
	else
		ИнфАккредитив.addChildEx("ИдентифИнфКО", InfoSID);
	end;
end;

private macro GetNNNComponent(msg)
	var cmd = RsdCommand("SELECT TO_NUMBER (SUBSTR(T_MSGFILENAME, 30, 3)) AS NNN, T_MSGFILENAME " + 
				"FROM dfmstorgmsg_dbt WHERE T_SENDBANKBIC = ? AND T_EMSGDATE = ? AND " +
				"(LENGTH (T_MSGFILENAME) > 0 AND T_MSGFILENAME IS NOT NULL AND T_MSGFILENAME <> CHR (1)) " + 
				"ORDER BY NNN DESC ");
	cmd.NullConversion = true;
				
	cmd.AddParam ("", RSDBP_IN, msg.SENDBANKBIC);
	cmd.AddParam ("", RSDBP_IN, msg.EMsgDate);
	var rs = RsdRecordset(cmd);
	
	var nnn = 1;
	if (rs.MoveNext())
		if (rs.value(0) != NULLVAL)
			nnn = Int(rs.value(0)) + 1;
		end;
	end;
	
	return nnn;
end;

macro СлужЧасть(StOrgMsg, node : FmXmlNodeElement)
	node.addChildEx("ВерсФорм", "1.1");
	node.addChildEx("ВерсПрог", "RS-Bank " + _GetDBVersion());
	node.addChildEx("ТипИнф",     "ИНФРФМ");
	node.addChildEx("БИККОНапрв", StOrgMsg.SendBankBIC);
	node.addChildEx("ДатаСооб", FormatDate(StOrgMsg.EMsgDate));
	node.addChildEx("УполнСотрудн", StOrgMsg.ProxyPost);
	
	var fio = node.addChild("ФИОУполнСотрудн", StOrgMsg.ProxyPost);
	fio.addChildEx("Фам", StOrgMsg.ProxyName1);
	fio.addChildEx("Имя", StOrgMsg.ProxyName2);
	fio.addChildEx("Отч", StOrgMsg.ProxyName3);
	
	node.addChildEx("ТелУполнСотрудн", StOrgMsg.ProxyPhone);
end;

macro СведИспБанкОсновной(KGRKO_BPtShortName, BPtFilialShortName, KGRKO_BPtNumber, BPtFilialNumber, BPtBIC, node : @FmXmlNodeElement)
	var НаимКО : string = KGRKO_BPtShortName;
	if (strlen(BPtFilialShortName) > 0)
		НаимКО = НаимКО + ", филиал " + BPtFilialShortName;
	end;
	
	node.addChildEx("НаимКО", НаимКО);
	node.addChildEx("РегНомКО", Clear3ZerosInString(KGRKO_BPtNumber));
	node.addChildEx("НомФл", Clear3ZerosInString(BPtFilialNumber));
	node.addChildEx("БИККО", BPtBIC);
end;

macro СведИспБанк(fmstorgmsg, node : @FmXmlNodeElement)
    СведИспБанкОсновной(fmstorgmsg.KGRKO_BPtShortName,
        fmstorgmsg.BPtFilialShortName,
        fmstorgmsg.KGRKO_BPtNumber,
        fmstorgmsg.BPtFilialNumber,
        fmstorgmsg.BPtBIC, node);
end;

private macro AppentAttribute(str, Attribute)
    var tmp = str;
    if (strlen(tmp))
        tmp = tmp + ",";
    end;
    tmp = tmp + Attribute;
    
    SetParm (0, tmp);
end;

private macro СтатусАккредитив(fmstorgmsg, akkred : @RsdRecordset, node : FmXmlNodeElement)
    var СтатусАккр = node.addChild("СтатусАккр");
    СтатусАккр.addChildEx("КодАккредитив", fmstorgmsg.ObjectState);
    
    // в случае открытия счета
    if (fmstorgmsg.ObjectState == FM_STOR_ACC_OPEN)
        СтатусАккр.addChildEx("ДатаАккредитив", FormatDate(akkred.value("T_AkkredDate")));
    end;
    
    // в случае изменения аккредитива
    if (fmstorgmsg.ObjectState == FM_STOR_ACC_CHANGE)
        СтатусАккр.addChildEx("ДатаИзмАккредитив", FormatDate(akkred.value("T_AkkredDate")));
    end;
    
    // в случае закрытия счета
    if (fmstorgmsg.ObjectState == FM_STOR_ACC_CLOSE)
        СтатусАккр.addChildEx("ДатаЗакАккредитив", FormatDate(akkred.value("T_AkkredDate")));
    end;
end;

private macro АккредитивОснИзм(fmstorgmsg, akkred : @RsdRecordset, node : FmXmlNodeElement)
    if ((akkred.value("T_Akk_IsAkkredChanged") == "X") or (fmstorgmsg.BE_IsBankChanged == "X")
        or (akkred.value("T_SO_IsStOrgChanged") == "X") or (akkred.value("T_Akk_IsBankChanged") == "X")
        or (akkred.value("T_Akk_IsReceiverChanged") == "X")
    )
    //begin
        var ОснИзмАккредитива = node.addChild("АккредитивОснИзм");
        
        var ПризнакИзмАккредитив = "";
        if (akkred.value("T_Akk_IsAkkredChanged") == "X")
            AppentAttribute(ПризнакИзмАккредитив, "1");
        end;
        
        if (fmstorgmsg.BE_IsBankChanged == "X")
            AppentAttribute(ПризнакИзмАккредитив, "2");
        end;
        
        if (akkred.value("T_SO_IsStOrgChanged") == "X")
            AppentAttribute(ПризнакИзмАккредитив, "3");
        end;
        
        if (akkred.value("T_Akk_IsBankChanged") == "X")
            AppentAttribute(ПризнакИзмАккредитив, "4");
        end;
        
        if (akkred.value("T_Akk_IsReceiverChanged") == "X")
            AppentAttribute(ПризнакИзмАккредитив, "5");
        end;
        
        ОснИзмАккредитива.addChildEx("ПризнакИзмАккредитив", ПризнакИзмАккредитив);
        
        ОснИзмАккредитива.addChildEx("ИзмСведБанкЭмитент", fmstorgmsg.BE_BankReasonChange);
        ОснИзмАккредитива.addChildEx("ИзмСведОбщества", akkred.value("T_SO_StOrgReasonChange"));
        ОснИзмАккредитива.addChildEx("ИзмСведИспБанк", akkred.value("T_Akk_BankReasonChange"));
        ОснИзмАккредитива.addChildEx("ИзмСведПолучатель", akkred.value("T_Akk_ReceiverReasonChange"));
    end;
end;

private macro ИнфАккрДоИзм(fmstorgmsg, akkred : @RsdRecordset, node : FmXmlNodeElement)
    var ИнфоАккредитив = node.addChild("ИнфАккрДоИзм");
    
    ИнфоАккредитив.addChildEx("НомАккредитив", akkred.value("T_Akk_Number"));
    ИнфоАккредитив.addChildEx("КодВал", akkred.value("T_Akk_ISO_Number"));
    ИнфоАккредитив.addChildEx("СумАккредитив", akkred.value("T_Akk_Sum"));
    ИнфоАккредитив.addChildEx("СрокАккредитив", FormatDate(akkred.value("T_Akk_EndDate")));
    ИнфоАккредитив.addChildEx("СумРубАккредитив", akkred.value("T_Akk_NatCurSum"));
end;

private macro СведИспБанкДоИзм(fmstorgmsg, akkred : @RsdRecordset, node : FmXmlNodeElement)
    var element = node.addChild("СведИспБанкДоИзм");

    fmstorgakkred.KGRKO_BPtShortName = akkred.value("T_Akk_KGRKO_BPtShortName");
    fmstorgakkred.BPtFilialShortName = akkred.value("T_Akk_BPtFilialShortName");
	fmstorgakkred.KGRKO_BPtNumber = akkred.value("T_Akk_KGRKO_BPtNumber");
	fmstorgakkred.BPtFilialNumber = akkred.value("T_Akk_BPtFilialNumber");
	fmstorgakkred.BPtBIC = akkred.value("T_Akk_BPtBIC");
	СведИспБанк(fmstorgakkred, element);
end;

private macro ФизЛицоТэг(node : FmXmlNodeElement, Surname, Name, Patronymic)
    var ФизЛицо = node.addChild("ФизЛицо");
    ФизЛицо.addChildEx("Фам", Surname);
    ФизЛицо.addChildEx("Имя", Name);
    ФизЛицо.addChildEx("Отч", Patronymic);
end;

private macro НаимПолучательТэг(node : FmXmlNodeElement,  ReceiverName, ReceiverFilialName, Surname, Name, Patronymic)
    var НаимПолучатель = node.addChild("НаимПолучатель");
    
    var str = String(ReceiverName);	
	if (strlen(ReceiverFilialName) > 0)
		str = str + ", филиал " + ReceiverFilialName;
	end;
    
    if (strlen(ReceiverName) > 0)
        НаимПолучатель.addChildEx("ЮрЛицо", str);
    else
        ФизЛицоТэг(НаимПолучатель, Surname, Name, Patronymic);
    end;
end;

private macro ПолучАккрТэг(node : FmXmlNodeElement, ReceiverType, ResidentSign, ReceiverName, ReceiverFilialName, ReceiverINN, Surname, Name, Patronymic)
	node.addChildEx("ТипПолучатель", ReceiverType);
	node.addChildEx("ПризнРез", ResidentSign);
    
    НаимПолучательТэг(node, ReceiverName, ReceiverFilialName, Surname, Name, Patronymic);
	node.addChildEx("ИННПолучатель", ReceiverINN);
end;

private macro ПолучАккрДоИзм(fmstorgmsg, akkred : @RsdRecordset, node : FmXmlNodeElement)
    var element = node.addChild("ПолучАккрДоИзм");
    ПолучАккрТэг(element, akkred.value("T_Akk_ReceiverType"), akkred.value("T_Akk_ResidentSign"),
        akkred.value("T_Akk_ReceiverName"), akkred.value("T_Akk_ReceiverFilialName"), akkred.value("T_Akk_ReceiverINN"),
        akkred.value("T_Akk_Surname"), akkred.value("T_Akk_Name"), akkred.value("T_Akk_Patronymic"));
end;

private macro FillИнфАккредитив(fmstorgmsg, rs : @RsdRecordset, ИнфАккредитив : FmXmlNodeElement)
    ИдентифИнфКОTeg(rs.value("T_ID"), rs.value("T_InfoSID"), InfoSIDMap, ИнфАккредитив, fmstorgmsg);
    ИнфАккредитив.addChildEx("ПризнакИнф", rs.value("T_InfoDirection"));
    СтатусАккредитив(fmstorgmsg, rs, ИнфАккредитив);
    
    // Элементы XML-узла <ИнфАккр> и их значения
    var ИнфАккр = ИнфАккредитив.addChild("ИнфАккр");
	ИнфАккр.addChildEx("НомАккредитив", rs.value("T_Number"));
	ИнфАккр.addChildEx("КодВал", rs.value("T_ISO_Number"));
	ИнфАккр.addChildEx("СумАккредитив", rs.value("T_Sum"));
	ИнфАккр.addChildEx("СрокАккредитив", FormatDate(rs.value("T_EndDate")));
	ИнфАккр.addChildEx("СумРубАккредитив", rs.value("T_NatCurSum"));
    
    var СвИспБанк = ИнфАккредитив.addChild("СведИспБанк");
		
	fmstorgakkred.KGRKO_BPtShortName = rs.value("T_KGRKO_BPtShortName");
	fmstorgakkred.BPtFilialShortName = rs.value("T_BPtFilialShortName");
	fmstorgakkred.KGRKO_BPtNumber = rs.value("T_KGRKO_BPtNumber");
	fmstorgakkred.BPtFilialNumber = rs.value("T_BPtFilialNumber");
	fmstorgakkred.BPtBIC = rs.value("T_BPtBIC");
	СведИспБанк(fmstorgakkred, СвИспБанк);
		
	var ПолучАккр = ИнфАккредитив.addChild("ПолучАккр");
    ПолучАккрТэг(ПолучАккр, rs.value("T_ReceiverType"), rs.value("T_ResidentSign"),
        rs.value("T_ReceiverName"), rs.value("T_ReceiverFilialName"), rs.value("T_ReceiverINN"),
        rs.value("T_Surname"), rs.value("T_Name"), rs.value("T_Patronymic"));
    ПолучАккрДоИзм(fmstorgmsg, rs, ИнфАккредитив);
 
    АккредитивОснИзм(fmstorgmsg, rs, ИнфАккредитив);
    ИнфАккрДоИзм(fmstorgmsg, rs, ИнфАккредитив);
    СведИспБанкДоИзм(fmstorgmsg, rs, ИнфАккредитив);
end;

private macro СоздатьНаимОбщMain(OrgName, FilialName)
	var str = OrgName;
	
	if (strlen(FilialName) > 0)
		str = str + ", филиал " + FilialName + " ";
	end;
	
	return str;
end;

private macro СоздатьНаимОбщ(fmstorg : @RsdRecordset)
	return СоздатьНаимОбщMain(fmstorg.value("T_OrgName"), fmstorg.value("T_FilialName"));
end;

private macro ИнфОбществоBody(ИнфОбщNode, НаимОбщ, OGRN, INN, KPP, RegDate, OKATO, Rayon, Place, Street, House, Corpus, Office, ExtraInfo, InfoTagName, AdrTagName)
    var InfoTag = "СведОбщ", AdrTag = "АдрОбщ";
    
    if (ValType(InfoTagName) == V_STRING)
        InfoTag = InfoTagName;
    end;
    
    if (ValType(AdrTagName) == V_STRING)
        AdrTag = AdrTagName;
    end;
    
    var СведОбщ = ИнфОбщNode.addChild(InfoTag);
	СведОбщ.addChildEx("НаимОбщ", НаимОбщ);
	СведОбщ.addChildEx("ОГРНОбщ", OGRN);
	СведОбщ.addChildEx("ИННОбщ", INN);
	СведОбщ.addChildEx("КППОбщ", KPP);
	//СведОбщ.addChildEx("ДатаРегОбщ", RegDate);
    
    var АдрОбщ = ИнфОбщNode.addChild(AdrTag);
	АдрОбщ.addChildEx("КодСубъектаПоОКАТО", OKATO);
	АдрОбщ.addChildEx("Район", Rayon);
	АдрОбщ.addChildEx("Пункт", Place);
	АдрОбщ.addChildEx("Улица", Street);
	АдрОбщ.addChildEx("Дом", House);
	АдрОбщ.addChildEx("Корп", Corpus);
	АдрОбщ.addChildEx("Оф", Office);
    
    if (strlen(ExtraInfo) != 0)
        ИнфОбщNode.addChildEx("Коммент", ExtraInfo);
    end;
end;

macro ИнфАккредитивБезИзмен(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
    var isFirst = true;
    var ИнфОбщ;
    
	var cmd = RsdCommand("SELECT * FROM DFMSTORGAKKRED_DBT WHERE T_STORGID = ? AND T_SO_ISSTORGCHANGED = CHR(0)");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
        if (isFirst)
            ИнфОбщ = node.addChild("ИнфОбщество");
            ИнфОбществоBody(ИнфОбщ, СоздатьНаимОбщ(fmstorg), fmstorg.value("T_OGRN"), fmstorg.value("T_INN"), fmstorg.value("T_KPP"),
                FormatDate(fmstorg.value("T_RegDate")), fmstorg.value("T_OKATO"), fmstorg.value("T_Rayon"), fmstorg.value("T_Place"),
                fmstorg.value("T_Street"), fmstorg.value("T_House"), fmstorg.value("T_Corpus"), fmstorg.value("T_Office"), fmstorg.value("T_EXTRAINFO"));
            isFirst = false;
        end;
		var ИнфАккредитив = ИнфОбщ.addChild("ИнфАккредитив");
		FillИнфАккредитив(fmstorgmsg, rs, ИнфАккредитив);
	end;
end;

macro ИнфАккредитивИзмен(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
	var cmd = RsdCommand("SELECT * FROM DFMSTORGAKKRED_DBT WHERE T_STORGID = ? AND T_SO_ISSTORGCHANGED = CHR(88)");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
        var ИнфОбщ = node.addChild("ИнфОбщество");
        
        var Name = СоздатьНаимОбщMain(rs.value("T_SO_OrgName"), rs.value("T_SO_FilialName"));
        ИнфОбществоBody(ИнфОбщ, Name, fmstorg.value("T_OGRN"), fmstorg.value("T_INN"), fmstorg.value("T_KPP"),
            FormatDate(fmstorg.value("T_RegDate")), fmstorg.value("T_OKATO"), fmstorg.value("T_Rayon"), fmstorg.value("T_Place"),
            fmstorg.value("T_Street"), fmstorg.value("T_House"), fmstorg.value("T_Corpus"), fmstorg.value("T_Office"), fmstorg.value("T_EXTRAINFO"));
        
		var ИнфАккредитив = ИнфОбщ.addChild("ИнфАккредитив");
		FillИнфАккредитив(fmstorgmsg, rs, ИнфАккредитив);	

        ИнфОбществоBody(ИнфОбщ, Name, rs.value("T_SO_OGRN"), rs.value("T_SO_INN"), rs.value("T_SO_KPP"),
            FormatDate(fmstorg.value("T_RegDate")), rs.value("T_SO_OKATO"), rs.value("T_SO_Rayon"), rs.value("T_SO_Place"),
            rs.value("T_SO_Street"), rs.value("T_SO_House"), rs.value("T_SO_Corpus"), rs.value("T_SO_Office"), fmstorg.value("T_EXTRAINFO"),
            "СведОбщДоИзм", "АдрОбщДоИзм");
	end;
end;

macro ПолучКодЦенБумаг(rs)
	var cmd = RsdCommand("select T_CODE from DLLVALUES_DBT where T_LIST = 1212 and T_ELEMENT = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, rs.value("T_FMSecurityType"));
	
	var code = "";
	var lrs = RsdRecordset(cmd);
	
	if (lrs.moveNext())
		code = lrs.value("T_CODE");
	end;
	
	return code;
End;

macro ИнфЦенБумаг(EmitentID, node : FmXmlNodeElement) 
	var cmd = RsdCommand("SELECT * FROM DFMSTORGEMITSECUR_DBT WHERE T_EMITENTID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, EmitentID);
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
		var ИЦенБумаг = node.addChild("ИнфЦенБумаг");
		
		ИЦенБумаг.addChildEx("КодЦенБумаг", ПолучКодЦенБумаг(rs));
		ИЦенБумаг.addChildEx("НомГосРегЦенБумаг", rs.value("T_IssueRegNumber"));
		ИЦенБумаг.addChildEx("КодВал", rs.value("T_NominalISO_Number"));
		ИЦенБумаг.addChildEx("КолЦенныхБумаг", rs.value("T_Quantity"));
		ИЦенБумаг.addChildEx("КодВалЦена", rs.value("T_PurchaseISO_Number"));
		ИЦенБумаг.addChildEx("ЦенаПриобретения", rs.value("T_Sum"));
		ИЦенБумаг.addChildEx("ЦенаРубПриобретения", rs.value("T_NatCurSum"));
	end;
end;

macro ИнфЭмитент(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)	
	var cmd = RsdCommand("SELECT * FROM DFMSTORGEMITENT_DBT WHERE T_STORGID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
		var ИнфЭмитент = node.addChild("ИнфЭмитент");
		ИдентифИнфКОTeg(rs.value("T_ID"), rs.value("T_InfoSID"), InfoSIDMap, ИнфЭмитент, fmstorgmsg);
		
		ИнфЭмитент.addChildEx("ПризнакИнф", rs.value("T_InfoDirection"));
        ИнфЭмитент.addChildEx("КодСостЦенБумаги", rs.value("T_SecurState"));
		ИнфЭмитент.addChildEx("ДатаПриобретения", FormatDate(rs.value("T_PurchaseDate")));
		
		var ЦенБумагиСвед = ИнфЭмитент.addChild("ЦенБумагиСвед");
		ЦенБумагиСвед.addChildEx("ТипЭмитент", rs.value("T_Type"));
		ЦенБумагиСвед.addChildEx("НаимЭмитент", rs.value("T_Name"));
		ЦенБумагиСвед.addChildEx("КодЭмитент", rs.value("T_Number"));
		
		ИнфЦенБумаг(rs.value("T_ID"), ЦенБумагиСвед);
	end;
end;

private macro СчетСтатус(fmstorgmsg, rs : @RsdRecordset, node : FmXmlNodeElement)
    var СчетСтатус = node.addChild("СчетСтатус");
    СчетСтатус.addChildEx("КодСостоянияСчета", fmstorgmsg.ObjectState);
    
    var DateTag;
    if(fmstorgmsg.ObjectState = FM_STOR_ACC_OPEN)
        DateTag = "ДатаСчета";
    elif (fmstorgmsg.ObjectState = FM_STOR_ACC_CHANGE)
        DateTag = "ДатаИзмСчета";
    else
        DateTag = "ДатаЗакСчета";
    end;
    СчетСтатус.addChildEx(DateTag, FormatDate(rs.value("T_AccountDate")));
end;

private macro ИнфоСчет(fmstorgmsg, rs : @RsdRecordset, node : FmXmlNodeElement)
    var ИнфоСчет = node.addChild("ИнфоСчет");
    
    ИнфоСчет.addChildEx("ДатаДоговор", FormatDate(rs.value("t_DepoAccContrBeginDate")));
    ИнфоСчет.addChildEx("НомДоговор", rs.value("t_DepoAccContrNumber"));
    ИнфоСчет.addChildEx("ВидСчета", rs.value("t_DepoAccKind"));
    ИнфоСчет.addChildEx("НомерСчета", rs.value("t_DepoAccountNumber"));
    
    if (rs.value("t_DepoAccContrEndDate") != ZeroValue(V_DATE))
        ИнфоСчет.addChildEx("ДатаРасторжения", FormatDate(rs.value("t_DepoAccContrEndDate")));
    end;
end;

private macro СчетОснИзм(fmstorgmsg, rs : @RsdRecordset, node : FmXmlNodeElement)
    var СчетОснИзм = node.addChild("СчетОснИзм");
    
    var ПризнакИзмСчета = "";
    if (rs.value("t_Acc_IsAccChanged") == "X")
        AppentAttribute(ПризнакИзмСчета, "1");
    end;
    
    if (fmstorgmsg.BE_IsBankChanged == "X")
        AppentAttribute(ПризнакИзмСчета, "2");
    end;
    
    if (rs.value("t_SO_IsStOrgChanged") == "X")
        AppentAttribute(ПризнакИзмСчета, "3");
    end;
    
    СчетОснИзм.addChildEx("ПризнакИзмСчета", ПризнакИзмСчета);
    СчетОснИзм.addChildEx("ИзмСведКОНФО", fmstorgmsg.BE_BankReasonChange);
    СчетОснИзм.addChildEx("ИзмСведОбщества", rs.value("t_SO_StOrgReasonChange"));
end;


private macro FillИнфСчетДепо(fmstorgmsg, rs : @RsdRecordset, ИнфСчетДепо : FmXmlNodeElement)
    ИдентифИнфКОTeg(rs.value("T_ID"), rs.value("T_InfoSID"), InfoSIDMap, ИнфСчетДепо, fmstorgmsg);
    ИнфСчетДепо.addChildEx("ПризнакИнф", rs.value("T_InfoDirection"));
    
    СчетСтатус(fmstorgmsg, rs, ИнфСчетДепо);
    ИнфоСчет(fmstorgmsg, rs, ИнфСчетДепо);
    
    if ((rs.value("t_Acc_IsAccChanged") == "X") or (fmstorgmsg.BE_IsBankChanged) or (rs.value("t_SO_IsStOrgChanged") == "X"))
        СчетОснИзм(fmstorgmsg, rs, ИнфСчетДепо);
    end;
    
    ИнфСчетДепо.addChildEx("НомерСчетаДоИзм", rs.value("T_Acc_AccountNumber"));
end;

private macro ИнфСчетДепоБезИзмен(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
    var isFirst = true;
    var ИнфОбщ;
    
	var cmd = RsdCommand("SELECT * FROM DFMSTORGDEPOACC_DBT WHERE T_STORGID = ? AND T_SO_ISSTORGCHANGED = CHR(0)");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
        if (isFirst)
            ИнфОбщ = node.addChild("ИнфОбщество");
            ИнфОбществоBody(ИнфОбщ, СоздатьНаимОбщ(fmstorg), fmstorg.value("T_OGRN"), fmstorg.value("T_INN"), fmstorg.value("T_KPP"),
                FormatDate(fmstorg.value("T_RegDate")), fmstorg.value("T_OKATO"), fmstorg.value("T_Rayon"), fmstorg.value("T_Place"),
                fmstorg.value("T_Street"), fmstorg.value("T_House"), fmstorg.value("T_Corpus"), fmstorg.value("T_Office"), fmstorg.value("T_EXTRAINFO"));
            isFirst = false;
        end;
        
        var ИнфСчетДепо = ИнфОбщ.addChild("ИнфСчетДепо");
        FillИнфСчетДепо(fmstorgmsg, rs, ИнфСчетДепо);		
	end;
end;

macro ИнфСчетДепоИзмен(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
	var cmd = RsdCommand("SELECT * FROM DFMSTORGDEPOACC_DBT WHERE T_STORGID = ? AND T_SO_ISSTORGCHANGED = CHR(88)");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorg.value("T_ID"));	
	
	var rs = RsdRecordset(cmd);
	while (rs.moveNext())
        var ИнфОбщ = node.addChild("ИнфОбщество");

        var Name = СоздатьНаимОбщMain(fmstorg.value("T_OrgName"), fmstorg.value("T_FilialName"));
        ИнфОбществоBody(ИнфОбщ, Name, fmstorg.value("T_OGRN"), fmstorg.value("T_INN"), fmstorg.value("T_KPP"),
            FormatDate(fmstorg.value("T_RegDate")), fmstorg.value("T_OKATO"), fmstorg.value("T_Rayon"), fmstorg.value("T_Place"),
            fmstorg.value("T_Street"), fmstorg.value("T_House"), fmstorg.value("T_Corpus"), fmstorg.value("T_Office"), fmstorg.value("T_EXTRAINFO"));
        
		var ИнфСчетДепо = ИнфОбщ.addChild("ИнфСчетДепо");
		FillИнфСчетДепо(fmstorgmsg, rs, ИнфСчетДепо);	

        ИнфОбществоBody(ИнфОбщ, Name, rs.value("T_SO_OGRN"), rs.value("T_SO_INN"), rs.value("T_SO_KPP"),
            FormatDate(fmstorg.value("T_RegDate")), rs.value("T_SO_OKATO"), rs.value("T_SO_Rayon"), rs.value("T_SO_Place"),
            rs.value("T_SO_Street"), rs.value("T_SO_House"), rs.value("T_SO_Corpus"), rs.value("T_SO_Office"), fmstorg.value("T_EXTRAINFO"),
            "СведОбщДоИзм", "АдрОбщДоИзм");
	end;
end;

private macro ИнфЭмитентИнфОбщество(fmstorgmsg, fmstorg : RsdRecordset, pInfoSID : @TArray, node : FmXmlNodeElement)
    var ИнфОбщ = node.addChild("ИнфОбщество");
    var Name = СоздатьНаимОбщMain(fmstorg.value("t_orgname"), fmstorg.value("t_filialname"));
    ИнфОбществоBody(ИнфОбщ, Name, fmstorg.value("T_OGRN"), fmstorg.value("T_INN"), fmstorg.value("T_KPP"),
            FormatDate(fmstorg.value("T_RegDate")), fmstorg.value("T_OKATO"), fmstorg.value("T_Rayon"), fmstorg.value("T_Place"),
            fmstorg.value("T_Street"), fmstorg.value("T_House"), fmstorg.value("T_Corpus"), fmstorg.value("T_Office"), fmstorg.value("T_EXTRAINFO"));
    ИнфЭмитент(fmstorgmsg, fmstorg, InfoSIDMap, ИнфОбщ);
end;

macro ИнфОбщество(fmstorgmsg, fmstorg : @RsdRecordset, node : FmXmlNodeElement)
	// Аккредитив
	if (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR)
        ИнфАккредитивБезИзмен(fmstorgmsg, fmstorg, InfoSIDMap, node);
        ИнфАккредитивИзмен(fmstorgmsg, fmstorg, InfoSIDMap, node);
	elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_SECUR)
		/*ИнфЭмитент*/ИнфЭмитентИнфОбщество(fmstorgmsg, fmstorg, InfoSIDMap, node);
    elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC)
        ИнфСчетДепоБезИзмен(fmstorgmsg, fmstorg, InfoSIDMap, node);
        ИнфСчетДепоИзмен(fmstorgmsg, fmstorg, InfoSIDMap, node);
	end;
end;

macro ИнформЧасть(fmstorgmsg, node : @FmXmlNodeElement)
	// Сформировать XML-узел "СведКО"
	var СведКО = node.addChild("СведКО");
	
	СведИспБанк(fmstorgmsg, СведКО);
	
	var cmd = RsdCommand("SELECT * FROM DFMSTORG_DBT WHERE T_MSGID = ?");
	cmd.NullConversion = true;
	cmd.AddParam ("", RSDBP_IN, fmstorgmsg.ID);
	var rs = RsdRecordset(cmd);
	
	while (rs.MoveNext())
		ИнфОбщество(fmstorgmsg, rs, node);
	end;
    
    if ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR) or (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC)
        and (fmstorgmsg.BE_IsBankChanged == "X")
    )
    // begin
        var СведКоДоИзм = node.addChild("СведКоДоИзм");
        СведИспБанкОсновной(fmstorgmsg.BE_KGRKO_BPtShortName,
        fmstorgmsg.BE_BPtFilialShortName,
        fmstorgmsg.BE_KGRKO_BPtNumber,
        fmstorgmsg.BE_BPtFilialNumber,
        fmstorgmsg.BE_BPtBIC, СведКоДоИзм);
    end;
end;

macro GetFileName(StOrgMsg)
	var fname : string;
	setbuff(fmstorgmsg, StOrgMsg);

    // SKO443_TI_bbbbbbbbb_GGGGMMDD_nnn.xml
	if (strlen(fmstorgmsg.MsgFileName) <= 0)
		var MessageKindPart = "";
        
        if ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_OPEN))
            MessageKindPart = "01";
        elif ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_CLOSE))
            MessageKindPart = "11";
        elif ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_CHANGE))
            MessageKindPart = "21";
        elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_SECUR)
            MessageKindPart = "02";
        elif ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_OPEN))
            MessageKindPart = "01";
        elif ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_CLOSE))
            MessageKindPart = "11";
        elif ((fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC) AND (fmstorgmsg.ObjectState == FM_STOR_ACC_CHANGE))
            MessageKindPart = "21";
        end;
        fname = String("SKO443_", MessageKindPart, "_",
			fmstorgmsg.SendBankBIC:t:9:r:o,
			"_",
			FormatDateToName(fmstorgmsg.EMsgDate),
			"_",
			GetNNNComponent(fmstorgmsg):o:3:r,
			".xml");
	else 
		fname = fmstorgmsg.MsgFileName;
	end;
	
	return fname;
end;

private macro UpdateStOrgMsg(StOrgMsgID, FileName)
	var fname = "";
	SplitFile(FileName, fname);
	fname = fname + ".xml";
	
	var cmd = RsdCommand("UPDATE DFMSTORGMSG_DBT SET T_MSGFILENAME = ?, " +
		"T_UNLOADDATE = SYSDATE, " +
		"T_UNLOADTIME = SYSDATE " +
		"WHERE T_ID = ?");
	cmd.NullConversion = true;
	
	cmd.AddParam ("", RSDBP_IN, fname);
	cmd.AddParam ("", RSDBP_IN, StOrgMsgID);
	
	return cmd.Execute();
end;

private macro UpdateAkkred(Id, InfoSID)
	var cmd = RsdCommand("UPDATE DFMSTORGAKKRED_DBT SET T_INFOSID = ? WHERE T_ID = ?");
	cmd.NullConversion = true;
	
	cmd.AddParam ("", RSDBP_IN, InfoSID);
	cmd.AddParam ("", RSDBP_IN, Id);
	
	return cmd.Execute();
end;

private macro UpdateEmitent(Id, InfoSID)
	var cmd = RsdCommand("UPDATE DFMSTORGEMITENT_DBT SET T_INFOSID = ? WHERE T_ID = ?");
	
	cmd.AddParam ("", RSDBP_IN, InfoSID);
	cmd.AddParam ("", RSDBP_IN, Id);
	
	return cmd.Execute();
end;

private macro UpdateDepoAcc(Id, InfoSID)
	var cmd = RsdCommand("UPDATE dfmstorgdepoacc_dbt SET T_INFOSID = ? WHERE T_ID = ?");
	
	cmd.AddParam ("", RSDBP_IN, InfoSID);
	cmd.AddParam ("", RSDBP_IN, Id);
	
	return cmd.Execute();
end;

private macro GetRootName(StOrgMsg)
    var name = "";
    
    if (StOrgMsg.messagekind == FM_ST_ORG_MSG_KIND_AKKR)
        name = "СообщАккредитивКО";
    elif (StOrgMsg.messagekind == FM_ST_ORG_MSG_KIND_SECUR)
        name = "СообщПриобрКО";
    elif (StOrgMsg.messagekind == FM_ST_ORG_MSG_KIND_DEPO_ACC)
        name = "СообщСчетДепоКО";
    end;
    
    return name;
end;

private macro CheckValidAkkredChanges(StOrgMsg)
    var stat = 0;
    
    if ((StOrgMsg.messagekind == FM_ST_ORG_MSG_KIND_AKKR) and (StOrgMsg.ObjectState == FM_STOR_ACC_CHANGE))
        if (StOrgMsg.BE_IsBankChanged != "X")
            var cmd = RsdCommand("SELECT COUNT (1) "
                "  FROM DFMSTORG_DBT FMSTORG "
                " WHERE     FMSTORG.T_MSGID = ? "
                "       AND EXISTS "
                "              (SELECT 1 "
                "                 FROM DFMSTORGAKKRED_DBT AKKRED "
                "                WHERE     AKKRED.T_STORGID = FMSTORG.T_ID "
                "                      AND AKKRED.T_AKK_ISAKKREDCHANGED = CHR (0) "
                "                      AND AKKRED.T_SO_ISSTORGCHANGED = CHR (0) "
                "                      AND AKKRED.T_AKK_ISBANKCHANGED = CHR (0) "
                "                      AND AKKRED.T_AKK_ISRECEIVERCHANGED = CHR (0))");
            cmd.NullConversion = true;
            cmd.AddParam ("", RSDBP_IN, StOrgMsg.id);
            
            var rs = RsdRecordset(cmd);
            if (rs.moveNext())
                if (rs.value(0) != 0)
                    // В сообщении об изменении аккредитива не заполнена информация об измененных параметрах
                    stat = 10036;
                end;
            end;
        end;
    end;
    
    return stat;
end;

private macro FM_UnloadStOrgMsgPrivateProc(StOrgMsg, FileName)
    var stat = 0;
    var xmlWriter = FmXmlDocumentWriter;
	xmlWriter.createProcessingInstruction("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
	var root : FmXmlNodeElement;
	root = xmlWriter.getRootElement(GetRootName(fmstorgmsg), root);
	
	var service : FmXmlNodeElement;
	var inform : FmXmlNodeElement;
	
	service = root.addChild("СлужЧасть");
	inform  = root.addChild("ИнформЧасть");
	
	СлужЧасть(fmstorgmsg, service);
	ИнформЧасть(fmstorgmsg, inform);
	
	var FullFileName = FileName;
	close(MessageXMLFile);
	DelFile(MessageXMLFile);
	if(open(MessageXMLFile, FullFileName, "utf8"))
		xmlWriter.saveToFile(MessageXMLFile);
		close(MessageXMLFile);
		
		UpdateStOrgMsg(fmstorgmsg.ID, FullFileName);
		
		var i = 0;
		if (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_AKKR)
			while (i < InfoSIDMap.size)
				UpdateAkkred (InfoSIDMap[i].First, InfoSIDMap[i].Second);
				i = i + 1;
			end;
		elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_SECUR)
			while (i < InfoSIDMap.size)
				UpdateEmitent (InfoSIDMap[i].First, InfoSIDMap[i].Second);
				i = i + 1;
			end;
        elif (fmstorgmsg.MessageKind == FM_ST_ORG_MSG_KIND_DEPO_ACC)
            while (i < InfoSIDMap.size)
				UpdateDepoAcc (InfoSIDMap[i].First, InfoSIDMap[i].Second);
				i = i + 1;
			end;
		end;
	else
		// Ошибка записи файла.
		stat = 3510;
	end;
    
    return stat;
end;

private macro FM_UnloadStOrgMsgPrivate(StOrgMsg, FileName)
    setbuff(fmstorgmsg, StOrgMsg);
    var stat = CheckValidAkkredChanges(fmstorgmsg);
    
    if (stat == 0)
        stat = FM_UnloadStOrgMsgPrivateProc(fmstorgmsg, FileName);
    end;
    
    return stat;
end;

private macro InsertXmlChunk(chunkstr, doc, Before)
    var xml = CreateXMLParser();
    xml.validateOnParse = false;
    xml.async = false;
    
    xml.loadXML(chunkstr);
    if (xml.parseError.errorCode == 0)
        var chuncNode = xml.documentElement;
        
        if (ValType(Before) == V_UNDEF)
            doc.appendChild(chuncNode);
        else
            doc.insertBefore(chuncNode, Before);
        end;
    else
        var myErr = xml.parseError;
        println(myErr.reason);
        return false;
    end;
    return true;
end;

private macro СведГосЗаказ(rs :@RsdRecordset, FileName)
    var xmlWriter = FmXmlDocumentWriter;
    
    var root : FmXmlNodeElement;
    root = xmlWriter.getRootElement("СведГосЗаказ", root);

    root.addChildEx("ИденГосКонтракт", rs.value("t_StateContractID"));
    root.addChildEx("ДатаГосКонтракт", FormatDate(rs.value("t_StateContractDate")));
    root.addChildEx("НомерГосКонтракт", rs.value("t_StateContractNumber"));
    root.addChildEx("ДатаКонтракт", FormatDate(rs.value("t_ContractDate")));
    root.addChildEx("НомерКонтракт", rs.value("t_ContractNumber"));
    
    var НаимКонтрагент = root.addChild("НаимКонтрагент");
    if (strlen(rs.value("t_ReceiverName")))
        НаимКонтрагент.addChildEx("ЮрЛицо", СоздатьНаимОбщMain(rs.value("t_ReceiverName"), rs.value("t_ReceiverFilialName")));
    else
        var ИП = НаимКонтрагент.addChild("ИП");
        ИП.addChildEx("Фам", rs.value("t_Surname"));
        ИП.addChildEx("Имя", rs.value("t_Name"));
        ИП.addChildEx("Отч", rs.value("t_Patronymic"));
    end;
    
    root.addChildEx("ИННКонтрагент", rs.value("t_ReceiverINN"));
    root.addChildEx("ПризнакВладелец", rs.value("t_ReceiverType"));
    
    var chunk = xmlWriter.saveToString(false);
    
    var xml = CreateXMLParser();
    xml.validateOnParse = false;
    xml.async = false;
    
    if(xml.load(FileName))
        var t = "";
        
        var rool = xml.documentElement;
        var doc = rool.firstChild;
        
        var i = 0;
        var node;
        while(i < doc.childNodes.length)
            if ((doc.childNodes(i).nodeName == "СвСчет") or (doc.childNodes(i).nodeName == "СвБанкСтар") 
                or (doc.childNodes(i).nodeName == "СвНПСтар") or (doc.childNodes(i).nodeName == "СвНПСтар"))
            // begin
                node = doc.childNodes(i);
            end;
            i = i + 1;
        end;

        var hr = true;
        if (node == doc.lastChild)
            hr = InsertXmlChunk(chunk, doc);
        else
            hr = InsertXmlChunk(chunk, doc, node.nextSibling);
        end;
        
        if (hr == true)
            xml.save(FileName);
            UpdateStOrgMsg(rs.value("t_msgid"), FileName);
        end;
    end;
end;

private macro AttachSepAcc(FileName)
    var cmd = RsdCommand("SELECT t2.*, t1.t_msgid  FROM dfmstorg_dbt t1, dfmstorgsepacc_dbt t2 WHERE t1.t_id = t2.t_StOrgID AND t2.t_IsSepAccount = CHR (88) AND t1.t_msgid = ? AND ROWNUM = 1");
    cmd.NullConversion = true;
    cmd.AddParam ("", RSDBP_IN, fmstorgmsg.ID);

    var rs = RsdRecordset(cmd);
    var f = "";
    var hr = false;
    if (rs.MoveNext())
        var vname, vext;
        SplitFile(FileName, vname, vext);
        vname = vname + vext;
        var mask = SubStr (FileName, 1, strlen(FileName) - strlen(vname));
        SelectFile (f, mask + "*.xml");
        
        hr = СведГосЗаказ(rs, f);
    else 
        return 10033;
    end;
    
    if (hr == false)
        return 3510;
    end;
    
    return f;
end;

macro FM_UnloadStOrgMsg(StOrgMsg, FileName)
	setbuff(fmstorgmsg, StOrgMsg);
	
	if (fmstorgmsg.MessageKind != FM_ST_ORG_MSG_KIND_ACC)
        return FM_UnloadStOrgMsgPrivate(StOrgMsg, FileName);
    else
        return AttachSepAcc(FileName);
    end;
end;