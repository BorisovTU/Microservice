/*
 $Name: boprepro.mac
 $Module: Ядро Banking
 $Description: Предобработка платежа из бэк-офиса
 */
//-----------------------------------------------------------------------------
// Блок     : 29037 - "Предобработка платежа из бэк-офиса"
// Шаг      : 10    - "Предобработка"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, pmdefbo, pm_syscont, pmchk117, bochkrst, pmfm, pmprepromass, pschkbnk, "pm_allowtransfer.mac";

var PaymentObj:RsbPayment;

MACRO ExecuteStep( doc, paymDoc )
  var stat:integer = 0;
  
  if( stat == 0 ) // Системный контроль
    stat = ExecuteSysControlStep( PaymentObj, true);
  end;
  

  // Проверка платежей, позиционированных на корсхему, в которой  корреспондент corschem.CorrID является ПБР
  if( (stat == 0) and ПлатежВнешний( PaymentObj ) )
    stat = CheckPmPSBR( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на причастность к терроризму
    stat = ПроверкаПричастностиТерроризму( PaymentObj );
    if( stat < 0 )
      stat = 1;
    elif( stat > 0 )
      stat = 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на банкротство
    stat = ПроверитьСтатусБанкротства( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;

  if( stat == 0 ) // Контроль по 117-И
    stat = КонтрольПо117И( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;

  if( stat == 0 ) // Проверка остатков по счетам
    stat = BO_CheckAccRest( PaymentObj );

    // если резерв не был создан, завершить выполнение шага
    if(stat)
      return ConvertPrcChkRsrvStatForStep(stat);
    end;
  end;

  return stat;
END;

/* -----------------------------------------------------------------------------
   Массовое выполнение шага "Предобработка платежа банка"
   ----------------------------------------------------------------------------- */

/* -----------------------------------------------------------------------------
   Предтранзакционные действия 
   Все проверки отражаются только на временной таблице dpmprepro_tmp,
   никакие изменения в БД не делаются
   ----------------------------------------------------------------------------- */
macro PrepMassExecuteStep() 

  // Подготовительные действия
  var stat:integer = execStoredFunc( "PM_BOPREPRO.MassPreprocessPrepare", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproPrepare();
  end;
  
  // Системный контроль
  if( not stat )
    stat = execStoredFunc( "PM_BOPREPRO.MassSysControlPrepare", V_INTEGER );
  end;

  // Проверка остатка
  if( not stat )
    stat = execStoredFunc( "PM_BOPREPRO.MassCheckRestPrepare", V_INTEGER );
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

/* -----------------------------------------------------------------------------
   Транзакционные действия 
   Всё, что напроверяли, отражается на статусах документа, платежа, операции 
   ----------------------------------------------------------------------------- */
macro MassExecuteStep()
  
  var stat:integer = execStoredFunc( "PM_BOPREPRO.MassPreprocessExecute", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproExecute();
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

/* -----------------------------------------------------------------------------
   Действия после транзакции
   Заполняем лог обработки платежей для отчета 
   ----------------------------------------------------------------------------- */
macro PostMassExecuteStep()

  var stat:integer = execStoredFunc( "PM_PREPRO.MassFillLog", V_INTEGER );

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

