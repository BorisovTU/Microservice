/* prgroundr.mac                                   Дудин В.А.            */
/* ============= Печать справочника оснований документов ================*/

 file ground(docgrnd);

MACRO print_gr(Oper,ground,DocKind,SubDocKind,AllGround)
 ARRAY SS;
 var len, num, j;

 if(ground.DocKind != DocKind) return; end;

 if((AllGround == 0) and (SubDocKind != 0) and (ground.SubDocKind != SubDocKind))
   return;
 end;
 if((Oper == ground.Oper) or (Oper == -1))
   len = strlen(ground.GroundText);
   num = (len/84);
   if(ground.GroundText == "")
     SS(0) = "";
   else
     strsplit(ground.GroundText,SS, 84,84,num );
   end;
   if(Oper == -1 )
     [│ ####### │ #### │ #################################################################################### │]
     (ground.GroundCode,ground.Oper,SS(0));
   else
     [│ ####### │ ####################################################################################        │]
     (ground.GroundCode,SS(0));
   end;
   j = 1;
   while(num)
     if(Oper == -1 )
       [│         │      │ #################################################################################### │]
       (SS(j));
     else 
       [│         │ ####################################################################################        │]
       (SS(j));
     end;
     num = num - 1;
     j = j + 1;
   end;
   
       [├───────────────────────────────────────────────────────────────────────────────────────────────────────┤];

 end;
END;

/* ==== Печать справочника оснований документов ======= */
 macro PRINT_GROUND ( Oper, DocKind, SubDocKind, AllGround )
      /* AllGround - Признак печати всех оснований для заданного DocKind */
      var Title;

      Rewind( ground );
      if(Oper == -1) /* Печать справочника для всех операционистов */
        Title = "СПРАВОЧНИК ОСНОВАНИЙ";
      elif(Oper == 0) Title = "ОБЩИЙ СПРАВОЧНИК ОСНОВАНИЙ";
      else            Title = "СПРАВОЧНИК ОСНОВАНИЙ ОПЕРАЦИОНИСТА \"" + string(Oper) +"\"";
      end;
      if(not next(ground))
        [Справочник оснований пуст]
      else
        [
                          ############################################](Title);
        [/───────────────────────────────────────────────────────────────────────────────────────────────────────\];

        if(Oper == -1 )
        [│   Код   │ Опер │ Наименование основания                                                               │];
        else
        [│   Код   │ Наименование основания                                                                      │];
        end;
        [└───────────────────────────────────────────────────────────────────────────────────────────────────────┘];
        print_gr(Oper,ground, DocKind, SubDocKind, AllGround);
        while(next(ground)) print_gr(Oper,ground, DocKind, SubDocKind, AllGround); end;
      end;
      return 0;
 END;
