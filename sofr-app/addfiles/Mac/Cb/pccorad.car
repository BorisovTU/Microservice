/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ОДБ"                                   */
/*                                                                          */
/*                                                                          */
/*  Имя файла: pccorad.CAR                                                  */
/*  Создан: 08.02.1999                                        Стасевич В.   */
/*                                                                          */
/****************************************************************************/

import mc_lib, pccarry;
import RsbDataSet, CTInter, globals;
import InsCarryDoc;


RECORD din( arhdoc );
RECORD dd( arhdoc );


FILE pcacc( pcacc );
FILE pcacnt( pcacnt );

/* Константы
CONST КассовыйМетод   = "К";
CONST МетодНачислений = "Н";
CONST НаВнебалансе    = "В";

CONST НомКатСчетПлательщик             = 1;     Номера категорий учета
CONST НомКатСчетПолучатель             = 2;
CONST НомКатСчетТребованийОбязательств = 3;
CONST НомКатСчетБудПериодов            = 4;
CONST НомКатСчетПросрочки              = 5;
CONST НомКатСчетНаВнебалансе           = 6;
CONST НомКатСчетДляКорреспонденции     = 7;

CONST Актив   = 1;
CONST Пассив  = 2;*/


/*------------------------------------------------------------------------
  Процедура получения кода валюты по номеру категории учета
--------------------------------------------------------------------------*/
MACRO НайтиВалютуПоКатегории( НомерКатегории )
  pcacnt.AutoPerc = pcacc.AutoKey;
  pcacnt.AcntCode = НомерКатегории;

  if(( GetGE( pcacnt ) )                      and
     ( pcacnt.AutoPerc == pcacc.AutoKey ) and
     ( pcacnt.AcntCode == НомерКатегории    )     )
    return pcacnt.FIID;
  else
    return -1;
  end;
END;

/*------------------------------------------------------------------------*/
MACRO НайтиСчетПоКатегории( НомерКатегории )
  pcacnt.AutoPerc = pcacc.AutoKey;
  pcacnt.AcntCode = НомерКатегории;

  if(( GetGE( pcacnt ) )                and
     ( pcacnt.AutoPerc == pcacc.AutoKey ) and
     ( pcacnt.AcntCode == НомерКатегории    )     )
    return pcacnt.Account;
  else
    СтрокаОшибки = string("Не найдена запись по счету для категории учета:",НомерКатегории);
/*    MsgBox(СтрокаОшибки);*/
    return "";
  end;
END;

/*┌──────────────────────────┐*/
/*│ Точка входа в программу  │*/
/*└──────────────────────────┘*/
MACRO ExecuteStep( Buffer, Document )

  var СчетОВП;  /* Номер лицевого счета ОВП (одинаковый для всех
                           глав и валют */
  var Счет, ДостаточностьСуммы, НадежностьСубъекта;

  var Сумма_17, Сальдо_17, СуммаПерерасчета;

  setbuff( din,  Document );
  setbuff( mc_out, Buffer   );

  copy( dd, din );

  pcacc.AutoKey = dd.AutoKey;
  getEQ( pcacc );
  dd.AutoKey = 0;

  if( not ПолучитьСчетОВП (СчетОВП, din.chapter, din.code_currency, 4504 ) )
    СчетОВП = "ОВП";
  end;

  /* Проводки - в зависимости от метода и типа остатка */

  if  ( Index( pcacc.UserType, КассовыйМетод ) > 0 )
    /* Оплата процентов - Кассовый метод */

      if( dd.Sum > $0 )
         copy( dout, dd ); /*1*/
         dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетБудПериодов );
         dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетТребованийОбязательств );
         InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
      end;

      if( dd.Sum < $0 )
         copy( dout, dd ); /*2*/
         dout.Sum = -dout.Sum;
         dout.Account_Payer    = НайтиСчетПоКатегории( НомКатСчетТребованийОбязательств );
         dout.Account_Receiver = НайтиСчетПоКатегории( НомКатСчетБудПериодов );
         InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
      end;
  elif( Index( pcacc.UserType, МетодНачислений ) > 0 )
    /* Оплата процентов - Метод начисления */
    if( dd.Sum > $0 )
      if( pcacc.RestKind == Пассив )
         ДостаточностьСуммы = ОпределитьДостаточностьСредств(pcacc.AutoKey, dout.Sum, dout.Code_Currency, dd.Date_Carry);

         if (ДостаточностьСуммы == 0) // если средств на счете достаточно

            copy( dout, dd ); // проводка 19

            if( (Счет = НайтиСчетПоКатегории(НомКатСчетПлательщик)) == "" )
               СтрокаОшибки = "";
               Счет = pcacc.PayerAccount;
            end;

            dout.Account_Payer       = Счет;
            dout.Account_Receiver    = НайтиСчетПоКатегории(НомКатСчетНачисленныхПроцентов);
            InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );

         elif (ДостаточностьСуммы > 1)
            MsgBox("Ошибка определения достаточности средств: " + ppLastError);
            return 1;
         end;
      elif (pcacc.RestKind == Актив)
         // определяем надежность субъекта
         if (NOT ОпределитьНадежностьСубъекта(pcacc.AutoKey, @НадежностьСубъекта, dd.Date_Carry))
            MsgBox("Ошибка определения надежности субъекта: " + ppLastError);
            return 1;
         end;

         if (НадежностьСубъекта == 1) // если субъект надежный

            if  ( Index( pcacc.UserType, НаВнебалансе  ) > 0 )
               MsgBox("Надо выполнить перенос с внебаланса.");
               return 1;
            end;

            copy( dout, dd ); // проводка 20

            if( (Счет = НайтиСчетПоКатегории(НомКатСчетПолучатель)) == "" )
               СтрокаОшибки = "";
               Счет = pcacc.PercReceiverAccount;
            end;

            dout.Account_Payer    = НайтиСчетПоКатегории(НомКатСчетНачисленныхПроцентов);
            dout.Account_Receiver = Счет;
            InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
         else // субъект ненадежный

            if  ( Index( pcacc.UserType, НаВнебалансе  ) <= 0 )
               MsgBox("Надо выполнить перенос на внебаланс.");
               return 1;
            end;

            copy( dout, dd ); // проводка 21
            dout.Chapter = ГлаваВ;
            dout.Account_Payer    = НайтиСчетПоКатегории(НомКатСчетНеполученныхПроцентов);
            dout.Account_Receiver = НайтиСчетПоКатегории (НомКатСчетДляКорреспонденции);
            InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );

         end;
      end;
    elif ( dd.Sum < $0 )// сумма меньше нуля
      dd.Sum = -dd.Sum;
      if( pcacc.RestKind == Пассив )
         ДостаточностьСуммы = ОпределитьДостаточностьСредств(pcacc.AutoKey, dout.Sum, dout.Code_Currency, dd.Date_Carry);

         if (ДостаточностьСуммы == 0) // если средств на счете достаточно

            copy( dout, dd ); // проводка 15

            if( (Счет = НайтиСчетПоКатегории(НомКатСчетПлательщик)) == "" )
               СтрокаОшибки = "";
               Счет = pcacc.PayerAccount;
            end;

            dout.Account_Payer    = НайтиСчетПоКатегории(НомКатСчетНачисленныхПроцентов);
            dout.Account_Receiver = Счет;
            InsertPcCarry(dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП);
         elif (ДостаточностьСуммы > 1)
            MsgBox("Ошибка определения достаточности средств: " + ppLastError);
            return 1;
         end;
      elif (pcacc.RestKind == Актив)
         // определяем надежность субъекта
         if (NOT ОпределитьНадежностьСубъекта(pcacc.AutoKey, @НадежностьСубъекта, dd.Date_Carry))
            MsgBox("Ошибка определения надежности субъекта: " + ppLastError);
            return 1;
         end;

         if (НадежностьСубъекта == 1) // если субъект надежный

            if  ( Index( pcacc.UserType, НаВнебалансе  ) > 0 )
               MsgBox("Надо выполнить перенос с внебаланса.");
               return 1;
            end;

            if( (Счет = НайтиСчетПоКатегории(НомКатСчетПолучатель)) == "" )
               СтрокаОшибки = "";
               Счет = pcacc.PercReceiverAccount;
            end;

            copy( dout, dd ); // проводка 16
            dout.Account_Payer    =  Счет;
            dout.Account_Receiver =  НайтиСчетПоКатегории(НомКатСчетНачисленныхПроцентов);
            InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
         else // субъект ненадежный

            if  ( Index( pcacc.UserType, НаВнебалансе  ) <= 0 )
               MsgBox("Надо выполнить перенос на внебаланс.");
               return 1;
            end;

            if( (Счет = НайтиСчетПоКатегории(НомКатСчетПолучатель)) == "" )
               СтрокаОшибки = "";
               Счет = pcacc.PercReceiverAccount;
            end;

            copy( dout, dd ); // проводка 17
            dout.Chapter = ГлаваВ;
            dout.Account_Payer    =  НайтиСчетПоКатегории (НомКатСчетДляКорреспонденции);
            dout.Account_Receiver =  НайтиСчетПоКатегории(НомКатСчетНеполученныхПроцентов);

            Сальдо_17 = Abs(ПолучитьСальдо(dout.Account_Receiver, dout.Chapter, dout.Code_Currency));

            // проверяем, что средств на счете для корреспонденции достаточно
            if (dd.Sum > Abs(ПолучитьСальдо(dout.Account_Payer, dout.Chapter, dout.Code_Currency)))
                MsgBox("Ошибка: сумма перерасчета больше сальдо счета корреспонденции. Перерасчет невозможен.");
                return 1;
            else
                Сумма_17 = Min(dd.Sum, Сальдо_17);
            end;

            dout.Sum = Сумма_17;

            if ( dout.Sum > 0 )
               InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
            end;

            if ((dd.Sum - Сумма_17) <= ПолучитьСальдо(Счет, 1, dout.Code_Currency))
               copy( dout, dd ); // проводка 18
               dout.Sum = dd.Sum - Сумма_17;
               dout.Account_Payer    =  Счет;
               dout.Account_Receiver =  НайтиСчетПоКатегории(НомКатСчетНачисленныхПроцентов);

               if (dout.Sum > 0)
                  InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
               end;
            else
               СтрокаОшибки == "Перерасчет невозможен: недостаточно средств на счете получателе.";
            end;
         end;
       end;
    end;
  end;

  if( СтрокаОшибки == "" )
     return 0;
  else
     MsgBox( СтрокаОшибки );
     return 1;
  end;
END;
