/*
$Name:           pt_extract.mac
$Module:         Ядро ГКБО
$Description:    Формирование выписки для ФСФМ
*/

import BankInter, PTInter, CTInter, rsd, oralib, likepy, globals;
import serviceaction;
import FMInter;
import fmreqi_lib;

private file f_fmrequestinfo(fmrequestinfo) key 0;
private file f_clientparty(party) key 0;
private file f_clientadress(adress) key 0;
private file f_institut(institut) key 0;
private file f_fmattachkind(fmattachkind) key 0;
private file f_llvalues(llvalues) key 0;

private record fmreqiattach(fmreqiattach);
private record fmrequestinfo(fmrequestinfo);
private record clientparty(party);
private record clientadress(adress);
private record attr(objattr);

private file ExtractXMLFile() txt write;
private file ExtractXMLFileR() txt;

private var AbsentFieldValuesArr = TArray;

private var ExtractXMLFileName;

private var objParty; //используется при формировании блоков для выгодоприобретателей и бенефициарных владельцев
private var useObjParty = false;
private var addToAbsentFieldValue = "";

private macro _AddAbsentFieldValue(Field)
  var idx = AbsentFieldValuesArr.size;
  if(addToAbsentFieldValue != "")
    AbsentFieldValuesArr[idx] = Field + " " + addToAbsentFieldValue;
  else
    AbsentFieldValuesArr[idx] = Field;
  end;
end;

private macro _СлужЧасть_ВерсФорм()
  ExtractXMLFile.str = "<ВерсФорм>1.1</ВерсФорм>";
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_ВерсПрог()
  ExtractXMLFile.str = "<ВерсПрог>" + "RS-Bank " + _GetDBVersion() + "</ВерсПрог>";
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_ДатВып()
  ExtractXMLFile.str = "<ДатВып>" + _GetSchemaFormatDate(date) + "</ДатВып>";
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_ВремВып()
  ExtractXMLFile.str = "<ВремВып>" + _GetSchemaFormatDate(time) + "</ВремВып>";
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_НаимКО()
  if(fmrequestinfo.KGRKO_BankShortName == "")
    _AddAbsentFieldValue("Отсутствует наименование кредитной организации");
  else
    ExtractXMLFile.str = "<НаимКО>" + fmrequestinfo.KGRKO_BankShortName + "</НаимКО>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_РегНомКО()
  if(fmrequestinfo.KGRKO_BankNumber == "")
    _AddAbsentFieldValue("Отсутствует номер кредитной организации по КГРКО");
  else
    ExtractXMLFile.str = "<РегНомКО>" + string(int(fmrequestinfo.KGRKO_BankNumber)) + "</РегНомКО>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_НомФл()
  if(string(int(fmrequestinfo.KGRKO_BankDprt)) == "")
    _AddAbsentFieldValue("Отсутствует номер филиала кредитной организации по КГРКО");
  else
    ExtractXMLFile.str = "<НомФл>" + string(int(fmrequestinfo.KGRKO_BankDprt)) + "</НомФл>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_БИККО()
  if(fmrequestinfo.BIC == "")
    _AddAbsentFieldValue("Отсутствует БИК кредитной организации");
  else
    ExtractXMLFile.str = "<БИККО>" + fmrequestinfo.BIC + "</БИККО>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_ТипКлиент()
  if((fmrequestinfo.ClientType < 1) or (3 < fmrequestinfo.ClientType))
    _AddAbsentFieldValue("Не определён тип клиента");
  else
    ExtractXMLFile.str = "<ТипКлиент>" + fmrequestinfo.ClientType + "</ТипКлиент>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_ПризнРез()
  if(fmrequestinfo.ClientNotResident != "X")
    ExtractXMLFile.str = "<ПризнРез>1</ПризнРез>";
  else
    ExtractXMLFile.str = "<ПризнРез>2</ПризнРез>";
  end;
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_ФИООтвСотрудн_Фам()
  if(fmrequestinfo.ProxyName1 == "")
    _AddAbsentFieldValue("Не указана фамилия уполномоченного лица");
  else
    ExtractXMLFile.str = "<Фам>" + fmrequestinfo.ProxyName1 + "</Фам>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_ФИООтвСотрудн_Имя()
  if(fmrequestinfo.ProxyName2 == "")
    _AddAbsentFieldValue("Не указано имя уполномоченного лица");
  else
    ExtractXMLFile.str = "<Имя>" + fmrequestinfo.ProxyName2 + "</Имя>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_ФИООтвСотрудн_Отч()
  if(fmrequestinfo.ProxyName3 != "")
    ExtractXMLFile.str = "<Отч>" + fmrequestinfo.ProxyName3 + "</Отч>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть_ФИООтвСотрудн()
  ExtractXMLFile.str = "<ФИООтвСотрудн>";
  insert(ExtractXMLFile);

  _СлужЧасть_ФИООтвСотрудн_Фам();
  _СлужЧасть_ФИООтвСотрудн_Имя();
  _СлужЧасть_ФИООтвСотрудн_Отч();

  ExtractXMLFile.str = "</ФИООтвСотрудн>";
  insert(ExtractXMLFile);
end;

private macro _СлужЧасть_ТелОтвСотрудн()
  if(fmrequestinfo.ProxyPhone != "")
    ExtractXMLFile.str = "<ТелОтвСотрудн>" + fmrequestinfo.ProxyPhone + "</ТелОтвСотрудн>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СлужЧасть(fmreqiattach)
  ExtractXMLFile.str = "<СлужЧасть>";
  insert(ExtractXMLFile);

  _СлужЧасть_ВерсФорм();
  _СлужЧасть_ВерсПрог();
  _СлужЧасть_ДатВып();
  //_СлужЧасть_ВремВып();
  _СлужЧасть_НаимКО();
  _СлужЧасть_РегНомКО();
  _СлужЧасть_НомФл();
  _СлужЧасть_БИККО();
  _СлужЧасть_ТипКлиент();
  _СлужЧасть_ПризнРез();
  _СлужЧасть_ФИООтвСотрудн();
  _СлужЧасть_ТелОтвСотрудн();

  ExtractXMLFile.str = "</СлужЧасть>";
  insert(ExtractXMLFile);
end;

private macro _GetClientPartyBuffer()
  var stat;
  var PartyID;
  if(not useObjParty)
    PartyID = fmrequestinfo.ClientID;
  else
    PartyID = objParty.PartyID;
  end;

  ClearRecord(f_clientparty);
  f_clientparty.PartyID = fmrequestinfo.ClientID;
  stat = GetEQ(f_clientparty);
  if(stat)
    Copy(clientparty, f_clientparty);
  end;
end;

private macro _GetBuffers(fmreqiattach)
  var stat = false;

  ClearRecord(f_fmrequestinfo);
  f_fmrequestinfo.ID = fmreqiattach.RequestInfoID;//15;//20;//19;//20;//22;//
  stat = GetEQ(f_fmrequestinfo);
  if(stat)
    Copy(fmrequestinfo, f_fmrequestinfo);

    _GetClientPartyBuffer();
  end;
  return stat;
end;

private macro _GetHeadPartyID(PartyID)
  var query = "";
  var rs;
  var params : TArray;
  var HeadPartyID = 0;

  query = query + "SELECT t.t_PartyID                                 ";
  query = query + "  FROM (    SELECT t_PartyID, t_Superior           ";
  query = query + "              FROM dparty_dbt                      ";
  query = query + "        START WITH t_PartyID = :PartyID            ";
  query = query + "        CONNECT BY PRIOR t_Superior = t_PartyID) t ";
  query = query + " WHERE t.t_Superior <= 0                           ";

  params = makeArray(SQLParam("PartyID", PartyID)
                    );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    HeadPartyID = rs.value(0);
  end;
  return HeadPartyID;
end;

private macro _НаимЮЛ()
  var Name = "";
  
  if(not useObjParty)
    Name = fmrequestinfo.ClientYurName;
  else
    Name = objParty.FullName;
  end;

  if(Name == "")
    _AddAbsentFieldValue("Не указано наименование организации");
  else
    ExtractXMLFile.str = "<НаимЮЛ>" + Name + "</НаимЮЛ>";
    insert(ExtractXMLFile);
  end;
end;

private macro _GetFirstPartyName(PartyID, NameTypeID)
  var Name = "";
  var NameArr = GetPartyNames(PartyID, NameTypeID);
  if(NameArr.size > 0)
    Name = NameArr[0];
  end;
  return Name;
end;

private macro _СокрНаимЮЛ()
  var PartyID = 0;
  var HeadPartyID = 0;
  var ShortName = "";
  var HeadShortName = "";

  if(not useObjParty)
    PartyID = fmrequestinfo.ClientID;
  else
    PartyID = objParty.PartyID;
  end;

  HeadPartyID = _GetHeadPartyID(PartyID);
  ShortName = _GetFirstPartyName(PartyID, 2);

  if(ShortName != "")
    if(HeadPartyID != PartyID)
      HeadShortName = _GetFirstPartyName(HeadPartyID, 2);
      if(HeadShortName != "")
        ShortName = HeadShortName + ", филиал " + ShortName;
      end;
    end;
    ExtractXMLFile.str = "<СокрНаимЮЛ>" + ShortName + "</СокрНаимЮЛ>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ИнНаимЮЛ()
  var PartyID = 0;
  var HeadPartyID = 0;
  var FrngName = "";
  var HeadFrngName = "";

  if(not useObjParty)
    PartyID = fmrequestinfo.ClientID;
  else
    PartyID = objParty.PartyID;
  end;
  
  HeadPartyID = _GetHeadPartyID(PartyID);
  FrngName = _GetFirstPartyName(PartyID, 3);

  if(FrngName != "")
    if(HeadPartyID != PartyID)
      HeadFrngName = _GetFirstPartyName(HeadPartyID, 3);
      if(HeadFrngName != "")
        FrngName = HeadFrngName + ", филиал " + FrngName;
      end;
    end;
  end;
  ExtractXMLFile.str = "<ИнНаимЮЛ>" + FrngName + "</ИнНаимЮЛ>";
  insert(ExtractXMLFile);
end;

private macro _ОргПравФорм()
  var ObjCat = RsbObjCategories(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY));
  if(ObjCat.GetMainAttr(28, {curdate}, attr) != 0)
    attr.Name = "";
    _AddAbsentFieldValue("Неизвестна организационно-правовая форма");
  end;
  ExtractXMLFile.str = "<ОргПравФорм>" + attr.Name + "</ОргПравФорм>";
  insert(ExtractXMLFile);
end;

private macro _ИНН(elemName, addAbsent)
  var INN;

  if(not useObjParty)
    INN = fmrequestinfo.ClientINN;
  else
    INN = objParty.Code(16);
  end;

  if(INN == "")
    if(addAbsent)
      _AddAbsentFieldValue("Неизвестен ИНН");
    end;
  end;
  if((INN != "") or (elemName == "ИННФЛ") or (elemName == "ИННИП"))
    ExtractXMLFile.str = "<" + elemName + ">" + INN + "</" + elemName + ">";
    insert(ExtractXMLFile);
  end;
end;

private macro _ОГРН(elemName, addAbsent)
  var OGRN;

  if(not useObjParty)
    OGRN = fmrequestinfo.ClientOGRN;
  else
    OGRN = objParty.Code(27);
  end;

  if(OGRN == "")
    if(addAbsent)
      _AddAbsentFieldValue("Неизвестен код регистрации");
    end;
  else
    ExtractXMLFile.str = "<" + elemName + ">" + OGRN + "</" + elemName + ">";
    insert(ExtractXMLFile);
  end;
end;

private macro _ДатаРег()
  var RegDate = date(0, 0, 0);
  
  if(not useObjParty)
    RegDate = fmrequestinfo.ClientRegDate;
  else
    if(not objParty.PartyRegDoc(7/*REGPT_TAXINSTITUTE*/, 4/*OBJKDOC_EVIDENCE_GOS_REG*/).IsEmpty)
      RegDate = objParty.PartyRegDoc(7/*REGPT_TAXINSTITUTE*/, 4/*OBJKDOC_EVIDENCE_GOS_REG*/).StartDate;
    end;
  end;
  
  if(RegDate == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата регистрации");
  else
    ExtractXMLFile.str = "<ДатаРег>" + _GetSchemaFormatDate(RegDate) + "</ДатаРег>";
    insert(ExtractXMLFile);
  end;
end;

private macro _GetRegPartyName(CodeKind)
  var query = "";
  var rs;
  var params : TArray;
  var RegPartyID = 0;
  var RegPartyName = "";
  var ObjectID = 0;

  if(not useObjParty)
    ObjectID = fmrequestinfo.ClientID;
  else
    ObjectID = objParty.PartyID;
  end;

  query = query + "  SELECT t_RegPartyID                                                                                              ";
  query = query + "    FROM dobjrgdoc_dbt                                                                                             ";
  query = query + "   WHERE t_ObjectType = 3                                                                                          ";
  query = query + "     AND t_ObjectID = :ObjectID                                                                                    ";
  query = query + "     AND t_CodeKind = :CodeKind                                                                                    ";
  query = query + "ORDER BY DECODE(t_FinishDate, TO_DATE('01010001', 'DDMMYYYY'), TO_DATE('31129999', 'DDMMYYYY'), t_FinishDate) DESC ";

  params = makeArray(SQLParam("ObjectID", ObjectID)
                    ,SQLParam("CodeKind", CodeKind)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    RegPartyID = rs.value(0);

    RegPartyName = _GetFirstPartyName(RegPartyID, 1);
  end;

  return RegPartyName;
end;

private macro _НаимРегОрг()
  var RegPartyName = "";
  var NotResident;

  if(not useObjParty)
    NotResident = (fmrequestinfo.ClientNotResident == "X");
  else
    NotResident = objParty.IsNotResident;
  end;

  if(not NotResident)
    RegPartyName = _GetRegPartyName(27);
  else
    RegPartyName = _GetRegPartyName(35);
    if(RegPartyName == "")
      RegPartyName = _GetRegPartyName(33);
    end;
  end;
  if(RegPartyName == "")
    _AddAbsentFieldValue("Неизвестен регистрационный орган");
  end;
  ExtractXMLFile.str = "<НаимРегОрг>" + RegPartyName + "</НаимРегОрг>";
  insert(ExtractXMLFile);
end;

private macro _GetCountryCodeNum3(CodeLat3)
  var query = "";
  var rs;
  var params : TArray;
  var CodeNum3 = "";

  query = query + "SELECT t_CodeNum3 FROM dcountry_dbt WHERE t_CodeLat3 = :CodeLat3";

  params = makeArray(SQLParam("CodeLat3", CodeLat3)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    CodeNum3 = rs.value(0);
  end;

  return CodeNum3;
end;

private macro _Адр_КодОКСМ(adress, adrtypename)
  var CodeNum3 = "";
  var NotResident;

  if(not useObjParty)
    NotResident = (fmrequestinfo.ClientNotResident == "X");
  else
    NotResident = objParty.IsNotResident;
  end;

  if(adress.Country != "")
    CodeNum3 = _GetCountryCodeNum3(adress.Country);
  end;
  if((CodeNum3 == "") and (not NotResident))
    CodeNum3 = "643";
  end;
  if(CodeNum3 == "")
    if(adrtypename == null)
      adrtypename = "места регистрации";
    end;
    _AddAbsentFieldValue("Неизвестна страна " + adrtypename);
  end;

  ExtractXMLFile.str = "<КодОКСМ>" + CodeNum3 + "</КодОКСМ>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Индекс(adress)
  ExtractXMLFile.str = "<Индекс>" + adress.PostIndex + "</Индекс>";
  insert(ExtractXMLFile);
end;

private macro _Адр_КодСубъектаПоОКАТО(adress)
  ExtractXMLFile.str = "<КодСубъектаПоОКАТО>" + substr(adress.Okato, 1, 2) + "</КодСубъектаПоОКАТО>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Регион(adress)
  var strValue = adress.Province + " " + adress.CodeProvince;
  ExtractXMLFile.str = "<Регион>" + trim(strValue) + "</Регион>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Пункт(adress, adrtypename)
  var strValue = adress.District + " " + adress.CodeDistrict + " " + adress.Place + " " + adress.CodePlace;
  if(trim(strValue) == "")
    if(adrtypename == null)
      adrtypename = "места регистрации";
    end;
    _AddAbsentFieldValue("Неизвестен населенный пункт " + adrtypename);
  end;
  ExtractXMLFile.str = "<Пункт>" + trim(strValue) + "</Пункт>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Улица(adress)
  var strValueS = adress.Street + " " + adress.CodeStreet;
  var strValueP = adress.Plan + " " + adress.CodePlan;

  var strValue = "";

  if((strlen(adress.Plan)>0) AND (strlen(adress.Street)>0))
    strValue = strValueP + " " + strValueS;
  elif(strlen(adress.Plan)>0)
    strValue = strValueP;
  elif(strlen(adress.Street)>0)
    strValue = strValueS;
  end;

  ExtractXMLFile.str = "<Улица>" + trim(strValue) + "</Улица>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Дом(adress)
  ExtractXMLFile.str = "<Дом>" + trim(_CorrectAddrFieldValue(adress.House)) + "</Дом>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Корп(adress)
  var Corp = "";

  if((strlen(adress.NumCorps)>0) AND (strlen(adress.Building)>0))
    Corp = adress.Building + "-" + adress.NumCorps;
  elif(strlen(adress.NumCorps)>0)
    Corp = adress.NumCorps;
  elif(strlen(adress.Building)>0)
    Corp = adress.Building;
  end;
  
  ExtractXMLFile.str = "<Корп>" + trim(_CorrectAddrFieldValue(Corp)) + "</Корп>";
  insert(ExtractXMLFile);
end;

private macro _Адр_Оф(adress)
  ExtractXMLFile.str = "<Оф>" + trim(_CorrectAddrFieldValue(adress.Flat)) + "</Оф>";
  insert(ExtractXMLFile);
end;

private macro _GetClientAddressBuffer()
  var stat;
  ClearRecord(f_clientadress);
  if(not useObjParty)
    f_clientadress.PartyID = fmrequestinfo.ClientID;
  else
    f_clientadress.PartyID = objParty.PartyID;
  end;
  f_clientadress.Type = 1;
  stat = GetEQ(f_clientadress);
  if(stat)
    Copy(clientadress, f_clientadress);
  end;
  return stat;
end;

private macro _Адр(elemName)
  var stat = _GetClientAddressBuffer();

  if(stat)
    ExtractXMLFile.str = "<" + elemName + ">";
    insert(ExtractXMLFile);

    _Адр_КодОКСМ(clientadress);
    _Адр_Индекс(clientadress);
    _Адр_КодСубъектаПоОКАТО(clientadress);
    _Адр_Регион(clientadress);
    _Адр_Пункт(clientadress);
    _Адр_Улица(clientadress);
    _Адр_Дом(clientadress);
    _Адр_Корп(clientadress);
    _Адр_Оф(clientadress);

    ExtractXMLFile.str = "</" + elemName + ">";
    insert(ExtractXMLFile);
  else
    _AddAbsentFieldValue("Неизвестно место регистрации");
  end;
end;

private macro _БИККлКО()
/*
  ExtractXMLFile.str = "<БИККлКО></БИККлКО>";
  insert(ExtractXMLFile);
*/
end;

private macro _ОргЮЛ()
  var Note = readNoteForObject(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY), 10);
  if(Note == null)
    Note = "";
  end;
  if(Note == "")
    _AddAbsentFieldValue("Неизвестны сведения об органах управления");
  else
    ExtractXMLFile.str = "<ОргЮЛ>" + Note + "</ОргЮЛ>";
    insert(ExtractXMLFile);
  end;
end;

private macro _УстКапЮЛ()
  var stat;
  ClearRecord(f_institut);

  if(not useObjParty)
    f_institut.PartyID = fmrequestinfo.ClientID;
  else
    f_institut.PartyID = objParty.PartyID;
  end;
  
  stat = GetEQ(f_institut);
  if(stat and ((f_institut.DeclareCapital != $0) or (f_institut.RealCapital != $0)))
    ExtractXMLFile.str = "<УстКапЮЛ>" + string(f_institut.DeclareCapital) + "," + string(f_institut.RealCapital) + "</УстКапЮЛ>";
    insert(ExtractXMLFile);
    if((f_institut.DeclareCapital == $0) or (f_institut.RealCapital == $0))
      _AddAbsentFieldValue("Сведения об уставном капитале неполны");
    end;
  else
    _AddAbsentFieldValue("Неизвестны сведения об органах управления");
  end;
end;

private macro _ПрисутАдрЮЛ()
  var ObjCat = RsbObjCategories(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY));
  if(ObjCat.GetMainAttr(56, {curdate}, attr) != 0)
    attr.NumInList = "";
    _AddAbsentFieldValue("Неизвестно нахождение управляющих органов по месту регистрации");
  else
    ExtractXMLFile.str = "<ПрисутАдрЮЛ>" + attr.NumInList + "</ПрисутАдрЮЛ>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ВыпискаЮЛ(elemName)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);
  
  _НаимЮЛ();
  _СокрНаимЮЛ();
  _ИнНаимЮЛ();
  _ОргПравФорм();
  _ИНН("ИННЮЛ", true);
  _ОГРН("ОГРНЮЛ", true);
  _ДатаРег();
  _НаимРегОрг();
  _Адр("АдрЮЛ");
  /*_БИККлКО();*/
  _ОргЮЛ();
  _УстКапЮЛ();
  _ПрисутАдрЮЛ();

  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _ФИО_Фам()
  var Name = "";

  if(not useObjParty)
    Name = fmrequestinfo.ClientName1;
  else
    Name = objParty.LastName;
  end;
  
  if(Name == "")
    _AddAbsentFieldValue("Неизвестна фамилия физического лица");
  else
    ExtractXMLFile.str = "<Фам>" + Name + "</Фам>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИО_Имя()
  var Name = "";
  
  if(not useObjParty)
    Name = fmrequestinfo.ClientName2;
  else
    Name = objParty.FirstName;
  end;
  
  if(Name == "")
    _AddAbsentFieldValue("Неизвестно имя физического лица");
  else
    ExtractXMLFile.str = "<Имя>" + Name + "</Имя>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИО_Отч()
  var Name = "";
  
  if(not useObjParty)
    Name = fmrequestinfo.ClientName3;
  else
    Name = objParty.Patronymic;
  end;
  
  if(Name != "")
    ExtractXMLFile.str = "<Отч>" + Name + "</Отч>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИО(elemName)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);

  _ФИО_Фам();
  _ФИО_Имя();
  _ФИО_Отч();
  
  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _ДатаРожд(elemName)
  var BirthDate = date(0, 0, 0);
  
  if(not useObjParty)
    BirthDate = fmrequestinfo.ClientBirthDate;
  else
    BirthDate = objParty.BirthDate;
  end;
  
  if(BirthDate == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата рождения");
  else
    ExtractXMLFile.str = "<" + elemName + ">" + _GetSchemaFormatDate(BirthDate) + "</" + elemName + ">";
    insert(ExtractXMLFile);
  end;
end;

private macro _МестоРожд_КодОКСМ()
  var BirthCountry = "";

  if(not useObjParty)
    BirthCountry = fmrequestinfo.ClientBirthCountry;
  else
    if(not objParty.IsNotResident)
      BirthCountry = "643";
    end;
  end;

  if(BirthCountry == "")
    _AddAbsentFieldValue("Неизвестна страна места рождения физического лица");
  else
    ExtractXMLFile.str = "<КодОКСМ>" + BirthCountry + "</КодОКСМ>";
    insert(ExtractXMLFile);
  end;
end;

private macro _GetBirthOKATO(RegionBorn)
  var query = "";
  var rs;
  var params : TArray;
  var BirthOKATO = "";

  query = query + "SELECT t_NumInList ";
  query = query + "  FROM dobjattr_dbt ";
  query = query + " WHERE     t_ObjectType = 3 ";
  query = query + "       AND t_GroupID = 12 ";
  query = query + "       AND UPPER (t_Name) IN (SELECT UPPER (t_NameRegion) ";
  query = query + "                                FROM dptregion_dbt ";
  query = query + "                               WHERE t_CodeRegion = :CodeRegion) ";

  params = makeArray(SQLParam("CodeRegion", RegionBorn)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    BirthOKATO = rs.value(0);
  end;

  return BirthOKATO;
end;

private macro _МестоРожд_КодСубъектаПоОКАТО()
  var BirthOKATO = "";
  
  if(not useObjParty)
    BirthOKATO = fmrequestinfo.ClientBirthOKATO;
  else
    BirthOKATO = _GetBirthOKATO(objParty.RegionBorn);
  end;

  ExtractXMLFile.str = "<КодСубъектаПоОКАТО>" + BirthOKATO + "</КодСубъектаПоОКАТО>";
  insert(ExtractXMLFile);
end;

private macro _МестоРожд_Регион()
  var RaionBorn = "";
  
  if(not useObjParty)
    RaionBorn = fmrequestinfo.ClientBirthRayon;
  else
    RaionBorn = objParty.RaionBorn;
  end;

  ExtractXMLFile.str = "<Регион>" + RaionBorn + "</Регион>";
  insert(ExtractXMLFile);
end;

private macro _МестоРожд_Пункт()
  var PlaceBorn = "";
  
  if(not useObjParty)
    PlaceBorn = fmrequestinfo.ClientBirthPlace;
  else
    PlaceBorn = objParty.PlaceBorn;
  end;

  if(PlaceBorn != "")
    _AddAbsentFieldValue("Неизвестен населенный пункт места рождения физического лица");
  end;

  ExtractXMLFile.str = "<Пункт>" + PlaceBorn + "</Пункт>";
  insert(ExtractXMLFile);
end;

private macro _МестоРожд(elemName)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);

  _МестоРожд_КодОКСМ();
  _МестоРожд_КодСубъектаПоОКАТО();
  _МестоРожд_Регион();
  _МестоРожд_Пункт();

  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _Гражд(elemName)
  var CodeNum3 = "";
  if(clientparty.NRCountry != "")
    CodeNum3 = _GetCountryCodeNum3(clientparty.NRCountry);
  end;
  if((CodeNum3 == "") and (clientparty.NotResident == "X"))
    CodeNum3 = "000";
  end;
  if(CodeNum3 == "")
    _AddAbsentFieldValue("Неизвестно гражданство физического лица");
  else
    ExtractXMLFile.str = "<" + elemName + ">" + CodeNum3 + "</" + elemName + ">";
    insert(ExtractXMLFile);
  end;
end;

private macro _ПризнДолжЛиц()
  var ObjCat = RsbObjCategories(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY));
  if(ObjCat.GetMainAttr(34, {curdate}, attr) != 0)
    attr.NumInList = "0";
  end;
  ExtractXMLFile.str = "<ПризнДолжЛиц>" + attr.NumInList + "</ПризнДолжЛиц>";
  insert(ExtractXMLFile);
end;

private macro _ДокУдЛичн()
  var PaperTULFM = 0;

  if(not useObjParty)
    PaperTULFM = fmrequestinfo.PaperTULFM;
  else
    if(not objParty.IsNotResident)
      PaperTULFM = 1;
    end;
  end;
  
  if(PaperTULFM == 0)
    _AddAbsentFieldValue("Не указано назначение удостоверения личности");
  else
    ExtractXMLFile.str = "<ДокУдЛичн>" + PaperTULFM + "</ДокУдЛичн>";
    insert(ExtractXMLFile);
  end;
end;

private macro _GetDefaultPaperCode(PersonID)
  var query = "";
  var rs;
  var params : TArray;
  var PaperCode = "";

  query = query + "SELECT t_Code ";
  query = query + "  FROM dfmpaperlink_dbt ";
  query = query + " WHERE t_DocKind IN (SELECT t_PaperKind ";
  query = query + "                       FROM dpersnidc_dbt ";
  query = query + "                      WHERE t_IsMain = CHR (88) AND t_PersonID = :PersonID) ";
  query = query + "   AND t_LinkKind IN (1, 2) ";

  params = makeArray(SQLParam("PersonID", PersonID)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    PaperCode = rs.value(0);
  end;

  return PaperCode;
end;

private macro _GetPaperName(PaperCode)
  var query = "";
  var rs;
  var params : TArray;
  var PaperName = "";

  query = query + "SELECT t_Name ";
  query = query + "  FROM dfmpaper_dbt ";
  query = query + " WHERE t_Code = :Code ";

  params = makeArray(SQLParam("Code", PaperCode)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    PaperName = rs.value(0);
  end;

  return PaperName;
end;

private macro _СведДокУдЛичн_ВидДокКод(pPaperCode)
  var PaperCode = "";
  
  if(not useObjParty)
    PaperCode = fmrequestinfo.PaperCode;
  else
    PaperCode = pPaperCode;
  end;

  if(PaperCode == "")
    _AddAbsentFieldValue("Не указан код вида удостоверения личности");
  else
    ExtractXMLFile.str = "<ВидДокКод>" + PaperCode + "</ВидДокКод>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокУдЛичн_ВидДокНаименование(pPaperName)
  var PaperName = "";
  
  if(not useObjParty)
    PaperName = fmrequestinfo.PaperName;
  else
    PaperName = pPaperName;
  end;
  
  if(PaperName == "")
    _AddAbsentFieldValue("Не указано наименование вида удостоверения личности");
  else
    ExtractXMLFile.str = "<ВидДокНаименование>" + PaperName + "</ВидДокНаименование>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокУдЛичн_СерияДок(pPaperSeries)
  var PaperSeries = "";
  
  if(not useObjParty)
    PaperSeries = fmrequestinfo.PaperSeries;
  else
    PaperSeries = pPaperSeries;
  end;
  
  ExtractXMLFile.str = "<СерияДок>" + PaperSeries + "</СерияДок>";
  insert(ExtractXMLFile);
end;

private macro _СведДокУдЛичн_НомДок(pPaperNumber)
  var PaperNumber = "";
  
  if(not useObjParty)
    PaperNumber = fmrequestinfo.PaperNumber;
  else
    PaperNumber = pPaperNumber;
  end;
  
  ExtractXMLFile.str = "<НомДок>" + PaperNumber + "</НомДок>";
  insert(ExtractXMLFile);
end;

private macro _СведДокУдЛичн_КемВыданДок(pPaperIssuer)
  var PaperIssuer = "";
  
  if(not useObjParty)
    PaperIssuer = fmrequestinfo.PaperIssuer;
  else
    PaperIssuer = pPaperIssuer;
  end;
  
  if(PaperIssuer == "")
    _AddAbsentFieldValue("Не указан орган, выдавший удостоверение личности");
  else
    ExtractXMLFile.str = "<КемВыданДок>" + PaperIssuer + "</КемВыданДок>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокУдЛичн_ДатВыдачиДок(pPaperIssueDate)
  var PaperIssueDate = date(0, 0, 0);
  
  if(not useObjParty)
    PaperIssueDate = fmrequestinfo.PaperIssueDate;
  else
    PaperIssueDate = pPaperIssueDate;
  end;
  
  if(PaperIssueDate == date(0, 0, 0))
    _AddAbsentFieldValue("Не указана дата выдачи удостоверения личности");
  else
    ExtractXMLFile.str = "<ДатВыдачиДок>" + _GetSchemaFormatDate(PaperIssueDate) + "</ДатВыдачиДок>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокУдЛичн_КодПодр(pPaperIssueCode)
  var PaperIssueCode = "";
  
  if(not useObjParty)
    PaperIssueCode = fmrequestinfo.PaperIssueCode;
  else
    PaperIssueCode = pPaperIssueCode;
  end;
  
  ExtractXMLFile.str = "<КодПодр>" + PaperIssueCode + "</КодПодр>";
  insert(ExtractXMLFile);
end;

private macro _CorrectPaperAttrValue(PaperAttrValue)
  var query = "";
  var rs;
  var params : TArray;
  var AttrValue = "";

  query = query + "SELECT NVL(UPPER(REPLACE(:AttrValue, ' ', '')), CHR(1)) FROM dual";

  params = makeArray(SQLParam("AttrValue", PaperAttrValue)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    AttrValue = rs.value(0);
  end;

  return AttrValue;
end;

private macro _GetDefaultPaperKind(Code, PersonID)
  var query = "";
  var rs;
  var params : TArray;
  var PaperKind = 0;

  query = query + "  SELECT t_PaperKind ";
  query = query + "    FROM dpersnidc_dbt ";
  query = query + "   WHERE t_PaperKind IN (SELECT t_DocKind ";
  query = query + "                           FROM dfmpaperlink_dbt ";
  query = query + "                          WHERE t_Code = :Code AND t_LinkKind IN (1, 2)) ";
  query = query + "     AND t_PersonID = :PersonID ";
  query = query + "ORDER BY t_IsMain DESC, t_PaperKind ";

  params = makeArray(SQLParam("Code", Code)
                    ,SQLParam("PersonID", PersonID)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    PaperKind = rs.value(0);
  end;

  return PaperKind;
end;

private macro _СведДокУдЛичн()
  var PaperCode = "";
  var PaperName = "";
  var PaperKind = 0;
  var PaperSeries = "";
  var PaperNumber = "";
  var PaperIssuer = "";
  var PaperIssueDate = date(0, 0, 0);
  var PaperIssueCode = "";

  if(useObjParty)
    PaperCode = _GetDefaultPaperCode(objParty.PartyID);
    PaperName = _GetPaperName(PaperCode);
    PaperKind = _GetDefaultPaperKind(PaperCode, objParty.PartyID);
    PaperSeries = _CorrectPaperAttrValue(objParty.PersonPaper(PaperKind).Series);
    PaperNumber = _CorrectPaperAttrValue(objParty.PersonPaper(PaperKind).Number);
    PaperIssuer = objParty.PersonPaper(PaperKind).Issuer;
    PaperIssueDate = objParty.PersonPaper(PaperKind).IssuedDate;
    PaperIssueCode = objParty.PersonPaper(PaperKind).IssuerCode;
  end;

  ExtractXMLFile.str = "<СведДокУдЛичн>";
  insert(ExtractXMLFile);

  _СведДокУдЛичн_ВидДокКод(PaperCode);
  _СведДокУдЛичн_ВидДокНаименование(PaperName);
  _СведДокУдЛичн_СерияДок(PaperSeries);
  _СведДокУдЛичн_НомДок(PaperNumber);
  _СведДокУдЛичн_КемВыданДок(PaperIssuer);
  _СведДокУдЛичн_ДатВыдачиДок(PaperIssueDate);
  _СведДокУдЛичн_КодПодр(PaperIssueCode);

  ExtractXMLFile.str = "</СведДокУдЛичн>";
  insert(ExtractXMLFile);
end;

private macro _СведДокПреб_ВидДокКод()
  if(fmrequestinfo.RightVisitDocCode == "")
    _AddAbsentFieldValue("Не указан код вида документа на право пребывания");
  else
    ExtractXMLFile.str = "<ВидДокКод>" + fmrequestinfo.RightVisitDocCode + "</ВидДокКод>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб_ВидДокНаименование()
  if(fmrequestinfo.RightVisitDocName == "")
    _AddAbsentFieldValue("Не указано наименование вида документа на право пребывания");
  else
    ExtractXMLFile.str = "<ВидДокНаименование>" + fmrequestinfo.RightVisitDocName + "</ВидДокНаименование>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб_СерияДокПреб()
  if(fmrequestinfo.RightVisitDocSeries != "")
    ExtractXMLFile.str = "<СерияДокПреб>" + fmrequestinfo.RightVisitDocSeries + "</СерияДокПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб_НомДокПреб()
  if(fmrequestinfo.RightVisitDocNumber != "")
    ExtractXMLFile.str = "<НомДокПреб>" + fmrequestinfo.RightVisitDocNumber + "</НомДокПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб_НачПравПреб()
  if(fmrequestinfo.RightVisitDocDateStart == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата начала срока действия документа на право пребывания");
  else
    ExtractXMLFile.str = "<НачПравПреб>" + _GetSchemaFormatDate(fmrequestinfo.RightVisitDocDateStart) + "</НачПравПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб_ОкончПравПреб()
  if(fmrequestinfo.RightVisitDocDateEnd == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата окончания срока действия документа на право пребывания");
  else
    ExtractXMLFile.str = "<ОкончПравПреб>" + _GetSchemaFormatDate(fmrequestinfo.RightVisitDocDateEnd) + "</ОкончПравПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведДокПреб()
  var PaperTULFM = 0;

  if(not useObjParty)
    PaperTULFM = fmrequestinfo.PaperTULFM;
  end;
  
  if((PaperTULFM == 2) or (PaperTULFM == 3) or (PaperTULFM == 4) or (PaperTULFM == 5))
    ExtractXMLFile.str = "<СведДокПреб>";
    insert(ExtractXMLFile);

    _СведДокПреб_ВидДокКод();
    _СведДокПреб_ВидДокНаименование();
    _СведДокПреб_СерияДокПреб();
    _СведДокПреб_НомДокПреб();
    _СведДокПреб_НачПравПреб();
    _СведДокПреб_ОкончПравПреб();

    ExtractXMLFile.str = "</СведДокПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт_КодМигрКарт()
  if(fmrequestinfo.MigratoryCardCode == "")
    _AddAbsentFieldValue("Неизвестен код вида миграционной карты");
  else
    ExtractXMLFile.str = "<КодМигрКарт>" + fmrequestinfo.MigratoryCardCode + "</КодМигрКарт>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт_СерияМигрКарт()
  if(fmrequestinfo.MigratoryCardSeries != "")
    ExtractXMLFile.str = "<СерияМигрКарт>" + fmrequestinfo.MigratoryCardSeries + "</СерияМигрКарт>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт_НомМигрКарт()
  if(fmrequestinfo.MigratoryCardNumber == "")
    _AddAbsentFieldValue("Неизвестен номер миграционной карты");
  else
    ExtractXMLFile.str = "<НомМигрКарт>" + fmrequestinfo.MigratoryCardNumber + "</НомМигрКарт>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт_ДатНачПреб()
  if(fmrequestinfo.MigratoryCardDateStart == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата начала срока действия миграционной карты");
  else
    ExtractXMLFile.str = "<ДатНачПреб>" + _GetSchemaFormatDate(fmrequestinfo.MigratoryCardDateStart) + "</ДатНачПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт_ДатОкончПреб()
  if(fmrequestinfo.MigratoryCardDateEnd == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата окончания срока действия миграционной карты");
  else
    ExtractXMLFile.str = "<ДатОкончПреб>" + _GetSchemaFormatDate(fmrequestinfo.MigratoryCardDateEnd) + "</ДатОкончПреб>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СведМигрКарт()
  var PaperTULFM = 0;

  if(not useObjParty)
    PaperTULFM = fmrequestinfo.PaperTULFM;
  end;
  
  if((PaperTULFM == 2) or (PaperTULFM == 3) or (PaperTULFM == 4) or (PaperTULFM == 5))
    ExtractXMLFile.str = "<СведМигрКарт>";
    insert(ExtractXMLFile);

    _СведМигрКарт_КодМигрКарт();
    _СведМигрКарт_СерияМигрКарт();
    _СведМигрКарт_НомМигрКарт();
    _СведМигрКарт_ДатНачПреб();
    _СведМигрКарт_ДатОкончПреб();

    ExtractXMLFile.str = "</СведМигрКарт>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ВыпискаФЛ(elemName)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);

  _ФИО("ФИОФЛ");
  _ИНН("ИННФЛ", false);
  _ДатаРожд("ДатаРождФЛ");
  _МестоРожд("МестоРождФЛ");
  _Гражд("ГраждФЛ");
  _ПризнДолжЛиц();
  _ДокУдЛичн();
  _СведДокУдЛичн();
  _СведДокПреб();
  _СведМигрКарт();

  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _ВыпискаИП(elemName)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);

  _ФИО("ФИОИП");
  _ИНН("ИННИП", true);
  _ДатаРожд("ДатаРождИП");
  _МестоРожд("МестоРождИП");
  _Гражд("ГраждИП");
  _ОГРН("ОГРНИП", true);
  _ДатаРег();
  _НаимРегОрг();
  _Адр("АдрИП");
  _ПризнДолжЛиц();
  _ДокУдЛичн();
  _СведДокУдЛичн();
  _СведДокПреб();
  _СведМигрКарт();

  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _АдрРег(elemName)
  var stat = false;
  var adrtypename;
  var ClientType;

  if(not useObjParty)
    ClientType = fmrequestinfo.ClientType;
  else
    if(objParty.LegalForm == PTLEGF_INST)
      ClientType = 1;
    elif((objParty.LegalForm == PTLEGF_PERSN) and (not objParty.IsEmployer))
      ClientType = 2;
    else/*((objParty.LegalForm == PTLEGF_PERSN) and (objParty.IsEmployer))*/
      ClientType = 3;
    end;
  end;
  
  if(ClientType == 2)
    adrtypename = "места жительства";
  end;

  ClearRecord(f_clientadress);
  if(not useObjParty)
    f_clientadress.PartyID = fmrequestinfo.ClientID;
  else
    f_clientadress.PartyID = objParty.PartyID;
  end;
  f_clientadress.Type = 1;
  stat = GetEQ(f_clientadress);
  if(stat)
    Copy(clientadress, f_clientadress);

    ExtractXMLFile.str = "<" + elemName + ">";
    insert(ExtractXMLFile);

    _Адр_КодОКСМ(clientadress, adrtypename);
    _Адр_Индекс(clientadress);
    _Адр_КодСубъектаПоОКАТО(clientadress);
    _Адр_Регион(clientadress);
    _Адр_Пункт(clientadress, adrtypename);
    _Адр_Улица(clientadress);
    _Адр_Дом(clientadress);
    _Адр_Корп(clientadress);
    _Адр_Оф(clientadress);

    ExtractXMLFile.str = "</" + elemName + ">";
    insert(ExtractXMLFile);
  end;

  if((not stat) and (ClientType == 2))
    ClearRecord(f_clientadress);
    if(not useObjParty)
      f_clientadress.PartyID = fmrequestinfo.ClientID;
    else
      f_clientadress.PartyID = objParty.PartyID;
    end;
    f_clientadress.Type = 2;
    stat = GetEQ(f_clientadress);
    if(stat)
      Copy(clientadress, f_clientadress);
    
      ExtractXMLFile.str = "<" + elemName + ">";
      insert(ExtractXMLFile);
    
      _Адр_КодОКСМ(clientadress, adrtypename);
      _Адр_Индекс(clientadress);
      _Адр_КодСубъектаПоОКАТО(clientadress);
      _Адр_Регион(clientadress);
      _Адр_Пункт(clientadress, adrtypename);
      _Адр_Улица(clientadress);
      _Адр_Дом(clientadress);
      _Адр_Корп(clientadress);
      _Адр_Оф(clientadress);
    
      ExtractXMLFile.str = "</" + elemName + ">";
      insert(ExtractXMLFile);
    end;
  end;
  
  if(not stat)
    if(ClientType == 2)
      _AddAbsentFieldValue("Неизвестно место жительства");
    end;
  end;
end;

private macro _АдрМП(elemName)
  var stat = false;
  var adrtypename;
  var ClientType;

  if(not useObjParty)
    ClientType = fmrequestinfo.ClientType;
  else
    if(objParty.LegalForm == PTLEGF_INST)
      ClientType = 1;
    elif((objParty.LegalForm == PTLEGF_PERSN) and (not objParty.IsEmployer))
      ClientType = 2;
    else/*((objParty.LegalForm == PTLEGF_PERSN) and (objParty.IsEmployer))*/
      ClientType = 3;
    end;
  end;
  
  if(ClientType == 2)
    adrtypename = "места пребывания";

    ClearRecord(f_clientadress);
    if(not useObjParty)
      f_clientadress.PartyID = fmrequestinfo.ClientID;
    else
      f_clientadress.PartyID = objParty.PartyID;
    end;
    f_clientadress.Type = 2;
    stat = GetEQ(f_clientadress);
    if(stat)
      Copy(clientadress, f_clientadress);
    
      ExtractXMLFile.str = "<" + elemName + ">";
      insert(ExtractXMLFile);
    
      _Адр_КодОКСМ(clientadress, adrtypename);
      _Адр_Индекс(clientadress);
      _Адр_КодСубъектаПоОКАТО(clientadress);
      _Адр_Регион(clientadress);
      _Адр_Пункт(clientadress, adrtypename);
      _Адр_Улица(clientadress);
      _Адр_Дом(clientadress);
      _Адр_Корп(clientadress);
      _Адр_Оф(clientadress);
    
      ExtractXMLFile.str = "</" + elemName + ">";
      insert(ExtractXMLFile);
    end;
    
    if(not stat)
      if(ClientType == 2)
        _AddAbsentFieldValue("Неизвестно место пребывания");
      end;
    end;
  end;
end;

private macro _Раздел1()
  ExtractXMLFile.str = "<Раздел1>";
  insert(ExtractXMLFile);

/*
  if(fmrequestinfo.ClientType = 2)
  end;
*/
  _АдрРег("АдрРег");
  _АдрМП("АдрМП");

  ExtractXMLFile.str = "</Раздел1>";
  insert(ExtractXMLFile);
end;

private macro _АдрИной(elemName)
  var stat = false;
  var adrtypename;

  var ClientType;

  if(not useObjParty)
    ClientType = fmrequestinfo.ClientType;
  else
    if(objParty.LegalForm == PTLEGF_INST)
      ClientType = 1;
    elif((objParty.LegalForm == PTLEGF_PERSN) and (not objParty.IsEmployer))
      ClientType = 2;
    else/*((objParty.LegalForm == PTLEGF_PERSN) and (objParty.IsEmployer))*/
      ClientType = 3;
    end;
  end;
  
  if((ClientType == 1) or (ClientType == 3))
    adrtypename = "иного адреса";

    ClearRecord(f_clientadress);
    if(not useObjParty)
      f_clientadress.PartyID = fmrequestinfo.ClientID;
    else
      f_clientadress.PartyID = objParty.PartyID;
    end;
    f_clientadress.Type = 2;
    stat = GetEQ(f_clientadress);
    if(stat)
      Copy(clientadress, f_clientadress);
    
      ExtractXMLFile.str = "<" + elemName + ">";
      insert(ExtractXMLFile);
    
      _Адр_КодОКСМ(clientadress, adrtypename);
      _Адр_Индекс(clientadress);
      _Адр_КодСубъектаПоОКАТО(clientadress);
      _Адр_Регион(clientadress);
      _Адр_Пункт(clientadress, adrtypename);
      _Адр_Улица(clientadress);
      _Адр_Дом(clientadress);
      _Адр_Корп(clientadress);
      _Адр_Оф(clientadress);
    
      ExtractXMLFile.str = "</" + elemName + ">";
      insert(ExtractXMLFile);
    end;
  end;
end;

private macro _ReplaceMultiplePhones(phones : string)
  var arrPhones = TArray;
  var tmpPhones = phones;
  var tmpPhone;
  var delim;
  var i;

  delim = index(tmpPhones, ",");
  if((delim <= 0) or ((index(tmpPhones, ";") > 0) and (delim > index(tmpPhones, ";"))))
    delim = index(tmpPhones, ";");
  end;
  while((delim > 0) or (strlen(tmpPhones) > 0))
    if(delim > 0)
      tmpPhone = substr(tmpPhones, 1, delim - 1);
    else
      tmpPhone = tmpPhones;
    end;
    arrPhones[arrPhones.size] = trim(tmpPhone);

    if(delim > 0)
      tmpPhones = substr(tmpPhones, delim + 1);
    else
      tmpPhones = "";
    end;
    
    delim = index(tmpPhones, ",");
    if((delim <= 0) or ((index(tmpPhones, ";") > 0) and (delim > index(tmpPhones, ";"))))
      delim = index(tmpPhones, ";");
    end;
  end;

  tmpPhones = "";
  i = 0;
  while(i < arrPhones.size)
    if((trim(arrPhones[i]) != "") and (index("," + tmpPhones + ",", "," + trim(arrPhones[i]) + ",") <= 0))
      if(tmpPhones != "")
        tmpPhones = tmpPhones + ",";
      end;
      tmpPhones = tmpPhones + trim(arrPhones[i]);
    end;
    i = i + 1;
  end;

  return tmpPhones;
end;

private macro _ТелФакс(elemName)
  var stat = false;
  var phones = "";
  var adrtype = 1;
  var Value = "";

  while(adrtype <= 3)
    ClearRecord(f_clientadress);
    if(not useObjParty)
      f_clientadress.PartyID = fmrequestinfo.ClientID;
    else
      f_clientadress.PartyID = objParty.PartyID;
    end;
    
    if (not FindAdressContactValue(f_clientadress.PartyID, adrtype, CNTADR_PHONENUMBER, Value))
        if(phones != "")
          phones = phones + ",";
        end;
        phones = phones + Value;
    end;
    
    Value = "";
    if(not FindAdressContactValue(f_clientadress.PartyID, adrtype, CNTADR_PHONENUMBER2, Value))
        if(phones != "")
          phones = phones + ",";
        end;
        phones = phones + Value;
    end;
    
    Value = "";
    if(not FindAdressContactValue(f_clientadress.PartyID, adrtype, CNTADR_FAX, Value))
        if(phones != "")
          phones = phones + ",";
        end;
        phones = phones + Value;
    end;

    adrtype = adrtype + 1;
  end;

  phones = _ReplaceMultiplePhones(phones);

  if(phones != "")
    ExtractXMLFile.str = "<" + elemName + ">" + phones + "</" + elemName + ">";
    insert(ExtractXMLFile);
  else
    _AddAbsentFieldValue("Неизвестны номера телефонов, факсов");
  end;
end;

private macro _Раздел2()
  ExtractXMLFile.str = "<Раздел2>";
  insert(ExtractXMLFile);

  _АдрИной("АдрИной");
  _ТелФакс("ТелФакс");

  ExtractXMLFile.str = "</Раздел2>";
  insert(ExtractXMLFile);
end;

private macro _GetRegDocForPart3(ObjectID)
  var query = "";
  var rs;
  var params : TArray;

  query = query + "SELECT                                                                                     ";
  query = query + "   orgd.t_RegDocKind                                                                       ";
  query = query + "  ,NVL(okd.t_Name, CHR(1))                                                                 ";
  query = query + "  ,NVL(ptcd.t_Code, CHR(1))                                                                ";
  query = query + "  ,orgd.t_DocDate                                                                          ";
  query = query + "  ,NVL(ptn.t_Name, CHR(1))                                                                 ";
  query = query + "  ,orgd.t_StartDate                                                                        ";
  query = query + "  ,orgd.t_FinishDate                                                                       ";
  query = query + "  FROM dobjrgdoc_dbt orgd, dobjkdoc_dbt okd, dpartcode_dbt ptcd, dpartyname_dbt ptn        ";
  query = query + " WHERE orgd.t_ObjectType = 3 AND orgd.t_ObjectID = :ObjectID                               ";
  query = query + "       AND orgd.t_IsClosed = CHR(0)                                                        ";
  query = query + "       AND SYSDATE BETWEEN orgd.t_StartDate AND DECODE(orgd.t_FinishDate                   ";
  query = query + "                                                      ,TO_DATE('01010001', 'DDMMYYYY')     ";
  query = query + "                                                      ,TO_DATE('31129999', 'DDMMYYYY')     ";
  query = query + "                                                      ,orgd.t_FinishDate)                  ";
  query = query + "       AND orgd.t_RegDocKind = okd.t_RegDocKind                                            ";
  query = query + "       AND okd.t_ObjectType = orgd.t_ObjectType                                            ";
  query = query + "       AND okd.t_ShortName LIKE '%ЛИЦ%'                                                    ";
  query = query + "       AND ptcd.t_CodeKind = orgd.t_CodeKind                                               ";
  query = query + "       AND ptcd.t_PartyID = orgd.t_ObjectID                                                ";
  query = query + "       AND ptn.t_PartyID(+) = orgd.t_RegPartyID                                            ";
  query = query + "       AND ptn.t_NameTypeID(+) = 1;                                                        ";

  params = makeArray(SQLParam("ObjectID", ObjectID)
                     );
  rs = execSQLselect(query, params, true);

  return rs;
end;

private macro _ПризнНалЛиценз(elemName, strVal)
  ExtractXMLFile.str = "<" + elemName + ">" + strVal + "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _ВидЛиценз(rs/*см. _GetRegDocForPart3*/)
  if(rs.value(1) == "")
    _AddAbsentFieldValue("Не указано описание вида лицензии " + string(rs.value(0)));
  else
    ExtractXMLFile.str = "<ВидЛиценз>" + rs.value(1) + "</ВидЛиценз>";
    insert(ExtractXMLFile);
  end;
end;

private macro _НомЛиценз(rs/*см. _GetRegDocForPart3*/)
  if(rs.value(2) == "")
    _AddAbsentFieldValue("Не указан номер лицензии " + rs.value(1));
  else
    ExtractXMLFile.str = "<НомЛиценз>" + rs.value(2) + "</НомЛиценз>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ДатВыдЛиценз(rs/*см. _GetRegDocForPart3*/)
  if(date(rs.value(3)) == date(0, 0, 0))
    _AddAbsentFieldValue("Не указана дата выдачи лицензии " + rs.value(1));
  else
    ExtractXMLFile.str = "<ДатВыдЛиценз>" + _GetSchemaFormatDate(date(rs.value(3))) + "</ДатВыдЛиценз>";
    insert(ExtractXMLFile);
  end;
end;

private macro _КемВыдЛиценз(rs/*см. _GetRegDocForPart3*/)
  if(rs.value(4) == "")
    _AddAbsentFieldValue("Не указан орган, выдавший лицензию " + rs.value(1));
  else
    ExtractXMLFile.str = "<КемВыдЛиценз>" + rs.value(4) + "</КемВыдЛиценз>";
    insert(ExtractXMLFile);
  end;
end;

private macro _СрокЛиценз(rs/*см. _GetRegDocForPart3*/)
  var strDateFrom, strDateTo;
  if((date(rs.value(5)) == date(0, 0, 0)) and (date(rs.value(6)) == date(0, 0, 0)))
    _AddAbsentFieldValue("Не указан срок лицензии " + rs.value(1));
  else
    strDateFrom = _GetSchemaFormatDate(date(rs.value(5)));
    strDateTo = _GetSchemaFormatDate(date(rs.value(6)));
    if(strDateFrom == "") strDateFrom = "00/00/0000"; end;
    if(strDateTo == "") strDateTo = "00/00/0000"; end;
    ExtractXMLFile.str = "<СрокЛиценз>" + strDateFrom + "-" + strDateTo + "</СрокЛиценз>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ПеречВидДеят(rs/*см. _GetRegDocForPart3*/)
end;

private macro _СведЛиценз(elemName, rs/*см. _GetRegDocForPart3*/)
  ExtractXMLFile.str = "<" + elemName + ">";
  insert(ExtractXMLFile);

  _ВидЛиценз(rs);
  _НомЛиценз(rs);
  _ДатВыдЛиценз(rs);
  _КемВыдЛиценз(rs);
  _СрокЛиценз(rs);
  /*_ПеречВидДеят(rs);*/

  ExtractXMLFile.str = "</" + elemName + ">";
  insert(ExtractXMLFile);
end;

private macro _Раздел3()
  var rs;
  var i = 0;
  ExtractXMLFile.str = "<Раздел3>";
  insert(ExtractXMLFile);

  rs = _GetRegDocForPart3(fmrequestinfo.ClientID);
  while(rs and rs.moveNext())
    if(i == 0)
      _ПризнНалЛиценз("ПризнНалЛиценз", "11");
    end;

    _СведЛиценз("СведЛиценз", rs);
    
    i = i + 1;
  end;

  if(i == 0)
    _ПризнНалЛиценз("ПризнНалЛиценз", "00");
  end;

  ExtractXMLFile.str = "</Раздел3>";
  insert(ExtractXMLFile);
end;

private macro _ПризнНалВП(strVal)
  ExtractXMLFile.str = "<ПризнНалВП>" + strVal + "</ПризнНалВП>";
  insert(ExtractXMLFile);
end;

private macro _Раздел41(rs/*см. _GetBeneficiaryForPart4*/)
  var rs_rgdoc;
  var i = 0;
  ExtractXMLFile.str = "<Раздел41>";
  insert(ExtractXMLFile);

  objParty = RsbParty(rs.value(0));
  useObjParty = true;
  addToAbsentFieldValue = "выгодоприобретателя";
  _GetClientPartyBuffer();

  if(objParty.LegalForm == PTLEGF_INST)
    _ВыпискаЮЛ("СведВПЮЛ");
  end;
  if((objParty.LegalForm == PTLEGF_PERSN) and (not objParty.IsEmployer))
    _ВыпискаФЛ("СведВПФЛ");
  end;
  if((objParty.LegalForm == PTLEGF_PERSN) and (objParty.IsEmployer))
    _ВыпискаИП("СведВПИП");
  end;

  _АдрРег("ВПАдрРег");
  _АдрМП("ВПАдрМП");

  _АдрИной("ВПАдрИной");
  _ТелФакс("ВПТелФакс");

  rs_rgdoc = _GetRegDocForPart3(objParty.PartyID);
  while(rs_rgdoc and rs_rgdoc.moveNext())
    if(i == 0)
      _ПризнНалЛиценз("ВППризнНалЛиценз", "11");
    end;

    _СведЛиценз("ВПСведЛиценз", rs_rgdoc);
    
    i = i + 1;
  end;
  if(i == 0)
    _ПризнНалЛиценз("ВППризнНалЛиценз", "00");
  end;
  
  addToAbsentFieldValue = "";
  useObjParty = false;
  objParty = null;
  _GetClientPartyBuffer();

  ExtractXMLFile.str = "</Раздел41>";
  insert(ExtractXMLFile);
end;

private macro _GetBeneficiaryForPart4()
  var query = "";
  var rs;
  var params : TArray;

  query = query + "SELECT                                                                ";
  query = query + "   ptbf.t_BeneficiaryID                                               ";
  query = query + "  FROM dptbeneficiary_dbt ptbf                                        ";
  query = query + " WHERE     ptbf.t_PartyID = :PartyID                                  ";
  query = query + "       AND SYSDATE BETWEEN     ptbf.t_BeginDate                       ";
  query = query + "                           AND DECODE(ptbf.t_EndDate                  ";
  query = query + "                                     ,TO_DATE('01010001', 'DDMMYYYY') ";
  query = query + "                                     ,TO_DATE('31129999', 'DDMMYYYY') ";
  query = query + "                                     ,ptbf.t_EndDate)                 ";

  params = makeArray(SQLParam("PartyID", fmrequestinfo.ClientID)
                     );
  rs = execSQLselect(query, params, true);

  return rs;
end;

private macro _Раздел4()
  var rs;
  var i = 0;
  ExtractXMLFile.str = "<Раздел4>";
  insert(ExtractXMLFile);

  rs = _GetBeneficiaryForPart4();
  while(rs and rs.moveNext())
    if(i == 0)
      _ПризнНалВП("11");
    end;

    _Раздел41(rs);
    
    i = i + 1;
  end;

  if(i == 0)
    _ПризнНалВП("00");
  end;

  ExtractXMLFile.str = "</Раздел4>";
  insert(ExtractXMLFile);
end;

private macro _ИдентБенефиц(IdentBeneOwner)
  ExtractXMLFile.str = "<ИдентБенефиц>" + IdentBeneOwner + "</ИдентБенефиц>";
  insert(ExtractXMLFile);
end;

private macro _СведБенефиц()
end;

private macro _БенефицАдрРег()
end;

private macro _БенефицАдрМП()
end;

private macro _GetRegBenefOwnerMinPercent()
  var stat;
  var Percent;
  GetRegistryValue("CB\\PARTY\\BENEFOWNER_MIN_PERCENT", V_INTEGER, Percent, stat);
  return Percent;
end;

private macro _CountBeneOwnerForPart5()
  var query = "";
  var rs;
  var params : TArray;
  var Count = 0;

  query = query + "SELECT COUNT(*)                                                       ";
  query = query + "  FROM dptbeneowner_dbt ptbwn                                         ";
  query = query + " WHERE     ptbwn.t_PartyID = :PartyID                                 ";
  query = query + "       AND ptbwn.t_OwnPrc > " + string(_GetRegBenefOwnerMinPercent());
  query = query + "       AND SYSDATE BETWEEN     ptbwn.t_BeginDate                      ";
  query = query + "                           AND DECODE(ptbwn.t_EndDate                 ";
  query = query + "                                     ,TO_DATE('01010001', 'DDMMYYYY') ";
  query = query + "                                     ,TO_DATE('31129999', 'DDMMYYYY') ";
  query = query + "                                     ,ptbwn.t_EndDate)                ";

  params = makeArray(SQLParam("PartyID", fmrequestinfo.ClientID)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    Count = rs.value(0);
  end;

  return Count;
end;

private macro _GetBeneOwnerForPart5()
  var query = "";
  var rs;
  var params : TArray;
  var BeneOwnerID;

  query = query + "SELECT                                                                ";
  query = query + "   ptbwn.t_BeneOwnerID                                                ";
  query = query + "  FROM dptbeneowner_dbt ptbwn                                         ";
  query = query + " WHERE     ptbwn.t_PartyID = :PartyID                                 ";
  query = query + "       AND ptbwn.t_OwnPrc > " + string(_GetRegBenefOwnerMinPercent());
  query = query + "       AND SYSDATE BETWEEN     ptbwn.t_BeginDate                      ";
  query = query + "                           AND DECODE(ptbwn.t_EndDate                 ";
  query = query + "                                     ,TO_DATE('01010001', 'DDMMYYYY') ";
  query = query + "                                     ,TO_DATE('31129999', 'DDMMYYYY') ";
  query = query + "                                     ,ptbwn.t_EndDate)                ";

  params = makeArray(SQLParam("PartyID", fmrequestinfo.ClientID)
                     );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    BeneOwnerID = rs.value(0);
  end;

  return BeneOwnerID;
end;

private macro _GetSubQueryBeneOwnerForParty()
  var query = "";

  query = query + "t.t_PartyID IN (                                                      ";
  query = query + " SELECT                                                               ";
  query = query + "   ptbwn.t_BeneOwnerID                                                ";
  query = query + "  FROM dptbeneowner_dbt ptbwn                                         ";
  query = query + " WHERE     ptbwn.t_PartyID = " + string(fmrequestinfo.ClientID);
  query = query + "       AND ptbwn.t_OwnPrc > " + string(_GetRegBenefOwnerMinPercent());
  query = query + "       AND SYSDATE BETWEEN     ptbwn.t_BeginDate                      ";
  query = query + "                           AND DECODE(ptbwn.t_EndDate                 ";
  query = query + "                                     ,TO_DATE('01010001', 'DDMMYYYY') ";
  query = query + "                                     ,TO_DATE('31129999', 'DDMMYYYY') ";
  query = query + "                                     ,ptbwn.t_EndDate)                ";
  query = query + "               )                                                      ";

  return query;
end;

private macro _Раздел5()
  var stat = 1;
  var skip = 0;
  var stop = 0;
  var IdentBeneOwner = "";
  var CountBeneOwner = _CountBeneOwnerForPart5();
  record ptbeneowner(ptbeneowner);
  var BeneOwnerID = 0;
  array TextConf;
  array BttnConf;

  TextConf(0) = "Вы не выбрали бенефициарного владельца.";
  TextConf(1) = "Укажите вариант дальнейших действий: повторить выбор,";
  TextConf(2) = "не включать данные бенефициарного владельца в анкету,";
  TextConf(3) = "прервать формирование вложения";
  BttnConf(0) = "  повторить  ";
  BttnConf(1) = " не включать ";
  BttnConf(2) = "  прервать   ";

  ExtractXMLFile.str = "<Раздел5>";
  insert(ExtractXMLFile);

  if(CountBeneOwner == 0)
    if(fmrequestinfo.ClientType == 1)
      IdentBeneOwner = "13";
    else
      IdentBeneOwner = "23";
    end;
    stat = 0;
  end;

  if(CountBeneOwner > 1)
    msgbox("У субъекта имеется несколько бенефициарных владельцев. Выберите того, данные которого необходимо включить в анкету");
    
    while((stat != 0) and (stop == 0) and (skip == 0))
      stat = ListPTBENEOWNER(fmrequestinfo.ClientID, ptbeneowner);

      if(stat)
        stat = confwin(TextConf, BttnConf);
        if(stat == 0)
          stat = 1;
        elif(stat == 1)
          stat = 0;
          skip = 1;
          IdentBeneOwner = "23";
        else
          stop = 1;
        end;
      else
        BeneOwnerID = ptbeneowner.BeneOwnerID;
      end;
    end;
  elif(CountBeneOwner != 0)
    BeneOwnerID = _GetBeneOwnerForPart5();
    stat = 0;
  end;

  if((stat == 0) and (BeneOwnerID != 0))
    objParty = RsbParty(BeneOwnerID);
    useObjParty = true;
    addToAbsentFieldValue = "бенефициарного владельца";
    _GetClientPartyBuffer();
    
    if(fmrequestinfo.ClientType == 1)
      if(msgboxex("Является ли бенефициарный владелец " + objParty.FullName + " единоличным исполнительным органом?" , MB_YES + MB_NO, IND_NO, "") == IND_YES)
        IdentBeneOwner = "12";
      else
        IdentBeneOwner = "11";
      end;
    else
      IdentBeneOwner = "21";
    end;

    _ИдентБенефиц(IdentBeneOwner);
    _ВыпискаФЛ("СведБенефиц");
    _АдрРег("БенефицАдрРег");
    _АдрМП("БенефицАдрМП");
    
    addToAbsentFieldValue = "";
    useObjParty = false;
    objParty = null;
    _GetClientPartyBuffer();
  end;

  if((stat == 0) and (BeneOwnerID == 0))
    _ИдентБенефиц(IdentBeneOwner);
  end;

  ExtractXMLFile.str = "</Раздел5>";
  insert(ExtractXMLFile);

  return stat;
end;

private macro _СтепРиск()
  var strVal = "";
  var RiskLevelNumber;
  var RiskLevelName;
  var RiskLevelGround;
  FM_GetActualPtRiskLevel(clientparty.PartyID, RiskLevelNumber, RiskLevelName, RiskLevelGround);

  strVal = RiskLevelName;

  if(RiskLevelGround != "")
    if(strVal != "")
      strVal = strVal + ",";
    end;
    strVal = strVal + RiskLevelGround;
  end;
  
  ExtractXMLFile.str = "<СтепРиск>" + strVal + "</СтепРиск>";
  insert(ExtractXMLFile);
end;

private macro _GetDateRelationBeginClient()
  var query = "";
  var rs;
  var params : TArray;
  var DateBegin = date(0, 0, 0);

  query = query + "SELECT NVL(MIN(t_StartDate), TO_DATE('01010001', 'DDMMYYYY'))               ";
  query = query + "  FROM dclient_dbt                                                          ";
  query = query + " WHERE t_PartyID = :PartyID                                                 ";
  query = query + "       AND t_StartDate != TO_DATE ('01010001', 'DDMMYYYY')                  ";
  query = query + "       AND t_Department IN                                                  ";
  query = query + "              (                                                             ";
  if(true /*fmrequestinfo.ChildDprtInc == "X"*/)
    query = query + "                   SELECT t_Code                                            ";
    query = query + "                     FROM ddp_dep_dbt                                       ";
    query = query + "               START WITH t_Code = :Code                                    ";
    query = query + "               CONNECT BY t_ParentCode = PRIOR t_Code AND t_NodeType = 1    ";
  else
    query = query + "               :Code                                                        ";
  end;
  query = query + "              )                                                             ";

  params = makeArray(SQLParam("PartyID", fmrequestinfo.ClientID)
                    ,SQLParam("Code", fmrequestinfo.SourceDprt)
                     );

  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    DateBegin = date(rs.value(0));
  end;

  return DateBegin;
end;

private macro _GetDateRelationBeginObjcode()
  var query = "";
  var rs;
  var params : TArray;
  var DateBegin = date(0, 0, 0);

  query = query + "SELECT NVL(MIN(t_BankDate), TO_DATE('01010001', 'DDMMYYYY')) ";
  query = query + "  FROM dobjcode_dbt                                          ";
  query = query + " WHERE     t_ObjectID = :PartyID                             ";
  query = query + "       AND t_ObjectType = 3                                  ";
  query = query + "       AND t_BankDate != TO_DATE('01010001', 'DDMMYYYY')     ";

  params = makeArray(SQLParam("PartyID", fmrequestinfo.ClientID)
                     );

  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    DateBegin = date(rs.value(0));
  end;

  return DateBegin;
end;

private macro _ДатНачОтнош()
  var DateBegin = _GetDateRelationBeginClient();
  if(DateBegin == date(0, 0, 0))
    DateBegin = _GetDateRelationBeginObjcode();
  end;
  if(DateBegin == date(0, 0, 0))
    _AddAbsentFieldValue("Неизвестна дата начала отношений с клиентом");
  else
    ExtractXMLFile.str = "<ДатНачОтнош>" + _GetSchemaFormatDate(DateBegin) + "</ДатНачОтнош>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ДатАнкет()
  var Note = readNoteForObject(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY), 1);
  if((Note == null) or (Note == ""))
    _AddAbsentFieldValue("Не указана дата заполнения анкеты клиента");
  else
    ExtractXMLFile.str = "<ДатАнкет>" + _GetSchemaFormatDate(date(Note)) + "</ДатАнкет>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ДатОбнАнкет()
  var Note = readNoteForObject(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY), 45);
  if((Note == null) or (Note == ""))
    _AddAbsentFieldValue("Не указана дата обновления анкеты клиента");
  else
    ExtractXMLFile.str = "<ДатОбнАнкет>" + _GetSchemaFormatDate(date(Note)) + "</ДатОбнАнкет>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИОКуратКО_Фам()
  if(fmrequestinfo.ProxyName1 != "")
    ExtractXMLFile.str = "<Фам>" + fmrequestinfo.ProxyName1 + "</Фам>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИОКуратКО_Имя()
  if(fmrequestinfo.ProxyName2 != "")
    ExtractXMLFile.str = "<Имя>" + fmrequestinfo.ProxyName2 + "</Имя>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИОКуратКО_Отч()
  if(fmrequestinfo.ProxyName3 != "")
    ExtractXMLFile.str = "<Отч>" + fmrequestinfo.ProxyName3 + "</Отч>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ФИОКуратКО()
  ExtractXMLFile.str = "<ФИОКуратКО>";
  insert(ExtractXMLFile);

  _ФИОКуратКО_Фам();
  _ФИОКуратКО_Имя();
  _ФИОКуратКО_Отч();
  
  ExtractXMLFile.str = "</ФИОКуратКО>";
  insert(ExtractXMLFile);
end;

private macro _ДолжнКуратКО()
  if(fmrequestinfo.ProxyPost != "")
    ExtractXMLFile.str = "<ДолжнКуратКО>" + fmrequestinfo.ProxyPost + "</ДолжнКуратКО>";
    insert(ExtractXMLFile);
  end;
end;

private macro _ДопСвед()
end;

private macro _Раздел6()
  ExtractXMLFile.str = "<Раздел6>";
  insert(ExtractXMLFile);

  _СтепРиск();
  _ДатНачОтнош();
  _ДатАнкет();
  _ДатОбнАнкет();
  _ФИОКуратКО();
  _ДолжнКуратКО();
  _ДопСвед();

  ExtractXMLFile.str = "</Раздел6>";
  insert(ExtractXMLFile);
end;

private macro _ИнфЧасть()
  var stat = 0;
  ExtractXMLFile.str = "<ИнфЧасть>";
  insert(ExtractXMLFile);

  if((fmrequestinfo.ClientType == 1) and (f_llvalues.Code != "91"))
    _ВыпискаЮЛ("СведЮЛ");
  end;
  if(fmrequestinfo.ClientType == 2)
    _ВыпискаФЛ("СведФЛ");
  end;
  if((fmrequestinfo.ClientType == 3) and (f_llvalues.Code != "91"))
    _ВыпискаИП("СведИП");
  end;

  if((f_llvalues.Code != "91"))
    _Раздел1();
  end;

  if((f_llvalues.Code != "91"))
    _Раздел2();
  end;

  if((f_llvalues.Code != "91"))
    if((fmrequestinfo.ClientType == 1) or (fmrequestinfo.ClientType == 3))
      _Раздел3();
    end;
  end;
  
  if((f_llvalues.Code != "91"))
    _Раздел4();
  end;
  if((f_llvalues.Code == "09") or (f_llvalues.Code == "91"))
    stat = _Раздел5();
  end;
  if((stat == 0) and (f_llvalues.Code != "91"))
    _Раздел6();
  end;

  ExtractXMLFile.str = "</ИнфЧасть>";
  insert(ExtractXMLFile);

  return stat;
end;

private macro _ВыпискаИзАнкетыВПиБФ()
  var stat = 0;
  ExtractXMLFile.str = "<ВыпискаИзАнкетыВПиБФ>";
  insert(ExtractXMLFile);
  
  _СлужЧасть();
  stat = _ИнфЧасть();

  ExtractXMLFile.str = "</ВыпискаИзАнкетыВПиБФ>";
  insert(ExtractXMLFile);

  return stat;
end;

private macro _CreateExtractXMLFile()
  var stat = 0;
  ExtractXMLFile.str = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>";
  insert(ExtractXMLFile);

  if(_GetBuffers(fmreqiattach))
    stat = _ВыпискаИзАнкетыВПиБФ();
  end;

  return stat;
end;

private macro _GetElemPairArr(regValue)
  var elemPairArr = TArray;
  var strValue = regValue;
  var idxDelim;
  
  idxDelim = index(strValue, ";");
  while(idxDelim > 0)
    elemPairArr[elemPairArr.size] = substr(strValue, 1, idxDelim - 1);
    strValue = substr(strValue, idxDelim + 1);
    idxDelim = index(strValue, ";");
  end;

  return elemPairArr;
end;

private macro _SetElemText(elem, code, delim)
  var strValue = "";
  var ObjCat;
  var Note;

  if(delim == "#")
    ObjCat = RsbObjCategories(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY));
    if(ObjCat.GetFirst(int(code), {curdate}, attr) == 0)
      strValue = attr.NumInList;
      while(ObjCat.GetNext(attr) == 0)
        strValue = strValue + "," + attr.NumInList;
      end;
    end;
  elif(delim == "$")
    ObjCat = RsbObjCategories(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY));
    if(ObjCat.GetFirst(int(code), {curdate}, attr) == 0)
      strValue = attr.Name;
      while(ObjCat.GetNext(attr) == 0)
        strValue = strValue + "," + attr.Name;
      end;
    end;
  elif(delim == "@")
    Note = readNoteForObject(OBJTYPE_PARTY, UniID(clientparty, OBJTYPE_PARTY), int(code));
    if(Note != null)
      strValue = strValue + Note;
    end;
  end;
  
  elem.text = strValue;
end;

private macro _SetChildElem(xmlDoc, root, elemTextPair)
  var idxDelim;
  var ElemName;
  var tmpElemTextPair;
  var elem;

  var delim;
  var i = 0;

  while(i < 4)
    if(i == 0)
      delim = ".";
    elif(i == 1)
      delim = "#";
    elif(i == 2)
      delim = "$";
    else
      delim = "@";
    end;

    idxDelim = index(elemTextPair, delim);
  
    if(idxDelim > 0)
      ElemName = substr(elemTextPair, 1, idxDelim - 1);
      if(ElemName != "")
        tmpElemTextPair = substr(elemTextPair, idxDelim + 1);
  
        elem = root.selectSingleNode(ElemName);
        if(elem == null)
          elem = xmlDoc.createElement(ElemName);
          root.appendChild(elem);
        end;

        if(i == 0)
          _SetChildElem(xmlDoc, elem, tmpElemTextPair);
        else
          _SetElemText(elem, tmpElemTextPair, delim);
        end;
      end;

      return;
    end;
    i = i + 1;
  end;
end;

private macro _SetXmlByElemTextPair(xmlDoc, elemTextPair)
  var root = xmlDoc.documentElement;
  _SetChildElem(xmlDoc, root, elemTextPair);
end;

private macro _AddUserValueByRegistry(xmlDoc)
  var stat : bool;
  var regValue;
  var elemPairArr;
  var i;

  GetRegistryValue("ФИНМОНИТОРИНГ\\REQINFO_CQ_SUBSTS", V_STRING, regValue, stat);
  
  if(regValue != "")
    elemPairArr = _GetElemPairArr(regValue);
    i = 0;
    while(i < elemPairArr.size)
      _SetXmlByElemTextPair(xmlDoc, elemPairArr[i]);
      i = i + 1;
    end;
  end;
end;

macro GetPartyExtract(fmrqa, AttachBody : @string)
  var stat = 0;
  var InfoAvailability = 1; //запрашиваемая информация имеется в полном объёме
  var AttachFileCreateDate = date;
  var AttachFileCreateTime = time;
  var AttachFileName;
  var AvailComment = "";
  var xmlDoc;
  var j = 0;
  var tostring = "";
  
  setbuff(fmreqiattach, fmrqa);

  ClearRecord(f_fmattachkind);
  ClearRecord(f_llvalues);
  f_fmattachkind.AttachKind = fmreqiattach.AttachKind;
  if(GetEQ(f_fmattachkind))
    f_llvalues.List = 3499;
    f_llvalues.Element = f_fmattachkind.RequestInfoKind;
    GetEQ(f_llvalues);
  end;

  ExtractXMLFileName = "..\\workfile\\pt_extract_" + _ПолучитьВременноеИмяФайла(fmreqiattach, "xml");
  
  if(open(ExtractXMLFile, ExtractXMLFileName, "utf8"))
    stat = _CreateExtractXMLFile();

    close(ExtractXMLFile);
  else
    stat = 1;
  end;

  if(stat == 0)
    xmlDoc = CreateXMLParser();
    xmlDoc.async = false;
    xmlDoc.load(ExtractXMLFileName);

    _AddUserValueByRegistry(xmlDoc);

    xmlDoc.save(ExtractXMLFileName);
    xmlDoc = null;
  end;

  if(stat == 0)
    j = 0;
    while(j < AbsentFieldValuesArr.size)
      if(j == 0)
        AvailComment = "Значение отсутствует";
        InfoAvailability = 2; //запрашиваемая информация имеется частично
      end;
      AvailComment = AvailComment + "\n" + AbsentFieldValuesArr[j];
      j = j + 1;
    end;

    fmreqiattach.InfoAvailability = InfoAvailability;
    fmreqiattach.AttachFileCreateDate = AttachFileCreateDate;
    fmreqiattach.AttachFileCreateTime = AttachFileCreateTime;
    AttachFileName = FM_GetReqAttachName(fmreqiattach, "xml");
    fmreqiattach.AttachFileName = AttachFileName;
    if((fmreqiattach.AvailComment != "") and (AvailComment != ""))
      fmreqiattach.AvailComment = fmreqiattach.AvailComment + "\n" + AvailComment;
    end;

    ReadFileToString(ExtractXMLFileName, tostring);
    AttachBody = tostring;
  end;

  return stat;
end;

//GetPartyExtract("fmrqa", "AttachBody");

//macro ReadProcDocOEM
//  var ob = TStreamDoc(ExtractXMLFileName, "r", "rsoem");
//  var v;
//  while(ob.readLine(@v))
//    println(v)
//  end;
//end;
//ReadProcDocOEM;

//macro ReadProcDocUTF8
//  var tostring;
//  ReadFileToString(ExtractXMLFileName, tostring);
//  println();
//  println(tostring);
//end;
//ReadProcDocUTF8;
