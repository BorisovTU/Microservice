/*
Макрос выполнения шага "Оплата" сервисной операции сопровождения ДО
*/

Import InsCarryDoc, "sfsrv_lib.mac";

record SfContr("sfcontr.dbt");
record SfConComRec("sfconcom.dbt");
record SfComissRec("sfcomiss.dbt");

record SfSrvDocRec("sfsrvdoc.dbt");

macro ExecuteStep( outBuff, primDoc )

  var stat = 0;

  setbuff( SfContr, primDoc  );

  var rs, cmd;
  record SfDefCom("sfdef.dbt");
  record SfAccrue("sfaccrue.dbt");

  var PayerAccount = "", ReceiverAccount = "";
  var NDSPayerAccount : string, NDSReceiverAccount : string;

  var prevFIID_CommSum = -1;
  var PayerFIID = -1, ReceiverFIID = -1;

  var SfConComPD : SfConComPrimDoc;
  var IsNVPI:bool;

  var AccTrnID = 0, AccTrnID_NDS = 0;

  var Action = DEFCOM_ACTIONKIND_PAY;
  var Comment = "";

  cmd = RsdCommand( " SELECT * FROM DSFDEFTMP_TMP WHERE t_Action = ? AND t_Skip = chr(0) " +
                    " ORDER BY t_ConID, t_ConComID, t_FIID_CommSum "  );
  cmd.addParam( "", RSDBP_IN, Action );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )    

    SfSrv_FillSfDefComRecord( rs, SfDefCom );
    SfSrv_FillSfAccrueRecord( rs, SfAccrue );
    
    if( prevFIID_CommSum != SfDefCom.FIID_Sum )
      prevFIID_CommSum = SfDefCom.FIID_Sum;
      IsNVPI = SfSrv_IsNVPICarry( SfContr.PayFIID, SfDefCom.FIID_Sum, SfContr.PayRateDateKind );

      SfConComPD = SfConComPrimDoc( 83, SfConComRec, SfContr, SfDefCom.FIID_Sum);

      stat = SfSrv_GetCarryParms( IsNVPI, SfConComPD, SfDefCom, SfAccrue, SfContr,  
                                  @PayerFIID, @ReceiverFIID,
                                  @PayerAccount, @ReceiverAccount, @NDSPayerAccount, @NDSReceiverAccount, 0, 0 );
    end;

    if( stat == 0 )
      if( (SfDefCom.Status == SFDEFCOM_STATUS_CALCULATED) AND (SfComissRec.ReceiverID == {HeadBankID}) )
        if( SfAccrue.Amount > $0 )
          stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContr, SfConComRec, 
                                   PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                                   @AccTrnID, @AccTrnID_NDS, 0, 0 );
        end;
      else
        if( SfAccrue.Amount != $0 )
          stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContr, SfConComRec, 
                                   PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                                   @AccTrnID, @AccTrnID_NDS, 0, 0 );
        end;
      end;

      if( (stat == 0) AND ((SfAccrue.Amount != 0) OR (SfAccrue.NDSAmount != $0)) )
        stat = SfSrv_AddSfDocs( Action, SfDefCom, AccTrnID, AccTrnID_NDS );
      end;
    end;

    if( stat == 0 )
      ChangeSfDefTmpStatus( SfDefCom, SFDEFCOM_STATUS_CALCULATED );
    end;

    SfSrv_InsertAccrueLogRec( SfDefCom, SfAccrue, PayerAccount, ReceiverAccount, 0, stat, Comment );
    
    if( stat == 0 )
      stat = SfSrv_FormDocs( OutBuff, SfContr, SfComissRec, SfDefCom, SfAccrue, 
                             SfConComPD, isNVPI, PayerAccount, ReceiverAccount, SfSrvDocRec.DatePay, false, 0, 0 );
    end;
  end;

  return stat;
end;


macro PostStepAction( message,     /* данный параметр может принимать следующие */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Number_Step, /* номер шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* идентификатор шага операции                     */

  var strQuery = "", cmd;
  setbuff( SfContr, FirstDoc );

  /* Происходит откат */
  if( (message==OP_EXECUTE_STEP) AND errTrn )
    SfSrv_UpdateAccrueLogRec( SfContr, errTrn );
    return 0;    
  end;

  return 0;
end;

private macro GetPayDocsTotalCount( Action )
  
  var count:integer = 0;
  var StrQuery = " SELECT count(*) FROM dsfdeftmp_tmp tmp, dsfcomiss_dbt com WHERE " +
                 " tmp.t_feeType = com.t_feeType AND tmp.t_CommNumber = com.t_Number " +
                 " AND tmp.t_Action = ? AND tmp.t_Skip = chr(0) AND tmp.t_CommSum > 0 " +
                 " AND com.t_InstantPayment = 'X' ";

  var cmd = RsdCommand( StrQuery );
  cmd.addParam( "", RSDBP_IN, Action );

  var rs = RsdRecordset( cmd );  
  
  if( rs.moveNext() )
    count = rs.value(0);
  end;

  return count;

end;

macro MassExecuteStep()

  var stat = 0;

  var rs, cmd;
  record SfDefCom("sfdef.dbt");
  record SfAccrue("sfaccrue.dbt");

  file SfContrFile( sfcontr ) key 0;
  file SfConComFile( sfconcom ) key 2;
  file SfComissFile( sfcomiss ) key 0;

  var PayerAccount = "", ReceiverAccount = "";
  var NDSPayerAccount : string, NDSReceiverAccount : string;

  var prevConID = -1, prevFIID_CommSum = -1, ConComID = 0, prevConComID = -1;
  var PayerFIID = -1, ReceiverFIID = -1;

  var SfConComPD : SfConComPrimDoc;
  var IsNVPI:bool, bNeedNewParms:bool = false;

  var AccTrnID = 0, AccTrnID_NDS = 0;

  var Action = DEFCOM_ACTIONKIND_PAY;
  var Comment = "";
  var ID_Operation = 0, ID_Step = 0;

  var payDocsTotal:integer = GetPayDocsTotalCount( Action );

  var batch:BatchMode; 
  var StrQuery = "";

  stat = SfDefTmp_TransferToReal( true );
  
  if( stat == 0 )
    batch = BatchMode( payDocsTotal );

    StrQuery = " SELECT * FROM DSFDEFTMP_TMP WHERE t_Action = ? AND t_Skip = chr(0) " +
               " AND t_ID NOT IN (SELECT t_SfDefComID FROM dsfrepacc_tmp WHERE t_ErrorCode = 1) " +
               " ORDER BY t_ConID, t_ConComID, t_FIID_CommSum ";

    cmd = RsdCommand( StrQuery );
    cmd.addParam( "", RSDBP_IN, Action );

    rs = RsdRecordset( cmd );
    while( rs.moveNext() )    

      SfSrv_FillSfDefComRecord( rs, SfDefCom );
      SfSrv_FillSfAccrueRecord( rs, SfAccrue );

      ConComID = rs.value("t_ConComID");

      ID_Operation = rs.value("t_ID_Operation");
      ID_Step = rs.value("t_ID_Step");

      if( prevConID != SfDefCom.SfContrID )
        prevConID = SfDefCom.SfContrID;
        SfContrFile.ID = SfDefCom.SfContrID;
        if( not getEQ( SfContrFile ) )
          Comment = "Не найден ДО с ID = " + SfDefCom.SfContrID;
          stat = 1;
        end;
      end;

      if( stat == 0 )
        if( prevConComID != ConComID )
          prevConComID = ConComID;
          prevFIID_CommSum = SfDefCom.FIID_Sum;
          bNeedNewParms = true;
          SfConComFile.ID = ConComID;
          if( not getEQ( SfConComFile ) )
            Comment = "Не найдена комиссия для ДО с ID = " + ConComID;
            stat = 1;
          end;
        elif( prevFIID_CommSum != SfDefCom.FIID_Sum )
          prevFIID_CommSum = SfDefCom.FIID_Sum;
          bNeedNewParms = true;
        end;
      end;
      
      if( stat == 0 )
        if( bNeedNewParms )
          bNeedNewParms = false;
          IsNVPI = SfSrv_IsNVPICarry( SfContrFile.PayFIID, SfDefCom.FIID_Sum, SfContrFile.PayRateDateKind );

          SfConComPD = SfConComPrimDoc( 83, SfConComFile, SfContrFile, SfDefCom.FIID_Sum);

          stat = SfSrv_GetCarryParms( IsNVPI, SfConComPD, SfDefCom, SfAccrue, SfContrFile,  
                                    @PayerFIID, @ReceiverFIID,
                                    @PayerAccount, @ReceiverAccount, @NDSPayerAccount, @NDSReceiverAccount,
                                    ID_Operation, ID_Step );
        end;
      end;

      if( stat == 0 )
        SfComissFile.feeType = SfDefCom.feeType;
        SfComissFile.number  = SfDefCom.commNumber;
        if( not getEQ( SfComissFile ) )
          Comment = "Не найдена комиссия";
          stat = 1;
        end;
      end;

      if( stat == 0 )
        if( (SfDefCom.Status == SFDEFCOM_STATUS_CALCULATED) AND (SfComissFile.ReceiverID == {HeadBankID}) )
          if( SfAccrue.Amount > $0 )
            stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContrFile, SfConComFile, 
                                     PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                                     @AccTrnID, @AccTrnID_NDS, ID_Operation, ID_Step );
          end;
        else
          if( SfAccrue.Amount != $0 )
            stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContrFile, SfConComFile, 
                                     PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                                     @AccTrnID, @AccTrnID_NDS, ID_Operation, ID_Step );
          end;
        end;

        if( (stat == 0) AND ((SfAccrue.Amount != 0) OR (SfAccrue.NDSAmount != $0)) )
          stat = SfSrv_AddSfDocs( Action, SfDefCom, AccTrnID, AccTrnID_NDS );
        end;
      end;

      if( SfDefCom.Status != SFDEFCOM_STATUS_CALCULATED )
        if( not Opr_ChangeSfDefComStatus(SfDefCom.ID, SFDEFCOM_STATUS_CALCULATED, ID_Operation, ID_Step, SfDefCom.FeeType) )
          return false;
        end;
      end;

      SfSrv_InsertAccrueLogRec( SfDefCom, SfAccrue, PayerAccount, ReceiverAccount, 0, stat, Comment );
      
      if( stat == 0 )
        stat = SfSrv_FormDocs( null, SfContrFile, SfComissFile, SfDefCom, SfAccrue, SfConComPD, isNVPI, 
                               PayerAccount, ReceiverAccount, SfSrvDocRec.DatePay, true, ID_Operation, ID_Step );
      end;
    end;

    stat = batch.TransferToBase( false );
  end;

  return stat;
end;