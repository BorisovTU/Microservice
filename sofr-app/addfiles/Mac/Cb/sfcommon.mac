/*

*/

import BankInter, FIInter, SfInter, CTInter, PaymInter, OprInter, 
       CurrInter, globals, likepy, oralib, RsbDataSet, InsCarryDoc;
import dlquery, "cb_sql.mac";

/*
    Счета категорий учета комиссии
*/
/*const OBJROLE_SFCOMISS_INCOME_ACC = 1;*/
const OBJROLE_SFCOMISS_TAX_ACC = 2;
const OBJROLE_SFCOMISS_TRANSIT_ACC = 3;



/*
*/

const SF_CALCAL_TRN_KIND    = 1;
const SF_CALCAL_RST_KIND    = 2;
const SF_CALCAL_OPR_KIND    = 3;
const SF_CALCAL_DOC_KIND    = 4;
const SF_CALCAL_CASH_KIND   = 5;
const SF_CALCAL_SND_KIND    = 6;
const SF_CALCAL_CST_KIND    = 7;
const SF_CALCAL_USR_KIND    = 8;

const SF_SHIFR_OPER_BANK_ORDER = "17";


/*const OBJTYPE_SFDEFCOM = 663; // Удержанная периодич. комиссия*/
const OBJTYPE_SFSINGDF   = 664; // Удержанная разовая комиссия
/*const OBJTYPE_OPRSFCOM = 665; // Удержанная единовременная комиссия*/
const OBJTYPE_SFINVOICE  = 59;

const SFCONTR_DOC = 3001;

const SF_OPR_DK_DEBTOVERDUE = 5100003; // дата выноса на просрочку

const PlusCalc_CatCode = "+Расчеты"; 
const PlusCalcNDS_CatCode = "+Расчеты НДС"; 
const MinusNDSAccrual_CatCode = "-НДС начисленный"; 

const PlusCalcNVPI_CatCode = "+Расчеты,НВПИ"; 
const PlusCalcNDS_NVPI_CatCode = "+Расчеты НДС,НВПИ";
const MinusNDSAccrual_NVPI_CatCode = "-НДС начисленный, НВПИ";

const COMMISS_INVEST_ADVISER_N = "ИнвестСоветник"; 

const SFDOCS_LINKKIND_COMISSOVERDUE = 6;

/*
    Открытие файла для отчета
*/
macro OpenReportFile(file_handle, new_file )
 var Name = FileName(file_handle);

 Name = GetWorkFileName( SubStr(Name,1,Index(Name,".")));

 if( ( Not ExistFile( Name, 1 ) ) or new_file )
  if (Not Create(file_handle,Name))
   MsgBox("Файл ",Name,"|не создан!!!");
   return 1;
  end;
  
 end;

 
 if ( Not Open( file_handle,Name ) )
  MsgBox("Файл ",Name,"|не открыт!!!");
  return 1;
 end;



 return 0;
end;/*OpenReportFile*/

/*открыть временный файл в Global Temporary Table*/
macro OpenReportGlobalFile(file_handle, new_file )

 if ( Not Open( file_handle ) )
  MsgBox("Временный файл ",file_handle," не открыт");
  return 1;
 end;

 if ( new_file and Not Clone( file_handle ) )
  MsgBox("Временный файл ",file_handle," не открыт");
  return 1;
 end;

 return 0;
end;/*OpenReportGlobalFile*/


/*
*/
macro Ins(file_handle)
 var message: string = "";
 var stat ;
 if(Not Insert(file_handle))
  stat = status( message );
  MsgBox("Не вставлена запись в файл ",file_handle, ": ",  stat, " ", message  );
  return 1;
 end;

 return 0;
end;

/*
*/
macro Del(file_handle)
 if(Not Delete(file_handle))
  MsgBox("Не удалена запись из файла ",file_handle);
  return 1;
 end;

 return 0;
end;

       
macro IsIncluded( InvoiceID:integer )
  var select:string;
  var params:TArray;
  var rs:object;
  var included = 0;

  select = " SELECT COUNT(1) FROM DSFDEFCOM_DBT " +
           " WHERE T_ISINCLUDED = 'X'      " +
           " AND T_INVOICEID = :InvoiceID ";

  params = makeArray( SQLParam( "InvoiceID" , InvoiceID ));

  rs = execSQLselect( select, params, FALSE );

  if( rs AND rs.moveNext() )
    included  = rs.value(0);
  end;

  return included;
end;

/* Проверить значение настройки MEMORDERFORCOMM */
macro bBankorderForComm_Setting()
  var flag = false, err;

  GetRegistryValue( "CB\\SERVICEFEE\\BANKORDERFORCOMM", V_BOOL, flag, err );

  if( err != 0 )
    flag = false;
  end;

  return flag;
end;

macro SfComAccrueGetRegValue()
  var Regval:integer;
  var error = 0;

  GetRegistryValue( "COMMON\\КОМИССИИ\\НАЧИСЛЕНИЕ", V_INTEGER, RegVal, error );
    
  if( error != 0)
    RegVal = 1;
  end;

  return RegVal;
end;

macro SfComNotRecalcGetRegValue()
  var Regval:integer = 0;
  var error = 0;

  GetRegistryValue( "COMMON\\КОМИССИИ\\ОПЛАТА_БЕЗ_ПЕРЕСЧЕТА", V_INTEGER, RegVal, error );
    
  if( error != 0)
    RegVal = 0;
  end;

  return RegVal;
end;


macro SfCashSizeGetRegValue()
  var Regval:integer;
  var error = 0;

  GetRegistryValue( "COMMON\\КОМИССИИ\\ПАКЕТНАЯ ВСТАВКА\\РАЗМЕР КЭША", V_INTEGER, RegVal, error );
    
  if( error != 0)
    RegVal = 1000;
  end;

  return RegVal;
end;


/* получить счет */
macro SfCommon_GetAccount( AccBuff, Account, FIID, Chapter )  
  
  file AccR( "account.dbt" );

  ClearRecord( AccR );
  AccR.Chapter       = Chapter;
  AccR.Code_Currency = FIID;
  AccR.Account       = Account;
  if( GetEQ(AccR) )
    Copy( AccBuff, AccR);
    return true;
  end;
  
  return false;
end;

const SFPAY_ERROR_FREE_AMOUNT_NOTENOUGH = 1;
const SFPAY_ERROR_PAYERACC_UNDEF = 2;
const SFPAY_ERROR_RECEIVERACC_UNDEF = 3;
const SFPAY_ERROR_DIFDPRTS = 4;
const SFPAY_ERROR_PAYERACC_NOTAINOURBANK = 5;
const SFPAY_ERROR_RECEIVERACC_NOTAINOURBANK = 6;
const SFPAY_ERROR_CLAIM_IMPOSSIBLE = 7;
const SFPAY_ERROR_FREEAMOUNT = 8;
const SFPAY_ERROR_RECEIVERACC_NOTAINOURBANK_NOCABS = 9;
const SFPAY_ERROR_COMMON = 10;
const SFPAY_ERROR_BOTHACCOUNTS_HAS_CASHTYPE = 11;
const SFPAY_ERROR_CASHORDER_DIFFERCURRENCIES = 12;

private var SfPay_ErrorTxt:TArray = makeArray(
  "Недостаточно средств на счете плательщика. Формирование проводки оплаты невозможно",
  "Невозможно создать проводку оплаты. Неизвестен счет плательщика. Требуется настройка макроса формирования проводок.",
  "Невозможно создать проводку оплаты. Неизвестен счет получателя. Требуется настройка макроса формирования проводок.",
  "Счета плательщика и получателя открыты в разных филиалах банка. Оплата комиссии проводками невозможна.",
  "Для комиссий, плательщиком по которым является наш банк, счет плательщика должен быть открыт в текущем филиале или в on-line филиале банка.",
  "Для комиссий, получателем по которым является наш банк, счет получателя должен быть открыт в текущем филиале или в on-line филиале банка.",
  "Невозможно создать платежное требование к счету, открытому не в нашем банке.",
  "Ошибка в получения свободного остатка на счете.",
  "Для комиссий, плательщиком по которым является наш банк, счет плательщика должен быть открыт в текущем филиале.",
  "Ошибка создания документа оплаты.",
  "Нельзя оплатить комиссию со счета кассы на счет кассы.",
  "Валюта счета кассы отличается от валюты комиссии, кассовый ордер сформировать нельзя."
  );

var SfPay_ErrMsg = "";


macro SfPayErrorTreat( error, bSayErr )

  if( error != SFPAY_ERROR_COMMON )
    SfPay_ErrMsg = SfPay_ErrorTxt[error-1];
    if( bSayErr )
      msgBox( SfPay_ErrMsg );
    end;
  end;
  return error;
end;

macro GetSfPay_ErrMsg()
  
  var ErrTxt = SfPay_ErrMsg;
  if( ErrTxt == "" )
    ErrTxt = GetErrMsg();
  end;
      
  if( ErrTxt == "" )
    ErrTxt = SfPay_ErrorTxt[SFPAY_ERROR_COMMON-1];
  end;
    
  return ErrTxt;
end;


private macro GetAccountDprt( FIID, Account )
  
  record AccountBuf(account);
  var AccDprt = -1;
  ClearRecord(AccountBuf);

  var cmd = RsdCommand( " SELECT t_Department FROM daccount_dbt WHERE t_Account = ? AND t_chapter = 1 AND t_Code_Currency = ? " );
  cmd.addParam( "", RSDBP_IN, Account );
  cmd.addParam( "", RSDBP_IN, FIID );
     
  var rs = RsdRecordset( cmd );
  if( rs.moveNext() )
    AccDprt = rs.value(0);
  else
     if(DL_FindAccount(1, FIID, Account, AccountBuf) == 0)
       AccDprt = AccountBuf.Department;
     end;
  end;
    
  return AccDprt;
end;

private macro GetAccount( AccRec:TRecHandler, Acc, FIID, Chapter )
  var Chapt;

  if( Chapter == null )
    Chapt = 1;
  else
    Chapt = Chapter;
  end;

  var query = " SELECT * FROM daccount_dbt WHERE T_Account = '" + string(Acc) + "' AND T_Chapter = " + string(Chapt);
  query = query + " AND T_Code_Currency = " + string(FIID);

  var rsb = TRsbDataSet( query );
  if( rsb.MoveNext() )
    SetParm( 0, rsb.rec );
    return true;
  end;

  return false;
end;

macro bSfRightFilial( sidebet, sicredit )  
  if( (sidebet.rec.Department == sicredit.rec.Department) AND (sidebet.rec.Department == {OperDprt}) )
    return true;
  else
    return false;
  end;

end;

/* Получить остаток на счете, пересчитанный в валюту оплаты ТО */
macro ПолучитьОстатокПлательщика( AccountFreeRest : @money, Account, FIID, todate, CommFIID )
  var AccRec = TRecHandler("account");
  var q, rsb;
  var stat = 1;

  if( GetAccount(AccRec, Account, FIID) ) 
    stat = 0;
  end;

  if( stat == 0 ) 
    AccGetFreeAmount( AccountFreeRest, NULL, Account, 1/*CHAPT1*/, FIID, date(todate) ); /* с учетом претензий */

    /* с учетом картотек */
    q = " select nvl(sum(ind.t_Sum),0) Sum" 
      + " from dpsindacc_dbt ind"
      + " where ind.t_account = '" + Account + "' "
      + " and ind.t_chapter = " + 1/*CHAPT1*/ + " and ind.t_fiid = " + string(FIID);
    rsb = TRsbDataSet( q );
    if( rsb.MoveNext() )
      AccountFreeRest = AccountFreeRest + rsb.Sum;
    end;

    if( FIID != CommFIID )
      stat = ConvSum( AccountFreeRest, AccountFreeRest, date(todate), FIID, CommFIID );
    end;
  end;

  return stat;
end;

/* Проверить субъекта на принадлежность к нашиму филиалу, а также на вхожднение в ЦАБС */
macro CheckParty( PartyID, IsCheckCABS )
  var q, rsb;

  if( PartyID == 0 )
    PartyID = {OurBank};
  end;

  q = "SELECT t_Code FROM ddp_dep_dbt " +
      " WHERE T_PartyID = " + string(PartyID);

  if( IsCheckCABS )
    q = q + " and t_AccessMode <> 0 ";
  end;

  rsb = TRsbDataSet( q );
  if( rsb.MoveNext() )
    return true;
  end;

  return false;
end;



macro CheckAccountDprt( Acc, FIID, Chapter )
  var AccDprt = 0;
  var Chapt;

  if( Chapter == null )
    Chapt = 1;
  else
    Chapt = Chapter;
  end;

  var query = " SELECT t_Code FROM ddp_dep_dbt dep, daccount_dbt acc WHERE acc.t_Account = ? AND acc.t_Chapter = ? ";
  query = query + " AND acc.t_Code_Currency = ? ";
  query = query + " AND acc.t_Department = dep.t_Code";

  var cmd = RsdCommand( query );
  cmd.addParam( "", RSDBP_IN, Acc );
  cmd.addParam( "", RSDBP_IN, Chapt );
  cmd.addParam( "", RSDBP_IN, FIID );

  var rs = RsdRecordset( cmd );
  if( rs.moveNext() )
    AccDprt = rs.value(0);
  end;

  return AccDprt;
end;


macro ClearTempTable(TableName)
  var cmd : RsdCommand;
  var sqlstr;
  cmd = RsdCommand("DELETE FROM " + TableName);
  cmd.execute();
end;



/* Проверить, инсталлировано ли РКО */
macro Sf_IsInstalledRKO()
  return true;
end;

macro GetRecHandlerFields(rh, alias)
  var ifld = 0;
  var nfld = FldNumber(rh);
  var str = "";

  while(ifld < nfld)
    if(ifld != 0)
      str = str + ",";
    end;

    str = str +" "+ alias + ".t_" + FldName(rh, ifld) + " As " + alias + FldName(rh, ifld);

    ifld = ifld + 1;
  end;

  return str;
end;

macro GetRecHandlerValueByRecordset(rs, rh, alias)
  var ifld = 0;
  var nfld = FldNumber(rh);
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    val = rs.value(alias + FldName(rh, ifld));
    if(ValType(GenGetProp(rh.rec, FldName(rh, ifld))) == V_STRING)
      GenSetProp(rh.rec, FldName(rh, ifld), SQL_ConvTypeStr(val));
    elif((val != null) AND (val != nullval))
      GenSetProp(rh.rec, FldName(rh, ifld), rs.value(alias + FldName(rh, ifld), null, ValType(GenGetProp(rh.rec, FldName(rh, ifld)))));
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;

/**
 @brief Получение названия месяца в именительном падеже
 @param[in] sdate дата
 @return название месяца
*/
macro GetNameMonth(sdate) 
  var day, mon, year;
  dateSplit(sdate, day, mon, year);
  mon = int(mon);
  if ( mon == 1 )
    return "январь";
  elif ( mon == 2 )
    return "февраль";
  elif ( mon == 3 )
    return "март";
  elif ( mon == 4 )
    return "апрель";
  elif ( mon == 5 ) 
    return "май";
  elif ( mon == 6 )
    return "июнь";
  elif ( mon == 7 )
    return "июль";
  elif ( mon == 8 )
    return "август";
  elif ( mon == 9 )
    return "сентябрь";
  elif ( mon == 10 )
    return "октябрь";
  elif ( mon == 11 )
    return "ноябрь";
  elif ( mon == 12 )
    return "декабрь";
  end;
end;

/**
 @brief Получение названия месяца в родительном падеже
 @param[in] sdate дата
 @return название месяца
*/
macro GetNameMonthRP(sdate) 
  var day, mon, year;
  dateSplit(sdate, day, mon, year);
  mon = int(mon);
  if ( mon == 1 )
    return "января";
  elif ( mon == 2 )
    return "февраля";
  elif ( mon == 3 )
    return "мартa";
  elif ( mon == 4 )
    return "апреля";
  elif ( mon == 5 ) 
    return "мая";
  elif ( mon == 6 )
    return "июня";
  elif ( mon == 7 )
    return "июля";
  elif ( mon == 8 )
    return "августа";
  elif ( mon == 9 )
    return "сентября";
  elif ( mon == 10 )
    return "октября";
  elif ( mon == 11 )
    return "ноября";
  elif ( mon == 12 )
    return "декабря";
  end;
end;

/**
 @brief Получение сокращенного ФИО клиента
 @param[in] partyid идентификатор клиента
 @return сокращенное ФИО клиента
*/
macro GetFIO(partyid:integer):string
  var sql, cmd, rs;
  sql = "select * from dparty_dbt where t_partyid = ? ";
  cmd = RSDCommand(sql);
  cmd.AddParam("partyid", RSDBP_IN, partyid);
  rs = RSDRecordSet(cmd);
  if(rs.MoveNext())
    return rs.value("t_shortName");
  end;
  return "";
end;

/**
 @brief Получение года из даты
 @param[in] sdate дата
 @return int-овое значение года
*/
macro GetYear(sdate:date):integer
  var day, mon, year;
  dateSplit(sdate, day, mon, year);
  year = int(year);
  return year;
end;

/**
 @brief Получение дня из даты
 @param[in] sdate дата
 @return int-овое значение дня
*/
macro GetDay(sdate:date):integer
  var day, mon, year;
  dateSplit(sdate, day, mon, year);
  day = int(day);
  return day;
end;

/**
 @brief Получение номера ДБО
 @param[in] dogid идентификатор субдоговора
 @param[in] DogNum номер субдоговора
 @return номер ДБО, если он не найден, то номер субдоговора
*/
macro GetNameDBO(dogid:integer, DogNum:string):string
  var sql, cmd, rs;
  sql = "select sf.* from ddlcontrmp_dbt mp, ddlcontr_dbt dl, dsfcontr_dbt sf  where mp.t_sfcontrid = ? and dl.t_dlcontrid = mp.t_dlcontrid and sf.t_id = dl.t_sfcontrid";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  rs = RSDRecordSet(cmd);
  if(rs.MoveNext())
    return rs.value("t_number");
  end;

  if((ValType(DogNum) != V_UNDEF) and (DogNum != ""))
    return DogNum;
  end;
  return "субдоговору c ID = " + string(dogid);
end;

/**
 @brief Получение даты заключения ДБО
 @param[in] dogid идентификатор субдоговора
 @return дата заключения ДБО
*/
macro GetDateDBO(dogid:integer):date
  var sql, cmd, rs;
  sql = "select sf.* from ddlcontrmp_dbt mp, ddlcontr_dbt dl, dsfcontr_dbt sf  where mp.t_sfcontrid = ? and dl.t_dlcontrid = mp.t_dlcontrid and sf.t_id = dl.t_sfcontrid";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  rs = RSDRecordSet(cmd);
  if(rs.MoveNext())
    return SQL_ConvTypeDate(rs.value("t_datebegin"));
  end;
  return date(0,0,0);
end;

/**
 @brief Получение кода ЕКК по договору
 @param[in] dogid идентификатор субдоговора
 @return код ЕКК
*/
macro GetEKKDBO(dogid:integer):string 
  var sql, cmd, rs;
  sql = "select code.* from ddlcontrmp_dbt mp, ddlobjcode_dbt code where mp.t_sfcontrid = ? and code.t_objecttype = 207 and code.t_objectid = mp.t_dlcontrid and code.t_codekind = 1";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  rs = RSDRecordSet(cmd);
  if(rs.MoveNext())
    return rs.value("t_code");
  end;
  return "";
end;

/**
 @brief Получение структуры субдоговора по его идентификатору
 @param[in]  SFContrID идентификатор субдоговора
 @param[out] SFContr   структура субдоговора, соответствующая записи в dsfcontr_dbt
 @return true, если субдоговор найден в БД
 @return false, если субдоговор не найден
*/
macro GetSfContrBuf(SFContrID:integer, SFContr:@variant)
  var sql, cmd, rs;
  sql = "select * from dsfcontr_dbt t where t.t_id = ?";
  cmd = DL_RsdCommand();
  cmd.AddParam(SFContrID);
  rs = cmd.execute(sql);
  if(rs.movenext)
    SFContr = rs.GetRecord();
    return true; 
  end;
  return false;
end;

/**
 @brief Получение идентификатора субдоговора по периодической комиссии
 @param[in] SFDefID идентификатор периодической комиссии
 @return идентификатор субдоговора
*/
macro GetSfIDFromSFDEF(SFDefID:integer)
  var sql, cmd, rs;
  sql = "select sfdef.t_sfcontrid from dsfdef_dbt sfdef where sfdef.t_feetype = 1 and sfdef.t_id = ? ";
  cmd = DL_RsdCommand();
  cmd.AddParam(SFDefID);
  rs = cmd.execute(sql);
  if(rs.movenext)
    return rs.SFContrID;
  end;  
  return 0;  
end;

/**
 @brief Получение структуры связки комиссии с договором обслуживания
 @param[in]  SFContrID идентификатор субдоговора
 @param[in]  SFComCode код комиссии
 @param[out] SFContr   структура связки комиссии с договором обслуживания, соответствующая записи в dsfconcom_dbt
 @return true, если связка найдена в БД
 @return false, если связка не найдена
*/
macro GetSfConCom(SFContrID:integer, SFComCode:string, SFConCom:@variant)
  var sql, cmd, rs;
  sql = String( "select concom.* from dsfconcom_dbt concom, dsfcomiss_dbt com where\n" 
               ,"concom.t_feetype = com.t_feetype and concom.t_commnumber = com.t_number\n"  
               ,"and upper(com.t_code) = ?\n"  
               ,"and concom.t_objecttype = 659 and concom.t_objectid = ?");
  cmd = DL_RsdCommand();
  cmd.AddParam(StrUpr(SFComCode));
  cmd.AddParam(SFContrID);
  rs = cmd.execute(sql);
  if(rs.movenext)
    SFConCom = rs.GetRecord();
    return true; 
  end;
  return false;
end;

/**
 @brief Получение клиентского счёта
 @param[in] dogid идентификатор субдоговора
 @param[in] CatID идентификатор категории учёта
 @param[in] fiid  идентификатор валюты
 @return номер счёта
*/
macro GetClientAccount(dogid, CatID, fiid) 
  var sql, cmd, rs, Birgacc = "";
  var AccBuf = TRecHandler("account");
  AccBuf.Clear();
  sql = "select mc.* from dsfcontr_dbt sf, dmcaccdoc_dbt mc where sf.t_id = ? and MC.T_CATID = ? and mc.t_iscommon = 'X' and mc.t_clientcontrid = sf.t_id and mc.t_currency = ?";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  cmd.AddParam("BirgId", RSDBP_IN, CatID);
  cmd.AddParam("fiid", RSDBP_IN, fiid);
  rs = RSDRecordSet(cmd);
  if(rs.MoveNext())
    Birgacc = rs.value("t_account");
  end;
  return Birgacc;
end;

/**
 @brief Получиение кода комиссии
 @param[in] CommNumber код комиссии
 @param[in] FeeType    тип взимания комиссии
 @return код комиссии
*/                    
macro GetComCode(CommNumber:integer, FeeType:integer):string
  var cmd, DataSet; 

  cmd = RSDCommand("SELECT t_Code " +
                   "  FROM dsfcomiss_dbt " +
                   " WHERE t_FeeType = ? " +
                   "   AND t_Number = ? "); 
  cmd.AddParam("", RSDBP_IN, FeeType);
  cmd.AddParam("", RSDBP_IN, CommNumber);

  cmd.Execute();
  DataSet = TRsbDataSet(cmd);
  if(DataSet.moveNext())
    return DataSet.Code;
  end;

  return "";
end;

/**
 @brief Проверка на то, что комиссия вынесена на просрочку. Получаем запросом актуальное состояние признака, т.к. без выхода из скроллинга шагов структура не обновляется
 @param[in] FeeType тип взимания комиссии
 @param[in] SfDefID идентификатор комиссии
 @return true, если комиссия вынесена на просрочку
 @return false, если комиссия НЕ вынесена на просрочку
*/                    
MACRO SfDefIsDebtOverdue(FeeType:integer, SfDefID:integer):bool
  var cmd, DataSet;
  cmd = DL_RSDCommand("SELECT 1 " +
                      "  FROM dsfdef_dbt " +
                      " WHERE t_FeeType = ? " +
                      "   AND t_ID = ? " +
                      "   AND t_IsDebtOverdue = 'X' ");
  cmd.addParam(FeeType);
  cmd.addParam(SfDefID);                                                          
  DataSet = cmd.Execute();
  if(DataSet.MoveNext())
    return true;
  end; 
  return false;
END;
