/*
$Name:         oprkind.mac
$Module:       Главная книга
$Description:  Получение подходящих под фильтр видов операций
*/

import OprInter;
import RSD;
import RsbDataSet;
import globals;

const FirstUserKindOperation = 5000;

// Проверить системые типы операции и фильтра на соответствие
macro CB_CheckSysTypes(
    FltKind:Integer // как фильтровать операции при выборе
   ,FltSysTypes:String // набор символов фильтра
   ,SysTypes:String // набор символов операции
):Bool

    var isValid:Bool = true;
    var symbCount:Integer = 1;
    var symbNumber:Integer = 0;

    // только полное совпадение типов
    if( FltKind == OPPF_FITALL )
        if( StrLen( SysTypes ) != StrLen( FltSysTypes ) )
            isValid = false;
        end;
    end;

    // все символы фильтра есть в операции
    if( ( FltKind == OPPF_FITALLINOPR ) or ( FltKind == OPPF_FITALL ) )
        if( isValid )
            symbCount = 1;
            symbNumber = StrLen( SysTypes );

            if( ( symbNumber == 0 ) and StrLen( FltSysTypes ) != 0 )
                isValid = false;
            end;

            while( isValid and ( symbCount <= symbNumber ) )
                if( Index( SysTypes, SubStr( FltSysTypes, symbCount, 1 ) ) == 0 )
                    isValid = false;
                end;

                symbCount = symbCount + 1;
            end;
        end;
    end;

    // все символы операции есть в фильтре
    if( FltKind == OPPF_FITALLINFLT )
        symbCount = 1;
        symbNumber = StrLen( FltSysTypes );

        if( ( symbNumber == 0 ) and StrLen( SysTypes ) != 0 )
            isValid = false;
        end;

        while( isValid and ( symbCount <= symbNumber ) )
            if( Index( FltSysTypes, SubStr( SysTypes, symbCount, 1 ) ) == 0 )
                isValid = false;
            end;

            symbCount = symbCount + 1;
        end;
    end;

    // хотя бы один символ из фильтра есть в операции
    if( FltKind == OPPF_FITONEINOPR )
        if( ( StrLen( FltSysTypes ) != 0 ) and ( StrBrk( SysTypes, FltSysTypes ) == 0 ) )
            isValid = false;
        end;
    end;

    // хотя бы один символ из операции есть в фильтре
    if( FltKind == OPPF_FITONEINFLT )
        if( ( StrLen( SysTypes ) != 0 ) and ( StrBrk( FltSysTypes, SysTypes ) == 0 ) )
            isValid = false;
        end;
    end;

    return isValid;
end;

// Получить первый подходящий вид операции
macro CB_GetFirstValidOpenOpKind(
    DocKind:Integer // номер первичного документа
   ,FltKind:Integer // как фильтровать операции при выборе
   ,FltSysTypes:String // набор символов фильтра
):Integer

    var opKind:Integer = 0;
    var query:String = "";
    var rsdCmd = null;
    var dataSet = null;
    var isFind:Bool = false;

    if( DocKind > 0 ) // ALL_KIND_OF_DOC
        query =
            " SELECT "
              + " koper.t_Kind_Operation "
             + " ,koper.t_SysTypes "
          + " FROM "
              + " doprkoper_dbt koper "
          + " WHERE "
              + " koper.t_NotInUse = CHR( 0 ) "
          + " AND koper.t_Kind_Operation > ? "
          + " AND koper.t_DocKind IN ( SELECT "
                                       + " kdoc.t_DocKind "
                                   + " FROM "
                                       + " doprkdoc_dbt kdoc "
                                   + " START WITH "
                                       + " kdoc.t_DocKind = ? "
                                   + " CONNECT BY "
                                   + " PRIOR kdoc.t_ParentDocKind = kdoc.t_DocKind ) ";

        rsdCmd = RsdCommand( query );

        if( {BPromUse} )
            rsdCmd.AddParam( "", RSDBP_IN, FirstUserKindOperation );
        else
            rsdCmd.AddParam( "", RSDBP_IN, 0 );
        end;
        rsdCmd.AddParam( "", RSDBP_IN, DocKind );
        rsdCmd.Execute();

        dataSet = TRsbDataSet( rsdCmd );

        while( ( dataSet.MoveNext() ) and ( not isFind ) )
            if( CB_CheckSysTypes( FltKind, FltSysTypes, dataSet.SysTypes ) )
                opKind = dataSet.Kind_Operation;
                isFind = true;
            end;
        end;
    end;

    return opKind;
end;
