import globals, cb_sql;

macro RecreateLogTable()
  /* Создаем таблицу протокола */
  SQL_Execute(
      "DECLARE " +
        "e_tab_not_exist EXCEPTION; " +
        "PRAGMA EXCEPTION_INIT( e_tab_not_exist, -942 ); " +
      "BEGIN " +
        "EXECUTE IMMEDIATE 'SELECT 1 FROM dcngptdpsfpl_log'; " +
      "EXCEPTION WHEN e_tab_not_exist THEN " +
        "EXECUTE IMMEDIATE 'CREATE GLOBAL TEMPORARY TABLE dcngptdpsfpl_log ( " +
                                  "t_party        NUMBER(10), " +
                                  "t_code         VARCHAR2(35), " +
                                  "t_shortname    VARCHAR2(60), " +
                                  "t_dprt         NUMBER(5), " +
                                  "t_dprtname     VARCHAR2(12), " +
                                  "t_servkind     NUMBER(5), " +
                                  "t_servkindname VARCHAR2(25), " +
                                  "t_stat         NUMBER(5), " +
                                  "t_errmsg       VARCHAR2(300)) " +
                                  "ON COMMIT PRESERVE ROWS'; " +
      "END;");

  SQL_Execute("DELETE FROM dcngptdpsfpl_log");
end;

macro ReportPrintHeader(SfPlanID, SfPlanName, CngDate)

  RecreateLogTable();

[Протокол назначения тарифного плана ########## ################################################################] (SfPlanID, SfPlanName);
[по видам обслуживания клиентов в филиалах на дату начала действия ##########                                   ] (CngDate);
[                                                                                                               ];
[                                                                                                               ];
[Дата и время выполнения: ########## ########                                                                   ] (date(), time());
[           Пользователь: ##### ################################################################################] ({oper}, {Name_Oper});
[                                                                                                               ];
[-------------------+---------------------------+------------+---------------------------+-----------+-------------------------------------------];
[ Код               | Наименование субъекта     | Узел ТС    | Вид обслуживания          | Действие  | Примечание                                ];
[-------------------+---------------------------+------------+---------------------------+-----------+-------------------------------------------];
end;

macro ReportPrintLine(party, code, shortname, dprt, dprtname, servkind, servkindname, stat, errmsg)
   var sqlStr;

   sqlStr = "INSERT INTO dcngptdpsfpl_log ( " +
              "t_party, " +
              "t_code, " +
              "t_shortname, " +
              "t_dprt, " +
              "t_dprtname, " +
              "t_servkind, " +
              "t_servkindname, " +
              "t_stat, " +
              "t_errmsg) " +
            "VALUES (" + 
            "  " + party        + " " + 
            ",'" + code         + "'" + 
            ",'" + shortname    + "'" + 
            ", " + dprt         + " " + 
            ",'" + dprtname     + "'" + 
            ", " + servkind     + " " + 
            ",'" + servkindname + "'" + 
            ", " + stat         + " " + 
            ",'" + errmsg       + "');";

   SQL_Execute(sqlStr);
end;

macro CountClients()
  var rs, sqlStr;
  sqlStr = "SELECT NVL(COUNT(*), 0) FROM (SELECT DISTINCT t_party FROM dcngptdpsfpl_log WHERE t_stat = 0);";
  rs = SQL_ExecuteAndGetRs(sqlStr);
  rs.moveNext(); 
  return int(rs.value(0));
end;

macro CountDepartments()
  var rs, sqlStr;
  sqlStr = "SELECT NVL(COUNT(*), 0) FROM (SELECT DISTINCT t_dprt FROM dcngptdpsfpl_log WHERE t_stat = 0);";
  rs = SQL_ExecuteAndGetRs(sqlStr); 
  rs.moveNext(); 
  return int(rs.value(0));
end;

macro CountServKinds()
  var rs, sqlStr;
  sqlStr = "SELECT NVL(COUNT(*), 0) FROM (SELECT DISTINCT t_servkind FROM dcngptdpsfpl_log WHERE t_stat = 0);";
  rs = SQL_ExecuteAndGetRs(sqlStr); 
  rs.moveNext(); 
  return int(rs.value(0));
end;

macro ReportPrintFooter()
  var rs, sqlStr, binded;
  var countPt, countDp, countSk : integer;

   sqlStr = "SELECT " +
               "t_code, " +
               "t_shortname, " +
               "t_dprt, " +
               "t_dprtname, " +
               "t_servkind, " +
               "t_servkindname, " +
               "t_stat, " +
               "NVL(t_errmsg,' ') " +
              "FROM dcngptdpsfpl_log " +
             "ORDER BY t_code, t_dprt, t_servkind;";

  rs = SQL_ExecuteAndGetRs(sqlStr); 

  while(rs.moveNext())

    if(rs.value(6) > 0)
      binded = "не назначен";
    else
      binded = "назначен";
    end;
                                                                             
[################### ########################### ############ ########################### ########### ###########################################] (rs.value(0),rs.value(1),rs.value(3),rs.value(5),binded,rs.value(7));
  end;

  countPt = CountClients();

  countDp = CountDepartments();

  countSk = CountServKinds();

[                                                                                                               ];
[                                                                                                               ];
[Тарифные планы изменены у ########## клиентов в ##### филиале(филиалах) по ##### видам обслуживания            ] (countPt, countDp, countSk);
end;