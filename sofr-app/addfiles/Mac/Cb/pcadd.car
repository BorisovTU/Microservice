/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ОДБ"                                   */
/*                                                                          */
/*                                                                          */
/*  Имя файла: pcadd.CAR                                                    */
/*  Создан: 26.11.1998                                        Стасевич В.   */
/*                                                                          */
/****************************************************************************/
/*****************************************************************************
    Изменения
Михайлов С., 16.11.2000: НайтиСчетПоКатегории теперь не ругается
Михайлов С., 23.11.2000: InsertPcCarry
*****************************************************************************/
IMPORT InsCarryDoc, BankInter, pcproc;
IMPORT pccarry;
import mc_lib;

FILE perc( pcacc );
FILE percacnt( pcacnt );

RECORD din( arhdoc );


/*------------------------------------------------------------------------
  Процедура получения кода валюты по номеру категории учета
--------------------------------------------------------------------------*/
MACRO НайтиВалютуПоКатегории( НомерКатегории )
  percacnt.AutoPerc = perc.AutoKey;
  percacnt.AcntCode = НомерКатегории;

  if(( GetGE( percacnt ) )                      and
     ( percacnt.AutoPerc == perc.AutoKey ) and
     ( percacnt.AcntCode == НомерКатегории    )     )
    return percacnt.FIID;
  else
    return -1;
  end;
END;

/*------------------------------------------------------------------------
  Процедура получения номера лицевого по номеру категории учета
--------------------------------------------------------------------------*/
MACRO НайтиСчетПоКатегории( НомерКатегории )
  percacnt.AutoPerc = perc.AutoKey;
  percacnt.AcntCode = НомерКатегории;

  if(( GetGE( percacnt ) )                      and
     ( percacnt.AutoPerc == perc.AutoKey ) and
     ( percacnt.AcntCode == НомерКатегории    )     )
    return percacnt.Account;
  else
    СтрокаОшибки = string("Не найдена запись по счету для категории учета:",НомерКатегории);
/*    MsgBox(СтрокаОшибки);*/
    return "";
  end;
END;

/*┌──────────────────────────┐*/
/*│ Точка входа в программу  │*/
/*└──────────────────────────┘*/
MACRO ExecuteStep( Buffer, arhdoc )
  /*ROV 18.07.00*/
  var Kind_OperB;           /*вид операции для балансового документа из Кредитов*/
  var Kind_OperNB;          /*вид операции для внебалансового документа из Кредитов*/
  var err;
  var stat;

  var СчетОВП ;  /* Номер лицевого счета ОВП (одинаковый для всех
                           глав и валют */

  stat = 0;

  setbuff( din,  arhdoc );
  setbuff( mc_out, Buffer   );

  copy( dout, din );

  if( dout.Account_Payer == "" )
     return 1;
  end;

  if( dout.Account_Receiver == "" )
     return 1;
  end;

  perc.AutoKey = din.CarryAcnt;
  getEQ( perc );
  din.CarryAcnt = 0;

  /*ROV 18.07.00*/
  if ((perc.ObjectType == PC_CRD_CON) or
      (perc.ObjectType == PC_CRD_EXP) or
      (perc.ObjectType == PC_CRD_LIM) or
      (perc.ObjectType == PC_DUTY) or
      (perc.ObjectType == PC_EXP_PC))
     GetRegistryValue( "КРЕДИТЫ_ДЕПОЗИТЫ\\КРЕДИТЫ\\ПЕРЕМЕННЫЕ\\KIND_OPER_BAL", V_INTEGER, Kind_OperB, err );
     if ( err != 0 ) Kind_OperB = 6; end;
     GetRegistryValue( "КРЕДИТЫ_ДЕПОЗИТЫ\\КРЕДИТЫ\\ПЕРЕМЕННЫЕ\\KIND_OPER_NOBAL", V_INTEGER, Kind_OperNB, err );
     if ( err != 0 ) Kind_OperNB = 1; end;
     if( dout.AutoKey == 5 )
       dout.Kind_Oper = " " + substr(string(Kind_OperNB), 1, 1);
     else
       dout.Kind_Oper = " " + substr(string(Kind_OperB), 1, 1);
     end;
  end;

  if( dout.Sum == $0 )
    return 0;
  end;

  if( not ПолучитьСчетОВП (СчетОВП, dout.chapter, dout.code_currency, 4501 ) )
    СчетОВП = "ОВП";
  end;

  if  ( dout.AutoKey == 1 )
     dout.AutoKey = 0;
     InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );

  elif( dout.AutoKey == 2 )
     dout.AutoKey = 0;
     InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );

  elif( dout.AutoKey == 3 )
     dout.AutoKey = 0;
     if( not БылаПросрочка( perc ) )
        /*1*/

        if( НайтиСчетПоКатегории(НомКатСчетПолучатель) == "" )
           СтрокаОшибки = "";
           InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
        else
           InsertPcCarry( dout.Code_Currency, НайтиВалютуПоКатегории(НомКатСчетПолучатель), СчетОВП, СчетОВП );
        end;
     else
        /*2*/

        InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
     end;

  elif( dout.AutoKey == 4 )
     dout.AutoKey = 0;
     if( НайтиСчетПоКатегории(НомКатСчетПлательщик) == "" )
        СтрокаОшибки = "";
        InsertPcCarry( dout.Code_Currency, dout.Code_Currency, СчетОВП, СчетОВП );
     else
        InsertPcCarry( НайтиВалютуПоКатегории(НомКатСчетПлательщик), dout.Code_Currency, СчетОВП, СчетОВП );
     end;

  elif( dout.AutoKey == 5 )
     dout.AutoKey = 0;
     dout.Chapter          = ГлаваВ;

     InsertPcCarry( din.Code_Currency, НайтиВалютуПоКатегории(НомКатСчетДляКорреспонденции), СчетОВП, СчетОВП );
  end;

  if( СтрокаОшибки == "" )
     return 0;
  else
     msgbox(СтрокаОшибки);
     return 1;
  end;
END;
