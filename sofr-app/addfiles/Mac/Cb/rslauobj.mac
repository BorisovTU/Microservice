/*
 $Name: rslauobj.mac
 $Module: Ядро ГКБО 
 $Description: Поддержка локальной аутентификации LAU сообщений SWIFT
 */
 /*
    Поддержка локальной аутентификации LAU сообщений SWIFT
 */
import BankInter, rcw, RSD, "globals.mac";

private var Simulate = false;

macro CreateRSLAUObj(interface, rsax : @variant)
    var RSLAUObj;

    if (isStandAlone())  
        // Если двузвенка
        RSLAUObj = ActiveX (interface);
    else
        // Если трехзвенка              
        rsax = CreateObject ("rsax","TRsAxServer","RsAxServer", isStandAlone());
        RSLAUObj = rsax.CreateComObject (interface);
    end;
    return RSLAUObj;
OnError(err)
    RunError("", "Не удалось создать интерфейс " + interface);
end;

private macro RSLAULIB_PutString(str)
    var tmp = "";
    EncodeBase64(str, tmp);
    return tmp;
end;

private macro RSLAULIB_GetString(str)
    var tmp = "";
    DecodeBase64(str, tmp);
    return tmp;
end;

// Функция управления резидентными ключами LAU
macro RSLAULIB_ProcessKeys(oper, cIdentProgram, errorText)
    var stat = 0;

    if (Simulate)
        return 0;
    end;

    var rsax;
    var obj = CreateRSLAUObj("RSLAUlib.RSLAUObj", rsax);

    obj.cIdentProgram = cIdentProgram;
    stat = obj.loadKeys(oper);
    if (stat)
        SetParm(2, obj.lastErrorText());
    end;

    obj = NULL;
    rsax = NULL;

    return stat;
OnError(error)
    SetParm(2, error.err);
    return -1;
end;

// Функция выгрузки резидентных ключей LAU
macro RSLAULIB_UnloadKeys(oper, errorText)
    var stat = 0;

    if (Simulate)
        return 0;
    end;

    var rsax;
    var obj = CreateRSLAUObj("RSLAUlib.RSLAUObj", rsax);
    stat = obj.unloadKeys(oper);

    if (stat)
        SetParm(1, obj.lastErrorText());
    end;

    obj = NULL;
    rsax = NULL;

    return stat;

OnError(error)
    SetParm(1, error.err);
    return -1;
end;

// Функция формирования метки подлинности LAU
macro RSLAULIB_CreateAuthTag(inIds, inMsgs, inOper, cIdentProgram, inNoUI, outIds, outMsgs, errorText)
    var stat = 0;
    var rsax;
    var i = 0;

    if (Simulate)
        if (inIds.size > 2)
            while(i < inIds.size)
                if (i < 2)
                    SetParm(7, "Признак ошибки обработки ЭС в составе пакета ЭС");
                end;
                outIds[outIds.size] = inIds[i];
                outMsgs[outMsgs.size] = inMsgs[i];
                i = i + 1;
            end;
        else
            SetParm(7, "Признак ошибки обработки ЭС в составе пакета ЭС");
            stat = 1;
        end;
        return stat;
    end;

    var obj = CreateRSLAUObj("RSLAUlib.RSLAUObj", rsax);
    obj.cIdentProgram = cIdentProgram;

    var inMsgObj = obj.createMsgObj();
    var outMsgObj = obj.createMsgObj();

    inMsgObj.useBase64 = true;
    outMsgObj.useBase64 = true;

    while(i < inIds.size)
        inMsgObj.add(RSLAULIB_PutString(inIds[i]), RSLAULIB_PutString(inMsgs[i]), "");
        i = i + 1;
    end;

    stat = obj.createAuthTag(inMsgObj, inOper, inNoUI, outMsgObj);

    if (stat)
        SetParm(7, obj.lastErrorText());
    else
        while (outMsgObj.next())
            outIds[outIds.size] = RSLAULIB_GetString(outMsgObj.msgId);
            outMsgs[outMsgs.size] = RSLAULIB_GetString(outMsgObj.msg);
        end;
    end;

    return stat;

OnError(error)
    SetParm(7, error.err);
    return -1;
end;

// Функция проверки метки подлинности LAU
macro RSLAULIB_CheckAuthTag(inIds, inMsgs, inHash, inOper, cIdentProgram, inNoUI, outIds, outMsgs, outResult, errorText)
    var stat = 0;
    var rsax;
    var i = 0;

    if (Simulate)
        if (inIds.size > 2)
            while(i < inIds.size)
                if (i < 2)
                    SetParm(9, "Признак ошибки обработки ЭС в составе пакета ЭС");
                end;
                outIds[outIds.size] = inIds[i];
                outMsgs[outMsgs.size] = inMsgs[i];
                outResult[outResult.size] = 0;
                i = i + 1;
            end;
        else
            SetParm(9, "Признак ошибки обработки ЭС в составе пакета ЭС");
            stat = 1;
        end;
        return stat;
    end;

    var obj = CreateRSLAUObj("RSLAUlib.RSLAUObj", rsax);
    obj.cIdentProgram = cIdentProgram;

    var inMsgObj = obj.createMsgObj();
    var outMsgObj = obj.createMsgObj();

    inMsgObj.useBase64 = true;
    outMsgObj.useBase64 = true;

    while(i < inIds.size)
        inMsgObj.add(RSLAULIB_PutString(inIds[i]), RSLAULIB_PutString(inMsgs[i]), RSLAULIB_PutString(inHash[i]));
        i = i + 1;
    end;

    stat = obj.checkAuthTag(inMsgObj, inOper, inNoUI, outMsgObj);

    if (stat)
        SetParm(9, obj.lastErrorText());
    else
        while (outMsgObj.next())
            outIds[outIds.size] = RSLAULIB_GetString(outMsgObj.msgId);
            outMsgs[outMsgs.size] = RSLAULIB_GetString(outMsgObj.msg);
            outResult[outResult.size] = outMsgObj.checkResult;
        end;
    end;

    return stat;

OnError(error)
    SetParm(9, error.err);
    return -1;
end;