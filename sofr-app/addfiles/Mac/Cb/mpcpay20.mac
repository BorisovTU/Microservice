/*
 $Name: mpcpay20.mac
 $Module: RSACCOUNT
 $Description: Операция "Оплата по ПД". Шаг "Расчет суммы".
*/
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.00.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcpay20.mac

  Библиотека       : RSACCOUNT

  Описание         : Операция "Оплата по ПД". Шаг "Расчет суммы".

  Программист      : Kirakozov Michael (KMB)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,  CTInter, InsCarryDoc, globals, mpclb;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    PaySum = $0,
    ContractID = 0,
    DocID = 0;


// Проверка существования цепочки периодов начисления, полностью
// соответствующих периоду от BeginDate до EndDate
PRIVATE MACRO CheckForEqualPeriod(ContractID, BeginDate, EndDate);
var cmd,rs,cmd2,rs2;

   cmd = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
   "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODBEGINDATE = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
       cmd2 = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
      "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODENDDATE = ?");
      cmd2.addParam( "", RSDBP_IN, ContractID );
      cmd2.addParam( "", RSDBP_IN, EndDate );
      cmd2.execute(); 
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())
         return true;
      end;
   end;

   return false;
END;

// Проверка существования периодов доначисления, частично попадающих
// в период от BeginDate до EndDate 
PRIVATE MACRO CheckForPartlyAddChargePeriods(ContractID, BeginDate, EndDate)
var cmd,rs;

   cmd = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
   "where T_SCHEDULEKIND = 2 and T_SPECIALTYPE = 1 and T_CONTRACTID = ? " +
   "and ((T_PERIODBEGINDATE < ? and T_PERIODENDDATE >= ?) or " +
   "(T_PERIODBEGINDATE <= ? and T_PERIODENDDATE > ?))");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
         return true;
   end;

   return false;
END;   

private macro UpdatePayState(ID, BeginDate, EndDate)
    var cmd = RSDCommand ("select T_CNTSCHEDID, T_PAYSTATE from DPRCCNTSCHED_DBT where T_CONTRACTID = ? " 
                      "   AND T_PAYSTATE = 7 AND T_SCHEDULEKIND = 2 OR T_SCHEDULEKIND = 1 "
                      "   AND T_PERIODBEGINDATE <= ? "
                      "   AND T_PERIODENDDATE >= ?");
    cmd.addParam("", RSDBP_IN, ID);
    cmd.addParam("", RSDBP_IN, BeginDate);
    cmd.addParam("", RSDBP_IN, EndDate);
    
    var rs = RsdRecordset(cmd);
    
    while (rs.MoveNext())
        PrcSetPayState(rs.value(0), PRC_PAYSTATUS_AWARDED, rs.value(1));
    end;

    return true;    
end;

MACRO ExecuteStep(Buffer, Document)
   var   cmd,rs,
         PeriodSum = $0,
         flagUnCalcPeriod = false,
         CntSchedID = 0,
         RoundSumDelta = $0,
         PrevRoundSumDelta = $0;
         
   SetBuff( cntopr_buf, Document );
   ContractID = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   prccntopr.KeyNum = 0;  
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      //msgbox ("Не найдена операция оплаты");
      PrcSendMessage(ContractID, "Не найдена операция оплаты");
      return 1;
   end; 

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;  
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      //msgbox ("Не найден договор с ID = " + ContractID);
      PrcSendMessage(ContractID, "Не найден договор с ID = " + ContractID);
      return 1;
   end;
   
   // 1.
   CntSchedID = GetCntSchedID(ContractID, 3, prccntopr.rec.OperDate);
   if (CntSchedID != 0)
      cntsched.KeyNum = 0;  
      cntsched.rec.CntSchedID = CntSchedID;
      if (not cntsched.GetEQ)
         //msgbox ("Не найден период оплаты на дату" + prccntopr.rec.OperDate);
         PrcSendMessage(ContractID, "Не найден период оплаты на дату" + prccntopr.rec.OperDate);
         return 1;
      end;
   else
      //msgbox ("Не найден период оплаты на дату" + prccntopr.rec.OperDate);
      PrcSendMessage(ContractID, "Не найден период оплаты на дату" + prccntopr.rec.OperDate);
      return 1;
   end;  

    if ((cntsched.rec.SumCalc != $0.0) 
        and (cntsched.rec.PayState == PRC_PAYSTATUS_SUSPENDED) 
        and (cntopr_buf.IsRecalc == "")
    )
        return 0;
    end;

    if (cntsched.rec.PayState == PRC_PAYSTATUS_SUSPENDED)
        if (cntopr_buf.IsRecalc == "")
            PrcSendMessage(ContractID, "В выбранном периоде начисление и оплата процентов приостановлены. Оплата без перерасчета процентов запрещена");
            return 1;
        else
            if (IsOprMultiExec())
                PrcSendMessage(ContractID, "В выбранном периоде начисление и оплата процентов приостановлены");
                return 1;
            else
                var ret = MsgBoxEx ("В выбранном периоде начисление и оплата процентов приостановлены. Выполнить операцию с перерасчетом процентов?", 
                    MB_YES + MB_NO, IND_NO);
                    
                if (ret == IND_NO)
                    return 1;
                else
                    UpdatePayState(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate);
                end;
            end;
        end;
    end;
    
   // 2.
   if ((cntsched.rec.datereal != date (0,0,0)) and (valtype(cntsched.rec.datereal) == V_DATE) and (cntopr_buf.IsRecalc == ""))
      return 0;
   // 3.
   elif (prccntopr.rec.IsRecalc == "X")
      PaySum = CalcPercentForPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate);
      PrevRoundSumDelta = GetPrevRoundSumDelta(ContractID, 3, cntsched.rec.periodbegindate);
      PaySum = round(PaySum + PrevRoundSumDelta, 5);
      RoundSumDelta = PaySum - round(PaySum, 2);

      // SCR 245616 погрешность округления ("дельта"), не распределенная на момент отражения процентов в бухгалтерском учете (оплате), 
      //            в дальнейшем расчете не участвует и обнуляется перед расчетом графиков (начисления и оплаты процентов) на следующий период.
      RoundSumDelta = $0;
   else      
      // ищем цепочку периодов начисления, соотв. нашему периоду оплаты
      if (CheckForEqualPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate))

         // 4.1.1
         if (CheckForPartlyAddChargePeriods(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate))
            //msgbox ("За этот период сумма оплаты не может быть рассчитана");
            PrcSendMessage(ContractID, "За этот период сумма оплаты не может быть рассчитана");
            return 0;
         end;
         
         // 4.1.2
         cmd = RSDCommand ("select T_SUMCALC, T_DATEREAL, T_DATE, T_ROUNDSUMDELTA from DPRCCNTSCHED_DBT " +
               "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and T_PERIODBEGINDATE >= ? " +
               "and T_PERIODENDDATE <= ?");
         cmd.addParam( "", RSDBP_IN, ContractID );
         cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
         cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
         cmd.execute();
         rs = TRsbDataSet(cmd);
         
         while (rs.movenext()) 
            if (ValType(rs.sumcalc) == V_UNDEF)
              PeriodSum = 0;
            else
              PeriodSum = rs.sumcalc;
            end;

            PaySum = PaySum + PeriodSum;
            
            if (ValType(rs.roundsumdelta) == V_UNDEF)
              RoundSumDelta = 0;
            else
              RoundSumDelta = rs.roundsumdelta;
            end;
            
            if ((ValType(rs.DateReal) == V_UNDEF) or (rs.DateReal == date(0,0,0)))
               flagUnCalcPeriod = true;
            end;
         end;

         // если хотя бы один из периодов начисления не рассчитан, то вычисляем сумму с помощью ХП
         if (flagUnCalcPeriod)
            PaySum = CalcPercentForPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate, @RoundSumDelta);
            PrevRoundSumDelta = GetPrevRoundSumDelta(ContractID, 3, cntsched.rec.periodbegindate);
            PaySum = round(PaySum + PrevRoundSumDelta, 5);
            RoundSumDelta = PaySum - round(PaySum, 2);

            // SCR 245616 погрешность округления ("дельта"), не распределенная на момент отражения процентов в бухгалтерском учете (оплате), 
            //            в дальнейшем расчете не участвует и обнуляется перед расчетом графиков (начисления и оплаты процентов) на следующий период.
            RoundSumDelta = $0;
         end;

      else
         PaySum = CalcPercentForPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate, @RoundSumDelta);
         PrevRoundSumDelta = GetPrevRoundSumDelta(ContractID, 3, cntsched.rec.periodbegindate);
         PaySum = round(PaySum + PrevRoundSumDelta, 5);
         RoundSumDelta = PaySum - round(PaySum, 2);
      end;
      
   end;


   // SCR 245616 погрешность округления ("дельта"), не распределенная на момент отражения процентов в бухгалтерском учете (оплате), 
   //            в дальнейшем расчете не участвует и обнуляется перед расчетом графиков (начисления и оплаты процентов) на следующий период.
   RoundSumDelta = $0;

   cmd = RSDCommand ("select T_SUMCALC,  T_DATEREAL, T_ROUNDSUMDELTA, T_CNTSCHEDID from DPRCCNTSCHED_DBT " +
         "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and T_PERIODBEGINDATE >= ? " +
         "and T_PERIODENDDATE <= ? ORDER BY T_PERIODENDDATE DESC");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
   cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   
   IF (rs.movenext()) 
     
      var PeriodSum2 = 0;
      var DateReal2 = date(0,0,0);
      var CntschedID2 = 0;

      if (ValType(rs.sumcalc) == V_UNDEF)
        PeriodSum2 = 0;
      else
        PeriodSum2 = rs.sumcalc;
      end;

      if ( (ValType(rs.DateReal) != V_UNDEF))
         DateReal2 = date(rs.DateReal);
      end;

      if ( (ValType(rs.CntschedID) != V_UNDEF))
         CntschedID2 = rs.CntschedID;
      end;

     if (PrcSetCntSchedSumByID(CntschedID2, PeriodSum2, DateReal2, RoundSumDelta) != 0)
        return 1;
     end;
   end;

   // 5.
   PaySum = money(round(PaySum,2));

   // 6.
   if (PrcSetCntSchedSumByID(CntSchedID, PaySum, prccntopr.rec.BankDate, RoundSumDelta) != 0)
      return 1;
   end;

   if (PrcSetPayState(CntSchedID, PRC_PAYSTATUS_NOT_CLAIMED, cntsched.rec.PayState) != 0)
      return 1;
   end;
  
   return 0;  
END;


MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */ 

  exit(1); 

END;
