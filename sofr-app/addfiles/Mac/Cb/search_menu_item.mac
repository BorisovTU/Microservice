/*
$Name:        search_menu_item.mac
$Module:      Сервис ГКБО
$Description: Макрос поиска пункта меню
*/

import Rsd; 

//Поиск искомого модуля среди системных и пользовательских модулей.
macro SearchMenuItem(IdentSubSystem, NameSubSystem, NameModule, NameMacro)
  var sql = "";
  var subsql = "";
  var nameModuleSql = nameModule;
  var nameMacroSql = nameMacro;
  nameModuleSql = StrSubst(nameModuleSql, "*", "%");
  nameModuleSql = StrSubst(nameModuleSql, "?", "_");
  nameMacroSql = StrSubst(nameMacroSql, "*", "%");
  nameMacroSql = StrSubst(nameMacroSql, "?", "_");

  if(nameModuleSql == "")
    if(nameMacroSql != "")
      nameModuleSql = "%";
    end;
  end;

  var str = "Путь к пункту меню \"" + NameModule + "\" с функцией \"" + NameMacro + "\"";
  println(str:100:c);
  str = "Подсистемы \"" + NameSubSystem + "\""; 
  println(str:100:c);

  var numtab = 0;

  while(numtab < 2)
    str = "ditemsyst_dbt";
    if(numtab == 1)
      str = "ditemuser_dbt";
    end;
    sql = "SELECT T_OBJECTID, T_IIDENTPROGRAM, T_INUMBERPOINT, T_ISTEMPLATE, dmenuitem_dbt.T_SZNAMEITEM, rsb_struct.getstring(to_blob(subquery.t_parm))";
    sql = sql + "FROM dmenuitem_dbt,";    
    sql = sql + "(SELECT T_ICASEITEM, T_SZNAMEITEM, T_PARM FROM " + str + " WHERE ";
    sql = sql + "REPLACE(LOWER(T_SZNAMEITEM), '~') LIKE LOWER('" + nameModuleSql + "') AND ";
    if(nameMacroSql != "")
      sql = sql + "LOWER(rsb_struct.getstring(to_blob(T_PARM))) LIKE LOWER ('" + nameMacroSql + "') AND ";
    end;
    sql = sql + "T_CIDENTPROGRAM = '" + StrFor(IdentSubSystem) + "') subquery WHERE ";
    sql = sql + "TRIM (REPLACE (dmenuitem_dbt.T_SZNAMEITEM, '~')) = TRIM (subquery.T_SZNAMEITEM) AND ";
    sql = sql + "t_istemplate = CHR (0) AND dmenuitem_dbt.T_ICASEITEM = subquery.T_ICASEITEM";

    var cmd = RsdCommand(sql);
    cmd.execute;
    var rs = RsdRecordset(cmd);
    var num = 1;
    

    while(rs.moveNext)
      sql = "SELECT t_sznameitem, t_inumberpoint FROM dmenuitem_dbt ";
      sql = sql + "START WITH t_objectid = " + rs.value(0) + " AND t_iidentprogram = " + rs.value(1) + " AND t_inumberpoint = " + rs.value(2) + " AND t_istemplate = chr(0) ";
      sql = sql + "CONNECT BY PRIOR t_inumberfather = t_inumberpoint AND PRIOR t_objectid = t_objectid ";
      sql = sql + "AND PRIOR t_iidentprogram = t_iidentprogram AND PRIOR  t_istemplate = t_istemplate ";
      sql = sql + "ORDER SIBLINGS  BY t_inumberpoint";

      var cmd2 = RsdCommand(sql);
      cmd2.execute;

      var rs2 = RsdRecordset(cmd2);

      str = "";

      while(rs2.moveNext)
        if(str == "")
          str = StrSubst(rs2.value(0), "~", "");
        else
          str = StrSubst(rs2.value(0), "~", "") + " - " + str;  
        end;
      end;

      print("\n", num:5, ".   ", rs.value(0), "   ", str);
      num = num + 1;

      if(nameMacroSql != "")
        println("\n                 ", rs.value(5));
      end;  
    end;

    numtab = numtab + 1;
  end;
end;