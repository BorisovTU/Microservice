/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Интерфейсные функции для импорта в справочник участников SWIFT          */
/*  из внешних источников                                                   */
/*                                                                          */
/*  Имя файла: bicimp.mac                                                   */
/*  Создан:  21.01.00                                        Васильев Д.В.  */
/****************************************************************************/

Import PTInter;

const PARTCODE_KEY_PK = 0,
      PARTCODE_KEY_KC = 1,
      PARTY_KEY_SHORTNAME = 1, 
      LEGAL_FORM_ORGANIZATION = 1,
      KIND_CODE        = 1,
      KIND_BIC_RUS     = 3,
      KIND_BIC_ISO     = 6,
      KIND_CHIPS_UID   = 10,
      KIND_BIC_PLUS    = 11,
      KIND_NATIONAL_ID = 12,
      OWN_KIND_BANK    = 2,
      OWN_KIND_SWIFT   = 10,
      KIND_CORESP      = 11,
      KIND_ABA         = 19;

const Mes_Code        = "Код",
      Mes_BIC_RUS     = "БИК ЦБ РФ",
      Mes_BIC_ISO     = "BIC",
      Mes_CHIPS_UID   = "CHIPS UID",
      Mes_BIC_PLUS    = "BIC+",
      Mes_NATIONAL_ID = "Нац. код",
      Mes_ABA         = "ABA";

const PARTCODE_ACTIVE = 0,
      PARTCODE_NOTACTIVE = 1;

const MesAllRight            = "Ok!",
      MesErrorInsertParty    = "Ошибка добавления записи в файл Party.dbf",
      MesErrorInsertPtbicdir = "Ошибка добавления записи в файл PtBicdir.dbt",
      MesErrorInsertPartCode = "Ошибка добавления записи в файл PartCode.dbt",
      MesErrorInsertPartyOwn = "Ошибка добавления записи в файл PartyOwn.dbt",
      MesErrorUpdateParty    = "Ошибка изменения записи в файле Party.dbf",
      MesErrorUpdatePtbicdir = "Ошибка изменения записи в файле PtBicdir.dbt",
      MesErrorUpdatePartCode = "Ошибка изменения записи в файле PartCode.dbt",
      MesErrorUpdatePartyOwn = "Ошибка изменения записи в файле PartyOwn.dbt",
      MesErrorDeleteParty    = "Ошибка удаления записи из файла Party.dbf",
      MesErrorDeletePtbicdir = "Ошибка удаления записи из файла PtBicdir.dbt",
      MesErrorDeletePartCode = "Ошибка удаления записи из файла PartCode.dbt",
      MesErrorDeletePartyOwn = "Ошибка удаления записи из файла PartyOwn.dbt",
      MesErrorNoFoundRusBank = "Не найден российский банк ",
      MesErrorNoFoundBankForUpdate = "Не найден банк для внесения изменений ",
      MesErrorNoFoundBankForDelete = "Не найден банк для удаления ",
      MesFoundSurerior         = "Найден вышестоящий банк - ";

const RU_Code = "RU", 
      SET_CHAR = "X", 
      UNSET_CHAR = "",
      EMPTY_VALUE = "Z";

var Error, BankErrorMessage:String, BankErrorExtInfo:string;

FILE    Party(party) write;
FILE    ptbicdir(ptbicdir) write;
FILE    PartCode(partcode) write key 1;
FILE    PartyOwn(partyown) write;
FILE    Corschem(corschem) key 4;


/* отсечение пробелов справа */
macro TrimRight( str )
  var i = strlen(str);
  while( i > 0 )
    if(SubStr(str,i,1)!=" ")
      return SubStr(str,1,i);
    end;
    i = i - 1;
  end;
  return "";
end;

/* отсечение всех пробелов справа, кроме одного */
macro TrimRight1( str )
  var i = strlen(str);
  if(SubStr(str,i,1)!=" ") return str; end;
  while( i > 0 )
    if(SubStr(str,i,1)!=" ")
      return SubStr(str,1,i) + " ";
    end;
    i = i - 1;
  end;
  return "";
end;

macro YYMMDDDate(Str)
  var vYear,vDate,vMon,
      sYear=SubStr(Str,1,2),sDate=SubStr(Str,5,2),sMon=SubStr(Str,3,2);
  if(sYear=="")
    vYear = 0;
  else
    vYear = int(sYear); /* YY */
    if( vYear > 50 ) /* вот так ловко мы решаем Проблему 2000-го года ;) */
      vYear = vYear + 1900;
    else
      vYear = vYear + 2000;
    end;
  end;
  if(sMon=="")
    vMon = 0;
  else
    vMon = int(sMon); /* YY */
  end;
  if(sDate=="")
    vDate = 0;
  else
    vDate = int(sDate); /* YY */
  end;
  return date(vDate,  /* DD */
              vMon,  /* MM */
              vYear);
end;

Macro PrintBankInformation( PartyID )
    var City, NameKind, Code, Info, oldPartyKey, oldkey, NoPrint, oldKeyPtbicdir;
    var posParty, posPartCode, posPtBicDir;
    var changePosParty, changePosPartCode, changePosBicDir;
        
    posParty    = GetPos( Party );
    posPartCode = GetPos( PartCode );
    posPtBicDir = GetPos( PtBicDir );

    NoPrint = false;
    oldPartyKey = KeyNum( Party, 0);    
    Party.PartyID = PartyID;
    if ( GetEQ(Party) )
       City = Party.Place;
       oldkey = KeyNum( PartCode, PARTCODE_KEY_PK );
       PartCode.PartyID = Party.PartyID;
       PartCode.CodeKind = KIND_BIC_ISO;
       if ( GetEQ(PartCode) ) /* У банка есть код BIC_ISO */
          Code = PartCode.Code;
          NameKind = Mes_BIC_ISO;
       else
          /* Найдем первый попавшийся код (BIC_ISO у этого банка нет) */
          PartCode.PartyID = Party.PartyID;
          PartCode.CodeKind = 0;
          if ( (GetGE(PartCode)) AND (PartCode.PartyID==Party.PartyID) )
             if ( PartCode.CodeKind==KIND_CODE )  NameKind = Mes_Code;
             elif ( PartCode.CodeKind==KIND_BIC_RUS ) NameKind = Mes_BIC_RUS;
             elif ( PartCode.CodeKind==KIND_BIC_ISO ) NameKind = Mes_BIC_ISO;
             elif ( PartCode.CodeKind==KIND_CHIPS_UID ) NameKind = Mes_CHIPS_UID;
             elif ( PartCode.CodeKind==KIND_BIC_PLUS ) NameKind = Mes_BIC_PLUS;
             elif ( PartCode.CodeKind==KIND_NATIONAL_ID ) NameKind = Mes_NATIONAL_ID;
             elif ( PartCode.CodeKind==KIND_ABA ) NameKind = Mes_ABA;
             end;
             Code = PartCode.Code;
          else
             NoPrint = true;
          end;
       end;
       KeyNum( PartCode, oldKey );

       oldKeyPtbicdir = KeyNum( ptbicdir, 0 );
       ptbicdir.PartyID = PartyID;
       if ( GetEQ(ptbicdir) )
          Info = ptbicdir.BranchInformation;
       else
          Info = "";
       end;
       KeyNum( ptbicdir, oldKeyPtbicdir );

       if ( NoPrint==false )
          [          #################### ######### ########### #]
          (substr(City,1,20), NameKind, Code, Info);
       end;
    end;
    KeyNum( Party, oldPartyKey);

    if ( posPartCode ) GetDirect( PartCode, posPartCode ); end;
    if ( posParty    ) GetDirect( Party, posParty );       end;    
    if ( posPtBicDir ) GetDirect( PtBicDir, posPtBicDir ); end;
end;

/* класс объект-источник данных импорта
   Подлежит переопределению */
class BICsource
  /* Данные общие для импорта из формата BICDIR и BIC+ */
  var MFO_Depart, PartyID, Flag_ABA_Directory, FindCreatin;
  var Tag_Identifier, Modification_Flag, BIC_Key, CountryCode, Institution_Name1,
      Institution_Name2, Institution_Name3, Branch_Information1, Branch_Information2,
      City_Heading, Subtype_Indication, Extra_Information, Location1, Location2, Location3,
      Country_Name1, Country_Name2, POB_Number, POB_Location1, POB_Location2, POB_Location3,
      POB_Country_Name1, POB_Country_Name2;

  var Value_Added_Services;
  var Physical_Address;

  /* Данные для формата BIC+ */
  var ZIP_Code,
      POB_ZIP_Code,
      CHIPS_UID,
      National_ID,
      BIC_RUS,
      BIC_Plus_Key,
      Update_Date,
      Activation_Date,
      Deactivation_Date,
      Undo_Merge_CHIPS,
      Undo_Merge_Nation_ID,
      ABA;

  var CurStr; /* текущая строка источника */

  CurStr = 0;

  /* инициализация источника и получение первой записи */
  macro Connect:bool
    return false;
  end;
  /* закрытие источника */
  macro Disconnect
  end;
  /* возвращает максимальное количество записей в источнике */
  macro NumRec:integer
    return 0;
  end;
  /* переход к следующей записи */
  macro NextRec
    CurStr = CurStr + 1;
  end;
  /* проверка на пропуск текущей записи */
  macro SkipRec:bool
    return true;
  end;
  /* заполнение буфера по текущей записи источника */
  macro GetRec
  end;
  /* признак исчерпания источника */
  macro EOF:bool
    return false;
  end;
  /* название источника - для лога и интерфейса */
  macro SourceName:string
    return "null";
  end;

  /* Макрос для поиска банка по текущей записи */
  macro FindThisBank : bool
     macro FindPartCode( kind, code )
        PartCode.Code = code;
        PartCode.CodeKind = kind;
        return GetEQ( PartCode );
     end;

     var oldkey, isFound = false, b_continue, oldPartyKey, NameKind, saveCode, StrBank;
     oldkey = KeyNum( PartCode, PARTCODE_KEY_KC );

     if ( BIC_Key!="" )
       isFound = FindPartCode(KIND_BIC_ISO, BIC_Key);
/* поисков по основным кодам должно вполне хватить
       if ( not isFound )
         isFound = FindPartCode(KIND_CODE, BIC_Key);
       end;
*/
     end;
     if ( not isFound )
       if ((BIC_Plus_Key!="") AND (BIC_Plus_Key!=EMPTY_VALUE))
         isFound = FindPartCode(KIND_BIC_PLUS, BIC_Plus_Key );
/*
         if ( not isFound )
           isFound = FindPartCode(KIND_CODE, BIC_Plus_Key);
         end;
*/
       end;
       if ( not isFound )
         if ((BIC_RUS!="") AND (BIC_RUS!=EMPTY_VALUE))
           isFound = FindPartCode(KIND_BIC_RUS, BIC_RUS );
/*
           if ( not isFound )
             isFound = FindPartCode(KIND_CODE, BIC_RUS);
           end;
*/
         end;
/* 
         if ( not isFound )
           if ((CHIPS_UID!="") AND (CHIPS_UID!=EMPTY_VALUE))
             isFound = FindPartCode(KIND_CHIPS_UID, CHIPS_UID );
           end;
           if ( not isFound )
             if ((National_ID!="") AND (National_ID!=EMPTY_VALUE))
               isFound = FindPartCode(KIND_NATIONAL_ID, National_ID );
             end;
           end;
         end;
*/
         if ( (Flag_ABA_Directory==true) AND (FindCreatin==true) AND (not isFound) )
            /* Встаем на существующую запись */
            ReWind(PartCode);
            Next( PartCode );
            ReWind(PtBicDir);
            Next( PtBicDir );
            /* Если импортируем Fedwire Directory, то поиск осуществляем по имени */
            if ( Institution_Name1!="" )              
              oldPartyKey = KeyNum( Party, PARTY_KEY_SHORTNAME);
              Party.ShortName = SubStr( Institution_Name1, 1, 20 );

              /* Выводим имя импортируемого банка */
              StrBank = string( CurStr, "." );
              while( strlen(StrBank)<7 ) StrBank = StrBank + " "; end;
              StrBank = StrBank + string(ABA, " ", substr(City_Heading,1,20), " ", Institution_Name1);
              while( strlen(StrBank)<65 ) StrBank = StrBank + " "; end;

              if ( GetGE(Party) )
                 b_continue = true;
                 while( b_continue AND (SubStr(Party.ShortName,1,20)==SubStr(Institution_Name1, 1, 20)) )
                    if ( SubStr(Party.Name,1,strlen(Institution_Name1))==Institution_Name1 )
                       if ( (Party.Country==CountryCode) AND
                            (substr(Party.Place,1,strlen(City_Heading))==City_Heading) )
                          /* Нашли банк с похожим именем и кодом страны, а также городом */
                          isFound = true;
                          b_continue = false;
                       else
                          if ( Party.Country==CountryCode)
                              if ( StrBank!="" )
                                 PrintLn( StrBank );
                                 StrBank = "";
                              end;
                              PrintBankInformation( Party.PartyID );
                          end;
                          b_continue = Next(Party);
                       end;                    
                    else
                       b_continue = Next(Party);
                    end;
                 end;
              end;
              KeyNum( Party, oldPartyKey);
              if (isFound) /*Банк был найден */                 
                 oldkey = KeyNum( PartCode, PARTCODE_KEY_PK );
                 PartCode.PartyID = Party.PartyID;
                 PartCode.CodeKind = KIND_BIC_ISO;
                 if ( GetEQ(PartCode) ) /* У банка есть код BIC_ISO */
                    if ( StrBank!="" )
                       PrintLn( StrBank );
                    end;
                    KeyNum( PartCode, PARTCODE_KEY_KC );
                    saveCode = substr( PartCode.Code, 1, 8 );
                    PartCode.CodeKind = KIND_BIC_ISO;
                    PartCode.Code = saveCode;
                    /* Выводим на экран все отделения найденного банка */
                    b_continue = GetGE(PartCode);
                    while( b_continue AND (substr(PartCode.Code, 1, 8)==saveCode) )
                        PrintBankInformation( PartCode.PartyID );
                        b_continue = Next(PartCode);
                    end;
                    /* Выбираем главный из них (длина BIC_ISO=8) или первый попавшийся */
                    PartCode.CodeKind = KIND_BIC_ISO;
                    PartCode.Code = saveCode;
                    GetGE(PartCode);
                    printLn(string("          Выбран: ", PartCode.Code) );
                 else
                    /* Найдем первый попавшийся код (BIC_ISO у этого банка нет) */
                    PartCode.PartyID = Party.PartyID;
                    PartCode.CodeKind = 0;
                    if ( (not GetGE(PartCode)) OR (PartCode.PartyID!=Party.PartyID) )
                       /* У данного банка вообще нет кодов */
                       isFound = false;
                       PrintLn( StrBank + "          Соответствие не определено, вводится новый банк" );
                    else
                       /* Выдаем информацию по выбранному банку */
                       if ( StrBank!="" )
                          PrintLn( StrBank );
                       end;
                       PrintBankInformation( PartCode.PartyID );
                       printLn(string("          Выбран: ", PartCode.Code) );
                    end;
                 end;
                 KeyNum( PartCode, oldkey );
              else
                  if ( StrBank=="" ) StrBank = "          "; end;
                  PrintLn( StrBank + "Соответствие не определено, вводится новый банк" );
              end;
            end;
         end;
       end;
     end;

     KeyNum( PartCode, oldkey );
     return isFound;
  end;

  /* вывести информацию в лог */
  macro Show(str)
    var code = "";
    if( (BIC_Key!="") AND (BIC_Key!=EMPTY_VALUE) )
      code = Mes_BIC_ISO+" "+BIC_Key;
    elif( (BIC_Plus_Key!="") AND (BIC_Plus_Key!=EMPTY_VALUE) )
      code = Mes_BIC_PLUS+" "+BIC_Plus_Key;
    elif ( (ABA!="") AND (ABA!=EMPTY_VALUE) )
      code = Mes_ABA+" "+ABA;
    end;
    println( CurStr:6," ",Modification_Flag," ",code," ", str );
  end; /* macro Show */

end; /* class BICsource */

var src:BICsource; /* объект-источник данных */

macro TestError( flag, MesError, ExtErrorInfo )
  if ( not flag )
    BankErrorMessage = MesError;
    BankErrorExtInfo = ExtErrorInfo;
    AbortTrn();
  end;
end;

/* Заполнение полей базы ptbicdir.dbt */
macro FillPtBICDir( PartyID, source )
  var src : BICsource;
  src = source;
  ptbicdir.PartyID = PartyID;
  ptbicdir.InstitutionName     = src.Institution_Name1 + src.Institution_Name2 +
                                 src.Institution_Name3;
  ptbicdir.BranchInformation   = src.Branch_Information1 + src.Branch_Information2;
  ptbicdir.SubtypeIndication   = src.Subtype_Indication;
  ptbicdir.ValueAddedServices1  = src.Value_Added_Services.Value(0);
  ptbicdir.ValueAddedServices2  = src.Value_Added_Services.Value(1);
  ptbicdir.ValueAddedServices3  = src.Value_Added_Services.Value(2);
  ptbicdir.ValueAddedServices4  = src.Value_Added_Services.Value(3);
  ptbicdir.ValueAddedServices5  = src.Value_Added_Services.Value(4);
  ptbicdir.ValueAddedServices6  = src.Value_Added_Services.Value(5);
  ptbicdir.ValueAddedServices7  = src.Value_Added_Services.Value(6);
  ptbicdir.ValueAddedServices8  = src.Value_Added_Services.Value(7);
  ptbicdir.ValueAddedServices9  = src.Value_Added_Services.Value(8);
  ptbicdir.ValueAddedServices10 = src.Value_Added_Services.Value(9);
  ptbicdir.ValueAddedServices11 = src.Value_Added_Services.Value(10);
  ptbicdir.ValueAddedServices12 = src.Value_Added_Services.Value(11);
  ptbicdir.ValueAddedServices13 = src.Value_Added_Services.Value(12);
  ptbicdir.ValueAddedServices14 = src.Value_Added_Services.Value(13);
  ptbicdir.ValueAddedServices15 = src.Value_Added_Services.Value(14);
  ptbicdir.ValueAddedServices16 = src.Value_Added_Services.Value(15);
  ptbicdir.ValueAddedServices17 = src.Value_Added_Services.Value(16);
  ptbicdir.ValueAddedServices18 = src.Value_Added_Services.Value(17);
  ptbicdir.ValueAddedServices19 = src.Value_Added_Services.Value(18);
  ptbicdir.ValueAddedServices20 = src.Value_Added_Services.Value(19);
  ptbicdir.ExtraInformation    = src.Extra_Information;
  if ( src.Update_Date!=EMPTY_VALUE )
     ptbicdir.UpdateDate = YYMMDDDate(src.Update_Date);
  end;
  if ( src.Activation_Date!=EMPTY_VALUE )
     ptbicdir.ActivationDate = YYMMDDDate(src.Activation_Date);
  end;
  if ( src.Deactivation_Date!=EMPTY_VALUE )
     ptbicdir.DeactivationDate = YYMMDDDate(src.Deactivation_Date);
  end;
  if ( src.Undo_Merge_CHIPS!=EMPTY_VALUE )
     ptbicdir.UndoMergeCHIPS = src.Undo_Merge_CHIPS;
  end;
  if ( src.Undo_Merge_Nation_ID!=EMPTY_VALUE )
     ptbicdir.UndoMergeNationalID = src.Undo_Merge_Nation_ID;
  end;

  ptbicdir.PhysicalAddress = src.Physical_Address.Value(0) +
                             src.Physical_Address.Value(1) +
                             src.Physical_Address.Value(2) +
                             src.Physical_Address.Value(3);
  ptbicdir.Location = src.Location1 + src.Location2 + src.Location3;
  if ( src.ZIP_Code!=EMPTY_VALUE )
     ptbicdir.Zipcode  = src.ZIP_Code;
  end;
  ptbicdir.City     = src.City_Heading;
  ptbicdir.Country  = src.Country_Name1 + src.Country_Name2;
  ptbicdir.POB_Location = src.POB_Location1 + src.POB_Location2 + src.POB_Location3;
  if ( src.POB_ZIP_Code!=EMPTY_VALUE )
     ptbicdir.POB_Zipcode  = src.POB_ZIP_Code;
  end;
  ptbicdir.POB_Number   = src.POB_Number;
  ptbicdir.POB_Country  = src.POB_Country_Name1 + src.POB_Country_Name2;
end; /* macro FillPtBICDir */


macro MesPartCode( Kind, Code )
  var str;
  if( Kind ==  KIND_BIC_RUS )
    str = Mes_BIC_RUS;
  elif( Kind == KIND_BIC_ISO )
    str = Mes_BIC_ISO;
  elif( Kind == KIND_CHIPS_UID )
    str = Mes_CHIPS_UID;
  elif( Kind == KIND_BIC_PLUS )
    str = Mes_BIC_PLUS;
  elif( Kind == KIND_NATIONAL_ID )
    str = Mes_NATIONAL_ID;
  elif( Kind == KIND_CODE )
    str = Mes_Code;
  elif( Kind == KIND_ABA )
    str = Mes_ABA;
  else
    str = "UndefCode";
  end;
  return str + " " + Code;
end;

var NatID, CHIPSUID, BICRUS, IsSetSuperior, ABA;

/* при совпадении кодов предыдущая организация объявляется объемлющей для данной */
macro SetSuperior( CodeKind, Code )
  var CodeDest = "";
  if(     (not IsSetSuperior)
      AND (Code != EMPTY_VALUE) 
      AND (Code != "") 
    )
    CodeDest = Code;
    KeyNum( PartCode, PARTCODE_KEY_KC );
    PartCode.CodeKind = CodeKind;
    PartCode.Code     = Code;
    if(GetEQ(PartCode) AND (PartCode.PartyID!=Party.PartyID) )
      if(Party.Superior == -1) 
        Party.Superior = PartCode.PartyID;
        src.Show(MesFoundSurerior+MesPartCode(PartCode.CodeKind,PartCode.Code)+" - изменен");
      else
        src.Show(MesFoundSurerior+MesPartCode(PartCode.CodeKind,PartCode.Code)+" - оставлен прежний");
      end;
      CodeDest = "";
      IsSetSuperior = true;
    end;
  end;
  return CodeDest;
end;

/* Выдаем предупреждение, что не найден российский банк и продолжаем работу */
macro NotFoundRusBank
  var str;
  if( src.BIC_Key != "" )
    str = MesErrorNoFoundRusBank + MesPartCode(KIND_BIC_ISO,src.BIC_Key) ;
  elif( src.BIC_RUS != "" )
    str = MesErrorNoFoundRusBank + MesPartCode(KIND_BIC_RUS,src.BIC_RUS) ;
  else
    str = MesErrorNoFoundRusBank + MesPartCode(KIND_BIC_PLUS,src.BIC_Plus_Key) ;
  end;
  src.Show(str);
  /* Если надо прервать работу, то закомментируйте предыдущую строку и
     раскомментируйте следующую. */
/*
  TestError( true, str ); 
*/
end;

/* Вставка банка */
macro InsertBank

  macro InsertInPartCode( PartyID, kind, value )
    if( (value!=EMPTY_VALUE) AND ( value!="" ) )
      PartCode.PartyID  = PartyID;
      PartCode.CodeKind = kind;
      PartCode.Code     = value;
/*      src.Show( "InsertCode " + "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code) );*/
      TestError( insert( PartCode ), MesErrorInsertPartCode, 
        "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code) );
    end;
  end;

  NatID = "";
  CHIPSUID = src.CHIPS_UID;
  BICRUS = src.BIC_RUS;
  IsSetSuperior = false;
/*
  if ( src.CountryCode != RU_Code )
*/
     ClearRecord(Party);
     Party.LegalForm   = LEGAL_FORM_ORGANIZATION; /* (организация) */
     Party.Name        = src.Institution_Name1 + src.Institution_Name2 + src.Institution_Name3 
                         + src.Branch_Information1 + src.Branch_Information2 ;
     Party.ShortName   = SubStr( src.Institution_Name1, 1, 20 );
     if ( src.ZIP_Code != EMPTY_VALUE ) 
       Party.PostIndex   = src.ZIP_Code; 
     end;
     Party.Address     = src.Physical_Address.Value(0) + src.Physical_Address.Value(1);
     Party.Place       = SubStr( src.City_Heading, 1, 30 );
     Party.Superior    = -1;
     /* при совпадении национальных кодов предыдущая организация 
        объявляется объемлющей для данной */
     NatID = SetSuperior( KIND_NATIONAL_ID, src.National_ID );
     /* при совпадении БИК ЦБ РФ предыдущая организация 
        объявляется объемлющей для данной */
     BICRUS = SetSuperior( KIND_BIC_RUS, src.BIC_RUS );
     /* при совпадении CHIPS UID предыдущая организация 
        объявляется объемлющей для данной */
     CHIPSUID = SetSuperior( KIND_CHIPS_UID, src.CHIPS_UID );
     /* при совпадении ABA предыдущая организация 
        объявляется объемлющей для данной */
     ABA = SetSuperior( KIND_ABA, src.ABA );


     Party.TaxInstitution = -1;
     Party.Country     = src.CountryCode;
     Party.NRCountry   = src.CountryCode;
     if ( src.CountryCode != RU_Code )
       Party.NotResident = SET_CHAR;
     else
       Party.NotResident = UNSET_CHAR;
     end;
     /*TestError( insert( Party ), MesErrorInsertParty );*/
     TestError( ВставитьСубъекта( Party ), MesErrorInsertParty );

     ClearRecord(ptbicdir);
     FillPtBICDir( Party.PartyID, src );
     TestError( insert( ptbicdir ), MesErrorInsertPtbicdir );

     ClearRecord(PartCode);
     if ( src.BIC_Key!="" )
       InsertInPartCode( Party.PartyID, KIND_CODE, src.BIC_Key );
     elif ( (BICRUS!= "") AND (BICRUS != EMPTY_VALUE) )
       InsertInPartCode( Party.PartyID, KIND_CODE, src.BIC_RUS );
     else
       InsertInPartCode( Party.PartyID, KIND_CODE, src.BIC_Plus_Key );
     end;
     InsertInPartCode( Party.PartyID, KIND_BIC_ISO,     src.BIC_Key );
     InsertInPartCode( Party.PartyID, KIND_CHIPS_UID,   CHIPSUID );
     InsertInPartCode( Party.PartyID, KIND_BIC_PLUS,    src.Bic_Plus_Key );
     InsertInPartCode( Party.PartyID, KIND_NATIONAL_ID, NatID );
     InsertInPartCode( Party.PartyID, KIND_BIC_RUS,     BICRUS );
     InsertInPartCode( Party.PartyID, KIND_ABA,         ABA );

     PartyOwn.PartyID = Party.PartyID;
//     PartyOwn.Sort = "";
     PartyOwn.PartyKind = OWN_KIND_BANK;
     TestError( insert( PartyOwn ), MesErrorInsertPartyOwn );

     if ( src.Flag_ABA_Directory==false )
        PartyOwn.PartyID = Party.PartyID;
        PartyOwn.PartyKind = OWN_KIND_SWIFT;
        TestError( insert( PartyOwn ), MesErrorInsertPartyOwn );
     end;
/*
  else
    /* Выдаем предупреждение, что не найден российский банк и продолжаем работу */
    NotFoundRusBank;
  end;
*/
  return true;
end; /* macro InsertBank */

/* Обновление банка */
macro UpdateBank
  macro UpdatePartCode( PartyID, kind, value )
    var str;
    if ( value!=EMPTY_VALUE )
      KeyNum( PartCode, PARTCODE_KEY_PK );
      PartCode.PartyID  = PartyID;
      PartCode.CodeKind = kind;
      if( not GetEQ( PartCode ) )
        if(value!="")
          ClearRecord(PartCode);
          PartCode.PartyID = PartyID;
          PartCode.CodeKind = kind;
          /* ввод нового кода этого типа */
          PartCode.Code = value;
          str = "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code);
/*          src.Show( "InsertCode " + ">" + str );*/
          TestError( insert( PartCode ), MesErrorInsertPartCode, str );
        end;
      else
        if(value=="")
          str = "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code);
/*          src.Show( "DeleteCode " + ">" + str );*/
          TestError( delete( PartCode ), MesErrorDeletePartCode, str );
        elif( PartCode.Code != value )
          PartCode.Code = value;
          str = "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code);
/*          src.Show( "UpdateCode " + ">" + str );*/
          TestError( update( PartCode ), MesErrorUpdatePartCode, str );
/*
        else
          str = "PartyID="+PartCode.PartyID+" "+MesPartCode(PartCode.CodeKind,PartCode.Code);
          src.Show( "NonChangedCode "+ ">" + str );
*/
        end;
      end;
    end;
  end;

  var PartyID, flagUpdate = 1, IsSameBICRUS=true;
  /* Существование файла проверяется еще до вызова этой функции */

  CHIPSUID = src.CHIPS_UID;
  BICRUS = src.BIC_RUS;
  NatID = "";
  IsSetSuperior = false;
  PartyID = src.PartyID;
/*
  if ( src.CountryCode != RU_Code )
*/
     Party.PartyID = PartyID;
     if( not GetEQ( Party ) )
        ClearRecord(Party);
        Party.PartyID = PartyID;
        flagUpdate = 0;
     end;
     /* при совпадении национальных кодов предыдущая организация 
        объявляется объемлющей для данной */
     NatID = SetSuperior( KIND_NATIONAL_ID, src.National_ID );
     /* при совпадении БИК ЦБ РФ предыдущая организация 
        объявляется объемлющей для данной */
     BICRUS = SetSuperior( KIND_BIC_RUS, src.BIC_RUS );
     /* при совпадении CHIPS UID предыдущая организация 
        объявляется объемлющей для данной */
     CHIPSUID = SetSuperior( KIND_CHIPS_UID, src.CHIPS_UID );
     /* при совпадении ABA предыдущая организация 
        объявляется объемлющей для данной */
     ABA = SetSuperior( KIND_ABA, src.ABA );

     /* для росс. банков оставляем основную информацию по справочнику ЦБ */
     if(BICRUS!="")
       KeyNum( PartCode, PARTCODE_KEY_PK );
       PartCode.PartyID  = PartyID;
       PartCode.CodeKind = KIND_BIC_RUS;
       if( (not GetEQ( PartCode )) OR (PartCode.Code!=BICRUS) )
         Party.LegalForm   = LEGAL_FORM_ORGANIZATION; /* (организация) */
         Party.Name        = src.Institution_Name1 + src.Institution_Name2 + src.Institution_Name3 
                             + src.Branch_Information1 + src.Branch_Information2 ;
         Party.ShortName   = SubStr( src.Institution_Name1, 1, 20 );
         Party.Address     = src.Physical_Address.Value(0) + src.Physical_Address.Value(1);
         Party.Place       = SubStr( src.City_Heading, 1, 30 );
         Party.TaxInstitution = -1;
         Party.Country     = src.CountryCode;
         Party.NRCountry   = src.CountryCode;
         if ( src.CountryCode != RU_Code )
           Party.NotResident = SET_CHAR;
         else
           Party.NotResident = UNSET_CHAR;
         end;
         if ( src.ZIP_Code != EMPTY_VALUE ) Party.PostIndex   = src.ZIP_Code; end;
         IsSameBICRUS = false;
       end;
     end;

     if ( flagUpdate )
        if ( src.Flag_ABA_Directory==false )
           TestError( update( Party ), MesErrorUpdateParty );
        end;
     else TestError( ВставитьСубъекта( Party ), MesErrorInsertParty ); end;
     flagUpdate = 1;
/*
  else
     Party.PartyID = PartyID;
     if( not GetEQ( Party ) )
        /* Выдаем предупреждение, что не найден российский банк и продолжаем работу */
        NotFoundRusBank;
        return true;
     end;
  end;
*/

  ptbicdir.PartyID = PartyID;
  if( not GetEQ( ptbicdir ) )
     ClearRecord(ptbicdir);
     ptbicdir.PartyID = PartyID;
     flagUpdate = 0;
  end;
  FillPtBICDir( PartyID, src );
  if ( flagUpdate )
     if ( src.Flag_ABA_Directory==false )
        TestError( update( ptbicdir ), MesErrorUpdatePtbicdir );
     end;
  else TestError( insert( ptbicdir ), MesErrorInsertPtbicdir ); end;
  flagUpdate = 1;
/*
  println("Update BIC_Key "+src.BIC_Key+" src.BIC_RUS "+src.BIC_RUS+" BICRUS "+BICRUS+" BIC_Plus_Key "+src.BIC_Plus_Key+" CHIPSUID "+CHIPSUID+
          " NatID "+NatID);
*/
  if(not IsSameBICRUS)
    if ( src.BIC_Key!="" )
      UpdatePartCode( Party.PartyID, KIND_CODE, src.BIC_Key );
    elif ( (BICRUS!= "") AND (BICRUS != EMPTY_VALUE) )
      UpdatePartCode( Party.PartyID, KIND_CODE, BICRUS );
    else
      UpdatePartCode( Party.PartyID, KIND_CODE, src.BIC_Plus_Key );
    end;
    UpdatePartCode( PartyID, KIND_BIC_RUS,      BICRUS );
  end;
  UpdatePartCode( PartyID, KIND_BIC_ISO,      src.BIC_Key );
  UpdatePartCode( PartyID, KIND_CHIPS_UID,    CHIPSUID );
  UpdatePartCode( PartyID, KIND_BIC_PLUS,     src.BIC_Plus_Key );
  UpdatePartCode( PartyID, KIND_NATIONAL_ID,  NatID );
  UpdatePartCode( PartyID, KIND_ABA,          ABA );

  PartyOwn.PartyID = PartyID;
  PartyOwn.PartyKind = OWN_KIND_BANK;
  if( not GetEQ( PartyOwn ) )
     ClearRecord(PartyOwn);
     PartyOwn.PartyID = PartyID;
     PartyOwn.PartyKind = OWN_KIND_BANK;
     flagUpdate = 0;
  end;
  if ( flagUpdate )
     if ( src.Flag_ABA_Directory==false )
        TestError( update( PartyOwn ), MesErrorUpdatePartyOwn );
     end;
  else TestError( insert( PartyOwn ), MesErrorInsertPartyOwn ); end;
  flagUpdate = 1;

  if ( src.Flag_ABA_Directory==false )
     PartyOwn.PartyID = PartyID;
     PartyOwn.PartyKind = OWN_KIND_SWIFT;
     if( not GetEQ( PartyOwn ) )
        ClearRecord(PartyOwn);
        PartyOwn.PartyID = PartyID;
        PartyOwn.PartyKind = OWN_KIND_SWIFT;
        flagUpdate = 0;
     end;
     if ( flagUpdate )      
          TestError( update( PartyOwn ), MesErrorUpdatePartyOwn );
     else TestError( insert( PartyOwn ), MesErrorInsertPartyOwn ); end;
     flagUpdate = 1;
  end;
  return true;
end; /* macro UpdateBank */

/* Удаление банка */
macro DeleteBank

  /******
  macro DeleteLinkFiles( _file, PartyID, MesError )
     var recPos, loop, oldkey;
     oldkey = KeyNum( _file, 0 );
     ClearRecord(_file);
     _file.PartyID = PartyID;
     loop = GetGE( _file );
     while( (loop) AND (_file.PartyID == PartyID) )
         loop = Next( _file );
         if ( loop )
            recPos = GetPos(_file);
            Prev( _file );
         end;
         TestError( delete( _file ), MesError );
         if ( loop )
            GetDirect( _file, recPos );
         end;
     end;
     KeyNum( _file, oldkey );
  end;

  var PartyID, flagDelete = 1, loop, recPos;
  /* Существование банка проверяется еще до вызова этой функции */
  PartyID = src.PartyID;
  /* Если этот банк корреспондент, то устанавливаем флаг - запрет удаления */
  PartyOwn.PartyID = PartyID;
  PartyOwn.PartyKind = KIND_CORESP;
  if( GetEQ( PartyOwn ) )
     flagDelete = 0;
  end;
  /* Ищем для этого банка корсхемы */
  Corschem.CorrID = PartyID;
  Corschem.CorAccount = "";
  if ( (GetGE( Corschem )) AND (Corschem.CorrID == PartyID) )
      flagDelete = 0;
  end;
  if ( flagDelete )
     Party.PartyID = PartyID;
     if ( GetEQ( Party ) )
         TestError( delete( Party ), MesErrorDeleteParty );
     end;
     ptbicdir.PartyID = PartyID;
     if( GetEQ( ptbicdir ) )
         TestError( delete( ptbicdir ), MesErrorDeletePtbicdir );
     end;
     DeleteLinkFiles( PartCode, PartyID, MesErrorDeletePartCode );
     DeleteLinkFiles( PartyOwn, PartyID, MesErrorDeletePartyOwn );
  else
     /* Раскомментировать эти строки, если не желаете обрабатывать банк в
        случае, если отсутствует запись в базе Party.dbt для банка - корреспондента */
     Party.PartyID = PartyID;
     if ( not GetEQ( Party ) )
        PrintLn( MesErrorNoFoundBankForDelete + src.BIC_Key );
        return true;
     end;
     Party.Locked = SET_CHAR;
     TestError( update( Party ), MesErrorUpdateParty );
     /* Если Вы раскомментировали предыдущий код, то закомментируте следующий. */
/*
     Party.PartyID = PartyID;
     if ( not GetEQ( Party ) )
        UpdateBank();
        Party.PartyID = PartyID;
        if ( not GetEQ( Party ) )
           /* Российский банк не найден */
           return true;
        end;
     end;
     Party.Locked = SET_CHAR;
     TestError( update( Party ), MesErrorUpdateParty );
*/
  end;
  *******/

  var PartyID = src.PartyID, IsExistParty = false, oldKey, b_continue, count, ThisCode;

  /* Теперь мы не удаляем субъекта, а лишь переводим его в закрытые */
  Party.PartyID = PartyID;
  if ( GetEQ(Party) )
     IsExistParty = true;
  end;

  ThisCode = partcode.Code;

  if ( partcode.State==PARTCODE_ACTIVE )
     partcode.State = PARTCODE_NOTACTIVE;
     TestError( update( partcode ), MesErrorUpdatePartCode );
  end;

  if ( IsExistParty AND (Party.Locked!=SET_CHAR) )
     /* Ищем активные коды субъекта, если их нету, закрываем субъекта */
     oldKey = KeyNum( partcode, 0 );
     ClearRecord( partcode );
     count = 0;
     partcode.PartyID = PartyID;
     b_continue = GetGE(partcode);
     while( b_continue AND (partcode.PartyID==PartyID) )
        if ( (partcode.CodeKind==KIND_CODE) AND (partcode.State==PARTCODE_ACTIVE) AND (partcode.Code==ThisCode) )
            partcode.State=PARTCODE_NOTACTIVE;
            TestError( update( partcode ), MesErrorUpdatePartCode );
        end;
        if ( partcode.State==PARTCODE_ACTIVE )            
            count = count + 1;
        end;
        b_continue = Next(partcode);
     end;
     KeyNum( partcode, oldKey );
     if ( count==0 )
         Party.Locked=SET_CHAR;
         TestError( update( Party ), MesErrorUpdateParty );
     end;
  elif ( IsExistParty  )
     /* Субъект закрыт, делаем нективными все его коды */
     oldKey = KeyNum( partcode, 0 );
     ClearRecord( partcode );
     partcode.PartyID = PartyID;
     b_continue = GetGE(partcode);
     while( b_continue AND (partcode.PartyID==PartyID) )
        if ( partcode.State==PARTCODE_ACTIVE )
            partcode.State=PARTCODE_NOTACTIVE;
            TestError( update( partcode ), MesErrorUpdatePartCode );
        end;
        b_continue = Next(partcode);
     end;
     KeyNum( partcode, oldKey );
  end;

  return true;
end; /* macro DeleteBank */

class BICImport( _BICsrc )
  var count;
  src = _BICsrc;

  
  macro LogBox(str)
    println(str);
    msgbox(str);
  end;

  /* инициализация */
  macro Init:bool
    if(not src.Connect)
      LogBox("Ошибка открытия исходных данных");
      return false;
    else
      return true;
    end;
  end;

  /* деструктор */
  macro Stop
    src.Disconnect;
  end;

  macro Show(str)
    src.Show(str);
  end;

  /* вывести информацию в лог с сообщением на экран */
  macro ShowBankError
    if(valtype(BankErrorExtInfo)==V_UNDEF)
      BankErrorExtInfo = "";
    end;
    Show( BankErrorMessage + " " + BankErrorExtInfo + " (" + String( Status( Error ) ) + ") " + Error);
    MsgBox( BankErrorMessage+", код " + String( Status( Error ) ) + "|" + Error );
  end; /* macro ShowMsg */

  /* обработка исходного файла */
  macro Make
    var lastNumRec = src.NumRec,
        stepNumRec;

    stepNumRec = LastNumRec;

    InitProgress(LastNumRec, "", " Импорт справочника банков "+src.SourceName);
    while( not src.EOF )

      if( not src.SkipRec )
        count = count + 1;
        src.GetRec;
        /* неизмененный банк - на всякий случай модифицируем */
        if( (src.Modification_Flag == "U") OR (src.Modification_Flag == "W") )
           src.Modification_Flag = "M";
        end;
        /* При обновлении записи */
        if( src.Modification_Flag == "M" )
          /* Ищем запись о банке */
          if( not src.FindThisBank() )
            /* Если не нашли запись - вставляем новую запись */
/*
            Show("Обновление - Не найден - ввод");
*/
            src.Modification_Flag = "A";
          else
            src.PartyID = partcode.PartyID;
          end;
        /* При вставке записи */
        elif( src.Modification_Flag == "A" )
          /* Ищем запись о банке  */
          if( src.FindThisBank() )
            /* Если нашли запись - обновляем запись */
/*
            Show("Ввод - Найден - обновление");
*/
            src.Modification_Flag = "M";
            src.PartyID = partcode.PartyID;
          end;
        /* При удалении записи */
        elif( src.Modification_Flag == "D" )
          /* Ищем запись о банке по его BIC ISO */
          if( not src.FindThisBank() )
            /* Если не нашли запись - ничего не делаем */
            src.Modification_Flag = "U";
          else
            src.PartyID = partcode.PartyID;
          end;
        end;

        /* Новая запись в справочнике BIC Directory */
        if( src.Modification_Flag == "A" )
          if( not ProcessTrn(null,"InsertBank",Party, ptbicdir, PartCode, PartyOwn) )
            ShowBankError;
            Exit();
          end;
        elif( src.Modification_Flag == "M" ) /* Запись в справочнике BIC Directory модифицируется */
          if( not ProcessTrn(0,"UpdateBank",Party, ptbicdir, PartCode, PartyOwn) )
            ShowBankError;
            Exit();
          end;
        elif( src.Modification_Flag == "D" ) /* Запись в справочнике BIC Directory удаляется */
          if( not ProcessTrn(0,"DeleteBank",Party, ptbicdir, PartCode, PartyOwn) )
            ShowBankError;
            Exit();
          end;
        elif( src.Modification_Flag == "U" ) /* Запись в справочнике BIC Directory не изменяется */
          /* Пока ничего не делаем !!! */
/*         
          Show("Не изменился");
*/
        else  /* Ошибочное значение флага */
          MsgBox( "Ошибочное значение поля Modification_Flag \""+src.Modification_Flag+"\"|в записи " +
                  src.CurStr + " " + src.SourceName );
        end;
      end;/*  SkipRec */

      src.NextRec;

      UseProgress(src.CurStr);
    end; /* while not EOF */

    RemProgress(src.CurStr);
  end;

  /* запуск */
  macro Start
    var t1 = Time(), t2;
    count = 0;
    Println( "Импорт справочника банков" );
    println( "Исходные данные:      ", src.SourceName );
    Println( "Дата импорта:         ", Date());
    println( "Начало импорта:       ", t1 );
    Println();
    if(Init)
      Make;
      Stop;
    end;
    t2 = Time();
    Println();
    println( "Завершение импорта:   ", t2 );
    Println( "Обработано:           ", count, " из ", src.CurStr, " записей" );
    println( "Время импортирования: ", t2-t1 );
  end;
end;
