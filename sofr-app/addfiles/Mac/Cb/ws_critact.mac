 /*
 $Name: ws_critact.mac
 $Module: Главная книга
 $Description: Макрос уведомлений операционистов
 */

import RsbDataSet, cb_sql;
import XmlRpcInter, globals;

private const CRITACT_NONE     = 0;  // Не обработана
private const CRITACT_ACCEPTED = 1;  // Подтверждена
private const CRITACT_DECLINED = 2;  // Отклонена
private const SET_CHAR = "X";

class TWsCriticalAction
    var Id : integer;
    var Oper : integer;
    var Description : string;
    var Date : datetime;
end;

private macro CopyCriticalAction(item :TWsCriticalAction, ds)
    item.Id = SQL_ConvTypeInteger(ds.Id);
    item.Oper = SQL_ConvTypeInteger(ds.Oper);
    item.Description = SQL_ConvTypeStr(ds.Desc);
    item.Date = DtTm(SQL_ConvTypeDate(ds.Date), SQL_ConvTypeTime(ds.Time));
end;

private macro GetNoticesByOper(oper, pagesize, pagenum)

  if (oper < 0)
    RunError("Не задан операционист");
  end;
  
  var query = "SELECT T_ID, T_OPER, T_DESC, T_DATE, T_TIME FROM DCRITACT_DBT WHERE T_TYPE = 0 AND T_ISNOTICE = 'X' AND T_STATE = 0 AND T_RECIPIENT = ? ORDER BY T_ID";
  
  if(pagesize != 0)
    query = SQL_WrapSelect( query, pagenum, pagesize );
  end; 
  
  var cmd = RsdCommand(query);

  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();
  
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  var result = TArray();
  var criticalAction = null;

  while (ds.moveNext())
    criticalAction = TWsCriticalAction();
    CopyCriticalAction(criticalAction, ds);
    result[result.Size] = criticalAction;
  end;

  return result;

end;

private macro GetNoticesCountByOper(oper)

  if (oper < 0)
    RunError("Не задан операционист");
  end;
  
  var query = "SELECT count(T_ID) count FROM DCRITACT_DBT WHERE T_TYPE = 0 AND T_ISNOTICE = 'X' AND T_STATE = 0 AND T_RECIPIENT = ? ORDER BY T_ID";
  
  var cmd = RsdCommand(query);

  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();
  
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  var result : integer;

  if (ds.moveNext())
    result = ds.count;
  end;

  return result;

end;

macro GetNotices(pagesize, pagenum)
    return GetNoticesByOper({oper}, pagesize, pagenum);
end;

macro GetNoticesCount()
    return GetNoticesCountByOper({oper});
end;

macro ChangeNoticeState(id :integer, state :integer)

    if ((state < CRITACT_NONE) or (state > CRITACT_DECLINED))
        RunError("Неверное состояние уведомления");
    end;

    var critact = TBFile("critact", "W");
    critact.rec.Id = id;

    if (not critact.GetEQ())
        RunError("Не найдено уведомление");
    end;

    if (critact.rec.IsNotice != SET_CHAR)
        RunError("Текущая запись не является уведомлением");
    end;

    critact.rec.State = state;
    critact.rec.IsProcessed = SET_CHAR;
    critact.rec.ProcessedBy = {oper};
    return critact.Update();
end;

macro SendUserNotice(text :string, user :integer)
    return SendNotice(text, user);
end;
