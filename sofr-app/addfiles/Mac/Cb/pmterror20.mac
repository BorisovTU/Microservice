/*
 $Name: pmterror20.mac
 $Module: Ядро Banking
 $Description: Макрос шага
*/

//-----------------------------------------------------------------------------
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 4001  - "Платеж"
//                Блок     - 29043 - "Проверки по ФМ"
//                Шаг      - 20    - "Дополнительный контроль по ФМ"
//-----------------------------------------------------------------------------
import PaymInter, "pmfm.mac", BankInter;

var PaymentObj : RsbPayment;

macro ExecuteCaseStep

  var action = PM_GetActionAddControlFM();
  var stat : integer = 0;

  if( action == ADDCONTROLFM_STEP_ACTION_EXECUTE ) // "Провести"
    // Установить категории "Отказать по ФМ?" значение "Проверен"
    stat = PaymentObj.Categories.DisConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, 0);
    if(not stat)
      stat = PaymentObj.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, ATTR_PAYMENT_FMDENY_CHECKED, null, null, {curdate});
    end;

    // Очистить значение категории "Причина отказа по ФМ"
    if(not stat)
      stat = PaymentObj.Categories.DisConnectAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, 0);
    end;
    // Очистить значение примечания "ПОД/ФТ. Результаты проверки на соответствие законодательству"
    if(not stat)
      stat = PaymentObj.Notes.DelNote(NOTEKIND_PAYM_FM_CHECK_MESSAGE);
    end;
    
    if(stat)
      return stat;
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    // Установка следующих статусов произойдет автоматически после выхода из блока, 
    // т.к. это выходные статусы по умолчанию:
    //   сегмент "Уточнение причастности к терроризму" = "Не требуется",
    //   сегмент "Ручная обработка" = "Не требуется",
    //   сегмент "Картотека" = "Нет"

    return ""; // Завершить выполнение блока

  elif( action == ADDCONTROLFM_STEP_ACTION_DENY ) // "Отказать и сообщить в ФМ"
    // Установить категории "Отказать по ФМ?" значение "Отказать"
    stat = PaymentObj.Categories.DisConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, 0);
    if(not stat)
      stat = PaymentObj.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, ATTR_PAYMENT_FMDENY_DENY, null, null, {curdate});
    end;

    // Завершить выполнение шага, вернуть номер шага "Отказ по ФМ"
    return НомерШагаОтказПоФМ;
  end;

  // "Прервать"
  // Прервать выполнение шага с ошибкой
  return 1;
end;
