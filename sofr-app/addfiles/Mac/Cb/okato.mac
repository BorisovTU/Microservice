import rsd, ptkladr, pt_lib; 

var prevPt, countPt, countAdr;

private macro updatePartyAdress(KladrOnly)
  file adress("adress.dbt" ) key 0 write;
  var  rs, Okato, Kladr;
  var  countProgress;

  if(KladrOnly)
    rs = RsdRecordset("SELECT COUNT(*) FROM dadress_dbt WHERE t_country = 'RUS' AND t_Kladr != CHR(1)");
    rs.moveNext();
    countProgress = rs.value(0);
    rs = RsdRecordset("SELECT t_partyid, t_type FROM dadress_dbt WHERE t_country = 'RUS' AND t_Kladr != CHR(1) ORDER BY t_partyid, t_type");
  else
    rs = RsdRecordset("SELECT COUNT(*) FROM dadress_dbt WHERE t_country = 'RUS'");
    rs.moveNext();
    countProgress = rs.value(0);
    rs = RsdRecordset("SELECT t_partyid, t_type FROM dadress_dbt WHERE t_country = 'RUS' ORDER BY t_partyid, t_type");
  end;
  
  InitProgress( countProgress, null, "Заполнение ОКАТО адресов субъектов" );

  while(rs.movenext)

    UseProgress( countAdr );

    adress.PartyID = rs.value(0);
    adress.Type    = rs.value(1);

    if( getEQ( adress ) )

      if(prevPt != adress.PartyID)
        countPt = countPt + 1;
      end;
      prevPt = adress.PartyID;
      countAdr = countAdr + 1;

[
Загрузка кода ОКАТО для адреса субъекта ########## типа #####:] (adress.PartyID, adress.Type);      
      if(trim(adress.Kladr) == "")
        if(not formingKLADRfromOKATO(adress, @Kladr))
          adress.Kladr = Kladr;
          Update(adress);
        end;
      end;
      
      if(adress.Kladr != "")
        Okato = GetOkatoByKladr(adress.Kladr);
        if((Okato != "") and (Okato != "noKladr"))
          if(adress.Okato == "")
            adress.Okato = Okato;
            Update(adress);
[Установлен код ОКАТО для адреса субъекта ########## типа #####] (adress.PartyID, adress.Type);
          else
            if(adress.Okato != Okato)
[Код ОКАТО уже существует и отличается от найденного для адреса субъекта ########## типа #####] (adress.PartyID, adress.Type);
            else
[Код ОКАТО уже существует для адреса субъекта ########## типа #####] (adress.PartyID, adress.Type);
            end;
          end;
        elif(Okato != "")
[Код КЛАДР для адреса субъекта ########## типа ##### не найден] (adress.PartyID, adress.Type);
        end;
      end;
    end;
  end;

  RemProgress();

end;

private macro LoadOKATO(KladrOnly)
  prevPt = 0;
  countPt = 0; 
  countAdr = 0;

  updatePartyAdress(KladrOnly);

[
Всего обработано ########## субъектов, ########## адресов.] (countPt, countAdr);

end;

LoadOKATO(false);
