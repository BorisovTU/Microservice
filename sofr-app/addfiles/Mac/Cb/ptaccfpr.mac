/*
Отчет о правах доступа пользователя
*/

import BankInter, "globals.mac", RSD;

var PrevPartyID = 0;

private macro PrintHeader()
  [
  
                      
                     Отчет о лицевых счетах СПИ субъектов экономики];
end;

private macro PrintUser( userType, userNum )
  var strSql : string;
  var rs : RsdRecordset;
  var User : string;
  array _User;
  var j : integer;

  strSql = "SELECT t1.t_oper, t2.t_name_type, t1.t_name, dp.t_Name, pt.t_ShortName " +
           "FROM dperson_dbt t1, dtypeac_dbt t2, ddp_dep_dbt dp, dparty_dbt pt     " +
           "WHERE                                                                  " +
           "  t1.t_oper = " + userNum + " AND                                      " +
           "  t2.t_inumtype = 13 AND                                               " +
           "  t2.t_type_account = t1.t_ctypeperson AND                             " +
           "  dp.t_Code = t1.t_codedepart AND                                      " +
           "  pt.t_PartyID = dp.t_PartyID                                          ";
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    User = rs.value( 0 ) + ", " + rs.value( 1 ) + ", " + rs.value( 2 ) + ", " + rs.value( 3 ) + " " + rs.value( 4 );
    StrSplit( User, _User, 58, 58, 2 );
    j = 0;
    while( StrLen(_User( j )) > 0 )
      if( j == 0 )
        [
              ############## ##########################################################] ( userType, _User( j ));
      else
        [                    ##########################################################] ( _User( j ));
      end;
      j = j + 1;
    end;
  end;
end;

private macro PrintDate()
[     Дата и время: ########## ########] ( date, time );
end;

private macro PrintTop()
  PrintUser( "Сформирован: ", {oper} );
  PrintDate(); 
end;

private macro PrintParm( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted )

  var sOpened, sClosed, sDeleted : string;

  if( Opened ) sOpened = "да"; else sOpened = "нет"; end;
  if( Closed ) sClosed = "да"; else sClosed = "нет"; end;
  if( Deleted ) sDeleted = "да"; else sDeleted = "нет"; end;

[  
   План счетов: ##### #################################################################] ( NumPlan, NamePlan );
[  Включая балансовые: ################################################################] ( MaskInclBal );
[  Исключая балансовые: ###############################################################] ( MaskExclBal );
  if( Currency == 0 )
[  Рублевые л/с ];
  else
[  Валютные л/с ];
  end;
[  Открытые: ###  Закрытые: ###  Удаленные: ###] ( sOpened, sClosed, sDeleted );

end;

private macro PrintAccount( rs : RsdRecordSet )
  var vOrder : integer;
  vOrder = rs.value( 2 );
[ ###### ######################################## ##################################            ] ( vOrder, rs.value( 3 ), rs.value( 0 ));

end;

private macro PrintParty( rs : RsdRecordSet )

if(( PrevPartyID == 0 ) or ( PrevPartyID != rs.value( 1 ))) /*если для этого субъекта уже начали печатать СПИ*/
  PrevPartyID = rs.value( 1 );
[
 Субъект: ################ #####################################################################] (rs.value( 4 ), rs.value( 5 )); 
[+------+----------------------------------------+----------------------------------+           ];
[| Сорт | Наименование СПИ                       | Лицевой счет                     |           ];
[+------+----------------------------------------+----------------------------------+           ];
end;

  PrintAccount( rs );
end;

private macro MakeSqlQuery( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted ) : string

  var sqlInclBal, sqlExclBal : string;
  var strSelAccBlnc : string;
  var strTableAccBlnc : string;
  var strSelSetTAcc : string;
  var strTableAccount : string;
  var strExistsOpened : string;
  var strExistsClosed : string;
  var strExistsDeleted : string;
  var strOpenedOrClosed : string;
  var strDeleted : string;
  var strWhereCurrency : string;
  var strWhereResult : string;
  var ConvertMask : string;

  if( Currency == 0 )
    strTableAccBlnc = "daccblnc_dbt acb";
    strTableAccount = "daccount_dbt acc";

    strWhereCurrency = " AND st.t_FIID = 0 ";
  else
    strTableAccBlnc = "daccblnc$_dbt acb";
    strTableAccount = "daccount$_dbt acc";

    strWhereCurrency = " AND st.t_FIID != 0 ";
  end;

  ConvertMask = ConvertMaskToSQLFormat( MaskInclBal, "acb.t_Balance" + NumPlan );
  if((strlen(ConvertMask) == 0) and (strlen(MaskInclBal) != 0))
    msgbox("Некорректное значение поля 'Включая балансовые'");
    exit(1);
  end;
  if(strlen(ConvertMask) == 0)
    sqlInclBal = "(1 = 1)";
  else
    sqlInclBal = "(" + ConvertMask + " AND acb.t_Balance" + NumPlan + " != CHR(1))";
  end;

  ConvertMask = ConvertMaskToSQLFormat( MaskExclBal, "acb.t_Balance" + NumPlan );
  if((strlen(ConvertMask) == 0) and (strlen(MaskExclBal) != 0))
    msgbox("Некорректное значение поля 'Исключая балансовые'");
    exit(1);
  end;
  if(strlen(ConvertMask) == 0)
    sqlExclBal = "(1 = 0)";
  else
    sqlExclBal = "(" + ConvertMask + " AND acb.t_Balance" + NumPlan + " != CHR(1))";
  end;

  strSelAccBlnc = "SELECT acb.t_Account FROM " + strTableAccBlnc + " WHERE " + sqlInclBal + " AND NOT " + sqlExclBal + " AND acb.t_Account = st.t_Account AND st.t_Chapter = acb.t_Chapter AND st.t_FIID = acb.t_Code_Currency";

  strExistsOpened = "EXISTS(SELECT 1 FROM " + strTableAccount + " WHERE acc.t_Open_Close = CHR(0) AND acc.t_Account = st.t_Account  AND st.t_Chapter = acc.t_Chapter AND st.t_FIID = acc.t_Code_Currency)";

  strExistsClosed = "EXISTS(SELECT 1 FROM " + strTableAccount + " WHERE acc.t_Open_Close != CHR(0) AND acc.t_Account = st.t_Account AND st.t_Chapter = acc.t_Chapter AND st.t_FIID = acc.t_Code_Currency)";

  strExistsDeleted = "NOT EXISTS(SELECT 1 FROM " + strTableAccount + " WHERE acc.t_Account = st.t_Account AND st.t_Chapter = acc.t_Chapter AND st.t_FIID = acc.t_Code_Currency) " +
                     "AND st.t_BankID IN (SELECT t_PartyID FROM ddp_dep_dbt)";

  if(( Opened == 0 ) and ( Closed == 0 ))
    strOpenedOrClosed = "";
  else 
    if(( Opened != 0 ) and ( Closed == 0 ))
      strOpenedOrClosed = " AND (" + strExistsOpened + ")";
    else
      if(( Opened == 0 ) and ( Closed != 0 ))
        strOpenedOrClosed = " AND (" + strExistsClosed + ")";
      else
        if(( Opened != 0 ) and ( Closed != 0 ))
          strOpenedOrClosed = " AND ((" + strExistsClosed + ") OR (" + strExistsOpened + "))";
        end;
      end;
    end;
  end;

  if((( Opened != 0 ) or ( Closed != 0 )) and ( Deleted != 0 ))
    strWhereResult = "( st.t_Account IN (" + strSelAccBlnc + ")" + strOpenedOrClosed + ")" + "OR (" + strExistsDeleted + ")";
  else
    if((( Opened == 0 ) and ( Closed == 0 )) and ( Deleted != 0 ))
      strWhereResult = "(" + strExistsDeleted + ")";
    else
      if((( Opened != 0 ) or ( Closed != 0 )) and ( Deleted == 0 ))
        strWhereResult = "( st.t_Account IN (" + strSelAccBlnc + ")" + strOpenedOrClosed + ")";
      end;
    end;
  end;
  
  strSelSetTAcc = "SELECT st.t_Account, st.t_PartyID, st.t_Order, st.t_ShortName, ptc.t_Code, pt.t_Name " + 
                  "FROM dsettacc_dbt st " +
                  "FULL OUTER JOIN dpartcode_dbt ptc ON (ptc.t_PartyID = st.t_PartyID AND ptc.t_CodeKind = 1) " +
                  "JOIN dparty_dbt pt ON (pt.t_PartyID = st.t_PartyID) " +
                  "WHERE (" + strWhereResult + ")" + strWhereCurrency + 
                  "ORDER BY ptc.t_Code, st.t_Order";

  return strSelSetTAcc;

end;

private macro PrintContext( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;
  
  strSql = MakeSqlQuery( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted );  

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())
    PrintParty( rs );
  end;

  if( PrevPartyID == 0 )
[  

  Записей, соответствующих установленным значениям настроек отчета не найдено];
  end;
end;

private macro PrintBottom()
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t2.t_post, t1.t_name                      " +
           "FROM dperson_dbt t1 FULL JOIN dofficer_dbt t2 ON " +
           "( t2.t_personid = t1.t_partyid ) WHERE t1.t_oper = " + {oper};
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    if( StrLen(rs.value( 0 )) > 0 )
      [
      #####################________________________#################################] ( rs.value( 0 ), rs.value( 1 ));
    else
      [
                           ________________________#################################] ( rs.value( 1 ));
    end;
  end;
end;

macro PrintReport( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted )
  PrintHeader();
  PrintTop();

  PrintParm( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted );

  PrintContext( NumPlan, NamePlan, MaskInclBal, MaskExclBal, Currency, Opened, Closed, Deleted );

  PrintBottom();
end;
