/*
    Библиотека макрофункций, расширяющих возможности языка RSL.

    Функции:
        Ternary     - аналог тернарного оператора в C/C++
        NVL         - аналог функции NVL в PL/SQL
        PureVirtualFunc - функция для реализации чисто виртуальных функций
                      и абстрактных классов.
        DefaultParm - параметры функции "по умолчанию".
*/

/*
    Аналог тернарного оператора в C/C++ (если верно _if, результат - _then, иначе _else).
    Параметр isStringalized (необяз.) введён для того, чтобы, как в C/C++, один
    из аргументов не выполнялся. Если isStringalized == true, то _then и _else
    передаются как строки, и выбранное из них значение выполняется с помощью ExecExp.
    По умолчанию false.
*/
MACRO Ternary( _if, _then, _else, isStringalized )
    if( isStringalized )
        if( _if )
            return ExecExp( _then );
        else
            return ExecExp( _else );
        end;
    else
        if( _if )
            return _then;
        else
            return _else;
        end;
    end;
END;

/*
    Аналог функции NVL из PL/SQL. Если val == NULL, то результат - newval,
    иначе val.
    Если потребуется, для newval можно ввести флаг isStringalized, как в Ternary.
*/
MACRO NVL( val, newval, checkspecval26 )
    if (checkspecval26 and (ValType(val) == 26) )
        return newval;
    end;
    if( val != NULL )
        return val;
    else
        return newval;
    end;
END;

// Эту функцию надо вызывать из "чисто виртуальных" функций.
// Абстрактный класс - класс с чисто виртуальным конструктором.
MACRO PureVirtualFunc( )
    RunError( "Ошибка программирования!|Вызвана функция, которую необходимо переопределять." );
END;

// Параметры функции "по умолчанию", во избежание путаницы настоятельно рекомендуется использование
// DefaultParm() сразу же за "заголовком" функции.
// Пример: macro SomeMacro( someInt : Integer ) 
//         DefaultParm( someInt, 100 );
MACRO DefaultParm( parm, value )
    if ( parm == NULL )
        SetParm( 0, value );
    end;
END;
/*
 проверяет вхождение значения в множество    
 пример:                                     
     in(что сравниваем, с чем (через запятую) );
*/
macro in(value)
    var i = 1;
    var v;
    while (getParm(i, v))
        if (value == v)
            return true;
        end;
        i = i + 1;
    end;
    return false;
end;
