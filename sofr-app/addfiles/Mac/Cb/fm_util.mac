/*
 *  Вспомогательные функции для заполнения файла выгрузки
 *
 *
 */

Import FMInter, PTInter, "globals.mac";

private var ClientAccountMask : string;

private var _3MonthsCalcType : integer;

private var IsImmDealControl : bool;

private var CashClientMode : integer;

/* Определить, является ли счет клиентским счетом */
macro IsClientAccount( AccountNumber : string ) : bool

  var stat;

  if( AccountNumber != "" )
    
    if( ClientAccountMask == null )
      GetRegistryValue( "PS\\REQOPENACC\\Счета клиентов", V_STRING, ClientAccountMask, stat );
    end;

    if( ClientAccountMask != null )
      if( CompareStrWithMasks(ClientAccountMask, AccountNumber) == 0 )
        return true;
      end;
    end;

  end;

  return false;

end;


/*Считать значение настройки "ФИНМОНИТОРИНГ / Исчисление трехмесячного срока".*/
macro Get_3MonthsCalcType(  ) : integer

  var stat;
  
  if( _3MonthsCalcType == null )      
    GetRegistryValue("ФИНМОНИТОРИНГ\\ИСЧИСЛЕНИЕ ТРЕХМЕСЯЧНОГО СРОКА", V_INTEGER, _3MonthsCalcType, stat);
  end;  

  return _3MonthsCalcType;

end;


/*Считать значение настройки "ФИНМОНИТОРИНГ / СДЕЛКИ С НЕДВИЖИМОСТЬЮ".*/
macro GetImmDealRegVal(  ) : bool

  var stat;    
  
  if( IsImmDealControl == null )
      GetRegistryValue("ФИНМОНИТОРИНГ\\СДЕЛКИ С НЕДВИЖИМОСТЬЮ", V_BOOL, IsImmDealControl, stat);    
  end;  

  return IsImmDealControl;

end;

/*Считать значение настройки "ФИНМОНИТОРИНГ / УЧАСТНИКИ ОПЕРАЦИИ РКО ПО КАССЕ".*/
macro GetCashClientMode() : integer

  var stat;    
  
  if( CashClientMode == null )
      GetRegistryValue("ФИНМОНИТОРИНГ\\УЧАСТНИКИ ОПЕРАЦИИ РКО ПО КАССЕ", V_INTEGER, CashClientMode, stat);    
  end;  

  return CashClientMode;

end;

/* Получить тип субъекта */
macro FM_GetPartyLegalForm( PartyID : integer ) : integer
  
  var LegalForm : integer;

  record ptRec("party.dbt");

  LegalForm = null;

  if( PartyID > 0 )
    
    if( ПолучитьСубъекта(PartyID, ptRec) == 0 )
      LegalForm = ptRec.LegalForm;
    end;

  end;

  return LegalForm;

end;

/* Проверить, является ли субъект юридическим лицом */
macro IsPartyInst( PartyID : integer ) : bool
  record ptRec("party.dbt");

  if( PartyID > 0 )
    if( ПолучитьСубъекта(PartyID, ptRec) == 0 )
      if( ptRec.LegalForm == PTLEGF_INST )
        return true;
      end;
    end;
  end;

  return false;
end;

/* Проверить, является ли субъект физическим лицом */
macro IsPartyPersn( PartyID : integer ) : bool
  record ptRec("party.dbt");

  if( PartyID > 0 )
    if( ПолучитьСубъекта(PartyID, ptRec) == 0 )
      if( ptRec.LegalForm == PTLEGF_PERSN )
        return true;
      end;
    end;
  end;

  return false;
end;

/* Проверить, является ли субъект юридическим лицом или предпринимателем */
macro IsPartyInstOrEmployer( PartyID : integer ) : bool
    
  file psRec("persn.dbt") key 0;

  if( IsPartyInst(PartyID) )

    return true;

  else
  
    ClearRecord( psRec );
    psRec.PersonID = PartyID;

    if( (GetEQ(psRec)) and (psRec.IsEmployer == "X") )
      return true;
    end;
  
  end;

  return false;
end;

