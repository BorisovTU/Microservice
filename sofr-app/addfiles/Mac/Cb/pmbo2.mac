 /*
 $Name:        pmbo2.mac
 $Module:      Ядро Банкинг
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок     : 29010 - "Обработка в БО"
// Шаг      : 20    - "Ожидание реакции бэк-офиса"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, cbsttls, pm_setst, OprInter, lnpaym;
var PaymentObj:RsbPayment;

private macro GetAttrDeny : integer
  var AttrDeny : integer = 0;

  var attr : TRecHandler = TRecHandler("objattr.dbt");
  attr.Clear();
  if( PaymentObj.Categories.GetMainAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, {curdate}, attr) == 0 )
    AttrDeny = attr.rec.AttrID;
  end;

  return AttrDeny;
end;

//-----------------------------------------------------------------------------
// Выполнение шаг
//-----------------------------------------------------------------------------
MACRO ExecuteCaseStep(Kind_Operation, Number_Step, paymDoc, KindDoc)

  if( PaymentObj.ToBackOffice == "В" )
    PaymentObj.ReplicationSession = 0;
  end;

  // Установить статус платежа
  if( PaymentObj.PaymStatus == PM_FINISHED )
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    // Установить статус первички "Закрыт"
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );

    var RetPaymentID : integer = 0;
    if( NeedGenPmCarryNotify(PaymentObj.PaymentID, @RetPaymentID) )
      var PaymForNotify : RsbPayment = RsbPayment(RetPaymentID);
      PaymForNotify.NeedExecNotify = WLPM_EXECNOTIFY_SUCCESS;
    end;

    return ""; // Завершить выполнение блока

  elif( PaymentObj.PaymStatus == PM_REJECTED )
    if( InList(GetAttrDeny(), 0, ATTR_PAYMENT_FMDENY_CHECKED) )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT_BO, OPR_PAYM_DO, OPR_PM_ST_RETURN ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    else
      if( УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NEED ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;

    return ""; // Завершить выполнение блока

  elif( PaymentObj.PaymStatus == PM_READIED )
    if( PaymentObj.PrimDocKind == WL_INDOC )
      PaymentObj.PaymStatus = PM_KVITPROCESSING;
    end;

    if( (PaymentObj.VO_Accept != ACPT_CC_NEED) and (PaymentObj.VO_Accept != ACPT_CC_DEFINE) )
      PaymentObj.ValueDate = {curdate};
      return "30";
    else
      return "";
    end;
  else
    msgbox("Не закончена обработка документа в бэк-офисе");
    return 1;
  end;

  return "";
END;

MACRO CheckStepAction( mes )
  
  if( mes == OP_BACKOUT_STEP )
    if( PaymentObj.ReplicationSession != 0 )
      msgbox("Невозможно откатить шаг операции,|т.к. платеж обрабатывается в ПС " + LnFindType(PaymentObj.ReplicationBO) );
      return 1;
    end;
  end;

  return 0;
END;
