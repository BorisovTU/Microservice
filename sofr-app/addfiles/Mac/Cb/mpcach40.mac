/*
 $Name: mpcach40.mac
 $Module: Главная книга
 $Description: Балансовый учет
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcach40.mac

  Библиотека       : RSACCOUNT

  Описание         : Балансовый учет

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet, InsCarryDoc, CTInter, PTInter,globals, CurrInter, mpcactacc, mpccarry,
       mpclb;


record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    ContractID = 0,
    DocID = 0,
    ResKind = 0,
    TypeDocs = 0,
    DeltaSum = $0,
    TrnMsg = "";

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);


PRIVATE MACRO ActuateAcc(CatID, Account:@string, FIID:@integer, Chapter:@integer)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID,CatCode, @CodeCurr, @Chapt );
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;

   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;


// Сохраняем изменения 
PRIVATE MACRO  WriteSumFact(Sum)
   var   cmd,
         stat = 0;

   if (prccntopr.rec.newcntschedid != 0)
      
      if (PrcSetCntSchedFactSumByID(prccntopr.rec.newcntschedid, money(Sum), prccntopr.rec.OperDate, prccntopr.rec.OperDate) != 0)
         return 1;
      end;
    
   else
      
      if (PrcInsertAddChargePeriod(ContractID,
                                    prccntopr.rec.OperDate,
                                    prccntopr.rec.BeginDate,
                                    prccntopr.rec.EndDate,
                                    money(0),
                                    money(Sum),
									prccntopr.rec.DocId,
                                    PRC_PAYSTATUS_AWARDED) != 0)
         return 1;
      end; 
      
   end;   

   return 0;
   
END;


PRIVATE MACRO DoOperationCarry()
   var Debet="",Credit="",
      DebetCatID = 0, CreditCatID = 0,
      DebetTypeID = 0, CreditTypeID = 0,
      CreditFIID, DebetFIID, ContrFIID,
      DebetDep = 0, CreditDep = 0,
      CarrySum = $0, DebetCarrySum = $0, CreditCarrySum = $0,
      Chapter = 0,
      НазначениеПроводки = "",
      PeriodBeginDate, PeriodEndDate,
      stat = 0,
      fd;

   record acc_buf (account);

   ContrFIID = prccontract.rec.fiid;
   GetDatesFromPeriod(ContractID, 2, prccntopr.rec.OperDate, @PeriodBeginDate, @PeriodEndDate);
   if (DeltaSum > 0)
      
      if (TypeDocs == PRC_ACCSCHEMA_PRIV)
         Chapter = 1;
         DebetTypeID = 3;
         CreditTypeID = 1;
         GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
         НазначениеПроводки = "Отнесение на расходы начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
      end;
      if (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL)
         Chapter = 1;
         DebetTypeID = 1;
         CreditTypeID = 3;
         GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
         НазначениеПроводки = "Отнесение на доходы начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
      end;

      if (TypeDocs == PRC_ACCSCHEMA_RAZM_NONBAL)
        if ({curdate} < ДАТА_ВВОДА)
            Chapter = 3;
            DebetTypeID = 5;
            GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  

            fd = PrcContractFD(ContractID);
            Credit = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                            fd,
                                            {curdate},
                                            NULL,
                                            MC_OPENACC_CREATE,
                                            acc_buf,
                                            NATCUR,
                                            NULL,
                                            MC_PAIRACCMODE_AUTO,
                                            NULL,
                                            NULL,
                                            null, null, null, null,
                                        {curdate}            // дата начала действия счета 
                                        );
            CreditFIID = acc_buf.code_currency;
            НазначениеПроводки = "Учет начисленных процентов за период с " +
                            PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                            prccontract.rec.Number +" по счету " + prccontract.rec.Account;
        else
            return 0;
        end;
      end;
   else
      if (TypeDocs == PRC_ACCSCHEMA_PRIV)
         Chapter = 1;
         DebetTypeID = 1;
         CreditTypeID = 3;
         GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
         НазначениеПроводки = "Корректировка излишне начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
      end;
      if (TypeDocs == PRC_ACCSCHEMA_RAZM_BAL)
         Chapter = 1;
         DebetTypeID = 3;
         CreditTypeID = 1;
         GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
         НазначениеПроводки = "Корректировка излишне начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
      end;
      if (TypeDocs == PRC_ACCSCHEMA_RAZM_NONBAL)
         Chapter = 3;
         CreditTypeID = 5;
         GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  

         fd = PrcContractFD(ContractID);
         Debet = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        acc_buf,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            // дата начала действия счета 
                                       );
         DebetFIID = acc_buf.code_currency;
         НазначениеПроводки = "Учет излишне начисленных процентов за период с " +
                        PeriodBeginDate + " по " + PeriodEndDate + " для договора " +
                        prccontract.rec.Number +" по счету " + prccontract.rec.Account;
      end;
   end;

   if ((Debet == "") and (DebetCatID == 0)) 
      TrnMsg = "Не определен счет дебета и не задана соотв.КУ для договора";
      return 1;
   else
      if (Debet == "")
         ActuateAcc(DebetCatID, @Debet, @DebetFIID, @Chapter);
         if (Debet == "")
            TrnMsg = "Не удалось активизировать счет дебета";
            return 1;
         else
            PrcSetCntAcc(ContractID, DebetTypeID, DebetFIID, Chapter, Debet);
         end;
      end;
   end;

   if ((Credit == "") and (CreditCatID == 0))
      TrnMsg = "Не определен счет кредита и не задана соотв.КУ для договора";
      return 1;
   else
      if (Credit == "")
         ActuateAcc(CreditCatID, @Credit, @CreditFIID, @Chapter);
         if (Credit == "")
            TrnMsg = "Не удалось активизировать счет кредита";
            return 1; 
         else
            PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
         end;
      end;
   end;

   CarrySum = abs(DeltaSum);
   if (CarrySum == 0)
      TrnMsg = "Сумма проводки не должна быть равной 0";
      return 1; 
   end; 

   if (ValType(DebetFIID) == V_UNDEF)
      DebetFIID = ContrFIID;
   end;
   if (ValType(CreditFIID) == V_UNDEF)
      CreditFIID = ContrFIID;
   end;

   if (ValType(DebetFIID) == V_UNDEF)
      TrnMsg = "Не опередлен код валюты дебета";
      return 1;
   end;
   if (ValType(CreditFIID) == V_UNDEF)
      TrnMsg = "Не определен код валюты кредита";
      return 1;
   end;
   
   if (DebetFIID != ContrFIID)
      ConvSum(DebetCarrySum, CarrySum, prccntopr.rec.DocDate, ContrFIID, DebetFIID );
   else
      DebetCarrySum  = CarrySum;
   end;
   if (CreditFIID != ContrFIID)
      ConvSum(CreditCarrySum, CarrySum, prccntopr.rec.DocDate, ContrFIID, CreditFIID );
   else
      CreditCarrySum = CarrySum; 
   end;

   if ((DebetCarrySum == 0) or (CreditCarrySum == 0))
      TrnMsg = "Сумма проводки не должна быть равной 0";
      return 1; 
   end;

   GetAccountDepartment(Debet, Chapter,DebetFIID, @DebetDep);
   GetAccountDepartment(Credit,Chapter,CreditFIID,  @CreditDep);   
   if (DebetDep == 0)
      DebetDep = {OperDprt};
   end;
   if (CreditDep == 0)
      CreditDep = {OperDprt};
   end;
   
   stat = Prc_DocCarry(  
                        Chapter,                // Глава
                        DebetFIID,              // Валюта Дебет
                        CreditFIID,             // Валюта Кредит
                        Debet,                  // Дебет
                        Credit,                 // Кредит
                        DebetCarrySum,          // сколько
                        CreditCarrySum,
                        prccntopr.rec.DocDate,  // За какое число
                        "09",
                        NULL,
                        0,                       // CarryResult
                        НазначениеПроводки
                       );


   if (stat != 0)
      return 1;
   end;

   PrcSaveOperDoc(
                     DocID, 
                     1,
                     Chapter,
                     Debet, 
                     DebetDep,
                     DebetFIID, 
                     money(DebetCarrySum),
                     Credit,
                     CreditDep,
                     CreditFIID,
                     money(CreditCarrySum),
                     НазначениеПроводки);
   
   WriteSumFact(DeltaSum);
   
   return 0;
END;


MACRO ExecuteStep(Buffer, Document)
   var ЗначениеНастройки,
       ClientQuality = 0;

   SetBuff( cntopr_buf, Document );

   ContractID  = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   prccntopr.KeyNum = 0;	
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      msgbox ("Не найдена операция доначисления");
      return 1;
   end; 
   
   DeltaSum    = prccntopr.rec.sum;

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;	
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      msgbox ("Не могу найти договор с ID = " + ContractID);
      return 1;
   end;
   
   ResKind = prccontract.rec.ResKind;

   // 1.0
   if (ResKind == PRC_RESKIND_PRIV) // Привлечение
      TypeDocs = PRC_ACCSCHEMA_PRIV;
   end;

    // Размещение
    if (ResKind == PRC_RESKIND_RAZM)
        if ({curdate} < ДАТА_ВВОДА)
            if (PrcGetClientQualityOnDate(ContractID, prccntopr.rec.EndDate, @ClientQuality) != 0)
                MemoryError(6829);
                DisplayError();
                return 1;
            end;
        
            if ((ClientQuality == 1) or (ClientQuality == 2))         
                TypeDocs = PRC_ACCSCHEMA_RAZM_BAL; // размещение.баланс
            elif ((ClientQuality == 4) or (ClientQuality == 5))         
                TypeDocs = PRC_ACCSCHEMA_RAZM_NONBAL; // размещение.внебаланс
            elif (ClientQuality == 3)
                if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CATEG3, @ЗначениеНастройки) != 0)
                    return 1;
                end;

                if (ЗначениеНастройки)
                    TypeDocs = PRC_ACCSCHEMA_RAZM_BAL; // // размещение.баланс
                else
                    TypeDocs = PRC_ACCSCHEMA_RAZM_NONBAL; // размещение.внебаланс 
                end;
            else
                MemoryError(6829);
                DisplayError();
                return 1;
            end;
        else
            TypeDocs = PRC_ACCSCHEMA_RAZM_BAL; // // размещение.баланс
        end;
    end;

   if (PrcSetOperTypeDocs(DocID, TypeDocs, 0) != 0)
      return 1;
   end;
   
   if (DoOperationCarry() != 0)
      if (TrnMsg != "")
         msgbox(TrnMsg);
      end;
      return 1;
   end;

   if (TrnMsg != "")
      msgbox(TrnMsg);
   end;
  
   return 0;
   
END;



MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

