 /*
 $Name: ws_fiunilistclass.mac
 $Module: Главная книга
 $Description: Макрос ФИ
 */

import rcw;
import rsd;
import rsbdataset;
import FIInter;
import "ws_ficlass.mac";
import "ws_datafilter.mac";
import "ws_partycommon.mac";

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

private macro GetSortStr( fltrParm,
                          OrderItems,            /* Параметры сортировки */
                          Table:string           /* Название таблицы c . */
                        ):String

  var SortStr:string = "";
  var DefaultSortStr:string = "";

  var CodeKind = 1;
  var Codekind2 = 1;

  if( fltrParm != null )
    if(fltrParm.CodeKindWidget != null)
      CodeKind = fltrParm.CodeKindWidget;
    end;

    if(fltrParm.CodeKindList != null)
      CodeKind2 = fltrParm.CodeKindList;
    end;
  end;

  DefaultSortStr = "kind";

  if(CodeKind != CodeKind2)
      DefaultSortStr = DefaultSortStr + ", CODE2";
    else
      DefaultSortStr = DefaultSortStr + ", CODE";
    end;

  if( (OrderItems == NULL) or (OrderItems.Size() <= 0) )

    SortStr = DefaultSortStr;

  else

    if(OrderItems[0].Column == "CodeList")

      if(CodeKind != CodeKind2)
        OrderItems[0].Column = " CODE2";
      else
        OrderItems[0].Column = " CODE";
      end;

    end;
    SortStr = " " + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");

    var i:integer = 1;
    while( i < OrderItems.size() )

      if(OrderItems[i].Column == "CodeList")

        if(CodeKind != CodeKind2)
          OrderItems[i].Column = " CODE2";
        else
          OrderItems[i].Column = " CODE";
        end;

      end;

      SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
      i = i + 1;
    end;
    SortStr = SortStr + ", " + DefaultSortStr;

  end;

  return SortStr;

end;

private macro GetSqlFilterCondition(FilterConditionItems)
  var fp = FilterParser( FilterConditionItems );

  fp.AddFieldCondition( 1, "fi", "t_fi_code");
  fp.AddFieldCondition( 2, "fi", "t_name");
  fp.AddFieldCondition( 3, "fi", "t_fiid" );

  fp.GetFullSQLConditionDebug( "ws_filister_filter_debug" );

  return fp.GetFullSQLCondition();
end;

private macro GetFIQuery( fltrParm,
                          AddWhereCond,
                          listLen,
                          filterConditionItems,
                          orderitems )

  var i = 0;

  var CodeKind = 1;
  var Codekind2 = 1;

  if( fltrParm != null )
    if(fltrParm.CodeKindWidget != null)
      CodeKind = fltrParm.CodeKindWidget;
    end;

    if(fltrParm.CodeKindList != null)
      CodeKind2 = fltrParm.CodeKindList;
    end;
  end;

  var queryFrom = "FROM dfininstr_dbt fi ";
  var queryWhere = "";
  var query = "SELECT fi.t_fiid AS Fiid, "
              "        fi.t_fi_kind AS kind, "
              "        fi.t_avoirkind AS avoirkind, "
              "        fi.t_name AS Name, "
              "        fi.t_iso_number AS isonum, ";

  if( (fltrParm == null) OR (fltrParm.FindFIID == null)  )
    queryWhere = " WHERE fi.t_FIID >=0 ";
  else
    queryWhere = " WHERE fi.t_FIID = " + string(fltrParm.FindFIID);
  end;

  if(filterConditionItems != NULL)
    var sqlFilterCondition = GetSqlFilterCondition(filterConditionItems);

    if(sqlFilterCondition != "")
      if(queryWhere == "")
        queryWhere = " WHERE " + sqlFilterCondition;
      else
        queryWhere = queryWhere + " AND " + sqlFilterCondition;
      end;
    end;
  end;

  if(CodeKind == 0)
    query = query + "fi.t_FIID";
  elif(CodeKind == FICK_USERCODE)
    query = query + "fi.t_FI_Code";
  elif(CodeKind ==FICK_ISONUMBER)
    query = query + "fi.t_ISO_Number";
  elif(CodeKind ==FICK_ISOSTRING)
    query = query + "fi.t_CCY";
  elif(CodeKind ==FICK_LSIN)
    query = query + "av.t_lsin";
    queryFrom  = queryFrom + ", davoiriss_dbt av ";
    queryWhere = queryWhere + " AND av.t_FIID = fi.t_FIID ";
  elif(CodeKind ==FICK_ISIN)
    query = query + "av.t_isin";
    queryFrom  = queryFrom + ", davoiriss_dbt av ";
    queryWhere = queryWhere + " AND av.t_FIID = fi.t_FIID ";
  elif(CodeKind ==6)
    query = query + "fi.t_CodeInAccount";
  else
    query = query + "  (SELECT oc.t_Code " +
                    "    FROM dobjcode_dbt oc " +
                    "   WHERE     oc.t_Objecttype = 9 " + // OBJTYPE_FININSTR
                    "         AND oc.t_CodeKind = " + string(CodeKind) +
                    "         AND oc.t_ObjectID = fi.t_FIID " +
                    "         AND oc.t_BankDate IN " +
                    "                (SELECT MAX (t_BankDate) " +
                    "                   FROM dobjcode_dbt " +
                    "                  WHERE     t_Objecttype = 9 " + // OBJTYPE_FININSTR
                    "                        AND t_CodeKind = " + string(CodeKind) +
                    "                        AND t_ObjectID = fi.t_FIID)) ";
  end;
  query = query + " AS Code " ;

  if(CodeKind != CodeKind2)
      query = query + ", ";
    if(CodeKind2 == 0)
      query = query + "fi.t_FIID";
    elif(CodeKind2 == FICK_USERCODE)
      query = query + "fi.t_FI_Code";
    elif(CodeKind2 ==FICK_ISONUMBER)
      query = query + "fi.t_ISO_Number";
    elif(CodeKind2 ==FICK_ISOSTRING)
      query = query + "fi.t_CCY";
    elif(CodeKind2 ==FICK_LSIN)
      query = query + "av2.t_lsin";
      queryFrom  = queryFrom + ", davoiriss_dbt av2 ";
      queryWhere = queryWhere + " AND av2.t_FIID = fi.t_FIID ";
    elif(CodeKind2 ==FICK_ISIN)
      query = query + "av2.t_isin";
      queryFrom  = queryFrom + ", davoiriss_dbt av2 ";
      queryWhere = queryWhere + " AND av2.t_FIID = fi.t_FIID ";
    elif(CodeKind2 ==6)
      query = query + "fi.t_CodeInAccount";
    else
      query = query + "  (SELECT oc.t_Code " +
                      "    FROM dobjcode_dbt oc " +
                      "   WHERE     oc.t_Objecttype = 9 " + // OBJTYPE_FININSTR
                      "         AND oc.t_CodeKind = " + string(CodeKind) +
                      "         AND oc.t_ObjectID = fi.t_FIID " +
                      "         AND oc.t_BankDate IN " +
                      "                (SELECT MAX (t_BankDate) " +
                      "                   FROM dobjcode_dbt " +
                      "                  WHERE     t_Objecttype = 9 " + // OBJTYPE_FININSTR
                      "                        AND t_CodeKind = " + string(CodeKind) +
                      "                        AND t_ObjectID = fi.t_FIID)) ";
    end;
    query = query + " AS Code2 " ;
  end;

  if( fltrParm != null )

    if(fltrParm.FIKinds != null)

      if( fltrParm.FIKinds.size > 0 )

        if(queryWhere != "")
          queryWhere = queryWhere + " AND ";
        end;
        queryWhere = queryWhere + " fi.t_fi_kind IN ( ";

        i = 0;
        while(i < fltrParm.FIKinds.size)
          if(i != 0 )
            queryWhere = queryWhere + ",";
          end;
          queryWhere = queryWhere + string(fltrParm.FIKinds[i]);
          i = i + 1;
        end;

        queryWhere = queryWhere + " ) ";

      end;

    end;

    if(fltrParm.AvoirKinds != null)
      if(queryWhere != "")
         queryWhere = queryWhere + " AND ";
      end;

      if((fltrParm.IncSubAvKinds == null) OR (fltrParm.IncSubAvKinds == false))
        queryWhere = queryWhere + " fi.t_avoirkind IN ( ";
        i = 0;
        while(i < fltrParm.AvoirKinds.size)
          if(i != 0 )
            queryWhere = queryWhere + ",";
          end;
          queryWhere = queryWhere + string(fltrParm.AvoirKinds[i]);
          i = i + 1;
        end;
        queryWhere = queryWhere + " ) ";
      else
        i = 0;
        queryWhere = queryWhere + " ( ";
        while(i < fltrParm.AvoirKinds.size)
          if(i != 0 )
            queryWhere = queryWhere + " OR ";
          end;

          queryWhere = queryWhere + " RSB_FIInstr.FI_AvrKindsEQ ("+ FIKIND_AVOIRISS +", ";
          queryWhere = queryWhere + string(fltrParm.AvoirKinds[i]);
          queryWhere = queryWhere + ", fi.t_avoirkind) > 0 ";
          i = i + 1;
        end;
        queryWhere = queryWhere + " ) ";
      end;
    end;

    if(fltrParm.TaxGroups != null)
      if(queryWhere != "")
        queryWhere = queryWhere + " AND ";
      end;

      queryFrom  = queryFrom + ", davoiriss_dbt avf ";
      queryWhere = queryWhere + " avf.t_FIID = fi.t_FIID ";

      if((fltrParm.NotInTaxGrp == null) OR (fltrParm.NotInTaxGrp == false))
        queryWhere = queryWhere + " AND avf.t_TaxGroup IN ( ";
      else
        queryWhere = queryWhere + " AND avf.t_TaxGroup NOT IN ( ";
      end;

      i = 0;
      while(i < fltrParm.TaxGroups.size)
        if(i != 0 )
          queryWhere = queryWhere + ",";
        end;
        queryWhere = queryWhere + string(fltrParm.TaxGroups[i]);
        i = i + 1;
      end;

      queryWhere = queryWhere + " ) ";
    end;

    if(fltrParm.IsClosed == false)
      queryWhere = queryWhere + " AND fi.T_ISCLOSED != 'X' ";
     end;
  end;

  query = query + queryFrom + queryWhere;
  
  if( AddWhereCond != null )
   query = query + " AND (" + AddWhereCond + ")";
  end;  

  if( (fltrParm!= null) AND (fltrParm.FICode != null) AND (fltrParm.FICode != ""))
    query = "SELECT * FROM (" + query +") WHERE Code = '" + fltrParm.FICode +"'";
  end;

  if( (fltrParm!= null) AND (fltrParm.LikeFICode != null) )
    query = "SELECT * FROM (" + query +") WHERE LOWER (Code) LIKE LOWER ('%" + fltrParm.LikeFICode + "%') ";
  end;

  if( (listLen != null) AND (listLen != 0) )
    query = query + " AND ROWNUM < " + string(listLen);
  end;

  query = query + " ORDER BY " + GetSortStr(fltrParm, orderitems, "fi.");

  return query;
end;

////////////////////////////////////////////////////////////////////////////////////////////////////////////
// class FIUNIList
////////////////////////////////////////////////////////////////////////////////////////////////////////////

class FIUNIList

  macro GetList( PageSize,
                 PageIndex,
                 fltrParm,
                 AddWhereCond,
                 listLen,
                 filterConditionItems,
                 orderitems):TArray

    var p;

    var result = TArray();

    var query = GetFIQuery( fltrParm, AddWhereCond, listLen, filterConditionItems, orderitems );

    if((ValType(PageSize) == V_INTEGER) AND (PageSize >= 0)
      AND (ValType(PageIndex) == V_INTEGER) AND (PageIndex >= 0))
      query = SQL_WrapSelect( query, PageIndex, PageSize);
    else
       query = SQL_WrapSelect( query, 0, 0);
    end;

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    var CodeKind = 1;
    var Codekind2 = 1;

    if( fltrParm != null )
      if(fltrParm.CodeKindWidget != null)
        CodeKind = fltrParm.CodeKindWidget;
      end;

      if(fltrParm.CodeKindList != null)
        CodeKind2 = fltrParm.CodeKindList;
      end;
    end;

    while (ds.MoveNext())
      if(CodeKind != CodeKind2)
        p = TWsFinInstr(ds.Fiid, IIF(ds.Code  == NULL, "", ds.Code), IIF(ds.Code2 == NULL, "", ds.Code2), ds.kind, ds.avoirkind, ds.Name, ds.isonum, "", "", "");
      else
        p = TWsFinInstr(ds.Fiid, IIF(ds.Code  == NULL, "", ds.Code), IIF(ds.Code  == NULL, "", ds.Code), ds.kind, ds.avoirkind, ds.Name, ds.isonum, "", "", "");
      end;
      result.value(result.Size) = p;
    end;

    return result;

  end;

  macro GetCount( fltrParm,
                  AddWhereCond,
                  listLen,
                  filterConditionItems,
                  orderitems ):integer

    var p;
    var result : integer = 0;

    var query = "SELECT COUNT(*) as recordsCount FROM ( ";
    var queryWhere = GetFIQuery( fltrParm, AddWhereCond, listLen, filterConditionItems, orderitems );

    query = query + queryWhere + ") tt";

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (ds.moveNext())
      result = ds.recordsCount;
    end;

    return result;

  end;

  macro LookUp( fltrParm, AddWhereCond, listLen ):TArray

    return GetList( NULL,
                     NULL,
                     fltrParm,
                     AddWhereCond,
                     listLen,
                     NULL,
                     NULL)

  end;

  macro GetRowNum( ) : integer

  end;

end;

////////////////////////////////////////////////////////////////////////////////////////////////////////
