/*
$Name:             cbrcursimp.mac
$Module:           Главная книга 
$Description:      Макрос загрузки курсов валют с сайта ЦБ
*/

import  XmlRpcInter, ServiceAction, FIInter, BankInter, globals, lib_str;

private var RUR_FI_Code = "";

private class TCBRate( rate )
  //properties
  var m_Vname:string;
  var m_Vnom:integer;
  var m_Vcurs;
  var m_Vcode:string;
  var m_VchCode:string;
  
  var m_FI_Code:string;
  var m_Capture:string;
  var m_bRsRateFound:bool;

  file isocur( "isocur.dbt" ) key 1;

  //constructor
  //m_Vname = rate.selectSingleNode("Vname").text;
  if(rate.selectSingleNode("Vnom"))
    m_Vnom = int( rate.selectSingleNode("Vnom").text );
  else
    m_Vnom = 0;
  end;
  if(rate.selectSingleNode("Vcurs"))
    m_Vcurs = rate.selectSingleNode("Vcurs").text;
  else
    m_Vcurs = "";
  end;
  if(rate.selectSingleNode("Vcode"))
    m_Vcode = rate.selectSingleNode("Vcode").text;
    m_Vcode = strLpad( m_Vcode, 3, "0" );
  else
    m_Vcode = "";
  end;
  if(rate.selectSingleNode("VchCode"))
    m_VchCode = rate.selectSingleNode("VchCode").text;
  else
    m_VchCode = "";
  end;
  m_Capture = "";

  m_bRsRateFound = false;

  isocur.numbercode = m_Vcode;
  if( getEQ(isocur) )
    m_Vname = isocur.namecurr;
  else
    m_Vname = "";
  end;

  //methods
  macro isEqual( ratedef )
    if( (m_Vcurs == ratedef.Rate) and (m_Vnom == ratedef.Scale) and (NATCUR == ratedef.FIID) )
      return true;
    else
      return false;
    end;
  end;

  macro getPoint()
    var Point = 0;
    var i = Index( m_Vcurs, "." );
    if( i > 0 )
      Point = strlen( m_Vcurs ) - i;
    end;
    return Point;
  end;

  macro printLogLine()
[├──────┼──────┼───────────────┼───────────┼─────────┼────────────────────────────────────────────┤
 │ ###  │ #### │###############│ ######### │ ####### │############################################│]
 (m_Vcode, m_VchCode, m_Vname:w, m_Vcurs, m_Vnom, m_Capture:w);
  end;

end;

private macro AreRatedefsEquals( rd1, rd2 )

  if( (rd1.IsInverse == rd2.IsInverse) or
      (rd1.Rate == rd2.Rate) or 
      (rd1.Scale == rd2.Scale) or
      (rd1.Point == rd2.Point)
    )
    return true;
  else
    return false;
  end;
end;

private macro decideIfNewRateNeeded( impOnDate, ratedef, cbRate:TCBRate )
  
  var bNewRateNeeded = false;
  record curratedef( "ratedef.dbt" );
  copy( curratedef, ratedef );
  var onSysDate = date();  
  
  if( ПолучитьЗначениеКурса(ratedef, impOnDate) != 0 )
    cbRate.m_Capture = "Импортировано новое значение курса в историю.";
    bNewRateNeeded = true;
  else
    if( cbRate.isEqual(ratedef) )
      cbRate.m_Capture = "Значение курса равно импортируемому, в импорте нет необходимости.";
    else
      if( impOnDate > onSysDate )
        cbRate.m_Capture = "Импортировано новое будущее значение курса.";
        bNewRateNeeded = true;
      elif( ПолучитьЗначениеКурса(curratedef, OnSysDate) == 0 )
        var err = 0;
        if( AreRatedefsEquals(ratedef,curratedef) )
          GetRegistryValue( "CB\\FINTOOLS\\ИМПОРТ_КУРСОВ_С_САЙТА_ЦБ\\ЗАМЕЩАТЬ_ДЕЙСТВУЮЩИЙ_КУРС", V_BOOL, bNewRateNeeded, err );
          if( bNewRateNeeded == false )
            cbRate.m_Capture = "Действующее значение курса не равно импортируемому, ";
            cbRate.m_Capture = cbRate.m_Capture + "однако импорт действующего значения запрещен настройкой реестра.";
          else
            cbRate.m_Capture = "Импортировано новое действующее значение курса взамен ранее имевшегося.";
          end;
        else
          GetRegistryValue( "CB\\FINTOOLS\\ИМПОРТ_КУРСОВ_С_САЙТА_ЦБ\\ЗАМЕЩАТЬ_ИСТОРИЧЕСКИЙ_КУРС", V_BOOL, bNewRateNeeded, err );
          if( bNewRateNeeded == false )
            cbRate.m_Capture = "Значение курса в истории не равно импортируемому, ";
            cbRate.m_Capture = cbRate.m_Capture + "однако импорт исторического значения запрещен настройкой реестра.";
          else
            cbRate.m_Capture = "Импортировано новое значение курса в историю взамен ранее имевшегося.";
          end;
        end;
        if( err != 0 )
          bNewRateNeeded = false;
        end;
      end;
    end;
  end;
  return bNewRateNeeded;
end;

private macro getErrorDesc( errCode )
  var errDesc = "";
  if( errCode != 0 )
    errDesc = "Ошибка установки курса:";

    if( errCode == 1 )
      errDesc = errDesc + " Не найдена котируемая валюта.";
    elif( errCode == 2 )
      errDesc = errDesc + " Не найдена базовая валюта.";
    elif( errCode == 3 )
      errDesc = errDesc + " Не найден вид курса для текущей валюты.";
    elif( errCode > 100 )
      errDesc = errDesc + " Ошибка Btrieve № " + errCode - 100;
    else
      errDesc = errDesc + " Описание ошибки отсутствует";
    end;
  end;

  return errDesc;
end;

private macro insertNewRate( cbRate, ratedef, realOnDate, ВидКурса )

  record rp( rateparm );  //RATEPARM

  if( cbRate.m_bRsRateFound == true )
    if( ratedef.FIID == NATCUR )
      rp.FI_Code   = RUR_FI_Code;
      rp.OtherFI   = cbRate.m_FI_Code;
      rp.IsInverse = "";
    else
      rp.FI_Code   = cbRate.m_FI_Code;
      rp.OtherFI   = RUR_FI_Code;
      rp.IsInverse = "X";
    end;
    rp.Type      = ratedef.Type;
  else
    rp.FI_Code   = RUR_FI_Code;
    rp.OtherFI   = cbRate.m_FI_Code;
    rp.Type      = ВидКурса;
    rp.IsInverse = "";

    cbRate.m_Capture = cbRate.m_Capture + " Курс для этой валюты отсутствовал, создан.";
  end;

  rp.SinceDate = realOnDate;
  
  rp.Point     = cbRate.getPoint();
  rp.Rate      = double(cbRate.m_Vcurs) * pow( 10, rp.Point );
  rp.Scale     = cbRate.m_Vnom;
  rp.IsDominant = "";

  var errCode = УстановитьКурс( rp );

  if( errCode != 0 )
    cbRate.m_Capture = getErrorDesc( errCode );
  else
    if( cbRate.m_Capture == "" )
      cbRate.m_Capture = cbRate.m_Capture + " Курс введен.";
    end;
  end;

  cbRate.printLogLine();
end;

// загрузка полученных значений курса с сайта ЦБ в RS-Bank
private macro transferOneRateToBase( impOnDate, realOnDate, ВидКурса, cbRate:TCBRate )

  var bNewRateNeeded = false;

  record fi( "fininstr.dbt" );
  record rd( "ratedef.dbt" );

  if( ПолучитьФинИн(cbRate.m_Vcode, fi, NULL, NULL, NULL, FICK_ISONUMBER) != 0 )
    cbRate.m_Capture = "Валюта для импорта курса не найдена.";
  else
    cbRate.m_FI_Code = fi.FI_Code;
    if( ПолучитьКурс( rd, NATCUR, fi.FIID, ВидКурса ) != 0 )
      bNewRateNeeded = true;
      cbRate.m_bRsRateFound = false;
    else //	если котировка найдена, то решаем переписывать ее или нет
      bNewRateNeeded = decideIfNewRateNeeded( impOnDate, rd, cbRate );
      cbRate.m_bRsRateFound = true;
    end;
  end;

  if( bNewRateNeeded )      
    insertNewRate( cbRate, rd, realOnDate, ВидКурса );
  else
    cbRate.printLogLine();
  end;

end;

private macro dateFromStr( strDate )
  return date( int(substr(strDate, 7, 8)), int(substr(strDate, 5, 2)), int(substr(strDate, 1, 4)) );
end;

private macro dateToStr( onDate )
  var S = "", dd, mm, yy;
  DateSplit(onDate, dd, mm, yy);

  // год
  S = S + yy + "-";

  // месяц
  if(mm < 10)
    S = S + "0" + mm + "-";
  else
    S = S + mm + "-";
  end;

  // день
  if(dd < 10)
    S = S + "0" + dd;
  else
    S = S + dd;
  end;
  return S;
end;

private macro printReportHeader( impOnDate, realOnDate )

[ Протокол импорта курсов валют с сайта ЦБ                   ];
[ 
  Время запуска процедуры: ########## ########               ](date(), time() );
[ Пользователь: ##### ###################################### ]({oper}, {Name_Oper});

[ Запрошенная дата начала действия курсов: ##########        ]( impOnDate );
[ Полученная дата начала действия курсов: ##########         ]( realOnDate );

end;

private macro printTableHeader()
[┌──────┬──────┬───────────────┬───────────┬─────────┬────────────────────────────────────────────┐
 │ Цифр │ Букв │  Название     │   Курс    │ Номинал │       Причина включения в протокол         │];
end;

private macro printTableFooter()
[└──────┴──────┴───────────────┴───────────┴─────────┴────────────────────────────────────────────┘];
end;

private macro setRUR_FI_Code()
  record fi( "fininstr.dbt" );
  if( ПолучитьФинИн( "643", fi, NULL, NULL, NULL, FICK_ISONUMBER ) == 0 )
    RUR_FI_Code = fi.FI_Code;
  end;
end;

private macro getCursOnDate( ВидКурса, impOnDate:date )
          
  var inParams = TArray;
  
  var prm = RslSoapPrm;
  prm.Name = "On_date";  
  prm.Value = dateToStr( impOnDate );
  inParams[0] = prm;

  var url = "http://www.cbr.ru/DailyInfoWebServ/DailyInfo.asmx?WSDL";

  var method = "GetCursOnDate";                               

  var outParams;
  var encoding;
  var outResponse;
  var errMsg:string;

  var guid = CreateGUID();
  var stat = CallSimpleExtService(guid, url, method, inParams, "", "", 0, outParams, encoding, outResponse);

  if( stat  )
    msgbox( errMsg );
  else
    // создаем MS-парсер
    var xml = CreateXMLParser();
    xml.validateOnParse = true;
    xml.async = false;

    // парсим полученный SOAP-ответ
    stat = xml.loadXML(outResponse);
    
    if( stat )
      // получение реальной даты курсов OnDate
      var realDateNodeList = xml.getElementsByTagName("xs:element[@name='ValuteData']");
      var strRealDate = realDateNodeList.item(0).attributes.item(3).nodeValue;
      var realOnDate = dateFromStr( strRealDate );

      printReportHeader( impOnDate, realOnDate );

      // получаем список тегов <ValuteCursOnDate>
      var rateList = xml.getElementsByTagName("ValuteCursOnDate");

      if( rateList.length > 0 )
        printTableHeader();
        setRUR_FI_Code();
      end; 

      var i = 0;
      while( i < rateList.length )
        var cbRate = TCBRate( rateList.item(i) );
        transferOneRateToBase( impOnDate, realOnDate, ВидКурса, cbRate );
        i = i + 1;   
      end;

      if( rateList.length > 0 )
        printTableFooter();
      end;
    end;
  end;
end;

macro CBR_CursImport( onDate:date )
  
  var ВидКурса = 0;

  var err = 0;
  GetRegistryValue( "CB\\FINTOOLS\\CBCURRATEKIND", V_INTEGER, ВидКурса, err );
  if( (err != 0) or (ValType(ВидКурса) != V_INTEGER) or (ВидКурса == 0) )
    msgBox("Не задано значение настройки CB\\FINTOOLS\\CBCURRATEKIND.");
    exit(1);
  else
    err = getCursOnDate( ВидКурса, onDate );
  end;

  return err;
end;
