/*---------------------------------------------------------------------------┐
 RS-Bank 5.10                                           R-Style Software Lab

 Печать мемориального ордера по форме 203 (печатная форма)

 CREATED :      16.03.2001
 FILE_NAME :    MO203.MAC
 MODIFICATIONS:
 NOTES:
+--------------------------------------------------------------------------*/

IMPORT FIInter, bankinter, reportinter, globals, get_mfo, prpm, "momul203.mac";

private file   dep(dp_dep);
private RECORD doc_inf("docinfo.rec");

private const КоличествоКопий = 0;
private const AKMULTYCARRY    = 15;
private const NATCUR          = 0;

private var   NumCopy         = КоличествоКопий;
private var   NeedDocInfo     = true; /* необходимость получения информации по документу */

private MACRO PutPlatPrivate(d, ncp )

  /* Подготовка строк к печати */
  ARRAY SS, SG, SBP, SBR, SP, SR;
  var Payer,
      Receiver,
      BIC_Payer       = "",
      BIC_Receiver    = "",
      CorAcc_Payer    = "",
      CorAcc_Receiver = "",
      Sum  = "",
      SumR = "",
      dateDoc,
      ncopy;

  if ( ValType(ncp) == V_INTEGER )
     NumCopy = ncp;
  end;

  if (NumCopy == 0)
    if ( not GetInt(NumCopy,"Введите количество копий.",3))
      exit(1);
    end;
  end;
  ncopy = NumCopy;

  /* Для печати мемордера по мультивалютной проводке используем отдельную функцию */
  if ((d.ConnAppKind == AKMULTYCARRY) and PutMultyDoc203(d, NumCopy))
    return;
  end;

  if (NeedDocInfo == true)
    ПолучитьИнформациюПоДокументу( d, doc_inf, 3); /* Вид кода - БИК */
  end;

  if ( d.Date_Carry > date(0, 0, 0) )
    dateDoc = d.Date_Carry;
  else
    dateDoc = {curdate};       /* Документ текущего дня */
  end;

  if ( doc_inf.PayerINN > "" )
       Payer = "ИНН " + doc_inf.PayerINN+ " " + doc_inf.PayerName;
  else Payer = doc_inf.PayerName;
  end;
  if ( doc_inf.ReceiverINN > "" )
       Receiver = "ИНН " + doc_inf.ReceiverINN+ " " + doc_inf.ReceiverName;
  else Receiver = doc_inf.ReceiverName;
  end;

  if(d.Code_Currency == NATCUR)
    BIC_Payer = {MFO_Bank};
    CorAcc_Payer = {CORAC_Bank};

    BIC_Receiver = {MFO_Bank};
    CorAcc_Receiver = {CORAC_Bank};
  end;

  strsplit( {Name_Bank},          SBP,   43,43,2 );
  strsplit( {Name_Bank},          SBR,   43,43,2 );

  /* Расчет суммы в рублях */
  if(d.Code_Currency != NATCUR)
    ConvSum( SumR , d.Sum, dateDoc, d.Code_Currency );
    Sum = CurToStrAlt( d.Sum, null, null, GetISOCode( d.Code_Currency ) );
  else
    Sum = RubToStrAlt( d.Sum );
  end;

  strsplit( Sum,                  SS,    76,76,2 );
  strsplit( d.Ground,             SG,    87,46,4 );
  strsplit( Payer,                SP,    43,43,4 );
  strsplit( Receiver,             SR,    43,43,5 );

  while(ncopy != 0)
 [
  ┌─────────────────────────────────────────────────────────────────────────────────────────┐
  │ ############################################                                   Ф.№№ 203 │
  │ ────────────────────────────────────────────                                            │
  │      Наименование филиала                                                               │
  │        Сбербанка России                                                                 │
  │                                                                                         │
  │    МЕМОРИАЛЬНЫЙ ОРДЕР N ###############                                                 │
  │                                                                                         │
  │        #######################                                                          │
  │                                                                                         │
  │ Сумма      ############################################################################ │
  │ прописью   ############################################################################ │
  ├─────────────────────────────────────────────┬──────────┬────────────────────────────────┤
  │ ########################################### │  Сумма   │ ############################## │
  │ ########################################### │          │ ############################## │
  │ ########################################### │          │                                │
  │ ########################################### ├──────────┼────────────────────────────────┤
  │ Плательщик                                  │  Сч.N    │ #########################      │
  ├─────────────────────────────────────────────┼──────────┤                                │
  │ ########################################### │  БИК     │ #################              │
  │ ########################################### ├──────────┤                                │
  │ Банк плательщика                            │  Сч.N    │ #########################      │
  ├─────────────────────────────────────────────┼──────────┼────────────────────────────────┤
  │ ########################################### │  БИК     │ #################              │
  │ ########################################### ├──────────┤                                │
  │ Банк получателя                             │  Сч.N    │ #########################      │
  ├─────────────────────────────────────────────┼──────────┤                                │
  │ ########################################### │  Сч.N    │ #########################      │
  │ ########################################### ├──────────┼────────┬──────────┬────────────┤
  │ ########################################### │ Вид. Оп. │ ###### │Срок плат.│            │
  │ ########################################### ├──────────┤        ├──────────┤            │
  │ ########################################### │Назн.плат.│        │Очер.плат.│            │
  │                                             ├──────────┤        ├──────────┤            │
  │ Получатель                                  │   Код    │        │Рез.поле  │            │
  ├─────────────────────────────────────────────┴──────────┴────────┴──────────┴────────────┤
  │ Назначение платежа (содержание операции) ############################################## │
  │ ####################################################################################### │
  │ ####################################################################################### │
  │ ####################################################################################### │
  │                                                                                         │
  │ ───────────────────────────────────────────────────────                                 │
  │            Подписи                    Отметки банка                                     │
  │                                                                                         │
  │                                                                                         │
  │            ───────────────────────────                                                  │
  │                                                                                         │
  │            ───────────────────────────                                                  │
  │                                                                                         │
  └─────────────────────────────────────────────────────────────────────────────────────────┘

]
   (GetDepartmentName(d),
    d.Numb_Document:c,
    dateDoc:m:c:f,
    SS(0),
    SS(1),
    SP(0),  d.Sum:l:f,
    SP(1),  SumR:l:f,
    SP(2),
    SP(3),  d.Account_Payer,
    SBP(0), GetCodeBik(d.Date_Carry,BIC_Payer),
    SBP(1), CorAcc_Payer,
    SBR(0), GetCodeBik(d.Date_Carry,BIC_Receiver),
    SBR(1), CorAcc_Receiver,
    SR(0),  d.Account_Receiver,
    SR(1),
    SR(2),  d.Shifr_oper, 
    SR(3),
    SR(4),  
    SG(0),  SG(1), SG(2), SG(3) );

    ncopy = ncopy - 1;
  end;
END;

MACRO PutPlatI(d, ncp)
  NeedDocInfo = true;
  PutPlatPrivate(d, ncp);
END;

MACRO PutPlatDocInfo(Doc, DocInfo, ncp)
  NeedDocInfo = false;
  Copy(doc_inf, DocInfo);
  PutPlatPrivate(Doc, ncp);
END;
