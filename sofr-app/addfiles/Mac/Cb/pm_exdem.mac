/*
  $Name:         pm_exdem.mac
  $Module:       Ядро Банкинг
  $Description:  Макрос шага
*/

//-----------------------------------------------------------------------------
// Блок     : 29046 - "Картотека требований-поручений"
// Шаг      : 10    - "Формирование платежа по оплате требований"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import globals, PaymInter, BankInter, FIInter, PTInter, pm_ext, pm_setst, likepy, 
       cbsttls, pm_tools, "bcfunc.mac";

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Создать платеж банка
//-----------------------------------------------------------------------------
private macro GetPayerBankCode_OutNostro(PayerCorschem/*record*/) : string
  var BankCode : string = "";

  var PartyID : integer = GetDprtPartyID(PayerCorschem.Department);

  if(PartyID > 0)
    BankCode = ПолучитьКодСубъекта (PartyID, PTCK_BIC, null, 1);
  end;

  return BankCode;
end;

private macro GetPayerName_OutNostro(PayerCorschem/*record*/) : string
  var PayerName : string = "";

  var Rec_Account:TRecHandler = TRecHandler( "account.dbt" );

  if(PM_GetAccountRecord( PayerCorschem.Account, PayerCorschem.FIID, CHAPT1, Rec_Account ))
    PayerName = Rec_Account.rec.NameAccount;
  end;

  return PayerName;
end;

private macro GetPayerINN_OutNostro() : string
  var PayerINN : string = "";

  var PartyID : integer = GetDprtPartyID(PaymentObj.Department);

  if(PartyID > 0)
    PayerINN = GetPartyINN( PartyID, 1 );
  end;

  return PayerINN;
end;

private macro GetInitialDocName : string
  var DocName : string = "платежному документу";

  if(PaymentObj.ShifrOper == "02")
    DocName = "требованию";
  elif(PaymentObj.ShifrOper == "06")
    DocName = "инкассовому поручению";
  end;

  return DocName;
end;

PRIVATE MACRO CreateBankPaym_OutNostro():object

  var Payment:RsbPayment = null;
  var BankPaym:object    = null;

  if( ( PaymentObj.PayerFIID == PaymentObj.BaseFIID ) AND
      ( PaymentObj.PayerFIID == PaymentObj.ReceiverFIID ) AND
      ( PaymentObj.PayerFIID == NATCUR ) )

    BankPaym               = GenObject( "RsbBankPayment", 0 );
    Payment                = BankPaym.Payment();

    BankPaym.Origin        = MEMORDER_FDOC_PAYDEMAND;
    BankPaym.Oper          = {oper}; 
    BankPaym.Status        = 1/*MEMORDER_STATUS_POST*/;

    Payment.DocKind        = DLDOC_BANKPAYMENT;
    Payment.Purpose        = PM_PURP_BANKPAYMENT;

    Payment.OrderFIID      = PaymentObj.PayerFIID;
    Payment.OrderAmount    = PaymentObj.BaseAmount;

  else
    BankPaym               = GenObject( "RsbBbCpOrder", 0 );
    Payment                = BankPaym.Payment();

    BankPaym.Origin        = CP_OR_PAYDEMAND;
    BankPaym.Oper          = {oper}; 
    BankPaym.CurrentState  = 0/*CP_ST_DEFERRED*/;

    Payment.DocKind        = BBANK_CPORDER;
    Payment.Purpose        = PM_PURP_BANKPAYMENT;
    Payment.ComissCharges  = PM_CHRG_SHA;

    Payment.OrderFIID      = PaymentObj.OrderFIID;
    Payment.OrderAmount    = PaymentObj.OrderAmount;
  end;
  
  Payment.Number      = PaymentObj.Number;
  Payment.Date        = {DprtCurDate};
  Payment.PaymentKind = PaymentObj.PaymentKind;
  Payment.BaseFIID    = PaymentObj.BaseFIID;
  Payment.BaseAmount  = PaymentObj.BaseAmount;

  RECORD PayerCorschem(corschem);
  var PayerCorsNumber : integer = PaymentObj.GetDEBET().rec.Corschem;
  var PayerCorsFIID : integer = PaymentObj.GetDEBET().rec.PayFIID;
  if( FindCorschem(PayerCorschem, PayerCorsNumber, PayerCorsFIID) != 0 )
    RunError("Не найдена корсхема с номером " + PayerCorsNumber + " и валютой " + PayerCorsFIID);
  end;

  Payment.PayDate     =
  Payment.ClientDate  = 
  Payment.ValueDate   = PaymentObj.ValueDate;

  Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                      PTID_UNKNOWNPARTY, //PayerBankID
                      PTCK_BIC, //PayerBankCodeKind
                      GetPayerBankCode_OutNostro(PayerCorschem), 
                      "", //BankName
                      "", //CorrAcc
                      PaymentObj.PayerFIID, 
                      CHAPT1, 
                      PayerCorschem.Account, //PayerAccount
                      null, //Payer
                      GetPayerName_OutNostro(PayerCorschem), 
                      GetPayerINN_OutNostro() );

  Payment.PayerAmount = PaymentObj.PayerAmount;

  Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                         PaymentObj.ReceiverBankID, 
                         PTCK_BIC, 
                         ПолучитьКодСубъекта(PaymentObj.ReceiverBankID, PTCK_BIC, null, 1),
                         "", //ReceiverBankName
                         "", //CorrAcc
                         PaymentObj.ReceiverFIID, 
                         CHAPT1, 
                         PaymentObj.ReceiverAccount, 
                         PaymentObj.Receiver, 
                         PaymentObj.ReceiverName, 
                         PaymentObj.ReceiverINN );

  Payment.ReceiverAmount = PaymentObj.ReceiverAmount;


  Payment.Ground      = 
    "Зачисление средств, поступивших по " + GetInitialDocName() + " " + 
    PaymentObj.Number + " от " + DDpMMpYYYY(PaymentObj.Date) + 
    " '" + PaymentObj.Ground + "'";

  Payment.ShifrOper   = "01";
  Payment.NumberPack  = 0;
  Payment.Priority    = PaymentObj.Priority;

  Payment.TaxAuthorState = PaymentObj.TaxAuthorState;
  Payment.BTTTICode = PaymentObj.BTTTICode;
  Payment.OKATOCode = PaymentObj.OKATOCode;
  Payment.TaxPmGround = PaymentObj.TaxPmGround;
  Payment.TaxPmPeriod = PaymentObj.TaxPmPeriod;
  Payment.TaxPmNumber = PaymentObj.TaxPmNumber;
  Payment.TaxPmDate = PaymentObj.TaxPmDate;
  Payment.TaxPmType = PaymentObj.TaxPmType;
  Payment.UIN = PaymentObj.UIN;
  Payment.PayType = PaymentObj.PayType;

  return BankPaym;
END;

PRIVATE MACRO CreateBankPaym_Other():object

  var Payment:RsbPayment = null;
  var BankPaym:object    = null;

  if( ( PaymentObj.PayerFIID == PaymentObj.BaseFIID ) AND
      ( PaymentObj.PayerFIID == PaymentObj.ReceiverFIID ) AND
      ( PaymentObj.PayerFIID == 0 /*NATCUR*/ ) )

    BankPaym               = GenObject( "RsbBankPayment", 0 );
    Payment                = BankPaym.Payment();

    BankPaym.Origin        = MEMORDER_FDOC_PAYDEMAND;
    BankPaym.Oper          = {oper}; 
    BankPaym.Status        = 1/*MEMORDER_STATUS_POST*/;

    Payment.DocKind        = DLDOC_BANKPAYMENT;
    Payment.Purpose        = PM_PURP_BANKPAYMENT;

    Payment.BaseFIID       =
    Payment.PayerFIID      = 
    Payment.ReceiverFIID   = 
    Payment.OrderFIID      = PaymentObj.PayerFIID;

    Payment.BaseAmount     = PaymentObj.BaseAmount;
    Payment.ReceiverAmount = PaymentObj.BaseAmount;
    Payment.PayerAmount    = PaymentObj.BaseAmount;
    Payment.OrderAmount    = PaymentObj.BaseAmount;

  else
    BankPaym               = GenObject( "RsbBbCpOrder", 0 );
    Payment                = BankPaym.Payment();

    BankPaym.Origin        = CP_OR_PAYDEMAND;
    BankPaym.Oper          = {oper}; 
    BankPaym.CurrentState  = 0/*CP_ST_DEFERRED*/;

    Payment.DocKind        = BBANK_CPORDER;
    Payment.Purpose        = PM_PURP_BANKPAYMENT;
    Payment.ComissCharges  = PM_CHRG_SHA;
                                                                                         
    Payment.BaseFIID       = PaymentObj.BaseFIID;
    Payment.PayerFIID      = PaymentObj.PayerFIID;
    Payment.ReceiverFIID   = PaymentObj.ReceiverFIID;
    Payment.OrderFIID      = PaymentObj.OrderFIID;
                                                                                               
    Payment.BaseAmount     = PaymentObj.BaseAmount;
    Payment.ReceiverAmount = PaymentObj.ReceiverAmount;
    Payment.PayerAmount    = PaymentObj.PayerAmount;
    Payment.OrderAmount    = PaymentObj.OrderAmount;
  end;
  
  Payment.Number      = PaymentObj.Number;
  Payment.PaymentKind = "Э";
  Payment.ShifrOper   = "01";

  Payment.PayDate     =
  Payment.ClientDate  = 
  Payment.Date        =
  Payment.ValueDate   = {curdate};

  Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                      PaymentObj.PayerBankID, 
                      PaymentObj.PayerBankCodeKind, 
                      PaymentObj.PayerBankCode, 
                      PaymentObj.PayerBankName,
                      PaymentObj.PayerBankCorrAcc,
                      PaymentObj.PayerFIID, 
                      1/*CHAPT1*/, 
                      PaymentObj.PayerAccount, 
                      PaymentObj.Payer, 
                      PaymentObj.PayerName, 
                      PaymentObj.PayerINN );

  Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                         PaymentObj.ReceiverBankID, 
                         PaymentObj.ReceiverBankCodeKind, 
                         PaymentObj.ReceiverBankCode, 
                         PaymentObj.ReceiverBankName,
                         PaymentObj.ReceiverBankCorrAcc,
                         PaymentObj.ReceiverFIID, 
                         1/*CHAPT1*/, 
                         PaymentObj.ReceiverAccount, 
                         PaymentObj.Receiver, 
                         PaymentObj.ReceiverName, 
                         PaymentObj.ReceiverINN );


  Payment.Ground      = PaymentObj.Ground;

  return BankPaym;
END;

PRIVATE MACRO CreateBankPaym():RsbPayment

  var Payment:RsbPayment = null;
  var BankPaym:object    = null;

  if( IsBankClaimOutNostro(PaymentObj) )
    BankPaym = CreateBankPaym_OutNostro();
  else
    BankPaym = CreateBankPaym_Other();
  end;
  Payment                = BankPaym.Payment();

  // Установить признак автозапуска операции.
  BankPaym.LaunchOper = true;

  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );
  return Payment;
END;

// ----------------------------------------------------------------------------
// Макроса шага
// ----------------------------------------------------------------------------
MACRO ExecuteStep( doc, paymDoc )

  var stat:integer = 0;
  var Action:integer;

  if( IsOprMultiExec() )
    msgbox( "Шаг необходимо выполнить в единичном режиме" );
    return 1;
  end;

  var PayPayment:RsbPayment = CreateBankPaym();

  array FlgRM;       // Массив флагов для R-макета
  array FlgKZRM;     // Массив флагов для R-макета ( казахская платежка )
  array FlgCP;       // Массив флагов для Swift
  array FlgKZCP;     // Массив флагов для Swift ( казахская платежка )
  FlgRM[PNRMPM_COUNT]      = 0;
  FlgKZRM[KZPNRMPM_COUNT]  = 0;
  FlgCP[PNCPPM_COUNT]      = 0;
  FlgKZCP[KZ_PNCPPM_COUNT] = 0;

  FlgRM[ PNRMPM_NUMBER      ] = 1; // R-maket
  FlgRM[ PNRMPM_DATE        ] = 1;
  FlgRM[ PNRMPM_KIND1       ] = 1;
  FlgRM[ PNRMPM_INNP        ] = 1;
  FlgRM[ PNRMPM_NAMEP       ] = 1;
  FlgRM[ PNRMPM_CODEKINDP   ] = 1;
  FlgRM[ PNRMPM_CODEKIND2P  ] = 1;
  FlgRM[ PNRMPM_INNR        ] = 1;
  FlgRM[ PNRMPM_NAMER       ] = 1;
  FlgRM[ PNRMPM_ACCOUNTR    ] = 1;
  FlgRM[ PNRMPM_CODEKINDR   ] = 1;
  FlgRM[ PNRMPM_CODEKIND2R  ] = 1;
  FlgRM[ PNRMPM_CODER       ] = 1;
  FlgRM[ PNRMPM_BANKR       ] = 1;
  FlgRM[ PNRMPM_BANKACCR    ] = 1;
  FlgRM[ PNRMPM_GROUND      ] = 1;
  FlgRM[ PNRMPM_SHIFR       ] = 1;
  FlgRM[ PNRMPM_PRIORITY    ] = 1;
  FlgRM[ PNRMPM_PACK        ] = 1;
  FlgRM[ PNRMPM_PAYDATE     ] = 1;

  FlgKZRM[ KZPNRMPM_NUMBER               ] = 1; // R-maket
  FlgKZRM[ KZPNRMPM_DATE                 ] = 1;
  FlgKZRM[ KZPNRMPM_KINDPAYMENT          ] = 1;
  FlgKZRM[ KZPNRMPM_RNN_PAYER            ] = 1;
  FlgKZRM[ KZPNRMPM_PAYER                ] = 1;
  FlgKZRM[ KZPNRMPM_CODEKINDP            ] = 1;
  FlgKZRM[ KZPNRMPM_CODEKIND2P           ] = 1;
  FlgKZRM[ KZPNRMPM_RNN_RECEIVER         ] = 1;
  FlgKZRM[ KZPNRMPM_RECEIVER             ] = 1;
  FlgKZRM[ KZPNRMPM_IIK_RECEIVER         ] = 1;
  FlgKZRM[ KZPNRMPM_KINDCODE_RECEIVER    ] = 1;
  FlgKZRM[ KZPNRMPM_KINDCODE_RECEIVERNAME] = 1;
  FlgKZRM[ KZPNRMPM_CODE_RECEIVER        ] = 1;
  FlgKZRM[ KZPNRMPM_BANK_RECEIVER        ] = 1;
  FlgKZRM[ KZPNRMPM_GROUND               ] = 1;
  FlgKZRM[ KZPNRMPM_SHIFR_OPER           ] = 1;
  FlgKZRM[ KZPNRMPM_PRIORITY             ] = 1;
  FlgKZRM[ KZPNRMPM_NUMBERPACK           ] = 1;
  FlgKZRM[ KZPNRMPM_PAY_DATE             ] = 1;

  FlgCP[ PNCPPM_NUMBER            ] = 1; // R-maket
  FlgCP[ PNCPPM_DATE              ] = 1;
  FlgCP[ PNCPPM_INNP              ] = 1;
  FlgCP[ PNCPPM_NAMEP             ] = 1;
  FlgCP[ PNCPPM_CODEKINDP         ] = 1;
  FlgCP[ PNCPPM_CODEKINDP2        ] = 1;
  FlgCP[ PNCPPM_INNR              ] = 1;
  FlgCP[ PNCPPM_NAMER             ] = 1;
  FlgCP[ PNCPPM_ACCOUNTR          ] = 1;
  FlgCP[ PNCPPM_CLIENTCODEKINDR   ] = 1;
  FlgCP[ PNCPPM_CLIENTCODEKINDR2  ] = 1;
  FlgCP[ PNCPPM_CLIENTCODER       ] = 1;
  FlgCP[ PNCPPM_CODEKINDR         ] = 1;
  FlgCP[ PNCPPM_CODEKINDR2        ] = 1;
  FlgCP[ PNCPPM_CODER             ] = 1;
  FlgCP[ PNCPPM_NAMEBANKR         ] = 1;
  FlgCP[ PNCPPM_CORRACCR          ] = 1;
  FlgCP[ PNCPPM_DETAILS           ] = 1;
  FlgCP[ PNCPPM_PRIORITY          ] = 1;
  FlgCP[ PNCPPM_PACK              ] = 1;

  FlgKZCP[ KZ_PNCPPM_Number          ] = 1; // Swift
  FlgKZCP[ KZ_PNCPPM_Date            ] = 1;
  FlgKZCP[ KZ_PNCPPM_PayerINN        ] = 1;
  FlgKZCP[ KZ_PNCPPM_PayerName       ] = 1;
  FlgKZCP[ KZ_PNCPPM_PayerCodeKind   ] = 1;
  FlgKZCP[ KZ_PNCPPM_KindCode_PAY    ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverINN     ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverName    ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverAccount ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverCodeKind] = 1;
  FlgKZCP[ KZ_PNCPPM_KindCode_BNF    ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverCode    ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverBankName] = 1;
  FlgKZCP[ KZ_PNCPPM_Ground          ] = 1;
  FlgKZCP[ KZ_PNCPPM_CodeKind        ] = 1;
  FlgKZCP[ KZ_PNCPPM_Priority        ] = 1;
  FlgKZCP[ KZ_PNCPPM_NumberPack      ] = 1;
  FlgKZCP[ KZ_PNCPPM_KindCode_ACC    ] = 1;
  FlgKZCP[ KZ_PNCPPM_BankCode        ] = 1;

  // Вызвать многозакладочную панель редактирования платежа
  if( not needUseKZpm() )
    Action = PM_ProcessPanel( PayPayment, 1, NULL, FlgRM, FlgCP );
  else
    Action = PM_ProcessPanel( PayPayment, 1, NULL, FlgKZRM, FlgKZCP );
  end;

  if( Action )
    if( Action == 4704 )
      msgbox( "Выполнение процедуры прервано пользователем" );
    else
      msgbox( "Ошибка при создании панели редактирования платежа" );
    end;
    return 1;
  end;
   
  // Создать связь
  if( PaymentObj.LinkPayment(PayPayment, PMLINK_KIND_KVITING ) )
    msgbox(GetErrMsg());
    return 1;
  end;

  PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );
  PaymentObj.PaymStatus = PM_FINISHED;
  PaymentObj.PropStatus = PM_PROP_CLOSED;
  
  if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  return 0;
END;
