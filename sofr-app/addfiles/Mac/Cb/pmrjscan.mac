IMPORT  PaymInter, OprInter;
IMPORT  "gstname.mac";

RECORD pm_oprstep( oprstep );

private
  var DrawHead = false;


const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;

PRIVATE MACRO GetDocument( ID_Operation:integer, PaymentID:integer, DocKind:integer, Number:string, Amount, PayAmount, OpDate:date ):bool
  var query:string = " SELECT pmpaym.t_paymentid, pmpaym.t_dockind, pmrmprop.t_number, pmpaym.t_amount, pmpaym.t_payamount, pmrmprop.t_date, pmpaym.t_valuedate " +
                       " FROM doproper_dbt oproper, dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop  " +
                      " WHERE oproper.t_id_operation = :id_operation " +
                        " AND TO_NUMBER (oproper.t_documentid) = pmpaym.t_paymentid " + 
                        " AND pmpaym.t_paymentid = pmrmprop.t_paymentid";                                

  var params:TArray = TArray();
  params[params.size] = SQLParam( "id_operation", ID_Operation);
  var rset:RsdRecordset = execSQLselect( query, params, true );

  if( rset and rset.moveNext() )
    SetParm(1, rset.value(0));
    SetParm(2, rset.value(1));
    SetParm(3, rset.value(2));
    SetParm(4, rset.value(3));
    SetParm(5, rset.value(4));
    if ( rset.value(1) != DLDOC_MEMORIALORDER )
      SetParm(6, rset.value(5));
    else
      SetParm(6, rset.value(6)); 
    end;
    return true;
  end;

  return false;
END; 


MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )

  var PaymentID:integer = 0, DocKind:integer = 0;
  var Number:string = "";
  var OpDate:date = date(0,0,0);
  var Amount = 0,
      PayAmount = 0;

  setbuff( pm_oprstep, firstdoc );

  if( Вызов == Первый )
      
    DrawHead = true;

  elif( Вызов == Ошибка )
    
    if( DrawHead )
    
      if( pm_oprstep.DocKind == CB_MULTYDOC )

[              Отчет о групповом помещении в отвергнутые мультивалютных документов.              ];
[┌─────────┬─────────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │ Сумма дебет │ Сумма кредит│   Дата   │Номер │                Сообщение             │];
[│документа│             │             │          │ошибки│                                      │];
[├─────────┼─────────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

      elif( pm_oprstep.DocKind == PS_BUYCURORDER )
      
[                 Отчет о групповом отвержении операций для поручений на п\п валюты.             ];
[┌─────────┬─────────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │    Сумма    │    Сумма    │   Дата   │Номер │                Сообщение             │];
[│документа│   продажи   │   покупки   │          │ошибки│                                      │];
[├─────────┼─────────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

      else

[              Отчет о групповом помещении документов в отвергнутые.               ];
[┌─────────┬─────────────┬──────────┬──────┬──────────────────────────────────────┐];
[│  Номер  │   Сумма     │   Дата   │Номер │                Сообщение             │];
[│документа│             │          │ошибки│                                      │];
[├─────────┼─────────────┼──────────┼──────┼──────────────────────────────────────┤];

      end;
    DrawHead = false;

    end;

    if( Статус == 0 )
      Сообщение = "Документ успешно помещен в отвергнутые";
    end;

    if( NOT GetDocument( pm_oprstep.ID_Operation, PaymentID, DocKind, Number, Amount, PayAmount, OpDate ) )
      Сообщение = "Не найдено платежное поручение";
    else 
      Сообщение = Сообщение + ". " + GetStepNameExecuteRead( PaymentID, DocKind );
    end;

    if( (pm_oprstep.DocKind == CB_MULTYDOC) or (pm_oprstep.DocKind == PS_BUYCURORDER) )
[│#########│#############│#############│##########│######│######################################│]
( Number, Amount, PayAmount, OpDate, Статус, StrSubst(Сообщение, "|", " " ):w );

    elif( pm_oprstep.DocKind == PS_INRQ )
[│#########│#############│##########│######│######################################│]
( Number, PayAmount, OpDate, Статус, StrSubst(Сообщение, "|", " " ):w );
 
    else
[│#########│#############│##########│######│######################################│]
( Number, Amount, OpDate, Статус, StrSubst(Сообщение, "|", " " ):w );
    end;
          
  elif( Вызов == Последний )
   
    if( (pm_oprstep.DocKind == CB_MULTYDOC) or (pm_oprstep.DocKind == PS_BUYCURORDER) )
[└─────────┴─────────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
    else
[└─────────┴─────────────┴──────────┴──────┴──────────────────────────────────────┘];
    end;

  end;

END;
