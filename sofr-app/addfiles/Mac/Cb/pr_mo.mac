/*
 *  Печать платежного поручения, требования и требования-поручения банка.
 *  Использованы формы из РКО, убрана псевдографика.
 *
 *  Из бухгалтерии банка вызывается макрофункция PrintMO ( mo, pm, dt, ct, rm ),
 *  где mo - первичный документ (memorder.dbt), pm - платеж (pmpaym.dbt),
 *  dt, ct - свойства платежа (pmprop.dbt), rm - R-макет (pmrmprop.dbt).
 *  Вид первичного документа определяется по полю memorder.DocKind.
 *
 *  Из списка платежей вызывается макрофункция PrintPayment ( mo, pm, dt, ct, rm ).
 *
 *  Если возвращаемое функцией значение равно 1, то отчет на экране
 *  показан не будет.
 *
 */


import BankInter, PTInter, OprInter, rslx, globals, gnd120p;

record pr_order     (memorder);
record pr_pm_paym   (pmpaym);
record pr_debet     (pmprop);
record pr_credit    (pmprop);
record pr_rmprop    (pmrmprop);

CONST ПутьНастройкиИспользованияVRSLПорученииБанка  = "CB\\MEMORDER\\USE_VRSL_REQ";
CONST ПутьНастройкиИспользованияVRSLТребованииБанка = "CB\\MEMORDER\\USE_VRSL_DEM";
CONST ПутьНастройкиИспользованияVRSLТребованииПорученииБанка = "CB\\MEMORDER\\USE_VRSL_DEMREQ";
CONST REG_OK = 0;          /* Процедура нахождения настройки банка завершилась успешно */
/* ======================== Настройка ================================== */
 const
   КоличествоКопий     = 0 ,
   РазделительКопеек   = "="; /* "-" или "="*/
/* ===================================================================== */
var IsBBANK = 1;

macro GetPartyProperty(PartyID,rec,MFO,CorAc)
file Коды_Субъектов(partcode);
file Параметры_Банков (bankdprt);
file Субъекты (party);

    if ( ПолучитьСубъекта (PartyID,rec) != 0 )
        msgbox("Не найден банк-получатель");
        return 1;
    else        
         Коды_Субъектов.PartyID  = rec.PartyID;
         Коды_Субъектов.CodeKind = 3;
         If (GetEQ(Коды_Субъектов)) /* Если есть БИК у самого банка, то возвращаем его */ 
                SetParm(2, Коды_Субъектов.Code);
                Параметры_Банков.PartyID = rec.PartyID;
                if (GetEQ(Параметры_Банков))
                    SetParm(3, Параметры_Банков.corAcc);
                else
                    SetParm(3, "");
                end;
         else        /* Если нет, берем вышестоящую организацию и ищем ее БИК*/
                if ( ПолучитьСубъекта (rec.Superior,rec) != 0 )
                    SetParm(2, "");
                    SetParm(3, "");
                else                        
                    Коды_Субъектов.PartyID  = rec.PartyID;
                    Коды_Субъектов.CodeKind = 3;
                    If (GetEQ(Коды_Субъектов))
                        SetParm(2, Коды_Субъектов.Code);
                    else
                        SetParm(2, "");
                    end;            
                end;
                Параметры_Банков.PartyID = rec.PartyID;
                if (GetEQ(Параметры_Банков))
                        SetParm(3, Параметры_Банков.corAcc);
                else
                        SetParm(3, "");
                end;
          end;
    end;

END;






/*
 *                    *** Печать платежного поручения ***
 *
 */
macro DrawBankReq( date_inbp, number, date_,dptch,payerINN,
                   payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                   sum,receiverINN, 
                   receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                   ground, queue, paydate, kindoper )

  var kopeck,rub,
      divide_kopeck,
      sumstr,shortsum,
      pospoint,ncopy,Плательщик,Получатель;

  
  Плательщик = payerINN + " " + payer;
  Получатель = receiverINN + " " + receiver;
  divide_kopeck = РазделительКопеек;
  ncopy = КоличествоКопий;

  if (ncopy == 0)
    ncopy = 1;
    if ( not GetInt(ncopy,"Введите количество копий.",3))
      ncopy = 0;
    end;
  end;

  if(ncopy == 0) return false;end;

  ARRAY SS,SG,SBP,SBR,SP,SR;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  ground = InterpretGround( ground );

  strsplit( Плательщик,         SP,    50,46,5 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   50,50,2 ); /* Банк-плательщик */
  strsplit( rub,                SS,    76,76,3 ); /* Сумма пpописью  */
  strsplit( ground,             SG,    85,85,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   50,50,2 ); /* Банк-получатель */
  strsplit( Получатель,         SR,    50,50,5 ); /* Получатель      */

  date_ = string( date_ );
  paydate = string( paydate );
while(ncopy != 0)
[                                                                                         ┌─────────┐
                                                                                          │ 0401060 │
                                                                                          └─────────┘
      ############
   ───────────────────
   Поступ. в банк плат.
                                                ############     #############                       
   Платежное поручение N ###############        ────────────     ─────────────                       
                                                   (Дата)        (Вид платежа)                       
                                                                                                     
   Сумма   │ ####################################################################################    
   прописью│ ####################################################################################    
           │ ####################################################################################    
  ─────────┴──────────────────────────────────────────┬───────────┬────────────────────────────────
   ИНН ############################################## │ Сумма     │ #############################    
   ################################################## │           │                                  
   ################################################## │           │                                  
   ################################################## ├───────────┤                                  
   ################################################## │ Сч.N.     │ #############################    
                                                      │           │                                  
   Плательщик                                         │           │                                  
  ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
   ################################################## │ БИК       │ #############################    
   ################################################## ├───────────┤                                  
                                                      │ Сч.N.     │ #############################    
   Банк плательщика                                   │           │                                  
  ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
   ################################################## │ БИК       │ #############################    
   ################################################## ├───────────┤                                  
                                                      │ Сч.N.     │ #############################    
   Банк получателя                                    │           │                                  
  ────────────────────────────────────────────────────┼───────────┤
   ИНН ############################################## │ Сч.N.     │ #############################    
   ################################################## │           │                                  
   ################################################## ├───────────┼──────┬────────────┬────────────  
   ################################################## │  Вид оп.  │ #### │ Срок плат. │ ##########   
   ################################################## ├───────────┤      ├────────────┤              
                                                      │ Наз. пл.  │      │ Очер плат. │ ##########   
                                                      ├───────────┤      ├────────────┤              
   Получатель                                         │ Код       │      │ Рез. поле  │              
  ────────────────────────────────────────────────────┴───────────┴──────┴────────────┴────────────
   Назначение платежа:
   ###############################################################################################   
   ###############################################################################################   
   ###############################################################################################   
                                                                                                     
  ─────────────────────────────────────────────────────────────────────────────────────────────────
                                            Подписи                        Отметки банка             
                                                                                                     
        М.П.                                                                                         
                                            ───────────────────────────────

                                            ───────────────────────────────
                                                                                                     


]
 (date_inbp:c,date_:c,dptch:c,number:l, 
  SS(0):l,SS(1):l,SS(2),
  SP(0):l,sum:l:f,SP(1):l,SP(2):l,SP(3),SP(4),
  deb_acc:l,
  SBP(0):l,payerMFO:l,SBP(1):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,kred_corracc:l,SR(0),
  kred_acc:l,SR(1),SR(2),SR(3),
  kindoper:l, paydate:l,SR(4), queue:l,
  SG(0):l,SG(1):l,SG(2):l
 );
 ncopy = ncopy - 1;
 end;

 return true;

end;


    

/*
 *                    *** Печать платежного требования ***
 *
 */
macro DrawBankDem(date_in, number, date_,dptch,payerINN,
                payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                sum,receiverINN,
                receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                ground, queue, paydate, kindoper,
                AcceptPeriod,Term )

  var kopeck,rub,req_rub, divide_kopeck, sumstr, ncopy, pospoint;

  divide_kopeck = РазделительКопеек;
  ncopy = КоличествоКопий;

  if (ncopy == 0)
    ncopy = 1;
    if ( not GetInt(ncopy,"Введите количество копий.",3))
      ncopy = 0;
    end;
  end;

  if(ncopy == 0) return false;end;

  ARRAY SS,SG,SBP,SBR,SP,SR,SQ;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  ground = InterpretGround( ground );

  strsplit( payer ,             SP,    44,44,4 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   44,44,3 ); /* Банк-плательщик */
  strsplit( rub,                SS,    68,68,3 ); /* Сумма пpописью  */
  strsplit( ground,             SG,    77,77,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   44,44,3 ); /* Банк-получатель */
  strsplit( receiver,           SR,    44,44,4 ); /* Получатель      */

  date_ = string( date_ );
  paydate = string( paydate );

while(ncopy != 0)
[                                                                   ┌─────────┐
                                                                    │ 0401061 │
                                                                    └─────────┘
     ############       ##########                                  
  ───────────────────   ────────────
  Поступ. в банк плат.  Срок платежа
                                          ##########  ############# 
  ПЛАТЕЖНОЕ ТРЕБОВАНИЕ N ###############  ──────────  ───────────── 
                                           (Дата)    (Вид платежа)
 ─────────┬─────────────────────────────────────────────┬────────────┬──────────
  Условие │ ########################################### │  Срок для  │    ###
  оплаты  │                                             │  акцепта   │
 ─────────┼─────────────────────────────────────────────┴────────────┴──────────
  Сумма   │####################################################################
  прописью│####################################################################
          │####################################################################
 ─────────┴───────────────────────────────────┬───────┬─────────────────────────
  ИНН ########################################│СУММА  │#########################
                                              │       │
  ############################################│       │
  ############################################├───────┼─────────────────────────
  ############################################│ Сч.N. │#########################
  ############################################│       │
  ПЛАТЕЛЬЩИК                                  │       │
 ─────────────────────────────────────────────┼───────┤
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПЛАТЕЛЬЩИКА                            │       │
 ─────────────────────────────────────────────┼───────┼─────────────────────────
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПОЛУЧАТЕЛЯ                             │       │
 ─────────────────────────────────────────────┼───────┤
  ИНН ########################################│ Сч.N. │#########################
                                              │       │
                                              ├───────┼────┬────────┬───────────
                                              │Вид Оп.│####│        │
  ############################################├───────┤    │Очер.пл.│###
  ############################################│Наз.пл.│    │        │
  ############################################├───────┤    ├────────┤
  ############################################│Код    │    │Рез.поле│
  ПОЛУЧАТЕЛЬ                                  │       │    │        │
 ─────────────────────────────────────────────┴───────┴────┴────────┴───────────
  Назначение платежа:
  #############################################################################
  #############################################################################
  #############################################################################

  Дата отсылки (вручения) плательщику предусмотренных
  договором документов
 ──────────────────────────────────────────────────────────────────────────
                              Подписи             Отметки банка получателя

      М.П.
                   ───────────────────────────────

                   ───────────────────────────────

         ┌────────────┬────────────┬──────────────┬─────────────┬───────────     Дата помещения
   N ч.  │   N плат.  │  Дата      │  Сумма       │  Сумма      │ Подпись         в картотеку
   плат. │   ордера   │  плат.     │  частичного  │  остатка    │                
         │            │  ордера    │  платежа     │  платежа    │
  ───────┴────────────┴────────────┴──────────────┴─────────────┴───────────    Отметки банка


]
 (date_in:c,paydate:c,
  date_:c,dptch:c,number:l,
  Term,AcceptPeriod,
  SS(0):l,SS(1):l,SS(2),
  payerINN,sum:l:f,SP(0):l,SP(1):l,SP(2):l,
  deb_acc:l,SP(3):l,
  SBP(0):l,payerMFO:l,SBP(1):l,SBP(2):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,SBR(2):l,kred_corracc:l,receiverINN,
  kred_acc:l,
  kindoper:l, SR(0):l,queue:l,SR(1):l,SR(2):l,SR(3):l,
  SG(0):l,SG(1):l,SG(2):l
  );

 ncopy = ncopy - 1;
 end;

 return true;

end;






/*
 *                *** Печать платежного требования-поручения ***
 *
 */
macro DrawBankDemReq(date_in, number, date_,dptch,payerINN,
                     payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                     sum,receiverINN,
                     receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                     ground, queue, paydate, kindoper,AcceptPeriod )

  var kopeck,rub,req_rub,
      divide_kopeck,reqsumstr,
      sumstr, ncopy, pospoint;

  divide_kopeck = РазделительКопеек;
  ncopy = КоличествоКопий;

  if (ncopy == 0)
    ncopy = 1;
    if ( not GetInt(ncopy,"Введите количество копий.",3))
      ncopy = 0;
    end;
  end;

  if(ncopy == 0) return false;end;

  ARRAY SS,SG,SBP,SBR,SP,SR,SQ;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  reqsumstr = string(MoneyL(sum));
  req_rub = RubToStrAlt(sum);
  pospoint = Index(reqsumstr, ".");
  if(Int(SubStr(reqsumstr, pospoint + 1, 2)) == 0 )
    req_rub = SubStr(req_rub,1,Index(req_rub,"коп")-4);
  end;

  ground = InterpretGround( ground );

  strsplit( payer ,             SP,    44,44,4 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   44,44,3 ); /* Банк-плательщик */
  strsplit( rub,                SS,    52,52,4 ); /* Сумма  к оплате */
  strsplit( ground,             SG,    68,68,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   44,44,3 ); /* Банк-получатель */
  strsplit( receiver,           SR,    44,44,4 ); /* Получатель      */
  strsplit( req_rub,            SQ,    54,54,4 ); /* Сумма           */

  date_ = string( date_ );
  paydate = string( paydate );

while(ncopy != 0)
[                                                                   ┌─────────┐
                                                                    │ 0401064 │
     ############       ##########                                  └─────────┘
  ───────────────────  ────────────
  Поступ. в банк плат. Срок платежа

  ПЛАТЕЖНОЕ                               ##########  #############
  ТРЕБОВАНИЕ-ПОРУЧЕНИЕ N ###############  ──────────  ─────────────
                                            (Дата)    (Вид платежа)
 ─────────┬──────────────────────────────────────────────────────┬────────┬─────
  Сумма   │######################################################│        │
  прописью│######################################################│Срок для│###
          │######################################################│акцепта │
          │######################################################│        │
 ─────────┼───────────────────────────────────┬───────┬──────────┴────────┴─────
  ИНН ########################################│СУММА  │#########################
                                              │       │
  ############################################│       │
  ############################################├───────┼─────────────────────────
  ############################################│ Сч.N. │#########################
  ############################################│       │
  ПЛАТЕЛЬЩИК                                  │       │
 ─────────────────────────────────────────────┼───────┤
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПЛАТЕЛЬЩИКА                            │       │
 ─────────────────────────────────────────────┼───────┼─────────────────────────
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПОЛУЧАТЕЛЯ                             │       │
 ─────────────────────────────────────────────┼───────┤
  ИНН ########################################│ Сч.N. │#########################
                                              │       │
                                              ├───────┼────┬────────┬───────────
                                              │Вид Оп.│####│        │
  ############################################├───────┤    │Очер.пл.│ ###
  ############################################│Наз.пл.│    │        │
  ############################################├───────┤    ├────────┤
  ############################################│ Код   │    │Рез.поле│
  ПОЛУЧАТЕЛЬ                                  │       │    │        │
 ─────────────────────────────────────────────┴───────┴────┴────────┴───────────
  Назначение платежа:
  #############################################################################
  #############################################################################
  #############################################################################

 ───────────────────────────────────────────────────────────────────────────────

                           Подписи получателя         Отметки банка получателя

        М.П.
                     ───────────────────────────────

                     ───────────────────────────────


 ─────────────────────────────────────────────────────┬─────────────────────────
  Сумма к оплате прописью                             │ Сумма к оплате
  ####################################################│#########################
  ####################################################│
  ####################################################│
  ####################################################│
 ─────────────────────────────────────────────────────┴─────────────────────────

                           Подписи плательщика

        М.П.                                                                    Дата помещения
                     ───────────────────────────────                             в картотеку


                     ───────────────────────────────



         ┌────────────┬────────────┬──────────────┬────────────┬────────────    Отметки банка
   N ч.  │   N плат.  │  Дата      │  Сумма       │  Сумма     │ Подпись       
   плат. │   ордера   │  плат.     │  частичного  │  остатка   │
         │            │  ордера    │  платежа     │  платежа   │
  ───────┴────────────┴────────────┴──────────────┴────────────┴────────────

]
 (date_in:c,paydate:c,
  date_:c,dptch:c,number:l,
  SQ(0):l,SQ(1):l,AcceptPeriod,SQ(2),SQ(3),
  payerINN,sum:l:f,SP(0):l,SP(1):l,SP(2):l,
  deb_acc:l,SP(3):l,
  SBP(0):l,payerMFO:l,SBP(1):l,SBP(2):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,SBR(2):l,kred_corracc:l,receiverINN,
  kred_acc:l,
  kindoper:l,SR(0):l,queue:l,SR(1):l,SR(2):l,SR(3):l,
  SG(0):l,SG(1):l,SG(2):l,
  SS(0):l, sum:l:f,
  SS(1), SS(2), SS(3)
  );


 ncopy = ncopy - 1;
 end;

 return true;

end;






/* ================= Печать платежного поручения ============================= */
macro print_report ( dockind, pm, dt, ct, rm )

   record pt  (party);
   file pknd(pmpopknd) key 0;

  VAR dptch,
   payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
   sum,
   receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO, Ground,
   MFO,CorAc,
   rub, divide_kopeck,sumstr, pospoint,
   stat,
   Use_VRSL_Req, Use_VRSL_Dem, Use_VRSL_DemReq, err, VRSL, report;
      
   setbuff( pr_pm_paym, pm );
   setbuff( pr_debet,   dt );
   setbuff( pr_credit,  ct );
   setbuff( pr_rmprop,  rm );

   if( IsBBANK == 1 )
     if ( dockind == DLDOC_BANKPAYMENT ) 
        GetRegistryValue( ПутьНастройкиИспользованияVRSLПорученииБанка, int, Use_VRSL_Req, err );
        if ( err != REG_OK )
           MsgBox( "Не определен ключ ", ПутьНастройкиИспользованияVRSLПорученииБанка, " в настройках банка" );
           return 1;
        end; 
     elif ( dockind == DLDOC_BANKCLAIM )
        GetRegistryValue( ПутьНастройкиИспользованияVRSLТребованииБанка, int, Use_VRSL_Dem, err );
        if ( err != REG_OK )
           MsgBox( "Не определен ключ ", ПутьНастройкиИспользованияVRSLТребованииБанка, " в настройках банка" );
           return 1;
        end;
     end;    
   else
     Use_VRSL_Req    = 0;
     Use_VRSL_Dem    = 0;
     Use_VRSL_DemReq = 0;
   end;

   /*Реквизиты плательщика*/
   deb_acc     = pr_pm_paym.PayerAccount;
   if ( pr_debet.BankCode == "" ) /* Внутренний документ банка */
      bank_payer = pr_rmprop.PayerBankName;
      if(bank_payer == "") bank_payer = {Name_Bank}; end;
      bank_payer = bank_payer;
      payerMFO   = {MFO_Bank};
      deb_corracc  = {CORAC_Bank};
   else
      if ( ПолучитьСубъекта (pr_pm_paym.PayerBankID,pt) != 0 )
         msgbox("Не найден банк-получатель");
         return 1;
      end;
      GetPartyProperty(pr_pm_paym.PayerBankID, pt, MFO, CorAc );
      bank_payer   = pr_rmprop.PayerBankName;
      deb_corracc  = pr_rmprop.PayerCorrAccNostro;
      if ( pr_debet.CodeKind == 3 )
         payerMFO = pr_debet.BankCode;
      else
         payerMFO = MFO;
      end;

      if ( deb_corracc == "" )
        deb_corracc  = CorAc;
      end;

   end;

   
  
   /*Реквизиты получателя*/
   kred_acc = pr_pm_paym.ReceiverAccount;
   if ( pr_credit.BankCode == "" ) /* Внутренний документ банка */
      bank_receiver = pr_rmprop.ReceiverBankName;
      if(bank_receiver == "") bank_receiver = {Name_Bank}; end;
      bank_receiver = bank_receiver;
      receiverMFO   = {MFO_Bank};
      kred_corracc  = {CORAC_Bank};
   else
      if ( ПолучитьСубъекта (pr_pm_paym.ReceiverBankID,pt) != 0 )
         msgbox("Не найден банк-получатель");
         return 1;
      end;
      GetPartyProperty(pr_pm_paym.ReceiverBankID,pt,MFO,CorAc);

      kred_corracc  = pr_rmprop.ReceiverCorrAccNostro;
      bank_receiver = pr_rmprop.ReceiverBankName;

      if ( pr_credit.CodeKind == 3 )
         receiverMFO = pr_credit.BankCode;
      else
         receiverMFO = MFO;
      end;

      if ( kred_corracc == "" )
        kred_corracc  = CorAc;
      end;

   end;
  
   pknd.PaymentKind = pr_rmprop.PaymentKind;
   if (GetEQ(pknd))
     dptch = pknd.Name;
   else
     msgbox("Не найден вид платежа");
   end;

   dptch = pknd.Name;

   if ( dockind == DLDOC_BANKPAYMENT ) 
     if( not Use_VRSL_Req)
         stat = DrawBankReq( pr_rmprop.Date, pr_rmprop.Number, pr_rmprop.Date, dptch,
                             pr_rmprop.PayerINN,
                             pr_rmprop.PayerName, deb_acc, deb_corracc,
                             bank_payer, payerMFO,
                             pr_pm_paym.Amount,
                             pr_rmprop.ReceiverINN,
                             pr_rmprop.ReceiverName, kred_acc, kred_corracc,
                             bank_receiver, receiverMFO,
                             pr_rmprop.Ground, pr_rmprop.Priority, pr_rmprop.PayDate, pr_rmprop.ShifrOper);
         if(not stat) return 1;
         else return 0;
         end;
     else
         VRSL = ActiveX("RSGENOLE.VRslRun.1"); 
         report = VRSL.RunRpt("ПлатежноеПоручение");
    
         divide_kopeck = РазделительКопеек;
    
         sumstr = string(MoneyL(pr_pm_paym.Amount));
         rub = RubToStrAlt(pr_pm_paym.Amount);
         pospoint = Index(sumstr, ".");
         if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
           rub = SubStr(rub,1,Index(rub,"коп")-4);
         end;
    
         report.AccC.text             =  kred_acc; 
         report.AccD.text             =  deb_acc;
         report.Amm.text              =  string(pr_pm_paym.Amount:f);
         report.Ammstr.text           =  rub;
         report.BICC.text             =  receiverMFO;
         report.BICD.text             =  payerMFO;
         report.Get_Date.text         =  string(pr_rmprop.Date);        
         report.Down.text             =  string(pr_rmprop.Date);
         report.ground.text           =  InterpretGround( pr_rmprop.Ground) ;
         report.INNC.text             =  pr_rmprop.ReceiverINN;
         report.INND.text             =  pr_rmprop.PayerINN;
         report.KindOper.text         =  pr_rmprop.ShifrOper;
         report.Nown.text             =  pr_rmprop.Number;  
         report.payDate.text          =  string(pr_rmprop.PayDate);
         report.payerBankName.text    =  bank_payer;
         report.payerCorAcc.text      =  deb_corracc;
         report.payerName.text        =  pr_rmprop.PayerName;
         report.payKind.text          =  dptch;
         report.payQueue.text         =  string(pr_rmprop.Priority);
         report.receiverBankName.text =  bank_receiver;
         report.receiverCorAcc.text   =  kred_corracc;
         report.receiverName.text     =  pr_rmprop.ReceiverName;
    
         report.runReport ();
         if (IsStandAlone)
          ViewRep (report);
         else
          TransferRep (report);
         end;
     end;
   elif ( dockind == DLDOC_BANKCLAIM )
     if( not Use_VRSL_Dem)
         stat = DrawBankDem( pr_rmprop.Date, pr_rmprop.Number, pr_rmprop.Date, dptch,
                             pr_rmprop.PayerINN,
                             pr_rmprop.PayerName, deb_acc, deb_corracc,
                             bank_payer,  payerMFO,
                             pr_pm_paym.Amount,
                             pr_rmprop.ReceiverINN,
                             pr_rmprop.ReceiverName, kred_acc, kred_corracc,
                             bank_receiver, receiverMFO,
                             pr_rmprop.Ground, pr_rmprop.Priority, pr_rmprop.PayDate, pr_rmprop.ShifrOper,
                             0,"Без акцепта" );
         if(not stat) return 1;
         else return 0;
         end;
     else
         VRSL = ActiveX("RSGENOLE.VRslRun.1"); 
         report = VRSL.RunRpt("ПлатежноеТребование");
    
         divide_kopeck = РазделительКопеек;
    
         sumstr = string(MoneyL(pr_pm_paym.Amount));
         rub = RubToStrAlt(pr_pm_paym.Amount);
         pospoint = Index(sumstr, ".");
         if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
           rub = SubStr(rub,1,Index(rub,"коп")-4);
         end;

         report.DataPostup.text       = string(pr_rmprop.Date);
         report.Number.text           = pr_rmprop.Number;
         report.Data.text             = string(pr_rmprop.Date);
         report.Dispatch.text         = dptch;
         report.UslPlat.text          = "Без акцепта";
         report.SrokAkcepta.text      = string(0);
         report.INN_Payer.text        = pr_rmprop.PayerINN;
         report.Sum.text              = string(pr_pm_paym.Amount:f);
         report.SumText.text          = rub;
         report.Payer.text            = pr_rmprop.PayerName;
         report.Account_Payer.text    = deb_acc;
         report.Bank_Payer.text       = bank_payer;
         report.MFO_Payer.text        = payerMFO;
         report.Corac_Payer.text      = deb_corracc;
         report.Receiver.text         = pr_rmprop.ReceiverName;
         report.Account_Receiver.text = kred_acc;
         report.MFO_Receiver.text     = receiverMFO;
         report.Bank_Receiver.text    = bank_receiver;
         report.Corac_Receiver.text   = kred_corracc;
         report.INN_Receiver.text     = pr_rmprop.ReceiverINN;
         report.Ground.text           = InterpretGround( pr_rmprop.Ground) ;
         report.Kind_Oper.text        = pr_rmprop.ShifrOper;
         report.Pay_Date.text         = string(pr_rmprop.PayDate);
         report.Payment.text          = string(pr_rmprop.Priority);
         report.Data_I2.text          = " ";
    
         report.runReport();
    
         if (IsStandAlone)
          ViewRep (report);
         else
          TransferRep (report);
         end;
     end;
   else
      stat = false;
   end;

   if(not stat) return 1;
   else         return 0;
   end;

end;




macro PrintMO ( mo, pm, dt, ct, rm )

   setbuff( pr_order,   mo );
   IsBBANK = 1;
   return print_report( pr_order.DocKind , pm, dt, ct, rm );

end;




macro PrintPayment ( pm, dt, ct, rm )

   Var menu_item;
   Array MenuStr;

   IsBBANK = 0;

   MenuStr(0) = "Платежное поручение";
   MenuStr(1) = "Платежное требование";

   menu_item = Menu( MenuStr, "", "Печать в виде:" );
   if ( menu_item == 0 )
      return print_report( DLDOC_BANKPAYMENT , pm, dt, ct, rm );
   elif ( menu_item == 1 )
      return print_report( DLDOC_BANKCLAIM , pm, dt, ct, rm );
   else
      return 1;
   end;

end;

end;



