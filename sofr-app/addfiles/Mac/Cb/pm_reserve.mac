//-----------------------------------------------------------------------------
// Блок     : Вне блока
// Шаг      : Вне шага
// Описание : Проверка возможности дебетования счета и создание резерва
//            Общие функции и переменные для макросов шагов
//-----------------------------------------------------------------------------
import PaymInter, pm_common, likepy, "pm_reserve_prcres.mac";

private macro ProcessOneAccount( Payment: RsbPayment, 
                               AllowOverdraft: bool, 
                               CheckI2: bool, 
                               CheckWaiting: bool, 
                               UseReserve: bool, 
                               SkipArest: bool, 
                               IsInsLimUse: bool, 
                               NeedMakeReserve:bool, 
                               ReasonID:@integer, 
                               CheckIWP:bool,
                               CheckILORO:bool,
                               Account : string,
                               Chapter : integer,
                               Code_Currency : integer,
                               Amount : money,
                               PmAddPIID : integer,
                               IsCalledFromPrepro : bool
                               
                             ) : integer

  var stat : integer = 0;

  var SumOverdraft : money = $0;
  var IsI2 : bool = false, IsWaiting : bool = false, IsWP : bool = false;

  var ChkReserveResult : integer = Payment.CheckRestAndMakeReserve
                                       ( Account, Chapter, Code_Currency, Amount,
                                         Payment.ValueDate, CR_CHECKREST, AllowOverdraft, 
                                         CheckI2, CheckWaiting, UseReserve, SkipArest, 
                                         SumOverdraft, NeedMakeReserve, ReasonID, 
                                         CheckIWP, CheckILORO, PmAddPIID, 
                                         IsI2, IsWaiting, IsWP, IsCalledFromPrepro
                                       );

  if(IsCalledFromPrepro)
    var IsPmAddPI : bool = ( (PmAddPIID != null) and (PmAddPIID > 0) );

    stat = ProcessCheckReserveResult
           ( ChkReserveResult, ReasonID, IsWP, IsI2, IsWaiting,
             Payment, Account, Chapter, Code_Currency, IsPmAddPI
           );

  else
    stat = ChkReserveResult;
  end;

  // Если использовался овердрафт, то вставляем запись об его использовании - только для счетов с типом '%V%'
  var TRecAccount : TRecHandler = TRecHandler("account.dbt");
  if( not stat and ( SumOverdraft > $0 ) and IsInsLimUse and 
      PM_GetAccountRecord( Account, Code_Currency, Chapter, TRecAccount ) and 
      Index( TRecAccount.rec.Type_Account, "V") )
    if( OV_GetOverdraftProcMode() == RBOV_MODE_INTEGRATED )
      if(InsertLimitUseTry_Int( Code_Currency, Chapter, Account, SumOverdraft, Payment.ValueDate, true ))
        MsgBox("Ошибка при вставке изменения лимита счета");
        return 1;
      end;
    end;
  end;

  return stat;

end;

// создание резерва с предварительной проверкой свободного остатка
macro CheckRestAndMakeReserve( Payment: RsbPayment, 
                               AllowOverdraft: bool, 
                               CheckI2: bool, 
                               CheckWaiting: bool, 
                               UseReserve: bool, 
                               РНО_Статус: integer, 
                               SkipArest: bool, 
                               IsInsLimUse: bool, 
                               NeedMakeReserve:bool, 
                               ReasonID:@integer, 
                               CheckIWP:bool,
                               CheckILORO:bool,
                               IsCalledFromPrepro : bool
                             ) : integer

  var AddPI:RsbPIPayment    = Payment.PIList(PRT_Debet);
  var CurrAddPI:TRecHandler = TRecHandler( "pmaddpi.dbt" );
  var IsNext                = AddPI.First(PRT_Debet);
  var stat                  = 0;

  if( РНО_Статус == OPR_PAYM_ST_PERMISSION_YES )
    SkipArest = true;
  elif( РНО_Статус == OPR_PAYM_ST_PERMISSION_NO )
    SkipArest = false;
  elif( (ValType(SkipArest) == V_UNDEF) or (SkipArest == null) )
    SkipArest = false;
  end;

  if(ValType(NeedMakeReserve) == V_UNDEF)
    NeedMakeReserve = true;
  end;

  if(ValType(CheckIWP) == V_UNDEF)
    CheckIWP = true;
  end;

  if(ValType(CheckILORO) == V_UNDEF)
    CheckILORO = false;
  end;

  ReasonID = 0;

  if( Payment.PIList(PRT_Debet).Size > 0 ) // Если есть разноска по дебету
    while( (stat == 0) and (IsNext == 0) and (AddPI.Current(CurrAddPI) == 0) )
      stat = ProcessOneAccount
             ( Payment, AllowOverdraft, CheckI2, CheckWaiting, UseReserve, SkipArest, 
               IsInsLimUse, NeedMakeReserve, @ReasonID, CheckIWP, CheckILORO,
               CurrAddPI.rec.Account, CurrAddPI.rec.Chapter, CurrAddPI.rec.FIID,
               CurrAddPI.rec.FuturePayerAmount, CurrAddPI.rec.PmAddPIID,
               IsCalledFromPrepro
             );

      IsNext = AddPI.Next();
    end;
  else // проверяем счет плательщика
    if( Payment.FuturePayerAmount != 0 )
      stat = ProcessOneAccount
             ( Payment, AllowOverdraft, CheckI2, CheckWaiting, UseReserve, SkipArest, 
               IsInsLimUse, NeedMakeReserve, @ReasonID, CheckIWP, CheckILORO,
               Payment.FuturePayerAccount, Payment.Chapter, Payment.FuturePayerFIID,
               Payment.FuturePayerAmount, null,
               IsCalledFromPrepro
             );

    else
      stat = ProcessOneAccount
             ( Payment, AllowOverdraft, CheckI2, CheckWaiting, UseReserve, SkipArest, 
               IsInsLimUse, NeedMakeReserve, @ReasonID, CheckIWP, CheckILORO,
               Payment.PayerAccount, Payment.Chapter, Payment.PayerFIID,
               Payment.PayerAmount, null,
               IsCalledFromPrepro
             );

    end;
  end;

  if(not IsCalledFromPrepro)
    if( stat == MR_INVALIDACCOUNT )
      msgbox("Неверный счет плательщика");
    elif( (stat == MR_NOFREEAMOUNT) and (ReasonID > 0) )
      msgbox("На счет плательщика наложено ограничение операций");
    elif( stat == MR_NOFREEAMOUNT )
      msgbox("Недостаточно средств на счете плательщика");
    end;
  end;

  return stat;
end;  
