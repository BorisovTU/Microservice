//-----------------------------------------------------------------------------
// Блок     : 29017 - "Предобработка кассового документа"
// Описание : "Проверка остатков по счетам"
//-----------------------------------------------------------------------------
import pm_common, cbsttls, pm_chkrst;

private macro WorkWithRetail():bool
  var Work_Retail:bool = false;
  var err:integer = 0;

  GetRegistryValue( "COMMON\\WORK_MODE\\WORK_WITH_RETAIL", V_BOOL, Work_Retail, err );
  if( err != 0 )
    msgbox(" Ошибка чтения настройки COMMON\\WORK_MODE\\WORK_WITH_RETAIL ");
    return false;
  end;

  return Work_Retail;
end;

// Проверить, что плательщик является клиентом счета плательщика
private macro IsPayerClientAcc( Payment:RsbPayment )

  var params:TArray;
  var rs:object;

  var SelectStr = "select t_Client from daccount_dbt  " +
                   "where  t_Account       = :Account " +  
                   "  and  t_Chapter       = :Chapter " + 
                   "  and  t_Code_Currency = :FIID    ";

  params = makeArray( SQLParam( "Account", Payment.FuturePayerAccount ),
                      SQLParam( "Chapter", Payment.Chapter      ),
                      SQLParam( "FIID"   , Payment.FuturePayerFIID    ) );

  rs = execSQLselect( SelectStr, params, FALSE );

  if( rs AND rs.moveNext() )
    if( rs.value(0) == Payment.Payer )
      return true;
    end;
  end;
  return false;
end;

MACRO CS_CheckAccRest( Payment:RsbPayment ):integer

  var obj:object;
  var CTRL_segment:integer = 0;
  var RestDebet;
  var RestCredit;
  var Rest;
  var pi :TRecHandler = TRecHandler( "pmaddpi.dbt" );
  var next:integer = 0;
  var CheckIndexDate:date = IfThenElse( Payment.PayerBankEnterDate == date(0,0,0), Payment.ValueDate, Payment.PayerBankEnterDate );

  if (GetOprStatus(OPR_PAYM_INDEX) <= OPR_PAYM_ST_INDEX_NO)
    //Проверим остаток на счете получателя
    if((Payment.DocKind == CASH_BOF_INCORDER) and (Payment.PIList(1).Size > 0))
      
      Payment.PIList(1).First;
      while(next == 0)
        next = Payment.PIList(1).Current(pi);
        
        if((next != 0) or (not CheckReceiverAccount(pi.rec.Account, pi.rec.FIID, pi.rec.Amount, Payment.ValueDate)))
          
          if(RejectPayment(Payment, "Недостаточно средств для зачисления" ))
            return 1;
          end;
          msgbox( "Недостаточно средств для зачисления" );
          return 0;

        end;
        
        if(next == 0)
          next = Payment.PIList(1).Next;
        end;
      end;

    elif( not CheckReceiverAccount(Payment.FutureReceiverAccount, Payment.FutureReceiverFIID, Payment.FutureReceiverAmount, Payment.ValueDate) )
      
      if(RejectPayment(Payment, "Недостаточно средств для зачисления" ))
        return 1;
      end;
      msgbox( "Недостаточно средств для зачисления" );
      return 0;

    end;
  end;
  
  //Проверим остаток на счете плательщика
  var stat : integer = CheckRestAndMakeReserve( Payment, true, true, true, true, 
                                                GetOprStatus(OPR_PAYM_PERMISSION), NULL, 
                                                true, true, NULL, true, true, true );
  if(stat)
    return stat;
  end;

  if( Payment.DocKind == CASH_BOF_ADDORDER )
    obj = GenObject( "RsbBBAddCashOrder", Payment.DocumentID );
  elif( Payment.DocKind == CASH_BOF_INCORDER )
    obj = GenObject( "RsbBBIncCashOrder", Payment.DocumentID );
  elif( Payment.DocKind == CASH_BOF_OUTORDER )
    obj = GenObject( "RsbBBOutCashOrder", Payment.DocumentID );
  elif (Payment.DocKind == DLDOC_INOUTORDER)
    obj = GenObject( "RsbBBInOutCashOrder", Payment.DocumentID );
  elif( Payment.DocKind == CASH_PS_INCORDER )
    obj = GenObject( "RsbPSInCashOrder" , Payment.DocumentID );
  else //CASH_PS_OUTORDER
    obj = GenObject( "RsbPSOutCashOrder", Payment.DocumentID );
  end;


  // Если платеж прошел все проверки
  // Для приходного кассового ордера, объявления на взнос наличными
  if ((Payment.DocKind == CASH_PS_INCORDER) or (Payment.DocKind == CASH_BOF_INCORDER))
    if( (WorkWithRetail()) AND (obj.Origin == 4) )
      CTRL_segment = OPR_PAYM_ST_CTRL_CONTROL;    
    elif (WorkWithRetail())
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTACCEPT;      
    else
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTINCABS;
    end;
     
  // Для приходно-расходного ордера и ордера подкрепления
  elif ((Payment.DocKind == DLDOC_INOUTORDER) or (Payment.DocKind == CASH_BOF_ADDORDER))
    if( WorkWithRetail() )
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTACCEPTR;      
    else
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTINCABSR;      
    end;
  elif ((Payment.DocKind == CASH_PS_OUTORDER) or (Payment.DocKind == CASH_BOF_OUTORDER))
    if( (WorkWithRetail()) AND (obj.Origin == 4) )
      CTRL_segment = OPR_PAYM_ST_CTRL_CONTROL;
    else
      CTRL_segment = OPR_PAYM_ST_CTRL_NOTCONTROL;
    end;     
  end;  
        
  if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, CTRL_segment ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;


  return 0;

END;
