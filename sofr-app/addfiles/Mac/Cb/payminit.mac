/*
  $Name:         payminit.mac
  $Module:       РКО
  $Description:  Макрос инициализации операции Платеж
*/

/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.0           */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Имя файла        : payminit.mac                                     */
/*                                                                      */
/*  Описание         : Макрос инициализации операции Платеж             */
/*                                                                      */
/*  Программист      : Копачев Д.В.                                     */
/*                                                                      */
/*  Создан           : 19.12.2005                                       */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import globals, FIInter, PaymInter, PSInter, PTInter, OprInter, CashInter, oralib, likepy, pmprops, pmprepromass, CTInter, InsCarryDoc, pm_tools;

VAR PaymentObj:RsbPayment;

PRIVATE const PAYMENT_KIND_MAIL = "П";  // почтой
PRIVATE const PAYMENT_KIND_TELG = "T";  // телеграф
PRIVATE const PAYMENT_KIND_ELEC = "Э";  // электронно
PRIVATE const PAYMENT_KIND_INST = "С";  // срочно


PRIVATE const CHOICE_BUTTON_CARRY  = 1; // "Провести"
PRIVATE const CHOICE_BUTTON_CANCEL = 0; // "Отменить"

PRIVATE const CHOICE_BUTTON_YES = 0; // "Да"
PRIVATE const CHOICE_BUTTON_NO  = 1; // "Нет"
PRIVATE const CHOICE_BUTTON_REFUSE  = 2; // "Отказаться"

//------------------------------------------------------------------------------
// Платеж исходящий
//------------------------------------------------------------------------------
PRIVATE MACRO IsOutgoingPayment( Payment:RsbPayment ):bool
 return ( ((Payment.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (Payment.PayerIsSender != "X") AND (Payment.ReceiverGroup != PAYMENTS_GROUP_EXTERNAL)) OR
          ((Payment.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL) AND (Payment.ReceiverIsSender != "X") AND (Payment.PayerGroup != PAYMENTS_GROUP_EXTERNAL)) );
END;

//------------------------------------------------------------------------------
// Предупреждение об отличии даты значения от текущего curdate
//------------------------------------------------------------------------------
PRIVATE MACRO GetChoiceAboutNotEQValueDateAndCurDate(Payment:RsbPayment)

  Array Text;
  Array Buttons;
  
  var DialogFlag;
  if(IsOprMultiExec())
    DialogFlag  = TSetDialogFlag(1);
  end;

  Text(0) = string( "В документе " + Payment.Number + " дата значения " + Payment.ValueDate + " отличается от даты текущего операционного дня " + {curdate} + ".|Провести документ текущим операционным днем?" );

  Buttons(CHOICE_BUTTON_YES)    = "Да"; 
  Buttons(CHOICE_BUTTON_NO)     = "Нет"; 
  Buttons(CHOICE_BUTTON_REFUSE) = "Отказаться"; 

  var Choise = ConfWin(Text,Buttons);

  return Choise;
END;


//------------------------------------------------------------------------------
// Старт операции
//------------------------------------------------------------------------------
MACRO InitOperation( KindOp, KindDoc, addr )

  var choice; 
  record pspayord( pspayord ); 
  
  // У РКОшных документов проверяем наличие вида обслуживания РКО у плательщика
  if( ( PaymentObj.DocKind == PS_PAYORDER ) or
      ( PaymentObj.DocKind == PS_CPORDER  ) or
      ( PaymentObj.DocKind == PS_INRQ     ) )

    if( ( not PaymentObj.PayerAccount ) and ( PaymentObj.DocKind != DLDOC_MULTYPM ) )
      MsgBox("Не задан счет плательщика");
      return 1;
    end;

    if( not ОбслуживаетсяКлиент(PaymentObj.Payer, PTSK_PAY, {curdate}, {curdate} ) )
      MsgBox("Клиент не состоит на обслуживании в РКО");
      return 1;
    end;
  end;

  // Проверить, что дата pmpaym.ValueDate открыта в филиале StartDepartment
  var select:string = " select t_IsClosed " +
                        " from dcurdate_dbt " +
                       " where t_Branch = :StartDepartment " +
                         " and t_CurDate = :ValueDate ";

  var params:TArray = makeArray( SQLParam( "StartDepartment", PaymentObj.StartDepartment ),
                                 SQLParam( "ValueDate"     , PaymentObj.ValueDate ) );
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );

  var query:string, param:TArray, rs:RsdRecordset;

  if( rset and not rset.moveNext() )
    MsgBox("Нет операционного дня с указанной в документе датой значения");
    return 1;
  end;

  var RegPath : string = "CB\\PAYMENTS\\PAYMENTBETWEENDEP\\STARTWITHOUTOPENDAY";
  var StartWithoutOpenDay : bool = Bnk_GetRegistryValue(RegPath, V_BOOL, false);
  
  if( (PaymentObj.StartDepartment != PaymentObj.EndDepartment) and (not StartWithoutOpenDay) )
    var NextDep:integer = MFR_GetNextDepartment( PaymentObj.Department, PaymentObj.EndDepartment );

    // Находим максимальный операционный день в следующем филиале платежа
    select = " select max(t_CurDate) " +
             " from dcurdate_dbt " +
             " where t_Branch = :NextDepartment ";
                          
    params = makeArray( SQLParam( "NextDepartment", NextDep ) );
    rset = execSQLselect( select, params, TRUE );
    if ( rset and rset.moveNext() and (rset.value(0) < PaymentObj.ValueDate) )
      var NextDepCode = "", NextDepName = "";
      CB_GetDepartmentCodeAndName(NextDep, NextDepCode, NextDepName);       
      var msg = "Не открыт операционный день " + PaymentObj.ValueDate + " в филиале " + NextDepCode + " " + NextDepName + "." +
                " Операция может быть запущена только после открытия этого или следующего операционного дня в филиале " + NextDepCode + " " + NextDepName;
      MsgBox(msg);
      return 1;
    end;
  end;
  
  
  return 0;
END;

//-----------------------------------------------------
//             Массовый старт операции
//-----------------------------------------------------
//Предтранзакционные действия
MACRO PrepMassInitOperation()
  
  var stat:integer = execStoredFunc( "PM_OPRINIT.MassInitOperationPrepare", V_INTEGER );

  if( stat )
    MemoryError( stat );
  end;

  return stat;

onerror(x)
  msgbox( x.Message );
  return 1;
END;

// Транзакционные действия
MACRO ExecMassInitOperation()  
  return 0;
END;

  

