/*
$Name:             ChangeEmitent.mac
$Module:           Ценные бумаги 
$Description:      Смена эмитента для ценной бумаги
*/
import FiInter, rsd, cb_sql;
import PTInter;

macro IsExistAccounts(fiid)
   var Query, DataSet, Params;
   var Categ1 = "Наш портфель ц/б";
   var Categ2 = "Наш портфель ПКУ, ц/б";

   Query = "SELECT distinct accdoc.t_Account, cat.t_Code, accdoc.t_FIID "+
           "  FROM dmcaccdoc_dbt accdoc, dmccateg_dbt cat "+
           " WHERE cat.t_Code IN ( :cat1, :cat2 ) "+
           "   AND accdoc.t_CatID = cat.t_ID "+
           "   AND accdoc.t_FIID = :fiid  ";

   Params = makeArray( SQLParam("cat1", Categ1),
                       SQLParam("cat2", Categ2),
                       SQLParam("fiid", fiid) );

   DataSet = execSQLselect( Query, Params );

   if( DataSet.MoveNext )
      return true;
   end;

   return false;
end;

private macro AddLog(p_text)
   var Query, DataSet, Params;
   Query = "begin it_log.log(:p_text,it_log.C_MSG_TYPE__DEBUG); end;";
   Params = makeArray( SQLParam("p_text", p_text) );
   execSQL( Query, Params );

end;

macro UpdateEmitent(partyid, fiid, partyid_old);
   var Query, DataSet, Params;
   Query = "update dfininstr_dbt set t_issuer=:partyid where t_fiid=:fiid";
   Params = makeArray( SQLParam("partyid", partyid),
                       SQLParam("fiid", fiid) );
   execSQL( Query, Params );
   AddLog ("Для ЦБ "+fiid+" сменен эмитент c "+partyid_old+" на "+partyid+" операционистом "+{oper});
end;


macro exec_change_emitent( fiid)
   var state = true;
   var err_txt = "OK";

   PRIVATE RECORD Fininstr("fininstr.dbt");
   PRIVATE RECORD av("avoiriss.dbt");
   PRIVATE RECORD pt( party );

   ПолучитьФинИн( FIID, Fininstr, av );

   if( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_EQUITY_SHARE, Fininstr.AvoirKind ) /*Акции обыкновенные*/
         or
       FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_PREFERENCE_SHARE, Fininstr.AvoirKind )  /*Акции привилегированные*/
         or
       FI_IsDeposReceipt( Fininstr ) /*Депозитарные расписки*/ )
   else 
      state = false;
      err_txt = "Из данного пункта меню возможна смена эмитента только для акций или депозитарных расписок";
   end;

   if (state)
      if (IsExistAccounts(fiid))
         state = false;
         err_txt = "Невозможно сменить эмитента при наличии портфельных счетов по выпуску";
      end;
   end;

   if (state)
      if (ListPText( pt, Fininstr.issuer, "t.t_legalform = 1 AND t.t_partyid IN (SELECT t_partyid FROM dpartyown_dbt WHERE t_partykind = 5)", true, "Эмитенты" ) )
         UpdateEmitent(pt.partyid, fininstr.fiid, Fininstr.issuer);
      else 
         state = false;
         err_txt = "Новый эмитент не выбран, смена эмитента не выполнена";
      end;
   end;

   msgbox(err_txt);
   return state;

end;