/*
$Name: ic_getacclaim.mac
$Module: Ядро Banking
$Description: Сервис "Запрос ограничений по счету"
*/


import BankInter, CTInter, OprInter, "bnk_common.mac", "ic_getaccclient.mac";

private const ACCCLAIM:integer = 0;
private const PSPOREQUEST:integer = 1;

private macro GetAcClaimCurAmountOnDate(ClaimID:integer, DateReq:date)

  var Sum = $0;
  var params = TArray;

  params[0] = SQLParam("p_ClaimID", ClaimID);
  params[1] = SQLParam("p_OnDate", DateReq );
  params[2] = SQLParam("p_CurrentAmount", V_DOUBLE, RSDBP_OUT );
  params[3] = SQLParam("p_StateDate", V_DATE, RSDBP_OUT );

  if(execStoredFunc( "RSI_RSB_ACCLAIM.GetAcClaimCurAmountOnDate", V_INTEGER, params ) != 0)
    RunError("Ошибка получения суммы претензии на дату " + DateReq);
  else
    Sum = params.Value(2).value;
  end;
  
  return Sum;
end;

// Определение реквизитов ограничени¤ по счету

private macro GetAccountRestriction(ClaimOrRequest:RSDRecordSet, Type:integer, OrgName:@string, OrgINN:@string, Amount:@string, ReqDate:@date, Priority:@Integer, IPNumber:@string, DocNumber:@string, OSPCode:@string )
  if(Type == ACCCLAIM )    
    if(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_TAX)
      OrgName = "1";
    elif(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_OFFICER_COURT)
      OrgName = "2";
    elif(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_SOCIAL)
      OrgName = "3";
    elif(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_PENSION_FUND)
      OrgName = "4";
    elif(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_CUSTOM)
      OrgName = "5";
    elif(ClaimOrRequest.value("Initiator") == ACCLAIM_INITIATOR_JUDICIAL)
      OrgName = "6";
    else
      OrgName = "7";
    end;

    DocNumber = ClaimOrRequest.value("DocNumber");
    Amount = GetAcClaimCurAmountOnDate(ClaimOrRequest.value("ClaimID"), {curdate});
    IPNumber = ClaimOrRequest.value("IPNumber");
    if((ClaimOrRequest.value("ClaimKind") == ACCLAIM_KIND_SPECIAL) or (ClaimOrRequest.value("ClaimKind") == ACCLAIM_KIND_ARREST ))
      Priority = 1;
    else
      Priority = ClaimOrRequest.value("RPriority");
    end;
    
  elif(Type == PSPOREQUEST)
    if((ClaimOrRequest.value("PayType") == BPT_TAX) or (ClaimOrRequest.value("Issuer") == ISSUER_FNS ))
      OrgName = "1";
    elif(ClaimOrRequest.value("Issuer") == ISSUER_FSSP)
      OrgName = "2";
    elif(((ClaimOrRequest.value("PayType") == BPT_INSURANCE) and (not CompareStrWithMasks("393*", ClaimOrRequest.value("BTTICode")))) or (ClaimOrRequest.value("Issuer") == ISSUER_FSS ))
      OrgName = "3";
    elif(((ClaimOrRequest.value("PayType") == BPT_INSURANCE) and (not CompareStrWithMasks("392*", ClaimOrRequest.value("BTTICode")))) or (ClaimOrRequest.value("Issuer") == ISSUER_PFR ))
      OrgName = "4";
    elif((ClaimOrRequest.value("PayType") == BPT_CUSTOM ) or (ClaimOrRequest.value("Issuer") == ISSUER_FTS ))
      OrgName = "5";
    elif(ClaimOrRequest.value("Issuer") == ISSUER_COURT)
      OrgName = "6";
    else
      OrgName = "7";
    end;

    var NoteSplitter = Splitter(ClaimOrRequest.Value("Note"), "//");

    IPNumber = NoteSplitter.Next();

    DocNumber = "";
    var temp = NoteSplitter.Next(); 
    var temp1 = ""; 

    while(temp != "")    
      temp1 = NoteSplitter.Next(); 

      if(temp1 != "")
        DocNumber = DocNumber + temp;
        temp = temp1;
      else
        OSPCode = temp;
        break;
      end;
    end;
    
    Amount = ClaimOrRequest.value("Amount");
    Priority = ClaimOrRequest.value("RPriority");
  end;

  if(ClaimOrRequest.value("INN") != "")
    OrgINN = ClaimOrRequest.value("INN");
  else
    OrgINN = "0000000000";
  end;
   
  ReqDate = ClaimOrRequest.value("ReqDate");  
  
end; 

class AccountRestrictionRequest                                  // Входящие параметры
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var          OrigReq :       ReqOrganisation;                  // Определяет параметры исходного Запроса от внешней АС
  var              BIC :                string =            "";  // БИК банка/филиала, где открыт счет
  var          Account :                string =            "";  // Номер счета
  var  PersonalAccount :                string =            "";  // Субсчет, карта
end;

class      AccountRestriction                                    // Описание обременения
  var             Type :               integer =            "";  // Тип
  var          OrgName :                string =            "";  // Кто наложил
  var           OrgINN :                string =            "";  // ИНН
  var           Amount :                 money =            $0;  // Сумма
  var             Date :                  date;                  // Дата наложения
  var         Priority :                string =            "";  // Очередность
  var         IPNumber :                string =            "";  // Номер ИП
  var        DocNumber :                string =            "";  // Номер постановления СПИ
  var          OSPCode :                string =            "";  // Код подразделения ОСП
  var      InternalKey :                string =            "";  // Уникальный номер Постановления СПИ
end;

class AccountRestrictionResponse
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var         SenderID :                string =            "";  // Идентификатор (код) АС, источник сведений
  var     RealDateTime :              DateTime = date(0, 0, 0);  // Дата, время актуальности сведений
  var              BIC :                string =            "";  // БИК Банка
  var          Account :                string =            "";  // Номер счета
  var         Currency :                string =            "";  // Код валюты счета
  var   ClientFullName :                string =            "";  // Наименование владельца счета
  var              INN :                string =            "";  // ИНН владельца счета
  var AccountEncumbranceDatum :         TArray =        TArray;  // Список обременений
end;

macro GetAcClaimIC(searchParameters)

  var query = 
  " SELECT accoun.t_AccountID AS AccountID, " +
  " accoun.t_Code_Currency AS FIID, "
  " accoun.t_Chapter AS Chapter, "
  " accoun.t_Account AS Account, "
  " dp_dep.t_PartyID AS Department, " +
  " accoun.t_Open_Date AS OpenDate, " +
  "      NVL(fininstr.t_Iso_Number, CHR(1)) AS CurrencyCode, " +
  "      CASE " +
  "        WHEN party.t_LegalForm = :PTLEGF_INST THEN party.t_Name " +
  "        ELSE persn.t_Name1 || ' ' || persn.t_Name2 || ' ' || persn.t_Name3 " +
  "      END AS ClientFullName, " +
  " NVL(REGEXP_SUBSTR (objcodeINN.t_Code, '[^/]+'), CHR(1)) AS INN " +
  "   FROM daccount_dbt accoun " +
  "        INNER JOIN ddp_dep_dbt dp_dep ON dp_dep.t_Code = accoun.t_Department " +
  "   LEFT OUTER JOIN dfininstr_dbt fininstr " +
  "       ON fininstr.t_FIID = accoun.t_code_Currency  " +
  "   LEFT OUTER JOIN dparty_dbt party ON party.t_PartyID = accoun.t_Client " +
  "   LEFT OUTER JOIN dpersn_dbt persn ON persn.t_PersonID = party.t_PartyID " +
  "   LEFT OUTER JOIN dobjcode_dbt objcodeINN  " +
  "       ON     objcodeINN.t_ObjectID = dp_dep.t_PartyID  " +
  "       AND objcodeINN.t_CodeKInD = :PTCK_INN  " +
  "       AND objcodeINN.t_State = 0  " +
  "  WHERE     accoun.t_Account = :sAccount " +
  " ORDER BY CASE WHEN accoun.t_Open_Date >= :CurDate THEN 0 ELSE 1 END ASC, accoun.t_Open_Date DESC ";
  
  var queryParams = TArray;
  
  queryParams[queryParams.Size] = SQLParam("PTLEGF_INST", PTLEGF_INST);
  queryParams[queryParams.Size] = SQLParam("PTCK_INN", PTCK_INN);
  queryParams[queryParams.Size] = SQLParam("sAccount", searchParameters.Account);
  queryParams[queryParams.Size] = SQLParam("CurDate", {curdate});

  var rs = execSQLselect(query, queryParams);
  
  var found = false;  
      
  while(rs and rs.MoveNext())
    if(GetPartCode(rs.value("Department"), PTCK_BIC) != searchParameters.BIC)
      continue;
    else   
      found = true;
      break;
    end;
  end; 
  
  if(not found)
    RunError("Не найден счет " + searchParameters.Account );
  else  
  
    var response = AccountRestrictionResponse;
    
    response.SenderID = Bnk_GetRegistryValue("RS-CONNECT\\SENDERID\\Banking", V_STRING, "");
    response.ReqID = searchParameters.ReqID;
    response.BIC = GetPartCode(rs.value("Department"), PTCK_BIC);
    response.Account = searchParameters.Account;
    response.Currency = rs.value("CurrencyCode");
    response.ClientFullName = rs.value("ClientFullName");
    response.INN = rs.value("INN");
  
    var ClaimQuery =   
    "  SELECT acclaiminit.t_Group AS Initiator, " +
    "  ac.t_FISCORGINN AS INN, " +
    "  ac.t_StartDate AS ReqDate, " +
    "  ac.t_ClaimKind AS RPriority, " +
    "  ac.t_FiscOrderNum AS IPNumber, " +
    "  ac.t_DocNumber AS DocNumber, " +
    "  ac.t_FiscOrgCode AS OSPCode, " +
    "  ac.t_ClaimID AS ClaimID, " +
    "  ac.t_ClaimKind AS ClaimKind " +
    "      FROM dacclaim_dbt ac " +
    "  INNER JOIN dacclaimstate_dbt acst ON acst.t_ClaimID = ac.t_ClaimID " +
    "  LEFT OUTER JOIN dacclaiminit_dbt acclaiminit " +
    "    ON acclaiminit.t_InitiatorID = ac.t_initiator " +
    "     WHERE ac.t_Chapter  = :AccChapter " +
    "       AND ac.t_Account  = :sAccount " +
    "       AND ac.t_FIID     = :AccFIID  " +
    "       AND (ac.t_ClaimKind = :ACCLAIM_KIND_ARREST ";
    
    var ClaimQueryParam = TArray;
    
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("AccChapter", rs.value("Chapter") );
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("sAccount", rs.value("Account") );
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("AccFIID", rs.value("FIID") );
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("ACCLAIM_KIND_ARREST", ACCLAIM_KIND_ARREST );
    
    var IncludeTargetUse = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\IncludeTargetUse", V_BOOL, false);
    
    if(IncludeTargetUse == true)
      ClaimQuery = ClaimQuery + " OR (ac.t_ClaimKind = :ACCLAIM_KIND_SPECIAL ) ) ";
      ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("ACCLAIM_KIND_SPECIAL", ACCLAIM_KIND_SPECIAL );
    else
      ClaimQuery = ClaimQuery + " ) ";
    end; 
    
    
    ClaimQuery = ClaimQuery +
    "       AND (acst.t_State = :ACCLAIM_STATUS_ACTIVE OR acst.t_State = :ACCLAIM_STATUS_MODIFIED)  " +
    "       AND acst.t_StateDate = (SELECT MAX(t.t_StateDate)  " +
    "                                 FROM dacclaimstate_dbt t  " +
    "                                WHERE t.t_ClaimID    = ac.t_ClaimID " +
    "                                  AND t.t_StateDate <= :DateReq); ";

    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("ACCLAIM_STATUS_ACTIVE", 1  );  
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("ACCLAIM_STATUS_MODIFIED", 2 );
    ClaimQueryParam[ClaimQueryParam.Size] = SQLParam("DateReq", {curdate});
    
    var claims = execSQLselect(ClaimQuery, ClaimQueryParam);
    
    while(claims and claims.MoveNext())
    
      var claim = AccountRestriction;
      
      if(claims.Value("ClaimKind") == ACCLAIM_KIND_ARREST)
        claim.Type = "1";
      elif(claims.Value("ClaimKind") == ACCLAIM_KIND_SPECIAL)
        claim.Type = "3";
      end;
    
      GetAccountRestriction(claims, ACCCLAIM, @claim.OrgName, @claim.OrgINN, @claim.Amount, @claim.Date, @claim.Priority, @claim.IPNumber, @claim.DocNumber, @claim.OSPCode );
      
      response.AccountEncumbranceDatum[response.AccountEncumbranceDatum.Size] = claim;
    end;
    
    var RequestQuery =
    " WITH pmhist1 " +
    "      AS (SELECT * " +
    "            FROM dpmhist_dbt pmhist " +
    "           WHERE pmhist.t_AutoKey = " +
    "                    (SELECT MIN (pmh.T_AutoKey) " +
    "                       FROM dpmhist_dbt pmh " +
    "                      WHERE     pmh.t_StatusIDFrom IN " +
    "                                   (:PM_I2PLACED1, :PM_IWPPLACED1) " +
    "                            AND pmh.t_PaymentID = pmhist.t_PaymentID)) " +
    " SELECT pmpaym.t_PaymentID, " +
    "        pmpaym.t_PayType AS PayType, " +
    "        pmrmprop.t_bttticode, " +
    "        PM_COMMON.GetObjAttrValue (:OBJTYPE_PAYMENT, " +
    "                                   LPAD (pmpaym.t_PaymentID, 10, '0'), " +
    "                                   :OBJ_PAYMENT_GROUP_ISSUER, " +
    "                                   :DateReq1) " +
    "           AS Issuer, " +
    "        NVL (REGEXP_SUBSTR (pmrmprop.t_ReceiverINN, '[^/]+'), CHR (1)) AS INN, " +
    "        PM_ACTUATE.ConvertSum_Ex (pmpaym.t_FutureBaseAmount, " +
    "                                  pmpaym.t_StartDepartment, " +
    "                                  pmpaym.t_FutureDRate, " +
    "                                  pmpaym.t_futureDRatePoint, " +
    "                                  pmpaym.t_futureDRateScale, " +
    "                                  pmpaym.t_FutureDrateisInverse, " +
    "                                  pmpaym.t_futureDRateType, " +
    "                                  :DateReq2, " +
    "                                  pmpaym.t_BaseFIID, " +
    "                                  pmpaym.t_FIID_FuturePayAcc) " +
    "           AS Amount, " +
    "        pmpaym.t_PayerBankEnterDate " +
    "           AS ReqDate, " +
    "        pmrmprop.t_Priority " +
    "           AS RPriority, " +
    "        NVL (PM_COMMON.GetNoteTextStr ( " +
    "                pmpaym.t_PaymentID, " +
    "                CASE " +
    "                   WHEN pmpaym.t_DocKind = :PS_PAYORDER THEN :OBJTYPE_PSPAYORD " +
    "                   WHEN pmpaym.t_DocKind = :PS_INRQ THEN :OBJTYPE_PSINRQ " +
    "                END, " +
    "                CASE " +
    "                   WHEN pmpaym.t_DocKind = :PS_PAYORDER1 THEN :NOTEKIND_PSPO_EXPROCDATA " +
    "                   WHEN pmpaym.t_DocKind = :PS_INRQ1 THEN  :NOTEKIND_PSINRQ_EXPROCDATA" +
    "                END, " +
    "                :DateReq3), " +
    "             CHR (1)) " +
    "           AS Note " +
    "   FROM dpmpaym_dbt pmpaym " +
    "        INNER JOIN dpmhist_dbt pmhist " +
    "           ON     pmpaym.t_PaymentID = pmhist.t_PaymentID " +
    "              AND pmhist.t_date <= :DateReq4 " +
    "              AND pmhist.t_AutoKey = " +
    "                     (SELECT MIN (pmh.T_AutoKey) " +
    "                        FROM dpmhist_dbt pmh " +
    "                       WHERE     pmh.t_StatusIDTo IN " +
    "                                    (:PM_I2PLACED2, :PM_IWPPLACED2) " +
    "                             AND pmpaym.t_PaymentID = pmh.t_PaymentID) " +
    "        LEFT OUTER JOIN pmhist1 " +
    "           ON     pmpaym.t_PaymentID = pmhist1.t_PaymentID " +
    "              AND pmhist1.t_date > :DateReq5 " +
    "        INNER JOIN dpmrmprop_dbt pmrmprop " +
    "           ON pmrmprop.t_PaymentID = pmpaym.t_PaymentID " +
    "        INNER JOIN dpspayord_dbt pspayord " +
    "           ON     pmpaym.t_PaymentID = pspayord.t_OrderID " +
    "              AND (   (    pmpaym.t_Dockind = :PS_PAYORDER2 " +
    "                       AND pspayord.t_Dockind = :PSPOKIND_REQUEST) " +
    "                   OR pmrmprop.t_ShifrOper = '06' " +
    "                   OR pmpaym.t_Dockind = :PS_INRQ2) " +
    "        LEFT OUTER JOIN dnotetext_dbt notetext " +
    "           ON     notetext.t_DocumentID = LPAD (pmpaym.t_PaymentID, 10, '0') " +
    "              AND notetext.t_ObjectType = :OBJTYPE_PAYMENT1 " +
    "              AND notetext.t_notekind = :NOTEKIND_PM_INDEX_STOPDATE " +
    "              AND notetext.t_Date <= :DateReq6 " +
    "              AND notetext.t_Date > RsbSessionData.CurDate " +
    "  WHERE     pmpaym.t_FIID = 0 " +
    "        AND pmpaym.t_PayerAccount = :sAccount " +
    "        AND PM_INDEX.PaymentIsStopped (notetext.t_text, :DateReq7) = 0; ";
    
    var RequestParam = TArray;
    
    RequestParam[RequestParam.Size] = SQLParam("PM_I2PLACED1", PM_I2PLACED);
    RequestParam[RequestParam.Size] = SQLParam("PM_IWPPLACED1", PM_IWPPLACED);
    RequestParam[RequestParam.Size] = SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT);
    RequestParam[RequestParam.Size] = SQLParam("OBJ_PAYMENT_GROUP_ISSUER", OBJ_PAYMENT_GROUP_ISSUER);
    RequestParam[RequestParam.Size] = SQLParam("DateReq1", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("DateReq2", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("PS_PAYORDER", PS_PAYORDER);
    RequestParam[RequestParam.Size] = SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD);
    RequestParam[RequestParam.Size] = SQLParam("PS_INRQ", PS_INRQ);
    RequestParam[RequestParam.Size] = SQLParam("OBJTYPE_PSINRQ", OBJTYPE_PSINRQ);
    RequestParam[RequestParam.Size] = SQLParam("PS_PAYORDER1", PS_PAYORDER);
    RequestParam[RequestParam.Size] = SQLParam("NOTEKIND_PSPO_EXPROCDATA", NOTEKIND_PSPO_EXPROCDATA);
    RequestParam[RequestParam.Size] = SQLParam("PS_INRQ1", PS_INRQ);
    RequestParam[RequestParam.Size] = SQLParam("NOTEKIND_PSINRQ_EXPROCDATA", NOTEKIND_PSINRQ_EXPROCDATA);
    RequestParam[RequestParam.Size] = SQLParam("DateReq3", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("DateReq4", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("PM_I2PLACED2", PM_I2PLACED);
    RequestParam[RequestParam.Size] = SQLParam("PM_IWPPLACED2", PM_IWPPLACED);
    RequestParam[RequestParam.Size] = SQLParam("DateReq5", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("PS_PAYORDER2", PS_PAYORDER);
    RequestParam[RequestParam.Size] = SQLParam("PSPOKIND_REQUEST", PSPOKIND_REQUEST);
    RequestParam[RequestParam.Size] = SQLParam("PS_INRQ2", PS_INRQ);
    RequestParam[RequestParam.Size] = SQLParam("OBJTYPE_PAYMENT1", OBJTYPE_PAYMENT);
    RequestParam[RequestParam.Size] = SQLParam("NOTEKIND_PM_INDEX_STOPDATE", 40);
    RequestParam[RequestParam.Size] = SQLParam("DateReq6", {curdate});
    RequestParam[RequestParam.Size] = SQLParam("sAccount", searchParameters.Account);
    RequestParam[RequestParam.Size] = SQLParam("DateReq7", {curdate});
    
    var requests = execSQLselect(RequestQuery, RequestParam);
    
    while(requests and requests.MoveNext())
    
      var request = AccountRestriction;
      
      request.Type = "2";
    
      GetAccountRestriction(requests, PSPOREQUEST, @request.OrgName, @request.OrgINN, @request.Amount, @request.Date, @request.Priority, @request.IPNumber, @request.DocNumber, @request.OSPCode );
      
      response.AccountEncumbranceDatum[response.AccountEncumbranceDatum.Size] = request;
    end;
    
    response.RealDateTime = DtTm(Date(), Time());
    
    return response;

  end;  
end;
