/*
$Name:             pm061_10.mac
$Module:           Платежи
$Description:      Макрос шага
*/

//-----------------------------------------------------------------------------
// Блок      : 29061 - "Зачисление валютных средств резиденту"
// Шаг       : 10    - "Зачислить на транзитный счет?"
//-----------------------------------------------------------------------------

import PaymInter, BankInter, PSInter, CTInter, OprInter, "wltools.mac", cbsttls, pm_common;

var PaymentObj:RsbPayment;

PRIVATE CONST ACTION_STEP_UNDEF:integer = -1;
PRIVATE CONST ACTION_STEP_TRANSACCOUNT:integer = 0;
PRIVATE CONST ACTION_STEP_RECEIVERACCOUNT:integer = 1;
PRIVATE CONST SIZEOBJECTLEN = 36;
PRIVATE CONST VALID_TO_DATE = date(31,12,9999);

private const STR_ACTION_STEP_QUESTION = "Какое действие следует произвести с документом?";
private const STR_ACTION_STEP_TRANSACCOUNT = "Зачислить на транзитный счет";
private const STR_ACTION_STEP_RECEIVERACCOUNT = "Зачислить на счет получателя";

private macro ChooseActionStep():integer

  Array Buttons;

  var DialogFlag;

  var select_button:integer = ACTION_STEP_UNDEF;
  
  if(IsOprMultiExec)
    DialogFlag  = TSetDialogFlag(1);
  end;

  Buttons(ACTION_STEP_TRANSACCOUNT) = STR_ACTION_STEP_TRANSACCOUNT;
  Buttons(ACTION_STEP_RECEIVERACCOUNT) = STR_ACTION_STEP_RECEIVERACCOUNT;

  select_button = Menu( Buttons, STR_ACTION_STEP_QUESTION, NULL, NULL, NULL, 2);
  
  return select_button;
end;
                                                                         
// ----------------------------------------------------------------------------
// Выполнение шага
// ----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )

  var stat = 0;
  var ActionStep:integer = ACTION_STEP_UNDEF;
  var pmaddpi    = TRecHandler("pmaddpi.dbt");
  var DateNoChange   :date = date(0,0,0);
  var TypeAccount:string = "";
  var TransAccount:string = "";
  var TransChapter;
  var TransFIID;
  var DepID:integer = 0;
  var NameAccount:string = "";
  var rsacc:RsdRecordset;
  var IfNext, pi;
  
  ActionStep = ChooseActionStep();
  
  if( ActionStep == ACTION_STEP_TRANSACCOUNT )

    var AccountID:string = "";
    MakeAccountID( PaymentObj.Chapter, PaymentObj.PayerFIID, PaymentObj.ReceiverAccount, AccountID, SIZEOBJECTLEN );
    var query:string = "SELECT t_AttrID FROM dobjlink_dbt             " +
                       " WHERE t_ObjectType     = :OBJTYPE_ACCOUNT_OBJ  " +
                        "  AND t_GroupID        = :OBJROLE_ACC_TRANSIT  " +
                        "  AND t_AttrType       = :OBJTYPE_ACCOUNT_ATTR " +
                        "  AND t_ObjectID       = :AccountID            " +
                        "  AND t_ValidFromDate <= :VALID_TO_DATE_FROM   " +
                        "  AND t_ValidToDate   >= :VALID_TO_DATE_TO     ";

    var params:TArray = makeArray( SQLParam("OBJTYPE_ACCOUNT_OBJ", OBJTYPE_ACCOUNT    ), 
                                   SQLParam("OBJROLE_ACC_TRANSIT"  , OBJROLE_ACC_TRANSIT),
                                   SQLParam("OBJTYPE_ACCOUNT_ATTR" , OBJTYPE_ACCOUNT    ),
                                   SQLParam("AccountID"            , AccountID          ),
                                   SQLParam("VALID_TO_DATE_FROM"   , VALID_TO_DATE      ),
                                   SQLParam("VALID_TO_DATE_TO"     , VALID_TO_DATE      ) );

    var rs:RsdRecordset = execSQLselect( query, params, true );
  
    if( rs and rs.moveNext() )
      TransAccount = SubStr(rs.Value(0), 10);
      TransChapter = int(SubStr(rs.Value(0),1, 2));
      TransFIID = int(SubStr(rs.Value(0),3, 7));

      if( PaymentObj.FutureReceiverFIID != TransFIID )
        msgbox("Валюта счета получателя отличается от валюты транзитного счета");
        return 1;
      end;

      if( PaymentObj.Chapter != TransChapter )
        msgbox("Глава платежа отличается от главы транзитного счета");
        return 1;
      end;

      if( СчетСуществуетИОткрыт( TransFIID, TransAccount, TransChapter, NULL, TypeAccount, NULL, DateNoChange ) )
        
        query = "SELECT t_Department, t_NameAccount FROM daccount_dbt " +
                       " WHERE t_Account       = :Account " +
                        "  AND t_Code_Currency = :FIID    " +
                        "  AND t_Chapter       = :CHAPT1  ";

        params = makeArray( SQLParam("Account ", TransAccount           ), 
                            SQLParam("FIID"    , TransFIID              ),
                            SQLParam("CHAPT1"  , TransChapter           ) );

        rsacc = execSQLselect( query, params, true );
  
        if( rsacc and rsacc.moveNext() )
          DepID = rsacc.Value(0);
          NameAccount = rsacc.Value(1);
        end;

        if( Index(TypeAccount, "У") OR ( (DateNoChange != date(0,0,0)) AND (DateNoChange >= PaymentObj.OutTransferDate ) ))
          msgbox( "Запрещено зачисление на счет № " + TransAccount + " в филиале № " + DepID );
          return 1;
        end;

        if( PaymentObj.PIList( PRT_Credit ).Size > 0 )
          pi = TRecHandler( "pmaddpi.dbt" );   
          IfNext = PaymentObj.PIList( PRT_Credit ).First( pi );
        
           while( ( IfNext == 0 ) AND ( PaymentObj.PIList( PRT_Credit ).Current( pi ) == 0 ) )
             if( PaymentObj.PIList( PRT_Credit ).Delete( pi ) )
               MsgBox( "Ошибка при удалении разноски платежа" );
               return 1;
             end;
             IfNext = PaymentObj.PIList( PRT_Credit ).First( pi );
           end;
        end;

        pmaddpi.Rec.DebetCredit = PRT_Credit;
        pmaddpi.Rec.PMFIID      = PaymentObj.FuturePayerFIID;
        pmaddpi.Rec.PMAmount    = PaymentObj.FuturePayeramount;
        pmaddpi.Rec.FIID        = PaymentObj.FutureReceiverFIID;
        pmaddpi.Rec.Amount      = PaymentObj.FutureReceiverAmount;
        pmaddpi.Rec.Account     = TransAccount;
        pmaddpi.Rec.NameAccount = NameAccount;
        pmaddpi.Rec.Chapter     = PaymentObj.Chapter;
        pmaddpi.Rec.Ground      = PaymentObj.ground;
        pmaddpi.Rec.Oper        = {oper};
        pmaddpi.Rec.Department  = DepID;
      
        if( PaymentObj.PIList( PRT_Credit ).Insert( pmaddpi ) != 0 ) 
          MsgBox( "Ошибка при вставке разноски по счету комиссии" );
          return 1;
        end;
      else
        msgbox( "Не найден транзитный валютный счет резидента для зачисления иностранной валюты" );
        return 1;
      end;
    else
      msgbox( "Не найден транзитный валютный счет резидента для зачисления иностранной валюты" );
      return 1;
    end;

  elif( ActionStep == ACTION_STEP_RECEIVERACCOUNT )
    
    if( PaymentObj.PIList( PRT_Credit ).Size > 0 )
      IfNext = PaymentObj.PIList( PRT_Credit ).First();
      pi = TRecHandler( "pmaddpi.dbt" );
      
      while( (IfNext == 0) and (PaymentObj.PIList( PRT_Credit ).Current( pi ) == 0))
        
        if( СчетСуществуетИОткрыт( pi.rec.FIID, pi.rec.Account, CHAPT1, NULL, TypeAccount, NULL, DateNoChange ) )
          if( Index(TypeAccount, "У") OR ( (DateNoChange != date(0,0,0)) AND (DateNoChange >= PaymentObj.OutTransferDate )) )
            msgbox( "Запрещено зачисление на счет № " + pi.rec.Account + " в филиале № " + pi.rec.Department );
            return 1;
          end;
        end;
        IfNext = PaymentObj.PIList( PRT_Credit ).Next();
      end;

    else
      
      if( СчетСуществуетИОткрыт( PaymentObj.ReceiverFIID, PaymentObj.ReceiverAccount, CHAPT1, NULL, TypeAccount, NULL, DateNoChange ) )
        if( Index(TypeAccount, "У") OR ( (DateNoChange != date(0,0,0)) AND (DateNoChange >= PaymentObj.OutTransferDate )) )
          query = "SELECT t_Department FROM daccounts_dbt " +
                       " WHERE t_Account       = :Account " +
                        "  AND t_Code_Currency = :FIID    " +
                        "  AND t_Chapter       = :CHAPT1  ";

          params = makeArray( SQLParam("Account ", PaymentObj.ReceiverAccount ), 
                              SQLParam("FIID"    , PaymentObj.ReceiverFIID    ),
                              SQLParam("CHAPT1"  , CHAPT1                     ) );

          rsacc = execSQLselect( query, params, true );
  
          if( rsacc and rsacc.moveNext() )
            DepID = rsacc.Value(0);
          end;

          msgbox( "Запрещено зачисление на счет № " + PaymentObj.ReceiverAccount + " в филиале № " + DepID );
          return 1;
        end;
      end;

    end;
  
  elif( ActionStep == -2 )
    msgbox("Пользователь прервал выполнение операции.");
    return 1; 
  end;         
  PaymentObj.PaymStatus = PM_KVITPROCESSING;
  return 0;
end;                                                                                         
