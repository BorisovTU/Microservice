/*
 $Name: chkcode.mac
 $Module: Ядро ГКБО 
 $Description: функции проверки кода для субъекта
 */
 /*
	функции проверки кода для субъекта
 */
import  BankInter, PTInter, CTInter,"pt_lib.mac";

//record fininstr( fininstr );

/*
пример функции проверки кода для субъекта
private macro CheckCodeParty( pObject : RsbParty, pCodeKind : integer, pCode : string, pMode : integer )
end;

пример функции проверки кода для фининструмента
private macro CheckCodeFinInstr( pObject, pCodeKind : integer, pCode : string, pMode : integer )
  setbuff( fininstr, pObject );

end;

*/

private var DIGITSTR = "1234567890";

private macro ПроверитьКодФинИнструмента( pObject, pCodeKind : integer, pCode : string, pMode : integer )
  //setbuff( fininstr, pObject );

  return 0;
end;


macro ПроверитьКодСубъектаИНН( pObject : RsbParty, pCodeKind : integer, pCode : string, pMode : integer )
  var Code, KPP = "";
  var sep;
  var i, contr1, contr2, contr3;
  var LegalForm = 0;
  var IsNotResident = false;

  array ves, ves1, ves2;               

  ves(0) =  2;   ves1(0) =  7;   ves2(0) =  3;
  ves(1) =  4;   ves1(1) =  2;   ves2(1) =  7;
  ves(2) = 10;   ves1(2) =  4;   ves2(2) =  2;
  ves(3) =  3;   ves1(3) = 10;   ves2(3) =  4;
  ves(4) =  5;   ves1(4) =  3;   ves2(4) = 10;
  ves(5) =  9;   ves1(5) =  5;   ves2(5) =  3;
  ves(6) =  4;   ves1(6) =  9;   ves2(6) =  5;
  ves(7) =  6;   ves1(7) =  4;   ves2(7) =  9;
  ves(8) =  8;   ves1(8) =  6;   ves2(8) =  4;
                 ves1(9) =  8;   ves2(9) =  6;
                                 ves2(10) = 8;

  if(not(pObject == NULL))
    LegalForm     = pObject.LegalForm;
    IsNotResident = pObject.IsNotResident;
  else
    if( strlen( pCode ) == 12 ) // 12 символов для физического лица - резидента
      LegalForm = 2;
    else if(strlen( pCode ) == 10 ) // 10 символов для юридического лица - резидента 
      LegalForm = 1;
    else if(strlen( pCode ) == 5)  // 5 символов  для КИО 
       LegalForm = 1;
       IsNotResident = true;
       // return 0;  ??
    else if(strlen( pCode ) == 0)  // значение 0 также дает успешную проверку
       return 0;  
    else
       return 1577;
    end;
    end;
    end;    
    end;
  end; 

  if( strlen( pCode ) == 0 )
    return 0;
  end;
  
  sep = index( pCode, "/" );
  if( sep > 0 )
    Code = substr( pCode, 1, sep - 1 );
    KPP = substr( pCode, sep + 1 );
  else
    Code = pCode;
  end;

  if( LegalForm == 1 )
    if( IsNotResident == false )
      if( strlen( Code ) != 10 )
        /*Ошибка: ИНН юридического лица - нерезидента должен иметь длину 10 символов*/
        return 21315;
      end;
    else
      if( strlen( Code ) == 5 )
        if (Code == "00000")
            // Ошибка: КИО не должен состоять из символов '0'
            return 21369;
        end;
        return 0;
      else
        if( strlen( Code ) != 10 )
          /*Ошибка: ИНН юридического лица - нерезидента должен иметь длину 10 символов, КИО - 5 символов*/
          return 21314;
        end;
      end;
    end;

    i = 0;
    contr1 = 0;
    while( i < 9 )
      contr1 = contr1 + ves( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;
  elif ( LegalForm == 2 )
    if( strlen( Code ) != 12 )
      /*Ошибка: ИНН физического лица должен иметь длину 12 символов*/
      return 21317;
    end;

    i = 0;
    contr1 = 0;
    while( i < 10 )
      contr1 = contr1 + ves1( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;

    i = 0;
    contr1 = 0;
    while( i < 11 )
      contr1 = contr1 + ves2( i ) * int( substr( Code, i + 1, 1 ));
      i = i + 1;
    end;
    contr2 = floor( double( contr1 / 11 )) * 11;
    contr3 = mod( contr1 - contr2, 10 );
    if( int( substr( Code, i + 1, 1 )) != contr3 )
      /*Ошибка: ИНН задан неверно*/
      return 21316;
    end;
  end;
  
    if (SubStr(Code, 1, 2) == "00")
        // Ошибка: ИНН не должен начинаться с символов '00'
        return 21396;
    end;
    
    if (strlen(KPP) > 2)
        if (SubStr(KPP, 1, 2) == "00")
            // Ошибка: КПП не должен начинаться с символов '00'
            return 21397;
        end;
    end;

  return 0;
end;

private macro ПроверитьКодСубъектаОГРН( pObject : RsbParty, pCodeKind : integer, pCode : string, pMode : integer )
  var Code;
  var contr1, contr2, contr3, contr4;

    return 0;

  if( strlen( pCode ) == 0 )
    return 0;
  end;

  Code = pCode;
  var codelen = strlen( Code );

  if(not(pObject == NULL))
  if( pObject.LegalForm == 1 )
    contr4 = 11;
        if( codelen != 13 )
      /*Ошибка: ОГРН юридического лица должен иметь длину 13 символов*/
      return 21318;
    end;
  elif( pObject.LegalForm == 2 )
    contr4 = 13;
        if( codelen != 15 )
      /*Ошибка: ОГРН физического лица должен иметь длину 15 символов*/
      return 21319;
    end;
  end;
  else
    if (codelen == 13) // ОГРН юридического лица
        contr4 = 11;
    elif(codelen == 15) // ОГРН физического лица
        contr4 = 13;
    else
        return 21433; // !!!!
    end;
  end;
    
  contr1 = double( substr( Code, 1, strlen( Code ) - 1 ));
  contr2 = floor( double( substr( Code, 1, strlen( Code ) - 1 )) / contr4 ) * contr4;
  contr3 = mod( contr1 - contr2, 10 );
  
  if( int( substr( Code, strlen( Code ), 1 )) != contr3 )
    /*Ошибка: ОГРН задан неверно*/
    return 21323;
  end;

  return 0;
end;
private macro ПроверитьКодСубъектаСНИЛС( pObject : RsbParty, pCodeKind : integer, pCode : string, pMode : integer )
  var stat = 0;
  var Code = pCode;
  var KK = "";
  var Digit = 0;
  var BreakCheck = false; 

  if(pObject.LegalForm == 1)
    stat = 19032; // 'Ошибка: СНИЛС имеет значение только для физических лиц'
  end;

  if((stat == 0) AND (strlen(Code) == 0))
    var ObjAlReg = TBFile("objalreg.dbt", "R", 1 );

    ObjAlReg.Clear();
    ObjAlReg.rec.ObjectType   = OBJTYPE_PARTY;
    ObjAlReg.rec.RegPartyKind = PTLIST_PENSFOND;
    ObjAlReg.rec.RegDocKind   = OBJKDOC_CERTIFICATE_OF_INSURANCE;
    ObjAlReg.rec.PartyID      = 0;

    if( ObjAlReg.GetEQ() )
      if(ObjAlReg.rec.CodeIsNotRequired == "X")
        BreakCheck = true; 
      end;
    end;
  end;

  if((stat == 0) AND (BreakCheck == false))
    // Изъять из строки пробельные символы (пробелы, табуляции).
    Code = StrSubst ( Code, " ", "" );

    if(strlen(Code)<2)
      stat = 19033; // 'Ошибка: проверяемый код имеет формат, отличный от XXX-XXX-XXX KK, где символами Х и K обозначены цифровые позиции'
    else
      // Контрольное число - два крайних правых символа - вынести в отдельную числовую переменную.
      KK = SubStr ( Code, strlen(Code)-1);
      Code = Trim(SubStr ( Code, 1, strlen(Code)-2));

      if( (Index(DIGITSTR, SubStr(KK, 1,1)) == 0) OR ( Index(DIGITSTR, SubStr(KK, 2,1) ) == 0)   )
        stat = 19033; // 'Ошибка: проверяемый код имеет формат, отличный от XXX-XXX-XXX KK, где символами Х и K обозначены цифровые позиции'
      end;
    end;
  end;
 
  if((stat == 0) AND (BreakCheck == false))
    var i = 1;
    var ch = "";
    var tmpCode = "";
    while (i <= strlen(Code) )
      ch = SubStr(Code, i,1);

      if(Index(DIGITSTR, ch) > 0)
        tmpCode = tmpCode + ch;
      end;
      i = i + 1;
    end;

    Code = tmpCode;

    // Если значение имеет длину, отличную от 9 разрядов
    if(strlen(Code) != 9)
      stat = 19033; // 'Ошибка: проверяемый код имеет формат, отличный от XXX-XXX-XXX KK, где символами Х и K обозначены цифровые позиции'
    end;
  end;

  if((stat == 0) AND (BreakCheck == false))
    // Если значение меньше или равно числу 001001998 - выйти из процедуры с успехом: эти значения не проверяются.
    if(Int(Code) > 001001998 )
      // Умножить каждую цифру числа на номер её разряда и просуммировать полученные значения.
      Digit = 0;
      i = 1;
      var Len = strlen(Code);
      while (i <= Len )
        ch = SubStr(Code, i,1);
        Digit = Digit + (Int(ch) * (Len-i + 1) );
        i = i + 1;
      end;

      // Если полученная сумма равна 100 или 101, считать проверяемым числом '00' (то есть 0)
      if((Digit == 100) OR (Digit == 101) ) 
        Digit = 0;
      end;

      // Если полученная сумма больше 101, разделить её нацело на 101, остаток от деления считать проверяемым числом
      if(Digit > 101) 
        Digit = mod(Digit, 101);
      end;

      if(Digit != int(KK))
        stat = 19034; // 'Ошибка: СНИЛС задан неверно'
      end;
    end;
  end;

  return stat;
end;