import cb_sql, rsd, globals, commonutil, likepy, oralib;
 
/* Атрибуты балансового счета */
class TWsPortBalance
  var iNumPlan        : integer;/*Номер плана*/
  var Balance         : string; /*Номер балансового счета*/
  var Kind_Account    : string; /*Вид счета*/
  var Type_Balance    : string; /*Тип счета*/
  var Part            : integer;/*Номер раздела*/
  var Name_Balance    : string; /*Название счета*/
  var PairBalance     : string; /*Парный балансовый счет*/
  var UserField1      : string; /*Пользовательское поле 1*/
  var UserField2      : string; /*Пользовательское поле 2*/
  var UserField3      : string; /*Пользовательское поле 3*/
  var UserField4      : string; /*Пользовательское поле 4*/
  var BottomRange     : integer;/*Нижняя граница срока*/
  var TopRange        : integer;/*Верхняя граница срока*/
  var AccountBlncMove : string; /*Балансовый счет в переносе*/
  var InMove          : string; /*Признак участия в переносе*/
  var Chapter         : integer;/*Глава счетов*/
  var ExtraType       : string; /*Контрактив и т.п.*/
  var cBlncUsedBWP    : string; /*Признак наличия счета в  РПС*/
  var bdIncludeBWP    : date;   /*Дата включения  счета в  РПС*/
  var bdExcludeBWP    : date;   /*Дата исключения счета из РПС*/

  iNumPlan = 0;
  Balance = "";
  Kind_Account = "";
  Type_Balance = "";
  Part = 0;
  Name_Balance = "";
  PairBalance = "";
  UserField1 = "";
  UserField2 = "";
  UserField3 = "";
  UserField4 = "";
  BottomRange = 0;
  TopRange = 0;
  AccountBlncMove = "";
  InMove = "";
  Chapter = 0;
  ExtraType = "";
  cBlncUsedBWP = "";
  bdIncludeBWP = date(0,0,0); 
  bdExcludeBWP = date(0,0,0); 

end;

/*Атрибуты главы счетов */
class TWsPortOBChapter
  var Chapter               : integer;/*Номер главы счетов*/
  var Name                  : string; /*Полное название главы*/
  var ShortName             : string; /*Короткое название главы*/
  var Symbol                : string; /*Символ главы*/
  var ORScheme              : integer;/*Схема переоценки*/
  var ExcludeFromFormalAcnt : string; /*Признак исключения главы из официального учета*/
  var Locked                : string; /*Является ли глава используемой*/
  var ContrBalA             : string; /*Контрсчет для корреспонденции с пассивными счетами*/
  var ContrBalP             : string; /*Контрсчет для корреспонденции с активными счетами*/
  var ContrBalNumPlan       : integer;/*Номер плана контрсчетов*/
  var AccountKey            : integer;/*Признак обязательности ключевания счетов при вводе*/
  var ListNatCurBalance     : TArray; /*<TWsPortBalance> Список рублевых балансовых, включенных в главу*/
  var ListFrnCurBalance     : TArray; /*<TWsPortBalance> Список валютных балансовых, включенных в главу*/

  Chapter = 0;
  Name = "";
  ShortName = "";
  Symbol = "";
  ORScheme = 0;
  ExcludeFromFormalAcnt = "";
  Locked = "";
  ContrBalA = "";
  ContrBalP = "";
  ContrBalNumPlan = 0;
  AccountKey = 0;
  ListNatCurBalance = TArray; 
  ListFrnCurBalance = TArray; 

end;

/* Атрибуты плана счетов*/ 
class TWsPortNamePlan
  var iNumPlan      : integer; /* Номер плана счетов*/
  var cNamePlan     : string;  /* Название плана*/
  var ListOBChapter : TArray;  /*<TWsPortOBChapter> Список глав, включенных в план*/

  iNumPlan = 0;
  cNamePlan = "";
  ListOBChapter = TArray; 
end;

private macro GetPortBalance(iNumPlan, Chapter, ListPortBalance : TArray, flag)
  var query;
  var rs;
  var params : TArray;
  var wsPortBalance : TWsPortBalance;
  var i = 0; /*это для проверки ошибки на больших объемах xml данных*/

  CaptureOutput;
[
   SELECT t_iNumPlan,
          t_Balance,
          t_Kind_Account,
          t_Type_Balance,
          t_Part,
          t_Name_Part,
          t_PairBalance,
          t_UserField1,
          t_UserField2,
          t_UserField3,
          t_UserField4,
          t_BottomRange,
          t_TopRange,
          t_AccountBlncMove,
          t_InMove,
          t_Chapter,
          t_ExtraType,
          t_cBlncUsedBWP,
          t_bdInclUdeBWP,
          t_bdExclUdeBWP
     FROM dbalance_dbt 
    WHERE t_iNumPlan = :iNumPlan 
      AND t_Chapter  = :Chapter
];
  query = StopCaptureOutput;

  CaptureOutput;
  if(flag == true)
[
      AND t_FINational = 'X'
];
  else
[
      AND t_FIForeign  = 'X'
      AND t_FIMetall   = 'X'
      AND t_FISecurity = 'X'
];
  end;
  query = query + " " + StopCaptureOutput;

  params = makeArray( SQLParam("iNumPlan", iNumPlan)
                     ,SQLParam("Chapter" , Chapter )
                    );
  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    wsPortBalance = TWsPortBalance;

    CU_CopyRecordsetToClassObj(rs, wsPortBalance);

    ListPortBalance[ListPortBalance.size] = wsPortBalance;

    //i = i + 1; if(i > 5) break; end;
  end;
end;



private macro GetPortOBChapter(iNumPlan, ListPortOBChapter : TArray)
  var query;
  var rs;
  var params : TArray;
  var wsPortOBChapter : TWsPortOBChapter;                                
  CaptureOutput;
[
SELECT chaptr.t_Chapter,
       chaptr.t_Name,
       chaptr.t_ShortName,
       chaptr.t_Symbol,
       chaptr.t_Orscheme,
       chaptr.t_Excludefromformalacnt,
       chaptr.t_Locked,
       chaptr.t_ContrBalA,
       chaptr.t_ContrBalP,
       chaptr.t_ContrBalNumPlan,
       chaptr.t_AccountKey
  FROM dobchaptr_dbt chaptr, dnameplan_chaptr_dbt np
 WHERE np.t_iNumPlan = :iNumPlan 
   AND np.t_Chapter  = chaptr.t_Chapter
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("iNumPlan", iNumPlan)
                    );
  rs = execSQLselect(query, params, true);
  //if(rs and rs.moveNext())
  while(rs and rs.moveNext())
    wsPortOBChapter = TWsPortOBChapter;

    CU_CopyRecordsetToClassObj(rs, wsPortOBChapter);    

    ListPortOBChapter[ListPortOBChapter.size] = wsPortOBChapter;

  GetPortBalance(iNumPlan, wsPortOBChapter.Chapter, wsPortOBChapter.ListNatCurBalance, true);
  GetPortBalance(iNumPlan, wsPortOBChapter.Chapter, wsPortOBChapter.ListFrnCurBalance, false); 
  end;

end;


private macro GetPortNamePlan(wsPortNamePlan : TWsPortNamePlan)
  var query;
  var rs;
  var params : TArray;
 
  CaptureOutput;
[
SELECT t_NamePlan
  FROM dnameplan_dbt
 WHERE t_iNumPlan = :iNumPlan
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("iNumPlan", wsPortNamePlan.iNumPlan)
                    );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    wsPortNamePlan.cNamePlan = rs.value(0);
  else
    RunError("План " + string(wsPortNamePlan.iNumPlan) + " не найден.");
  end;

  GetPortOBChapter(wsPortNamePlan.iNumPlan, wsPortNamePlan.ListOBChapter);

end;

/* Сервис GetNamePlan - возвращает атрибуты плана, включенных в план глав и балансовых счетов.

   Параметры: 
     OnDate   - Дата актуальности справочника
     iNumPlan - Номер плана счетов
*/
  
macro GetNamePlan( OnDate, iNumPlan )

  var ErrMessage : string;
  var wsPortNamePlan : TWsPortNamePlan;
      wsPortNamePlan = TWsPortNamePlan;

  //var i = 0;
  //while(i < 100000000)
  //  i = i + 1;
  //end;

  wsPortNamePlan.iNumPlan = 0;
  if((iNumPlan != null) and (ValType(iNumPlan) != V_UNDEF))
    wsPortNamePlan.iNumPlan = iNumPlan;
  end;
  GetPortNamePlan(wsPortNamePlan);

 return wsPortNamePlan;
end;