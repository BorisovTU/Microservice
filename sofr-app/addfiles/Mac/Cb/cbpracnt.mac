/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : cbpracnt.mac                                       */
/*                                                                      */
/*  Описание       : Вызов печати счета к оплате                        */
/*                                                                      */
/*  Программист    : AAV                                                */
/*                                                                      */
/*  Создан         : 21.11.02                                           */
/*                                                                      */
/************************************************************************/

import prpm, SFInter, oralib, likepy;

file f_oprsfcom( oprsfcom ) key 0;
file f_sfdefcom(sfdefcom)   key 0;

MACRO PrintDocument(ncopy:integer):bool
  
  var payer:string, receiver:string, ground:string;
  var amount:money = 0, amountNDS:money = 0;
  var contr:string = "Договор обслуживания не найден";
  var select:string;
  var params:TArray;
  var rs:object;
  ARRAY SG, SP, SR, SC;

  /* Плательщик */
  payer = pr_pmrmprop.rec.PayerName;
  if( pr_pmrmprop.rec.PayerINN != "" )
    payer =  payer + " ИНН " + pr_pmrmprop.rec.PayerINN;
  end;
  payer =  payer + "\nР/cчет: " + pr_pmpaym.rec.PayerAccount;
  if( pr_pmpaym.rec.PayerBankID )
    payer =  payer + " в банке " + pr_pmrmprop.rec.PayerBankName;
  end;

  /* Получатель */
  receiver = pr_pmrmprop.rec.ReceiverName;
  if( pr_pmrmprop.rec.ReceiverINN != "" )
    receiver =  receiver + " ИНН " + pr_pmrmprop.rec.ReceiverINN;
  end;
  receiver =  receiver + "\nР/cчет: " + pr_pmpaym.rec.ReceiverAccount;
  if( pr_pmpaym.rec.ReceiverBankID )
    receiver =  receiver + " в банке " + pr_pmrmprop.rec.ReceiverBankName;
  end;

  /* Если задана ссылка на единовременную комиссию */
  if( pr_pmpaym.rec.defComID AND (pr_pmpaym.rec.feeType == SF_FEE_TYPE_SINGLE) )
    f_oprsfcom.ID = pr_pmpaym.rec.defComID;
    if( getEQ( f_oprsfcom ) )
      amount = f_OprSfCom.Sum;
      amountNDS = f_OprSfCom.SumNDS;
    end;
  else
    /* периодическая комиссия */
    if(pr_pmpaym.rec.feeType == SF_FEE_TYPE_PERIOD)
      f_sfdefcom.ID = pr_pmpaym.rec.defComID;
      if( getEQ(f_sfdefcom) )
      end;
    end;

    amount = pr_pmpaym.rec.PayAmount;
    /* При печати из панели ввода поле PayAmount еще не проинициализировано, поэтому для печати исаользуем Amount */
    if( (amount == $0) and (pr_pmpaym.rec.PaymentID == 0) and (pr_pmpaym.rec.FIID == pr_pmpaym.rec.PayFIID) )
      amount = pr_pmpaym.rec.Amount;
    end;
  end;

  /* Если платеж является частичной оплатой картотеки 2, то суммы берем из платежа частичной оплаты */
  if( (pr_pmpaym.rec.DocKind == PS_PAYORDER) and (pr_pmpaym.rec.PartPaymNumber > 0) )
    amount    = pr_pmpaym.rec.PayAmount;
    AmountNDS = pr_pmpaym.rec.AmountNDS;
  end;

  /* Получим договор по счету плательщика*/
  select = "SELECT t_Name, t_Number, t_DateBegin "   +
           "FROM dsfcontr_dbt sc "                   +
           "WHERE sc.t_ObjectType = :objtype "       +
           "  AND sc.t_ServKind   = :servk "         +
           "  AND sc.t_FIID       = :fiid "          +
           "  AND sc.t_Object     = '"+pr_pmpaym.rec.PayerAccount + "'";
  params = makeArray( SQLParam("objtype", SF_ACCOUNT),
                      SQLParam("servk",   PTSK_PAY),
                      SQLParam("fiid",    NATCUR));
  rs = execSQLselect( select, params, TRUE );
  if(rs and rs.moveNext())
    contr = String( "Согласно документу: \"", rs.Value(0), "\" № ", rs.Value(1), " от ", rs.Value(2) );
  end;

  /* Наименование выполненной услуги */
  ground = pr_pmrmprop.rec.Ground;

  strsplit( payer,    SP,  44, 44, 6 );
  strsplit( receiver, SR,  44, 44, 6 );
  strsplit( ground,   SG,  24, 24, 6 );
  strsplit( contr,    SC,  22, 25, 6 );

  while(ncopy != 0)
    [

                       СЧЕТ К ОПЛАТЕ N ################   от ##########


                  ПЛАТЕЛЬЩИК                                       ПОЛУЧАТЕЛЬ

      ############################################  ############################################
      ############################################  ############################################
      ############################################  ############################################
      ############################################  ############################################
      ############################################  ############################################
      ############################################  ############################################

      ┌────────────────────────┬─────────────┬─────────────┬─────────────┬─────────────────────────┐
      │      Наименование      │    Сумма    │    Сумма    │ Итого сумма │        Основание        │
      │         услуги         │             │     НДС     │   с НДС     │                         │
      ├────────────────────────┼─────────────┼─────────────┼─────────────┼─────────────────────────┤
      │########################│#############│#############│#############│#########################│
      │########################│             │             │             │#########################│
      │########################│             │             │             │#########################│
      │########################│             │             │             │#########################│
      │########################│             │             │             │#########################│
      │########################│             │             │             │#########################│
      └────────────────────────┴─────────────┴─────────────┴─────────────┴─────────────────────────┘


                Подпись                                 ##########
                                                        ──────────
                  М.П.                                    (Дата)

    
    ]
     (pr_pmrmprop.rec.Number:l, replace(string(pr_pmpaym.rec.ValueDate:f), "-", "."):c,
      SP(0):l, SR(0):l,
      SP(1):l, SR(1):l,
      SP(2):l, SR(2):l,
      SP(3):l, SR(3):l,
      SP(4):l, SR(4):l,
      SP(5):l, SR(5):l,
      SG(0):l, (amount-amountNDS):r:a, amountNDS:r:a, amount:r:a, SC(0),
      SG(1):l, SC(1),
      SG(2):l, SC(2),
      SG(3):l, SC(3),
      SG(4):l, SC(4),
      SG(5):l, SC(5),
      replace(string(date():f), "-", "."):c );

    ncopy = ncopy - 1;
  end;

  return TRUE;

END;
