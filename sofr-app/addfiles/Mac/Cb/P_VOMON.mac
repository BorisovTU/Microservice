/*
  $Name:         P_VOMON.mac
  $Module:       РКО
  $Description:  Панель параметров отбора документов для мониторинга
*/

import Календарь;
import globals, FIInter, PaymInter, BankInter;

RECORD R_VOMONPANEL (P_VOMON, "bank.lbr") dialog;
RECORD R_FININSTR (fininstr);   /* для "листалки" финансовых инструментов */
FILE   F_FININSTR (fininstr);   /* справочник финансовых инструментов */

var ZeroDate = date(0,0,0);

var MenuChoose = 0;

array MenuSelect;
MenuSelect(0) = "";
MenuSelect(1) = "X";

array MenuFIID;
MenuFIID(0) = "";
MenuFIID(1) = "-";
MenuFIID(2) = "!";

array MenuAmount;
MenuAmount(0) = "";
MenuAmount(1) = "-";
MenuAmount(2) = "!";
MenuAmount(3) = ">";
MenuAmount(4) = "<";
MenuAmount(5) = "}";
MenuAmount(6) = "{";

private macro GetFIByCode (FI_Code)
  var stat = false;
  keynum (F_FININSTR, 1);
  F_FININSTR.FI_Code = Trim (FI_Code);
  if ( GetEQ(F_FININSTR) )
    stat = true;
  end;
  return stat;
end;

private macro UpdateCurrency(dlg_rec)
  DisableFields(dlg_rec, fldindex(dlg_rec, "CodeCurrencyName"));
  
  if ( dlg_rec.UseFIID == "")
    DisableFields(dlg_rec, fldindex(dlg_rec, "CodeCurrency"));
  else
    EnableFields(dlg_rec, fldindex(dlg_rec, "CodeCurrency"));
  end;   

  if (not GetFIByCode(dlg_rec.CodeCurrency)) 
    dlg_rec.CodeCurrencyName = "Все валюты";
    dlg_rec.CodeCurrency = "";
  else
    dlg_rec.CodeCurrencyName = F_FININSTR.Name;
  end;
end;

private macro UpdateAmount(dlg_rec)
  
  if ( dlg_rec.UseAmount == "")
    DisableFields(dlg_rec, fldindex(dlg_rec, "Amount"));
  else
    EnableFields(dlg_rec, fldindex(dlg_rec, "Amount"));
  end;   

end;

private macro SpaceSelect(charSelect)
  var v_char = MenuSelect(0);
  if(charSelect == MenuSelect(0))
    v_char = MenuSelect(1);
  end;
  return v_char;
end;

private macro SpaceFIID(charFIID)
  var v_char = MenuFIID(0);
  if(charFIID == MenuFIID(0))
    v_char = MenuFIID(1);
  elif(charFIID == MenuFIID(1))
    v_char = MenuFIID(2);
  end;
  return v_char;
end;

private macro SpaceAmount(charAmount)
  var v_char = MenuAmount(0);
  if(charAmount == MenuAmount(0))
    v_char = MenuAmount(1);
  elif(charAmount == MenuAmount(1))
    v_char = MenuAmount(2);
  elif(charAmount == MenuAmount(2))
    v_char = MenuAmount(3);
  elif(charAmount == MenuAmount(3))
    v_char = MenuAmount(4);
  elif(charAmount == MenuAmount(4))
    v_char = MenuAmount(5);
  elif(charAmount == MenuAmount(5))
    v_char = MenuAmount(6);
  end;
  return v_char;
end;

private macro Init(dlg_rec)
  var stat;
  ClearRecord(dlg_rec);
  
  dlg_rec.Select0   = "X";
  dlg_rec.Select1   = "X";
  dlg_rec.Select2   = "";
  dlg_rec.Select3   = "";
  dlg_rec.DateFrom  = {curdate};
  dlg_rec.DateTo    = {curdate};
  dlg_rec.UseFIID   = "";
  dlg_rec.CodeCurrency = "";
  UpdateCurrency(dlg_rec);
  dlg_rec.UseAmount = "";
  dlg_rec.Amount    = 0;
  UpdateAmount(dlg_rec);
end;

private macro RunSelect(dlg_rec)

  private const RegPath173FZAll :string = "PS\\REQOPENACC\\173-ФЗ счета нерезидентов\\Все счета";
  var stat = 0;
  var MaskAccounts;
  
  getRegistryValue(RegPath173FZAll, V_STRING, MaskAccounts, stat);

  if (stat != 0)
    MaskAccounts = "";
  end;

  var SqlLink = " (1 = 1)";
  var SqlLinkSelectCheck = " and ( (1 = 0)";
  var SubStr = "";
  GetFIByCode(dlg_rec.CodeCurrency);
  
  macro AddAndStr(str)
    SqlLink = SqlLink + " and " + str;
  end;

  if ( dlg_rec.DateFrom != ZeroDate )
    AddAndStr(" pmpaym.t_ValueDate >= to_date( '" + dlg_rec.DateFrom + " ', 'dd.mm.yyyy' )");
  end;

  if ( dlg_rec.DateTo != ZeroDate )
    AddAndStr(" pmpaym.t_ValueDate <= to_date( '" + dlg_rec.DateTo + " ', 'dd.mm.yyyy' )");
  end;
  
  if ( dlg_rec.UseFIID == MenuFIID(1) )
    AddAndStr(" pmpaym.t_BaseFIID = " + F_FININSTR.FIID);
  elif ( dlg_rec.UseFIID == MenuFIID(2) )
    AddAndStr(" pmpaym.t_BaseFIID <> " + F_FININSTR.FIID);
  end;
  
  if ( dlg_rec.UseAmount == MenuAmount(1) )
    AddAndStr(" pmpaym.t_BaseAmount = " + dlg_rec.Amount);
  elif ( dlg_rec.UseAmount == MenuAmount(2) )
    AddAndStr(" pmpaym.t_BaseAmount <> " + dlg_rec.Amount);
  elif ( dlg_rec.UseAmount == MenuAmount(3) )
    AddAndStr(" pmpaym.t_BaseAmount > " + dlg_rec.Amount);
  elif ( dlg_rec.UseAmount == MenuAmount(4) )
    AddAndStr(" pmpaym.t_BaseAmount < " + dlg_rec.Amount);
  elif ( dlg_rec.UseAmount == MenuAmount(5) )
    AddAndStr(" pmpaym.t_BaseAmount >= " + dlg_rec.Amount);
  elif ( dlg_rec.UseAmount == MenuAmount(6) )
    AddAndStr(" pmpaym.t_BaseAmount <= " + dlg_rec.Amount);
  end;

  
  if ( dlg_rec.Select0 == "X" )
    SqlLinkSelectCheck = SqlLinkSelectCheck + " or ";
    SubStr = " ( pmpaym.t_PaymentID = pmcurtr.t_paymentID and pmcurtr.t_IsVO = 'X' ) ";
    SqlLinkSelectCheck = SqlLinkSelectCheck + SubStr;
  end;

  if ( dlg_rec.Select1 == "X" )
    SqlLinkSelectCheck = SqlLinkSelectCheck + " or ";
    SubStr = " (  pmpaym.t_FIID <> " + NATCUR +
             " or pmpaym.t_PayFIID <> " + NATCUR +
             " or pmpaym.t_BaseFIID <> " + NATCUR +
             " or exists ( select 1 from dpmaddpi_dbt pma where pma.t_PaymentID = pmpaym.t_PaymentID and pma.t_FIID <> " + NATCUR + " )" +
             " or exists ( select 1 from dparty_dbt pt where pt.t_partyID = pmpaym.t_payer and pt.t_notresident = 'X' )" +
             " or exists ( select 1 from dparty_dbt pt where pt.t_partyID = pmpaym.t_PayerBankID and pt.t_notresident = 'X' )" +
             " or exists ( select 1 from dparty_dbt pt where pt.t_partyID = pmpaym.t_receiver and pt.t_notresident = 'X' )" +
             " or exists ( select 1 from dparty_dbt pt where pt.t_partyID = pmpaym.t_receiverBankID and pt.t_notresident = 'X' )" +
             " or exists ( select 1 from dpmaddpi_dbt pma, dparty_dbt pt where pma.t_PaymentID = pmpaym.t_PaymentID and pma.t_AccountClient = pt.t_partyID and pt.t_notresident = 'X' )"  +
             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', pmpaym.t_PayerAccount) = 1" +
             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', pmpaym.t_ReceiverAccount) = 1" +
             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', " +
                                                    "( select  cor.t_Account from dcorschem_dbt cor, dpmprop_dbt pmprop " + 
                                                    "  where pmprop.t_PaymentID = pmpaym.t_PaymentID and pmprop.t_DebetCredit = 0 " +
                                                    "   and cor.t_Number = pmprop.t_Corschem and cor.t_FIID = pmprop.t_PayFIID ) ) = 1" +
             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', " +
                                                    "( select  cor.t_Account from dcorschem_dbt cor, dpmprop_dbt pmprop " + 
                                                    "  where pmprop.t_PaymentID = pmpaym.t_PaymentID and pmprop.t_DebetCredit = 1 " +
                                                    "   and cor.t_Number = pmprop.t_Corschem and cor.t_FIID = pmprop.t_PayFIID ) ) = 1" +   
             " ) "              
             ; 
    SqlLinkSelectCheck = SqlLinkSelectCheck + SubStr;
  end;

  if ( dlg_rec.Select2 == "X" )
    SqlLinkSelectCheck = SqlLinkSelectCheck + " or ";
    SubStr = " exists ( select 1 from dacctrn_dbt acctrn, dpmdocs_dbt pmdocs " +
                        " where pmpaym.t_PaymentID = pmdocs.t_PaymentID " +
                        " and pmdocs.t_AcctrnID = acctrn.t_AcctrnID " +
                        " and acctrn.t_State = " + TRN_STATE_FACT +
                        " and (acctrn.t_FIID_Payer <> " + NATCUR + 
                             " or acctrn.t_FIID_Receiver <> " + NATCUR + 
                             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', acctrn.t_Account_Payer) = 1"  +
                             " or rsb_mask.compareStringWithMask('" + MaskAccounts + "', acctrn.t_Account_Receiver) = 1" +
                             " )";
                        
    if ( dlg_rec.DateFrom != ZeroDate )
      SubStr = SubStr + " and acctrn.t_Date_Carry >= to_date( '" + dlg_rec.DateFrom + " ', 'dd.mm.yyyy' )";
    end;
    
    if ( dlg_rec.DateTo != ZeroDate )
      SubStr = SubStr + " and acctrn.t_Date_Carry <= to_date( '" + dlg_rec.DateTo + " ', 'dd.mm.yyyy' )";
    end;
   
    if ( dlg_rec.UseAmount == MenuAmount(1) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer = " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver = " + dlg_rec.Amount;
    elif ( dlg_rec.UseAmount == MenuAmount(2) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer <> " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver <> " + dlg_rec.Amount;
    elif ( dlg_rec.UseAmount == MenuAmount(3) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer > " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver > " + dlg_rec.Amount;
    elif ( dlg_rec.UseAmount == MenuAmount(4) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer < " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver < " + dlg_rec.Amount;
    elif ( dlg_rec.UseAmount == MenuAmount(5) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer >= " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver >= " + dlg_rec.Amount;
    elif ( dlg_rec.UseAmount == MenuAmount(6) )
      SubStr = SubStr + " and acctrn.t_Sum_Payer <= " + dlg_rec.Amount;
      SubStr = SubStr + " and acctrn.t_Sum_Receiver <= " + dlg_rec.Amount;
    end;
   
    SubStr = SubStr + " ) ";
    SqlLinkSelectCheck = SqlLinkSelectCheck + SubStr;
  end;

  if ( dlg_rec.Select3 == "X" )
    SqlLinkSelectCheck = SqlLinkSelectCheck + " or ";
    SubStr = " ( Trim(pmrmprop.t_ground) like '{VO%') ";
    SqlLinkSelectCheck = SqlLinkSelectCheck + SubStr;
  end;

  SqlLinkSelectCheck = SqlLinkSelectCheck + " ) ";
  SqlLink = SqlLink + SqlLinkSelectCheck;
  
  BB_COControlScrol("6",SqlLink);
 
end;

macro EventPan(dlg, cmd, id, key)

  if(CMD == DLG_INIT)
    Init(dlg);
    UpdateFields(dlg);

//  elif(CMD == DLG_SETFOCUS)
 
  elif(CMD == DLG_REMFOCUS)
    if((trim(fldname(dlg, id)) == "CodeCurrency"))
      UpdateCurrency(dlg);
    end;
    UpdateFields(dlg);
    
  elif((CMD == DLG_KEY) and (key == 32)) //K_SPACE
    if(trim(fldname(dlg, id)) == "Select0")
      dlg.Select0 = SpaceSelect(dlg.Select0);
    elif(trim(fldname(dlg, id)) == "Select1")
      dlg.Select1 = SpaceSelect(dlg.Select1);
    elif(trim(fldname(dlg, id)) == "Select2")
      dlg.Select2 = SpaceSelect(dlg.Select2);
    elif(trim(fldname(dlg, id)) == "Select3")
      dlg.Select3 = SpaceSelect(dlg.Select3);
    elif(trim(fldname(dlg, id)) == "UseFIID")
      dlg.UseFIID = SpaceFIID(dlg.UseFIID);
      UpdateCurrency(dlg);
    elif(trim(fldname(dlg, id)) == "UseAmount")
      dlg.UseAmount = SpaceAmount(dlg.UseAmount);
      UpdateCurrency(dlg);
      UpdateAmount(dlg);
    end;
    UpdateFields(dlg);

  elif((CMD == DLG_KEY) and (key == 317)) //K_F3
    if(trim(fldname(dlg, id)) == "DateFrom")
      dlg.DateFrom = GetDateByCalendar( dlg.DateFrom );

    elif( trim(fldname(dlg, id)) == "DateTo")
      dlg.DateTo = GetDateByCalendar( dlg.DateTo );
      
    elif(trim(fldname(dlg, id)) == "CodeCurrency")
      if (ListFI (FIKIND_CURRENCY, 0, R_FININSTR))
        dlg.CodeCurrency     = R_FININSTR.FI_Code; /* польз. код валюты (не ID) */
        dlg.CodeCurrencyName = R_FININSTR.Name;
      end;
    elif(trim(fldname(dlg, id)) == "UseFIID")
      MenuChoose = Menu(MenuFIID);
      if ( MenuChoose >= 0 )      
        dlg.UseFIID = MenuFIID(MenuChoose);
      end;
      UpdateCurrency(dlg);
    elif(trim(fldname(dlg, id)) == "UseAmount")  
      MenuChoose = Menu(MenuAmount);   
      if ( MenuChoose >= 0 )      
        dlg.UseAmount = MenuAmount(MenuChoose);
      end;
      UpdateAmount(dlg);
    end;
    UpdateFields(dlg);
  elif((CMD == DLG_KEY) and (key == 316)) //K_F2
    RunSelect(dlg);

//  elif(CMD == DLG_SAVE)

  else
    return CM_DEFAULT;
  end;
end;

RunDialog(R_VOMONPANEL, "EventPan");
exit (1);
