/**
 @file PrintTrnOut.mac
 @brief Выгрузка проводок из журнала

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |           |               |                                                |

*/

import OprInter, "or_rep_h.mac", rsd, RsbDataSet, FIInter, dlmisc;

private const TRN_PROHIBITED = 1,
              TRN_BLOCKED = 2;
PRIVATE CONST POI_MODE = TRUE;
PRIVATE CLASS (CB_CReportTemplate) Trn_Out(v_KindTrn, v_FromStr, v_WhereStr)
  var KindTrn = v_KindTrn, FromStr = v_FromStr, WhereStr = v_WhereStr;
  var IsFirst = true;
  var m_CurrSheetName;

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  /**
  @brief Печать шапки таблицы запрещенных
  */
  macro print_reg_header_proh()
    CopyAllSheetInTotalBook( m_CurrSheetName, false, "TableHeaderPr", 0, m_CurrSheetName, true );
    RegisterTable( "TableDataPr", null,
                   "N_A1", "N_A2", "N_A3", "N_A4", "N_A5", "N_A6", "N_A7", "N_A8", "N_A9","N_A10", "N_A11"); 
  end;

  /**
  @brief Печать шапки таблицы заблокированных
  */
  macro print_reg_header_block()
    CopyAllSheetInTotalBook( m_CurrSheetName, false, "TableHeaderBl", 0, m_CurrSheetName, true );
    RegisterTable( "TableDataBl", null,
                   "N_A1", "N_A2", "N_A3", "N_A4", "N_A5", "N_A6", "N_A7", "N_A8", "N_A9","N_A10", "N_A11", "N_A12", "N_A13"); 
    
  end;

  /**
  @brief Поиск наименование операциониста
  @param[in] Oper Идентификатор операциониста
  */
  PRIVATE macro GetNameOper(Oper:integer)
    file p(person);
    p.Oper = Oper;
    if(not GetEQ(p))
      return "";
    else
      return p.Name;
    end;

    return "";
  end;

  /**
  @brief Поиск под ФИ
  @param[in] FIID Идентификатор ФИ
  */
  PRIVATE MACRO GetFiCode(FIID:integer)
  VAR fi;

    fi = Trechandler( "fininstr.dbt" ); 

    if( ПолучитьФинИн( FIID, fi ) != 0 ) 
      return "";
    else
      return fi.rec.FI_Code;
    end;

    return "";
  END;

  /**
  @brief Поиск названия операции
  @param[in] ID Идентификатор операции
  */
  macro GetKindOperName(ID:integer)
    var Name = "";
    var cmd = RSDCommand("SELECT oprk.t_Name FROM doprkoper_dbt oprk, doproper_dbt opr WHERE opr.t_id_operation = ? and oprk.t_Kind_Operation = opr.t_Kind_Operation");

    cmd.AddParam("", RSDBP_IN, ID);

    var ds = TRsbDataSet(cmd);
    if(ds.moveNext()) 
      Name = ds.Name;
    end;

    return Name;
  end;

  PRIVATE MACRO CReport_NoDataMsg()
    msgbox( "Нет данных для выгрузки" );
  END;

  /**
  @brief Поиск данных и печать excel
  @param[in] KindTrn Вид журнала проводок
  @param[in] FromStr FROM из фильта журнала
  @param[in] WhereStr WHERE из фильта журнала
  */
  macro GetAndPrint()
    var que, cmd, rs;
    var progress = 0;

    If (FromStr == "")
      if(KindTrn == TRN_PROHIBITED)
        FromStr = "dtrn_prohibited_dbt t";
      else
        FromStr = "dtrn_blocked_dbt t";
      end;
    end;

    var Rep = CMakeReport();

    que = "SELECT t.* FROM " + FromStr + IIF(WhereStr != "", " WHERE " + WhereStr, "");
    cmd = RSDCommand(que);

    cmd.execute();

    rs = TRsbDataSet(cmd);
    while (rs.MoveNext())
      if (IsFirst)
        IsFirst = false;
        if (KindTrn == TRN_PROHIBITED)
          print_reg_header_proh();
        else
          print_reg_header_block();
        end;
      end;

      if (KindTrn == TRN_PROHIBITED)
        PrintTableLine( "N_A1", SQL_ConvTypeDate(rs.Date_Carry), null,
                        "N_A2", rs.Account_Payer, null,
                        "N_A3", rs.Account_Receiver, null,
                        "N_A4", GetFiCode(rs.FIID_Payer), null,
                        "N_A5", GetFiCode(rs.FIID_Receiver), null,
                        "N_A6", rs.Ground, null,
                        "N_A7", GetNameOper(rs.Oper), null,
                        "N_A8", rs.Sum_Payer, null,
                        "N_A9", rs.Sum_Receiver, null,
                        "N_A10", rs.Sum_NatCur, null,
                        "N_A11", GetKindOperName(rs.DocID), null);
      else
        PrintTableLine( "N_A1", SQL_ConvTypeDate(rs.Date_Carry), null,
                        "N_A2", rs.Account_Payer, null,
                        "N_A3", rs.Account_Receiver, null,
                        "N_A4", GetFiCode(rs.FIID_Payer), null,
                        "N_A5", GetFiCode(rs.FIID_Receiver), null,
                        "N_A6", rs.Ground, null,
                        "N_A7", GetNameOper(rs.Oper), null,
                        "N_A8", rs.Sum_Payer, null,
                        "N_A9", rs.Sum_Receiver, null,
                        "N_A10", rs.Sum_NatCur, null,
                        "N_A11", GetKindOperName(rs.DocID), null,
                        "N_A12", SQL_ConvTypeDate(rs.Date_Input), null,
                        "N_A13", SQL_ConvTypeTime(rs.Time_Input), null);
      end;
      UseProgress(progress = progress + 1);
    end;

    if (not IsFirst)
      EndTable();
      CopyAllSheetInTotalBook( m_CurrSheetName, false, GetTableRange(), 0, m_CurrSheetName, true );
    end;
  end;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    SetActiveSheet( "Проводки" );
    CreateTotalBook();
  END;

  PRIVATE MACRO Create( NumCopy:INTEGER )
    InitProgress( -1, "Идет выгрузка проводок из журнала", "Выгрузка проводок из журнала" );
    GetAndPrint();
    RemProgress();
    if (IsFirst)
      CReport_NoDataMsg();
    end;
  END;

  PRIVATE MACRO EndReport( NumCopy:INTEGER )
    SaveTotalBook();
  END;

  initCB_CReportTemplate( null, "PrintTrnOut.xlsx", null, false, null, POI_MODE);
END;

/**
@brief Поиск данных и печать excel
@param[in] KindTrn Вид журнала проводок
@param[in] FromStr FROM из фильта журнала
@param[in] WhereStr WHERE из фильта журнала
*/
macro PrintTRNOut(KindTrn, FromStr, WhereStr)
  Trn_Out(KindTrn, FromStr, WhereStr).Run();
END;
