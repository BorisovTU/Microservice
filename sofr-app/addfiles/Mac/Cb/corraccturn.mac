/*
 *  Отчет процедуры исправления оборотов лицевых счетов
 */
Import rsd, "corraccrep.mac";

macro PrintReport( Chapter : integer, Balance : string, AccountMask : string, CurrSign : bool )
  
  var Head : string;
  var Count : integer, AccountCount : integer;
  var Account : string;
  var NewDateValue : date, OldDateValue : date;
  var NewValue : money, OldValue : money;
  
  var rs : RsdRecordset;

  rs = RsdRecordset( "SELECT count(1) FROM dacc_rdc_tmp" );

  rs.moveNext();

  Count = rs.value(0);

  Head = "Протокол исправления оборотов за месяц ";

  if( CurrSign ) Head = Head + "по валютным счетам";
  else           Head = Head + "по счетам в национальной валюте";
  end;

  /* печать заголовка отчета */
  PrintHeader( Head, Count, Chapter, Balance, AccountMask );

  if( Count )
    
    /* печать заголовка таблицы */
    PrintTableHeader();
    
    rs = RsdRecordset( "SELECT * FROM dacc_rdc_tmp ORDER BY t_Chapter, t_Currency, t_Account, t_StartDate DESC" );

    while( rs.moveNext() )
      
      Account      = rs.value("t_Account"  );
      NewDateValue = OldDateValue = rs.value("t_StartDate");

      NewValue = ReadSelectValue( rs, "t_Rest"    );
      OldValue = ReadSelectValue( rs, "t_OldRest" );

      if( NewValue == null ) NewDateValue = null; end;
      if( OldValue == null ) OldDateValue = null; end;
      
      PrintFirstTableLine( Account, NewDateValue, OldDateValue );

      if( NewValue != OldValue )

        PrintTableLine( "Остаток", NewValue, OldValue );
      
      end;

      /* Дебет */
      NewValue = ReadSelectValue( rs, "t_Debet"    );
      OldValue = ReadSelectValue( rs, "t_OldDebet" );

      if( NewValue != OldValue )

        PrintTableLine( "Дебет", NewValue, OldValue );
      
      end;

      /* Кредит */
      NewValue = ReadSelectValue( rs, "t_Credit"    );
      OldValue = ReadSelectValue( rs, "t_OldCredit" );

      if( NewValue != OldValue )

        PrintTableLine( "Кредит", NewValue, OldValue );
      
      end;

      /* Дебет СПОД */
      NewValue = ReadSelectValue( rs, "t_DebetReport"    );
      OldValue = ReadSelectValue( rs, "t_OldDebetReport" );

      if( NewValue != OldValue )

        PrintTableLine( "Дебет СПОД", NewValue, OldValue );
      
      end;

      /* Кредит СПОД */
      NewValue = ReadSelectValue( rs, "t_CreditReport"    );
      OldValue = ReadSelectValue( rs, "t_OldCreditReport" );

      if( NewValue != OldValue )

        PrintTableLine( "Кредит СПОД", NewValue, OldValue );
      
      end;

    end;

    rs = RsdRecordset( "SELECT count(1) FROM (SELECT 1 FROM dacc_rdc_tmp GROUP BY t_Account, t_Chapter, t_Currency)" );

    rs.moveNext();

    AccountCount = rs.value(0);

    /* печать низа таблицы */
    PrintTableBottom( AccountCount, Count );

  end;

end;
