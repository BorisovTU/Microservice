 /*
 $Name: memorddu.mac
 $Module: Ядро Banking
 $Description: Для дубликатов платежей, требований и требований-поручений банка
 */

/*---------------------------------------------------------------------------\
|                       Файл    memorddu.mac                                 |
|     Для дубликатов платежей, требований и требований-поручений банка       |
\---------------------------------------------------------------------------*/
import BankInter, PTInter, OprInter, CTInter, globals, "pm_tools.mac", PaymInter;

FILE   fmo_party      ("party.dbt");
FILE   fmo_account  ("account.dbt");
FILE   fmo_partcode   ("partcode.dbt");

record Order     ( memorddu );
record Payment   ( pmdupaym );
record DbProp    ( pmduprop );
record CrProp    ( pmduprop );
record RmProp    ( pmdurmpp );
record Demand    ( pmdemand );
record CurTr     ( pmcurtr  );
record Pmkz      ( pmkz     );
record PmServ    ( pmserv   );


record OldOrder  ( memorddu );
record OldPayment( pmdupaym );
record OldDbProp ( pmduprop );
record OldCrProp ( pmduprop );
record OldRmProp ( pmdurmpp );
record OldDemand ( pmdemand );
record OldCurTr  ( pmcurtr  );
record OldPmkz   ( pmkz     );
record OldPmServ ( pmserv   );


/*номера полей в панели*/
const fld_Number   = 1,
      fld_bankaccr = 17,
      fld_bankaccp = 12,
      fld_Ground   = 22,
      fld_PayerINN = 5,  /*инн получателя*/
      fld_ReceiverINN = 19; /*инн плательщика*/

private const DUMEMORDER_STATUS_OPEN = 1,
              DLDOC_DUP_BANKPAYMENT = 92;


/* Просто ищем Party по PartyId*/
macro FindPartyByPartyID( PartyID )
   var retval, OldKey;
   OldKey = KeyNum (0, fmo_party);

   fmo_party.PartyId = PartyId;
   retval = getEQ(fmo_party);

   keyNum (OldKey, fmo_party);
   return retval;
end;

/* Ищем имеем ли мы БИК*/
macro FindBIC ( PartyID )
   var retval, OldKey;
   OldKey = KeyNum (0, fmo_partcode);

   fmo_partcode.PartyId = PartyId;
   fmo_partcode.CodeKind = PTCK_BIC;

   retval = getEQ(fmo_partcode);

   keyNum (OldKey, fmo_partcode);
   return retval;
end;

/* Возвращает код головного банка, если таковой есть, для банка заданного ID*/
macro GetHeadBankCode (BankID)
   if( not FindBIC( BankID ) )
     FindPartyByPartyID(BankID);
     if(fmo_party.Superior != -1 )
       if( FindPartyByPartyID( fmo_party.Superior ) )
         return fmo_party.PartyId;
       else return -1;
       end;
     else return -1;
     end;
   else return -1;
   end;
end;

macro CheckBankID( BankID, Account )
   
   return 0;

   if ( (BankID == {OurBank}) or (BankID == GetHeadBankCode({OurBank})) )
      /* ищем среди наших счетов */
      fmo_account.Chapter        = 1;
      fmo_account.Account        = Account ;
      if ( GetEQ ( fmo_account ) == TRUE )
      if( Order.DocKind == DLDOC_DUP_BANKPAYMENT)
           if ( RsbGetTrue (false, false, "Проверьте реквизиты получателя. | Указанный счет есть в \"нашем банке\". | Создать \"внешний\" дубликат? ") == false)
              return fld_bankaccr;
           end;
         else
           if ( RsbGetTrue (false, false, "Проверьте реквизиты плательщика. | Указанный счет есть в \"нашем банке\". | Создать \"внешний\" дубликат? ") == false)
              return fld_bankaccp;
           end;
         end;
      end;
   end;
   return 0;
end;

/* Проверка на числовой номер */
macro IsDigitNumber( Number )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Number)) )
        ch = SubStr( Number, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1;
        end;
        i = i + 1;
      end;

  return stat;

end;

macro    НовыйДокумент()
        return 0;
end;

macro    ПроверитьДокумент( Режим )
  var stat = 0, BankID, Account, NeedCheckBankID = 0,
      CHANG_IMPORTANT = -11,
      CHANG_NOTIMPORTANT = -10,
      res,
      Sh_Oper;
      
         /* Проверяем номер для всех */
         if( (Режим == 2 ) or (Режим == 3) )
           if(not stat)
                res = CheckINN(rmprop.PayerINN);
                if (res)
                   msgBox ("Ошибка в ИНН плательщика");
                   return 1;
                end;

                res = CheckINN(rmprop.ReceiverINN);
                if (res)
                   msgBox ("Ошибка в ИНН получателя");
                   return 1;
                end;


                if(stat==2)    
                 return stat = fld_PayerINN;
                end;
                if(stat==3)    
                 return stat = fld_ReceiverINN;
                end;
           end;
           
           if( StrLen( Rmprop.Number ) == 0 )
             MsgBox("Не задан номер документа");
             return fld_Number;
           end;

           if( IsDigitNumber(Rmprop.Number) )
             MsgBox("Номер документа нечисловой");
             return fld_Number;
           else
              if( int(Rmprop.Number) == 0 )
                 MsgBox("Номер документа не может быть нулевым");
                 return fld_Number;
              end;
           end;

           Sh_Oper = Rmprop.ShifrOper;
           if( ( (Sh_Oper == "01") or (Sh_Oper == "02") or (Sh_Oper == "05") or (Sh_Oper == "06") or (Sh_Oper == "08") or (Sh_Oper == "16")) and
               ( SubStr( Rmprop.Number, StrLen(Rmprop.Number)-2 ) == "000" ) )
             MsgBox("Три последних разряда номера должны быть отличны от '000'");
             return fld_Number;
           end;

           if( StrLen( Rmprop.Ground ) == 0 )
             MsgBox("Введите Назначение платежа");
             return fld_Ground;
           end;

           /*  Если код банка равен нашему банку или код банка равен коду нашего головного банка */
           if( Order.DocKind == DLDOC_DUP_BANKPAYMENT )
             if( strlen(CrProp.BankCode) and
                 ((Режим != 3) or ((Режим == 3) and
                                   ((CrProp.BankCode != OldCrProp.BankCode) or
                                    (Payment.ReceiverAccount != OldPayment.ReceiverAccount)))))
               NeedCheckBankID = 1;
               BankID  = Payment.ReceiverBankID;
               Account = Payment.ReceiverAccount;
             end;
           else
             if( strlen(DbProp.BankCode) and
                 ((Режим != 3) or ((Режим == 3) and
                                   ((DbProp.BankCode != OldDbProp.BankCode) or
                                    (Payment.PayerAccount != OldPayment.PayerAccount)))))
               NeedCheckBankID = 1;
               BankID  = Payment.PayerBankID;
               Account = Payment.payerAccount;
             end;
           end;
           /*платеж внешний*/
           if( NeedCheckBankID and (Режим != 3) and (OldOrder.OrderID == 0) ) 
             stat = CheckBankID( BankID, Account );
             if( stat ) return stat; end;
           end;




         end;

         if(Режим == 1 ) /*УДАЛЕНИЕ ДОКУМЕНТА*/
         end;

         if(Режим == 2)  /* ВВОД ДОКУМЕНТА */
           if(Order.Kind_Operation and (Order.Status == DUMEMORDER_STATUS_OPEN))
             if(Payment.ValueDate < {curdate}) /* Дата валютирования*/
               Payment.ValueDate = {curdate};
             end;
           end;
         end;

         if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
           if( (not stat) and NeedCheckBankID ) /*платеж внешний*/
             stat = CheckBankID( BankID, Account );
             if( stat ) return stat; end;
           end;
           if((stat == 0) and(Order.Status == DUMEMORDER_STATUS_OPEN))/* документ находится в открытых */
             stat = CHANG_NOTIMPORTANT;
           end;
           
         end;

  return stat;

end;

macro    Проверить_Счет_В_Документе( поле ) /*0-счет плательщика, 1-счет получателя*/
        var новое_значение_счета;

        if(поле) новое_значение_счета = Payment.ReceiverAccount;
        else новое_значение_счета = Payment.PayerAccount;
        end;
        return новое_значение_счета;
end;

macro    ФункцияПользователя_Документ( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */        return 0;
end;
