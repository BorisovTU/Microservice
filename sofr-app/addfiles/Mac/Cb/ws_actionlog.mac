 /*
 $Name: ws_actionlog.mac
 $Module: WEB
 $Description: Работа с журналом недавних операций над объектами системы
 */
import cb_sql, rsd, commonutil;

// максимальное количество записей журнала по операционисту
private const MAX_ITEMS_PER_OPER = 100;

class ActionLogItem
    var Oper : integer;
    var ObjectType : integer;
    var ObjectID : string;
    var TypeName : string;
    var ObjectName : string;
    var Action : integer;
    var Time : datetime;
    var IsFix : bool;
end;

private macro ConvertToActionLogItem(rs)
    var item = ActionLogItem();

    item.Oper       = SQL_ConvTypeInteger(rs.value("T_OPER"));
    item.ObjectType = SQL_ConvTypeInteger(rs.value("T_OBJECTTYPE"));
    item.ObjectID   = SQL_ConvTypeStr(rs.value("T_OBJECTID"));
    item.TypeName   = SQL_ConvTypeStr(rs.value("T_TYPENAME"));
    item.ObjectName = SQL_ConvTypeStr(rs.value("T_OBJECTNAME"));
    item.Action     = SQL_ConvTypeInteger(rs.value("T_ACTION"));
    item.Time       = SQL_ConvTypeDateTime(rs.value("T_TIME"));
    item.IsFix      = SQL_ConvTypeBool(rs.value("T_ISFIX"));

    return item;
end;

private macro Bool2Char(v)
    if (v)
        return 88;
    else
        return 0;
    end;
end;

private macro InsertActionLogRecord(item)

    var cmdText = "insert into DWEB_ACTIONLOG_DBT(T_OPER,T_OBJECTTYPE,T_OBJECTID,T_ACTION,T_TIME,T_TYPENAME,T_OBJECTNAME,T_ISFIX) Values(?,?,?,?,?,?,?,chr(?))";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, item.Oper);
    cmd.AddParam("", RSDBP_IN, item.ObjectType);  
    cmd.AddParam("", RSDBP_IN, item.ObjectID);    
    cmd.AddParam("", RSDBP_IN, item.Action);
    cmd.AddParam("", RSDBP_IN, item.Time);
    cmd.AddParam("", RSDBP_IN, item.TypeName);
    cmd.AddParam("", RSDBP_IN, item.ObjectName);
    cmd.AddParam("", RSDBP_IN, Bool2Char(item.IsFix));

    cmd.execute();
end;


private macro UpdateActionLogRecord(item)

    // T_ISFIX не обновляем, он должен наследоваться от старой записи
    var cmdText = "UPDATE DWEB_ACTIONLOG_DBT SET T_ACTION = ?, T_TIME = ?, T_TYPENAME = ?, T_OBJECTNAME = ? WHERE T_OPER = ? and T_OBJECTTYPE = ? and T_OBJECTID = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, item.Action);
    cmd.AddParam("", RSDBP_IN, item.Time);
    cmd.AddParam("", RSDBP_IN, item.TypeName);
    cmd.AddParam("", RSDBP_IN, item.ObjectName);

    cmd.AddParam("", RSDBP_IN, item.oper);
    cmd.AddParam("", RSDBP_IN, item.objectType);  
    cmd.AddParam("", RSDBP_IN, item.objectID);    
    cmd.execute();

end;

// обновляем только признак закрепления
private macro UpdateIsFixed(oper, objectType, objectID, isFix)

    var cmdText = "UPDATE DWEB_ACTIONLOG_DBT SET T_ISFIX = chr(?) WHERE T_OPER = ? and T_OBJECTTYPE = ? and T_OBJECTID = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, Bool2Char(isFix));
    cmd.AddParam("", RSDBP_IN, oper);
    cmd.AddParam("", RSDBP_IN, objectType);  
    cmd.AddParam("", RSDBP_IN, objectID);    
    cmd.execute();
end;


private macro IsActionLogRecordExists(oper, objectType, objectID)

    var cmdText = "select 1 FROM DWEB_ACTIONLOG_DBT WHERE T_OPER = ? and T_OBJECTTYPE = ? and T_OBJECTID = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, oper);
    cmd.AddParam("", RSDBP_IN, objectType);  
    cmd.AddParam("", RSDBP_IN, objectID);    
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext)
        return true;
    end;
    return false;
end;


// ограничиваем количество записей по операционисту MAX_ITEMS_PER_OPER
private macro TruncateActionLog(oper)

    CaptureOutput;
    [
     DELETE FROM DWEB_ACTIONLOG_DBT
          WHERE ROWID NOT IN (SELECT *
                                FROM ( SELECT ROWID
                                          FROM DWEB_ACTIONLOG_DBT
                                         WHERE T_OPER = ?
                                      ORDER BY T_ISFIX DESC, T_TIME DESC)
                               WHERE ROWNUM <= #)
    ](MAX_ITEMS_PER_OPER);

    var cmdText = StopCaptureOutput;

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, oper);
    cmd.execute();

end;

private macro SelectActionLogRecords(oper)

    var cmdText = "select T_OPER,T_OBJECTTYPE,T_OBJECTID,T_ACTION,T_TIME,T_TYPENAME,T_OBJECTNAME,T_ISFIX from DWEB_ACTIONLOG_DBT where T_OPER = ? order by T_ISFIX desc, T_TIME desc";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, oper);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    var result = TArray();
    var item = null;

    while (rs.moveNext)
        result[result.size] = ConvertToActionLogItem(rs);
    end;
    return result
end;

macro WriteActionLog(item)

    if (item == null)
        RunError("Элемент вставляемый в журнал должен быть задан");
    end;

    if (IsActionLogRecordExists(item.oper, item.objectType, item.objectID))
        UpdateActionLogRecord(item);
    else
        InsertActionLogRecord(item);
        TruncateActionLog(item.Oper);
    end;

    return true;
end;

macro GetActionLog(oper)

    if (oper <= 0)
        RunError("Не задан операционист");
    end;

    return SelectActionLogRecords(oper);
end;

macro SetIsItemFixed(oper, objectType, objectID, isFix)

    if (oper <= 0)
        RunError("Не задан операционист");
    end;

    if (objectType < 0)
        RunError("Не задан вид объекта");
    end;

    if ((objectID == null) or (objectID == ""))
        RunError("Не задан вид объекта");
    end;


    UpdateIsFixed(oper, objectType, objectID, isFix);
    return true;
end;

/*
var item = ActionLogItem();
item.Oper       = 55;
item.ObjectType = 100;
item.ObjectID   = "12312334234";
item.TypeName   = "Сделка";
item.ObjectName = "Сделка 555";
item.Action     = 1;
item.Time       = DtTm(date(23,10,2004), time(14,23,34));
item.IsFix      = true;
WriteActionLog(item);

var res = GetActionLog(55);
var x = 10;
*/

/*
private macro GetActionLogRecord(oper, objectType, objectID)

    var cmdText = "select T_OPER,T_OBJECTTYPE,T_OBJECTID,T_ACTION,T_TIME,T_TYPENAME,T_OBJECTNAME,T_ISFIX from DWEB_ACTIONLOG_DBT WHERE T_OPER = ? and T_OBJECTTYPE = ? and T_OBJECTID = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, oper);
    cmd.AddParam("", RSDBP_IN, objectType);  
    cmd.AddParam("", RSDBP_IN, objectID);    
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext)
        return ConvertToActionLogItem(rs);
    end;
    return null;
end;

private macro RemoveActionLogRecord(oper, objectType, objectID)

    var cmdText = "DELETE FROM DWEB_ACTIONLOG_DBT WHERE T_OPER = ? and T_OBJECTTYPE = ? and T_OBJECTID = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, oper);
    cmd.AddParam("", RSDBP_IN, objectType);  
    cmd.AddParam("", RSDBP_IN, objectID);    
    cmd.execute();

end;

*/