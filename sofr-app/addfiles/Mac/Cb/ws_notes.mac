/*
$Name:        ws_notes.mac
$Module:      WEB - Общее
$Description: Макрос примечаний субъектов
*/

import globals;
import RsbDataSet;  
import CurrInter, XmlRpcInter, BankInter;   
import oralib;
import serviceaction;
import rcw;
import dlwreps;
import rsd;
import PTInter;
import CTInter;

import "cb_sql.mac";
import "ws_validation.mac";
import "commonutil.mac";
import "likepy.mac";


PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;

private const ALG_NOTE_TYPE = 3404; // ???? ??????

private const NOTEKIND_INUSE    = 0,
              NOTEKIND_NOTINUSE = 1,
              NOTEKIND_ANY      = 2;

class TWsNoteKind()
  var ObjectType        : integer;
  var Kind              : integer;
  var Name              : string;
  var TypeID            : integer;
  var TypeName          : string; 
  var MaxLen            : integer;
  var KeepOldValues     : bool;
  var IsProgOnly        : bool;
end;

class TWsNoteText()
  var ID                    : integer;  
  var ObjectType            : integer;
  var ObjectID              : string;
  var Kind;                 //: TWsNoteKind;
  var NoteValue;            //: TWsNoteValue;
  var ValidToDate           ; // Date
  var Oper                  : integer;
  var Date                  ; // Date
  var Time                  ; // Time
end;

class TWsNoteValue(Type, val)
  var ValType     : integer;

  var IntVal      : integer = 0;  // для int и long
  var DoubleVal   : double  = 0;   // для float, floatg и double
  var StringVal   : string;   // для string, numstr, snr, uchr, chr
  var DateVal     : Date;     // для Date
  var TimeVal     : Time;     // для Time
  var MoneyVal    : money   = $0;    // для money

  macro construct
  (
    Type  : integer,
    val
  )
    if((Type == 7) OR (Type == 8) OR (Type == 18) OR (Type == 12) OR (Type == 13))
      stringval = val;
    elif((Type == 0) OR (Type == 1))
      intval = val;
    elif((Type == 2) OR (Type == 3) OR (Type == 4))
      doubleval = val;
    elif((Type == 9))
      dateval = val;
    elif((Type == 10) OR (Type == 11))
      timeval = val;
    elif((Type == 25))
      moneyval = val;
    end;

    valtype = Type;
  end;

  construct(Type, val);
end;

macro GetNoteTextValue
(
  NoteValue//: TWsNoteValue
)
  var value = NULL;

  if((NoteValue.valtype == 7) OR (NoteValue.valtype == 8) OR (NoteValue.valtype == 18) OR (NoteValue.valtype == 12) OR (NoteValue.valtype == 13))
    value = NoteValue.stringval;
  elif((NoteValue.valtype == 0) OR (NoteValue.valtype == 1))
    value = int(NoteValue.intval);
  elif((NoteValue.valtype == 2) OR (NoteValue.valtype == 3) OR (NoteValue.valtype == 4))
    value = double(NoteValue.doubleval);
  elif((NoteValue.valtype == 9))
    value = date(NoteValue.dateval);
  elif((NoteValue.valtype == 10) OR (NoteValue.valtype == 11))
    value = time(NoteValue.timeval);
  elif((NoteValue.valtype == 25))
    value = money(NoteValue.moneyval);
  end;

  return value;
end;

macro GetNameType(NoteType : Integer)
  var cmd : RsdCommand, outString, dataSet;

  cmd.cmdText = "SELECT t_sznamealg FROM dnamealg_dbt WHERE t_iTypeAlg = ? AND t_iNumberAlg = ?";

  cmd.AddParam("", RSDBP_IN, ALG_NOTE_TYPE);
  cmd.AddParam("", RSDBP_IN, NoteType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    outString = dataset.value(0);
  end;

  return outString;
end;

macro CB_GetObjectNoteKinds
(
  ObjectType    : Integer,
  NoteKindType  : Integer,
  History  		: bool,
  ObjData
)
 
  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsNoteKind(), dataSet;
  
  var NoteKindInUseOnly = true;

  if(NoteKindType == NOTEKIND_NOTINUSE)
    NoteKindInUseOnly = false;
	
  else
  
    NoteKindInUseOnly = true;
	
  end;

  var defaultSqlQuery = GetNoteListSqlQuery(ObjectType, NoteKindInUseOnly);

  var sqlText = "SELECT * FROM dnotekind_dbt WHERE t_ObjectType = ?					"+
                " AND (t_ObjectType, t_NoteKind) IN " +
                "(SELECT t_ObjectType, t_NoteKind FROM (" + defaultSqlQuery + "))";

  
  if(History == true)

    sqlText = sqlText + " AND T_KEEPOLDVALUES = 'X' ";
  
  end;
  
  if(objData != null)
    var fieldsNotUse = TArray; //<int32>
    var stat = GetObjectFieldsNotUse(ObjectType, UFFLTR_OBJFLDTYPE_NOTE, objData, fieldsNotUse);
    
    if( (stat == 0) AND (fieldsNotUse != null) AND (fieldsNotUse.Size() > 0) )
      sqlText = sqlText + "AND t_NoteKind NOT IN ("+join(fieldsNotUse, ", ")+")";
    end;
  end;
  
  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, ObjectType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj             = TWsNoteKind();

    tempObj.Kind        = dataset.value("t_notekind");
    tempObj.ObjectType  = dataset.value("t_objecttype");
    tempObj.Name        = dataset.value("t_name");    
    tempObj.TypeID      = dataset.value("t_notetype");
    tempObj.TypeName    = GetNameType(dataset.value("t_notetype"));    
    tempObj.MaxLen      = dataset.value("t_maxlen");
    tempObj.KeepOldValues      = SQL_ConvType(dataset.value("t_keepoldvalues"));
    tempObj.IsProgOnly = dataset.value("t_isprogonly");

    outArray[outArray.size] = tempObj;
  end;

  return outArray;
end;

macro CB_GetObjectTypeName
(
  ObjectType : Integer
)
  var cmd : RsdCommand, outString, dataSet;

  cmd.cmdText = "SELECT t_name FROM dobjects_dbt WHERE t_objecttype = ?";

  cmd.AddParam("", RSDBP_IN, ObjectType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    outString = dataset.value(0);
  end;

  return outString;
end;

macro CB_GetObjectNotes
(
  ObjectType    : Integer,
  DocumentID    : String,
  NoteKindType  : Integer,
  History	    : bool,
  NoteKind      : Integer,
  ObjData
)
  var NoteKindInUseOnly = true;

  if(NoteKindType == NOTEKIND_NOTINUSE)
    NoteKindInUseOnly = false;
	
  else
  
    NoteKindInUseOnly = true;
	
  end;

  var defaultSqlQuery = GetNoteListSqlQuery(ObjectType, NoteKindInUseOnly);

  var totalSqlQuery = "SELECT t.*, nk.* from dnotetext_dbt t, dnotekind_dbt nk " +
                      "WHERE  t.t_documentid = ? AND t.t_objecttype = ? "
                      " AND t.t_objecttype = nk.t_objecttype AND t.t_notekind = nk.t_notekind "
                      " AND (t.t_objecttype, t.t_notekind) IN " +
                      "(SELECT t_objecttype, t_notekind FROM (" + defaultSqlQuery + "))";
					  
  if(NoteKind>0)

    totalSqlQuery = totalSqlQuery + " AND t.t_notekind = " + NoteKind;

  end;  
  
  if(objData != null)
    var fieldsNotUse = TArray; //<int32>
    var stat = GetObjectFieldsNotUse(ObjectType, UFFLTR_OBJFLDTYPE_NOTE, objData, fieldsNotUse);

    if( (stat == 0) AND (fieldsNotUse != null) AND (fieldsNotUse.Size() > 0) )
      totalSqlQuery = totalSqlQuery + "AND t.t_NoteKind NOT IN ("+join(fieldsNotUse, ", ")+")";
    end;
  end;
  if(History == false)

    totalSqlQuery = totalSqlQuery + " AND t.t_validtodate = TO_DATE('31.12.9999', 'DD.MM.YYYY') ";
  
  else
  
    totalSqlQuery = totalSqlQuery + " AND t.t_validtodate != TO_DATE('31.12.9999', 'DD.MM.YYYY') ORDER BY t.T_DATE";

  end;  
                      

  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsNoteText(), dataSet, valType;

  cmd.cmdText = totalSqlQuery;    

  cmd.AddParam("", RSDBP_IN, DocumentID);
  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj             = TWsNoteText();
    tempObj.ID          = dataset.value("t_id");
    tempObj.ObjectType  = dataset.value("t_objecttype");
    tempObj.ObjectID    = dataset.value("t_documentid");
    
    tempObj.Kind        = TWsNoteKind();
    tempObj.Kind.Kind       = dataset.value("t_notekind");
    tempObj.Kind.ObjectType = dataset.value("t_objecttype");
    tempObj.Kind.Name       = dataset.value("t_name");
    tempObj.Kind.MaxLen     = dataset.value("t_maxlen");
    tempObj.Kind.TypeID     = dataset.value("t_notetype");
    tempObj.Kind.TypeName   = GetNameType(dataset.value("t_notetype"));
    tempObj.Kind.KeepOldValues = dataset.value("T_KEEPOLDVALUES"); 
    tempObj.Kind.IsProgOnly = dataset.value("t_isprogonly");

    var val = readNoteForObject( ObjectType, DocumentID, tempObj.Kind.Kind, date(dataset.value("t_ValidToDate")) );

    tempObj.NoteValue = TWsNoteValue(tempObj.Kind.TypeID, val);

    tempObj.ValidToDate = dataset.value("t_ValidToDate");
    tempObj.Oper        = dataset.value("t_oper");
    tempObj.Date        = SQL_ConvTypeDate(dataset.value("t_date"));
    tempObj.Time        = SQL_ConvTypeTime(dataset.value("t_time"));

    outArray[outArray.size] = tempObj;
  end;

  return outArray;
  
end;
        
macro CB_SaveNoteText
(
  noteText, //: TWsNoteText, 
  objectID : String,
  delLayeNoteInHistory : Bool
) : WebServiceResponse // <int>

  var rb = WebResponseBuilder();
  var err = 0;  
  
  if (delLayeNoteInHistory)
  
    var ProcValue;  
    GetRegistryValue("SECUR/РЕЖИМ ХРАНИЛИЩА ДАННЫХ ДЛЯ НУ", V_BOOL, ProcValue, err);
    
    if(err != 0)
      rb.AddErrorEx(err, MessageSeverity.RuntimeError, "Ошибка реестра "+err);
      rb.SetUserObject(err);
    else
      if ((ProcValue == false) AND (noteText.Date > {curdate})) // NO
        err = 259;
        CreateErrMsg(err);
        var errobj = AL_GetErrorInfo();
        rb.AddErrorEx(errobj.Stat, MessageSeverity.RuntimeError, errobj.Message);
        rb.SetUserObject(errobj.Stat);
      else
        // удаляем примечания с датой позднее сохраняемой
        CaptureOutput; 
        [
          SELECT t_ID,
                 T_OBJECTTYPE,
                 T_DOCUMENTID T_NOTEKIND,
                 t_date
            FROM dnotetext_dbt nt
           WHERE     NT.T_OBJECTTYPE = :objectType
                 AND NT.T_DOCUMENTID = :documentid
                 AND NT.T_NOTEKIND = :noteKind
                 AND nt.t_date > :svdate
                 AND NT.T_VALIDTODATE <> TO_DATE ('31.12.9999', 'dd.mm.yyyy');
        ];
        var query = StopCaptureOutput;
        var params = TArray();
        params[params.size] = SQLParam("objectType", noteText.ObjectType, RSDBP_IN);
        params[params.size] = SQLParam("documentid", noteText.objectID, RSDBP_IN);
        params[params.size] = SQLParam("noteKind", noteText.Kind.Kind, RSDBP_IN);
        params[params.size] = SQLParam("svdate", noteText.Date, RSDBP_IN);
        
        var rs = execSqlSelect(query, params);
        
        while ((rs.MoveNext()) AND (err == 0))        
          var id = SQL_ConvTypeInteger(rs.value("t_id"));
          err = CB_DeleteNotetextHist(id);             
          if(err != 0)
            errobj = AL_GetErrorInfo();
            rb.AddErrorEx(errobj.stat, MessageSeverity.RuntimeError, errobj.message); 
          end;
        end;
        
      end;
    end;
  end;
  
  if(err == 0)
    var Val = GetNoteTextValue(noteText.NoteValue);
    
    var stat = addNoteForObject(noteText.ObjectType, objectID, noteText.Kind.Kind, Val, noteText.Date);             
    
    if(stat)
      rb.AddErrorEx(stat, MessageSeverity.RuntimeError, "Не удалось сохранить значение примечания объекта.");
      rb.SetUserObject(stat);
    else 
      rb.SetUserObject(0);
    end;
  end;
  
  return rb.Result();
end;

macro CB_DeleteNoteText
(
  noteText// : TWsNoteText
)
  var stat = RemoveNoteForObject( noteText.ObjectType, noteText.objectID, noteText.Kind.Kind );  

  if(stat == true)              
    return 0;
  else 
    RunError("Не удалось удалить значение примечания объекта.");
    return 1;
  end;
end;                                    

macro WebCore_CB_InsertUpDateNotetextHist (noteText, DocumentID):WebResponseBuilder    

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  if(noteText.ID == 0)
    res = CB_InsertNotetextHist( noteText.ObjectType, DocumentID, noteText.Kind.Kind, noteText.Date, noteText.ValidToDate, GetNoteTextValue(noteText.NoteValue), noteText.ID );
	
	
  else
  
    res = CB_UpdateNotetextHist( noteText.ID, noteText.Date, noteText.ValidToDate, GetNoteTextValue(noteText.NoteValue) );
  
  end;

  if(res != 0)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end; 


macro WebCore_CB_DeleteNotetextHist (ID):WebResponseBuilder

  var res = 0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  res = CB_DeleteNotetextHist(ID);
  
  if(res != 0)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;


macro WebCore_CB_GetRegParm ():WebResponseBuilder

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var err=0;
  
  var ProcValue;
  
  GetRegistryValue( "BANK_INI/ОБЩИЕ ПАРАМЕТРЫ/ДОСТУП/ИСТОРИЯ ЗНАЧЕНИЙ ПРИМЕЧАНИЙ", V_BOOL, ProcValue, err);

  if(err == 0)
    
    responseBuilder.SetUserObject( ProcValue);
	 
  else

    responseBuilder.SetUserObject( false);
	
  end;

  return responseBuilder.Result;

end;                              

macro CB_InitNoteDate(ObjectType : Integer, // Вид объекта
  DocumentID : String, // Строковый ид. объекта
  NoteKind : Integer // Вид примечания
) // : Date
  
  var retval = date();
  var err = 0;  
  var ProcValue;  
  GetRegistryValue("SECUR/РЕЖИМ ХРАНИЛИЩА ДАННЫХ ДЛЯ НУ", V_BOOL, ProcValue, err);
  
  if(err != 0)
    RsbThrow("Ошибка реестра "+err);
  else
    if (ProcValue == false) // NO
      retval = {curdate};
    else // YES
      CaptureOutput; // последняя по дате строка, либо NULL
      [
        SELECT *
          FROM (  SELECT T_OBJECTTYPE, T_DOCUMENTID, T_NOTEKIND, t_date
                    FROM dnotetext_dbt nt
                   WHERE     NT.T_OBJECTTYPE = :objectType
                         AND NT.T_DOCUMENTID = :documentid
                         AND NT.T_NOTEKIND = :noteKind
                         AND nt.t_date >= SYSDATE
                ORDER BY t_date DESC)
         WHERE ROWNUM = 1;
      ];
      var query = StopCaptureOutput;
      var params = TArray();
      params[params.size] = SQLParam("objectType", ObjectType, RSDBP_IN);
      params[params.size] = SQLParam("documentid", DocumentID, RSDBP_IN);
      params[params.size] = SQLParam("noteKind", NoteKind, RSDBP_IN);
      
      var rs = execSqlSelect(query, params);
      
      if (rs.MoveNext()) 
        retval = SQL_ConvTypeDate(rs.value("t_date"));
      else // нет более новых
        retval = date();
      end;
    end;
  end;
  
  return retval;
end;

