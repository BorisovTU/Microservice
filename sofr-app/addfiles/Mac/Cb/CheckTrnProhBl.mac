/**
 @file CheckTrnProhBl.mac
 @brief 

 
 
 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |           |               |                                                |

*/

import OprInter, "or_rep_h.mac", rsd, RsbDataSet, SQLCONV, dlmisc, RsbFormsInter;
PRIVATE CONST ALG_TRNR_SIDE      = 8550, ///< Сторона
              ALG_TRNR_ATTRKIND  = 8551, ///< Вид атрибуты
              ALG_TRNR_OPER      = 8552, ///< Вид операции
              ALG_TRNR_STATUS    = 8553; ///< Статус
PRIVATE CONST
  ALG_TRNR_SIDE_DT = 0, ///< Дебет 
  ALG_TRNR_SIDE_CR = 1;  ///< Кредит 

PRIVATE CONST
  ALG_TRNR_ATTRKIND_ALL   = -1, ///< любые
  ALG_TRNR_ATTRKIND_ACCDT = 0,  ///< счет дебета
  ALG_TRNR_ATTRKIND_ACCCR = 1,  ///< счета кредита
  ALG_TRNR_ATTRKIND_CURDT = 2,  ///< валюта дебета
  ALG_TRNR_ATTRKIND_CURCR = 3,  ///< валюта кредита
  ALG_TRNR_ATTRKIND_OPER  = 4;   ///< операционист

PRIVATE CONST
  ALG_TRNR_OPER_EQ      = 0, ///< =
  ALG_TRNR_OPER_NOT_EQ  = 1,  ///< !=
  ALG_TRNR_OPER_NOT_IN  = 2;  ///< not in


PRIVATE CONST РолиДляРедактирования = "10010,10004,10011,10013,10015,10017";
PRIVATE RECORD  trnrules_changed(trnrules);
/**
@brief Печать шапки таблицы
@param[in] Rep Объект отчета
*/
private macro GetNameAlg( Type, Number )
  var NameAlg= TBFile("namealg");
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;

macro InsertProh(Rs, TrnRulID, TrnRulCond)
  file trn_proh("trn_prohibited.dbt") key 0 write;

  trn_proh.ID = 0;
  trn_proh.AccTrnID = Rs.AccTrnID;
  trn_proh.Date_Carry = SQL_ConvTypeDate(Rs.Date_Carry);
  trn_proh.FIID_Payer = Rs.FIID_Payer;
  trn_proh.FIID_Receiver = Rs.FIID_Receiver;
  trn_proh.AccountID_Payer = Rs.AccountID_Payer;
  trn_proh.AccountID_Receiver = Rs.AccountID_Receiver;
  trn_proh.Account_Payer = Rs.Account_Payer;
  trn_proh.Account_Receiver = Rs.Account_Receiver;
  trn_proh.Sum_NatCur = Rs.Sum_NatCur;
  trn_proh.Sum_Payer = Rs.Sum_Payer;
  trn_proh.Sum_Receiver = Rs.Sum_Receiver;
  trn_proh.Oper = Rs.Oper;
  trn_proh.Ground = Rs.Ground;
  trn_proh.DocID = Rs.ID_Operation;
  trn_proh.DocKind = Rs.DocKind;
  trn_proh.SystemDate = Rs.SystemDate;
  trn_proh.SystemTime = Rs.SystemTime;
  trn_proh.TrnRul_ID = TrnRulID;
  trn_proh.TrnRul_Cond = TrnRulCond;
  trn_proh.IsExcluded = UNSET_CHAR;
  trn_proh.ExcludeDate = date(0,0,0);
  return insert(trn_proh);
end;

macro UpdateProh(ProhID, ExcludeDate)
  var que, cmd;

  que = "UPDATE DTRN_PROHIBITED_DBT SET T_ISEXCLUDED = CHR(88), T_EXCLUDEDATE = ? WHERE T_ID = ?";
  cmd = RSDCommand(que);
  cmd.addParam( "", RSDBP_IN, ExcludeDate );
  cmd.addParam( "", RSDBP_IN, ProhID );
  cmd.execute();
end;

PRIVATE CLASS Rules (Rs)
  var Number = Rs.ID, 
      Acc_Mask = Rs.Acc_Mask ,
      Side = Rs.Side,
      Attr_Kind = Rs.Attr_Kind,
      Oper = Rs.Oper,
      Attr_Value = Rs.Attr_Value;
END;

MACRO GetRules(TrnDate, ArrRules:@TArray, ErrStr:@string, TrnRulID)
  var que, cmd, rs;
  var stat = 0;

  if(valType(TrnRulID) == V_UNDEF)
    TrnRulID = 0;
  end;

  que = "SELECT * FROM DTRNRULES_DBT WHERE T_STARTDATE <= ? AND T_ENDDATE > ? AND T_ISACTIVE = CHR(88) " + IIF(TrnRulID > 0, "AND T_ID > ? ", "") + " ORDER BY T_ID";
  cmd = RSDCommand(que);
  cmd.addParam( "", RSDBP_IN, TrnDate );
  cmd.addParam( "", RSDBP_IN, TrnDate );
  if(TrnRulID > 0)
    cmd.addParam( "", RSDBP_IN, TrnRulID );
  end;

  cmd.execute();
  rs = TRsbDataSet(cmd);
  while (rs.MoveNext())
    ArrRules[ArrRules.size] = Rules(Rs);
  END;

  if(ArrRules.size == 0)
    stat = 1;
    ErrStr = "Не найдены активные правила на дату."
  end;

  return stat;
END;

PRIVATE MACRO CheckMasksNotIn(Masks:string, Account)
  var Mask = "", RestMasks = Masks;
  var SpecChar = 0;
  var SpecCharS = 0;
  var ЕстьМаска = false;
  SpecChar = StrBrk(RestMasks, ",") - 1;
  while (SpecChar)
    Mask = trim(substr(RestMasks, 1, SpecChar));
    SpecCharS = IIF(StrBrk(Mask, "*") == 0, strlen(Mask), StrBrk(Mask, "*") - 1);
    RestMasks = trim(substr(RestMasks, SpecChar + 1));
    if (substr(Account, 1, SpecCharS) == substr(Mask, 1, SpecCharS))
      ЕстьМаска = true;
      break;
    end;
    SpecChar = StrBrk(RestMasks, ",");
  end;

  if ((not ЕстьМаска) AND RestMasks != "")
    SpecCharS = IIF(StrBrk(RestMasks, "*") == 0, strlen(RestMasks), StrBrk(RestMasks, "*") - 1);
    if (substr(Account, 1, SpecCharS) == trim(substr(RestMasks, 1, SpecCharS)))
      ЕстьМаска = true;
    end;
  end;

  return ЕстьМаска;
END;

MACRO GetRulesForAccTrn(ArrRules:TArray, AccTrnID:integer, Account_Payer:string, Account_Receiver:string, FIID_Payer:integer, FIID_Receiver:integer, Oper:integer, ResultStr:@string, TrnRulID:@integer, TrnRulCond:@string)
  var ЕстьПравило = false;
  var rule:Rules;
  var SpecChar = 0;
  var LengthSubStr = 0;
  for(rule, ArrRules)
    SpecChar = StrBrk(rule.Acc_Mask, "*");
    LengthSubStr = IIF(SpecChar == 0, strlen(rule.Acc_Mask), SpecChar - 1);
    if(((rule.Side == ALG_TRNR_SIDE_DT) AND (substr(rule.Acc_Mask, 1, LengthSubStr) == substr(Account_Payer, 1, LengthSubStr))) OR
      ((rule.Side == ALG_TRNR_SIDE_CR) AND (substr(rule.Acc_Mask, 1, LengthSubStr) == substr(Account_Receiver, 1, LengthSubStr))))
      if(rule.Attr_Kind == ALG_TRNR_ATTRKIND_ALL)
        ЕстьПравило = true;
        ResultStr = "ID проводки: " + AccTrnID + 
            " Номер правила: " + rule.Number + 
            " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        TrnRulID = rule.Number;
        TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        break;
      elif((rule.Attr_Kind == ALG_TRNR_ATTRKIND_CURDT) AND 
            (((rule.Oper == ALG_TRNR_OPER_EQ) AND (rule.Attr_Value == string(FIID_Payer))) OR 
            ((rule.Oper == ALG_TRNR_OPER_NOT_EQ) AND (rule.Attr_Value != string(FIID_Payer)))))
        ЕстьПравило = true;
        ResultStr = "ID проводки: " + AccTrnID + 
            " Номер правила: " + rule.Number + 
            " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        TrnRulID = rule.Number;
        TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        break;
      elif((rule.Attr_Kind == ALG_TRNR_ATTRKIND_CURCR) AND 
            (((rule.Oper == ALG_TRNR_OPER_EQ) AND (rule.Attr_Value == string(FIID_Receiver))) OR 
            ((rule.Oper == ALG_TRNR_OPER_NOT_EQ) AND (rule.Attr_Value != string(FIID_Receiver)))))
        ЕстьПравило = true;
        ResultStr = "ID проводки: " + AccTrnID + 
            " Номер правила: " + rule.Number + 
            " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        TrnRulID = rule.Number;
        TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        break;
      elif((rule.Attr_Kind == ALG_TRNR_ATTRKIND_OPER) AND 
            (((rule.Oper == ALG_TRNR_OPER_EQ) AND (rule.Attr_Value == string(Oper))) OR 
            ((rule.Oper == ALG_TRNR_OPER_NOT_EQ) AND (rule.Attr_Value != string(Oper)))))
        ЕстьПравило = true;
        ResultStr = "ID проводки: " + AccTrnID + 
            " Номер правила: " + rule.Number + 
            " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        TrnRulID = rule.Number;
        TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
        break;
      elif((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCCR) OR (rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCDT))
        SpecChar = StrBrk(rule.Attr_Value, "*");
        LengthSubStr = IIF(SpecChar == 0, strlen(rule.Attr_Value), SpecChar - 1);
        if (rule.Oper == ALG_TRNR_OPER_EQ)
          if( ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCDT) AND (substr(rule.Attr_Value, 1, LengthSubStr) == substr(Account_Payer, 1, LengthSubStr))) OR
              ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCCR) AND (substr(rule.Attr_Value, 1, LengthSubStr) == substr(Account_Receiver, 1, LengthSubStr))))
            ЕстьПравило = true;
            ResultStr = "ID проводки: " + AccTrnID + 
                " Номер правила: " + rule.Number + 
                " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            TrnRulID = rule.Number;
            TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            break;
          end;
        elif (rule.Oper == ALG_TRNR_OPER_NOT_EQ)
          if( ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCDT) AND (substr(rule.Attr_Value, 1, LengthSubStr) != substr(Account_Payer, 1, LengthSubStr))) OR
              ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCCR) AND (substr(rule.Attr_Value, 1, LengthSubStr) != substr(Account_Receiver, 1, LengthSubStr))))
            ЕстьПравило = true;
            ResultStr = "ID проводки: " + AccTrnID + 
                " Номер правила: " + rule.Number + 
                " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            TrnRulID = rule.Number;
            TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            break;
          end;
        elif (rule.Oper == ALG_TRNR_OPER_NOT_IN)
          if( ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCDT) AND (not CheckMasksNotIn(rule.Attr_Value, Account_Payer))) OR
              ((rule.Attr_Kind == ALG_TRNR_ATTRKIND_ACCCR) AND (not CheckMasksNotIn(rule.Attr_Value, Account_Receiver))))
            ЕстьПравило = true;
            ResultStr = "ID проводки: " + AccTrnID + 
                " Номер правила: " + rule.Number + 
                " атрибут/значение: " + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + " " + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            TrnRulID = rule.Number;
            TrnRulCond = rule.Acc_Mask + "/" + GetNameAlg(ALG_TRNR_SIDE, rule.Side) + "/" + GetNameAlg(ALG_TRNR_ATTRKIND, rule.Attr_Kind) + "/" + IIF(rule.Oper > -1, GetNameAlg(ALG_TRNR_OPER, rule.Oper), "") + " " + rule.Attr_Value;
            break;
          end;
        end;
      end;
    end;
  end;

  return ЕстьПравило;
END;

/**
@brief 
@param[in] 
*/
macro CheckTrnProh(IsSheduler, OnDate)
  var que, cmd, rs;
  var ArrRules:TArray = TArray();
  var ErrStr = "";
  var ResultStr = "";
  var stat = 0;
  var ЕстьПравило = false;
  var CountErr = 0;
  var TrnRulID = 0, TrnRulCond = "";
  var acctrnDate = {curdate} - 1;

  if(valType(IsSheduler) == V_UNDEF) 
    IsSheduler = false;
  end;

  if(IsSheduler)
    acctrnDate = OnDate;
  end;
  
  if(not IsSheduler)
    if (not GetDate(acctrnDate, "Дата проверки запрещенных проводок") )
      return 0;
    end;
  end;
  
  stat = GetRules(acctrnDate, @ArrRules, @ErrStr);
  if(not stat)
    SetOutPut(gettxtfilename("CheckTrnProh_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);
    que = "SELECT NVL (OPER.T_ID_OPERATION, -1) ID_OPERATION, " +
          "       NVL (OPER.T_DOCKIND, -1) DOCKIND, " +
          "       TRN.*, " +
          "       CASE WHEN EXISTS (SELECT * FROM DTRN_PROHIBITED_DBT proh1 WHERE proh1.T_ACCTRNID = TRN.T_ACCTRNID) THEN 1 ELSE 0 END IsAlreadyProh,"
          "       NVL((SELECT DISTINCT FIRST_VALUE(proh2.T_ISEXCLUDED) OVER (ORDER BY proh2.T_SYSTEMDATE DESC, proh2.T_ID DESC) FROM DTRN_PROHIBITED_DBT proh2 WHERE proh2.T_ACCTRNID = TRN.T_ACCTRNID), CHR(0)) IsExcluded,"
          "       NVL((SELECT DISTINCT FIRST_VALUE(proh3.T_ID) OVER (ORDER BY proh3.T_SYSTEMDATE DESC, proh3.T_ID DESC) FROM DTRN_PROHIBITED_DBT proh3 WHERE proh3.T_ACCTRNID = TRN.T_ACCTRNID), 0) IDProh"
          "  FROM DACCTRN_DBT TRN " +
          "       LEFT JOIN DOPRDOCS_DBT docs ON DOCS.T_ACCTRNID = TRN.T_ACCTRNID " +
          "       LEFT JOIN DOPROPER_DBT oper " +
          "          ON OPER.T_ID_OPERATION = docs.T_ID_OPERATION " +
          " WHERE     TRN.T_DATE_CARRY = ? " +
          "       AND TRN.T_STATE =  " + TRN_STATE_FACT;
    cmd = RSDCommand(que);
    cmd.addParam( "", RSDBP_IN, acctrnDate );

    cmd.execute();
    if(not IsSheduler)
      InitProgress( -1, "Идет проверка запрещенных проводок", "Проверка запрещенных проводок" );
    end;
    rs = TRsbDataSet(cmd);
    while (rs.MoveNext())
      TrnRulID = 0;
      TrnRulCond = "";
      ЕстьПравило = GetRulesForAccTrn(ArrRules, rs.AccTrnID, rs.Account_Payer, rs.Account_Receiver, rs.FIID_Payer, rs.FIID_Receiver, rs.Oper, @ResultStr, @TrnRulID, @TrnRulCond);
      if (ЕстьПравило)
        if(not rs.IsAlreadyProh)
          InsertProh(rs, TrnRulID, TrnRulCond);
        elif(rs.IsAlreadyProh AND (rs.IsExcluded == SET_CHAR))
          InsertProh(rs, TrnRulID, TrnRulCond);
        end;
        println(ResultStr);
        CountErr = CountErr + 1;
      elif(rs.IsAlreadyProh AND (rs.IsExcluded == UNSET_CHAR))
        UpdateProh(rs.IDProh,date());
      end;
    end;
    if(not IsSheduler)
      RemProgress();
    end;
    if (not CountErr)
      println("Не проводок, попадающих под правила");
    end;
    if(not IsSheduler)
      ViewFile(SetOutPut(null, true));
    end;
    SetOutPut(null, true)
  else
    if(not IsSheduler)
      msgbox(ErrStr);
    end;
  end;

  return 0;
END;

MACRO CheckOperRole(opernum)
  var cmd, res, rs;

  cmd = RsdCommand("SELECT * FROM DACSOPROLE_DBT A WHERE A.T_ROLEID IN (" + РолиДляРедактирования + ") AND A.T_OPER = " + opernum);
  cmd.Execute();
  rs = TRsbDataSet(cmd);
  if(rs.MoveNext())
    return 0;
  end;
  return 1;
END;

PRIVATE CLASS (TRsbPanel) PanForEmails (Emails, opernum)
  var m_Emails = Emails;
  var m_opernum = opernum;
  initTRsbPanel();
  SetStatus("~ESC~ Выход ~Enter~ Ввести"); 
  SetCaption("Email для рассылки отчета запрещенных проводок");
  setSize(52, 1);
  setPosition(30,5);
  var Tab = 2;
  var m_EmailString:TRsbEditField = TRsbEditField(7);
  m_EmailString.SetPosition(2,1);
  m_EmailString.SetSize(48,1);
  m_EmailString.Name = "EmailString";
  m_EmailString.editable = true;
  m_EmailString.textLength = 70;
  m_EmailString.value = m_Emails;
  addControl (m_EmailString);
  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
  SetFocus("EmailString");
  updateNavigationRoute();
  ReDraw();

  macro onKeyPressed(ev) 
    if((ev.keycode == 13) OR (ev.keycode == 323))
      var CurEmail:string = getControl("EmailString").value;
      if(CurEmail != m_Emails)
        if (NOT SetRegistryValue("РСХБ\\РАССЫЛКА_ОТЧ_ЗАПР_ПРОВОДОК", CurEmail, m_opernum))
            MsgBox("Ошибка записи значения настройки реестра! \"РСХБ\\РАССЫЛКА_ОТЧ_ЗАПР_ПРОВОДОК\"");
        end;
      end;
      this.close(1);
    elif(ev.keycode == 27) /*Esc*/
      this.close(1);
    end;
  end; /*onKeyPressed*/
END;


MACRO EnterEmailsForDistr(opernum)
  var Emails = "", OldEmails = "";
  var err_reg:integer = 0;
  GetRegistryValue("РСХБ\\РАССЫЛКА_ОТЧ_ЗАПР_ПРОВОДОК", V_STRING, Emails, err_reg);
  if (err_reg)
    MsgBox("Ошибка получение значений настроек \"РСХБ\\РАССЫЛКА_ОТЧ_ЗАПР_ПРОВОДОК\"");
    return 0; 
  end;

  var myPanel:TRsbPanel = PanForEmails (Emails, opernum);
  myPanel.run;
  return 0;
END;

MACRO CheckProhTrnForExclude(doc)
  var que, cmd, rs;
  var ResultStr = "";
  var ЕстьПравило = false;
  setbuff( trnrules_changed, doc );

  var ArrRules:TArray = TArray();
  ArrRules[ArrRules.size] = Rules(trnrules_changed);

  que = "SELECT * FROM DTRN_PROHIBITED_DBT WHERE T_ISEXCLUDED = CHR(0) AND T_TRNRUL_ID = ? AND T_DATE_CARRY <= ?";
  cmd = RSDCommand(que);
  cmd.addParam( "", RSDBP_IN, ArrRules[0].Number );
  cmd.addParam( "", RSDBP_IN, trnrules_changed.ChangeDate );

  cmd.execute();
  rs = TRsbDataSet(cmd);
  while(rs.MoveNext())
    ЕстьПравило = GetRulesForAccTrn(ArrRules, rs.AccTrnID, rs.Account_Payer, rs.Account_Receiver, rs.FIID_Payer, rs.FIID_Receiver, rs.Oper, NULL, NULL, NULL);
    if(not ЕстьПравило)
      UpdateProh(rs.ID, trnrules_changed.ChangeDate);
    end;
  end;
END;