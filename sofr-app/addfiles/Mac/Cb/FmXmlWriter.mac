/*
 $Name: FmXmlWriter.mac
 $Module: Ядро ГКБО 
 $Description: формирование xml файла
 */
 /*
	формирование xml файла
 */
import FMInter, BankInter;

CONST LVL_SPACE : integer = 3;
private var xmlSaveToString = "";
macro FmXmlFillStr(char, count) : string
	var i = 0;
	var str : string = "";
	while (i < count)
		str = str + char;
		i = i + 1;
	end;
	return str;
end;

private macro PrintXmlHandler(str)
    xmlSaveToString = xmlSaveToString + str;
end;

class FmXmlNodeElementAttribute
    var AttributeName = "";
    var AttributeValue = "";
end;

class FmXmlNodeElement
	var NodeName = "";
	var NodeValue = "";
	var Childs = TArray;
    var Attributes = TArray;
	
    macro setAttribute(name, value)
        var attr = FmXmlNodeElementAttribute();
        attr.AttributeName = name;
        attr.AttributeValue = string(value);
        
        var i = 0;
        var fAddNew = true;
        while (i < Attributes.size)
            if(Attributes[i].AttributeName == name)
                fAddNew = false;
                Attributes[i] = attr;
            end;
            i = i + 1;
        end;
        
        if (fAddNew)
            Attributes[Attributes.size] = attr;
        end;
    end;
    
	macro addChild(name, outNode)
		var newSize : integer = Childs.size;
		Childs[newSize] = FmXmlNodeElement;
		Childs[newSize].NodeName = name;
		
		return Childs[newSize];
	end;
	
	macro addChildEx(name, value)
		var newSize : integer = Childs.size;
		Childs[newSize] = FmXmlNodeElement;
		Childs[newSize].NodeName  = name;
		Childs[newSize].NodeValue = value;
		
		return Childs[newSize];
	end;
	
	macro getValue()
		return NodeValue;
	end;
	
	macro setValue(value)
		NodeValue = value;
	end;
	
	macro setName(name)
		NodeName = name;
	end;
	
	macro getName()
		return NodeName;
	end;
	
	macro saveNode(lvl, space, f) : string
		var i = 0;
		var oStr : string = "";
		var cStr : string = "";
		var format : integer = lvl * space;
		
		oStr = FmXmlFillStr(" ", format);
		oStr = oStr + "<"  + NodeName;
    
        if (Attributes.size != 0)
            var j = 0;
            while (j < Attributes.size)
                var atr = Attributes[j];
                oStr = oStr + " " + atr.AttributeName + "=\"" + atr.AttributeValue + "\"";
                j = j + 1;
            end;
        end;
        
        oStr = oStr + ">";
		
		if (Childs.size > 0)
			cStr = FmXmlFillStr(" ", format);
		end;
        
        cStr = cStr + "</" + NodeName + ">";
		
		if (Childs.size <= 0)
			var out_str;
			var value = "";
			if (ValType(NodeValue) != V_UNDEF)
				value = NodeValue;
			end;
			
			out_str = oStr + String(value) + cStr;
			
			println(out_str);
            if (ValType(f) != V_UNDEF)
                f.str = out_str;
                insert(f);
            end;
		else
			println(oStr);
            
            if (ValType(f) != V_UNDEF)
                f.str = oStr;
                insert(f);
            end;
            
			while (i < Childs.size)
				Childs[i].saveNode(lvl + 1, space, f);
				i = i + 1;
			end;
			println(cStr);
            
            if (ValType(f) != V_UNDEF)
                f.str = cStr;
                insert(f);
            end;
		end;
	end;
end;

class FmXmlDocumentWriter
	private var pRoot;
	private var pProcessIn : string;
	private var space = LVL_SPACE;
	
	macro getRootElement(name)
		pRoot = FmXmlNodeElement;
		pRoot.setName(name);
		
		return pRoot;
	end;
	
	macro createProcessingInstruction(str : string)
		pProcessIn = str;
	end;
	
	macro saveToFile(f)
		println (pProcessIn);
		
		f.str = pProcessIn;
		insert(f);
		
		pRoot.saveNode(0, space, f);
	end;
    
    macro saveToString(fProcessIn)
        xmlSaveToString = "";

        SetOutHandler (@PrintXmlHandler);
        if (fProcessIn)
            println (pProcessIn);
        end;
        
        pRoot.saveNode(0, space);
        SetOutHandler (NULL);
        return xmlSaveToString;
    end;
end;