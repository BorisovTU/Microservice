/*
$Name:        ws_MassReValuation.mac
$Module:      Главная книга
$Description: Макрос конвейера обработчик пачки счетов
*/


Import FMInter, PTInter, RSD, "serviceaction.mac", "revaluation_panel.mac";
import oralib, likepy, commonutil;
private const OBJTYPE_PARTY = 3;



private macro _TruncateTable(TableName) : bool
  var cmd : RsdCommand;
  cmd = RsdCommand("TRUNCATE TABLE " + TableName);
  cmd.execute();
end;

private macro _InsertAccountsIntoAccOvervalue(RevalueAccPack)
  var stmt = "";
  var cmd;
  
  _TruncateTable("daccovervalue_tmp");

  stmt = stmt + "INSERT INTO daccovervalue_tmp ";
  stmt = stmt + "( t_AccountID ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + ") ";
  stmt = stmt + "SELECT ";
  stmt = stmt + "  t_AccountID ";
  stmt = stmt + " ,t_Chapter ";
  stmt = stmt + " ,t_Code_Currency ";
  stmt = stmt + " ,t_Account ";
  stmt = stmt + "  FROM drevalueaccsel_dbt ";
  stmt = stmt + " WHERE     t_ParamID = " + RevalueAccPack.ParamID;
  stmt = stmt + "       AND t_Branch = " + RevalueAccPack.DprtID;
  stmt = stmt + "       AND t_RecID BETWEEN " + RevalueAccPack.StartRecID + " AND " + (RevalueAccPack.StartRecID + RevalueAccPack.PackSize - 1);

  cmd = RsdCommand(stmt);
  cmd.execute();
end;

macro MassReValuation(TaskID, ExecID, ConvTypeID, OperationID, PackID, PanelObj)
 
  CaptureOutput();
[
SELECT t_BRANCH AS dprtid,
       (SELECT COUNT (1)
          FROM dcnvdoc_dbt
         WHERE     t_execid = :execid AND t_convtypeid = :convtypeid AND t_packid = :packid)
          packSize,
       cnv.*
  FROM (  SELECT t_objecttype AS paramid, MIN (t_objectid) AS recid
            FROM dcnvdoc_dbt
           WHERE     t_execid = :execid AND t_convtypeid = :convtypeid AND t_packid = :packid
        GROUP BY t_objecttype) cnv,
       drevalueaccsel_dbt
 WHERE t_ParamID = cnv.paramid AND t_recid = cnv.recid 
];
  var s = StopCaptureOutput();
  var rs = execSQLSelect( s,
    MakeArray(SQLParam("execid"    , execid    ,RSDBP_IN),
              SQLParam("convtypeid", convtypeid,RSDBP_IN),
              SQLParam("packid"    , packid    ,RSDBP_IN))
    );

  var RevalueAccPack = TWebReValueAccPack();
  if(rs.movenext())
    RevalueAccPack.DprtID     = int(SQL_ConvTypeInteger(rs.value("dprtid"))); // int(...) почему-то приходит double
    RevalueAccPack.StartRecID = int(SQL_ConvTypeInteger(rs.value("recid")));
    RevalueAccPack.ParamID    = int(SQL_ConvTypeInteger(rs.value("paramid")));
    RevalueAccPack.PackSize   = int(SQL_ConvTypeInteger(rs.value("packSize")));
  else
    RunError("drevalueaccsel_dbt не зодержит записей для t_execid = "+int(execid)+
        "t_convtypeid = " + int(convtypeid) +"t_packid =  " + int(packid));
  end;
  
  var accKind = 0;
  var restKind = 0;

  if(PanelObj.RestKind != null)
    if (GenClassName (PanelObj.RestKind) == "TArray")
      restKind = TArray;
      var i = 0;
      while (i < PanelObj.RestKind.size)
        restKind[restKind.size] = PanelObj.RestKind[i].ID;
        i = i + 1;
      end;
    else
       restKind = PanelObj.RestKind.ID;
    end;
  end;

  if(PanelObj.IsAllAccKindsSelected == true)
    if(PanelObj.AccKind != null)
      accKind = PanelObj.AccKind.ID;
    end;
  end;

  _InsertAccountsIntoAccOvervalue(RevalueAccPack);

  var stat:integer = CB_OverEstimateAccount
  (
    RevalueAccPack.ParamID  
   ,RevalueAccPack.StartRecID    
   ,restKind
   ,RevalueAccPack.DprtID            
   ,0            
   ,""            
   ,0      
   ,PanelObj.RegulateDate       
   ,accKind
   ,PanelObj.RegCarry           
   ,PanelObj.ActuateAccOvervalue
   ,PanelObj.PrintReport        
   ,PanelObj.PrintOrders        
   ,PanelObj.AllDays            
   ,PanelObj.Backout            
   ,PanelObj.FirstCarryNumber          
  );
  
  execSQL(
    "UPDATE dcnvpack_dbt SET T_STATE = T_STATE + 1 WHERE T_EXECID = ? AND T_CONVTYPEID = ? AND T_PACKID = ?",
    MakeArray(
      SQLParam( "", ExecID    , RSDBP_IN ),
      SQLParam( "", ConvTypeID, RSDBP_IN ),
      SQLParam( "", PackID    , RSDBP_IN )));

  return stat;
end;


