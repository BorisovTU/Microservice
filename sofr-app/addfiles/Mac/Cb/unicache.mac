// Базовый класс для организации кешей

import PSInter;

class TUniCacheKey
  macro AsSimpleValue():variant
    return 0;
  end;
end;

class (PSRepMap) TUniCache( p_keyType:integer )

  InitPSRepMap( p_keyType );
  
  private class TStoredValue( p_Value:variant, p_Found:bool, p_ErrCode:integer, p_ErrText:string )
    var m_Value  :variant = p_Value,
        m_Found  :bool    = p_Found,
        m_ErrCode:integer = p_ErrCode,
        m_ErrText:string  = p_ErrText;
  end;
  
  private macro Find( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool
    p_Value = NULL;
    p_ErrCode = 0;
    p_ErrText = "Метод Find необходимо переопределить в наследнике";
    return false;
  end;

  macro Get( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool

    var simpleKey:variant = p_Key;
    if( IsEqClass( "TUniCacheKey", p_Key ) )
      simpleKey = p_Key.AsSimpleValue();
    else
      simpleKey = p_Key;
    end;
    var tmpStoredVal:TStoredValue = Value( simpleKey );
    if( tmpStoredVal == NULL )
      var tmpValue:variant, tmpErrCode:integer, tmpErrText:string;
      var tmpFound:bool = Find( p_Key, @tmpValue, @tmpErrCode, @tmpErrText );
      tmpStoredVal = TStoredValue( tmpValue, tmpFound, tmpErrCode, tmpErrText );
      Add( simpleKey, tmpStoredVal );
    end;
    p_Value   = tmpStoredVal.m_Value;
    p_ErrCode = tmpStoredVal.m_ErrCode;
    p_ErrText = tmpStoredVal.m_ErrText;
    return tmpStoredVal.m_Found;

  end;

end;
