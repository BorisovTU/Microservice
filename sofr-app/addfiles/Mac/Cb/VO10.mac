 /*
 $Name:        VO10.mac
 $Module:      Ядро Банкинг
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок      : 29053 - Валютный контроль
// Шаг       : 10   - "Акцепт валютным контролером"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, PSInter, cbsttls, OprInter, CashInter, pm_setst, pm_common;

var PaymentObj:RsbPayment;

private const STR_ACTION_STEP_QUESTION = "Какое действие следует произвести с документом?";
private const STR_ACTION_STEP_ACCEPT_COMMISSION = "Акцептовать с взиманием комиссии";
private const STR_ACTION_STEP_ACCEPT = "Акцептовать без взимания комиссии";
private const STR_ACTION_STEP_REFUSE = "Отказать";

private macro ChooseActionStep():integer

  //Array Text;
  Array Buttons;

  var DialogFlag;

  var select_button:integer = VO_ACTION_STEP_UNDEF;
  
  if(IsOprMultiExec)
    DialogFlag  = TSetDialogFlag(1);
  end;

  //Text(0) = STR_ACTION_STEP_QUESTION;
  Buttons(VO_ACTION_STEP_ACCEPT_COMMISSION) = STR_ACTION_STEP_ACCEPT_COMMISSION;
  Buttons(VO_ACTION_STEP_ACCEPT) = STR_ACTION_STEP_ACCEPT;
  Buttons(VO_ACTION_STEP_REFUSE) = STR_ACTION_STEP_REFUSE;

  select_button = Menu( Buttons, STR_ACTION_STEP_QUESTION, NULL, NULL, NULL, 2);
  //select_button = ConfWin( Text, Buttons );
  
  return select_button;
end;
 
private macro IsSetVO_Code(PaymentObj:RsbPayment)
  var PmCOObj:RsbPmCO = PaymentObj.PmCO;
  var IsNext = PmCOObj.First();
  var pmco:TRecHandler = TRecHandler("pmco.dbt", "bank.def");
  
  while((IsNext == 0) and (PmCOObj.Current(pmco) == 0))
    if(strlen(pmco.rec.VO_CodeStr) != 0)
      return true;
    end;
    IsNext = PmCOObj.Next();
  end;
  
  return false;
end;

macro ExecuteCaseStep( Kind_Operation, Number_step, first, DocKind )
  
  var ActionStep:integer = VO_ACTION_STEP_UNDEF;
  var note_text:string = "";
  var DialogFlag;
  var NewDefaultTransferDate;

  RECORD oprstep("oprstep");
  StepInfoEx(Number_Step, oprstep);

  if(CheckDateStartOpr( oprstep.ID_Operation ))
    return 1;
  end;

  // Установить дату начала операции равной дате операционного дня пользователя
  SetOprDate(29000000, {curdate});

  
  if(PaymentObj.PrimDocKind != WL_INDOC)
    ActionStep = GetCachedVar(VOCONTROL_CONTEXT_QUESTION, "ChooseActionStep");
  else
    ActionStep = VO_ACTION_STEP_ACCEPT;
  end;
  
  if((ActionStep == VO_ACTION_STEP_REFUSE) or 
     (ActionStep == VO_ACTION_STEP_ACCEPT_COMMISSION) or 
     (ActionStep == VO_ACTION_STEP_ACCEPT))
  
    if(ActionStep == VO_ACTION_STEP_REFUSE)
      
      while(note_text == "")
        if(not CashgetString( note_text, "Причина отказа (возврата)" ))
          msgbox( "Пользователь прервал выполнение шага операции" );
          return 1;
        end;
        
        if(note_text == "")
          DialogFlag = TSetDialogFlag(1);
          msgbox("Необходимо указать причину отказа");
        end;
      end;

      // Заполнить примечание
      if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text ) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;

      if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;

      PaymentObj.PaymStatus = PM_REJECTED;
      PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );
      
      PaymentObj.VO_Accept = ACPT_CC_REJECTED;
      PaymentObj.VO_Oper = {Oper};

    else
      if((PaymentObj.IsVO != "X") or (not IsSetVO_Code(PaymentObj)))
        if(PaymentObj.PrimDocKind == WL_INDOC)        
          if( ConfWin(  makeArray( "Не указан код вида валютной операции в реквизитах ВО|Акцептовать?" ),
                        makeArray( " Да", " Нет" )
                     ) == 1 )
            msgbox("Пользователь прервал выполнение операции.");
            return 1;                     
          end;        
        else        
          msgbox("Не указан код вида валютной операции в реквизитах ВО");
          return 1;
        end;
      end;
      
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CURCONTROL, OPR_PAYM_ST_CURCONTROL_NO ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;

      if( PaymentObj.PrimDocKind == WL_INDOC )
        if(УстановитьСтатусыПлатежа(OPR_PAYM_STATE, OPR_PM_ST_CLOSE ))
          msgbox("Ошибка при установке статуса платежа");
          return 1;
        end;
                           
        PaymentObj.PaymStatus = PM_FINISHED
      end;     

      PaymentObj.VO_Accept = ACPT_CC_ACCEPTED;
      PaymentObj.VO_Oper = {Oper};
      PaymentObj.VO_Description = "";
      PaymentObj.ValueDate = {curdate};
      
      NewDefaultTransferDate = PmGetDefaultOutTransferDate( PaymentObj );
      if( NewDefaultTransferDate > PaymentObj.OutTransferDate )
        PaymentObj.OutTransferDate = NewDefaultTransferDate;
      end;

      if(ActionStep == VO_ACTION_STEP_ACCEPT_COMMISSION)
        return "20";
      else
        return "";  
      end;
    end;
  elif(ActionStep == -2)
    msgbox("Пользователь прервал выполнение операции.");
    return 1;
  else
    msgbox("Ошибка при выборе действия над документом");
    return 1;
  end;
  
  return "";
end;
