// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание    : Формирование элементов наименования
//
//  Программист : Чукина Т.А.
//
//  Создан      : 28.03.2014
//
// --------------------------------------------------------------------

import CTInter, PTInter, "adress.mac", oralib, likepy, "pm_const.mac", "bnk_ptlib.mac";

private macro AddField (ar, name, tp, sz, dec)
  ar [ar.size] = name;
  ar [ar.size] = tp;
  ar [ar.size] = sz;
  ar [ar.size] = dec;
  ar [ar.size] = 0;
end;

private const NAME_ELEMENT_LEN = 40,
              DESCRIPTION_LEN = 200,
              VALUE_LEN = 320;

// !!! эту структуру можно менять только синхронно с сишной структурой CLIENTNAMEEL
private macro GetDescrArr : TArray
  var Arr : TArray = TArray();

  AddField (Arr, "Sort", V_INTEGER, 2, 0);
  AddField (Arr, "NameElement", V_STRING, NAME_ELEMENT_LEN + 1, 0); // прибавляем 1 на нуль-символ
  AddField (Arr, "Description", V_STRING, DESCRIPTION_LEN + 1, 0);
  AddField (Arr, "Error", V_INTEGER, 4, 0);
  AddField (Arr, "Value", V_STRING, VALUE_LEN + 1, 0);

  return Arr;
end;

private macro AddNameElem
( NameElements : TArray,
  Sort : integer,
  NameElement : string,
  Description : string,
  Error : integer,
  Value : string
)
  var NameElem : TRecHandler = TRecHandler( "clientnameel", GetDescrArr() );
  NameElem.rec.Sort = Sort;
  NameElem.rec.NameElement = substr(NameElement, 1, NAME_ELEMENT_LEN);
  NameElem.rec.Description = substr(Description, 1, DESCRIPTION_LEN);
  NameElem.rec.Error = Error;
  NameElem.rec.Value = substr(Value, 1, VALUE_LEN);

  NameElements[ NameElements.size ] = NameElem;
end;

private macro GetActivityKind(ClientID : integer, ActivityKind : @string) : integer
  ActivityKind = "";
  var Error : integer = 0;

  var q : string = "select t_IsEmployer "
                   "  from dpersn_dbt "
                   " where t_PersonID = :PersonID ";

  var rs : RsdRecordset = execSQLselect(q, makeArray( SQLParam("PersonID", ClientID) ));
  if(rs and rs.moveNext())
    if(rs.value("t_IsEmployer") == "X")
      ActivityKind = "(ИП)";
    else
      var AttrID : integer = GetPartyMainAttr_PartyType(ClientID);

      if(AttrID == 0)
        Error = PAYMERR_NO_PT_ACTIVITY_KIND;
      elif(AttrID == PARTY_ATTR_PTTYPE_NOTARIUS)
        ActivityKind = "(нотариус)";
      elif(AttrID == PARTY_ATTR_PTTYPE_ADVOCAT)
        ActivityKind = "(адвокат)";
      elif(AttrID == PARTY_ATTR_PTTYPE_KFH)
        ActivityKind = "(КФХ)";
      else
        Error = PAYMERR_REQ_NO_ACTIVITY_KIND;
      end;
    end;
  else
    Error = PAYMERR_INST_NO_ACTIVITY_KIND;
  end;

  return Error;
end;

private macro GetClientAddr
( ClientID : integer, 
  AddrType : integer, 
  Address  : @string
) : integer

  var Error : integer = 0;
  Address = "";

  record addr(adress);
  if(ClientID > 0)
    if( НайтиАдресСубъекта(ClientID, AddrType, addr) )
      Address = addr.adress;
    end;
  end;

  if(Address == "")
    if(AddrType == PTADDR_LEGAL)
      Error = PAYMERR_NO_PT_LEGAL_ADDR;
    elif(AddrType == PTADDR_REAL)
      Error = PAYMERR_NO_PT_REAL_ADDR;
    elif(AddrType == PTADDR_POST)
      Error = PAYMERR_NO_PT_POST_ADDR;
    end;
  end;

  return Error;
end;

private macro DoubleSlash(s : @string)
  if(s)
    s = "//" + s + "//";
  end;
end;

private macro GetPtName(PartyID : integer, NameTypeID : integer) : string

  if(PartyID > 0)
    var arrNames : TArray = GetPartyNames(PartyID, NameTypeID);

    if(arrNames.size > 0)
      return arrNames[0];
    end;
  end;

  return "";
end;

private macro GetFIO(ClientID : integer, FIO : @string) : integer

  macro JoinFIO(f : string, i : string, o : string) : string
    var FIO : string = "";

    if(f)
      FIO = f;
    end;

    if(i)
      if(FIO)
        FIO = FIO + " ";
      end;

      FIO = FIO + i;

      if(o)
        FIO = FIO + " " + o;
      end;
    end;

    return FIO;
  end;


  FIO = "";

  if(ClientID > 0)
    var f : string, i : string, o : string;
    if( GetPersonNames(ClientID, @f, @i, @o) )
      FIO = JoinFIO(f, i, o);
    end;
  end;

  if(FIO == "")
    return PAYMERR_NO_PT_FIO;
  end;

  return 0;
end;

private macro GetActivityKindAndAddr(ClientID : integer, ActKindAddr : @string) : integer
  ActKindAddr = "";

  var ActivityKind : string = "";
  var Error : integer = GetActivityKind(ClientID, @ActivityKind);
  if(not Error)
    var Address : string = "";
    Error = GetClientAddr(ClientID, PTADDR_LEGAL, @Address);
    if(Error)
      Error = GetClientAddr(ClientID, PTADDR_REAL, @Address);
    end;
  end;

  if(not Error)
    DoubleSlash(@Address);
    ActKindAddr = "(" + ActivityKind + ", " + Address + ")";
  end;

  return Error;
end;

macro GetClNameElements
( ClientID : integer, 
  BankID : integer, 
  NameElements : TArray,
  Sort : integer // необязательный
)
  macro NeedInclude(CheckSort : integer) : bool
    if( not Sort or (Sort == CheckSort) )
      return true;
    end;

    return false;
  end;

  var CurrSort : integer, ElemValue : string, Error : integer;

  CurrSort = 10;
  if( NeedInclude(CurrSort) )
    Error = GetActivityKind(ClientID, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "Вид деятельности",
        "Заключенное в скобки указание на вид деятельности субъекта-физ. лица: ИП, нотариуса, адвоката, КФХ.",
        Error,
        ElemValue
      );
  end;

  CurrSort = 15;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_LEGAL, @ElemValue);
    DoubleSlash(@ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "//Юридический адрес//",
        "Адрес субъекта с типом \"юридический\" с добавлением символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 20;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_REAL, @ElemValue);
    DoubleSlash(@ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "//Фактический адрес//",
        "Адрес субъекта с типом \"фактический\" с добавлением символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 25;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_POST, @ElemValue);
    DoubleSlash(@ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "//Почтовый адрес//",
        "Адрес субъекта с типом \"почтовый\" с добавлением символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 30;
  if( NeedInclude(CurrSort) )
    ElemValue = GetPtName(ClientID, PTKN_FULLNAME);
    if(ElemValue)
      Error = 0;
    else
      Error = PAYMERR_NO_PT_FULLNAME;
    end;

    AddNameElem
      ( NameElements,
        CurrSort,
        "Полное наименование",
        "Полное наименование субъекта",
        Error,
        ElemValue
      );
  end;

  CurrSort = 35;
  if( NeedInclude(CurrSort) )
    ElemValue = GetPtName(ClientID, PTKN_SHORTNAME);
    if(ElemValue)
      Error = 0;
    else
      Error = PAYMERR_NO_PT_SHORTNAME;
    end;

    AddNameElem
      ( NameElements,
        CurrSort,
        "Сокращенное наименование",
        "Сокращенное наименование субъекта",
        Error,
        ElemValue
      );
  end;

  CurrSort = 40;
  if( NeedInclude(CurrSort) )
    Error = GetFIO(ClientID, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "ФИО",
        "Фамилия, имя и отчество субъекта из карточки субъекта.",
        Error,
        ElemValue
      );
  end;

  CurrSort = 45;
  if( NeedInclude(CurrSort) )
    ElemValue = GetPtName(ClientID, PTKN_TAXNAME);
    if(ElemValue)
      Error = 0;
    else
      Error = PAYMERR_NO_PT_TAXNAME;
    end;

    AddNameElem
      ( NameElements,
        CurrSort,
        "Наименование для налоговых платежей",
        "Наименование, сформированное в карточке субъекта для использования в бюджетных платежах",
        Error,
        ElemValue
      );
  end;

  CurrSort = 50;
  if( NeedInclude(CurrSort) )
    if(BankID == 0)
      ElemValue = "";
      Error = PAYMERR_OTHER_PT_BNKNAME;
    else
      ElemValue = GetPtName(BankID, PTKN_FULLNAME);
      if(ElemValue)
        Error = 0;
      else
        Error = PAYMERR_NO_BNK_FULLNAME;
      end;
    end;

    AddNameElem
      ( NameElements,
        CurrSort,
        "Наименование банка, полное",
        "Полное наименование банка (филиала), в котором обслуживается плательщик или получатель.",
        Error,
        ElemValue
      );
  end;

  CurrSort = 55;
  if( NeedInclude(CurrSort) )
    if(BankID == 0)
      ElemValue = "";
      Error = PAYMERR_OTHER_PT_BNKNAME;
    else
      ElemValue = GetPtName(BankID, PTKN_SHORTNAME);
      if(ElemValue)
        Error = 0;
      else
        Error = PAYMERR_NO_BNK_SHORTNAME;
      end;
    end;

    AddNameElem
      ( NameElements,
        CurrSort,
        "Наименование банка, сокращенное",
        "Полное наименование банка (филиала), в котором обслуживается плательщик или получатель.",
        Error,
        ElemValue
      );
  end;

  CurrSort = 60;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_LEGAL, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "Юридический адрес",
        "Адрес субъекта с типом \"юридический\" без добавления символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 65;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_REAL, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "Фактический адрес",
        "Адрес субъекта с типом \"фактический\" без добавления символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 70;
  if( NeedInclude(CurrSort) )
    Error = GetClientAddr(ClientID, PTADDR_POST, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "Почтовый адрес",
        "Адрес субъекта с типом \"почтовый\" без добавления символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;

  CurrSort = 75;
  if( NeedInclude(CurrSort) )
    Error = GetActivityKindAndAddr(ClientID, @ElemValue);

    AddNameElem
      ( NameElements,
        CurrSort,
        "Вид деятельности + адрес",
        "Вид деятельности субъекта-физ. лица и его адрес с добавлением символов \"//\" до и после информации об адресе",
        Error,
        ElemValue
      );
  end;
end;
