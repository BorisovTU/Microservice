/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*               Copyright (c) R-Style Software Lab                     */
/*                                                                      */
/*  Имя файла      : mo.mac                                             */
/*                                                                      */
/*  Описание       : Печать мемориального ордера                        */
/*                   по рублевой проводке по главе А плана счетов ЦБ РФ */
/*                                                                      */
/************************************************************************/

import prpm, globals, "mocommon.mac", "prnlib.mac", likepy;

private var ExRateDebetAccount = "", ExRateCreditAccount = "", ExRateSumNatCur = $0; 

private macro GetDataFromExRateTrn( СчетДебета, СчетКредита, AccTrnId ):bool

  // Найдем проводку курсовых разниц
  var select = "select exratetr.t_Account_Payer, exratetr.t_Account_Receiver  "+
               "  from   dacctrn_dbt tr, dacctrn_dbt exratetr                 "+
               " where tr.t_AcctrnID                 = :AccTrnID              "+
               "   and exratetr.t_AccTrnID           = tr.t_ExRateAccTrnID    "+
               "   and exratetr.t_Result_Carry       = 83                     ";

  var params:TArray = TArray();
  params[params.size] = SQLParam( "AccTrnID", AccTrnID );
  
  var rset = execSQLselect( select, params, false );
  
  while( rset and rset.moveNext() )
    SetParm(0, rset.value(0));
    SetParm(1, rset.value(1));
    return true;
  end;

  return false;
end;

private macro RubAmount( d, str )                             
  if( ( d.FIID_Payer == d.FIID_Receiver ) and ( d.FIID_Payer != 0/*NATCUR*/ ) and ( d.Sum_NatCur > 0 ) )
[│ ########################################### │          │ ############################   │]( str, d.Sum_NatCur:l:f );
    return 1;
  end;
  return 0;
end;

private macro ExRatePayerAccount( d )
  if( ( ExRateDebetAccount != "" ) and ( ExRateDebetAccount != d.Account_Receiver ) )
[│                                             │          │ #########################      │](ExRateDebetAccount);
  end;
end;

private macro ExRateReceiverAccount( d, str )
  if( ( ExRateCreditAccount != "" ) and ( ExRateCreditAccount != d.Account_Payer ) )
[│ ########################################### │          │ #########################      │]( str, ExRateCreditAccount);
    return 1;
  end;
  return 0;
end;
/*---------------------------------------------------------*/
/* Печать рублевой проводки по главе А плана счетов ЦБ РФ  */
/* d       - буфер проводки                                */
/* ncopy   - число копий                                   */
/*---------------------------------------------------------*/
MACRO PutPlatI( d, ncopy:integer ):bool

  /* Подготовка строк к печати */
  ARRAY SS, SG, SBP, SBR, SP, SR;

  var Payer   :string = MO_GetClientString( d.Account_Payer,    d.Chapter, d.FIID_Payer    );
  var Receiver:string = MO_GetClientString( d.Account_Receiver, d.Chapter, d.FIID_Receiver );
  var dateDoc :date   = {curdate};
  var NameDocument    = "";
  var SPOD_str:string = IfThenElse( Index( d.TypeDocument, "З" ), "СПОД", "    " );
  var offSet = 0;

  GetDataFromExRateTrn( ExRateDebetAccount, ExRateCreditAccount, d.AccTrnID );

  StrSplit( GetOurBankName(),    SBP,    43, 43, 2 );
  StrSplit( GetOurBankName(),    SBR,    43, 43, 2 );
  StrSplit( CurToStrAlt( d.Sum_Payer, null, null, GetISOCode( d.FIID_Payer) ), SS,    76, 76, 2 );
  StrSplit( d.Ground,             SG,    87, 67, 3 );
  StrSplit( Payer,                SP,    43, 43, 4 );
  StrSplit( Receiver,             SR,    43, 43, 4 );

  NameDocument    = "МЕМОРИАЛЬНЫЙ";
  if( IsCORRECTING( d.TypeDocument ) )
    NameDocument = "ИСПРАВИТЕЛЬНЫЙ";
  end;

  if( d.Date_Carry != date(0, 0, 0) )
    dateDoc = d.Date_Carry;
  end;

  while( ncopy > 0 )
    [┌──────────────────────────┬─────────────────┬────────────────────────────────────────────┐
     │  ############## ОРДЕР N  │ ############### │ #######################         ####       │
     │                          └─────────────────┘ ───────────────────────                    │
     │                                                     /Дата/                              │
     ├──────────┬──────────────────────────────────────────────────────────────────────────────┤
     │ Сумма    │ ############################################################################ │
     │ прописью │ ############################################################################ │
     ├──────────┴──────────────────────────────────┬──────────┬────────────────────────────────┤
     │ ########################################### │  Сумма   │ ############################   │]
      ( NameDocument:r, d.Numb_Document:c, dateDoc:m:c:f, SPOD_str, SS(0), SS(1), SP(0), d.Sum_Payer:l:f );
    offSet = RubAmount( d, SP(1) );
    [│ ########################################### ├──────────┼────────────────────────────────┤
     │ Плательщик ################################ │  Сч.N    │ #########################      │]
    ( SP(1+offSet), SP(2+offSet), d.Account_Payer );
    ExRatePayerAccount( d );
    [├─────────────────────────────────────────────┼──────────┤                                │
     │ ########################################### │  БИК     │ #################              │
     │ ########################################### ├──────────┤                                │
     │ Банк плательщика                            │  Сч.N    │ #########################      │
     ├─────────────────────────────────────────────┼──────────┼────────────────────────────────┤
     │ ########################################### │  БИК     │ #################              │
     │ ########################################### ├──────────┤                                │
     │ Банк получателя                             │  Сч.N    │ #########################      │
     ├─────────────────────────────────────────────┼──────────┤                                │
     │ ########################################### │  Сч.N    │ #########################      │]
     (SBP(0), GetCodeBik(d.Date_Carry,{MFO_Bank}),
      SBP(1), {CORAC_Bank},
      SBR(0), GetCodeBik(d.Date_Carry,{MFO_Bank}),
      SBR(1), {CORAC_Bank},
      SR(0),  d.Account_Receiver );
    offSet = ExRateReceiverAccount( d, SR(1) );
    [│ ########################################### ├──────────┼────────┬──────────┬────────────┤
     │ ########################################### │ Шифр.док.│ ###### │Срок плат.│            │
     │ ########################################### ├──────────┤        ├──────────┤            │
     │                                             │Назн.плат.│        │Очер.плат.│            │
     │ Получатель                                  │   Код    │        │Рез.поле  │            │
     ├─────────────────────────────────────────────┴──────────┴────────┴──────────┴────────────┤
     │ Назначение платежа  ################################################################### │
     │ ####################################################################################### │
     │ ####################################################################################### │
     │                                                                                         │
     │                              Подписи                                                    │
     └─────────────────────────────────────────────────────────────────────────────────────────┘
     ](SR(1+offSet), SR(2+offSet), d.Shifr_oper,
        SR(3),
        SG(0), SG(1), SG(2) );

    ncopy = ncopy - 1;
  end;

  return TRUE;

END;

MACRO PutPlat( a, ncopy)
  RECORD d( "acctrn.dbt", "bank.def" );
  SetBuff( d, a);
  PutPlatI( d, NULL, ncopy);
END;

MACRO PutPlatInf( a, d_inf)
  RECORD d( "acctrn.dbt", "bank.def" );
  SetBuff( d, a );
  PutPlatI( d, NULL, 1 );
END;
