/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                 Формирование блока целевой информации                    */
/*                                                                          */
/*  Имя файла: clbsource.mac                                                */
/*  Создан:    02.07.04                                      Копачев Д.В.   */
/****************************************************************************/

import PaymInter /* для работы с RsbPayment */;
import BankInter; /* для работы с RsbMemorialOrder и RsbMultyDoc */
import CashInter; /* для работы с RsbCashOrder */
import PsInter;
import rsd, oralib, likepy;

private const TAG_DELIMETER = "\n"; /* Разделитель данных при формировании блока */

/* Формирование строки */
private macro AddAttribute( attribute, SumStr:string, len_Str:integer )
  var День, Месяц, Год, pos;

  if( ValType(attribute) == V_DATE )
    datesplit( attribute, День, Месяц, Год );

    if( День  < 10 )   День  = String( "0", день  );  end;
    if( Месяц < 10 )   Месяц = String( "0", месяц );  end;

    if( Len_Str == 6 ) /* формат ДДММГГ */
      pos = 3;
    else               /* формат ДДММГГГГ */
      pos = 1;
    end;

    Год = SubStr( String( год ), pos );

    SumStr = SumStr + String( День, Месяц, Год );
  else
    SumStr = SumStr + string( attribute:len_Str );
  end;

  SetParm( 1, SumStr );
end;

/* Преобразованные значения полей должны быть объединены в
   одну ASCII-строку с разделителем LF (0x0a) */
private macro AddAttrRules( str );
  return String( str, TAG_DELIMETER );
end;

/* Строки (STRING) выводятся с заменой символов CR (0x0d) и LF (0x0a)
   на пробелы (0x20) и последующим удалением лидирующих и завершающих
   пробельных символов (0x20, 0x09). Строки, таким образом, имеют
   переменную длину, определяемую их значимым содержимым. */
private macro AddAttrString( attribute, SumStr:string )
  SumStr = SumStr + AddAttrRules( trim( StrSubst( attribute, "\n", " " ) ) );
  SetParm( 1, SumStr );
end;

/* Номера счетов (SNR) выводятся как строка без каких-либо преобразований */
private macro AddAttrSnr( attribute, SumStr:string )
  SumStr = SumStr + AddAttrRules( attribute );
  SetParm( 1, SumStr );
end;

/* Денежное значение (MONEY) выводится как строка, представляющая старшую
   денежную единицу целым числом без лидирующих нулей, и, через точку -
   младшую денежную единицу (ровно два знака с дополнением нулями слева). */
private macro AddAttrMoney( attribute, SumStr:string )
  while( SubStr( attribute, 1, 1 ) == "0" )
    attribute = substr( attribute, 2 );
  end;
  SumStr = SumStr + AddAttrRules( attribute );
  SetParm( 1, SumStr );
end;

/* Дата выводится в виде шестибайтовой строки ДДММГГ (т.е. с лидирующими нулями) */
private macro AddAttrData( attribute, SumStr:string )
  AddAttribute( attribute, SumStr, 6 );
  SumStr = AddAttrRules( SumStr );
  SetParm( 1, SumStr );
end;

/* Числовые значения (INT, LONG) выводятся без лидирующих нулей.
   Число 0 выводится как строка, содержащая один символ "0". */
private macro AddAttrInt( attribute, SumStr:string );
  while( SubStr( attribute, 1, 1 ) == "0" )
    attribute = substr( attribute, 2 );
  end;
  SumStr = SumStr + AddAttrRules( attribute );
  SetParm( 1, SumStr );
end;


/* Метод формирование БЦИ "Ключевание Клиент-Сбербанк"
   RsPaym      - сконструированный объект класса RsbPayment
   SourceAttrs - набор атрибутов, участвующих в формированиии БЦИ
   Возвращает блок данных для формата БЦИ */
macro ClbSourceKeyed( RsPaym:RsbPayment, SourceAttrs )
  var PIB:string = "";

  AddAttribute( "",                                              PIB, 3    ); /* N_PACK     3   */
  AddAttribute( SubStr( string(RsPaym.Number), 1, 7 ),           PIB, 7    ); /* N_DOCUMENT 7   */
  AddAttribute( RsPaym.Date,                                     PIB, 8    ); /* DATADOC    8   */
  AddAttribute( RsPaym.PayDate,                                  PIB, 8    ); /* DATAPAY    8   */
  AddAttribute( SubStr( RsPaym.Priority),                        PIB, 3    ); /* ORDER      3   */
  AddAttribute( SubStr( RsPaym.PayerAccount, 1, 3 ),             PIB, 25   ); /* ACC_PAYER  25  */
  AddAttribute( SubStr( RsPaym.PayerName, 1, 160 ),              PIB, 160  ); /* PAYER      160 */
  AddAttribute( SubStr( RsPaym.PayerINN, 1, 12 ),                PIB, 12   ); /* INN_PAYER  12  */
  AddAttribute( SubStr( RsPaym.PayerAmount ),                    PIB, 20   ); /* SUM        20  */
  AddAttribute( "",                                              PIB, 14   ); /* TEL        14  */
  AddAttribute( SubStr( string(RsPaym.ReceiverBankCode), 1, 9 ), PIB, 9    ); /* MFO        9   */
  AddAttribute( "",                                              PIB, 9    ); /* MFORKC     9   */
  AddAttribute( "",                                              PIB, 10   ); /* UCH_SB     10  */
  AddAttribute( SubStr( RsPaym.ReceiverCorrAccNostro, 1, 25),    PIB, 25   ); /* ACC_BANK   25  */
  AddAttribute( SubStr( RsPaym.ReceiverName, 1, 160 ),           PIB, 160  ); /* RECEIVER   160 */
  AddAttribute( SubStr( RsPaym.ReceiverAccount, 1, 25),          PIB, 25   ); /* ACC_REC    25  */
  AddAttribute( SubStr( RsPaym.ReceiverINN, 1, 12 ),             PIB, 12   ); /* INN_REC    12  */
  AddAttribute( SubStr( RsPaym.Ground, 1, 210 ),                 PIB, 210  ); /* GROUND     210 */
  AddAttribute( "",                                              PIB, 10   ); /* UNNUM      10  */
  AddAttribute( "",                                              PIB, 8    ); /* TIME       8   */
  AddAttribute( "",                                              PIB, 5    ); /* SUM_OTPR   5   */
  AddAttribute( "",                                              PIB, 10   ); /* COD_UCH    10  */

  return PIB; /* Успешное завершение */
end;

/* Обертка для удобства Мемориальный ордер */
class PseudoMOPayment( MoPayment:RsbMOPayment )
  var Payment:RsbMOPayment = MoPayment; // платеж
  var Memorial:object = GenObject("RsbMemorialOrder", Payment.PaymentID); // первичка

  MACRO PayerFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAmount():money
    return Payment.PayerAmount;
  END;
  MACRO ReceiverFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAccount():string
    return Payment.PayerAccount;
  END;
  MACRO ReceiverAccount():string
    return Payment.ReceiverAccount;
  END;
  MACRO ValueDate():date
    return Payment.ValueDate;
  END;
  MACRO PayerINN():string
    return Payment.PayerINN;
  END;
  MACRO ReceiverBankName():string
    return Payment.ReceiverBankName;
  END;
  MACRO ReceiverINN():string
    return Payment.ReceiverINN;
  END;
  MACRO ShifrOper():string
    return Payment.ShifrOper;
  END;
  MACRO Ground():string
    return Payment.Ground;
  END;
  MACRO ReceiverBankCode():string
    return Payment.ReceiverBankCode;
  END;
  MACRO Number():string
    return Payment.Number;
  END;
end;

/* Обертка для удобства  Мультивалютный документ */
class PseudoMDPayment( McPayment:RsbMDPayment )
  var Payment:RsbMDPayment = McPayment; // платеж

  MACRO PayerFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAmount():money
    return Payment.PayerAmount;
  END;
  MACRO ReceiverFIID():integer
    return Payment.ReceiverFIID;
  END;
  MACRO PayerAccount():string
    return Payment.PayerAccount;
  END;
  MACRO ReceiverAccount():string
    return Payment.ReceiverAccount;
  END;
  MACRO ValueDate():date
    return Payment.ValueDate;
  END;
  MACRO PayerINN():string
    return Payment.PayerINN;
  END;
  MACRO ReceiverBankName():string
    return Payment.ReceiverBankName;
  END;
  MACRO ReceiverINN():string
    return Payment.ReceiverINN;
  END;
  MACRO ShifrOper():string
    return Payment.ShifrOper;
  END;
  MACRO Ground():string
    return Payment.Ground;
  END;
  MACRO ReceiverBankCode():string
    return Payment.ReceiverBankCode;
  END;
  MACRO Number():string
    return Payment.Number;
  END;
end;

/* Обертка для удобства  Кассовый документ */
class PseudoCSPayment( _CSPayment:RsbCOPayment )
  var Payment:RsbCOPayment = _CSPayment; // платеж

  MACRO PayerFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAmount():money
    return Payment.PayerAmount;
  END;
  MACRO ReceiverFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAccount():string
    return Payment.PayerAccount;
  END;
  MACRO ReceiverAccount():string
    return Payment.ReceiverAccount;
  END;
  MACRO ValueDate():date
    return Payment.ValueDate;
  END;
  MACRO PayerINN():string
    return Payment.PayerINN;
  END;
  MACRO ReceiverBankName():string
    return "";
  END;
  MACRO ReceiverINN():string
    return Payment.ReceiverINN;
  END;
  MACRO ShifrOper():string
    return "";
  END;
  MACRO Ground():string
    return Payment.Ground;
  END;
  MACRO ReceiverBankCode():string
    return Payment.ReceiverBankCode;
  END;
  MACRO Number():string
    return Payment.Number;
  END;
end;

/* Обертка для удобства  Банковский ордер */
class PseudoBankOrder( _BankOrder:RsbBankOrder )
  var Payment:RsbBankOrder = _BankOrder; // платеж

  MACRO PayerFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAmount():money
    return Payment.BaseAmount;
  END;
  MACRO ReceiverFIID():integer
    return Payment.ReceiverFIID;
  END;
  MACRO PayerAccount():string
    return Payment.PayerAccount;
  END;
  MACRO ReceiverAccount():string
    return Payment.ReceiverAccount;
  END;
  MACRO ValueDate():date
    return Payment.ValueDate;
  END;
  MACRO PayerINN():string
    return Payment.PayerINN;
  END;
  MACRO ReceiverBankName():string
    return Payment.ReceiverBankName;
  END;
  MACRO ReceiverINN():string
    return Payment.ReceiverINN;
  END;
  MACRO ShifrOper():string
    return Payment.ShifrOper;
  END;
  MACRO Ground():string
    return Payment.Ground;
  END;
  MACRO ReceiverBankCode():string
    return "";
  END;
  MACRO Number():string
    return "";
  END;
end;

/* Обертка для удобства  Банковский ордер */
class PseudoSumMemOrder( _SumMemOrder:RsbPayment )
  var Payment:RsbPayment = _SumMemOrder; // платеж

  MACRO PayerFIID():integer
    return Payment.PayerFIID;
  END;
  MACRO PayerAmount():money
    return Payment.PayerAmount;
  END;
  MACRO ReceiverFIID():integer
    return Payment.ReceiverFIID;
  END;
  MACRO PayerAccount():string
    return Payment.PayerAccount;
  END;
  MACRO ReceiverAccount():string
    return Payment.ReceiverAccount;
  END;
  MACRO ValueDate():date
    return Payment.ValueDate;
  END;
  MACRO PayerINN():string
    return Payment.PayerINN;
  END;
  MACRO ReceiverBankName():string
    return "";
  END;
  MACRO ReceiverINN():string
    return Payment.ReceiverINN;
  END;
  MACRO ShifrOper():string
    return "";
  END;
  MACRO Ground():string
    return Payment.Ground;
  END;
  MACRO ReceiverBankCode():string
    return "";
  END;
  MACRO Number():string
    return "";
  END;
end;

/* Метод формирования БЦИ "Единый БЦИ РДД"
   RsPaym      - сконструированный объект класса RsbPayment (или RsbMemorialOrder, или RsbMultyDoc)
   SourceAttrs - набор атрибутов, участвующих в формированиии БЦИ
   Возвращает блок данных для БЦИ */
macro PaymentPIBSource( RsPaym:object, SourceAttrs )
  var PIB:string = "";
  var obj:object;
  var IsMass:bool = false, retval:bool;
  var t_Macro:string = "clbsource.mac", t_MacroFunc:string = "PaymentPIBSource";
  var params:TArray;
  var PmAddPIArray:TArray;
  var stat = 0, i = 0;

  if( getparm(2,IsMass) and (IsMass == true) ) // массовый режим
    params = makeArray( SQLParam( "p_Macro", t_Macro ),
                        SQLParam( "p_MacroFunc", t_MacroFunc )
                      );
    retval = execStoredFunc( "PM_SgnMass.MakePaymentPIB", V_INTEGER, params );
    return retval;
  else
    if( (StrUpr(GenClassName(RsPaym)) == "RSBPAYMENT"  ) or
        (StrUpr(GenClassName(RsPaym)) == "RSBCPPAYMENT") or
        (StrUpr(GenClassName(RsPaym)) == "RSBBBPAYMENT") or
        (StrUpr(GenClassName(RsPaym)) == "RSBPSPAYMENT") )
      obj = RsPaym;
    elif( StrUpr(GenClassName(RsPaym)) == "RSBMOPAYMENT" )
      obj = PseudoMOPayment(RsPaym);
    elif( StrUpr(GenClassName(RsPaym)) == "RSBMDPAYMENT" )
      obj = PseudoMDPayment(RsPaym);
    elif( StrUpr(GenClassName(RsPaym)) == "RSBCOPAYMENT" )
      obj = PseudoCSPayment(RsPaym);
    elif( StrUpr(GenClassName(RsPaym)) == "RSBBANKORDER" )
      obj = PseudoBankOrder(RsPaym);
    elif( StrUpr(GenClassName(RsPaym)) == "RSBSUMMARYMEMORIALORDER" )
      obj = PseudoSumMemOrder(RsPaym);
    else
      MsgBox("Нельзя использовать метод формирования БЦИ для объекта " + GenClassName(RsPaym));
      return PIB;
    end;

    AddAttrInt   ( obj.PayerFIID,        PIB );
    AddAttrMoney ( obj.PayerAmount,      PIB );
    AddAttrInt   ( obj.ReceiverFIID,     PIB );
    AddAttrSnr   ( obj.PayerAccount,     PIB );
    AddAttrSnr   ( obj.ReceiverAccount,  PIB );
    AddAttrData  ( obj.ValueDate,        PIB );
    AddAttrString( obj.PayerINN,         PIB );
    AddAttrString( obj.ReceiverBankName, PIB );
    AddAttrString( obj.ReceiverINN,      PIB );
    AddAttrString( obj.ShifrOper,        PIB );
    AddAttrString( obj.Ground,           PIB );
    AddAttrString( obj.ReceiverBankCode, PIB );
    AddAttrString( obj.Number          , PIB );

    // для сводных добавляем еще атрибуты разноски
    if( StrUpr(GenClassName(RsPaym)) == "RSBSUMMARYMEMORIALORDER" )
      PmAddPIArray = RsPaym.PIList(PRT_Debet).AsTArray( stat );
      if( stat )
        PmAddPIArray = RsPaym.PIList(PRT_Credit).AsTArray( stat );
        if( stat )
          DisplayError();
        end;
      end;
      AddAttrInt( PmAddPIArray.Size(), PIB );
      while( i < PmAddPIArray.Size() )
        AddAttrInt   ( PmAddPIArray[i].rec.DebetCredit , PIB );
        AddAttrInt   ( PmAddPIArray[i].rec.PmFIID      , PIB );
        AddAttrMoney ( PmAddPIArray[i].rec.PmAmount    , PIB );
        AddAttrInt   ( PmAddPIArray[i].rec.FIID        , PIB );
        AddAttrSnr   ( PmAddPIArray[i].rec.Account     , PIB );
        AddAttrMoney ( PmAddPIArray[i].rec.Amount      , PIB );
        i = i + 1;
      end;
    end;

    return PIB; /* Успешное завершение */
  end;
end;
