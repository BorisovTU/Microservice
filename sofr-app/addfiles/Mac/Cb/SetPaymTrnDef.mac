/*
  $Name:        SetPaymTrnDef.mac
  $Module:      Ядро Банкинг
  $Description: Установка атрибутов проводок по платежу
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Описание     : Установка атрибутов проводок по платежу
//
// Программист  : Чукина Т.А.
//
// Создан       : 02.02.2016
//
//-----------------------------------------------------------------------------

import globals, PaymInter, "pm_const.mac", oralib, likepy;

private macro SetOfrSymbol( AccTrn : RsbAccTransactionData, PaymentID: integer, PmAddPIID : integer, MassCarryData )
  
  if(AccTrn.Chapter == CHAPT1)
    var OfrSymbol : string = "";

    if( MassCarryData != null )  // Массовый режим
      OfrSymbol = IfThenElse(MassCarryData.PIOFRSymbol != "", MassCarryData.PIOFRSymbol, MassCarryData.OFRSymbol );
    else // Единичный режим
      var PmObj = RsbPayment(PaymentID);

      if( PmAddPIID > 0 ) // Смотрим разноску
        var r_pmaddpi:TRecHandler = TRecHandler("pmaddpi.dbt");

        if( PmObj.PIList(PRT_Debet).Find( PmAddPIID, r_pmaddpi ) == 0 )
          OfrSymbol = r_pmaddpi.rec.OFRSymbol;
        elif( PmObj.PIList(PRT_Credit).Find( PmAddPIID, r_pmaddpi ) == 0 )
          OfrSymbol = r_pmaddpi.rec.OFRSymbol;
        end;
      end;

      if( OfrSymbol == "" )  // Если в разноске нет, то берем из примечания
        OfrSymbol = PmObj.Notes.ReadNote(NOTEKIND_PAYM_OFRSYMBOL, {curdate});
      end;
    end;

    if( OfrSymbol != "" ) // Устанавливаем в проводке
      AccTrn.AddOFRSymbol(OfrSymbol);
    end;
  end;

  return 0;
end;

/*
    Дополнительная доинициализация параметров проводки по платежу
    при единичном исполнении вызывается из сишника
    при массовой проводке надо звать напрямую из макроса (см пример зачисления платежа)

    AccTrn - проводка
    PaymentID - ид платежа
    PmAddPIID - ид разноски
    MassCarryData - данные проводки при массовом исполнении. Если задана, то это массовый режим
    
    !!! В массовом режиме все данные берутся из MassCarryData. В массовом режиме нельзя создавать Rsb-объекты, нельзя делать единичные запросы в БД.!!!

*/
macro SetPaymTransactionDef( AccTrn:RsbAccTransactionData, PaymentID:integer, PmAddPIID:integer, MassCarryData ) : integer

  var stat : integer = 0;

  // Пока реализовать в функции установку символа ОФР.
  if(not stat)
    stat = SetOfrSymbol(AccTrn, PaymentID, PmAddPIID, MassCarryData );
  end;

  return stat;
end;
