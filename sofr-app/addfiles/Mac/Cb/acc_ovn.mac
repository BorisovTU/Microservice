/*
  $Name:         acc_ovn.mac
  $Module:       Бухгалтерия банка
  $Description:  Макрос шага
  Блок      : 10005 - "Урегулирование счетов в овердрафтом
                      (неинтегрированный режим)"
  Шаг       : 510   - "Обработка овердрафта (неинтегрированный режим)"
*/
import InsCarryDoc, "pm_categ.mac", CurrInter;
import oralib, likepy;
import cb_sql;

record accop     ( "accopsrv.dbt" ); // Сервисная операция
record account   ( "account.dbt"  ); // Основной счет
record acc_link  ( "account.dbt"  ); // Счет овердрафта
record acc_notbal( "account.dbt"  ); // Внебалансовый счет лимита по овердрафту

// Получить остаток на счете
private macro FindAccRest( FIID:integer, Chapter:integer, Acc:string )

  return execStoredFunc( "rsb_account.restac", V_MONEY, makeArray( SQLParam( "" , Acc      ),
                      SQLParam( "" , FIID     ),
                      SQLParam( "" , {curdate}),
                      SQLParam( "" , Chapter  ),
                      SQLParam( "" , 0        )) );
end;

// Получить минимальную из двух сумм
private macro MinSum( Sum1, Sum2 )
  if( Sum1 < Sum2 )
    return Sum1;
  else
    return Sum2;
  end;
end;

MACRO ExecuteStep( doc, paymDoc )

  var AccountRest       = FindAccRest( account.Code_Currency, account.Chapter, account.Account );
  var AccountLinkRest   = FindAccRest( acc_link.Code_Currency, acc_link.Chapter, acc_link.Account );
  var AccountNotBalRest = FindAccRest( acc_notbal.Code_Currency, acc_notbal.Chapter, acc_notbal.Account );

  var FD_CorrAcc:NotBalCorrAcc_FirstDoc = NotBalCorrAcc_FirstDoc( "А" );
  
  var СчетКорреспонденции:string = FD_CorrAcc.FindAndOpenSysCurAccount( "ВнебалСчетКорресп", 0, {curdate}, account.Code_Currency);

  var PayAmount:money = $0;
  var RepayAmount:money = $0;

  var AccountFreeRest = $0;
  /*if( AccGetFreeAmount( AccountFreeRest, NULL, account.Account, account.Chapter, account.Code_Currency, {curdate} ) )
    msgbox("Ошибка при определении свободного остатка");
    return 1;
  end;*/
  AccountFreeRest = GetFreeAmountForOverdraft( account.Account, account.Chapter, account.Code_Currency, {curdate} );

  if( strlen(СчетКорреспонденции) == 0 )
    msgbox("Ошибка при поиске счета корреспонденции");
    return 1;
  end;

  if( AccountRest == 0 )
   return 0; // Если нулевой остаток - не надо обрабатывать
  end;

  if( AccountLinkRest < 0 )
    AccountLinkRest = -AccountLinkRest;
  end;

  var acctrn1:RsbAccTransaction = RsbAccTransaction(); // Проводка по главе 1
  var acctrn3:RsbAccTransaction = RsbAccTransaction(); // Проводка по главе 3

  if( acctrn1 == NULL )
    MsgBox("Ошибка при создании проводки по счету");
    return 1;
  end;

  if( acctrn3 == NULL )
    MsgBox("Ошибка при создании проводки по счету");
    return 1;
  end;

  if( AccountRest > 0 ) // Остаток положительный - погашение

    PayAmount = MinSum( abs(AccountFreeRest), abs(AccountLinkRest) );

    if( PayAmount == 0 )
      return 0;
    end;

    // Проводка по балансу - погашение овердрафтного кредита
    acctrn1.Chapter         = 1;
    acctrn1.Date_Carry      = {curdate};  /* Пока что делаем текущей датой */
    acctrn1.ResultCarry     = REPAYOVERCARRY;
    acctrn1.Kind_Oper       = " 1";
    acctrn1.Ground          = "Погашение овердрафтного кредита";
    acctrn1.Department      = account.Department;
    acctrn1.AccountPayer    = account.Account;
    acctrn1.AccountReceiver = acc_link.Account;
    acctrn1.FIIDPayer       = account.Code_Currency;
    acctrn1.FIIDReceiver    = acc_link.Code_Currency;
    acctrn1.SumPayer        = PayAmount;
    acctrn1.SumReceiver     = PayAmount;

    // Проводка по внебалансу - восстановление неиспользованного лимита
    acctrn3.Chapter         = 3;
    acctrn3.Date_Carry      = {curdate};  /* Пока что делаем текущей датой */
    acctrn3.ResultCarry     = REPAYOVERCARRY;
    acctrn3.Kind_Oper       = " 1";
    acctrn3.Ground          = "Восстановление неиспользованного лимита овердрафта";
    acctrn3.Department      = account.Department;
    acctrn3.AccountPayer    = СчетКорреспонденции;
    acctrn3.AccountReceiver = acc_notbal.Account;
    acctrn3.FIIDPayer       = account.Code_Currency;
    acctrn3.FIIDReceiver    = acc_notbal.Code_Currency;
    acctrn3.SumPayer        = PayAmount;
    acctrn3.SumReceiver     = PayAmount;

    // Вставить запись изменения лимита
    if( InsertChangeLimit( account.Code_Currency, account.Chapter, account.Account, PayAmount ) )
      MsgBox("Ошибка при вставке изменения лимита счета");
      return 1;
    end;

  else // Остаток отрицательный - выдача

    RepayAmount = MinSum( abs(AccountNotBalRest), abs(AccountRest) );

    if( RepayAmount == 0 )
      return 0;
    end;

    // Проводка по балансу - выдача овердрафтного кредита
    acctrn1.Chapter         = 1;
    acctrn1.Date_Carry      = {curdate};  /* Пока что делаем текущей датой */
    acctrn1.ResultCarry     = PAYOVERCARRY;
    acctrn1.Kind_Oper       = " 1";
    acctrn1.Ground          = "Выдача овердрафтного кредита";
    acctrn1.Department      = account.Department;
    acctrn1.AccountPayer    = acc_link.Account;
    acctrn1.AccountReceiver = account.Account;
    acctrn1.FIIDPayer       = acc_link.Code_Currency;
    acctrn1.FIIDReceiver    = account.Code_Currency;
    acctrn1.SumPayer        = RepayAmount;
    acctrn1.SumReceiver     = RepayAmount;

    // Проводка по внебалансу - использование лимита овердрафта
    acctrn3.Chapter         = 3;
    acctrn3.Date_Carry      = {curdate};  /* Пока что делаем текущей датой */
    acctrn3.ResultCarry     = PAYOVERCARRY;
    acctrn3.Kind_Oper       = " 1";
    acctrn3.Ground          = "Использование лимита овердрафта";
    acctrn3.Department      = account.Department;
    acctrn3.AccountPayer    = acc_notbal.Account;
    acctrn3.AccountReceiver = СчетКорреспонденции;
    acctrn3.FIIDPayer       = acc_notbal.Code_Currency;
    acctrn3.FIIDReceiver    = account.Code_Currency;
    acctrn3.SumPayer        = RepayAmount;
    acctrn3.SumReceiver     = RepayAmount;
  
  end;

  if( not acctrn1.Carry )
    MsgBox("Ошибка при актуализации проводки");
    return 1;
  end;
  
  if( not acctrn3.Carry )
    MsgBox("Ошибка при актуализации проводки");
    return 1;
  end;

  if(AccountRest <= 0)
    if( InsertChangeLimit( account.Code_Currency, account.Chapter, account.Account, -RepayAmount) )
      MsgBox("Ошибка при вставке изменения лимита счета");
      return 1;
    end; 
  end;

  return 0;

END;
