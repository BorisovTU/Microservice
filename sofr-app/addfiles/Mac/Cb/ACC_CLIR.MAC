/*──────────────────────────────────────────────────────────────────────────────
Latyev A.L., Support Group, R-Style Software Lab.
Create Data: 10.02.1999
File:        acc_clir.mac
Name:        Формирование БД связи 4 символов номера счета и кода клиринга
             учреждения получателя.
Resume:      БД создается в каталоге DBFILE, используется в дополнительной 
             панели клиринговых платежей. Для своего формирования требует
             справочник, рассылаемый ТРЦ (clirindx.dbf)
──────────────────────────────────────────────────────────────────────────────*/
file acc (accclir) write;
file cl () dbf;
var GoodAdd = 0, All = 0, GoodIsp = 0, fil;
/*clone (acc);*/
if (not open(acc))
        msgbox ("Проблемы с результирующим файлом.");
        exit (1);
end;
if (not selectfile(fil, "..\\dbf\\*indx.dbf", "Выберите файл для обработки."));
        exit (1);
end;
open (cl, fil);
rewind (cl);
while (next(cl))         
        All = All + 1;
        message(cl.Nameuc);
        acc.Un_Code = substr(trim(cl.Ucsb), 1, 2)+substr(trim(cl.Indo), 1, 2);
        if (GetEQ(acc))
                if (acc.Code_Cliring != trim(cl.Ucsb)+"0000") 
                        acc.Code_Cliring = trim(cl.Ucsb)+"0000";
                        acc.Name_Depart = trim(cl.Nameuc);
                        [Исправление данных по участнику ########## #](acc.Code_Cliring, acc.Name_Depart);
                        if (Update(acc))
                                GoodIsp = GoodIsp + 1;
                                [Исправление прошло успешно.];
                        else
                                [Исправление прошло неуспешно.];
                        end;
                end;
        else
                acc.Un_Code = substr(trim(cl.Ucsb), 1, 2)+substr(trim(cl.Indo), 1, 2);
                acc.Code_Cliring = trim(cl.Ucsb)+"0000";
                acc.Name_Depart = trim(cl.Nameuc);
                [Занесение данных по участнику ########## #](acc.Code_Cliring, acc.Name_Depart);
                if (Insert (acc))
                        [Занесение прошло успешно.];
                        GoodAdd = GoodAdd + 1;
                else
                        [Занесение прошло неуспешно.];
                end;
        end;
end;
msgbox ("Всего обработано "+string(All)+ " кодов, |Из них исправлено "+string(GoodIsp)+" участников, |Занесено "+string(GoodAdd)+ " кодов.");
