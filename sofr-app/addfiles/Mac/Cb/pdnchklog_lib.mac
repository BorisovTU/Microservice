/*
$Name: pdnchklog_lib.mac
$Module: Ядро ГКБО 
$Description: Протокол проверки обработки ПДн
*/

import "global.mac", ptinter, oralib, RsbDataSet, cb_sql, rsd, commonutil, likepy;

const ЗавершениеОбработкиПДн = 1; /*Завершение обработки ПДн*/
const НеобходимоУничтожитьПДн = 2;/*Необходимо уничтожить ПДн*/
const ПредупреждениеОНеобходимостиУничтоженияПДн = 3;/*Предупреждение о необходимости уничтожения ПДн*/
const НеОформленаОбработкаПДн = 4;/*Не оформлена обработка ПДн*/
const ЗавершениеОбработкиПДнОшибка = 5;/*Функция проверки завершения обработки ПДн возвратила признак ошибки */

macro PdnChkLogClear()
  SQL_Execute("DELETE FROM dpdnchklog_tmp", "Очистка файла протокола...");
end;

macro PdnChkLogAddLine(pRecordType, pPartyID, pPdnTypeParty, pCmpltPrcssID, pDate, pCmpltPrcssFn)
  file LogFile("pdnchklog.tmp") write;
  ClearRecord(LogFile);

  LogFile.RecordType = pRecordType;
  LogFile.PartyID = pPartyID;
  if(pPdnTypeParty != null)
    LogFile.PdnTypeParty = pPdnTypeParty;
  end;
  if(pCmpltPrcssID != null)
    LogFile.CmpltPrcssID = pCmpltPrcssID;
  end;
  if (pCmpltPrcssFn != null)
    LogFile.CmpltPrcssFunc = pCmpltPrcssFn;
  end;
  if(pDate != null)
    LogFile.Date = pDate;
  end;
  
  Insert( LogFile );
end;

macro GetPartyFieldByID(PartyID)
  var query;
  var rs;
  var params : TArray;
  var PartyName = "все";

  if(PartyID > 0)
    CaptureOutput;
[
SELECT pt.t_PartyID, NVL(pc.t_Code, CHR(1)), pt.t_Name 
  FROM dparty_dbt pt, dpartcode_dbt pc 
 WHERE pc.t_PartyID(+) = pt.t_PartyID AND pc.t_CodeKind(+) = 1 AND pt.t_PartyID = :PartyID
];
    query = StopCaptureOutput;
    params = makeArray(SQLParam("PartyID", PartyID)
                    );
    rs = execSQLselect(query, params, true);
    if(rs and rs.moveNext())
      PartyName = PartyID + " " + rs.value(1) + " " + rs.value(2);
    end;
  end;

  return PartyName;
end;

private macro _GetPartyXmlTag(PartyID, Code1, FIO)
  return "<субъект>" + 
         "<id_субъекта>" + PartyID + "</id_субъекта>" + 
         "<код1>" + Code1 + "</код1>" +
         "<фио>" + FIO + "</фио>" +
         "</субъект>";
end;

macro GetPartyFieldByID_Xml(PartyID)
  var query;
  var rs;
  var params : TArray;
  var PartyName = "все";

  if(PartyID > 0)
    CaptureOutput;
[
SELECT pt.t_PartyID, NVL(pc.t_Code, CHR(1)), pt.t_Name 
  FROM dparty_dbt pt, dpartcode_dbt pc 
 WHERE pc.t_PartyID(+) = pt.t_PartyID AND pc.t_CodeKind(+) = 1 AND pt.t_PartyID = :PartyID
];
    query = StopCaptureOutput;
    params = makeArray(SQLParam("PartyID", PartyID)
                    );
    rs = execSQLselect(query, params, true);
    if(rs and rs.moveNext())
      PartyName = _GetPartyXmlTag(PartyID, rs.value(1), rs.value(2));
    end;
  end;

  return PartyName;
end;

macro GetPdnPartyKindFieldByID(PdnPartyKind)
  var query;
  var rs;
  var params : TArray;
  var PdnPartyKindName = "все";

  if(PdnPartyKind > 0)
    CaptureOutput;
[
SELECT t_Name 
  FROM dpdnpartykind_dbt
 WHERE t_PartyKind = :PdnPartyKind
];
    query = StopCaptureOutput;
    params = makeArray(SQLParam("PdnPartyKind", PdnPartyKind)
                    );
    rs = execSQLselect(query, params, true);
    if(rs and rs.moveNext())
      PdnPartyKindName = rs.value(0);
    end;
  end;

  return PdnPartyKindName;
end;

private macro PrintLogTxtTop
(
  OpenCheck, 
  CloseCheck,
  PdnPartyKind,
  PartyID
)
[Протокол проверки обработки персональных данных                                   ];
[                                                                                  ];
[Дата, время: ########## ########                                                  ] (date:f, time:f);                
[Пользователь: ##### #                                                             ] ({oper}, {Name_Oper});   
[                                                                                  ];
[Субъекты ПДн:      #                                                              ] (GetPartyFieldByID(PartyID));
[Вид субъектов ПДн: #                                                              ] (GetPdnPartyKindFieldByID(PdnPartyKind));
[                                                                                  ];
end;

macro GetRsForBranch(RecordType)
  var query;
  var rs;
  var params : TArray;
  CaptureOutput;
[
SELECT pt.t_PartyID, NVL(pc.t_Code, CHR(1)), pt.t_Name, 
       NVL(ppk.t_Name, CHR(1)),
       NVL(pcp.t_Name, CHR(1)),
               pcl.t_Date,
               NVL(fn.t_ID, 0),
               NVL(fn.t_System, CHR(0))
  FROM dpdnchklog_tmp pcl, dparty_dbt pt, dpartcode_dbt pc, 
               dpdntypeparty_dbt ptp, dpdnpartykind_dbt ppk, dpdncmpltprcss_dbt pcp, dpdncmpltprcssfn_dbt fn
 WHERE     pc.t_PartyID(+) = pt.t_PartyID 
       AND pc.t_CodeKind(+) = 1 
       AND pt.t_PartyID(+) = pcl.t_PartyID
       AND ptp.t_ID(+) = pcl.t_PdnTypeParty
       AND ppk.t_PartyKind(+) = ptp.t_PartyKind
       AND pcp.t_ID(+) = pcl.t_CmpltPrcssID
               AND fn.t_PdnCmpltID(+) = pcl.t_CmpltPrcssID
               AND fn.t_ID(+) = pcl.t_cmpltprcssfunc
       AND pcl.t_RecordType = :RecordType
 ORDER BY pcl.t_PartyID, pcl.t_PdnTypeParty
];
  query = StopCaptureOutput;
  
    params = makeArray(SQLParam("RecordType", RecordType));
  rs = execSQLselect(query, params, true);
    
  return rs;
end;

macro GetRsForBranch_4()
  var query;
  var rs;
  var params : TArray;
  CaptureOutput;
[
SELECT pt.t_PartyID, NVL(pc.t_Code, CHR(1)), pt.t_Name
  FROM dpdnchklog_tmp pcl, dparty_dbt pt, dpartcode_dbt pc 
 WHERE     pc.t_PartyID(+) = pt.t_PartyID 
       AND pc.t_CodeKind(+) = 1 
       AND pt.t_PartyID = pcl.t_PartyID
       AND pcl.t_RecordType = :RecordType
 ORDER BY pcl.t_PartyID, pcl.t_PdnTypeParty
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("RecordType", НеОформленаОбработкаПДн)
                    );
  rs = execSQLselect(query, params, true);
  return rs;
end;

private macro PrintLogTxtBranch_1_Errors()
    var RecCount = 0;
    var rs = GetRsForBranch(ЗавершениеОбработкиПДнОшибка);
    
    while(rs.MoveNext())
        if(RecCount == 0)
[                                                                                  ];
[Ошибка функции проверки завершения обработки ПДн:                                 ];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
[| Субъект                                                           | Вид субъекта ПДн             | Вид завершения обработки ПДн |Функция    |];
[|                                                                   |                              |                              |           |];
[|                                                                   |                              |                              |           |];
[|                                                                   |                              |                              |           |];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
        end;
        RecCount = RecCount + 1;
        
[|###################################################################|##############################|##############################|########## |]
(string(int(rs.value(0)) + " " + rs.value(1) + " " + rs.value(2)):w,
 string(rs.value(3)):w,
 string(rs.value(4)):w,
 string(Int(rs.value(6)) + " " + rs.value(7)):w
 );
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+]; 
    end;
end;

private macro PrintLogTxtBranch_1()
  var RecCount = 0;

  var rs = GetRsForBranch(ЗавершениеОбработкиПДн);
  
  while( rs.MoveNext() )
    if( RecCount == 0 )
[                                                                                  ];
[Завершение обработки ПДн:                                                         ];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
[| Субъект                                                           | Вид субъекта ПДн             | Вид завершения обработки ПДн |Дата       |];
[|                                                                   |                              |                              |завершения |];
[|                                                                   |                              |                              |обработки  |];
[|                                                                   |                              |                              |ПДн        |];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
    end;
    RecCount = RecCount + 1;
[|###################################################################|##############################|##############################|########## |]
(string(int(rs.value(0)) + " " + rs.value(1) + " " + rs.value(2)):w,
 string(rs.value(3)):w,
 string(rs.value(4)):w,
 date(rs.value(5)):f
 );
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
  end;
end;

private macro PrintLogTxtBranch_2()
  var RecCount = 0;

  var rs = GetRsForBranch(НеобходимоУничтожитьПДн);
  
  while( rs.MoveNext() )
    if( RecCount == 0 )
[                                                                                  ];
[Необходимо уничтожить ПДн:                                                        ];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
[| Субъект                                                           | Вид субъекта ПДн             | Вид завершения обработки ПДн |Плановая   |];
[|                                                                   |                              |                              |дата       |];
[|                                                                   |                              |                              |уничтожения|];
[|                                                                   |                              |                              |ПДн        |];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
    end;
    RecCount = RecCount + 1;
[|###################################################################|##############################|##############################|########## |]
(string(int(rs.value(0)) + " " + rs.value(1) + " " + rs.value(2)):w,
 string(rs.value(3)):w,
 string(rs.value(4)):w,
 date(rs.value(5)):f
 );
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
  end;
end;

private macro PrintLogTxtBranch_3()
  var RecCount = 0;

  var rs = GetRsForBranch(ПредупреждениеОНеобходимостиУничтоженияПДн);
  
  while( rs.MoveNext() )
    if( RecCount == 0 )
[                                                                                  ];
[Предупреждение о необходимости уничтожения ПДн:                                   ];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
[| Субъект                                                           | Вид субъекта ПДн             | Вид завершения обработки ПДн |Плановая   |];
[|                                                                   |                              |                              |дата       |];
[|                                                                   |                              |                              |уничтожения|];
[|                                                                   |                              |                              |ПДн        |];
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
    end;
    RecCount = RecCount + 1;
[|###################################################################|##############################|##############################|########## |]
(string(int(rs.value(0)) + " " + rs.value(1) + " " + rs.value(2)):w,
 string(rs.value(3)):w,
 string(rs.value(4)):w,
 date(rs.value(5)):f
 );
[+-------------------------------------------------------------------+------------------------------+------------------------------+-----------+];
  end;
end;

private macro PrintLogTxtBranch_4()
  var RecCount = 0;

  var rs = GetRsForBranch_4();
  
  while( rs.MoveNext() )
    if( RecCount == 0 )
[                                                                                  ];
[Не оформлена обработка ПДн:                                                       ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
[##############################################################################################################################################]
(string(int(rs.value(0)) + " " + rs.value(1) + " " + rs.value(2)):w);
  end;
end;

private macro _GetDprtPartyID( DprtID : integer )
  
  file dprt("dp_dep.dbt");
  var PartyID : integer = -1;

  dprt.Code = DprtID;

  if( getEQ(dprt) )
    PartyID = dprt.PartyID;
  end;

  return PartyID;

end;

private macro _GetPersonPost(DprtPartyID : integer, PersonID : integer)
  
  var strSql : string;
  var rs : RsdRecordset;

  var Post = "";

  strSql = "select t_Post from dofficer_dbt where t_PartyID = " + DprtPartyID + " and t_PersonID = " + PersonID;
  
  rs = RsdRecordset( strSql );

  if( rs.MoveNext() )
    Post = rs.value(0);
  end;

  return Post;
end;

private macro _GetPersonPostAndFullName( oper : integer )
  file person("person.dbt");

  var PartyID : integer;
  var PostAndFullName = "";
  
  if( oper )
    person.Oper = oper;
    if( getEQ(person) )
      PostAndFullName = person.Name;
      if( (PostAndFullName != "") and (person.PartyID > 0) )
        PartyID = _GetDprtPartyID(person.CodeDepart);
        if( PartyID > 0 )
          PostAndFullName = _GetPersonPost( PartyID, person.PartyID ) + " " + PostAndFullName;
        end;
      end;
    end;
  end;

  return PostAndFullName;
end;

private macro PrintLogTxtBottom()
[                                                                                  ];
[##################################################################################] (_GetPersonPostAndFullName({oper}));   
end;

macro PrintLogTxt
(
  OpenCheck, 
  CloseCheck,
  PdnPartyKind,
  PartyID,
  CheckFinProc,
  CheckNeedDelete,
  CheckDecor,
  IsScheduler
)
  file LogFile() txt;
  var LogFileName;

  setoutput( GetTxtFileName("pdnchklog_" + strsubst(string(date:f), ".", "") + "_" + strsubst(string(time:f), ":", "")) + ".txt" );/* Перенаправить вывод, чтобы можно было юзать рeальный файл вывода*/

  PrintLogTxtTop
  (
    OpenCheck, 
    CloseCheck,
    PdnPartyKind,
    PartyID
  );

  if(CheckFinProc)
    PrintLogTxtBranch_1_Errors();
    PrintLogTxtBranch_1();
  end;

  if(CheckNeedDelete)
    PrintLogTxtBranch_2();
    PrintLogTxtBranch_3();
  end;

  if(CheckDecor)
    PrintLogTxtBranch_4();
  end;

  PrintLogTxtBottom();

  LogFileName = setoutput(null);

  /*
  if(not IsScheduler)
    if( not Open( LogFile, LogFileName) )
      msgbox("Ошибка открытия файла с протоколом");
    else
      ViewFile( LogFile );
      close( LogFile );
    end;
  end;
  */
  return LogFileName;       
end;

private macro PrintLogXmlTop
(
  OpenCheck, 
  CloseCheck,
  PdnPartyKind,
  PartyID
)
[<?xml version="1.0" encoding="cp866"?>];
[<!--Протокол проверки обработки персональных данных-->];
[<протокол>];
[ <общие_сведения>];
[  <дата>##########</дата>] (date:f);                
[  <время>########</время>] (time:f);                
[  <пользователь>];
[   <номер>
     #####
    </номер>] ({oper});   
[   <фио>
     #
    </фио>] ({Name_Oper});   
[  </пользователь>];
[  <субъекты_пдн>];
[    #] (GetPartyFieldByID_Xml(PartyID));
[  </субъекты_пдн>];
[  <вид_субъектов_пдн>#
   </вид_субъектов_пдн>] (GetPdnPartyKindFieldByID(PdnPartyKind));
[ </общие_сведения>];
end;

private macro PrintLogXmlBranch_1()
  var RecCount = 0;

  var rs = GetRsForBranch(ЗавершениеОбработкиПДн);

[<!--Раздел "Завершение обработки ПДн"-->];
[ <завершение_обработки>];
  
  while( rs.MoveNext() )
[  <обработка_завершена>];
[#] (_GetPartyXmlTag(rs.value(0), rs.value(1), rs.value(2)));
[   <вид_субъекта_пдн>#
    </вид_субъекта_пдн>] (string(rs.value(3)));
[   <вид_завершения>#
    </вид_завершения>] (string(rs.value(4)));
[   <дата_завершения>##########</дата_завершения>] (date(rs.value(5)):f);
[  </обработка_завершена>];

  end;

[ </завершение_обработки>];
end;

private macro PrintLogXmlBranch_2()
  var RecCount = 0;

  var rs = GetRsForBranch(НеобходимоУничтожитьПДн);
  
[<!--Раздел "Необходимо уничтожить ПДн"-->];
[ <необходимо_уничтожить>];
  
  while( rs.MoveNext() )
[  <пдн_для_уничтожения>];
[#] (_GetPartyXmlTag(rs.value(0), rs.value(1), rs.value(2)));
[   <вид_субъекта_пдн>#
    </вид_субъекта_пдн>] (string(rs.value(3)));
[   <вид_завершения>#
    </вид_завершения>] (string(rs.value(4)));
[   <дата_для_уничтожения>##########</дата_для_уничтожения>] (date(rs.value(5)):f);
[  </пдн_для_уничтожения>];

  end;

[ </необходимо_уничтожить>];
end;

private macro PrintLogXmlBranch_3()
  var RecCount = 0;

  var rs = GetRsForBranch(ПредупреждениеОНеобходимостиУничтоженияПДн);
  
[<!--Раздел "Предупреждение о необходимости уничтожения ПДн"-->];
[ <предупреждение>];
  
  while( rs.MoveNext() )
[  <пдн_для_уничтожения>];
[#] (_GetPartyXmlTag(rs.value(0), rs.value(1), rs.value(2)));
[   <вид_субъекта_пдн>#
    </вид_субъекта_пдн>] (string(rs.value(3)));
[   <вид_завершения>#
    </вид_завершения>] (string(rs.value(4)));
[   <дата_для_уничтожения>##########</дата_для_уничтожения>] (date(rs.value(5)):f);
[  </пдн_для_уничтожения>];

  end;

[ </предупреждение>];
end;

private macro PrintLogXmlBranch_4()
  var RecCount = 0;

  var rs = GetRsForBranch_4();
  
[<!--Раздел "Не оформлена обработка ПДн"-->];
[ <не_оформлены_пдн>];
  
  while( rs.MoveNext() )
[#] (_GetPartyXmlTag(rs.value(0), rs.value(1), rs.value(2)));

  end;

[ </не_оформлены_пдн>];
end;

private macro PrintLogXmlBottom()
[</протокол>];
end;

macro PrintLogXml
(
  OpenCheck, 
  CloseCheck,
  PdnPartyKind,
  PartyID,
  CheckFinProc,
  CheckNeedDelete,
  CheckDecor,
  IsScheduler
)
  file LogFile() txt;
  var LogFileName;

  setoutput( GetTxtFileName("pdnchklog_" + strsubst(string(date:f), ".", "") + "_" + strsubst(string(time:f), ":", "")) + ".xml" );/* Перенаправить вывод, чтобы можно было юзать рeальный файл вывода*/

  PrintLogXmlTop
  (
    OpenCheck, 
    CloseCheck,
    PdnPartyKind,
    PartyID
  );

  if(CheckFinProc)
    PrintLogXmlBranch_1();
  end;

  if(CheckNeedDelete)
    PrintLogXmlBranch_2();
    PrintLogXmlBranch_3();
  end;

  if(CheckDecor)
    PrintLogXmlBranch_4();
  end;

  PrintLogXmlBottom();

  LogFileName = setoutput(null);

  return LogFileName;       
end;

