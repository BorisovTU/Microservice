 /*
 $Name: ws_category.mac
 $Module: Главная книга
 $Description: Макрос Категории
 */

import globals;
import RsbDataSet;  
import CurrInter, XmlRpcInter, BankInter, CTInter;   
import oralib;
import serviceaction;
import rcw;
import dlwreps;
import rsd;

import "ws_datafilter.mac", "cb_sql.mac", "ws_validation.mac";
import "ws_file.mac";
import "likepy.mac";

class TWsCategory()
  var GroupID             : integer ;
  var Name                : string  ;
  var FullNameIsBasic     : bool ;
  var Exclusive           : bool ;
  var IsMeanParentNode    : bool ;
  var Order               : integer ;
  var KeepOldValues       : bool ;
end;

class TWsCategoryAttribute()
  var GroupID             : integer ;
  var AttrID              : integer ;
  var NumInList           : string  ;
  var CodeList            : string  ;
  var Name                : string  ;
  var FullName            : string  ;
  var HasChildren;        // boolean
  var Children;           // List<TWsCategoryAttribute>
end;

class TWsCategoryValue()
  var Category;           // TWsCategory
  var Attribute;          // TWsCategoryAttribute
  var IsGroupRoot         : bool ;
  var IsGeneral           : bool ;
  var UserID              : integer ;
  var GroupID             : integer ;
  var AttrID              : integer ;
  var DateOper            : Date ;
  var DateFrom            : Date ;
  var DateTo              : Date ;
  var TimeFrom            : Time ;
  var IsAuto              : bool ;
  var Children;           // List<TWsCategoryValue>
  var Action              : string ;
end;

class CategoryPack()
  var Categories;             // List<TWsCategory>
  var CategoriesAttributes;   // List<TWsCategoryAttribute>
  var CategoriesValues;       // List<TWsCategoryValue>
end;

class TWebCatListGridItem()
  var GroupID;
  var Name;
  var IsImport;
end;

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message, severity)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = severity; // MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro GetErrorMessage(ErrorCode : @integer, ErrorMessage : @string)

  var objErr = AL_GetErrorInfo();

  ErrorCode     = objErr.stat;
  ErrorMessage  = objErr.message;

  InitError();

end;



/* Вспомогательные функции сервиса */
macro GetCategoryByGroupId
(
  ObjectType  : integer,
  ObjectID    : string,
  Groups,
  History	  : bool
)
  var sqlText = " SELECT * FROM dobjgroup_dbt objg WHERE 1=1 ";

  if(ObjectID != null)

    if(History != true)

    sqlText = sqlText + " AND (objg.t_GroupID, objg.t_objectType) IN (SELECT obj.t_GroupID, obj.t_objectType FROM dobjatcor_dbt obj WHERE obj.t_ObjectType = ? AND obj.t_Object = ? AND obj.t_validtodate >= ?) ";

	else
	
	  sqlText = sqlText + " AND (objg.t_GroupID, objg.t_objectType) IN (SELECT obj.t_GroupID, obj.t_objectType FROM dobjatcor_dbt obj WHERE obj.t_ObjectType = ? AND obj.t_Object = ? ) ";
	
	end;

  end;

  if((Groups != null) AND(Groups.size > 0))

    sqlText = sqlText + " AND objg.t_GroupID IN ( ";

    var i = 0;

    while(i < Groups.size)

      if(i != Groups.size - 1)

        sqlText = sqlText + Groups[i] + ", ";

      else

        sqlText = sqlText + Groups[i];

      end;

      i = i + 1;

    end;

    sqlText = sqlText + " ) ";

  end;

  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsCategory(), dataSet;

  cmd.cmdText = sqlText;

  var maxPossibleDate = Date(31, 12, 9999);

  if(ObjectID != null)

  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, ObjectID);
    if(History != true)

  cmd.AddParam("", RSDBP_IN, maxPossibleDate);

  end;


  end;

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj                     = TWsCategory();

      tempObj.GroupID           = dataSet.value("t_groupid");
      tempObj.Name              = dataSet.value("t_name");
      tempObj.FullNameIsBasic   = dataSet.value("t_fullnameisbasic");
      tempObj.Exclusive         = dataSet.value("t_type");
      tempObj.IsMeanParentNode  = dataSet.value("t_ismeanparentnode");
      tempObj.Order             = dataSet.value("t_Order");
	  tempObj.KeepOldValues             = dataSet.value("t_KeepOldValues");

    outArray[outArray.size]     = tempObj;
  end;

  return outArray;
end;

macro GetSqlFilterCondition(FilterConditionItems)
  var fp = FilterParser( FilterConditionItems );

  fp.AddFieldCondition( 1, "obj", "t_name");
  fp.AddFieldCondition( 2, "obj", "t_numinlist");
  fp.AddFieldCondition( 3, "obj", "t_CodeList" );

  fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

  return fp.GetFullSQLCondition();
end;

macro GetCategoryAttributes
(
  ObjectType  : integer,
  ObjectID    : string,
  FilterConditionItems,
  Values,
  Groups,
  History
)
  var outArray = TArray();

  if(ObjectID != null)

  var sqlText = "SELECT * FROM (SELECT obja.*, objg.t_FullNameIsBasic, "
                " (case when objg.t_fullnameisbasic = 'X' THEN obja.t_name ELSE obja.t_fullname end) my_fullname, "
                " (case when objg.t_fullnameisbasic = 'X' THEN obja.t_fullname ELSE obja.t_name end) my_shortname FROM dobjattr_dbt obja, dobjgroup_dbt objg "
                "WHERE objg.t_groupid = obja.t_GroupID AND objg.t_objecttype = obja.t_ObjectType "
                "AND (obja.t_GroupID, obja.t_AttrID, obja.t_ObjectType) IN (SELECT obj.t_GroupID, obj.t_AttrID, obj.t_ObjectType FROM dobjatcor_dbt obj WHERE obj.t_ObjectType = ? AND obj.t_Object = ?  ";

  if(History == false)

    sqlText = sqlText + " AND obj.t_ValidToDate >= TO_DATE('31.12.9999', 'dd.mm.yyyy')) ";
  
  else
  
    sqlText = sqlText + " AND obj.t_ValidToDate != TO_DATE('31.12.9999', 'dd.mm.yyyy') ) ";

  end;  
				
  sqlText = sqlText + " ) filtered "; 			

  var cmd : RsdCommand, tempObj = TWsCategoryAttribute(), dataSet;
  var i;
  cmd.cmdText = sqlText;

  var maxPossibleDate = Date(31,12,9999);

  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, ObjectID);
  cmd.AddParam("", RSDBP_IN, maxPossibleDate);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  dataSet.NullConversion = true;

  while(dataSet.moveNext())
    tempObj                 = TWsCategoryAttribute();
  
      tempObj.GroupID       = dataSet.value("t_GroupID");
      tempObj.AttrID        = dataSet.value("t_AttrID");
      tempObj.NumInList     = dataSet.value("t_NumInList");
      tempObj.CodeList      = dataSet.value("t_CodeList");

      tempObj.FullName  = dataSet.value("my_fullname");
      tempObj.Name      = dataSet.value("my_shortname");

    outArray[outArray.size] = tempObj;
  end;

  else

    if((Values != null) AND (Values.size > 0))

      i = 0;

      while(i < Values.size)
        
        outArray[outArray.size] = Values[i].Attribute;
      
        i = i + 1;
        
      end;

    end;

  end;

  if((Groups != null) AND (Groups.size > 0))

    i = 0;

      while(i < Groups.size)
        
		tempObj = TWsCategoryAttribute();
		
		tempObj.GroupID = Groups[i];
		
        outArray[outArray.size] = tempObj;
      
        i = i + 1;
        
      end;

    end;

  return outArray;
end;

macro isAttributeHasChildren (objectType : integer, groupId : integer, attrId : integer)  
  var sqlText = "SELECT 1 FROM dobjattr_dbt WHERE t_objectType = ? AND t_groupId = ? AND t_parentId = ? AND ROWNUM = 1";

  var cmd : RsdCommand, result : bool, dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, objectType);
  cmd.AddParam("", RSDBP_IN, groupId);
  cmd.AddParam("", RSDBP_IN, attrId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    return true; 
  end;

  return false;
end;

/* Функции сервиса */

/* Получение имени объекта */
macro CB_GetObjectTypeName
(
  ObjectType : Integer
)
  var cmd : RsdCommand, outString, dataSet;

  cmd.cmdText = "SELECT t_name FROM dobjects_dbt WHERE t_objecttype = ?";

  cmd.AddParam("", RSDBP_IN, ObjectType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    outString = dataset.value(0);
  end;

  return outString;
end;

/* Получение списка категорий объекта */
macro CB_GetListCategory 
(
  ObjectType : integer, // ??? ????
  Groups,
  History : bool,
  objData
)
  var sqlText = "SELECT * FROM dobjgroup_dbt obj WHERE obj.t_ObjectType = ? AND obj.T_ATTROBJECTTYPE = 0 AND obj.T_ISHIDDEN = CHR (0)";


  if(History == true)

     sqlText = sqlText + " AND obj.t_KeepOldValues = 'X' ";
  
  end;
   
  
  if((Groups != null) AND(Groups.size > 0))

    sqlText = sqlText + " AND obj.t_GroupID IN ( ";

    var i = 0;

    while(i < Groups.size)

      if(i != Groups.size - 1)

        sqlText = sqlText + Groups[i] + ", ";

      else

        sqlText = sqlText + Groups[i];

      end;

      i = i + 1;

    end;

    sqlText = sqlText + " ) ";

  end;
  
  if(objData != null)
    var fieldsNotUse = TArray; //<int32>
    var stat = GetObjectFieldsNotUse(ObjectType, UFFLTR_OBJFLDTYPE_CATEG, objData, fieldsNotUse);
    
    if( (stat == 0) AND (fieldsNotUse != null) AND (fieldsNotUse.Size() > 0) )
      sqlText = sqlText + "AND obj.t_GroupID NOT IN ("+join(fieldsNotUse, ", ")+")";
    end;
  end;  

  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsCategory(), dataSet, valType;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, ObjectType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj                     = TWsCategory();
      tempObj.GroupID           = dataSet.value("t_groupid");
      tempObj.Name              = dataSet.value("t_name");
      tempObj.FullNameIsBasic   = SQL_ConvTypeBool(dataSet.value("t_fullnameisbasic"));
      tempObj.Exclusive         = SQL_ConvTypeBool(dataSet.value("t_type"));
      tempObj.IsMeanParentNode  = SQL_ConvTypeBool(dataSet.value("t_ismeanparentnode"));
      tempObj.Order             = dataSet.value("t_Order");
      tempObj.KeepOldValues		  = SQL_ConvTypeBool(dataSet.value("t_keepoldvalues"));
    outArray[outArray.size] = tempObj;
  end;

  return outArray;
end;

/* Сервис получения списка атрибутов категории объекта */
macro CB_GetListCategoryAttribute
(
  ObjectType  : integer, // Вид объекта
  GroupID     : integer, // Номер категории объекта
  ParentID    : integer,
  filterConditionItems,
  includezerorecord
)
  var outArray = TArray(), tempObj = TWsCategoryAttribute();
  
  if(includezerorecord == true)
  
    tempObj = TWsCategoryAttribute;

    tempObj.GroupId     = 0;
    tempObj.AttrId      = 0;
    tempObj.NumInList   = "0";
    tempObj.CodeList    = "0";
    tempObj.Name        = "0";
    tempObj.FullName    = "0";
    tempObj.HasChildren = false;
	
    outArray[outArray.size] = tempObj;
    
  end; 

  var sqlText = "SELECT obj.*, objg.t_FullNameIsBasic FROM dobjattr_dbt obj, dobjgroup_dbt objg WHERE objg.t_groupid = obj.t_GroupID AND objg.t_objecttype = obj.t_ObjectType AND obj.t_ObjectType = ? AND obj.t_GroupID = ?";

  if(filterConditionItems != NULL)
    var sqlFilterCondition = GetSqlFilterCondition(filterConditionItems);

    if(sqlFilterCondition != "")
      sqlText = sqlText + " AND " + sqlFilterCondition;
    end;
  end;
  
  if(ParentID == NULL)
    sqlText = sqlText + " AND obj.t_parentid = 0";
  else
    sqlText = sqlText + " AND obj.t_parentid = ?";
  end;

  var cmd : RsdCommand, dataSet, valType;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, GroupID);

  if(ParentID != NULL)
    cmd.AddParam("", RSDBP_IN, ParentID);
  end;

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  dataSet.NullConversion = true;

  while(dataSet.moveNext())
    tempObj             = TWsCategoryAttribute();
      tempObj.GroupID   = dataSet.value("t_GroupID");
      tempObj.AttrID    = dataSet.value("t_AttrID");
      tempObj.NumInList = dataSet.value("t_NumInList");
      tempObj.CodeList  = dataSet.value("t_CodeList");

      if(dataSet.value("t_fullnameisbasic") == "X")
        tempObj.Name          = dataSet.value("t_FullName");
        tempObj.FullName      = dataSet.value("t_Name");
      else
        tempObj.Name          = dataSet.value("t_Name");
        tempObj.FullName      = dataSet.value("t_FullName");      
      end;

      tempObj.HasChildren = isAttributeHasChildren(ObjectType, GroupID, tempObj.AttrID);
      
    outArray[outArray.size] = tempObj;
  end;

  return outArray;  
end;

/* Сервис получения списка категорий и их атрибутов */
macro CB_GetListCategoryValue
(
  ObjectType          : integer,  // Вид объекта
  ObjectID            : string,   // Идентификатор объекта
  Groups,
  Values,
  IsTransient,
  FilterConditionItems,
  History : bool,
  objData
)
  var outArray = TArray();
  var dataPack = CategoryPack();

  if((ObjectID == null) AND (IsTransient == true))

    outArray = Values;

  else

    var sqlText = "SELECT * FROM dobjatcor_dbt obj WHERE obj.t_ObjectType = ? AND obj.t_Object = ? " ;

    if(History == false)

      sqlText = sqlText + " AND obj.t_ValidToDate >= TO_DATE('31.12.9999', 'dd.mm.yyyy') ";
    
    else

      sqlText = sqlText + " AND obj.t_ValidToDate != TO_DATE('31.12.9999', 'dd.mm.yyyy') ";

    end;  
    
    if(objData != null)
      var fieldsNotUse = TArray; //<int32>
      var stat = GetObjectFieldsNotUse(ObjectType, UFFLTR_OBJFLDTYPE_CATEG, objData, fieldsNotUse);
      
      if( (stat == 0) AND (fieldsNotUse != null) AND (fieldsNotUse.Size() > 0) )
        sqlText = sqlText + "AND obj.t_GroupID NOT IN ("+join(fieldsNotUse, ", ")+")";
      end;
    end;

    sqlText = sqlText + " AND 'X' !=  (SELECT gr.t_IsHidden  FROM DOBJGROUP_DBT gr " +
                        "              WHERE  gr.t_ObjectType = obj.t_ObjectType   " +
                        "                    AND gr.t_GroupId = obj.t_GroupId)     ";

      var cmd : RsdCommand, tempObj = TWsCategoryValue(), dataSet;

    cmd.cmdText = sqlText;


    cmd.AddParam("", RSDBP_IN, ObjectType);
    cmd.AddParam("", RSDBP_IN, ObjectID);

    cmd.execute();

    dataSet = RsdRecordset(cmd);

    while(dataSet.moveNext())
      tempObj               = TWsCategoryValue();

        tempObj.IsGeneral   = dataSet.value("t_general");
        tempObj.UserID      = dataSet.value("t_oper");
        tempObj.DateOper    = dataSet.value("t_validfromdate");
        tempObj.DateFrom    = dataSet.value("t_sysdate");
        tempObj.DateTo      = dataSet.value("t_validtodate");
        tempObj.TimeFrom    = dataSet.value("t_systime");
        tempObj.IsAuto      = dataSet.value("t_isauto");
        tempObj.GroupID     = dataSet.value("t_GroupID");
        tempObj.AttrID      = dataSet.value("t_AttrID");

      outArray[outArray.size] = tempObj;
    end;

  end;

  dataPack.Categories           = GetCategoryByGroupId(ObjectType, ObjectID, Groups,History ); // TWsCategory
  
  if( IsTransient == true )
  
    dataPack.CategoriesAttributes = GetCategoryAttributes(ObjectType, null, FilterConditionItems, Values, Groups, History, Groups, History);
	
  else
  
    dataPack.CategoriesAttributes = GetCategoryAttributes(ObjectType, ObjectID, FilterConditionItems, Values, Groups, History, Groups, History);
  
  end;
  
  dataPack.CategoriesValues     = outArray; //children
  
  return dataPack;  
end;

macro SaveListCategoryValues
(
  ObjectType : integer,                       // Вид объекта
  ObjectID : string,                          // Ид. объекта
  CategoryValues //: TArray<TWsCategoryValue> // Массив признаков для сохранения
)
  var rsbCategories = RsbObjCategories(ObjectType, ObjectID);
  var stat       : integer = 0;
  var deleteStat : integer = 0;
  var i = 0;

  record objatcor("objatcor.dbt");

  while(i < CategoryValues.size)
    if((CategoryValues[i].Action == "REMOVE") and (CategoryValues[i].IsGeneral == false))
      stat = rsbCategories.DisconnectAttr(CategoryValues[i].GroupID, CategoryValues[i].AttrID);
    end;

    if(stat > 0)
      break;
    end;

    i = i + 1;
  end;

  i = 0;

  if (stat == 0)  
    while(i < CategoryValues.size)
      if((CategoryValues[i].Action == "REMOVE") and (CategoryValues[i].IsGeneral))
        stat = rsbCategories.DisconnectAttr(CategoryValues[i].GroupID, CategoryValues[i].AttrID);
      end;

      if(stat > 0)
        break;
      end;

      i = i + 1;
    end;
  end;

  if(stat == 0)
    stat = rsbCategories.Save(ObjectID);
  else
    deleteStat = stat;
    stat = 0;
  end;

  i = 0;

  while(i < CategoryValues.size)
    if(CategoryValues[i].Action == "CREATE")

      objatcor.GroupID = CategoryValues[i].GroupID;
      objatcor.AttrID = CategoryValues[i].AttrID;
      objatcor.SysTime = CategoryValues[i].TimeFrom;
      objatcor.SysDate = CategoryValues[i].DateFrom;
      objatcor.ValidFromDate = CategoryValues[i].DateOper;
      objatcor.IsAuto = "";

      stat = rsbCategories.ConnectAttr(CategoryValues[i].GroupID, CategoryValues[i].AttrID, NULL, NULL, CategoryValues[i].DateOper, objatcor);
      
      if((CategoryValues[i].Action == "CREATE") AND (CategoryValues[i].IsGeneral == true))
      stat = rsbCategories.SetMainAttr(CategoryValues[i].GroupID, CategoryValues[i].AttrID);
    end;

    end;

    if(stat > 0)
      break;
    end;

    i = i + 1;
  end;

  if(stat == 0)
    stat = rsbCategories.Save(ObjectID);
  end;

  // Ниже идет еще одна попытка удаления
  if(deleteStat > 0)
    i = 0;

    while(i < CategoryValues.size)
      if(CategoryValues[i].Action == "REMOVE")
        stat = rsbCategories.DisconnectAttr(CategoryValues[i].GroupID, CategoryValues[i].AttrID);
      end;

      if(stat > 0)
        break;
      end;

      i = i + 1;
    end;

    if(stat == 0)
      stat = rsbCategories.Save(ObjectID);
    end;
  end;

  return stat;
end;

macro WebCore_GetWsCategoryAttribute (ObjectType , GroupID , Str, Mode, listLen, includezerorecord) : TArray

  var result  = TArray;
  
  var cmd, rs, obj;

  if(includezerorecord == true)
  
    obj = TWsCategoryAttribute;

    obj.GroupId     = 0;
    obj.AttrId      = 0;
    obj.NumInList   = "0";
    obj.CodeList    = "0";
    obj.Name        = "0";
    obj.FullName    = "0";
    obj.HasChildren = false;
	
    result[result.size] = obj;
    
  end;

  var query = 	" 	SELECT t.*										"
                " 	FROM dobjattr_dbt t								"
                "	WHERE t.t_objecttype = ? AND t.t_groupid = ?	";
		
  var query_cond = " ";

  if( listLen != 0 )
  
    query_cond = query_cond + " AND ROWNUM < " + string(listLen);

end;

  if(Mode==1)

    query_cond = query_cond +" AND LOWER(t.t_numinlist) LIKE LOWER ('%' || ? || '%') ";

    query_cond = query_cond + "ORDER BY t.t_numinlist ";
	
  end;
     
  if(Mode==2)
  
    query_cond = query_cond +" AND LOWER(t.t_codelist) LIKE LOWER ('%' || ? || '%') ";
	
    query_cond = query_cond + "ORDER BY t.t_codelist ";
	
	end;
  
  if(Mode==3)
	
    query_cond = query_cond +" AND LOWER(t.t_name) LIKE LOWER ('%' || ? || '%') ";
	
    query_cond = query_cond + "ORDER BY t.t_name ";
  
  end;
  
  query= query + query_cond;

  cmd = RsdCommand(query);
  
  cmd.addParam( "ObjectType", RSDBP_IN, ObjectType );
  
  cmd.addParam( "GroupID", RSDBP_IN, GroupID );

  cmd.addParam( "Str", RSDBP_IN, Str );

  rs = RsdRecordset(cmd);

  while (rs.movenext)

    obj = TWsCategoryAttribute;

	obj.GroupId  = SQL_ConvType(rs.value("T_GROUPID"));
    obj.AttrId  = SQL_ConvType(rs.value("T_ATTRID"));
	obj.NumInList  = SQL_ConvType(rs.value("T_NUMINLIST"));
    obj.CodeList  = SQL_ConvType(rs.value("T_CODELIST"));
	obj.Name  = SQL_ConvType(rs.value("T_NAME"));
    obj.FullName  = SQL_ConvType(rs.value("T_FULLNAME"));
    obj.HasChildren = isAttributeHasChildren(ObjectType, GroupID, obj.AttrID);
		
	result[result.size] = obj;

  end; 

  return result;

end;

macro WebCore_GetObjAttrbranchIDs (ObjectType , GroupID , AttrID) : TArray

  var result  = TArray;
  
  var cmd, rs, obj;

  var query = 	" 	    SELECT t_AttrID "
                "         FROM dobjattr_dbt "
                "        WHERE t_AttrID != ? "
                "   START WITH t_objecttype = ? AND t_GroupID = ? AND t_AttrID = ? "
                "   CONNECT BY     t_objecttype = PRIOR ? "
                "              AND t_GroupID = PRIOR ? "
                "              AND t_AttrID = PRIOR t_ParentID "
                "     ORDER BY LEVEL DESC	";
	
  cmd = RsdCommand(query);
  
  cmd.addParam( "AttrID1", RSDBP_IN, AttrID );
  cmd.addParam( "ObjectType1", RSDBP_IN, ObjectType );
  cmd.addParam( "GroupID1", RSDBP_IN, GroupID );
  cmd.addParam( "AttrID2", RSDBP_IN, AttrID );
  cmd.addParam( "ObjectType2", RSDBP_IN, ObjectType );
  cmd.addParam( "GroupID2", RSDBP_IN, GroupID );

  rs = RsdRecordset(cmd);

  while (rs.movenext)

    result[result.size] = SQL_ConvType(rs.value("T_ATTRID"));

  end; 

  return result;

end;

macro WebCore_GetObjAttrbranch (ObjectType , GroupID , AttrID) : TArray

  var result  = TArray;
  
  var cmd, rs, obj;

  var query = 	" 	    SELECT t_AttrID "
                "         FROM dobjattr_dbt "
                "        WHERE t_AttrID != ? "
                "   START WITH t_objecttype = ? AND t_GroupID = ? AND t_AttrID = ? "
                "   CONNECT BY     t_objecttype = PRIOR ? "
                "              AND t_GroupID = PRIOR ? "
                "              AND t_AttrID = PRIOR t_ParentID "
                "     ORDER BY LEVEL DESC	";
	
  cmd = RsdCommand(query);
  
  cmd.addParam( "AttrID1", RSDBP_IN, AttrID );
  cmd.addParam( "ObjectType1", RSDBP_IN, ObjectType );
  cmd.addParam( "GroupID1", RSDBP_IN, GroupID );
  cmd.addParam( "AttrID2", RSDBP_IN, AttrID );
  cmd.addParam( "ObjectType2", RSDBP_IN, ObjectType );
  cmd.addParam( "GroupID2", RSDBP_IN, GroupID );

  rs = RsdRecordset(cmd);

  while (rs.movenext)
    
    result[result.size] = CB_GetListCategoryAttribute( ObjectType, GroupID, SQL_ConvType(rs.value("T_ATTRID")), null, false);

  end; 

  return result;

end;


macro WebCore_CB_InsertCategoryValueHist(ObjectType, Object, CategoryValue, ConfirmedChecks):WebResponseBuilder

  var res = 0;
  
  var YesNoQuestion, NeedConfirmCheck=0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_InsertObjAttrHist(ObjectType, Object, CategoryValue.Attribute.GroupId, CategoryValue.DateOper, CategoryValue.DateTo, CategoryValue.Attribute.AttrId, CategoryValue.IsGeneral, CategoryValue.IsAuto, ConfirmedChecks, @NeedConfirmCheck, @YesNoQuestion );
  
  if(res > 0)

    AddErrorMessage(@ErrorMessage);

  end;

  if(res < 0)
  
    ErrorMessage.Code  = NeedConfirmCheck  ;
	ErrorMessage.Message = YesNoQuestion  ;
    ErrorMessage.Severity = MessageSeverity.Info;
	ResponseBuilder.AddMessage( ErrorMessage );
	
  end;

  ResponseBuilder.SetUserObject(res);

  return ResponseBuilder.Result;

end;


macro WebCore_CB_UpDateCategoryValueHist (ObjectType, Object, OldCategoryValue, CategoryValue, ConfirmedChecks):WebResponseBuilder

  var res = 0;
  
  var YesNoQuestion, NeedConfirmCheck=0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  res = CB_UpdateObjAttrHist(ObjectType, Object, CategoryValue.Attribute.GroupId, OldCategoryValue.DateTo, CategoryValue.DateOper, CategoryValue.DateTo, OldCategoryValue.Attribute.AttrId, CategoryValue.Attribute.AttrId, CategoryValue.IsGeneral, CategoryValue.IsAuto, ConfirmedChecks, @NeedConfirmCheck, @YesNoQuestion );
  
  if(res > 0)

    AddErrorMessage(@ErrorMessage);

  end;
  
  if(res < 0)
  
    ErrorMessage.Code  = NeedConfirmCheck  ;
	ErrorMessage.Message = YesNoQuestion  ;
    ErrorMessage.Severity = MessageSeverity.Info;
	ResponseBuilder.AddMessage( ErrorMessage );
	
  end;

  ResponseBuilder.SetUserObject(res);

  return ResponseBuilder.Result;

end;


macro WebCore_CB_DeleteCategoryValueHist (ObjectType, Object, Selectedlist, ConfirmedChecks):WebResponseBuilder

  var res = 0;
  
  var i = 0;
  
  var YesNoQuestion, NeedConfirmCheck=0;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  while(i< Selectedlist.size)
  
      res = CB_DeleteObjAttrHist(ObjectType, Object, Selectedlist[i].Attribute.GroupId,  Selectedlist[i].DateTo, Selectedlist[i].Attribute.AttrId, ConfirmedChecks, @NeedConfirmCheck, @YesNoQuestion );
  
    if(res > 0)

      AddErrorMessage(@ErrorMessage);

    end;

    if(res < 0)
  
      ErrorMessage.Code  = NeedConfirmCheck  ;
	  ErrorMessage.Message = YesNoQuestion  ;
      ErrorMessage.Severity = MessageSeverity.Info;
	  ResponseBuilder.AddMessage( ErrorMessage );
	
    end;
  
    i=i+1;
  
  end;
  


  ResponseBuilder.SetUserObject(res);

  return ResponseBuilder.Result;

end;

macro WebCore_CB_GetRegParmCategory ():WebResponseBuilder

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var err=0;
  
  var ProcValue;
  
  GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДОСТУП\\ИСТОРИЯ ПРИЗНАКОВ КАТЕГОРИЙ", V_BOOL, ProcValue, err);

  if(err == 0)
    
    responseBuilder.SetUserObject( ProcValue);
	 
  else

    responseBuilder.SetUserObject( false);
	
  end;

  return responseBuilder.Result;

end;                              

macro WebCore_ImportXMLCategory(ObjectType, ObjectID, FileName, GroupID, Stages, Answers, GroupList)
  
  var stat = 0;
  
  var Stage = 0;
  var Question = "";
  
  var SelectGroups = false;

  var result  = TArray;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  if(ExistFile("../WorkFile/" + FileName) == true)
    
    stat = CB_ObjAttrImport("../WorkFile/" + FileName,
                            ObjectType,
                            GroupID,
                            ObjectID,
                            Stages, 
                            Stage,
                            Question,
                            Answers,
                            GroupList,
                            SelectGroups);

    if (stat < 0)
      
      result = null;
      
      AddErrorMessageExt(@ErrorMessage, Stage, Question, MessageSeverity.Info);      
      
    end;
    
    if (stat > 0)
      
      result = null;
      
      AddErrorMessage(@ErrorMessage);
      
    end;
    
    if (stat == 0)
      
      if (SelectGroups == true)
        
        var cmd, rs, obj;

        var query = 	" 	    SELECT OG.T_GROUPID, OG.T_NAME "
                      "         FROM dobjgroup_dbt og "
                      "        WHERE     OG.T_GROUPID IN "
                      "                     (  SELECT ATRCORIM.T_GROUPID "
                      "                          FROM DOBJATRCORIMP_TMP ATRCORIM, dobjgroup_dbt og "
                      "                         WHERE     ATRCORIM.T_OBJECTTYPE = ? "
                      "                               AND ATRCORIM.T_OBJECT = ? "
                      "                      GROUP BY ATRCORIM.T_GROUPID) "
                      "              AND og.T_OBJECTTYPE = ? ";

        cmd = RsdCommand(query);

        cmd.addParam( "ObjectType1", RSDBP_IN, ObjectType );
        cmd.addParam( "ObjectID", RSDBP_IN, ObjectID );
        cmd.addParam( "ObjectType2", RSDBP_IN, ObjectType );

        rs = RsdRecordset(cmd);

        while (rs.movenext)
          
          var tempObj          = TWebCatListGridItem();

          tempObj.GroupID  = rs.value("T_GROUPID");
          tempObj.Name     = rs.value("T_NAME");
          tempObj.IsImport = false;
          
          result[result.size] = tempObj;

        end;      

      end;
      
    end;      

  else

    AddErrorMessageExt(@ErrorMessage, 0, "Файл " + FileName + " не существует.", MessageSeverity.RuntimeError);

  end;

  responseBuilder.SetUserObject(result);

  return responseBuilder.Result;

end;

macro WebCore_ExportXMLCategory(ObjectType, ObjectID, GroupIDList)

  var fc = null;

  var res = false;
  
  var stat = 0;
  
  var FileName = "";

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  
  var i = 0;

  while( i < GroupIDList.size )
    
    ObjAttrAddToExport(ObjectType, GroupIDList[i], ObjectID);

    i = i + 1;
    
  end;  
  
  stat = ObjAttrMakeExportName(FileName);
  
  if(stat > 0)
    
    res = false;
    
  end;
  
  if(stat == 0)

    stat = ObjAttrExport(FileName);
    
    if(stat > 0)
    
      res = false;
    
    end;

  end;

  if(FileName != "")
  
    fc = GetTextFile(FileName);  

    res = true;    
  
  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(fc);

  return responseBuilder.Result;

end;

macro WebCore_ChangeAttrDate(ObjectType, ObjectID, OldFromDate, NewFromDate, GroupID, AttrID)

  file objatcor ("objatcor.dbt") key 0 write;
  
  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();  

  var maxPossibleDate = Date(31, 12, 9999);

  var res = true;
  
  var cmd, rs, obj;

  var query = 	" 	    SELECT COUNT (*) COUNT "
                "         FROM DOBJATCOR_DBT atcor, DOBJGROUP_DBT gr "
                "        WHERE     ATCOR.T_VALIDTODATE != ? "
                "              AND ATCOR.T_VALIDTODATE >= ? "
                "              AND ATCOR.T_GROUPID = ? "
                "              AND ATCOR.T_OBJECTTYPE = ? "
                "              AND ATCOR.T_OBJECT = ? "
                "              AND ATCOR.T_OBJECTTYPE = GR.T_OBJECTTYPE "
                "              AND ATCOR.T_GROUPID = GR.T_GROUPID "
                "              AND ( (GR.T_TYPE = 'X') OR (GR.T_TYPE != 'X' AND ATCOR.T_ATTRID = ?)) ";

  cmd = RsdCommand(query);

  cmd.addParam( "maxPossibleDate", RSDBP_IN, maxPossibleDate );
  cmd.addParam( "NewFromDate", RSDBP_IN, NewFromDate );
  cmd.addParam( "GroupID", RSDBP_IN, GroupID );
  cmd.addParam( "ObjectType", RSDBP_IN, ObjectType );
  cmd.addParam( "ObjectID", RSDBP_IN, ObjectID );
  cmd.addParam( "AttrID", RSDBP_IN, AttrID );

  rs = RsdRecordset(cmd);

  if (rs.movenext)

    if( rs.value("count") == 0 )
      
      objatcor.objecttype    = ObjectType;
      objatcor.object        = ObjectID;
      objatcor.groupid       = GroupID;
      objatcor.attrid        = AttrID;
      objatcor.validfromdate = OldFromDate;
      objatcor.validtodate   = maxPossibleDate;

      res = GetEQ(objatcor);

      if(res == true)

        objatcor.validfromdate = NewFromDate;
        
        res = UpDate(objatcor);

      end; 

    else
      
      AddErrorMessageExt(@ErrorMessage, 0, "Изменение невозможно: в истории категории уже имеется запись за эту дату", MessageSeverity.RuntimeError);
      
      res = true;
    
    end;

  end;
  
  if(res != true)

    AddErrorMessageExt(@ErrorMessage, 0, "Ошибка сохранения", MessageSeverity.RuntimeError);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;
  
end;

macro WebCore_CreateTWsCategoryAttributeFromBD
(
  ObjectType : integer,
  GroupID : integer,
  AttrID : integer
) : TWsCategoryAttribute
  
  var obj;

  var cmd, rs;
  
  var query = " SELECT * FROM dobjattr_dbt WHERE t_objecttype = ? AND t_groupid = ? AND t_attrid = ? ";  
  
  cmd = RsdCommand(query);

  cmd.AddParam( "ObjectType", RSDBP_IN, ObjectType );
  cmd.AddParam( "GroupID"   , RSDBP_IN, GroupID );
  cmd.AddParam( "AttrID"    , RSDBP_IN, AttrID );

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    
    obj = TWsCategoryAttribute;  
  
    obj.AttrID        = SQL_ConvType(rs.value( "t_attrid" ));
    // obj.Balance       = SQL_ConvType(rs.value( "t_balance" ));
    // obj.ChAttr        = SQL_ConvTypeBool( rs.value( "t_chattr" ));
    // obj.Classificator = SQL_ConvType(rs.value( "t_classificator" ));
    // obj.CloseDate     = SQL_ConvType(rs.value( "t_closedate" ));
    obj.CodeList      = SQL_ConvType(rs.value( "t_codelist" ));
    // obj.CorracType    = SQL_ConvType(rs.value( "t_corractype" ));
    obj.FullName      = SQL_ConvType(rs.value( "t_fullname" ));
    obj.GroupID       = SQL_ConvType(rs.value( "t_groupid" ));
    // obj.IntAttr       = SQL_ConvType(rs.value( "t_intattr" ));
    // obj.IsObject      = SQL_ConvTypeBool( rs.value( "t_isobject" ));
    // obj.LongAttr      = SQL_ConvType(rs.value( "t_longattr" ));
    obj.Name          = SQL_ConvType(rs.value( "t_name" ));
    // obj.NameObject    = SQL_ConvType(rs.value( "t_nameobject" ));
    obj.NumInList     = SQL_ConvType(rs.value( "t_numinlist" ));
    // obj.ObjectType    = SQL_ConvType(rs.value( "t_objecttype" ));
    // obj.OpenDate      = SQL_ConvType(rs.value( "t_opendate" ));
    // obj.ParentID      = SQL_ConvType(rs.value( "t_parentid" ));

  end;

  return obj;
  
end;
