/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : prpmchop.mac
  Created     : 12.11.2008
  Description : Печать отчета о массовой работе процедуры 
                смены операциониста

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, globals;

const   ПечатьЗаписи = 0,
        Первый       = 1,
        Последний    = 2;

macro PrintHeaderReport(NewOper)
  var Tb_Person:Tbfile = Tbfile("person.dbt", "bank.def", 0);
  Tb_Person.Clear();
  Tb_Person.rec.Oper = NewOper;
  Tb_Person.GetEQ();
  
[ Протокол процедуры передачи документов операционисту ####### #########################################################
  
  Дата выполнения: #############
  
  ┌─────┬───────────┬───────┬───────┬────────────────────────────────────────────────────┐
  │Номер│Дата валют.│ Сумма │Номер  │                   Сообщение                        │
  │     │           │       │ошибки │                                                    │
  ├─────┼───────────┼───────┼───────┼────────────────────────────────────────────────────┤](NewOper, Tb_Person.rec.Name, {curdate}); 
end;

macro PrintRecordReport(Номер, ДатаВалют, Сумма, НомерОшибки, Сообщение)
 if(НомерОшибки == 0)
   Сообщение = "Автор документа изменен успешно";
 end;           

[ │#####│###########│#######│#######│####################################################│](Номер, ДатаВалют, Сумма, НомерОшибки, StrSubst(Сообщение, "|", " " ):w);
end;

macro PrintFooterReport()
[ └─────┴───────────┴───────┴───────┴────────────────────────────────────────────────────┘];
end;

MACRO Печать_ОтчетСканирования(Вызов,firstdoc,Статус,Сообщение, UserVal)
   
  record pm_rmprop (pmrmprop);
  record pm_paym (pmpaym);
  
  setbuff(pm_paym, firstdoc);

  if (Вызов==Первый)
    PrintHeaderReport(UserVal);
  elif (Вызов == ПечатьЗаписи)
    if ( FindPayment( pm_paym.PaymentID,      // Индентификатор 
                      0,    
                      0,    
                      0,    
                      0,
                      true,         // Поиск по реальной базе
                      pm_paym,
                      null,
                      null,
                      pm_rmprop
                      ) != 0 )

      Статус = 1;
      Сообщение = "Не найден платеж";
    end;

    PrintRecordReport(pm_rmprop.Number, pm_paym.ValueDate , pm_paym.BaseAmount, Статус, Сообщение);

  elif (Вызов==Последний)
    PrintFooterReport();
  end;

END;
