 /*
 $Name: bnk_dttmfrmt.mac
 $Module: Ядро Банкинг
 $Description: Функции форматирования дат и времени
 */

import oralib, likepy;
// Нежелательно импортировать здесь другие макросы Расч. банка,
// т.к. этот макрофайл сам предназначен для импорта в макросы РБ


// Часы    HH, hh
// Минуты  MI, mi
// Секунды SS, ss
macro TimeToStr(tm : time, format : string)
  var str : string = "";

  var ss, mi, hh;
  TimeSplit(tm, hh, mi, ss);

  var hhStr : string = StrSubst( string(hh:2), " ", "0" );
  var miStr : string = StrSubst( string(mi:2), " ", "0" );
  var ssStr : string = StrSubst( string(ss:2), " ", "0" );

  str = StrSubst(format, "HH", hhStr);
  str = StrSubst(str, "hh", hhStr);
  str = StrSubst(str, "MI", miStr);
  str = StrSubst(str, "mi", miStr);
  str = StrSubst(str, "SS", ssStr);
  str = StrSubst(str, "ss", ssStr);

  return str;
end;

// date -> YYYYMMDD
macro DateToStr_YYYYMMDD( dt )
   var day, mon, year, str;
   DateSplit( date(dt), day, mon, year );
   str = string(year:4, mon:2, day:2);
   str = StrSubst ( str, " ", "0" );
   return str;
end;

// YYYYMMDD -> date
macro ToDate_YYYYMMDD( strYYYYMMDD )
  return date( int(substr(strYYYYMMDD, 7, 2)), 
               int(substr(strYYYYMMDD, 5, 2)), 
               int(substr(strYYYYMMDD, 1, 4)) 
             );
end;

// date -> YYYY-MM-DD
macro YYYY_MM_DD( dateInit )
  var day, mon, year, str;

  DateSplit( date(dateInit), day, mon, year );
  if ( day < 10 )
    day = "0"+string(day);
  else
    day = string(day);
  end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;

  str = string(year) + "-" + mon + "-" + day ;
  return str;
end;

// YYYY-MM-DD -> date
macro ToDateYYYY_MM_DD( strYYYY_MM_DD )
  return date( int(substr(strYYYY_MM_DD, 9, 2)), 
               int(substr(strYYYY_MM_DD, 6, 2)), 
               int(substr(strYYYY_MM_DD, 1, 4)) 
             );
end;

// date -> DD.MM.YYYY
macro DDpMMpYYYY( dateInit )
  var day, mon, year;
  DateSplit( date(dateInit), day, mon, year );
  return StrSubst(string(day:2, ".", mon:2, ".", year:4), " ", "0");
end;

// DD.MM.YYYY -> date
macro ToDate_DDpMMpYYYY( strDDpMMpYYYY )
  return date( int(substr(strDDpMMpYYYY, 1, 2)), 
               int(substr(strDDpMMpYYYY, 4, 2)), 
               int(substr(strDDpMMpYYYY, 7, 4)) 
             );
end;

// date -> YYMMDD
macro YYMMDD( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(substr(string(year),3):2, mon:2, day:2);
  str = StrSubst ( str, " ", "0" );
  return str;
end;

// date -> DDMMYY
macro DDMMYY( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(day:2, mon:2, substr(string(year),3):2);
  str = StrSubst ( str, " ", "0" );
  return str;
end;


macro GetCurrentDateTimeUTC( dateInit ) : string

  var sDate:string = "", sTime:string = "";

  var query_time = "select to_char(systimestamp, 'HH24:MI:SS.FF3') FROM dual ";
  var rs_time:RsdRecordset = execSQLselect(query_time);

  if (rs_time and rs_time.moveNext() )
    sTime = "T" + trim( rs_time.value(0) );
  end;  

  if( sTime )
    var query_zone = "select tz_offset(to_char(DBTIMEZONE)) from dual";
    var rs_zone:RsdRecordset = execSQLselect(query_zone);

    if( rs_zone and rs_zone.moveNext() )
      sTime = sTime + trim( rs_zone.value(0) );
    end;

    sDate = YYYY_MM_DD( dateInit ) + sTime;
  end;

  return sDate;

end;

macro GetSysTime() : time

  var sTime: time;
  var query_time = "select to_date( '01010001' || to_char( sysdate, 'hh24miss' ), 'ddmmyyyyhh24miss' ) from dual";
  var rs_time:RsdRecordset = execSQLselect( query_time );

  if ( rs_time and rs_time.moveNext() )
    sTime = rs_time.value(0);
  end;  

  return sTime;

end;

macro DivideYYYY_MM_DDThhmmssZToDateTime( YYYY_MM_DDThhmmssZ : string, YYYYMMDD : @string, hhmmssZ : @string ):date
  var TIndx = index( YYYY_MM_DDThhmmssZ, "T" );
  if( (not TIndx) and (strlen( YYYY_MM_DDThhmmssZ ) == 10) )
    if( valtype( YYYYMMDD ) != V_UNDEF )
      YYYYMMDD = YYYY_MM_DDThhmmssZ; 
    end;
    return ToDateYYYY_MM_DD( YYYY_MM_DDThhmmssZ );
  else
    var tmpDateStr = SubStr( YYYY_MM_DDThhmmssZ, 1, TIndx - 1 );
    var tmpTimeStr = SubStr( YYYY_MM_DDThhmmssZ, TIndx + 1 );
    if( valtype( YYYYMMDD ) != V_UNDEF )
      YYYYMMDD = tmpDateStr; 
    end;
    if( valtype( hhmmssZ ) != V_UNDEF )
      hhmmssZ = tmpTimeStr; 
    end;
    return ToDateYYYY_MM_DD( tmpDateStr );
  end;
end;


// hh:mm:ss -> time
macro hhmmssToTime( hh_mm_ssStr:string ):time
  return time( int( SubStr(hh_mm_ssStr,1,2) ), int( SubStr(hh_mm_ssStr,4,2) ), int( SubStr(hh_mm_ssStr,7,2) ) );
end;
