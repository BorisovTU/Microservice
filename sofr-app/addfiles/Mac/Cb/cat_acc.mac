//------------------------------------------------------------------------------
// Базовый класс для работы со счетами КУ
//------------------------------------------------------------------------------
import BankInter, CTInter, globals;

//------------------------------------------------------------------------------
//                         Список параметров
//                  доступных для первичного документа
//------------------------------------------------------------------------------

CLASS AvailableParametrs

//------------------------------------------------------------------------------
// данные класса
//------------------------------------------------------------------------------

   PRIVATE VAR
      List = TArray, last = 0;

//------------------------------------------------------------------------------
// регистрация параметра
//------------------------------------------------------------------------------
   MACRO registration( parm )
      List[last] = parm;
      last = last + 1;
      return parm;
   END;

//------------------------------------------------------------------------------
// зарегистрирован ли параметр
//------------------------------------------------------------------------------
   MACRO isMY( parm )
      var i = 0;

      while( i != last )
         if( List[i] == parm ) return true; end;
         i = i + 1;
      end;

      return false;
   END;

END;
//------------------------------------------------------------------------------


//------------------------------------------------------------------------------
// Первичный документ для категорий
//------------------------------------------------------------------------------
CLASS TCommonFirstDoc

//------------------------------------------------------------------------------
// данные класса
//------------------------------------------------------------------------------
    VAR
      ParmA        = TArray,
      FIRoleBArray = TArray,
      regList      = AvailableParametrs;

    VAR
      Error = 0, kind, id;

//------------------------------------------------------------------------------
// инициализация массива
//------------------------------------------------------------------------------
    MACRO InitParmArray
      ParmA[regList.registration(MC_TYPE_PARAMETR_OWNER)] = {OurBank};

      FIRoleBArray[0] = 0;
    END;

//------------------------------------------------------------------------------
// вернуть параметр второго рода
//------------------------------------------------------------------------------
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
       VAR Parametr = -1;
       if( regList.isMY(ParmKind) ) return ParmA[ParmKind]; end;
       return Parametr;
    END;

//------------------------------------------------------------------------------
// вернуть параметр первого рода
//------------------------------------------------------------------------------
    MACRO GetParametrTemplate ( ObjectID, Classificator, OperDate, FIRole )         
       VAR Parametr = -1;
       return Parametr;
    END;

//------------------------------------------------------------------------------
// основная роль фИ
//------------------------------------------------------------------------------
    MACRO GetBasisFIRole(FIRole)
       return FIROLE_UNDEF;
    END;

//------------------------------------------------------------------------------
// вернуть весь базис ролей
//------------------------------------------------------------------------------
    MACRO GetFIRoleBArray()
       return FIRoleBArray;
    END;

//------------------------------------------------------------------------------
// найти/открыть счет
//------------------------------------------------------------------------------
   MACRO FindAndOpenAccount( Category, isMass, Open_Date )
      var opendate:Date = {curDate};
      
      if ( ValType(Open_Date) == V_DATE )
         opendate = Open_Date;
      end;

      return MC_FindAndOpenAccount( Category, this, opendate, isMass, MC_OPENACC_CREATE, null, null, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// найти/открыть общесистемный счет
//------------------------------------------------------------------------------
   MACRO FindAndOpenSysAccount( Category, isMass, Open_Date )
      var opendate:Date = {curDate};
      
      if ( ValType(Open_Date) == V_DATE )
         opendate = Open_Date;
      end;

      return MC_FindAndOpenCommonAccByFD( Category, this, opendate, isMass, MC_OPENACC_CREATE, null, null, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// найти/открыть общесистемный счет в валюте FIID
//------------------------------------------------------------------------------
   MACRO FindAndOpenSysCurAccount( Category, isMass, Open_Date, FIID )
      var opendate:Date = {curDate};
      var accFIID = -1;
      
      if ( ValType(Open_Date) == V_DATE )
         opendate = Open_Date;
      end;

      if( ValType(FIID) == V_INTEGER )
        accFIID = FIID;
      end;

      return MC_FindAndOpenCommonAccByFD( Category, this, opendate, isMass, MC_OPENACC_CREATE, null, accFIID, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// найти счет
//------------------------------------------------------------------------------
   MACRO FindAccount( Category, isMass )
      return MC_FindAndOpenAccount( Category, this, {curDate}, isMass, MC_OPENACC_ACT, null, null, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// найти общесистемный счет
//------------------------------------------------------------------------------
   MACRO FindSysAccount( Category, isMass )
      return MC_FindAndOpenCommonAccByFD( Category, this, {curDate}, isMass, MC_OPENACC_ACT, null, null, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// привязать счет заново
//------------------------------------------------------------------------------
   MACRO RecreateAccount( Category, isMass )
      return MC_FindAndOpenAccount( Category, this, {curDate}, isMass, MC_OPENACC_RECREATE, null, null, null, null, null, null );
   END;

//------------------------------------------------------------------------------
// Корректировка счета
//------------------------------------------------------------------------------
   MACRO CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc,  OperDate )
      return true;
   END;

//------------------------------------------------------------------------------
// Вернуть дату
//------------------------------------------------------------------------------
   MACRO GetDate()
      return GetParametr( MC_TYPE_PARAMETR_INDEXDATE, null, null, null );
   END;

//------------------------------------------------------------------------------
// Конструктор
//------------------------------------------------------------------------------
   InitParmArray();
END;
//------------------------------------------------------------------------------
