/************************************************************************
  RS-Bank V.6

  Импорт Справочника БИК ЦБ РФ
// KS 14.05.2019 Доступен "Тихий режим". Для запуска использовать параметр вызова -SilentMode:true
//               Можно использовать в планировщике
*************************************************************************/

Import ServiceAction;
Import mfo_lib;
Import rsexts;

Import convED807;

import fileop;         // KS Объект для работы с файлами на сервере и терминале

macro CU_YesNoWin(msg : string)
  Array Text;
  Array Button;
  Button(0) = "  Да   ";
  Button(1) = "  Нет  ";
  var but, ind = 0;

  Text(ind) = msg;
  but = ConfWin(Text, Button);
  
  return (but == 0);
end;

  PRIVATE CONST V_SPECVAL = 26;
  var stat = true;
  var xml = CreateXMLParser();  // создаем MS-парсер

  var FullImportFileName;
  var ImportFileName;
  var ImportFileDir;
  var error;
  var NameOper;
  var cmd, rs;
  var strHeading;
  var pos;

  var prefix = "";
  var ED807;
  var xmlns;
  var InfoTypeCode;
  var EDNo;
  var EDDate;
  var CreationDateTime, CreationDate, CreationTime;
  var BusinessDay, DirectoryVersion;
  var BICDirectoryEntry;

  GetRegistryValue("CB\\BNKSIMPORT\\IMPORTFILEDIR", V_STRING, ImportFileDir, error, false, {oper}); // Каталог импорта

  if(substr(ImportFileDir, strlen(ImportFileDir)) != "\\" )
    ImportFileDir = ImportFileDir + "\\";
  end;

  // KS start 14.05.2019
  // Тихий режим. В этом случае загружается подходящий файл, который потом переносится в архив вместе с протоколом загрузки.
  var cmdpar, SilentMode;
  var arh = ImportFileDir + "arh\\"; 
  GetCmdLineParm("SilentMode", cmdpar, V_STRING);
  if (cmdpar == "true")
    SilentMode = true;
  else
    SilentMode = false;
  end;
  // Ищем подходящий файл
  // Пример имени файла 20190424_ED807_full.xml
  var Attr :integer= 0 ; /* Атрибут найденного файла */
  if (SilentMode)
    stat = FileFindFirst( ImportFileDir + "*_ED807_*.xml", ImportFileName, Attr);
    if(stat)
      FullImportFileName = ImportFileDir + ImportFileName;
      setoutput(arh + ImportFileName + ".txt", true);
    end;
  end;

  while (stat) // Пока есть файлы для загрузки
  if((ValType(FullImportFileName)!=v_undef) or // если файл для загрузки уже известен, то диалог уже не нужен
  // KS end
     (SelectFile(FullImportFileName, ImportFileDir + "*.xml", "Выбор файла импорта", null, IsStandAlone)))
    if(not IsStandAlone) /*Трехзвенка*/
      ImportFileName = FullImportFileName;
      pos = StrBrk(ImportFileName, "\\");
      while(pos != 0)
        ImportFileName = SubStr(ImportFileName, pos+1);
        pos = StrBrk(ImportFileName, "\\");
      end;
      pos = SubStr(FullImportFileName, 1, 1);
      if (pos == "$")
        if(not CopyFile("$" + FullImportFileName, "..\\WorkFile\\" + ImportFileName))
          MsgBox ("Ошибка: файл "+FullImportFileName+" не скопирован");
          stat = false;
        else
          FullImportFileName = "..\\WorkFile\\" + ImportFileName;
        end;
      end;
    end;

    if(stat)
      stat = xml.load(FullImportFileName);
    
      if(stat)
        ED807 = xml.getElementsByTagName(prefix + "ED807");
        if(ED807.length == 0)
          prefix = "ed:";
          ED807 = xml.getElementsByTagName(prefix + "ED807");
        end;

        if(ED807.length != 0)
          xmlns = ED807.item(0).getAttribute("xmlns");
          if(ValType(xmlns) == V_SPECVAL)
            xmlns = ED807.item(0).getAttribute("xmlns:ed");
          end;
          if(ValType(xmlns) != V_SPECVAL)
            if(xmlns == "urn:cbr-ru:ed:v2.0")
              InfoTypeCode  = ED807.item(0).getAttribute("InfoTypeCode");
              if(InfoTypeCode == "FIRR")
                stat = CU_YesNoWin("Будет импортирован полный Справочник БИК. Выполнить?");
              end;
              if(InfoTypeCode == "SIRR")
                stat = CU_YesNoWin("Будет выполнен импорт изменений в Справочник БИК. Выполнить?");
              end;

              if(stat)
                EDNo = ED807.item(0).getAttribute("EDNo");
                if(ValType(EDNo) == V_SPECVAL)
                  EDNo = "Значения нет";
                end;
                EDDate = ED807.item(0).getAttribute("EDDate");
                if(ValType(EDDate) == V_SPECVAL)
                  EDDate = "Значения нет";
                else
                  EDDate = SubStr(EDDate, 9, 2) + "." + SubStr(EDDate, 6, 2) + "." + SubStr(EDDate, 1, 4);
                end;
                CreationDateTime = ED807.item(0).getAttribute("CreationDateTime");
                if(ValType(CreationDateTime) == V_SPECVAL)
                  CreationDate = "Значения нет";
                  CreationTime = "Значения нет";
                else
                  CreationDate = SubStr(CreationDateTime, 9, 2) + "." + SubStr(CreationDateTime, 6, 2) + "." + SubStr(CreationDateTime, 1, 4);
                  CreationTime = SubStr(CreationDateTime, 12, 8);
                end;

                BusinessDay = ED807.item(0).getAttribute("BusinessDay");
                if(ValType(BusinessDay) == V_SPECVAL)
                  BusinessDay = "Значения нет";
                else
                  BusinessDay = SubStr(BusinessDay, 9, 2) + "." + SubStr(BusinessDay, 6, 2) + "." + SubStr(BusinessDay, 1, 4);
                end;

                DirectoryVersion = ED807.item(0).getAttribute("DirectoryVersion");
                if(ValType(DirectoryVersion) == V_SPECVAL)
                  DirectoryVersion = "Значения нет";
                end;
                BICDirectoryEntry = ED807.item(0).getElementsByTagName(prefix + "BICDirectoryEntry");
              end;
            else
              MsgBox("Ошибка: файл " + FullImportFileName + " имеет не поддерживаемую структуру. Отсутствует контейнер ED807");
              stat = false;
            end;
          else
            MsgBox("Ошибка: файл " + FullImportFileName + " имеет не поддерживаемую структуру. Отсутствует контейнер ED807");
            stat = false;
          end;
        else
          MsgBox("Ошибка: файл " + FullImportFileName + " имеет не поддерживаемую структуру. Отсутствует контейнер ED807");
          stat = false;
        end;
      else
        MsgBox("Ошибка загрузки xml файла");
      end;
    end;
  else
    stat = false;
    setoutput();
    exit(1);
  end;

  if(stat)
    if(RunImportED807Text(xml.xml, FullImportFileName, 0, 0))
      println("Ошибка при импорте Справочника БИК ЦБ РФ");
    else
      if (SilentMode)
        setoutput(arh + ImportFileName + ".txt", true);
      end;
      println("Протокол импорта файла Справочника БИК ЦБ РФ\n");
      println("Файл: ", FullImportFileName);
      println("InfoTypeCode=", InfoTypeCode, " EDNo=", EDNo, " EDDate=", EDDate, " CreationDateTime=", CreationDate, " ", CreationTime);
      println("BusinessDay=", BusinessDay, " DirectoryVersion=", DirectoryVersion);
      println("Количество записей: ", BICDirectoryEntry.length);
      println("\nДата: ", date, " ", time);
      cmd = RsdCommand("SELECT t_Name FROM dperson_dbt WHERE t_Oper = " + {oper});
        cmd.execute;
        rs = RsdRecordset(cmd);
        if(rs.moveNext)
          NameOper = rs.value(0);
        end;
      println("Пользователь: ", {oper}, " ", NameOper, "\n");
      cmd = RsdCommand("SELECT t_SysDate, t_SysTime, t_Message FROM dimpbiclog_tmp WHERE t_Message LIKE 'PartAggregateID%'"); 
      cmd.execute;
      rs = RsdRecordset(cmd);
      if(rs.moveNext)
        strHeading = rs.value(2);
        println(strHeading);
      end;
      cmd = RsdCommand("SELECT t_SysDate, t_SysTime, t_Message FROM dimpbiclog_tmp ORDER BY t_SysDate, t_SysTime "); 
      cmd.execute;
      rs = RsdRecordset(cmd);
      while(rs.moveNext)
        if(rs.value(2) != strHeading)
          println(rs.value(2));
        end;
      end;
      PrintProtocolErrED807();
    end;
  end;

  // KS start 14.05.2019
  if (SilentMode)
    setoutput();
    renameFile(FullImportFileName, arh + ImportFileName);
    // Поиск следующего подходящего файла
    stat = FileFindNext( ImportFileName, Attr );
    if(stat)
      FullImportFileName = ImportFileDir + ImportFileName;
      setoutput(arh + ImportFileName + ".txt", true);
    end;
  else
    stat = false;
  end;
  end;
  if (SilentMode)
    FileFindClose(); // закроем поток
  end;
  // KS end
end;