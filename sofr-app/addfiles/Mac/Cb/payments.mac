/*
$Name:             payments.mac
$Module:           Ядро Banking
$Description:      Макрофункции для работы с платежами
*/

/*
 *   payments.mac
 *
 *   Макрофункции для работы с платежами
 *
 *   Режимы функции проверки:
 *     PMMAC_DELETE    = 1 - удаление
 *     PMMAC_INSERT    = 2 - ввод
 *     PMMAC_UPDATE    = 3 - обновление
 *     PMMAC_GENERATED = 4 - проверка сгенерированного платежа
 *   (константы доступны при подключенном PaymInter)
 *
 *   09.02.99 AS
 *
 */

import PaymInter;
import oralib,likepy;

import bnk_common; // KS 24.05.2019 RSHBSOFR-1019 Перечень проверок

record Платеж ( pmpaym );
record Дебет  ( pmprop );
record Кредит ( pmprop );
record МакетR ( pmrmprop );

record СтарыйПлатеж ( pmpaym );
record СтарыйДебет  ( pmprop );
record СтарыйКредит ( pmprop );
record СтарыйМакетR ( pmrmprop );



/*
 *                    Инициализация нового платежа
 */
macro НовыйПлатеж
   return 0;
end;

private macro ПроверитьСчетВалКлючДлина(_Account, _BIC, _leftright)

  if ( ( StrLen(_Account) == 0 ) )
     return 0;
  end;

  if ( ( StrLen(_Account) != 20 ) )
     MsgBox("Длина номера счета " + _leftright + " не соответствует требуемой (20 символов)");
     return 1;
  end;

  if ( ( GetKey(_Account, _BIC) != _Account ) )
     MsgBox("Неверный ключ в номере счета " + _leftright + "! Правильный - " + substr(GetKey(_Account, _BIC),9,1));
     return 1;
  end;

  return 0;
end;

private macro ПроверитьИННДлина(_INN, _partyid, _leftright)

  if ( ( StrLen(_INN) == 0 ) )
     return 0;
  end;

  var i = index(_INN, "/");
  if (i > 0)
    _INN = substr(_INN, 1, i-1);
  end;

  // Предварительная проверка
  /*if ( ( StrLen(_INN) != 10 ) and ( StrLen(_INN) != 12 ))
     MsgBox("Длина ИНН " + _leftright + " не соответствует требуемой (10 для ЮЛ и 12 для ФЛ)");
     return 1;
  end;*/

  var query, rs;
  var params : TArray;
  query =         "SELECT t_LegalForm ";
  query = query + "  FROM dparty_dbt ";
  query = query + " WHERE t_partyid = ? and T_NOTRESIDENT = chr(0)"; /*PNV 538373*/
  params = makeArray(SQLParam("", _partyid));
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    if   ((rs.value("t_LegalForm") == PTLEGF_PERSN) and ( StrLen(_INN) != 12 ))
      MsgBox("Длина ИНН " + _leftright + " не соответствует требуемой (12 для ФЛ)");
      return 1;
    elif ((rs.value("t_LegalForm") == PTLEGF_INST)  and ( StrLen(_INN) != 10 ))
      MsgBox("Длина ИНН " + _leftright + " не соответствует требуемой (10 для ЮЛ)");
      return 1;
    end
  end;

  return 0;
end;

/*
 *              Проверка платежа при вводе/редактировании
 */
macro ПроверитьПлатеж( режим )

     if ((Платеж.fiid == 0) and
         (Платеж.payfiid == 0))
       if (Дебет.codekind == 3)
         if(ПроверитьСчетВалКлючДлина(Платеж.payeraccount, Дебет.bankcode, "плательщика") == 1)
           return 1;
         end;
       end;
       if (Кредит.codekind == 3)
         if(ПроверитьСчетВалКлючДлина(Платеж.receiveraccount, Кредит.bankcode, "получателя") == 1)
           return 1;
         end;
       end;
       if (ПроверитьИННДлина(МакетR.payerINN, Платеж.payer, "плательщика") == 1)
           return 1;
       end;
       if (ПроверитьИННДлина(МакетR.receiverINN, Платеж.receiver, "получателя") == 1)
           return 1;
       end;
     end;

     return 0;
end;



/*
 *                    Пользовательская функция
 *   Режимы функции
 *      UFN_PANEL_INPUT = 1 - панель ввода
 *      UFN_PANEL_EDIT  = 2 - панель редактирования
 *      UFN_SCROL       = 3 - вызов из скроллинга
 *   (константы доступны при подключенном BankInter)
 */

macro ПользФункцияПлатеж( режим )
  var sql,rs,rr,rs2;
  if ((Платеж.dockind == 199) or (Платеж.dockind == 4811) or (Платеж.dockind == 4813) or (Платеж.dockind == 4815))
    if( MsgBoxEx("Вы хотите снять/установить признак неттинга с платежа ?", MB_YES + MB_NO, IND_YES) == IND_YES )
     if (Платеж.NETTING == "X")
      
      sql = "UPDATE dpmpaym_dbt t SET t.t_netting = chr(0) WHERE t.t_paymentid = "+Платеж.PaymentID+" AND t.t_netting = chr(88)";
      rs = ExecSql(sql);
      if (rs != null)
        MsgBox("Признак неттинга успешно снят с платежа!");
      else
        MsgBox("Ошибка! Невозможно убрать признак неттинга!");
        return 1;
      end;
     else
      //MsgBox("На данном платеже отсутствует признак неттинга!");
      sql = "UPDATE dpmpaym_dbt t SET t.t_netting = chr(88) WHERE t.t_paymentid = "+Платеж.PaymentID+" AND t.t_netting = chr(0)";
      rs = ExecSql(sql);
      if (rs != null)
        MsgBox("Признак неттинга успешно установлен на платежа!");
      else
        MsgBox("Ошибка! Невозможно установить признак неттинга!");
        return 1;
      end;
     end;
    end;
    return 0;
  end;

  if (Платеж.dockind == 102)
      sql="select t_ground  from dpmrmprop_dbt where t_paymentid="+Платеж.PaymentID;
      rs = execSQLselect(sql, null, true);
      if(rs and rs.moveNext())
         rr=rs.value(0);
         getstring(rr,"Ввод основания платежа ",200);
         sql="update dpmrmprop_dbt set t_ground='"+rr+"' where t_paymentid="+Платеж.PaymentID;
         rs2 = ExecSql(sql);
      end;
  end;


end;

/* Установка подсказки для скролингов из макроса */

private const Hint_Payer:string =
"/*+FIRST_ROWS LEADING(t pmprop pmprop2 pmrmprop ) INDEX(t dpmpaym_dbt_idxf) USE_NL(t pmprop pmprop2 pmrmprop)*/";

private const Hint_Receiver:string =
"/*+FIRST_ROWS LEADING(t pmprop pmprop2 pmrmprop ) INDEX(t dpmpaym_dbt_idx10) USE_NL(t pmprop pmprop2 pmrmprop)*/";

private const Hint_ValueDate:string =
"/*+FIRST_ROWS LEADING(t pmprop pmprop2 pmrmprop ) INDEX(t DPMPAYM_DBT_IDX11) USE_NL(t pmprop pmprop2 pmrmprop)*/";

private MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string ):string
  if( IndexNum == PM_PAYMKEY_PAYER )
    return Hint_Payer;
  elif( IndexNum == PM_PAYMKEY_RECEIVER )
    return Hint_Receiver;
  elif( IndexNum == PM_PAYMKEY_VALUE_DATE )
    return Hint_ValueDate;
  end;
  return DefaultHint;

END;
