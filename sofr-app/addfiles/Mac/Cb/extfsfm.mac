/*
$Name:               extfsfm.mac
$Module:            Фин. мониторинг
$Description:      Формирование выписки по запросу ФМ
*/
import globals, BankInter, PaymInter, FMInter, PTInter, FIInter, likepy, oralib, wltools,
       "bnk_ptlib.mac", "bnk_dttmfrmt.mac";

var FileBody: string;
private record party ( party );
private var persn = Tbfile("persn.dbt", "r");
/* Виды платежей */
const WL_DEBET   =  1, /*'Д' - дебетовый  */ 
      WL_CREDIT  =  2, /*'К' - кредитовый */
      WL_DKUNDEF = -1; /* Неопределен */

private macro WriteValue(name, val)
  FileBody = FileBody + name + ":" + val + "\n";
end;

private macro WriteEndBlock
  FileBody = FileBody + "###\n";
end;

private macro WriteEndFrag
  FileBody = FileBody + "@@@\n";
end;

private macro WriteEndFile
  FileBody = FileBody + "===\n";
end;

private macro ФормированиеДанныхКонтрагента(rsacc: RsdRecordset)
  /*НомСчПП*/
  var AccountPP = rsacc.value("t_ClientAccount"); /* значение поля "НомСчПП" */
  if (AccountPP == "") AccountPP = " "; end;
  var AccountPPClient = int(rsacc.value("t_ClientID")); /* клиент по счету из поля "НомСчПП"  */
  var FullINN = rsacc.value("t_ClientINN");
  var INNPP = RemoveKPP(FullINN);
  
  /*НаимПП / ФИОПП*/
  var IsPerson = strlen(INNPP) == 12;
  if( (rsacc.value("t_UsePayment") > 0) and (AccountPPClient <= 0) )
    if (IsPerson)
      WriteValue("ФИОПП", substr( rsacc.value("t_ClientName"), 1, 160 ));
    else
      WriteValue("НаимПП", substr( rsacc.value("t_ClientName"), 1, 160 ));
    end;
  elif(AccountPPClient > 0)
    if(not IsPerson)
      ПолучитьСубъекта( AccountPPClient, party );
      WriteValue("НаимПП", substr( party.Name, 1, 160 ));
    else
      persn.rec.PersonID = AccountPPClient;
      GetEQ(persn);
      WriteValue("ФИОПП", substr( persn.rec.Name1 + " " + persn.rec.Name2 + " " + persn.rec.Name3, 1, 160 ) );
    end;
  end;

  /*ИННПП*/
  if( (INNPP == "") or (strlen(INNPP) > 12) or (not StrIsNumber(INNPP)) )
    INNPP = " ";
  end;
                
  WriteValue("ИННПП", INNPP);
  /*КПППП*/
  var KPPPP = RemoveINN(FullINN);
  if (IsPerson or (not StrIsNumber(KPPPP)) or (strlen(KPPPP) > 9))
    KPPPP = " ";
  end;
  WriteValue("КПППП", KPPPP);

  /*НомСчПП*/
  WriteValue("НомСчПП", AccountPP);

end;

private macro ProcessAcTrn(rs, DateBegin, DateEnd)
  record Fin( fininstr ); ClearRecord( Fin );
  record Acc( account  ); ClearRecord( Acc );
  WriteValue( "НомСч", rs.value("t_Account") );
  ПолучитьФинИн( rs.value("t_Code_Currency"), Fin );
  WriteValue( "ВалСч", Fin.ISO_Number );
  WriteValue( "ДатаНачала", DDpMMpYYYY( DateBegin ) );
  WriteValue( "ДатаКонца", DDpMMpYYYY( DateEnd ) );
  WriteValue( "ОстатНач", GetFreeAmount(rs.value("t_Account"), 1, rs.value("t_Code_Currency"), DateBegin - 1) );

  var query = "  SELECT ad.t_DKFlag,\n"
              "         NVL(SUM (DECODE (ad.t_DKFlag, 0, ad.t_Sum_Payer, ad.t_Sum_Receiver)), 0) t_Sum\n"
              "    FROM (SELECT ad.*, DECODE (?, ad.t_Account_Payer, 0, 1) t_DKFlag\n"
              "            FROM dacctrn_dbt ad\n"
              "           WHERE     ad.t_Chapter = 1\n"
              "                 AND ad.t_State = 1\n"
              "                 AND (   (    ad.t_Account_Payer = ?\n"
              "                          AND ad.t_FIID_Payer = ?\n"
              "                          AND ad.t_Sum_Payer <> 0)\n"
              "                      OR (    ad.t_Account_Receiver = ?\n"
              "                          AND ad.t_FIID_Receiver = ?\n"
              "                          AND ad.t_Sum_Receiver <> 0))\n"
              "                 AND ad.t_Date_Carry BETWEEN ? AND ?) ad\n"
              "GROUP BY t_DKFlag";
  var rsacc:RsdRecordset = execSQLselect(query, 
      MakeArray(SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Code_Currency")),
        SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Code_Currency")),
        SQLParam("", DateBegin),
        SQLParam("", DateEnd)));
  var sum = MakeArray($0, $0);
  while (rsacc.moveNext())
    sum(rsacc.value(0)) = rsacc.value("t_Sum", V_MONEY);
  end;
  WriteValue( "СуммаДеб", string(sum(0):0:2) );
  WriteValue( "СуммаКред", string(sum(1):0:2) );
  WriteValue( "ОстатКон", GetFreeAmount(rs.value("t_Account"), 1, rs.value("t_Code_Currency"), DateEnd) );
  WriteEndBlock;

    /*Блок "ВыпискаПоСчету\ОперацииПоСчету"*/
  query = string(
"SELECT ad.*,\n"
"       CASE\n"
"          WHEN ad.t_UsePayment = 1\n"
"          THEN\n"
"             CASE\n"
"                WHEN ad.t_DKFlag = 1 THEN ad.t_ReceiverINN\n"
"                ELSE ad.t_PayerINN\n"
"             END\n"
"          ELSE\n"
"             NVL (PM_SCRHLP.GetPartyCode (ad.t_ClientID, 16), CHR (1))\n"
"       END\n"
"          t_ClientINN\n"
"  FROM (SELECT ad.*,\n"
"               CASE\n"
"                  WHEN ad.t_UsePayment = 0\n"
"                  THEN\n"
"                     acc.t_Client -------------------------обработка настройки банка\n"
"                  ELSE\n"
"                     CASE\n"
"                        WHEN ad.t_DKFlag = 1 THEN pmpaym.t_Receiver\n"
"                        ELSE pmpaym.t_Payer\n"
"                     END\n"
"               END\n"
"                  t_ClientID,\n"
"               CASE\n"
"                  WHEN ad.t_UsePayment = 0\n"
"                  THEN\n"
"                     acc.t_Account\n"
"                  ELSE\n"
"                     CASE\n"
"                        WHEN ad.t_DKFlag = 1 THEN pmpaym.t_ReceiverAccount\n"
"                        ELSE pmpaym.t_PayerAccount\n"
"                     END\n"
"               END\n"
"                  t_ClientAccount\n"
"          FROM (SELECT ad.*,\n"
"                       NVL (prp.t_ShifrOper, ad.t_Shifr_Oper) t_ShifrOper,\n"
"                       NVL (prp.t_Number, ad.t_Numb_Document) t_Number,\n"
"                       NVL (prp.t_Date, ad.t_Date_Carry) t_Date,\n"
"                       CASE\n",
"                          WHEN pmpaym.t_PaymentID IS NULL\n"
"                          THEN\n"
"                             ' '\n"
"                          WHEN ad.t_DKFlag = 1                    /*WL_DEBET*/\n"
"                          THEN\n"
"                             CASE\n"
"                                WHEN prp.t_ReceiverCorrAccNostro = CHR (1)\n"
"                                THEN\n"
"                                   ' '\n"
"                                ELSE\n"
"                                   prp.t_ReceiverCorrAccNostro\n"
"                             END\n"
"                          ELSE\n"
"                             CASE\n"
"                                WHEN prp.t_PayerCorrAccNostro = CHR (1)\n"
"                                THEN\n"
"                                   ' '\n"
"                                ELSE\n"
"                                   prp.t_PayerCorrAccNostro\n"
"                             END\n"
"                       END\n"
"                          t_CorrAcc,\n"
"                       CASE\n"
"                          WHEN pmpaym.t_PaymentID IS NULL THEN CHR (1)\n"
"                          WHEN ad.t_DKFlag = 1 THEN prp.t_ReceiverBankName\n"
"                          ELSE prp.t_PayerBankName\n"
"                       END\n"
"                          t_BankName,\n"
"                       CASE\n"
"                          WHEN pmpaym.t_PaymentID IS NULL\n"
"                          THEN\n"
"                             CHR (1)\n"
"                          WHEN ad.t_DKFlag = 1\n"
"                          THEN\n"
"                             CASE\n"
"                                WHEN pc.t_CodeKind = 3         --CNST.PTCK_BIC\n"
"                                THEN\n"
"                                   pc.t_BankCode\n"
"                                ELSE\n"
"                                   NVL(PM_SCRHLP.GetPartyCode (\n"
"                                      pmpaym.t_ReceiverBankID,\n"
"                                      3                      /*CNST.PTCK_BIC*/\n"
"                                       ), CHR(1))\n"
"                             END\n"
"                          ELSE\n",
"                             CASE\n"
"                                WHEN pd.t_CodeKind = 3         --CNST.PTCK_BIC\n"
"                                THEN\n"
"                                   pd.t_BankCode\n"
"                                ELSE\n"
"                                   NVL(PM_SCRHLP.GetPartyCode (\n"
"                                      pmpaym.t_PayerBankID,\n"
"                                      3                      /*CNST.PTCK_BIC*/\n"
"                                       ), CHR(1))\n"
"                             END\n"
"                       END\n"
"                          t_BankCode,\n"
"                       CASE\n"
"                          WHEN pmpaym.t_PaymentID IS NULL\n"
"                          THEN\n"
"                             CHR (1)\n"
"                          ELSE\n"
"                             CASE\n",
"                                WHEN ad.t_DKFlag = 1 THEN prp.t_ReceiverName\n"
"                                ELSE prp.t_PayerName\n"
"                             END\n"
"                       END\n"
"                          t_ClientName,\n"
"                       NVL (prp.t_Ground, ad.t_Ground) t_Description,\n"
"                       DECODE (ad.t_DKFlag,\n"
"                               1, ad.t_Sum_Payer,\n"
"                               ad.t_Sum_Receiver)\n"
"                          t_Sum,\n"
"                       CASE\n"
"                          WHEN    pd.t_PaymentID IS NULL\n"
"                               OR EXISTS\n"
"                                     (SELECT 1\n"
"                                        FROM dpmaddpi_dbt pma\n"
"                                       WHERE     pma.t_PaymentID =\n"
"                                                    pd.t_PaymentID\n"
"                                             AND pma.t_DebetCredit =\n"
"                                                    DECODE (ad.t_DKFlag,\n"
"                                                            1, 1,\n"
"                                                            0))\n"
"                          THEN\n"
"                             0\n"
"                          ELSE\n"
"                             1\n"
"                       END\n",
"                          t_UsePayment,\n"
"                       NVL (pd.t_PaymentID, 0) t_PaymentID,\n"
"                       NVL (prp.t_ReceiverINN, CHR (1)) t_ReceiverINN,\n"
"                       NVL (prp.t_PayerINN, CHR (1)) t_PayerINN\n"
"                  FROM (SELECT ad.*,\n"
"                               DECODE (?, ad.t_Account_Payer, 1, 2) t_DKFlag\n"
"                          FROM dacctrn_dbt ad\n"
"                         WHERE     ad.t_Chapter = 1\n"
"                               AND ad.t_State = 1\n"
"                               AND (   (    ad.t_Account_Payer = ?\n"
"                                        AND ad.t_FIID_Payer = ?\n"
"                                        AND ad.t_Sum_Payer <> 0)\n"
"                                    OR (    ad.t_Account_Receiver = ?\n"
"                                        AND ad.t_FIID_Receiver = ?\n"
"                                        AND ad.t_Sum_Receiver <> 0))\n"
"                               AND ad.t_Date_Carry BETWEEN ? AND ?) ad,\n"
"                       dpmdocs_dbt pmd,\n",
"                       dpmpaym_dbt pmpaym,\n"
"                       dpmprop_dbt pc,\n"
"                       dpmprop_dbt pd,\n"
"                       dpmrmprop_dbt prp\n"
"                 WHERE     pmd.t_AccTrnID(+) = ad.t_AccTrnID\n"
"                       AND pmpaym.t_PaymentID(+) = pmd.t_PaymentID\n"
"                       AND pc.t_PaymentID(+) = pmd.t_PaymentID\n"
"                       AND pc.t_DebetCredit(+) = 1\n"
"                       AND pd.t_PaymentID(+) = pmd.t_PaymentID\n"
"                       AND pd.t_DebetCredit(+) = 0\n"
"                       AND prp.t_PaymentID(+) = pmd.t_PaymentID) ad,\n"
"               dpmpaym_dbt pmpaym,\n"
"               daccount_dbt acc\n"
"         WHERE     acc.t_Code_Currency =\n"
"                      DECODE (ad.t_DKFlag,\n"
"                              1, ad.t_FIID_Receiver,\n"
"                              ad.t_FIID_Payer)\n"
"               AND acc.t_Account =\n"
"                      DECODE (ad.t_DKFlag,\n"
"                              1, ad.t_Account_Receiver,\n"
"                              ad.t_Account_Payer)\n"
"               AND pmpaym.t_PaymentID(+) = ad.t_PaymentID) ad");

  rsacc = execSQLselect(query, 
      MakeArray(SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Code_Currency")),
        SQLParam("", rs.value("t_Account")),
        SQLParam("", rs.value("t_Code_Currency")),
        SQLParam("", DateBegin),
        SQLParam("", DateEnd)));
  var tmpstr;
  var rcount = 0;
  if (rsacc)
    while ( rsacc.moveNext() )
      WriteValue( "ДатаОпер", DDpMMpYYYY( rsacc.value( "t_Date_Carry" ) ) );
      WriteValue( "ВидДок", rsacc.value( "t_ShifrOper" ) );
      var DocNumLen = strLen( rsacc.value( "t_Number" ) );
      if( DocNumLen <= 6 )
        WriteValue( "НомДок", rsacc.value( "t_Number" ) );
      else /* последние 6 символов */
        WriteValue( "НомДок", subStr( rsacc.value( "t_Number" ), DocNumLen - 5 ) );
      end;
      WriteValue( "ДатаДок", DDpMMpYYYY( rsacc.value("t_Date") ) );
      WriteValue( "НомКорСч", rsacc.value("t_CorrAcc") );
      if( rsacc.value("t_PaymentID") > 0 )
        WriteValue( "НаимБП", rsacc.value("t_BankName"));
        tmpstr = rsacc.value("t_BankCode");
      else
        WriteValue( "НаимБП", rs.value("bankName"));
        tmpstr = GetPartyCode( rs.value("bank"), PTCK_BIC );
      end;
      if( tmpstr == "" )
        tmpstr = "000000000";
      end;
      WriteValue( "БИКБП", tmpstr );

      ФормированиеДанныхКонтрагента(rsacc);

      /*Дебет*/
      if( rsacc.value( "t_DKFlag" ) == WL_DEBET )
        WriteValue( "Дебет", string( rsacc.value( "t_Sum" ):0:2 ) );
        WriteValue( "Кредит", "0.00" );
      else
        WriteValue( "Дебет", "0.00" );
        WriteValue( "Кредит", string( rsacc.value( "t_Sum" ):0:2 ) );
      end;
      WriteValue( "НазнПл", rsacc.value( "t_Description" ) );
      WriteEndBlock;
      rcount = rcount + 1;
    end;
  end;
  return rcount;
end;

macro CheckAcc(rs, ClientID, Department, SubBranch)
  if (ClientID != rs.value("t_Client"))
    return false;
  end;
  if (SubBranch)
    var r = execSQLselect("select 1 from dual where ? in (SELECT includedDprt.t_code\n"
                                      "                     FROM ddp_dep_dbt includedDprt\n"
                                      "               CONNECT BY PRIOR includedDprt.t_code =\n"
                                      "                             includedDprt.t_parentCode\n"
                                      "               START WITH includedDprt.t_code = ?)\n", MakeArray(SQLParam("", rs.value("t_Department")), SQLParam("", Department)));
    return (r and r.moveNext());
  elif (rs.value("t_Department") != Department)
    return false;
  end;

  return true;
end;

macro GetExtractAccount(
                                ClientID, // - ID клиента (обязательный)
                                Department, // - Филиал, исполняющий запрос (обязательный)
                                SubBranch, // - Признак "по всем подчиненным подразделениям" (необязательный, передается, если необходимо сформировать выписку по счетам всех филиалов банка)
                                DateBegin: Date, // - ..
                                DateEnd: Date, //  - Период, за который формируется выписка (обязательный)
                                Account, // - номер счета (необязательный)
                                //AllAccounts, удален, считается установленным если Account == "" - Флаг "по всем счетам" (обязательно взведен, если не передан Account)
                                ExistData: @integer, // - Признак наличия запрашиваемых данных (обязательный). Принимает значения:
                                            //0 - запрашиваемые данные отсутствуют;
                                            //1 - запрашиваемые данные есть в полном объеме;
                                            //2 - запрашиваемые данные есть частично
                                Comment: @string,
                                IsInfoAbsent: @bool
                               ): integer
  var query = "";
  FileBody = "";
  var cache = PaymentFieldStringCache;
  var accounts, rs;
  IsInfoAbsent = false;
  var IsExists = false;
  if (Account == "")
    rs = execSQLselect("select bnk.t_Name bankName, cln.t_Name clientName, cln.t_PartyID t_Client "
         "  from dparty_dbt bnk, dparty_dbt cln "
         " where bnk.t_PartyID = ? and cln.t_PartyID = ? ", MakeArray(SQLParam("", {OurBank}), SQLParam("", ClientID)));
    if (not rs.MoveNext())
      MemoryError(1, "Клиент не найден.");
      DisplayError();
      ExistData = 0;
      return 1;
    end;
    
    query = "select acc.*, dep.t_PartyID bank, bnk.t_Name bankName, cln.t_Name clientName "
            "  from daccount_dbt acc, ddp_dep_dbt dep, dparty_dbt bnk, dparty_dbt cln "
            " where acc.t_Client = ? and acc.t_Open_Date <= ? and (acc.t_Close_Date = to_date('01010001', 'DDMMYYYY') or acc.t_Close_Date >= ?) "
            "   and acc.t_Chapter = 1 and acc.t_Department = dep.t_Code and acc.t_Department in (";
    if (SubBranch)
      query = query + "SELECT includedDprt.t_code\n"
   "                     FROM ddp_dep_dbt includedDprt\n"
   "               CONNECT BY PRIOR includedDprt.t_code =\n"
   "                             includedDprt.t_parentCode\n"
   "               START WITH includedDprt.t_code = ?)\n";
    else
      query = query + " ?) ";
    end;
    query = query + "   and dep.t_PartyID = bnk.t_PartyID and acc.t_Client = cln.t_PartyID and (1 = 0 ";
    var Code = "", Error;
    GetRegistryValue("ФИНМОНИТОРИНГ\\407-П\\04 ВЫПИСКА\\STATEMENTTYPEACC", V_STRING, Code, Error);
    if (Code == "")
      Code = "ЧXЯQJG";
    end;
    var i;
    for (i, 1, strlen(Code), 1)
      query = query + " or acc.t_Type_Account like '%" + substr(Code, i, 1) + "%' ";
    end;
    query = query + ")";
    accounts = execSQLselect(query, 
        MakeArray(SQLParam("", ClientID), SQLParam("", DateEnd),
          SQLParam("", DateBegin), SQLParam("", Department)));
    IsExists = (accounts and accounts.moveNext());
  else
    rs = execSQLselect("select bnk.t_Name bankName, cln.t_Name clientName, cln.t_PartyID t_Client "
         "  from daccount_dbt acc, ddp_dep_dbt dep, dparty_dbt bnk, dparty_dbt cln "
         " where bnk.t_PartyID = ? and acc.t_Client = cln.t_PartyID "
         "   and acc.t_Account = ? and acc.t_Chapter = 1 and acc.t_Code_Currency = ? ", 
         MakeArray(SQLParam("", {OurBank}), SQLParam("", Account), SQLParam("", cache.GetFIID(substr(Account, 6, 3)))));
    if (not rs.MoveNext())
      MemoryError(1, "Счет не найден.");
      DisplayError();
      ExistData = 0;
      return 1;
    end;
    query = "select acc.*, dep.t_PartyID bank, bnk.t_Name bankName, cln.t_Name clientName "
            "  from daccount_dbt acc, ddp_dep_dbt dep, dparty_dbt bnk, dparty_dbt cln "
            " where acc.t_Account = ? and acc.t_Chapter = 1 and acc.t_Code_Currency = ? and acc.t_Department = dep.t_Code "
            "   and dep.t_PartyID = bnk.t_PartyID and acc.t_Client = cln.t_PartyID";
    accounts = execSQLselect(query, 
        MakeArray(SQLParam("", Account),
          SQLParam("", cache.GetFIID(substr(Account, 6, 3)))));
    IsExists = (accounts and accounts.moveNext());
    if (IsExists and (not CheckAcc(accounts, ClientID, Department, SubBranch)))
      GetRegistryValue("ФИНМОНИТОРИНГ\\407-П\\04 ВЫПИСКА\\SENDNOTBELONGSTATEMENT", V_BOOL, Code, Error);
      IsInfoAbsent = true;
      if (not Code)
        ExistData = 0;
        return 0;
      end;
    end;
  end;
  ExistData = 1;
  var r;
  if (IsExists)
    WriteValue("ИдФайл", "<!--~`#1-->");
    WriteValue("ТипИнф", "ОПЕРАЦИИПОСЧ");
    WriteValue("ВерсПрог", "RS-Bank V.6");
    r = execSQLselect("select NVL(off.t_PhoneNumber, chr(1)), NVL(off.t_Post, chr(1)), pers.t_Name from dofficer_dbt off, dperson_dbt pers where pers.t_Oper = ? and off.t_PersonID(+) = pers.t_PartyID", MakeArray(SQLParam("", {oper})));
    r.moveNext();
    WriteValue("ТелОтпр", r.value(0));
    WriteValue("ДолжнОтпр", r.value(1));
    WriteValue("ФамОтпр", r.value(2));
    WriteValue("КолДок", "1");
    WriteValue("ВерсФорм", "2.01");
    WriteEndBlock;
    WriteEndFrag;
    WriteValue("ИдДок", execStoredFunc( "sys_guid", V_STRING ));
    var Seq  = "";
    var stat = GetSequenceValue( 238, Seq, {curdate}, time );
    if (stat != 0)
      MemoryError(stat);
      DisplayError();
      ExistData = 0;
      return 1;
    end;
    WriteValue("НомВыпис", string(Seq));
    var FullINN = GetPartyINN({OurBank}, 1);
    WriteValue("ИННКО", RemoveKPP( FullINN ) );
    WriteValue("КППКО", RemoveINN( FullINN ) );
    Code = ПолучитьКодСубъекта({OurBank}, PTCK_BIC, Error, 1);
    WriteValue( "БИК", code );
    WriteValue( "НаимКО", rs.value("bankName") );
    Code = ПолучитьКодСубъекта( {OurBank}, PTCK_BANKREGNUM, Error);
    var ind = index(Code, "/");
    if ( (ind == 0) or (ind == strlen(Code) ) )
      WriteValue( "НомФ", "0" );
    else
      WriteValue( "НомФ", SubStr(Code, ind + 1) );
    end;
    FullINN = GetPartyINN(rs.value("t_Client"), 1);
    var INNNP = RemoveKPP( FullINN );
    WriteValue("ИНННП", INNNP );
    if (strlen(INNNP) != 12)
      WriteValue("КППНП", RemoveINN( FullINN ) );
      WriteValue("НаимНП", rs.value("clientName") );
    else
      persn.rec.PersonID = rs.value("t_Client");
      GetEQ(persn);
      WriteValue("ФИОИП", persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3);
    end;
    GetRegistryValue("ФИНМОНИТОРИНГ\\407-П\\04 ВЫПИСКА\\SIGNATUREPOSITION", V_STRING, Code, Error);
    WriteValue("ДолжнПрБ", Code);
    GetRegistryValue("ФИНМОНИТОРИНГ\\407-П\\04 ВЫПИСКА\\SIGNATURENAME", V_STRING, Code, Error);
    WriteValue( "ФИОПрБ", Code );
    WriteValue( "ДатаСправ", DDpMMpYYYY({curdate}) );
    WriteValue( "ДатаСооб", DDpMMpYYYY(date) );
    WriteEndBlock;
    if (ProcessAcTrn(accounts, DateBegin, DateEnd) == 0)
      ExistData = 2;
    end;
    while (accounts.moveNext())
      if (ProcessAcTrn(accounts, DateBegin, DateEnd) == 0)
        ExistData = 2;
      end;
    end;
    WriteEndFrag;
    WriteEndFile;
  else
    r = execSQLselect("select party.t_Name from dparty_dbt party, ddp_dep_dbt dpdep where dpdep.t_partyID = party.t_PartyID and dpdep.t_Code = ?", MakeArray(SQLParam("", Department)));
    var Name = "";
    if (r.moveNext)
      Name = r.value(0);
    end;
    var err = "В банке (филиале) " + Name;
    if (SubBranch)
      err = err + ", а также в подчиненных ему филиалах,";
    end;
    err = err + " не найдено счетов,  принадлежащих ";
    if(strlen(GetPartyINN(ClientID, 1)) != 12)
      ПолучитьСубъекта( ClientID, party );
      err = err + party.Name;
    else
      persn.rec.PersonID = ClientID;
      GetEQ(persn);
      err = err + persn.rec.Name1 + " " + persn.rec.Name2 + " " + persn.rec.Name3;
    end;
    MemoryError(1, Err);
    DisplayError();
    ExistData = 0;
    return 1;
  end;
  return 0;
onerror(erObj)
  ExistData = 0;
  MemoryError(erObj.code);
  DisplayError();
  return 1;
end;

macro GetExtract(fmrqa, Extract_Body: @string, IsInfoAbsent: @bool)
  record fmreqiattach("fmreqiattach.dbt");
  setbuff(fmreqiattach, fmrqa);
  var Comment: string = "";
  if ((fmreqiattach.PeriodDateBegin == date(0,0,0)) or (fmreqiattach.PeriodDateEnd == date(0,0,0)))
    MemoryError(26249);
    DisplayError();
    return 26249;
  end;
  file fmrequestinfo("fmrequestinfo.dbt");
  fmrequestinfo.ID = fmreqiattach.requestInfoId;
  GetEQ(fmrequestinfo);
  var stat = GetExtractAccount(fmrequestinfo.ClientID, fmrequestinfo.sourcedprt, /*fmrequestinfo.childdprtinc == "X"*/true, 
                               fmreqiattach.PeriodDateBegin, fmreqiattach.PeriodDateEnd, fmreqiattach.Account, 
                               @fmreqiattach.InfoAvailability, @Comment, @IsInfoAbsent);
  if (stat == 0)
    if (Comment != "")
      if (fmreqiattach.AvailComment != "")
        fmreqiattach.AvailComment = fmreqiattach.AvailComment + "\n";
      end;
      fmreqiattach.AvailComment = fmreqiattach.AvailComment + Comment;
    end;
    Extract_Body = FileBody;
    fmreqiattach.AttachFileCreateDate = date;
    fmreqiattach.AttachFileCreateTime = time;
    fmreqiattach.AttachFileName = FM_GetReqAttachName(fmreqiattach, "txt");
  end;
  return stat;
end;