/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab 2005              */
/*  Имя файла        : cbsfinvac.mac                                    */
/*  Описание         : Печать счета на оплату                           */
/*  Программист      : Чернов А.Г.                                      */
/*  Создан           : 10-06-2005                                       */
/************************************************************************/

import "globals.mac", FIInter, PTInter, SfInter, OprInter, RsbDataSet;

macro GetRate( RateID )

  file RateType("RateType");

  RateType.Type = RateID;
  if( GetEQ( RateType ) )
    return RateType.TypeName;
  end;

  return "";

end;

macro PrintHeader()

  var i, str_line = "";
  record pt(party);

  if ( ПолучитьСубъекта ({OurBank},pt) != 0 ) 
    msgbox("Не могу получить анкету банка");
    exit(1);
  end;

[###############################################################################](pt.Name:c:w);
  i=0;
  while( i < strlen(pt.Name) + 4 )
    str_line = str_line + "-";
    i = i + 1;
  end;
[###############################################################################](str_line:c);
  println();

end;

macro PrintComiss( sfinv )

  record fininstr(fininstr);
  var sQuery = "", DataSet;
  var iCount = 1;

  sQuery = " select comiss.t_name as name, defcom.t_commSum as Sum, defcom.t_NDSSum as NDSSum "
           " from dsfcomiss_dbt comiss, dsfdefcom_dbt defcom "
           " where defcom.t_InvoiceID = " + sfinv.InvoiceID + 
           " and comiss.t_feeType = defcom.t_feeType and comiss.t_number = defcom.t_commNumber ";

  DataSet = TRSBDataSet(sQuery);
  while( DataSet.MoveNext() )
    if( iCount == 1 )
[ +-------------------------------------------------+------+---+-----------+---------------+];
[ | № |Оказанные услуги                             |Кол-во|Ед.|Цена, #### |Сумма,   ####  |]
      ( GetFICode(sfinv.InvoiceFIID), GetFICode(sfinv.InvoiceFIID) );
[ +---+---------------------------------------------+------+---+-----------+---------------+];
    end;

[ |###|#############################################|######|###|###########|###############|]
    ( iCount, DataSet.name, 1, "шт", DataSet.Sum + DataSet.NDSSum, DataSet.Sum + DataSet.NDSSum );
    iCount = iCount + 1;
  end;

  if( iCount > 1 )
[ +---+---------------------------------------------+------+---+-----------+---------------+];
[                                                                ИТОГО: ################### ]
    ( sfinv.TotalAmount );
    if( sfinv.TotalAmount != $0 )
[                                                       В том числе НДС ###% ############## ]
      ( int(sfinv.NDSAmount/(sfinv.TotalAmount-sfinv.NDSAmount)*100), sfinv.NDSAmount );
    end;

    if( ПолучитьФинИн(sfinv.InvoiceFIID, fininstr) == 0 )
      if( sfinv.InvoiceFIID == NATCUR )  
    [ ######################################################################################### ]
        ( RubToStrAlt( sfinv.TotalAmount ):r );
      else
    [ ######################################################################################### ]
        ( CurToStrAlt( sfinv.TotalAmount, NULL, NULL, int(fininstr.ISO_Number) ):r );
      end;
    end;
[ -----------------------------------------------------------------------------------------];
  end;

end;


/* печать счета на оплату ТО */
macro SfPrintInvoice( sfinv )
  file TmpFile() TXT;

  if ((sfinv.InvMethod != SFINV_INVMETHOD_ACCOUNT) and (sfinv.InvMethod != SFINV_INVMETHOD_NOPAYDOC))
    return 1;
  end;

  PrintHeader();

[   СЧЕТ № #################### от ########## ]( sfinv.DocNumber, sfinv.InvoiceDate );
[ -----------------------------------------------------------------------------------------];
[ Плательщик: ИНН ################### КПП ################### ]( sfinv.PayerINN, sfinv.PayerCode );
[ #########################################################################################]( sfinv.PayerName:w );
[ #########################################################################################]( sfinv.Details:w );

  println();

  PrintComiss( sfinv );

[ Счет действителен в течение ############################################################# ]( sfinv.InvoiceDuration + " дней" );
[ Оплата в ### по курсу ############# перечислением на счет ############################# ]
  ( GetFICode(sfinv.PayFIID), GetRate( sfinv.PayRateID ), sfinv.BeneAccount );
[ в ####################################################################################### ]( sfinv.BeneName:w );

  println();

[ ################################################# ####################################### ]( sfinv.SignedByPosition, sfinv.SignedByName );
[ Бухгалтер ];

  if( not IsOprMultiExec() )
    /* не для массового режима выводим отчет на экран */
    open(TmpFile, setoutput("nul"));
    ViewFile(TmpFile);
  end;
end;
