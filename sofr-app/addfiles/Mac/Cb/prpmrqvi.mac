/*
$Name:             prpmrqvi.mac 
$Module:           
$Description:      Печать платежного поручения (WR)  
*/
/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmrqvi.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежного поручения (WR)                   */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 22.09.04                                           */
/*                                                                      */
/************************************************************************/

import prpm;
import "BnkTemplReport.mac", "pmbudg_lib.mac";

class (BnkTemplReport) PayOrder0401060Report(PayerP:TPartyProperties, ReceiverP:TPartyProperties)

   var PayerPr = PayerP;
   var ReceiverPr = ReceiverP;
   var paym   :TRecHandler = TRecHandler( "pmpaym.dbt",   "bank.def" );
   var debet  :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var credit :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var rmprop :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   
   copy( paym   , pr_pmpaym   );
   copy( debet  , pr_debet    );
   copy( credit , pr_credit   );
   copy( rmprop , pr_pmrmprop );

   
   macro Create()

     var Sum = string(paym.rec.Amount);
     StrSet(Sum, StrBrk(Sum, ".,"), "-");
     var pINN, pKPP, rINN, rKPP;
     splitINN_KPP(PayerPr.INN, pINN, pKPP);
     splitINN_KPP(ReceiverPr.INN, rINN, rKPP);

     SetSheetName( rmprop.rec.Number );

     SetValues( "PayerBankEnterDate", string(paym.rec.PayerBankEnterDate:f),
                "PayerChargeOffDate", string(rmprop.rec.PayerChargeOffDate:f),
                "Data", string(rmprop.rec.Date:f),
                "PaymentKind", GetPaymentKind(rmprop.rec.PaymentKind),
                "Number", rmprop.rec.Number,
                "TaxAuthorState", rmprop.rec.TaxAuthorState,
                "SumInPrint", RubToStrAlt(paym.rec.Amount),
                "pINN", pINN,
                "pKPP", pKPP,
                "Summa", Sum, 
                "PayerName", PayerPr.Name,
                "PayerAccount", string(PayerPr.Account),
                "PayerBankName", PayerPr.BankName,
                "BIC_PB", PayerPr.BankBIC,
                "PayerBankCorrAccount", string(PayerPr.BankCorrAccount),
                "ReceiverBankName", ReceiverPr.BankName,
                "ReceiverBankBIC", ReceiverPr.BankBIC,
                "ReceiverBankCorrAccount", string(ReceiverPr.BankCorrAccount),
                "rINN", rINN,
                "rKPP", rKPP,
                "ReceiverAccount", ReceiverPr.Account,
                "ReceiverName", ReceiverPr.Name,
                "ShifrOper", rmprop.rec.ShifrOper,
                "ShifrNazn", "",
                "ShifrCode", IfThenElse(rmprop.rec.Date < Date(31,3,2014), "", rmprop.rec.UIN),
                "Srok", "",
                "Priority", rmprop.rec.Priority,
                "ResultPole", "",  
                "BttTICode", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.BttTICode,   ""),
                "OkatoCode", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.OKATOCode,   ""),
                "TaxPmGround", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.TaxPmGround, ""),
                "TaxPmPeriod", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.TaxPmPeriod, ""),
                "TaxPmNumber", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.TaxPmNumber, ""),
                "TaxPmDate", IfThenElse(rmprop.rec.TaxAuthorState, rmprop.rec.TaxPmDate,   ""),
                "TaxPmType", GetForm110Str(rmprop),
                "Ground", rmprop.rec.Ground,
                "OtBankPl", string(paym.rec.PayerBankMarkDate)
              );

   end;
   
   initBnkTemplReport("PayOrder0401060.xls");
end;

MACRO PrintPayReqRSF(): bool

  var PayerPr   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverPr:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  return PayOrder0401060Report( PayerPr, ReceiverPr ).Print();
END;
