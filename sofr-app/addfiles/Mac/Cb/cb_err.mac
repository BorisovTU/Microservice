/**
 @file 		cb_err.mac
 @brief 	Утилиты для сообщений об ошибках

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |14.06.2024 |Велигжанин А.В.|DEF-66627                                       |Добавлена процедура CB_ErrMsg() для для преобразования 
 |           |               |                                                |неинформативного сообщения об ошибке
*/
import globals;
import rsd, oralib, likepy;

/**
  @brief                  	Возвращает код клиента
  @param[in] p_PartyID		ID клиента
  @param[in] p_CodeKind		вид кода клиента
*/
PRIVATE MACRO GetPartCode( p_PartyID: integer, p_CodeKind: integer ):string
  var sql:string = "SELECT r.t_Code FROM dpartcode_dbt r WHERE r.t_CodeKind = :CodeKind AND r.t_PartyID = :PartyID";
  var params:TArray = makeArray( 
     SQLParam( "CodeKind", p_CodeKind )
     , SQLParam( "PartyID" , p_PartyID ) 
  );
  var rs:RsdRecordset = execSQLselect( sql, params, FALSE );
  if( rs and rs.moveNext() )
    return rs.Value( 0 );
  end;

  return "";
END;

/**
  @brief                  	Возвращает Ccy валюты 
  @param[in] p_FIID  		код валюты
*/
PRIVATE MACRO GetCcy( p_FIID: integer ):string
  var sql:string = "SELECT r.t_ccy FROM dfininstr_dbt r WHERE r.t_fiid = :FIID";
  var params:TArray = makeArray( 
     SQLParam( "FIID", p_FIID )
  );
  var rs:RsdRecordset = execSQLselect( sql, params, FALSE );
  if( rs and rs.moveNext() )
    return rs.Value( 0 );
  end;

  return "";
END;

/**
  @brief                  	Процедура для преобразования неинформативного сообщения об ошибке
  @param[in] p_ErrMsg  		сообщение
  @param[in] p_PartyID  	код клиента
  @param[in] p_FIID  		код валюты
*/
macro CB_ErrMsg(p_ErrMsg: string, p_PartyID: integer, p_FIID: integer)
  var x_NewMsg:string = StrLwr(p_ErrMsg);    

  /* DEF-66627, переопределяем неинформативное сообщение:
     Ошибка СУБД:|RS-Bank|Виртуальная таблица: settacc.dbt|(4) Значение ключа не найдено|Операция 5 */
  if((Index(x_NewMsg, "таблица: settacc.dbt") > 0) and (Index(x_NewMsg, "значение ключа не найдено") > 0)) 
    x_NewMsg = "Не найдено СПИ клиента " + GetPartCode(p_PartyID, 101) + " по валюте " + GetCcy(p_FIID);
  else 
    x_NewMsg = p_ErrMsg;
  end;
  return x_NewMsg;
end;

