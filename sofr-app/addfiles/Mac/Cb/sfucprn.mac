/*
$Name:           sfucprn.mac
$Module:         Ядро ГКБО
$Description:    ПЗО: Макрос массовой печати СДО
*/

import SfInter, BankInter, PTInter, CTInter, FIInter, globals, RSD;

var sfunioncontr_rec:TRecHandler  = TRecHandler("sfunioncontr.dbt", "bank.def");

macro FinInName(FIID)
   file fininstr(fininstr);
   fininstr.FIID = FIID;
   if(not GetEQ(fininstr))
     runerror("Не найден финансовый инструмент");
   end;
   return fininstr.Name;
end;

macro ServKindInName(ServKind)
   file servkind1("servkind.dbt") key 0;
   servkind1.ServiseKind = ServKind;
   if(not GetEQ(servkind1))
     runerror("Не найден вид обслуживания");
   end;
   return servkind1.Name;
end;


MACRO PrintDocument( ncopy:integer):bool
  var i : integer = 0;

  for (i, 1, ncopy, 1)
    println("Номер: ", sfunioncontr_rec.rec.Number);
    println("Название: ", sfunioncontr_rec.rec.Name);
    println("Дата заключения: ", sfunioncontr_rec.rec.DateConc);
    println("Дата закрытия: ", sfunioncontr_rec.rec.DateClose);
    println("Вид обслуживания: ", sfunioncontr_rec.rec.ServKind, " ", ServKindInName(sfunioncontr_rec.rec.ServKind));
    println("\n");  

    var sqlString = "SELECT t_ID, t_Name, t_DateConc, t_DateBegin, t_DateClose, t_ServKind, t_ObjectType, t_Object, t_FIID FROM dsfcontr_dbt WHERE t_UnionContrID = " + string(sfunioncontr_rec.rec.UnionContrID) +" ORDER BY t_ID";


    var cmd = RsdCommand( sqlString );
    cmd.NullConversion = true;
    var rs = RsdRecordset( cmd );

    while(rs.moveNext())
      
      println("Номер договора: ", rs.value(0));
      println("Название: ", rs.value(1));
      println("Дата заключения: ", date(rs.value(2)));
      println("Даты начала и окончания периода действия: ", date(rs.value(3)), date(rs.value(4)));
      println("Вид обслуживания: ", rs.value(5), " ", ServKindInName(rs.value(5))  );
      println("Тип и идентификатор объекта договора: ", rs.value(6)," ", rs.value(7));
      println("Валюта договора: ", ПолучитьКодФинИн(rs.value(8)) ," ", FinInName(rs.value(8)));
      println("\n");
    end;

    println("\n");
  end;

  return TRUE;

END;
