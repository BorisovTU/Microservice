/*
 $Name:        prpmorvi.mac
 $Module:      РКО
 $Description: Печать платежного ордера (WR)
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmorvi.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежного ордера (WR)                      */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 22.09.04                                           */
/*                                                                      */
/************************************************************************/

import "BnkTemplReport.mac", "pmbudg_lib.mac";
import prpm, gnd120p;


class (BnkTemplReport) PayOrder0401066Report(payerP:TPartyProperties, receiverP:TPartyProperties)

   var payerProps = payerP;
   var receiverProps = receiverP;
 
   var pmpaym_part  :TRecHandler = TRecHandler("pmpaym.dbt",   "bank.def");
   var pmprop_part  :TRecHandler = TRecHandler("pmprop.dbt",   "bank.def");
   var pmrmprop_part:TRecHandler = TRecHandler("pmrmprop.dbt", "bank.def");
   var pmrmprop     :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   var paym_prim    :TRecHandler = TRecHandler("pmpaym.dbt",   "bank.def");   
   var pmrmprop_prim:TRecHandler = TRecHandler("pmrmprop.dbt", "bank.def");


   copy( pmpaym_part, pr_pmpaym_part  );
   copy( pmprop_part, pr_pmprop_part );
   copy( pmrmprop   , pr_pmrmprop     );
   copy( paym_prim  , pr_pmpaym_prim );
   copy( pmrmprop_part, pr_pmrmprop_part );
   copy( pmrmprop_prim, pr_pmrmprop_prim );

   macro Create( sender: Object )

     var ground       :string, 
         rest         :moneyl,
         shortSum     :string,
         shortRestSum :string,
         payKind      :string;

     payerProps.Name            = pmrmprop_part.rec.PayerName;
     payerProps.INN             = pmrmprop_part.rec.PayerINN;
     payerProps.Account         = pmpaym_part.rec.PayerAccount;

    rest = IfThenElse( ( ( pr_pmpaym_prim.rec.PaymentID != 0 ) and ( pr_pmpaym_part.rec.DocKind == PS_PAYORDER ) ),
                       RsbPayment( pr_pmpaym_part.rec.PaymentID ).PartPaymRestAmountMain, $0.0 );

     /* Получим сумму остатка исходного платежного документа, если он есть */

     /* Сумма документа частичной оплаты разделяется символом "=", 
        а сумма остатка платежа - символом "-"  */
     shortSum = String(pmpaym_part.rec.Amount);
     StrSet(shortSum, StrBrk(shortSum, ".,"), "-");

     shortRestSum = string(rest);
     if( (rest == $0.0) or (StrBrk(shortRestSum, ".,") == 1) )
       shortRestSum = "0" + shortRestSum;
     end;
     StrSet(shortRestSum, StrBrk(shortRestSum, ".,"), "-");
     
     if( pmpaym_part.rec.PartPaymNumber == 0 )
       shortRestSum = "";
       payKind      = "";
     else
       payKind = "Частичная оплата";
     end;

     var pKPP, pINN, rKPP, rINN;
     splitINN_KPP(pmrmprop_part.rec.PayerINN, pINN, pKPP);
     splitINN_KPP(receiverProps.INN, rINN, rKPP);
     
     SetSheetName( pmrmprop_part.rec.Number );

     //Заполнение отчета
     SetValues( "Data", replace(string(pmrmprop_part.rec.Date:f), "-", "."),
                "PaymentKind", GetPaymentKind(pmrmprop_part.rec.PaymentKind),
                "Number",pmrmprop_part.rec.Number,
                "TaxAuthor", pmrmprop_part.rec.TaxAuthorState,
                "AmountInLine", RubToStrAlt(pmpaym_part.rec.Amount),
                "pINN", pINN,
                "pKPP", pKPP,
                "Amount", shortSum,
                "pName", pmrmprop_part.rec.PayerName,
                "pAccount", string(PayerProps.Account), 
                "pBankName", PayerProps.BankName,
                "pBankBIC", PayerProps.BankBIC,
                "pBankAccount", string(PayerProps.BankCorrAccount),
                "rBankName", ReceiverProps.BankName,
                "rBankBIC", ReceiverProps.BankBIC,
                "rBankAccount", string(ReceiverProps.BankCorrAccount),
                "rINN", rINN,
                "rKPP", rKPP,
                "rAccount", string(ReceiverProps.Account),
                "rName", ReceiverProps.Name,
                "ShifrOper", pmrmprop_part.rec.ShifrOper,
                "ShifrNazn", "",
                "ShifrCode", IfThenElse(pmrmprop.rec.Date < Date(31,3,2014), "", pmrmprop.rec.UIN),
                "Priority", pmrmprop_part.rec.Priority,
                "RezPole", "",
                "SubPurpose", pmpaym_part.rec.PartPaymNumber,
                "SOper", pmrmprop_prim.rec.ShifrOper,
                "sNumber", pmrmprop_prim.rec.Number,
                "sDate", replace(string(pmrmprop_prim.rec.Date:f), "-", "."),
                "RestSum", shortRestSum,
                "payKind", payKind,
                "BttTICode", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.BttTICode,   ""),
                "OkatoCode", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.OKATOCode,   ""),
                "TaxPmGround", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.TaxPmGround, ""),
                "TaxPmPeriod", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.TaxPmPeriod, ""),
                "TaxPmNumber", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.TaxPmNumber, ""),
                "TaxPmDate", IfThenElse(pmrmprop_part.rec.TaxAuthorState, pmrmprop_part.rec.TaxPmDate,   ""),
                "TaxPmType", GetForm110Str(pmrmprop_part),
                "Ground", InterpretGround( pmrmprop_part.rec.Ground ),
                "pBankMarkDate", string(pmpaym_part.rec.PayerBankMarkDate)
              );

   end;

   initBnkTemplReport("PayOrder0401066.xls");
end;

MACRO PrintPayOrderRSF( ncopy:integer ):bool

  var payerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym_part, pr_pmprop_part, pr_pmrmprop);
  var receiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym_part, pr_pmprop_part, pr_pmrmprop);

  return PayOrder0401066Report(payerProps, receiverProps ).Print();
END;
