import rsd;
import BankInter;
import RsbDataSet;
import XmlRpcInter, serviceaction;

var {oper};
private const COMMON_STATE = 0;
private const UOL_WEB_CHANGE_PATTERN_SETTINGS = 1452;

class PanelStateHolder
    var PrivateState :string;
    var CommonState :string;
end;

class TPatternSettings
  var Name :string;
  var Pattern :string;
  var Mask :string;
  var PatternHelpText :string;
  var PatternErrorText :string;
    macro Equals(other)
        if (other == null)
            return false;
        end;
        return (Name == other.Name) and (Pattern == other.Pattern) and (Mask == other.Mask) and (PatternHelpText == other.PatternHelpText) and (PatternErrorText == other.PatternErrorText)
    end;
end;

macro PatternSettingsToString(i)
    return "Name = " + i.Name  + "; Pattern = " + i.Pattern + "; Mask = " + i.Mask  + "; PatternHelpText = " + i.PatternHelpText  + "; PatternErrorText = " + i.PatternErrorText + ";";
end;

macro IsPatternSettingsEmpty(i)
    if (i == null)
        return true;
    end;
    return ("" == i.Pattern) and ("" == i.Mask) and ("" == i.PatternHelpText) and ("" == i.PatternErrorText);
end;

macro PatternSettingsKeyEqComparer(a, b)
    return a.Name == b.Name;    
end;
/*
macro HasItem
macro Except(a :TArray, b: TArray)
    for (i, a)
    
    end;
end;

macro TPatternSettingsEquals(a, b)
    if (a == b)
        return true;
    end;
    if (a != null)
        return a.Equals(b);
    end;
    return false;
end;

macro SplitElements(oldList, newList, changed, added, removed)
    if (oldList == null)
        SetParm(2, null);
        SetParm(3, newList);
        SetParm(4, null);
        return;
    end;

    if (newList == null)
        SetParm(2, null);
        SetParm(3, null);
        SetParm(4, oldList);
        return;
    end;

end;
*/
class PanelCommonState
    var PatternSettings :TArray = TArray(); //TArray of TPatternSettings
end;

private macro Rsl2Xml(obj)
    var xml_obj = null;
    if (0 != ConvertToXML(obj, null, xml_obj))
        RunError("Ошибка преобразования объекта в XML");
    end;
    return xml_obj;
end;

private macro Xml2Rsl(xml)
    if (xml == "")
        return null;
    end;
    
    var obj = null;
    ConvertToRSL(xml, obj);
    return obj;
end;

private macro IsPanelStateExists(panel :string, oper :integer)
  var cmd, cmdText;

  cmdText = "SELECT 1 FROM DWEB_WINFORM_DBT WHERE T_OPERID = ? AND TO_CHAR(t_panel) = ?";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, oper);
  cmd.AddParam("", RSDBP_IN, panel);

  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.movenext)
    return true;
  end;

  return false;
end;

private macro UpdatePanelState(panel :string, oper :integer, xmlstate :string)
  var cmd, cmdText;

  cmdText = "update DWEB_WINFORM_DBT set T_STATE = ? WHERE T_OPERID = ? AND TO_CHAR(t_panel) = ?";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, xmlstate);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.AddParam("", RSDBP_IN, panel);

  cmd.execute();
end;

private macro InsertPanelState(panel :string, oper :integer, xmlstate :string)
  var cmd, cmdText;

  cmdText = "insert into DWEB_WINFORM_DBT(T_OPERID, T_PANEL, T_STATE) VALUES(?, ?, ?)";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, oper);
  cmd.AddParam("", RSDBP_IN, panel);
  cmd.AddParam("", RSDBP_IN, xmlstate);

  cmd.execute();
end;

private macro UpdateState(panel :string, oper :integer, xmlstate :string)

  if ((xmlstate == null) or (xmlstate == ""))
    return;
  end;

  if (IsPanelStateExists(panel, oper))
    UpdatePanelState(panel, oper, xmlstate);
  else
    InsertPanelState(panel, oper, xmlstate);
  end;

  return 0;
end;


private macro GetPanelStateByOper(panel :string, oper :integer)

  var cmdText = 
        " SELECT 1, "
        "  (SELECT T_STATE "
        "   FROM DWEB_WINFORM_DBT "
        "  WHERE     T_OPERID = ? "
        "        AND TO_CHAR (t_panel) = ?) PERSSTATE FROM DUAL";

  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.AddParam("", RSDBP_IN, panel);
  cmd.execute();

  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  if (ds.movenext)
    return ds.PERSSTATE;
  end;

  return "";
end;


private macro StoreCommonState(panel :string, commonState)
    var xml = Rsl2Xml(commonState);
    UpdateState(panel, COMMON_STATE, xml);
end;

private macro GetCommonState(panel :string)
    var xml = GetPanelStateByOper(panel, COMMON_STATE);
    var t = Xml2Rsl(xml);
    //return Xml2Rsl(xml);
    var commonState = PanelCommonState();
	if ((t != null) and (ValType(t.PatternSettings) != V_UNDEF))
        commonState.PatternSettings = t.PatternSettings;
    end;
    return commonState;
end;

macro SettingsToString(settings)
    var result = "";
    
    var i;
    for (i, settings)
        if (not IsPatternSettingsEmpty(i))
            result = result + PatternSettingsToString(i) + "\n";
        end;
    end;
    return result;
end;

private macro AddChangesToAuditJournal(oldState, newState)
    var oldStateText = "";
    var newStateText = "";

    if ((oldState != null) and (oldState.PatternSettings != null))
        oldStateText = SettingsToString(oldState.PatternSettings);
    end;

    if ((newState != null) and (newState.PatternSettings != null))
        newStateText = SettingsToString(newState.PatternSettings);
    end;

    var result = "";
    if (oldStateText != "")
        result = result + "Старые параметры:\n" + oldStateText;
    end;
    if (newStateText != "")
        result = result + "Новые параметры:\n" + newStateText;
    end;
    WriteAuditLog(UOL_WEB_CHANGE_PATTERN_SETTINGS, result);
    return result;
end;

// PanelCommonState
macro StoreCommonPanelState(panel :string, commonState)

  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  if (commonState == null)
    RunError("Не задано состояние панели");
  end;

  var oldState = GetCommonState(panel);
  StoreCommonState(panel, commonState);
  AddChangesToAuditJournal(oldState, commonState);

  return 0;
end;

macro GetCommonPanelState(panel :string)
  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  return GetCommonState(panel);
end;

macro GetPanelState(panel :string)

  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  return GetPanelStateByOper(panel, {oper});
end;

macro StorePanelState(panel :string, xmlstate :string)

  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  if (xmlstate == null)
    RunError("Не задано состояние панели");
  end;

  UpdateState(panel, {oper}, xmlstate);

  return 0;
end;
