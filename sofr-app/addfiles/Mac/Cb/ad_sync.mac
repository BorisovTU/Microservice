/*
$Name:        ad_sync.mac
$Module:      Сервис ГКБО
$Description: Отчет синхронизации пользователей с Active Directory
*/
import "globals.mac", rsd, commonutil, oralib, likepy;

macro PrintReportHeader(IsScheduler)
    if (IsScheduler)
        [Отчет синхронизации пользователей с Active Directory планировщиком
        ];
    else
        [Отчет синхронизации пользователей с Active Directory
        ];
    end;

    println();

    [Дата операции:    ##########](Date({curdate}):c);
    [Операционист: #### #########]({oper}, {Name_Oper});

    println();
end;

macro PrintLine(str)
    if (ValType(str) != V_UNDEF)
        println(str);
    else
        println();
    end;
end;

// ---------------------------------------------------------------------------------
private macro PrintMembersTableHeader()
[+---------------------------------------------+----------------------------------+-----------------+-----------------+--------------+
| Имя в домене                                |               GUID               |    uSNChanged   | IsAccountLocked | Операционист |
+---------------------------------------------+----------------------------------+-----------------+-----------------+--------------+];
end;

macro PrintMembersTable()
    println("");
    println("Состав прочитанных групп");
    CaptureOutput;
    [
    select 
        grp.T_GROUPID,
        t.T_OPER, 
        t.T_GUID, 
        grp.T_NAME,
        domain.T_NAME T_DOMAIN,
        t.T_USNCHANGED, 
        t.T_USNCHANGEDGROUP, 
        grp.T_USNCHANGED T_USNCHANGEDBD,
        t.T_NAMEINDOMAIN, 
        decode(t.T_ISACCOUNTLOCKED, chr(88), 'Да', 'Нет') T_ISACCOUNTLOCKED
    from 
        DADMEMBERS_TMP t, 
        dadgroup_dbt grp, 
        dllvalues_dbt domain
    where 
        t.T_GROUPID = grp.T_GROUPID 
        and domain.T_ELEMENT (+)= grp.T_DOMAIN 
        and domain.T_LIST (+)= 4044 
    order by grp.T_GROUPID, t.T_NAMEINDOMAIN
    ];

    var query = StopCaptureOutput;

    var lastGroup = -1;
    var rs : RsdRecordset = execSQLselect(query, makeArray(), false);
    while (rs.MoveNext())
        var GroupD = Int(rs.value("T_GROUPID"));

        if (GroupD != lastGroup)
            lastGroup = GroupD;
[
#################################################################################################################################]
("Группа <" + rs.value("T_NAME") + "> в домене <" + rs.value("T_DOMAIN") + ">");
PrintMembersTableHeader();
        end;

[| ########################################### | ################################ | ############### | ############### | ############ |
+---------------------------------------------+----------------------------------+-----------------+-----------------+--------------+]
(rs.value("T_NAMEINDOMAIN"), rs.value("T_GUID"), BigInt(rs.value("T_USNCHANGED")), rs.value("T_ISACCOUNTLOCKED"):c, rs.value("T_OPER"):c);
    end;

    println();
end;

macro PrintMembersSummary()
    println("Количество пользователей в группах:");

    CaptureOutput;
    [
    select 
        grp.T_NAME, 
        domain.T_NAME T_DOMAIN, 
        count(grp.T_NAME) t_count
    from 
        DADMEMBERS_TMP t, 
        dadgroup_dbt grp, 
        dllvalues_dbt domain
    where 
        t.T_GROUPID = grp.T_GROUPID 
        and domain.T_ELEMENT (+)= grp.T_DOMAIN 
        and domain.T_LIST (+)= 4044 
    group by grp.T_NAME, domain.T_NAME
    order by grp.T_NAME
    ];

    var query = StopCaptureOutput;

    var fFirst = true;
    var rs : RsdRecordset = execSQLselect(query, makeArray(), false);

    while (rs.MoveNext())
        if (fFirst)
            [+----------------------------------------------------------------------+-----------------+];
            fFirst = false;
        end;

        [| #################################################################### | ############### |
        +----------------------------------------------------------------------+-----------------+]
        (rs.value("T_NAME") + " в домене " + rs.value("T_DOMAIN"), Int(rs.value("t_count")));
    end;

    println();
end;

macro PrintCloseHeader()
    println("Обрабготка пользователей, которые есть среди операционистов и входят в состав синхронизируемых групп, ");
    println("но больше не входят ни в одну из групп в Active Directory, указанных в БД");
end;

macro PrintProcessHeader()
    println("Обрабготка пользователей, которых включили в состав групп Active Directory");
    println("либо у которых атрибут uSNChanged не совпадает на сервере БД и в Active Directory");
end;

macro PrintOperMessage(Message)
[+---------------------------------------------------------------------------------------------------------------+
| ############################################################################################################# |
+---------------------------------------------------------------------------------------------------------------+]
(Message:w);
end;