/*
$Name: cbinfint.mac
$Module: Ядро ГКБО
$Description: Низкоуровневые функции вывода для информирования о необработанных документах.
*/

/*
    (c) R-Style Software Lab 2001
    RS Bank 5.10

    Информирование о необработанных документах


    filename cbinfint.mac ( CBINF INTerface)
    

    Низкоуровневые функции вывода 
    для информирования о необработанных документах.

    Для унификации и удобства внесения изменений в интерфейс
    вывод сообщений "на экран" (т.е. не в отчет) и ошибок 
    должен быть реализован через эти функции

            Mikhailov S.,   22.08.2001, Wed
                v.2         30-10-2001, Tue
*/

Import BankInter;


/*
    Способ вывода на экран
*/

const               CBINF_OutMsg        = 1;    /* Сообщить о первом документе и закончить*/
const               CBINF_OutReport     = 2;    /* Сформировать отчет о всех найденных документах*/

/*
    Коды возврата внутренние и внешние . Процедура с ошибкой, если вернула > 0 
*/
const               CBINF_StatusOk      = 0;    /*Продолжать, либо выход без ошибки*/
const               CBINF_StatusError   = 1;    /*Прекратить, выход с ошибкой*/

const               CBINF_StatusExitOk  = -1;   /*Прекратить (нашли документ), выход без ошибки*/



/*
    Признак , что нашли документ
    Необходимо обнулять в начале работы каждой процедуры информировния CBINF_Proc.
    По окончании ее работы, если найден документ, установить в 1.
    PrintHeader обнуляет.
    PrintLine и MsgDocFound уст. сами.
*/
var CBINF_Result ;

/*
    Статус ошибки.
    Необходимо обнулять в начале работы каждой процедуры информировния CBINF_Proc.
    По окончании ее работы, если произошла ошибка, установить в 1.
    CBINF_Error устан. сама. Обнулять должен сам макрос.
*/
var CBINF_ErrorStatus ;



/*
    Функция для вывода сообщений "на экран" ( типа MsgBox ). Все собщения должны идти через нее
*/
macro CBINF_Message( message )
    if( AddMessageList( message ) )
        exit( 1, "Переполнен системный буфер" );
    end;

    return CBINF_StatusOk;
end;


/*
    Сообщение об ошибке -- сделано для унификации
*/
macro CBINF_Error( error, OutType )

    CBINF_ErrorStatus = 1;
    
    if( OutType == CBINF_OutMsg )
        CBINF_Message( error );
    elif( OutType == CBINF_OutReport )
        println( error );
    end;

end;


/*Сообщение об ошибке при взятии параметра реестра*/
macro WriteRegError( Parm, RegError, OutType )
    var message = "Ошибка "+ RegError +" при определении параметра "+ Parm;

    CBINF_Error( message, OutType );
    
    return CBINF_StatusError;
end;/*WriteRegError*/


