/**
 @file 		cblogger.mac
 @brief 	Утилиты для логгирования

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |03.11.2023 |Велигжанин А.В.|DEF-52575                                       |Добавлена процедура AddItLog() для логиррования сообщения в БД
 |10.12.2023 |Сенников И.В.  |DEF-52575                                       |Ограничение для AddItLog по количеству символов
*/
import globals;
import rsd, oralib, likepy;

/**
@brief                  Логгирование сообщения в БД (смотреть через select * from itt_log order by id_log desc)
@param[in] p_text       сообщение
*/
macro AddItLog(p_text)
  execSQL("begin it_log.log(?,it_log.C_MSG_TYPE__DEBUG); end;", makeArray(SQLParam("msg", SubStr(p_text,1,4000))), true);
end;

/**
@brief                  Класс для логгирования
*/
private class CBLogger
    private var m_logName;
    private var m_used;
    private macro DateTimeStr
       var dd, mm, yyyy;
       var hh, min, sec;
       var dt: date;
       var tm: time;

       dt = date;
       tm = time;
       DateSplit(dt, dd, mm, yyyy);
       TimeSplit(tm, hh, min, sec);

       return string(yyyy:o:4)+"/"+string(mm:o:2)+"/"+string(dd:o:2)+" "+string(hh:o:2)+":"+string(min:o:2)+":"+string(sec:o:2);
    end;
    private macro DateTimeName
       var dd, mm, yyyy;
       var hh, min, sec;
       var dt: date;
       var tm: time;

       dt = date;
       tm = time;
       DateSplit(dt, dd, mm, yyyy);
       TimeSplit(tm, hh, min, sec);

       return string(yyyy:o:4)+string(mm:o:2)+string(dd:o:2)+"_"+string(hh:o:2)+string(min:o:2)+string(sec:o:2);
    end;
    macro Write(msg: string, appended: bool)
        var m_appended: bool;
        if(m_used != 1)
           /* логгер не используется, ничего не делаем */
           return;
        end;
	if(ValType(appended) == v_undef)
	   m_appended = true;
        else 
	   m_appended = appended;
	end;
        setoutput(GetTXTFileName(m_logName), m_appended);
        println(DateTimeStr()+" "+msg);
        setoutput();
    end; 
    macro Init(_use: integer, _logName: string)
       var logExist: bool;
	if(ValType(_use) != v_integer)
	   m_used = 0;
        else 
	   m_used = _use;
        end;
	if(ValType(_logName) == v_undef)
	   m_logName = DateTimeName();
        else 
	   m_logName = _logName;
        end;
        logExist = ExistFile(GetTXTFileName(m_logName));
        if(not logExist)
           Write("Logger started", false);
        end;
    end; 
end;

private var lgr = CBLogger();

/**
@brief 				Процедура включает механизм логгирования (через класс)
@param[in] _use			флаг включения: 1 -- механизм логгирования включается, 0 -- выключается
@param[in] _logName		имя файла журнала, необязательный параметр, если не задан, 
                                имя будет сгенерировано автоматически по временной метке
*/
macro InitCBLog(_use: integer, _logName: string)
   lgr.Init(_use, _logName);	
end;

/**
@brief 				Процедура добавляет строку в файл журнала
@param[in] msg			сообщение
@param[in] appended		если true, сообщение добавляется в конец файла, 
                                если false -- прежние сообщения сбрасываются, запись в файл начинается заново
*/
macro LogIt(msg: string, appended: bool)
   lgr.Write(msg, appended);	
end;

/**
@brief 				Функция структурирует сообщение об ошибке
@param[in] ObjError		объект ошибки
@return 			строка сообщения, структурированная 
*/
macro GetErrorMessage( ObjError:object )

    var errCode, errMes, count, i;

    if (IsEqClass ("TRslError",ObjError))
        errMes = ObjError.Message; 
        errCode = ObjError.Code; 
    else
        errMes = ObjError.getMessage(); 
        errCode = ObjError.getErrorCode(); 
    end;

    var Cause = ObjError.err;

    if (IsEqClass ("TRsComErr", Cause))
        count = Cause.Count;
        i = 0;
        while (i < count)
            errMes = errMes + ";"+ Cause.Message (i) + " (Code = " + Cause.Code (i) +
                                                      ", Level = " + Cause.Level (i) + ")" ;
            i = i + 1
        end;
    end;

    if(IsEqClass("TDbError", Cause))
      errMes = errMes + ";" + Cause.Stat + "-" + Cause.Oper + "-" + Cause.Table + "-" + Cause.Message;
    end;

    if(isEqClass("TRsdError", Cause))
        i = 0;
        while(i < Cause.environment.ErrorCount)
            errMes = errMes + ";" + Cause.environment.Error(i).Descr;
            i = i + 1;
        end;
    end;

    if(errCode != 17)
	return "\n|Code    : " +string( errCode )+
               "\n|Message : "+string( errMes )+
               "\n|Module  : "+string( ObjError.Module )+
               "\n|Line    : "+string( ObjError.Line )+
               "\n|AxCode  : "+string( ObjError.AxCode )+
               "\n|Err     : "+string( Cause )+
               "\n|AxMes   : "+string( ObjError.AxMes );
    else 
        return "Прерывание пользователя!"; // прерывание пользователя
    end;
end;

