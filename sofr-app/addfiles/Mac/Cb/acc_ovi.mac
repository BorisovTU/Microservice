//-----------------------------------------------------------------------------
// Блок     : 10004 - "Урегулирование счетов в овердрафтом
//                     (интегрированный режим)"
// Шаг      : 410   - "Обработка овердрафта (интегрированный режим)"
// Описание : Макрос шага
//-----------------------------------------------------------------------------

import InsCarryDoc, accovfun, CurrInter, likepy, oralib;

record accop     ( "accopsrv.dbt" ); // Сервисная операция
record account   ( "account.dbt"  ); // Основной счет

MACRO ExecuteStep( doc, paymDoc )

  var Sum = $0;
  

  var params:TArray;
  var rs:object;
  
  var select = "select nvl(sum(ch.t_Uselimit - ch.t_ReconstructionLimit), 0) "
                  "from dcredit_c_dbt cred, dovc_sfc_dbt ovc, dchlimit_dbt ch "
                 "where cred.t_creditnumber = ovc.t_creditnumber_ref "
                   "and cred.t_regdate <= :date1 "
                   "and cred.t_ReturnDate >= :date2 "
                   "and ch.t_CreditNumber_Ref = cred.t_CreditNumber "
                   "and ch.t_IsProcessing = chr(0) "
                   "and ch.t_Date = :date3 "
                   "and ch.t_IsReject = chr(0) "
                   "and ovc.t_id = ( select sf.t_ID "
                                      "from dsfcontr_dbt sf "
                                      "where sf.t_ServKind = 3 "
                                        "and sf.t_ObjectType = 1 "
                                        "and sf.t_FIID = :fiid "
                                        "and sf.t_Object = :acc "
                                        "and t_DateBegin <= :date4 "
                                        "and (t_DateClose >= :date5 OR t_DateClose = to_date('01010001','ddmmyyyy')) "
                                   ") ";

  
  params = makeArray( SQLParam( "date1", {curdate} ),
                      SQLParam( "date2", {curdate} ),
                      SQLParam( "date3", {curdate} ),
                      SQLParam( "fiid",  account.Code_Currency ),
                      SQLParam( "acc",   account.Account ),
                      SQLParam( "date4", {curdate} ),
                      SQLParam( "date5", {curdate} ));

  rs = execSQLselect( select, params, FALSE );

  var AccountFreeRest = $0;
  if( AccGetFreeAmount( AccountFreeRest, NULL, account.Account, account.Chapter, account.Code_Currency, {curdate} ) )
    msgbox("Ошибка при определении свободного остатка");
    return 1;
  end;

  if( rs.moveNext() )
    Sum = AccountFreeRest + rs.value(0);
  end;


  if(Sum > 0)

    if( InsertLimitRestoreTry( account.Code_Currency, 1, account.Account, Sum, {curdate}, true ) )
      MsgBox("Ошибка при восстановлении лимита счета");
      return 1;
    end;
  
  elif(Sum < 0)
       
    if( InsertLimitUseTry_Int( account.Code_Currency, 1, account.Account, -Sum, {curdate}, true ) )
      MsgBox("Ошибка при использовании лимита счета");
      return 1;
    end;

  end;

  return 0;

END;
