 /*
 $Name: fm_frzchkout.mac
 $Module: ФМ 
 $Description: Выгрузка сообщений о "действиях по замораживанию" в транспортный файл
 */

Import BankInter, FMInter, PTInter, FIInter, CTInter, "fm_out_util", "globals.mac";

/* Код терр. учреждения по ОКАТО */
private var KTU : string;

/* Коды нашего банка */
private var BANK_INN : string;
private var REGN     : string;
private var NUMBF_S  : string;

/* Путь к эталонному транспортному файлу */
private var DIR_ORIG : string;

/* Название транспортного файла */
private var OutFileName : string;

private var OutFileType : string;

record FreezCheck("fmfreezecheck.dbt" );


private macro FormatDate(dt:date)
  var day, mon, year;
  var str = "";   
  if(dt != date(0,0,0))
    DateSplit ( dt, day, mon, year );
    str = string(day:2:o)+ string(mon:2:o)+substr(string(year:4:0),3,2) ;
  else
    str = "01012099";
  end;
  return str;
end;

/*
private macro FormatTime(tm:time)
  var hour, min, sec;
  var str = "";   

  TimeSplit ( dt, hour, min, sec );
  str = string(hour:2:o)+ string(min:2:o)+substr(string(sec:2:0),3,2) ;

  return str;
end;
*/


/*
 * Определить тип транспортного файла,
 * в который необходимо выгрузить данную операцию
 */
private macro DefineFileType( Repeat )
    
  if((FreezCheck.MessageType == FMFREEZ_CHECK_MESSAGE_TYPE_ADD))
    return "k";
  else
    return "m";
  end;
end;


/*
 * Сформировать название файла выгрузки
 * Возвращаемое значение: название файла
 */
private macro CreateOutFileName( Date_Seance, Seance, Repeat, FileKind )
  
  var day, mon, year, fname, ext;
  var OutFileName;

  var error;
  /* вид кода филиала */
  var BankID, DprtCodeParty, DprtCodeKind;
  OutFileType = DefineFileType( Repeat );

  DateSplit( Date_Seance, day , mon, year );

  /* имя файла для КФМ*/
//  if( FileKind == 0 )
    fname = SubStr( {MFO_Bank}, 5, 5 ) + string(OutFileType);
    if( day < 10 ) 
       fname = fname + "0" ;
    end;
    fname = fname + String(day);
    
    /* расширение */
    ext = "";
    if( Seance < 10 ) 
       ext =  "0";
    end;

    ext = ext + Seance + "9" ;
//  end;

  /* имя файла для ВФ*/
/*
  if( FileKind == 1 )

  end; 
*/
  /* формируем файл */
  OutFileName = MergeFile( FM_GetOutDir(), fname, ext );
  
  return OutFileName;

end;

/* Подкорректировать незаполненные поля в транспортном файле */
private macro CorrectTransportFile(TranspFile)

  var i = 0;

  while( i < FldNumber(TranspFile) )

    if( (ValType(TranspFile(i)) == V_STRING) and ((Trim(TranspFile(i)) == "") or (Trim(TranspFile(i) ) == ",")) )
         
      TranspFile(i) = "0";
      
    elif( (ValType(TranspFile(i)) == V_DATE ) and (TranspFile(i) == Date(0,0,0)) )
         
      TranspFile(i) = Date(1, 1, 2099) ;
      
    end;

    i = i + 1;

  end;

end;


/* Макрофункция вызывается в начале выгрузки
 *
 *   Возвращаемые значения:
 *      0 - продолжить выгрузку
 *  not 0 - остановить выгрузку
 *
 */
macro FrzChOutStart()
  /* определим код территориального учреждения по ОКАТО */
/*
  KTU = FM_GetKTU();
  if( Trim(KTU) == "" )
    msgbox( "Не определен код территории учреждения по ОКАТО" );
    return 1;
  end;
*/  
  BANK_INN = GetCodeParty( {OurBank}, PTCK_INN );

  ПолучитьНомерРегистрацииБанка( @REGN, @NUMBF_S );

  return 0;

end;


/* Макрофункция вызывается по завершении выгрузки
 *
 *   Возвращаемое значение не анализируется
 *
 */
macro FrzChOutFinish()
end;



private macro FreezCheckOutToDBF( Date_Seance, Seance, First, Repeat, FileKind )

   file SampleFile("fz134ch.dbf") dbf;
   file TranspFile("fz134ch.dbf") dbf write;

   var  FMZeroDate = date(0,0,0);

   if( not Open(SampleFile, DIR_ORIG) )
     MsgBox( "Невозможно открыть эталонный файл " + DIR_ORIG + ", заданный в настройках подсистемы" );
     return 1;
   end;

   if( First ) /* если выгрузка первой записи - создаем файл */
     
     OutFileName = CreateOutFileName( Date_Seance, Seance, Repeat, FileKind );

     if( not clone(SampleFile, OutFileName) )
       MsgBox( "Невозможно создать файл выгрузки " + OutFileName );
       return 1;
     end;

   else

     if( OutFileType != DefineFileType(Repeat) )
       return -1;
     end;

   end;

   if( not open(TranspFile, OutFileName) )
     MsgBox( "Невозможно открыть файл выгрузки " + OutFileName );
     return 1;
   end;

   ClearRecord( TranspFile );

   /* Служебная информация */
   TranspFile.VERSION    = "9";
   TranspFile.ACTION     = FreezCheck.MessageType;
   TranspFile.NUMB_P     = FreezCheck.MessageNumber;
   TranspFile.DATE_P     = FreezCheck.MessageDate;
   TranspFile.REGN       = FreezCheck.BankKGRKO;
   TranspFile.KTU_S      = FreezCheck.InitDprtOkato;
   TranspFile.BIK_S      = FreezCheck.InitDprtBIC;
   TranspFile.NUMBF_S    = FreezCheck.CheckedDprtKGRKO;

   if((FreezCheck.InitDprt == FreezCheck.CheckedDprt) AND (FreezCheck.ChildDprtInc == "X"))
     TranspFile.KTU_SS     = "0";
     TranspFile.BIK_SS     = "0";
   else
     TranspFile.KTU_SS     = FreezCheck.CheckedDprtOKATO;
     TranspFile.BIK_SS     = FreezCheck.CheckedDprtBIC;
   end;

   TranspFile.RESRV_S    = "0";

   if( (FreezCheck.FreezedTotal == 0) AND
       (FreezCheck.FreezedYurics == 0) AND
       (FreezCheck.FreezedPhysics == 0) AND
       (FreezCheck.FreezedMsgTotal == 0) AND
       (FreezCheck.NotFreezedTotal == 0) AND
       (FreezCheck.NotFreezedYurics == 0) AND
       (FreezCheck.NotFreezedPhysics == 0) AND
       (FreezCheck.NotFreezedMsgTotal == 0)
     )
     TranspFile.TIPINF = "ПРОВЕРКА_0";
   else
     TranspFile.TIPINF = "ПРОВЕРКА";
   end;

   if(FreezCheck.PrevCheckDateEnd != FMZeroDate)
     TranspFile.DATE_PP    = FreezCheck.PrevCheckDateEnd;
   else
     TranspFile.DATE_PP    = FreezCheck.CheckDateTo;
   end;

   TranspFile.DATE_TP    = FreezCheck.CheckDateTo;

   TranspFile.KOL10    = FreezCheck.FreezedTotal;
   TranspFile.KOL11    = FreezCheck.FreezedYurics;
   TranspFile.KOL12    = FreezCheck.FreezedPhysics;
   TranspFile.KOL20    = FreezCheck.FreezedMsgTotal;
   TranspFile.KOL30    = FreezCheck.NotFreezedTotal;  
   TranspFile.KOL31    = FreezCheck.NotFreezedYurics; 
   TranspFile.KOL32    = FreezCheck.NotFreezedPhysics;
   TranspFile.KOL50    = FreezCheck.NotFreezedMsgTotal;

   TranspFile.DESCR     = FreezCheck.Comment;
   TranspFile.RESRV_P   = "0";
   TranspFile.RESRV_PP  = "0";

   
   /* Подкорректируем некоторые поля */
   CorrectTransportFile(TranspFile);

   if ( not Insert( TranspFile ) )
      MsgBox( "Ошибка вставки записи в транспортный файл" );
      return 1;
   end;
   
   return 0; /* Запись успешно выгружена */

end;




/*
 *  2. Выгрузка в транспортный файл 
 *
 *   Параметры:
 *      Date_Seance : date     - дата выгрузки
 *      Seance      : integer  - номер сеанса выгрузки
 *      First       : bool     - флаг выгрузки первой записи
 *      Repeat      : bool     - признак выгрузки в случае ошбики структурного контроля первичного файла
 *
 *   Перед вызовом макрофункции заполняются структуры:
 *      OpContr       - запись об операции, подлежащей контролю
 *
 *   Возвращаемые значения:
 *      
 *      0    - запись успешно обработана
 *    < 0    - операция должна быть выгружена другим сеансом
 *    > 0    - прерывание выполнения
 *
 */
 
macro FrzChOutToDBF( Date_Seance, Seance, First, Repeat, FileKind )

  var stat = 0;
  /* определим название файла-эталона */
  DIR_ORIG = "..\\mac\\cb\\fz134ch.dbf";//FM_GetDirOrigUnloadFile();
   
  if( Trim(DIR_ORIG) == "" )
    msgbox( "В настройках подсистемы не задан эталонный файл выгрузки" );
    return 1;
  end;
  
  return FreezCheckOutToDBF( Date_Seance, Seance, First, Repeat, FileKind );
end;