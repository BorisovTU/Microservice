/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prmfrpay.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежа по МФР в текущем филиале            */
/*                                                                      */
/*  Программист    : Popova O.                                          */
/*                                                                      */
/*  Создан         : 29.11.11                                           */
/*                                                                      */
/************************************************************************/

import prpm, pmprops, mfr_acc;

private macro IsWLPayment( DocKind:integer ):bool
  return ( ( DocKind == WL_INDOC ) or ( DocKind == 321/*WL_DOCHEAD*/ ) or ( DocKind == WL_WIPM ) );
end;

private macro СчетПервойПроводкиПоПлатежу( side )

  var query = IfThenElse( side == PRT_Debet, "SELECT T_PAYERACCOUNT "    ,
                                             "SELECT T_RECEIVERACCOUNT " ) + 
                                             "FROM TABLE( PM_CARFUN.GetPaymentCarries( ? ) )";
  var params = makeArray( SQLParam( "PaymentID", pr_pmpaym.rec.PaymentID ) );
  var rs:RsdRecordset = execSQLselect( query, params, false );
  if( rs.moveNext() )
    return rs.value(0);
  end;
  return "";
ONERROR(x)
  MsgBox( x.Message );
  return "";
end;

private macro УточненныеРеквизитыКлиента( Name:@string, Account:@string, side:integer )

  Name = IfThenElse( side == PRT_Debet, pr_pmrmprop.rec.PayerName, pr_pmrmprop.rec.ReceiverName ) + 
         ", сч. " + IfThenElse( side == PRT_Debet, pr_pmpaym.rec.PayerAccount, pr_pmpaym.rec.ReceiverAccount ) + 
         " в " + Bnk_GetPartyName( IfThenElse( side == PRT_Debet, pr_pmpaym.rec.PayerBankID, pr_pmpaym.rec.ReceiverBankID ) );

  Account = СчетПервойПроводкиПоПлатежу( side );
  var Dep = -1;
  if( Account == "" )
    var select:string = " select t_DepartmentFrom              " +
                        "   from dpmdphist_dbt                 " +
                        "  where t_PaymentID     = :PaymentID  " +
                        "    and t_DepartmentTo  = :Department " +
                        " order by t_HistoryID desc            " ;

    var params:TArray = makeArray( SQLParam( "PaymentID" , pr_pmpaym.rec.PaymentID  ), 
                                   SQLParam( "Department", pr_pmpaym.rec.Department ) );
    var rset:RsdRecordset = execSQLselect( select, params, false ); 
    if( rset and rset.moveNext() )
      Dep = rset.value(0);
    end;
    if( Dep != -1 )
      Account = IfThenElse( side == PRT_Debet, MFR_GetAccountActive ( Dep, pr_pmpaym.rec.Department, pr_pmpaym.rec.FIID, 0 ),
                                               MFR_GetAccountPassive( Dep, pr_pmpaym.rec.Department, pr_pmpaym.rec.PayFIID, 0 ) );
    end;
  end;

ONERROR(x)
  MsgBox( x.Message );
  Account = "";
end;

private macro УточненныеРеквизитыБанка( Name:@string, BIC:@string, CorAcc:@string )
  // БИК
  var dp_dep = TBfile("dp_dep.dbt", "r");
  dp_dep.rec.Code = pr_pmpaym.rec.Department;
  if( not getEQ(dp_dep))
    msgbox("Не найден филиал");
  else
    BIC = ПолучитьКодСубъекта( dp_dep.rec.PartyID, PTCK_BIC );
  end;
  // Корсчет
  var dprt = TBfile("bankdprt.dbt", "r");
  dprt.rec.PartyID = dp_dep.rec.PartyID;
  if( not getEQ(dprt))
    msgbox("Не найден филиал");
  else
    CorAcc = dprt.rec.CorAcc;
  end;
  // Наименование
  var DocKind = IfThenElse( IsWLPayment( pr_pmpaym.rec.DocKind ), WL_INDOC, pr_pmpaym.rec.DocKind );
  var params:TArray = makeArray( SQLParam( "p_Context"  , execStoredFunc( "PM_NAMES.GetContextForPrimaryDoc", V_STRING, 
                                                                           makeArray( SQLParam( "p_DocKind", DocKind ), 
                                                                                      SQLParam( "p_SubDocKind", IfThenElse( pr_pmpaym.rec.DocKind == PS_PAYORDER, pr_pspayord.rec.DocKind, 0 ) ) ) ) ), 
                                 SQLParam( "p_PartyID"  , dp_dep.rec.PartyID ),
                                 SQLParam( "p_Date"     , {curdate} ),
                                 SQLParam( "p_BankName" , V_STRING, RSDBP_OUT )  );
  var stat = execStoredFunc( "PM_NAMES.PM_SetBankName", V_INTEGER, params );
  if( stat == 0 )
    Name = string(params.Value(3).value);
  end;

ONERROR(x)
  MsgBox( x.Message );
  Name = "";
end;

MACRO PrintDocument( ncopy:integer ):bool

  var ShifrOper:string = pr_pmrmprop.rec.ShifrOper;
  var DocKind:integer  = pr_pmpaym.rec.DocKind;
  var stat:bool;

  // Банковский ордер и аккредитив выводятся без изменений
  if( ShifrOper == "17" )
    return ExecMacroFile( "prpmboi.mac", "PrintBankOrder", ncopy );
  elif( ( ( DocKind == PS_PAYORDER ) and ( pr_pspayord.rec.DocKind == PSPOKIND_AKKREDITIV ) ) or
        ( IsWLPayment( DocKind )     and ( ShifrOper == "08" ) ) )
    return ExecMacroFile("prpmaci.mac", "PrintAkkreditiv", ncopy );
  end;

  // Для остальных документов переопределим заполнение некоторых полей
  // выводим все так, как будто транзитный филиал формирует собственный платеж 
  if( ( pr_pmpaym.rec.Department != pr_pmpaym.rec.StartDepartment ) and
      ( pr_pmpaym.rec.DbFlag != "X" ) )
  // кредитовым платежам переопределим реквизиты по дебету
    УточненныеРеквизитыКлиента( @pr_pmrmprop.rec.PayerName, @pr_pmpaym.rec.PayerAccount, PRT_Debet );
    УточненныеРеквизитыБанка( @pr_pmrmprop.rec.PayerBankName, @pr_debet.rec.BankCode, @pr_pmrmprop.rec.PayerCorrAccNostro );

  elif( ( pr_pmpaym.rec.Department != pr_pmpaym.rec.StartDepartment ) and
        ( pr_pmpaym.rec.DbFlag == "X" ) )
  // дебетовым платежам переопределим реквизиты по кредиту
    УточненныеРеквизитыКлиента( @pr_pmrmprop.rec.ReceiverName, @pr_pmpaym.rec.ReceiverAccount, PRT_Credit );
    УточненныеРеквизитыБанка( @pr_pmrmprop.rec.ReceiverBankName, @pr_credit.rec.BankCode, @pr_pmrmprop.rec.ReceiverCorrAccNostro );
  end;

  /* все остальное - как обычно */
  /* Платежный ордер */
  if( ShifrOper == "16" ) 
    stat = FillBuffersForPayOrderPrint();
    if(stat)
      stat = ExecMacroFile("prpmori.mac", "PrintPayOrder", ncopy);
    end;

  /*Платежное поручение*/
  elif( ( DocKind == DLDOC_BANKPAYMENT ) or
      ( ( DocKind == PS_PAYORDER ) and ( pr_pspayord.rec.DocKind == PSPOKIND_ORDER ) ) or
      ( IsWLPayment( DocKind ) and ( ShifrOper == "01") ) or
      paymentIsRequest( pr_pmpaym, pr_debet, pr_credit ) )

    stat = ExecMacroFile( "prpmrqi.mac", "PrintPayReq", ncopy );

  /*Платежное требование*/
  elif( ( DocKind == DLDOC_BANKCLAIM ) or
        ( ( DocKind == PS_PAYORDER ) and ( pr_pspayord.rec.DocKind == PSPOKIND_DEMAND ) ) or
        ( IsWLPayment( DocKind ) and ( ShifrOper == "02" ) ) or
        paymentIsDemand( pr_pmpaym, pr_debet, pr_credit ) )
    stat = ExecMacroFile("prpmdmi.mac", "PrintPayDem", ncopy);

  /*Инкассовое поручение*/
  elif( ( ( DocKind == PS_PAYORDER ) and ( pr_pspayord.rec.DocKind == PSPOKIND_REQUEST ) ) or
        ( IsWLPayment( DocKind ) and ( ( ShifrOper == "05" ) or ( ShifrOper == "06" ) ) ) )
    stat = ExecMacroFile("prpmini.mac", "PrintIncashRequest", ncopy);

  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return false;
  end;

  return stat;

END;
