/*
 *
 * ЭЦП. Отчет "Использование ЭЦП в разрезе абонентов СКЗИ"
 *
 */
Import "sgn_util.mac", "globals.mac";

/* Параметры отчета */
var CryptoSysID  ;
var OnlyActiveKey;
var TypeAction   ;

var PrevPartyID      ;
var PrevCryptoSysID  ;
var PrevKindOperation;

/* Уровни отступа */
const INDENT_CRYPTO    = 1; /* печать криптосистемы                       */
const INDENT_KEYID     = 2; /* печать ИД ключа                            */
const INDENT_DOCKIND   = 3; /* печать данных о роли и первичном документе */
const INDENT_OPERATION = 4; /* печать операции                            */
const INDENT_OPRSTEP   = 5; /* печать настроек на шаге                    */

/* Сформировать полный отступ перед строкой */
macro DefineIndent( LevelIndent )
  var i, FullIndent;
  if( (LevelIndent > INDENT_CRYPTO) and (CryptoSysID != 0) ) LevelIndent = LevelIndent - 1; end;
  FullIndent = "";
  i = 0;
  while( i < LevelIndent )
    FullIndent = FullIndent + Indent;
    i = i + 1;
  end;
  return FullIndent;
end;

/* Получить сформированную строку - информация о криптосистеме */
macro FormCryptoSysString( CryptoSysID )
  return "СКЗИ: " + CryptoSysID + " " + GetCryptoSysName( CryptoSysID );
end;

/* Получить имя операции */
macro GetOperationName( Kind_Operation )
  file file_oprkoper("oprkoper.dbt");
  file_oprkoper.Kind_Operation = Kind_Operation;
  if( GetEQ(file_oprkoper) )
    return file_oprkoper.Name;
  end;
  return "";
end;

macro GetSgnKeyStatusName( State )
  if( State == SGN_KEY_ACTIV )
    return "активный";
  end;
  return "неактивный";

end;


/*
   Печать заголовка отчета
*/
macro PrintHeader( _CryptoSysID, _OnlyActiveKey, _TypeAction, _Department )
  
  /* Параметры отчета */
  CryptoSysID   = _CryptoSysID;
  OnlyActiveKey = _OnlyActiveKey;
  TypeAction    = _TypeAction;

  /* Рабочие данные */
  PrevPartyID = -1;

  PrintSplit();
  println( "          Использование ЭЦП в разрезе абонентов СКЗИ" );
  if( CryptoSysID )
    println( FormCryptoSysString( CryptoSysID ) );
  end;
  if( OnlyActiveKey )
    println( "Только активные ключи" );
  end;
  if( TypeAction )
    println( "Действие над ЭЦП : " + GetTypeActionName( TypeAction ) );
  end;
 
  
  PrintDepartmentName( _Department );
  
end;

/*
   Печать информации о ключе
*/
macro PrintAbonentData( buff_sgnkey )
  
  record sgnkey("sgnkey.dbt");
  var KeyString;

  SetBuff( sgnkey, buff_sgnkey );

  if( PrevPartyID != sgnkey.PartyID )
    println( "\nАбонент: " + GetPartyName( sgnkey.PartyID ) );
    PrevCryptoSysID = 0;
  end;
  
  if( (PrevCryptoSysID != sgnkey.CryptoSysID) and (CryptoSysID == 0) )
    println( DefineIndent(INDENT_CRYPTO) + FormCryptoSysString( sgnkey.CryptoSysID ) );
  end;
  
  KeyString = DefineIndent( INDENT_KEYID ) + "ИД ключа: " + sgnkey.KeyID;
  if( not OnlyActiveKey )
    KeyString = KeyString + Indent + GetSgnKeyStatusName( sgnkey.State );
  end;
  println( KeyString );
  
  PrevPartyID     = sgnkey.PartyID;
  PrevCryptoSysID = sgnkey.CryptoSysID;

end;

/*
   Печать ролей, которые абонент может подписывать данным ключом
*/
macro PrintAbonentKey( buff_sgnaccess )
  record sgnaccess("sgnaccss.dbt");

  SetBuff( sgnaccess, buff_sgnaccess );
  println( DefineIndent(INDENT_DOCKIND)
         + "Перв. док-т: " + string(GetOprkdocName (sgnaccess.DocKind):29                   )
         + "Роль: "        + string(GetSignKindName( sgnaccess.DocKind, sgnaccess.SignKind ))
         );

  PrevKindOperation = 0;
end;

/*
   Печать информации об операциях и шагах, на которые есть доступ у абонента СКЗИ
*/
macro PrintAbonentKeyAccess( buff_oprkdsgn )
  record oprkdsgn("oprkdsgn.dbt");
  var StepString;

  SetBuff( oprkdsgn, buff_oprkdsgn );

  if( PrevKindOperation != oprkdsgn.Kind_Operation )
    println( DefineIndent(INDENT_OPERATION)
           + "Операция: " + oprkdsgn.Kind_Operation + " " + GetOperationName( oprkdsgn.Kind_Operation ) );
  end;
  
  StepString = DefineIndent( INDENT_OPRSTEP );
  if( TypeAction == 0 )
    StepString = StepString + string(GetTypeActionName( oprkdsgn.TypeAction ):9 ) + Indent;
  end;
  /*if( RoleAction == 0 )
    StepString = StepString + string(GetRoleActionName( oprkdsgn.RoleAction ):14) + Indent 
  end;*/
  StepString = StepString + "Шаг: " + oprkdsgn.Number_Step
                          + " " + GetStepName( oprkdsgn.Kind_Operation, oprkdsgn.Number_Step );
  println( StepString );

  PrevKindOperation = oprkdsgn.Kind_Operation;
end;

macro PrintFooter()
  PrintSplit( "\n" );
end;
