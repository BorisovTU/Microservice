//NotesCommonClass.mac
//общий класс примечаний.
//Должны быть только общие методы по работе с примечаниями.
//Бизнес-логика или какая-нибудь особая обработка конкретного вида примечания/объекта/документа должна реализовываться в классах-наследниках. В других mac-файлах
import oralib, likepy, globals, "cblogger2.mac";

class NotesCommonClass()
  private var documentID:string = "";
  private var objectType:integer = 0;
  private var logger:c_logger;

  private macro GetNote(noteKind:integer):string
    var query = "select note_read.get_note(p_object_type => :object_type, "
              + "                          p_note_kind   => :note_kind, "
              + "                          p_document_id => :document_id) notetext from dual ";

    var sql = ExecSQLselectPrmDyn(query, objectType, noteKind, documentID);
    if (sql.MoveNext() and (valtype(sql.value("notetext")) != 26) )
      return sql.value("notetext");
    end;

    return "";
  OnError(err)
    logger.ErrorClob("GetNote. documentID = " + documentID + "; noteKind = " + noteKind + "; objtype = " + objectType, GetFullErrMsg(err));
    return null;
  end;

  private macro GetNoteMoney(noteKind:integer):double
    var params = MakeArray(SqlParam("p_object_type", objectType),
                           SqlParam("p_note_kind",   noteKind),
                           SqlParam("p_document_id", documentID));
    var note = ExecStoredFunc("note_read.get_note_money", V_DOUBLE, params);
    if (valtype(note) == 26)
      return 0;
    end;
    return note;
  OnError(err)
    logger.ErrorClob("GetNoteMoney. documentID = " + documentID + "; noteKind = " + noteKind + "; objtype = " + objectType, GetFullErrMsg(err));
    return null;
  end;

  private macro SaveNote(noteKind:integer, noteValue:string, dt:date):bool
    var params = MakeArray(SqlParam("p_object_type", objectType),
                           SqlParam("p_note_kind",   noteKind),
                           SqlParam("p_document_id", documentID),
                           SqlParam("p_note",        noteValue),
                           SqlParam("p_date",        dt));
    ExecStoredFunc("note_utils.save_note", V_UNDEF, params);
    return true;
  OnError(err)
    logger.ErrorClob("SaveNote. documentID = " + documentID + "; noteKind = " + noteKind, GetFullErrMsg(err));
    return false;
  end;
end;