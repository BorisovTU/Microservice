/*
    Библиотечный макрос для работы с датами.

    Класс CPeriod представляет временной промежуток, интерпретирует его
    по-разному для сравнения с днями, месяцами и годами.

    Константа DateMin означает минимальную валидную дату и предназначена для
    определения "неопределённой" даты путём проверки на < DateMin.
*/

import BankInter, Календарь;

const DateMin = Date(1, 1, 1000);     // Меньше этой даты заведомо не бывает
const MonthsInYear = 12;    // в году 12 месяцев

var DaysInMonth;            // кол-во дней в месяце, NULL - по календарю
var DaysInYear;             // кол-во дней в году, NULL - по календарю

// Установка значений DaysInMonth и DaysInYear.
// При любых ошибках - значения по умолчанию.
local macro SetDaysInParams()
    var parmstr, err, dm, dy, pos;
    GetRegistryValue("REPTREG\\REP_GROUPS\\COMMON\\Расчет срока", V_STRING, parmstr, err);
    pos = strbrk(parmstr, "/\\");
    if (pos == 0)
        return;
    end;
    dy = int(substr(parmstr, 1, pos - 1));
    dm = int(substr(parmstr, pos + 1));
    if (dy != 0)
        DaysInYear = dy;
    end;
    if (dm != 0)
        DaysInMonth = dm;
    end;
end;

// Инициализация.
SetDaysInParams();

// Срок, период времени
// Конструкторы:
//   CPeriod(from: date, to: date)
//   CPeriod(from: date, days: integer, months: integer, years: integer)
// Первый параметр обязателен, остальные - нет.
class CPeriod(pfrom, pdays, pmonths, pyears)
    var From: date;
    var To: date;

    // Добавить к дате дни/годы/месяцы (в том числе можно отрицательные!)
    macro AddValues(dat: date, days: integer, months: integer, years: integer)
        var d, m, y;
        if (ValType(years) == V_INTEGER)
            if (DaysInYear == NULL)
                dat = DateAfterCalenMonths(dat, years * MonthsInYear);
            else
                dat = dat + years * DaysInYear;
            end;
        end;
        if (ValType(months) == V_INTEGER)
            if (DaysInMonth == NULL)
                dat = DateAfterCalenMonths(dat, months);
            else
                dat = dat + months * DaysInMonth;
            end;
        end;
        if (ValType(days) == V_INTEGER)
            dat = dat + days;
        end;
        return dat;
    end;
    
    // Сравнение срока с "абсолютным" сроком: в днях/месяцах/годах.
    // Возвращает значение < 0, если this меньше, > 0, если this больше, 0, если равны.
    macro Compare(days: integer, months: integer, years: integer)
        return To - AddValues(From, days, months, years);
    end;

    // Возвращает кол-во дней в периоде.
    macro GetDays()
        return To - From;
    end;
    
    // Возвращает кол-во месяцев в периоде.
    macro GetMonths()
        var beginDay   = 0, beginMonth = 0, beginYear  = 0;
        var endDay     = 0, endMonth   = 0, endYear    = 0;

        dateSplit(from, beginDay, beginMonth, beginYear);
        dateSplit(to,   endDay,   endMonth,   endYear);

        if (daysInMonth == null)
            return (endYear - beginYear) * monthsInYear + (endMonth - beginMonth) + (endDay - beginDay) / 30.0;
        else
            return (endYear - beginYear) * monthsInYear + (endMonth - beginMonth) + (endDay - beginDay) / Double(daysInMonth);
        end;
    end;

    // Возвращает кол-во лет в периоде.
    macro GetYears()
        var beginDay   = 0, beginMonth = 0, beginYear  = 0;
        var endDay     = 0, endMonth   = 0, endYear    = 0;

        dateSplit(from, beginDay, beginMonth, beginYear);
        dateSplit(to,   endDay,   endMonth,   endYear);

        if (daysInYear == null)
            return (endYear - beginYear) + (endMonth - beginMonth) / Double(monthsInYear) + (endDay - beginDay) / 365.0;
        else
            return (endYear - beginYear) + (endMonth - beginMonth) / Double(monthsInYear) + (endDay - beginDay) / Double(daysInYear);
        end;
    end;

    // Конструктор из дат "с" и "по".
    private macro ConstructorDates(pfrom: date, pto: date)
        From = pfrom;
        To = pto;
    end;

    // Конструктор из даты начала и лет/месяцев/дней.
    private macro ConstructorValues(pfrom: date, pdays: integer, pmonths: integer, pyears: integer)
        ConstructorDates(pfrom, AddValues(pfrom, pdays, pmonths, pyears));
    end;

    // Общий конструктор.
    local macro Constructor(pfrom, pdays, pmonths, pyears)
        if (ValType(pfrom) != V_DATE)
            RunError("|Неправильный вызов конструктора класса CPeriod:|первый параметр обязателен и должен быть датой");
        end;
        if (ValType(pdays) == V_DATE)
            ConstructorDates(pfrom, pdays);
        else
            ConstructorValues(pfrom, pdays, pmonths, pyears);
        end;
    end;

    Constructor(pfrom, pdays, pmonths, pyears);
end;

macro dateMakeFromString(stringDate : String)
    if (stringDate == "")
        return Date(0, 0, 0);
    end;

    var position = 0;
    var day   = 0; 
    var month = 0;
    var year  = 0;

    position   = strBrk(stringDate, "./-");
    day        = Int(subStr(stringDate, 1, position));
    stringDate = subStr(stringDate, position + 1);

    position   = strBrk(stringDate, "./-");
    month      = Int(subStr(stringDate, 1, position));
    stringDate = subStr(stringDate, position + 1);

    year = Int(stringDate);

    return Date(day, month, year);
end;
