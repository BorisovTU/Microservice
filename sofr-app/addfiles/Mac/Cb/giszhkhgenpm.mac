/*
 $Name: giszhkhgenpm.mac
 $Module: Ядро Banking
 $Description: Функция генерации платежа по начислению ГИС ЖКХ
*/

import globals, PaymInter, BankInter, FIInter, "pm_tools.mac";

macro FindPartyByINN ( INN:string )
   
  var PartyID = 0;
  var select = "SELECT PARTY.T_PARTYID " +
               "   FROM DPARTY_DBT PARTY, DPARTCODE_DBT CODE " +
               " WHERE     CODE.T_CODEKIND = 16 " +
               "         AND PARTY.T_PARTYID = CODE.T_PARTYID " +
               "         AND CODE.T_CODE = :INN;";

  var prm = makeArray(SQLParam("INN", INN));

  var rs = execSQLselect( select, prm, FALSE );
  
  if( rs.moveNext() )
    PartyID = rs.value("T_PARTYID");
  end;

  return PartyID;

end;


/*
   Структура для работы в транзакции
 */
private class TrnParm
  
  var paymentObj;
  var PMServID:integer;

end;

private var _trnParm : TrnParm;


private macro AskForReGenerate( message:string ):integer
  return ConfWin( makeArray( message ), makeArray( " Да", " Нет" ) );
end;

// Транзакция вставки платежа и обновления pmserv
private macro InsertChargePayment()

  if( _trnParm.PaymentObj.Update() != 0 )
    AbortTrn();
  end;

  if( execSQL( "UPDATE DPMSERV_DBT SET T_CREATEPAYMENTID = :CreatePaymentID WHERE T_ID = :PMServID;",
            makeArray( SQLParam( "CreatePaymentID", _trnParm.PaymentObj.payment.PaymentID ), SQLParam( "PMServID", _trnParm.PMServID ) ) ) == NULL )
     AbortTrn();
  end;
end;

// Транзакция обновления платежа
private macro UpdateChargePayment()

  if( _trnParm.PaymentObj.Update() != 0 )
    AbortTrn();
  end;
end;

//Генерация платежа по начислению ГИС ЖКХ
macro GeneratePaymentFromCharge(PMServID:integer, PaymentID:integer)
  
  var params:TArray;
  var rs:object;
  
  var select = "SELECT * FROM DPMSERV_DBT WHERE T_ID = :PMServID;";
  
  params = makeArray( SQLParam( "PMServID", PMServID ) );

  rs = execSQLselect( select, params, FALSE );
  
  if( rs.moveNext() )    
    
    if(rs.value("t_CreatePaymentID") != 0)
    
    var payment = RsbPayment(rs.value("t_CreatePaymentID"));
   
    if((payment) and ( payment.PaymStatus != PM_REJECTED ) and ( payment.BaseAmount == rs.value("t_Amount") ) )
      msgbox("По начислению сформирован платеж. Повторная генерация запрещена");
      return true;
    end;
  
    if((payment) and (payment.PaymStatus != PM_REJECTED) and ((payment.BaseAmount != rs.value("t_Amount")) or (rs.value("t_Amount") == 0) ) )
    
      var question_existed = "По начислению сформирован платеж № " + payment.Number + " от " + payment.Date + " на сумму " + payment.BaseAmount + 
      ". Вы уверены, что хотите еще раз выполнить оплату по этому начислению?";
    
      if(AskForReGenerate(question_existed) != 0)
        return true;
      end;
   
    end;

    if((payment) and (payment.PaymStatus == PM_REJECTED) and (payment.CloseDate == date(0,0,0) ) ) 

      var question_rejected = "По начислению сформирован платеж № " + payment.Number + " от " + payment.Date + " на сумму " + payment.BaseAmount + 
      ". Этот платеж находится в состоянии 'отвергнут' и его обработка еще может быть продолжена. Вы уверены, что хотите еще раз выполнить оплату по этому начислению?";
    
      if(AskForReGenerate(question_rejected) != 0)
        return true;
      end;
   
    end;  
    
    end;

  if(PaymentID  == 0 )
    var createPayment = RsbPayment(0);
    
    var docNum;
    GenerateReference(272, docNum);
    
    createPayment.Number = docNum;
    createPayment.ValueDate = {curdate};
    createPayment.Date = {curdate};
    createPayment.PayerFIID = NATCUR;
    createPayment.BaseFIID = NATCUR;
    createPayment.ReceiverFIID = NATCUR;
    createPayment.PayerAmount = rs.value("t_Amount");
    createPayment.BaseAmount = rs.value("t_Amount");
    createPayment.ReceiverAmount = rs.value("t_Amount");
    createPayment.ground = rs.value("t_ground");
    
    //Реквизиты получателя    
    createPayment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                      {OurBank}, 
                      3, 
                      {MFO_Bank}, 
                      createPayment.PayerBankName,
                      createPayment.PayerCorrAccNostro,
                      createPayment.PayerFIID, 
                      1, 
                      createPayment.PayerAccount, 
                      FindPartyByINN(rs.value("t_PayerINN")), 
                      IfThenElse(
                        rs.value("t_PayerName") == "",
                        trim(rs.value("t_PayerName1") + " " + rs.value("t_PayerName2") + " " + rs.value("t_PayerName3")),
                        rs.value("t_PayerName")),
                      rs.value("t_PayerINN"));

    createPayment.HCSPayerUID = PayerIDForm(createPayment.Payer, "", rs.value("t_PayerINN") );

    createPayment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                         createPayment.ReceiverBankID, 
                         3, 
                         rs.value("t_ReceiverBankCode"), 
                         rs.value("t_ReceiverBankName"),
                         rs.value("t_ReceiverCorrAccNostro"),
                         createPayment.ReceiverFIID, 
                         1,
                         createPayment.ReceiverAccount, 
                         FindPartyByINN(rs.value("t_ReceiverINN")),
                         rs.value("t_ReceiverName"), 
                         rs.value("t_ReceiverINN") );
    
    //копирование PMSERV
    createPayment.HCSDocID = rs.value("t_HCSDocID");
    createPayment.HCSAccount = rs.value("t_HCSAccount");
    createPayment.HCSPayment = rs.value("t_HCSPayment");
    createPayment.HCSSupplierDocNum = rs.value("t_SupplierDocNum");
    createPayment.HCSSupplierAccount = rs.value("t_SupplierAccount");
    createPayment.HCSSupplierID = rs.value("t_SupplierID");
    createPayment.HCSSupplierINN = rs.value("t_SupplierINN");
    createPayment.HCSSupplierName = rs.value("t_SupplierName");
    createPayment.HCSSupplierName1 = rs.value("t_SupplierName1");
    createPayment.HCSSupplierName2 = rs.value("t_SupplierName2");
    createPayment.HCSSupplierName3 = rs.value("t_SupplierName3");
    createPayment.HCSMonth = rs.value("t_Month");
    createPayment.HCSYear = rs.value("t_Year");
    createPayment.HCSServiceID = rs.value("t_ServiceID");
    createPayment.HCSHouseGUID = rs.value("t_HouseGUID");
    createPayment.HCSHouseNum = rs.value("t_HouseNum");
    createPayment.HCSRoomNum = rs.value("t_RoomNum");
    createPayment.HCSPayerINN = rs.value("t_PayerINN");
    createPayment.HCSPayerName = rs.value("t_PayerName");
    createPayment.HCSPayerName1 = rs.value("t_PayerName1");
    createPayment.HCSPayerName2 = rs.value("t_PayerName2");
    createPayment.HCSPayerName3 = rs.value("t_PayerName3");
    
    var key = PM_ProcessPanelWithUserHandler( createPayment, 0, "giszhkhgenpm.mac"/*, 2, null, null*/ );

    if( key == 27 )
      return true;
    elif( (key == 316) or (key == 361) )     
    
      _trnParm.PaymentObj = IfThenElse( key == 316, RsbPSPayorder(0), RsbBankPayment(0) );
      _trnParm.PMServID = PMServID;
      _trnParm.PaymentObj.payment.SetAttrFromPayment(createPayment.PaymentID);
      _trnParm.PaymentObj.Origin = IfThenElse( key == 316, PSPO_OR_HCSCHARGE, MEMORDER_FDOC_HCSCHARGE );
    
    if( not ProcessTrn( null,  "InsertChargePayment" ) )
      DisplayError();
      return true;
    end;    

    return true;

    end;
  
  else
    var currentPayment = RsbPayment(PaymentID);

    if(currentPayment != null)
      
      if( currentPayment.PayerAmount == 0 )
        currentPayment.PayerAmount = rs.value("t_Amount");
      end;
      
      if( currentPayment.BaseAmount == 0 )
        currentPayment.BaseAmount = rs.value("t_Amount");
      end;
      
      if( currentPayment.ReceiverAmount == 0 )
        currentPayment.ReceiverAmount = rs.value("t_Amount");
      end;
      
      if(currentPayment.ground == "")
        currentPayment.ground = rs.value("t_ground");
      end;
      
      //Реквизиты получателя    
      currentPayment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                        IfThenElse(currentPayment.PayerBankID == 0, {OurBank}, currentPayment.PayerBankID),
                        IfThenElse(currentPayment.PayerBankCodeKind == 0, 3, currentPayment.PayerBankCodeKind), 
                        IfThenElse(currentPayment.PayerBankCode == 0, {MFO_Bank}, currentPayment.PayerBankCode), 
                        currentPayment.PayerBankName,
                        currentPayment.PayerCorrAccNostro,
                        currentPayment.PayerFIID, 
                        IfThenElse(currentPayment.Chapter == 0, 1, currentPayment.Chapter), 
                        currentPayment.PayerAccount, 
                        FindPartyByINN(rs.value("t_PayerINN")),

                        IfThenElse(currentPayment.PayerName == "", 
                        IfThenElse(
                          rs.value("t_PayerName") == "",
                          trim(rs.value("t_PayerName1") + " " + rs.value("t_PayerName2") + " " + rs.value("t_PayerName3")),
                          rs.value("t_PayerName")), currentPayment.PayerName),
                        IfThenElse(currentPayment.PayerINN == "", rs.value("t_PayerINN"), currentPayment.PayerINN));

      currentPayment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                           currentPayment.ReceiverBankID, 
                           IfThenElse(currentPayment.PayerBankCodeKind == 0, 3, currentPayment.PayerBankCodeKind), 
                           IfThenElse(currentPayment.ReceiverBankCode == "", rs.value("t_ReceiverBankCode"), currentPayment.ReceiverBankCode),
                           IfThenElse(currentPayment.ReceiverBankName == "", rs.value("t_ReceiverBankName"), currentPayment.ReceiverBankName),
                           IfThenElse(currentPayment.ReceiverCorrAccNostro == "", rs.value("t_ReceiverCorrAccNostro"), currentPayment.ReceiverCorrAccNostro),
                           currentPayment.ReceiverFIID, 
                           IfThenElse(currentPayment.Chapter == 0, 1, currentPayment.Chapter), 
                           currentPayment.ReceiverAccount, 
                           FindPartyByINN(rs.value("t_ReceiverINN")),
                           IfThenElse(currentPayment.ReceiverName == "", rs.value("t_ReceiverName"), currentPayment.ReceiverName), 
                           IfThenElse(currentPayment.ReceiverINN == "", rs.value("t_ReceiverINN"), currentPayment.ReceiverINN));
            
      //копирование PMSERV
      currentPayment.HCSDocID = rs.value("t_HCSDocID");
      currentPayment.HCSAccount = rs.value("t_HCSAccount");
      currentPayment.HCSPayment = rs.value("t_HCSPayment");
      currentPayment.HCSSupplierDocNum = rs.value("t_SupplierDocNum");
      currentPayment.HCSSupplierAccount = rs.value("t_SupplierAccount");
      currentPayment.HCSSupplierID = rs.value("t_SupplierID");
      currentPayment.HCSSupplierINN = rs.value("t_SupplierINN");
      currentPayment.HCSSupplierName = rs.value("t_SupplierName");
      currentPayment.HCSSupplierName1 = rs.value("t_SupplierName1");
      currentPayment.HCSSupplierName2 = rs.value("t_SupplierName2");
      currentPayment.HCSSupplierName3 = rs.value("t_SupplierName3");
      currentPayment.HCSMonth = rs.value("t_Month");
      currentPayment.HCSYear = rs.value("t_Year");
      currentPayment.HCSServiceID = rs.value("t_ServiceID");
      currentPayment.HCSHouseGUID = rs.value("t_HouseGUID");
      currentPayment.HCSHouseNum = rs.value("t_HouseNum");
      currentPayment.HCSRoomNum = rs.value("t_RoomNum");
      currentPayment.HCSPayerINN = rs.value("t_PayerINN");
      if( rs.value("t_PayerUID") != "" )
        currentPayment.HCSPayerUID = rs.value("t_PayerUID");
      else
        currentPayment.HCSPayerUID = PayerIDForm(currentPayment.Payer, "", rs.value("t_PayerINN") );
      end;
      currentPayment.HCSPayerName = rs.value("t_PayerName");
      currentPayment.HCSPayerName1 = rs.value("t_PayerName1");
      currentPayment.HCSPayerName2 = rs.value("t_PayerName2");
      currentPayment.HCSPayerName3 = rs.value("t_PayerName3");

      _trnParm.PaymentObj = RsbPSPayorder(PaymentID);
    
      if( not ProcessTrn( null,  "UpdateChargePayment" ) )
        DisplayError();
        return true;
      end;
      
      return true;
      
    end;
  
  end;
    
  end;   
  
end;


macro ProcessPanel(mode, key, field, panel)

  if( (mode == 2) and (key == 522))
    
    //Скрыть пункт по названию, который пересекается
    panel.MenuItems.HideItem( null, "Выполнить                      F2 ", true  );
    
    // Добавить пункты меню
    panel.MenuItems.AddItem( 0, "Клиентский платеж  F2", 316, 0  );
    panel.MenuItems.AddItem( 0, "Платеж банка  Alt-F2", 361, 0  );
    panel.MenuItems.AddItem( 0, "Отказаться  Esc", 27, 0  );
    
  end;

  // Выйти из панели по нажатию F2 / Alt-F2 / Esc
  if((mode == 1) and ((key == 316) or (key == 361) or (key == 27)))
    key = -key;
  end;      

  return key;
end;

//GeneratePaymentFromCharge(15, 47928);