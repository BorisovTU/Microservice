//SfSubcontr.mac
import PTInter, oralib;

/*
   Глобальный класс для субдоговоров обслуживания.
   должен использоваться только для общего функционала с субдоговорами
   и не должен содержать в себе особенности какой-либо бизнес-логики
*/
class SfSubContr(_sfContrID:integer)
  private var sfContrID:integer = _sfContrID;
  private var sfcontr = TRecHandler("sfcontr.dbt");
  private var clientNameValue:string = "";
  private var ekkValue:string = "";
  private var mpCode:string = "";
  private var dLContrID = 0;
  private var subContrID = 0;


  private macro Init()
    var query = "select c.* "
              + "  from dsfcontr_dbt c "
              + " where c.t_id = :sfcontrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);

    //может быть, есть более удачной способ поместить данные из sql в структуру, но я не умею
    if (sql.MoveNext())
      sfcontr.clear();
      sfcontr.rec.ID       = sql.value("t_id");
      sfcontr.rec.Number   = sql.value("t_number");
      sfcontr.rec.DateConc = sql.value("t_dateconc");

      clientNameValue = GetPartyNames(sql.value("t_partyid"), 0)(1);
    end;

  end;

  //lazy init
  macro Ekk():string
    if (ekkValue != "") 
      return ekkValue;
    end;

    var query = "select c.t_code "
              + "  from ddlcontrmp_dbt m "
              + "  join ddlobjcode_dbt c on c.t_objectid = m.t_dlcontrid "
              + "                        and c.t_codekind = 1 "
              + "                        and c.t_objecttype = 207 "
              + " where (   c.t_bankclosedate = to_date('01.01.0001', 'dd.mm.yyyy') "
              + "        or c.t_bankclosedate > sysdate) "
              + "  and m.t_sfcontrid = :contrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      ekkValue = sql.value("t_code");
    end;

    return ekkValue;
  end;

  macro ContrNum():string
    return sfcontr.rec.Number;
  end;

  macro ContrDate():date
    return sfcontr.rec.DateConc;
  end;

  macro ClientName():string
    return clientNameValue;
  end;

  macro GetMpCode():string
    if (mpCode != "") 
      return mpCode;
    end;

    var query1 = "select c.t_id "
              +  "  from dsfcontr_dbt c  "
              +  "  join ddlcontrmp_dbt m on m.t_sfcontrid = c.t_id "
              +  "  join ddlcontrmp_dbt m2 on m2.t_dlcontrid = m.t_dlcontrid "
              +  "  where m2.t_sfcontrid = :sfContrID "
              +  "  and c.t_servkind = 1 "
              +  "  and c.t_servkindsub = 8 "
              +  "  and m.t_marketid = 2 ";
    var sql1 = ExecSQLselectPrmDyn(query1, sfContrID);

    if (sql1.MoveNext())
      subContrID = sql1.value("t_id"); 
    end;         

    var query2 = "select m.t_mpcode "
              + "  from ddlcontrmp_dbt m "
              + " where m.t_sfcontrid = :subContrID ";
    var sql2 = ExecSQLselectPrmDyn(query2, subContrID);
    if (sql2.MoveNext())
      mpCode = sql2.value("t_mpcode");
    end;

    return mpCode;
  end;

  macro GetContrID():integer
    return sfcontr.rec.ID; 
  end;

  macro IsExchange():bool
    var result = false;
    var query = "select T_MARKETID  "
            + "  from ddlcontrmp_dbt  "
            + "  where t_sfcontrid = :sfContrID  ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
        result = sql.value("T_MARKETID") > 0;
    end;

    return result;        
  end;


  macro GetDlContrID():integer
  if (dLContrID != 0) 
      return dLContrID;
    end;
    var query = "select t_dlcontrid "
              + "  from ddlcontrmp_dbt m "
              + " where t_sfcontrid = :sfcontrID ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      dLContrID = sql.value("t_dlcontrid");
    end;

    return dLContrID;
  end;

  Init();
end;