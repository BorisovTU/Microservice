/*
 $Name: nvpirep.mac
 $Module: Главная книга
 $Description: Печать отчета по переоценке НВПИ
*/

Import rsd, FIInter, PTInter, "globals.mac", "reg_rep_lib.mac", "consrep.mac";

private var i : integer, count : integer;

private var TotalSum    : money;
private var TotalSumNat : money;

private var OperName : string;
private var OperPost : string;


private macro PrintEmptyLog( DprtID : integer );

  PrintLogDepartment( DprtID );

[Счетов для урегулирования не найдено];

end;

private macro PrintLogLine( rs : RsdRecordset )

  var Numb_Document    : string;
  var Account_Payer    : string;
  var Account_Receiver : string;
  var Sum              : money;
  var SumNat           : money;

  if( rs.value(0) == NULLVAL ) Numb_Document = "";
  else Numb_Document    = rs.value(0);
  end;
  Account_Payer    = rs.value(1);
  Account_Receiver = rs.value(2);
  Sum              = rs.value(3);
  SumNat           = rs.value(4);

[|#####|#########################|#########################|###############|]
( Numb_Document:r, Account_Payer:f, Account_Receiver:f, Sum );
[+-----+----+--------------------+-------------------------+---------------+];

  TotalSum = TotalSum + Sum;
  TotalSumNat = TotalSumNat + SumNat;

end;

/* Печать заголовка таблицы */
private macro PrintLogTableTop( DprtID : integer, Date_Carry : date, Chapter : integer, CurrencyEq : integer, Currency : integer, Backout : bool )

  PrintLogDepartment( DprtID );

[                               РЕЕСТР                                      ];
  if(Backout)
[     удаленных документов по операции переоценки НВПИ на ##########        ]( Date_Carry:f );
  else
[          документов по операции переоценки НВПИ на ##########             ]( Date_Carry:f );
  end;
[                                                                           ];
[                                                                           ];
[Глава:                #                                                    ]( GetChapterString( Chapter ));
[Валюта-эквивалент:    #                                                    ]( GetCurrencyString( CurrencyEq ));
[Валюта обязательства: #                                                    ]( GetCurrencyString( Currency ));
[                                                                           ];
[+-----+-------------------------+-------------------------+---------------+];
[|  №  |          Дебет          |         Кредит          |Сумма в валюте |];
[|докум|                         |                         | обязательства |];
[+-----+-------------------------+-------------------------+---------------+];

  TotalSum    = $0;
  TotalSumNat = $0;

end;

/* Печать нижней части таблицы */
private macro PrintLogTableBot()

[|Итого     |                                              |###############|] ( TotalSum );
[+----------+----------------------------------------------+---------------+];
[|Итого(руб)|                                              |###############|] ( TotalSumNat );
[+----------+----------------------------------------------+---------------+];
[                                                                           ];
[                                                                           ];
[                                                                           ];
[##############################     #######                                 ]( OperPost:w, "(" + string({oper}) + ")" );
[                                    #                                      ]( OperName );
[                                                                           ];
[Контролер                          (                        )              ];
[                                                                           ];
[                                                                           ];
end;

private macro PrintBlock( prs : RsdRecordset, Backout : bool )
  
  var sqlString : string;
  var rs : RsdRecordset;

  var Date_Carry  : date;
  var Chapter     : integer;
  var CurrencyEq  : integer;
  var Currency    : integer;
  var Account_Exp : string;
  var Dprt        : integer;

  Date_Carry  = prs.value(0);
  Chapter     = prs.value(1);
  CurrencyEq  = prs.value(2);
  Currency    = prs.value(3);
  Account_Exp = prs.value(4);
  Dprt        = prs.value(5);

  PrintLogTableTop( Dprt, Date_Carry, Chapter, CurrencyEq, Currency, Backout );

  sqlString  = "SELECT t_Numb_Document, t_Account_Payer, t_Account_Receiver, t_Sum, t_SumNat " + 
               "  FROM dnvpi_log_tmp " +
               " WHERE t_Date_Carry = TO_DATE('" + string(Date_Carry) + "','DD.MM.YYYY')" +
               "   AND t_Chapter = " + Chapter +
               "   AND t_Dprt = " + Dprt +
               "   AND t_CurrencyEq = " + CurrencyEq +
               "   AND t_Currency = " + Currency +
               "   AND t_Account_Exp = '" + Account_Exp + "'";

  rs = RsdRecordset( sqlString );

  while( rs.moveNext() )
    
    UseProgress( i );

    PrintLogLine( rs );

    i = i + 1;

  end;

  OperName = GetOperString( {oper}, @OperPost );
  PrintLogTableBot();

end;

private macro PrintCarryLog(Backout : bool)

  var sqlString : string;
  var rs : RsdRecordset;

  sqlString = "SELECT count(1) FROM dnvpi_log_tmp";

  rs = RsdRecordset( sqlString );

  rs.moveNext();

  count = rs.value(0);

  if( count == 0 )
    /* Выводим пустой отчет */
    PrintEmptyLog({operdprt});

  else

    sqlString  = "SELECT DISTINCT t_Date_Carry, t_Chapter, t_CurrencyEq, t_Currency, t_Account_Exp, t_Dprt " + 
                 "  FROM dnvpi_log_tmp " +
                 " ORDER BY t_Dprt, t_Date_Carry, t_Chapter, t_CurrencyEq, t_Currency, t_Account_Exp";

    rs = RsdRecordset( sqlString );

    InitProgress( count, null, "Формирование реестра" );

    i = 0;
    while( rs.moveNext() )
    
      PrintBlock( rs, Backout );

    end;

    RemProgress();

  end;

end;

macro PrintCarryLogExtNVPI(Backout : bool)
  return PrintCarryLog(Backout);
end;