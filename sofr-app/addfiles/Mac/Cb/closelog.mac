/*
  ВЕДОМОСТЬ ЗАКРЫТИЯ ЛИЦЕВЫХ СЧЕТОВ 
*/

import RSD;

var PrevDprtSuccess = -1;
var PrevDprtError = -1;

private macro PrintHeader()
  [
  
                      
                     ВЕДОМОСТЬ ЗАКРЫТИЯ ЛИЦЕВЫХ СЧЕТОВ];
end;

macro GetDepartmentName( DprtID ) : string

  var sqlString : string;
  var rs : RsdRecordset;

  sqlString = "SELECT t_Name " +
              "FROM ddp_dep_dbt " +
              "WHERE t_Code = " + DprtID;

  rs = RsdRecordset( sqlString );

  if( rs.moveNext())
    return rs.value( 0 );
  end;

  return "";
end;

macro PrintDepartmentName( DprtID )

  var strDprt : string;

  strDprt = GetDepartmentName( DprtID );

[
   Филиал: ####################################################################] ( strDprt );
end;

private macro GetUser( userNum ) : string
  var strSql : string;
  var strUser : string;
  var rs : RsdRecordset;

  strSql = "SELECT t_oper, t_name " +
           "FROM dperson_dbt      " +
           "WHERE                 " +
           "  t_oper = " + userNum ;
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    strUser = rs.value( 0 ) + " " + rs.value( 1 );
  end;
  return strUser;
end;

private macro PrintFooter( ParmID )
  var strUser : string;
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t_Oper, t_SysDate " +
           "FROM dcloseprm_dbt      " +
           "WHERE                 " +
           "  t_ParmID = " + string( ParmID );
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())

    strUser = GetUser( rs.value( 0 ));

[
 Дата: ##########              Отв. исполнитель: #############################] ( string(rs.value( 1 )), strUser );

  end;
end;

private macro MakeSqlQueryError( ParmID, DprtID ) : string

  var sqlString : string;

  sqlString = "SELECT " +
              "  cl.t_ClosedAccount, " +
              "  cl.t_ErrorMsg " +
              "FROM dcloselog_dbt cl " +
              "WHERE cl.t_ParmID = " + string( ParmID ) + " AND cl.t_Department = " + string( DprtID ) +
              "  AND cl.t_Success != 'X' "
              "ORDER BY cl.t_ClosedAccount";

  return sqlString;        

end;

private macro PrintError( ParmID, DprtID )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;
  
  strSql = MakeSqlQueryError( ParmID, DprtID );  

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())

    if( PrevDprtError != DprtID )
      PrevDprtError = DprtID;
[
                                  ПРОТОКОЛ ОШИБОК
 +-------------------------+------------------------------------------------------+];
[| Номер лицевого счета    |Причина ошибки при закрытии                           |];
[+-------------------------+------------------------------------------------------+];
    end;
[|#########################|######################################################|] ( rs.value( 0 ), rs.value( 1 ));
[+-------------------------+------------------------------------------------------+];

  end;
end;

private macro MakeSqlQuerySuccess( ParmID, DprtID ) : string

  var sqlString : string;

  sqlString = "SELECT " +
              "  cl.t_ClosedAccount, " +
              "  cp.t_BankDate " +
              "FROM dcloselog_dbt cl, dcloseprm_dbt cp " +
              "WHERE cl.t_ParmID = " + string( ParmID ) + " AND cl.t_Department = " + string( DprtID ) +
              "  AND cp.t_ParmID = cl.t_ParmID AND cl.t_Success = 'X' "
              "ORDER BY cl.t_ClosedAccount";

  return sqlString;        

end;

private macro PrintSuccess( ParmID, DprtID )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;
  
  strSql = MakeSqlQuerySuccess( ParmID, DprtID );  

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())

    if( PrevDprtSuccess != DprtID )
      PrevDprtSuccess = DprtID;
[
 +-------------------------+----------+];
[| Номер лицевого счета    |Дата      |];
[|                         |закрытия  |];
[+-------------------------+----------+];
    end;
[|#########################|##########|] ( rs.value( 0 ), string( rs.value( 1 )));
[+-------------------------+----------+];

  end;
end;

private macro MakeSqlQueryDprt( ParmID ) : string

  var sqlString : string;

  sqlString = "SELECT " +
              "  DISTINCT t_Department " +
              "FROM dcloselog_dbt " +
              "WHERE t_ParmID = " + string( ParmID ) +
              " ORDER BY t_Department";

  return sqlString;        

end;

private macro PrintContext( ParmID )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;
  
  strSql = MakeSqlQueryDprt( ParmID );  

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())
    PrintDepartmentName( rs.value( 0 ));
    
    PrintSuccess( ParmID, rs.value( 0 ));

    PrintError( ParmID, rs.value( 0 ));
  end;

end;

macro PrintReport( ParmID )
  PrintHeader();

  PrintContext( ParmID );

  PrintFooter( ParmID );
end;
