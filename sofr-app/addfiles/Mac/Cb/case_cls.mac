/*
$Name: case_cls.mac
$Module: Ядро ГКБО
$Description: Портфель однородных требований. Класс первичного документа для категорий учета
*/

Import CTInter, OprInter, rsd, "acc_cls.mac", "globals.mac";

/* получить буфер операциониста */
private macro find_oper( oper : integer, operRec ) : bool
  file operFile("person.dbt");
  var stat : bool;

  operFile.Oper = oper;

  stat = GetEQ( operFile );
  if( stat )
    Copy( operRec, operFile );
  end;

  return stat;

end;

/* получить филиал операциониста */
private macro find_oper_department( oper : integer ) : integer
  record operRec("person.dbt");
  var Department : integer;

  Department = 0;
  if( oper )
    if( find_oper(oper, operRec) )
      Department = operRec.CodeDepart;
    end;
  end;

  return Department;
end;

/*
 *  Первичный документ для категорий (Портфель однородных требований)
 */
class (ReservePrimdoc) AccCasePrimdoc( accase, accasepm, accasscs )

/* данные класса ---------------------------------------------------- */
  var casesub_record;
  var case_record;
  var casepm_record;

  private var FIRoleBArray = TArray;


  var Error = 0, Kind, ID;

/* ------------------------------------------------------------------ */

/* вернуть параметр второго рода ------------------------------------ */

  macro GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = find_oper_department( case_record.Oper );
    end;

    return Parametr;
  end;

/* вернуть параметр первого рода ------------------------------------ */

  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole )
    var Parametr = -1;
    var sqlString   : string;
    var rs          : RsdRecordset;
    var ReserveType : integer;
    var CategoryContragent;
    var Classif, Balance, iBegBrackets = 0, iEndBrackets = 0;
    
    sqlString = "SELECT t_ReserveType " +
                "FROM daccassub_dbt " +
                "WHERE t_CaseID = " + casesub_record.CaseSubID + "; ";

    rs = RsdRecordset( sqlString );

    if( rs.moveNext())
      ReserveType = rs.value(0);
    end;

    if( Classificator == LLCLASS_PRODUCT_ANRAS )

      Parametr = 1000;

    elif( Classificator == LLCLASS_RSRVLOSS_BY_ACC )
      
      if( case_record.ReserveKind == ACCASE_RESERVEKIND_RVP )
        Parametr = ReserveType;
      end;

    elif( Classificator == LLCLASS_RSVLOANSLOSS_BY_ACC )
      
      if( case_record.ReserveKind == ACCASE_RESERVEKIND_RVPS )
        Parametr = ReserveType;
      end;

    elif( Classificator == LLCLASS_KIND_OF_ASSETS_RESERV )

      Parametr = OBJTYPE_CASE;

    elif( Classificator == LLCLASS_KINDSUBJ_KONTRMM )

      if(casesub_record.Contragent != 0)
        Parametr = casesub_record.Contragent;
      else
        Classif = GetCaseReserveClassif(case_record.ReserveKind, ReserveType, case_record);
        iBegBrackets = index(Classif, "(");
        iEndBrackets = index(Classif, ")");
        if((Classif != null) and (iBegBrackets > 0) and (iEndBrackets > 0))
          Balance = substr(Classif, iBegBrackets + 1, iEndBrackets - iBegBrackets - 1);
        end;
        if(Balance != null)
          CategoryContragent = GetCategoryContragent( Balance, casepm_record.AccountChapter );
        end;
        if(CategoryContragent != null)
          Parametr = CategoryContragent;
        end;
      end;
      
    elif( Classificator == LLCLASS_KINDSUBJ_DEBETSECUR )

      if(casesub_record.Contragent != 0)
        Parametr = casesub_record.Contragent;
      else
        Classif = GetCaseReserveClassif(case_record.ReserveKind, ReserveType, case_record);
        iBegBrackets = index(Classif, "(");
        iEndBrackets = index(Classif, ")");
        if((Classif != null) and (iBegBrackets > 0) and (iEndBrackets > 0))
          Balance = substr(Classif, iBegBrackets + 1, iEndBrackets - iBegBrackets - 1);
        end;
        if(Balance != null)
          CategoryContragent = GetCategoryContragent( Balance, casepm_record.AccountChapter );
        end;
        if(CategoryContragent != null)
          Parametr = CategoryContragent;
        end;
      end;

    elif( Classificator == 1660 )

      Classif = GetCaseReserveClassif(case_record.ReserveKind, ReserveType, case_record);
      iBegBrackets = index(Classif, "(");
      iEndBrackets = index(Classif, ")");
      if((Classif != null) and (iBegBrackets > 0) and (iEndBrackets > 0))
        Balance = substr(Classif, iBegBrackets + 1, iEndBrackets - iBegBrackets - 1);
      end;
      if(Balance != null)
        CategoryContragent = GetCategoryContragent( Balance, casepm_record.AccountChapter );
      end;
      if(CategoryContragent != null)
        Parametr = CategoryContragent;
      end;

    end;

    return Parametr;
  end;

  macro GetDepartment() : integer
    var Parametr = -1;
    Parametr = find_oper_department( case_record.Oper );

    return Parametr;
  end;


/* остновная роль фИ ------------------------------------------------ */
  macro GetBasisFIRole(FIRole)
    return FIROLE_UNDEF;
  end;

/* вернуть весь базис ролей ----------------------------------------- */
  macro GetFIRoleBArray()
    return FIRoleBArray;
  end;

/* найти/открыть счет ---------------------------------------------- */
  macro FindAndOpenAccount( CatCode : string, DateCarry : date, RealOpenMode : @integer, PairAccMode : integer )
    return MC_FindAndOpenAccount( 
       CatCode           
      ,this              
      ,DateCarry         
      ,IsOprMultiExec()  
      ,MC_OPENACC_CREATE 
      ,null              
      ,null              
      ,null              
      ,PairAccMode              
      ,null              
      ,null              
      ,RealOpenMode      
      ,null              
      ,null              
      ,null              
      ,null              
      ,2 );
  end;

/* найти/закрыть счет ---------------------------------------------- */
  macro FindAndCloseAccount( CatCode : string, DateCarry : date, IsClose : @integer )
    var ReserveAccount = MC_FindAndOpenAccount( CatCode, this, DateCarry, IsOprMultiExec(), MC_OPENACC_CHECKEXIST );
    if( not MC_FindAndCloseAccount( CatCode, this, DateCarry, null, null, null ))
      IsClose = 1;
    else
      IsClose = 0;
    end;
    return ReserveAccount;
  end;

/* найти счет ------------------------------------------------------- */
  macro FindAccount( CatCode : string, DateCarry : date, PairAccMode : integer)
    return MC_FindAndOpenAccount( CatCode, this, DateCarry, IsOprMultiExec(), MC_OPENACC_CHECKEXIST, null, null, null, PairAccMode );
  end;

/* привязать счет заново -------------------------------------------- */
  macro RecreateAccount( CatCode : string, DateCarry : date, RealOpenMode : @integer )
    return MC_FindAndOpenAccount( CatCode, this, DateCarry, IsOprMultiExec(), MC_OPENACC_RECREATE, null, null, null, null, null, null, RealOpenMode );
  end;

/* Корректировка счета ---------------------------------------------- */
   MACRO CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
      return true;
   END;

   macro NameObject() : string
     return case_record.Name;
   end;

   macro GetObjParam() : string
     return "портфелю \"" + case_record.Name + "\"";
   end;

   macro GetGroupRisk( DateGroup : date ) : string
     return casepm_record.RiskGroup;
   end;

   macro GetReserveProcent( DateProcent : date ) : double
     return casepm_record.ReservePercent;
   end;

/* Конструктор */
   Kind = DOCKIND_CASE;
   Id   = accasscs.CaseSubID;

   case_record   = accase;
   casepm_record = accasepm;
   casesub_record = accasscs;

   FIRoleBArray[0] = 0;
/* ------------------------------------------------------------------ */

end;
