/*
 $Name: prcret40.mac
 $Module: Возврат излишне начисленных процентов
 $Description: Отражать в учете?
 */
 /*
	Блок      : 452700 - "Возврат излишне начисленных процентов"
    Шаг       : 40    - "Отражать в учете?"
    Назначение: Макрос шага
    Описание  : Макрос шага
 */

IMPORT RsbDataSet, CTInter, InsCarryDoc, BankInter, PaymInter, pm_common,
       PTInter,globals, CurrInter, mpcactacc, mpccarry, mpcpaym, SFInter,
       mpclb, mpcpayor,mpclb;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);

RECORD settacc( settacc );
record acc_buf (account);

var prccontract = TBfile("prccontract.dbt"),
    cntsched  = TBfile("prccntsched.dbt"),
    cntsched2 = TBfile("prccntsched.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    ContractID = 0,
    ResKind = 0,
    DocID = 0,
    TypeDocs = 0, 
    OldTypeDocs = 0;

// Проверить, инсталлировано ли РКО 
PRIVATE MACRO IsInstalledRKO()
  return true;
END;

PRIVATE MACRO ActuateAcc(CatID, Account:@string, FIID:@integer, Chapter:@integer, acc_buf)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID, CatCode, @CodeCurr, @Chapt, acc_buf);
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;
   
   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;

PRIVATE MACRO СчетПроводки( PrcAccount, МаскиСчетов1 )
  var rs, cmd;
  var strQuery = "SELECT T_ACCOUNT_RECEIVER FROM DACCTRN_DBT WHERE T_ACCOUNT_PAYER = :PrcAccount";
  cmd = RsdCommand( strQuery );
  cmd.addParam( "", RSDBP_IN, PrcAccount );
  cmd.execute();      
 
  rs = TRsbDataSet( cmd );
  
  while( rs.movenext() )
    var Account_Receiver = rs.Account_Receiver;
    if( CompareStrWithMasks (МаскиСчетов1, Account_Receiver)  == 0 )
      return true;
    end;
  end;
  return false;
END;

private macro MoreAtrSub( party )
  
  file persn("persn.dbt") key 0;
  file objattr("objattr.dbt") key 0;
  var FindAttr:bool;
if( persn.PersonID = party.PartyID)
 if( getEQ( persn ) )
  if ( party.LegalForm == PTLEGF_PERSN )
    if( (persn.IsEmployer == "X") 
      OR ((CheckObjAttrPresence ( FindAttr, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY), 16, 2, "", "", false ) == true) AND (FindAttr == true) ) 
      OR ((CheckObjAttrPresence ( FindAttr, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY), 16, 3, "", "", false ) == true ) AND (FindAttr == true) )
    )
      return true;
    end;
  end;
 end;
end;
  return false;
end;

private macro BankPropertyYuris( party )

  var cmd;
  var rs;
  
  var isPro = false;

  if( party.LegalForm != PTLEGF_PERSN )
    
    cmd = RsdCommand( "SELECT * FROM dpartyown_dbt WHERE t_PartyID = :PartyID AND (t_PartyKind = 2 OR t_PartyKind = 56 OR t_PartyKind = 29 OR t_PartyKind = 55)" );
    cmd.addParam( "", RSDBP_IN, party.PartyID );
       
    rs = RsdRecordset( cmd );
  
    if( rs.moveNext() )
      return isPro = false;
    else
      return isPro = true;
    end;
  end;

  return isPro;

end;
PRIVATE MACRO Prc_AddGroundVO( Ground, PrcAccount, FIID, PrcClientID)
   
   var МаскиСчетов1 = "", МаскиСчетов2 = "", err, err1, err2, Flag;
    
   GetRegistryValue("BBANK\\PERCENT\\СЧЕТА КРЕДИТОВ", V_STRING, МаскиСчетов1, err1);
   GetRegistryValue("BBANK\\PERCENT\\СЧЕТА ПРОСРОЧКИ", V_STRING, МаскиСчетов2, err2);
      
   file party("party.dbt") key 0;

   party.PartyID = PrcClientID;
  GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, Flag, err);
  if( Flag )
   if( getEQ( party ) )
     if ( (err1 == 0) AND (err2 == 0) )
       if( (FIID == NATCUR) AND (party.NotResident == "X") AND (party.LegalForm != PTLEGF_PERSN) ) 
         if ( (CompareStrWithMasks (МаскиСчетов1,PrcAccount) == 0 ) 
           OR ( (CompareStrWithMasks (МаскиСчетов2,PrcAccount) == 0 ) 
          AND (СчетПроводки( PrcAccount,МаскиСчетов1) == true )) )
             Ground = "{VO80010} " + ground;  
         else
             Ground = "{VO80050} " + ground;  
           end;
       elif( (FIID != NATCUR) AND (party.NotResident != "X") AND ( (MoreAtrSub( party ) == true) OR (BankPropertyYuris( party ) == true) ) ) 
         if ( (CompareStrWithMasks (МаскиСчетов1,PrcAccount) == 0) 
           OR ((CompareStrWithMasks (МаскиСчетов2,PrcAccount) == 0 )
          AND ( СчетПроводки(PrcAccount,МаскиСчетов1 ) == true)) )
             Ground = "{VO80110} " + ground;  
         else
           Ground = "{VO80150} " + ground;
           end;
         end;
       end;
     end;
   end;
   return Ground;
END;

MACRO GetCntSchedIDType3(ContractID, ScheduleKind, Date)
   var cmd,rs,
   stat = 0,
   ID = 0;

   cmd = RSDCommand ("select T_CNTSCHEDID from DPRCCNTSCHED_DBT where T_CONTRACTID = ? "
         + "and T_SCHEDULEKIND = ? and T_DATE = ? and T_SPECIALTYPE = 3");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, ScheduleKind );
   cmd.addParam( "", RSDBP_IN, Date );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      ID = rs.cntschedid; 
   end;

   return ID;
END;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep( Buffer, Document )
    SetBuff(cntopr_buf, Document);
    
    var stat = 0;
    ContractID = cntopr_buf.ContractID;
    DocID      = cntopr_buf.DocID;
  
    var Bdate = cntopr_buf.BeginDate;
    var Edate = cntopr_buf.EndDate;
    var dSо = $0.0;

    ClearRecord( contract_buf );

    prccontract.KeyNum = 0;  
    prccontract.rec.ContractID = ContractID;
    if (not prccontract.GetEQ)
        msgbox(ContractID, "Не найден договор с ID = " + ContractID);
        return 1;
    end;
    
    var CntSchedID  = GetCntSchedIDType3(ContractID, 3, cntopr_buf.OperDate);
    var CntSchedID2 = GetCntSchedIDType3(ContractID, 2, cntopr_buf.OperDate);
    
    cntsched.KeyNum = 0;
    cntsched.rec.CNTSCHEDID = CntSchedID;
    cntsched.GetEQ();

    cntsched2.KeyNum = 0;
    cntsched2.rec.CNTSCHEDID = CntSchedID2;
    cntsched2.GetEQ();
    
    dSо = cntsched.rec.SUMCALC;
    var dSнач = cntsched2.rec.SUMCALC;
    
    var Debet="",Credit="",
       DebetCatID = 0, CreditCatID = 0,
       DebetTypeID = 0, CreditTypeID = 0,
       CreditFIID, DebetFIID, ContrFIID,
       DebetDep = 0, CreditDep = 0,
       CarrySum = $0, DebetCarrySum = $0, CreditCarrySum = $0,
       PayerAccount, Chapter, PayerFIID, PayerSettAcc, AlienAcc,
       CreditSettAcc, DebetSettAcc;

    ContrFIID = prccontract.rec.FIID;

    if (dSо != $0.0)
        PayerAccount = prccontract.rec.Account;
        PayerFIID = prccontract.rec.FIID;
    end;
        
    var AccTrn;
    var НазначениеПроводки;
    if (dSо != $0.0)
        Debet = PayerAccount;
        DebetFIID = PayerFIID;
        CreditTypeID = 7;
        CarrySum = dSо;
        GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
        
        if ((Credit == "") and (CreditCatID == 0))
                msgbox("Не определен счет кредита и не задана соотв.КУ для договора");
                    return 1;
        else
            if (Credit == "")
                ActuateAcc(CreditCatID, @Credit, @CreditFIID, @Chapter);
                if (Credit == "")
                    msgbox("Не удалось актуализировать счет кредита");
                    return 1;
                else
                    PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
                end;
            end;
        end;
        
        var Основание = "Отнесение на доходы оплаченных процентов при пересчете по пониженной процентной ставке " +
        "за период " + cntopr_buf.BeginDate + " по " + cntopr_buf.EndDate + " для договора " + prccontract.rec.Number + " по счету " + prccontract.rec.Account;
        Основание = Prc_AddGroundVO( Основание,  prccontract.rec.account, prccontract.rec.fiid, prccontract.rec.Client );
        
        if (not IsInstalledRKO())
            if (CarrySum < $0)
                CarrySum = CarrySum * Money(-1);
            end;
            
            if (DebetFIID != ContrFIID)
                ConvSum(DebetCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, DebetFIID );
            else
                DebetCarrySum  = CarrySum;
            end;
            if (CreditFIID != ContrFIID)
                ConvSum(CreditCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, CreditFIID );
            else
                CreditCarrySum = CarrySum; 
            end;
            
            stat = Prc_DocCarry( 
                Chapter,                  // Глава
                DebetFIID,                // Дебет Валюта
                CreditFIID,               // Кредит Валюта
                Debet,                    // Дебет
                Credit,                   // Кредит
                DebetCarrySum, 
                CreditCarrySum,           // сколько
                cntopr_buf.DocDate,    // За какое число
                "09",
                NULL,
                0,                         // CarryResult
                Основание,
                NULL,
                NULL,
                AccTrn
                );
            
            if (stat)
                msgbox("Ошибка отражения в учете");
                return 1;
            end;
            
            PrcSaveOperDoc(
                DocID, 
                1,
                Chapter,
                Debet, 
                DebetDep,
                DebetFIID, 
                money(DebetCarrySum),
                Credit,
                CreditDep,
                CreditFIID,
                money(CreditCarrySum),
                Основание);
                
            if(PrcSetAccTrnQuitLink(cntsched.rec.CntSchedID, 
                    money(CarrySum), 
                    money(DebetCarrySum), 
                    cntopr_buf.DocDate,
                    AccTrn.AccTrnID
            ))
                msgbox("Ошибка отражения в учете");
                return 1;
            end;
            
            if (PrcSetCntSchedFactSumByID(cntsched.rec.CntSchedID, cntsched.rec.SumCalc, {curdate}, cntsched.rec.DateReal) != 0)
                msgbox("Ошибка отражения в учете");
                return 1;
            end;
          
            if (PrcSetOperSum(DocID, CarrySum) != 0)
                msgbox("Ошибка отражения в учете");
                return 1;
            end;
        else // если работаем с РКО
            var PaymentDoc;
            GetAccountDepartment(Debet, Chapter,DebetFIID, @DebetDep);
            GetAccountDepartment(Credit,Chapter,CreditFIID,  @CreditDep);
        
            var flagPostDoc = false;
            if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_POSTDOC, @flagPostDoc) != 0)
                return 1;
            end;
            
            if ((DebetDep == CreditDep) and (cntopr_buf.isCreateMO == "X") and (not AlienAcc))
                Основание = "Отнесение на доходы оплаченных процентов при пересчете по пониженной процентной ставке " +
                "за период " + cntopr_buf.BeginDate + " по " + cntopr_buf.EndDate + " для договора " + prccontract.rec.Number + " по счету " + prccontract.rec.Account;
                Основание = Prc_AddGroundVO( Основание,  prccontract.rec.account, prccontract.rec.fiid, prccontract.rec.Client );
                DebetSettAcc = PayerSettAcc;
                
                if (CarrySum < $0)
                    CarrySum = CarrySum * Money(-1);
                end;
                
                if (DebetFIID != ContrFIID)
                    ConvSum(DebetCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, DebetFIID );
                else
                    DebetCarrySum  = CarrySum;
                end;
                if (CreditFIID != ContrFIID)
                    ConvSum(CreditCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, CreditFIID );
                else
                    CreditCarrySum = CarrySum; 
                end;
                
                Prc_CreateBankOrder( DebetFIID, 
                    CreditFIID, 
                    Debet, 
                    Credit, 
                    cntopr_buf.DocDate, 
                    CarrySum, 
                    prccontract.rec.FIID,
                    Основание,
                    cntsched.rec.CntSchedID,
                    cntopr_buf.DocID,
                    DebetSettAcc,
                    CreditSettAcc,
                    flagPostDoc);
                                  
                PrcSaveOperDoc(
                    DocID, 
                    1,
                    Chapter,
                    Debet, 
                    DebetDep,
                    DebetFIID, 
                    money(DebetCarrySum),
                    Credit,
                    CreditDep,
                    CreditFIID,
                    money(CreditCarrySum),
                    Основание);
                
                if (PrcSetPayState(cntsched.rec.CntSchedID, PRC_PAYSTATUS_NOT_PAID, cntsched.rec.PayState) != 0)
                    msgbox("Ошибка отражения в учете");
                    return 1;
                end;
            end;
        end;
    end;
    
    if (dSнач != $0.0)
        НазначениеПроводки = "Отнесение на доходы начисленных процентов за период  с  " + 
              cntopr_buf.BeginDate + " по " + cntopr_buf.EndDate +
              " для договора " +  prccontract.rec.Number + " по счету " + prccontract.rec.Account;
        НазначениеПроводки = Prc_AddGroundVO( НазначениеПроводки,  prccontract.rec.account, prccontract.rec.fiid, prccontract.rec.Client );
        
        DebetTypeID = 1;
        CreditTypeID = 7;
        GetAccountFromList(ContractID, DebetTypeID, @Debet, @DebetCatID, @DebetFIID, @Chapter);  
        GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);
        
        if ((Debet == "") and (DebetCatID == 0)) 
            msgbox("Не определен счет дебета и не задана соотв.КУ для договора");
            return 1;
        else
            if (Debet == "")
                ActuateAcc(DebetCatID, @Debet, @DebetFIID, @Chapter);
                if (Debet == "")
                    msgbox("Не удалось актуализировать счет дебета");
                    return 1;
                else
                    PrcSetCntAcc(ContractID, DebetTypeID, DebetFIID, Chapter, Debet);
                end;
            end;
       end;

       if ((Credit == "") and (CreditCatID == 0))
            msgbox("Не определен счет кредита и не задана соотв.КУ для договора");
            return 1;
       else
            if (Credit == "")
                ActuateAcc(CreditCatID, @Credit, @CreditFIID, @Chapter);
                if (Credit == "")
                    msgbox("Не удалось актуализировать счет кредита");
                    return 1;
                else
                    PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
                end;
            end;
       end;
                
        GetAccountDepartment(Debet, Chapter,DebetFIID, @DebetDep);
        GetAccountDepartment(Credit,Chapter,CreditFIID,  @CreditDep);
        
        CarrySum = dSнач;
        
        if (CarrySum < $0)
            CarrySum = CarrySum * Money(-1);
        end;
        
        if (DebetFIID != ContrFIID)
            ConvSum(DebetCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, DebetFIID );
        else
            DebetCarrySum  = CarrySum;
        end;
        
        if (CreditFIID != ContrFIID)
            ConvSum(CreditCarrySum, CarrySum, cntopr_buf.DocDate, ContrFIID, CreditFIID );
        else
            CreditCarrySum = CarrySum; 
        end;    
   
        if (DebetDep == 0)
            DebetDep = {OperDprt};
        end;
        if (CreditDep == 0)
            CreditDep = {OperDprt};
        end;
        
        stat = Prc_DocCarry( 
                              Chapter,               // Глава
                              DebetFIID,             // Дебет Валюта
                              CreditFIID,            // Кредит Валюта
                              Debet,                 // Дебет
                              Credit,                // Кредит
                              money(DebetCarrySum), 
                              money(CreditCarrySum), // сколько
                              cntopr_buf.DocDate, // За какое число
                              "09",
                              NULL,
                              0,                     // CarryResult
                              НазначениеПроводки,
                              NULL,
                              NULL,
                              AccTrn
                           );
        if (stat)
            msgbox("Ошибка отражения в учете");
            return 1;
         end; 

        PrcSaveOperDoc(
            DocID, 
            2,
            Chapter,
            Debet, 
            DebetDep,
            DebetFIID, 
            money(DebetCarrySum),
            Credit,
            CreditDep,
            CreditFIID,
            money(CreditCarrySum),
            НазначениеПроводки);
            
        if (PrcSetCntSchedFactSumByID(cntsched2.rec.CntSchedID, dSнач, {curdate}, cntsched2.rec.DateReal) != 0)
            msgbox("Ошибка отражения в учете");
            return 1;
        end;
    end;
    
    msgbox("Сформированные документы переданы в обработку");
    return 0;
END;