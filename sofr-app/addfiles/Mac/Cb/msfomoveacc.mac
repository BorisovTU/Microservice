/*
 $Name: msfomoveacc.mac
 $Module: Ядро ГКБО 
 $Description: МСФО-9 Процедура переноса и списания остатков
 */
 /* 
	МСФО-9 Процедура переноса и списания остатков
 */
import BankInter, PTInter, CTInter, FIInter, rsd, oralib, likepy, globals, keyac_r,oralib;

record bmsfoparmsrv(msfoparmsrv);
private const CHOICE_BUTTON_YES = 0; // "Да"
private const CHOICE_BUTTON_NO  = 1; // "Нет"

private const FldBlBalCorActAcc  = "внебалансовый счет корреспонденции с активными счетами";
private const FldBlBalCorPassAcc = "внебалансовый счет корреспонденции с пассивными счетами";
private const FldProfitAccount   = "cчет финансового результата - доход";
private const FldLossAccount     = "cчет финансового результата - расходов";

private const ErrOk               = 0; // не удалось открыть счет
private const ErrOpenAccount      = 1; // не удалось открыть счет
private const ErrMcAccdoc         = 2; // не удалось привязать счет к КУ
private const ErrCarry            = 3; // ошибка при проводке
private const ErrTargetClosed     = 4; // ошибка при проводке
private const ErrSrcTargetKind    = 5; // ошибка вид исходного счета не совпадает с видом целевого счета
private const ErrSrcChapter       = 6; // не корректна задана глава исходного счета
private const ErrPrcCntrNotFound  = 7; // не найден процентный договор, к которому привязан счет
private const ErrAccCategNotFound = 8; // не найден счет для категории
private const ErrTargetChapter    = 9; // не найден счет для категории
private const ErrPrcAccType       = 10; // не корректно задан вид лицевого счета по учету процентов

private const CрочБалансового           = 1; // Cрочных процентов для балансового учета
private const CрочПросрочБалансового    = 2; //Счет просроченных процентов процентов для балансового учета
private const CрочВнебалансового        = 5; // Счет срочных процентов для внебалансового учета
private const CрочПросрочВнебалансового = 6; // Счет просроченных процентов для внебалансового учета

private var ParmID = 0;
private var OurBik = "";

private macro YesNoWindow(msg)
    Array Text;
    Array Buttons;

    Text(0) = msg;
    Buttons(CHOICE_BUTTON_YES) = "Да"; 
    Buttons(CHOICE_BUTTON_NO)  = "Нет"; 

    return ConfWin(Text, Buttons);
end;

private macro CheckAccountFld(acc)
    var stat = false;

    if (strlen(acc) >= 20)
        stat = true;
    end;

    return stat;
end;

private macro InsertMSFOPARMSRV(rec)
    var stat = false;
    file msfoparmsrv(msfoparmsrv) write;
    Copy(msfoparmsrv, rec);
    msfoparmsrv.ParmID = 0;

    stat = Insert(msfoparmsrv);

    if (stat)
        Copy(rec, msfoparmsrv);
    end;

    return stat;
end;

private macro GetCopyAttributesMessage(attrname)
    var msg = "У \"целевого\" счета имеется дополнительная информация";
    if (ValType(attrname) != V_UNDEF)
        msg = msg + "(" + attrname + ")";
    end;
    msg = msg + ", аналогичные данные с \"исходного\" счета перенесены не были";

    return msg;
end;

private macro FillSourceAccounts()
    var cmd = RsdCommand("begin RSI_MSFO.FILLSOURCEACCOUNTS(?, ?, ?); end;");
    cmd.addParam("", RSDBP_IN, ParmID);
    cmd.addParam("", RSDBP_IN, {oper});
    cmd.addParam("", RSDBP_IN, {OperDprt});
    cmd.execute;
end;

private macro MsfoCarSrvLog(AccTrnID)
    var msfocarsrv = Tbfile ("msfocarsrv.dbt", "W", 0);
    msfocarsrv.rec.ParmID = ParmID;
    msfocarsrv.rec.AccTrnID = AccTrnID;
    return msfocarsrv.insert();
end;

private macro MsfoLogEx(ConvID, SourceDepartment, SourceAccount, TargetAccount, Chapter, FIID, Success, Rest, ErrorMSG, McAccDoc)
    var msfologsrv = Tbfile ("msfologsrv.dbt", "W", 0);
    msfologsrv.rec.ParmID = ParmID;
    msfologsrv.rec.ConvID = ConvID;
    msfologsrv.rec.Department = SourceDepartment;
    msfologsrv.rec.SourceAccount = SourceAccount;
    msfologsrv.rec.TargetAccount = TargetAccount;
    msfologsrv.rec.Chapter = Chapter;
    msfologsrv.rec.FIID = FIID;
    msfologsrv.rec.Success = Success;
    msfologsrv.rec.Rest = Rest;
    msfologsrv.rec.ErrorMSG = ErrorMSG;

    if (ValType(McAccDoc) != V_UNDEF)
        msfologsrv.rec.McAccDoc = McAccDoc;
    else
        msfologsrv.rec.McAccDoc = 0;
    end;

    return msfologsrv.insert();
end;

private macro MsfoLogRs(rs : @RsdRecordSet, Success, ErrorMSG, McAccDoc)
    return MsfoLogEx
    (
        rs.value("T_CONVID"),
        rs.value("T_SOURCEDEPARTMENT"),
        rs.value("T_SOURCEACCOUNT"),
        rs.value("T_TARGETACCOUNT"),
        rs.value("T_SOURCECHAPTER"),
        rs.value("T_SOURCEFIID"),
        Success,
        rs.value("T_SOURCEREST"),
        ErrorMSG,
        McAccDoc
    );
end;

private macro MsfoLogRsAccTrnIDs(rs : @RsdRecordSet, MSG, McAccDoc, AccTrnIDs, Sum)
    var Summa = Sum;

    if (ValType(Sum) == V_UNDEF)
        Summa = rs.value("T_SOURCEREST");
    end;

    MsfoLogEx
    (
        rs.value("T_CONVID"),
        rs.value("T_SOURCEDEPARTMENT"),
        rs.value("T_SOURCEACCOUNT"),
        rs.value("T_TARGETACCOUNT"),
        rs.value("T_SOURCECHAPTER"),
        rs.value("T_SOURCEFIID"),
        "X",
        Summa,
        MSG,
        0
    );

    var i = 0;
    while (i < AccTrnIDs.size)
        MsfoCarSrvLog(AccTrnIDs[i]);
        i = i + 1;
    end;
end;

class AccountProcess
    var rs : RsdRecordSet;
    var result = ErrOk;
    var OutMcAccdocID = 0, CatName = "";
    var Status_After = 0, ErrorMessage = "";
    var AccTrnIDs = TArray(false, 5, 2);
    var isCreated = false;
    var TargetsAccount = TArray(false, 5, 2);
    var Sum;

    private macro CheckMcAccDoc(TargetAccountID, TargetAccount, BankDate, OutMcAccdocID : @integer, CatName : @string)
        var stat = 0;
        ErrorMessage = "";

        var cmd = RsdCommand("begin ? := RSI_MSFO.CheckMcAccDoc(?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?); end;");
        cmd.NullConversion = true;
        cmd.addParam("retval", RSDBP_OUT, V_INTEGER);
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNT"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNTID"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCECHAPTER"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEFIID"));
        cmd.addParam("", RSDBP_IN, TargetAccount);
        cmd.addParam("", RSDBP_IN, TargetAccountID);
        cmd.addParam("", RSDBP_IN, rs.value("T_TARGETBALANCE"));
        cmd.addParam("", RSDBP_IN, rs.value("T_TARGETCHAPTER"));
        cmd.addParam("", RSDBP_IN, BankDate);
        cmd.addParam("OutMcAccdocID", RSDBP_OUT, V_INTEGER);
        cmd.addParam("CatName", RSDBP_OUT, V_STRING);
        cmd.execute;

        stat = cmd.value("retval");
        CatName = cmd.value("CatName");
        OutMcAccdocID = cmd.value("OutMcAccdocID");

        if (stat)
            ErrorMessage = MSFO_GetErrorString();
        end;

        return stat;
    end;

    private macro IsOpen(OpenClose : @string)
        var exists = false;
        var rsd = execSQLselect("SELECT T_ACCOUNT, T_OPEN_CLOSE FROM DACCOUNT_DBT WHERE T_ACCOUNT = ?", makeArray(SQLParam("", rs.value("t_TargetAccount"))));

        if (rsd.movenext())
            exists = true;
            OpenClose = rsd.value("T_OPEN_CLOSE");
        end;

        return exists;
    end;

    private macro CopyAccountNotes(TargetChapter, TargetCurrency, TargetAccount)
        var cmd = RsdCommand("begin ? := RSI_MSFO.CopyAccountNotes(?, ?, ?, ?, ?, ?); end;");
        cmd.NullConversion = true;
        cmd.addParam("retval", RSDBP_OUT, V_INTEGER);

        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCECHAPTER"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEFIID"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNT"));

        cmd.addParam("", RSDBP_IN, TargetChapter);
        cmd.addParam("", RSDBP_IN, TargetCurrency);
        cmd.addParam("", RSDBP_IN, TargetAccount);
        cmd.execute;

        if (cmd.value("retval") != 0)
            MsfoLogRs(rs, "", GetCopyAttributesMessage("примечания к объекту"), 0);
        end;
    end;

    private macro CopyAccountAttcor(TargetChapter, TargetCurrency, TargetAccount)
        var cmd = RsdCommand("begin ? := RSI_MSFO.CopyAccountAttcor(?, ?, ?, ?, ?, ?); end;");
        cmd.NullConversion = true;
        cmd.addParam("retval", RSDBP_OUT, V_INTEGER);

        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCECHAPTER"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEFIID"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNT"));

        cmd.addParam("", RSDBP_IN, TargetChapter);
        cmd.addParam("", RSDBP_IN, TargetCurrency);
        cmd.addParam("", RSDBP_IN, TargetAccount);
        cmd.execute;

        if (cmd.value("retval") != 0)
            MsfoLogRs(rs, "", GetCopyAttributesMessage("атрибуты объекта"), 0);
        end;
    end;

    private macro CopyAccountObjLinks(TargetChapter, TargetCurrency, TargetAccount)
        var cmd = RsdCommand("begin ? := RSI_MSFO.CopyAccountObjLinks(?, ?, ?, ?, ?, ?); end;");
        cmd.NullConversion = true;
        cmd.addParam("retval", RSDBP_OUT, V_INTEGER);

        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCECHAPTER"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEFIID"));
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNT"));

        cmd.addParam("", RSDBP_IN, TargetChapter);
        cmd.addParam("", RSDBP_IN, TargetCurrency);
        cmd.addParam("", RSDBP_IN, TargetAccount);
        cmd.execute;

        if (cmd.value("retval") != 0)
            MsfoLogRs(rs, "", GetCopyAttributesMessage("связанные объекты"), 0);
        end;
    end;

    private macro OpenAccount()
        record account ("account.dbt");
        record accblnc ("accblnc.dbt");

        clearrecord (account);
        clearrecord (accblnc);

        account.Chapter = Int(rs.value("T_TARGETCHAPTER"));
        account.Account  = rs.value("t_TargetAccount");
        account.Balance = rs.value("T_TARGETBALANCE");
        account.Kind_account = rs.value("t_TargetAccountKind");
        account.Oper = Int(rs.value("T_SOURCEOPER"));
        account.Client = rs.value("t_Client");
        account.NameAccount  = rs.value("t_NameAccount");
        account.Open_Date = {curdate};
        account.Department = rs.value("t_SourceDepartment");
        account.Branch = rs.value("t_SourceDepartment");
        account.Code_currency = rs.value("t_SourceFIID");

        account.type_account = rs.value("T_TYPE_ACCOUNT");
        account.type_account = StrSubst(account.type_account, "Ш", "");

        account.usertypeaccount = rs.value("T_USERTYPEACCOUNT");
        account.symbol = rs.value("T_SYMBOL");
        account.userfield1 = rs.value("T_USERFIELD1");
        account.userfield2 = rs.value("T_USERFIELD2");
        account.userfield3 = rs.value("T_USERFIELD3");
        account.userfield4 = rs.value("T_USERFIELD4");
        account.orscheme = rs.value("T_ORSCHEME");
        account.officeid = rs.value("T_OFFICEID");
        account.beneficiaryid = rs.value("T_BENEFICIARYID");

        accblnc.Chapter = account.chapter;
        accblnc.account = account.account;
        accblnc.Balance0 = account.balance;

        var stat = Create_Account(account, accblnc);
        if (stat)
            result = ErrOpenAccount; // не удалось открыть счет
        else
            isCreated = true;
            stat = CheckMcAccDoc(account.accountid, account.Account, bmsfoparmsrv.BankDate, @OutMcAccdocID, @CatName);

            if (stat)
                result = ErrMcAccdoc; // не удалось привязать счет к КУ
            else
                CopyAccountNotes(account.Chapter, account.Code_currency, account.Account);
                CopyAccountAttcor(account.Chapter, account.Code_currency, account.Account);
                CopyAccountObjLinks(account.Chapter, account.Code_currency, account.Account);
            end;
        end;
    end;

    private macro DoCarry(accTrn : @RsbAccTransaction, SumPayerReceiver)
        accTrn.TypeDocument = "М";
        accTrn.Date_Carry = {curdate};

        var stat = accTrn.Carry(Status_After, ErrorMessage);

        if (stat)
            AccTrnIDs[AccTrnIDs.size] = accTrn.AccTrnID;
            Sum = SumPayerReceiver;
        end;

        return stat;
    end;

    private macro RunCarry()
        var accTrn;

        if ( (rs.value("T_SOURCEREST") != $0) or (rs.value("T_DOCAMOUNT") != $0) )

          if (rs.value("T_TARGETCHAPTER") == 1)
            accTrn = RsbAccTransaction();
            accTrn.Chapter = rs.value("T_TARGETCHAPTER");
            accTrn.FIID = rs.value("T_SOURCEFIID");
            accTrn.Ground = "Перенос остатка при переходе на учет в соответствии с требованиями МСФО";
            accTrn.Department = bmsfoparmsrv.Department;

            if (rs.value("T_DOCAMOUNT") == $0)
                accTrn.Sum = Abs(rs.value("T_SOURCEREST"));
            else
                accTrn.Sum = Abs(rs.value("T_DOCAMOUNT"));
            end;

            if ((rs.value("T_SOURCEACCOUNTKIND") == "П") and (rs.value("T_TARGETACCOUNTKIND") == "П"))
                accTrn.AccountPayer = rs.value("T_SOURCEACCOUNT");
                accTrn.AccountReceiver = rs.value("T_TARGETACCOUNT");
            elif ((rs.value("T_SOURCEACCOUNTKIND") == "А") and (rs.value("T_TARGETACCOUNTKIND") == "А"))
                accTrn.AccountPayer = rs.value("T_TARGETACCOUNT");
                accTrn.AccountReceiver = rs.value("T_SOURCEACCOUNT");
            else
                result = ErrSrcTargetKind;
                ErrorMessage = "не совпадают типы счетов(исходный счет \""+ rs.value("T_SOURCEACCOUNTKIND") +"\", целевой счет \"" + rs.value("T_TARGETACCOUNTKIND") + "\")";
            end;

            if (result == ErrOk)
                if (not DoCarry(accTrn, accTrn.Sum))
                    result = ErrCarry;
                end;
            end;
        elif (rs.value("T_TARGETCHAPTER") == 3)
            accTrn = RsbAccTransaction();

            // необходимость использования внебалансовых счетов корреспонденции при переносе остатков по внебалансовым счетам
            if (bmsfoparmsrv.MoveRestObACC == "X") 
                var Sum1 = 0;
                var Sum2 = 0;
            
                var accTrn2 = RsbAccTransaction();

                accTrn.Chapter = rs.value("T_TARGETCHAPTER");
                accTrn.Ground = "Перенос остатка при переходе на учет в соответствии с требованиями МСФО";
                accTrn.Department = bmsfoparmsrv.Department;

                accTrn2.Chapter = rs.value("T_TARGETCHAPTER");
                accTrn2.Ground = "Перенос остатка при переходе на учет в соответствии с требованиями МСФО";
                accTrn2.Department = bmsfoparmsrv.Department;

                if ((rs.value("T_SOURCEACCOUNTKIND") == "П") and (rs.value("T_TARGETACCOUNTKIND") == "П"))

                  
                    accTrn.FIIDPayer = rs.value("T_SOURCEFIID");
                    accTrn.FIIDReceiver = NATCUR;

                    accTrn.AccountPayer = rs.value("T_SOURCEACCOUNT");
                    accTrn.AccountReceiver = bmsfoparmsrv.BlBalCorPassAcc; // Внебалансовый счет корреспонденции с пассивными счетами

                    accTrn2.FIIDPayer = NATCUR;
                    accTrn2.FIIDReceiver = rs.value("T_SOURCEFIID");

                    accTrn2.AccountPayer = bmsfoparmsrv.BlBalCorPassAcc;
                    accTrn2.AccountReceiver = rs.value("T_TARGETACCOUNT");


                    if (rs.value("T_DOCAMOUNT") == $0)
                        accTrn.SumPayer = Abs(rs.value("T_SOURCEREST"));

                        if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
                          accTrn.SumReceiver = accTrn.SumPayer;
                        else
                          accTrn.SumReceiver = 0;
                        end;

                        accTrn2.SumReceiver = Abs(rs.value("T_SOURCEREST"));
                        if(accTrn2.FIIDPayer == accTrn2.FIIDReceiver)
                          accTrn2.SumPayer = accTrn2.SumReceiver;
                        else
                          accTrn2.SumPayer = 0;
                        end; 
                    else
                        accTrn.SumPayer = Abs(rs.value("T_DOCAMOUNT"));

                        if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
                          accTrn.SumReceiver = accTrn.SumPayer;
                        else
                          accTrn.SumReceiver = 0;
                        end;

                        accTrn2.SumReceiver = Abs(rs.value("T_DOCAMOUNT"));
                        if(accTrn2.FIIDPayer == accTrn2.FIIDReceiver)
                          accTrn2.SumPayer = accTrn2.SumReceiver;                        
                        else
                          accTrn2.SumPayer = 0;
                        end;
                    end;  

                    Sum1 = accTrn.SumPayer;
                    Sum2 = accTrn2.SumReceiver;                    

                elif ((rs.value("T_SOURCEACCOUNTKIND") == "А") and (rs.value("T_TARGETACCOUNTKIND") == "А"))
                  
                    
                    accTrn.FIIDPayer = NATCUR;
                    accTrn.FIIDReceiver = rs.value("T_SOURCEFIID");    
                    
                    accTrn.AccountPayer = bmsfoparmsrv.BlBalCorActAcc;
                    accTrn.AccountReceiver = rs.value("T_SOURCEACCOUNT"); 
                    
                    accTrn2.FIIDPayer = rs.value("T_SOURCEFIID");
                    accTrn2.FIIDReceiver = NATCUR;                    

                    accTrn2.AccountPayer = rs.value("T_TARGETACCOUNT");
                    accTrn2.AccountReceiver = bmsfoparmsrv.BlBalCorActAcc;

                    if (rs.value("T_DOCAMOUNT") == $0)
                        accTrn.SumReceiver = Abs(rs.value("T_SOURCEREST"));
                        if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
                          accTrn.SumPayer = accTrn.SumReceiver;
                        else
                          accTrn.SumPayer = 0;
                        end;

                        accTrn2.SumPayer = Abs(rs.value("T_SOURCEREST"));
                        if(accTrn2.FIIDPayer == accTrn2.FIIDReceiver)
                          accTrn2.SumReceiver = accTrn2.SumPayer;
                        else
                          accTrn2.SumReceiver = 0;
                        end;
                    else
                        accTrn.SumReceiver = Abs(rs.value("T_DOCAMOUNT"));
                        if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
                          accTrn.SumPayer = accTrn.SumReceiver;
                        else
                          accTrn.SumPayer = 0;
                        end;

                        accTrn2.SumPayer = Abs(rs.value("T_DOCAMOUNT"));
                        if(accTrn2.FIIDPayer == accTrn2.FIIDReceiver)
                          accTrn2.SumReceiver = accTrn2.SumPayer;
                        else
                          accTrn2.SumReceiver = 0;
                        end;
                    end; 

                    Sum1 = accTrn.SumReceiver;
                    Sum2 = accTrn2.SumPayer; 
                else
                    result = ErrSrcTargetKind;
                    ErrorMessage = "не совпадают типы счетов(исходный счет \""+ rs.value("T_SOURCEACCOUNTKIND") +"\", целевой счет \"" + rs.value("T_TARGETACCOUNTKIND") + "\")";
                end;

                if (result == ErrOk)
                    if (not DoCarry(accTrn, Sum1))
                        result = ErrCarry;
                        ErrorMessage = "первая проводка(" + ErrorMessage + ")";
                    else
                        if (not DoCarry(accTrn2, Sum2))
                            result = ErrCarry;
                            ErrorMessage = "вторая проводка(" + ErrorMessage + ")";
                        end;
                    end;
                end;
            else
                accTrn.Chapter = rs.value("T_TARGETCHAPTER");
                accTrn.FIID = rs.value("T_SOURCEFIID");
                accTrn.Ground = "Перенос остатка при переходе на учет в соответствии с требованиями МСФО";
                accTrn.Department = bmsfoparmsrv.Department;

                if (rs.value("T_DOCAMOUNT") == $0)
                    accTrn.Sum = Abs(rs.value("T_SOURCEREST"));
                else
                    accTrn.Sum = Abs(rs.value("T_DOCAMOUNT"));
                end;

                if ((rs.value("T_SOURCEACCOUNTKIND") == "П") and (rs.value("T_TARGETACCOUNTKIND") == "П"))
                    accTrn.AccountPayer = rs.value("T_SOURCEACCOUNT");
                    accTrn.AccountReceiver = rs.value("T_TARGETACCOUNT");
                elif ((rs.value("T_SOURCEACCOUNTKIND") == "А") and (rs.value("T_TARGETACCOUNTKIND") == "А"))
                    accTrn.AccountPayer = rs.value("T_TARGETACCOUNT");
                    accTrn.AccountReceiver = rs.value("T_SOURCEACCOUNT");
                else
                    result = ErrSrcTargetKind;
                    ErrorMessage = "не совпадают типы счетов(исходный счет \""+ rs.value("T_SOURCEACCOUNTKIND") +"\", целевой счет \"" + rs.value("T_TARGETACCOUNTKIND") + "\")";
                end;

                if (result == ErrOk)
                    if (not DoCarry(accTrn, accTrn.Sum))
                        result = ErrCarry;
                    end;
                end;
            end;
          else
            result = ErrTargetChapter;
          end;
        end;
    end;

    macro OpenAccountTrn()
        var OpenClose = "";
        var exists = IsOpen(OpenClose);

        if (exists)
            if (OpenClose != "")
                result = ErrTargetClosed;
            else
                CopyAccountNotes(rs.value("T_TARGETCHAPTER"), rs.value("T_SOURCEFIID"), rs.value("t_TargetAccount"));
                CopyAccountAttcor(rs.value("T_TARGETCHAPTER"), rs.value("T_SOURCEFIID"), rs.value("t_TargetAccount"));
                CopyAccountObjLinks(rs.value("T_TARGETCHAPTER"), rs.value("T_SOURCEFIID"), rs.value("t_TargetAccount"));
            end;
        else
            OpenAccount();
        end;

        if (result == ErrOk)
            RunCarry();
        end;

        if (result)
            AbortTrn();
        end;
    end;

    private macro FindAccountForType(ContractID : integer, AccType : integer)
        var acc = "";
        var rs = execSQLselect("SELECT nvl(T.T_ACCOUNT,chr(1)) as account FROM DPRCCNTACC_DBT T WHERE  T.T_CONTRACTID  = ? AND T.T_ACCTYPEID = ?", 
            makeArray(SQLParam("", ContractID), SQLParam("", AccType)), FALSE);
        
        if (rs.movenext())
            acc = rs.value("account");
        end;

        return acc;
    end;

    private macro FindAccTypeForAccount(ContractID : integer, Account : string)
        var acc = "";
        var rs = execSQLselect("SELECT T.T_ACCTYPEID FROM DPRCCNTACC_DBT T WHERE  T.T_CONTRACTID  = ? AND T.T_ACCOUNT = ?", 
            makeArray(SQLParam("", ContractID), SQLParam("", Account)), FALSE);
        
        if (rs.movenext())
            acc = rs.value("T_ACCTYPEID");
        end;

        return acc;
    end;

    private macro DisRestPrcAcc()
        var ContractID = 0;
        var cmd = RsdCommand("SELECT T_CONTRACTID FROM DPRCCNTACC_DBT WHERE T_ACCOUNT = ?");
        cmd.addParam("", RSDBP_IN, rs.value("T_SOURCEACCOUNT"));

        var ContractRs = RsdRecordSet(cmd);
        if (ContractRs.movenext())
            ContractID = ContractRs.value("T_CONTRACTID");
        else
            result = ErrPrcCntrNotFound;
        end;

        if (not result)
            var cat916  = FindAccTypeForAccount(ContractID, rs.value("T_SOURCEACCOUNT"));
            var accTrn  = RsbAccTransaction();

            accTrn.Chapter = 1;
            accTrn.FIIDPayer = rs.value("T_SOURCEFIID");
            accTrn.FIIDReceiver = NATCUR;
            accTrn.Ground = "Cписание остатков счетов 916* при переходе на учет в соответствии с требованиями МСФО";
            accTrn.Department = bmsfoparmsrv.Department;
            accTrn.SumPayer = Abs(rs.value("T_SOURCEREST"));

            if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
              accTrn.SumReceiver = accTrn.SumPayer;
            else
              accTrn.SumReceiver = 0;
            end;


            if (cat916 == CрочВнебалансового) // Счет срочных процентов для внебалансового учета
                var СчетНачисленА = FindAccountForType(ContractID, CрочБалансового/*НачислА*/);

                if (СчетНачисленА == "")
                    result = ErrAccCategNotFound;
                    ErrorMessage = "Начисленные проценты (глава А)";
                end;
                accTrn.AccountPayer = СчетНачисленА; 
                accTrn.AccountReceiver = bmsfoparmsrv.ProfitAccount; // Счет финансового результата - доход
            elif (cat916 == CрочПросрочВнебалансового) // Счет просроченных процентов для внебалансового учета
                var СчетНачислАПр = FindAccountForType(ContractID, CрочПросрочБалансового);
                if (СчетНачислАПр == "")
                    result = ErrAccCategNotFound;
                    ErrorMessage = "Начисленные проценты (глава А)";
                end;
                accTrn.AccountPayer = СчетНачислАПр; 
                accTrn.AccountReceiver = bmsfoparmsrv.ProfitAccount; // Счет финансового результата - доход
            else
                result = ErrPrcAccType;
            end;

            if (result == ErrOk)
                if (not DoCarry(accTrn, accTrn.SumPayer))
                    result = ErrCarry;
                else
                    MsfoLogEx(rs.value("T_CONVID"), rs.value("T_SOURCEDEPARTMENT"), accTrn.AccountPayer, accTrn.AccountReceiver,
                        accTrn.Chapter, accTrn.FIIDPayer, "X", accTrn.SumPayer, "", 0);
                end;
            end;
        end;
    end;

    macro DisRest916Trn()
        
        if ( (rs.value("T_SOURCEREST")) != $0 )

          var accTrn = RsbAccTransaction();
          accTrn.Chapter = rs.value("T_SOURCECHAPTER");
        
          accTrn.FIIDPayer    = NATCUR;
          accTrn.FIIDReceiver = rs.value("T_SOURCEFIID");
        
          accTrn.Ground = "Cписание остатков счетов 916* при переходе на учет в соответствии с требованиями МСФО";
          accTrn.Department = bmsfoparmsrv.Department;

          accTrn.SumReceiver = Abs(rs.value("T_SOURCEREST")); 

          if(accTrn.FIIDPayer == accTrn.FIIDReceiver)
            accTrn.SumPayer = accTrn.SumReceiver;
          else
            accTrn.SumPayer = 0;
          end;

          if (rs.value("T_SOURCECHAPTER") == 3)
              accTrn.AccountPayer = bmsfoparmsrv.BlBalCorActAcc; // Внебалансовый счет корреспонденции с активными счетами
              accTrn.AccountReceiver = rs.value("T_SOURCEACCOUNT"); 
          else
              result = ErrSrcChapter;
          end;

          if (not DoCarry(accTrn, accTrn.SumReceiver))
              result = ErrCarry;
          else
              MsfoLogEx(rs.value("T_CONVID"), rs.value("T_SOURCEDEPARTMENT"), accTrn.AccountPayer, accTrn.AccountReceiver,
                  accTrn.Chapter, accTrn.FIIDReceiver, "X", accTrn.SumReceiver, "", 0);
          end;

          if (not result)
              DisRestPrcAcc();
          end;

          if (result)
              AbortTrn();
          end;

        end;
    end;

    macro TryClose()
        return CB_CloseAccount(rs.value("T_SOURCECHAPTER"), rs.value("T_SOURCEFIID"), rs.value("T_SOURCEACCOUNT"), {curdate}, ErrorMessage);
    end;
end;
// ===============================================================================================
private macro PrintProtocolSectionHeader(SectionName, err)
    if (ValType(err) == V_UNDEF)
[

############################################################
+-----------------------------+-----------------------------+-----------------------------+
| ########################### | ########################### | ########################### | 
+-----------------------------+-----------------------------+-----------------------------+
](SectionName:l,
  "Номер  лицевого счета по старому плану счетов":w:c, 
  "Номер  лицевого счета, на который выполнен перенос":w:c, 
  "Перенесенный входящий остаток":w:c);
    else
[

############################################################
+-----------------------------+-----------------------------------------------------------+
| ########################### | ######################################################### |
+-----------------------------+-----------------------------------------------------------+
](SectionName:l,
  "Номер  лицевого счета по старому плану счетов":w:c, 
  "Причина невыполнения переноса":w:c);
    end;
end;

private macro PrintProtocolSectionRow(SourceAccount, TargetAccount, Rest)
    if (ValType(Rest) != V_UNDEF)
[| ########################### | ########################### | ########################### | 
](SourceAccount, TargetAccount, Rest);
    else
[| ########################### | ######################################################### |
](SourceAccount, TargetAccount:w);
    end;
end;

private macro PrintProtocolSectionDelemitor(short)
    if (ValType(short) == V_UNDEF)
[+-----------------------------+-----------------------------+-----------------------------+];
    else
[+-----------------------------+-----------------------------------------------------------+];
    end;
end;

macro RunAccountRestMSFOProtocolBody()
    var rs, fFound = false;
[                                                  ВЕДОМОСТЬ 
                    ПЕРЕНОСА И СПИСАНИЯ ОСТАТКОВ ЛИЦЕВЫХ СЧЕТОВ ПРИ ПЕРЕХОДЕ НА НОВЫЙ ПЛАН СЧЕТОВ
                                                НА ##########

Филиал: ###############]
({curdate}, bmsfoparmsrv.Department:l);

    if (bmsfoparmsrv.MoveRestAcc == "X")
        PrintProtocolSectionHeader("ВЕДОМОСТЬ ПЕРЕНОСА ОСТАТКОВ");
        rs = execSQLselect("select t_SourceAccount, t_TargetAccount, t_Rest, t_Department from dmsfologsrv_dbt where t_parmid = ? and t_Success = CHR(88) AND t_ConvID <> 0 ORDER BY t_Department ASC, T_LOGID ASC", 
            makeArray(SQLParam("", bmsfoparmsrv.ParmID)), FALSE);

        var DepartmentSection = 0;
        while (rs.movenext)
            var Department = Int(rs.value("t_Department"));
            if (not fFound)
                DepartmentSection = Department;
            fFound = true;
            end;
            
            if (DepartmentSection != Department)
                PrintProtocolSectionDelemitor();
                DepartmentSection = Department;
            end;

            PrintProtocolSectionRow(rs.value("t_SourceAccount"), rs.value("t_TargetAccount"), rs.value("t_Rest"));
        end;

        if (fFound)
            PrintProtocolSectionDelemitor();
        end;
    end;

    fFound = false;
    if (bmsfoparmsrv.DisRest916 == "X")
        PrintProtocolSectionHeader("ВЕДОМОСТЬ СПИСАНИЯ ОСТАТКОВ");
        rs = execSQLselect("select t_SourceAccount, t_TargetAccount, t_Rest, t_Department from dmsfologsrv_dbt where t_parmid = ? and t_Success = CHR(88) AND t_ConvID = 0 AND t_TargetAccount <> CHR(1) ORDER BY t_Department ASC, T_LOGID ASC", 
                makeArray(SQLParam("", bmsfoparmsrv.ParmID)), FALSE);

        while (rs.movenext)
            Department = Int(rs.value("t_Department"));
            if (not fFound)
                DepartmentSection = Department;
            fFound = true;
            end;

            if (DepartmentSection != Department)
                PrintProtocolSectionDelemitor();
                DepartmentSection = Department;
            end;

            PrintProtocolSectionRow(rs.value("t_SourceAccount"), rs.value("t_TargetAccount"), rs.value("t_Rest"));
        end;

        if (fFound)
            PrintProtocolSectionDelemitor();
        end;
    end;

    fFound = false;
    rs = execSQLselect("select t_SourceAccount, t_ErrorMSG, t_Department from dmsfologsrv_dbt where t_parmid = ? and t_Success <> CHR(88) ORDER BY t_Department ASC, T_LOGID ASC", 
            makeArray(SQLParam("", bmsfoparmsrv.ParmID)), FALSE);

    while (rs.movenext())
        Department = Int(rs.value("t_Department"));
        if (not fFound)
            fFound = true;
            DepartmentSection = Department;
            PrintProtocolSectionHeader("ПРОТОКОЛ ОШИБОК", true);
        end;

        if (DepartmentSection != Department)
            PrintProtocolSectionDelemitor(true);
            DepartmentSection = Department;
        end;

        PrintProtocolSectionRow(rs.value("t_SourceAccount"), rs.value("t_ErrorMSG"));
    end;

    if (fFound)
        PrintProtocolSectionDelemitor(true);
    end;

    rs = execSQLselect("SELECT T.T_NAME, T.T_OPER FROM DPERSON_DBT T, DMSFOPARMSRV_DBT T2 WHERE T.T_OPER = T2.T_OPER AND T2.T_PARMID = ?", 
            makeArray(SQLParam("", bmsfoparmsrv.ParmID)));
    rs.movenext();
[
Дата: ##########       Ответственный исполнитель: #################################################
](bmsfoparmsrv.SysDate, String(rs.value("T_NAME")));
end;
// ===============================================================================================
macro RunMoveAccountRestMSFO (MSFOPARMSRV)

    var stat = 0;
    clearrecord (bmsfoparmsrv);
    SetBuff(bmsfoparmsrv, MSFOPARMSRV);

    if (bmsfoparmsrv.BankDate > {curdate})
        MsgBox ("Дата операционного дня меньше даты переноса, выполнение операции невозможно");
        stat = 1;
    end;

    if ((not stat) and (bmsfoparmsrv.MoveRestObACC == "X"))
        if (not CheckAccountFld(bmsfoparmsrv.BlBalCorActAcc))
            stat = 5059;
            CreateErrMsg(stat, FldBlBalCorActAcc);
        end;

        if ((not stat) and not CheckAccountFld(bmsfoparmsrv.BlBalCorPassAcc))
            stat = 5059;
            CreateErrMsg(stat, FldBlBalCorPassAcc);
        end;
    end;

    if ((not stat) and (bmsfoparmsrv.DisRest916 == "X"))
        if (not CheckAccountFld(bmsfoparmsrv.BlBalCorActAcc))
            stat = 5059;
            CreateErrMsg(stat, FldBlBalCorActAcc);
        end;

        if ((not stat) and not CheckAccountFld(bmsfoparmsrv.BlBalCorPassAcc))
            stat = 5059;
            CreateErrMsg(stat, FldBlBalCorPassAcc);
        end;

        if ((not stat) and not CheckAccountFld(bmsfoparmsrv.ProfitAccount))
            stat = 5059;
            CreateErrMsg(stat, FldProfitAccount);
        end;

        if ((not stat) and not CheckAccountFld(bmsfoparmsrv.LossAccount))
            stat = 5059;
            CreateErrMsg(stat, FldLossAccount);
        end;
    end;

    if (not stat)
        bmsfoparmsrv.SysDate = Date();
        bmsfoparmsrv.SysTime = Time();

        if (not InsertMSFOPARMSRV(bmsfoparmsrv))
            // Ошибка создания параметра выполнения процедуры переноса
            stat = 5058;
            CreateErrMsg(stat);
        else
            ParmID = bmsfoparmsrv.ParmID;
        end;
    end;

    var rs;
    if ((not stat) and (bmsfoparmsrv.MoveRestAcc == "X"))
        FillSourceAccounts();

        var cmd = RsdCommand("SELECT T.T_CONVID, "
            "        T3.T_DOCAMOUNT, "
            "        T3.T_CLOSE, "
            "        t.t_SourceAccountID, "
            "        t.t_SourceAccount, "
            "        T2.T_CLIENT, "
            "        T2.T_CHAPTER AS T_SOURCECHAPTER, "
            "        T2.T_KIND_ACCOUNT AS T_SOURCEACCOUNTKIND, "
            "        T2.T_CODE_CURRENCY AS T_SOURCEFIID, "
            "        T2.T_DEPARTMENT AS T_SOURCEDEPARTMENT, "
            "        T.T_SOURCEREST, "
            "        T.T_TARGETACCOUNT, "
            "        T.T_TARGETACCOUNTKIND, "
            "        T.T_CHAPTER AS T_TARGETCHAPTER, "
            "        T.T_BALANCE AS T_TARGETBALANCE, "
            "        T.T_NAMEACCOUNT, "
            "        T.T_SKIPCAUSE, "
            "        T2.T_TYPE_ACCOUNT, "
            "        T2.T_USERTYPEACCOUNT, "
            "        T2.T_SYMBOL, "
            "        T2.T_USERFIELD1, "
            "        T2.T_USERFIELD2, "
            "        T2.T_USERFIELD3, "
            "        T2.T_USERFIELD4, "
            "        T2.T_ORSCHEME, "
            "        T2.T_OFFICEID, "
            "        T2.T_BENEFICIARYID, "
            "        T2.T_OPER AS T_SOURCEOPER "
            "    FROM DMSFOSOURCEACC_TMP t, daccount_dbt t2, dmsfoaccsrv_dbt t3 "
            "WHERE T.T_SOURCEACCOUNTID = T2.T_ACCOUNTID AND T3.T_CONVID = T.T_CONVID AND T.T_SKIPCAUSE = RSI_MSFO.GetNoSkipFlag "
            "ORDER BY T2.T_DEPARTMENT ");
        cmd.NullConversion = true;
        rs = RsdRecordSet(cmd);
        while (rs.movenext())
            var acc = AccountProcess();
            acc.rs = rs;
            
            ProcessTrn(NULL, R2M(acc, "OpenAccountTrn"));

            if (acc.result == ErrOpenAccount)
                MsfoLogRs(rs, "", "Не удалось открыть счет " + rs.value("T_TARGETACCOUNT"), 0);
            elif (acc.result == ErrMcAccdoc)
                MsfoLogRs(rs, "", "Не удалось привязать счет  " + rs.value("T_TARGETACCOUNT") + " к КУ '" + acc.CatName + "': " + acc.ErrorMessage, 0);
            elif (acc.result == ErrCarry)
                MsfoLogRs(rs, "", "Не удалось выполнить перенос остатка при переходе на учет в соответствии с требованиями МСФО: " + acc.ErrorMessage, 0);
            elif (acc.result == ErrTargetClosed)
                MsfoLogRs(rs, "", "Перенос остатков не производился: целевой счет закрыт", 0);
            elif (acc.result == ErrSrcTargetKind)   
                MsfoLogRs(rs, "", "Перенос остатков не производился: " + acc.ErrorMessage, 0);
            elif (acc.result == ErrTargetChapter)
                MsfoLogRs(rs, "", "Перенос остатков не производился: некорректно задана либо указана глава целевого счета", 0);
            elif (acc.result == ErrPrcAccType)
                MsfoLogRs(rs, "", "Перенос остатков не производился: не корректно задан вид лицевого счета по учету процентов", 0);
            else
                var msg = "Перенос остатков выполнен";

                if (acc.isCreated)
                    msg = msg + ". Открыт счет " + rs.value("T_TARGETACCOUNT");

                    if (acc.OutMcAccdocID != 0)
                        msg = msg + ". Счет привязан к КУ '" + acc.CatName + "'";
                    end;
                end;
                MsfoLogRsAccTrnIDs(rs, msg, acc.OutMcAccdocID, acc.AccTrnIDs, acc.Sum);

                if (rs.value("T_CLOSE") == "X")
                    if (acc.TryClose())
                        MsfoLogRs(rs, "", "Не удалось закрыть счет " + rs.value("T_SOURCEACCOUNT") + ": " + acc.ErrorMessage, 0);
                    end;
                end;
            end;
        end;
    end;

    if ((not stat) and (bmsfoparmsrv.DisRest916 == "X"))
        var sql = "SELECT acc.T_ACCOUNTID, "
            "        ACC.T_ACCOUNT as T_SOURCEACCOUNT, "
            "        ACC.T_BALANCE, "
            "        ACC.T_CHAPTER as T_SOURCECHAPTER, "
            "        ACC.T_CODE_CURRENCY as T_SOURCEFIID, "
            "        ACC.T_DEPARTMENT, "
            "        ACC.T_KIND_ACCOUNT, "
            "        CHR(1) as T_TARGETACCOUNT, "
            "        0 as T_CONVID, "
            "        ACC.T_DEPARTMENT AS T_SOURCEDEPARTMENT,"
            "        NVL ( (SELECT T_REST "
            "                    FROM (  SELECT t2.T_REST, T2.T_ACCOUNTID "
            "                            FROM DRESTDATE_DBT t2, DACCOUNT_DBT T "
            "                             WHERE T2.T_ACCOUNTID = T.T_ACCOUNTID AND T2.T_RESTCURRENCY = T.T_CODE_CURRENCY "
            "                        ORDER BY T2.T_RESTDATE DESC) rst "
            "                WHERE rst.T_ACCOUNTID = ACC.T_ACCOUNTID AND ROWNUM = 1), "
            "                0) "
            "            AS T_SOURCEREST "
            "    FROM daccount_dbt acc "
            "    WHERE     SUBSTR (acc.T_balance, 0, 3) = '916' "
            "        AND (   acc.T_OPEN_CLOSE = CHR (0) "
            "                OR (    acc.T_CLOSE_DATE <> TO_DATE ('01/01/0001', 'dd/mm/yyyy') "
            "                    AND acc.T_CLOSE_DATE < TRIM (?))) ";
        
        if (bmsfoparmsrv.SubOrdinate == "X")
            sql = sql + "AND (acc.t_Department = " + {OperDprt} + " OR acc.t_Department IN(";
            sql = sql + "SELECT T_CODE FROM DDP_DEP_DBT START WITH T_PARENTCODE = " + {OperDprt} + " CONNECT BY PRIOR T_CODE = T_PARENTCODE))"
        else
            sql = sql + "AND acc.t_Department = " + {OperDprt};
        end;

        cmd = RsdCommand(sql);
        cmd.addParam("", RSDBP_IN, bmsfoparmsrv.BankDate);
        cmd.NullConversion = true;

        rs = RsdRecordSet(cmd);
        while (rs.movenext())
            acc = AccountProcess();
            acc.rs = rs;

            ProcessTrn(NULL, R2M(acc, "DisRest916Trn"));

            if (acc.result == ErrSrcChapter)
                MsfoLogRs(rs, "", "Не корректна задана глава исходного счета", 0);
            elif (acc.result == ErrPrcCntrNotFound)
                MsfoLogRs(rs, "", "Не найден процентный договор, к которому привязан счет " + rs.value("T_SOURCEACCOUNT"), 0);
            elif (acc.result == ErrAccCategNotFound)
                MsfoLogRs(rs, "", "Не найден счет для категории '" + acc.ErrorMessage + "'", 0);
            elif (acc.result == ErrCarry)
                MsfoLogRs(rs, "", "Не удалось выполнить списание остатков при переходе на учет в соответствии с требованиями МСФО: " + acc.ErrorMessage, 0);
            else
                msg = "Списание остатков выполнено";
                MsfoLogRsAccTrnIDs(rs, msg, acc.OutMcAccdocID, acc.AccTrnIDs, acc.Sum);

                if (acc.TryClose())
                    MsfoLogRs(rs, "", "Не удалось закрыть счет " + rs.value("T_SOURCEACCOUNT") + ": " + acc.ErrorMessage, 0);
                else
                    MsfoLogRs(rs, "X", "Закрыт счет " + rs.value("T_SOURCEACCOUNT"), 0);
                end;
            end;
        end;
    end;

    if (not stat)
        RunAccountRestMSFOProtocolBody();
    end;

    return stat;
end;

// ===============================================================================================
macro RunAccountRestMSFOProtocol(MSFOPARMSRV)
    clearrecord (bmsfoparmsrv);
    SetBuff(bmsfoparmsrv, MSFOPARMSRV);
    RunAccountRestMSFOProtocolBody();
end;