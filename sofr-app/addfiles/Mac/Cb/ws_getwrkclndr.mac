/*
 $Name: ws_getwrkclndr.mac
 $Module: Главная книга
 $Description: Сервис получения рабочего календаря
*/

import rsd;
import cb_sql;
import "ws_lib_fun.mac";
import Календарь;

class TWsGetWorkingCalendarRequest
  var dateTypeFilter:String;           // Строка, содержащая коды типов запрашиваемых дней календаря
  var dateRangeBegin:Date;	       // Первый день запрашиваемого диапазона дней календаря
  var dateRangeEnd:Date;               // Последний день запрашиваемого диапазона дней календаря
  var calendarID:Integer;              // Вид календаря (dcalkind_dbt.t_ID)
  var dprtCode:Integer;                // Системный ид. подразделения (ddp_dep_dbt.t_Code)
end;

private macro FillRequest (oRequest, wsRequest : TWsGetWorkingCalendarRequest)
  
  var ParamN     : integer;
  var ObjectName : string;

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue(@wsRequest.dateTypeFilter, ParamN, ObjectName, "dateTypeFilter", oRequest, V_STRING,  false, "");
  WS_SetAttributeValue(@wsRequest.dateRangeBegin, ParamN, ObjectName, "dateRangeBegin", oRequest, V_DATE,    true,  null);
  WS_SetAttributeValue(@wsRequest.dateRangeEnd,   ParamN, ObjectName, "dateRangeEnd",   oRequest, V_DATE,    true,  null);
  WS_SetAttributeValue(@wsRequest.calendarID,     ParamN, ObjectName, "calendarID",     oRequest, V_INTEGER, false, null);
  WS_SetAttributeValue(@wsRequest.dprtCode,       ParamN, ObjectName, "dprtCode",       oRequest, V_INTEGER, false, null);

end;

/* Сервис "Получить рабочий календарь" */
macro GetWorkingCalendar( Request )
     
  var wsRequest : TWsGetWorkingCalendarRequest;
  var result : TArray = TArray();
  var cmd, rs;
  var calendarID;
  var dateTypeFilter : String = "";

  WS_CheckParameter( 1, Request, true, V_GENOBJ );  
  wsRequest = TWsGetWorkingCalendarRequest;
  FillRequest( Request, wsRequest );
  
  if ( valtype (wsRequest.dateRangeBegin) != V_DATE )
    RunError("Не задано обязательное поле: Первый день запрашиваемого диапазона дней календаря");  
  end;
    
  if ( valtype (wsRequest.dateRangeEnd) != V_DATE )
    RunError("Не задано обязательное поле: Последний день запрашиваемого диапазона дней календаря");
  end;

  if ( valtype (wsRequest.calendarID) == V_INTEGER )
    cmd = RsdCommand( " select * from dcalkind_dbt " +     
                      " where " +
                      " t_ID = :ID " );
    rs = RsdRecordset( cmd );
    cmd.addParam( "ID", RSDBP_IN, wsRequest.calendarID );
    cmd.execute;
    if ( not rs.moveNext() )
      RunError("Вид календаря не найден", -1);  
    end;
    calendarID = wsRequest.calendarID;
  end;

  if ( valtype (wsRequest.dprtCode) == V_INTEGER )
    cmd = RsdCommand( " select * from ddp_dep_dbt " +     
                      " where " +
                      " t_Code = :Code " );
    rs = RsdRecordset( cmd );
    cmd.addParam( "Code", RSDBP_IN, wsRequest.dprtCode );
    cmd.execute;
    if ( not rs.moveNext() )
      RunError("Узел ТС не найден", -1);  
    end;
    cmd = RsdCommand("BEGIN ? := RSI_RsbCalendar.GetCalendar(?); END;");
    cmd.addParam( "calendarID", RSDBP_OUT, V_INTEGER );
    cmd.addParam( "dprtCode", RSDBP_IN, wsRequest.dprtCode );  
    SQL_Execute(cmd);
    calendarID = cmd.value("calendarID");
  end;

  if ( ( valtype (wsRequest.calendarID) != V_INTEGER ) and ( valtype (wsRequest.dprtCode) != V_INTEGER ) )
    calendarID = 0;
  end;  

  if ( valtype (wsRequest.dateTypeFilter) == V_STRING )
    dateTypeFilter = wsRequest.dateTypeFilter; 
  end;

  GetWorkingCalendarByID(dateTypeFilter, wsRequest.dateRangeBegin, wsRequest.dateRangeEnd, calendarID, result);

  if ( result.size == 0 ) 
    result = NULL;
  end;

  return result;
       
end;

