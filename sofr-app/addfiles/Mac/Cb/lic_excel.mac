/*
$Name:        lic_excel.mac
$Module:      Сервис ГКБО
$Description: Макрос выгрузки информации о расходе лицензий в Excel
*/
/*
   Выгрузка информации о расходе лицензий в Excel
*/

Import rslx, rcw, rsexts;

private var ExcelApplication : Object;

private var WorkBook : Object;

private var StartAX : Object;

private var prevDisplayAlerts : bool;

private var currRow : integer;

private var startPeriodRow : integer;

/*
 * Вывести информацию о расходе лицензии
 */
private macro PutLicenseUsingInfo( DateBeginPeriod : date, LicIndex : integer, DatePeriod : TArray, UsingValue : TArray )

  var i : integer;
  var DateCount : integer;
  
  var firstLicRow : integer;
  var lastLicRow  : integer;

  var Sheet : Object;

  Sheet = WorkBook.Sheets(1);

  DateCount = DatePeriod.size;

  firstLicRow = currRow + 1;

  i = 0;
  while( i < DateCount )
    
    currRow = currRow + 1;

    Sheet.Range(string("A", currRow)) = DateBeginPeriod;
    Sheet.Range(string("B", currRow)) = LicIndex + 1;
    Sheet.Range(string("C", currRow)) = DatePeriod[i];
    Sheet.Range(string("D", currRow)) = UsingValue[i];

    i = i + 1;

  end;

  lastLicRow = currRow - 1;

  if( firstLicRow <= lastLicRow )
    Sheet.Rows(string(firstLicRow, ":", lastLicRow)).Group;
  end;

end;

/*
 * Начало обработки контрольного периода
 */
private macro StartProcessControlPeriod( DatePeriod : date )

  startPeriodRow = currRow + 1;

end;

/*
 * Завершение обработки контрольного периода
 */
private macro FinishProcessControlPeriod( DatePeriod : date, LicID : TArray, LicName : TArray, LicType : TArray, LicLimit : TArray, LicUsing : TArray )

  var i : integer;
  var LicCount : integer;

  var Sheet : Object;

  WorkBook.Sheets("dd.mm.yyyy").Copy(WorkBook.Sheets(WorkBook.Sheets.Count));

  Sheet = WorkBook.Sheets(WorkBook.Sheets.Count - 1);

  Sheet.Name = string(DatePeriod:f);

  LicCount = LicID.size;

  i = 0;
  while( i < LicCount )
    i = i + 1;

    Sheet.Range(string("A", i)) = LicID   [i - 1];
    Sheet.Range(string("B", i)) = LicName [i - 1];
    Sheet.Range(string("C", i)) = LicType [i - 1];
    Sheet.Range(string("D", i)) = LicLimit[i - 1];
    Sheet.Range(string("E", i)) = LicUsing[i - 1];

  end;

  WorkBook.Sheets(1).Rows(string(startPeriodRow, ":", (currRow))).Group;

  currRow = currRow + 1;

end;


/*
 * Запуск Excel
 */
private macro StartLicInfoIntoExcel( DateBegin : date, DateEnd : date )

  var XLTPath : string;

  var FileName : string;
  
  ExcelApplication = null;

  StartAX = null;

  XLTPath = GetCurDir( IsStandAlone() == false );

  if( IsStandAlone() )
    ExcelApplication = ActiveX("Excel.Application", null, false);

    XLTPath = XLTPath + "\\..\\Templs\\Cb\\";
  else
    StartAX = CreateObject("rsax", "TRsAxServer", "LicAxServer", false);
     
    ExcelApplication = StartAX.CreateComObject("Excel.Application");

    XLTPath = XLTPath + "\\Dotfile\\";
  end;

  if( ExcelApplication != null )

    prevDisplayAlerts = ExcelApplication.DisplayAlerts;

    ExcelApplication.DisplayAlerts = false;

    XLTPath = XLTPath + "LicUsingStats.xlt";

    WorkBook = ExcelApplication.WorkBooks.Add( XLTPath );

    currRow = 0;

  end;

end;


/*
 * Запуск Excel
 */
private macro FinishLicInfoIntoExcel( IsAccess : bool )

  if( ExcelApplication != null )

    if( IsAccess == false )

      ExcelApplication.Quit;

    else

      WorkBook.Sheets("dd.mm.yyyy").Delete;

      ExcelApplication.DisplayAlerts = prevDisplayAlerts;

      WorkBook.Sheets(1).Activate;

      ExcelApplication.Visible = true;

    end;

  end;

end;
