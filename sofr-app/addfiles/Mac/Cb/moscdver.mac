/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : moscdver.mac
  Created     : 20.03.2003
  Programmer  : Андреева Е.П.
  Description : Печать отчета о групповой верификации

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, oralib, likepy;

RECORD mo_firstdoc (memorddu);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
   var stat, i = 1;
   array StringArray;

   var select:string;
   var params:TArray;
   var rs:object;
   var num;

        setbuff (mo_firstdoc,firstdoc);

        if (Вызов==Первый)
[            Отчет о групповой верификации документов бухгалтерии банка         ];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|дубликата|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Верификация проведена успешно";
                end;

                if (Статус == 531)
                        Статус    = 1834;
                        Сообщение = "Для дубликата найдено несколько оригиналов";
                end;

               
                select = "SELECT rm.t_Number "                       + 
                         "FROM dpmdupaym_dbt pm, dpmdurmpp_dbt rm " + 
                         "WHERE pm.t_DocKind    = :dockind "        +  
                         "  AND pm.t_DocumentID = :docid "          + 
                         "  AND pm.t_Purpose    = :purp "           + 
                         "  AND pm.t_SubPurpose = :subpurp "        +
                         "  AND pm.t_PaymentID  = rm.t_PaymentID; " ;

                params = makeArray( SQLParam("dockind",mo_firstdoc.DocKind ),
                                    SQLParam("docid",  mo_firstdoc.OrderID ),
                                    SQLParam("purp",   PM_PURP_BANKPAYMENT ),
                                    SQLParam("subpurp", 0) );
                rs = execSQLselect( select, params, FALSE );
                
                if( rs AND rs.moveNext() ) 
                   num = rs.value(0) ;
                end;

         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);
[|#########|######|############################################################|](num,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;
                
        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;