import globals;
import RsbDataSet;  
import CurrInter, XmlRpcInter;   
import oralib;
import serviceaction;
import rcw;
import dlwreps;
import rsd;


class TWsOprkoper()
	var KindOperation;
	var Name;
end;
           
/* Создание объекта TWsOprkoper через параметры */
macro CreateOprKindFromParam(KindOperation, Name)
  var OprKindItem : TWsOprkoper;

  OprKindItem.Name      		= Name;
  OprKindItem.KindOperation = KindOperation;

  return OprKindItem;
end;

/* Создание объекта TWsOprkoper с подкачкой Name из базы */
macro CreateOprKindFromAppServ(KindOperation)
  var OprKindItem : TWsOprkoper = TWsOprkoper();

  var cmd : RsdCommand, dataSet;

  cmd.cmdText = "select t_name from doprkoper_dbt WHERE t_kind_operation = ?";

  cmd.AddParam("", RSDBP_IN,  KindOperation);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    OprKindItem.Name          = dataSet.value(0);
    OprKindItem.KindOperation = KindOperation;
  end;

  return OprKindItem;
end;

macro GetListOprkoper(KindOperations, DocKind, InUseOnly) // Integer-Массив номеров видов операций                   
	var cmd : RsdCommand, outArray = TArray(), i = 0, arSize, tempObj = TWsOprkoper(), dataSet;
    
	cmd.cmdText = "select t_kind_operation, t_name from doprkoper_dbt WHERE ";

	if((KindOperations != NULL) OR (DocKind > 0))
	  if(DocKind > 0)
	  	cmd.cmdText = cmd.cmdText + "doprkoper_dbt.t_dockind = ?"; 
			cmd.AddParam("", RSDBP_IN,  DocKind);                           
		elif(KindOperations != NULL)
			arSize = KindOperations.size();

			while(i < (arSize - 1))
				cmd.cmdText = cmd.cmdText + "t_kind_operation = ?" + " OR ";
				cmd.AddParam("", RSDBP_IN,  KindOperations[i]);
		
				i = i + 1;
			end;
			
			cmd.cmdText = cmd.cmdText + "t_kind_operation = ?";
			cmd.AddParam("", RSDBP_IN, KindOperations[arSize - 1]);
		end;

    if ((InUseOnly != NULL) AND (InUseOnly == true))
      cmd.cmdText = cmd.cmdText + " AND t_notinuse <> 'X'";
    end;

		cmd.execute();

		dataSet = RsdRecordset(cmd);


		i = 0;

		while(dataSet.moveNext())
			tempObj = TWsOprkoper();
			tempObj.KindOperation 	= dataSet.value(0);
			tempObj.Name 						= dataSet.value(1);

			outArray[outArray.size] = tempObj;

			i = i + 1;
		end;
		
	else              
		var err = "Отсутствует обязательные входные параметры";
                                                                 
		RunError(err);	// ошибка
	end;
                                        
	return outArray;	
end;   

macro GetListQuery(GetInfo)
	var DocKind, KindOperations;

	var iprop = GenPropID(GetInfo, "DocKind");
	if(iprop != -1)
		DocKind = GenGetProp(GetInfo, "DocKind")
	end;

	iprop = GenPropID(GetInfo, "KindOperations");
	if(iprop != -1)
		KindOperations = GenGetProp(GetInfo, "KindOperations")
	end;


	if((DocKind == 0) and (KindOperations == NULL))
		return 1;
	else
		return GetListOprkoper(KindOperations, DocKind);
	end;
end;