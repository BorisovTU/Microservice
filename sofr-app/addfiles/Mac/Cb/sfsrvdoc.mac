/**                                                                                                             
 @file  sfsrvdoc.mac                                                                                           
 @brief Макрос скроллинга "Сервисные операции начисления и оплаты"
                                                                                                                
 #tag                                                                                                          
 - functional_block:Ядро ГКБО                
 - functional_block:ПЗО                                                                                                                                          
 - code_type:API 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.23 |Жиц М.В.       |DEF-60218           |Реализация проверки существования расчета при начислении комиссии "ИнвестСоветник"                                      
 |2024.01.17 |Жиц М.В.       |BIQ-14887           |Реализация проверок при начислении/списании комиссии "ИнвестСоветник"                                      
 |2022.03.08 |Дорохов А.Н.   |BIQ-8687            |Создание макроса                                                                                                                                                                                                                                                            
*/ 

import RSD, SFInter;

/* Буфер вводимой/редактируемой записи sfsrvdoc.dbt
 * структура записи 
 * ID:integer; 
 * ServKind:integer;      // Вид обслуживания, для ДО которого запускалась операция
 * DocKind:integer;       // Вид ПД сервисной операции
 * Kind:integer;          // Вид операции: 1 - Начисление 2 - Оплата
 * SfContrID:integer;     // Ссылка на ДО, по которому стартована сервисная операция
 * Status:integer;        // Возможные статусы сервисной операции: 1 - Открыта 2 - Закрыта.
 * DatePeriodEnd:date;    // Дата окончания периода сервисной операции.
 * DatePay:date;          // Дата выполнения проводок, формирования документов сервисной операции
 * CommNumber:integer;    // Номер комиссии
 * GroupID:integer;       // Группа комиссий, для которых запущена сервисная операция
 * AttrID:integer;        // 
 * Branch:integer;        // Узел ТС, для объектов которого запущена сервисная операция
 * Executer:integer;      // Операционист запустивший операцию
 * ExecutionDate:date;    // Опер. дата запуска операции
 * SysDate:date;          // Системная дата запуска операции
 * SysTime:time;          // Системное время запуска операции  
 * UnionContrID:integer;  // ссылка на СДО  
 * FeeType:integer;       // Тип взимания (SF_FEE_TYPE_PERIOD || SF_FEE_TYPE_LEVEL)
 * Oper:integer;          // Операционист, по счетам которого выполняется операция
 *
 */
private record SfSrvDocRec( sfsrvdoc );


const BrocerFixComis_Name = "БрокерФикс";
const InvestComComis_Name = "ИнвестСоветник";

/**
 @brief Получение номера комиссии по ее наименованию
 @param[in]  ComName  наименование комиссии
 @return номер комиссии
*/
private macro GetComis_Number(ComName):integer
  var sql =  "select t_number from dsfcomiss_dbt where t_code like (?)";
  var cmd = RSDCommand(sql);
  cmd.AddParam("", rsdbp_in, ComName);
  var rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
  if(rs.movenext)
    return rs.value("t_number");
  end;
  return 0;
end; 

/**
 @brief Аналог конструкции In в SQL
 @param[in]  val  сравниваемый параметр
 @return true, если переданный параметр содержится в списке
 @return false, если переданный параметр содержится в списке
*/
private macro InList(val):bool
  var parm, i = 1; 
  while(GetParm(i, parm))
    if(val == parm)
      return true;
    end;                             
    i = i + 1;
  end;
  return false;
end;

/**
 @brief Получение последнего календарного дня в месяце
 @param[in]  dt  обрабатываемая дата
 @return последний календарный день в месяце
*/
private macro LastDateInMonth(dt:date):date
  var day, month, year;
  DateSplit(dt, null, month, year);
  if (InList(month, 1, 3, 5, 7, 8, 10, 12))
    day = 31;
  elif (InList(month, 4, 6, 9, 11))
    day = 30;
  elif (month == 2)
    if (mod(year, 4) == 0) 
      day = 29;
    else
      day = 28;
    end;
  end;
  return date(day, month, year);
end;

/**
  @brief Проверка наличия рассчитанных базовых сумм за дату:
         если начисление осуществляется по конкретному ДО, то проверяем наличие актуальной базовой суммы именно по нему, а если по всем ДО, то ищем хоть что-то
  @param[in] CalcDate   дата расчета
  @param[in] CommNumber номер комиссии
  @param[in] SfContrID  идетификатор субдоговора, при запуске по всем ДО равен 0 и не учитывается
  @return true, если базовые суммы существуют
  @return false, если нет базовых сумм
*/
private macro CheckBaseSumByDate(CalcDate:date, CommNumber:integer, SfContrID:integer):bool
  var sql, cmd, rs; 

  sql = "select 1 " +       
        "  from ddl_basesum_dbt " +
        " where t_State = CHR(0) " +
        "   and t_ComNumber = ? " +    
        "   and t_FeeType = ? " +    
        "   and t_Date = ? ";    
  if(SfContrID > 0)
    sql = sql + " and t_DlContrID in (select mp.t_DlContrID " + 
                "                       from ddlcontrmp_dbt mp " +
                "                      where mp.t_SfContrID = ? " +
                "                    ) ";
  end;
  
  cmd = RSDCommand(sql);
  cmd.AddParam("", rsdbp_in, CommNumber);
  cmd.AddParam("", rsdbp_in, SF_FEE_TYPE_PERIOD);
  cmd.AddParam("", rsdbp_in, CalcDate);
  if(SfContrID > 0)
    cmd.AddParam("", rsdbp_in, SfContrID);
  end;
  
  rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
  if(rs.moveNext())
    return true;
  end;

  return false;
end;

/**
 @brief Функция проверки при сохранении панели ввода/редактирования
 Входные параметры - значения полей панели ввода/редактирования сервисной операции: 
 @param[in]  FIID:integer       идентификатор валюты
 @param[in]  bOnlyForeign:bool  признак "Только ин. валюта"
 @param[in]  Object:string      счет
 @return true, если проверка прошла успешно - панель сохраняем
 @return false, если проверка не прошла - остаемся в панели
*/
macro CheckSfSrvDoc(FIID, bOnlyForeign, Object):bool
  var BrokerFixComis_Number = GetComis_Number(BrocerFixComis_Name);
  var InvestcomComis_Number = GetComis_Number(InvestComComis_Name);

  if(SfSrvDocRec.DocKind == 3012)
    if(SfSrvDocRec.CommNumber == BrokerFixComis_Number)
      if(SfSrvDocRec.DatePeriodEnd != LastDateInMonth(SfSrvDocRec.DatePeriodEnd))
        msgbox("Для комиссии " + BrocerFixComis_Name + " дата окончания периода должна быть последним днем месяца расчетного периода");
        return false;
      end;
      if(SfSrvDocRec.ISCALCONEPERIOD != "X")
        msgbox("Для комиссии " + BrocerFixComis_Name + " установите признак \"Расчитывать за последний период\"");
        return false;
      end;
      if(SfSrvDocRec.Kind != 2)
//        msgbox("Для комиссии " + BrocerFixComis_Name + " укажите вид операции \"Оплата\"");
//        return false;
      end;
    end;

    /* Комиссия ИнвестСоветник*/
    if(SfSrvDocRec.CommNumber == InvestcomComis_Number)
      if(SfSrvDocRec.SfContrID == 0)
        if(SfSrvDocRec.DatePeriodEnd != LastDateInMonth(SfSrvDocRec.DatePeriodEnd))
          msgbox("Для комиссии " + InvestComComis_Name + ", заводимой по всем договорам, дата окончания периода должна быть последним днем месяца расчетного периода");
          return false;
        end;
      else
        if(SfSrvDocRec.DatePeriodEnd != LastDateInMonth(SfSrvDocRec.DatePeriodEnd))
          if(not GetTrue(true, "Для комиссии " + InvestComComis_Name + ", заводимой по одному договору, дата окончания периода должна быть последним днем месяца расчетного периода. Исключением является дата окончания услуги, сохранить операцию?"));
            return false;
          end;
        end;
      end;
      if(SfSrvDocRec.ISCALCONEPERIOD != "X")
        msgbox("Для комиссии " + InvestComComis_Name + " установите признак \"Рассчитывать за последний период\"");
        return false;
      end;
      if(SfSrvDocRec.Kind == 1) /*Начисление*/
        if(not CheckBaseSumByDate(SfSrvDocRec.DatePeriodEnd, SfSrvDocRec.CommNumber, SfSrvDocRec.SfContrID))
          if(not GetTrue(true, "За дату " + string(SfSrvDocRec.DatePeriodEnd:f) + " нет актуальных данных по расчету комиссии " + InvestComComis_Name + ". Вероятно, необходимо сначала выполнить расчет по эту дату. Сохранить операцию начисления?"));
            return false;
          end;
        end;
      end;
    end;
  end;

  return true;
end;