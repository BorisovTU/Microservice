Import BankInter, FMInter, PTInter, "globals.mac", "ws_lib_fun.mac";

class TWsFmPaym
  var DocumentID         ;     // string  Идентификатор документа во внешней системе      
  var DocNumber          ;     // string  Номер документа во внешней системе
  var DocDate            ;     // date    Дата документа
  var PayerID            ;     // integer Идентификатор субъекта-плательщика в RS-Bank.   
  var ReceiverID         ;     // integer Идентификатор субъекта-получателя в RS-Bank
  var ReceiverAccount    ;     // string  Номер счета получателя  
  var ReceiverName       ;     // string  Наименование получателя 
  var ReceiverBankID     ;     // integer Идентификатор субъекта-банка получателя в RS-Bank
  var ReceiverBankCode   ;     // string  БИК/SWIFT банка получателя             
  var ReceiverBankCorrAcc;     // string  Корсчет банка получателя                
  var ReceiverBankName   ;     // string  Наименование банка получателя           
  var ReceiverINN        ;     // string  ИНН получателя          
  var ReceiverKPP        ;     // string  КПП получателя          
  var CurrencyISONumber  ;     // string  Цифровой код ISO валюты платежа        
  var Sum                ;     // money   Сумма платежа в валюте CurrencyISONumber               
  var Ground             ;     // string  Назначение платежа             
end;



private macro CreateFmPaym /* Создание операции ФМ. */
(
  FmPaym : TWsFmPaym,
  FmPaymParm /*: TWsFmPaym*/     /* Параметры операции ФМ. */
)

  var ParamN      : integer;
  var ObjectName  : string;
 
  var FIID : integer; /* ИД фининструмента лицевого счета */

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue( @FmPaym.DocumentID         , ParamN, ObjectName, "DocumentID"         , FmPaymParm, V_STRING , true ,null, true  );
  WS_SetAttributeValue( @FmPaym.DocNumber          , ParamN, ObjectName, "DocNumber"          , FmPaymParm, V_STRING , false,null, true  );
  WS_SetAttributeValue( @FmPaym.DocDate            , ParamN, ObjectName, "DocDate"            , FmPaymParm, V_DATE   , false,null, true  );
  WS_SetAttributeValue( @FmPaym.PayerID            , ParamN, ObjectName, "PayerID"            , FmPaymParm, V_INTEGER, true ,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverID         , ParamN, ObjectName, "ReceiverID"         , FmPaymParm, V_INTEGER, false,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverAccount    , ParamN, ObjectName, "ReceiverAccount"    , FmPaymParm, V_STRING , false,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverName       , ParamN, ObjectName, "ReceiverName"       , FmPaymParm, V_STRING , false,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverBankID     , ParamN, ObjectName, "ReceiverBankID"     , FmPaymParm, V_INTEGER, false,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverBankCode   , ParamN, ObjectName, "ReceiverBankCode"   , FmPaymParm, V_STRING , false,null, true  );
  WS_SetAttributeValue( @FmPaym.ReceiverBankCorrAcc, ParamN, ObjectName, "ReceiverBankCorrAcc", FmPaymParm, V_STRING , false,null, true  );  
  WS_SetAttributeValue( @FmPaym.ReceiverBankName   , ParamN, ObjectName, "ReceiverBankName"   , FmPaymParm, V_STRING , false,null, true  );   
  WS_SetAttributeValue( @FmPaym.ReceiverINN        , ParamN, ObjectName, "ReceiverINN"        , FmPaymParm, V_STRING , false,null, true  );   
  WS_SetAttributeValue( @FmPaym.ReceiverKPP        , ParamN, ObjectName, "ReceiverKPP"        , FmPaymParm, V_STRING , false,null, true  );   
  WS_SetAttributeValue( @FmPaym.CurrencyISONumber  , ParamN, ObjectName, "CurrencyISONumber"  , FmPaymParm, V_STRING , true ,null, true  );   
  WS_SetAttributeValue( @FmPaym.Sum                , ParamN, ObjectName, "Sum"                , FmPaymParm, V_MONEY  , true ,null, true  );   
  WS_SetAttributeValue( @FmPaym.Ground             , ParamN, ObjectName, "Ground"             , FmPaymParm, V_STRING , false,null, true  );   
                                                                                                
end;

private macro CheckFmPaym(FmPaym: TWsFmPaym)
  record party_rec("party.dbt");
  record fi ( fininstr );

  if(FmPaym.PayerID > 0)
    if( ПолучитьСубъекта(FmPaym.PayerID, party_rec) != 0 )
      RunError( "Субъект экономики с указанным идентификатором плательщика не существует", -1 );
    end;
  end;

  if((FmPaym.ReceiverID != NULL) AND (FmPaym.ReceiverID > 0))
    if( ПолучитьСубъекта(FmPaym.ReceiverID, party_rec) != 0 )
      RunError( "Субъект экономики с указанным идентификатором получателя не существует", -1 );
    end;
  end;

  if(ПолучитьФинИн(FmPaym.CurrencyISONumber, fi, NULL, NULL, NULL, FICK_ISONUMBER) != 0)
    RunError( "Финансовый инструмент с указанным цифровым кодом не существует.", -1 );
  end;

end;


private macro FillFmOperation(FmOpr: RsbFMOperation, FmPaym: TWsFmPaym)
  record fi ( fininstr );
  var FIID = -1;
  if(ПолучитьФинИн(FmPaym.CurrencyISONumber, fi, NULL, NULL, NULL, FICK_ISONUMBER) == 0)
    FIID = fi.FIID;
  end;

  FmOpr.OperationType = FM_OPER_TYPE_LEGAL;
  FmOpr.DocKind       = 3100; // FM_OPERATION5_50; // $<ОПЕРАЦИЯ_ФМ_5_50>
  FmOpr.DocumentID    = FmPaym.DocumentID;
  FmOpr.FIID          = FIID;
  FmOpr.DateCarry     = {curdate};
  FmOpr.Sum           = FmPaym.Sum;

  if(FmPaym.Ground != NULL)
    FmOpr.Ground        = FmPaym.Ground;
  else
    FmOpr.Ground        = "";
  end;

  if(FmPaym.DocNumber != NULL)
    FmOpr.PaymDocNumber = FmPaym.DocNumber;
  end;

  if(FmPaym.DocDate != NULL)
    FmOpr.PaymDocDate = FmPaym.DocDate;
  end;

  var FMPayer = FmOpr.OprParty(_FM_PARTY_PAYER);
  FMPayer.PartyID = FmPaym.PayerID;

  var FMReceiver = fmopr.OprParty(_FM_PARTY_RECEIVER);

  if((FmPaym.ReceiverID != NULL) AND (FmPaym.ReceiverID > 0))
    FMReceiver.PartyID = FmPaym.ReceiverID;
  else
    if(FmPaym.ReceiverName       != NULL) FMReceiver.Name          = FmPaym.ReceiverName        ; end;
    if(FmPaym.ReceiverAccount    != NULL) FMReceiver.Account       = FmPaym.ReceiverAccount     ; end;
    if(FmPaym.ReceiverBankID     != NULL) FMReceiver.BankID        = FmPaym.ReceiverBankID      ; end;
    if(FmPaym.ReceiverBankCode   != NULL) FMReceiver.BankCode      = FmPaym.ReceiverBankCode    ; end;
    if(FmPaym.ReceiverBankName   != NULL) FMReceiver.BankName      = FmPaym.ReceiverBankName    ; end;
    if(FmPaym.ReceiverBankCorrAcc!= NULL) FMReceiver.CorrAccount   = FmPaym.ReceiverBankCorrAcc ; end;
    if(FmPaym.ReceiverINN        != NULL) FMReceiver.INN           = FmPaym.ReceiverINN         ; end;
  end;
end;


// Сервис проверки платежа по ФМ FM 
// FmPaymParm - Параметры платежа по ФМ - объект класса TWsFmPaym
// Возвращаемое значение
//       0 - Выполнение операции разрешено.
//       1 - Выполнение операции должно быть приостановлено.
//       2 - Выполнение операции запрещено.
//       3 - Сумма операция подпадает под условия ФМ.

macro FM_PaymCheck( FmPaymParm )

  var RetVal = 0;

  var FmPaym = TWsFmPaym();

  CreateFmPaym(FmPaym, FmPaymParm);

  CheckFmPaym(FmPaym);

  var FmOpr = RsbFMOperation();

  FillFmOperation(FmOpr, FmPaym);

  var stat = FmOpr.Check(true, false, false, false, true);

  if(stat == OPCONTR_CHECK_ERROR)
     RunError( RsbFMOprParty.ErrorMessage, RetVal );
  elif(stat == OPCONTR_CHECK_OK)
     RetVal = 0; // Выполнение операции разрешено.
  elif(stat == OPCONTR_CHECK_DEFER)
     RetVal = 1; // Выполнение операции должно быть приостановлено. 
  elif(stat == OPCONTR_CHECK_STOP)
     RetVal = 2; // Выполнение операции запрещено. 
  end;

  return RetVal;
end;