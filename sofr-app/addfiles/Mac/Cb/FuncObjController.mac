//FuncObjController.mac
import oralib, likepy;

class FuncObjController()
  private var defaultPriority:integer = 31;
  private var nullParam:string = "";

  private macro SaveTask(objID:integer, funcID:integer, param:string, priority:integer)
    var params = TArray();
    params(params.size) = SQLParam("objid",    objID);
    params(params.size) = SQLParam("funcid",   funcid);
    params(params.size) = SQLParam("param",    param);
    params(params.size) = SQLParam("priority", priority);

    var query = "begin "
              + "  funcobj_utils.save_task(p_objectid => :objid, "
              + "                          p_funcid   => :funcid, "
              + "                          p_param    => :param, "
              + "                          p_priority => :priority); "
              + "end;";
    execSql(query, params);
  end;

  private macro SaveTaskByCode(objID:integer, funcCode:string, param:string, priority:integer)
    var params = TArray();
    params(params.size) = SQLParam("objid",    objID);
    params(params.size) = SQLParam("funcCode", funcCode);
    params(params.size) = SQLParam("param",    param);
    params(params.size) = SQLParam("priority", priority);

    var query = "begin "
              + "  funcobj_utils.save_task(p_objectid => :objid, "
              + "                          p_funcid   => funcobj_utils.get_func_id(p_code => :funcCode), "
              + "                          p_param    => :param, "
              + "                          p_priority => :priority); "
              + "end;";
    execSql(query, params);
  end;

 private macro DeleteActiveTask(objID:integer, funcID:integer)
   var params = TArray ();
   params(params.size) = SQLParam("objid",    objID);
   params(params.size) = SQLParam("funcid",   funcid);

   var query = "begin "
              + "  funcobj_utils.delete_active_task(p_objectid => :objid, "
              + "                          p_funcid   => :funcid); "                        
              + "end;";

    execSql(query, params);
 end;


  macro Save(objID:integer, funcID:integer, _param:string, _priority:integer)
    var priority = _priority;
    if (priority == null)
      priority = defaultPriority;
    end;

    var param = _param;
    if (param == null)
      param = nullParam;
    end;


    SaveTask(objID, funcID, param, priority);
  end;

  macro getPriorityFromReserve(funcCode:string):integer
    var query = "select funcobj_utils.get_priority_from_reserve(p_code => :code) priority_value from dual";
    var sql = ExecSQLselectPrmDyn(query, funcCode);
    if (sql.MoveNext())
      return sql.value("priority_value");
    end;
    return defaultPriority;
  end;

  macro SaveByCode(objID:integer, funcCode:string, _param:string, _priority:integer)
    var priority = _priority;
    if (priority == null)
      priority = getPriorityFromReserve(funcCode);
    end;

    var param = _param;
    if (param == null)
      param = nullParam;
    end;

    SaveTaskByCode(objID, funcCode, param, priority);
  end;

  macro DeleteTask(objID:integer, funcID:integer)
    DeleteActiveTask(objID, funcID);
  end;

end;