/*
  $Name:         cbsttls.mac
  $Module:       Ядро Банкинг
  $Description:  Общие для всех макросов шагов функции
*/

/****************************************************************************/
/*           Общие для всех макросов шагов функции                          */
/*                                                                          */
/****************************************************************************/
import  InsCarryDoc, oralib, likepy, globals, "bnk_common.mac";
                         

/*Установка статусов для платежей*/
/*УстановитьСтатусыПлатежа(Статус1, Значение1, Статус2, Значение2, ..., СтатусN, ЗначениеN)*/
macro УстановитьСтатусыПлатежа()
  var i = 0, Статус:Integer, Значение:Integer, err = 0;
  while( (not err) and GetParm(i, Статус))
    if(GetParm(i + 1, Значение))
       if(not InsertOprStatus(Статус, Значение)) 
          err = 1;
       end;
    else
       return 1; /*Ошибка параметров*/
    end;
    i = i + 2;
  end;
  return err;
end;

/*
Производится проверка на разрешение контроля пользователю, выполняющему шаг. 
Для этого проверяется что пользователь не является одним из пользователей, 
которые осуществляли блок "Редактирование" для текущего платежа, 
а кроме того - пользователем, который создал платёж и пользователем, 
выполнившим предыдущий шаг. 
*/
macro КонтрольПлатежа(PaymObj, ID_Operation, ID_Step)
  var rs:object;
  var select:string;
  var params:TArray;

  if(PaymObj.Oper == {Oper})
      return false;
  end;
  select = "select oprstep.t_Oper "+
           "from doprstep_dbt oprstep "+
           "where oprstep.t_ID_Operation=:ID_Operation "+
           "AND oprstep.t_IsExecute='X' "+
           "AND oprstep.t_BlockID=29044 ";
  params = makeArray( SQLParam("ID_Operation", ID_Operation));
  rs = execSQLselect( select, params, FALSE );

  while(rs.moveNext())
     if ( rs.value(0) == {Oper} )
        return false;
     end;
  end;
  select = "select oprstep2.t_Oper "+
           "from doprstep_dbt oprstep1, doprstep_dbt oprstep2 "+
           "where oprstep1.t_ID_Operation=:ID_Operation "+
           "AND oprstep1.t_ID_Step=:ID_Step "+
           "AND oprstep1.t_Previous_Step=oprstep2.t_ID_Step "+
           "AND oprstep2.t_ID_Operation=:ID_Operation "+
           "AND oprstep2.t_IsExecute='X' ";
  params = makeArray( SQLParam("ID_Operation", ID_Operation), 
                      SQLParam("ID_Step", ID_Step) );

  rs = execSQLselect( select, params, FALSE );

  if(rs.moveNext())
     if ( rs.value(0) == {Oper} )
        return false;
     end;
  end;

  return true;
end;

//Есть ли связанный документ, изменяющий претензию у операции?
macro IsClaimByOperStep(ID_Operation)

  var params:TArray;
  var rs:object;
  var select = "SELECT 1 "+
               "FROM dual "+
               "WHERE EXISTS( "+
                  "SELECT 1 "+
                  "FROM doprdocs_dbt opd "+
                  "WHERE opd.t_id_operation = :ID_Operation "+
                  "AND opd.t_dockind = 976) "/*DOCKIND_ACCLMCNG*/;
  params = makeArray( SQLParam("ID_Operation", ID_Operation));
  rs = execSQLselect( select, params, FALSE );
  if(rs and rs.moveNext())
    return true;  
  end;
  return false;
end;

macro NeedGenPmCarryNotify(PaymentID : integer, RetPaymentID : @integer) : bool
  var SP_Name : string = Bnk_GetRegistryValue
    ( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\УВЕДОМЛЕНИЯ\\УВЕДОМЛЕНИЯ_ХП",
      V_STRING,
      "WLD_UFEBS.NeedGenPmCarryNotify"
    );

  var params : TArray = makeArray( SQLParam("p_PaymentID", PaymentID, RSDBP_IN_OUT) );

  var NeedNotify : integer = execStoredFunc( SP_Name, 
                                             V_INTEGER, 
                                             params );
  RetPaymentID = params.value(0).value;

  return (NeedNotify == 1);
end;
