//-----------------------------------------------------------------------------
// Блок     : Вне блока
// Шаг      : Вне шага
// Описание : Печатная форма отказа от акцепта требования
//-----------------------------------------------------------------------------

import PaymInter, PTInter, globals ;
import oralib, likepy;

var pr_pmdenial:TRecHandler = TRecHandler( "pmdenial.dbt", "bank.def" );

// Получить код субъекта
PRIVATE MACRO GetPartyCode( PartyID:integer, CodeKind:integer, Code:string):bool
  var select:string = "select pc.t_Code " +
                      "from dpartcode_dbt pc " +
                      "where pc.t_PartyID=:PartyID " + 
                      "  and pc.t_CodeKind=:CodeKind";
  var params:TArray = TArray();
  params[params.size] = SQLParam( "PartyID" , PartyID );
  params[params.size] = SQLParam( "CodeKind" , CodeKind );
  var rset:object = execSQLselect( select, params, false );
  if( rset AND rset.moveNext() )
    Code = rset.Value(0);
    SetParm( 2, Code );
    return true;
  end;
  return false;
END;


MACRO PrintDocument(ncopy:integer):bool

  var DenialKind      :string = "";
  var PayerName       :string = "";
  var PayerAccount    :string = "";
  var PayerAccountTop :string = "сч. ";
  var ReceiverName    :string = "";
  var ReceiverAccount :string = "";
  var ReceiverBankName:string = "";
  var CorrAccount     :string = "";
  var BIC             :string = "";
  var DocNumber       :string = "";
  var DocDate   :date = {curdate};
  var AcceptDate:date = {curdate};
  var Payer          :integer = 0;
  var ReceiverBankID :integer = 0;
  var PaymAmount   = 0;
  var AcceptAmount = 0;

  var PaymAmountStr   :string = "";
  var DenAmountStr    :string = "";
  var AcceptAmountStr :string = "";
  array PaymAmountArr;
  array DenAmountArr;
  array AcceptAmountArr;

  var BodyStr:string = "";
  var Str1   :string = "";
  var Str2   :string = "";
  var Str3   :string = "";
  var Str4   :string = "";
  var Str5   :string = "";
  var Str6   :string = "";
  var Str7   :string = "";

  var select:string;
  var params:TArray;
  var rs:object;

  // Ищем платеж
  select = " select pm.t_PayerAccount, pm.t_Payer, pm.t_ReceiverAccount, pm.t_ReceiverBankID, "
           "        rm.t_Number, rm.t_Date, rm.t_ReceiverName, rm.t_ReceiverBankName, rm.t_ReceiverCorrAccNostro, " +
           "        dem.t_ReqSum, dem.t_AcceptDate " +
           " from DPMPAYM_DBT pm, DPMRMPROP_DBT rm, DPSPAYDEM_DBT dem " +
           " where pm.T_PAYMENTID  = :PaymentID     " + 
           "   and rm.T_PAYMENTID  = pm.T_PAYMENTID " +
           "   and dem.T_ORDERID   = pm.T_PAYMENTID ";

  params = makeArray( SQLParam("PaymentID", pr_pmdenial.rec.PaymentID) );
  rs     = execSQLselect( select, params, FALSE );
    
  if( rs AND rs.moveNext() ) 
    PayerAccount     = rs.value(0);
    Payer            = rs.value(1);
    ReceiverAccount  = rs.value(2);
    ReceiverBankID   = rs.value(3);
    DocNumber        = rs.value(4);
    DocDate          = rs.value(5);
    ReceiverName     = rs.value(6);
    ReceiverBankName = rs.value(7);
    CorrAccount      = rs.value(8);
    PaymAmount       = rs.value(9);
    AcceptDate       = rs.value(10);
    if( strlen(CorrAccount) < 2 )
      CorrAccount = {CORAC_Bank};
      BIC         = {MFO_Bank};
    end;
    if( strlen(BIC) == 0 )
      GetPartyCode( ReceiverBankID, PTCK_BIC, BIC );
    end;
  else
    msgbox("Не найден платеж");
    return false;
  end;

  // Ищем название плательщика
  select = " select party.t_Name      " +
           " from   DPARTY_DBT party  " +
           " where  party.t_PartyID = :PartyID ";

  params = makeArray( SQLParam("PartyID", Payer) );
  rs     = execSQLselect( select, params, FALSE );

  if( rs AND rs.moveNext() ) 
    PayerName = rs.value(0);
  else
    msgbox("Не найден плательщик");
    return false;
  end;

  AcceptAmount = PaymAmount - pr_pmdenial.rec.Amount;

  if( AcceptAmount > 0 )
    DenialKind = "частично";
  else
    DenialKind = "полностью";
  end;

  // Приводим к нужному виду

  PayerAccountTop = PayerAccountTop + PayerAccount;

  strsplit( RubToStrAlt(PaymAmount), PaymAmountArr, 100, 100, 1 );
  PaymAmountStr = PaymAmount + " (" + PaymAmountArr(0) + ")";

  strsplit( RubToStrAlt(pr_pmdenial.rec.Amount), DenAmountArr, 100, 100, 1 );
  DenAmountStr = pr_pmdenial.rec.Amount + " (" + DenAmountArr(0) + ")";

  strsplit( RubToStrAlt(AcceptAmount), AcceptAmountArr, 100, 100, 1 );
  AcceptAmountStr = AcceptAmount + " (" + AcceptAmountArr(0) + ")";
  
  Str1 = string("Заявляем об отказе от акцепта платежного требования N ", DocNumber, " от ", DocDate:m:c, " на сумму ", StrSubst( PaymAmountStr, ".", "-"), ".");
  Str2 = string("Получатель ", ReceiverName );
  Str3 = string("Счет N ", ReceiverAccount, " в банке ", ReceiverBankName );
  Str4 = string("Корреспондентский счет банка N ", CorrAccount, " БИК ", BIC );
  Str5 = string("Акцептовано в сумме ", StrSubst( AcceptAmountStr, ".", "-") );
  Str6 = string("Отказ от акцепта (", DenialKind, ") в сумме ", StrSubst( DenAmountStr, ".", "-") );
  Str7 = string("Мотив отказа (пункт, N, дата договора) ", pr_pmdenial.rec.Ground );

  while( ncopy != 0 )
[
                                                                ┌─────────┐
                                                                │ 0401004 │
                                                                └─────────┘

 ###############################################################################
 ######################################         ################################
 ──────────────────────────────────────         ────────────────────────────────

 Заявление об отказе от акцепта 
 ######################################

 ###############################################################################

 Оконч. срока акцепта ###################################
 ###############################################################################
 ###############################################################################
 ###############################################################################
 ###############################################################################
 ###############################################################################

 ###############################################################################

                          Подписи плательщика                Отметки банка


 М.П.

]
( PayerName:w,
  PayerAccountTop, {Name_Bank}:w,
  pr_pmdenial.rec.Date:m,
  Str1:w,
  AcceptDate:m,
  Str2:w,
  Str3:w,
  Str4:w,
  Str5:w,
  Str6:w,
  Str7:w
);

  ncopy = ncopy - 1;
  end;

  return true;

END;
