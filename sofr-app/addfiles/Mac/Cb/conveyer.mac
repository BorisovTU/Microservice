/*
$Name:           conveyer.mac
$Module:         Ядро ГКБО
$Description:    Дистрибутивные функции конвейера
*/

import globals, RsbMPExecutor, FuncObjExecutor, XmlRpcInter, RSD, BankInter;
import "bnk_dttmfrmt.mac";
import "conveyerfun.mac";

var operationid_Array   : TArray = TArray;
var rslmpexecutor_Array : TArray = TArray;
var usePriority = IsUsePriority();

private macro NeedRunFuncObj(taskID)
  var NeedFuncObj = true;
  var Regval = "";
  var error = 0;

  if(AL_RunningWine() == true)
    GetRegistryValue( "COMMON\\FUNCOBJ\\USE_FUNCOBJ_TASK", V_STRING, RegVal, error );
      
    if( error != 0)
      RegVal = "";
    end;

    if(Regval == "")
      NeedFuncObj = true;
    else
      NeedFuncObj = false;

      var i = 1;
      var NumStr= "";
      while(i<=strlen(RegVal))
         var ch = substr(RegVal, i, 1);
         if((ch == ",") OR (i == strlen(RegVal)))
            if(i == strlen(RegVal))
              NumStr = NumStr + ch;
            end;

            if( Trim(NumStr) == string(taskID))
              NeedFuncObj = true;
            end;
            NumStr = "";
         else
           NumStr = NumStr + ch;
         end;

         i = i + 1; 
      end;
    end;
  else
    NeedFuncObj = false;
  end;

  return NeedFuncObj;
end;

macro Start_Operation( ConvTypeID, OperationID, Threads, Service, Macro_, Proc, taskID, execID, PackID, Params )


  var CurPos = 0;
  
  var Flag = false;
  
  var i = 0;
  
  while( i < operationid_Array.Size )
    
    if( operationid_Array[ i ] == OperationID )
    
      CurPos = i;
      Flag   = true;
      
      break;
      
    end;
    
    i = i + 1;

  end;
  
  var NeedFuncObj = NeedRunFuncObj( taskID );

  if( Flag != true )
  
    CurPos = operationid_Array.Size;
    
    operationid_Array[ CurPos ]   = OperationID;
    if(NeedFuncObj)
      rslmpexecutor_Array[ CurPos ] = RslFuncObjExecutor( Service, Threads ); 
    else
      rslmpexecutor_Array[ CurPos ] = RslMPExecutor( Service, Threads );
    end;

    rslmpexecutor_Array[ CurPos ].Run();
    
  end;
  
  var Start_str = Index(Params, "<param>");
  var End_str   = Index(Params, "</params>");
  
  Params = SubStr( Params, Start_str, End_str - Start_str );


  var guid = string(execID)+ "_"+ string(ConvTypeID) + "_" + string(PackID) + "_conv_"+ CreateGUID();
  var curdateStr = YYYY_MM_DD({curdate}) + "Z";
            
  var request =
  "<?xml version='1.0' encoding='windows-1251'?>\n" +
  "<methodCall xmlns=\"http://www.softlab.ru/xml-rpc/schema\"\n" +
  "            xmlns:r=\"http://www.softlab.ru/xml-rpc/schema\"\n" +
  "            r:sender=\"2\"\n" +
  "            r:oper=\""  + {oper} + "\" \n" +
  "            r:curdate=\""  + curdateStr + "\" \n" ;

if(NeedFuncObj)
  request = request + "            r:internal=\"i_SecuredRq\"\n";
end;

  request = request + "            r:reqId=\"" + guid   + "\" >\n" +
  "  <methodName>RunMacro." + StrSubst(Macro_, ".mac", "")+ "." + Proc + "</methodName>\n" +
  "  <params>\n" +
  "    <param><value><integer>" + taskID + "</integer></value></param>\n" +
  "    <param><value><integer>" + execID + "</integer></value></param>\n" +
  "    <param><value><integer>" + ConvTypeID + "</integer></value></param>\n" +
  "    <param><value><integer>" + OperationID + "</integer></value></param>\n" +
  "    <param><value><integer>" + PackID + "</integer></value></param>\n" + 
  "    " + Params + "\n" +
  "  </params>\n" +
  "</methodCall>";
  
  if(NeedFuncObj)
    rslmpexecutor_Array[ CurPos ].Add(request, NULL, NULL, taskID, execID, convTypeID, operationID, packID );
  else
    if (not usePriority)
      rslmpexecutor_Array[ CurPos ].Add(request);
    else
      rslmpexecutor_Array[ CurPos ].AddRequestWithConvPriority(request, 0, 0, GetRequestConvPriority(taskID, execID, PackID));
    end;
  end;
  
  return 0;
  OnError(err)
    ConvAddDBLog(GetFullErrorMessage(err));
    return 20390; 
end;

private macro ParcReqID( reqStr:string, execID : @integer, ConvTypeID : @integer, PackID : @integer, Error : @integer, ErrMsg : @string )


  var ind = Index ( reqStr, "_conv_" );
  var StrAll = "";
  var StrNum = "";

  if(ind > 0)
    StrAll = substr(reqStr, 1, ind);

    ind = Index ( StrAll, "_" );
    StrNum = substr(StrAll, 1, ind-1); 
    execID = StrNum;


    StrAll = substr(StrAll, ind+1); 
    ind = Index ( StrAll, "_" );
    StrNum = substr(StrAll, 1, ind-1); 
    ConvTypeID = StrNum;

    StrAll = substr(StrAll, ind+1); 
    ind = Index ( StrAll, "_" );
    StrNum = substr(StrAll, 1, ind-1); 
    PackID = StrNum;

    ind = Index ( reqStr, "_conv_" );
    StrAll = substr(reqStr, ind + 6);
    ind = Index ( StrAll, "_" );
    if(ind > 0)
      StrNum = substr(StrAll, ind + 1); 
      ind = Index ( StrNum, "#" );
      if(ind > 0)
        StrAll = substr(StrNum, ind + 1);
        ind = Index ( StrAll, "#" );
        StrNum = substr(StrAll, 1, ind - 1);
        if((StrNum != "") and (StrNum != "0"))
          if((0 < int(StrNum)) and (int(StrNum) < 32767))
            Error = StrNum;
          end;
        end;
        StrNum = substr(StrAll, ind + 1);
        ErrMsg = StrNum;
      else
        ErrMsg = StrNum;
      end;
    end;
  end;
end;


macro SaveErrors()
  var ArrErr;
  var ExecID : integer, ConvTypeID : integer, PackID : integer;
  var Error : integer;
  var ErrMsg : string;
  var size = rslmpexecutor_Array.Size;
  var sqlString, cmd;

  var i = 0;
  var j = 0;

  while(i < size)
    var CurMPExecutor = rslmpexecutor_Array[i];

    if(IsEqClass("RslFuncObjExecutor", CurMPExecutor))
       CurMPExecutor.SetCnvPackError();
    else
      ArrErr = CurMPExecutor.GetFaultTasks(true);
      j = 0;
      while( j < ArrErr.size)
        Error = 20385;
        ParcReqID( ArrErr(j), @ExecID, @ConvTypeID, @PackID, @Error, @ErrMsg );

        if((ExecID > 0) AND (ConvTypeID > 0) AND (PackID > 0))
          sqlString = "UPDATE dcnvpack_dbt SET t_Error = ?, t_ErrMsg = ? WHERE t_ExecID = ? AND t_ConvTypeID = ? AND t_PackID = ?";

          cmd = RSDCommand( sqlString );
          cmd.addParam( "", RSDBP_IN, Error );
          cmd.addParam( "", RSDBP_IN, ErrMsg );
          cmd.addParam( "", RSDBP_IN, ExecID );
          cmd.addParam( "", RSDBP_IN, ConvTypeID );
          cmd.addParam( "", RSDBP_IN, PackID );

          cmd.Execute();
        end;

        j = j + 1;
      end;
    end;

    i = i + 1;
  end;

  return 0;
end;

macro GetCountExecuteSuccess(OperationID)
  var Count = 0;
  var i = 0;

  while(i < rslmpexecutor_Array.Size)
    if(OperationID > 0)
      if(operationid_Array[i] == OperationID)
        Count = rslmpexecutor_Array[i].GetSuccessTaskCount();
        break;
      end;
    else
      Count = Count + rslmpexecutor_Array[i].GetSuccessTaskCount();
    end;
    i = i + 1;
  end;

  return Count;
end;

macro GetCountExecuteError(OperationID)
  var Count = 0;
  var i = 0;

  while(i < rslmpexecutor_Array.Size)
    if(OperationID > 0)
      if(operationid_Array[i] == OperationID)
        Count = rslmpexecutor_Array[i].GetFaultTaskCount();
        break;
      end;
    else
      Count = Count + rslmpexecutor_Array[i].GetFaultTaskCount();
    end;
    i = i + 1;
  end;

  return Count;
end;

macro GetCountExecuteWait(OperationID)
  var Count = 0;
  var i = 0;

  while(i < rslmpexecutor_Array.Size)
    if(OperationID > 0)
      if(operationid_Array[i] == OperationID)
        Count = rslmpexecutor_Array[i].GetWaitingTaskCount();
        break;
      end;
    else
      Count = Count + rslmpexecutor_Array[i].GetWaitingTaskCount();
    end;
    i = i + 1;
  end;

  return Count;
end;

macro ClearRequestQueue()
  var i = 0;

  while(i < rslmpexecutor_Array.Size)
    rslmpexecutor_Array[i].clearQueue();
    i = i + 1;
  end;
end;

macro GetCountExecuteWorking()
  var Count = 0;
  var i = 0;

  while(i < rslmpexecutor_Array.Size)
    Count = Count + rslmpexecutor_Array[i].GetWorkingTaskCount();
    i = i + 1;
  end;

  return Count;
end;