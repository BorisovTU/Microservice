/*
 $Name: pmterror.mac
 $Module: Ядро Banking
 $Description: Макрос шага
*/

//-----------------------------------------------------------------------------
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 4001  - "Платеж"
//                Блок     - 29043 - "Проверки по ФМ"
//                Шаг      - 10    - "Приостановление по ФМ"
//-----------------------------------------------------------------------------
import PaymInter, FMInter, "pmfm.mac", OprInter, BankInter;

var PaymentObj : RsbPayment;

private const FM_ERROR_PAYMENT_STOPED : integer = 9843; // платеж приостановлен

macro ExecuteCaseStep
  var FM_Operation : RsbFMOperation = RsbFMOperation();
  var ErrMsg : string = "";

  if( FM_Operation.FindByDoc(DLDOC_PAYMENT, PaymentObj.PaymentID) )
    var Result : integer = FM_Operation.Check(true, false, null, null, null, 1/*HUMBEN_CHECK_MODE_NOT_CHECKED*/);
    var MessageFM : string = FM_Operation.GetMessage();

    if(Result == OPCONTR_CHECK_OK) // "Проводка разрешена"
      if( IsPmCategFMDeny(PaymentObj, ATTR_PAYMENT_FMDENY_DENY) )
        if( not IsOprMultiExec() )
          msgbox("По платежу принято решение об отказе от выполнения операции по основаниям фин.мониторинга");
        end;

        return НомерШагаОтказПоФМ;
      end;

      if( IsPmCategFMDeny(PaymentObj, ATTR_PAYMENT_FMDENY_ADDCONTROL) )
        if( not IsOprMultiExec() )
          msgbox("Дополнительный контроль по ФМ");
        end;

        return НомерШагаДополнительныйКонтрольПоФМ;
      end;

      if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
      // Установка следующих статусов произойдет автоматически после выхода из блока, 
      // т.к. это выходные статусы по умолчанию:
      //   сегмент "Уточнение причастности к терроризму" = "Не требуется",
      //   сегмент "Ручная обработка" = "Не требуется",
      //   сегмент "Картотека" = "Нет"

      // Актуализировать дату значения
      PaymentObj.ValueDate = PM_GetDefaultValueDate(PaymentObj.Department, PaymentObj.PrimDocKind);

      // Очистить значение примечания платежа "ПОД/ФТ. Результаты проверки на соответствие законодательству"
      if( PaymentObj.Notes.DelNote(NOTEKIND_PAYM_FM_CHECK_MESSAGE) != 0 )
        msgbox("Ошибка при удалении примечания платежа \"ПОД/ФТ. Результаты проверки на соответствие законодательству\"");
        return 1;
      end;

      // Завершить выполнение блока
      return "";

    elif(Result == OPCONTR_CHECK_STOP) // "Отказать"
      var stat : integer = PaymentObj.Categories.DisConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, 0);
      if(not stat)
        stat = PaymentObj.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, ATTR_PAYMENT_FMDENY_DENY, NULL, NULL, {curdate});
      end;

      if(not stat)
        stat = PaymentObj.Categories.DisConnectAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, 0);
      end;
      if(not stat)
        stat = SetAttrFmDenyReason(PaymentObj, MessageFM);
      end;

      if(stat)
        return stat;
      end;

      // ПОД/ФТ. Результаты проверки на соответствие законодательству
      if(MessageFM)
        if( PaymentObj.Notes.AddNote( NOTEKIND_PAYM_FM_CHECK_MESSAGE, MessageFM ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return 1;
        end;
      end;

      return НомерШагаОтказПоФМ;

    elif( Result == 8 /*OPCONTR_CHECK_NEED_HAND*/ ) // "Требуется ручная обработка" 
      var Name = "";
      if( PaymentObj.PayerGroup == PAYMENTS_GROUP_EXTERNAL )
        Name = PaymentObj.PayerName;
      else
        Name = PaymentObj.ReceiverName;
      end;

      ErrMsg = "В перечне ФСФМ найден(ы) субъект(ы), похожий(е) на " + Name + ". Шаг необходимо выполнить в единичном режиме.";
      msgbox(ErrMsg);
      return 1;
    else 
      // Вывести ошибку, возвращенную функций фин.мониторинга. Шаг не выполнять
      ErrMsg = MessageFM;
      if(not ErrMsg)
        ErrMsg = FM_Operation.ErrorMessage();
      end;

      if( ErrMsg )
        msgbox(ErrMsg);
      else
        MemoryError(FM_ERROR_PAYMENT_STOPED);
        msgbox(GetErrMsg());
      end;

      return 1;
    end;
  end;

  return "";

end;
