/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prchattr.mac                                       */
/*                                                                      */
/*  Описание       : Печать ПД с отметкой об изменении реквизитов       */
/*                   и реквизитами до изменения (2410-У)                */
/*                                                                      */
/*  Программист    : Popova O.                                          */
/*                                                                      */
/*  Создан         : 20.04.10                                           */
/*                                                                      */
/************************************************************************/

import prpm;

private macro PrintMarkModifyAttr( DateMod:date )
[ Изменение реквизитов
  ####################

  ────────────────────
       (подпись)
](DateMod:c);
end;

MACRO PrintDocument( ncopy:integer ):bool

   var IsEdit      : bool = false,
       IsDebetEdit : bool = false, 
       IsCreditEdit: bool = false, 
       DebetChDate : date = date(0,0,0), 
       CreditChDate: date = date(0,0,0);

   var DocKind:integer = pr_pmpaym.rec.DocKind,
       stat:bool;
   if( DocKind == 261 )
       DocKind = PS_PAYORDER;
   end;

   if( DocKind != PS_CPORDER )
     GetEditFlag( pr_pminhist.rec.PaymentID, DocKind, pr_pminhist.rec.HistoryNum, @IsDebetEdit, @DebetChDate, @IsCreditEdit, @CreditChDate, 
                  @IsEdit );
   end;
   if( IsEdit )
     while( ncopy != 0 )
       if( IsDebetEdit )
         PrintMarkModifyAttr(DebetChDate);
       end;
       /* Платежный ордер (частичная оплата картотеки 2 печатается так всегда) */
       if( (DocKind == PS_PAYORDER) and (pr_pmrmprop.rec.ShifrOper == "16" ) )
         stat = FillBuffersForPayOrderPrint();
         if(stat)
           stat = ExecMacroFile("prpmori.mac", "PrintPayOrder", ncopy, false );
         end;

       /* Клиентское платежное поручение с шифром 06 является инкассовым поручением */
       elif( ((DocKind == PS_PAYORDER) and (pr_pspayord.rec.DocKind == PSPOKIND_REQUEST) and (pr_pmrmprop.rec.ShifrOper == "06" )) 
             or (DocKind == PS_INRQ)
           )
         stat = ExecMacroFile("prpmini.mac", "PrintIncashRequest", ncopy, false );

       /*Платежное поручение*/
       elif( ((DocKind == PS_PAYORDER) and (pr_pspayord.rec.DocKind == PSPOKIND_ORDER)) )
         stat = ExecMacroFile("prpmrqi.mac", "PrintPayReq", ncopy, false );

       /*Платежное требование*/
       elif( ((DocKind == PS_PAYORDER) and (pr_pspayord.rec.DocKind == PSPOKIND_DEMAND)) )
         stat = ExecMacroFile("prpmdmi.mac", "PrintPayDem", ncopy, false );

       elif( DocKind == DLDOC_BANKORDER )
         stat = ExecMacroFile( "prpmboi.mac", "PrintBankOrder", ncopy, false );
       /*Валютный платеж*/
       elif( DocKind == PS_CPORDER )
         stat = ExecMacroFile( "ps_cpprn.mac", "PrintCPOrder", ncopy );
       else
         MsgBox("Данный вид документа не поддерживается макросом");
         stat = FALSE;
       end;
       if( IsCreditEdit )
         PrintMarkModifyAttr(CreditChDate);
       end;
       PrintEOF();
       ncopy = ncopy - 1;
     end;
   else
     MsgBox("Изменение реквизитов документа не выполнялось");
     return true;
   end;
   return stat;
END;