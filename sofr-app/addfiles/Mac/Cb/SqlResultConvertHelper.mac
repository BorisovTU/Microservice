//SqlResultConvertHelper.mac

class SqlResultConvertHelper
    private const SQL_EMPTY_RESULT_TYPE = 26;
    private const SET_CHAR_VALUE = "X";

    //возвращает null если value == Specval (null в бд)
    macro orNull(value)
        if (ValType(value) == SQL_EMPTY_RESULT_TYPE)
            return null;
        end;
        return value;
    end;

    //полученный символ "X" конвертирует в bool
    macro getBoolFromChar(value):bool
        if (ValType(value) == SQL_EMPTY_RESULT_TYPE)
            return false;
        end;

        return value == SET_CHAR_VALUE;
    end;

    macro getTime(value):time
        var tm = Time(0, 0, 0);

        if (ValType(value) == V_DTTM)
            DtTmSplit(value, null, tm);
        end;

        return tm;
    end;

    //возвращет date(0, 0, 0) если dt == null
    macro getDate(value):date
        var dt = Date(0, 0, 0);

        if (ValType(value) == V_DTTM)
            DtTmSplit(value, dt);
        end;
        
        if( ValType(value) == V_DATE )
            dt = value;
        end;

        return dt;
    end;

    macro getDateTime(value):datetime
        return DtTm(getDate(value), getTime(value));
    end;
end;