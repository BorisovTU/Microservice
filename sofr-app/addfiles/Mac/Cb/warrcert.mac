// ---------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank v6
//                 Copyright (c) R-Style SoftWare Lab  2006
//
//  File Name    : scdpcert.mac
//
//  Description  : Отчет "ИК складского свидетельства"
//
//  Programmer   : Nikonorov Evgeny
//
//  Created      : 16.01.2006
//
// ---------------------------------------------------------------------------
import PTInter, FIInter, "or_rep_h.mac", cb_sql;
import RsbDataSet;

record invcard("invcard.dbt");
record warr("warrant");
var vficert;

var sign = "     ";
const DEPOATTR_ATTNUM_STORAGE = 10;

var HeaderInv = "┌──────────────┬──────────────────────┬───────────────────┬────────────────┐\n"+
                "│    Серия     │      № начальный     │     № конечный    │   количество   │\n"+
                "├──────────────┼──────────────────────┼───────────────────┼────────────────┤";


var CertRep = CMakeReport(HeaderInv);
const FontStyleTitel = "ex_FS()";


macro GetNameAlg( TypeAlg, NumberAlg)
  file na("namealg");

  na.iTypeAlg   = TypeAlg;
  na.iNumberAlg = NumberAlg;
  if( GetEQ(na))
    return na.szNameAlg;
  else
    return "";
  end;

  return "";

end;


macro PrintSeparator()
   CertRep.AddStr();
   CertRep.AddPrintCell( "---------------------------------------------------------------------------------------------", 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
end;


macro ПечататьЗаголовокОтчета( Xid )

   //проверить, если разделители | в строке
   CertRep.AddPrintCell( "И Н В Е Н Т А Р Н А Я   К А Р Т О Ч К А", 100, 0, "c:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( "С К Л А Д С К О Г О   С В И Д Е Т Е Л Ь С Т В А", 100, 0, "c:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( "№ "+Xid, 100, 0, "c:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();

end;

macro ПечататьОбщиеПараметры()
   var width = 25;
   file dp("dp_dep");
   var Department = "-"; 

   CertRep.AddEmptyStr();

   //1. Дата составления
   CertRep.AddStr();
   CertRep.AddPrintCell( "составлена:", width, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( invcard.RegistrDate, 0, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2. состояние карточки
   CertRep.AddStr();
   CertRep.AddPrintCell( "состояние карточки:", width, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( GetNameAlg( 5211, invcard.Status), 0, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( "установлено: " + invcard.StatusDate, 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.кол-во сертификатов
   CertRep.AddStr();
   CertRep.AddPrintCell( "всего сертификатов:", width, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( 1+" (" +NumToStr(1)+") шт.", 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //4.Филиал
   dp.Code = invcard.Department;
   if( GetEQ(dp))
     Department = dp.Name;
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( "филиал:", width, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( Department, 0, 0, "l:" + FontStyleTitel, REP_ELEM_STR );


   PrintSeparator();
end;

macro ПечататьДанныеВыдавшейОрганизации()
   file party("party");
   file adress("adress");
   file obj("objcode");
   var PartyCode = "-", PartyName = "-";
   var PartyAdress = "-";
   

   //получить все данные
   party.PartyID = vficert.Issuer;
   if( GetEQ(party))
     PartyName = party.Name;

     obj.ObjectType = 3;
     obj.CodeKind   = 1;
     obj.ObjectID   = party.PartyID;
     if( GetEQ(obj) )
       PartyCode = obj.Code;
     end;

     adress.PartyID = party.PartyID;
     adress.Type    = 1; //юридический
     if(GetEQ(adress))
       PartyAdress = adress.Adress;
     end;

   end;

   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "ВЫДАВШАЯ ОРГАНИЗАЦИЯ", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.код анкеты
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"код анкеты:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyCode, 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.Наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"наименование:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyName, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //4.местонахождение
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"местонахождение:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyAdress, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

   CertRep.AddStr();

end;

macro ПечататьДанныеСклада()
   file party("party");
   file adress("adress");
   file obj("objcode");
   var PartyCode = "-";
   var PartyAdress = "-";
   var PartyName = "-";
   

   //получить все данные
   party.PartyID = warr.StorageID;
   if( GetEQ(party))
     PartyName = party.Name;
   else
     PartyName = warr.StorageShortName;
   end;

   obj.ObjectType = 3;
   obj.CodeKind   = 1;
   obj.ObjectID   = warr.StorageID;
   if( GetEQ(obj) )
     PartyCode = obj.Code;
   end;

   adress.PartyID = warr.StorageID;
   adress.Type    = 1; //юридический
   if(GetEQ(adress))
     PartyAdress = adress.Adress;
   end;

   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "СКЛАД ХРАНЕНИЯ ТОВАРА", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.код анкеты
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"код анкеты:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyCode, 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.Наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"наименование:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyName, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //4.местонахождение
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"местонахождение:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyAdress, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

end;


macro ПечататьПараметрыСкладскогоСвидетельства()
   var QuantityStr = "";
   file party("party");
   file obj("objcode");
   file fin("fininstr");
   file Country("Country") key 2;
   file Costfin("fininstr");
   var CountryStr = "-";
   var PartyCode = "-";
   var PartyName = "-";
   var CostFIIDStr = "-";
   var StorageTermStr = "";
   var PaymentTermStr = "";

   fin.FIID = vficert.FIID;
   GetEQ(fin);  //бумаги не может не быть ни при каких условиях
   
   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "ПАРАМЕТРЫ СКЛАДСКОГО СВИДЕТЕЛЬСТВА", 60, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.код ФИ
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"код анкеты:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( fin.FI_Code, 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.имя ФИ
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"наименование:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( fin.Name, 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //4.Вид сертификата
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"вид:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( GetNameAlg( 5201, warr.Kind), 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //5.Дата выдачи
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"дата выдачи свидетельства:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( date(vficert.IssueDate), 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //6.Описание товара
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"ОПИСАНИЕ ТОВАРА", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //7.наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"наименование:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( warr.GoodsName+"  (код на складе - "+warr.GoodsCode+")", 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //8.количество
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"количество:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   QuantityStr = warr.Quantity+" ("+NumToStr(warr.Quantity)+") "+ GetNameAlg( 5203, warr.QuantityUnit);
   CertRep.AddPrintCell( QuantityStr, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //9.страна происхождения
   Country.CodeLat3 = warr.Country;
   if( GetGE( Country ) )
     CountryStr = Country.CodeCyr3+" "+Country.Name;
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"страна происхождения:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( CountryStr, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //10.производитель
   party.PartyID = warr.ManufacturerID;
   if( GetEQ(party))
     PartyName = party.Name;
   else
     PartyName = warr.ManufacturerShortName;
   end;

   obj.ObjectType = 3;
   obj.CodeKind   = 1;
   obj.ObjectID   = warr.ManufacturerID;
   if( GetEQ(obj) )
     PartyCode = obj.Code;
   end;

   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"производитель:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+sign+"код анкеты:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyCode, 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+sign+"наименование:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyName, 321, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //11.стоимость
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"стоимость:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( warr.Cost+" ("+NumToStr(warr.Cost)+")", 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //12.валюта
   Costfin.FIID = warr.CostFIID;
   if( GetEQ(Costfin) )
     CostFIIDStr = Costfin.Name;
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"валюта:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( CostFIIDStr, 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //13.срок хранения
   if( warr.StorageTerm == 0 )
     StorageTermStr = "до востребования";
   elif( warr.StorageTerm == 1 )
     StorageTermStr = "до "+warr.StorageDate;
   elif( warr.StorageTerm == 2 )
     StorageTermStr = warr.StorageDays+" дн.";
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"срок хранения:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( StorageTermStr, 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //14.Плата за хранение
   if( warr.PaymentTerm == 0 )
     PaymentTermStr = warr.PaymentRate+" ("+NumToStr(warr.PaymentRate)+") %г.";
   elif( warr.PaymentTerm == 1 )
     PaymentTermStr = warr.PaymentSum+" ("+NumToStr(warr.PaymentSum)+") ";

     ClearRecord(fin);
     fin.FIID = warr.PaymentFIID;
     if( GetEQ(fin) )
       PaymentTermStr = PaymentTermStr + fin.FI_Code+" ("+fin.Name+")";
     end;  
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"плата за хранение:", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PaymentTermStr, 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

end;

macro ПечататьДанныеВладельцаТовара()
   file party("party");
   file adress("adress");
   file obj("objcode");
   var PartyCode = "-";
   var PartyAdress = "-";
   var PartyName = "-";
   

   //получить все данные
   party.PartyID = warr.OwnerID;
   if( GetEQ(party))
     PartyName = party.Name;
   else
     PartyName = warr.OwnerShortName;
   end;

   obj.ObjectType = 3;
   obj.CodeKind   = 1;
   obj.ObjectID   = warr.OwnerID;
   if( GetEQ(obj) )
     PartyCode = obj.Code;
   end;

   adress.PartyID = warr.OwnerID;
   adress.Type    = 1; //юридический
   if(GetEQ(adress))
     PartyAdress = adress.Adress;
   end;

   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "ТОВАРОВЛАДЕЛЕЦ", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.код анкеты
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"код анкеты:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyCode, 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.Наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"наименование:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyName, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //4.местонахождение
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"местонахождение:", 30, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PartyAdress, 200, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

end;

macro ПечататьСертификаты()
   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "СЕРТИФИКАТЫ", 40, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.Сертификаты
   CertRep.AddStr();
   CertRep.AddPrintCell( vficert.Series, 0, 0, "l:" + FontStyleTitel );
   CertRep.AddPrintCell( trim(vficert.Number), 0, 0, "l:" + FontStyleTitel );
   CertRep.AddPrintCell( trim(vficert.Number), 0, 0, "l:" + FontStyleTitel );
   CertRep.AddPrintCell( vficert.Copies, 0, 0, "l:" + FontStyleTitel );

   CertRep.AddStr();

end;

macro ПечатьТекущегоЗачисления()
   file party("party");
   file obj("objcode");
   file acc("account") key 1;
   file acnt("depoacnt");
   file atvl("depoatvl");
   file spl("spplaces");
   var DeponentName = "-", DeponentCode = "-", DeponentAcnt = "-", DeponentAcc = "-";
   var PlaceName = "-", PlaceCode = "-", PlaceAcnt = "-", PlaceAcntAutoKey = 0, PlaceAcc = "-";
   var StorageName = "-";

   acc.AccountID = invcard.HAccountID;
   if( GetEQ(acc) )
     party.PartyID = acc.Client;

     acnt.AutoKey = acc.DepoAcc;
     if(GetEQ(acnt))
       DeponentAcnt = acnt.BriefCode;
     end;
     if(GetEQ(party))
       DeponentName = party.Name;
       obj.ObjectType = 3;
       obj.CodeKind   = PTCK_CONTR;
       obj.ObjectID   = party.PartyID;
       if(GetEQ(obj))
         DeponentCode = obj.Code;
       end;
     end;

     DeponentAcc = acc.Account;
   end;

   ClearRecord(acc);
   ClearRecord(party);
   ClearRecord(acnt);
   ClearRecord(obj);
   acc.AccountID = invcard.LAccountID;
   if( GetEQ(acc) )
     party.PartyID = acc.Client;

     acnt.AutoKey = acc.DepoAcc;
     if(GetEQ(acnt))
       PlaceAcnt = acnt.BriefCode;
       PlaceAcntAutoKey = acnt.AutoKey;
     end;
     if(GetEQ(party))
       PlaceName = party.Name;
       obj.ObjectType = 3;
       obj.CodeKind   = PTCK_CONTR;
       obj.ObjectID   = party.PartyID;
       if(GetEQ(obj))
         PlaceCode = obj.Code;
       end;
     end;

     PlaceAcc = acc.Account;
   end;

   CertRep.AddEmptyStr();

   //1.заголовок
   CertRep.AddStr();
   CertRep.AddPrintCell( "ТЕКУЩЕЕ ЗАЧИСЛЕНИЕ", 60, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.Депонент
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"Депонент", 15, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.1.код анкеты
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"код анкеты:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( DeponentCode, 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.2.наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"наименование:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( DeponentName, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.3.счет/раздел
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"счет/раздел:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( DeponentAcnt, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //2.4.лицевой счет
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"лицевой счет:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( DeponentAcc, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

   //3.Место хранения
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+"Место хранения", 20, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.1.код анкеты
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"код анкеты:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PlaceCode, 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.2.наименование
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"наименование:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PlaceName, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.3.счет/раздел
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"счет/раздел:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PlaceAcnt, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.4.лицевой счет
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"лицевой счет:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( PlaceAcc, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.5.хранилище
   if( PlaceAcntAutoKey != 0 )
     atvl.AttrNum = DEPOATTR_ATTNUM_STORAGE;
     atvl.IsType  = "";
     atvl.ID      = PlaceAcntAutoKey;
     if(GetEQ(atvl))
       spl.Num = int(atvl.value);
       if(GetEQ(spl))
         StorageName = spl.ShortName;
       end;
     end;
   end;
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"хранилище:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( StorageName, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.6.секция
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"секция:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( invcard.Section, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   //3.7.ячейка
   CertRep.AddStr();
   CertRep.AddPrintCell( sign+sign+"ячейка:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddPrintCell( invcard.Cell, 150, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

end;

macro ПачататьСекциюПримечаний()
   CertRep.AddStr();
   CertRep.AddPrintCell( "ПРИМЕЧАНИЯ:", 25, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( "______________________________________________________________________", 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( "______________________________________________________________________", 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddPrintCell( "______________________________________________________________________", 100, 0, "l:" + FontStyleTitel, REP_ELEM_STR );

end;


macro ИК_СкладскогоСвидетельства( _invcard, _vficert, _warrant, Excel )
  file filewar("warrant") key 1;
  var stat = 0;

  copy(invcard, _invcard);
  copy(warr, _warrant);
  vficert = _vficert;
  
  if( not stat )
    //1.Заголовок
    ПечататьЗаголовокОтчета( invcard.Number ); 
    //2.Общие параметры карточки
    ПечататьОбщиеПараметры();
    //3.Выдавшая организация
    ПечататьДанныеВыдавшейОрганизации();
    //4.Данные склада хранения товара
    ПечататьДанныеСклада(); 
    //5.Параметры складского свидетельства
    ПечататьПараметрыСкладскогоСвидетельства();
    //6.Данные владельца товара
    ПечататьДанныеВладельцаТовара();
    //7.Сертификаты
    ПечататьСертификаты();
    //8.Текущее зачисление
    ПечатьТекущегоЗачисления();
    //9.Примечания
    ПачататьСекциюПримечаний();

    if( Excel )
      CertRep.PrintWinRep();
      CertRep.ShowWinRep();
    else
      CertRep.PrintRep();
    end;

  else
    println("\n Невозможно определить аттрибуты инвентарной карточки.");
  end;
   
end;