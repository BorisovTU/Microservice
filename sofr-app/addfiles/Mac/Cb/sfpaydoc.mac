/*
    sfpaydoc.mac
    Оплата ПЗО на шаге операции методом проводок
        21-02-2003, Fr
*/


import FIInter, globals, RSD, OprInter, BilFacturaInter, SFInter, "sf_lib.mac", "sfground.mac";

private CONST NATCUR = 0;

private RECORD parm(arhdoc);

private const PlusCalc_CatCode = "+Расчеты"; 
private const PlusCalcNDS_CatCode = "+Расчеты НДС"; 
private const MinusNDSAccrual_CatCode = "-НДС начисленный"; 

private const PlusCalcNVPI_CatCode = "+Расчеты,НВПИ"; 
private const PlusCalcNDS_NVPI_CatCode = "+Расчеты НДС,НВПИ";
private const MinusNDSAccrual_NVPI_CatCode = "-НДС начисленный, НВПИ";

private var ExtPayerAccount = "";

/*  Макрос пересчета суммы из одной валюты в другую */
private macro ConvertSum( sum_from, FIID_from, FIID_to, PayFIID, PayFIIDSum )
     var stat, sum_to;
     var dateCarry;

     if ( FIID_from!=FIID_to )
        if ( (valtype(PayFIID)!=V_UNDEF) AND (PayFIID==FIID_to) )
           sum_to = PayFIIDSum;
        else
           if ( parm.Date_Carry!=date(0,0,0) )
               dateCarry = parm.Date_Carry;
           else
               dateCarry = {curdate};
           end;

           /* Пересчет сумм комиссий выполняются в текуще дате */
           stat = ConvSum( sum_to, sum_from, dateCarry, FIID_from, FIID_to );
           if( stat )
              RunError( "Ошибка конвертации суммы" );
           end;
        end;
     else
        sum_to = sum_from;
     end;

     return sum_to;
end;


/* Проводка документа */
private macro SfPayDocCarry( RecOurBank, dbAccount, dbFIID, crAccount, crFIID, payAmount, payDate, Ground,
                             linkKind, objectType, objectBuf,
                             bFacturable, bilfDocArray:@TArray, arIndex:@Integer, ID_Operation, ID_Step )

  var Doc     = RsbAccTransaction;

  if( (payAmount != null) AND (payAmount > $0) )

    Doc.Chapter       = 1;
    Doc.Date_Carry    = payDate;
    
    Doc.ResultCarry   = SFPAY_CARRY;    
    Doc.Department    = {OperDprt};
  
    Doc.FIIDPayer     = dbFIID;
    Doc.AccountPayer  = dbAccount;
    /*Doc.SumPayer      = payAmount;*/

    Doc.FIIDReceiver    = crFIID;    
    Doc.AccountReceiver = crAccount;
    Doc.SumReceiver   = payAmount;
    
    Doc.Ground        =  Ground;

    if( (ID_Operation != NULL) AND (ID_Step != NULL) )
      Doc.ID_Operation = ID_Operation;
      Doc.ID_Step = ID_Step;
    end;

    if( RecOurBank )
      if( bBankorderForComm_Setting() )
        Doc.Shifr_Oper = SF_SHIFR_OPER_BANK_ORDER;
      end;
    end;

    if( not Doc.Carry() )      
      msgbox( "Ошибка при вставке проводки" );
      return 1;
    else
      if( bFacturable == true ) 
        bilfDocArray[arIndex] = TRecHandler("bilf_doc.rec");
        bilfDocArray[arIndex].Clear();
        bilfDocArray[arIndex].rec.DocID = Doc.AccTrnID;
        bilfDocArray[arIndex].rec.DocKind = DLDOC_CARRY;
        bilfDocArray[arIndex].rec.FIID  =  crFIID;

        bilfDocArray[arIndex].rec.Amount = payAmount;

        arIndex = arIndex + 1;
      end;

      SfPay_ConnectSfDocs( linkKind, objectType, objectBuf, Doc.AccTrnID, Doc.ID_Operation, Doc.ID_Step );
    end;
  end;
  
  return 0;

end;

private macro GetCurrencyShortName( CCur ) : string

  var sqlString : string;
  var rs : RsdRecordset;

  sqlString = "SELECT t_Ccy FROM dfininstr_dbt WHERE t_fiid = " + CCur;

  rs = RsdRecordset( sqlString );

  if( rs.moveNext())
    return rs.value( 0 );
  end;

  return "";
end;


private macro GetSfSi_CatAccounts( isNVPI, SfComPD, objectType, objectID, payDate, payFIID, 
                                   CalcNDS_Account:@string, Calc_Account:@string, NDSAccrual_Account:@string )

  var CalcNDS_CatCode:string, Calc_CatCode:string, NDSAccrual_CatCode;
  if( isNVPI )
    CalcNDS_CatCode = PlusCalcNDS_NVPI_CatCode;
    Calc_CatCode = PlusCalcNVPI_CatCode;
    NDSAccrual_CatCode = MinusNDSAccrual_NVPI_CatCode;
  else
    CalcNDS_CatCode = PlusCalcNDS_CatCode;
    Calc_CatCode = PlusCalc_CatCode;
    NDSAccrual_CatCode = MinusNDSAccrual_CatCode;
  end;

  if( (objectType == OBJTYPE_OPRSFCOM) OR (( objectType != OBJTYPE_SFINVOICE ) AND (objectID > 0)) )
    CalcNDS_Account = SfComPD.GetDefComSfSiAccount( ObjectID, CALCNDS_SFSI_KIND, CalcNDS_CatCode, payDate, payFIID );
    Calc_Account = SfComPD.GetDefComSfSiAccount( ObjectID, CALC_SFSI_KIND, Calc_CatCode, payDate, payFIID );
    NDSAccrual_Account = SfComPD.GetDefComSfSiAccount( ObjectID, NDSACRUAL_SFSI_KIND, NDSAccrual_CatCode, payDate, payFIID );
  else
    CalcNDS_Account = SfComPD.FindAndOpenAccount( CalcNDS_CatCode, payDate, payFIID );
    Calc_Account = SfComPD.FindAndOpenAccount( Calc_CatCode, payDate, payFIID );
    NDSAccrual_Account = SfComPD.FindAndOpenAccount( NDSAccrual_CatCode, payDate, payFIID );
  end;

end;

private macro afterSfPay_Invoice( SfInv, dpAmount, dpNDSAmount )

  var sfdefcom:TBfile = TBFile( "sfdefcom.dbt" );
  var i = 0, sfDefComs = TArray(), err:bool = false;  

  sfdefcom.addFilter( " t_InvoiceID = " + SfInv.InvoiceID );
  err = sfdefcom.GetGE();
 
  while( err )    
    sfDefComs[i] = TArray();
    sfDefComs[i][0] = sfdefcom.rec.feeType;
    sfDefComs[i][1] = sfdefcom.rec;
    i = i + 1;
    err = sfdefcom.Next();
  end;
  
  if( afterSfPay(sfDefComs, dpAmount, dpNDSAmount, SfInv.PayFIID, 0) == false )
    return 1;
  end;

  return 0;
end;


private macro afterSfPay_OneDefcom( sfcomiss, DefCom, dpAmount, dpNDSAmount, payFIID )  

  var sfDefComs = TArray();

  sfDefComs[0] = TArray();
  sfDefComs[0][0] = sfcomiss.FeeType;
  sfDefComs[0][1] = DefCom;
    
  if( afterSfPay(sfDefComs, dpAmount, dpNDSAmount, payFIID, 0) == false )
    return 1;
  end;

  return 0;

end;


private macro afterSfPayExt( sfcomiss, objectType, objectBuf, paySum, taxSum, payDate, fromFIID, payFIID )

  var dpAmount = $0, dpNDSAmount = $0;
  var retval = 0;

  if( fromFIID == payFIID )
    dpAmount =  paySum;
    dpNDSAmount = taxSum;
  else
    if( ConvSum( dpAmount, paySum, payDate, fromFIID, payFIID, sfcomiss.Ratetype ) != 0 )
      dpAmount = $0;
    end;

    if( ConvSum( dpNDSAmount, taxSum, payDate, fromFIID, payFIID, sfcomiss.Ratetype ) != 0 )
      dpNDSAmount = $0;
    end;
  end;

  if( objectType == OBJTYPE_SFINVOICE )
    retval = afterSfPay_Invoice( objectBuf, dpAmount, dpNDSAmount );
  else
    retval = afterSfPay_OneDefcom( sfcomiss, objectBuf, dpAmount, dpNDSAmount, payFIID );
  end;

  return retval;

end;

macro SfFormDocuments( sidebet, sicredit, sfcomiss, payDate, paySum, taxSum, FIID, SfComPD, IsIncluded, isNVPI,
                       FacturaID, bilfDocArray:@TArray, objectType, objectBuf, ID_Operation, ID_Step )

  var CalcNDS_Account:string, Calc_Account:string, NDSAccrual_Account:string;

  var payAmount = $0;
  var Ground:string;
  var fiidCCY = "";

  var payFIID = FIID;
  if( sfcomiss.FIID_paySum != -1 )
    payFIID = sfcomiss.FIID_paySum; 
  end;

  var AccountFreeRest = $0;
  var bSayError = true;
  if( IsOprMultiExec() )
    bSayError = false;
  end;

  /*1. Если средств на счете плательщика для оплаты комиссии (если комиссия облагается НДС, 
      то для одновременной оплаты комиссии и НДС) недостаточно, то */
  if( ПолучитьОстатокПлательщика(@AccountFreeRest, sidebet.rec.Account, sidebet.rec.FIID, payDate, payFIID) != 0 )
    return SfPayErrorTreat( SFPAY_ERROR_FREEAMOUNT, bSayError );
  end;
  if( AccountFreeRest < (paySum + taxSum) )
    return SfPayErrorTreat( SFPAY_ERROR_FREE_AMOUNT_NOTENOUGH, bSayError );
  end;
    
  /*2. Если счет плательщика не в нашем банке, то*/      
  if( (sidebet.rec.BankID != {HeadBankID} ) AND (sidebet.rec.BankID != {OurBank}) )
    if( ExtPayerAccount == "" ) /*Если счет в макросе не задан, то выдать сообщение*/      
      return SfPayErrorTreat( SFPAY_ERROR_PAYERACC_UNDEF, bSayError );    
    else /*Заполнить счет плательщика значением, задаваемым в макросе*/
      sidebet.rec.Account = ExtPayerAccount;
    end;
  /*3. Если счет получателя не в нашем банке, то */
  elif( (sicredit.rec.BankID != {HeadBankID} ) AND (sicredit.rec.BankID != {OurBank}) )
    if( ExtPayerAccount == "" ) /*Если счет в макросе не задан, то выдать сообщение*/      
      return SfPayErrorTreat( SFPAY_ERROR_RECEIVERACC_UNDEF, bSayError );    
    else /*Заполнить счет плательщика значением, задаваемым в макросе*/
      sicredit.rec.Account = ExtPayerAccount;
    end;
  end;
  
  /*4. Если счет плательщика и счет получателя открыты в разных филиалах нашего банка, то */
  if( not bSfRightFilial(sidebet, sicredit) )
    return SfPayErrorTreat( SFPAY_ERROR_DIFDPRTS, bSayError );
  end;

  fiidCCY = GetCurrencyShortName( FIID );

  var arIndex = 0;
  var bFacturable = false;
  if( (FacturaID != 0) AND (bilfDocArray != NULL) )
    bFacturable = true;
  end;

  var objectID = 0;
  if( objectType == OBJTYPE_SFINVOICE )
    objectID = objectBuf.InvoiceID;
  else
    objectID = objectBuf.ID;
  end;

  if( (SfComPD != NULL) AND (IsIncluded != NULL) AND (isNVPI != NULL) )
    /*Если получатель комиссии "Наш банк" и комиссия учтена в доход (DOPRSFCOM_DBT.T_ISINCLUDED = "X")*/ 
    if( (sicredit.rec.PartyId == {HeadBankID}) AND (IsIncluded == "X") )  
      /*получить счета начисления по КУ*/
      GetSfSi_CatAccounts( isNVPI, SfComPD, objectType, objectID, payDate, payFIID, @CalcNDS_Account, @Calc_Account, @NDSAccrual_Account );
        
      /*Если счета начисления НДС "+Расчеты НДС[, НВПИ]" и комиссии "+Расчеты[, НВПИ]" совпадают и комиссия облагается НДС*/
      if( (CalcNDS_Account == Calc_Account) AND (taxSum > 0) )        
        /*то выполнить проводку по оплате комиссии и ее НДС:*/
        Ground = "Оплата комиссии " + sfcomiss.Code +". В том числе НДС " + taxSum + " " + fiidCCY + ".";
       
        Ground = SfGroundAddVO( Ground, payFIID, sidebet.rec.PartyID, sicredit.rec.PartyID, payDate );
        
        if( ConvSum( payAmount, paySum + taxSum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
          payAmount = $0;
        end;

        if( SfPayDocCarry(true, sidebet.rec.Account, sidebet.rec.FIID, Calc_Account, payFIID, 
                          payAmount, payDate, Ground, SFDOCS_LINKKIND_COMISSPAY, objectType, objectBuf, 
                          bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
          return 1;
        end;
      else
        /*Выполнить проводку по оплате комиссии */
        Ground = "Оплата комиссии " + sfcomiss.Code;

        Ground = SfGroundAddVO( Ground, payFIID, sidebet.rec.PartyID, sicredit.rec.PartyID, payDate );
        
        if( ConvSum( payAmount, paySum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
          payAmount = $0;
        end;

        if( SfPayDocCarry(true, sidebet.rec.Account, sidebet.rec.FIID, Calc_Account, payFIID, 
                          payAmount, payDate, Ground, SFDOCS_LINKKIND_COMISSPAY, objectType, objectBuf, 
                          bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
          return 1;
        end;
                  
        /*Если комиссия облагается НДС, то выполнить проводку по оплате НДС*/
        if( taxSum > 0 )
          Ground = "Оплата НДС комиссии " + sfcomiss.Code;
 
          Ground = SfGroundAddVO( Ground, payFIID, sidebet.rec.PartyID, sicredit.rec.PartyID, payDate );
          
          if( ConvSum( payAmount, taxSum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
            payAmount = $0;
          end;

          if( SfPayDocCarry(true, sidebet.rec.Account, sidebet.rec.FIID, CalcNDS_Account, payFIID, 
                            payAmount, payDate, Ground, SFDOCS_LINKKIND_NDSPAY, objectType, objectBuf, 
                            bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
            return 1;
          end;
        end;
      end;
      /*Выполнить проводку по учету НДС*/
      Ground = "Учет НДС комиссии " + sfcomiss.Code;
      if( ConvSum( payAmount, taxSum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
        payAmount = $0;
      end;

      if( (payFIID != NATCUR) AND (payAmount > 0) )
        if( ConvSum( payAmount, payAmount, payDate, payFIID, NATCUR, 0 ) != 0 )
          MsgBox("Не найден основной курс ",ПолучитьКодФинИн(payFIID), " относительно ",ПолучитьКодФинИн(NATCUR), " за ", payDate);
          return 1;
        end;
      end;

      if( SfPayDocCarry(true, NDSAccrual_Account, payFIID, sicredit.rec.ReceiverNDSAccount, NATCUR, 
                        payAmount, payDate, Ground, SFDOCS_LINKKIND_NDSDISCOUNT, objectType, objectBuf, 
                        false, null, null, ID_Operation, ID_Step) )
        return 1;
      end;
    else
      /*Если получатель комиссии наш банк, то */
      if( sicredit.rec.PartyId == {HeadBankID} )
        /*Выполнить проводку по оплате комиссии 2БАа (Таблица 17):
        Дт.     СПИ договора обслуживания УПК 
        Кт.     СПИ комиссии 
        Сумма   Сумма комиссии переведенная в валюту оплаты по курсу указанному в 
                виде комиссии (DSFCOMISSION_DBT.T_RATETYPE) на дату PayDate, 
        Валюта  Валюта оплаты комиссии (DSFCOMISS_DBT.T_FIID_PAYSUM)
        Дата    PayDate
        Основание "Оплата комиссии <код комиссии>."*/
        Ground = "Оплата комиссии " + sfcomiss.Code;
        
        Ground = SfGroundAddVO( Ground, payFIID, sidebet.rec.PartyID, sicredit.rec.PartyID, payDate );

        if( ConvSum( payAmount, paySum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
          payAmount = $0;
        end;

        if( (payFIID != sicredit.rec.FIID) AND (payAmount > 0) )
          if( ConvSum( true, payAmount, payAmount, payDate, payFIID, sicredit.rec.FIID, 0 ) != 0 )
            payAmount = $0;
          end;
        end;
    
        if( SfPayDocCarry(true, sidebet.rec.Account, sidebet.rec.FIID, sicredit.rec.Account, sicredit.rec.FIID, 
                          payAmount, payDate, Ground, SFDOCS_LINKKIND_COMISSPAY, objectType, objectBuf,
                          bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
          return 1;
        end;
    
        if( taxSum > 0 )
          /*Выполнить проводку по оплате комиссии 2БАб (Таблица 17):
          Дт.     СПИ договора обслуживания УПК 
          Кт.     "-НДС"
          Сумма   Сумма НДС переведенные в валюту оплаты по курсу указанномые 
                  в виде комиссии (DSFCOMISSION_DBT.T_RATETYPE) на дату PayDate, 
                  а затем в рубли по основному курсу на дату PayDate.
          Валюта  НацВал 
          Дата    PayDate
          Основание "Оплата НДС комиссии <код комиссии>."*/
          Ground = "Оплата НДС комиссии " + sfcomiss.Code;
          
          Ground = SfGroundAddVO( Ground, payFIID, sidebet.rec.PartyID, sicredit.rec.PartyID, payDate );
          
          if( ConvSum( payAmount, taxSum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
            payAmount = $0;
          end;
    
          if( (payFIID != NATCUR) AND (payAmount > 0) )
            if( ConvSum( true, payAmount, payAmount, payDate, payFIID, NATCUR, 0 ) != 0 )
              MsgBox("Не найден основной курс ",ПолучитьКодФинИн(payFIID), " относительно ",ПолучитьКодФинИн(NATCUR), " за ", payDate);
              return 1;
            end;
          end;
    
          if( SfPayDocCarry(true, sidebet.rec.Account, sidebet.rec.FIID, sicredit.rec.ReceiverNDSAccount, NATCUR, 
                            payAmount, payDate, Ground, SFDOCS_LINKKIND_NDSPAY, objectType, objectBuf, 
                            bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
            return 1;
          end;
        end;
      elif( sidebet.rec.PartyID == {HeadBankID} )
        /*Выполнить проводку по оплате комиссии 1А (Таблица 17):
        Дт.     СПИ договора обслуживания УПК 
        Кт.     СПИ комиссии
        Сумма   Сумма комиссии переведенная в валюту оплаты по курсу указанному в виде комиссии (DSFCOMISSION_DBT.T_RATETYPE) 
                на дату PayDate, 
        Валюта  Валюта оплаты комиссии (DSFCOMISS_DBT.T_FIID_PAYSUM)
        Дата    PayDate
        Основание "Оплата комиссии <код комиссии>."*/
        Ground = "Оплата комиссии " + sfcomiss.Code;
        if( ConvSum( payAmount, paySum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
          payAmount = $0;
        end;

        if( (payFIID != sicredit.rec.FIID) AND (payAmount > 0) )
          if( ConvSum( true, payAmount, payAmount, payDate, payFIID, sicredit.rec.FIID, 0 ) != 0 )
            payAmount = $0;
          end;
        end;
    
        if( SfPayDocCarry(false, sidebet.rec.Account, sidebet.rec.FIID, sicredit.rec.Account, sicredit.rec.FIID, 
                          payAmount, payDate, Ground, SFDOCS_LINKKIND_COMISSPAY, objectType, objectBuf,
                          bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
          return 1;
        end;
    
        if( taxSum > 0 )
          /*Выполнить проводку по оплате комиссии 1Б (Таблица 17):
          Дт.     "+НДС" 
          Кт.     СПИ комиссии
          Сумма   Сумма НДС переведенные в валюту оплаты по курсу указанномые в виде комиссии (DSFCOMISSION_DBT.T_RATETYPE) 
                  на дату PayDate, а затем в рубли по основному курсу на дату PayDate.
          Валюта  НацВал 
          Дата    PayDate
          Основание "Оплата НДС комиссии <код комиссии>."*/
          Ground = "Оплата НДС комиссии " + sfcomiss.Code;
          if( ConvSum( payAmount, taxSum, payDate, FIID, payFIID, sfcomiss.Ratetype ) != 0 )
            payAmount = $0;
          end;
    
          if( (payFIID != NATCUR) AND (payAmount > 0) )
            if( ConvSum( payAmount, payAmount, payDate, payFIID, NATCUR, 0 ) != 0 )
              MsgBox("Не найден основной курс ",ПолучитьКодФинИн(payFIID), " относительно ",ПолучитьКодФинИн(NATCUR), " за ", payDate);
              return 1;
            end;
          end;
    
          if( SfPayDocCarry(false, sidebet.rec.ReceiverNDSAccount, sidebet.rec.FIID, sicredit.rec.Account, NATCUR, 
                            payAmount, payDate, Ground, SFDOCS_LINKKIND_NDSPAY, objectType, objectBuf,
                            bFacturable, @bilfDocArray, @arIndex, ID_Operation, ID_Step) )
            return 1;
          end;
        end;
      end;
    end;
    
    if( sfcomiss.ServiceKind == PTSK_DEPOS )        
      if( afterSfPayExt(sfcomiss, objectType, objectBuf, paySum, taxSum, payDate, FIID, payFIID) )
        return 1;
      end;
    end;
  end;

  return 0;
end;
