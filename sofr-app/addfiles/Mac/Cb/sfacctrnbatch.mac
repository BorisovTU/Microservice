/*
$Name:           sfacctrnbatch.mac
$Module:         Ядро ГКБО
$Description:    ПЗО: Класс управления проводками в пакетном режиме
*/

import SfInter, CTInter, OprInter, BankInter, BilFacturaInter, "globals.mac";

class TSfDefAccTrnDataCharger( CacheSize )
  var m_CacheSize = CacheSize;
  var accTrnBatch = RsbBatchAccTrn;
  var SfTrnIndex  = 0;

  // Параметры для создания sfdocs_dbt, у всех массивов общий индекс, для i-й проводки
  var SfDocsArray       = TArray;
  var ID_OperationArray = TArray;
  var ID_StepArray      = TArray;

  //private var ndsDiscountSfdocsCache = null;

  private macro AddSfDocs( LinkKind, sfdefID, SfAccrueIndex )
    if(LinkKind != NULL)
      var sfdocs = TRecHandler("sfdocs.dbt");
      ClearRecord( sfdocs );

      sfdocs.rec.LinkKind   = LinkKind;
      sfdocs.rec.ObjectType = OBJTYPE_SFDEFCOM;
      sfdocs.rec.ObjectID   = sfdefID;
      sfdocs.rec.SfAccrueID = SfAccrueIndex;
      sfdocs.rec.AccTrnID   = SfTrnIndex;
      SfDocsArray[SfDocsArray.size] = sfdocs;
   end;
  end;

  // Добавить проводку
  macro AddDocsTrn(trnData, SfDefID, LinkKind, SfAccrueIndex, ID_Operation, ID_Step)
     accTrnBatch.AddAccTransaction( trnData, null,  ID_Operation, ID_Step);
     AddSfDocs( LinkKind, sfdefID, SfAccrueIndex );
     ID_OperationArray[SfTrnIndex] = ID_Operation;
     ID_StepArray[SfTrnIndex]      = ID_Step;

     SfTrnIndex = SfTrnIndex + 1;
  end;

  macro SetPoint()
     accTrnBatch.SetPoint();
  end;
  
  macro ClearPoint()
     accTrnBatch.ClearPoint();
  end;

  

  private macro AddSfDocsToSqlInsert( index, AccTrnID, SfAccrueID, sfDocsRsbSqlInsert )
    SfDocsArray[index].rec.AccTrnID = AccTrnID;
    SfDocsArray[index].rec.SfAccrueID = SfAccrueID;
    sfDocsRsbSqlInsert.AddRecord(SfDocsArray[index]);

    // Привязать связь проводок к начислениям по УПК
    // sprintf( DocID, "%05d%05d%010ld", (int)connDocs->LinkKind, (int)connDocs->ObjectType, (long)connDocs->ObjectID );
    var DocID = String(SfDocsArray[index].rec.LinkKind:5)+String(SfDocsArray[index].rec.ObjectType:5)+String(SfDocsArray[index].rec.ObjectID:10);
    DocID = StrSubst( DocID, " ", "0" );
    AddDocumentToStepMass(2020, DocID, ID_OperationArray[index], ID_StepArray[index]); // SFPAY_CONNECT_SFDOCS = 2020
  end;


  private macro InsertSfAccrue(accrueDataArray, sfDefArray, SfSrvDoc )
    var accrueInserter = RsbSQLInsert("sfaccrue.dbt");
    var InsAccrueArray = TArray; // номер позиции вставленной записи в accrueDataArray 
    var realIDs = TArray;
    var stat = 0;

    var i = 0;
    while( i < accrueDataArray.size )
      if(accrueDataArray[i].IsValid == true)
        InsAccrueArray[accrueInserter.size] = i; // сохраним индексы вставленных записей
        accrueInserter.AddRecord(accrueDataArray[i].SfAccrue);
      end;
      i = i + 1;
    end;

    if((stat == 0) AND (accrueInserter.size > 0))
      accrueInserter.AddReturning( "t_id", V_INTEGER );
      if(not accrueInserter.Insert())
        stat = 1;
      else
        accrueInserter.GetReturning( realIDs ); 
      end;

      if(stat == 0)
        i = 0;
        while(i < InsAccrueArray.size)
          var AccrueOpr = accrueDataArray[InsAccrueArray[i]];

          AccrueOpr.SfAccrue.rec.ID = realIDs[i];

          var AccrueDocID = string(AccrueOpr.SfAccrue.rec.ID:10);
          AccrueDocID = StrSubst( AccrueDocID, " ", "0" );
          AddDocumentToStepMass(2000, AccrueDocID, sfdefArray[AccrueOpr.DefComIndex].oprID_Operation, sfdefArray[AccrueOpr.DefComIndex].oprID_Step); // SFDOC_PERIODICAL_COMISS = 2000

          i = i + 1;
        end;
      end;
    end;

    return stat;
  end;
         
  macro ExecuteAccrueTrnDataArray( accrueDataArray, sfDefArray, SfSrvDoc )
    var stat = 0;
    var ErrorStatusArray  = TArray;
    var ErrorMessageArray = TArray; 
    var DocKindArray      = TArray;
    var DocumentIDArray   = TArray;
    var i = 0;

    if(SfTrnIndex <= 0)
      if(accrueDataArray.size > 0)
        stat = InsertSfAccrue(accrueDataArray, sfDefArray, SfSrvDoc);
      end;
    else
      // выполняем проводки
      accTrnBatch.Execute();

      // обрабатываем результат
      var arrErrCode : TArray, arrErrMessage : TArray;
      var nCarry = accTrnBatch.GetResult( arrErrCode, arrErrMessage );
      var sfDocsSqlInsert = RsbSQLInsert("sfdocs.dbt");

      // массив записей для отчета об начислении
      var sfrepaccCache = RsbSQLInsert("sfrepacc.tmp");
      var sfrepacc = TRecHandler ("sfrepacc.tmp");
      
      //массив идентификаторов экземпляров операция и шагов по ним, на которых произошли ошибки выполнения проводок
      var err_ID_Operation_Array = TArray, err_ID_Step_Array = TArray;

      var AccrueIndex = 0;
      // Вставим только те записи sfaccrue_dbt, для которых вставились проводки.
      if((stat == 0) AND (accrueDataArray.size > 0))

        // Сохраним ошибки
        i = 0;
        while( i < nCarry )
          AccrueIndex = SfDocsArray[i].rec.SfAccrueID;      
          if( arrErrCode[i] != 0 )
            accrueDataArray[AccrueIndex].IsValid = false;
          end;
          i = i + 1;
        end;
        
        stat = InsertSfAccrue(accrueDataArray, sfDefArray, SfSrvDoc);
      end;

      i = 0;
      while( i < nCarry )
        // получаем проводку
        var accTrn = accTrnBatch.GetAccTransaction(i);

        // дозаполняем буфер привязки проводки к начислению суммы УПК реальными ID
        AccrueIndex = SfDocsArray[i].rec.SfAccrueID;      

        // формируем отчет о проводке начислении
        ClearRecord( sfrepacc );
        sfrepacc.rec.sfdefcomID = accrueDataArray[AccrueIndex].SfAccrue.rec.SfDefComID; // ид. сформированной УПК (DSFDEFCOM_DBT)
  //      sfrepacc.rec.sfaccrueID = accrueDataArray[tmpAccrueID].SfAccrue.rec.ID;         //ид. сформированной записи в таблицу начислений (DSFACCRUE_DBT)
        sfrepacc.rec.debit           = accTrn.AccountPayer;
        sfrepacc.rec.credit          = accTrn.AccountReceiver;  //счет кредита проводки
        sfrepacc.rec.BeginDate       = accrueDataArray[AccrueIndex].SfAccrue.rec.BeginDate       ;
        sfrepacc.rec.EndDate         = accrueDataArray[AccrueIndex].SfAccrue.rec.EndDate         ;
        sfrepacc.rec.TransactionDate = accrueDataArray[AccrueIndex].SfAccrue.rec.TransactionDate ;
        sfrepacc.rec.Amount          = accrueDataArray[AccrueIndex].SfAccrue.rec.Amount          ;
        sfrepacc.rec.ContrID         = sfDefArray[AccrueIndex].SfDef.rec.SfContrID          ;
        sfrepacc.rec.FeeType         = SF_FEE_TYPE_PERIOD;                            ;
        sfrepacc.rec.ComissNumber    = sfDefArray[AccrueIndex].SfDef.rec.CommNumber         ;

        if( arrErrCode[i] == 0 )
          AddSfDocsToSqlInsert( i, accTrn.AccTrnID, accrueDataArray[AccrueIndex].SfAccrue.rec.ID, sfDocsSqlInsert );

        if(SfSrvDoc.Kind == 1) // SFSRVDOC_KIND_ACCRUE = 1
           sfDefArray[AccrueIndex].SfDef.rec.Status = SFDEFCOM_STATUS_ACCRUAL;
        else
           sfDefArray[AccrueIndex].SfDef.rec.Status = SFDEFCOM_STATUS_CALCULATED;
        end;

        else
          sfdefArray[AccrueIndex].Error = arrErrCode[i];
          sfrepacc.rec.comment = string("Ошибка при выполнении проводки по начислению суммы УПК:", arrErrMessage[i]); //=
          err_ID_Operation_Array.value( err_ID_Operation_Array.size ) = ID_OperationArray[i] ;
          err_ID_Step_Array.value( err_ID_Step_Array.size ) = ID_StepArray[i];

          ErrorStatusArray[ErrorStatusArray.size]   = arrErrCode[i];
          ErrorMessageArray[ErrorMessageArray.size] = arrErrMessage[i];
          DocKindArray[DocKindArray.size]           = 51;
          var DocID = String(sfrepacc.rec.sfdefcomID:34);
          DocID = StrSubst( DocID, " ", "0" );
          DocumentIDArray[DocumentIDArray.size]     = DocID;
        end;

        if(SfDocsArray[i].rec.LinkKind == SFDOCS_LINKKIND_COMISSACCRUE)
          sfrepaccCache.AddRecord( sfrepacc );
        end;
        
        i = i + 1;
      end;

      sfDocsSqlInsert.insert();

      // Выполнить пакетную вставку в протокол - в таблицу 
      sfrepaccCache.insert();

      // Пакетно обновить doprtemp_tmp - для каждого i-того элемента массива err_ID_Operation_Array
      // в поле t_Error занести ошибку 
      if(DocumentIDArray.size > 0)
        var upd = RsbSQLInsert("UPDATE doprtemp_tmp SET t_errorstatus = ?, t_errormessage = ? WHERE t_dockind = ? AND t_documentid = ?", 4, DocumentIDArray.Size );
        upd.AddParam( V_INTEGER, ErrorStatusArray        );
        upd.AddParam( V_STRING , ErrorMessageArray, 2000 );
        upd.AddParam( V_INTEGER, DocKindArray            );
        upd.AddParam( V_STRING , DocumentIDArray  , 35   );
        if(not upd.Insert())
          stat = 1;
        end;
      end;
    end;
    return stat;
  end;

  macro ExecuteDiscountNDS()
/*   
    var accTrnBatch = RsbBatchAccTrn;
    var i = 0;

    // переносим проводки в объект класса RsbBatchAccTrn
    while( i < trnDataArray.size ) 
      accTrnBatch.AddAccTransaction( trnDataArray[i], null );
      i = i + 1;
    end;

    // выполняем проводки
    accTrnBatch.Execute();

    // обрабатываем результат
    var arrErrCode : TArray, arrErrMessage : TArray;
    var nCarry = accTrnBatch.GetResult( arrErrCode, arrErrMessage );

    //массив идентификаторов экземпляров операция и шагов по ним, на которых произошли ошибки выполнения проводок
    var err_ID_Operation_Array = TArray, err_ID_Step_Array = TArray;

    vas sfDocsSqlInsert = RsbSQLInsert("sfdocs.dbt");
    
    i = 0;
    while( i < nCarry )

      // получаем проводку
      var accTrn = accTrnBatch.GetAccTransaction(i);
      ndsDiscountSfdocsCache[i].AccTrnID = accTrn.AccTrnID;

      if( arrErrCode[i] == 0 )
        AddSfDocsToSqlInsert( SFDOCS_LINKKIND_NDSDISCOUNT, i, accTrn.AccTrnID, 0, sfDocsSqlInsert, accTrn.ID_Operation, accTrn.ID_Step );
      else  
        sfrepacc.comment = string("Ошибка при выполнении проводки по начислению суммы УПК:", arrErrMessage[i]); //=
        err_ID_Operation_Array.value( err_ID_Operation_Array.size ) = accTrn.ID_Operation;
        err_ID_Step_Array.value( err_ID_Step_Array.size ) = accTrn.ID_Step;
      end;
    end;

    sfDocsSqlInsert.insert();
*/
  end;

  macro ExecuteTrnDataArray2( sfdefArray )
    var stat = 0;
    var ErrorStatusArray  = TArray;
    var ErrorMessageArray = TArray; 
    var DocKindArray      = TArray;
    var DocumentIDArray   = TArray;
    var i = 0;

    var prevID_Operation = 0;
    var prevID_Step      = 0;
    var prevFactruraID   = 0;

    // выполняем проводки
    accTrnBatch.Execute();

    // обрабатываем результат
    var arrErrCode : TArray, arrErrMessage : TArray;
    var nCarry = accTrnBatch.GetResult( arrErrCode, arrErrMessage );
    var sfDocsSqlInsert = RsbSQLInsert("sfdocs.dbt");

    // массив записей для отчета об начислении
    var sfrepaccCache = RsbSQLInsert("sfrepacc.tmp");
    var sfrepacc = TRecHandler ("sfrepacc.tmp");

    var bbeBatch = BilBookEntryBatchCharger(); 
    var bilfDocArray = TArray;                 
    var bbeIndex = 0;                          

    if(nCarry > 0)
      var prevSfDefID = sfdefArray[SfDocsArray[0].rec.ObjectID].SfDef.rec.ID; 
      
      //массив идентификаторов экземпляров операция и шагов по ним, на которых произошли ошибки выполнения проводок
      var err_ID_Operation_Array = TArray, err_ID_Step_Array = TArray;
      
      i = 0;
      while( i < nCarry )
        // получаем проводку
        var accTrn = accTrnBatch.GetAccTransaction(i);

        // дозаполняем буфер привязки проводки к начислению суммы УПК реальными ID
  //      var AccrueIndex = SfDocsArray[i].rec.SfAccrueID;      
        var SfdefIndex = SfDocsArray[i].rec.ObjectID;

        if(SfDocsArray[i].rec.LinkKind == SFDOCS_LINKKIND_COMISSPAY)
          // формируем отчет о проводке
          ClearRecord( sfrepacc );
          sfrepacc.rec.sfdefcomID      = sfdefArray[SfdefIndex].SfDef.rec.ID; // ид. сформированной УПК (DSFDEFCOM_DBT)
          sfrepacc.rec.debit           = accTrn.AccountPayer;
          sfrepacc.rec.credit          = accTrn.AccountReceiver;  //счет кредита проводки
          sfrepacc.rec.BeginDate       = sfdefArray[SfdefIndex].SfDef.rec.DatePeriodBegin;
          sfrepacc.rec.EndDate         = sfdefArray[SfdefIndex].SfDef.rec.DatePeriodEnd;
          sfrepacc.rec.TransactionDate = date(0,0,0);
          sfrepacc.rec.Amount          = accTrn.SumPayer/*sfdefArray[SfdefIndex].SfDef.rec.Sum*/;
          sfrepacc.rec.InvoiceID       = -1;
          sfrepacc.rec.ErrorCode       = arrErrCode[i];
          sfrepacc.rec.Comment         = arrErrMessage[i];
          sfrepacc.rec.feetype         = SF_FEE_TYPE_PERIOD;
          sfrepacc.rec.comissNumber    = sfdefArray[SfdefIndex].SfDef.rec.commNumber;
          sfrepacc.rec.ContrID         = sfdefArray[SfdefIndex].SfDef.rec.SfContrID;
          sfrepacc.rec.Kind            = 1;
          sfrepaccCache.AddRecord( sfrepacc );
        end;

        if( arrErrCode[i] == 0 )
          SfDocsArray[i].rec.ObjectID = sfdefArray[SfdefIndex].SfDef.rec.ID;
          AddSfDocsToSqlInsert( i, accTrn.AccTrnID, 0, sfDocsSqlInsert );

          sfdefArray[SfdefIndex].SfDef.rec.Status = SFDEFCOM_STATUS_PAYED;

          if((SfDocsArray[i].rec.LinkKind != SFDOCS_LINKKIND_NDSDISCOUNT) AND (sfdefArray[SfdefIndex].SfDef.rec.FacturaID > 0))
            //Если accTrn - проводка по УПК с ID, отличным от prevSfDefID и заполнен bilfDocArray, то:
            if((sfdefArray[SfdefIndex].SfDef.rec.ID != prevSfDefID) AND (bilfDocArray.size > 0))
               bbeBatch.add(prevFactruraID, bilfdocArray, {curdate}, prevID_Operation, prevID_Step);
               bilfDocArray = TArray;
               bbeIndex = 0;
            else
               // сформировать параметры ЗК и добавить их в кэш:
               bbeIndex = bilfDocArray.size();
               bilfDocArray[bbeIndex] = TRecHandler("bilf_doc.rec");
               bilfDocArray[bbeIndex].Clear();
               bilfDocArray[bbeIndex].rec.DocID   = accTrn.AccTrnID;
               bilfDocArray[bbeIndex].rec.DocKind = DLDOC_CARRY;
               bilfDocArray[bbeIndex].rec.FIID    = accTrn.FIIDReceiver;
               bilfDocArray[bbeIndex].rec.Amount  = accTrn.SumReceiver;
                
               prevID_Operation = ID_OperationArray[i];
               prevID_Step      = ID_StepArray[i];
               prevFactruraID   = sfdefArray[SfdefIndex].SfDef.rec.FacturaID;
               prevSfDefID      = sfdefArray[SfdefIndex].SfDef.rec.ID;
            end;
          end;
        else
          sfdefArray[SfdefIndex].Error = arrErrCode[i];
          if(SfDocsArray[i].rec.LinkKind == SFDOCS_LINKKIND_COMISSPAY)
            err_ID_Operation_Array.value( err_ID_Operation_Array.size ) = ID_OperationArray[i] ;
            err_ID_Step_Array.value( err_ID_Step_Array.size ) = ID_StepArray[i];

            ErrorStatusArray[ErrorStatusArray.size]   = arrErrCode[i];
            ErrorMessageArray[ErrorMessageArray.size] = arrErrMessage[i];
            DocKindArray[DocKindArray.size]           = 51;
            var DocID = String(sfdefArray[SfdefIndex].SfDef.rec.ID:34);
            DocID = StrSubst( DocID, " ", "0" );
            DocumentIDArray[DocumentIDArray.size]     = DocID;
          end;
        end;
        
        i = i + 1;
      end;

      // Если заполнен массив bilfDocArray, то перенести параметры ЗК в кэш bbeBatch:
      if(bilfDocArray.size()>0)
        bbeBatch.add(prevFactruraID, bilfdocArray, {curdate}, prevID_Operation, prevID_Step);
      end;

      // Выполнить пакетную вставку ЗК 
      if(stat == 0)
        stat = bbeBatch.run();
      end;

      if(stat == 0)
        sfDocsSqlInsert.insert();

        // Выполнить пакетную вставку в протокол - в таблицу 
        sfrepaccCache.insert();

        // Пакетно обновить doprtemp_tmp - для каждого i-того элемента массива err_ID_Operation_Array
        // в поле t_Error занести ошибку 
        if(DocumentIDArray.size > 0)
          var upd = RsbSQLInsert("UPDATE doprtemp_tmp SET t_errorstatus = ?, t_errormessage = ? WHERE t_dockind = ? AND t_documentid = ?", 4, DocumentIDArray.Size );
          upd.AddParam( V_INTEGER, ErrorStatusArray        );
          upd.AddParam( V_STRING , ErrorMessageArray, 2000 );
          upd.AddParam( V_INTEGER, DocKindArray            );
          upd.AddParam( V_STRING , DocumentIDArray  , 35   );
          if(not upd.Insert())
            stat = 1;
          end;
        end;
      end;
    end;

    return stat;
  end;

  macro ExecuteOneTimeCom(sfdefArray)
    var stat = 0;
    var ErrorStatusArray  = TArray;
    var ErrorMessageArray = TArray; 
//    var DocKindArray      = TArray;
//    var DocumentIDArray   = TArray;
    var err_ID_Operation_Array = TArray; 
    var err_ID_Step_Array      = TArray; 

    accTrnBatch.Execute();

    // обрабатываем результат
    var arrErrCode : TArray, arrErrMessage : TArray;
    var nCarry = accTrnBatch.GetResult( arrErrCode, arrErrMessage );

    var  i = 0, indx = 0;
    while( indx < nCarry )
      i = indx;
      if( arrErrCode[i] != 0 )
        ErrorStatusArray[ErrorStatusArray.size]   = arrErrCode[i];
        ErrorMessageArray[ErrorMessageArray.size] = arrErrMessage[i];

        err_ID_Operation_Array[err_ID_Operation_Array.size] = sfdefArray[i].oprID_Operation;
        err_ID_Step_Array[err_ID_Step_Array.size]           = sfdefArray[i].oprID_Step     ;

      end;
      indx = indx + 1;
    end;

     if(ErrorStatusArray.size > 0)
       var upd = RsbSQLInsert("UPDATE doprtemp_tmp SET t_errorstatus = ?, t_errormessage = ? WHERE t_id_operation = ? AND t_id_step = ?", 4, ErrorStatusArray.Size );
       upd.AddParam( V_INTEGER, ErrorStatusArray        );
       upd.AddParam( V_STRING , ErrorMessageArray, 2000 );
       upd.AddParam( V_INTEGER, err_ID_Operation_Array  );
       upd.AddParam( V_INTEGER, err_ID_Step_Array       );
       if(not upd.Insert())
         stat = 1;
       end;
    end;

    return stat;
  end;
end;

