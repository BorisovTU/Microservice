/*
 $Name:        bb_corsf.mac
 $Module:      BB
 $Description: Печать платежного поручения (WR)
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2012              */
/*                                                                      */
/*  Имя файла      : bb_corsf.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежного поручения (WR)                   */
/*                                                                      */
/************************************************************************/

import "BnkTemplReport.mac";
import prpm, likepy;
import "adress.mac";

class (BnkTemplReport)CurPaymentReport()
  
  var Client_Address, INN, Currency, sumstr, cur, 
    BankBen_Address, BankBen_SWIFT, AccCorrBank, 
    BankCorr_Name, BankCorr_Address, BankCorr_SWIFT;  

   var rmprop :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   var paym   :TRecHandler = TRecHandler( "pmpaym.dbt",   "bank.def" );
   
   copy( paym   , pr_pmpaym   );
   copy( rmprop , pr_pmrmprop );

   macro Create()

    var arrClientName    = TArray();
    var arrClientAddress = TArray();
    arrClientName    = StrSplit2( rmprop.rec.PayerName, 52/*split len*/, 52/*first pos*/, 2/*min seg*/ );
    arrClientAddress = StrSplit2( Client_Address, 52/*split len*/, 52/*first pos*/, 2/*min seg*/ );

    SetSheetName( rmprop.rec.Number );

    SetValues(  "Date_Document"       , string(rmprop.rec.Date),
                "Number"              , rmprop.rec.Number, 
                "Client_Name_1"       , arrClientName.value(0),
                "Client_Name_2"       , arrClientName.value(1),
                "Client_Address_1"    , arrClientAddress.value(0),
                "Client_Address_2"    , arrClientAddress.value(1),
                "INN"                 , INN,
                "Currency"            , Currency,
                "Amount"              , sumstr,
                "Amount_Str"          , cur,
                "Details"             , rmprop.rec.Ground,
                "Payer_Account"       , paym.rec.PayerAccount,
                "Beneficiary_Account" , paym.rec.ReceiverAccount, 
                "BEN_1", ifThenElse(rmprop.rec.ComissCharges, "X", ""),
                "OUR_1", ifThenElse(rmprop.rec.ComissCharges, "", "X"),
                "C_BEN", ifThenElse(rmprop.rec.ComissCharges, "X", ""),
                "C_OUR", ifThenElse(rmprop.rec.ComissCharges, "", "X"),
                "BankBen_Name"        , rmprop.rec.ReceiverBankName,
                "BankBen_Address"     , BankBen_Address,  
                "Ben_SWIFT"           , BankBen_SWIFT,
                "AccCorrBank"         , AccCorrBank,
                "Beneficiar"          , replace(rmprop.rec.ReceiverName, "\n", " " ),
                "Corr_Name"           , BankCorr_Name,
                "Corr_Address"        , BankCorr_Address,
                "Corr_SWIFT"          , BankCorr_SWIFT,
                /* пока так за не имением данных*/
                "SPOT_1"                , "X",
                "TOD_1"                 , ""
             );
   end;

   initBnkTemplReport("CurPayment.xls");
end;

macro joinAddress(PostIndex:string, Address:string):string
  return join(map(filter(makeArray(PostIndex, Address)), @Trim), ", ");
end;

MACRO PrintHeader()

END;

MACRO PrintFooter()

END;
    
/* Печать платежного поручения */
macro PrintDocument(ncopy:integer):bool

  var Name_Country, err, report;
  var DocKind:integer = pr_pmpaym.rec.DocKind;
  
  if( DocKind != BBANK_CPORDER )
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;

  FILE party    (party) key 0;
  FILE partcode (partcode) key 1;

  RECORD RecordAdress ( adress  );

  report = CurPaymentReport();

   report.Currency = ПолучитьКодФинИН(pr_pmpaym.rec.FIID);

   party.PartyID = pr_pmpaym.rec.Payer;
   
   if(GetEq(party))
      if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
        report.Client_Address = joinAddress( RecordAdress.PostIndex, RecordAdress.Adress );
      else
        report.Client_Address = "";
      end;
      report.INN = GetPartyINN( party.PartyID, 1 );
   else
      report.Client_Address = "";
      report.INN = "";
   end;

   party.PartyID = pr_pmpaym.rec.ReceiverBankID;
   
   if(GetEq(party))
      if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
        report.BankBen_Address = joinAddress( RecordAdress.PostIndex, RecordAdress.Adress );
      else
        report.BankBen_Address = "";
      end;
   else
      report.BankBen_Address = "";
   end;

   report.BankBen_SWIFT = ПолучитьКодСубъекта( pr_pmpaym.rec.ReceiverBankID, PTCK_SWIFT, err );
   if(err)
      report.BankBen_SWIFT   = "";
   end;

   if ( (pr_credit.rec.PaymentID==pr_pmpaym.rec.PaymentID) AND (pr_credit.rec.IsSender!="X") )
      report.AccCorrBank    = pr_credit.rec.CorrAcc;
      report.BankCorr_SWIFT = pr_credit.rec.CorrCode;

      partcode.CodeKind = pr_credit.rec.CorrCodeKind;
      partcode.Code     = pr_credit.rec.CorrCode;
      if(getEQ(partcode))
         party.PartyID = partcode.PartyID;
         if(getEQ(party))
            if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
              report.BankCorr_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress );
            else
              report.BankCorr_Address = "";
            end;
            report.BankCorr_Name  = party.ShortName;
         else
            report.BankCorr_Address = "";
            report.BankCorr_Name  = "";
         end;
      else
         report.BankCorr_Name    = "";
         report.BankCorr_Address = "";
      end;
   else
      report.AccCorrBank    = pr_debet.rec.CorrAcc;
      report.BankCorr_SWIFT = pr_debet.rec.CorrCode;

      partcode.CodeKind = pr_debet.rec.CorrCodeKind;
      partcode.Code     = pr_debet.rec.CorrCode;
      if(getEQ(partcode))
         party.PartyID = partcode.PartyID;
         if(getEQ(party))
            if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
              report.BankCorr_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress );
            else
              report.BankCorr_Address = "";
            end;
            report.BankCorr_Name  = party.ShortName;
         else
            report.BankCorr_Address = "";
            report.BankCorr_Name  = "";
         end;
      else
         report.BankCorr_Name    = "";
         report.BankCorr_Address = "";
      end;
   end;

   report.sumstr = string(MoneyL(pr_pmpaym.rec.Amount));
   report.cur = CurToStrAlt(pr_pmpaym.rec.Amount , null, null, getISOCode(pr_pmpaym.rec.FIID) );

  return report.Print();
end;