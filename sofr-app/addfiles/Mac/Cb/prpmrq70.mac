/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmrq70.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежного поручения 0401070                */
/*                                                                      */
/*  Программист    : Popova O.                                          */
/*                                                                      */
/*  Создан         : 14.06.11                                           */
/*                                                                      */
/************************************************************************/

import prpm, rmtools;

// является ли PartyID Учреждением Банка России
private macro isUBR( PartyID:integer ):bool
  return existsSQLselect( " select 1 from dbankdprt_dbt where t_partyid = :partyid and t_pzn in ('00', '10', '40') ", 
                          makeArray( SQLParam( "partyid", PartyID ) ) );
end;

private macro isResident( PartyID:integer ):bool
  file party( party ) key 0;
  party.PartyID = PartyID;
  if( not getEQ( Party ) ) 
    return false;
  end;
  return party.NotResident == "";
end;

// найти первый УБР в маршруте
private macro GetFirstUBRBank():TRecHandler
  var Route:TRecHandler = TRecHandler("pmroute.dbt");
  if( ПолучитьПервыйУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Route ) )
    if( not isUBR( Route.rec.PartyID ) )
      while( ПолучитьСледующийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Route ) )
        if( isUBR( Route.rec.PartyID ) )
          return Route;
        end;
      end;
      if( isUBR( pr_pmpaym.rec.PayerBankID ) )
        Route.rec.PartyID = pr_pmpaym.rec.PayerBankID;
      elif( isUBR( pr_pmpaym.rec.ReceiverBankID ) )
        Route.rec.PartyID = pr_pmpaym.rec.ReceiverBankID;
      end;
      return Route;
    end;
  end;
  return Route;
end;

// определить номер корсчета банка
private macro GetBankCorrAccount( PartyID:integer ):string
  file dp( bankdprt );
  dp.PartyID = PartyID;
  if( PartyID > 0 )
    if( getEQ( dp ) )
      return dp.CorAcc;
    end;
  end;
  return "";
end;

// определить короткое наименование клиента
private macro GetPartyShortName( PartyID:integer ):string
  file pt( party );
  pt.PartyID = PartyID;
  if( PartyID > 0 )
    if( getEQ( pt ) )
      return pt.ShortName;
    end;
  end;
  return "";
end;

private class УзелМаршрута()

  var PartyID     :integer = - 1;
  var CodeKind    :integer = - 1;
  var CodeName    :string  =  "";
  var CodeValue   :string  =  "";
  var Name        :string  =  "";
  var CorAccount  :string  =  "";
  macro Init( id:integer, ckind:integer, cname:string, cval:string, pname:string, acc:string )
    PartyID     = id;
    CodeKind    = ckind;
    CodeName    = cname;
    CodeValue   = cval;
    Name        = pname;
    CorAccount  = acc;
    return this;
  end;
  macro InitByRoute( route:TRecHandler )
    PartyID     = route.rec.PartyID;
    CodeKind    = route.rec.CodeKind;
    CodeName    = route.rec.CodeName;
    CodeValue   = route.rec.CodeValue;
    Name        = route.rec.Name;
    CorAccount  = route.rec.OutAccount;
    return this;
  end;

  macro InitByCopy( узел:УзелМаршрута )
    PartyID     = узел.PartyID;
    CodeKind    = узел.CodeKind;
    CodeName    = узел.CodeName;
    CodeValue   = узел.CodeValue;
    Name        = узел.Name;
    CorAccount  = узел.CorAccount;
    return this;
  end;
end;

private class МаршрутПлатежа

  private var  inprop = IfThenElse( pr_debet.rec.IsSender  == "X", pr_debet, pr_credit );
  private var outprop = IfThenElse( pr_credit.rec.IsSender == "X", pr_debet, pr_credit );

  var KindOper           :integer = RsbPayment( pr_pmpaym.rec.PaymentID ).Notes.ReadNote( PM_NOTEKIND_PAYM_KO0401070, {curdate} );
  var БанкПлательщика    :УзелМаршрута  = УзелМаршрута.Init( pr_pmpaym.rec.PayerBankID, inprop.rec.CodeKind, inprop.rec.CodeName, 
                                                             inprop.rec.BankCode, pr_pmrmprop.rec.PayerBankName, pr_pmrmprop.rec.PayerCorrAccNostro );
  var БанкПолучателя     :УзелМаршрута  = УзелМаршрута.Init( pr_pmpaym.rec.ReceiverBankID, outprop.rec.CodeKind, outprop.rec.CodeName, 
                                                             outprop.rec.BankCode, pr_pmrmprop.rec.ReceiverBankName, pr_pmrmprop.rec.ReceiverCorrAccNostro );
  var БанкОтправителя    :УзелМаршрута;  // узел, находящийся непосредственно перед УБР
  var БанкИсполнителя    :УзелМаршрута;  // узел, находящийся сразу после УБР
  var БанкПосредника     :УзелМаршрута;  // узел, находящийся перед получателем, если:
                                             // - получатель не УБР и не исполнитель или 
  if( KindOper == 11 )                       // - УБР в маршруте нет, а между плательщиком и посредником есть хотя бы один узел
    return;
  end;
  
  private var ПервыйУзел:TRecHandler = TRecHandler("pmroute.dbt"); 
  ПолучитьПервыйУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, ПервыйУзел );

  private var Узел:TRecHandler = TRecHandler("pmroute.dbt"); 
  private var УБР :TRecHandler = GetFirstUBRBank();
    
  if( ( УБР.rec.PartyID <= 0 ) or                        /* УБР не найден     или */
      ( УБР.rec.PartyID == БанкПлательщика.PartyID ) or  /* УБР - плательщик  или */
      ( УБР.rec.PartyID == БанкПолучателя.PartyID ) )    /* УБР - получатель      */
    if( ( KindOper == 12 ) or ( KindOper == 13 ) or ( KindOper == 14 ) )// ищем только посредника
      // если предпоследний узел - не УБР и не плательщик
      if( ПолучитьПоследнийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) and 
          ПолучитьПредыдущийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
        if( not isUBR( Узел.rec.PartyID ) and ( Узел.rec.PartyID != ПервыйУзел.rec.PartyID ) )
          // посредником будет пред-пред-последний узел
          if( ПолучитьПредыдущийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) and 
              ( Узел.rec.PartyID != ПервыйУзел.rec.PartyID ) )
            БанкПосредника = УзелМаршрута.InitByRoute( Узел );
          end;
        end;
      end;
      return;
    end;
    // некорректные значения KindOper приравниваем к 11
    KindOper = 11;
    return;
  end;
  // если УРБ был найден
  // определяем отправителя - предыдущий для УБР
  if( ПолучитьПредыдущийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
    БанкОтправителя = УзелМаршрута.InitByRoute( Узел );
    ПолучитьСледующийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел );// снова спозиционируемся на УБР
  else
    БанкОтправителя = УзелМаршрута.InitByCopy( БанкПлательщика );
  end;
  // определяем исполнителя - следующий для УБР
  if( ПолучитьСледующийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
    if( not isUBR( Узел.rec.PartyID ) and ( БанкИсполнителя.PartyID == -1 ) )
      БанкИсполнителя = УзелМаршрута.InitByRoute( Узел );
      ПолучитьПоследнийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел );
    else
      while( ( БанкИсполнителя.PartyID == -1 ) and 
             ПолучитьСледующийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) ) 
        if( not isUBR( Узел.rec.PartyID ) )
          БанкИсполнителя = УзелМаршрута.InitByRoute( Узел );
          ПолучитьПоследнийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел );
        end;
      end;
    end;
  end;
  // определяем посредника - предыдущий для получателя, если получатель - не УБР и не исполнитель
  if( ПолучитьПредыдущийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
    if( not isUBR( Узел.rec.PartyID ) and ( Узел.rec.PartyID != БанкИсполнителя.PartyID ) )
      БанкПосредника = УзелМаршрута.InitByRoute( Узел );
    end;
  end;
  if( ( KindOper == 12 ) or ( KindOper == 13 ) or ( KindOper == 14 ) )
    return;
  end;
  // исправляем некорректные значения KindOper
  if( ( ( pr_pmpaym.rec.Payer    == -1 ) and ( pr_pmpaym.rec.PayerCode    == "" ) and ( pr_pmrmprop.rec.PayerName    == "" ) ) or
      ( ( pr_pmpaym.rec.Receiver == -1 ) and ( pr_pmpaym.rec.ReceiverCode == "" ) and ( pr_pmrmprop.rec.ReceiverName == "" ) ) or
      ВидСубъекта( pr_pmpaym.rec.Payer   , PTK_BANK ) or
      ВидСубъекта( pr_pmpaym.rec.Receiver, PTK_BANK )   )
    KindOper = 14;
    return;
  end;
  // если кто-то из участников операции - нерезидент
  if( ПолучитьПервыйУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
   if( not isResident( Узел.rec.PartyID ) )
     KindOper = 13;
     return;
   end;
   while( ПолучитьСледующийУзел( OBJTYPE_PAYMENT, pr_pmpaym.rec.PaymentID, Узел ) )
     if( not isResident( Узел.rec.PartyID ) )
       KindOper = 13;
       return;
     end;
   end;
  end;  
  if( not isResident( pr_pmpaym.rec.PayerBankID ) or not isResident( pr_pmpaym.rec.ReceiverBankID ) or
      ( ( pr_pmpaym.rec.Payer > 0 ) and not isResident( pr_pmpaym.rec.Payer ) or 
        PM_FindBalanceInReg_117( "173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ", pr_pmpaym.rec.PayerAccount ) ) or 
      ( ( pr_pmpaym.rec.Receiver > 0 ) and not isResident( pr_pmpaym.rec.Receiver ) or 
        PM_FindBalanceInReg_117( "173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ", pr_pmpaym.rec.ReceiverAccount ) ) )
    KindOper = 13;
    return;
  end;

  if( ( ( БанкОтправителя.PartyID != БанкПлательщика.PartyID ) and
        ( БанкОтправителя.PartyID != ПервыйУзел.rec.PartyID  ) ) or
      ( ( БанкИсполнителя.PartyID != БанкПолучателя.PartyID  ) and
        ( БанкИсполнителя.PartyID != Узел.rec.PartyID/*это должен быть последний узел маршрута*/  ) ) )
    KindOper = 12;
    return;
  end;

end;

MACRO PrintPayReq0401070( ncopy:integer ):bool

  record fi( "fininstr.dbt", "bank.def" );
  if( ПолучитьФинИн( pr_pmpaym.rec.BaseFIID, fi ) )
    msgbox( "Не найдена валюта ", pr_pmpaym.rec.BaseFIID );
  end;

  file llval( llvalues ) key 0;
  llval.List = 1680;
  llval.Element = pr_pmrmprop.rec.ComissCharges;
  if( not getEQ( llval ) ) 
    llval.Name = "";
  end;

  var Route:МаршрутПлатежа;
  var PayerCode = "", ReceiverCode = "";
  var payment:RsbPayment = RsbPayment( pr_pmpaym.rec.PaymentID );
  // параметры исходящей корсхемы
  file csout( corschem );  
  file dpcsout( dp_dep );
  if( payment.OutCorschem >= 0 )
    if( НайтиКорсхему( payment.OutCorschem, pr_pmpaym.rec.BaseFIID, csout ) )
      msgBox("Не найдена исходящая корсхема");
    end;
    dpcsout.Code = csout.Department;
    if( not getEQ( dpcsout ) )
      msgBox("Не найден филиал корсхемы");
    end;
  end;

  // параметры входящей корсхемы
  file csin( corschem );  
  if( payment.InCorschem >= 0 )
    if( НайтиКорсхему( payment.InCorschem, pr_pmpaym.rec.BaseFIID, csin ) )
      msgBox("Не найдена входящая корсхема");
    end;
  end;

  var PayerIsResident:bool = isResident( pr_pmpaym.rec.Payer );
  var ReceiverIsResident:bool = isResident( pr_pmpaym.rec.Receiver );
  // уточним реквизиты всех участников               
  if( Route.KindOper == 14 )
    pr_pmrmprop.rec.PayerName    = "";
    pr_pmrmprop.rec.ReceiverName = "";
  elif( Route.KindOper == 13 )
    PayerCode = ПолучитьКодСубъекта( pr_pmpaym.rec.Payer, PTCK_SWIFT );   
    if( (( PayerCode != "" ) and not PayerIsResident ) or
        PM_FindBalanceInReg_117( "173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ", pr_pmpaym.rec.PayerAccount ) ) 
      pr_pmrmprop.rec.PayerName = "";
    end;
    ReceiverCode = ПолучитьКодСубъекта( pr_pmpaym.rec.Receiver, PTCK_SWIFT );   
    if( (( ReceiverCode != "" ) and not ReceiverIsResident ) or
        PM_FindBalanceInReg_117( "173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ", pr_pmpaym.rec.ReceiverAccount ) ) 
      pr_pmrmprop.rec.ReceiverName = "";
    end;
    if( PayerIsResident )
      PayerCode = "";
    end;
    if( ReceiverIsResident )
      ReceiverCode = "";
    end;
  end;


  if( ( Route.БанкОтправителя.PartyID == Route.БанкПлательщика.PartyID ) and ( Route.KindOper != 11 ) )
    Route.БанкПлательщика.CodeValue  = "";
    Route.БанкПлательщика.Name       = "";
    Route.БанкПлательщика.CorAccount = "";
  else
    if( ( ( Route.KindOper == 13 ) or ( Route.KindOper == 14 ) ) and not isResident( Route.БанкПлательщика.PartyID ) )
      Route.БанкПлательщика.CodeValue = ПолучитьКодСубъекта( Route.БанкПлательщика.PartyID, PTCK_SWIFT );
      Route.БанкПлательщика.Name      = "";
    else 
      Route.БанкПлательщика.CodeValue  = ПолучитьКодСубъекта( Route.БанкПлательщика.PartyID, PTCK_BIC );
      if( ( Route.БанкОтправителя.PartyID  > 0            ) and 
          ( Route.БанкОтправителя.PartyID == csout.CorrID  ) and 
          ( dpcsout.PartyID == Route.БанкПлательщика.PartyID ) )
            Route.БанкПлательщика.CorAccount = dpcsout.Account;
      end;
      if( ( Route.БанкПлательщика.CodeKind != 3 ) and ( Route.БанкПлательщика.CodeKind != 6 ) and ( Route.БанкПлательщика.CorAccount == "" ) ) 
        Route.БанкПлательщика.Name     = "";
      end;
    end;
  end;

  if( ( Route.БанкПолучателя.PartyID == Route.БанкИсполнителя.PartyID ) and ( Route.KindOper != 11 ) )
    Route.БанкПолучателя.CorAccount = "";
    Route.БанкПолучателя.CodeValue  = "";
    Route.БанкПолучателя.Name       = "";
  elif( ( Route.БанкПолучателя.CodeKind == PTCK_SWIFT ) or 
        ( ( Route.БанкПолучателя.CodeKind != PTCK_BIC ) and ( Route.БанкПолучателя.CorAccount == "" ) ) )
    Route.БанкПолучателя.Name       = "";
  end;

  if( Route.KindOper != 11 )
    Route.БанкИсполнителя.Name        = GetPartyShortName( Route.БанкИсполнителя.PartyID );
    Route.БанкОтправителя.Name        = GetPartyShortName( Route.БанкОтправителя.PartyID );
    Route.БанкОтправителя.CodeValue   = ПолучитьКодСубъекта( Route.БанкОтправителя.PartyID, PTCK_BIC );
    Route.БанкИсполнителя.CodeValue   = ПолучитьКодСубъекта( Route.БанкИсполнителя.PartyID, PTCK_BIC );
    Route.БанкОтправителя.CorAccount  = GetBankCorrAccount( Route.БанкОтправителя.PartyID );
    Route.БанкИсполнителя.CorAccount  = GetBankCorrAccount( Route.БанкИсполнителя.PartyID );
  else
    Route.БанкОтправителя.Name        = "";
    Route.БанкИсполнителя.Name        = "";
    Route.БанкОтправителя.CodeValue   = "";
    Route.БанкИсполнителя.CodeValue   = "";
    Route.БанкОтправителя.CorAccount  = "";
    Route.БанкИсполнителя.CorAccount  = "";
  end;

  if( Route.БанкПосредника.PartyID > 0 )
    if( ( payment.InCorschem >= 0 ) and 
        ( ( csin.Department == Route.БанкПосредника.PartyID ) or 
          ( ( Route.БанкПосредника.CodeKind == PTCK_SWIFT ) and ( Route.БанкПосредника.CodeValue != "" ) ) or
          ( ( Route.БанкПосредника.CodeKind != PTCK_SWIFT ) and ( Route.БанкПосредника.CodeKind != PTCK_BIC ) and 
            ( Route.БанкПосредника.CodeValue != "" ) and ( Route.БанкПосредника.CorAccount == "" ) ) ) )
      Route.БанкПосредника.Name       = "";
      Route.БанкПосредника.CodeValue  = "";
      Route.БанкПосредника.CorAccount = "";
    end;
  end;

  array GroudAr, AddInfoAr, 
        PayerNameAr, PayerBankNameAr, 
        ReceiverNameAr, ReceiverBankNameAr, 
        SenderBankNameAr, ExecutorBankNameAr, IntermediaryBankNameAr;
  strsplit( pr_pmrmprop.rec.Ground         , GroudAr                 , 95, 95, 4 );
  strsplit( pr_pmrmprop.rec.AdditionalInfo , AddInfoAr               , 95, 95, 2 );
  strsplit( pr_pmrmprop.rec.PayerName      , PayerNameAr             , 50, 50, 4 );
  strsplit( pr_pmrmprop.rec.ReceiverName   , ReceiverNameAr          , 50, 50, 4 );
  strsplit( Route.БанкПлательщика.Name     , PayerBankNameAr         , 50, 50, 2 );
  strsplit( Route.БанкПолучателя.Name      , ReceiverBankNameAr      , 50, 50, 2 );
  strsplit( Route.БанкОтправителя.Name     , SenderBankNameAr        , 50, 50, 3 );
  strsplit( Route.БанкИсполнителя.Name     , ExecutorBankNameAr      , 50, 50, 3 );
  strsplit( Route.БанкПосредника.Name      , IntermediaryBankNameAr  , 50, 50, 3 );

  while(ncopy != 0)
    [                                                                                  ┌─────────┐
                                                                                       │ 0401070 │
                                                                                       └─────────┘
          ############           ############
      ────────────────────  ──────────────────────
      Поступ. в банк плат.   Списано со сч. плат.
                                                   ############     #############           ┌────┐
      ПЛАТЕЖНОЕ ПОРУЧЕНИЕ N ###############        ────────────     ─────────────           │ ## │
                                                       Дата          Вид платежа            └────┘

     ──────────────────┬─────────────────────────────────┬───────────┬────────────────────────────────
      ИНН ############ │ КПП #########                   │ Сумма     │ #############################
     ──────────────────┴─────────────────────────────────┤           │
      ################################################## │           │
      ################################################## ├───────────┼────────────────────────────────
      ################################################## │ Сч.N.     │ #############################
      ################################################## │           │
                                                         ├───────────┤
      Плательщик                                         │ Код       │ #############################
     ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
      ################################################## │ БИК       │ #############################
      ################################################## ├───────────┤
                                                         │ Сч.N.     │ #############################
      Банк плательщика                                   │           │
     ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
      ################################################## │ БИК       │ #############################
      ################################################## ├───────────┤
                                                         │ Сч.N.     │ #############################
      Банк получателя                                    │           │
     ──────────────────┬─────────────────────────────────┼───────────┤
      ИНН ############ │ КПП #########                   │ Сч.N.     │ #############################
     ──────────────────┴─────────────────────────────────┤           │
                                                         ├───────────┤
      ################################################## │ Код       │ #############################
      ################################################## ├───────────┼──────┬────────────┬────────────
      ################################################## │ Вид оп.   │ #### │ Срок плат. │ ##########
      ################################################## ├───────────┤      ├────────────┤
                                                         │ Наз. пл.  │      │ Очер. плат.│ ##########
                                                         ├───────────┤      ├────────────┤
      Получатель                                         │ Код оп.   │ ##   │ Рез. поле  │
     ─────────────────────┬─────────────┬────┬───────────┴┬──────────┴──────┼────────────┼────────────
      ####################│ ########### │ ## │ ########## │ ############### │ ########## │ ##
     ─────────────────────┴─────────────┴────┴────────────┴─────────────────┴────────────┴────────────
      ###############################################################################################
      ###############################################################################################
      ###############################################################################################
      ###############################################################################################
      Назначение платежа
     ────────────────────────────────────────────────────┬───────────┬────────────────────────────────
      ################################################## │ БИК       │ ##############################
      ################################################## ├───────────┤
      ################################################## │ Сч. N     │ ##############################
      Банк-отправитель                                   │           │
      ───────────────────────────────────────────────────┼───────────┼────────────────────────────────      
      ################################################## │ БИК       │ ##############################
      ################################################## ├───────────┤
      ################################################## │ Сч. N     │ ##############################
      Банк-исполнитель                                   │           │
      ───────────────────────────────────────────────────┼───────────┼────────────────────────────────
      ################################################## │ БИК       │ ##############################
      ################################################## ├───────────┤
      ################################################## │ Сч. N     │ ##############################
      Банк-посредник                                     │           │
      ───────────────────────────────────────────────────┴───────────┴────────────────────────────────
      ###############################################################################################
      ###############################################################################################
      Банковская информация
      ───────────────────────────────────────────────────┬───────────┬──────┬────────────┬────────────
                                                         │ Расходы   │######│ № исх. док.│###########
                                                         ├───────────┤      ├────────────┤
                                                         │ Сумма     │      │ Дата       │
      Детали расходов                                    │ исх. док. │######│ исх. док.  │###########
     ────────────────────────────────────────────────────┴───────────┴──────┴────────────┴────────────
                                               Подписи                        Отметки банка
                                                                                                      
           М.П.
                                               ───────────────────────────────

                                               ───────────────────────────────


    ]
    ( pr_pmpaym.rec.PayerBankEnterDate:f:c,   pr_pmrmprop.rec.PayerChargeOffDate:f:c,
      pr_pmrmprop.rec.Date:f:c, "срочно":c,
      pr_pmrmprop.rec.Number:l, "",
      getINN( pr_pmrmprop.rec.PayerINN ):l, "", string( pr_pmpaym.rec.BaseAmount:f ) + " " + fi.Ccy,
      PayerNameAr(0),
      PayerNameAr(1),
      PayerNameAr(2), pr_pmpaym.rec.PayerAccount:l,
      PayerNameAr(3),
      PayerCode,
      PayerBankNameAr(0), Route.БанкПлательщика.CodeValue,
      PayerBankNameAr(1),
      Route.БанкПлательщика.CorAccount,
      ReceiverBankNameAr(0), Route.БанкПолучателя.CodeValue,
      ReceiverBankNameAr(1),
      Route.БанкПолучателя.CorAccount,
      getINN( pr_pmrmprop.rec.ReceiverINN ):l, "", pr_pmpaym.rec.ReceiverAccount:l,
      ReceiverNameAr(0), ReceiverCode,
      ReceiverNameAr(1),
      ReceiverNameAr(2), pr_pmrmprop.rec.ShifrOper:c, "",
      ReceiverNameAr(3),
      pr_pmrmprop.rec.Priority:l,
      Route.KindOper:l,
      "","","","","","","",
      GroudAr(0):l,
      GroudAr(1):l,
      GroudAr(2):l,
      GroudAr(3):l,
      SenderBankNameAr(0), Route.БанкОтправителя.CodeValue,
      SenderBankNameAr(1),
      SenderBankNameAr(2), Route.БанкОтправителя.CorAccount,
      ExecutorBankNameAr(0), Route.БанкИсполнителя.CodeValue,
      ExecutorBankNameAr(1),
      ExecutorBankNameAr(2), Route.БанкИсполнителя.CorAccount,
      IntermediaryBankNameAr(0), Route.БанкПосредника.CodeValue,
      IntermediaryBankNameAr(1),
      IntermediaryBankNameAr(2), Route.БанкПосредника.CorAccount,
      AddInfoAr(0):l,
      AddInfoAr(1):l,
      llval.Name:l, "",
      "",""
    );

    ncopy = ncopy - 1;
  end;
  
  return TRUE;
  
END;

MACRO PrintDocument(ncopy:integer):bool
  return PrintPayReq0401070( ncopy );
END;