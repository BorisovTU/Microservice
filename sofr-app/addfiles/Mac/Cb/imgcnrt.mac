/*
 $Name: imgcnrt.mac
 $Module: Ядро ГКБО 
 $Description: Протокол работы процедуры переноса прикрепленных объектов в хранилище
 */
 /*
	Протокол работы процедуры переноса прикрепленных объектов в хранилище
 */
import BankInter, rsd, oralib;
import "globals.mac";

private macro FindOperName(pOper)
    var cmd = RsdCommand("SELECT T_NAME from DPERSON_DBT WHERE T_OPER = ?");
	cmd.AddParam ("", RSDBP_IN, pOper);
	var rs = RsdRecordset(cmd);
    
    if (rs.moveNext())
        return rs.value(0);
    end;
    
    return "";
end;

private macro AddProtocolMessage(ObjectType, ObjectID, ImageID, pOper, Message)
    var cmd = RsdCommand("insert into DIMGCNRTPROTOCOL_TMP values(?,?,?,?,?)");
	cmd.AddParam ("", RSDBP_IN, ObjectType);
    cmd.AddParam ("", RSDBP_IN, ObjectID);
    cmd.AddParam ("", RSDBP_IN, ImageID);
    cmd.AddParam ("", RSDBP_IN, pOper);
    
    if (ValType(Message) == V_UNDEF)
        cmd.AddParam ("", RSDBP_IN, "");
    else
        cmd.AddParam ("", RSDBP_IN, pOper);
    end;
end;

private macro PrintProtocolHeader(pAllUsers, pOper)
    println("Протокол работы процедуры переноса прикрепленных объектов в хранилище");
    println("");
    println("Дата: ", Date());
    println("Время: ", Time());
    println("Пользователь: ", String({Oper}:4), " ", FindOperName({Oper}));
    println("");
    
    print("Выполнить перенос в хранилище: ");
    
    if (pAllUsers == true)
        println("для всех пользователей");
    else
        println(FindOperName(pOper));
    end;
    println("");
end;

private macro CreateSuccessBlock()
    println("Перенос выполнен успешно:");
    
    var cmd = RsdCommand("select * from DIMGCNRTPROTOCOL_TMP where LENGTH(T_MESSAGE) <= 1");
    var rs = RsdRecordset(cmd);
    
[+---------+--------------------------------+-------------------+--------+
| Вид     | Ид.объекта                     | Ид.прикрепленного | Опер.  |
| объекта |                                | объекта           |        |
+---------+--------------------------------+-------------------+--------+];

    var count = 0;
    while(rs.moveNext())
        count = count + 1;
[| ####### | ############################## | ################# | ###### |](rs.value("t_ObjectKind"):r, rs.value("t_Object"):r, rs.value("t_ImgID"):r, rs.value("t_Oper"):r);
[+---------+--------------------------------+-------------------+--------+];
    end;
    println("Всего перенесено: ", count);
end;

private macro CreateFailBlock()
    println("");
    println("Перенос не выполнен из-за ошибок:");
    
    var cmd = RsdCommand("select * from DIMGCNRTPROTOCOL_TMP where LENGTH(T_MESSAGE) > 1");
    var rs = RsdRecordset(cmd);

[+---------+--------------------------------+-------------------+--------+-----------------------------------+ 
| Вид     | Ид.объекта                     | Ид.прикрепленного | Опер.  |            Описание               | 
| объекта |                                | объекта           |        |                                   | 
+---------+--------------------------------+-------------------+--------+-----------------------------------+ ];
    
    var count = 0;
    while(rs.moveNext())
        count = count + 1;
[| ####### | ############################## | ################# | ###### | ################################# |](rs.value("t_ObjectKind"):r, rs.value("t_Object"):r, rs.value("t_ImgID"):r, rs.value("t_Oper"):r, rs.value("t_Message"):w);
[+---------+--------------------------------+-------------------+--------+-----------------------------------+ ];
    end;
    println("Всего ошибок: ", count);
end;

macro IMG_RunConvert(pAllUsers, pOper)
    PrintProtocolHeader(pAllUsers, pOper);
    
    CreateSuccessBlock();
    CreateFailBlock();
end;
