/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prcash.mac                                         */
/*                                                                      */
/*  Описание       : Печать приходного и расходного кассового ордера    */
/*                   из ББ и РКО согласно Постановления Госкомстата №88 */
/*                                                                      */
/*  Программист    : Смирнов С.В.                                       */
/*                                                                      */
/*  Создан         : 18.07.02                                           */
/*                                                                      */
/************************************************************************/

import FIInter, PTInter, pralcash;

FILE fsymbcash ("symbcash.dbt", "bank.def") KEY 0;
FILE fcaccount ("account.dbt",  "bank.def") KEY 0;
FILE fpaprkind ("paprkind.dbt", "bank.def") KEY 0;

/* виды символов */
CONST SYMB_KIND_INCOME:integer = 1,
      SYMB_KIND_OUTGO :integer = 2,
      SYMB_KIND_OUTBAL:integer = 3;

/* виды документов для symbcash.DocKind */
CONST SYMB_DOCKIND_RUR:integer = 1,
      SYMB_DOCKIND_CUR:integer = 7;
      
/*------------------------------------------------------------------------
  Класс - кассовый сивол в документе.
  Инициализируется записью в таблице symbcash.dbt
  ------------------------------------------------------------------------*/
CLASS TCashSymbol(rsymbcash)
  var kind:integer = rsymbcash.Kind,
      code:string  = rsymbcash.Symbol,
      sum :moneyl  = rsymbcash.Sum;
END;

/*------------------------------------------------------------------------
  Класс - список кассовых сиволов в документе.
  Инициализируется записью в таблице rpscshdoc.dbt
  (первичным кассовым документом) и видом символов
  ------------------------------------------------------------------------*/
CLASS (TArray)TCashSymbolList(rpscshdoc, kind:integer)
  MACRO append(item)
    Value(Size) = item;
  END;
  MACRO get(rpscshdoc, kind:integer)
    var stat:bool, 
        dockind:integer,
        appkey:string;
    if(rpscshdoc.IsCurrency=="X") dockind = SYMB_DOCKIND_CUR;
    else                          dockind = SYMB_DOCKIND_RUR;
    end;
    if(rpscshdoc.ConnectAppKind and rpscshdoc.ConnectAppKey)
      appkey = string(rpscshdoc.ConnectAppKind:5:o, rpscshdoc.ConnectAppKey);
    else
      appkey = string(rpscshdoc.AutoKey:12:o);
    end;
    fsymbcash.DocKind        = dockind;
    fsymbcash.ApplicationKey = appkey;
    fsymbcash.Kind           = kind;
    stat = getGE(fsymbcash);
    while(stat)
      if((fsymbcash.DocKind == dockind) and
         (fsymbcash.Kind    == kind) and
         (fsymbcash.ApplicationKey == appkey))
        append(TCashSymbol(fsymbcash));
        stat = next(fsymbcash);
      else
        stat = FALSE;
      end;
    end;
  END;
  InitTArray();
  get(rpscshdoc, kind);
END;

/*------------------------------------------------------------------------
  Получить клиента счета
    acc_num:string       - номер счета
    FIID:integer         - идентификатор валюты (для рублей должен быть 0)
    record client(party) - буфер клиента
    Возвращает id клиента и заполняет client, если он передан.
  ------------------------------------------------------------------------*/
MACRO GetAccountClient(accnum:string, FIID:integer, client):integer
  FILE faccount ("account.dbt", "bank.def") KEY 0;
  var PartyID:integer;
  PartyID = 0;
  faccount.Chapter       = 1;
  faccount.Account       = accnum;
  faccount.Code_Currency = FIID;
  if( GetEQ(faccount) ) PartyID = faccount.Client;
  end;
  if(PartyID and (ValType(client) != V_UNDEF))
    ПолучитьСубъекта(PartyID, client);
  end;
  return PartyID;
END;

/*------------------------------------------------------------------------
  Получить вид документа, удостоверяющего личность
    PaperKind:integer  - ID вида
    record rpaprkind ("paprkind.dbt") - буфер записи
  Заполняет rpaprkind.
  ------------------------------------------------------------------------*/
MACRO GetPAPRKIND(PaperKind:integer, rpaprkind):bool
  fpaprkind.PaperKind = PaperKind;
  if(GetEQ(fpaprkind))
    Copy(rpaprkind, fpaprkind);
    return TRUE;
  else
    ClearRecord(rpaprkind);
    return FALSE;
  end;
END;

/*------------------------------------------------------------------------
  Получить наименование вида документа, удостоверяющего личность
    PaperKind:integer  - ID вида
  Возвращает наименование вида или пустую строку.
  ------------------------------------------------------------------------*/
MACRO GetPaperKindName(PaperKind:integer):string
  record rpaprkind( "paprkind.dbt", "bank.def" );
  if( GetPAPRKIND(PaperKind, rpaprkind) ) return rpaprkind.Name;
  else                                    return "";
  end;
END;

/*------------------------------------------------------------------------
  Разложить строку полного ИНН (FullINN) на ИНН (INN) и КПП (KPP)
  ------------------------------------------------------------------------*/
MACRO SplitINN_KPP(FullINN:string, INN:string, KPP:string)
  var delim:integer = Index(FullINN, "/");
  INN = "";
  KPP = "";
  if(delim)
    INN = SubStr(FullINN, 1, delim-1);
    KPP = SubStr(FullINN,    delim+1);
  end;
  SetParm(1, INN);
  SetParm(2, KPP);
END;

/*------------------------------------------------------------------------
  Печать приходного кассового ордера из ББ и РКО
  согласно Постановления Госкомстата №88
    record rpmpaym   ("pmpaym.dbt",   "bank.def");
    record rpscshdoc ("pscshdoc.dbt", "bank.def");
    ncopy:integer - число копий
  ------------------------------------------------------------------------*/
MACRO PrintIncomeCashOrder(rpmpaym, rpscshdoc, ncopy)
  
  var RURsum:moneyl,
      CURsum:moneyl,
      CURlabel:string,
      CURstr:string,
      CURtext:string,
      ReceiverINN:string,
      ReceiverKPP:string,
      SymbList:TCashSymbolList,
      i:integer;

  array ArrayClientFIO,
        ArrayReceiverName,
        ArrayGround,
        ArraySum;
        
  record receiver("party.dbt", "bank.def");
  record fi("fininstr.dbt", "bank.def");

  if(ncopy == 0)
    ncopy = GetNumCopy();
  end;

  /*Валюта*/
  ПолучитьФинИн(rpmpaym.FIID, fi);
  
  /*Суммы*/
  CURsum = rpmpaym.Amount;
  if(rpscshdoc.IsCurrency=="X")
    CURlabel = "Сумма в ин. валюте";
    if ( rpmpaym.PaymStatus == PM_FINISHED )
      ConvSum( RURsum , CURsum, rpmpaym.ValueDate, rpmpaym.FIID );
    else
      ConvSum( RURsum , CURsum, {curdate}, rpmpaym.FIID );
    end;
    CURstr  = string(CURsum:f);
    CURtext = CurToStrAlt(CURsum, NULL, NULL, int(fi.ISO_Number));
  else
    CURlabel = "";
    CURstr   = "";
    RURsum   = CURsum;
    CURtext  = RubToStrAlt(RURsum);
  end;
  StrSplit(CURtext, ArraySum, 61, 61, 2);
  
  /*Символы*/
  SymbList=TCashSymbolList(rpscshdoc, SYMB_KIND_INCOME);

  /*Получатель*/
  GetAccountClient(rpmpaym.ReceiverAccount, fi.FIID, receiver);
  StrSplit(receiver.Name, ArrayReceiverName, 36, 25, 3);
  SplitINN_KPP(ПолучитьКодСубъекта(receiver.PartyID, PTCK_INN), ReceiverINN, ReceiverKPP);

  /*Клиент*/
  StrSplit(rpscshdoc.FIOClient, ArrayClientFIO, 36, 28, 3);
  
  /*Основание платежа*/
  StrSplit(rpscshdoc.Ground, ArrayGround, 59, 40, 3);

  /*Печать*/
  while(ncopy > 0)
    ncopy = ncopy - 1;
    /*Печать шапки*/
    [+-----------------------------------------------------------------------------------------+
     |      ПРИХОДНЫЙ КАССОВЫЙ ОРДЕР № #############                                           |
     |          ###################                                                            |
     |                                                                         Код валюты #####|
     |                                      +-------------------------+------------------------+
     | От кого ############################ |         Дебет           | Сумма в рублях         |
     | #################################### | ########################|  ######################|
     | #################################### |       сч.№ 20202        |                        |
     +--------------------------------------+-------------------------+                        |
     | Получатель ######################### |        Кредит           | ##################     |
     | #################################### | ########################|  ######################|
     | #################################### |                         |                        |
     | ИНН ########### КПП ############     +-------------------------+------------------------+
     |                                                                                         |
     | Назначение платежа ########################################                             |
     | ###########################################################       Символы отчетности    |
     | ###########################################################    +-----------------+------+]
    (rpscshdoc.Numb_Document:l, rpmpaym.ValueDate:m, fi.ISO_Number:z,
     ArrayClientFIO(0), ArrayClientFIO(1), rpmpaym.PayerAccount:f:l, RURsum:f:l, ArrayClientFIO(2),
     ArrayReceiverName(0), CURlabel, ArrayReceiverName(1), rpmpaym.ReceiverAccount:f:l, CURstr:l,
     ArrayReceiverName(2), ReceiverINN, ReceiverKPP, ArrayGround(0), ArrayGround(1), ArrayGround(2) );
   
    /*Печать символов*/
    i = 0;
    while(i < SymbList.Size )
      [|                                                                | ############### |######|]
      (SymbList.Value(i).sum:f, SymbList.Value(i).code:c);
      i = i + 1;
    end;

    /*Печать подвала*/
    [+----------------------------------------------------------------+-----------------+------+
     | #############################################################  | Шифр документа  |######|
     | #############################################################  +-----------------+------+
     | Сумма прописью                                                 | № отв. Исполн.  |######|
     +----------------------------------------------------------------+-----------------+------+
     |                                                                                         |
     |                                                                                         |
     | Подпись вносителя         Контролер            Бухгалтер              Кассир            |
     +-----------------------------------------------------------------------------------------+
     ]
    (ArraySum(0), "09", ArraySum(1), rpscshdoc.Oper:l);
  end;
END;

/*------------------------------------------------------------------------
  Печать расходного кассового ордера из ББ и РКО
  согласно Постановления Госкомстата №88
    record rpmpaym   ("pmpaym.dbt",   "bank.def");
    record rpscshdoc ("pscshdoc.dbt", "bank.def");
    ncopy:integer - число копий
  ------------------------------------------------------------------------*/
MACRO PrintOutgoCashOrder(rpmpaym, rpscshdoc, ncopy)
  var RURsum:moneyl,
      CURsum:moneyl,
      CURlabel:string,
      CURstr:string,
      CURtext:string,
      PayerINN:string,
      PayerKPP:string,
      SymbList:TCashSymbolList,
      i:integer;

  array ArrayClientFIO,
        ArrayPayerName,
        ArrayGround,
        ArraySum;
        
  record payer("party.dbt", "bank.def");
  record fi("fininstr.dbt", "bank.def");

  if(ncopy == 0)
    ncopy = GetNumCopy();
  end;

  /*Валюта*/
  ПолучитьФинИн(rpmpaym.FIID, fi);
  
  /*Суммы*/
  CURsum = rpmpaym.Amount;
  if(rpscshdoc.IsCurrency=="X")
    CURlabel = "Сумма в ин. валюте";
    if ( rpmpaym.PaymStatus == PM_FINISHED )
      ConvSum( RURsum , CURsum, rpmpaym.ValueDate, rpmpaym.FIID );
    else
      ConvSum( RURsum , CURsum, {curdate}, rpmpaym.FIID );
    end;
    CURstr  = string(CURsum:f);
    CURtext = CurToStrAlt(CURsum, NULL, NULL, int(fi.ISO_Number));
  else
    CURlabel = "";
    CURstr   = "";
    RURsum   = CURsum;
    CURtext  = RubToStrAlt(RURsum);
  end;
  StrSplit(CURtext, ArraySum, 61, 61, 2);
  
  /*Символы*/
  SymbList=TCashSymbolList(rpscshdoc, SYMB_KIND_OUTGO);

  /*Плательщик*/
  GetAccountClient(rpmpaym.PayerAccount, fi.FIID, payer);
  StrSplit(payer.Name, ArrayPayerName, 36, 25, 3);
  SplitINN_KPP(ПолучитьКодСубъекта(payer.PartyID, PTCK_INN), PayerINN, PayerKPP);

  /*Клиент*/
  StrSplit(rpscshdoc.FIOClient, ArrayClientFIO, 36, 28, 3);
  
  /*Основание платежа*/
  StrSplit(rpscshdoc.Ground, ArrayGround, 59, 40, 3);

  /*Печать*/
  while(ncopy > 0)
    ncopy = ncopy - 1;
    /*Печать шапки*/
    [+-----------------------------------------------------------------------------------------+
     |      РАСХОДНЫЙ КАССОВЫЙ ОРДЕР № ##############                                          |
     |           ####################                                                          |
     |                                                                         Код валюты #####|
     |                                      +-------------------------+------------------------+
     | Выдать ############################# |         Дебет           | Сумма в рублях         |
     | #################################### | ########################|  ######################|
     | #################################### |                         |                        |
     +--------------------------------------+-------------------------+                        |
     | Плательщик ######################### |        Кредит           | ##################     |
     | #################################### | ########################|  ######################|
     | #################################### |       сч.№ 20202        |                        |
     | ИНН ########### КПП ############     +-------------------------+------------------------+
     |                                                                                         |
     | Назначение платежа ########################################                             |
     | ###########################################################       Символы отчетности    |
     | ###########################################################    +-----------------+------+]
    (rpscshdoc.Numb_Document:l, rpmpaym.ValueDate:m, fi.ISO_Number:z,
     ArrayClientFIO(0), ArrayClientFIO(1), rpmpaym.PayerAccount:f:l, RURsum:f:l, ArrayClientFIO(2),
     ArrayPayerName(0), CURlabel, ArrayPayerName(1), rpmpaym.ReceiverAccount:f:l, CURstr:l,
     ArrayPayerName(2), PayerINN, PayerKPP, ArrayGround(0), ArrayGround(1), ArrayGround(2) );

    /*Печать символов*/
    i = 0;
    while(i < SymbList.Size )
      [|                                                                | ############### |######|]
      (SymbList.Value(i).sum:f, SymbList.Value(i).code:c);
      i = i + 1;
    end;
     
    /*Подвал*/
    [+----------------------------------------------------------------+-----------------+------+
     | #############################################################  | Шифр документа  |######|
     | #############################################################  +-----------------+------+
     | Сумма прописью                                                 | № отв. Исполн.  |######|
     +----------------------------------------------------------------+-----------------+------+
     | Предъявлен ######################### № ####################                             |
     | Выдан #####################################################################             |
     | Указанную в ордере сумму получил __________________________                             |
     |                                                                                         |
     | Контролер                          Бухгалтер                          Кассир            |
     +-----------------------------------------------------------------------------------------+
     ]
    (ArraySum(0), "09", ArraySum(1), rpscshdoc.Oper:l, 
     GetPaperKindName(rpscshdoc.PaperKind), rpscshdoc.PaperSeries + " " + rpscshdoc.PaperNumber,
     rpscshdoc.PaperIssuer + " " + string(rpscshdoc.PaperIssuedDate:f));
  end;
END;
