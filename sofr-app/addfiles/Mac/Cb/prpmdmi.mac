/*
  $Name:         prpmdmi.mac
  $Module:       Ядро Banking
  $Description:  Печать платежного требования
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmdmi.mac                                        */
/*                                                                      */
/*  Описание       : Печать платежного требования.                      */
/*                                                                      */
/*  Программист    : Смирнов С.В. SMR PRN                               */
/*                                                                      */
/*  Создан         : 09.10.02                                           */
/*                                                                      */
/************************************************************************/

import prpm, "prpmdmi_lib.mac";

private class (TPayDemPrnInfo) TPayDemPrnInfoByPayment
  InitTPayDemPrnInfo();

  PayerProps    = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  ReceiverProps = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  ground = pr_pmrmprop.rec.Ground;

  if( pr_pmpaym.rec.DocKind == PS_PAYORDER )
    partPayList  = TPartPayList(pr_pspaydem.rec.OrderID);
    AcceptPeriod = pr_pspaydem.rec.AcceptPeriod;
    if( (pr_pspayord.rec.DocKind == PSPOKIND_DEMAND ) or 
        (pr_pspayord.rec.DocKind == PSPOKIND_REQUEST) )
      if(pr_pmrmprop.rec.Date < date(1,4,2013))
        if((pr_pspaydem.rec.AcceptTerm == 1))
          AcceptTerm = "без акцепта";
        else
          AcceptTerm = "c акцептом";
        end;
      else
        if(pr_pspaydem.rec.AcceptTerm == 0)
          AcceptTerm = "2";
        else
          AcceptTerm = "1";
        end;
      end;
      PayDate    = pr_pspaydem.rec.AcceptDate;
      if((pr_pspaydem.rec.AcceptTerm == 0) and (pr_pspaydem.rec.PreacptID != 0) and (pr_pspaydem.rec.Accept == 1)
          or (pr_pspaydem.rec.AcceptTerm == 2) )
        PayDate = date(0, 0, 0);
        AcceptPeriod = 0;
      elif((pr_pspaydem.rec.AcceptTerm == 1))
        PayDate = date(0, 0, 0);
      end;
    else
      PayDate = pr_pmrmprop.rec.PayDate;
    end;
  elif( pr_pmpaym.rec.DocKind == DLDOC_BANKCLAIM )
    if((pr_pmdemand.rec.AcceptTerm == 1))
      AcceptTerm = "без акцепта";
      PayDate = date(0, 0, 0);
    else
      AcceptTerm = "c акцептом";
    end;
  else
    AcceptTerm = "Без акцепта";
  end;
 
  Date_Into     = pr_pmpaym.rec.PayerBankEnterDate;
  I2_Date       = pr_pmpaym.rec.I2PlaceDate;
  chargeOffDate = pr_pmrmprop.rec.PayerChargeOffDate;

  if( pr_pmpaym.rec.DocKind == PS_PAYORDER )
    Amount = pr_pmpaym.rec.Amount;
  else
    Amount = pr_pmpaym.rec.OrderAmount;
  end;

  UIN = GetUIN( pr_pmrmprop.rec.Date, pr_pmrmprop.rec.UIN, pr_pmpaym.rec.PayType );

  if( pr_pmpaym.rec.DocKind == PS_PAYORDER )  
    AmountStr = RubToStrAlt(Amount);
  else
    AmountStr = CurToStrAlt(Amount , null, null, getISOCode(pr_pmpaym.rec.OrderFIID));
  end;

  DocDate = pr_pmrmprop.rec.Date;
  PaymentKind = pr_pmrmprop.rec.PaymentKind;
  DocNumber = pr_pmrmprop.rec.Number;
  ShifrOper = pr_pmrmprop.rec.ShifrOper;
  Priority = pr_pmrmprop.rec.Priority;
  PayerBankMarkDate = pr_pmpaym.rec.PayerBankMarkDate;
end;

/* Печать платежного требования */
MACRO PrintPayDem(ncopy:integer):bool
  var isPrintEOF:bool;               // Печать символа окончания листа
  if( not GetParm( 1, isPrintEOF ) ) // по умолчанию печатать надо
    isPrintEOF = true;
  end;        
  if( pr_PrintEA )
    PrintEAHeader();
  end;
  var stat = true;

  var PayDemPrnInfo = TPayDemPrnInfoByPayment();
  while( (ncopy != 0) and (stat == true) )
    stat = PrintPayDem1256(PayDemPrnInfo);
    ncopy = ncopy - 1;
  end;

  if( isPrintEOF )
    PrintEOF();
  end;
  return stat;
END;
