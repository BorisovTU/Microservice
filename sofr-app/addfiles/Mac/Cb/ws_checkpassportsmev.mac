/*
 $Name: ws_checkpassportsmev.mac
 $Module: Главная книга
 $Description: Сервис 
*/

import rsd;
import cb_sql;
import "ws_lib_fun.mac";
import PTInter;

class TWsCheckPassportViaSMEVRequest

  var DOC_NAME1     :String;    // Фамилия           
  var DOC_NAME2     :String;    // Имя               
  var DOC_NAME3     :String;    // Отчество          
  var DOC_BIRTHDATE :Date;      // Дата рождения     

  var DOC_SERIE     :String;    // Серия паспорта.
  var DOC_NUMBER    :String;    // Номер паспорта.
  var DOC_ISSUEDATE :Date;      // Дата выдачи паспорта.

  var DOC_ISSUERCODE:String ;   // Код подразделения
  var DOC_REGION    :String = "";   // Регион           
  var RESPONCEPERIOD:Integer = 0;
end;

class TWsCheckPassportViaSMEVResponse
  var DOC_STATUS            :Integer   = 0  ; // Статус документа. 
  var DOC_STATUS_DESC       :String    = "" ; // Расшифровка статуса документа.
  var INVALIDITY_REASON     :Integer   = 0  ; // Причина недействительности.
  var INVALIDITY_REASON_DESC:String    = "" ; // Расшифровка причины недействительности.
  var INVALIDITY_SINCE      :Date      = date(0,0,0) ; // Дата начала недействительности документа.
end;


private macro FillRequest (oRequest, wsRequest : TWsCheckPassportViaSMEVRequest)
  
  var ParamN     : integer;
  var ObjectName : string;

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue(@wsRequest.DOC_SERIE    , ParamN, ObjectName, "DOC_SERIE"    , oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_NUMBER   , ParamN, ObjectName, "DOC_NUMBER"   , oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_ISSUEDATE, ParamN, ObjectName, "DOC_ISSUEDATE", oRequest, V_DATE,    false, null);

end;


private macro FillRequestFull (oRequest, wsRequest : TWsCheckPassportViaSMEVRequest)
  
  var ParamN     : integer;
  var ObjectName : string;

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue(@wsRequest.DOC_NAME1    , ParamN, ObjectName, "DOC_NAME1"    , oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_NAME2    , ParamN, ObjectName, "DOC_NAME2"    , oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_NAME3    , ParamN, ObjectName, "DOC_NAME3"    , oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_BIRTHDATE, ParamN, ObjectName, "DOC_BIRTHDATE", oRequest, V_DATE,    true, date(0,0,0));

  WS_SetAttributeValue(@wsRequest.DOC_ISSUERCODE, ParamN, ObjectName, "DOC_ISSUERCODE", oRequest, V_STRING,  true, "");
  WS_SetAttributeValue(@wsRequest.DOC_REGION    , ParamN, ObjectName, "DOC_REGION"    , oRequest, V_STRING,  false, "");
  WS_SetAttributeValue(@wsRequest.RESPONCEPERIOD, ParamN, ObjectName, "RESPONCEPERIOD", oRequest, V_INTEGER, false, 0);

  FillRequest (oRequest, wsRequest);
end;


/* Сервис "" */
macro CheckPassportNameViaSMEV( Request )
     
  var wsRequest : TWsCheckPassportViaSMEVRequest;
  var wsResult  : TWsCheckPassportViaSMEVResponse = TWsCheckPassportViaSMEVResponse;
  var ErrMsg = "";

  WS_CheckParameter( 1, Request, true, V_GENOBJ );  
  wsRequest = TWsCheckPassportViaSMEVRequest;
  FillRequestFull( Request, wsRequest );
  
  if ( valtype (wsRequest.DOC_SERIE) != V_STRING )
    RunError("Не задано обязательное поле: Серия паспорта");  
  end;

  if ( valtype (wsRequest.DOC_NUMBER) != V_STRING )
    RunError("Не задано обязательное поле: Номер паспорта.");  
  end;


  if ( valtype (wsRequest.DOC_NAME1) != V_STRING )
    RunError("Не задано обязательное поле: Фамилия.");  
  end;                                           

  if ( valtype (wsRequest.DOC_NAME2) != V_STRING )
    RunError("Не задано обязательное поле: Имя.");  
  end;

  if ( valtype (wsRequest.DOC_NAME3) != V_STRING )
    RunError("Не задано обязательное поле: Отчество.");  
  end;

  if ( valtype (wsRequest.DOC_BIRTHDATE) != V_DATE )
    RunError("Не задано обязательное поле: Дата рождения.");  
  end;

  if ( valtype (wsRequest.DOC_ISSUERCODE) != V_STRING )
    RunError("Не задано обязательное поле: Код подразделения.");  
  end;                                                
/*
  if ( valtype (wsRequest.DOC_REGION) != V_STRING )
    RunError("Не задано обязательное поле: Регион.");  
  end;
*/

  var DocSeries = Trim(wsRequest.DOC_SERIE);
  var DocNumber = Trim(wsRequest.DOC_NUMBER);
  var Name1     = Trim(wsRequest.DOC_NAME1);
  var Name2     = Trim(wsRequest.DOC_NAME2);
  var Name3     = Trim(wsRequest.DOC_NAME3); 
  var IssuerCode= Trim(wsRequest.DOC_ISSUERCODE);

  wsRequest.DOC_SERIE  =  DocSeries;
  wsRequest.DOC_NUMBER =  DocNumber;
  wsRequest.DOC_NAME1  =  Name1;    
  wsRequest.DOC_NAME2  =  Name2;    
  wsRequest.DOC_NAME3  =  Name3;    
  wsRequest.DOC_ISSUERCODE = IssuerCode;

  var stat =  PT_CheckPersnNameviaSMEV(wsRequest.DOC_NAME1, 
                                       wsRequest.DOC_NAME2, 
                                       wsRequest.DOC_NAME3, 
                                       wsRequest.DOC_BIRTHDATE, 
                                       wsRequest.DOC_SERIE, 
                                       wsRequest.DOC_NUMBER, 
                                       wsRequest.DOC_ISSUEDATE, 
                                       wsRequest.DOC_ISSUERCODE,
                                       wsRequest.DOC_REGION,
                                       wsRequest.RESPONCEPERIOD,
                                       wsResult.DOC_STATUS, 
                                       wsResult.DOC_STATUS_DESC, 
                                       wsResult.INVALIDITY_REASON, 
                                       wsResult.INVALIDITY_REASON_DESC, 
                                       wsResult.INVALIDITY_SINCE);

  if ( stat != 0 ) 
    ErrMsg = GetErrMsg(true);
    RunError(ErrMsg);      
  end;

  return wsResult;

end;




macro CheckPassportViaSMEV( Request )
     
  var wsRequest : TWsCheckPassportViaSMEVRequest;
  var wsResult  : TWsCheckPassportViaSMEVResponse = TWsCheckPassportViaSMEVResponse;
  var ErrMsg = "";

  WS_CheckParameter( 1, Request, true, V_GENOBJ );  
  wsRequest = TWsCheckPassportViaSMEVRequest;
  FillRequest( Request, wsRequest );
  
  if ( valtype (wsRequest.DOC_SERIE) != V_STRING )
    RunError("Не задано обязательное поле: Серия паспорта");  
  end;

  if ( valtype (wsRequest.DOC_NUMBER) != V_STRING )
    RunError("Не задано обязательное поле: Номер паспорта.");  
  end;


  var stat = PT_CheckPersnIDCviaSMEV(wsRequest.DOC_SERIE, wsRequest.DOC_NUMBER, wsRequest.DOC_ISSUEDATE, wsResult.DOC_STATUS, wsResult.DOC_STATUS_DESC, wsResult.INVALIDITY_REASON, wsResult.INVALIDITY_REASON_DESC, wsResult.INVALIDITY_SINCE, true);

  if ( stat != 0 ) 
    ErrMsg = GetErrMsg(true);
    RunError(ErrMsg);      
  end;

  return wsResult;

end;


