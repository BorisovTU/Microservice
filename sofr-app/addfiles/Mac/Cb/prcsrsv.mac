/*
$Name: prcsrsv.mac
$Module: Ядро ГКБО
$Description: Печатная форма "Остатки по портфелю за даты формирования резервов"
*/
import BankInter, globals, "res_lib.mac", "case_cls.mac", "res_carry.mac";
var pr_accase:TRecHandler = TRecHandler( "accase.dbt", "bank.def" );
file accasscs(accasscs) key 1;

MACRO PrintDocument(ncopy:integer):bool
 var accase = pr_accase.rec;

 var DateFrom:date = {curdate};
 var DateTo:date   = {curdate};

 if(not SelectDate(@DateFrom,@DateTo))
   exit;
 end;

 var PeriodStr:string = "";
 if(DateFrom == DateTo)
   PeriodStr = string("на дату ", DateFrom);
 else
   PeriodStr = string("за период с ", DateFrom, " по ", DateTo);
 end;

[Портфель ########## ##################################################
 Список остатков за даты резервирования
 ####################################
 ┌──────────┬───────────┬────────────────────────────┬────────────────────────────┬───────────────────────┐
 │ Дата     │Субпортфель│ Остаток                    │Остаток резерва             │ Корректировка резерва │
 ├──────────┼───────────┼────────────────────────────┼────────────────────────────┼───────────────────────┤
](accase.CaseID, accase.Name, PeriodStr);

var dateNext:date = DateFrom;
var rest:money, restReserveAcc:money;
var result:bool = true;
var subresult:bool = true;
var existReserve:bool = false;

var restEstimatedReservAcc:money, restEstimatedReservAccAll:money;
restReserveAcc = $0;

restEstimatedReservAcc = $0;
var IncomeReserveAccount : string;
var OutlayReserveAccount : string;
file AccCaseParm("accasepm.dbt") key 0;
var accasePD : AccCasePrimdoc;

while( (dateNext <= DateTo) and result )
  
  /* Отбираем даты, за которые формировался резерв */
  /* Если первый день периода, то остаток печатаем всегда */
  if(dateNext == DateFrom)
    existReserve = true;
  else
    existReserve = caseIsReserveInPeriod(pr_accase, dateNext, dateNext);
  end;

  if(existReserve)
    ClearRecord(accasscs);
    result = CalcACCASERest(pr_accase, accasscs, dateNext, rest);
    GetAccountReserveForCase(pr_accase, accasscs, dateNext, restReserveAcc);

    /* Вычисляем остаток оценочного резерва портфеля */
    accasscs.CaseID = accase.CaseID;
    subresult = getGE(accasscs);
    AccCaseParm.CaseID = accase.CaseID;
    getGE(AccCaseParm);
    restEstimatedReservAccAll = $0;
    while(subresult)
      subresult = CalcACCASERest(pr_accase, accasscs, dateNext, rest );

      accasePD = AccCasePrimdoc(accase, AccCaseParm, accasscs);
      IncomeReserveAccount = FindReserveEstimatedAccountIncome(accasePD, dateNext);
      OutlayReserveAccount = FindReserveEstimatedAccountOutlay(accasePD, dateNext);
      restEstimatedReservAcc = GetAbsAccRest(IncomeReserveAccount, dateNext, BalanceChapter);
      if(restEstimatedReservAcc == $0)
        restEstimatedReservAcc = GetAbsAccRest(OutlayReserveAccount, dateNext, BalanceChapter);
      end;
      restEstimatedReservAccAll = restEstimatedReservAccAll + restEstimatedReservAcc;

      subresult = next(accasscs);
      if(subresult) subresult = (accasscs.CaseID == accase.CaseID); end;
    end;

    /* Печатаем строку */
    if(result)
[ ##########             ############################ ############################ #######################]
(dateNext, rest:f, restReserveAcc:f, restEstimatedReservAccAll:f);

      accasscs.CaseID = accase.CaseID;
      subresult = getGE(accasscs);

      /* Печатаем строки об остатках субпортфелей */
      while(subresult)
        subresult = CalcACCASERest(pr_accase, accasscs, dateNext, rest );
        GetAccountReserveForCase(pr_accase, accasscs, dateNext, restReserveAcc);

        accasePD = AccCasePrimdoc(accase, AccCaseParm, accasscs);
        IncomeReserveAccount = FindReserveEstimatedAccountIncome(accasePD, dateNext);
        OutlayReserveAccount = FindReserveEstimatedAccountOutlay(accasePD, dateNext);
        restEstimatedReservAcc = GetAbsAccRest(IncomeReserveAccount, dateNext, BalanceChapter);
        if(restEstimatedReservAcc == $0)
          restEstimatedReservAcc = GetAbsAccRest(OutlayReserveAccount, dateNext, BalanceChapter);
        end;

[ ########## ########### ############################ ############################ #######################]
(dateNext, accasscs.Number, rest:f, restReserveAcc:f, restEstimatedReservAcc:f);
        subresult = next(accasscs);
        if(subresult) subresult = (accasscs.CaseID == accase.CaseID); end;
      end;
    end;
  end;

  dateNext = dateNext + 1;
end;

if(not result)
  msgbox(GetErrMsg());
  return false;
end;

return true;

END;