/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcrcl60.mac

  Библиотека       : RSACCOUNT

  Описание         : Операция реклассификации: Балансовый учет

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,CTInter, PTInter,globals, CurrInter, mpcactacc, mpccarry,
       mpclb, mpckvit;


record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    DocID      = 0,
    ContractID = 0,
    FIID, FIID_Pc, FIID_PcOD, OperDate,
    TrnMsg = "",
    AccountPc = "", AccountPcOD = "",
    AccRestPc = $0, AccRestPcOD = $0;


// Актуализация лицевого счета по КУ
PRIVATE MACRO GetDocNumber(DocID, DocNumber:@integer)
   var   cmd, rs;

   DocNumber = 0;

   cmd = RSDCommand ("select max(T_NUMBER) as maxnumber from DPRCOPRDOCS_DBT where T_DOCID = ? ");
   cmd.addParam( "", RSDBP_IN, DocID );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      DocNumber = rs.maxnumber;   
      if (valtype(DocNumber) == V_UNDEF)
         DocNumber = 0;
      end;
   end;
      
   return 0;
END;


PRIVATE MACRO ActuateAcc(CatID, Account:@string, FIID:@integer, Chapter:@integer)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID, CatCode, @CodeCurr, @Chapt);
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;
   
   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;


PRIVATE MACRO DoOperationCarry()
   
   var Debet1="",Credit1="",
       Debet2="",Credit2="",
       Debet3="",Credit3="",
       Debet4="",Credit4="",
       
       Debet1CatID = 0, Credit1CatID = 0,
       Debet2CatID = 0, Credit2CatID = 0,
       Debet3CatID = 0, Credit3CatID = 0,
       Debet4CatID = 0, Credit4CatID = 0,
       
       Debet1FIID = 0,  Credit1FIID = 0,
       Debet2FIID = 0,  Credit2FIID = 0,
       Debet3FIID = 0,  Credit3FIID = 0,
       Debet4FIID = 0,  Credit4FIID = 0,
       
       Chapter1 = 0,
       Chapter2 = 0,
       Chapter3 = 0, 
       Chapter4 = 0,

       Debet1Dep = 0,   Credit1Dep = 0,
       Debet2Dep = 0,   Credit2Dep = 0,
       Debet3Dep = 0,   Credit3Dep = 0,
       Debet4Dep = 0,   Credit4Dep = 0,

       Debet1CarrySum = 0, Credit1CarrySum = 0,
       Debet2CarrySum = 0, Credit2CarrySum = 0,
       Debet3CarrySum = 0, Credit3CarrySum = 0,
       Debet4CarrySum = 0, Credit4CarrySum = 0,
       
       DebetTypeID = 0, CreditTypeID = 0,
       НазначениеПроводки1 = "",
       НазначениеПроводки2 = "",
       НазначениеПроводки3 = "",
       НазначениеПроводки4 = "",

       DocNumber = 1,       
       stat = 0,
       fd;

   GetDocNumber(DocID, @DocNumber);
   DocNumber = DocNumber + 1;

   НазначениеПроводки1 = "Перенос начисленных срочных процентов на балансовый учет для договора " +
                          prccontract.rec.Number + " по счету " + prccontract.rec.Account +
                         " в связи с реклассификацией актива";
   НазначениеПроводки2 = "Перенос начисленных просроченных процентов на балансовый учет для договора " +
                          prccontract.rec.Number + " по счету " + prccontract.rec.Account +
                         " в связи с реклассификацией актива";
   НазначениеПроводки3 = "Перенос начисленных просроченных процентов на балансовый учет для договора " +
                          prccontract.rec.Number + " по счету " + prccontract.rec.Account +
                         " в связи с реклассификацией актива"; 
   НазначениеПроводки4 = "Отнесение на доходы начисленных просроченных процентов " +
                          prccontract.rec.Number + " по счету " + prccontract.rec.Account +
                         " в связи с реклассификацией актива"; 

   // Срочные проценты
   if ((AccountPc != "") and (AccRestPc > $0))

      Chapter1 = 3; 
   
      fd = PrcContractFD(ContractID);
      Debet1 = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        NULL,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            /* дата начала действия счета */
                                       );
      Debet1FIID = 0;

      Credit1 = AccountPc;
      Credit1FIID = FIID_Pc;           

      DebetTypeID = 1;
      CreditTypeID = 3;
      Chapter2 = 1;
      GetAccountFromList(ContractID, DebetTypeID, @Debet2, @Debet2CatID, @Debet2FIID, @Chapter2);  
      GetAccountFromList(ContractID, CreditTypeID, @Credit2, @Credit2CatID, @Credit2FIID);

      if (Debet1 == "") 
         msgbox("Не определен счет дебета по проводке для срочных процентов");
         stat = 1;
      end;

      if ((Debet2 == "") and (Debet2CatID == 0)) 
         TrnMsg = "Не определен счет дебета и не задана соотв.КУ для договора по проводке для срочных процентов";
         return 1;
      else
         if (Debet2 == "")
            ActuateAcc(Debet2CatID, @Debet2, @Debet2FIID, @Chapter2);
            if (Debet2 == "")
               TrnMsg = "Не удалось активизировать счет дебета| по проводке для срочных процентов";
               return 1;
            else
               PrcSetCntAcc(ContractID, DebetTypeID, Debet2FIID, Chapter2, Debet2);
            end;
         end;
      end;

      if ((Credit2 == "") and (Credit2CatID == 0))
         TrnMsg = "Не определен счет кредита и не задана соотв.КУ| для договора по проводке для срочных процентов";
         return 1;
      else
         if (Credit2 == "")
            ActuateAcc(Credit2CatID, @Credit2, @Credit2FIID, @Chapter2);
            if (Credit2 == "")
               TrnMsg = "Не удалось активизировать счет кредита| по проводке для срочных процентов";
               return 1;
            else
               PrcSetCntAcc(ContractID, CreditTypeID, Credit2FIID, Chapter2, Credit2);
            end;
         end;
      end;   

      if (Debet1FIID != FIID_Pc)
         ConvSum(Debet1CarrySum, AccRestPc, cntopr_buf.DocDate, FIID_Pc, Debet1FIID );
      else
         Debet1CarrySum  = AccRestPc;
      end;

      Credit1CarrySum  = AccRestPc;

      if (Debet2FIID != FIID_Pc)
         ConvSum(Debet2CarrySum, AccRestPc, cntopr_buf.DocDate, FIID_Pc, Debet2FIID );
      else
         Debet2CarrySum  = AccRestPc;
      end;

      if (Credit2FIID != FIID_Pc)
         ConvSum(Credit2CarrySum, AccRestPc, cntopr_buf.DocDate, FIID_Pc, Credit2FIID );
      else
         Credit2CarrySum  = AccRestPc;
      end;      

      if (stat == 0)

         GetAccountDepartment(Debet1, Chapter1,Debet1FIID, @Debet1Dep);
         GetAccountDepartment(Credit1,Chapter1,Credit1FIID,  @Credit1Dep);
         if (Debet1Dep == 0)
            Debet1Dep = {OperDprt};
         end;
         if (Credit1Dep == 0)
            Credit1Dep = {OperDprt};
         end;
         
         stat = Prc_DocCarry( 
                        Chapter1,                         // Глава
                        Debet1FIID,                // Дебет Валюта
                        Credit1FIID,               // Кредит Валюта
                        Debet1,                    // Дебет
                        Credit1,                   // Кредит
                        money(Debet1CarrySum),                // сколько
                        money(Credit1CarrySum),                // сколько
                        cntopr_buf.DocDate,        // За какое число
                        "09",
                        NULL,
                        0,                         // CarryResult
                        НазначениеПроводки1
                     );
      end;

      if (stat == 0)
         PrcSaveOperDoc(
                        DocID, 
                        DocNumber,
                        Chapter1,
                        Debet1, 
                        Debet1Dep,
                        Debet1FIID, 
                        money(Debet1CarrySum),
                        Credit1,
                        Credit1Dep,
                        Credit1FIID,
                        money(Credit1CarrySum),
                        НазначениеПроводки1);
         DocNumber = DocNumber + 1;
      end;
      
      if (stat == 0)

         GetAccountDepartment(Debet2, Chapter2,Debet2FIID, @Debet2Dep);
         GetAccountDepartment(Credit2,Chapter2,Credit2FIID,  @Credit2Dep);
         if (Debet2Dep == 0)
            Debet2Dep = {OperDprt};
         end;
         if (Credit2Dep == 0)
            Credit2Dep = {OperDprt};
         end;
      
         stat = Prc_DocCarry( 
                        Chapter2,                 // Глава
                        Debet2FIID,               // Дебет Валюта
                        Credit2FIID,              // Кредит Валюта
                        Debet2,                   // Дебет
                        Credit2,                  // Кредит
                        money(Debet2CarrySum),                // сколько
                        money(Credit2CarrySum),                // сколько
                        cntopr_buf.DocDate,        // За какое число
                        "09",
                        NULL,
                        0,                         // CarryResult
                        НазначениеПроводки2
                     );
      end;

      if (stat == 0)
         PrcSaveOperDoc(
                        DocID, 
                        DocNumber,
                        Chapter2,
                        Debet2, 
                        Debet2Dep,
                        Debet2FIID, 
                        money(Debet2CarrySum),
                        Credit2,
                        Credit2Dep,
                        Credit2FIID,
                        money(Credit2CarrySum),
                        НазначениеПроводки2);
         DocNumber = DocNumber + 1;
      end;
      
   end;

   if (stat != 0)
       return 1;
   end;      

   // Просроченные проценты
   if ((AccountPcOD != "") and (AccRestPcOD > $0))
      
      fd = PrcContractFD(ContractID);
      Debet3 = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        NULL,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            /* дата начала действия счета */
                                       );
      Credit3 = AccountPcOD;  
      Credit3FIID = FIID_PcOD;
      Chapter3 = 3;
      Debet3FIID = 0;
      
      DebetTypeID = 2;
      CreditTypeID = 4;
      Chapter4 = 1;
      GetAccountFromList(ContractID, DebetTypeID, @Debet4, @Debet4CatID, @Debet4FIID, @Chapter4);  
      GetAccountFromList(ContractID, CreditTypeID, @Credit4, @Credit4CatID, @Credit4FIID);

      if (Debet3 == "") 
         msgbox("Не определен счет дебета по проводке для просроченных процентов");
         stat = 1;
      end;

      if ((Debet4 == "") and (Debet4CatID == 0)) 
         TrnMsg = "Не определен счет дебета и не задана соотв.КУ для договора по проводке для просроченных процентов";
         return 1;
      else
         if (Debet4 == "")
            ActuateAcc(Debet4CatID, @Debet4, @Debet4FIID, @Chapter4);
            if (Debet4 == "")
               TrnMsg = "Не удалось активизировать счет дебета| по проводке для просроченных процентов";
               return 1;
            else
               PrcSetCntAcc(ContractID, DebetTypeID, Debet4FIID, Chapter4, Debet4);
            end;
         end;
      end;

      if ((Credit4 == "") and (Credit4CatID == 0))
         TrnMsg = "Не определен счет кредита и не задана соотв.КУ| для договора по проводке для просроченных процентов";
         return 1;
      else
         if (Credit4 == "")
            ActuateAcc(Credit4CatID, @Credit4, @Credit4FIID, @Chapter4);
            if (Credit2 == "")
               TrnMsg = "Не удалось активизировать счет кредита| по проводке для просроченных процентов";
               return 1;
            else
               PrcSetCntAcc(ContractID, CreditTypeID, Credit4FIID, Chapter4, Credit4);
            end;
         end;
      end;

      if (Debet3FIID != FIID_PcOD)
         ConvSum(Debet3CarrySum, AccRestPcOD, cntopr_buf.DocDate, FIID_PcOD, Debet3FIID );
      else
         Debet3CarrySum  = AccRestPcOD;
      end;

      Credit3CarrySum  = AccRestPcOD;

      if (Debet4FIID != FIID_PcOD)
         ConvSum(Debet4CarrySum, AccRestPcOD, cntopr_buf.DocDate, FIID_PcOD, Debet4FIID );
      else
         Debet4CarrySum  = AccRestPcOD;
      end;

      if (Credit4FIID != FIID_PcOD)
         ConvSum(Credit4CarrySum, AccRestPcOD, cntopr_buf.DocDate, FIID_PcOD, Credit4FIID );
      else
         Credit4CarrySum  = AccRestPcOD;
      end;


      if (stat == 0)

         GetAccountDepartment(Debet3, Chapter3,Debet3FIID, @Debet3Dep);
         GetAccountDepartment(Credit3,Chapter3,Credit3FIID,  @Credit3Dep);
         if (Debet3Dep == 0)
            Debet3Dep = {OperDprt};
         end;
         if (Credit3Dep == 0)
            Credit3Dep = {OperDprt};
         end;
         
         stat = Prc_DocCarry( 
                        Chapter3,                  // Глава
                        Debet3FIID,                // Дебет Валюта
                        Credit3FIID,               // Кредит Валюта
                        Debet3,                    // Дебет
                        Credit3,                   // Кредит
                        money(Debet3CarrySum),    // сколько
                        money(Credit3CarrySum),   // сколько
                        cntopr_buf.DocDate,        // За какое число
                        "09",
                        NULL,
                        0,                         // CarryResult
                        НазначениеПроводки3
                     );
      end;

      if (stat == 0)
         PrcSaveOperDoc(
                        DocID, 
                        DocNumber,
                        Chapter3,
                        Debet3, 
                        Debet3Dep,
                        Debet3FIID, 
                        money(Debet3CarrySum),
                        Credit3,
                        Credit3Dep,
                        Credit3FIID,
                        money(Credit3CarrySum),
                        НазначениеПроводки3);
         DocNumber = DocNumber + 1;
      end;
      
      if (stat == 0)

         GetAccountDepartment(Debet4, Chapter4,Debet4FIID, @Debet4Dep);
         GetAccountDepartment(Credit4,Chapter4,Credit4FIID,  @Credit4Dep);
         if (Debet4Dep == 0)
            Debet4Dep = {OperDprt};
         end;
         if (Credit4Dep == 0)
            Credit4Dep = {OperDprt};
         end;
      
         stat = Prc_DocCarry( 
                        Chapter4,                 // Глава
                        Debet4FIID,               // Дебет Валюта
                        Credit4FIID,              // Кредит Валюта
                        Debet4,                   // Дебет
                        Credit4,                  // Кредит
                        money(Debet4CarrySum),   // сколько
                        money(Credit4CarrySum),  // сколько
                        cntopr_buf.DocDate,        // За какое число
                        "09",
                        NULL,
                        0,                         // CarryResult
                        НазначениеПроводки4
                     );
      end;

      if (stat == 0)
         PrcSaveOperDoc(
                        DocID, 
                        DocNumber,
                        Chapter4,
                        Debet4, 
                        Debet4Dep,
                        Debet4FIID, 
                        money(Debet4CarrySum),
                        Credit4,
                        Credit4Dep,
                        Credit4FIID,
                        money(Credit4CarrySum),
                        НазначениеПроводки4);
         DocNumber = DocNumber + 1;
      end;
      
   end;

   if (stat != 0)
       return 1;
   end;            

   return 0;   
END;


MACRO ExecuteStep(Buffer, Document)
   var   CatID, stat = 0;

   SetBuff( cntopr_buf, Document );

   ContractID = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   prccntopr.KeyNum = 0;	
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      msgbox ("Не найдена операция");
      return 1;
   end; 

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;	
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      msgbox ("Не могу найти договор с ID = " + ContractID);
      return 1;
   end;

   FIID = prccontract.rec.FIID;
   OperDate = cntopr_buf.OperDate;
    
   // 2.0
   GetAccountFromList(ContractID, 5, @AccountPc, @CatID, @FIID_Pc);
   if (AccountPc != "")
      if ( FIID_Pc == NATCUR )
         AccRestPc = abs(RestA( AccountPc, OperDate, NULL, 3/*CHAPT3*/ ));
      else
         AccRestPc = abs(RestAC( AccountPc, FIID, OperDate, NULL, 3/*CHAPT3*/ ));
      end;   
   end;

   GetAccountFromList(ContractID, 6, @AccountPcOD, @CatID, @FIID_PcOD);
   if (AccountPcOD != "")
      if ( FIID_PcOD == NATCUR )
         AccRestPcOD = abs(RestA( AccountPcOD, OperDate, NULL, 3/*CHAPT3*/ ));
      else
         AccRestPcOD = abs(RestAC( AccountPcOD, FIID, OperDate, NULL, 3/*CHAPT3*/ ));
      end;   
   end;
   

   if ((AccRestPcOD != 0) or (AccRestPc != 0))
      if (DoOperationCarry() != 0)
         if (TrnMsg != "")
            msgbox(TrnMsg);
         end;
         return 1;      
      else
         PrcInsertClientQuality(ContractID, date(prccntopr.rec.DocDate + 1), prccntopr.rec.NewClientQuality);
         if (PrcReceiverAccountChange(ContractID, prccntopr.rec.DocID, prccntopr.rec.OperDate) != 0)
            return 1;
         end;
      end;
   else
      TrnMsg = "Не заданы лицевые счета учета процентов |'Срочные проценты для внебалансового учета' " +
               "|и 'Просроченные проценты для внебалансового учета' |или остатки по счетам нулевые";
      msgbox(TrnMsg);
      return 1;
   end;

   if (TrnMsg != "")
      msgbox(TrnMsg);
   end;
   
   return 0;   
END;



MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

