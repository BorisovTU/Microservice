/*
$Name:          xls_parser.mac
$Module:        Ядро ГКБО
$Description:   Класс для парсинга MS Excel файла
*/
Import rsexts;  // Здесь описан TDirList
import rcw;     // Здесь описан CreateObject

macro ConvertColNumberToLetter(ColNumb)
  var iCol = ColNumb;
  var a = iCol;
  var b = 0;
  var result = "";
  while (iCol > 0)
    a = int(floor(double((iCol - 1) / 26)));
    b = Mod((iCol - 1),26);
    result = StrFor(b + 65) + result;
    iCol = a;
  end;
  return result;
end;

Class CExcelPoi()
  private var workbook;
  private var reportCreator;
  private var jvm;
  
  Macro Open(filename: String)
    workbook = reportCreator.openWorkbook(filename);
    if (workbook == null) 
      return false;
    end;
    return true;
  end;
  
  Macro Close()
    if (workbook != null)
      workbook.close();
      workbook = null;
    end;
  end;
  
  Macro SheetChange(sheetId: Variant): Bool
    if (ValType(sheetId) == V_INTEGER)
      workbook.setActiveSheet(sheetId);
    elif (ValType(sheetId) == V_STRING)
      workbook.setActiveSheetByName(sheetId);
    else
      return false;
    end;
    
    return true;
    
    onError (mErr)
      return false;
  end;
  
  Macro GetLastRowNum(): Integer
    return workbook.getLastRowNum(workbook.getSheetName());
  end;

  macro CellValueByRowAndCol(RowNum,ColNum)
    return workbook.getCellValue(ConvertColNumberToLetter(ColNum)+String(RowNum));
  end;
  
  Macro CellValue(cellName: String): Variant
    return workbook.getCellValue(cellName);
  end;
  
  Macro GetWorkbookName(): String
    return workbook.getWorkbookName();
  end;

  macro Destructor()
    Close();
  end;
  
  //--------- Конструктор ----------
  jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  reportCreator = jvm.CallStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
end; //CExcelPoi

Class CPoiReader()
  private var reportReader;
  private var jvm;
  private var sheetName = "";

  Macro AddCellNameByRowAndCol(RowNum, ColNum)
    reportReader.addTargetCell(sheetName, ConvertColNumberToLetter(ColNum)+String(RowNum))                                                                         
  end;

  Macro AddCellName(cellName: String)
    reportReader.addTargetCell(sheetName, cellName)                                                                         
  end;

  Macro ClearTargetCell()
    reportReader.clearTargetCell();
  end;

  Macro Open(filename: String)
    reportReader.open(filename);

    return true;

    onError (mErr)
      return false;
  end;
  
  Macro Close()
    reportReader.close();
  end;
  
  Macro SheetChange(sheetId: Variant): Bool
    if (ValType(sheetId) == V_STRING)
      sheetName = sheetId;
    else
      return false;
    end; 
    
    return true;
    
    onError (mErr)
      return false;
  end;
  
  Macro GetLastRowNum(): Integer
    return reportReader.getLastRowNum(sheetName);
  end;

  macro CellValueByRowAndCol(RowNum,ColNum)
    return reportReader.readCellValue(sheetName, ConvertColNumberToLetter(ColNum)+String(RowNum));
  end;
  
  Macro CellValue(cellName: String): Variant
    return reportReader.readCellValue(sheetName, cellName);
  end;
  
  Macro GetWorkbookName(): String
    return reportReader.getWorkbookName();
  end;

  macro Destructor()
    reportReader.close();
  end;
  
  //--------- Конструктор ----------
  jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  reportReader = jvm.CallStaticMethod("ru.softlab.rsbank.poireports.ReportReader", "getInstance");
end; //CExcelPoi

Class CExcel()
  private var DisplayAlerts = NULL;

  var Application:object = NULL,
      Book       :object = NULL,
      Sheet      :object = NULL;

  // Константы
  const xlUp     :integer = -4162;
  const xlLastCell:integer = 11;       // Последняя ячейка

  macro CreateApplication()
    var axServer:object = NULL;

    if(isStandAlone())
      Application = ActiveX("Excel.Application", NULL, true);
    else
      axServer = CreateObject("rsax", "TRsAxServer", "RsAxServer", isStandalone());
      Application = axServer.CreateComObject("Excel.Application", true);
    end;

    return true;

    OnError( ObjError )
      println( "Для получения отчета в формате <Excel.Application>|необходимо установить соответствующее приложение! " );
      return false;
  end;

  /* Метод открывает файл с именем   FileName (полный путь), в случае успеха возвращает TRUE, иначе FALSE */
  macro Open( FileName:string ):bool
    DisplayAlerts = Application.DisplayAlerts; 
    Application.DisplayAlerts = FALSE; 
    //Application.ActiveProtectedViewWindow.Edit;

    Book = Application.Workbooks.Open(FileName);

    Sheet = Book.Sheets(1);
    return TRUE;

    OnError( Err )
      println("Ошибка открытия файла \"" + FileName + "\"");
      return FALSE;
  end; /* Open */

  /* Метод устанавливает текущим лист с индексом(наименованием) _Sheet, индексация начинается с 1 */
  macro SheetChange( _Sheet:variant )
    if( ValType(_Sheet) == V_STRING )
      //Sheet = Book.Sheets(_Sheet); 
      var i = 1, isFound = false;
      while( i <= Book.Sheets.Count )
        Sheet = Book.Sheets.Item(i);
        if( Sheet.Name == _Sheet )
          isFound = true;
          break;
        end;
        i = i + 1;
      end;
      if( isFound == false )
        println("Лист с именем <" + String(_Sheet) + "> отсутствует");
        return False;
      end;
    else
      if(_Sheet <= Book.Sheets.Count)
        Sheet = Book.Sheets.Item(_Sheet);
      else 
        println("Лист с номером " + String(_Sheet) + " отсутствует");
        return False;
      end;
    end;

    Sheet.Activate();
    return True;

    OnError( ObjErr )
      if( ValType(_Sheet) == V_STRING )
        println("Лист с именем <" + String(_Sheet) + "> отсутствует");
      else
        println("Лист с номером " + String(_Sheet) + " отсутствует");
      end;
      return false;
  end; /* SheetChange */

  /* Деструктор */
  Macro Destructor()
    if( (Application != NULL) AND (NOT Application.Visible) )
      Application.DisplayAlerts = FALSE;
      if( Application.Workbooks.Count AND Valtype(Book) )
        Book.Close(FALSE);
      end;
      Application.Quit;
      Book        = NULL;
      Application = NULL;
    elif(Application != NULL)
      if( ValType(DisplayAlerts) )
        Application.DisplayAlerts = DisplayAlerts;
      end;
    end;

    OnError( Err )
      return FALSE;
  End; /* Destructor */
end;

/*
macro ParsingXlsFile( objExcel:CExcel, FileName:string )
      if( objExcel.Open(FileName) )
          objExcel.SheetChange("0212_Свод"); // Активируем нужную вкладку отчета

          println(objExcel.Sheet.Range("A1").text, objExcel.Sheet.Range("C1").text);

          //Ищем нужные строки
          var i       :integer = 29; // Начнем искать с 29-ой строки
          var iLastRow:integer = objExcel.Sheet.Cells.SpecialCells(objExcel.xlLastCell).Row;
          var КодСтроки:string = "";
          while( i <= iLastRow )
              КодСтроки = objExcel.Sheet.Range("C"+string(i)).text;

              if( КодСтроки != "" )
                  println(objExcel.Sheet.Range("C"+string(i)).text, "=",objExcel.Sheet.Range("E"+string(i)).text);
              end;
              i = i + 1;
          end;
      end;
end;
*/
// Создаем объект для работы сCExcel
//var objExcel:CExcel = CExcel();
//    objExcel.CreateApplication();

// Перебираем файлы в каталоге PathFiles
//var PathFiles:string = "d:\\";
//var MaskFile :string = "*.xls*"; // могут быть xls, xlsx, xlsm
/*
var ListFiles:TDirList = TDirList;
ListFiles.List( PathFiles + MaskFile, "F"); // Перебираем все файлы в каталоге по маске
if( ListFiles.Count > 0 )
    var i = 0, k, j;
    while( i < ListFiles.Count )
        println(ListFiles.Name(i));
        ParsingXlsFile( objExcel, PathFiles + ListFiles.Name(i) );
        i = i + 1;
    end;
else
    println("Файлы по маске <",MaskFile,"> не найдены!!!");
end;*/