import BankInter;
import commonutil;
import rsd, oralib, likepy;

private macro _GetMaxPtRiskLevelForParty(PartyID : integer, onDate : date)
  var query;
  var rs;
  var params : TArray;
  var Number;

  CaptureOutput;
[
  SELECT NVL(MAX(ptrfk1.t_RiskLevel), 0)
    FROM dptriskfactorkind_dbt ptrfk1
   WHERE ptrfk1.t_Number IN
            (SELECT ptrf1.t_Kind
               FROM dptriskfactor_dbt ptrf1
              WHERE ptrf1.t_Status = 1
                    AND (ptrf1.t_PartyID, ptrf1.t_Kind, ptrf1.t_BeginDate) IN
                           (  SELECT ptrf2.t_PartyID,
                                     ptrf2.t_Kind,
                                     MAX (ptrf2.t_BeginDate)
                                FROM dptriskfactor_dbt ptrf2
                               WHERE ptrf2.t_PartyID = :PartyID /*42*/
                                     AND ptrf2.t_BeginDate <= :BeginDate /*TO_DATE ('01012013', 'DDMMYYYY')*/
                            GROUP BY ptrf2.t_PartyID, ptrf2.t_Kind))
         AND ptrfk1.t_Number IN
                (    SELECT ptrfk2.t_Number
                       FROM dptriskfactorkind_dbt ptrfk2
                 START WITH ptrfk2.t_ParentKind = 0
                 CONNECT BY PRIOR ptrfk2.t_Number = ptrfk2.t_ParentKind
                            AND ptrfk2.t_NotInUse = CHR(0))
         AND NOT EXISTS(SELECT 1 FROM dptriskfactorkind_dbt ptrfk3 WHERE ptrfk3.t_ParentKind = ptrfk1.t_Number)
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("PartyID", PartyID)
                    ,SQLParam("BeginDate", onDate)
                    );
  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    Number = rs.value(0);
  end;
  return Number;
end;

private macro _GetMinPtRiskLevel()
  var query;
  var rs;
  var params : TArray;
  var Number;

  CaptureOutput;
[
SELECT NVL(MIN(t_Element), 0)
  FROM dllvalues_dbt
 WHERE t_List = :List;
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("List", 3496)
                    );
  rs = execSQLselect(query, params, true);

  if(rs and rs.moveNext())
    Number = rs.value(0);
  end;
  return Number;
end;

//Функция определения уровня риска субъекта по факторам риска
macro GetPtRiskLevelByFactors(PartyID : integer, onDate : date, RiskLevelNumber : @integer)
  var stat = 0;
  var Number;
  
  if(PartyID == null)
    // Не задан обязательный параметр <ID субъекта>
    stat = 26114;
    CreateErrMsg(stat);
  end;

  if(not stat)
    Number = _GetMaxPtRiskLevelForParty(PartyID, onDate);
    if(Number == 0)
      Number = _GetMinPtRiskLevel();
    end;
    RiskLevelNumber = Number;
  end;

  return stat;
end;

  