 /*
 $Name: ws_imgdata.mac
 $Module: Главная книга
 $Description: Макрос Фото
 */

import rsd, CTInter;
import "ws_validation.mac";
import rsbdataset, oralib;
import "ws_partycommon.mac";

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

////////////////////////////////
// Объект-избражение
////////////////////////////////
class TWsImage
  var ImageID:integer;
  var ObjectType:integer;
  var ObjectID:integer;
  var ImageType:integer;
  var HexData:string;
  var PreviewData:string;
  var FileName;
  var FileExt;
  var ImgChunkID:integer;
  var Res:bool;
end;

class TWebImgChunkObject
  var ImgChunkID:integer;	
  var ChunkNum:integer;
  var ChunkSize:integer;
  var Blob:string;
end;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  if(objErr.stat != 0)
    ErrorMessage.Code     = objErr.stat;
    ErrorMessage.Message  = objErr.message;
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    ResponseBuilder.AddMessage( ErrorMessage );
  end;

end;

macro GetRecPartyByID(PartyID : integer);
  var cmd, rs;

  var Rec = TRecHandler("party");

  var query = "select * from dparty_dbt where t_partyid = " + string(PartyID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

  end;

  return Rec;
end;

private macro MakeFileName(ObjectID, Ext)

  var day, month, year;
  var hh, mm, ss;

  DateSplit (GetDBDate(), day, month, year);
  if (day < 10)
    day = String ("0"+day);
  else
    day = String (day);
  end;
  if (month < 10)
    month = String ("0"+month);
  else
    month = String (month);
  end;

  TimeSplit (GetDBTime, hh, mm, ss);
  if (hh < 10)
    hh = String ("0"+hh);
  else
    hh = String (hh);
  end;
  if (mm < 10)
    mm = String ("0"+mm);
  else
    mm = String (mm);
  end;
  if (ss < 10)
    ss = String ("0"+ss);
  else
    ss = String (ss);
  end;

  var res = string(ObjectID) + "-" + year + month + day + "-" + hh + mm + ss + "." + Ext;

  return res;

end;

macro CheckImage(ObjectType, ObjectID, ImageType, ImageStr, FileName, Ext, ImgChunkID)

  var res = True;

  var FileNameStr;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var RecParty = GetRecPartyByID(ObjectID);

  var ObjectIDStr = UniID(RecParty, ObjectType);

  if(FileName == null)

    FileNameStr = MakeFileName(ObjectID, Ext);

  else

    FileNameStr = FileName;

  end;

  res = WEB_CheckImage(ObjectType, ObjectIDStr, ImageType, ImageStr, FileNameStr, ImgChunkID);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro SaveImage(ObjectType, ObjectID, ImageType, ImageStr, FileName, Ext, ImgChunkID, ThumbnobjStr)

  var p:TWsImage;

  p.Res = True;

  var FileNameStr;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  var RecParty = GetRecPartyByID(ObjectID);

  var ObjectIDStr = UniID(RecParty, ObjectType);

  if(FileName == null)

    FileNameStr = MakeFileName(ObjectID, Ext);

  else

    FileNameStr = FileName;

  end;

  p.Res = WEB_SaveImage(ObjectType, ObjectIDStr, ImageType, ImageStr, ThumbnobjStr, FileNameStr, ImgChunkID, p.ImageID);

  if(p.Res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(p);

  return responseBuilder.Result;

end;

macro CloseImageExt(ImageID)

  var res = True;

  res = WEB_DeleteImage(ImageID);

  return res;

end;

macro CloseImage(ImageID)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CloseImageExt(ImageID);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

///////////////////////////////////////////////////////////
// получить картинку-изображение клиента в HEX-строке
///////////////////////////////////////////////////////////
MACRO GetImage(ObjectType:integer, ObjectID:integer, ImageType:integer, ImageID:integer):TWsImage

  var query = "";
  var ds;

  var p:TWsImage;

  if ((ValType(ImageID) == V_UNDEF) or (ImageID <= 0))
    
    if ((ValType(ImageType) == V_UNDEF) or (ImageType <= 0))

      var query1 = " select t.t_ImageType "
                   "  from dimgtype_dbt t "
                   " where "
                   "  t.t_objecttype = " + string(ObjectType) +
                   "  and t.t_isdefault = 'X' ";

      ds = TRsbDataSet(query1, RSDVAL_CLIENT, RSDVAL_STATIC);

      if( ds.MoveNext() )
        ImageType = ds.ImageType;
      end;

    else

      query = " select d.*, RSB_PRDCLIENT.BlobToHex(T_FMTBLOBDATA_XXXX) as HexData "
              "  from dimage_dbt i,dimgdata_dbt d "
              " where "
              "  i.t_imageid = d.t_imageid and "
              "  d.t_objecttype = " + string(ObjectType) +
              "  and d.t_imagetype = " + ImageType +
              "  and d.t_objectid = " + string(ObjectID) +
              "  and d.t_closedate = TO_DATE ('01010001', 'DDMMYYYY') ORDER BY d.t_IsMain desc ";

      ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

      if( ds.MoveNext() )

        p.ImageID    = ds.ImageID;
        p.ObjectType = ds.ObjectType;
        p.ObjectID   = ds.ObjectID;
        p.ImageType  = ds.ImageType;
        p.HexData    = ds.HexData;

      else

        return null;

      end;

    end;  

  else

    query = " select d.*, RSB_PRDCLIENT.BlobToHex(T_FMTBLOBDATA_XXXX) as HexData "
            "  from dimage_dbt i,dimgdata_dbt d "
            " where "
            "  I.T_IMAGEID = " + string(ImageID) +
            "  and I.T_IMAGEID = D.T_IMAGEID "
            "  and d.T_CLOSEDATE = TO_DATE ('01010001', 'DDMMYYYY') ";

    ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if( ds.MoveNext() )

      p.ImageID    = ds.ImageID;
      p.ObjectType = ds.ObjectType;
      p.ObjectID   = ds.ObjectID;
      p.ImageType  = ds.ImageType;
      p.HexData    = ds.HexData;

    else

      return null;

    end;

  end;

  return p;

end;

macro WebCore_GetImgChunkObject(ImgSize)

  var chunkObj:TWebImgChunkObject;
  
  var res = 0;

  var err;

  GetRegistryValue( "CB\\PARTY\\IMAGEDATA\\MAXCHUNKSIZE", V_INTEGER, res, err );

  if(err == 0)
    
    chunkObj.ChunkSize = res;
    
    var os = Mod(ImgSize, chunkObj.ChunkSize);
    
    if(os != 0)
      
      chunkObj.ChunkNum = ((ImgSize - os)/res) + 1;
      
    else  
      
        chunkObj.ChunkNum = ImgSize/res;
    
    end;

  else
    RunError("Ошибка при чтении настройки реестра!");
  end;
  
  if(chunkObj.ChunkNum > 1)
    
    var query = " SELECT DIMGCHUNK_DBT_SEQ_ID.NEXTVAL FROM dual ";

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if( ds.MoveNext() )

      chunkObj.ImgChunkID = ds.nextval;

    end;  
    
  end;
  
  return chunkObj;

end;

macro WebCore_InsImgChunkObject(ImgChunkObj)

  var res = True;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = WEB_InsertImgChunk(ImgChunkObj.ImgChunkID, ImgChunkObj.ChunkNum, ImgChunkObj.Blob);

  if(res != True)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;  

end;