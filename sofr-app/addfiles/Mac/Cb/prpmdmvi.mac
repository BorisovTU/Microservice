/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmdmvi.mac                                       */
/*                                                                      */
/*  Описание       : Печать платежного требования (WR)                  */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 22.09.04                                           */
/*                                                                      */
/************************************************************************/

import prpm, gnd120p;
import "BnkTemplReport.mac";


class (BnkTemplReport) PayDemand0401061Report(PayerP:TPartyProperties, ReceiverP:TPartyProperties)
   
   var PayerProps = PayerP;
   var ReceiverProps = ReceiverP;
   
   var paym   :TRecHandler = TRecHandler( "pmpaym.dbt",   "bank.def" );
   var debet  :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var credit :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var rmprop :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   var payord :TRecHandler = TRecHandler( "pspayord.dbt", "bank.def" );
   var paydem :TRecHandler = TRecHandler( "pspaydem.dbt", "bank.def" );
   var pmdem  :TRecHandler = TRecHandler( "pmdemand.dbt", "bank.def" );
   
   copy( paym   , pr_pmpaym   );
   copy( debet  , pr_debet    );
   copy( credit , pr_credit   );
   copy( rmprop , pr_pmrmprop );
   copy( payord , pr_pspayord );
   copy( paydem , pr_pspaydem );
   copy( pmdem  , pr_pmdemand );


   macro Create()

     var AcceptTerm    :string  = "",
         AcceptPeriod  :integer = 0, 
         PayDate       :date    = date(0, 0, 0);
       
     SetSheetName( rmprop.rec.Number );

     if( paym.rec.DocKind == PS_PAYORDER )
       AcceptPeriod = paydem.rec.AcceptPeriod;
       if( (payord.rec.DocKind == PSPOKIND_DEMAND ) or 
           (payord.rec.DocKind == PSPOKIND_REQUEST) )
         if(paydem.rec.AcceptTerm == 0)
            AcceptTerm = "с акцептом";
            PayDate    = paydem.rec.AcceptDate;
         else
            AcceptTerm = "без акцепта. " + StrUpr(Trim(paydem.rec.PayCondition), 1);
         end;
       else
         PayDate = rmprop.rec.PayDate;
       end;
     elif( ( pmdem.rec.AcceptDate != date(0, 0, 0) ) and ( pmdem.rec.AcceptTerm == 0 ) )
       AcceptTerm = "с акцептом";
       PayDate    = pmdem.rec.AcceptDate;
     else
       AcceptTerm = "Без акцепта";
     end;

     var s:string;
     s = string(paym.rec.Amount);
     StrSet(s, StrBrk(s, ".,"), "-");
     var pINN, rINN;
     splitINN_KPP(PayerProps.INN, pINN);
     splitINN_KPP(ReceiverProps.INN, rINN);

     //Заполнение отчета
     SetValues( "Ground", InterpretGround( rmprop.rec.Ground ),
                "DateInto", string(paym.rec.PayerBankEnterDate:f), 
                "ChargeOffDate", string(rmprop.rec.PayerChargeOffDate),
                "Number", rmprop.rec.Number,
                "Data", string(rmprop.rec.Date:f),
                "PaymentKind", GetPaymentKind(rmprop.rec.PaymentKind),
                "PayDate", string(PayDate:f),
                "AcceptTerm", AcceptTerm,
                "AcceptPeriod", AcceptPeriod,
                "AmountInLine", RubToStrAlt(paym.rec.Amount),
                "Amount", s,
                "pINN", pINN,
                "pName", PayerProps.Name,
                "pAccount", string(PayerProps.Account),
                "pBankName", PayerProps.BankName,
                "pBankBIC", PayerProps.BankBIC,
                "pBankAccount", string(PayerProps.BankCorrAccount),
                "rBankName", ReceiverProps.BankName,
                "rBankBIC", ReceiverProps.BankBIC,
                "rBankAccount", string(ReceiverProps.BankCorrAccount),
                "rINN", rINN,
                "rName", ReceiverProps.Name,
                "rAccount", string(ReceiverProps.Account),
                "ShifrOper", rmprop.rec.ShifrOper,
                "ShifrNazn", "",
                "ShifrCode", IfThenElse(rmprop.rec.Date < Date(31,3,2014), "", rmprop.rec.UIN),
                "Priority", rmprop.rec.Priority,
                "RezPole", "",
                "I2PlaceDate", string(paym.rec.I2PlaceDate),
                "PayerBankMarkDate", string(paym.rec.PayerBankMarkDate)
              );
     
     var partPayList :TPartPayList;
     var table:object;
     var i = 0;
     
     if( paym.rec.DocKind == PS_PAYORDER )
       partPayList  = TPartPayList(paydem.rec.OrderID);
     end;

     if((partPayList != null) and (partPayList.size > 0))
       table = RegisterTable("PartPayments");
       while( i < partPayList.size )
         table.SetValueCell("PartCount",  string(partPayList.Value(i).SubPurp));  
         table.SetValueCell("PartNumber", partPayList.Value(i).Number);           
         table.SetValueCell("PartPayDate", string(partPayList.Value(i).PayDate:f));
         table.SetValueCell("PartAmount", string(partPayList.Value(i).Amount:f)); 
         table.SetValueCell("PartRest", string(partPayList.Value(i).Rest:f));   
         table.AddStr();
         i = i + 1;
       end;
       table.EndTable();
     end;

   end;

   initBnkTemplReport("PayDemand0401061.xls");

end;


MACRO PrintPayDemRSF():bool   

  var PayerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  return PayDemand0401061Report(PayerProps, ReceiverProps).Print();
END;

