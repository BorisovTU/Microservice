 /*
 $Name: bb_cash.mac
 $Module: Бухгалтерия банка Banking
 $Description: Макрос скроллинга кассовых документов ББ
 */
//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: Макрос скроллинга
// Описание  : Макрос скроллинга кассовых документов ББ
//-----------------------------------------------------------------------------

import BankInter, globals, check117, CashInter, PaymInter, "pm_opr.mac", "pmchoper.mac", "pm_tools.mac", pm_chksave, pmedit, pmbuff;

/*номера полей в панели*/
const fld_Number    = 1,
      fld_Date      = 2,
      fld_DateValue = 3, 
      fld_Ground    = 21; /*SMR 18.07.02*/

// Проверка на числовой номер
MACRO IsDigitNumber( Number )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Number)) )
        ch = SubStr( Number, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1;
        end;
        i = i + 1;
      end;

  return stat;

END;

// Сгенерировать номер
MACRO СгенерироватьНомер( ):integer
  var stat:integer = 0; 
  var NumberDoc:string = r_pmrmprop.Number;
  
  if( CashGenerateNumber( NumberDoc ) )
    msgbox("Ошибка генерации номера документа");
  else
    r_pmrmprop.Number = NumberDoc;
  end;

  return stat;
END;

MACRO Новый_Документ( )
/*                r_pmpaym.dockind           = 440;
                r_pscshdoc.dockind        = 440;*/
  return 0;
END;

//-----------------------------------------------------------------------------
//  Счет является маской
//-----------------------------------------------------------------------------
private macro AccountIsMask( Account ):bool
  if( Index( Account, "*" ) or
      Index( Account, "?" ) )
    return true;
  else
    return false;
  end;
end;

MACRO Проверить_Документ( Режим )

  var stat = 0, 
      saveNullNumber = false,
      CHANG_IMPORTANT = -11,
      CHANG_NOTIMPORTANT = -10;
  var ErrStr:string = "";
  var Payment:RsbPayment = RsbPayment(r_pmpaym.PaymentID);
  record debet_rec ( "pmprop.dbt" );
  record credit_rec ( "pmprop.dbt" );
  ClearRecord( debet_rec );
  debet_rec.PaymentID = credit_rec.PaymentID = r_pmpaym.PaymentID;
  debet_rec.Group     = credit_rec.Group     = PAYMENTS_GROUP_INTERNAL;
  debet_rec.IsSender  = credit_rec.IsSender  = "X";

  // Проверяем номер для всех документов, в том числе и отложенных
  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) 
  
    if( r_pmrmprop.Date == date(0,0,0) )
      msgbox("Дата документа должна быть задана");
      return fld_Date;
    end;
    if( r_pmpaym.ValueDate < r_pmrmprop.PayDate )
      msgbox( "Дата валютирования не должна быть|меньше даты платежа" );
      return fld_DateValue;
    end;

    if( StrLen( r_pmrmprop.Number ) == 0 )

      if( RsbGetTrue( true, false, "Не задан номер документа.|Сохранить документ?" ) == false )
        if( not GetDialogFlag() )
          MsgBox("Не задан номер документа");
        end;
        return fld_Number;
      else
        saveNullNumber = true;
      end;
    
    end;

    if( not r_pscshdoc.IsCurrency ) // Проверки для рублевых
      if( IsDigitNumber( r_pmrmprop.Number ) )
         MsgBox("Номер документа нечисловой");
         return fld_Number;
      elif( not saveNullNumber )
         if( int( r_pmrmprop.Number ) == 0 )
            MsgBox("Номер документа не может быть нулевым");
            return fld_Number;
         end;
      end;

      /* Проверка убрана по запросу 155530.
         Ждём, когда она снова кому-нибудь понадобится.
      if( SubStr( r_pmrmprop.Number, StrLen( r_pmrmprop.Number )-2 ) == "000" )
         MsgBox("Три последних разряда номера должны быть отличны от '000'");
         return fld_Number;
      end;
      */
    end;

    if( StrLen( r_pmrmprop.Ground ) == 0 )
      MsgBox("Введите Назначение платежа");
      return fld_Ground;
    end;
    
    if((r_pmpaym.DocKind != CASH_BOF_INCORDER) or (Payment.PIList(1).Size <= 0))
      if(r_pmpaym.ReceiverAccount == "")
        MsgBox("Не задан счет получателя");
        return 1;
      elif(Index( r_pmpaym.ReceiverAccount, "*" ) or Index( r_pmpaym.ReceiverAccount, "?" ))
        MsgBox("Нельзя задавать счет получателя маской");
        return 1;
      end;
    end;
   
    ErrStr = PM_CheckPaymAccounts( r_pmpaym, NULL, NULL, NULL, 1 );
    if( strlen(ErrStr) > 0 )
      msgbox( ErrStr );
      return 1;
    end;

    /* Общие проверки по списку */
    stat = CS_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, debet_rec, credit_rec, r_pmrmprop );
    if( stat != NOTERROR )
      return stat;
    end;

    /*проверка реквизитов валютной операции*/
    if(PM_CheckCO(r_pmpaym,r_pmrmprop,0,0))
       return 1;
    end;

    // Проверить по 117-И
    if( CheckOnSave_117( r_pmpaym, NULL, NULL, NULL ) )
      return fld_Number;
    end;

  end;

  if( Режим == 1 ) // УДАЛЕНИЕ ДОКУМЕНТА
    if(not isDLMRuning())
      if( r_pscshdoc.Origin == CASH_FDOC_LOANS ) 
        if(not Index( "Ц", StrFor(GetIdentProgram())))
          MsgBox("Документ порoжден в п/с \"Кредитование\".| Корректировать (удалять) запрещено.");
          return 1;
        end;
      elif( r_pscshdoc.Origin == CASH_FDOC_DEPOSIT ) 
        if(not Index( "Д", StrFor(GetIdentProgram())))
          MsgBox("Документ порoжден в п/с \"Депозиты\".| Корректировать (удалять) запрещено.");
          return 1;
        end;
      elif( r_pscshdoc.Origin == CASH_FDOC_RETAIL )
        if(not Index( "ВБD", StrFor(GetIdentProgram())))
          MsgBox("Документ порoжден в п/с \"Обслуж.физ.лиц\".| Корректировать (удалять) запрещено.");
          return 1;
        end;
      elif( r_pscshdoc.Origin != CASH_FDOC_MANUAL )
         MsgBox("Документ порoжден в БЭК-офисе.| Корректировать (удалять) запрещено."); 
         return 1;
      end;
    end;

     if(CheckDeletePayment(r_pmpaym.PaymentID))
       return 1;
     end;
  end;

  if(Режим == 3)  // РЕДАКТИРОВАНИЕ ДОКУМЕНТА

    stat = StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, NULL, NULL, r_pmrmprop), 
        TCheckUpdateParam(r_pmpaym_old, NULL, NULL, r_pmrmprop_old));

    if (stat != CHANG_NOTKEEP)
         
      if( ( Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "К" ) Or 
            Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "А" ) Or
            Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "Е" ) Or
            Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "е" ) ) 
          And
          ( r_pmpaym.PaymStatus != PM_REJECTED) )

         if ( 
            (r_pscshdoc_old.Series          != r_pscshdoc.Series         ) or
            (r_pscshdoc_old.FIOClient       != r_pscshdoc.FIOClient      ) or
            (r_pscshdoc_old.PaperSeries     != r_pscshdoc.PaperSeries    ) or
            (r_pscshdoc_old.PaperNumber     != r_pscshdoc.PaperNumber    ) or
            (r_pscshdoc_old.PaperIssuedDate != r_pscshdoc.PaperIssuedDate) or
            (r_pscshdoc_old.PaperIssuer     != r_pscshdoc.PaperIssuer    ) or
            (r_pscshdoc_old.Oper            != r_pscshdoc.Oper           ) or
            (r_pscshdoc_old.Kind_Operation  != r_pscshdoc.Kind_Operation )
            )                   
               stat = CHANG_NOTKEEP;
         end;
      end;
    end;
         
  end;   

  if( Режим == 7 ) // ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ
  end;
  if( Режим == 8 )  // ВВОД В ОТЛОЖЕННЫЕ
  end;
  
  
  if(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperBbCashOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_pscshdoc, r_pscshdoc_old);
  end;

  return stat;
END;

/* Установка подсказки для скролингов из макроса */

/* Хинт для списков по дате значения */
private const Hint_ByValueDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) INDEX(t dpscshdoc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

/* Хинт для списков по дате закрытия */
private const Hint_ByCloseDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) INDEX(t dpscshdoc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

/* Хинт для списков по статусу первички */
private const Hint_ByStatus:string = 
"/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper oprcurst) INDEX(t dpscshdoc_idx4) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper oprcurst)*/";

/* Хинт для списков по символу шага */
private const Hint_ByStep     :string = 
"/*+FIRST_ROWS LEADING(oprstep oproper t pmpaym pmrmprop oprcurst) INDEX(oprstep doprstep_dbt_idx10) INDEX(t dpscshdoc_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(oprstep oproper t pmpaym pmrmprop oprcurst)*/";

MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string

  // Возможные значения ScrolStates:
  //   0 Все
  //   1 Отложенные
  //   2 Открытые
  //   3 Закрытые
  //   4 Отвергнутые
  //   5 Подготовленные
  
  if(  ( ScrolStates == 0 )   // Все
    or ( ScrolStates == 3 ) ) // Закрытые

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByCloseDate;
    end;

  elif(  ( ScrolStates == 1 )   // Отложенные
      or ( ScrolStates == 2 )   // Открытые
      or ( ScrolStates == 4 ) ) // Отвергнутые

    return Hint_ByStatus;

  elif ( ScrolStates == 5 ) // Подготовленные

    return Hint_ByStep;

  end;

  return DefaultHint;

END;

MACRO Проверить_Счет_В_Документе( поле ) /*0-счет кассы, 1-счет клиента*/
  var новое_значение_счета:string = "";

  if( поле ) /* счет клиента */
     if( r_pscshdoc.DocKind == 430 ) /*  Объявление на взнос наличными в ББ  */
       новое_значение_счета = r_pmpaym.ReceiverAccount;
     end;
     /* Ордер подкрепления в ББ или Чек в ББ */
     if( (r_pscshdoc.DocKind == 400) or (r_pscshdoc.DocKind == 440 ) ) 
       новое_значение_счета = r_pmpaym.PayerAccount;
     end;
  else 
     if( r_pscshdoc.DocKind == 430 ) /* Объявление на взнос наличными в ББ */
       новое_значение_счета = r_pmpaym.PayerAccount;
     end;
     /* Ордер подкрепления в ББ или Чек в ББ */
     if( (r_pscshdoc.DocKind == 400) or (r_pscshdoc.DocKind == 440) ) 
       новое_значение_счета = r_pmpaym.ReceiverAccount;
     end;
  end;

  return новое_значение_счета;
END;

MACRO Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
 return 0;
END;


