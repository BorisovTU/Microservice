/*
 $Name: fm4077u_out_lib.mac
 $Module: Ядро ГКБО 
 $Description: ФМ.4077-У: Формирование сообщения об отказах: выполнение операции 
 */
 /*
  ФМ.4077-У: Формирование сообщения об отказах: выполнение операции 
 */
import Rsd, BankInter;
import PTInter, CTInter, oralib, likepy, globals;
import FMInter, "fmreqi_lib.mac", "FmXmlWriter.mac";
import rsexts;

file rjsrvmsgidprt("fmrjsrvmsgidprt.dbt") key 1;

const FSFM_RJMSGSEQ : integer = 274; 
const FMRJSRVMSG_VER_1_1 : string = "1.1";
const FMRJSRVMSG_VER_1_2 : string = "1.2";
const FMRJSRVMSGVERSIONDEFAULT : string = FMRJSRVMSG_VER_1_1;
private var fRunForOperation = false;

class TRlBackRjMsg_Out_Prm
    var xml : FmXmlDocumentWriter = FmXmlDocumentWriter();
    var root : FmXmlNodeElement;
    var MsgBuf;
end;

macro setRunForOperation(value)
    fRunForOperation = value;
end;

macro WriteXmlNodeObli(node : @FmXmlNodeElement, ElementName : string, value, isObli)
    if (fRunForOperation)
        if ((isObli == false) and (ValType(value) != V_UNDEF) and (ZeroValue(ValType(value)) != value) and (value != ""))
            node.addChildEx(ElementName, value);
        end;
    else    
        node.addChildEx(ElementName, value);
    end;
end;

macro GetRjSrvMsgStringType(MsgBuf)
    var kind = "01";
    
    if (MsgBuf.MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)
        kind = "02"
    end;
    
    return kind;
end;

macro GetNameLLValuesFromCode(List, Code)
    var DocKindName = Tbfile ("llvalues.dbt");
    DocKindName.keynum = 1;
    DocKindName.rec.List = List;
    DocKindName.rec.Code = Code;
    DocKindName.GetEQ();
    
    return DocKindName.rec.Name;
end;

macro GetNameLLValuesFromElemnt(List, Element, Code : @string)
    var DocKindName = Tbfile ("llvalues.dbt");
    DocKindName.keynum = 0;
    DocKindName.rec.List = List;
    DocKindName.rec.Element = Element;
    DocKindName.GetEQ();
    
    Code = DocKindName.rec.Code;
    return DocKindName.rec.Name;
end;

private macro FormatOKCM(code)
    var cmd = RsdCommand("SELECT T_CODENUM3 FROM DCOUNTRY_DBT WHERE T_CODELAT3 = ?");
    cmd.addParam("", RSDBP_IN, code);
	cmd.execute();
    
    var result : string = code;
    var rs = RsdRecordset(cmd);
    
    if (rs.MoveNext())
        result = rs.value(0);
    end;

    return result;
end;

private macro LPAD2(val, str)
    return StrSubst(String(val:2), " ", str)
end;

macro LPAD4(val, str)
    return StrSubst(String(val:4), " ", str)
end;

private macro DeleteLeading(str)
  var i = 0;
  var fZero = true;
  while((i < strlen(str)) AND (fZero))
    if (substr(str,i + 1, 1) != "0")
      fZero = false;
    end;
    i = i + 1;
  end;

  return SubStr(str, i);
end;

macro FormatDate(DatePrm)
    var str = "";

    var day = 0, mon = 0, year = 0;
    DateSplit(DatePrm, day, mon, year);
    str = String(LPAD2(day, "0"), "/", LPAD2(mon, "0"), "/", LPAD4(year, "0"));
    
    return str;
end;

macro FormatRecNumber(Mask)
    var RecNumber = Mask;

    var pos = Index(Mask, "?") - 1;
    if (pos > 0)
        var MaskLen = StrLen(Mask) - pos;
        var SeqVal = 0;
        GetSequenceValue(FSFM_RJMSGSEQ, SeqVal);
        
        RecNumber = SubStr(Mask, 1, StrLen(Mask) - MaskLen) + SubStr(StrSubst(String(SeqVal:100), " ", "0"), 100 - MaskLen + 1);
    end;
    
    return RecNumber;
end;

macro FM_Out_RjSrvMsgCommonInfoProxy(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("ФИОУполнСотрудн");
    node.addChildEx("Фам", PPrm.MsgBuf.ProxyName1);
    node.addChildEx("Имя", PPrm.MsgBuf.ProxyName2);
    
    if (PPrm.MsgBuf.ProxyName3 != "")
        node.addChildEx("Отч", PPrm.MsgBuf.ProxyName3);
    end;
end;

// Формирование блока "Служебная информация ОЭС"
macro FM_Out_RjSrvMsgCommonInfo(PPrm : TRlBackRjMsg_Out_Prm)
    var stat = true;
    var xmlWriter : FmXmlDocumentWriter = @PPrm.xml;
    
    var rootName = "";
    
    if (PPrm.MsgBuf.MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)
        rootName = "СообщОтказВып";
    elif(PPrm.MsgBuf.MsgKind == RJSRVMSG_MSGKIND_REJ_ACC_CNTR)
        rootName = "СообщОтказРастор";
    elif(PPrm.MsgBuf.MsgKind == RJSRVMSG_MSGKIND_TERM_ACC_CNTR)
        rootName = "СообщОтказРастор";
    end;

    PPrm.root = xmlWriter.getRootElement(rootName, PPrm.root);
    var service : FmXmlNodeElement;
    service = PPrm.root.addChild("СлужЧасть");
    
    service.addChildEx("ВерсияФормата", PPrm.MsgBuf.Version);
    service.addChildEx("ВерсПрог", "RS-Bank " + _GetDBVersion());
    service.addChildEx("ТипИнф", GetRjSrvMsgStringType(PPrm.MsgBuf));
    service.addChildEx("БИККОНапрв", PPrm.MsgBuf.BIC);
    service.addChildEx("ДатаСообщения", FormatDate(PPrm.MsgBuf.EMessageDate));
    service.addChildEx("УполнСотрудн", PPrm.MsgBuf.ProxyPost);
    
    FM_Out_RjSrvMsgCommonInfoProxy(PPrm, service);
    
    service.addChildEx("ТелУполнСотрудн", PPrm.MsgBuf.ProxyPhone);
    
    if (strlen(PPrm.MsgBuf.ProxyEMail))
        service.addChildEx("ЭлектроннаяПочта", PPrm.MsgBuf.ProxyEMail);
    end;
    
    return stat;
end;

macro ИнфБанк(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("ИнфБанк");
    node.addChildEx("РегНомКО", DeleteLeading(PPrm.MsgBuf.KGRKO_BankNumber));
    node.addChildEx("БИККО", PPrm.MsgBuf.BIC_BankDprt);
    
    if ((PPrm.MsgBuf.KGRKO_BankDprt) != 0 AND (PPrm.MsgBuf.KGRKO_BankDprt != {NumDprt}))
        node.addChildEx("НомФлПередающий", DeleteLeading(PPrm.MsgBuf.KGRKO_BankDprt));
    end;
    node.addChildEx("ОКАТОКО", PPrm.MsgBuf.OKATO_BankDprt);
end;

macro ИнфФилиал(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("ИнфФилиал");
    node.addChildEx("НомФл", DeleteLeading(rjsrvmsgidprt.KGRKO_BankDprt));
    node.addChildEx("БИКФл", rjsrvmsgidprt.BIC_BankDprt);
    node.addChildEx("ОКАТОФл", rjsrvmsgidprt.OKATO_BankDprt);
end;

private macro GetCountryNameFromISOCode(CountryISOCode)
    var Name = "";
    var cmd = RsdCommand("SELECT T_NAME FROM DCOUNTRY_DBT WHERE T_CODENUM3 = ?  AND T_CODENUM3 <> CHR(1)");
    cmd.NullConversion = true;
    cmd.AddParam ("", RSDBP_IN, CountryISOCode);
        
    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        Name = rs.value(0);
    end;
        
    return Name;
end;

macro Адрес(PPrm : TRlBackRjMsg_Out_Prm, Parent, NodeName, AddressID)
    var fmaddress = Tbfile("fmaddress.dbt");
    fmaddress.keynum = 0;
    fmaddress.rec.ID = AddressID;

    if ((fmaddress.GetEQ()) and ((fmaddress.rec.CountryISOCode != "") or (fmaddress.rec.CountryISOCode != "") 
        or (fmaddress.rec.OKATO != "") or (fmaddress.rec.DistrictName != "") or (fmaddress.rec.PlaceName != "")
        or (fmaddress.rec.StreetName != "") or (fmaddress.rec.House != "") or (fmaddress.rec.Corps != "")
        or (fmaddress.rec.Flat != ""))
    )
        var node = Parent.addChild(NodeName);
        WriteXmlNodeObli(node, "КодОКСМ", FormatOKCM(fmaddress.rec.CountryISOCode), false);
        WriteXmlNodeObli(node, "СтранаНаименование", GetCountryNameFromISOCode(fmaddress.rec.CountryISOCode), false);
        WriteXmlNodeObli(node, "КодСубъектаПоОКАТО", fmaddress.rec.OKATO, false);
        WriteXmlNodeObli(node, "Район", fmaddress.rec.DistrictName, false);
        WriteXmlNodeObli(node, "Пункт", fmaddress.rec.PlaceName, false);
        WriteXmlNodeObli(node, "Улица", fmaddress.rec.StreetName, false);
        WriteXmlNodeObli(node, "Дом", fmaddress.rec.House, false);
        WriteXmlNodeObli(node, "Корп", fmaddress.rec.Corps, false);
        WriteXmlNodeObli(node, "Оф", fmaddress.rec.Flat, false);
    end;
end;

macro УчредительPart(PPrm : TRlBackRjMsg_Out_Prm, Parent, RjSrvFndrID)  
    var rjsrvpartfndr = Tbfile ("fmrjsrvpartfndr.dbt");
    rjsrvpartfndr.keynum = 0;
    rjsrvpartfndr.rec.ID = RjSrvFndrID;
    
    if(rjsrvpartfndr.GetEQ())
        var node = Parent.addChild("Учредитель");
        var node2 = node.addChild("НаимУчредитель");
        
        node2.addChildEx("ЮрЛицо", rjsrvpartfndr.rec.Name);
        Адрес(PPrm, node, "АдрУчредитель", rjsrvpartfndr.rec.AddressID);
    end;
end;

macro УчредительPers(PPrm : TRlBackRjMsg_Out_Prm, Parent, RjSrvFndrID)  
    var rjsrvpersfndr = Tbfile ("fmrjsrvpersfndr.dbt");
    rjsrvpersfndr.keynum = 0;
    rjsrvpersfndr.rec.ID = RjSrvFndrID;
    
    if(rjsrvpersfndr.GetEQ())
        var rjsrvpersfndrnode = Parent.addChild("Учредитель");
        var rjsrvpersfndrnode2 = rjsrvpersfndrnode.addChild("НаимУчредитель");
        var rjsrvpersfndrnode3 = rjsrvpersfndrnode2.addChild("ФизЛицо");
        
        rjsrvpersfndrnode3.addChildEx("Фам", rjsrvpersfndr.rec.Name1);
        rjsrvpersfndrnode3.addChildEx("Имя", rjsrvpersfndr.rec.Name2);
        rjsrvpersfndrnode3.addChildEx("Отч", rjsrvpersfndr.rec.Name3);
        Адрес(PPrm, rjsrvpersfndrnode, "АдрУчредитель", rjsrvpersfndr.rec.AddressID);
    end;
end;

macro СведИНБОЮЛ(PPrm : TRlBackRjMsg_Out_Prm, Parent, RjSrvClntID)
    var node = Parent.addChild("СведИНБОЮЛ");
    
    var rjsrvforeign = Tbfile ("fmrjsrvforeign.dbt");
    rjsrvforeign.keynum = 0;
    rjsrvforeign.rec.RjSrvClntID = RjSrvClntID;
    rjsrvforeign.GetEQ();
    
    node.addChildEx("ПолноеНаимИНБОЮЛ", rjsrvforeign.rec.FullName);
    
    WriteXmlNodeObli(node, "НаимИНБОЮЛ", rjsrvforeign.rec.ShortName, false);
    
    var rjsrvclntcode = Tbfile ("fmrjsrvclntcode.dbt");
    rjsrvclntcode.keynum = 1;
    rjsrvclntcode.rec.RjSrvClntID = RjSrvClntID;
    rjsrvclntcode.rec.CodeKind = RJSRVCLNTCODE_CODE_FOREIGN_ENT;
    rjsrvclntcode.rec.Code = "";
    
    var stat = rjsrvclntcode.GetGE();
    while (stat AND (rjsrvclntcode.rec.CodeKind == RJSRVCLNTCODE_CODE_FOREIGN_ENT) AND
        (rjsrvclntcode.rec.RjSrvClntID == RjSrvClntID))
        node.addChildEx("КодИНБОЮЛ", rjsrvclntcode.rec.Code);
        stat = rjsrvclntcode.next();
    end;
  
    rjsrvclntcode.rec.RjSrvClntID = RjSrvClntID;
    rjsrvclntcode.rec.CodeKind = RJSRVCLNTCODE_NUMBER_FOREIGN_ENT;
    rjsrvclntcode.rec.Code = "";
    
    stat = rjsrvclntcode.GetGE();
    while (stat AND (rjsrvclntcode.rec.CodeKind == RJSRVCLNTCODE_NUMBER_FOREIGN_ENT) AND
        (rjsrvclntcode.rec.RjSrvClntID == RjSrvClntID))
        node.addChildEx("НомерИНБОЮЛ", rjsrvclntcode.rec.Code);
        stat = rjsrvclntcode.next();
    end;
    
    if (rjsrvforeign.rec.AddressID > 0)
        Адрес(PPrm, node, "МестоДеятельностьИНБОЮЛ", rjsrvforeign.rec.AddressID);
    end;
     
    node.addChildEx("ПризнакОргФормаИНБОЮЛ", rjsrvforeign.rec.LegalForm);

    if (rjsrvforeign.rec.LegalForm == 0)
        var rjsrvpartfndr = Tbfile ("fmrjsrvpartfndr.dbt");
        rjsrvpartfndr.keynum = 1;
        rjsrvpartfndr.rec.RjSrvClntID = RjSrvClntID;
        stat = rjsrvpartfndr.GetEQ();
    
        while (stat AND (rjsrvpartfndr.rec.RjSrvClntID == RjSrvClntID))
            УчредительPart(PPrm, node, rjsrvpartfndr.rec.ID);
            stat = rjsrvpartfndr.next();
        end;
        
        var rjsrvpersfndr = Tbfile ("fmrjsrvpersfndr.dbt");
        rjsrvpersfndr.keynum = 1;
        rjsrvpersfndr.rec.RjSrvClntID = RjSrvClntID;
        stat = rjsrvpersfndr.GetEQ();
        
        while (stat AND (rjsrvpersfndr.rec.RjSrvClntID == RjSrvClntID))
            УчредительPers(PPrm, node, rjsrvpersfndr.rec.ID);
            stat = rjsrvpersfndr.next();
        end;
    end;
end;

macro СведенияЮЛ(PPrm : TRlBackRjMsg_Out_Prm, Parent, RjSrvClntID)
    var node = Parent.addChild("СведЮЛ");
    
    var RjSrvParty = Tbfile ("fmrjsrvparty.dbt");
    RjSrvParty.keynum = 0;
    RjSrvParty.rec.RjSrvClntID = RjSrvClntID;
    RjSrvParty.GetEQ();
    
    node.addChildEx("НаимЮЛ", RjSrvParty.rec.Name);
    
    WriteXmlNodeObli(node, "ИННЮЛ", RjSrvParty.rec.INN, false);
    
    if (RjSrvParty.rec.KPP != "")
        node.addChildEx("КППЮЛ", RjSrvParty.rec.KPP);
    end;
    
    if (RjSrvParty.rec.OKPO != "")
        node.addChildEx("ОКПОЮЛ", RjSrvParty.rec.OKPO);
    end;
    
    var Code = Tbfile ("fmrjsrvclntclcode.dbt");
    if (PPrm.MsgBuf.Version == FMRJSRVMSG_VER_1_1)
        Code.keynum = 1;
        Code.rec.RjSrvClntID = RjSrvClntID;
        Code.rec.CodeKind = RJSRVCLNTCLCODE_OKVED;
        Code.rec.Code = "";
        var stat = Code.GetGE();

        while(stat)
            if ((Code.rec.RjSrvClntID == RjSrvClntID) AND (Code.rec.CodeKind == RJSRVCLNTCLCODE_OKVED))
                node.addChildEx("ОКВЭДЮЛ", Code.rec.Code);
            end;
            stat = Code.next();
        end;
    end;
    
    Code.rec.RjSrvClntID = RjSrvClntID;
    Code.rec.CodeKind = RJSRVCLNTCLCODE_OKVED2;
    Code.rec.Code = "";
    stat = Code.GetGE();
    
    while(stat)
        if ((Code.rec.RjSrvClntID == RjSrvClntID) AND (Code.rec.CodeKind == RJSRVCLNTCLCODE_OKVED2))
            node.addChildEx("ОКВЭД2ЮЛ", Code.rec.Code);
        end;
        stat = Code.next();
    end;
    
    WriteXmlNodeObli(node, "ОГРНЮЛ", RjSrvParty.rec.OGRN, false);
    
    if (RjSrvParty.rec.RegPlaceID > 0)
        Адрес(PPrm, node, "АдрРегЮЛ", RjSrvParty.rec.RegPlaceID);
    end;
    
    if (RjSrvParty.rec.AddressID > 0)
        Адрес(PPrm, node, "АдрЮЛ", RjSrvParty.rec.AddressID);
    end;
end;

macro СведенияФЛ(PPrm : TRlBackRjMsg_Out_Prm, RjSrvClntID, ClientType, Parent)
    var node = Parent.addChild("СведФЛИП");
        
    var RjSrvPerson = Tbfile ("fmrjsrvperson.dbt");
    RjSrvPerson.keynum = 0;
    RjSrvPerson.rec.RjSrvClntID = RjSrvClntID;
    RjSrvPerson.GetEQ();
    
    var RjSrvEmployer = Tbfile ("fmrjsrvemployer.dbt");
    if(ClientType == RJSRVCLNT_CT_INDIVID)
      RjSrvEmployer.keynum = 0;
      RjSrvEmployer.rec.RjSrvClntID = RjSrvClntID;
      RjSrvEmployer.GetEQ();
    end;
        
    var RjSrvLawyer = Tbfile ("fmrjsrvlawyer.dbt");
    if(ClientType == RJSRVCLNT_CT_PERSN_PRA)
      RjSrvLawyer.keynum = 0;
      RjSrvLawyer.rec.RjSrvClntID = RjSrvClntID;
      RjSrvLawyer.GetEQ();
    end;

    var ФИОФЛИП = node.addChild("ФИОФЛИП");
    ФИОФЛИП.addChildEx("Фам", RjSrvPerson.rec.Name1);
    ФИОФЛИП.addChildEx("Имя", RjSrvPerson.rec.Name2);
    if(RjSrvPerson.rec.Name3 != "")
        ФИОФЛИП.addChildEx("Отч", RjSrvPerson.rec.Name3);
    end;

    WriteXmlNodeObli(node, "ИННФЛИП", RjSrvPerson.rec.INN, false);

    if(RjSrvPerson.rec.SNILS != "")
      node.addChildEx("СНИЛСФЛ", RjSrvPerson.rec.SNILS);
    end;

    var Code = Tbfile ("fmrjsrvclntclcode.dbt");
    if(ClientType == RJSRVCLNT_CT_INDIVID)
        if (PPrm.MsgBuf.Version == FMRJSRVMSG_VER_1_1)
            Code.keynum = 1;
            Code.rec.RjSrvClntID = RjSrvClntID;
            Code.rec.CodeKind = RJSRVCLNTCLCODE_OKVED;
            Code.rec.Code = "";
            var stat = Code.GetGE();
              
            while(stat AND 
                (Code.rec.RjSrvClntID == RjSrvClntID) AND
                (Code.rec.CodeKind == RJSRVCLNTCLCODE_OKVED))
                node.addChildEx("ОКВЭДИП", Code.rec.Code);
                stat = Code.next();
            end;
        end;

        ClearRecord(Code);

        Code.keynum = 1;
        Code.rec.RjSrvClntID = RjSrvClntID;
        Code.rec.CodeKind = RJSRVCLNTCLCODE_OKVED2;
        Code.rec.Code = "";
        stat = Code.GetGE();
      
        while(stat AND 
            (Code.rec.RjSrvClntID == RjSrvClntID) AND
            (Code.rec.CodeKind == RJSRVCLNTCLCODE_OKVED2))
            node.addChildEx("ОКВЭД2ИП", Code.rec.Code);
            stat = Code.next();
        end;

        if(RjSrvEmployer.rec.OGRNIP != "")
            node.addChildEx("ОГРНИП", RjSrvEmployer.rec.OGRNIP);
        end;
    end;

    if(ClientType == RJSRVCLNT_CT_PERSN_PRA)
      if(RjSrvLawyer.rec.Type != 0)
        node.addChildEx("ТипФЛЧастнаяПрактика", RjSrvLawyer.rec.Type);
      end;

      if(RjSrvLawyer.rec.Type != 0)
        node.addChildEx("РегнНомер", RjSrvLawyer.rec.RegNumber);
      end;
    end;

    if((ClientType == RJSRVCLNT_CT_INDIVID) and (RjSrvEmployer.rec.RegPlaceID > 0) and (fRunForOperation))
      Адрес(PPrm, node, "АдрРегИП", RjSrvEmployer.rec.RegPlaceID);
    end;

    var llCode = "";
    var llName = GetNameLLValuesFromElemnt(3566, RjSrvPerson.rec.PaperKind, @llCode);
    
    /*
        если нет данных для формирования хотя бы одного элемента, контейнер не включать в файл
    */
    if ((llName != "") and ((RjSrvPerson.rec.PaperSeries != "")
        and (RjSrvPerson.rec.PaperNumber != "") and (RjSrvPerson.rec.PaperIssuedDate != ZeroValue(V_DATE))
        and (RjSrvPerson.rec.PaperIssuer != "") and (RjSrvPerson.rec.PaperIssuerCode != ""))
    )
        var ДокУдост = node.addChild("СведДокУдЛичн");
        ДокУдост.addChildEx("ВидДокКод", llCode);
        ДокУдост.addChildEx("ВидДокНаименование", llName);
        
        if(((llCode == "28") OR (llCode == "35") OR (llCode == "40")) and (RjSrvPerson.rec.PaperName != ""))
           ДокУдост.addChildEx("ИноеНаименованиеДок", RjSrvPerson.rec.PaperName);
        end;
        
        ДокУдост.addChildEx("СерияДок", RjSrvPerson.rec.PaperSeries);
        ДокУдост.addChildEx("НомДок", RjSrvPerson.rec.PaperNumber);
        ДокУдост.addChildEx("ДатВыдачиДок", FormatDate(RjSrvPerson.rec.PaperIssuedDate)); 
        ДокУдост.addChildEx("КемВыданДок", RjSrvPerson.rec.PaperIssuer);
        ДокУдост.addChildEx("КодПодр", RjSrvPerson.rec.PaperIssuerCode);
    end;

    if (RjSrvPerson.rec.BirthDate != ZeroValue (V_DATE))
        node.addChildEx("ДатаРождения", FormatDate(RjSrvPerson.rec.BirthDate));
    end;

    if ((RjSrvPerson.rec.BirthCountryISOCode != "") or (RjSrvPerson.rec.CountryName != "") 
        or (RjSrvPerson.rec.OKATO != "") or (RjSrvPerson.rec.DistrictName != "") or (RjSrvPerson.rec.PlaceName != "")
    )
        var МестоРожд = node.addChild("МестоРожд");
        WriteXmlNodeObli(МестоРожд, "КодОКСМ", FormatOKCM(RjSrvPerson.rec.BirthCountryISOCode), false);
        МестоРожд.addChildEx("СтранаНаименование", RjSrvPerson.rec.CountryName);
        
        if (RjSrvPerson.rec.OKATO != "")
            МестоРожд.addChildEx("КодСубъектаПоОКАТО", RjSrvPerson.rec.OKATO);
        end;
        
        if (RjSrvPerson.rec.DistrictName != 0)
            МестоРожд.addChildEx("Район", RjSrvPerson.rec.DistrictName);
        end;
        МестоРожд.addChildEx("Пункт", RjSrvPerson.rec.PlaceName);
    end;

    WriteXmlNodeObli(node, "КодОКСМ", FormatOKCM(RjSrvPerson.rec.CountryISOCode), false);

    var CountryName = GetCountryNameFromISOCode(RjSrvPerson.rec.CountryISOCode);
    WriteXmlNodeObli(node, "СтранаНаименование", CountryName, false);

    if(RjSrvPerson.rec.IsPublicOfficial >= 0)
      node.addChildEx("ПризнакПубЛицо", RjSrvPerson.rec.IsPublicOfficial);
    end;


    if (RjSrvPerson.rec.AddressID > 0)
        Адрес(PPrm, node, "АдрРег", RjSrvPerson.rec.AddressID);
    end;
end;

macro FM_Out_RjSrvMsgSave(MessageXMLFile, sFileName, xml)
    var stat = 0;
    close(MessageXMLFile);
    DelFile(MessageXMLFile);
    if(open(MessageXMLFile, sFileName, "utf8"))
      xml.saveToFile(MessageXMLFile);
      close(MessageXMLFile);

      if (not IsStandAlone) /*Трехзвенка*/
        if(substr(sFileName,1,2) != "\\\\") // если не на машину по сетке 
          CopyFile (sFileName,"$"+sFileName);
        end;
      end;
    else
        stat = 31694;
        CreateErrMsg(stat, sFileName);
    end;

    return stat;
end;

