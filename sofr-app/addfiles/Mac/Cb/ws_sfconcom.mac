import RsbDataSet;
import globals;


class TWsSfComissBase
  var FeeType : integer;
  var Number  : integer;
  var Code  : string;
  var Name_ : string;
end;

class (TWsSfComissBase) TWsSfConCom
  var ID : integer;
  var ObjectType : integer;
  var ObjectID : integer;
  var DateBegin : date;
end;

private const SF_FEE_TYPE_LEVEL = 10;
private const NullArgumentErrorMessageText = "Не заданы обязательные параметры для поиска комиссии объекта";


/* Создание объекта TWsSfConCom через параметры */
macro CreateSfConComFromParam
(
  FeeType,
  Number,
  Code ,
  Name_,
  ID,
  ObjectType,
  ObjectID,
  DateBegin
)
  var SfConComItem : TWsSfConCom;

  SfConComItem.FeeType      = FeeType;
  SfConComItem.Number       = Number;
  SfConComItem.Code         = Code;
  SfConComItem.Name_        = Name_;
  SfConComItem.ID           = ID;
  SfConComItem.ObjectType   = ObjectType;
  SfConComItem.ObjectID     = ObjectID;
  SfConComItem.DateBegin    = DateBegin;

  return SfConComItem;
end;

/* Создание объекта TWsLlvalues из базы */
macro FindContrComiss
(
  ID,
  ObjectType,
  ObjectID,
  FeeType,
  CommNumber,
  SfPlanId,
  DateBegin
)
  var SfConComItem : TWsSfConCom = NULL;

  var sqlQueryText, cmd = RSDCommand(), rs;

  if((DateBegin == NULL) OR (DateBegin < date(2, 1, 1)))
    DateBegin = {curdate};
  end;

  if((ValType(ID) != V_UNDEF) AND (ID > 0))

    sqlQueryText = "select * from dsfconcom_dbt concom where t_id = :ID";
  
    cmd.addParam( "ID", RSDBP_IN, ID );

  elif((ObjectType > 0) AND (ObjectID > 0) AND (FeeType > 0) AND (CommNumber > 0))

    sqlQueryText =  "select comiss.t_code, comiss.t_name, concom.* from dsfconcom_dbt concom, dsfcomiss_dbt comiss where " +
                    "concom.t_objecttype  = :ObjectType and " + 
                    "concom.t_objectid    = :ObjectId and " + 
                    "concom.t_feetype     = :FeeType and " + 
                    "concom.t_commnumber  = :CommNumber and " + 
                    "concom.t_datebegin   = :DateBegin and " +

                    "concom.t_feetype     = comiss.t_feetype and " +
                    "concom.t_commnumber  = comiss.t_number ";


    cmd.addParam( "ObjectType", RSDBP_IN, ObjectType );
    cmd.addParam( "ObjectId",   RSDBP_IN, ObjectID );
    cmd.addParam( "FeeType",    RSDBP_IN, FeeType );
    cmd.addParam( "CommNumber", RSDBP_IN, CommNumber );

    cmd.addParam( "DateBegin",  RSDBP_IN, DateBegin );

    /* Дополнительные параметры */
    if((ValType(SfPlanId) != V_UNDEF) AND (SfPlanId >= 0))
      sqlQueryText = sqlQueryText + " and t_sfplanid = :SfPlanId";

      cmd.addParam( "SfPlanId",   RSDBP_IN, SfPlanId );
    end;
  else
    RunError(NullArgumentErrorMessageText);
  end;

  sqlQueryText = sqlQueryText + " ORDER BY concom.t_datebegin DESC";

  cmd.CmdText = sqlQueryText;
  cmd.NullConversion = true;

  rs = rsdRecordSet( cmd );

  if( rs.moveNext() )
    SfConComItem.ID         = rs.value("t_id");
    SfConComItem.ObjectType = rs.value("t_objecttype");
    SfConComItem.ObjectID   = rs.value("t_objectid");
    SfConComItem.DateBegin  = rs.value("t_datebegin");
    SfConComItem.FeeType    = rs.value("t_feetype");
    SfConComItem.Number     = rs.value("t_commnumber");
    SfConComItem.Code       = rs.value(0L);
    SfConComItem.Name_      = rs.value(1L);
  end;


  return SfConComItem;
end;    

macro SF_GetConComSimpleList
(
  ObjectType, // Вид объекта
  ObjectID,   // Идентификатор объекта
  OnDate,     // Дата действия комиссий
  FeeType     // Тип взимания
)
  var cmd = RSDCommand();
  var query =
       " SELECT concom.t_ID, concom.t_objectType, concom.t_ObjectID, concom.t_DateBegin, " +
       "   comiss.t_FeeType, comiss.t_Number, comiss.t_Code, comiss.t_Name " +
       " FROM dsfconcom_dbt concom, dsfcomiss_dbt comiss " +
       " WHERE concom.t_FeeType = comiss.t_FeeType " +
       " AND concom.t_CommNumber = comiss.t_Number " +
       " AND concom.t_ObjectType = :ObjectType  " +
       " AND concom.t_ObjectID = :ObjectID      " +
       " AND :OnDate BETWEEN concom.t_DateBegin " +
          "  AND DECODE(concom.t_DateEnd, to_date('01 01 0001','dd.mm.yyyy'), " + 
          "  to_date('31 12 9999','dd.mm.yyyy'), concom.t_DateEnd) " ;


  cmd.addParam( "ObjectType", RSDBP_IN, ObjectType );
  cmd.addParam( "ObjectID",   RSDBP_IN, ObjectID );
  cmd.addParam( "OnDate",     RSDBP_IN, OnDate );

  if( (FeeType != null) AND (FeeType != 0) )
    query = query + " AND concom.t_FeeType = :FeeType ";

    cmd.addParam( "FeeType", RSDBP_IN, FeeType );
  else // исключаем из выборки "уровни комиссий"
    query = query + " AND concom.t_Feetype <> " + SF_FEE_TYPE_LEVEL;
  end;

  query = query + " ORDER BY comiss.t_Code ";

  cmd.CmdText = query;
  cmd.NullConversion = true;

  var rs = rsdRecordSet( cmd );

  var result = NULL;

  while( rs.moveNext() )
    if(result == NULL)
      result = TArray();
    end;

    var concom = TWsSfConCom;
    
    concom.ID         = rs.value("t_ID");
    concom.ObjectType = rs.value("t_ObjectType");
    concom.ObjectID   = rs.value("t_ObjectID");
    concom.DateBegin  = rs.value("t_DateBegin");
    concom.FeeType    = rs.value("t_FeeType");
    concom.Number     = rs.value("t_Number");
    concom.Code       = rs.value("t_Code");
    concom.Name_      = rs.value("t_Name");

    result.value(result.Size) = concom;

  end;

  return result;
end;
