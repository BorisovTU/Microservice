/*
 * Вспомогательеы функции для отчетов
 */

macro AddToSplitLine( Line, Length )
  var i = 0;
  if( Length > 0 )
    if( SubStr(Line, StrLen(Line)) != "+" ) Line = Line + "+"; end;
    while( i < Length )
      i = i + 1;
      Line = Line + "-";
    end;
    Line = Line + "+";
  end;
  return Line;
end;

macro AddSpace( StrSource, Length )
  var i = 0;
  while( i < Length )
    StrSource = StrSource + " ";
    i = i + 1;
  end;
  return StrSource;
end;

private macro AddToTabLine( Line, Content, Length, IndentMode, NoTabMode )
  var i = 0;
  var LeftIndent, RightIndent;
  var TabSymb;

  if( Length > 0 )
    TabSymb = "|";
    if( valtype(NoTabMode) != V_BOOL )
      NoTabMode = false;
    end;
    
    LeftIndent  = 0;
    RightIndent = Length - StrLen(Content);
    if( (valtype(IndentMode) == V_STRING) )
      if( IndentMode == "r" )
        LeftIndent  = Length - StrLen(Content);
        RightIndent = 0;
      elif( IndentMode == "c" )
        LeftIndent  = (Length - StrLen(Content)) / 2;
        RightIndent = (Length - StrLen(Content)) - LeftIndent;
      end;
    end;

    if( (not NoTabMode) and (SubStr(Line, StrLen(Line)) != TabSymb) ) Line = Line + TabSymb; end;
    Line = AddSpace( Line, LeftIndent );
    Line = Line + Content;
    Line = AddSpace( Line, RightIndent );
  
    if( (not NoTabMode) )
      Line = Line + TabSymb;
    else
      Line = Line + " ";
    end;

    /*if( (not NoTabMode) ) Line = Line + TabSymb; end;*/
  end;
  return Line;
end;

class RepCage( _CageLength, _TypeIndent, _LeftIndentSize, _RightIndentSize )
  var CageContent : string;
  private var CageLength  : integer;

  private var LeftIndentSize  : integer,
              RightIndentSize : integer;
  var TypeIndent : string;

  private var SplitContent : TArray;

  /* Конструктор */
  CageContent = "";
  CageLength  = _CageLength;

  LeftIndentSize  = 0; if( valtype(_LeftIndentSize ) == V_INTEGER ) LeftIndentSize  = _LeftIndentSize ; end;
  RightIndentSize = 0; if( valtype(_RightIndentSize) == V_INTEGER ) RightIndentSize = _RightIndentSize; end;
  
  TypeIndent = "l"; if( valtype(_TypeIndent) == V_STRING ) TypeIndent = _TypeIndent; end;

/*************** МЕТОДЫ КЛАССА ********************/

  /* разбить содержимое ячейки на строки */
  macro GetCageLength()
    return CageLength;
  end;

  /* получить режим форматирования */
  macro GetTypeIndent()
    return TypeIndent;
  end;

  /* разбить содержимое ячейки на строки */
  macro Split()
    var i;
    array _SplitContent;
    strsplit( CageContent, _SplitContent, CageLength - LeftIndentSize - RightIndentSize );
    SplitContent = TArray( asize(_SplitContent) );
    i = 0;
    while( i < asize(_SplitContent) )
      SplitContent(i) = _SplitContent(i);
      i = i + 1;
    end;
    return SplitContent.Size();
  end;

  /* получить строку из массива по индексу */
  macro GetLine( nLine )
    var i, Indent;
    if( nLine < SplitContent.Size() )
      Indent = ""; i = 0;
      while( i < LeftIndentSize )
        Indent = Indent + " ";
        i = i + 1;
      end;
      return Indent + SplitContent(nLine);
    end;
    return "";
  end;

end;

class ReportLib()
  
  private var ReportLength : integer; /* Длина отчета */
  private var ReportCage   : TArray;  /* Массив ячеек отчета */

/*************** КОНСТРУКТОР КЛАССА ********************/
  ReportLength = 1;
  ReportCage = TArray();

/*************** МЕТОДЫ КЛАССА ********************/
  
  /* добавить основную ячейку к отчету */
  macro AddCage( CageID, CageLength, TypeIndent, LeftIndentSize, RightIndentSize )
    var nextI = ReportCage.size;
    
    ReportCage.Value(nextI) = TArray(2);

    ReportCage.Value(nextI)(0) = CageID;
    ReportCage.Value(nextI)(1) = RepCage( CageLength, TypeIndent, LeftIndentSize, RightIndentSize );

    ReportLength = ReportLength + CageLength + 1;
  end;

  /* установить содержимое ячейки */
  macro SetCageContent( CageID, CageContent, TypeIndent )
    var i, N, success;

    i = 0; N = ReportCage.size; success = false;
    while( i < N )
      
      if( ReportCage.Value(i)(0) == CageID )
        ReportCage.Value(i)(1).CageContent = CageContent;
        if( TypeIndent != null )
          ReportCage.Value(i)(1).TypeIndent  = TypeIndent;
        end;
        success = true;
      end;

      i = i + 1;
    
    end;

    return success;
  end;

  /* напечатать линию-разделитель */
  macro PrintSplitLine( UseCage )
    var i, N;
    var SplitLine = "";

    if( UseCage )
      SplitLine = AddToSplitLine( SplitLine, ReportLength - 2 );
    else
      i = 0; N = ReportCage.size;

      while( i < N )
        SplitLine = AddToSplitLine( SplitLine, ReportCage.Value(i)(1).GetCageLength() );
        i = i + 1;
      end;
    end;
    
    println( SplitLine );
  end;

  /* напечатать строку */
  macro PrintLine( str, TypeIndent, NoTabMode )
    println( AddToTabLine("", str, ReportLength - 2, TypeIndent, NoTabMode) );
  end;

  /* разбить содержимое ячеек на строки */
  private macro SplitCage()
    var i, N;
    var CurN, nString = 0;

    i = 0; N = ReportCage.Size();

    while( i < N )
      CurN = ReportCage.Value(i)(1).Split();
      if( nString < CurN )
        nString = CurN;
      end;
      i = i + 1;
    end;
  
    return nString;
  end;

  private macro print_one_line( CurLine, NoTabMode )
    var i, N;
    var Cage;
    var TabLine = "";

    i = 0; N = ReportCage.size;
    while( i < N )

      Cage = ReportCage.Value(i)(1);

      TabLine = AddToTabLine( TabLine, Cage.GetLine(CurLine), Cage.GetCageLength, Cage.GetTypeIndent, NoTabMode );
      i = i + 1;
    end;

    println( TabLine );
  end;

  /* напечатать строку отчета */
  macro Print( NoTabMode )
    var i, NStrings;
    
    /* Сначала разобъем содержимое ячеек на строки */
    NStrings = SplitCage();

    i = 0;
    while( i < NStrings )
      
      print_one_line( i, NoTabMode );
      
      i = i + 1;
    end;

  end;
  
end;
