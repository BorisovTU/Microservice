//------------------------------------------------------------------------------
//            Автоматизированная банковская система RS-Bank
//
//   File Name        : pmchcom.mac
//
//   Description      : Макрос отбора единовременных комиссий
//                      для РДД расчетного банка.
//
//   Author           : Смирнов С.В.
//
//   History          : Создан : 08.09.2005
//
//------------------------------------------------------------------------------

import globals, OprInter, PaymInter, PSInter, likepy, pm_opr;

//Номера комиссий в РКО
CONST PM_COMISS_RKC             :integer =  5, //КлиентскаяРКЦ
      PM_COMISS_CUR_TRANSFER    :integer =  6, //РКО-ВалПеревод
      PM_COMISS_CASH_RECEPRION  :integer = 12, //РКО-ПрНалСчет
      PM_COMISS_CASH_ISSUE      :integer = 13, //РКО-ВыдНалСчет
      PM_COMISS_INSTANCY        :integer = 22, //РКО-Срочность
      PM_COMISS_INSTANCY_CUR    :integer = 23, //РКО-СрочнВал
      PM_COMISS_RMINSTANCY      :integer = 24, //АРМ-Срочность
      PM_COMISS_RMINSTANCY_CUR  :integer = 25; //АРМ-СрочнВал

// Вид абонента инструкции
CONST INSTRUCTION_BANK_RECEIVER    :integer = 1,
      INSTRUCTION_CORRBANK_RECEIVER:integer = 2;
      
// Первая запись исходящей ветки маршрута
CONST FIRST_OUT_DIRECT:integer = 1;
      
// Номера комиссий для срочных платежей
PRIVATE VAR PM_COMISSIONS_ARRAY:TArray = makeArray( PM_COMISS_INSTANCY,   PM_COMISS_INSTANCY_CUR, 
                                                    PM_COMISS_RMINSTANCY, PM_COMISS_RMINSTANCY_CUR );
                                                                                                     
// Номера комиссий для несрочных платежей
PRIVATE VAR PM_NOT_INSTANT_COMISSIONS_ARRAY:TArray = makeArray( PM_COMISS_RKC,   PM_COMISS_CUR_TRANSFER );     

//------------------------------------------------------------------------------
// У комиссии есть СПИ для филиала ?
//  comiss   - RECORD комиссии
//  dprtCode - номер филиала
//------------------------------------------------------------------------------
PRIVATE MACRO _ComissHasDprtSSI( comiss, dprtCode:integer ):bool

  var select:string = "select 1" +
                     " from dsfssi_dbt t" +
                     " inner join dsettacc_dbt sa on sa.T_SETTACCID = t.T_SETACCID" +
                     " inner join ddp_dep_dbt dp on dp.t_PartyID = sa.t_BankID" +
                     " where t.t_ObjectType = 650" +
                       " and t.t_objectid = :ComissID" +
                       " and dp.T_CODE = :DprtCode";

  var params:TArray = makeArray( SQLParam( "ComissID", string(comiss.FeeType:o:5, comiss.Number:o:5) ),
                                 SQLParam( "DprtCode", dprtCode ) );

  return existsSQLselect( select, params );

END;

//------------------------------------------------------------------------------
// Получить ID головного филиала
//------------------------------------------------------------------------------
private var _HeadDeprtID = -1;

PRIVATE MACRO _GetHeadDeprtID( PartyID:integer ):integer

  if( _HeadDeprtID != -1 )
    return _HeadDeprtID;
  end;

  var select:string = "SELECT dp.t_partyid, LEVEL FROM ddp_dep_dbt dp " +
                      "CONNECT BY ( PRIOR dp.t_parentcode = dp.t_code and dp.t_status = 2) " +
                      "START WITH dp.t_partyid = :PartyID ORDER BY LEVEL DESC";
  var params:TArray = makeArray( SQLParam( "PartyID" , PartyID  ) );
  var rset:RsdRecordset = execSQLselect( select, params );

  if( rset.moveNext() )
    _HeadDeprtID = rset.Value( 0 );
  else
    _HeadDeprtID = 0;
  end;

  return _HeadDeprtID;
END;


//------------------------------------------------------------------------------
// Комиссия подходит для документа?
//  pmpaym - RECORD платежа
//  comiss - RECORD комиссии
//------------------------------------------------------------------------------
MACRO PM_IsProperComission( PaymentObj:RsbPayment, comiss ):bool

  var PayOrder        :object = NULL; //RsbPSPayOrder
  var CpOrder         :object = NULL; //RsbPsCpOrder

  // Рублевая платежка РКО

  if( PaymentObj.DocKind == PS_PAYORDER )

    PayOrder = GenObject( "RsbPSPayOrder", PaymentObj.DocumentID );

    if( PaymentObj.PlaceToIndex )
      //документ идет в К2 - пока никаких комиссий
      return false;
    elif( ( PayOrder.DocKind    == PSPOKIND_DEMAND      ) and
          ( PayOrder.AcceptTerm == PSPAYDEM_TERM_ACCEPT ) and
          ( PayOrder.Accept     == PSPAYDEM_ST_WAIT     ) )
      //документ идет в К1 - пока никаких комиссий
      return false;
    elif( PayOrder.Origin == PSPO_OR_SF )
      //с оплаты ПЗО разумеется никаких комиссий
      return false;
    end;


  // Валютная платежка РКО

  elif( PaymentObj.DocKind == PS_CPORDER )

    CpOrder = GenObject( "RsbPsCpOrder", PaymentObj.DocumentID );
    
    if( CpOrder.Origin == CP_OR_SF )
      //с оплаты ПЗО разумеется никаких комиссий
      return false;
    end;
  
  end;

  // Только если у комиссии есть СПИ для филиала платежа
  if( not _ComissHasDprtSSI( comiss, PaymentObj.Department ) )
    return false;
  end;

  // С документа, подлежащего обработке в БО, комиссии не берем
  var Status:integer = -1;
  if( PM_GetOprStatus( PaymentObj.DocKind, PaymentObj.DocumentID, OPR_PAYM_BO_PROCESS, @Status ) )
    if( Status == OPR_PAYM_ST_BO_YES )
      return false;
    end;
  end;

  return true;

END;

//------------------------------------------------------------------------------
// Фильтр комиссий для платежей
//------------------------------------------------------------------------------
MACRO PM_ChooseComPayment( comiss, Payment:RsbPayment ):bool

  var b_continue, IsBelongOutRoute = false, error, BankID, IsCorresp=false;
  var rs, count;
  FILE route( pmroute ) key 1;

  // С требований комиссию не берём
  if( Payment.ReceiverIsSender )
    return false;
  end;
  
  /* С внутренних платежей комиссии не берём */
  if( Payment.ReceiverGroup != PAYMENTS_GROUP_EXTERNAL )
    return false;
  end;

  // Отбор комиссий по виду документа
  if( not PM_IsProperComission( Payment, comiss ) )
    return false;
  end;

  if( ( comiss.ReceiverID == _GetHeadDeprtID( {OurBank} ) ) OR 
      ( comiss.ReceiverID == Payment.ReceiverBankID      ) )
    // Нашим комиссиям и комиссиям получателя у нас везде уважение и почет
    // Единственное для несрочных платежей не берем комисию срочных
    if ( ( not Payment.Instancy ) and ( Payment.PaymentKind != "С" ) )
      if ( find( PM_COMISSIONS_ARRAY, comiss.Number ) != -1 ) 
        return false;
      else
        return true;
      end;
    else // для срочных платежей не берем комиссию несрочных
      if ( find( PM_NOT_INSTANT_COMISSIONS_ARRAY, comiss.Number ) != -1 ) 
        return false;
      else
        return true;
      end;
    end;
  end;

  // Посмотрим не является ли получателем основной инструкции статическая часть маршрута

  if( Payment.OutInstructionAbonent==INSTRUCTION_BANK_RECEIVER )
     // Прямая исходящая ветка состоит только из одного узла, и его мы уже проверили
    return false;
  elif( Payment.OutInstructionAbonent==INSTRUCTION_CORRBANK_RECEIVER )
     // Прямая исходящая ветка состоит из двух узлов, один из них мы уже проверили
     if( not Payment.ReceiverBankCorrCode )
       return false;
     else
       BankID = ПолучитьКодСубъекта( Payment.ReceiverBankCorrCode, Payment.ReceiverBankCorrCodeKind, error );
       return ( ( not error ) AND ( BankID == comiss.ReceiverID ) );
     end;
  else
    // Допроверим статическую часть маршрута и больше к ней возвращаться не будем
    if( Payment.ReceiverBankCorrCode )
      BankID = ПолучитьКодСубъекта( Payment.ReceiverBankCorrCode, Payment.ReceiverBankCorrCodeKind, error );
      if( ( not error ) AND ( BankID == comiss.ReceiverID ) )
        return true;
      end;
    end;
  end;

  // Принадлежит-ли эта комиссия исходящей ветке маршрута
  // В первую очередь проверяем корреспондента
   
  var select:string = "select 1" +
                     " from dual" +
                     " where exists ( select 1" +
                                    " from dcorschem_dbt" +
                                    " where t_FI_Kind = 1" +
                                      " and t_FIID    = :CorFIID" +
                                      " and t_Number  = :CorNumber" +
                                      " and t_CorrID  = :CorID1 )" +
                        " or exists ( select 1" +
                                    " from dpmroute_dbt" +
                                    " where t_ObjKind = 501" +
                                      " and t_ObjID = :PaymentID" +
                                      " and t_Sort >= 1" +
                                      " and t_PartyID = :CorID2 )";
                  
  var params:TArray = makeArray( SQLParam( "CorFIID",   Payment.BaseFIID     ),
                                 SQLParam( "CorNumber", Payment.OutCorschem  ),
                                 SQLParam( "CorID1",    comiss.ReceiverID    ),
                                 SQLParam( "PaymentID", Payment.PaymentID    ),
                                 SQLParam( "CorID2",    comiss.ReceiverID    ) );
  if( execSQLselect( select, params ).moveNext() )
    return true;
  end;

  return false;

END;
