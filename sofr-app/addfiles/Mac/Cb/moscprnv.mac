/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : moscrejc.mac
  Created     : 20.03.2003
  Programmer  : Андреева Е.П.
  Description : Печать отчета о групповой печати ведомости расхождения

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;

RECORD mo_firstdoc (memorder);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);

   var stat, i = 1;
   array StringArray;

        setbuff (mo_firstdoc,firstdoc);

        if (Вызов==Первый)
[               Отчет о групповой печати ведомости расхождения                  ];
[                 документов и дубликатов бухгалтерии банка                     ];
[+---------+------+------------------------------------------------------------+];
[|   ID    |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Ведомость успешно напечатана";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_BANKPAYMENT,      
                        0,      /* Subpurpose                   */
                        mo_firstdoc.DocKind, /* Вид первичного документа */ 
                        mo_firstdoc.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                end;

         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);
[|#########|######|############################################################|](pm_rmprop.Number,Статус,StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|         |      |############################################################|](StringArray(i));
           i = i + 1;
         end;
                
        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;