/*
$Name: upg2fias.mac
$Module: Ядро ГКБО
$Description: Конвертация адресов из формата КЛАДР в формат ФИАС
*/

import rsexts;
import PtInter;
import rsd, cb_sql, globals, likepy, oralib;
import commonutil;

private macro _СreateFileName()
  var textDir = "";
  var day, mon, year, hour, min, sec;
  var fname = "";
    
  DateSplit(Date(), day, mon, year);
  TimeSplit(Time(), hour, min, sec);
    
  fname = "..\\Txtfile\\KladrToFiasConvertion_" + String(year:4:o) + String(mon:2:o) + String(day:2:o) + "_" + String(hour:2:o) + String(min:2:o) + String(sec:2:o) + ".xml";
    
  return fname;
end;

private macro _GetPartyName(PartyID)
  var query;
  var rs;
  var params : TArray;
  var PartyName = "";

  CaptureOutput;
[
SELECT pt.t_Name
  FROM dparty_dbt pt
 WHERE pt.t_PartyID = :PartyID
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("PartyID", PartyID)
                    );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    PartyName = rs.value(0);
  end;
  return PartyName;
end;

private macro _AddElement( stringBuffer, newElement )
 var result : string; 
 if( strlen(stringBuffer) == 0 ) 
   result = newElement;
 else 
   result = stringBuffer + ", " + newElement;
 end;
 return result;
end;

private macro _AddElement2( stringBuffer, newElement )
 var result : string; 
 if( strlen(stringBuffer) == 0 ) 
   result = newElement;
 else 
   result = stringBuffer + ";" + newElement;
 end;
 return result;
end;

private macro _AddElementSpace( stringBuffer, newElement )
  var result : string; 
  if( strlen(stringBuffer) == 0 ) 
    result = newElement;
  else 
    result = stringBuffer + " " + newElement;
  end;
  return result;
end;

private macro _GetAddressKLADR(adress)

  var Result : string = "";
  var TempHouse : string = "";

  if(( adress.Region != "" ) and ( adress.CodeRegion != "" )) 
    Result = _AddElement( Result, adress.CodeRegion + " " + adress.Region ); 
  end;
  
  if(( adress.Province != "" ) and ( adress.CodeProvince != "" )) 
    Result = _AddElement( Result, adress.CodeProvince + " " + adress.Province ); 
  end;
  
  if(( adress.District != "" ) and ( adress.CodeDistrict != "" ))
    Result = _AddElement( Result, adress.CodeDistrict + " " + adress.District ); 
  end;

  if(( adress.Place != "" ) and ( adress.CodePlace != "" )) 
    Result = _AddElement( Result, adress.CodePlace + " " + adress.Place ); 
  end;
  
  if(( adress.Street != "" ) and ( adress.CodeStreet != "" )) 
    Result = _AddElement( Result, adress.CodeStreet + " " + adress.Street ); 
  end;

  if( adress.House != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "д. " + adress.House ); 
  end;
  if( adress.NumCorps != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "к. " + adress.NumCorps ); 
  end;
  if( adress.Building != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "стр. " + adress.Building ); 
  end;
 
  if(TempHouse != "")
    Result = _AddElement( Result, TempHouse ); 
  end;

  if( adress.Flat != "" ) 
    Result = _AddElement( Result, adress.Flat ); 
  end;

  Result = _AddElement2( Result, adress.Kladr ); 

  Result = _AddElement2( Result, adress.PostIndex ); 
  
  Result = _AddElement2( Result, adress.Okato ); 

  return Result;
end;

private macro _GetAddressFIAS(adress)

  var Result : string = "";
  var TempHouse : string = "";

  if(( adress.Region != "" ) and ( adress.CodeRegion != "" )) 
    Result = _AddElement( Result, adress.CodeRegion + " " + adress.Region ); 
  end;
  
  if(( adress.Province != "" ) and ( adress.CodeProvince != "" )) 
    Result = _AddElement( Result, adress.CodeProvince + " " + adress.Province ); 
  end;
  
  if(( adress.District != "" ) and ( adress.CodeDistrict != "" ))
    Result = _AddElement( Result, adress.CodeDistrict + " " + adress.District ); 
  end;

  if(( adress.Place != "" ) and ( adress.CodePlace != "" )) 
    Result = _AddElement( Result, adress.CodePlace + " " + adress.Place ); 
  end;
  
  if(( adress.Plan != "" ) and ( adress.CodePlan != "" )) 
    Result = _AddElement( Result, adress.CodePlan + " " + adress.Plan ); 
  end;

  if(( adress.Street != "" ) and ( adress.CodeStreet != "" )) 
    Result = _AddElement( Result, adress.CodeStreet + " " + adress.Street ); 
  end;

  if( adress.House != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "д. " + adress.House ); 
  end;
  if( adress.NumCorps != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "к. " + adress.NumCorps ); 
  end;
  if( adress.Building != "" ) 
    TempHouse = _AddElementSpace( TempHouse, "стр. " + adress.Building ); 
  end;
 
  if(TempHouse != "")
    Result = _AddElement( Result, TempHouse ); 
  end;

  if( adress.Flat != "" ) 
    Result = _AddElement( Result, adress.Flat ); 
  end;

  Result = _AddElement2( Result, adress.Fias ); 

  Result = _AddElement2( Result, adress.PostIndex ); 

  Result = _AddElement2( Result, adress.Okato ); 

  return Result;
end;

private macro _GetAddressCount()
  var Nrec = 0;
  var query = "SELECT COUNT(*) FROM dadress_dbt WHERE t_Country = :Country AND t_FiasGuid = :EmptyStr /*AND t_partyid in (6,7,8)*/";
  var params = makeArray(SQLParam("Country", "RUS"),
                         SQLParam("EmptyStr", ""));

  var rs = execSQLselect(query, params, false);

  if(rs and rs.moveNext())
    Nrec = int(rs.value(0));
  end;
  return Nrec;
end;

private macro _GetAddressRecordset()
  var query = "SELECT * FROM dadress_dbt WHERE t_Country = :Country AND t_FiasGuid = :EmptyStr /*AND t_partyid in (6,7,8)*/";
  var params = makeArray(SQLParam("Country", "RUS"),
                         SQLParam("EmptyStr", ""));

  var rs = execSQLselect(query, params, false);

  return rs;
end;

private macro _GetFIASGUID_ByKLADR(Kladr)
  var FiasGuid = "";
  var query = "SELECT t_aoguid, t_nextid, t_enddate FROM das_addrobj_dbt WHERE t_PlainCode = :Kladr ORDER BY DECODE(t_nextid, CHR(1), 1, 2), t_enddate DESC";
  var params = makeArray(SQLParam("Kladr", Kladr));

  var rs = execSQLselect(query, params, false);
  if(rs and rs.moveNext())
    FiasGuid = rs.value(0);
  end;
  return FiasGuid;
end;

private macro _PrepareKladr(Kladr)
  var retval = "000000000000000";
  if((strlen(Kladr) >= 2) and (strlen(Kladr) < 5))
    retval = substr(Kladr, 1, 11) + "0000000000000";
  end;
  if((strlen(Kladr) >= 5) and (strlen(Kladr) < 8))
    retval = substr(Kladr, 1, 11) + "0000000000";
  end;
  if((strlen(Kladr) >= 8) and (strlen(Kladr) < 11))
    retval = substr(Kladr, 1, 11) + "0000000";
  end;
  if((strlen(Kladr) >= 11) and (strlen(Kladr) < 15))
    retval = substr(Kladr, 1, 11) + "0000";
  end;
  if(strlen(Kladr) >= 15)
    retval = substr(Kladr, 1, 15);
  end;
  return retval;
end;

private macro _AddXmlLogHeader()
  println("<?xml version=\"1.0\" encoding=\"cp866\"?>");
  println("<KladrToFiasConvertion>");
end;

private macro _AddXmlLogFooter()
  println("</KladrToFiasConvertion>");
end;

private macro _AddXmlLogLine(PartyID, PartyName, AddrType, AddrKladr, AddrFias, Reason)
  println("  <KladrToFiasConvertionItem>");
  println("    <PartyID>", PartyID, "</PartyID>");
  println("    <PartyName>", PartyName, "</PartyName>");
  println("    <AddrType>", AddrType, "</AddrType>");
  println("    <AddrKladr>", AddrKladr, "</AddrKladr>");
  println("    <AddrFias>", AddrFias, "</AddrFias>");
  println("    <Reason>", Reason, "</Reason>");
  println("  </KladrToFiasConvertionItem>");
end;

private macro _UpdateAdress(adress)
  file f_adress(adress) key 0 write;

  ClearRecord(f_adress);
  f_adress.PartyID = adress.PartyID;
  f_adress.Type = adress.Type;

  if(GetEQ(f_adress))
    Copy(f_adress, adress);
    Update(f_adress);
  end;
end;

private macro _CheckAOLevels(old_adress, new_adress, Diffs : @integer)
  var Result = "";

  if((old_adress.CodeRegion != new_adress.CodeRegion) or (old_adress.Region != new_adress.Region))
    Result = _AddElement(Result, "регион");
    Diffs = Diffs + 1; 
  end;
  if((old_adress.CodeProvince != new_adress.CodeProvince) or (old_adress.Province != new_adress.Province))
    Result = _AddElement(Result, "район"); 
    Diffs = Diffs + 1; 
  end;
  if((old_adress.CodeDistrict != new_adress.CodeDistrict) or (old_adress.District != new_adress.District))
    Result = _AddElement(Result, "город"); 
    Diffs = Diffs + 1; 
  end;
  if((old_adress.CodePlace != new_adress.CodePlace) or (old_adress.Place != new_adress.Place))
    Result = _AddElement(Result, "населенный пункт"); 
    Diffs = Diffs + 1; 
  end;
  if((old_adress.CodePlan != new_adress.CodePlan) or (old_adress.Plan != new_adress.Plan))
    Result = _AddElement(Result, "планировочная структура"); 
    Diffs = Diffs + 1; 
  end;
  if((old_adress.CodeStreet != new_adress.CodeStreet) or (old_adress.Street != new_adress.Street))
    Result = _AddElement(Result, "улица"); 
    Diffs = Diffs + 1; 
  end;

  if(Result != "")
    Result = "Не совпадают элементы адреса: " + Result;
  end;

  return Result;
end;

private macro _CheckCodes(old_adress, new_adress, Diffs : @integer)
  var Result = "";

  if(old_adress.PostIndex != new_adress.PostIndex)
    Result = _AddElement(Result, "почтовый индекс");
    Diffs = Diffs + 1; 
  end;
  if(old_adress.Okato != new_adress.Okato)
    Result = _AddElement(Result, "ОКАТО"); 
    Diffs = Diffs + 1; 
  end;

  if(Result != "")
    Result = "Не совпадает: " + Result;
  end;

  return Result;
end;

private macro _ProcessAddress(old_adress)
  var Kladr = _PrepareKladr(old_adress.Kladr);
  var FiasGuid = _GetFIASGUID_ByKLADR(Kladr);
  record tmp_adress(adress);
  record new_adress(adress);
  var DiffsAOLevels = 0;
  var CheckAOLevelsResult = "";
  var DiffsCodes = 0;
  var CheckCodesResult = "";
  var iStr = 0;

  Copy(tmp_adress, old_adress);
  Copy(new_adress, old_adress);
  tmp_adress.House = "";
  tmp_adress.NumCorps = "";
  tmp_adress.Building = "";

  if(FiasGuid == "")
    if(not PT_GetFIASForAddress(tmp_adress, FiasGuid))
    end;
  elif(FiasGuid != "")
    if(not PT_GetAddressByGUID(FiasGuid, tmp_adress))
    end;
  end;

  if(FiasGuid == "")
    _AddXmlLogLine(old_adress.PartyID, _GetPartyName(old_adress.PartyID), old_adress.Type, _GetAddressKLADR(old_adress), "", "Адрес в ФИАС не найден");
    new_adress.FiasGuid = "0"; /*чтобы не обновлялся при повторном запуске*/
    _UpdateAdress(new_adress);
  else
    tmp_adress.House    = old_adress.House   ;
    tmp_adress.NumCorps = old_adress.NumCorps;
    tmp_adress.Building = old_adress.Building;
    
    DiffsAOLevels = 0;
    CheckAOLevelsResult = "";
    CheckAOLevelsResult = _CheckAOLevels(old_adress, tmp_adress, @DiffsAOLevels);
    if(CheckAOLevelsResult != "")
      _AddXmlLogLine(old_adress.PartyID, _GetPartyName(old_adress.PartyID), old_adress.Type, _GetAddressKLADR(old_adress), _GetAddressFIAS(tmp_adress), CheckAOLevelsResult);
    end;

    tmp_adress.House = StrSubst(tmp_adress.House, "влд.", "");
    tmp_adress.House = StrSubst(tmp_adress.House, "влд", "");

    iStr = index(tmp_adress.House, "стр.");
    if(iStr > 0)
      tmp_adress.Building = trim(substr(tmp_adress.House, iStr + 4));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;
    iStr = index(tmp_adress.House, "стр");
    if(iStr > 0)
      tmp_adress.Building = trim(substr(tmp_adress.House, iStr + 3));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;
    iStr = index(tmp_adress.House, "корп.");
    if(iStr > 0)
      tmp_adress.NumCorps = trim(substr(tmp_adress.House, iStr + 5));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;
    iStr = index(tmp_adress.House, "корп");
    if(iStr > 0)
      tmp_adress.NumCorps = trim(substr(tmp_adress.House, iStr + 4));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;
    iStr = index(tmp_adress.House, "к.");
    if(iStr > 0)
      tmp_adress.NumCorps = trim(substr(tmp_adress.House, iStr + 2));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;
    iStr = index(tmp_adress.House, "к");
    if(iStr > 0)
      tmp_adress.NumCorps = trim(substr(tmp_adress.House, iStr + 1));
      tmp_adress.House = trim(substr(tmp_adress.House, 1, iStr - 1));
    end;

    if(not PT_GetFIASForAddress(tmp_adress, FiasGuid))
      new_adress.CodeRegion    = tmp_adress.CodeRegion   ;
      new_adress.Region        = tmp_adress.Region       ;
      new_adress.CodeProvince  = tmp_adress.CodeProvince ;
      new_adress.Province      = tmp_adress.Province     ;
      new_adress.CodeDistrict  = tmp_adress.CodeDistrict ;
      new_adress.District      = tmp_adress.District     ;
      new_adress.CodePlace     = tmp_adress.CodePlace    ;
      new_adress.Place         = tmp_adress.Place        ;
      new_adress.CodePlan      = tmp_adress.CodePlan     ;
      new_adress.Plan          = tmp_adress.Plan         ;
      new_adress.CodeStreet    = tmp_adress.CodeStreet   ;
      new_adress.Street        = tmp_adress.Street       ;
      
      new_adress.House         = tmp_adress.House        ;
      new_adress.NumCorps      = tmp_adress.NumCorps     ;
      new_adress.Building      = tmp_adress.Building     ;

      new_adress.FiasGuid      = tmp_adress.FiasGuid     ;
      new_adress.Fias          = tmp_adress.Fias         ;
      new_adress.Okato         = tmp_adress.Okato        ;
      new_adress.Oktmo         = tmp_adress.Oktmo        ;
      new_adress.PostIndex     = tmp_adress.PostIndex    ;

      DiffsCodes = 0;
      CheckCodesResult = "";
      CheckCodesResult = _CheckCodes(old_adress, new_adress, @DiffsCodes);
      if(CheckCodesResult != "")
        _AddXmlLogLine(old_adress.PartyID, _GetPartyName(old_adress.PartyID), old_adress.Type, _GetAddressKLADR(old_adress), _GetAddressFIAS(new_adress), CheckCodesResult);
      end;
    
    else
      new_adress.CodeRegion    = tmp_adress.CodeRegion   ;
      new_adress.Region        = tmp_adress.Region       ;
      new_adress.CodeProvince  = tmp_adress.CodeProvince ;
      new_adress.Province      = tmp_adress.Province     ;
      new_adress.CodeDistrict  = tmp_adress.CodeDistrict ;
      new_adress.District      = tmp_adress.District     ;
      new_adress.CodePlace     = tmp_adress.CodePlace    ;
      new_adress.Place         = tmp_adress.Place        ;
      new_adress.CodePlan      = tmp_adress.CodePlan     ;
      new_adress.Plan          = tmp_adress.Plan         ;
      new_adress.CodeStreet    = tmp_adress.CodeStreet   ;
      new_adress.Street        = tmp_adress.Street       ;

      new_adress.FiasGuid      = tmp_adress.FiasGuid     ;
      new_adress.Fias          = tmp_adress.Fias         ;
      if(DiffsAOLevels < 3)
        new_adress.Oktmo         = tmp_adress.Oktmo        ;
      end;

      _AddXmlLogLine(old_adress.PartyID, _GetPartyName(old_adress.PartyID), old_adress.Type, _GetAddressKLADR(old_adress), _GetAddressFIAS(new_adress), "Заданный дом в ФИАС не найден");
    end;

    _UpdateAdress(new_adress);
    _AddXmlLogLine(old_adress.PartyID, _GetPartyName(old_adress.PartyID), old_adress.Type, _GetAddressKLADR(old_adress), _GetAddressFIAS(new_adress), "Адрес конвертирован");

  end;
end;

private macro _RunUpgrade()
  var rs;
  var _Count = _GetAddressCount();
  record old_adress_dbt(adress);
  var nCount = 0;
  var OutputFileName = _СreateFileName();

  var strBeginAt = "Начало процедуры: " + string(date():f) + " " + string(time():f);

  setoutput(OutputFileName);
  
  _AddXmlLogHeader();

  InitProgress(_Count, "~Alt-Break~ Прервать", "Конвертация адресов из формата КЛАДР в формат ФИАС" );

  rs = _GetAddressRecordset();
  
  while(rs and rs.moveNext())
    CU_CopyRSetToFBuff(old_adress_dbt, rs);

    _ProcessAddress(old_adress_dbt);

    nCount = nCount + 1;

    UseProgress( nCount );
  end;
  RemProgress();

  _AddXmlLogFooter();

  setoutput(null);

  println(strBeginAt);
  println("Окончание процедуры: ", string(date():f), " ", string(time():f));

  println("Обработано записей: ", nCount);

  println("Сформирован протокол: ", OutputFileName);

  return true;
end;

macro RunUpgrade2FIAS()
  var retval = false;
  
  if(CU_YesNoWin("Будут переведены из формата КЛАДР в формат ФИАС адреса субъектов экономики. Выполнить?"))
    retval = _RunUpgrade();
  end;

  return retval;
end;

if(not RunUpgrade2FIAS())
  exit(1);
end;