/*
 *  Печать платежного поручения, требования и требования-поручения банка.
 *  Использованы формы из РКО, убрана псевдографика.
 *
 *  Из бухгалтерии банка вызывается макрофункция PrintMO ( mo, pm, dt, ct, rm ),
 *  где mo - первичный документ (memorder.dbt), pm - платеж (pmpaym.dbt),
 *  dt, ct - свойства платежа (pmprop.dbt), rm - R-макет (pmrmprop.dbt).
 *  Вид первичного документа определяется по полю memorder.DocKind.
 *
 *  Из списка платежей вызывается макрофункция PrintPayment ( mo, pm, dt, ct, rm ).
 *
 *  Если возвращаемое функцией значение равно 1, то отчет на экране
 *  показан не будет.
 *
 *  Вернули псевдографику
 *
 */


import rslx, globals, gnd120p, "escord.mac";


record pr_mo        (memorder);
record pr_pay       (pspayord);
record pr_pm_paym   (pmpaym);
record pr_debets    (pmprop);
record pr_credits   (pmprop);
record pr_rmprop    (pmrmprop);
record pr_paydem    (pspaydem);
record pr_akkr      (pmakkr);

/* ======================== Настройка ================================== */
 const
   КоличествоКопий     = 0 ,
   РазделительКопеек   = "="; /* "-" или "="*/
/* ===================================================================== */

var  MassCopy = 0;
var  menuitem = -1;
var  gErrStr = "";

file typeac ( typeac );

file pm_key (pmpaym) key 1; /* Для поиска частичной оплаты */
file rm_key (pmrmprop);

macro GetPartyPropertys(PartyID,rec,MFO,CorAc,Place)
file Коды_Субъектов(partcode);
file Параметры_Банков (bankdprt);
file Субъекты (party);

    if ( ПолучитьСубъекта (PartyID,rec) != 0 )
        msgbox("Не найден банк-получатель");
        return 1;
    else
         Коды_Субъектов.PartyID  = rec.PartyID;
         Коды_Субъектов.CodeKind = 3;
         If (GetEQ(Коды_Субъектов)) /* Если есть БИК у самого банка, то возвращаем его */
                SetParm(2, Коды_Субъектов.Code);
                Параметры_Банков.PartyID = rec.PartyID;
                if (GetEQ(Параметры_Банков))
                    SetParm(3, Параметры_Банков.corAcc);
                else
                    SetParm(3, "");
                end;
         else        /* Если нет, берем вышестоящую организацию и ищем ее БИК*/
                if ( ПолучитьСубъекта (rec.Superior,rec) != 0 )
                    SetParm(2, "");
                    SetParm(3, "");
                else
                    Коды_Субъектов.PartyID  = rec.PartyID;
                    Коды_Субъектов.CodeKind = 3;
                    If (GetEQ(Коды_Субъектов))
                        SetParm(2, Коды_Субъектов.Code);
                    else
                        SetParm(2, "");
                    end;
                end;
                Параметры_Банков.PartyID = rec.PartyID;
                if (GetEQ(Параметры_Банков))
                        SetParm(3, Параметры_Банков.corAcc);
                else
                        SetParm(3, "");
                end;
          end;
          Параметры_Банков.PartyID = rec.PartyID;
/*          if (GetEQ(Параметры_Банков))
                  SetParm(4, Параметры_Банков.place + " " + rec.Place );
          else*/
                  SetParm(4, "");
/*          end;*/
    end;

END;

macro GetINN_KPP( FullINN )
  var pos_delim, str_INN_KPP ;

  pos_delim = Index( FullINN, "/");
  if( pos_delim )
    str_INN_KPP = substr( FullINN, 1, pos_delim-1 ) + 
                  " КПП " +substr( FullINN, pos_delim+1 ) + " ";
  else
    str_INN_KPP = FullINN;
  end;

  return str_INN_KPP;

end;

macro GetINN_KPP_VRSL( FullINN )
  
  return "ИНН " + GetINN_KPP( FullINN );

end;


/*
   Замена в строке str символов What на символ Symb.
   Заимствован из \KVITER\format.mac и преобразован.
*/
macro ChangeSymbol( str, What, Symb )
  var i;
  while( (i = index( str, What )) != 0 )
    strset( str, i, Symb );
  end;
  setparm( 0, str );
  return str;
end;


/*
 *                *** Печать платежного ордера ***
 *
 */
macro DrawBankPayOrd(  number, date_,dptch,payerINN,
                payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
                sum, fiid, receiverINN,
                receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO,
                ground, queue, paydate, shifr,
                part,  numdoc, datedoc, restsum , shifr_old )

  var kopeck,rub, ncopy = 0,
      divide_kopeck,
      sumstr,shortrestsum,shortsum,
      pospoint,part_str,srub,kind_pay;

  divide_kopeck = РазделительКопеек;

  ARRAY SS,SG,SBP,SBR,SP,SR;

  if(MassCopy == 0)
    if(ncopy == 0)
      ncopy = 1;
      if ( not GetInt(ncopy,"Введите количество копий.",3))
        ncopy = 0;
      end;
    end;
  else ncopy = MassCopy;
  end;

  if(ncopy == 0) return false;end;

  /*  Сумма документа частичной оплаты разделяется символом "=", а сумма
   *  остатка платежа - символом "-"
   */
  sumstr = string(MoneyL(sum));
  srub = /*RubToStrAlt(sum);*/ CurToStrAlt( sum, null, null, getISOCode(fiid) );
  StrSet(sumstr, Index(sumstr, "."), "=");
  shortsum = sumstr;

  sumstr = string(MoneyL(restsum));
  if((restsum == 0) or (Index(sumstr,".") == 1) ) sumstr = "0" + sumstr;end;
  StrSet(sumstr, Index(sumstr, "."), "-");
  shortrestsum = sumstr;

  strsplit( payer ,             SP,    44,44,4 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   44,44,3 ); /* Банк-плательщик */
  strsplit( srub,               SS,    68,68,3 ); /* Сумма пpописью  */
  strsplit( ground,             SG,    51,51,4 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   44,44,3 ); /* Банк-получатель */
  strsplit( receiver,           SR,    44,44,4 ); /* Получатель      */

  if ( part == 0 )
     /*  в соответствующие графы не пишется ничего */
     part_str = "";
     shortrestsum = "";
     kind_pay = "";
  else
     part_str = String(part);
     kind_pay = "Частичная оплата";
  end;

  date_   = ChangeSymbol( string( date_   ), "-", "." );
  paydate = ChangeSymbol( string( paydate ), "-", "." );
  datedoc = ChangeSymbol( string( datedoc ), "-", "." );

while(ncopy != 0)
[
                                         ##########  #############  ┌─────────┐
  ПЛАТЕЖНЫЙ ОРДЕР N ###################  ──────────  ─────────────  │ 0401066 │
                                            (Дата)   (Вид платежа)  └─────────┘

 ─────────┬─────────────────────────────────────────────────────────────────────
  Сумма   │####################################################################
  прописью│####################################################################
          │####################################################################
 ─────────┴───────────────────────────────────┬───────┬─────────────────────────
  ИНН ########################################│СУММА  │#########################
                                              │       │
  ############################################│       │
  ############################################├───────┼─────────────────────────
  ############################################│Сч.N.  │#########################
  ############################################│       │
  ПЛАТЕЛЬЩИК                                  │       │
 ─────────────────────────────────────────────┼───────┤
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПЛАТЕЛЬЩИКА                            │       │
 ─────────────────────────────────────────────┼───────┼─────────────────────────
  ############################################│ БИК   │#########################
  ############################################├───────┤
  ############################################│ Сч.N. │#########################
  БАНК ПОЛУЧАТЕЛЯ                             │       │
 ─────────────────────────────────────────────┼───────┤
  ИНН ########################################│ Сч.N. │#########################
                                              │       │
                                              ├───────┼────┬────────┬────────────
  ############################################│Вид Оп.│####│Очер.пл.│ ###
  ############################################│       │    │        │
  ############################################├───────┤    ├────────┤
  ############################################│Наз.пл.│    │Рез.поле│
  Получатель                                  │       │    │        │
 ──────┬──────────┬───────────────┬───────────┼───────┤    │        │
  N ч. │Шифр плат.│  N плат.      │Дата плат. │ Код   │    │        │
  плат.│ док.     │  док.         │ док.      │       │    │        │
       │          │               │           ├───────┼────┴────────┴────────────
  ###  │ ######   │###############│########## │ Сумма │ #########################
 ──────┴──────────┴───────────────┴───────────┤ост.пл.├──────────────────────────
  Содержание операции    ################     │       │
  ┌───────────────────────────────────────────┴───────┼──────────────────────────
  │ Назначение платежа                                │ Отметки банка
  │###################################################│
  │###################################################│
  │###################################################│
  │###################################################│




]
 (date_:c,dptch:c,number:l,
  SS(0):l,SS(1):l,SS(2),
  GetINN_KPP(payerINN),sum:l:f,SP(0):l,SP(1):l,SP(2):l,
  deb_acc:l,SP(3):l,
  SBP(0):l,payerMFO:l,SBP(1):l,SBP(2):l,deb_corracc:l,
  SBR(0):l,receiverMFO:l,SBR(1):l,SBR(2):l,kred_corracc:l,GetINN_KPP(receiverINN),
  kred_acc:l,
  SR(0):l,shifr:l, queue:l,SR(1):l,SR(2):l,SR(3):l, 
  part_str:l, shifr_old:c, numdoc:c, datedoc:c, shortrestsum:l, kind_pay:l,
  SG(0):l,SG(1):l,SG(2):l,SG(3):l
 );
 ncopy = ncopy - 1;
 end;

 return true;

end;

/* ================= Печать платежного ордера ============================= */
 macro _PSPO_print_i2( GroupPrint, Use_VRSL, ErrStr,  /*GroupPrint - признак массовой печати */
   PrimPmpaym,
   PrimPmprop,
   PrimPmrmprop,
   PartPmpaym, 
   PartPmprop, 
   PartPmrmprop )

 record pt  (party);
 file pknd(pmpopknd) key 0;

 VAR
   dptch,
   payer, deb_acc, deb_corracc, bank_payer,  payerMFO,
   sum,
   receiver, kred_acc, kred_corracc,bank_receiver, receiverMFO, Ground, Rest=$0, 
   stat = true, 
   VRSL, report,
   sumstr, rub, rest_sumstr,divide_kopeck,
   MFO, CorAc;

  if(GroupPrint) /* Массовая печать*/
    MassCopy = GroupPrint;/*1;*/
  else
    MassCopy = КоличествоКопий;

    if (MassCopy == 0)
      MassCopy = 1;
      if ( not GetInt(MassCopy,"Введите количество копий.",3))
        MassCopy = 0;
      end;
    end;

  end;
  if(MassCopy == 0) return 1;end;

   /*Реквизиты плательщика*/
   deb_acc     = PartPmpaym.PayerAccount;
   bank_payer  = {Name_Bank};
   payerMFO    = {MFO_Bank};

   deb_corracc = {CORAC_Bank};

   /*Реквизиты получателя*/
   kred_acc = PartPmpaym.ReceiverAccount;
   if ( PartPmprop.BankCode == "" ) /* Внутренний документ банка */
     bank_receiver = PartPmrmprop.ReceiverBankName;
     if(bank_receiver == "") bank_receiver = {Name_Bank}; end;
     receiverMFO   = {MFO_Bank};
     kred_corracc  = {CORAC_Bank};
   else
     if ( ПолучитьСубъекта (PartPmpaym.ReceiverBankID,pt) != 0 )
       if ( valtype(ErrStr) != V_UNDEF )
        setparm( 2, "Не найден банк-получатель" );
       end;
       return 1;
     end;
     GetPartyPropertys(PartPmpaym.ReceiverBankID,pt,MFO,CorAc);
     bank_receiver = PartPmrmprop.ReceiverBankName;
     receiverMFO   = MFO;
     kred_corracc  = CorAc;
   end;

   pknd.PaymentKind = PartPmrmprop.PaymentKind;
   if (GetEQ(pknd))
     dptch = pknd.Name;
   else
     if ( valtype(ErrStr) != V_UNDEF )
        setparm( 2, "Не найден вид платежа "+string( PartPmrmprop.PaymentKind) );
     end;
     return 1;
   end;
 
     /* Получить сумму остатка исходного платежного документа, если он есть */
   if ( PrimPmpaym.PaymentID != 0) 
     Rest = PartPmpaym.PartPaymRestAmountMain;
   end;

   if( not Use_VRSL)
     stat = DrawBankPayOrd(  PartPmrmprop.Number, /*PartPmrmprop.Date*/ PartPmpaym.ValueDate, dptch,
            PartPmrmprop.PayerINN,
            PartPmrmprop.PayerName, deb_acc, deb_corracc,
            bank_payer, payerMFO,
            PartPmpaym.Amount,
            PartPmpaym.FIID,
            PartPmrmprop.ReceiverINN,
            PartPmrmprop.ReceiverName, kred_acc, kred_corracc,
            bank_receiver, receiverMFO,
            PartPmrmprop.Ground, PartPmrmprop.Priority, PartPmrmprop.PayDate, PartPmrmprop.ShifrOper,
            PartPmpaym.SubPurpose, PrimPmrmprop.Number, PrimPmrmprop.Date, Rest, PrimPmrmprop.ShifrOper );

     if(not stat) return 1;
     else return 0;
     end;
   else
      VRSL = ActiveX("RSGENOLE.VRslRun.1");
      report = VRSL.RunRpt("ПлатежныйОрдер");

      divide_kopeck = РазделительКопеек;

      sumstr = string(MoneyL(PartPmpaym.Amount));
      rub = RubToStrAlt(PartPmpaym.Amount);
      StrSet(sumstr, Index(sumstr, "."), divide_kopeck);

      rest_sumstr = string(MoneyL(Rest));
      if((Rest == 0) or (Index(rest_sumstr,".") == 1) ) rest_sumstr = "0" + rest_sumstr;end;
      StrSet(rest_sumstr, Index(rest_sumstr, "."), "-");

      report.Account_Payer.text    = deb_acc;
      report.Account_Receiver.text = kred_acc;
      report.Bank_Payer.text       = bank_payer;
      report.Bank_Receiver.text    = bank_receiver;
      report.CorAc_Payer.text      = deb_corracc;
      report.CorAc_Receiver.text   = kred_corracc;
      report.Data.text             = string(/*PartPmrmprop.Date*/ PartPmpaym.ValueDate);
      report.Dispatch.text         = dptch;
      report.Ground.text           = PartPmrmprop.Ground;
      report.INN_Payer.text        = GetINN_KPP_VRSL(PartPmrmprop.PayerINN);
      report.INN_Receiver.text     = GetINN_KPP_VRSL(PartPmrmprop.ReceiverINN);
      report.Kind_Oper.text        = PartPmrmprop.ShifrOper;
      report.MFO_Payer.text        = payerMFO;
      report.MFO_Receiver.text     = receiverMFO;
      report.Number.text           = PartPmrmprop.Number;
      /*Убрано согласно 120П*/
      /*report.Pay_Date.text         = string(PartPmrmprop.PayDate);*/
      report.Payer.text            = PartPmrmprop.PayerName;
      report.Receiver.text         = PartPmrmprop.ReceiverName;
      report.Payment.text          = string(PartPmrmprop.Priority);
      report.Sum.text              = sumstr;
      report.SumText.text          = rub;
      report.NumIshDoc.text        = PrimPmrmprop.Number;
      report.DateIshDoc.text       = string(PrimPmrmprop.Date);
      report.ShifrPlat.text        = PrimPmrmprop.ShifrOper;
      if( PartPmpaym.SubPurpose == 0 )
         report.Npay.text          = "";
         report.SumOst.text        = "";
      else
         report.Npay.text          = string(PartPmpaym.SubPurpose);
      end;
      report.SumOst.text           = rest_sumstr;

      report.runReport();

      if (IsStandAlone)
       if( ViewRep (report) )
         exit( 1 );
       end;
      else
       if( TransferRep (report) )
         exit( 1 );
       end;
     end;

   end;

 END;