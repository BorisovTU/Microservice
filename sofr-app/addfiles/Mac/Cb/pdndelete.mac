/*
 $Name: pdndelete.mac
 $Module: Ядро ГКБО 
 $Description: Сервисные функции видов ПДн проекта RS-Core
 */
 /*
	Процедура уничтожения ПДн субъекта ПДн
 */
import BankInter, oralib, RsbDataSet, cb_sql, rsd, RmInter, "globals.mac", BankInter, PtInter;

file pdnfinprocparty("pdnfinprocparty.dbt") key 0;
var IsAuto;
var PartyKinds;
var statusArr;
var statText;

private macro PrintProtocolHeader(title)
    const VALUE_GROUND       = 0; 
    const VALUE_PARTYKIND    = 1;
    const VALUE_KINDNAME     = 2;
    const VALUE_CMPLTPRCSSID = 3;
    const VALUE_CMPLTNAME    = 4;
    const VALUE_PERSONID     = 5;
    const VALUE_CODE         = 6;
    
    var cmd = RsdCommand("SELECT PTTYPE.T_GROUND, " + 
        "       KIND.t_PartyKind, " + 
        "       KIND.T_NAME, " + 
        "       FINPROC.T_CMPLTPRCSSID, " + 
        "       CMPLT.T_NAME, " + 
        "       PTTYPE.T_PERSONID, " + 
        "       DECODE(CODE.T_CODE, NULL, chr(1),CODE.T_CODE) " + 
        "  FROM dpdntypeparty_dbt pttype, " + 
        "       dpdnpartykind_dbt kind, " + 
        "       dpdnfinprocparty_dbt finproc, " + 
        "       dpdncmpltprcss_dbt cmplt, " + 
        "       (SELECT T_OBJECTID, T_CODE " + 
        "          FROM dobjcode_dbt " + 
        "         WHERE T_CODEKIND = 1 AND T_OBJECTTYPE = 3) code " + 
        " WHERE     KIND.T_PARTYKIND = PTTYPE.T_PARTYKIND " + 
        "       AND FINPROC.T_PDNTYPEPARTY = PTTYPE.T_ID " + 
        "       AND CMPLT.T_ID = FINPROC.T_CMPLTPRCSSID " + 
        "       AND CODE.T_OBJECTID(+) = PTTYPE.T_PERSONID " + 
        "       AND FINPROC.T_ID = ?");
        cmd.addParam("", RSDBP_IN, pdnfinprocparty.Id);
        cmd.NullConversion = true;
        var rs = RsdRecordset(cmd);
        if (rs.movenext())
[#########################################################################

Дата, время: ########## #########
Пользователь: #### ##########################

Субъект ПДн:              id ############, код ########################### 
Вид субъекта ПДн:         #### ########################################### 
Причина обработки ПДн:    ################################################          
Вид завершения обработки: ###### #########################################

](title, Date(), Time(), {oper}, {Name_Oper}, rs.value(VALUE_PERSONID):r, rs.value(VALUE_CODE), rs.value(VALUE_PARTYKIND):r, rs.value(VALUE_KINDNAME), 
        rs.value(VALUE_GROUND), rs.value(VALUE_CMPLTPRCSSID):r, rs.value(VALUE_CMPLTNAME));
    end;
end;

private macro WriteProtocolErrMsg(mes, IsAuto)
//begin
    println(mes);
    
    if (not IsAuto)
        MsgBox (mes);
    end;
end;

private macro CheckEndDate()
//begin
    var result = false;
    var cmd = RsdCommand("SELECT TARGET.T_ENDDATE " + 
        "  FROM dpdnpartytarget_dbt target, dpdnfinprocparty_dbt finproc " + 
        " WHERE TARGET.T_PDNPTTYPE = FINPROC.T_PDNTYPEPARTY AND FINPROC.T_ID = ?");
    cmd.addParam("", RSDBP_IN, pdnfinprocparty.Id);
    cmd.NullConversion = true;
    var res = RsdRecordset(cmd);
    
    while (res.movenext())
        var dt = res.value(0);
        if ((dt == ZeroValue(V_DATE)) or (dt > Date()))
            result = true;
        end;
    end;
    
    return result;
end;

private macro AskPdnKind(PartyID)
//begin
    var result = false;
    var cmd;
    
    if (ValType(PartyID) == V_UNDEF)
        cmd = RsdCommand("SELECT PT.T_NAME, PDNSET.T_NAME " + 
            "  FROM dpdntypeparty_dbt pttype, " + 
            "       dpdnfinprocparty_dbt finproc, " + 
            "       dparty_dbt pt, " + 
            "       dpdnpartykind_dbt kind, " + 
            "       dpdnset_dbt pdnset " + 
            " WHERE     FINPROC.T_PDNTYPEPARTY = PTTYPE.T_ID " + 
            "       AND PT.T_PARTYID = PTTYPE.T_PERSONID " + 
            "       AND KIND.T_PARTYKIND = PTTYPE.T_PARTYKIND " + 
            "       AND PDNSET.T_SETID = KIND.T_SETID " + 
            "       AND FINPROC.T_ID = ?");
        cmd.addParam("", RSDBP_IN, pdnfinprocparty.Id);
        cmd.NullConversion = true;
    else
        cmd = RsdCommand("SELECT PT.T_NAME FROM dparty_dbt pt WHERE PT.T_PARTYID = ?");
        cmd.addParam("", RSDBP_IN, PartyID);
        cmd.NullConversion = true;
    end;
    
    var res = RsdRecordset(cmd);
    if (res.movenext())
        var msg;
        if (ValType(PartyID) == V_UNDEF)
            msg = "Будут уничтожены персональные данные физического лица " + res.value(0) + " в составе набора '" + res.value(1) + "'. Выполнить?";
            if (MsgBoxEx(msg, MB_YES + MB_NO) == IND_YES)
                result = true;
            end;
        else
            msg = "Будут уничтожены все персональные данные физического лица " + res.value(0) + ". Выполнить?";
            if (MsgBoxEx(msg, MB_YES + MB_NO) == IND_YES)
                result = true;
            end;
        end;
    end;
    
    return result;
end;

var SelectPdnKindsToDelete = "";
macro SelectPdnKindsToDeleteHandler(str)
//begin
    SelectPdnKindsToDelete = SelectPdnKindsToDelete + str;
end;

macro MakeSelectPdnKindsToDelete()
//begin
    SelectPdnKindsToDelete = "";
    SetOutHandler(@SelectPdnKindsToDeleteHandler);
[
  SELECT PDNKIND.T_KIND, pdnkind.T_NAME, FINPROC.T_PDNTYPEPARTY
    FROM dpdntypeparty_dbt pttype,
         dpdnpartykind_dbt kind,
         dpdnfinprocparty_dbt finproc,
         dpdnsetcontent_dbt setc,
         dpdnset_dbt setdbt,
         dpdnkind_dbt pdnkind
   WHERE     KIND.T_PARTYKIND = PTTYPE.T_PARTYKIND
         AND FINPROC.T_PDNTYPEPARTY = PTTYPE.T_ID
         AND SETC.T_SETID = KIND.T_SETID
         AND SETDBT.T_SETID = SETC.T_SETID
         AND PDNKIND.T_KIND = SETC.T_KIND
         AND SETDBT.T_NOTUSE <> CHR (88)
         AND FINPROC.T_ID = ?
         AND PDNKIND.T_KIND NOT IN
                (SELECT PDNKIND2.T_KIND
                   FROM dpdnkind_dbt PDNKIND2,
                        dpdnpartykind_dbt PTKIND2,
                        dpdnfinprocparty_dbt finproc2,
                        dpdnsetcontent_dbt scnt2,
                        dpdntypeparty_dbt tppt2
                  WHERE     TPPT2.T_ID = FINPROC2.T_PDNTYPEPARTY
                        AND FINPROC2.T_PERSONID = TPPT2.T_PERSONID
                        AND PTKIND2.T_PARTYKIND = TPPT2.T_PARTYKIND
                        AND PTKIND2.T_SETID = SCNT2.T_SETID
                        AND PDNKIND2.T_KIND = SCNT2.T_KIND
                        AND tppt2.t_TermDate =
                               TO_DATE ('01/01/0001 00:00:00',
                                        'MM/DD/YYYY HH24:MI:SS')
                        AND NOT EXISTS
                                   (SELECT 1
                                      FROM dpdnpartytarget_dbt trgt
                                     WHERE     TRGT.T_PDNPTTYPE =
                                                  FINPROC2.T_PDNTYPEPARTY
                                           AND TRGT.T_PERSONID =
                                                  tppt2.t_PersonID
                                           AND (   TRGT.T_ENDDATE =
                                                      TO_DATE (
                                                         '01/01/0001 00:00:00',
                                                         'MM/DD/YYYY HH24:MI:SS')
                                                OR TRGT.T_ENDDATE >
                                                      TRIM (SYSDATE)))
                        AND finproc2.T_ID <> FINPROC.T_ID
                        AND FINPROC2.T_PERSONID = pttype.t_PersonID)
         AND PDNKIND.T_KIND NOT IN
                (SELECT PDNKIND4.T_KIND
                   FROM dpdnkind_dbt PDNKIND4,
                        dpdnpartykind_dbt PTKIND4,
                        dpdnfinprocparty_dbt finproc4,
                        dpdnsetcontent_dbt scnt4,
                        dpdntypeparty_dbt tppt4
                  WHERE     PTKIND4.T_PARTYKIND = TPPT4.T_PARTYKIND
                        AND PTKIND4.T_SETID = SCNT4.T_SETID
                        AND PDNKIND4.T_KIND = SCNT4.T_KIND
                        AND TPPT4.T_PERSONID = pttype.t_PersonID
                        AND NOT EXISTS
                               (SELECT 1
                                  FROM dpdnfinprocparty_dbt
                                 WHERE t_PdnTypeParty = TPPT4.t_ID))
         AND PDNKIND.T_SERVICEFUNC <> CHR (1)
         AND PDNKIND.T_NOTUSE <> CHR (88)
ORDER BY PDNKIND.T_KIND ASC
];
    SetOutHandler(NULL);
end;

private macro ProcessDelete_trn()
//begin
    file pdntypeparty("pdntypeparty.dbt") key 0;
    file party("party.dbt") key 0;
    file pdntypeparty_old("pdntypeparty.dbt");  
    var cmd;
    
    if (pdnfinprocparty.PdnTypeParty != 0)
        MakeSelectPdnKindsToDelete();
        cmd = RsdCommand(SelectPdnKindsToDelete);
    else
        cmd = RsdCommand("SELECT T_KIND, T_NAME, 0 FROM DPDNKIND_DBT WHERE T_NOTUSE <> CHR(88)");
    end;
    cmd.addParam("", RSDBP_IN, pdnfinprocparty.Id);
    
    var rs = RsdRecordset(cmd);
    var fFound = false;
    var fFirst = true;
    var stat = 0;
    
    PartyKinds = TArray;
    statusArr = TArray;
    statText = TArray;
    
    while (rs.movenext())
        if (fFirst)
[Уничтожены ПДн видов:
];
            fFirst = false;
        end;
        fFound = true;
        stat = PDN_KindDeleteValue(pdnfinprocparty.PersonID, rs.value(0));
        
        PartyKinds[PartyKinds.size] = rs.value(0);
        statusArr[statusArr.size] = stat;
        
        if (stat)
            var err = GetErrMsg();
            statText[statText.size] = err;
[#######. ######################################: ###### ###################################################################################################
](rs.value(0), rs.value(1), stat, err);
        else
            statText[statText.size] = "";
[#######. ######################################
](rs.value(0), rs.value(1));
        end;
    end;
    
    if (not fFound)
        var msg = "Для данного субъекта нет ПДн, подлежащих уничтожению: они либо не заданы, либо должны продолжать использоваться в рамках других видов субъекта ПДн, если они заданы";
        statText[statText.size] = msg;
        WriteProtocolErrMsg(msg, IsAuto);
        PDN_AddLogDestrMessage(pdnfinprocparty.PersonID, PDNEVENTLOG_MANUAL, PartyKinds, statusArr, statText, pdnfinprocparty.Id, true);
    else
        PDN_AddLogDestrMessage(pdnfinprocparty.PersonID, PDNEVENTLOG_MANUAL, PartyKinds, statusArr, statText, pdnfinprocparty.Id, true);
        
        if (pdnfinprocparty.PdnTypeParty)
            pdntypeparty.Id = pdnfinprocparty.PdnTypeParty;
            GetEq(pdntypeparty);
            Copy(pdntypeparty_old, pdntypeparty);
            
            pdntypeparty.TermDate = Date();
            pdntypeparty.TermOper = {oper};
            PDN_AddLogMessage(pdnfinprocparty.PersonID, PDNEVENTLOG_MANUAL, PDNEVENTLOG_PDN_SUBJECT, PDNEVENTLOG_UPDATE, pdntypeparty_old, pdntypeparty, true, true);
        else
            cmd = RsdCommand("select t_ID from dpdntypeparty_dbt where t_ID = ?");
            cmd.addParam("", RSDBP_IN, pdnfinprocparty.PdnTypeParty);
            rs = RsdRecordset(cmd);
            
            while (rs.movenext())
                pdntypeparty.Id = rs.value(0);
                GetEq(pdntypeparty);
                Copy(pdntypeparty_old, pdntypeparty);
                pdntypeparty.TermDate = Date();
                pdntypeparty.TermOper = {oper};
                PDN_AddLogMessage(pdnfinprocparty.PersonID, PDNEVENTLOG_MANUAL, PDNEVENTLOG_PDN_SUBJECT, PDNEVENTLOG_UPDATE, pdntypeparty_old, pdntypeparty, true, true);
            end;
        end;
    end;

    cmd = RsdCommand("SELECT COUNT (*) " + 
        "  FROM dpdntypeparty_dbt prtype, dpdnpartykind_dbt kind " + 
        " WHERE     PRTYPE.T_PARTYKIND = KIND.T_PARTYKIND " + 
        "       AND KIND.T_NOTUSE <> CHR (88) " + 
        "       AND TRUNC (prtype.t_TermDate) = TO_DATE ('01/01/0001', 'MM/DD/YYYY') " + 
        "       AND PRTYPE.T_PERSONID = ?");
    cmd.addParam("", RSDBP_IN, pdnfinprocparty.PersonID);
    rs = RsdRecordset(cmd);
    if (rs.movenext() and (rs.value(0) == 0))
        var pt = RsbParty(pdnfinprocparty.PersonID);
        var tbparty = TBfile("party.dbt", "W", 0);
        tbparty.rec.PartyID = pdnfinprocparty.PersonID;
        
        if (tbparty.getEQ())
            if (pt.TestForLock)
                tbparty.rec.Locked = "X";
            end;
            tbparty.rec.PdnDestr = Date();
        end;
        tbparty.update();
    end;
end;

private macro PDN_StartDeleteBoddy()
//begin
    if (pdnfinprocparty.EndDate > Date())
        RunError("", "Уничтожить ПДн невозможно: обработка ПДн ещё не завершена на текущий день");
    else
        var result = true;
        
        if (pdnfinprocparty.PdnTypeParty)
            if (not IsAuto)
                result = AskPdnKind();
            end;
        else
            if (not IsAuto)
                result = AskPdnKind(pdnfinprocparty.PersonID);
            end;
        end;
        
        if (result)
            if (not IsAuto)
                BegAction (0, "Уничтожение ПДн субъекта");
            end;
            result = ProcessTrn(NULL, "ProcessDelete_trn");
            if (not IsAuto)
                EndAction (0);
            end;
        end;
    end;
end;

macro PDN_StartDelete(PdnFinProcPartyID, fIsAuto)
//begin
    pdnfinprocparty.Id = PdnFinProcPartyID;
    IsAuto = fIsAuto;

    if (GetEQ(pdnfinprocparty))
        var NeedCheck = true;
        
        if (pdnfinprocparty.PdnTypeParty != 0)
            if (CheckEndDate())
                PrintProtocolHeader("Отказ в уничтожении ПДн");
                NeedCheck = false;
                if (not IsAuto) 
                    WriteProtocolErrMsg("Уничтожить ПДн невозможно: для субъекта ПДн заданы цели обработки ПДн, не оформленные как достигнутые на текущий день", IsAuto);
                end;
            end;
        end;
        
        if (NeedCheck)
            PrintProtocolHeader("Акт об уничтожении персональных данных");
            PDN_StartDeleteBoddy(IsAuto);
        end;
    end;
    
    return 0;
OnError(err)
    MsgBox(err.err);
    return 1;
end;

macro PDN_StartDeleteMass(fIsAuto)
//begin
    var sql_template = " FROM dpdntypeparty_dbt pdntypeparty, " +
        "       dparty_dbt pt, " +
        "       dpdnfinprocparty_dbt pdnfinprocparty " +
        " WHERE     PDNTYPEPARTY.T_PERSONID(+) = PT.T_PARTYID " +
        "       AND PT.T_LEGALFORM = 2 " +
        "       AND PT.T_PDNDESTR = TO_DATE ('01/01/0001', 'MM/DD/YYYY') " +
        "       AND (   (    PDNFINPROCPARTY.T_PDNTYPEPARTY = PDNTYPEPARTY.T_ID " +
        "                AND PDNTYPEPARTY.t_TermDate = " +
        "                       TO_DATE ('01/01/0001', 'MM/DD/YYYY')) " +
        "            OR PDNFINPROCPARTY.T_PERSONID = PT.T_PARTYID) " +
        "       AND (   (    pdnfinprocparty.t_PlanDate = " +
        "                       TO_DATE ('01/01/0001', 'MM/DD/YYYY') " +
        "                AND pdnfinprocparty.t_EndDate <= TRIM (SYSDATE)) " +
        "            OR (    pdnfinprocparty.t_PlanDate <> " +
        "                       TO_DATE ('01/01/0001', 'MM/DD/YYYY') " +
        "                AND pdnfinprocparty.t_PlanDate <= TRIM (SYSDATE)))";

    var cmd = RsdCommand("SELECT count(*) from (select distinct PT.T_PARTYID " + sql_template + ")");
    var rs = RsdRecordset(cmd);
    if (rs.movenext())
        var result = true;
        var count = rs.value(0);
        if (count == 0)
            if (not fIsAuto)
                MsgBox ("Субъектов - физических лиц, ПДн которых подлежат уничтожению, не обнаружено");
                result = false;
            end;
        else
            if (MsgBoxEx("Будут уничтожены ПДн субъектов - физических лиц: " + Int(count) + ", Выполнить?", MB_YES + MB_NO) == IND_NO)
                result = false;
            end;
        end;
        
        if (result)
            cmd = RsdCommand("SELECT DISTINCT PDNFINPROCPARTY.T_ID, PT.T_PARTYID " + sql_template);
            rs = RsdRecordset(cmd);
            
            var curvalue = 0;
            var last_pt = 0;
            InitProgress(Int(count));
            while (rs.movenext())
                if (rs.value(1) != last_pt)
                    curvalue = curvalue + 1;
                end;
                
                PDN_StartDelete(Int(rs.value(0)), true);
                UseProgress (curvalue);
            end;
            RemProgress();
        end;
    end;
end;

//PDN_StartDeleteMass(false);