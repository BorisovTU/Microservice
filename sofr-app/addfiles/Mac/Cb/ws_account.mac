Import BankInter, CTInter, PTInter, FIInter, GateInter, "ws_lib_fun.mac", "globals.mac";
import rsd, oralib, likepy;

/*
    Структура TWsAccount - атрибуты лицевого счета
 */
private class TWsAccount
  
  var Chapter         : integer;    /* Глава счета                                    */
  var Code_Currency   : variant;    /* Идентификатор ФИ счета во внешней системе      */
  var Client          : string ;    /* Идентификатор клиента счета во внешней системе */
  var ClientCodeKind  : integer;    /* Вид кода переданного в поле Client             */
  var Open_Close      : string ;    /* Признак "Открыт-Закрыт"                        */
  var Account         : string ;    /* Номер лицевого счета                           */
  var Oper            : integer;    /* Операционист                                   */
  var Balance         : string ;    /* Балансовый счет                                */
  var Index2          : string ;    /* Признак наличия на счете картотеки 2           */
  var Index3          : string ;    /* Признак наличия на счете картотеки 3           */
  var Kind_Account    : string ;    /* Вид счета                                      */
  var Type_Account    : string ;    /* Тип счета                                      */
  var EType_Account   : string ;    /* Тип счета 2                                    */
  var UserTypeAccount : string ;    /* Пользовательский тип счета                     */
  var Symbol          : string ;    /* Символ отчетности                              */
  var Limit           : money  ;    /* Лимит                                          */
  var LimitDate       : date   ;    /* Дата начала действия лимита                    */
  var Open_Date       : date   ;    /* Дата открытия счета                            */
  var Close_Date      : date   ;    /* Дата закрытия счета                            */
  var NameAccount     : string ;    /* Наименование счета                             */
  var Change_Date     : date   ;    /* Дата последнего изменения счета                */
  var Change_DatePrev : date   ;    /* Дата предпоследнего изменения счета            */
  var PairAccount     : string ;    /* Номер парного счета                            */
  var UserField1      : string ;    /* Пользовательское поле 1                        */
  var UserField2      : string ;    /* Пользовательское поле 2                        */
  var UserField3      : string ;    /* Пользовательское поле 3                        */
  var UserField4      : string ;    /* Пользовательское поле 4                        */
  var OperationDate   : date   ;    /* Дата начала операции                           */
  var DaysToEnd       : integer;    /* Срок в днях до окончания операции              */
  var Department      : integer;    /* Филиал лицевого счета                          */
  var Branch          : integer;    /* Подразделение лицевого счета                   */
  var ORScheme        : integer;    /* Номер схемы переоценки                         */
  var Balance1        : string ;    /* Балансовый счет плана 1                        */
  var Balance2        : string ;    /* Балансовый счет плана 2                        */
  var Balance3        : string ;    /* Балансовый счет плана 3                        */
  var Balance4        : string ;    /* Балансовый счет плана 4                        */
  var Balance5        : string ;    /* Балансовый счет плана 5                        */
  var Balance6        : string ;    /* Балансовый счет плана 6                        */
  var Balance7        : string ;    /* Балансовый счет плана 7                        */
  var Balance8        : string ;    /* Балансовый счет плана 8                        */
  var Balance9        : string ;    /* Балансовый счет плана 9                        */
  var Balance10       : string ;    /* Балансовый счет плана 10                       */
  var Balance11       : string ;    /* Балансовый счет плана 11                       */
end;

/*
   Структура для работы в транзакции
 */
private class TrnParm
  
  var account;
  var accblnc;
  var wsAccountParm : TWsAccount;
  var iAppID        : integer   ;

end;

private var _trnParm : TrnParm;

/*
    функция инициализации объекта TWsAccount параметрами сервиса
 */
private macro FillTWsAccount( oAccountParm, wsAccountParm : TWsAccount, insMode : bool )

  var ParamN     : integer;
  var ObjectName : string;

  var IsMandatory = insMode;

  ParamN     = 1;
  ObjectName = "";

  WS_SetAttributeValue( @wsAccountParm.Chapter        , ParamN, ObjectName, "Chapter"        , oAccountParm, V_INTEGER, true       , 0    );
  WS_SetAttributeValue( @wsAccountParm.Code_Currency  , ParamN, ObjectName, "Code_Currency"  , oAccountParm, null     , true       , null );
  WS_SetAttributeValue( @wsAccountParm.Client         , ParamN, ObjectName, "Client"         , oAccountParm, null     , IsMandatory, null );
  WS_SetAttributeValue( @wsAccountParm.ClientCodeKind , ParamN, ObjectName, "ClientCodeKind" , oAccountParm, V_INTEGER, false      , null );
  WS_SetAttributeValue( @wsAccountParm.Open_Close     , ParamN, ObjectName, "Open_Close"     , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Account        , ParamN, ObjectName, "Account"        , oAccountParm, V_STRING , true       , ""   );
  WS_SetAttributeValue( @wsAccountParm.Oper           , ParamN, ObjectName, "Oper"           , oAccountParm, V_INTEGER, false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance        , ParamN, ObjectName, "Balance"        , oAccountParm, V_STRING , IsMandatory, ""   );
  WS_SetAttributeValue( @wsAccountParm.Index2         , ParamN, ObjectName, "Index2"         , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Index3         , ParamN, ObjectName, "Index3"         , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Kind_Account   , ParamN, ObjectName, "Kind_Account"   , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Type_Account   , ParamN, ObjectName, "Type_Account"   , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.EType_Account  , ParamN, ObjectName, "EType_Account"  , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.UserTypeAccount, ParamN, ObjectName, "UserTypeAccount", oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Symbol         , ParamN, ObjectName, "Symbol"         , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Limit          , ParamN, ObjectName, "Limit"          , oAccountParm, V_MONEY  , false      , null );
  WS_SetAttributeValue( @wsAccountParm.LimitDate      , ParamN, ObjectName, "LimitDate"      , oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Open_Date      , ParamN, ObjectName, "Open_Date"      , oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Close_Date     , ParamN, ObjectName, "Close_Date"     , oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.NameAccount    , ParamN, ObjectName, "NameAccount"    , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Change_Date    , ParamN, ObjectName, "Change_Date"    , oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Change_DatePrev, ParamN, ObjectName, "Change_DatePrev", oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.PairAccount    , ParamN, ObjectName, "PairAccount"    , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.UserField1     , ParamN, ObjectName, "UserField1"     , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.UserField2     , ParamN, ObjectName, "UserField2"     , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.UserField3     , ParamN, ObjectName, "UserField3"     , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.UserField4     , ParamN, ObjectName, "UserField4"     , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.OperationDate  , ParamN, ObjectName, "OperationDate"  , oAccountParm, V_DATE   , false      , null );
  WS_SetAttributeValue( @wsAccountParm.DaysToEnd      , ParamN, ObjectName, "DaysToEnd"      , oAccountParm, V_INTEGER, false      , null );
  /*
  WS_SetAttributeValue( @wsAccountParm.Department     , ParamN, ObjectName, "Department"     , oAccountParm, V_INTEGER, false      , null );
  WS_SetAttributeValue( @wsAccountParm.Branch         , ParamN, ObjectName, "Branch"         , oAccountParm, V_INTEGER, false      , null );
  */
  WS_SetAttributeValue( @wsAccountParm.ORScheme       , ParamN, ObjectName, "ORScheme"       , oAccountParm, V_INTEGER, false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance1       , ParamN, ObjectName, "Balance1"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance2       , ParamN, ObjectName, "Balance2"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance3       , ParamN, ObjectName, "Balance3"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance4       , ParamN, ObjectName, "Balance4"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance5       , ParamN, ObjectName, "Balance5"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance6       , ParamN, ObjectName, "Balance6"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance7       , ParamN, ObjectName, "Balance7"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance8       , ParamN, ObjectName, "Balance8"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance9       , ParamN, ObjectName, "Balance9"       , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance10      , ParamN, ObjectName, "Balance10"      , oAccountParm, V_STRING , false      , null );
  WS_SetAttributeValue( @wsAccountParm.Balance11      , ParamN, ObjectName, "Balance11"      , oAccountParm, V_STRING , false      , null );

  if( wsAccountParm.ClientCodeKind == null ) wsAccountParm.ClientCodeKind = PTCK_CONTR; end;

end;

/*
    функция инициализации буфера лицевого счета по атрибутам параметра сервиса
 */
private macro FillAccountRecord( account, accblnc, wsAccountParm : TWsAccount, iAppID : integer )

  var stat : integer;

  var Client        : integer; /* ИД клиента лицевого счета        */
  var Code_Currency : integer; /* ИД фининструмента лицевого счета */

  /* определяем финансовый инструмент */
  if( wsAccountParm.Code_Currency != null )
    
    Code_Currency = WS_GetFIID( wsAccountParm.Code_Currency, iAppID );

  end;
  
  /* определяем клиента счета */
  if( wsAccountParm.Client != null )

    Client = WS_GetPartyID( wsAccountParm.Client, wsAccountParm.ClientCodeKind, iAppID );

  end;

  /* Заполняем буфер лицевого счета */

  if( Client        != null ) account.Client        = Client       ; end;
  if( Code_Currency != null ) account.Code_Currency = Code_Currency; end;

  if( wsAccountParm.Chapter         != null ) account.Chapter         = wsAccountParm.Chapter        ; end;
  if( wsAccountParm.Account         != null ) account.Account         = wsAccountParm.Account        ; end;
  if( wsAccountParm.Open_Close      != null ) account.Open_Close      = wsAccountParm.Open_Close     ; end;
  if( wsAccountParm.Oper            != null ) account.Oper            = wsAccountParm.Oper           ; end;
  if( wsAccountParm.Balance         != null ) account.Balance         = wsAccountParm.Balance        ; end;
  if( wsAccountParm.Index2          != null ) account.Index2          = wsAccountParm.Index2         ; end;
  if( wsAccountParm.Index3          != null ) account.Index3          = wsAccountParm.Index3         ; end;
  if( wsAccountParm.Kind_Account    != null ) account.Kind_Account    = wsAccountParm.Kind_Account   ; end;
  if( wsAccountParm.Type_Account    != null ) account.Type_Account    = wsAccountParm.Type_Account   ; end;
  if( wsAccountParm.EType_Account   != null ) account.EType_Account   = wsAccountParm.EType_Account  ; end;
  if( wsAccountParm.UserTypeAccount != null ) account.UserTypeAccount = wsAccountParm.UserTypeAccount; end;
  if( wsAccountParm.Symbol          != null ) account.Symbol          = wsAccountParm.Symbol         ; end;
  if( wsAccountParm.Open_Date       != null ) account.Open_Date       = wsAccountParm.Open_Date      ; end;
  if( wsAccountParm.Close_Date      != null ) account.Close_Date      = wsAccountParm.Close_Date     ; end;
  if( wsAccountParm.NameAccount     != null ) account.NameAccount     = wsAccountParm.NameAccount    ; end;
  if( wsAccountParm.Change_Date     != null ) account.Change_Date     = wsAccountParm.Change_Date    ; end;
  if( wsAccountParm.Change_DatePrev != null ) account.Change_DatePrev = wsAccountParm.Change_DatePrev; end;
  if( wsAccountParm.PairAccount     != null ) account.PairAccount     = wsAccountParm.PairAccount    ; end;
  if( wsAccountParm.UserField1      != null ) account.UserField1      = wsAccountParm.UserField1     ; end;
  if( wsAccountParm.UserField2      != null ) account.UserField2      = wsAccountParm.UserField2     ; end;
  if( wsAccountParm.UserField3      != null ) account.UserField3      = wsAccountParm.UserField3     ; end;
  if( wsAccountParm.UserField4      != null ) account.UserField4      = wsAccountParm.UserField4     ; end;
  if( wsAccountParm.OperationDate   != null ) account.OperationDate   = wsAccountParm.OperationDate  ; end;
  if( wsAccountParm.DaysToEnd       != null ) account.DaysToEnd       = wsAccountParm.DaysToEnd      ; end;
  if( wsAccountParm.OperationDate   != null ) account.OperationDate   = wsAccountParm.OperationDate  ; end;
  /*
  if( wsAccountParm.Department      != null ) account.Department      = wsAccountParm.Department     ; end;
  if( wsAccountParm.Branch          != null ) account.Branch          = wsAccountParm.Branch         ; end;
  */
  if( wsAccountParm.ORScheme        != null ) account.ORScheme        = wsAccountParm.ORScheme       ; end;
  if( wsAccountParm.OperationDate   != null ) account.OperationDate   = wsAccountParm.OperationDate  ; end;

  /* Заполняем буфер разноски лицевого счета по балансовым счетам */
  accblnc.Chapter       = account.Chapter      ;
  accblnc.Code_Currency = account.Code_Currency;
  accblnc.Account       = account.Account      ;
  accblnc.Balance0      = account.Balance      ;

  if( wsAccountParm.Balance1  != null ) accblnc.Balance1  = wsAccountParm.Balance1 ; end;
  if( wsAccountParm.Balance2  != null ) accblnc.Balance2  = wsAccountParm.Balance2 ; end;
  if( wsAccountParm.Balance3  != null ) accblnc.Balance3  = wsAccountParm.Balance3 ; end;
  if( wsAccountParm.Balance4  != null ) accblnc.Balance4  = wsAccountParm.Balance4 ; end;
  if( wsAccountParm.Balance5  != null ) accblnc.Balance5  = wsAccountParm.Balance5 ; end;
  if( wsAccountParm.Balance6  != null ) accblnc.Balance6  = wsAccountParm.Balance6 ; end;
  if( wsAccountParm.Balance7  != null ) accblnc.Balance7  = wsAccountParm.Balance7 ; end;
  if( wsAccountParm.Balance8  != null ) accblnc.Balance8  = wsAccountParm.Balance8 ; end;
  if( wsAccountParm.Balance9  != null ) accblnc.Balance9  = wsAccountParm.Balance9 ; end;
  if( wsAccountParm.Balance10 != null ) accblnc.Balance10 = wsAccountParm.Balance10; end;
  if( wsAccountParm.Balance11 != null ) accblnc.Balance11 = wsAccountParm.Balance11; end;

end;

/*
    функция поиска лицевого счета по параметра сервиса
 */
private macro FindAccount( account, sObjectCode : string, iAppID : integer )

  var ErrNum : integer;

  var stat       : integer;
  var ErrMessage : string ;

  ClearRecord( account );

  if( iAppID == null )
    
    /* если параметр iAppID не задан, то в sObjectCode передается идентификатор счета в RS-Bank */
    
    KeyNum( account, 1 );

    account.AccountID = int(sObjectCode);

    if( not GetEQ(account) )
      ErrNum = 23101;
      CreateErrMsg( ErrNum, account.AccountID );
      RunError( GetErrMsg(), ErrNum );
    end;

  else

    /* параметр iAppID задан, то в sObjectCode передается идентификатор счета в системе iAppID */

    var sAccountID : string;

    var ObjectID : integer;

    /* ищем объект шлюза */
    stat = FindObject( iAppID, RG_ACCOUNT, sObjectCode, ObjectID, ErrMessage );

    if( stat != 0 ) RunError( ErrMessage, stat ); end;

    /* ищем идентификатор лицевого счета в RS-Bank - код объекта шлюза */
    stat = FindObjectCode( ObjectID, GT_APP_RSBANKV6_SRC, sAccountID, ErrMessage, RG_ACCOUNT );

    if( stat != 0 ) RunError( ErrMessage, stat ); end;

    RestoreFromUniID( sAccountID, account, OBJTYPE_ACCOUNT );

    KeyNum( account, 0 );

    if( not GetEQ(account) )
      ErrNum = 23102;
      CreateErrMsg( ErrNum, string(account.Account:f) );
      RunError( GetErrMsg(), ErrNum );
    end;

  end;

end;

private macro _SetAccountBranchDprtByOper(account)
  var query = "";
  var rs;
  var params = TArray;

  query =         "SELECT t_codedepart ";
  query = query + "  FROM dperson_dbt  ";
  query = query + " WHERE t_oper = ?   ";
         
  params = makeArray(SQLParam("", account.Oper));
        
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    account.Branch = rs.value(0);
    CB_GetParentFilial(account.Branch, account.Department);
  end;
end;

/* транзакция обновления лицевого счета */
private macro _UpdateAccount()

  var wsAccountParm : TWsAccount;

  var stat : integer;

  wsAccountParm = _trnParm.wsAccountParm;

  if( _trnParm.iAppID != null ) UnswitchReceiver( _trnParm.iAppID ); end;

  stat = Update_AccountEx( _trnParm.account, _trnParm.accblnc );

  if( _trnParm.iAppID != null ) UnswitchReceiver( 0 ); end;

  if( stat == 0 )
    if( wsAccountParm.Limit != null )
      stat = CB_SetAccountLimit( _trnParm.account.AccountID, wsAccountParm.Limit, wsAccountParm.LimitDate );
    end;
  end;

  if( stat != 0 )
    CreateErrMsg( stat );
    AbortTrn();
  end;

end;

/*
    Сервис InsertAccount - вставка лицевого счета

    Параметры.
        oAccountParm - атрибуты лицевого счета
        sObjectCode  - ИД лицевого счета во внешней системе
        iAppID       - ИД внешней системы
 */
macro InsertAccount( oAccountParm, sObjectCode : string, iAppID : integer )
  
  record account("account.dbt");
  record accblnc("accblnc.dbt");

  record fininstr("fininstr.dbt");

  var stat : integer;
  var ErrMessage : string;

  var Client        : integer; /* ИД клиента лицевого счета        */
  var Code_Currency : integer; /* ИД фининструмента лицевого счета */
  
  var wsAccountParm : TWsAccount;
  var sOper : integer;

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, oAccountParm, true, V_GENOBJ );

  WS_CheckParameter( 2, sObjectCode, iAppID      != null, V_STRING  );
  WS_CheckParameter( 3, iAppID     , sObjectCode != null, V_INTEGER );
  
  /* создаем объект класса TWsAccount и заполняем его атрибуты в соответствии с атрибутами параметра oAccountParm */
  wsAccountParm = TWsAccount;

  FillTWsAccount( oAccountParm, wsAccountParm, true );

  ClearAccountRecord( account, accblnc );

  account.Oper            = {oper}        ;
  account.Open_Date       = {curdate}     ;
  account.Department      = {OperDprt}    ;
  account.Branch          = {OperDprtNode};

  sOper = account.Oper;
  
  FillAccountRecord( account, accblnc, wsAccountParm, iAppID );

  if(sOper != account.Oper)
    _SetAccountBranchDprtByOper(account);
  end;

  if( iAppID != null ) UnswitchReceiver( iAppID, sObjectCode ); end;

  stat = Create_Account( account, accblnc, ErrMessage, wsAccountParm.Limit );

  if( iAppID != null ) UnswitchReceiver( 0 ); end;

  if( stat != 0 )
    RunError( ErrMessage, stat );
  end;

  /* возвращаем строковый идентификатор созданного лицевого счета */
  return MakeAccountIDEx(account);

end;

/*
    Сервис UpdateAccount - изменение лицевого счета

    Параметры.
        oAccountParm - атрибуты лицевого счета
        sObjectCode  - ИД лицевого счета во внешней системе
        iAppID       - ИД внешней системы
 */
macro UpdateAccount( oAccountParm, sObjectCode : string, iAppID : integer )

  file account("account.dbt");
  file accblnc("accblnc.dbt");

  var stat : integer;
  var ErrMessage : string;

  var wsAccountParm : TWsAccount;
  var sOper : integer;

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, oAccountParm, true , V_GENOBJ  );
  WS_CheckParameter( 2, sObjectCode , true , V_STRING  );
  WS_CheckParameter( 3, iAppID      , false, V_INTEGER );

  FindAccount( account, sObjectCode, iAppID );

  wsAccountParm = TWsAccount;

  /* инициализируем параметр сервиса oAccountParm основными атрибутами лицевого счета */
  
  wsAccountParm.Account       = account.Account;       /* номер счета                      */
  wsAccountParm.Chapter       = account.Chapter;       /* глава учета                      */
  wsAccountParm.Code_Currency = account.Code_Currency; /* ИД финансового инструмента счета */

  FillTWsAccount( oAccountParm, wsAccountParm, false );

  sOper = account.Oper;
  
  FillAccountRecord( account, accblnc, wsAccountParm, iAppID );

  if(sOper != account.Oper)
    _SetAccountBranchDprtByOper(account);
  end;

  _trnParm = TrnParm;

  _trnParm.account       = account;
  _trnParm.accblnc       = accblnc;
  _trnParm.wsAccountParm = wsAccountParm;
  _trnParm.iAppID        = iAppID;

  var trnStat = ProcessTrn( null, "_UpdateAccount" );

  _trnParm = null;
  
  if( not trnStat )
    
    var bankMsg = AL_GetErrorInfo();
    
    RunError( bankMsg.Message, bankMsg.Stat );

  end;

end;

/*
    Сервис DeleteAccount - удаление лицевого счета

    Параметры.
        sObjectCode  - ИД лицевого счета во внешней системе
        iAppID       - ИД внешней системы
 */
macro DeleteAccount( sObjectCode : string, iAppID : integer )

  file account("account.dbt");

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, sObjectCode, true , V_STRING  );
  WS_CheckParameter( 2, iAppID     , false, V_INTEGER );

  FindAccount( account, sObjectCode, iAppID );

  if( iAppID != null ) UnswitchReceiver( iAppID ); end;

  var stat = Delete_Account( account.AccountID );

  if( iAppID != null ) UnswitchReceiver( 0 ); end;

  if( stat != 0 )
    WS_CreateError( stat );
  end;

end;

/*
    Сервис CloseAccount - закрытие лицевого счета

    Параметры.
        sObjectCode  - ИД лицевого счета во внешней системе
        iAppID       - ИД внешней системы
 */
macro CloseAccount( sObjectCode : string, iAppID : integer )

  file account("account.dbt");

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, sObjectCode, true , V_STRING  );
  WS_CheckParameter( 2, iAppID     , false, V_INTEGER );

  FindAccount( account, sObjectCode, iAppID );

  if( iAppID != null ) UnswitchReceiver( iAppID ); end;

  var stat = CB_CloseAccount( account.Chapter, account.Code_Currency, account.Account );

  if( iAppID != null ) UnswitchReceiver( 0 ); end;

  if( stat != 0 )
    WS_CreateError( stat );
  end;

end;

/*
    Сервис OpenClosedAccount - закрытие лицевого счета

    Параметры.
        sObjectCode  - ИД лицевого счета во внешней системе
        iAppID       - ИД внешней системы
 */
macro OpenClosedAccount( sObjectCode : string, iAppID : integer )

  file account("account.dbt");

  /* проверяем корректность задания параметров Web-сервиса */
  WS_CheckParameter( 1, sObjectCode, true , V_STRING  );
  WS_CheckParameter( 2, iAppID     , false, V_INTEGER );

  FindAccount( account, sObjectCode, iAppID );

  if( iAppID != null ) UnswitchReceiver( iAppID ); end;

  var stat = CB_OpenClosedAccount( account.Chapter, account.Code_Currency, account.Account );

  if( iAppID != null ) UnswitchReceiver( 0 ); end;

  if( stat != 0 )
    WS_CreateError( stat );
  end;

end;
