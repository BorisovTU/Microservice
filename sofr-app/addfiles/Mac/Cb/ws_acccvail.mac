/*
 $Name: ws_acccvail.mac
 $Module: Ядро ГКБО 
 $Description: Интерфейс "Запрос о наличии счетов"
 */
 /*
    Интерфейс "Запрос о наличии счетов"
 */
Import BankInter, CTInter, PTInter, FIInter, GateInter, "ws_lib_fun.mac", "globals.mac";
import rsd, oralib, likepy, "ws_client_lib.mac";

private class TOrigReq
    var ReqOrg : string;      // код ЦИК
    var ReqOrgName : string;  // Наименование ЦИК
    var InternalKey : string; // Уникальный идентификатор запроса (имя файла запроса)
end;

// Интерфейс "Запрос о наличии счетов"
private class TAccountsAvailability
    var ReqID : string;        // Уникальный идентификатор запроса
    var OrigReq;               // Параметры исходного Запроса
    var TypeID : string;       // Код вида идентификатора клиента
    
    var ClientID : string;     // Идентификатор клиента
    var ReqFindClient;         // Поисковые реквизиты
    var DataReq : Date;        // Дата, на которую осуществляется поиск данных
    var TypeCloseAcc : string; // Признак счета, O - только открытые (Open), 
                               // C - только закрытые (Close)
                               // A - все (All)
                               // Если не задан = 'O'
    var TypeCurRate : string;  // Признак по какому курсу считать рублевый эквивалент
                               // 1 - Курс ЦБ
                               // 2 - Курс покупки
                               // 3 - Курс продажи
end; 

private class TAccounts           // Список счетов
    var AccountType : string;     // Вид счета
    var Acc : string;             // Номер счета
    var PersonalAcc : string;     // Номер субсчета
    var AccountName : string;     // Наименование счета
    var BicBank : string;         // БИК банка
    var BankOGRN : string;        // ОГРН банка
    var BankINN : string;         // ИНН банка
    var BankKPP : string;         // КПП банка
    var BankRegionCode : string;  // Код региона
    var BankAddress : string;     // Адрес банка
    var DeptCode : string;        // Филиал
    var ContractNumber : string;  // Номер договора счета
    var ContractStartDate : date; // Дата начала действия договора счета
    var ContractFinDate : date;   // Дата окончания действия договора счета
    var CurrencyCode : string;    // Валюта счета
    var DateMove : date;          // Дата последнего движения по счету
    
    var SummaVal : money;         // Сумма в валюте счета - общий остаток на дату
    var SummaRUB : money;         // Сумма в рублях - общий остаток на дату
    var RealRestVal : money;      // Сумма в валюте  счета - доступный остаток
    var RealRestRUB : money;      // Сумма в рублях - доступный остаток
    var SummaArestVal : money;    // Сумма обременения в валюте
    var SummaArestRUB : money;    // Сумма обременения в рублях
    
    var DateChange : date;        // Дата курса
    var TypeCurRate : string;     // Вид курса
    var BaseCurRate : Decimal;    // Базовая валюта
    var RubRate : Decimal;        // Котируемая валюта, Сумма в рублях
end;

private class TAccountsAvailabilitySuccess
    var ReqID : string;           // идентификатор исходного запроса
    var SenderID : string;        // Идентификатор (код) АС, источник сведений
    var RealDateTime : DateTime;  // Дата, время актуальности сведений
    var Contractor;               // Исполнитель
    
    var CountClient : integer;    // Количество клиентов
    var CountAcc : integer;       // Количество счетов в ответе
    var Accounts;                 // Список счетов
end;

private macro FillTWsAccountsAvailability(oAccAvail, wsAccAvail)
    var ParamN     : integer;
    var ObjectName : string;
    ParamN     = 1;
    ObjectName = "";
    
    WS_SetAttributeValue(@wsAccAvail.ReqID, ParamN, ObjectName, "ReqID", oAccAvail, V_STRING, true, null);
    WS_SetAttributeValue(@wsAccAvail.OrigReq, ParamN, ObjectName, "OrigReq", oAccAvail, V_GENOBJ, false, null);
    if (wsAccAvail.OrigReq != null)
        var OrigReq = @wsAccAvail.OrigReq;
        WS_SetAttributeValue(@OrigReq.ReqOrg, ParamN, "OrigReq", "ReqOrg", oAccAvail.OrigReq, V_STRING, true, null);
        WS_SetAttributeValue(@OrigReq.ReqOrgName, ParamN, "OrigReq", "ReqOrgName", oAccAvail.OrigReq, V_STRING, true, null);
        WS_SetAttributeValue(@OrigReq.InternalKey, ParamN, "OrigReq", "InternalKey", oAccAvail.OrigReq, V_STRING, true, null);
    end;
    WS_SetAttributeValue(@wsAccAvail.TypeID, ParamN, ObjectName, "TypeID", oAccAvail, V_STRING, false, null);
    WS_SetAttributeValue(@wsAccAvail.ClientID, ParamN, ObjectName, "ClientID", oAccAvail, V_STRING, false, "");

    if ((wsAccAvail.ClientID == null) or (wsAccAvail.ClientID == 0))
        WS_SetAttributeValue(@wsAccAvail.ReqFindClient, ParamN, ObjectName, "ReqFindClient", oAccAvail, V_GENOBJ, true, null);
        
        if (wsAccAvail.ReqFindClient != null)
            wsAccAvail.ReqFindClient = TClientRequisites;
            FillTReqFindClient(@wsAccAvail.ReqFindClient, oAccAvail.ReqFindClient, ParamN);
        end;
    end;
    
    WS_SetAttributeValue(@wsAccAvail.DataReq, ParamN, ObjectName, "DataReq", oAccAvail, V_DATE, false, null);
    WS_SetAttributeValue(@wsAccAvail.TypeCloseAcc, ParamN, ObjectName, "TypeCloseAcc", oAccAvail, V_STRING, false, null);
    WS_SetAttributeValue(@wsAccAvail.TypeCurRate, ParamN, ObjectName, "TypeCurRate", oAccAvail, V_STRING, false, 1);
    
    if (wsAccAvail.DataReq == null)
        wsAccAvail.DataReq = GetCurrDate();
    end;
    
    if (wsAccAvail.TypeCloseAcc == null)
        wsAccAvail.TypeCloseAcc = "O";
    end;
end;

private macro AccountsAvailabilityBuildAnsw(oAccAvail : @variant, ClientID, CountClient)
    var result = TAccountsAvailabilitySuccess;
    result.ReqID = oAccAvail.ReqID;
    result.RealDateTime = GetCurrDateTime();
    result.CountClient = CountClient;
    
    var helper = TAccountsAvailabilityHelper;
    helper.SetClientID(ClientID);
    helper.SetDataReq(oAccAvail.DataReq);
    helper.SetTypeCloseAcc(oAccAvail.TypeCloseAcc);
    result.Contractor = helper.GetContructor();
    helper.PrepareSql();
    
    result.Accounts = TArray;
    while (helper.Next())
        var acc = TAccounts;
        acc.AccountType = helper.AccountType();
        acc.Acc = helper.value("T_ACCOUNT");
        acc.PersonalAcc = helper.value("T_PAIRACCOUNT");
        acc.AccountName = helper.value("T_NAMEACCOUNT");
        
        acc.BicBank = helper.BicBank();
        acc.BankOGRN = helper.BankOGRN();
        helper.BankINN_KPP(@acc.BankINN, @acc.BankKPP);
        acc.BankRegionCode = helper.BankRegionCode();
        acc.BankAddress = helper.BankAddress();
        acc.DeptCode = helper.DeptCode();
        acc.DateMove = helper.DateMove();
        acc.CurrencyCode = helper.CurrencyCode();
        acc.SummaVal = helper.SummaVal();
        acc.SummaRUB = helper.SummaRUB();
        result.Accounts[result.Accounts.size] = acc;
    end;
    
    result.CountAcc = helper.CountAcc();
    
    return result;
end;

private macro AccountsAvailabilityBody(oAccAvail : @variant, wsAccAvail)
    FillTWsAccountsAvailability(oAccAvail, wsAccAvail);
    
    var ClientID = 0;
    var CountRezult = 0;
    var result : TClientIdentSuccess = null;

    if ((wsAccAvail.ClientID == NULL) or (wsAccAvail.ClientID == 0))
        var ClientRequisites = wsAccAvail.ReqFindClient;
        if (ClientRequisites.Type == ReqTypePersn)
            result = IdentClientByReqFindClientPerson(ClientRequisites);
        elif (ClientRequisites.Type == ReqTypeIns)
            result = IdentClientByReqFindClientLegal(ClientRequisites);
        end;
        CountRezult = result.CountRezult;
        
        if ((result.CountRezult == 1) and (result.ClientID != 0))
            ClientID = result.ClientID;
            CountRezult = result.CountRezult;
        end;
    else
        var party = TBfile("party.dbt");
        party.KeyNum = 0;
        party.rec.PartyID = wsAccAvail.ClientID;
        
        if (not party.GetEQ)
            ClientID = 0;
            CountRezult = 0;
        else
            ClientID = wsAccAvail.ClientID;
            CountRezult = 1;
        end;
    end;
    
    var res;
    if (ClientID != 0)
        res = AccountsAvailabilityBuildAnsw(wsAccAvail, ClientID, CountRezult);
    else
        res = TAccountsAvailabilitySuccess;
        res.ReqID = oAccAvail.ReqID;
        res.RealDateTime = GetCurrDateTime();
        res.CountClient = CountRezult;
        res.CountAcc = 0;
    end;
    
    return res;
end;

// Интерфейс "Запрос о наличии счетов"
macro AccountsAvailability(oAccAvail)
    var wsAccAvail : TAccountsAvailability;
    WS_CheckParameter(1, oAccAvail, true, V_GENOBJ);
    wsAccAvail = TAccountsAvailability;
    
    return AccountsAvailabilityBody(oAccAvail, wsAccAvail);
end;