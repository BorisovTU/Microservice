/*
$Name:             mctplprm.mac
$Module:           Ядро ГКБО
$Description:      Процедуры для получения параметров шаблонов счетов
                   параметры - это значения из llvalues.dbt для заданных
                   справочников (objects.dbt) и классификаторов (llclass.dbt)
*/
IMPORT PTInter, FIInter, CTInter, globals, RsbDataSet;

PRIVATE RECORD party("party.dbt");
PRIVATE FILE   bank("bankdprt.dbt") key 0;
PRIVATE FILE   persn(persn) key 0;
PRIVATE RECORD fin("fininstr.dbt");
PRIVATE RECORD av("avoiriss.dbt");

/*------------ Определение параметров, связанных с субъектами -------------------*/

/* Формы собственности субъекта экономики (NumInList)*/
private const 
      НеЗадано                   = "",
      МинфинРоссии               = "1",
      НекоммерчОргГосудСобств    = "10",
      НегосудФинанОрг            = "11",
      НегосудКоммерчОрг          = "12",
      НегосудНекоммерчОрг        = "13",
      ЮридическиеЛицаНерезиденты = "14",
      ФизическиеЛица             = "15",
      ФизическиеЛицаНерезиденты  = "16",
      ФедеральноеКазначейство    = "18",
      ФинансовыеОрганыВласти     = "2",
      ГосфондыВнебюджетные       = "3",
      ВнебюджетныеФонды          = "4",
      ФинанОргФедерСобств        = "5",
      КоммерчОргФедерСобств      = "6",
      НекоммерчОргФедерСобст     = "7",
      ФинанОргГосудСобствен      = "8",
      КоммерчОргГосудСобств      = "9";

/*************************************************************************
  получение вида субъекта                                               
  вернет:
    1 - банк 
    2 - небанк. кред. орг-ция 
    3 - РФ 
    4 - СубъектРФ 
    5 - ИнострГос 
    6 - местн.власть иностр. гос-ва
    555 - международный банк или организация
*************************************************************************/
MACRO MC_GetKindParty( PartyID, IsRes, Superior, LegalForm, FirstMinFin )
  var AttrID;
   if( ПолучитьСубъекта( PartyID, party ) == 0 )

      if( IsRes != null )
         if( party.NotResident == "" )      
            SetParm( 1, true );  /*резидент*/
         else
            SetParm( 1, false ); /*нерезидент*/
         end;
      end;

      if( Superior != null )
         SetParm( 2, party.Superior ); /*ID вышестоящей организации*/
      end;

      if( LegalForm != null )
         SetParm( 3, party.LegalForm );
      end;

      ClearRecord(bank);
      bank.PartyID = PartyID;
      GetEQ( bank );

      /*if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 16, AttrID, NULL, NULL, NULL ) )
         if( AttrID==1)
            return 7;
         end;
      end;*/

      if(ValType(FirstMinFin) == V_UNDEF)
        FirstMinFin = true;
      end;

      if( ВидСубъекта( PartyID, PTK_CENTRBANK ) == true )
        return 7; //центробанк
      end;


      if( (FirstMinFin == true) and (ВидСубъекта( PartyID, PTK_MINFIN ) == true) )
        return 8; //министерство финансов
      end;
      
      if( ВидСубъекта( PartyID, PTK_BANK ) == true )

         if( bank.NotBank != "X" )

            /*KD*/
            if( ВидСубъекта( PartyID, 32 ) == true )
              return 555; 
            end;

            return 1; /*банк*/
         else
            return 2; /*небанк. кред. орг-ция*/
         end;
      end;

      if( ВидСубъекта( PartyID, PTK_STATE_BODY ) == true )
         if( (party.NRCountry == "") OR (party.NRCountry == "RUS") )
            return 3; /*орган гос.власти РФ*/
         else
            return 5; /*иностр. гос-во*/
         end;
      end;           

      if( ВидСубъекта( PartyID, PTK_STATE_BODY_SUBJECT ) == true )
         if( (party.NRCountry == "") OR (party.NRCountry == "RUS") )
            return 4; /*орган гос.власти субъекта РФ*/ 
         else
            return 6; /*орган местной власти иностр. гос-ва*/          
         end;
      end;

      if( ВидСубъекта( PartyID, PTK_LOCAL_BODY ) == true )
         if( (party.NRCountry == "") OR (party.NRCountry == "RUS") )
            return 4; /*орган гос.власти субъекта РФ или местной власти*/ 
         else
            return 6; /*орган местной власти иностр. гос-ва*/ 
         end;
      end;

      if( (FirstMinFin == false) and (ВидСубъекта( PartyID, PTK_MINFIN ) == true) )
        return 8; //министерство финансов
      end;

      return 999; /*прочие*/
 
   end;
   return -1;
END;

/**************************************************************************/
/*  резидентность\нерезидентность                                         */
/*  OBJTYPE_RESIDENT, LLCLASS_RESIDENT_ISS                                */
/*                  , LLCLASS_RESIDENT_CLCB                               */
/**************************************************************************/
MACRO MC_IsResident( PartyID )
   if( ПолучитьСубъекта( PartyID, party ) == 0 )

      if( party.NotResident == "" )      
         return 1;  /*резидент*/
      else
         return 2; /*нерезидент*/
      end;
   end;

   return 1;
END;

/**************************************************************************/
/*  получение вида эмитента                                               */
/*  OBJTYPE_KINDSUBJ, LLCLASS_KINDSUBJ_ISSUER                             */
/**************************************************************************/
MACRO MC_GetKindIssuer( Issuer, FirstMinFin )

   var Res = false, Kind = MC_GetKindParty( Issuer, Res, NULL, NULL, FirstMinFin );

   if( (Kind == 7) AND (Res == true))
      return 1; /*ЦБ РФ*/
   elif( (Kind == 8) AND (Res == true))
      return 300; /*Министерство финансов*/
   elif( (Kind == 1) AND (Res == false) )
      return 3; /* банк - нерезидент */
   elif( (Kind == 1) AND (Res == true) )
      return 4; /* банк - резидент */
   elif( Kind == 555 )
      return 3; /* банк - нерезидент */
   elif( Kind == 3 )
      return 100; /* орган гос. власти РФ */
   elif( Kind == 4 )
      return 101; /* орган гос. власти субъекта РФ или местная власть */
   elif( Kind == 5 )
      return 102; /* иностранное государство */
   elif( Kind == 6 )
      return 103; /* орган местной власти иностранного государства */
   elif( Res == true  )
      return 9999; /* другой резидент*/
   elif( Res == false )
      return 1000; /* другой нерезидент*/
   end;

   return -1;

END;

/*****************************************************************************
  получение категории дебитора в сделках ц/б
  Объект        - OBJTYPE_KINDSUBJ, 
  Классификатор - LLCLASS_KINDSUBJ_DEBETSECUR,
******************************************************************************/
MACRO MC_GetKindSecurDebet( PartyID )
   var IsRes = false, OwnForm = НеЗадано, LegalForm = 0, Kind;

   Kind = MC_GetKindParty( PartyID, IsRes, null, LegalForm );

   getMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ),
                   1, /*Форма собственности*/
                   null, null, OwnForm );

   if( (Kind == 7) AND (IsRes == true)) 
     return 1; /*Центробанк*/
   elif( (Kind == 8) AND (IsRes == true))
      return 300; /*Министерство финансов*/
   elif( ( (Kind == 1) OR (Kind == 2) ) AND  /*имеет вид - банк*/
       (IsRes == true) AND
       ( (OwnForm == НеЗадано) OR (OwnForm == НегосудКоммерчОрг) )
     ) 
      return 2; /*КредОргРез*/

   elif( (Kind == 1) AND /*имеет вид - банк и неустановленный признак небанковской кр. организации*/
         (IsRes == false) AND
         ( (OwnForm == НеЗадано) OR (OwnForm == ЮридическиеЛицаНерезиденты) )
       )
      return 3; /*БанкНеРез*/

   elif( LegalForm == 1 ) /*Субъект является ЮЛ */

      if( IsRes == true ) /* Субъект является резидентом */
         
         if( OwnForm == МинфинРоссии ) 
            return 300; /*Минфин РФ*/

         elif( OwnForm == ФинансовыеОрганыВласти ) 
            return 301; /*ФинОргСубъРФ*/

         elif( OwnForm == ГосфондыВнебюджетные )
            return 302; /*ГосВнебФонд*/

         elif( OwnForm == ВнебюджетныеФонды )
            return 303; /*ВнебФондСубъРФ*/

         elif( OwnForm == ФинанОргФедерСобств )
            return 304; /*ФинОргФедер*/

         elif( OwnForm == КоммерчОргФедерСобств )
            return 305; /*КомОргФедер*/

         elif( OwnForm == НекоммерчОргФедерСобст )
            return 306; /*НекомОргФедер*/

         elif( OwnForm == ФинанОргГосудСобствен )
            return 307; /*ФинОргГос*/
         
         elif( OwnForm == КоммерчОргГосудСобств)
            return 308; /*КомОргГос*/

         elif( OwnForm == НекоммерчОргГосудСобств )
            return 309; /*НекомОргГос*/

         elif( OwnForm == НегосудФинанОрг )
            return 310; /*НегосФинОрг*/

         elif( ( (Kind != 1) AND (Kind != 2) ) AND /*субъект не имеет вида банк*/
               ( (OwnForm == НегосудКоммерчОрг) OR (OwnForm == НеЗадано) )
             )
            return 311; /*НегосКомОрг*/

         elif( OwnForm == НегосудНекоммерчОрг )
            return 312; /*НегосНекомОрг*/

         elif( OwnForm == ФедеральноеКазначейство )
            return 313; /*Федеральное казначейство*/
         end;
      else /* Субъект является ЮЛ - НЕрезидентом */

         if( (Kind != 1) AND /*не имеет вида- банк или имеет неустановленный признак небанковской кр. организации*/
             ( (OwnForm == ЮридическиеЛицаНерезиденты) OR (OwnForm == НеЗадано) )
           )
            return 9;
         end;
      end;

   elif( LegalForm == 2 ) /* Физ лицо */
        
      if( (IsRes == true) AND /*резидент*/
          ( (OwnForm == НеЗадано) OR (OwnForm == ФизическиеЛица) )
        )
         persn.PersonID = PartyID;
         if( getEQ(persn) )
            if( persn.IsEmployer == "X" ) /*является ЧП*/  
               return 50; /*ФЛПредпр*/
            else 
               return 51; /*Гражданин*/
            end;
         end;

      elif( (IsRes == false) AND /*нерезидент*/
            ( (OwnForm == НеЗадано) OR (OwnForm == ФизическиеЛицаНерезиденты) )
          )
          return 10; /*ФЛНерез*/
      end;
   end;

   return -1;
END;

/*****************************************************************************
  получение категории контрагента плательщика комиссии
  Объект        - OBJTYPE_CAT_CONTR, 
  Классификатор - LLCLASS_CAT_CONTR,
******************************************************************************/
MACRO MC_GetKindSecurCatContr( PartyID )
   var LegalForm = 0, Kind;

   Kind = MC_GetKindParty(PartyID, null, null, LegalForm);

   if(( Kind == 1 ) or ( Kind == 7 ))/*DEF-40160 банк или центробанк*/
      return 1; /*Кредитная организация*/
   elif( LegalForm == 1 )
      return 2; /*Юр. лица кроме кредитных организаций*/
   elif( LegalForm == 2 )
      persn.PersonID = PartyID;
      if( getEQ(persn) )
         if( persn.IsEmployer == "X" ) /*является ЧП*/
            return 3; /*Индивидуальный предприниматель*/
         else 
            return 4; /*Физическое лицо*/
         end;
      end;
   end;

   return -1;
END;

/*****************************************************************************
  получение категории кредитора в сделках ц/б
  Объект        - OBJTYPE_KINDSUBJ, 
  Классификатор - LLCLASS_KINDSUBJ_CREDITSECUR,
******************************************************************************/
MACRO MC_GetKindSecurCredit( PartyID )
   var IsRes = false, OwnForm = НеЗадано, LegalForm = 0, Kind;

   Kind = MC_GetKindParty( PartyID, IsRes, null, LegalForm );

   getMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ),
                   1, /*Форма собственности*/
                   null, null, OwnForm );

   if( (Kind == 7) AND (IsRes == true)) 
     return 1; /*Центробанк*/
   elif( (Kind == 8) AND (IsRes == true))
      return 300; /*Министерство финансов*/
   elif( ( (Kind == 1) OR (Kind == 2) ) AND  /*имеет вид - банк*/
       (IsRes == true)
     ) 
     if( (IsBankType(PartyID, PT_KIND_PAYM_CASH_CENTRE)) OR (IsBankType(PartyID, PT_KIND_FIELDOFFICE_CENTRALBANK)) )
        return 1; /*ЦентробанкРФ*/

     elif( (OwnForm == НеЗадано) OR (OwnForm == НегосудКоммерчОрг) )
        return 2; /*КредОргРез*/
     end;

   elif( (Kind == 1) AND /*имеет вид - банк и неустановленный признак небанковской кр. организации*/
         (IsRes == false) AND
         ( (OwnForm == НеЗадано) OR (OwnForm == ЮридическиеЛицаНерезиденты) )
       )
      return 3; /*БанкНеРез*/

   elif( LegalForm == 1 ) /*Субъект является ЮЛ */

      if( (IsRes == true) AND (Kind != 1) AND (Kind != 2) ) /* Субъект является резидентом и не имеет вида- банк */
          return 7; /*ЮЛРез*/
      
      elif( (IsRes == false) AND (Kind != 1) AND /*НЕрезидент и не имеет вида- банк или имеет неустановленный признак небанковской кр. организации*/
            ( (OwnForm == ЮридическиеЛицаНерезиденты) OR (OwnForm == НеЗадано) )
          )
          return 9; /*ЮЛНерез*/
      end;

   elif( LegalForm == 2 ) /* Физ лицо */
  
      if( IsRes == true ) /*резидент*/
         return 8; /*ФЛРез*/
      else
         return 10; /*ФЛНерез*/
      end;
   end;

   return -1;
END;

/*вид субъекта - Категория контрагента в доходах/расходах по процентам*/
/*****************************************************************************
  получение категории контрагента в доходах/расходах по процентам
  Объект        - OBJTYPE_KINDSUBJ, 
  Классификатор - LLCLASS_KINDSUBJ_KDRMM,
******************************************************************************/
MACRO MC_GetKindContragentByPC( PartyID )
   var IsRes = false, OwnForm = НеЗадано, LegalForm = 0, Kind;

   Kind = MC_GetKindParty( PartyID, IsRes, null, LegalForm );

   getMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ),
                   1, /*Форма собственности*/
                   null, null, OwnForm );

   /*имеет вид - банк*/
   if( Kind == 1 ) 
     if( IsRes == true ) 
        if( (IsBankType(PartyID, PT_KIND_PAYM_CASH_CENTRE)) OR (IsBankType(PartyID, PT_KIND_FIELDOFFICE_CENTRALBANK)) )
           return 1; /*ЦентробанкРФ*/
        end;
     elif( (OwnForm == НеЗадано) OR (OwnForm == ЮридическиеЛицаНерезиденты) )
        return 3; /*БанкНеРез*/
     end;

   elif( (Kind == 2) AND (IsRes == true) AND
         ( (OwnForm == НеЗадано) OR (OwnForm == НегосудКоммерчОрг) ))
      return 2; /*КредОргРез*/
   end;
   return 1001; /*Другой субъект*/
end;

private MACRO IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else
        return value_false;
    end;
END;

/**************************************************************************/
/*  получение вида эмитента                                               */
/*  OBJTYPE_KINDSUBJ, LLCLASS_KINDSUBJ_ISSUER_DR                          */
/**************************************************************************/
MACRO MC_GetKindIssuerDR( Issuer, FirstMinFin, FiRole )
   var Res = false, Kind = MC_GetKindParty( Issuer, Res, NULL, NULL, FirstMinFin );

   var PKU = false;
   if ( (Valtype(FiRole) != V_UNDEF) and (FiRole == FIROLE_PKU) )
      PKU = true;
   end;

   if( (Kind == 7) AND (Res == true))
      return 1; /*Центробанк*/
   elif( (Kind == 8) AND (Res == true))
      return 300; /*Министерство финансов*/
   elif( ((Kind == 1) OR (Kind == 2)) AND (Res == true) )
      return IIF(PKU, 10000,2); /* кредитная организация - резидент */ 
   elif( ((Kind == 1) or (Kind == 555)) AND (Res == false) ) // Golovkin 23.09.2019 ID : 495198 or (Kind == 555) "Облигации RU000A0JXT58 МИБ оббП01 учитываются на счетах доходов/расходов неверной формы собственности (IM3058509)"
      return IIF(PKU,10002,3); /* банк - нерезидент */  
   elif( Kind == 3 )
      return 100; /* орган гос. власти РФ */
   elif( Kind == 4 )
      return 101; /* орган гос. власти субъекта РФ или местная власть */
   elif( Kind == 5 )
      return 102; /* иностранное государство */
   elif( Kind == 6 )
      return 103; /* орган местной власти иностранного государства */
   elif( Res == true  )
      return IIF(PKU,10001,9999); /* другой резидент*/   
   elif( Res == false )
      return IIF(PKU,10003,1000); /* другой нерезидент*/  
   end;

   return -1;
END;

/**************************************************************************/
/*  получение вида эмитента векселя                                       */
/*                                                                        */
/**************************************************************************/
MACRO MC_GetKindIssuerBnr( Issuer, FirstMinFin )

   var Res = false, Kind = MC_GetKindParty( Issuer, Res, NULL, NULL, FirstMinFin );

   if( (Kind == 7) AND (Res == true))
      return 1; /*Центробанк*/
   elif( (Kind == 8) AND (Res == true))
      return 300; /*Министерство финансов*/
   elif( ((Kind == 1) OR (Kind == 2)) AND (Res == true) )
      return 4; /* банк - резидент */
   elif( ((Kind == 1) OR (Kind == 2)) AND (Res == false) )
      return 3; /* банк - нерезидент */
   elif( Kind == 3 )
      return 100; /* орган гос. власти РФ */
   elif( Kind == 4 )
      return 101; /* орган гос. власти субъекта РФ или местная власть */
   elif( Kind == 5 )
      return 102; /* иностранное государство */
   elif( Kind == 6 )
      return 103; /* орган местной власти иностранного государства */
   elif( Res == true  )
      return 9999; /* другой резидент*/
   elif( Res == false )
      return 1000; /* другой нерезидент*/
   end;

   return -1;
END;


/**************************************************************************/
/*  получение вида субсидиария                                            */
/*  OBJTYPE_KINDSUBJ, LLCLASS_KINDSUBJ_SUB                                */
/**************************************************************************/
MACRO MC_GetKindSubsidiary( Issuer )

   var Res = false, Superior = -1, Kind = MC_GetKindParty( Issuer, Res, Superior );
      
   if( (Kind == 1) AND (Res == true) AND (Superior == {OurBank}) )
      return 4; /* банк - резидент */
   elif( (Kind == 1) AND (Res == false) AND (Superior == {OurBank}) )
      return 3; /* банк - нерезидент */
   /*!!!! зарезервировано*/
   /*
   elif( Kind ==  )
      return 200; /* паевой банк - резидент */
   elif( Kind ==  )
      return 201; /* паевой банк - резидент */
   */
   elif( (Kind != 1) AND (Res == true) AND (Superior == {OurBank}) )
      return 5; /* предприятие - резидент */
   elif( (Kind != 1) AND (Res == false) AND (Superior == {OurBank}) )
      return 6; /* предприятие - нерезидент */
   end;

   return -1;

END;

/**************************************************************************/
/*  получение вида депонента                                              */
/*  OBJTYPE_KINDOWNER, LLCLASS_OWNER_AVOIR                                */
/**************************************************************************/
MACRO MC_GetKindOwnerCB( PartyID )

  if( (PartyID == {OurBank}) OR (PartyID == -1) )
     return 2;  /* НашБанк */
  else
     return 1;  /* НеНашБанк (сторонний депонент) */
  end;

  return -1;
END;


/**************************************************************************/
/*  получение по депоненту параметры для КУ "-Расчеты, Выплаты по ц/б     */
/*  OBJTYPE_KINDSUBJ, LLCLASS_KINDSUBJ_DEPONENT                           */
/**************************************************************************/
MACRO MC_GetDeponent( PartyID: Integer )
  if( ПолучитьСубъекта( PartyID, party ) == 0 )
    
    if (party.NotResident == "") // резидент
        if (party.LegalForm == 1) // ЮЛ
          return 7; // ЮлРез
        else
          return 8; // ФлРез
        end;
    else                        // нерезидент
        if (party.LegalForm == 1) // ЮЛ
          return 9; // ЮЛНерез
        else
          return 10; // ЮЛНерез
        end; 
    end;

  end;

  return -1;
END;

/**************************************************************************/
/*  получение вида принадлежности ц/б (свои\чужие)                        */
/*  OBJTYPE_SECURITY_OWN, LLCLASS_SECUR_OWNERSHIP_SEPARATE                */
/**************************************************************************/
MACRO MC_GetKindSecOwn( PartyID )

  if(PartyID == {OurBank} )
     return 1;  /* Свои  */
  else
     return 2;  /* Чужие */
  end;

END;

/*------- Определение параметров, связанных с фин инструментами ----------*/

/*
AVOIRISSKIND_EQUITY_SHARE ,     // Акция обыкновенная
AVOIRISSKIND_PREFERENCE_SHARE,  // Акция привилегированная
AVOIRISSKIND_ORDINARY_BOND,     // Облигация бескупонная
AVOIRISSKIND_COUPON_BOND,       // Облишация купонная
AVOIRISSKIND_BILL,              // Вексель
AVOIRISSKIND_DRAFT,             // Чек
AVOIRISSKIND_PREFERENCE_CONV_SHARE,  // Акция привилегированная с правом конверсии
AVOIRISSKIND_CONV_BOND,              // Облигация с правом конверсии
AVOIRISSKIND_DEPOSIT_CERTIFICATE,    // Депозитный сертификат
AVOIRISSKIND_DEPOSITORY_RECEIPT,     // Депозитарная расписка
AVOIRISSKIND_PROMISSORY_NOTE,        // Долговое обязательство
AVOIRISSKIND_SAVING_CERTIFICATE
*/

/**************************************************************************/
/*  получение вида ц/б из портфеля                                        */
/*  OBJTYPE_KINDCB, LLCLASS_KINDCB_PORTF                                  */
/**************************************************************************/
MACRO MC_GetKindCbPortf( FIID, p_FiRole )
  var FiRole = FIROLE_UNDEF;

  if( p_FiRole != null )
     FiRole = p_FiRole;
  end;

  /*1-акция, 3-долговое обяз-во*/
  if( (FIID >= 0) AND (ПолучитьФинИн( FIID, fin ) == 0) ) 
     var IsInvestShare:bool = (FI_IsInvestmentShare(fin) or FI_IsHypothecaryCert(fin));

     if( (FiRole == FIROLE_BAINCONTR) and (IsInvestShare == true) )
        return 103; /*Инвестиционный пай*/
     elif( FI_IsShare( fin ) OR (IsInvestShare == true) )
        return 102; /*акция*/
     elif(  FI_IsBond( fin ) == true ) 
        return 3; /*долговое обязательство*/
     end;
  end;

  return -1;
END;

/**************************************************************************/
/*  получение вида ц/б в расходах по облигациям                           */
/*  OBJTYPE_KINDCB, LLCLASS_KINDCB_RASHOD                                 */
/**************************************************************************/
MACRO MC_GetKindCbRashod( FIID )
  /*2-Вексель, 4-Облигация, 5-Депозитный сертификат, 6-Сберегательный сертификат*/
  if( (FIID >= 0) AND (ПолучитьФинИн( FIID, fin ) == 0) ) 

     if( FI_IsBond( fin ) == true )
        return 4; /*облигация*/
     elif( fin.AvoirKind == AVOIRISSKIND_BILL )
        return 2; /*вексель*/
     elif( fin.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE )
        return 5; /*депозитный сертификат*/
     elif( fin.AvoirKind == AVOIRISSKIND_SAVING_CERTIFICATE )
        return 6; /*сберегательный сертификат*/
     end;
  end;

  return -1;
END;

/**************************************************************************/
/*  получение вида ц/б закрытого хранения                                 */
/*  OBJTYPE_KINDCB, LLCLASS_KINDCB_CLS_SAVE                               */
/**************************************************************************/
MACRO MC_GetKindCb_CLS_SAVE( FIID )
  if( (FIID >= 0) AND (ПолучитьФинИн( FIID, fin ) == 0) ) 
     if( fin.AvoirKind == AVOIRISSKIND_BILL )
        return 2; /*вексель*/
     end;
  end;

  return -1;
END;

/**************************************************************************/
/*  получение вида ц/б в резерве                                          */
/*  OBJTYPE_KINDCB, LLCLASS_KINDCB_RESERV                                 */
/**************************************************************************/
MACRO MC_GetKindCbReserve( FIID )
  /* 1 - Акция, 2-Вексель, 3 - долговое обяз-во*/
  if( (FIID >= 0) AND (ПолучитьФинИн( FIID, fin ) == 0) ) 

     if( FI_IsShare(fin) OR FI_IsInvestmentShare(fin) OR FI_IsHypothecaryCert(fin) )
        return 1; /*акция*/
     elif( fin.AvoirKind == AVOIRISSKIND_BILL )
        return 2; /*вексель*/
     elif( FI_IsBond( fin ) == true )
        return 3; /*долговое обязательство*/
     end;

  end;

  return -1;
END;

/***************************************************************************/
/* OBJTYPE_TSSUBKIND_FI - "ПодвидФИ"  - Подвиды ФИ                         */
/***************************************************************************/
MACRO MC_GetSubKind_FI( Fininstr )

   var Parm:integer = -1;
                                                          
   if( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_EQUITY_SHARE, Fininstr.rec.AvoirKind ) == true )
      Parm = 3;/*Акции обыкновенные*/
   elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_PREFERENCE_SHARE, Fininstr.rec.AvoirKind ) == true )
      Parm = 4; /*Акции привилегированные*/
   elif( FI_IsAvrKindBond( Fininstr.rec.AvoirKind ) )
      Parm = 5; /*Облигации*/
   elif( FI_IsDeposReceipt( Fininstr ) )
      Parm = 6; /*Депозитарные расписки*/
   elif( FI_IsAvrKindInvestmentShare(Fininstr.rec.AvoirKind) OR FI_IsAvrKindHypothecaryCert(Fininstr.rec.AvoirKind) )
      Parm = 7; /*Инвестиционные паи*/
   elif( Fininstr.rec.AvoirKind == AVOIRISSKIND_BILL )
      Parm = 8; /*Вексель*/
   elif( Fininstr.rec.AvoirKind == AVOIRISSKIND_DEPOSIT_CERTIFICATE )
      Parm = 9; /*Депозитарный сертификат*/
   elif( Fininstr.rec.AvoirKind == AVOIRISSKIND_SAVING_CERTIFICATE )
      Parm = 10; /*Сберегательный сертификат*/
   end; 

   return Parm;
END;

/***************************************************************************/
/* OBJTYPE_TSKIND_BOND - "ВидОбл"    - Вид облигации  */
/***************************************************************************/
MACRO MC_GetKind_Bond( Avoiriss )

   var Parm = -1;            
   var BondKind;          

   macro ПолучитьВидОблигации( avr )
     var cmd, DS;

     cmd = RSDCommand( "select t_AvoirKind from dfininstr_dbt where t_FIID = ?" );
     cmd.addParam( "", RSDBP_IN, avr.FIID );
     cmd.execute();
     DS = TRsbDataSet(cmd);
     if( DS.movenext() )
        return DS.AvoirKind;
     end;
     return -1;
   end;

   BondKind = ПолучитьВидОблигации( Avoiriss.rec );

   if( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_PARTY, BondKind ) == true )
      Parm = 1; /*Облигации субъектов федерации*/
   elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_FED, BondKind ) == true )
      Parm = 2; /*Федеральные облигации*/
   elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL, BondKind ) == true )
      Parm = 3; /*Муниципальные облигации*/
   elif( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE, BondKind ) == true )
      Parm = 4; /*Корпоративные облигации*/
   end;
   return Parm;
END;
