/*
  $Name:         bnkorder.mac
  $Module:       ББ
  $Description:  Макрос скроллинга банковских ордеров
*/
/***********************************************************************
          Автоматизированная банковская система RS-Bank v6.0           
                  Copyright (c) R-Style Software Lab                   
   Имя файла        : bnkorder.mac
   Описание         : Макрос скроллинга банковских ордеров
   Создан           : 11.02.2010                                       
***********************************************************************/

import bbbodoc;
import "cb_FillFactura.mac";

/* Установка подсказки оптимизатору ORACLE */

private const Hint_ByPaymStatus:string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx16) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByValueDate :string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx11) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByCloseDate :string = 
"/*+FIRST_ROWS LEADING(t pmrmprop oproper) INDEX(t dpmpaym_dbt_idx15) USE_NL(t pmrmprop oproper)*/";

private const Hint_ByStep      :string = 
"/*+FIRST_ROWS LEADING(t oproper pmpaym pmrmprop) INDEX(t doprstep_dbt_idx10) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper pmpaym pmrmprop)*/";

macro УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  
  // Возможные значения ScrolStates:
  //   0 Все
  //   1 Отложенные
  //   2 Открытые
  //   3 Закрытые
  //   4 Отвергнутые
  //   5 Для контроля, Для визирования, Ожидающие поступлений, В К2, В КОР, В картотеке невыясненных сумм

  if(  ( ScrolStates == 0 )   // Все
    or ( ScrolStates == 3 ) ) // Закрытые

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByCloseDate;
    end;

  elif(  ( ScrolStates == 1 )   // Отложенные
      or ( ScrolStates == 2 )   // Открытые
      or ( ScrolStates == 4 ) ) // Отвергнутые

    return Hint_ByPaymStatus;

  elif ( ScrolStates == 5 ) // Всякие подготовленные

    return Hint_ByStep;

  end;

  return DefaultHint;

end;

macro Новый_Документ()
  
  if( r_pmpaym.DocKind == DLDOC_BANKORDER )
    return BB_BankOrderNewDoc();
  end;

  msgbox("Неизвестный вид документа");
  return 1;

end;

/*
                      Пользовательская функция
     Режимы функции
        UFN_PANEL_INPUT = 1 - панель ввода
        UFN_PANEL_EDIT  = 2 - панель редактирования
        UFN_SCROL       = 3 - вызов из скроллинга
     (константы доступны при подключенном BankInter)
*/
macro Функция_Пользователя( режим )
  // режим == 4 (UFN_SCROL_FMASS) Вызов пользоветельской макро функции до группового выполнения
  // режим == 6 (UFN_SCROL_LMASS) Вызов пользоветельской макро функции после группового выполнения
  // Для данных режимов буфера не заполняются
  if ( (режим == 4) OR (режим == 6))
    return 0;
  end;

  if( r_pmpaym.DocKind == DLDOC_BANKORDER )
    return BB_BankOrderUserFunc( режим );
  end;

  msgbox("Неизвестный вид документа");
  return 1;
end;

macro Проверить_Документ( mode )
  
  if( r_pmpaym.DocKind == DLDOC_BANKORDER )
    return BB_BankOrderCheckDoc( mode );
  end;

  msgbox("Неизвестный вид документа");
  return 1;

end;
