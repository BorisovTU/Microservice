/*
$Name: sfinsdoc.mac
$Module: Ядро ГКБО
$Description: Формирование бухгалтерских проводок по оплате комиссий
*/

/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ГКБО"                                  */
/*                                                                          */
/*  Формирование бухгалтерских проводок по оплате комиссий                  */
/*                                                                          */
/*  Имя файла: sfinsdoc.mac                                                 */
/*  Создан: 30.07.2001                                                      */
/****************************************************************************/
import BankInter;
import SFInter;
import PTInter; // CORS_ACC_COPENPM, GetCorAcc
import PaymInter; // RsbPayment
private CONST NATCUR = 0;

private RECORD parm(arhdoc);
private RECORD sf_d(arhdoc);
private RECORD sf_mc(multycar);
private var    MultyMetodID;


private MACRO InsertDocument(rsbpaym, Ground/*2 обязательных параметра*/, CreditAccount, CreditAccountFIID, Amount, AmountFIID, IsPartOfMain )

 var paymtr;

  paymtr = rsbpaym.MakeTransaction(CreditAccount, CreditAccountFIID, Amount, AmountFIID);
  if( paymtr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
  end;

  if( ValType( IsPartOfMain ) != V_Undef ) paymtr.IsPartOfMain = IsPartOfMain; end;

  paymtr.Chapter = 1;
  paymtr.Date_Carry = paymtr.Date_Document = parm.Date_Carry;
  paymtr.Number_Pack   = parm.Number_Pack;
  paymtr.Numb_Document = parm.Numb_Document;
  paymtr.ResultCarry  = SFPAY_CARRY;
  paymtr.Kind_Oper     = " 1";
  paymtr.Ground           = Ground;
  
  if( not paymtr.Carry )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;  

  return 0;
END;

/* pmpaym - платеж(pmpaym.dbt), на основании которого формируется ПЗО */
MACRO ВставитьПлатуЗаОбслуживание(
             pmpaym,
             addrDoc, /* адрес вставляемого документа */
             Numb_Document, Date_Carry, Department, Number_Pack) /* Параметры проводки */
/*
    Информация из pmpaym, которая используется всегда:
(    Amount )
    AmountNDS
(    PayAmount)

    FIID
    PayFIID

    PayerAccount

+++ ReceiverAccount

    FeeType   -- в SfGetCategoryAccountsByPmPaym
    DefComID  -""-
*/
  RECORD accTransit(account);
  RECORD accIncome (account); 
  RECORD accTax    (account);// не нужен
  RECORD SfComissBuf(sfcomiss);


  VAR isTransitAcc = 0;   /* Признак использования транзитного счета -- не нужен*/
  VAR PayNDS = 0;         /* Признак оплаты НДС */
  var stat, mes;

  if( pmpaym.PaymentID == 0)
    MsgBox( "Не реализованы проводки ПЗО для несохраненного платежа" );
    return 1;
  end;

  stat = SfGetCategoryAccountsByPmPaym(pmpaym.FeeType,
                                   pmpaym.DefComId,
//                                   accIncome,
                                   accTax, // нехорошо, он хранится в платеже
                                   accTransit,
                                   isTransitAcc,
                                   PayNDS,
                                   MultyMetodID,
                                   SfComissBuf);
  if( stat )
    mes = GetErrMsg();
    if( mes == "" )
        msgbox("Ошибка при определении параметров взимания ПЗО");
    else
        msgbox("Ошибка #", stat, ": ", mes );
    end;
    
    return 1;
  end;

  ClearRecord(AccTax);
  AccTax.Chapter = 1;
  AccTax.Code_currency = NATCUR;
  AccTax.Account = pmpaym.AccountNDS;

  if( pmpaym.AmountNDS and ( PayNDS == SF_NOT_TAX ) )
    MsgBox("Не задан метод оплаты НДС");
    return 1;
  end;

  ClearRecord(parm);

  /* Заполняем параметры проводки */
  parm.Numb_Document = Numb_Document;
  parm.Date_Carry = Date_Carry;
  parm.Department = Department;
  parm.Number_Pack = Number_Pack;

  var DocGroundStr = "";

  var rsbpaym = RSBPAYMENT( pmpaym.PaymentID );
  if( rsbpaym == NULL )
    MsgBox("Ошибка при создании объекта платежа");
    return 1;
  end;

  ClearRecord(accIncome);

  if( rsbpaym.IsExternal )
    if( rsbpaym.IsCredit )
      accIncome.Account = GetCorAcc( pmpaym.PayFIID, rsbpaym.OutCorschem, CORS_ACC_COPENPM );
      accIncome.Code_Currency = pmpaym.PayFIID;
      accIncome.Chapter = 1;

      if(accIncome.Account == "")
        msgbox("В схеме расчетов не задан счет \"Средства клиентов по незавершенным расчетам\"");
        return 1;
      end;

    else
        MsgBox("Не реализованы проводки оплаты ПЗО для внешнего плательщика");
        return 1;
    end;
  else
      accIncome.Account = pmpaym.ReceiverAccount;
      accIncome.Code_Currency = pmpaym.PayFIID;
      accIncome.Chapter = 1;
  end;

  

  if(PayNDS == SF_NOT_TAX)      /* Не облагается */
    if(isTransitAcc == 0)       /* Оплата без использования транзитного счета */
       DocGroundStr =  "Оплата комиссии " +
                       SfComissBuf.Code;
                       
//Указывать accIncome - на случай внешнего платежа....
       if(InsertDocument(rsbpaym, DocGroundStr, accIncome.Account, accIncome.Code_Currency ) )
          return 1;
       end;
    else   /* Оплата c использованием транзитного счета */
       DocGroundStr =  "Оплата комиссии " +
                       SfComissBuf.Code
                       ;
      if(InsertDocument( rsbpaym, DocGroundStr, accTransit.Account, accTransit.Code_Currency  ) )
          return 1;
       end;

      DocGroundStr =  "Оплата комиссии " +
                      SfComissBuf.Code
                       ;
      if(InsertDocument(rsbpaym, DocGroundStr, accIncome.Account, accIncome.Code_Currency ) )
          return 1;
       end;
    end;
  elif( (PayNDS==SF_ADD_TAX) OR (PayNDS==SF_NOT_ADD_TAX) )
  /* Включен, или не включен, но облагается */
    if(isTransitAcc == 0) /* Оплата без использования транзитного счета */

       DocGroundStr =  "НДС по комиссии " +
                       SfComissBuf.Code
                       ;
      if(InsertDocument(rsbpaym, DocGroundStr, accTax.Account, accTax.Code_Currency, pmpaym.AmountNDS, pmpaym.BaseFIID, true ) )
          return 1;
      end;

       DocGroundStr =  "Оплата комиссии " +
                       SfComissBuf.Code
                       ;
      if(InsertDocument(rsbpaym, DocGroundStr, accIncome.Account, accIncome.Code_Currency ) )
          return 1;
      end;
    else    /* Оплата с использованием транзитного счета */

       DocGroundStr =  "Оплата комиссии " +
                       SfComissBuf.Code +
                       ". НДС по комиссии "+
                       string(pmpaym.AmountNDS) + "."
                       ;
      if(InsertDocument( rsbpaym, DocGroundStr, accTransit.Account, accTransit.Code_Currency ) )
          return 1;
      end;

       DocGroundStr =  "НДС по комиссии " +
                       SfComissBuf.Code
                       ;
      if(InsertDocument( rsbpaym, DocGroundStr, accTax.Account, accTax.Code_Currency, pmpaym.AmountNDS, pmpaym.BaseFIID, true ) )
          return 1;
      end;

       DocGroundStr =  "Оплата комиссии " +
                       SfComissBuf.Code
                       ;
      if(InsertDocument( rsbpaym, DocGroundStr, accIncome.Account, accIncome.Code_Currency ) )
          return 1;
      end;
    end;
  end;

  return 0;

  OnError(er) /* Обработка ошибок времени выполнения */
      MsgBox(String(er.Message, "|Модуль: ", er.Module," Строка: ", er.Line));
      return 1;
END;
