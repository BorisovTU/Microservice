/*
 *    RS-Bank
 * 
 *
 */

/* Проверка на числовой номер */
private macro IsDigitNumber( Number )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Number)) )
        ch = SubStr( Number, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1;
        end;
        i = i + 1;
      end;

  return stat;

end;


private macro CheckDate( fname )

  var day, mon, year;  
  var sDay, sMon, sYear;

  sDay  = SubStr( fname, 6, 2 );
  sMon  = SubStr( fname, 8, 2 );
  sYear = SubStr( fname, 10, 2 );
  
  /* Проверка, являются ли символы цифрами */
  if( IsDigitNumber( sDay ) == 0 )    
    day  = Int( sDay );
  else
    return 1;
  end;

  if( IsDigitNumber( sMon ) == 0 )
    mon  = Int( sMon );
  else
    return 1;
  end;

  if( IsDigitNumber( sYear ) == 0 ) 
    year = Int( sYear );
  else
    return 1;
  end;

  if( year < 0 )
    return 1;
  end;

  if( (mon < 1) or (mon > 12) )
    return 1;
  end;

  if( day < 1 )
    return 1;
  end;

  if( mon == 2 )
    if( day > 29 )
      return 1;
    end;
  end;

  if( (mon==1) or (mon==3) or (mon==5) or (mon==7) or (mon==8) or (mon==10) or (mon==12) )
    if( day > 31 )
      return 1;
    end;
  else
    if( day > 30 )
      return 1;
    end;
  end;

  return 0;
  
end;


private macro CheckSymP( fname )

  var p, sP;

  sP = SubStr( fname, 5, 1 );
  if( (sP == "0") or (sP == "1") or (sP == "5") or (sP == "a") or (sP == "b"))
    return 0;
  end;

  return 1;   

end;

private macro CheckExt( fname )

  var ext;
  if( SubStr( fname, 12, 1 ) == "." )
    ext = SubStr( fname, strlen(fname)-2, 3 );
    if( IsDigitNumber( ext ) )
      return 1;  
    end;
  else
    return 1;
  end;

  return 0;   

end;


macro CheckFileNameFormat( fname )

  var stat = 0;      

  stat = CheckExt( fname );
  
  if( not stat )    
     stat = CheckSymP( fname );
  end;
  
  
  if( not stat )    
     stat = CheckDate( fname );
    end;  
  
  return stat;
  
end;





