/*
$Name:        ws_web_system.mac
$Module:      Web-общее
$Description: Макрос с различными системными сервисами Web-клиента RS-Bank V.6
*/

Import "BankInter", "RSD";

const
  EW_WEBACTION_REQ_STATE_OPEN     = 0,
  EW_WEBACTION_REQ_STATE_CLOSED   = 1,
  EW_WEBACTION_REQ_STATE_FINISHED = 2;

/*
 *  Информация о результатах выполнения Ping
 */
class TEasyWinActionInfo
  
  var Code         : integer;  /* Результат работы сервиса */
  var ErrorMessage : string;   /* Текст с расшифровкой результата работы сервиса */
  var RequestID    : string;   /* Идентификатор сформированного сообщения */

end;

class TEasyWinActionResult(_Xml)

  var Xml = _Xml;
end;


/*
 * Вставка сообщения о выполнении в EasyWin-интерфейсе действия по запросу от Web-интерфейса
 */
macro WS_AddWebEasyWinAction( Oper : integer, MAC_Address : string, SessionID : string, XMLRequest : string )

  var ResultInfo = TEasyWinActionInfo;
  
  ResultInfo.Code = AddWebEasyWinAction( Oper, MAC_Address, SessionID, XMLRequest, ResultInfo.RequestID );

  if( ResultInfo.Code != 0 )
    CreateErrMsg( ResultInfo.Code );
    ResultInfo.ErrorMessage = GetErrMsg();
  end;

  return ResultInfo;

end;

/*
 * Выполнение системного модуля EasyWin
 */
macro WS_RunEasyWinMenuItem( menuParam : string )

  var SubSystemSymb : string;
  var SubSystemID : integer;
  var Module : integer;

  var stat : integer = 0;

  /* Определение подсистемы */

  GetCmdLineParm( "mod", SubSystemSymb, V_STRING, menuParam );
  if( (SubSystemSymb != null) and (StrLen(SubSystemSymb) == 1) )

    SubSystemID = CodeFor(SubSystemSymb);
    
  else

    GetCmdLineParm( "modnum", SubSystemID, V_INTEGER, menuParam );

    if( (SubSystemID == null) or (SubSystemID == 0) )

      stat = 1;
      msgbox( "Не задана подсистема. Некорректно указан параметр modnum." );

    end;

  end;


  /* Определение номера системного модуля */

  if( stat == 0 )

    GetCmdLineParm( "exec", Module, V_INTEGER, menuParam );

    if( (Module == null) or (Module == 0) )

      stat = 1;
      msgbox( "Не задан номер модуля" );

    end;

  end;

  /* Определение номера системного модуля */

  if( stat == 0 )

    system( Module, SubSystemID, menuParam );

  end;

end;


macro WS_WaitForEasyWinActionCompletion(RequestID)
  var Cmd = RsdCommand("select t_state from dew_web_action_dbt where t_requestid = ?");
  Cmd.AddParam("p_requestid", RsdBp_In, RequestID);
  var count = 0;

  for (count, 1, 50)
    Cmd.Execute;
    var Rs = RsdRecordset(Cmd);
    if (Rs.MoveNext)
      if (Rs.Value(0) == EW_WEBACTION_REQ_STATE_FINISHED)
        return true;    
      end;    
    else
      return true;
    end; 
    Delay(1000);
  end;
  return false;
end;


macro WS_GetEasyWinActionResult(RequestID)
  var Cmd = RsdCommand("select t_xmlResponseSize, t_xmlResponse from dew_web_action_dbt where t_requestid = ?");
  Cmd.AddParam("p_requestid", RsdBp_In, RequestID);

  Cmd.Execute;
  var Rs = RsdRecordset(Cmd);
  if (Rs.MoveNext)
    var Xml;
    Rs.Fld(1).Read(Xml, Rs.Value(0));
    return TEasyWinActionResult("<![CDATA[" + Xml + "]]>");
  else
    return null;
  end; 
end;

/*
 * Создать оповещение лицензирования об использовании демо-режима
 */
macro Web_CreateDemoModeLicAlert( RestrictionID )

  CreateDemoModeLicAlert( RestrictionID );

end;
