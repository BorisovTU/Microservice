/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : pmch302.mac
  Created     : 20.11.2007
  Programmer  : Осмаковский Е.В.
  Description : Процедура замены закрытых счетов в платежах

└───────────────────────────────────────────────────────────────────────────*/
Import PaymInter, oralib, globals, likepy, pm_tools;

private var Tb_pmch302  = Tbfile("pmch302.tmp", "w");
private var ObjPayment:RsbPayment; 
private var ChangeFD = false;//Флаг необходимости изменения первички

//Маcки счетов, попадающие под закрытие по 302П
private var  MaskAcc:TArray = TArray(); 
  MaskAcc(MaskAcc.size) = "10604%"; 
  MaskAcc(MaskAcc.size) = "10702%";
  MaskAcc(MaskAcc.size) = "10703%";
  MaskAcc(MaskAcc.size) = "10704%";
  MaskAcc(MaskAcc.size) = "40409%"; 
  MaskAcc(MaskAcc.size) = "475%"; 
  MaskAcc(MaskAcc.size) = "50609%"; 
  MaskAcc(MaskAcc.size) = "50610%";
  MaskAcc(MaskAcc.size) = "50611%";
  MaskAcc(MaskAcc.size) = "50612%";
  MaskAcc(MaskAcc.size) = "50613%";
  MaskAcc(MaskAcc.size) = "508%"; 
  MaskAcc(MaskAcc.size) = "50111%"; 
  MaskAcc(MaskAcc.size) = "50112%";
  MaskAcc(MaskAcc.size) = "50113%";
  MaskAcc(MaskAcc.size) = "50114%";
  MaskAcc(MaskAcc.size) = "50115%";
  MaskAcc(MaskAcc.size) = "50213%"; 
  MaskAcc(MaskAcc.size) = "50312%"; 
  MaskAcc(MaskAcc.size) = "50405%"; 
  MaskAcc(MaskAcc.size) = "50406%"; 
  MaskAcc(MaskAcc.size) = "52502%"; 
  MaskAcc(MaskAcc.size) = "60303%"; 
  MaskAcc(MaskAcc.size) = "60304%"; 
  MaskAcc(MaskAcc.size) = "61201%"; 
  MaskAcc(MaskAcc.size) = "61202%";
  MaskAcc(MaskAcc.size) = "61203%";
  MaskAcc(MaskAcc.size) = "61204%";
  MaskAcc(MaskAcc.size) = "61205%";
  MaskAcc(MaskAcc.size) = "61206%";
  MaskAcc(MaskAcc.size) = "61207%";
  MaskAcc(MaskAcc.size) = "61208%";
  MaskAcc(MaskAcc.size) = "61302%"; 
  MaskAcc(MaskAcc.size) = "61303%"; 
  MaskAcc(MaskAcc.size) = "61306%"; 
  MaskAcc(MaskAcc.size) = "61308%"; 
  MaskAcc(MaskAcc.size) = "61402%"; 
  MaskAcc(MaskAcc.size) = "61404%"; 
  MaskAcc(MaskAcc.size) = "61406%"; 
  MaskAcc(MaskAcc.size) = "61408%"; 
  MaskAcc(MaskAcc.size) = "701%";
  MaskAcc(MaskAcc.size) = "702%";
  MaskAcc(MaskAcc.size) = "703%"; 
  MaskAcc(MaskAcc.size) = "704%"; 
  MaskAcc(MaskAcc.size) = "807%";


//Формирует условие отбора по маске
private macro AddMaskAcc(acc:string):string

  var i = 1;
  var StrMask = acc + " LIKE '" + MaskAcc(0) + "' ";
  while(i < MaskAcc.size)
    StrMask = StrMask + "or "+ acc + " LIKE '" + MaskAcc(i) + "' ";
    i = i + 1;
  end;
  return StrMask;
end;

//Запрос для отбора документов по счетам, закрытым в процессе перехода на 302-П
private macro CreateQuery(prm:TArray):string
  
  var Query = "INSERT INTO dpmch302_tmp "+
              "SELECT pm.t_paymentid, CHR(0), CHR(1), CHR(1) "+
                "FROM dpmpaym_dbt pm "+
                "INNER JOIN dpmprop_dbt db on ( db.t_debetcredit = 0 and db.t_paymentid   = pm.t_paymentid)  "+
                "INNER JOIN dpmprop_dbt cr on ( cr.t_debetcredit = 1 and cr.t_paymentid   = pm.t_paymentid)  "+
                "INNER JOIN ( SELECT t_Code_Currency, t_Account "+
                               "FROM daccount_dbt acc "+
                              "WHERE acc.t_chapter = 1 "+
                                "AND acc.t_open_close = 'З' "+
                                "AND (" + AddMaskAcc("acc.t_Account") +" )) ac "+
                        "ON  ( ac.t_code_currency = pm.t_fiid_futurerecacc  "+
                              "AND ac.t_account = pm.t_futurereceiveraccount "+
                              "AND cr.t_group = 2 "+
                              "AND pm.t_receiverbankid = :OurBank1  )  "+
                           "OR "+
                            "( ac.t_code_currency = pm.t_fiid_futurepayacc  "+
                              "AND ac.t_Account = pm.t_futurepayeraccount "+
                              "AND db.t_group = 2 "+
                              "AND pm.t_payerbankid = :OurBank2 ) "+
                "WHERE pm.t_paymstatus IN (0, 1000, 2000, 2990, 2995, 3000, 3010, 3100) "+
                  "AND pm.t_dockind IN ( 16, 27, 201, 202, 320, 322, 450 ) "+
                  //"AND pm.t_valuedate >= TO_DATE ('01.01.2008', 'dd.mm.yyyy') "+
                "GROUP BY pm.t_PaymentID"; //Если в платеже два счета закрыты, то без "GROUP BY pm.t_PaymentID " получается 2 записи

  prm[prm.size] = SQLParam("OurBank1", {OurBank});
  prm[prm.size] = SQLParam("OurBank2", {OurBank});

  return Query;
end;

//Необходим для формирования отчета (содержит одну запись обработанного платежа)
private class RecReport(Tb_pmch302:Tbfile, ObjPayment:RsbPayment)
  var О,
      Номер,
      ДатаВалют,
      Валюта,
      Сумма,
      БанкПлат,
      СчетПлательщика,
      НовыйСчетСписания,        
      БанкПолуч,
      СчетПолучателя,
      НовыйСчетЗачисления;

  О                   = Tb_pmch302.rec.IsFinished;
  Номер               = ObjPayment.Number;
  ДатаВалют           = ObjPayment.ValueDate;
  Валюта              = ObjPayment.BaseFIID;
  Сумма               = ObjPayment.BaseAmount;
  БанкПлат            = ObjPayment.PayerBankCode;   
  СчетПлательщика     = ObjPayment.PayerAccount;
  НовыйСчетСписания   = Tb_pmch302.rec.NewPayerAccount;
  БанкПолуч           = ObjPayment.ReceiverBankCode;
  СчетПолучателя      = ObjPayment.ReceiverAccount;
  НовыйСчетЗачисления = Tb_pmch302.rec.NewReceiverAccount;

end;

macro PrintBeginTab
[ Перечень документов по закрытым счетам 
                                                                                                                                                
  ┌───┬───────┬───────────┬──────┬───────────────┬──────────┬───────────────────────┬───────────────────────┬───────────┬───────────────────────┬───────────────────────┐
  │ О.│Номер  │Дата валют.│Валюта│Сумма          │Банк плат.│Счет плательщика       │Новый счет списания    │Банк получ.│ Счет получателя       │Новый счет зачисления  │
  ├───┼───────┼───────────┼──────┼───────────────┼──────────┼───────────────────────┼───────────────────────┼───────────┼───────────────────────┼───────────────────────┤];
end;

private macro PrintRecord(rec:RecReport)
  //Найдем код валюты
  var Tb_fininstr  = Tbfile("fininstr.dbt");
  Tb_fininstr.rec.FIID = rec.Валюта;
  Tb_fininstr.GetEQ();
[ │ # │#######│###########│######│###############│##########│#######################│#######################│###########│#######################│#######################│]
  (rec.О,                 
   rec.Номер,             
   rec.ДатаВалют,         
   Tb_fininstr.rec.CodeInAccount,            
   rec.Сумма,             
   rec.БанкПлат,          
   rec.СчетПлательщика,   
   rec.НовыйСчетСписания, 
   rec.БанкПолуч,         
   rec.СчетПолучателя,    
   rec.НовыйСчетЗачисления);
end;

macro PrintEndTab
[ └───┴───────┴───────────┴──────┴───────────────┴──────────┴───────────────────────┴───────────────────────┴───────────┴───────────────────────┴───────────────────────┘];
end;

macro PrintRecordMass( PaymentID ) 
  ObjPayment = RsbPayment( PaymentID );
  Tb_pmch302.rec.PaymentID = PaymentID;
  if( ObjPayment and Tb_pmch302.GetEQ() )
    PrintRecord(RecReport(Tb_pmch302, ObjPayment));
  else
    msgbox("Ошибка получения данных для печати.");
  end;
end;

//Печать отчета
private macro PrintReportProc(FinishedDocs:TArray) 
  var i = 0;
  PrintBeginTab();
  while(i < FinishedDocs.size)
    PrintRecord(FinishedDocs[i]);
    i = i + 1;
  end;
  PrintEndTab();
end;
 

private macro UpdatePM()
  
  var FirstDoc:object;
  if( ChangeFD and( (ObjPayment.DocKind == 201) or (ObjPayment.DocKind == 202) or (ObjPayment.DocKind == 27)))
    
    if(ObjPayment.DocKind == 201/*PS_PAYORDER*/)
      FirstDoc = GenObject( "RsbPsPayOrder", ObjPayment.DocumentID ); 
    elif(ObjPayment.DocKind == 202/*PS_CPORDER*/)
      FirstDoc = GenObject( "RsbPsCpOrder", ObjPayment.DocumentID ); 
    elif(ObjPayment.DocKind == 27/*BBANK_CPORDER*/)
      FirstDoc = GenObject( "RsbBbCpOrder", ObjPayment.DocumentID ); 
    end;
      
    if( FirstDoc )
      FirstDoc.PayerAccount = ObjPayment.FuturePayerAccount;
      if( FirstDoc.Update() )
        AbortTrn();
      end;
    else
      AbortTrn();
    end;

  end;

  Tb_pmch302.rec.IsFinished = "X";
  if(ObjPayment.Update() or (not Tb_pmch302.Update()))
    AbortTrn();
  end;
end;


//Процедура замены счетов
macro ЗаменитьСчета()
                                  
  var FinishedDocs = TArray();
  var stat:bool = true;

  Tb_pmch302.ReWind();
  while(Tb_pmch302.Next())
    if(Tb_pmch302.rec.IsFinished != "X")
      if((Tb_pmch302.rec.NewPayerAccount != "") or (Tb_pmch302.rec.NewReceiverAccount != ""))
        
        ObjPayment = RsbPayment( Tb_pmch302.rec.PaymentID );

        if(stat and (Tb_pmch302.rec.NewPayerAccount != ""))

          if(ObjPayment.FuturePayerAccount == ObjPayment.PayerAccount)
            stat = ObjPayment.SetPayerAccount(ObjPayment.PayerFIID, 
                                              ObjPayment.Chapter, 
                                              Tb_pmch302.rec.NewPayerAccount); 
            ChangeFD = true;
          end;
          ObjPayment.FuturePayerAccount = Tb_pmch302.rec.NewPayerAccount;
        end;
        
        if( stat and (Tb_pmch302.rec.NewReceiverAccount != ""))

          if(ObjPayment.FutureReceiverAccount == ObjPayment.ReceiverAccount)
            stat = ObjPayment.SetReceiverAccount(ObjPayment.ReceiverFIID,
                                                 ObjPayment.Chapter, 
                                                 Tb_pmch302.rec.NewReceiverAccount); 
          end;

          ObjPayment.FutureReceiverAccount = Tb_pmch302.rec.NewReceiverAccount;
        end;

        if( stat )
          if( not ProcessTrn(NULL, "UpdatePM", Tb_pmch302 ))
            Tb_pmch302.rec.IsFinished = "";
          end;
          //Заполняем данные для отчета
          FinishedDocs[FinishedDocs.size] = RecReport(Tb_pmch302, ObjPayment);
        else
          msgbox("Ошибка установки нового номера счета платежа PaymentID = ", Tb_pmch302.rec.PaymentID );
        end;

      end;
    end;
    ChangeFD = false;
  end;

  //Печатаем отчет
  if(FinishedDocs.size)
    PrintReportProc(FinishedDocs);
  else
    msgbox("Не найдено документов, для которых |изменен номер счета зачисления (списания)");
    return 1;
  end;

  return 0;
end;

//Проверить наличие записей в таблице
macro IsEmptyTab()
  Tb_pmch302.ReWind();
  if(Tb_pmch302.Next())
    return false;
  end;
  return true;
end;

//Заполняем временную таблицу
macro ЗаполнитьТаблицу()

  //Проверяем есть ли записи в таблице
  if((not IsEmptyTab()) and 
   ( RsbGetTrue(false,false,"Обновить список с учетом вновь появившихся документов? " +
                            "|Все несохраненные изменения в этом случае будут утеряны.") == false ))
   return 0; 
  end;
  
  var prm:TArray = TArray();
  //Удаляем старые данные
  execSQL( "delete from dpmch302_tmp", NULL, false );
  //Заполняем новыми
  var StrQuery = CreateQuery(prm);
  execSQL( StrQuery, prm, false );
  return 0;
end;

