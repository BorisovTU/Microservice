/*
 * Макрос выполнения шага "Начисление" операции по обработке периодической комиссии
 */

Import InsCarryDoc, "sf_lib.mac";

record SfAccrueRec("sfaccrue.dbt");

record SfDefComRec("sfdef.dbt");

record SfContrRec("sfcontr.dbt");
record SfConComRec("sfconcom.dbt");


macro InsertSfAccrueLogRec( SfDefComRec, SfAccrueRec, debetAccount, creditAccount )

  var strSql : string; 
  var query = "";  
  var cmd, rs;
  var count = 0;

  query = " SELECT count(1) as ncount FROM DSFREPACC_TMP WHERE T_BEGINDATE = ?  AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND T_KIND = ? ";

  cmd = RsdCommand( query );

  cmd.addParam( "", RSDBP_IN, SfAccrueRec.BeginDate    );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.SfContrID    );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.FeeType      );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.CommNumber   );
  cmd.addParam( "", RSDBP_IN, 0                        );

  rs = RsdRecordset( cmd );

  if( rs.moveNext() )
    count = rs.value("ncount" );
  end;

  if(count == 0)
    strSql = " INSERT INTO DSFREPACC_TMP ( T_SFDEFCOMID, T_BEGINDATE , T_ENDDATE, T_TRANSACTIONDATE, " +          
             " T_AMOUNT, T_DEBIT, T_CREDIT, T_CONTRID, T_FEETYPE, T_COMISSNUMBER, T_DEPARTMENT, T_KIND, T_ERRORCODE ) "
             " VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, 0, 0 )" ;
  else
    strSql = " UPDATE DSFREPACC_TMP SET T_BEGINDATE = ?, T_ENDDATE = ?, T_TRANSACTIONDATE = ?, " +          
             " T_AMOUNT = ?, T_DEBIT = ?, T_CREDIT = ?, T_CONTRID = ?, T_FEETYPE = ?, T_COMISSNUMBER = ?, T_DEPARTMENT = ?, T_KIND = 0, T_ERRORCODE = 0 " +
             " WHERE T_BEGINDATE = ?  AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND T_KIND = ?";
  end;

   
  cmd = RsdCommand( strSql );
  if(count == 0)
    cmd.addParam( "", RSDBP_IN, SfDefComRec.ID );
  end;
  cmd.addParam( "", RSDBP_IN, SfAccrueRec.BeginDate );
  cmd.addParam( "", RSDBP_IN, SfAccrueRec.EndDate );
  cmd.addParam( "", RSDBP_IN, SfAccrueRec.TransactionDate );
  cmd.addParam( "", RSDBP_IN, SfAccrueRec.Amount );
  cmd.addParam( "", RSDBP_IN, debetAccount );
  cmd.addParam( "", RSDBP_IN, creditAccount );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.SfContrID );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.FeeType );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.CommNumber );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.Department );

  if(count != 0)
    cmd.addParam( "", RSDBP_IN, SfAccrueRec.BeginDate );
    cmd.addParam( "", RSDBP_IN, SfDefComRec.SfContrID );
    cmd.addParam( "", RSDBP_IN, SfDefComRec.FeeType   );
    cmd.addParam( "", RSDBP_IN, SfDefComRec.CommNumber);
    cmd.addParam( "", RSDBP_IN, 0                     );
  end;
  
  cmd.execute();   

  return 0;

end;

macro UpdateSfAccrueLogRec( SfDefComRec )

  var strSql : string; 
  var cmd : RsdCommand;

  var ErrMsg = "Ошибка при начислении комиссии: "   + GetErrMsg();

  strSql = " UPDATE DSFREPACC_TMP " +
           " SET T_COMMENT = ?, " +                   
           " T_ERRORCODE = 1 "
           " WHERE T_BEGINDATE = ? AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND (T_ERRORCODE IS NULL OR T_ERRORCODE = 0)";
  
   cmd = RsdCommand( strSql );

   cmd.addParam( "", RSDBP_IN, ErrMsg );
   cmd.addParam( "", RSDBP_IN, SfDefComRec.DatePeriodBegin );
   cmd.addParam( "", RSDBP_IN, SfDefComRec.SfContrID       );
   cmd.addParam( "", RSDBP_IN, SfDefComRec.FeeType         );
   cmd.addParam( "", RSDBP_IN, SfDefComRec.CommNumber      );

   cmd.execute();

   return 0;
end;


/*
 * Функция выполнения шага операции
 */
macro ExecuteStep( outBuff, primDoc )

  var stat = 0;
  var debetAccount = "", creditAccount = "";
  var FIID;
  var SfRegVal:integer;  

  SetBuff( SfDefComRec, primDoc );  

  if( SfAccrueRec.Amount > $0 )

    stat = SfCarryAccrue( SfDefComRec, SfAccrueRec, SfContrRec, SfConComRec, @debetAccount, @creditAccount );

    if( stat == 0 )
      stat = InsertSfAccrueLogRec( SfDefComRec, SfAccrueRec, debetAccount, creditAccount ); 
    end;

  end; 

  return stat;

end;


macro PostStepAction( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                primDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */     
   
   
   if( errTrn and (message == OP_EXECUTE_STEP) ) 
       /* Ошибка при выполнении шага */
     SetBuff( SfDefComRec, primDoc );
     
     UpdateSfAccrueLogRec( SfDefComRec )  
   end;   
   
   return 0;
end;


