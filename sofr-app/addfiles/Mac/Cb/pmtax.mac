/*
 $Name: pmtax.mac
 $Module: Ядро Banking
 $Description: Проверка налогового платежа
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : pmtax.mac                                          */
/*                                                                      */
/*  Описание       : Проверка налогового платежа.                       */
/*                                                                      */
/*  Программист    : Копачев Д.В.                                       */
/*                                                                      */
/*  Создан         : 16.01.04                                           */
/*                                                                      */
/************************************************************************/

import Globals, PaymInter, BankInter, likepy, oralib, "bnk_common.mac", "pm_setst.mac";

private const WLD_TRNP_UFBS = 9; // Транспорт УФЭБС


private MACRO PM_CheckTaxProp( PaymentID:integer, NeedToReject:bool ):integer

  var CheckParamsMode = 1; //Значение обоих настроек по умолчанию
  var Payment:RsbPayment = RsbPayment( PaymentID );
  var select = " Select 1 from dpmprop_dbt "
               "  Where t_PaymentID = :PaymentID "
               "    and t_IsSender = chr(0) "
               "    and t_TpID = :TpUfebs "
               "    and t_Group = :GroupExternal ";
  if( existsSQLSelect( select, MakeArray( SQLParam("PaymentID", PaymentID),
                                          SQLParam("TpUfebs", WLD_TRNP_UFBS),
                                          SQLParam("GroupExternal", PAYMENTS_GROUP_EXTERNAL))))
    GetRegistryValue( "COMMON\\CHECKTAXPAYMPARAMS\\НА ПРЕДОБРАБОТКЕ ДЛЯ УФЭБС", V_INTEGER, CheckParamsMode);
  else
    GetRegistryValue( "COMMON\\CHECKTAXPAYMPARAMS", V_INTEGER, CheckParamsMode);
  end;
  execSQL("DELETE FROM dpmtax_tmp");

  execSQL( "INSERT INTO dpmtax_tmp ( t_PaymentID ) values ( :PaymentID ) ",
           makeArray( SQLParam( "", PaymentID ) ) 
         );

  var retval = execStoredFunc( "RSI_PM_TAXPROP.MassCheckTaxProp", V_INTEGER );  

  var rs = execSQLselect( "select t_Message from dpmtax_tmp where t_PaymentID = :PaymentID", makeArray( SQLParam( "", PaymentID )), FALSE );

  if( rs.moveNext() )

    if( strlen(rs.value("t_Message")) == 0 )
      RsbPayment(PaymentID).Notes.DelNote( NOTEKIND_PAYM_TAXWARNING );
    else
      if( CheckParamsMode == 1 )
        RsbPayment(PaymentID).Notes.AddNote( NOTEKIND_PAYM_TAXWARNING, SubStr(StrSubst(rs.value(0), "|", "\n"), 1, 1499)); // 1499 - ограничение на длинну примечания
        CreateWarning( null, PAYMERR_TAXWARNING, rs.value("t_Message") );
      elif( CheckParamsMode == 2 )
        CreateErrMsg( PAYMERR_TAXWARNING, rs.value("t_Message") );
        if( NeedToReject == true )
          RejectPayment( Payment, rs.value("t_Message"));
        end;
        return PAYMERR_TAXWARNING;
      end;
    end;
  else
    CreateErrMsg( PAYMERR_TAXWARNING, "Ошибка при проверке налоговых реквизитов." );
    return PAYMERR_TAXWARNING;
  end;
  
  return 0;

end;

// -----------------------------------------------------------------
// Проверка налоговых реквизитов при вводе/редактировании документа
// !!!вызывается в транзакции!!!, вопросы задвать нельзя
// -----------------------------------------------------------------
MACRO PM_CheckTaxPropForSave( PaymentID:integer ):integer
  return PM_CheckTaxProp( PaymentID );  
END;

// -----------------------------------------------------------------
// Проверка налоговых реквизитов на шаге операции (единичный режим)
// -----------------------------------------------------------------
MACRO PM_CheckTaxPropForStep( PaymentID:integer ):integer
  return PM_CheckTaxProp( PaymentID, true );
end;

private macro IsLikeKINDBUDGPAY( BttTICode : string, RecAccount : string, 
                                 RegBranchName : string,
                                 DefaultMaskBtt : string, 
                                 DefaultMaskAcc : string ) : bool

  var CommonPath = "CB\\PAYMENTS\\DEPARTMENTALINFO\\KINDBUDGPAY\\" + RegBranchName + "\\",
      PathBtt = CommonPath + "КБК",
      PathAcc = CommonPath + "СЧЕТ";

  if( (CompareStrWithMasks( Bnk_GetRegistryValue(PathBtt, V_STRING, DefaultMaskBtt), 
                            BttTICode
                          ) == 0)
      and
      (CompareStrWithMasks( Bnk_GetRegistryValue(PathAcc, V_STRING, DefaultMaskAcc), 
                            RecAccount
                          ) == 0)
    )
    return TRUE;
  end;

  return FALSE;
end;
                                                                                       
// Определение типа платежа
macro GetPayType(CheckedAccount, BttTICode : string, PayType):integer

  if  ( IsLikeKINDBUDGPAY(BttTICode, CheckedAccount, "НАЛОГОВЫЙ",  "182*", "40101*") )
    return BPT_TAX;
  elif( IsLikeKINDBUDGPAY(BttTICode, CheckedAccount, "ТАМОЖЕННЫЙ", "153*", "40101*") )
    return BPT_CUSTOM;
  else
    return BPT_OTHERS;
  end;
  
  return PayType;

end;

macro CheckTaxPropForUfebsMes
( Payment : RsbPayment, 
  MesForm : string, 
  ErrMsg : @string

) : integer

  execSQL("delete from dpmmeschk_tmp");

  var params : TArray = makeArray
  ( SQLParam( "p_Type", MesForm ),
    SQLParam( "p_PaymentID", Payment.PaymentID ),
    SQLParam( "p_DocKind", Payment.DocKind ),
    SQLParam( "p_RmNumber", Payment.Number ),
    SQLParam( "p_RmDate", Payment.Date ),
    SQLParam( "p_PayerINN", Payment.PayerINN ),
    SQLParam( "p_ReceiverINN", Payment.ReceiverINN ),
    SQLParam( "p_TaxAuthorState", Payment.TaxAuthorState ),
    SQLParam( "p_BTTTICode", Payment.BTTTICode ),
    SQLParam( "p_OkatoCode", Payment.OkatoCode ),
    SQLParam( "p_TaxPmDate", Payment.TaxPmDate ),
    SQLParam( "p_TaxPmNumber", Payment.TaxPmNumber ),
    SQLParam( "p_PartPaymDateMain", Payment.PartPaymDateMain),
    SQLParam( "p_UIN", Payment.UIN ),
    SQLParam( "p_ReceiverAccount", Payment.ReceiverAccount),
    SQLParam( "p_TaxPmGround", Payment.TaxPmGround),
    SQLParam( "p_TaxPmPeriod", Payment.TaxPmPeriod),
    SQLParam( "p_ReceiverBankID", Payment.ReceiverBankID)
  );

  var retval : integer = execStoredFunc( "RSI_PM_TAXPROP.CheckTaxPropForUfebsMesRSL", V_INTEGER, params );
  if( retval )
    var rs = execSQLselectPrm( "select t_ErrMes, NVL(t_ShortErrMes, CHR(1)) ShortErrMes from dpmmeschk_tmp "
                                 " where t_PaymentID = :PaymentID ",
                                 SQLParam("PaymentID", Payment.PaymentID) );
    if(rs and rs.moveNext())
      ErrMsg = IfThenElse( MesForm != "ED108", rs.value("t_ErrMes"), rs.value("ShortErrMes") );
      
        Payment.Notes.DelNote( NOTEKIND_PAYM_TAXWARNING );
        Payment.Notes.AddNote( NOTEKIND_PAYM_TAXWARNING, SubStr(StrSubst(rs.value("t_ErrMes"), "|", "\n"), 1, 1499));
        Payment.Notes.Save();
    end;

  else
    Payment.Notes.DelNote( NOTEKIND_PAYM_TAXWARNING );    
    Payment.Notes.Save();
  end;

  if(strlen(ErrMsg) >= 500 )
    ErrMsg = SubStr(ErrMsg, 1, 499);
  end;

  return retval;
end;
