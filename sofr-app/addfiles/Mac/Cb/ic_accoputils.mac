/*
$Name: ic_accoputils.mac
$Module: ББ
$Description: общие функции для сервиса выписки
*/

import oralib, likepy, FIInter, CurrInter;

macro GetRateMain(FIID, D: date)
  var ft;
  record rate(ratedef);
  if (FIID == 0)
    return 1;
  end;
  ft = ПолучитьКурс(rate, NATCUR, FIID);
  if (ft == 0)
    ft = ПолучитьЗначениеКурса(rate, D);
  end;
  return ConvertSum($1, rate.rate, rate.scale, rate.point, rate.isinverse);
end;

class TExtractDocParent(_TypeAccStat, _NationalCurrency, _FIID, _cmd)
  var PaymentID: String;
  var PostDate: Date;
  var PostTime: Time;
  var InBalance: Money;
  var TurnType: Integer;
  var Amount: Money;
  var CurRate: Double;
  var AmountNatCur: Money;
  var DocKind: String = NULL;
  var DocNumber: String = NULL;
  var DocDate: Date = NULL;
  var Ground: String = NULL;

  if (_cmd != NULL)
    PaymentID = _cmd.value("t_PaymentID");
    PostDate = _cmd.value("t_PostDate");
    PostTime = _cmd.value("t_PostTime");
    InBalance = _cmd.value("t_InBalance");
    TurnType = IfThenElse(_cmd.value("t_TurnType") == 1, 0, 1);
    Amount = _cmd.value("t_Amount");
    if (_NationalCurrency == 1)
      AmountNatCur = _cmd.value("t_AmountNatCur");
      CurRate = GetRateMain(_FIID, PostDate);
    end;
    if (_TypeAccStat == 2)
      DocKind = _cmd.value("t_DocKind");
      DocNumber = _cmd.value("t_DocNumber");
      DocDate = _cmd.value("t_DocDate");
      Ground = _cmd.value("t_Ground");
    end;
  end;
end;

macro GetRateCB(FIID, D: date)
  var ft;
  record rate(ratedef);
  if (FIID == 0)
    return 1;
  end;
  ft = ПолучитьКурс(rate, NATCUR, FIID, 7);
  if (ft == 0)
    ft = ПолучитьЗначениеКурса(rate, D);
  end;
  if (ft != 0)
    ft = ПолучитьКурс(rate, NATCUR, FIID);
    if (ft == 0)
      ft = ПолучитьЗначениеКурса(rate, D);
    end;
  end;
  return ConvertSum($1, rate.rate, rate.scale, rate.point, rate.isinverse);
end;

class TExtractInfo(_AccountID, _AccountNumber, _DateFrom, _DateTo, _NationalCurrency, _FIID)
  var NumRowStat: Integer = 0;
  var InBalance : Money = $0;
  var InCurRate : Double = 0;
  var InBalanceNatCur: Money = $0;
  var TotalTurnDeb: Money = $0;
  var TotalTurnCre: Money = $0;
  var OutBalance: Money = $0;
  var OutCurRate: Double = 0;
  var OutBalanceNatCur: Money = $0;
  var DateLastMov: Date = NULL;
  var PayDocList: TArray = TArray();
  private macro _Create(_AccountID, _AccountNumber, _DateFrom, _DateTo, _NationalCurrency, _FIID)
    var cmd;
    var sql = "select RSB_ACCOUNT.restall(?, 1, ?, ?), RSB_ACCOUNT.restall(?, 1, ?, ?)";
    var param = makeArray(SQLParam("", _AccountNumber), SQLParam("", _FIID), SQLParam("", date(_DateFrom - 1)),
                       SQLParam("", _AccountNumber), SQLParam("", _FIID), SQLParam("", _DateTo));
    if (_NationalCurrency  == 1)
      sql = sql + ", RSB_ACCOUNT.restall(?, 1, ?, ?, 0), RSB_ACCOUNT.restall(?, 1, ?, ?, 0)";
      param[param.size] = SQLParam("", _AccountNumber);
      param[param.size] = SQLParam("", _FIID);
      param[param.size] = SQLParam("", date(_DateFrom - 1));
      param[param.size] = SQLParam("", _AccountNumber);
      param[param.size] = SQLParam("", _FIID);
      param[param.size] = SQLParam("", _DateTo);
    end;
   
    cmd = execSQLSelect(sql + " from dual", param);
    cmd.moveNext();
    InBalance = cmd.value(0);
    OutBalance = cmd.value(1);
    if (_NationalCurrency  == 1)
      InCurRate = GetRateCB(_FIID, _DateFrom);
      InBalanceNatCur = cmd.value(2);
      OutCurRate = GetRateCB(_FIID, _DateTo);
      OutBalanceNatCur = cmd.value(3);
    end;
    cmd = execSQLSelect("select NVL(sum(t_Sum_Payer), 0) from dacctrn_dbt where t_Chapter = 1 and t_State = 1 and t_AccountID_Payer = ? and t_Date_Carry BETWEEN ? AND ?",
                        MakeArray(SQLParam("", _AccountID), SQLParam("", _DateFrom), SQLParam("", _DateTo)));
    cmd.moveNext();
    TotalTurnDeb = cmd.value(0);

    cmd = execSQLSelect("select NVL(sum(t_Sum_Receiver), 0) from dacctrn_dbt where t_Chapter = 1 and t_State = 1 and t_AccountID_Receiver = ? and t_Date_Carry BETWEEN ? AND ?",
                        MakeArray(SQLParam("", _AccountID), SQLParam("", _DateFrom), SQLParam("", _DateTo)));
    cmd.moveNext();
    TotalTurnCre = cmd.value(0);

    cmd = execSQLSelect(string("select NVL(Max(t_Date_Carry), to_date('01010001', 'DDMMYYYY')) ",
                               "  from dacctrn_dbt where t_Chapter = 1 and t_State = 1 ",
                               "   and (t_AccountID_Payer = ? or t_AccountID_Receiver = ?) and t_Date_Carry < ? ",
                               "   and decode(t_AccountID_Payer, ?, t_Sum_Payer, t_Sum_Receiver) <> 0"),
                        MakeArray(SQLParam("", _AccountID), SQLParam("", _AccountID), SQLParam("", _DateFrom), SQLParam("", _AccountID)));
    if (cmd.moveNext() and (cmd.value(0) != date(0,0,0)))
      DateLastMov = cmd.value(0);
    end;
  end;
  if (ValType(_AccountID) != V_UNDEF)
    _Create(_AccountID, _AccountNumber, _DateFrom, _DateTo, _NationalCurrency, _FIID);
  end;
end;

macro CheckAndCorrectParams(
                          AccountID: @Integer, //Идентификатор счета в АБС
                          AccountNumber: @String, //Номер счета (внешний) в АБС
                          DateFrom: @date, //Начальная дата периода выписки
                          DateTo: @date, //Конечная дата периода выписки
                          TypeAccStat: integer, //Тип выписки Возможные значения:(1 - краткая; 2 - расширенная.)
                          NationalCurrency: @integer, //Признак национальной валюты Признак необходимости возврата сумм в национальной валюте. Возможные значения:0 - нет; 1 - да (по умолчанию)
                          FIID: @integer //возвращает валюту счета
                        ): Bool //если True, то нужно вернуть пустую выписку
  if (NationalCurrency == NULL)
    NationalCurrency = 1;
  end;
  if (((AccountID == NULL) or (AccountID <= 0)) and ((AccountNumber == NULL) or (AccountNumber == "")))
    RunError("Не указаны параметры счета для формирования выписки");
  end;
  if ((TypeAccStat != 1) and (TypeAccStat != 2))
    RunError("Некорректное значение параметра <Тип выписки>");
  end;
  if ((NationalCurrency != 0) and (NationalCurrency != 1))
    RunError("Некорректное значение параметра <Признак национальной валюты>");
  end;
  var cmd;
  if (AccountID)
    cmd = execSQLSelect("select t_Account, t_Code_Currency, t_Open_Date, t_Close_Date, t_Branch from daccount_dbt where t_AccountID = ?", MakeArray(SQLParam("", AccountID)));
    if (not cmd.moveNext())
      RunError("Не найден счет с идентификатором " + AccountID);
    end;
    FIID = cmd.value(1);
    AccountNumber = cmd.value(0);
  else
    cmd = execSQLSelect("select acc.t_AccountID, fi.t_FIID, acc.t_Open_Date, acc.t_Close_Date, acc.t_Branch from daccount_dbt acc, dfininstr_dbt fi " +
                        " where acc.t_Account = ? and acc.t_Chapter = 1 and acc.t_Code_Currency = fi.t_FIID and fi.t_CodeInAccount = ?", 
                        MakeArray(SQLParam("", AccountNumber), SQLParam("", substr(AccountNumber, 6, 3))));
    if (not cmd.moveNext())
      RunError("Не найден счет " + AccountNumber);
    end;
    FIID = cmd.value(1);
    AccountID = cmd.value(0);
  end;
  if ((DateFrom != NULL) and (DateFrom != date(0,0,0)) and (DateTo != NULL) and (DateTo != date(0,0,0)) and (DateFrom > DateTo))
    RunError("Дата начала периода выписки не может быть больше даты окончания периода выписки");
  end;
  if ((DateFrom == NULL) or (DateFrom == date(0,0,0)))
    DateFrom = cmd.value(2);
  elif ((cmd.value(3) != date(0,0,0)) and (DateFrom > cmd.value(3)))
    return True;
  end;
  if ((DateTo == NULL) or (DateTo == date(0,0,0)))
    cmd = execSQLSelect("select greatest(?, t_Curdate) from dcurdate_dbt where t_Branch = ? and t_IsMain = 'X'", MakeArray(SQLParam("", cmd.value(3)), SQLParam("", cmd.value(4))));
    if (not cmd.moveNext())
      DateTo = date(0,0,0);
    else
      DateTo = cmd.value(0);
    end;
  elif (DateTo < cmd.value(2))
    return True;
  end;
  return False;
end;
