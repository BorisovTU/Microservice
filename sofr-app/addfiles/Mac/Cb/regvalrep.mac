import globals, RSD;

const REP_DEFAULTVALUES = 0;
const REP_BRANCHVALUES = 1;
const REP_USERVALUES = 2;

macro GetOperDprtNodeInfo()
  var strSql : string;
  var rs : RsdRecordset;  
  var cmd : RsdCommand;

  var Dprt : string;

  strSql = "SELECT dp.t_Name, pt.t_Name " +
           "FROM ddp_dep_dbt dp, dparty_dbt pt " +
           "WHERE dp.t_Code = ? AND pt.t_PartyID = dp.t_PartyID";

  cmd = RSDCommand( strSql );
  cmd.addParam( "", RSDBP_IN, {OperDprtNode} );
  rs = RsdRecordset( cmd );
  if( rs.MoveNext())
    Dprt = rs.value( 0 ) + " " + rs.value( 1 );
  else
    Dprt = "";
  end;

  return Dprt;
end;

macro GetTypePersonName( TypePerson )
  var strSql : string;
  var rs : RsdRecordset;  
  var cmd : RsdCommand;

  var TypePersonName:string;

  strSql = " SELECT ta.t_name_type FROM dtypeac_dbt ta " +
           " WHERE ta.t_inumtype = 13 AND ta.t_type_account = chr(?) ";

  cmd = RSDCommand( strSql );
  cmd.addParam( "", RSDBP_IN, TypePerson );
  rs = RsdRecordset( cmd );
  if( rs.MoveNext())
    TypePersonName = rs.value( 0 );
  else
    TypePersonName = "";
  end;

  return TypePersonName;
end;

macro RegValReport_PrintHeader( RepVariant, GlobalOnly, DprtCode, DprtName, UserNum, UserName, UserTypePerson,
                           TechValues, SecurValues, SetValues )

  /*Вариант выпуска отчета*/
  var ReportInfo1 = "", ReportInfo2 = "";
  if( RepVariant == REP_DEFAULTVALUES )
    ReportInfo1 = "значения настроек по умолчанию";
    if( GlobalOnly )      
      ReportInfo2 = "только глобальные значения";
    end;
  elif( RepVariant == REP_BRANCHVALUES )
    ReportInfo1 = "значения настроек  узла ТС";
    ReportInfo2 = DprtCode + " " + DprtName;
  elif( RepVariant == REP_USERVALUES )
    ReportInfo1 = "значения настроек пользователя";
    ReportInfo2 = UserNum + ", " + GetTypePersonName(UserTypePerson) + ", " + UserName  + ", " + DprtCode + " " + DprtName;
  end;

  /*включены значения*/
  var ValuesTypes = "";
  if( TechValues )
    ValuesTypes = "технологические";
  end;

  if( SecurValues )
    if( StrLen(ValuesTypes) > 0 )
      ValuesTypes = ValuesTypes + ", " + "безопасности";
    else
      ValuesTypes = "безопасности";
    end;
  end;

  if( SetValues )
    if( StrLen(ValuesTypes) > 0 )
      ValuesTypes = ValuesTypes + ", " + "установленные";
    else
      ValuesTypes = "установленные";
    end;
  end;

  /*Сформирован*/
  var ExecuterInfo = "";
  ExecuterInfo = {oper} + ", " + GetTypePersonName({cRealTypePerson}) + ", " + {Name_Oper} + ", " + GetOperDprtNodeInfo();


[ Отчет о значениях настроек реестра  
                                                                                    ];
[ Вариант отчета: ##################################################################](ReportInfo1);
[ ##################################################################################](ReportInfo2);
[                                                                                   
  Включены значения: ###############################################################](ValuesTypes);
[                                                                                   
  Сформирован: #####################################################################](ExecuterInfo);
[ Дата/время: ########### ##########                                                ](date(), time());
[                                                                                   
  ----------------------------------------------------------------------------------
  ───────────────────────────────────────────────────────────────────────────────── ];
      
end;

macro GetUserPost( PartyID, PersonID )
  
  var strSql : string;
  var rs : RsdRecordset;  
  var cmd : RsdCommand;

  var Post : string;

  strSql = "SELECT t_Post FROM dofficer_dbt WHERE t_PartyID = ? AND t_PersonID = "+
           "(SELECT person.t_PartyID FROM dperson_dbt person WHERE person.t_Oper = ?)";
  
  rs = RsdRecordset( strSql );

  cmd = RSDCommand( strSql );
  cmd.addParam( "", RSDBP_IN, {OurBank} );
  cmd.addParam( "", RSDBP_IN, {oper} );
  rs = RsdRecordset( cmd );

  if( rs.MoveNext())
    Post = rs.value( 0 );
  else
    Post = "";
  end;

  return Post;

end;

macro RegValReport_PrintFooter( )
  var UserPost = "";

  UserPost = GetUserPost();

[                                                                                   
  ----------------------------------------------------------------------------------
  ───────────────────────────────────────────────────────────────────────────────── 
                                                                                    
  ###############################          ###############################          ](UserPost, {Name_Oper});

end;

macro RegValReport_Print( RepVariant, RegPath, RegType, RegValue, ObjectID, RealRegKind, RealObjectID, 
                          bVSet, SpecRegType, SpecRegValue )

  message(RegPath); // ShchebininSV
  
  var VType = "", VNode = "", VType2 = "";
  
  if( RepVariant == REP_DEFAULTVALUES )
    VType = "Г: ";
  elif( RepVariant == REP_BRANCHVALUES )
    if( RealRegKind == 0 )
      VType = "*Г: ";
    elif( RealRegKind == REGVAL_KIND_DPRT )
      VType = "*У: ";
      if( RealObjectID != ObjectID )
        VNode = "(" + RealObjectID + ")";
      end;
    end;

    if( bVSet == true )
      VType2 = " У: ";
    end;
  elif( RepVariant == REP_USERVALUES )
    if( RealRegKind == 0 )
      VType = "*Г: ";    
    elif( RealRegKind == REGVAL_KIND_DPRT )
      VType = "*У: ";
      VNode = "(" + RealObjectID + ")";
    elif( RealRegKind == REGVAL_KIND_OPER )
      VType = "*П: ";
    end;
    
    if( bVSet == true )
      VType2 = " П: ";
    end;
  end;

  if( RegType == V_BOOL )
    if( RegValue == true )
      RegValue = "YES";
    else
      RegValue = "NO"
    end;
  else 
    RegValue =  string(RegValue);
  end;

  var CommonValue = VType + RegValue + " " + VNode ;

  var CommonValue2 = "";

[#############################################################################################################](RegPath:w);
[#############################################################################################################](CommonValue:w);
  
  if( bVSet == true )
    if( SpecRegType == V_BOOL )
      if( RegValue == true )
        SpecRegValue = "YES";
      else
        SpecRegValue = "NO"
      end;
    else 
      SpecRegValue =  string(SpecRegValue);
    end;
    CommonValue2 = VType2 + SpecRegValue;

[#############################################################################################################](CommonValue2:w);
  end;
  println();  // ShchebininSV


end;



/*│║
 *┌─┬─┐ ╔═╦═╗ ╓─╥─╖╒═╤═╕
 *├─┼─┤ ╠═╬═╣ ╟─╫─╢╞═╪═╡
 *└─┴─┘ ╚═╩═╝ ╙─╨─╜╘═╧═╛
 */