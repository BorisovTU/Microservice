/*
  $Name:         gstname.mac
  $Module:       Banking
  $Description:  Определение наименования шага документа
*/
IMPORT oralib, likepy;

MACRO GetStepNameExecuteRead( PaymentID:integer, DocKind:integer ):string 
  
  VAR PaymStatus:integer; 
  IF ( getparm ( 2, PaymStatus ) )
    IF ( PaymStatus == 32000 )
      return "Документ находится в закрытых"
    END;
    IF ( PaymStatus == 0 )
      return "Документ находится в отложенных"
    END;
  END;

  VAR select:string = "SELECT oprostep.T_NAME " +
                        "FROM doproper_dbt opr, " +                    
                             "doprstep_dbt step, " +                    
                             "doprostep_dbt oprostep " +                    
                       "WHERE opr.T_DOCKIND = :DocKind " +
                         "AND opr.t_DOCUMENTID = LPAD(:PaymentID, 34, '0' )" +
                         "AND opr.t_ID_OPERATION = step.t_ID_OPERATION " +
                         "AND step.t_ISEXECUTE = 'R' " +
                         "AND step.T_DOCKIND = :DocKind " +                    
                         "AND oprostep.T_BLOCKID = step.T_BLOCKID " +     
                         "AND oprostep.T_NUMBER_STEP = step.T_NUMBER_STEP";

  VAR params:TArray = makeArray( SQLParam( "DocKind"  , DocKind   ),
                                 SQLParam( "PaymentID", PaymentID ) );

  VAR rs:RsdRecordset = execSQLselect( select, params, TRUE );
  var names:TArray = TArray();

  IF(rs)

    WHILE(rs.moveNext())
       names.Value(names.size) = rs.value(0);
    END;
    if(names.size == 0)
      return "Обработка платежа завершена. ";
    else
      return "Документ готов к выполнению шага: " + join( names, ", " );
    end;
  END;

  return "Шаг документа не выяснен";

ONERROR(x)
  
  MsgBox( x.Message );
  return "Шаг документа не выяснен";

END;