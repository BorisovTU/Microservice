/*
$Name:        sfpaym.mac
$Module:      РКО (ПЗО)  
$Description: Оплата методом "платеж банка"
*/

import sfpay_lib;

macro SfFillBankPayment( Paym, PayDate )

  Paym.Purpose            = PM_PURP_BANKPAYMENT;
  Paym.PaymStatus         = PM_PREPARING;
  Paym.NumberPack         = 995;
  Paym.PayerBankEnterDate = PayDate;
  Paym.ToBackOffice       = "";

  Paym.Date               = PayDate;
  Paym.PayDate            = PayDate;
  Paym.ClientDate         = PayDate;
  Paym.PaymentKind        = "Э";
  Paym.ComissCharges      = 0;
  if( Paym.DocKind == DLDOC_BANKCLAIM )
    Paym.ShifrOper        = "02";
  else
    Paym.ShifrOper        = "01";
  end;

  return 0;
end;

/*
        ----------------- Оплата платежом ------------------------------
*/
macro SfFormBankPaymentExt( sidebet, sicredit, sfcomiss, payParams:TSfPayParams, 
                            PaymStatus, sfinvlnk, SumInInvFIID,
                            IsBatchMode, oprchild )

  var Payment : RsbPayment;
  var BankPaym:object; 
  var Number  : string;
  var RefID;

  var PmAmount = payParams.paySum;
  if( (SumInInvFIID != null) AND (sidebet.rec.FIID != payParams.payFIID) )
    PmAmount = SumInInvFIID;
  end;

  /* все проверки плат. и получ. в sfpay.mac.
  if( sidebet.Rec.BankID != {OurBank} )
    MsgBox("При оплате ПЗО платежом банка|плательщик должен быть в нашем банке");
    return 1;
  end;
  */

  if( (payParams.payFIID == NATCUR) AND (sidebet.rec.FIID == NATCUR) AND (sicredit.rec.FIID == NATCUR) )
    BankPaym = RsbBankPayment(0);
    Payment = BankPaym.Payment;

    BankPaym.Origin     = MEMORDER_FDOC_SF;
    BankPaym.Oper       = {oper}; 
    BankPaym.Status     = 1 /*MEMORDER_STATUS_POST*/;
    BankPaym.LaunchOper  = true;

    Payment.DocKind     = DLDOC_BANKPAYMENT;
    Payment.Purpose     = PM_PURP_BANKPAYMENT;
    
    if( SfFillPaymentExt( Payment, sidebet, sicredit, sfcomiss, payParams, PmAmount )) 
      return 1; 
    end;
    if( SfFillBankPayment( Payment, payParams.PayDate ))
      return 1; 
    end;

  else
    BankPaym = GenObject( "RsbBbCpOrder", 0 );
    Payment = BankPaym.Payment;

    BankPaym.Origin       = CP_OR_SF;
    BankPaym.Oper         = {oper}; 
    BankPaym.CurrentState = 0 /*CP_ST_DEFERRED*/;
    BankPaym.LaunchOper  = true;

    Payment.PayerFIID   = sidebet.rec.FIID; 
    Payment.DocKind     = BBANK_CPORDER;
    Payment.Purpose     = PM_PURP_BANKPAYMENT;

    if( SfFillPaymentExt( Payment, sidebet, sicredit, sfcomiss, payParams, PmAmount )) 
      return 1; 
    end;
    if( SfFillBankPayment( Payment, payParams.PayDate ))
      return 1; 
    end;
    
  end;

  if( payParams.ground == "" )
    Payment.Ground = "Комиссия за обслуживание";
  else
    Payment.Ground = payParams.ground;
  end;

  if( SfPayDocGetRefNumber(@Payment.Number, payParams) != 0 )
    return 1;
  end;


  if( PaymStatus != null )
    Payment.PaymStatus = PaymStatus;
  end;

  if( sfinvlnk != null )
    Payment.LinkSfInvoice( sfinvlnk.rec.InvoiceID, sfinvlnk.rec.PayAmount, sfinvlnk.rec.PaidNDS, sfinvlnk.rec.PayFIID );
  end;

  Payment.FeeType = payParams.feeType;
  if( payParams.objectID != null )
    Payment.DefComID = payParams.objectID;
  end;

  if( (IsBatchMode != null) AND (IsBatchMode == true) )
    
    BankPaym.ConnectToOperation( oprchild.ID_Operation, oprchild.ID_Step );

    var err = BankPaym.Update();
    if( err != 0 )
      var BankMsg = AL_GetErrorInfo();
      var ErrMsg = "Ошибка при обновлении платежа " + BankMsg.Message;
      payParams.SetError(err, ErrMsg);
      return 1;
    end;

    oprchild.Child_DocKind    = Payment.DocKind;
    oprchild.Child_DocumentID = PM_MakeDocumentID( Payment.DocKind, Payment.DocumentID );
  end;

  return 0;
   
end;

macro SfFormBankPayment( sidebet, sicredit, sfcomiss, payDate, ground, paySum, taxSum, payFIID, PaymStatus, sfinvlnk, 
                         PlusCalc_Account, PlusCalcNDS_Account, feeType, DefComID, SumInInvFIID,
                         IsBatchMode, ID_Operation, ID_Step, ChildDocKind:@integer, ChildDocID:@string )

  var payParams = TSfPayParams;


  payParams.payDate = payDate;
  payParams.ground = ground; 
  payParams.paySum = paySum; 
  payParams.taxSum = taxSum;
  payParams.payFIID = payFIID;

  payParams.PlusCalc_Account = PlusCalc_Account;
  payParams.PlusCalcNDS_Account = PlusCalcNDS_Account;
  
  payParams.feeType = feeType;
  payParams.objectID = DefComID;

  return SfFormBankPaymentExt( sidebet, sicredit, sfcomiss, payParams, PaymStatus, sfinvlnk, SumInInvFIID,
                               IsBatchMode, ID_Operation, ID_Step, @ChildDocKind, @ChildDocID );

end;

macro SfFormMemorialOrder( sidebet, sicredit, sfcomiss, PayDate, ground, paySum, taxSum, FIID, sfinvlnk, 
                           PlusCalc_Account, PlusCalcNDS_Account, DefComID, SumInInvFIID, payParams:TSfPayParams )

  const MCDOC_STATUS_POST = 1;

  var Payment:object;
  var Memorial:object; /*RsbMemorialOrder*/;
  var Multy:object;
  var Number      : string;
  var RefID;

  var PmAmount = paySum;
  if( (SumInInvFIID != null) AND (sidebet.rec.FIID != FIID) )
    PmAmount = SumInInvFIID;
  end;

  if( sidebet.rec.FIID == sicredit.rec.FIID )

    Memorial = GenObject( "RsbMemorialOrder", 0 );
    Payment  = Memorial.Payment();

    Memorial.State         = 0; /*CB_DOC_STATE_DEFERRED*/;
    Memorial.Oper          = {oper};
    Memorial.Chapter       = 1; /*CHAPT1*/;
    Memorial.Code_Currency = FIID;
    Memorial.Kind_Oper     = " 4";
    Memorial.Origin        = CB_DOC_ORIGIN_MANUAL;
    Memorial.LaunchOper     = true;

    Payment.DocKind        = DLDOC_MEMORIALORDER;
    Payment.Purpose        = PM_PURP_MEMORDER;
    Payment.ShifrOper      = "09";

  else
    Multy = GenObject( "RsbMultyDoc", 0 );
    Payment  = Multy.Payment();

    Multy.Oper    = {oper};
    Multy.Chapter = 1;    
    Multy.Origin  = MULTYDOC_ORIGIN_AUTO;
    Multy.Status  = MCDOC_STATUS_POST;
    Multy.LaunchOper = true;

    Payment.DocKind    = CB_MULTYDOC;
    Payment.Purpose    = PM_PURP_MULTYDOC;
    Payment.ShifrOper  = "09";

  end;

  if( SfFillPayment( Payment, sidebet, sicredit, sfcomiss, PayDate, paySum, taxSum, FIID, 
                     PlusCalc_Account, PlusCalcNDS_Account, PmAmount )) 
    return 1; 
  end;
 
  if( ground == "" )
    Payment.Ground = "Комиссия за обслуживание";
  else
    Payment.Ground = ground;
  end;

  if( SfPayDocGetRefNumber(@Payment.Number, payParams) != 0 )
    return 1;
  end;

  if( sfinvlnk != null )
    Payment.LinkSfInvoice( sfinvlnk.rec.InvoiceID, sfinvlnk.rec.PayAmount, sfinvlnk.rec.PaidNDS, sfinvlnk.rec.PayFIID );
  end;

  Payment.FeeType = sfcomiss.feeType;
  if( DefComID != null )
    Payment.DefComID = DefComID;
  end;

  return 0;

end;


macro SfFormBankOrderExt( sidebet, sicredit, sfcomiss, payParams:TSfPayParams, sfinvlnk, SumInInvFIID,
                          IsBatchMode, oprchild )

  const MCDOC_STATUS_POST = 1;

  var BankOrder:object;
  
  var Number:string;
  var RefID;

  var BankMsg;
  var ErrMsg =  "";

  var PmAmount = payParams.paySum;
  if( (SumInInvFIID != null) AND (sidebet.rec.FIID != payParams.payFIID) )
    PmAmount = SumInInvFIID;
  end;

  BankOrder = GenObject( "RsbBankOrder", 0 );

  BankOrder.Oper    = {oper};
  BankOrder.LaunchOper    = true;
  BankOrder.PrimDocOrigin = PD_OR_SF;

  BankOrder.ShifrOper = SF_SHIFR_OPER_BANK_ORDER;

  if( SfFillPaymentExt( BankOrder, sidebet, sicredit, sfcomiss, payParams, PmAmount )) 
    return 1; 
  end;
 
  if( payParams.ground == "" )
    BankOrder.Ground = "Комиссия за обслуживание";
  else
    BankOrder.Ground = payParams.ground;
  end;

  if( SfPayDocGetRefNumber(@BankOrder.Number, payParams) != 0 )
    return 1;
  end;

  if( sfinvlnk != null )
    BankOrder.LinkSfInvoice( sfinvlnk.rec.InvoiceID, sfinvlnk.rec.PayAmount, sfinvlnk.rec.PaidNDS, sfinvlnk.rec.PayFIID );
  end;

  BankOrder.FeeType = payParams.feeType;
  if( payParams.ObjectID != null )
    BankOrder.DefComID = payParams.ObjectID;
  end;

  if( (IsBatchMode != null) AND (IsBatchMode == true) )

    BankOrder.ConnectToOperation( oprchild.ID_Operation, oprchild.ID_Step );
    
    var err = BankOrder.Update();
    if( err != 0 )
      BankMsg = AL_GetErrorInfo();
      ErrMsg = "Ошибка при обновлении платежа " + BankMsg.Message;
      payParams.SetError(err, ErrMsg);
    end;
    oprchild.Child_DocKind    = BankOrder.DocKind;
    oprchild.Child_DocumentID = PM_MakeDocumentID( BankOrder.DocKind, BankOrder.DocumentID );
  end;

  return 0;

end;

macro SfFormBankOrder( sidebet, sicredit, sfcomiss, payDate, ground, paySum, taxSum, payFIID, sfinvlnk, 
                       PlusCalc_Account, PlusCalcNDS_Account, feeType, DefComID, SumInInvFIID,
                       IsBatchMode, ID_Operation, ID_Step, ChildDocKind:@integer, ChildDocID:@string )
  var payParams = TSfPayParams;

  payParams.payDate = payDate;
  payParams.ground = ground; 
  payParams.paySum = paySum; 
  payParams.taxSum = taxSum;
  payParams.payFIID = payFIID;
  
  payParams.feeType = feeType;
  payParams.objectID = DefComID;

  payParams.PlusCalc_Account = PlusCalc_Account;
  payParams.PlusCalcNDS_Account = PlusCalcNDS_Account;

  return SfFormBankOrderExt( sidebet, sicredit, sfcomiss, payParams, sfinvlnk, SumInInvFIID,
                             IsBatchMode, ID_Operation, ID_Step, @ChildDocKind, @ChildDocID );
end;
