/*
$Name:        mpcconv.mac
$Module:      Главная книга
$Description: Конвертация таблиц старых процентов в новые
*/

/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.20
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcconv.mac

  Библиотека       : RSACCOUNT

  Описание         : Конвертация старых процентов

  Программист      : Kirakozov Michael (KMB)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet, CTInter, PaymInter, PTInter, globals, CurrInter, FIInter, Проценты;

//--------  СТАРЫЕ ПРОЦЕНТЫ: ---------

// Вид графика расчета счета
const PC_SCHEDTYPE_ABSOLUTE       = 1, // абсолютный
      PC_SCHEDTYPE_RELATIVE       = 2, // относительный
      PC_SCHEDTYPE_CALCDAY        = 3, // по дню расчета
      PC_SCHEDTYPE_UNSET          = 4, // не задан
      PC_SCHEDTYPE_DAILY          = 5; // ежедневный

// Вид процентной ставки счета процентов
const PC_RATETYPE_ABSOLUTE       = 1, // абсолютная
      PC_RATETYPE_RELATIVE       = 2, // относительная
      PC_RATETYPE_CONTRACT       = 3; // от договора

// Тип ставки
const PC_RATEALG_DATE            = 1, // ставка по дате
      PC_RATEALG_COMPL           = 2; // сложная     

//--------  НОВЫЕ ПРОЦЕНТЫ:  ----------

// Вид графика
const PRC_SCHEDKIND_USER         = 0, // пользовательский
      PRC_SCHEDKIND_CALC         = 1, // расчета
      PRC_SCHEDKIND_CHARGE       = 2, // начисления
      PRC_SCHEDKIND_PAY          = 3; // оплаты
      
// Тип графика
const PRC_SCHEDTYPE_ABSOLUTE      = 1, // абсолютный
      PRC_SCHEDTYPE_RELATIVE      = 2; // относительный


// Тип процентной ставки
const PRC_RATETYPE_ABSOLUTE      = 1, // абсолютная
      PRC_RATETYPE_USER          = 2, // пользовательская
      PRC_RATETYPE_FLOATING      = 3; // плавающая

// Статус оплаты
const PRC_PAYSTATUS_NOT_CALCULATED     = 0,     // не рассчитано
      PRC_PAYSTATUS_NOT_CLAIMED        = 1,     // не предъявлено
      PRC_PAYSTATUS_NOT_PAID           = 2,     // не оплачено
      PRC_PAYSTATUS_PARTLY_PAID        = 3,     // частично оплачено
      PRC_PAYSTATUS_PAID               = 4,     // оплачено
      PRC_PAYSTATUS_PARTLY_PAID_MANUAL = 5,     // частично оплачено ручной ввод
      PRC_PAYSTATUS_PAID_MANUAL        = 6;     // оплачено ручной ввод

// Имя шаблона файла для протокола конвертации 
private var  ConvReportNameTempl = "conv";

// Расширение файла для протокола конвертации 
//private var  ConvReportExtention = ".txt";

// Флаг, если true - включать в имя файла протокола дату и время
// в виде "год-месяц-день-часы-минуты" 
private var  IncludeDateToReportName = true; 

// Указатель на файл протокола
private file ConvReportFile() txt write;       
private file PrcContractFile ("PRCCONTRACT.DBT") write;
private file PrcCntSchedFile ("PRCCNTSCHED.DBT") write;

//---------------------------------------------------------------------
//  ВНИМАНИЕ! Эта функция отбирает конвертируемые счета процентов.
//  Управлять условиями отбора здесь!
//---------------------------------------------------------------------

PRIVATE MACRO CreateDataSet(cmd:@object)
   
   cmd = RSDCommand ("select * from DPCACC_DBT " +
         "where (T_CLOSEDATE = '01.01.0001' or T_CLOSEDATE is null) and " +
         "(T_OBJECTTYPE in (7,9,10,1001,1002,1011,1012,1013)) ");
   cmd.execute();
   
   return 0;               
END;

//---------------------------------------------------------------------
//  Блок функций по работе с файлом протокола
//---------------------------------------------------------------------

// Добавить строку в протокол
PRIVATE MACRO PrintReportLine(str)
    return insert(ConvReportFile, str );
END;


// Проинициализировать файл протокола
PRIVATE MACRO InitReportFile()
   var day,mon,year,hh,mm,ss,
       Today, Now,
       FileName = "";

   if (IncludeDateToReportName)
      Today = Date();
      DateSplit(Today,day,mon,year);
      Now = Time();
      TimeSplit(Now,hh,mm,ss);
      FileName = ConvReportNameTempl + "-" + string(year:4) + "-" + string(mon:2:o) +
                 "-" + string(day:2:o) + "-" + string(hh:2:o) + "-" + string(mm:2:o);// + ConvReportExtention;
   else
      FileName = ConvReportNameTempl;// + ConvReportExtention;
   end;

   FileName = GetTxtFileName(FileName);

   close( ConvReportFile ); //На всякий случай
   open( ConvReportFile, FileName );

   PrintReportLine("                 ОТЧЕТ ОПЕРАЦИИ КОНВЕРТАЦИИ ПРОЦЕНТОВ ");
   PrintReportLine("  ");
   PrintReportLine("                       Дата выполнения: " + string(Today));
   PrintReportLine("  ");
   PrintReportLine("______________________________________________________________________________ ");
   PrintReportLine("  ");

   return 0;     
END;


// Вывести протокол по окончании работы
PRIVATE MACRO EndReport()
   
   PrintReportLine("  ");
   PrintReportLine("Операция конвертации процентов завершена. ");
   PrintReportLine("______________________________________________________________________________ ");
   PrintReportLine("  ");
   close( ConvReportFile );
   ViewFile( ConvReportFile );

   return 0;
END;                 

//---------------------------------------------------------------------
//  Блок функций по созданию процентного договора
//---------------------------------------------------------------------

// Вставка записей в таблицу "Счета по ПД"
PRIVATE MACRO InsertCntRecByType(ContractID,AccTypeID,CatID)
   var cmd;

   cmd = RSDCommand ("insert into DPRCCNTACC_DBT (T_CONTRACTID, T_ACCTYPEID, T_CATID, T_FIID, T_CHAPTER) " +
                      "values (?,?,?,-1,0)" );
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, AccTypeID );
   cmd.addParam( "", RSDBP_IN, CatID );
   cmd.execute();
   
   return 0;
      
   OnError( RslErrObj ) 
  return 1;
END;


// Определение категорий учета по умолчанию
PRIVATE MACRO GetCatIDByAccType(ResKind, FIID, AccTypeID, CatID:@integer)
   var cmd,rs,oldCatID;

   oldCatID = CatID;
   cmd = RSDCommand ("select T_CATID_PRIVL,T_CATID_PRIVL$,T_CATID_RAZM,T_CATID_RAZM$ " +
                      "from DPRCACCTYPE_DBT where T_ACCTYPEID = ?" );
   cmd.addParam( "", RSDBP_IN, AccTypeID );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      if ((ResKind == 1) and (FIID == NATCUR))
         CatID = rs.Value("T_CATID_PRIVL");
      elif ((ResKind == 1) and (FIID != NATCUR))
         CatID = rs.Value("T_CATID_PRIVL$");
      elif ((ResKind == 2) and (FIID == NATCUR))
         CatID = rs.Value("T_CATID_RAZM");
      elif ((ResKind == 2) and (FIID != NATCUR))
         CatID = rs.Value("T_CATID_RAZM$");   
      else
         return 1;
      end;
      
      if (ValType(CatID) == V_UNDEF)
         CatID = oldCatID;
      end;
   end;   
   
   return 0;         
END; // end macro GetCatIDByAccType


// Перенос счетов по ПД из старых процентов
PRIVATE MACRO MoveOldAccountsToContract(ContractID, ResKind, FIID, AutoKey, rsPcAcc)
   var cmd,rs,cmd2,cmd3,rs3,
       AccTypeID = 0, CatID3 = 0, Account = "";
   cmd = RSDCommand ("select acnt.T_FIID,acnt.T_ACCOUNT, acnt.T_ACNTCODE, acnttp.T_CHAPTER " +
                      "from DPCACNT_DBT acnt, DPCACNTTP_DBT acnttp " +
                      "where acnt.T_ACNTCODE = acnttp.T_CODE and " +
                      "acnt.T_AUTOPERC = ? and acnt.T_ACNTCODE IN(3,5,6,9)");
   cmd.addParam( "", RSDBP_IN, AutoKey );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   while (rs.movenext())
      if (rs.AcntCode == 3)
         AccTypeID = 1;
      elif (rs.AcntCode == 5)
         AccTypeID = 2;
      elif (rs.AcntCode == 6)
         AccTypeID = 5;
      else //  rs.AcntCode == 9
         AccTypeID = 6;
      end;
      
      cmd3 = RSDCommand ("select T_ACCTYPEID from DPRCCNTACC_DBT " +
                         "where T_CONTRACTID = ? and T_ACCTYPEID = ?");
      cmd3.addParam( "", RSDBP_IN, ContractID );
      cmd3.addParam( "", RSDBP_IN, AccTypeID );
      cmd3.execute();
      rs3 = TRsbDataSet(cmd3);

      if (rs3.movenext())
         cmd2 = RSDCommand ("update DPRCCNTACC_DBT set T_FIID = ?, T_CHAPTER =?, T_ACCOUNT = ? " +
                          "where T_CONTRACTID = ? and T_ACCTYPEID = ?");
          cmd2.addParam( "", RSDBP_IN, rs.FIID );
          cmd2.addParam( "", RSDBP_IN, rs.Chapter );
          cmd2.addParam( "", RSDBP_IN, rs.Account );
          cmd2.addParam( "", RSDBP_IN, ContractID );
          cmd2.addParam( "", RSDBP_IN, AccTypeID );      
          cmd2.execute(); 
      
      end;
   end;                                             

   // Счет по учету процентов "Счет доходов (расходов) срочных процентов" (prcCntAcc.AccTypeID=3) заполняем следующим образом:
   if ((GetCatIDByAccType(ResKind, FIID, 3, @CatID3) == 0) AND ((ResKind == 1) OR (ResKind == 2)))
     AccTypeID = 3;
     var cmdacc, rsacc;

     if(rsPcAcc.RestKind == 1)
        cmdacc = RSDCommand ("SELECT t_Account FROM dpcAcnt_dbt WHERE t_AcntCode = ? AND T_AUTOPERC = ? " );
        cmdacc.addParam( "", RSDBP_IN, 2 );
        cmdacc.addParam( "", RSDBP_IN, AutoKey );
        cmdacc.execute();
        rsacc = TRsbDataSet(cmdacc);
        if (rsacc.movenext())
          Account = rsacc.Account;
        else
          Account = rsPcAcc.PercReceiverAccount;
        end;
     else
        cmdacc = RSDCommand ("SELECT t_Account FROM dpcAcnt_dbt WHERE t_AcntCode = ? AND T_AUTOPERC = ? " );
        cmdacc.addParam( "", RSDBP_IN, 1 );
        cmdacc.addParam( "", RSDBP_IN, AutoKey );
        cmdacc.execute();
        rsacc = TRsbDataSet(cmdacc);
        if (rsacc.movenext())
          Account = rsacc.Account;
        else
          Account = rsPcAcc.PayerAccount;
        end;
     end;

     cmd3 = RSDCommand ("select T_ACCTYPEID from DPRCCNTACC_DBT " +
                        "where T_CONTRACTID = ? and T_ACCTYPEID = ?");
     cmd3.addParam( "", RSDBP_IN, ContractID );
     cmd3.addParam( "", RSDBP_IN, AccTypeID );
     cmd3.execute();
     rs3 = TRsbDataSet(cmd3);
     if (rs3.movenext())
        cmd2 = RSDCommand ("update DPRCCNTACC_DBT set T_FIID = ?, T_CHAPTER =?, T_ACCOUNT = ? " +
                         "where T_CONTRACTID = ? and T_ACCTYPEID = ?");
        cmd2.addParam( "", RSDBP_IN, rsPcAcc.FIID );
        cmd2.addParam( "", RSDBP_IN, 1 );
        cmd2.addParam( "", RSDBP_IN, Account );
        cmd2.addParam( "", RSDBP_IN, ContractID );
        cmd2.addParam( "", RSDBP_IN, AccTypeID );      
        cmd2.execute(); 
     else
        cmd2 = RSDCommand ("insert into DPRCCNTACC_DBT (T_CONTRACTID, T_ACCTYPEID, T_CATID, T_FIID, T_CHAPTER, T_ACCOUNT) " +
                           "values (?,?,?,?,?,?)" );
        cmd2.addParam( "", RSDBP_IN, ContractID );
        cmd2.addParam( "", RSDBP_IN, AccTypeID );
        cmd2.addParam( "", RSDBP_IN, 0 );
        cmd2.addParam( "", RSDBP_IN, rsPcAcc.FIID );
        cmd2.addParam( "", RSDBP_IN, 1 );
        cmd2.addParam( "", RSDBP_IN, Account );
        cmd2.execute(); 
     end;
   end;
END;


// Инициализация счетов по ПД
PRIVATE MACRO InitContractAccounts(ContractID, rsPcAcc)
   var prccontract = TBfile("prccontract.dbt"),
       stat = 0,
       ResKind,FIID,
//       cmd,cmd2,rs2,cmd3,rs3,cmd4,
       CatID1 = 0, CatID2 = 0, CatID3 = 0, CatID4 = 0, CatID5 = 0, CatID6 = 0;
   var AutoKey = rsPcAcc.AutoKey;

   prccontract.KeyNum = 0;  
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      PrintReportLine("Ошибка переноса лицевых счетов по договору ID = " + ContractID);
      return 1;
   end;
   ResKind = prccontract.rec.ResKind;
   FIID = prccontract.rec.FIID;

   if ((ResKind == 1) or (ResKind == 2))
      if (GetCatIDByAccType(ResKind, FIID, 1, @CatID1) == 0)
         InsertCntRecByType(ContractID,1,CatID1);
      end;
      if (GetCatIDByAccType(ResKind, FIID, 2, @CatID2) == 0)
         InsertCntRecByType(ContractID,2,CatID2);
      end;
      if (GetCatIDByAccType(ResKind, FIID, 3, @CatID3) == 0)
         InsertCntRecByType(ContractID,3,CatID3);
      end;
      if (GetCatIDByAccType(ResKind, FIID, 4, @CatID4) == 0)
         InsertCntRecByType(ContractID,4,CatID4);
      end;
      if (GetCatIDByAccType(ResKind, FIID, 5, @CatID5) == 0)
         InsertCntRecByType(ContractID,5,CatID5);
      end;
      if (GetCatIDByAccType(ResKind, FIID, 6, @CatID6) == 0)
         InsertCntRecByType(ContractID,6,CatID6);
      end;

      MoveOldAccountsToContract(ContractID, ResKind, FIID, AutoKey, rsPcAcc);
   else
       PrintReportLine("Ошибка переноса лицевых счетов по договору ID = " + ContractID);
       return 1;
   end;   
   
   return 0;    
   
END;


// Инициализация дополнительных параметров ПД
PRIVATE MACRO InitAddParams(ContractID, RestKind)
   var prccontract = TBfile("prccontract.dbt"),
       stat = 1,
       Balance = "",
       Balance3s = "",
       cmd,rs,cmd2,rs2,cmd3,rs3,cmd4,
       ResKind = -1, ResType = -1, LegalForm = -1, 
       Contragent = -1, CreditOrg = -1;

   prccontract.KeyNum = 0;  
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      PrintReportLine("Не удалось инициализировать дополнительные параметры договора ID = " + ContractID);
      return 1;
   end;
   
   if (prccontract.rec.Account == "") // Dealing or Securities

      if (RestKind == 1)
         ResKind = 2;
      else
         ResKind = 1;
      end;
      stat = 0;

   else     // GK

      cmd = RSDCommand ("select T_BALANCE from DACCOUNT_DBT where T_ACCOUNT = ?" );
      cmd.addParam( "", RSDBP_IN, prccontract.rec.Account );
      cmd.execute();
         
      rs = TRsbDataSet(cmd);
      if (rs.movenext())
         Balance = rs.balance;   
      end;   
     
      cmd2 = RSDCommand ("select T_RESKIND,T_RESTYPE,T_LEGALFORM,T_CREDITORG,T_CONTRAGENT " +
                          "from DPRCBLNCAT_DBT where T_BALANCE = ? and T_CHAPTER = ?" );
      cmd2.addParam( "", RSDBP_IN, Balance );
      cmd2.addParam( "", RSDBP_IN, prccontract.rec.Chapter );
      cmd2.execute();
         
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())  // Поиск параметров по балансовому счету
         ResKind    = rs2.ResKind;
         ResType    = rs2.ResType;
         LegalForm  = rs2.LegalForm;
         Contragent = rs2.Contragent;
         CreditOrg  = rs2.CreditOrg;
         stat = 0;
      else                 // если не нашли - ищем по трем первым символам
         Balance3s = SubStr(Balance,1,3);
         cmd3 = RSDCommand ("select T_RESKIND,T_RESTYPE,T_LEGALFORM,T_CREDITORG,T_CONTRAGENT " +
                             " from DPRCBLNCAT_DBT where T_BALANCE = ? and T_CHAPTER = ?" );
         cmd3.addParam( "", RSDBP_IN, Balance3s );
         cmd3.addParam( "", RSDBP_IN, prccontract.rec.Chapter );
         cmd3.execute();
         
         rs3 = TRsbDataSet(cmd3);
         
         if (rs3.movenext())
            
            ResKind    = rs3.ResKind;
            ResType    = rs3.ResType;
            LegalForm  = rs3.LegalForm;
            Contragent = rs3.Contragent;
            CreditOrg  = rs3.CreditOrg;
            stat = 0;
            
         else // если не нашли и по трем первым символам
         
            if (RestKind == 1)
               ResKind = 2;
            else
               ResKind = 1;
            end;
            stat = 0;
            
         end;
      end;

   end;

   cmd4 = RSDCommand ("update DPRCCONTRACT_DBT set T_RESKIND = ?, T_RESTYPE = ?, " +
                     "T_LEGALFORM = ?, T_CONTRAGENT = ?, T_CREDITORG = ?" +
                     "where T_CONTRACTID = ?");
   cmd4.addParam( "", RSDBP_IN, ResKind );
   cmd4.addParam( "", RSDBP_IN, ResType );
   cmd4.addParam( "", RSDBP_IN, LegalForm );
   cmd4.addParam( "", RSDBP_IN, Contragent );
   cmd4.addParam( "", RSDBP_IN, CreditOrg );
   cmd4.addParam( "", RSDBP_IN, ContractID );
   cmd4.execute();

   if (stat  != 0)
      PrintReportLine("Не удалось инициализировать дополнительные параметры договора ID = " + ContractID);
   end;
  
   return stat;
      
   OnError( RslErrObj )
      PrintReportLine("Не удалось инициализировать дополнительные параметры договора ID = " + ContractID);  
      return 1;
   
END;


PRIVATE MACRO GetAccountClient(Account,FIID, Chapter, Client:@integer)
   var   cmd,rs,cmd2,rs2,
         stat = false;

   cmd = RSDCommand("select T_CLIENT from DACCOUNT_DBT where T_ACCOUNT = ? " +
                    "and T_CODE_CURRENCY = ? and T_CHAPTER = ?");

   cmd.addParam( "", RSDBP_IN, Account );
   cmd.addParam( "", RSDBP_IN, FIID );
   cmd.addParam( "", RSDBP_IN, Chapter );
   stat = cmd.execute();
      
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      Client = rs.Client;   
      stat = true;  
   end;
   
   return stat;
END;


PRIVATE MACRO DefineBO(BCID, BackOffice:@integer)
   var cmd, rs;

   cmd = RSDCommand ("select dep.T_PARTYID from DVSBANNER_DBT vsb, DDP_DEP_DBT dep " +
                     "where dep.T_PARTYID = vsb.T_ISSUER and vsb.T_BCID = ?");
   cmd.addParam( "", RSDBP_IN, BCID);
   cmd.execute();
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      BackOffice = 1;   
   else
      BackOffice = 7;  
   end;   

   return 0;
END;

PRIVATE MACRO CalcContractNumber(AutoKey, BackOffice)
   var cmd, rs, 
       Number = AutoKey,
       fExitCycle = 0;

   while (fExitCycle != 1)
      cmd = RSDCommand ("select T_NUMBER from DPRCCONTRACT_DBT " +
                        "where T_NUMBER = ? and T_BACKOFFICE = ?");
      cmd.addParam( "", RSDBP_IN, Number);
      cmd.addParam( "", RSDBP_IN, BackOffice);
      cmd.execute();
      rs = TRsbDataSet(cmd);
      if (rs.movenext())
         Number = Number + 1000;   
      else
         fExitCycle = 1;  
      end;   
   end;

   return Number;
END;


// Создание нового процентного договора.
PRIVATE MACRO CreateNewContract(rs, ContractID:@integer)
   var cmd, cmd2, cmd3, rs2,
   BackOffice     = 0, 
   Number         = 0, 
   Client         = 0, 
   ClientQuality  = 0, 
   ContractType   = 0,
   ObjectType     = 0, 
   ObjectID       = "",
   FIID = 0, Chapter = 0, Account = "", 
   PerAccount = "",
   UserType = "", 
   UserField1 = "",UserField2 = "",UserField3 = "",UserField4 = "",
   BCID:integer,
   
   party  = TBfile("party.dbt",    "R", 0);
 
   ContractID = 0;

   if ((rs.ObjectType == 7) or (rs.ObjectType == 9) or (rs.ObjectType == 10)) // Dealing
                     
      BackOffice    = 2;
      Chapter       = 0;
      ContractType  = rs.ObjectType;
      ObjectType    = 103;
      Client        = {OurBank};
      ClientQuality = 1;

      if (ValType(rs.FIID) != V_UNDEF)
         FIID = rs.FIID;
      end;
      if (ValType(rs.Account) != V_UNDEF)
         ObjectID = rs.Account;
      end;

      
   
   elif ((rs.ObjectType == 1001) or (rs.ObjectType == 1002) or (rs.ObjectType == 1011) or (rs.ObjectType == 1012)) // GK

      BackOffice     = 9;
      
      if ((rs.ObjectType == 1001) or (rs.ObjectType == 1002) )   
         Chapter = 1;
      else
         Chapter = 3;
      end;

      if (ValType(rs.Account) != V_UNDEF)
         Account = rs.Account;
      end;      
      if (ValType(rs.FIID) != V_UNDEF)
         FIID = rs.FIID;
      end;

      GetAccountClient(rs.Account, FIID, Chapter, @Client);

      party.rec.PartyID = Client;
      if (party.getEQ == false)
         PrintReportLine(string("Ошибка создания договора. Не найден клиент по счету ", rs.Account) );
         return 1;
      end;

      if (not GetMainObjAttr(null, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 13, ClientQuality, null, null, {curdate}))
        PrintReportLine(string("Для контрагента '", party.rec.ShortName,"' не задана категория качества ") );
        ClientQuality = 0;
      end;
      
   else     // Securities
   
      BCID = int(rs.Account);
      DefineBO(BCID, @BackOffice);        
      
      Chapter        = 0;
      ContractType   = rs.ObjectType;
      ObjectType     = 651;
      ClientQuality  = 1;
      
      if (ValType(rs.FIID) != V_UNDEF)
         FIID = rs.FIID;
      end;
      if (ValType(rs.Account) != V_UNDEF)
         ObjectID = rs.Account;
      end;
      
   end;

   Number = CalcContractNumber(rs.AutoKey, BackOffice);

   if (rs.RestKind == 1) 
      if (ValType(rs.PayerAccount) != V_UNDEF)
         PerAccount = rs.PayerAccount;
      end;
   elif (rs.RestKind == 2)
      if (ValType(rs.PercReceiverAccount) != V_UNDEF)
         PerAccount = rs.PercReceiverAccount;
      end;
   end;   
   
   if (ValType(rs.UserType) != V_UNDEF)
      UserType = rs.UserType;
   end;
   if (ValType(rs.UserField1) != V_UNDEF)
      UserField1 = rs.UserField1;
   end;
   if (ValType(rs.UserField2) != V_UNDEF)
      UserField2 = rs.UserField2;
   end;
   if (ValType(rs.UserField3) != V_UNDEF)
      UserField3 = rs.UserField3;
   end;
   if (ValType(rs.UserField4) != V_UNDEF)
      UserField4 = rs.UserField4;
   end;      
      
   ClearRecord(PrcContractFile);

   PrcContractFile.BackOffice   = BackOffice   ;
   PrcContractFile.Number       = Number       ;
   PrcContractFile.Chapter      = Chapter      ;
   PrcContractFile.FIID         = FIID         ;
   PrcContractFile.Account      = Account      ;
   PrcContractFile.Client       = Client       ;
   PrcContractFile.BeginDate    = rs.BeginDate ;
   PrcContractFile.EndDate      = rs.EndDate   ;
   PrcContractFile.PerAccount   = PerAccount   ;
   PrcContractFile.UserType     = UserType     ;
   PrcContractFile.Oper         = {Oper}       ;
   PrcContractFile.UserField1   = UserField1   ;
   PrcContractFile.UserField2   = UserField2   ;
   PrcContractFile.UserField3   = UserField3   ;
   PrcContractFile.UserField4   = UserField4   ;
   PrcContractFile.ContractType = ContractType ;
   PrcContractFile.ObjectType   = ObjectType   ;
   PrcContractFile.ObjectID     = ObjectID     ;

   if ((ValType(rs.PercReceiverAccount) != V_UNDEF) AND (ValType(rs.Account) != V_UNDEF) AND (rs.PercReceiverAccount == rs.Account))
      PrcContractFile.isReckonAmoung = "X";
   elif((rs.RestKind == 1) OR (rs.RestKind == 2))
      PrcContractFile.useOurAccount = "X";
   end;

   Insert( PrcContractFile );


   cmd2 = RSDCommand ("select max(T_CONTRACTID) as maxid from DPRCCONTRACT_DBT " +
         "where (T_BACKOFFICE = ? and T_NUMBER = ?)");
   cmd2.addParam( "", RSDBP_IN, BackOffice);
   cmd2.addParam( "", RSDBP_IN, Number);
   cmd2.execute();
   rs2 = TRsbDataSet(cmd2);
   if (rs2.movenext())
      ContractID = rs2.maxid;   
   end;   

   if (ClientQuality != 0)
      cmd3 = RSDCommand ("insert into DPRCCNTQUAL_DBT " +
                         "(T_CONTRACTID, T_DATE, T_CLIENTQUALITY) " +
                         "values (?,?,?)");
      cmd3.addParam( "", RSDBP_IN, ContractID );
      cmd3.addParam( "", RSDBP_IN, rs.BeginDate );
      cmd3.addParam( "", RSDBP_IN, ClientQuality );
      cmd3.execute();
   end;

   PrintReportLine(string("Создан договор для счета ", rs.Account, " с ID = ", ContractID));
   return 0;
      
   OnError( RslErrObj ) 
       PrintReportLine(string("Ошибка создания договора по счету ", rs.Account, 
                       ",\n возможно уже существует договор с № ", rs.AutoKey) );
  return 1;
 
END;

//---------------------------------------------------------------------
//  Блок функций по созданию графиков и условия расчета
//---------------------------------------------------------------------


// Десятичное - в Hex
PRIVATE MACRO DecToHex(DecNum):string
   var HexStr = "";

   if (DecNum < 0)
      HexStr = "";
   elif (DecNum < 10)
      HexStr = string(DecNum);
   elif (DecNum == 10)
      HexStr = "A";
   elif (DecNum == 11)
      HexStr = "B";
   elif (DecNum == 12)
      HexStr = "C";
   elif (DecNum == 13)
      HexStr = "D";
   elif (DecNum == 14)
      HexStr = "E";
   elif (DecNum == 15)
      HexStr = "F";
   end;

   return HexStr;
END;


// Конвертация даты в Hex формат.
PRIVATE MACRO ConvDateToHex(ConvDate):string
   var day = 0, mon = 0, year = 0,
       year_l = 0, year_h = 0,
       StrDay = "", StrMon = "", StrYear = "", StrDate = "";

   DateSplit(ConvDate,day,mon,year);

   if   (day == 0)
      StrDay = "00";
   elif (day < 16)
      StrDay = "0" + DecToHex(day);
   elif (day >= 16)
      StrDay = "1" + DecToHex(day - 16);
   end;
   
   StrMon = "0" + DecToHex(mon);

   year_h = Int(year / 256);
   year_l = mod(year, 256);
    
   if   (year_l == 0)
      StrYear = "00";
   else 
      StrYear = DecToHex(Int(year_l / 16)) + DecToHex(mod(year_l, 16));
   end;

   if   (year_h == 0)
      StrYear = StrYear + "00";
   else 
      StrYear = StrYear + DecToHex(Int(year_h / 16)) + DecToHex(mod(year_h, 16));
   end;

   StrDate = StrDay + StrMon + StrYear;

   return StrDate;    
END;


// Создание параметров для абсолютного графика
PRIVATE MACRO CreateParamsForAbsoluteGraph(AutoKey,Kind, ScheduleID)
   var stat = 0,
       cmd,rs,cmd2,cmd3,
       day = 0, mon = 0, year = 0,
       StrFirstDate = "", StrDelay = "",
       iRowNum = 1;

   cmd = RSDCommand ("select T_CALCDATE, T_PAYDATE from DPCCALC_DBT " +
                     "where T_AUTOPERC = ? ORDER BY T_CALCDATE");
   cmd.addParam( "", RSDBP_IN, AutoKey );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   
   while (rs.movenext())
      if ((string(rs.CalcDate) != "") and (ValType(rs.CalcDate) != V_UNDEF))
         StrFirstDate = ConvDateToHex(date(rs.CalcDate));
         
         if (Kind != PRC_SCHEDKIND_PAY) // графики расчета и начисления
            StrDelay = "0";
         else                             // график оплаты 
            StrDelay = string(date(rs.PayDate) - date(rs.CalcDate));
         end;
         
         cmd2 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
                 "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_FIRSTDATE, " +
                 "T_DELAYTYPE, T_DELAY) " +
                 "values(?,?,4,?,2,?)");
         cmd2.addParam( "", RSDBP_IN, ScheduleID );
         cmd2.addParam( "", RSDBP_IN, iRowNum );
         cmd2.addParam( "", RSDBP_IN, StrFirstDate);
         cmd2.addParam( "", RSDBP_IN, StrDelay);
         cmd2.execute();
         
         iRowNum = iRowNum + 1;
      end;
   end;

   cmd3 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
              "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_DELAYTYPE, T_DELAY)" +
              "values(?,?,2,2,0)");
   cmd3.addParam( "", RSDBP_IN, ScheduleID );
   cmd3.addParam( "", RSDBP_IN, iRowNum );
   cmd3.execute();

   return stat;
END;


// Создание параметров для абсолютного графика (тип исходного - ежедневный)
PRIVATE MACRO CreateParamsForAbsoluteEDGraph(AutoKey,Kind, ScheduleID)
   var stat = 0,
       cmd,rs,cmd2,rs2,cmd3,cmd4,
       day,mon,year,
       StrFirstDate = "", StrDelay = "",
       iRowNum = 1;
   
   if (Kind != PRC_SCHEDKIND_PAY) // графики расчета и начисления

      cmd = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
              "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_DELAYTYPE, T_DELAY)" +
              "values(?,1,1,2,0)");
      cmd.addParam( "", RSDBP_IN, ScheduleID );
      cmd.execute();
      
      cmd2 = RSDCommand ("select T_BEGINDATE from DPCADDHIS_DBT " +
                     "where T_AUTOPERC = ? ORDER BY T_BEGINDATE");
      cmd2.addParam( "", RSDBP_IN, AutoKey );
      cmd2.execute();
      rs2 = TRsbDataSet(cmd2);
      
      iRowNum = 2;
      while (rs2.movenext())
         if ((string(rs2.BeginDate) != NULL) and (ValType(rs2.BeginDate) != V_UNDEF))
            StrFirstDate = ConvDateToHex(date(rs2.BeginDate));
      
            cmd3 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
                 "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_FIRSTDATE, " +
                 "T_DELAYTYPE, T_DELAY) " +
                 "values(?,?,4,?,2,0)");
            cmd3.addParam( "", RSDBP_IN, ScheduleID );
            cmd3.addParam( "", RSDBP_IN, iRowNum );
            cmd3.addParam( "", RSDBP_IN, StrFirstDate);
            cmd3.execute();
            iRowNum = iRowNum + 1;
         end;
      end;
      
   else          // график оплаты
      
      cmd2 = RSDCommand ("select T_BEGINDATE, T_ENDDATE, T_PAYDATE from DPCPAYGRH_DBT " +
                     "where T_AUTOPERC = ? ORDER BY T_BEGINDATE");
      cmd2.addParam( "", RSDBP_IN, AutoKey );
      cmd2.execute();
      rs2 = TRsbDataSet(cmd2);

      iRowNum = 1;
      while (rs2.movenext()) 
         DateSplit(date(rs2.EndDate),day,mon,year);
         if (year != 9999)
            StrFirstDate = ConvDateToHex(date(rs2.EndDate));
            StrDelay = string(date(rs2.PayDate) - date(rs2.EndDate));
            cmd3 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
                 "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_FIRSTDATE, " +
                 "T_DELAYTYPE, T_DELAY) " +
                 "values(?,?,4,?,2,?)");
            cmd3.addParam( "", RSDBP_IN, ScheduleID );
            cmd3.addParam( "", RSDBP_IN, iRowNum );
            cmd3.addParam( "", RSDBP_IN, StrFirstDate);
            cmd3.addParam( "", RSDBP_IN, StrDelay);
            cmd3.execute();
            iRowNum = iRowNum + 1;
         end;
      end;      
      
   end; 

   cmd4 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
              "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_DELAYTYPE, T_DELAY)" +
              "values(?,?,2,2,0)");
   cmd4.addParam( "", RSDBP_IN, ScheduleID );
   cmd4.addParam( "", RSDBP_IN, iRowNum );
   cmd4.execute();

   return stat;
END;


// Создание параметров для относительного графика
PRIVATE MACRO CreateParamsForRelativeGraph(AutoKey,Kind, ScheduleID, BeginDate, GraphType)
   var stat = 0,
       cmd,rs,cmd2,cmd3,
       StrFirstDate = "", BegDate,
       PeriodType = 0,
       PayDelay = 0,
       iRowNum = 1;

   cmd = RSDCommand ("select T_BEGINDATE, T_PERIODTYPE, T_PERIOD, T_PAYDELAY from DPCRCALC_DBT " +
                     "where T_AUTOPERC = ? ORDER BY T_BEGINDATE");
   cmd.addParam( "", RSDBP_IN, AutoKey );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   
   while (rs.movenext()) 
      
      if (GraphType == PC_SCHEDTYPE_RELATIVE)
         if (BeginDate > rs.BeginDate)
            BegDate = BeginDate;
         else
            BegDate = rs.BeginDate;
         end;
         PeriodType = rs.PeriodType;
      else
         if ((ValType(rs.BeginDate) == V_UNDEF) or (rs.BeginDate == NULL))
            BegDate = BeginDate;
         else
            BegDate = rs.BeginDate;
         end;
         PeriodType = rs.PeriodType + 2;
      end;
      
      StrFirstDate = ConvDateToHex(date(rs.BeginDate));
      
      if (Kind != PRC_SCHEDKIND_PAY)
         PayDelay = 0;
      else
         PayDelay = rs.PayDelay;
      end;

      cmd2 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
              "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_FIRSTDATE, " +
              "T_PERIODTYPE, T_PERIODLENGTH, T_DELAYTYPE, T_DELAY) " +
              "values(?,?,4,?, ?,?,2,?)");
      cmd2.addParam( "", RSDBP_IN, ScheduleID );
      cmd2.addParam( "", RSDBP_IN, iRowNum );
      cmd2.addParam( "", RSDBP_IN, StrFirstDate);
      cmd2.addParam( "", RSDBP_IN, PeriodType );
      cmd2.addParam( "", RSDBP_IN, rs.Period );
      cmd2.addParam( "", RSDBP_IN, PayDelay);
      cmd2.execute();
      
      iRowNum = iRowNum + 1;
   end; // end while  

   cmd3 = RSDCommand ("insert into DPRCSCHEDPRM_DBT " +
              "(T_SCHEDULEID, T_PARAMID, T_SCHEDULEPARAMTYPE, T_DELAYTYPE, T_DELAY)" +
              "values(?,?,2,2,0)");
   cmd3.addParam( "", RSDBP_IN, ScheduleID );
   cmd3.addParam( "", RSDBP_IN, iRowNum );
   cmd3.execute();
   
  return stat;
END;      


// Создание одного графика и заполнение его параметров
PRIVATE MACRO CreateOneGraphic(Kind, AutoKey, BeginDate, GraphType, ScheduleID:@integer );

   var SchedType = 0,
       OpenDate,
       Number = 1000,
       cmd, cmd2,rs2;

   if ((GraphType == PC_SCHEDTYPE_ABSOLUTE) or (GraphType == PC_SCHEDTYPE_UNSET) 
       or (GraphType == PC_SCHEDTYPE_DAILY))
      SchedType = PRC_SCHEDTYPE_ABSOLUTE;           // абсолютный
   elif ((GraphType == PC_SCHEDTYPE_RELATIVE) or (GraphType == PC_SCHEDTYPE_CALCDAY) )
      SchedType = PRC_SCHEDTYPE_RELATIVE;           // относительный
   end;

   OpenDate = date(BeginDate) - 1;
   Number = 1000 + AutoKey;

   cmd = RSDCommand ("insert into DPRCSCHED_DBT " +
         "(T_SCHEDULEKIND, T_ISUSER, T_NUMBER, T_SHORTNAME, " +
         "T_NAME, T_OPENDATE, T_CLOSEDATE, T_GROUPSCHEDULEID, " +
         "T_SCHEDULETYPE) " +
         "values (?,'X',?,?, ?,?,'01.01.0001',0, ?)");
   cmd.addParam( "", RSDBP_IN, Kind );
   cmd.addParam( "", RSDBP_IN, Number );
   cmd.addParam( "", RSDBP_IN, string(AutoKey) );

   cmd.addParam( "", RSDBP_IN, string(AutoKey) );
   cmd.addParam( "", RSDBP_IN, OpenDate);
   
   cmd.addParam( "", RSDBP_IN, SchedType );

   cmd.execute();

   cmd2 = RSDCommand ("select max(T_SCHEDULEID) as maxid from DPRCSCHED_DBT " +
         "where T_SCHEDULEKIND = ? and T_NUMBER = ? and T_SHORTNAME = ? and T_NAME = ? and T_OPENDATE = ?");
   cmd2.addParam( "", RSDBP_IN, Kind );
   cmd2.addParam( "", RSDBP_IN, Number );
   cmd2.addParam( "", RSDBP_IN, string(AutoKey) );
   cmd2.addParam( "", RSDBP_IN, string(AutoKey) );
   cmd2.addParam( "", RSDBP_IN, OpenDate);
   cmd2.execute();
   
   rs2 = TRsbDataSet(cmd2);
   if (rs2.movenext())
      ScheduleID = rs2.maxid;   
   end; 

   // в зависимости от типа заполняем параметры графика
   if ((GraphType == PC_SCHEDTYPE_ABSOLUTE) or (GraphType == PC_SCHEDTYPE_UNSET)) //абсолютный
      CreateParamsForAbsoluteGraph(AutoKey,Kind, ScheduleID);
   elif (GraphType == PC_SCHEDTYPE_DAILY)                 // абсолютный для старого ежедневного
      CreateParamsForAbsoluteEDGraph(AutoKey,Kind, ScheduleID);
   else                                  // относительный
      CreateParamsForRelativeGraph(AutoKey,Kind, ScheduleID, BeginDate, GraphType);
   end;
   
   return 0;
END;

// Создание условия расчета
PRIVATE MACRO CreateContractCalc(ContractID, rs);

   var stat,
       ResKind = 0,
       CalcScheduleID = 0, ChargeScheduleID = 0, PayScheduleID = 0,
       CalcID = 0,
       OpenDate,
       CalcBaseType = 1,
       MacroProc = "",
       Number = 1000,
       cmd,cmd2,rs2,cmd3;  

   if ((rs.GraphType <=0) or (rs.GraphType > 5))
      PrintReportLine("Ошибка создания условия расчета для договора ID = " + ContractID + ". Неверный тип графика");
      return 1;
   end;

   // Создаем графики расчета. начисления, оплаты
   CreateOneGraphic(PRC_SCHEDKIND_CALC,   rs.AutoKey, rs.BeginDate, rs.GraphType, @CalcScheduleID );
   CreateOneGraphic(PRC_SCHEDKIND_CHARGE, rs.AutoKey, rs.BeginDate, rs.GraphType, @ChargeScheduleID );
   CreateOneGraphic(PRC_SCHEDKIND_PAY,    rs.AutoKey, rs.BeginDate, rs.GraphType, @PayScheduleID );

   if (rs.RestKind == 1)
      ResKind = 2;
   else
      ResKind = 1;
   end;
   OpenDate = date(rs.BeginDate) - 1;

   if ((rs.ObjectType == 7) 
       or (rs.ObjectType == 9) 
       or (rs.ObjectType == 10)) // Dealing
                     
      CalcBaseType = 2;
      MacroProc = "RSO_Dealing.RSI_RestList";
   
   elif ((rs.ObjectType == 1001) 
         or (rs.ObjectType == 1002) 
         or (rs.ObjectType == 1011) 
         or (rs.ObjectType == 1012)) // CB

      CalcBaseType = 1;
      MacroProc = "";
      
   else     // Securities
   
      CalcBaseType = 2;
      MacroProc = "RSI_RSB_PERCENT.PrcBillRestList";
      
   end;

   Number = 1000 + rs.AutoKey;

   // Создаем само условие расчета
   cmd = RSDCommand ("insert into DPRCCALC_DBT " +
         "(T_ISUSER, T_NUMBER, T_SHORTNAME, T_NAME, " +
         " T_OPENDATE, T_CLOSEDATE, T_GROUPCALCID, T_RESOURCEKIND, " +
         "T_GROUPRATEID, T_CALCSCHEDULEID, T_CHARGESCHEDULEID, T_PAYSCHEDULEID, " +
         "T_USERSCHEDULEID, T_CALENDAR, T_RESTTYPE, T_CALCBASETYPE, " +
         "T_MACROPROC) " +
         "values ('X',?,?,?, ?,'01.01.0001',0,?, 0,?,?,?, 0,?,?,?, ?)");   
   cmd.addParam( "", RSDBP_IN, Number );
   cmd.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd.addParam( "", RSDBP_IN, string(rs.AutoKey) );

   cmd.addParam( "", RSDBP_IN, OpenDate);
   cmd.addParam( "", RSDBP_IN, ResKind );

   cmd.addParam( "", RSDBP_IN, CalcScheduleID );
   cmd.addParam( "", RSDBP_IN, ChargeScheduleID );
   cmd.addParam( "", RSDBP_IN, PayScheduleID );   
   
   cmd.addParam( "", RSDBP_IN, rs.CalendAlg );
   cmd.addParam( "", RSDBP_IN, rs.RestUseType );
   cmd.addParam( "", RSDBP_IN, CalcBaseType );

   cmd.addParam( "", RSDBP_IN, MacroProc );
   cmd.execute();

   cmd2 = RSDCommand ("select max(T_CALCID) as maxid from DPRCCALC_DBT " +
         "where T_NUMBER = ? and T_SHORTNAME = ? and T_NAME = ? and T_OPENDATE = ?");
   cmd2.addParam( "", RSDBP_IN, Number );
   cmd2.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd2.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd2.addParam( "", RSDBP_IN, OpenDate );
   cmd2.execute();
   
   rs2 = TRsbDataSet(cmd2);
   if (rs2.movenext())
      CalcID = rs2.maxid;   
   end; 
    
   cmd3 = RSDCommand ("update DPRCCONTRACT_DBT set T_CALCID = ? " +
         "where T_CONTRACTID = ?");
   cmd3.addParam( "", RSDBP_IN, CalcID );
   cmd3.addParam( "", RSDBP_IN, ContractID );
   cmd3.execute(); 
   
   return 0;
END;


//---------------------------------------------------------------------
//  Блок функций по созданию процентной ставки
//---------------------------------------------------------------------

// Создание пользовательского макроса
PRIVATE MACRO CreateRateMacroFile(FileName, ProcName, RateVal)
   var day,mon,year,hh,mm,ss,
       Today, Now;
   file UserRateFile() txt write;

   close( UserRateFile ); //На всякий случай
   open( UserRateFile, "..\\Mac\\Cb\\" +FileName );

   insert( UserRateFile, "MACRO " + ProcName);
   insert( UserRateFile, "");
   insert( UserRateFile, "   return " + string(RateVal) + ";");
   insert( UserRateFile, "END; ");

   return 0;     
END;

// Анализ параметров ставки по старым процентам
PRIVATE MACRO AnalyzeRateVal(RateType, AutoKey, BeginDate,EndDate, 
                            PrcRateType:@integer, FirstRateDate:@date);

   var cmd,rs,cmd2,rs2,cmd3,rs3;
   
   if (RateType == PC_RATETYPE_CONTRACT)
      cmd2 = RSDCommand ("select max(T_BEGINDATE) as MAXDATE from DPCRATE_DBT " +
               "where T_AUTOPERC = ? and T_BEGINDATE <= ? ");
      cmd2.addParam( "", RSDBP_IN, AutoKey );
      cmd2.addParam( "", RSDBP_IN, BeginDate );
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())
         FirstRateDate = rs2.MaxDate;
      else 
         return 1;
      end;
   end;

   //  Определяем вид процентной ставки в новых процентах 

   // абсолютная по умолчанию  
   PrcRateType =  PRC_RATETYPE_ABSOLUTE;

   // пользовательская 
   if ((RateType == PC_RATETYPE_ABSOLUTE) or (RateType == PC_RATETYPE_RELATIVE))

      cmd = RSDCommand ("select T_AUTOKEY from DPCRATE_DBT " +
          "where T_MACRO <> 0 and T_AUTOPERC = ?");
      cmd.addParam( "", RSDBP_IN, AutoKey );
   elif (RateType == PC_RATETYPE_CONTRACT)

      cmd = RSDCommand ("select T_BEGINDATE, T_AUTOKEY from DPCRATE_DBT " +
          "where T_MACRO <> 0 and T_AUTOPERC = ? and T_BEGINDATE <= ?" +
          "and T_BEGINDATE >= ?");
      cmd.addParam( "", RSDBP_IN, AutoKey );
      cmd.addParam( "", RSDBP_IN, EndDate );
      cmd.addParam( "", RSDBP_IN, FirstRateDate );      
   end;

   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      PrcRateType = PRC_RATETYPE_USER;
      return 0;
   end;

   //  плавающая
   if ( PrcRateType != PRC_RATETYPE_USER)
    if ((RateType == PC_RATETYPE_ABSOLUTE) or (RateType == PC_RATETYPE_RELATIVE))
  
        cmd3 = RSDCommand ("select T_AUTOKEY from DPCRATE_DBT " +
            "where T_PERCALG = 2 and T_AUTOPERC = ?");
        cmd3.addParam( "", RSDBP_IN, AutoKey );
    elif (RateType == PC_RATETYPE_CONTRACT)
  
        cmd3 = RSDCommand ("select T_BEGINDATE, T_AUTOKEY from DPCRATE_DBT " +
            "where T_PERCALG <> 0 and T_AUTOPERC = ? and T_BEGINDATE <= ?" +
            "and T_BEGINDATE >= ?");
        cmd3.addParam( "", RSDBP_IN, AutoKey );
        cmd3.addParam( "", RSDBP_IN, EndDate );
        cmd3.addParam( "", RSDBP_IN, FirstRateDate );      
    end;
  
    cmd3.execute();
     
    rs3 = TRsbDataSet(cmd3);
    if (rs3.movenext())
        PrcRateType = PRC_RATETYPE_FLOATING;
    end;
   end;
    
   return 0;
   
END;

// Создание параметров процентной ставки
PRIVATE MACRO CreateRateParams(ContractID, FIID, RateID,RateType, AutoKey, BeginDate, EndDate, PrcRateType)
   var MacroFile:string, MacroProc:string,
       fldNameRest, fldNamePercent,
       cmd,rs,cmd2,rs2,cmd3,rs3,
       iRecNum = 1,
       RateVal = 0,
       day = 0,
       strZero = "";

   if (PrcRateType == PRC_RATETYPE_ABSOLUTE)      // абсолютная ставка  - одним запросом записываем значения 
      if ((RateType == PC_RATETYPE_ABSOLUTE) or (RateType == PC_RATETYPE_RELATIVE))
         cmd = RSDCommand ("insert into DPRCRATEVAL_DBT (T_RATEID, T_RATEDATE, T_RATE, T_MACROFILE, " +
                           "T_MACROPROC, T_VARIABLENAME, T_VARIABLEPERIOD, T_VARIABLEDAYS) " +
                           "select ?, T_BEGINDATE, T_PERCENT1,'','','',0,0 from DPCRATE_DBT " +
                           "where T_AUTOPERC = ?");
         cmd.addParam( "", RSDBP_IN, RateID );
         cmd.addParam( "", RSDBP_IN, AutoKey );
      elif (RateType == PC_RATETYPE_CONTRACT)

         cmd = RSDCommand ("insert into DPRCRATEVAL_DBT (T_RATEID, T_RATEDATE, T_RATE, T_MACROFILE, " +
                           "T_MACROPROC, T_VARIABLENAME, T_VARIABLEPERIOD, T_VARIABLEDAYS) " +
                           "select ?, T_BEGINDATE, T_PERCENT1,'','','',0,0 from DPCRATE_DBT " +
                           "where T_AUTOPERC = ? and T_BEGINDATE <= ? and T_BEGINDATE >= ?");
         cmd.addParam( "", RSDBP_IN, RateID );
         cmd.addParam( "", RSDBP_IN, AutoKey );
         cmd.addParam( "", RSDBP_IN, EndDate );
         cmd.addParam( "", RSDBP_IN, BeginDate ); 
      end;
      cmd.execute();
   
   elif (PrcRateType == PRC_RATETYPE_USER)    // относительная ставка

      if ((RateType == PC_RATETYPE_ABSOLUTE) or (RateType == PC_RATETYPE_RELATIVE))

         cmd = RSDCommand ("select r.T_BEGINDATE, r.T_MACRO, r.T_PERCALG, r.T_PERCENT1, m.T_FILENAME " +
                           "from DPCRATE_DBT r, DPCMACRO_DBT m " +
                           "where r.T_MACRO = m.T_NUMBER(+) and r.T_AUTOPERC = ?");
         cmd.addParam( "", RSDBP_IN, AutoKey );
      elif (RateType == PC_RATETYPE_CONTRACT)

         cmd = RSDCommand ("select r.T_BEGINDATE, r.T_MACRO, r.T_PERCALG, r.T_PERCENT1, m.T_FILENAME " +
                           "from DPCRATE_DBT r, DPCMACRO_DBT m " +
                           "where r.T_MACRO = m.T_NUMBER(+) and r.T_AUTOPERC = ? " +
                           "and r.T_BEGINDATE <= ? and r.T_BEGINDATE >= ?");
         cmd.addParam( "", RSDBP_IN, AutoKey );
         cmd.addParam( "", RSDBP_IN, EndDate );
         cmd.addParam( "", RSDBP_IN, BeginDate ); 
      end;
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      while (rs.movenext())
         if (rs.value("t_macro") != 0) // старая ставка задана макросом
            MacroFile = rs.FileName;
            MacroProc = rs.FileName;
         elif (rs.PercAlg == 1) // старая ставка - число
            DateSplit(date(rs.BeginDate), day, NULL, NULL);
            if (day < 10)
               strZero = "0";
            else
               strZero = "";
            end;
            MacroFile = string(AutoKey,"_") + strZero + trim(string(date(rs.BeginDate))) + ".mac";
            MacroProc = "GenerateRateValue";
            RateVal = rs.value("t_percent1");
            CreateRateMacroFile(MacroFile, MacroProc, RateVal);
         elif (rs.PercAlg == 2) // сложная старая ставка
            MacroFile = "";
            MacroProc = "";
           PrintReportLine(string("Для договора с ID = ",ContractID," ставка, действующая с ",rs.BeginDate,
                          "не перенесена. Необходимо создание и подключение макроса для расчета"));
         end;

         cmd2 = RSDCommand ("insert into DPRCRATEVAL_DBT (T_RATEID, T_RATEDATE, T_RATE, T_MACROFILE, " +
                            "T_MACROPROC, T_VARIABLENAME, T_VARIABLEPERIOD, T_VARIABLEDAYS) " +
                            "values(?,?,0,?,?,' ',0,0)");
         cmd2.addParam( "", RSDBP_IN, RateID );
         cmd2.addParam( "", RSDBP_IN, rs.BeginDate );
         cmd2.addParam( "", RSDBP_IN, MacroFile );
         cmd2.addParam( "", RSDBP_IN, MacroProc );
         cmd2.execute();
         
      end;

   elif (PrcRateType == PRC_RATETYPE_FLOATING)   // плавающая ставка
       
      if ((RateType == PC_RATETYPE_ABSOLUTE) or (RateType == PC_RATETYPE_RELATIVE))

         cmd = RSDCommand ("select r.T_BEGINDATE, r.T_PERCALG, " +
                           "m.T_FILENAME, r.T_PERCRESTALG, r.T_RESTCOUNT,  " +
                           "r.T_REST1, r.T_PERCENT1, r.T_REST2, r.T_PERCENT2, " +
                           "r.T_REST3, r.T_PERCENT3, r.T_REST4, r.T_PERCENT4, " +
                           "r.T_REST5, r.T_PERCENT5, r.T_REST6, r.T_PERCENT6, " +
                           "r.T_REST7, r.T_PERCENT7, r.T_REST8, r.T_PERCENT8, " +
                           "r.T_REST9, r.T_PERCENT9, r.T_REST10, r.T_PERCENT10, " +
                           "r.T_REST11, r.T_PERCENT11, r.T_REST12, r.T_PERCENT12, " +
                           "r.T_REST13, r.T_PERCENT13, r.T_REST14, r.T_PERCENT14, " +
                           "r.T_REST15, r.T_PERCENT15, r.T_REST16, r.T_PERCENT16, " +
                           "r.T_REST17, r.T_PERCENT17, r.T_REST18, r.T_PERCENT18, " +
                           "r.T_REST19, r.T_PERCENT19, r.T_REST20, r.T_PERCENT20 " +
                           "from DPCRATE_DBT r, DPCMACRO_DBT m " +
                           "where r.T_MACRO = m.T_NUMBER(+) and r.T_AUTOPERC = ?");
         cmd.addParam( "", RSDBP_IN, AutoKey );
      elif (RateType == PC_RATETYPE_CONTRACT)

         cmd = RSDCommand ("select r.T_BEGINDATE, r.T_PERCALG, " +
                           "m.T_FILENAME, r.T_PERCRESTALG, r.T_RESTCOUNT,  " +
                           "r.T_REST1, r.T_PERCENT1, r.T_REST2, r.T_PERCENT2, " +
                           "r.T_REST3, r.T_PERCENT3, r.T_REST4, r.T_PERCENT4, " +
                           "r.T_REST5, r.T_PERCENT5, r.T_REST6, r.T_PERCENT6, " +
                           "r.T_REST7, r.T_PERCENT7, r.T_REST8, r.T_PERCENT8, " +
                           "r.T_REST9, r.T_PERCENT9, r.T_REST10, r.T_PERCENT10, " +
                           "r.T_REST11, r.T_PERCENT11, r.T_REST12, r.T_PERCENT12, " +
                           "r.T_REST13, r.T_PERCENT13, r.T_REST14, r.T_PERCENT14, " +
                           "r.T_REST15, r.T_PERCENT15, r.T_REST16, r.T_PERCENT16, " +
                           "r.T_REST17, r.T_PERCENT17, r.T_REST18, r.T_PERCENT18, " +
                           "r.T_REST19, r.T_PERCENT19, r.T_REST20, r.T_PERCENT20 " +
                           "from DPCRATE_DBT r, DPCMACRO_DBT m " +
                           "where r.T_MACRO = m.T_NUMBER(+) and r.T_AUTOPERC = ? " +
                           "and r.T_BEGINDATE <= ? and r.T_BEGINDATE >= ?");
         cmd.addParam( "", RSDBP_IN, AutoKey );
         cmd.addParam( "", RSDBP_IN, EndDate );
         cmd.addParam( "", RSDBP_IN, BeginDate ); 
      end;
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      while (rs.movenext())
         if ((rs.PercAlg == 1) and (rs.PercRestAlg == 1)) 
         
            cmd3 = RSDCommand ("insert into DPRCRATEVALVAR_DBT (T_RATEID, T_RATEDATE, T_PARAMID, " + 
                               "T_PARENTPARAMID, T_TYPE, T_RATE)" +
                               "values(?,?,1,0,1,?)");
            cmd3.addParam( "", RSDBP_IN, RateID );
            cmd3.addParam( "", RSDBP_IN, rs.BeginDate );
            cmd3.addParam( "", RSDBP_IN, string(rs.Percent1:10:2) );
            cmd3.execute();
            
         elif ((rs.PercAlg == 2) and (rs.PercRestAlg == 1))

            while (iRecNum <= rs.RestCount)
               fldNameRest = string("t_rest", iRecNum);
               fldNamePercent = string("t_percent", iRecNum);
                              
               cmd3 = RSDCommand ("insert into DPRCRATEVALVAR_DBT (T_RATEID, T_RATEDATE, T_PARAMID, " + 
                                  "T_PARENTPARAMID, T_TYPE, T_PARAM, T_CONDITION, T_CONDITIONVALUE, " +
                                  "T_UNIT, T_RATE) " +
                                  "values(?,?,?, 0,3,1,'=',?, ?,?)");
               cmd3.addParam( "", RSDBP_IN, RateID );
               cmd3.addParam( "", RSDBP_IN, rs.BeginDate );
               cmd3.addParam( "", RSDBP_IN, iRecNum);
               
               cmd3.addParam( "", RSDBP_IN, rs.Value(fldNameRest) );

               cmd3.addParam( "", RSDBP_IN, FIID );
               cmd3.addParam( "", RSDBP_IN, rs.Value(fldNamePercent) );
               cmd3.execute();
               iRecNum  = iRecNum +1;
            end;
            
         elif ((rs.PercAlg == 2) and (rs.PercRestAlg == 2)) 
         
            PrintReportLine(string("Для договора с ID = ",ContractID," ставка, действующая с ",rs.BeginDate,
                          " не перенесена. Необходимо настроить ее вручную"));
            return 0;
         end;

         cmd2 = RSDCommand ("insert into DPRCRATEVAL_DBT (T_RATEID, T_RATEDATE, T_RATE, T_MACROFILE, " +
                           "T_MACROPROC, T_VARIABLENAME, T_VARIABLEPERIOD, T_VARIABLEDAYS) " +
                           "values(?,?,0,'','','',0,0)");
         cmd2.addParam( "", RSDBP_IN, RateID );
         cmd2.addParam( "", RSDBP_IN, rs.BeginDate );
         cmd2.execute();          
      end;             

   end;
   
   return 0;
END;


// Создание процентной ставки
PRIVATE MACRO CreateContractRate(ContractID, rs)
   var PrcRateType = 0,
       RateID = 0,
       Number = 1000,
       FirstRateDate:date,
       cmd,cmd2,rs2,cmd3;

   if ((rs.RateType != PC_RATETYPE_ABSOLUTE) and (rs.RateType != PC_RATETYPE_RELATIVE) and (rs.RateType != PC_RATETYPE_CONTRACT))
      PrintReportLine("Неверный тип процентной ставки для договора" + ContractID);
      return 1;
   end;

   // анализируем параметры ставки по старым процентам
   if (AnalyzeRateVal(rs.RateType, rs.AutoKey, rs.BeginDate, rs.EndDate, @PrcRateType, @FirstRateDate) != 0)
      PrintReportLine("Не найдено ни одного параметра процентной ставки для договора ID = " + ContractID);
      return 1;
   end;

   Number = 1000 + rs.AutoKey;

   // записываем новую ставку
   cmd = RSDCommand ("insert into DPRCRATE_DBT " +
         "(T_ISUSER, T_NUMBER, T_SHORTNAME, T_NAME, " +
         " T_OPENDATE, T_CLOSEDATE, T_GROUPRATEID, T_RATETYPE) " +
         "values ('X',?,?,?, ?,'01.01.0001',0,?)");   
   cmd.addParam( "", RSDBP_IN, Number );
   cmd.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd.addParam( "", RSDBP_IN, string(rs.AutoKey) );

   cmd.addParam( "", RSDBP_IN, date(rs.BeginDate) - 1);
   cmd.addParam( "", RSDBP_IN, PrcRateType );
   cmd.execute();

   cmd2 = RSDCommand ("select max(T_RATEID) as maxid from DPRCRATE_DBT " +
         "where T_NUMBER = ? and T_SHORTNAME = ? and T_NAME = ? and T_OPENDATE = ?");
   cmd2.addParam( "", RSDBP_IN, Number );
   cmd2.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd2.addParam( "", RSDBP_IN, string(rs.AutoKey) );
   cmd2.addParam( "", RSDBP_IN, date(rs.BeginDate) - 1 );
   cmd2.execute();
   rs2 = TRsbDataSet(cmd2);
   if (rs2.movenext())
      RateID = rs2.maxid;   
   end; 
    
   cmd3 = RSDCommand ("update DPRCCONTRACT_DBT set T_RATEID = ? " +
         "where T_CONTRACTID = ?");
   cmd3.addParam( "", RSDBP_IN, RateID );
   cmd3.addParam( "", RSDBP_IN, ContractID );
   cmd3.execute(); 

   // создаем параметры для новой ставки
   CreateRateParams(ContractID, rs.FIID, RateID, rs.RateType, rs.AutoKey, FirstRateDate, rs.EndDate, PrcRateType);

END;

//---------------------------------------------------------------------
//  Блок функций по генерации графиков для договора
//---------------------------------------------------------------------

// Генерация графика
/*               
PRIVATE MACRO GenerateSchedule(ContractID, ScheduleID)
   var cmd,rs,       
       stat = 0;

   cmd = RSDCommand( "{ call RSB_PERCENT.Prc_GenerateCalendarGraph(?,?) }" );
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, ScheduleID );
   cmd.execute();

   return 0;

   OnError( RslErrObj )
      return 1;

END;
*/

// Запись сумм в график по договору
PRIVATE MACRO WriteSumToCntSched(kind,ContractID, DataSet)

   var cmd, rs, cmd2, PayState = 0;

   if (kind == PRC_SCHEDKIND_CALC)     //  График расчета
      
      cmd = RSDCommand( "select s.T_CNTSCHEDID, ah.T_ADDSUM, ah.T_ADDDATE " +
            "from DPCADDHIS_DBT ah, DPRCCNTSCHED_DBT s " +
            "where ah.T_AUTOPERC = ? and s.T_CONTRACTID = ? and s.T_SCHEDULEKIND = ? " +
            "and s.T_PERIODBEGINDATE = ah.T_BEGINDATE and s.T_PERIODENDDATE = ah.T_ENDDATE");
      cmd.addParam( "", RSDBP_IN, DataSet.AutoKey );
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, kind );
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      while (rs.movenext())
         cmd2 = RSDCommand( "update  DPRCCNTSCHED_DBT set T_SUMCALC = ?, T_DATEREAL = ? " +                             
                             "where T_CNTSCHEDID = ? ");
         cmd2.addParam( "", RSDBP_IN, rs.AddSum );
         cmd2.addParam( "", RSDBP_IN, rs.AddDate );
         cmd2.addParam( "", RSDBP_IN, rs.CntSchedID );
         cmd2.execute();
      end; // end while
   end;

   if (kind == PRC_SCHEDKIND_CHARGE)   //   График начисления
      
      cmd = RSDCommand( "select s.T_CNTSCHEDID, ah.T_ADDSUM, ah.T_ADDDATE " +
            "from DPCADDHIS_DBT ah, DPRCCNTSCHED_DBT s " +
            "where ah.T_AUTOPERC = ? and s.T_CONTRACTID = ? and s.T_SCHEDULEKIND = ? " +
            "and s.T_PERIODBEGINDATE = ah.T_BEGINDATE and s.T_PERIODENDDATE = ah.T_ENDDATE " +
            "and ah.T_OPRID > 0");
      cmd.addParam( "", RSDBP_IN, DataSet.AutoKey );
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, kind );
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      while (rs.movenext())
         cmd2 = RSDCommand( "update  DPRCCNTSCHED_DBT set T_SUMCALC = ?, T_DATEREAL = ?, " +
                             "T_SUMFACT = ? " +
                             "where T_CNTSCHEDID = ? ");
         cmd2.addParam( "", RSDBP_IN, rs.AddSum );
         cmd2.addParam( "", RSDBP_IN, rs.AddDate );
         cmd2.addParam( "", RSDBP_IN, rs.AddSum );
         cmd2.addParam( "", RSDBP_IN, rs.CntSchedID );
         cmd2.execute();
      end; // end while
   end;

   if (kind == PRC_SCHEDKIND_PAY)        //   График оплат

      cmd = RSDCommand( "select s.T_CNTSCHEDID, pg.T_CALCSUM, pg.T_PAYDATE, pg.T_PAYSUM " +
            "from DPCPAYGRH_DBT pg, DPRCCNTSCHED_DBT s " +
            "where pg.T_AUTOPERC = ? and s.T_CONTRACTID = ? and s.T_SCHEDULEKIND = ? " +
            "and s.T_PERIODBEGINDATE = pg.T_BEGINDATE and s.T_PERIODENDDATE = pg.T_ENDDATE");
      cmd.addParam( "", RSDBP_IN, DataSet.AutoKey );
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, kind );
      cmd.execute();
      
      rs = TRsbDataSet(cmd);
      while (rs.movenext())
         
         if ((rs.CalcSum > 0) and (rs.PaySum == 0))
            PayState = PRC_PAYSTATUS_NOT_CLAIMED;
         elif ((rs.CalcSum > 0) and (rs.PaySum == rs.CalcSum))
            PayState = PRC_PAYSTATUS_PAID_MANUAL;
         elif ((rs.CalcSum > 0) and (rs.PaySum < rs.CalcSum))
            PayState = PRC_PAYSTATUS_PARTLY_PAID_MANUAL;
         elif (rs.CalcSum == 0)
            PayState = PRC_PAYSTATUS_NOT_CALCULATED;
         else
            PayState = -1;
         end;

         if (PayState != -1)
            cmd2 = RSDCommand( "update  DPRCCNTSCHED_DBT set T_SUMCALC = ?, T_DATEREAL = ?, " +
                                "T_SUMFACT = ?, T_PAYSTATE = ? where T_CNTSCHEDID = ? ");
            cmd2.addParam( "", RSDBP_IN, rs.CalcSum );
            cmd2.addParam( "", RSDBP_IN, rs.PayDate );
            cmd2.addParam( "", RSDBP_IN, rs.PaySum );
            cmd2.addParam( "", RSDBP_IN, PayState );
            cmd2.addParam( "", RSDBP_IN, rs.CntSchedID );
            cmd2.execute();
         end;
      end; // end while
   
   end;     

   return 0;
END;


PRIVATE MACRO InsertPrcCntSchedRec(ContractID, Kind, rs2 )
   ClearRecord(PrcCntSchedFile);

   PrcCntSchedFile.CntSchedID        = 0 ;
   PrcCntSchedFile.ContractID        = ContractID;
   PrcCntSchedFile.ScheduleKind      = Kind   ;
   PrcCntSchedFile.SpecialType       = 0   ;
   PrcCntSchedFile.Date              = date(rs2.PayDate)   ;
   PrcCntSchedFile.PeriodBeginDate   = date(rs2.BeginDate)   ;
   PrcCntSchedFile.PeriodEndDate     = date(rs2.EndDate)   ;
   PrcCntSchedFile.DelayDate         = date(rs2.PayDate)   ;
   PrcCntSchedFile.SumCalc           = rs2.Sum   ;
   PrcCntSchedFile.DateReal          = date(rs2.PayDate)   ;
   PrcCntSchedFile.SumFact           = rs2.Sum   ;
   if(PrcCntSchedFile.ScheduleKind == 3)
     PrcCntSchedFile.PayState = PRC_PAYSTATUS_PAID;
   end;
   PrcCntSchedFile.RoundSumDelta     = $0;

   Insert( PrcCntSchedFile );
END;

PRIVATE MACRO GeneratePrcCntSchedNotDaily(ContractID, ScheduleID, ContractEndDate, DataSet)
   VAR cmd2,rs2;
   VAR stat = 0;
   VAR PeriodEndDate = date(0,0,0);

   cmd2 = RSDCommand( "SELECT * FROM dpccalc_dbt " +
                      " WHERE t_AutoPerc = ?  " +
                      " AND t_PayDate <> to_date('01.01.0001', 'DD.MM.YYYY')");
   cmd2.addParam( "", RSDBP_IN, DataSet.AutoKey );
   cmd2.execute();
   
   rs2 = TRsbDataSet(cmd2);
   while (rs2.movenext())
      InsertPrcCntSchedRec(ContractID, PRC_SCHEDKIND_CALC  , rs2 );
      InsertPrcCntSchedRec(ContractID, PRC_SCHEDKIND_CHARGE, rs2 );
      InsertPrcCntSchedRec(ContractID, PRC_SCHEDKIND_PAY   , rs2 );

      if(date(rs2.EndDate) > PeriodEndDate)
        PeriodEndDate = date(rs2.EndDate);
      end;
   end; // end while
                                        
   if(PeriodEndDate != date(0,0,0))
     Prc_ReGenerateCalendarGraphEndDate(ContractID, ScheduleID, PeriodEndDate, ContractEndDate);
   end;

   return stat;
END;

// Генерация графиков
PRIVATE MACRO GenerateSchedules(ContractID, DataSet)
   var CalcScheduleID, ChargeScheduleID, PayScheduleID, ContractEndDate,
       cmd,rs,
       stat = 0;

   cmd = RSDCommand( "select c.T_CALCSCHEDULEID, c.T_CHARGESCHEDULEID, c.T_PAYSCHEDULEID, t.T_ENDDATE " +
         "from DPRCCALC_DBT c, DPRCCONTRACT_DBT t " +
         "where t.T_CALCID = c.T_CALCID and t.T_CONTRACTID = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CalcScheduleID = rs.CalcScheduleID;
      ChargeScheduleID = rs.ChargeScheduleID;
      PayScheduleID = rs.PayScheduleID;
      ContractEndDate = date(rs.EndDate);
   else
      return 1;
   end; 

                                                
   if (DataSet.GraphType == PC_SCHEDTYPE_DAILY)
      if (Prc_GenerateCalendarGraph(ContractID, CalcScheduleID) != 0)
         PrintReportLine("Ошибка генерации графика расчета для договора ID = " + ContractID); 
      end;
      if (Prc_GenerateCalendarGraph(ContractID, ChargeScheduleID) != 0)
         PrintReportLine("Ошибка генерации графика начисления для договора ID = " + ContractID); 
      end;          
      if (Prc_GenerateCalendarGraph(ContractID, PayScheduleID) != 0)
         PrintReportLine("Ошибка генерации графика оплаты для договора ID = " + ContractID); 
      end;

      WriteSumToCntSched(PRC_SCHEDKIND_CALC,ContractID, DataSet);
      WriteSumToCntSched(PRC_SCHEDKIND_CHARGE,ContractID, DataSet);
      WriteSumToCntSched(PRC_SCHEDKIND_PAY,ContractID, DataSet);
   else 
      GeneratePrcCntSchedNotDaily(ContractID, CalcScheduleID, ContractEndDate, DataSet);
      GeneratePrcCntSchedNotDaily(ContractID, ChargeScheduleID, ContractEndDate, DataSet);
      GeneratePrcCntSchedNotDaily(ContractID, PayScheduleID, ContractEndDate, DataSet);
   end;

   return 0;

   OnError( RslErrObj ) 
  return 1;

END;
   
//---------------------------------------------------------------------
//  Основная функция
//---------------------------------------------------------------------

MACRO ExecuteConversion()
   var cmd,rs,
       ContractID,
       stat = 0;

   InitReportFile();
   CreateDataSet(@cmd);

   rs = TRsbDataSet(cmd);
   while (rs.movenext())
      if (CreateNewContract(rs, @ContractID) == 0)
         if (InitAddParams(ContractID, rs.RestKind) == 0)
            InitContractAccounts(ContractID, rs);         
         end;
         CreateContractCalc(ContractID, rs);
         CreateContractRate(ContractID, rs);
         GenerateSchedules(ContractID, rs);
         PrintReportLine("  ");
      end;
   end;

   EndReport();
   return stat;
END;


// Этот блок для отладки.
var stat = 0;
ExecuteConversion();
END; 

