import cb_sql;

const PT_ZERODATE = date( 0, 0, 0 );
const PT_NONE = 0, PT_CREATE = 1, PT_MODIFY = 2, PT_REMOVE = 3;

/* Аналог оператора ?: в си. */
macro PT_IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

/*
 Копирование значений полей текущей записи Recordset'а по таблице
 в свойства объекта TRecHandler по одноименной таблице.
 Требование к полям Recordset'а:
   - имена полей Recordset'а должны совпадать с именами полей TRecHandler
     и иметь префикс "t_": значение поля Recordset'а с именем t_partyid
     в TRecHandler будет скопировано в поле partyid
*/
macro _CopyRecordsetToRecHandler(rs, rh)
  var ifld = 0;
  var nfld = FldNumber(rh);
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    val = rs.value("t_" + FldName(rh, ifld));
    if(ValType(GenGetProp(rh.rec, FldName(rh, ifld))) == V_STRING)
      GenSetProp(rh.rec, FldName(rh, ifld), SQL_ConvTypeStr(val));
    elif(val != null)
      GenSetProp(rh.rec, FldName(rh, ifld), rs.value("t_" + FldName(rh, ifld), null, ValType(GenGetProp(rh.rec, FldName(rh, ifld)))));
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;

/*
 Копирование значений полей текущей записи Recordset'а по таблице
 в одноименные свойства объекта пользовательского класса
 Требование к полям Recordset'а:
   - имена полей Recordset'а должны совпадать с именами свойств класса
     и иметь префикс "t_": значение поля Recordset'а с именем t_partyid
     в классе будет скопировано в поле partyid
*/
macro _CopyRecordsetToClassObj(rs, cls)
  var iprop = 0;
  var ifld = 0;
  var nfld = rs.fldCount;
  var val;
  var savedNullConv;

  if(IsEqClass("RsdRecordset", rs))
    savedNullConv = rs.Command.NullConversion;
    rs.Command.NullConversion = true;
  end;

  while(ifld < nfld)
    iprop = GenPropID(cls, substr(rs.fld(ifld).name, 3));
    if(iprop != -1)
      val = rs.value(ifld);
      if(ValType(cls(iprop)) == V_STRING)
        cls(iprop) = SQL_ConvTypeStr(val);
      elif(val != null)
        cls(iprop) = rs.value(ifld, null, ValType(cls(iprop)));
      end;
    end;
    ifld = ifld + 1;
  end;

  if(IsEqClass("RsdRecordset", rs))
    rs.Command.NullConversion = savedNullConv;
  end;
end;

macro PT_PrintDebug(FileName, Str)

  var fullOutFileName:String = "";
  FILE outFile() txt append;

  if( Open( outFile, FileName ) == true )
      insert(outFile, Str);
      Close( outFile );
  end;

end;
