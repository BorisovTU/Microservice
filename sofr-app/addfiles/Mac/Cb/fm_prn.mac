/*
   "Финансовый мониторинг"
   Макрос печати сообщения
*/
import FMInter, BankInter, FIInter, CTInter, rsd, rsexts, PTInter, "globals.mac", rslx, rcw;

/* приложение и документ*/
var WordApp, WordDoc;
var TablesInDocument = 0; /*реальное количество таблиц в формируемом документе*/
var TablesInDot = 9;      /*количество таблиц в шаблоне*/

/* шаблон сообщения */
const DotFileName = "fm_prn.dot";

/* путь к файлу сообщения на терминале */
const TermFilePath = "";

PRIVATE MACRO GetDotFileName(TemplName)
   var RegKey  :string  = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\MACRODIR"; // Ключ в реестре с путем к шаблонам
   var DirName_Template = "";
   var TempStr          = "";

   // Найдем месторасположение шаблонов на сервере, они лежат в одном из путей прописанных в 
   // "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR"
   GetRegistryValue( RegKey, V_STRING, DirName_Template, "" );

   if( NOT DirName_Template )
      MsgBox( "Ошибка!|Не задан путь до папки с шаблонами." );
      return "";
   end;

   // Определим месторасположение каждого шаблона из списка
   TempStr = FindPath(toANSI(TemplName), DirName_Template);
   if( NOT TempStr )
      MsgBox( "В путях <",DirName_Template, ">|не найден шаблон|<",TemplName,">!|",
               "Поместите шаблон в указанный путь и повторите операцию." );
      return "";
   end;
/*
   // Если в строке пути есть знак ":" или "\\", то это абсолютный путь
   if( Index(toAnsi(TempStr), ":") OR Index(toAnsi(TempStr), "\\\\") )
       ;
   else
       // Добавим текущий путь
       TempStr = GetCurDir(false)+"\\"+ TempStr;
   end;
*/

   return TempStr;
END;

/* Открыть документ по шаблону. Вернет открытый документ */
private macro CreateWordApplication()
   var startAX;  
   var WordApplication;
      if (isStandAlone())
         WordApplication = ActiveX("Word.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "FmAxServer", isStandalone());
         end;
         WordApplication = startAX.CreateComObject("Word.Application");
      end;

  return WordApplication;
end;

private macro _rpad( src : string, maxlen : integer ) : string

  var len : integer;

  len = StrLen( src );

  while( len < maxlen )

    src = src + " ";

    len = len + 1;

  end;

  return src;

end;

/* получить коды операции */
private macro GetCodes( opcontr, CodeOC : @string, CodeUO : @string )
  
  var ArrayCodeOC : TArray;
  var ArrayCodeUO : TArray;

  macro FormCodeString( ArrayCode )
    var i, N;
    var CodeString = "";
    N = ArrayCode.Size() ;
    if( N )
      CodeString = ArrayCode(0);
      i = 1;
      while( i < N )
        CodeString = CodeString + "," + ArrayCode(i);
        i = i + 1;
      end;
    end;
    return CodeString;
  end;
  
  CodeOC = ""; CodeUO = "";
  if( ПолучитьКодыОперации( opcontr, ArrayCodeOC, ArrayCodeUO ) )
    CodeOC = FormCodeString( ArrayCodeOC );
    CodeUO = FormCodeString( ArrayCodeUO );
  end;

end;

/* получить дополнительную информацию по операции */
private macro GetAddInfo( opcontr ) : string

  return readNoteForObject( OBJTYPE_OPCONTR, MakeOpContrID(opcontr.OperationID), NOTEKIND_DESCR );

end;

/* получить код и наименование валюты операции */
private macro GetCodeNameCurrency( Code_Currency : integer )
  
  var Code : string, Name : string;

  Code = ""; Name = "";

  Code = ПолучитьКодФинИн( Code_Currency );
  Name = ПолучитьИмяФинИн( Code_Currency );
  
  return Code + " " + Name;

end;

/* сформировать строку, кем и когда был выдан документ, удостоверяющий личность */
private macro GetPaperIssuer( opcntrpt ) : string

  var ResultString : string;

  ResultString = opcntrpt.PaperIssuer;

  if( opcntrpt.PaperIssuedDate != date(0, 0, 0) )

    ResultString = ResultString + " ";
    ResultString = ResultString + string(opcntrpt.PaperIssuedDate:f);

  end;

  return ResultString;

end;

/* получить данные из справочника сотрудников для пользователя */
private macro GetPersonOfficeData( BankID : integer, PersonID : integer, PersonPost : @string, PersonOffice : @string )

  var OfficeID : integer;
  var rs : RsdRecordset;

  file ptoffice("ptoffice.dbt");

  rs = RsdRecordset( "SELECT t_Post, t_OfficeID " +
                     "FROM dofficer_dbt " +
                     "WHERE t_PartyID = " + BankID +
                     " AND t_PersonID = " + PersonID +
                     " ORDER BY t_AutoInc"
                   );

  if( rs.moveNext() )
  
    PersonPost = rs.value(0);
    if( PersonPost == StrFor(1) ) PersonPost = ""; end;
    
    OfficeID = rs.value(1);
    
    if( OfficeID )
      
      ClearRecord(ptoffice);
      ptoffice.PartyID  = BankID;
      ptoffice.OfficeID = OfficeID;
      if( GetEQ(ptoffice) )
        PersonOffice = ptoffice.OfficeName;
      end;

    end;

  end;

end;

/* получить параметры пользователя */
private macro GetPersonData( Oper : integer, PersonName : @string, PersonPost : @string, PersonOffice : @string )

  file person("person.dbt");
  file dp_dep("dp_dep.dbt");

  PersonName   = "";
  PersonPost   = "";
  PersonOffice = "";

  person.Oper = Oper;

  if( GetEQ(person) )

    PersonName = person.Name;

    if( (person.PartyID != 0) and (person.CodeDepart != 0) )
      
      dp_dep.Code = person.CodeDepart;
      if( GetEQ(dp_dep) )
        if( dp_dep.PartyID )
          GetPersonOfficeData( dp_dep.PartyID, person.PartyID, @PersonPost, @PersonOffice );
        end;
      end;

    end;

  end;

end;

private macro PrintBookmark( WordDoc, BmName : string, BmText : string )
    WordDoc.Bookmarks( BmName ).Range.Text = BmText;
    // удалим закладку, чтобы при дописывании в конец 
    // чистого документа по шаблону закладки не задваивались 
    WordDoc.Bookmarks( BmName ).Delete;
end;

private macro ПолучитьСчетУчастника(opcntrpt)
  var Account = "";
  var bic ="", swift ="", inn = "";
  var error = 0; 
  record Party(party);
  file bankdprt( bankdprt ) key 0;

  Account = opcntrpt.Account;

  if( (opcntrpt.BankName != "") and (opcntrpt.BankId > 0) )

    bic = ПолучитьКодСубъекта( opcntrpt.BankId, PTCK_BIC, error );
    if( error == 0 )
  
//    <Номер счета участника операции> 
//    в <Наименование банка>, БИК: <БИК банка> 
//    корр. счет: <Корр. счет банка>
//    ИНН: <ИНН банка>.
        
      Account = Account + " в " + opcntrpt.BankName + ", БИК: " + bic; 

      ClearRecord(bankdprt);
      bankdprt.PartyID = opcntrpt.BankId;

      if( getEQ(bankdprt) )
        Account = Account + " корр. счет: " + bankdprt.CorAcc;
      end;

      inn = ПолучитьКодСубъекта(opcntrpt.BankId, PTCK_INN, error);
      if(error == 0)
        Account = Account + " ИНН: " + inn;
      end;
    
    else
      
      swift = ПолучитьКодСубъекта(opcntrpt.BankId, PTCK_SWIFT, error);
      
      if (error == 0)
//    <Номер счета участника операции> 
//     в <Наименование банка>, SWIFT: <BIC ISO>.
         Account = Account + " в " + opcntrpt.BankName + ", SWIFT: " + swift ; 
      end; 

    end;

  end;

  return Account;
end;

private macro ПолучитьАдресУчастника(opcntrpt, type)
  var Address       = "";
  var AddrCountry   = "";
  var AddrOKATO     = "";
  var AddrRegion    = "";
  var AddrPlaceName = "";
  var AddrStreet    = "";
  var AddrHouse     = "";
  var AddrBuilding  = "";
  var AddrOffice    = "";

  var error = 0; 
  file country(country) key 3;
  file objattr(objattr) key 2;

/*
1.Значение формируется из полей "Адрес места регистрации" сведений об участнике операции: 
  Наименование страны + 
  наименование региона, код ОКАТО которого указан в сведениях об участнике + 
  Наименование района + 
  наименование населенного пункта + 
  наименование улицы + 
  номер дома + 
  номер корпуса + 
  номер квартиры.

2.Регион указывается с учетом наименования административно-территориальной единицы 
(например, Алтайский край, Республика Адыгея и пр.). 

3.Отдельные части адреса (т.е. название страны, региона, города и пр.) 
разделяются символами ", ". Если какая-либо часть адреса не заполнена, 
то разделитель в виде символов ", " после отсутствующей части не указывается.
*/

  if(type == 1)
    AddrCountry   = opcntrpt.szCountryR      ;
    AddrOKATO     = opcntrpt.RegAddrOKATO    ;
    AddrRegion    = opcntrpt.RegAddrRegion   ;
    AddrPlaceName = opcntrpt.RegAddrPlaceName;
    AddrStreet    = opcntrpt.RegAddrStreet   ;
    AddrHouse     = opcntrpt.RegAddrHouse    ;
    AddrBuilding  = opcntrpt.RegAddrBuilding ;
    AddrOffice    = opcntrpt.RegAddrOffice   ;
  else
    AddrCountry   = opcntrpt.szCountryP       ;
    AddrOKATO     = opcntrpt.FactAddrOKATO    ;
    AddrRegion    = opcntrpt.FactAddrRegion   ;
    AddrPlaceName = opcntrpt.FactAddrPlaceName;
    AddrStreet    = opcntrpt.FactAddrStreet   ;
    AddrHouse     = opcntrpt.FactAddrHouse    ;
    AddrBuilding  = opcntrpt.FactAddrBuilding ;
    AddrOffice    = opcntrpt.FactAddrOffice   ;
  end;
  
  if(AddrCountry != "")
    ClearRecord(country);
    country.codenum3 = AddrCountry;

    if((getEQ(country)) and (country.FullName != ""))
      if(Address != "") Address = Address + ", "; end;
      Address = Address + country.FullName;
    end;
  end;

  if(AddrOKATO != "")
    ClearRecord(objattr);
    objattr.ObjectType = 3;
    objattr.GroupID    = 12;
    objattr.NumInList  = AddrOKATO;

    if((getEQ(objattr)) and (objattr.FullName != ""))
      if(Address != "") Address = Address + ", "; end;
      Address = Address + objattr.FullName;
    elif((getEQ(objattr)) and (substr(objattr.NumInList, 1, 2) == AddrOKATO) and (objattr.FullName != ""))
      if(Address != "") Address = Address + ", "; end;
      Address = Address + objattr.FullName;
    end;
  end;

  if( AddrRegion != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrRegion;
  end;

  if( AddrPlaceName != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrPlaceName;
  end;

  if( AddrStreet != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrStreet;
  end;

  if( AddrHouse != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrHouse;
  end;

  if( AddrBuilding != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrBuilding;
  end;

  if( AddrOffice != "")
    if(Address != "") Address = Address + ", "; end;
    Address = Address + AddrOffice;
  end;

  return Address;
end;

private macro ПолучитьДокументУчастника(opcntrpt)
  var PaperName = "";
  var error = 0; 
  file fmpaper(fmpaper) key 0;

  if(opcntrpt.CodeDocum != "")
    ClearRecord(fmpaper);
    fmpaper.Code = opcntrpt.CodeDocum;
    if(getEQ(fmpaper))
      PaperName = fmpaper.Name;
    end;
  end;

  return PaperName;
end;

/* напечатать данные об участнике операции */
private macro PrintOprParty( opcntrpt, WordDoc, Prefix : string )
  var Account  = "";
  var RegAddr  = "";
  var StayAddr = "";
  var PayEmployer = " ";
  var PaperName = "";

  var TypeNoneSign = "";

  Account  = ПолучитьСчетУчастника(opcntrpt);     
  RegAddr  = ПолучитьАдресУчастника(opcntrpt, 1); //Адрес регистрации
  StayAddr = ПолучитьАдресУчастника(opcntrpt, 2); //Адрес местонахождения

  if( (opcntrpt.Type == "Ф") or (opcntrpt.Type == "П") )

    if(opcntrpt.Type == "П")
       PayEmployer = "X";
    else
       PayEmployer = " ";
    end;

    PaperName = ПолучитьДокументУчастника(opcntrpt);

    PrintBookmark( WordDoc, Prefix + "PersnName"       , opcntrpt.Name                   );
    PrintBookmark( WordDoc, Prefix + "Employer"        , PayEmployer                     );
    PrintBookmark( WordDoc, Prefix + "PersnAddress"    , RegAddr                         );
    PrintBookmark( WordDoc, Prefix + "PersnStayAddress", StayAddr                        );
    PrintBookmark( WordDoc, Prefix + "PaperName"       , PaperName                       );
    PrintBookmark( WordDoc, Prefix + "PaperSeries"     , _rpad(opcntrpt.PaperSeries, 15) );
    PrintBookmark( WordDoc, Prefix + "PaperNumber"     , _rpad(opcntrpt.PaperNumber, 25) );
    PrintBookmark( WordDoc, Prefix + "PaperIssuer"     , GetPaperIssuer(opcntrpt)        );
    PrintBookmark( WordDoc, Prefix + "PaperRegNumber"  , opcntrpt.RegNumber              );
    PrintBookmark( WordDoc, Prefix + "PaperRegDate"    , string(opcntrpt.RegDate:f)      );
    PrintBookmark( WordDoc, Prefix + "PersnINN"        , _rpad(opcntrpt.INN, 12)         );
    PrintBookmark( WordDoc, Prefix + "Birthday"        , string(opcntrpt.Birthday:f)     );
    PrintBookmark( WordDoc, Prefix + "PersnAccount"    , Account                         );

    PrintBookmark( WordDoc, Prefix + "Name"       , "" );
    PrintBookmark( WordDoc, Prefix + "Address"    , "" );
    PrintBookmark( WordDoc, Prefix + "StayAddress", "" );
    PrintBookmark( WordDoc, Prefix + "OKPO"       , "" );
    PrintBookmark( WordDoc, Prefix + "RegNumber"  , "" );
    PrintBookmark( WordDoc, Prefix + "RegDate"    , "" );
    PrintBookmark( WordDoc, Prefix + "INN"        , "" );
    PrintBookmark( WordDoc, Prefix + "Account"    , "" );

  else
  
    if( (opcntrpt.Type != "") and (opcntrpt.Type != "Ю") )
      TypeNoneSign = "(тип участника операции не установлен)";
    end;
    
    PrintBookmark( WordDoc, Prefix + "Name"       , opcntrpt.Name              );
    PrintBookmark( WordDoc, Prefix + "Address"    , RegAddr                    );
    PrintBookmark( WordDoc, Prefix + "StayAddress", StayAddr                   );
    PrintBookmark( WordDoc, Prefix + "OKPO"       , opcntrpt.OKPO              );
    PrintBookmark( WordDoc, Prefix + "RegNumber"  , opcntrpt.RegNumber         );
    PrintBookmark( WordDoc, Prefix + "RegDate"    , string(opcntrpt.RegDate:f) );
    PrintBookmark( WordDoc, Prefix + "INN"        , opcntrpt.INN               );
    PrintBookmark( WordDoc, Prefix + "Account"    , Account                    );

    PrintBookmark( WordDoc, Prefix + "PersnName"       , "" );
    PrintBookmark( WordDoc, Prefix + "Employer"        , " " );
    PrintBookmark( WordDoc, Prefix + "PersnAddress"    , "" );
    PrintBookmark( WordDoc, Prefix + "PersnStayAddress", "" );
    PrintBookmark( WordDoc, Prefix + "PaperName"       , "" );
    PrintBookmark( WordDoc, Prefix + "PaperSeries"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperNumber"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperIssuer"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperRegNumber"  , "" );
    PrintBookmark( WordDoc, Prefix + "PaperRegDate"    , "" );
    PrintBookmark( WordDoc, Prefix + "PersnINN"        , "" );
    PrintBookmark( WordDoc, Prefix + "Birthday"        , "" );
    PrintBookmark( WordDoc, Prefix + "PersnAccount"    , "" );

  end;

  PrintBookmark( WordDoc, Prefix + "TypeNoneSign", TypeNoneSign );

end;


private macro PrintEmptyOprParty(WordDoc, Prefix : string )

    PrintBookmark( WordDoc, Prefix + "PersnName"       , "" );
    PrintBookmark( WordDoc, Prefix + "Employer"        , "" );
    PrintBookmark( WordDoc, Prefix + "PersnAddress"    , "" );
    PrintBookmark( WordDoc, Prefix + "PersnStayAddress", "" );
    PrintBookmark( WordDoc, Prefix + "PaperName"       , "" );
    PrintBookmark( WordDoc, Prefix + "PaperSeries"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperNumber"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperIssuer"     , "" );
    PrintBookmark( WordDoc, Prefix + "PaperRegNumber"  , "" );
    PrintBookmark( WordDoc, Prefix + "PaperRegDate"    , "" );
    PrintBookmark( WordDoc, Prefix + "PersnINN"        , "" );
    PrintBookmark( WordDoc, Prefix + "Birthday"        , "" );
    PrintBookmark( WordDoc, Prefix + "PersnAccount"    , "" );
                                                         
    PrintBookmark( WordDoc, Prefix + "Name"       , "" );
    PrintBookmark( WordDoc, Prefix + "Address"    , "" );
    PrintBookmark( WordDoc, Prefix + "StayAddress", "" );
    PrintBookmark( WordDoc, Prefix + "OKPO"       , "" );
    PrintBookmark( WordDoc, Prefix + "RegNumber"  , "" );
    PrintBookmark( WordDoc, Prefix + "RegDate"    , "" );
    PrintBookmark( WordDoc, Prefix + "INN"        , "" );
    PrintBookmark( WordDoc, Prefix + "Account"    , "" );

    PrintBookmark( WordDoc, Prefix + "TypeNoneSign", "");
end;

/*
 *  Печать сообщения
 */
macro СоздатьСообщение( )
  
  var FullDotFileName : string;
  var wdPageBreak = 7;
  var wdSectionBreakNextPage = 2;
  var wdStory = 6;
  var addFileName = "";

  if (isStandAlone())
    addFileName = GetDotFileName(DotFileName);
    FullDotFileName = MergeFile( GetCurDir(), addFileName );
  else
    FullDotFileName = MergeFile( GetCurDir(true), TermFilePath + "Dotfile\\fm_prn.dot" );
  end;

  /*
  если приложение и документ уже открыты, значит идет массовая печать
  и нужно добавить новый шаблон в конец уже существующего документа,
  иначе создать заново
  */
  if( WordApp and WordDoc )
    WordApp.Selection.EndKey( wdStory );
    WordApp.Selection.InsertBreak( wdSectionBreakNextPage );
    WordApp.Selection.InsertFile( FullDotFileName );
  else
    WordApp = CreateWordApplication();
    if( WordApp )
      /* получаем полное имя шаблон */
      WordDoc = WordApp.Documents.Add( FullDotFileName );
    end;
  end;
end;

macro ПечататьСообщение( _opcontr )

  var CodeOC : string, CodeUO : string;
  var nDeleteTable : integer;
  var PersonName   : string;
  var PersonPost   : string;
  var PersonOffice : string;
  var hh : integer;
  var mm : integer;
  var dd     : integer;
  var mounth : integer;
  var year   : integer;

  record opcontr ("opcontr.dbt" );
  record opcntrpt("opcntrpt.dbt");

  SetBuff( opcontr, _opcontr );
  if( WordApp and WordDoc )
    nDeleteTable = 0;

    if( opcontr.Numb_P == 0 )
      PrintBookmark( WordDoc, "NumbP", "" );
    else
      PrintBookmark( WordDoc, "NumbP", opcontr.Numb_P );
    end;

    if( opcontr.Date_P == date(0, 0, 0) )
      PrintBookmark( WordDoc, "DateP", "" );
    else
      PrintBookmark( WordDoc, "DateP", string(opcontr.Date_P:f) );
    end;

    GetCodes( opcontr, @CodeOC, @CodeUO );

    WordDoc.Bookmarks("CodeOC").Range.Text = CodeOC;

    if( CodeUO != "" )
      WordDoc.Bookmarks("CodeUO").Range.Text = CodeUO;
      WordDoc.Bookmarks("CodeUO_Name").Range.Text = WordDoc.Bookmarks("CodeUO_Name").Range.Text;
      WordDoc.Bookmarks("CodeUO_End").Range.Text = WordDoc.Bookmarks("CodeUO_End").Range.Text;
    else
      WordDoc.Bookmarks("CodeUO_End").Range.Delete;
      WordDoc.Bookmarks("CodeUO").Range.Delete;
      WordDoc.Bookmarks("CodeUO_Name").Range.Delete;
    end;

    WordDoc.Bookmarks("Ground"    ).Range.Text = opcontr.Ground;
    WordDoc.Bookmarks("Date_Carry").Range.Text = string(opcontr.Date_Carry:f);
    WordDoc.Bookmarks("FIID"      ).Range.Text = GetCodeNameCurrency(opcontr.Code_Currency);
    WordDoc.Bookmarks("SumRub"    ).Range.Text = _rpad(string(opcontr.SumRub), 25);

    if(opcontr.Code_Currency != 0)
      WordDoc.Bookmarks("Sum"     ).Range.Text = string(opcontr.SumCur);
    else
      WordDoc.Bookmarks("Sum"     ).Range.Delete;
    end;

    WordDoc.Bookmarks("Describe"  ).Range.Text = GetAddInfo(opcontr);
                        
    
    /* Выводим данные об участниках операции */

    /* - плательщик */
    ПолучитьУчастникаОперации( opcntrpt, opcontr, _FM_PARTY_PAYER );
    PrintOprParty( opcntrpt, WordDoc, "Pay" );

    /* - получатель */
    ПолучитьУчастникаОперации( opcntrpt, opcontr, _FM_PARTY_RECEIVER );
    PrintOprParty( opcntrpt, WordDoc, "Rec" );

    /* - по поручению которого совершается операция */
    ПолучитьУчастникаОперации( opcntrpt, opcontr, _FM_PARTY_ORDER );
    PrintOprParty( opcntrpt, WordDoc, "Order" );

    if( ПолучитьУчастникаОперации(opcntrpt, opcontr, _FM_PARTY_PAYER_REPRESENT) )
      WordDoc.Bookmarks("PayReprTable").Range.Text = WordDoc.Bookmarks("PayReprTable").Range.Text;
      PrintOprParty( opcntrpt, WordDoc, "PayRepr" );
    else
      WordDoc.Bookmarks("PayReprTable").Range.Text = "";
//      PrintOprParty( opcntrpt, WordDoc, "PayRepr" );
      WordDoc.Tables(TablesInDocument + 5 - nDeleteTable).Delete;
//      WordDoc.Bookmarks("EndPayReprTable").Range.Text = "";
      
      nDeleteTable = nDeleteTable + 1;
    end;
  
    if( ПолучитьУчастникаОперации(opcntrpt, opcontr, _FM_PARTY_RECEIVER_REPRESENT) )
      WordDoc.Bookmarks("RecReprTable").Range.Text = WordDoc.Bookmarks("RecReprTable").Range.Text;
      PrintOprParty( opcntrpt, WordDoc, "RecRepr" );
    else
      WordDoc.Bookmarks("RecReprTable").Range.Text = "";
//      PrintEmptyOprParty(WordDoc, "RecRepr" );
      WordDoc.Tables(TablesInDocument + 6 - nDeleteTable).Delete;
//      WordDoc.Bookmarks("EndRecReprTable").Range.Text = "";

      nDeleteTable = nDeleteTable + 1;
    end;

    GetPersonData( opcontr.Executer, @PersonName, @PersonPost, @PersonOffice );

    WordDoc.Bookmarks("PersonName"  ).Range.Text = PersonName;
    WordDoc.Bookmarks("PersonPost"  ).Range.Text = PersonPost;
    WordDoc.Bookmarks("PersonOffice").Range.Text = PersonOffice;

    TimeSplit( time, hh, mm );
    WordDoc.Bookmarks("HH"  ).Range.Text = hh;
    WordDoc.Bookmarks("MM"  ).Range.Text = mm;
    
    DateSplit( date, dd, mounth, year );
    WordDoc.Bookmarks("DD"    ).Range.Text = dd;
    WordDoc.Bookmarks("MOUNTH").Range.Text = StrLwr(MonName(mounth));
    WordDoc.Bookmarks("YYYY"  ).Range.Text = year;

    TablesInDocument = TablesInDocument + TablesInDot - nDeleteTable;
  end;
end;

macro ПоказатьСообщение()
  var wdStory = 6;

  if( WordApp and WordDoc )
    WordApp.Selection.HomeKey( wdStory );

    WordApp.Visible = true;
  end;
end;
