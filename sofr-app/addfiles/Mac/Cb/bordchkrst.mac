//-----------------------------------------------------------------------------
// Блок     : 29018 - "Предобработка банковского ордера"
// Шаг      : 10    - "Проверка остатков по счетам"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import pschkrst, cbsttls;

// Варианты ответов StageLORO_Question
var Dlg_Ans_Pay       = " Оплатить ";
var Dlg_Ans_Queue     = " В очередь ";
var Dlg_Ans_Rej       = " Отвергнуть ";

const Dlg_Var_Pay       :integer = 0;
const Dlg_Var_Queue     :integer = 1;
const Dlg_Var_Rej       :integer = 2;

// Этап 1 Определение реальных владельцев счетов
private macro Stage_AccountOwnerIsBank( Payment:RsbPayment, PrmStage:ParmStage ) : bool

  var Account, FIID;
  var pi:TRecHandler = TRecHandler( "pmaddpi.dbt" );

  if( Payment.PIList(PRT_Debet).Size > 0 )
    if( (Payment.PIList(PRT_Debet).First() == 0) and (Payment.PIList(PRT_Debet).Current(pi) == 0) )
      Account = pi.rec.Account;
      FIID = pi.rec.FIID;
    end;
  else
    Account = Payment.FuturePayerAccount;
    FIID = Payment.FutureReceiverFIID;
  end;

  if( PM_IsBankAccount( Account, FIID ) )
    PrmStage.NextStage(БО_Проверка_остатка_на_счете_плательщика_банка);
  end;

  return 0;

end;

// Этап 7 Проверка свободного остатка на счете плательщика
private macro Stage_CheckFreeRestPayerAcc_BO(Payment:RsbPayment, PrmStage:ParmStage):integer
  
  if( ЗарезервированыСредства(Payment) )
    PrmStage.NextStage( БО_Проверка_остатка_на_счете_получателя );
  else
    var stat = CheckRestAndMakeReserve( Payment, true, true, true, true, 
                                        GetOprStatus(OPR_PAYM_PERMISSION), NULL, 
                                        true, NULL, 0, true, true, true );

    if(stat == PAYMERR_NORESERVE_OTHERPROC)
      PrmStage.NextStage( БО_Определение_статусов );
      return 0;
    end;

    return stat;
  end;

  return 0;
end;

// Этап 9 Проверка остатка на счете плательщика (Если владелец счета плательщика - Банк)
private macro Stage_CheckFreeRestPayerAcc_BO_ForBank(Payment:RsbPayment, PrmStage:ParmStage):integer
  
  var stat : integer = 0;
  
  //Проверим возможность дебетования счета и создадим резерв
  stat = CheckRestAndMakeReserve( Payment, true, true, true, true, 
                                  GetOprStatus(OPR_PAYM_PERMISSION), NULL, 
                                  true, null, null, null, true, true );

  if(stat == PAYMERR_NORESERVE_OTHERPROC)
    PrmStage.NextStage( БО_Определение_статусов );
    return 0;
  end;

  return stat;

end;

// Этап 10 Определение статусов
private macro Stage_SetStatus_BO(Payment:RsbPayment, PrmStage:ParmStage):integer
  
  var stat = 0;

  //Контроль
  if( GetOprStatus(OPR_PAYM_CONTROL) == 0 )
    if( Payment.Origin == PAYMENT_OR_AUTO )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL );
    else
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
    end;
  end;

  if( (PrmStage.INDEX_Segment != OPR_PAYM_ST_INDEX_WAIT) and (GetOprStatus(OPR_PAYM_DO) != OPR_PM_ST_PRIORITY) )
    if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  return 0;

end;


MACRO BOrd_CheckAccRest( Payment:RsbPayment, ID_Operation, ID_Step ):integer

  var PrmStage = ParmStage(ID_Operation, ID_Step, БО_Определение_реальных_владельцев_счетов);

  var Stages:TArray = MakeArray( TStage( БО_Определение_реальных_владельцев_счетов,             @Stage_AccountOwnerIsBank    ),
                                 TStage( БО_Проверка_остатка_на_счете_плательщика_клиента,      @Stage_CheckFreeRestPayerAcc_BO ),
                                 TStage( БО_Проверка_остатка_на_счете_получателя,               @Stage_CheckRestReceiverAcc ),
                                 TStage( БО_Направление_в_невыясненные,                         @Stage_GoToInIndexUnknown ),
                                 TStage( БО_Проверка_остатка_на_счете_плательщика_банка,        @Stage_CheckFreeRestPayerAcc_BO_ForBank ),
                                 TStage( БО_Проверка_остатка_на_счете_получателя_2,             @Stage_CheckRestReceiverAcc_ForBank ),
                                 TStage( БО_Определение_статусов,                               @Stage_SetStatus_BO ) );

  return ExecStages(Payment, Stages, PrmStage);

END;
