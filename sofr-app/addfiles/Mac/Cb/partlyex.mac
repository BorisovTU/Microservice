/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : partlyexlib.mac
  Created     : 18.07.2012
  Programmer  : 
  Description : Библиотека для печатной формы "Частичное исполнение" 
└───────────────────────────────────────────────────────────────────────────*/

import "prpm.mac", "bnk_common.mac";

MACRO CreatePartlyExForm():bool
  var partPayList  :TPartPayList;
  var Dockind = Bnk_GetLLValuesName(2030, pr_pmrmprop.rec.ShifrOper);
  var DocNum = pr_pmrmprop.rec.Number;
  var DocDate = pr_pmrmprop.rec.Date ;
  var DocAmount = pr_pmpaym.rec.BaseAmount;
  var sort:string;
  var i:integer;

  partPayList  = TPartPayList(pr_pmpaym.rec.PaymentID);      //Выбираем частичные платежи
  if (partPayList.size == 0)                                 //Если их количество нулевое - ругаемся
    if (IsOprMultiExec() == false)  
    [
     ┌──────────────────────────────────────────────────────────────────────────────────────────────┐
     │                    По документу не производилось частичное исполнение!                       │
     └──────────────────────────────────────────────────────────────────────────────────────────────┘
    ];
    else
      MsgBox("По документу не производилось частичное исполнение!");
    end;
    return false;
  end; 
  
    [
                                                                            Приложение к распоряжению
                                                                            #########################
                                                                            ─────────────────────────               
                                                                      № ############### от ##########
                                                                      ─────────────────    ──────────
                                                                      на сумму     ##################
                                                                                   ──────────────────




      Частичное исполнение:
     ┌────────────┬─────────────────┬──────────────┬────────────────────────┬────────────────────────┐
     │ № частич.  │    № платеж.    │ Дата платеж. │      Сумма частич.     │      Сумма остатка     │
     │  платежа   │      ордера     │    ордера    │         платежа        │         платежа        │
     ├────────────┼─────────────────┼──────────────┼────────────────────────┼────────────────────────┤
    ](Dockind:r,DocNum:r,DocDate:l,DocAmount:r);                            

  i = 0;
  while(i < partPayList.size)
    [│  ##########│ ############### │ ############ │ ###################### │ ###################### │ 
    ]
    ( partPayList.Value(i).SubPurp:l, 
      partPayList.Value(i).Number:l, 
      partPayList.Value(i).PayDate:f:l,
      partPayList.Value(i).Amount:r:f,
      partPayList.Value(i).Rest:r:f );
      i=i+1;
  end;
   
  if( partPayList.size )
    [└────────────┴─────────────────┴──────────────┴────────────────────────┴────────────────────────┘ 
    ];
  end;
 
    [
      ───────────────────────────────────────────────────────────────────────────────────────────────
                                                Подписи                      Отметки банка


                  М.П.                ────────────────────────────

                                      ────────────────────────────
    ];

  return true;
END;                   

private VAR CopyCont = 1;          /* Кол-во копий                         */

MACRO PrintDocument(ncopy: integer):bool

  CopyCont = ncopy;
  while(CopyCont) 
    CreatePartlyExForm();
    CopyCont = CopyCont - 1;
    end;  
  return true;
END;
