/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v6.00.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcchg30.mac

  Библиотека       : RSACCOUNT

  Описание         : Операция "Начисление по ПД". Шаг "Отражать в учете?"

  Программист      : Kirakozov Michael (KMV)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,  CTInter,  globals, mpclb;


record contract_buf (prccontract);
record cntopr_buf (prccntopr);
var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    ContractID = 0;



// Поиск периода оплаты с той же датой окончания
PRIVATE MACRO FindPayPeriodWithEqualEndDate(ContractID, EndDate);;
   var cmd,rs;

   cmd = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
                     "where T_SCHEDULEKIND = 3 AND T_CONTRACTID = ? AND T_PERIODENDDATE = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.execute(); 
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      return true;
   end;

   return false;
END;


MACRO ExecuteCaseStep(OperationKind, StepNumber, Document, DocKind)
   var ЗначениеНастройки,
       flagPayPeriod,
       CntSchedID = 0;

   SetBuff( cntopr_buf, Document );

   ContractID = cntopr_buf.contractid;

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;  
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      //msgbox ("Не найден договор с ID = " + ContractID);
      PrcSendMessage(ContractID, "Не найден договор с ID = " + ContractID);
      return 1;
   end;

   if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CHARGEBEFORE, @ЗначениеНастройки) != 0)
      return 1;
   end;

   CntSchedID = GetCntSchedID(ContractID, 2, cntopr_buf.OperDate);
   if (CntSchedID != 0)
      cntsched.KeyNum = 0;   
      cntsched.rec.CntSchedID = CntSchedID;
      if (not cntsched.GetEQ)
         //msgbox ("Не найден период начисления" );
         PrcSendMessage(ContractID, "Не найден период начисления");
         return 1;
      end;
   else
      //msgbox ("Не найден период начисления" );
      PrcSendMessage(ContractID, "Не найден период начисления");
      return 1;
   end;

   flagPayPeriod = FindPayPeriodWithEqualEndDate(ContractID, cntsched.rec.PeriodEndDate);

   if (cntopr_buf.isCreateDoc == true) 
      if ((prccontract.rec.reskind == PRC_RESKIND_PRIV) and (ЗначениеНастройки == false) and (flagPayPeriod == true))
         return "50";
      else
         return "40";
      end;
   else
      return "50";
   end;

END;


MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */
  exit(1); 

END;

