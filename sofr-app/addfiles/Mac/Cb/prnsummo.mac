/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                           R-Style Software Lab

  Печать сводного мемориального ордера

CREATED :
FILE_NAME :     faprn.mac
MODIFICATIONS:
NOTES:


└───────────────────────────────────────────────────────────────────────────*/

IMPORT "bbprncom.mac";

record d (arhdoc);

file officer     ( officer  ) key 2;
file persn       ( persn    );
file person      ( person   );
file account     ( account  );

var   NumInList = 0, TotalSum = $0;

VAR   NumCopy = КоличествоКопий;

CONST SET_CHAR = "X",
      POINT    = "." ;


/*Шапка*/
MACRO PutHeadline( Номер, Дата, СчетЗадан, Счет )

array НазваниеКазначейства;

strsplit( {Name_Bank}, НазваниеКазначейства, 42 );

var CorAccount;

if(СчетЗадан==CodeFor("X")) CorAccount = "расчетный счет: " + Счет;
else CorAccount = "";
end;

if(strlen({Name_Bank}) > 42 )
[##########################################
 ##########################################
](
  НазваниеКазначейства(0):c,
  НазваниеКазначейства(1):c
 );
else
[##########################################
](
  НазваниеКазначейства(0):c
 );
end;

[──────────────────────────────────────────
 (наименование органа, исполняющего бюджет)


                                                                                            ╓─────────────────────╖
                                                                          Код формы по КФД  ║       0512012       ║
                                                                                            ╙─────────────────────╜

                              МЕМОРИАЛЬНЫЙ ОРДЕР N ############### за #######################
                              ###############################################################

 ╓───────╥─────────────────────────────────────────────────────────────╥────────────────────╥─────────────────────╖
 ║   №   ║              Основание (дата и номер документа              ║    Номер счета     ║                     ║
 ║       ║                   или содержание записи)                    ╟─────────╥──────────╢     Сумма, руб.     ║
 ║  п/п  ║                                                             ║по дебету║по кредиту║                     ║
 ╟───────╫─────────────────────────────────────────────────────────────╫─────────╫──────────╫─────────────────────╢
 ║   1   ║                             2                               ║    3    ║    4     ║          5          ║
]
   ( Номер:r,
     Дата:m:l,
     CorAccount
   );

END;


MACRO PutLine_Doc(doc)

  VAR БалансовыйПоДебету, БалансовыйПоКредиту;

      setbuff( d, doc);

      /*При каждом вызове увеличиваем итоговую сумму*/
      TotalSum = TotalSum + d.Sum;
      /*Счетчик номера строки*/
      NumInList = NumInList + 1;

      account.Chapter = d.Chapter;
      account.Account = d.Account_Payer;
      if( GetEQ(account) ) /*по дебету*/
          БалансовыйПоДебету = account.Balance;
      else
          msgbox("Не найден счет плательщика: " + d.Account_Payer );
          exit(1);
      end;

      account.Account = d.Account_Receiver;
      if( GetEQ(account) ) /*по кредиту*/
          БалансовыйПоКредиту = account.Balance;
       else
          msgbox("Не найден счет получателя: " + d.Account_Receiver);
          exit(1);
      end;


[╟───────╫─────────────────────────────────────────────────────────────╫─────────╫──────────╫─────────────────────╢
 ║ ##### ║ ########################################################### ║  #####  ║  #####   ║ ################### ║
]
   ( NumInList:c,
     d.Ground,
     БалансовыйПоДебету:c,
     БалансовыйПоКредиту:c,
     d.Sum:a:r
   );

END;

/*Печать задника*/
MACRO PutBackdrop()

 VAR   AccountantGeneral, _CurDate;

      /*ФИО главбуха*/
      officer.PartyID = {OurBank};
      officer.IsSecondPerson = SET_CHAR;
      if( GetEQ(officer) )
          persn.PersonID = officer.personID;
          if( GetEQ(persn) )
              AccountantGeneral = persn.Name1 + " " +
                                  SubStr(trim(persn.Name2), 1, 1) + POINT +
                                  SubStr(trim(persn.Name3), 1, 1) + POINT;

          else
              msgbox("Не найден главный бухгалтер");
              exit(1);
          end;
      else
          msgbox("Не найден главный бухгалтер");
          exit(1);
      end;

      /*Имя операциониста*/
      person.Oper = {oper};
      if( GetEQ(person) == FALSE )
          msgbox("Не найден операционист.");
          exit(1);
      end;


[╙───────╨─────────────────────────────────────────────────────────────╨─────────╨──────────╫─────────────────────╢
                                                                                    Итого   ║ ################### ║
                                                                                            ╙─────────────────────╜
  Главный бухгалтер______________  ####################################

  Исполнитель      ______________  ####################################

  #######################

  Приложение на _____ листах
]

   ( TotalSum:a:r,
     AccountantGeneral:l,
     person.Name:l,
     {curdate}:m:l
   );

 TotalSum = $0;
 NumInList = 0;

END;

/*eof*/
