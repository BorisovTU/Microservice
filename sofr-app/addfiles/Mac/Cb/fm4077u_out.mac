/*
 $Name: fm4077u_out.mac
 $Module: Ядро ГКБО 
 $Description: ФМ.4077-У: Формирование сообщения об отказах: выполнение операции 
 */
 /*
  ФМ.4077-У: Формирование сообщения об отказах: выполнение операции 
 */
import "fm4077u_out_lib.mac";
import rsexts;

private file MessageXMLFile() txt write;
private file rjcnclcarryoper("fmrjcnclcarryoper.dbt") key 1 WRITE;
private file rjcncloperdoc("fmrjcncloperdoc.dbt") key 1;
private file rjcnclopercode("fmrjcnclopercode.dbt") key 1;
private file rjsrvptoper("fmrjsrvptoper.dbt") key 1;
private file rjsrvclnt("fmrjsrvclnt.dbt") key 0;
private record fmrjsrvmsg(fmrjsrvmsg); 

private macro ОснованиеОп(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("ОснованиеОп");
    node.addChildEx("КодДок", rjcncloperdoc.DocKind);
    node.addChildEx("НаимДок", GetNameLLValuesFromCode(3565, rjcncloperdoc.DocKind));
    
    if (StrLen(rjcncloperdoc.DocName) > 0)
        node.addChildEx("ИноеНаимДок", rjcncloperdoc.DocName);
    end;
    
    if (rjcncloperdoc.DocDate != ZeroValue(V_DATE))
        node.addChildEx("ДатаДок", FormatDate(rjcncloperdoc.DocDate));
    end;
    
    if (StrLen(rjcncloperdoc.DocNumber) > 0)
        node.addChildEx("НомДок", rjcncloperdoc.DocNumber);
    end;
    node.addChildEx("СодДок", rjcncloperdoc.DocSummary);
end;

private macro СведенияКО(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведенияКО");
    node.addChildEx("БИККО", rjsrvptoper.BIC);
    node.addChildEx("НомерСчета", rjsrvptoper.AccountNumber);
    node.addChildEx("НаимКО", rjsrvptoper.BankName);
end;

private macro Участник(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("Участник");
    
    node.addChildEx("СтатусУчастника", rjsrvptoper.State);

    var CodeClient = Tbfile ("llvalues.dbt");
    CodeClient.keynum = 0;
    CodeClient.rec.List = 3567;
    CodeClient.rec.Element = rjsrvptoper.PtOperKind;
    CodeClient.GetEQ();
    
    node.addChildEx("КодУчастника", CodeClient.rec.Code);
    node.addChildEx("ТипУчастника", rjsrvclnt.ClientType);
    
    var Sign = "1";
    if (rjsrvclnt.IsNotResident == "X")
        Sign = "0";
    end;
    node.addChildEx("ПризнУчастника", Sign);
    
    if (rjsrvclnt.ClientType == RJSRVCLNT_CT_LEGAL)
        СведенияЮЛ(PPrm, node, rjsrvptoper.RjSrvClntID);
    elif ((rjsrvclnt.ClientType == RJSRVCLNT_CT_PERSN) OR
        (rjsrvclnt.ClientType == RJSRVCLNT_CT_INDIVID) OR
        (rjsrvclnt.ClientType == RJSRVCLNT_CT_PERSN_PRA))
        СведенияФЛ(PPrm, rjsrvclnt.ID, rjsrvclnt.ClientType, node);
    elif (rjsrvclnt.ClientType == RJSRVCLNT_CT_FOREIGN_ENT)
        СведИНБОЮЛ(PPrm, node, rjsrvptoper.RjSrvClntID);
    end;
    
    СведенияКО(PPrm, node);
end;

private macro СведОтказ(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведОтказ");
    
    rjcnclcarryoper.RecNumber = FormatRecNumber(rjcnclcarryoper.RecNumber);
    Update(rjcnclcarryoper);
    
    node.addChildEx("НомерЗаписи", rjcnclcarryoper.RecNumber);
    node.addChildEx("ТипЗаписи", rjcnclcarryoper.RecType);
    
    if (rjcnclcarryoper.UsedCBInfo == "X")
        node.addChildEx("ПризнакИнф", "1");
    else
        node.addChildEx("ПризнакИнф", "0");
    end;
    
    var RejectGround = "07";
    if (rjcnclcarryoper.RejectGround == 1)
        RejectGround = "07";
    elif (rjcnclcarryoper.RejectGround == 2)
        RejectGround = "08";
    elif (rjcnclcarryoper.RejectGround == 3)
        RejectGround = "80";
    end;
    
    node.addChildEx("КодОтказа", RejectGround);
    
    var stat;
    if (PPrm.MsgBuf.Version != FMRJSRVMSG_VER_1_1)
        var Has0000 = false;
        var FailureCode = Tbfile ("fmrjfailurecode.dbt");
        FailureCode.keynum = 1;
        FailureCode.rec.RjCnclCarryOperID = rjcnclcarryoper.ID;
        stat = FailureCode.GetGE();
        while (stat)
            if (FailureCode.rec.RjCnclCarryOperID == rjcnclcarryoper.ID)
                node.addChildEx("КодПричинаОтказ", FailureCode.rec.Code);
                
                if (FailureCode.rec.Code == "0000")
                    Has0000 = true;
                end;
            end;
            stat = FailureCode.next();
        end;
        
        if (Has0000)
            node.addChildEx("ИнаяПричинаОтказа", rjcnclcarryoper.OtherReason);
        end;
    end;
    
    node.addChildEx("ДатаОтказа", FormatDate(rjcnclcarryoper.RejectDate));
    node.addChildEx("КодВал", rjcnclcarryoper.CurrencyISONumber);
    node.addChildEx("СумОперации", rjcnclcarryoper.SumCur);
    node.addChildEx("СумРуб", rjcnclcarryoper.SumNat);
    
    ClearRecord(rjcncloperdoc);
    rjcncloperdoc.RjCnclCarryOperID = rjcnclcarryoper.ID;
    stat = GetGE(rjcncloperdoc);
    while(stat AND (rjcncloperdoc.RjCnclCarryOperID == rjcnclcarryoper.ID))
        ОснованиеОп(PPrm, node);
        stat = next(rjcncloperdoc);
    end;
    
    var IsStuff = "0";
    if (rjcnclcarryoper.IsStuff == "X")
        IsStuff = "1";
    end;
    node.addChildEx("КодПризнОперации", IsStuff);
    
    if (rjcnclcarryoper.IsStuff != "X")
        node.addChildEx("КодДенежСредств", rjcnclcarryoper.MoneyKind);
    end;
    node.addChildEx("ХарактерОп", rjcnclcarryoper.OperationInfo);
    
    ClearRecord(rjcnclopercode);
    rjcnclopercode.RjCnclCarryOperID = rjcnclcarryoper.ID;
    stat = GetGE(rjcnclopercode);
    while(stat AND (rjcnclopercode.RjCnclCarryOperID == rjcnclcarryoper.ID))
        var ObjAttr = Tbfile ("objattr.dbt");
        ObjAttr.keynum = 0;
        ObjAttr.rec.ObjectType = 700;
        ObjAttr.rec.GroupID = 2;
        ObjAttr.rec.AttrID = rjcnclopercode.Code;
        ObjAttr.GetEQ();
        
        var Code = ObjAttr.rec.NumInList;
        if (strlen(Code) > 4)
            Code = SubStr(Code, 1, 4);
        end;
        
        node.addChildEx("ПризнНеобОперации", LPAD4(Code, "0"));
        stat = next(rjcnclopercode);
    end;
    
    ClearRecord(rjsrvptoper);
    rjsrvptoper.RjCnclCarryOperID = rjcnclcarryoper.ID;
    stat = GetGE(rjsrvptoper);
    while(stat AND (rjsrvptoper.RjCnclCarryOperID == rjcnclcarryoper.ID))
        ClearRecord(rjsrvclnt);
        rjsrvclnt.ID = rjsrvptoper.RjSrvClntID;
        stat = GetEQ(rjsrvclnt);
        if(stat)
          Участник(PPrm, node);
        end;
        stat = next(rjsrvptoper);
    end;
    
    if (rjcnclcarryoper.AddInfo != "")
        node.addChildEx("Коммент", rjcnclcarryoper.AddInfo);
    end;
    
    if ((PPrm.MsgBuf.Version != FMRJSRVMSG_VER_1_1) and 
        ((rjcnclcarryoper.RecType == RJENTCONTRACC_DELETE) or (rjcnclcarryoper.RecType == RJENTCONTRACC_REM_GROUND) or (rjcnclcarryoper.RecType == RJENTCONTRACC_ABS_GROUND))
       )
        node.addChildEx("ПричинаУдаления", rjcnclcarryoper.DeleteReason);
    end;
end;

private macro СведПредстав(PPrm : TRlBackRjMsg_Out_Prm, Parent)
    var node = Parent.addChild("СведПредстав");
    
    if (rjsrvmsgidprt.SourceDprt != PPrm.MsgBuf.SourceDprt)
        node.addChildEx("ПризнакПредставСвед", "1");

        ИнфФилиал(PPrm, node);
    else
        node.addChildEx("ПризнакПредставСвед", "0");
    end;
    
    ClearRecord(rjcnclcarryoper);
    rjcnclcarryoper.RjSrvMsgIDprtID = rjsrvmsgidprt.ID;
    var stat = GetGE(rjcnclcarryoper);
    
    while(stat AND (rjcnclcarryoper.RjSrvMsgIDprtID == rjsrvmsgidprt.ID))
        СведОтказ(PPrm, node);
        stat = next(rjcnclcarryoper);
    end;
end;

// Процедура формирования сообщения об отказе от выполнения операции
macro FM_Out_RjSrvMsgCnClCarryOper(memMsgBuf, sFileName)
    record MsgBuf( "fmrjsrvmsg.dbt" );
    SetBuff( MsgBuf, memMsgBuf );

    setRunForOperation(true);
    var prm = TRlBackRjMsg_Out_Prm();
    prm.MsgBuf = MsgBuf;
    
    prm.xml.createProcessingInstruction("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
    
    if (FM_Out_RjSrvMsgCommonInfo(prm))
        var inform : FmXmlNodeElement;
        inform  = prm.root.addChild("ИнформЧасть");
        ИнфБанк(prm, inform);

        var stat = true;
        ClearRecord(rjsrvmsgidprt);
        rjsrvmsgidprt.RjSrvMsgID = prm.MsgBuf.ID;
        keynum(rjsrvmsgidprt, 1);
        stat = GetGE(rjsrvmsgidprt);
        while(stat AND (rjsrvmsgidprt.RjSrvMsgID == prm.MsgBuf.ID))
            СведПредстав(prm, inform);
            stat = next(rjsrvmsgidprt);
        end;
    end;
    
    return FM_Out_RjSrvMsgSave(MessageXMLFile, sFileName, prm.xml);
end;