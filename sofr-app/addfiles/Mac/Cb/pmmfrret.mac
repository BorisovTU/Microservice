IMPORT  PaymInter, OprInter, rsd, oralib, likepy, CTInter;  

RECORD pm_paym( pmpaym );
RECORD pm_rmprop (pmrmprop);


const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;
private var begin:Time;

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )
  file dp_dep("dp_dep.dbt");

  var PaymentID:integer = 0, DocKind:integer = 0, Department = "";
  var Number:string = "", Trn:string = "";
  var select:string;
  var params:TArray;
  var rs    :object;

  setbuff( pm_paym, firstdoc ); 

  if( Вызов == Первый )      
[  Отчет о групповом отзыве платежей по МФР                                                       ];
[┌─────────┬─────────────┬──────────┬──────────┬──────────┬──────────────────────────────────────┐];
[│  Номер  │   Сумма     │   Дата   │  Филиал- │ Референс │           Результат обработки        │];
[│документа│             │документа │получатель│сообщения │                                      │];
[├─────────┼─────────────┼──────────┼──────────┼──────────┼──────────────────────────────────────┤];
    begin = time();
  elif( Вызов == Ошибка )
    if( Статус == 0 )
      Сообщение = "Запрос на отзыв создан успешно. ";
    end;
    
    if( FindPayment( pm_paym.PaymentID, 0, 0, 0, 0, true, null, null, null, pm_rmprop ) != 0 )
      Статус = 1;
      Сообщение = "Документ не найден";
    elif(Статус == 0)
      dp_dep.Code = pm_paym.EndDepartment;
      if(getEQ(dp_dep))
        Department = dp_dep.Name;
      end;
      select = "select wlmes.t_Trn " +
                " from dwlreqlnk_dbt wlreqlnk, " +
                     " dwlmeslnk_dbt wlmeslnk, " +
                     " dwlmes_dbt wlmes " +
               " where wlreqlnk.t_ObjID = ? " +
                 " and wlreqlnk.t_ObjKind = ? " +
                 " and wlmeslnk.t_ObjKind = ? " +
                 " and wlmeslnk.t_ObjID = wlreqlnk.t_ReqID " +
                 " and wlmes.t_MesID = wlmeslnk.t_MesID " ;

      params = makeArray( SQLParam("", pm_paym.PaymentID), 
                          SQLParam("", OBJTYPE_MFRPAYMENT),
                          SQLParam("", OBJTYPE_REQ)
                        );

      rs = execSQLselect( select, params, true );

      if( rs and rs.moveNext() )
        Trn = rs.value(0);
      end;
    end;
    
[│#########│#############│##########│##########│##########│######################################│]
( pm_rmprop.Number,pm_paym.BaseAmount,pm_rmprop.Date, Department, Trn, StrSubst(Сообщение, "|", " " ):w );
          
  elif( Вызов == Последний )
   
[└─────────┴─────────────┴──────────┴──────────┴──────────┴──────────────────────────────────────┘];
[Время выполнения # с.](time() - begin);

  end;

END;
