//-----------------------------------------------------------------------------
// Блок     : 29047 - "Акцепт в кассе ЦАБС"
// Шаг      : 10    - "Акцепт в кассе ЦАБС"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import pm_setst, cbsttls, PTInter, CashInter;

var PaymentObj:RsbPayment;

private const CHOICE_BUTTON_YES = 0;    //Да
private const CHOICE_BUTTON_NO = 1;     //Нет
private var str = "По этому документу Вы уже подтвердили расход";

private macro MsgSubmBefore()

  Array Text;
  Array Buttons;

  Text(0) = str + " Продолжить?" ;

  Buttons(CHOICE_BUTTON_YES)  = " Да ";
  Buttons(CHOICE_BUTTON_NO)   = " Нет ";
  return ConfWin(Text,Buttons);
end;

private macro IsBlockAlsoExecutebyOper( DocumentID:variant, DocKind:integer, Symbol:String, IsExec:String , BlockID:integer ):bool
                                                                                    
  var query:string, params:TArray, rs:RsdRecordset;  

  if (ValType(BlockID) == V_INTEGER)

     query = "SELECT ST.t_Oper " +
                           " FROM DOPROPER_DBT OP, " +
                                " DOPRSTEP_DBT ST " +
                          " WHERE OP.T_DOCKIND = :DOCKIND " +
                            " AND OP.T_DOCUMENTID = LPAD(:DOCUMENTID, 34, '0') " +
                            " AND ST.T_ID_OPERATION = OP.T_ID_OPERATION " +
                            " AND ST.T_ISEXECUTE = :ISEXECUTE " +
                            " AND ST.T_BLOCKID = :BLOCKID ";
     params = makeArray(            SQLParam( "DOCKIND"   , DocKind   ),
                                    SQLParam( "DOCUMENTID", DocumentID ),
                                    SQLParam( "ISEXECUTE" , IsExec ),
                                    SQLParam( "BLOCKID" , BlockID ) );

     rs = execSQLselect( query, params, true );
  else
     MsgBox( "Ошибка в указании шага операции");
     return false;
  end;
  
  if( rs )
    if ( rs.moveNext())
       if( rs.Value(0) == {oper})
          return true;
       else
          return false 
       end;        
    end;
  else
    return false;
  end;

end;


MACRO ExecuteStep( doc, paymDoc )
  var note_text:string = "";
 

  /*Дополнительная проверка для приходно-расходного кассового ордера*/
  if ((PaymentObj.DocKind == DLDOC_INOUTORDER) and
     (Opr_IsStepExecuteSymb(PaymentObj.DocumentID, PaymentObj.DocKind, "Г", "X", 29047) and (Opr_IsStepExecuteSymb(PaymentObj.DocumentID, PaymentObj.DocKind, "Г", "R", 29047))))
    if(IsBlockAlsoExecutebyOper(PaymentObj.DocumentID,PaymentObj.DocKind, "Г", "X", 29047))
      if (not IsOprMultiExec())
        if (MsgSubmBefore()==1)
          return 1;
        end;
      else    
        msgbox(str);
        return 1;      
      end;         
    end;
  end;                                                                                       

  if( PM_NeedRejectControl() == true )

    if( NOT CashgetString( note_text, "Причина отказа (возврата)" ) )
      msgbox( "Пользователь прервал выполнение шага операции" );
      return 1;
    end;
    
    PaymentObj.PaymStatus = PM_REJECTED;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );

    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

  else /* если кассовый документ акцептован кассиром:*/
       /*Если документ не является приходно-расходным и акцепт делается впервые*/

    if (not ((PaymentObj.DocKind == DLDOC_INOUTORDER) and (Opr_IsStepExecuteSymb(PaymentObj.DocumentID, PaymentObj.DocKind, "Г", "X", 29047)))) 
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL ) )       //Сегмент контроль - проконтролирован
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    
      if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER ) )                     //Документооборот - зачисление
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;     
       
    /*Если это повторное выполнение шага*/

    elif (Opr_IsStepExecuteSymb( PaymentObj.DocumentID, PaymentObj.DocKind, "Г", "X", 29047))           
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL ) )       //Сегмент контроль - проконтролирован
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    
      if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )   
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;  
    end;
  end;

  // Заполнить примечание
  if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text ) != 0 )
    msgbox( "Ошибка при вставке примечания платежа" );
    return 1;
  end;
  
  return 0;
END;
