/*
  $Name:             multypm.mac
  $Module:           Ядро Banking
  $Description:      Сводный платеж
*/ 
//-----------------------------------------------------------------------------
// Блок      : Вне блока
// Шаг       : Вне шага
// Назначение: сводный платеж
// Описание  : сводный платеж
//-----------------------------------------------------------------------------

import PaymInter, BankInter, "pm_tools.mac", RMInter, "pm_common.mac", wluftool;

FILE cors(corschem) key 0;
FILE dp_dep(dp_dep) key 0;
FILE party(party) key 0;

private CONST SET_CHAR   = "X";
private CONST UNSET_CHAR = "";

const PathAutoCreate  = "CB\\PAYMENTS\\MULTI\\AUTOCREATE";
const PathMaxPayment  = "CB\\PAYMENTS\\MULTI\\MAXPAYMENT";

macro IsIncludedInMultyPM(Payment:RsbPayment)
  var select;
  var params:TArray = TArray();
  VAR rs:RsdRecordset;

  select = "select link.t_InitialPayment from dpmlink_dbt link where link.t_PurposePayment = :PaymentID and link.t_LinkKind = :LinkKind";
  params = makeArray(SQLParam( "PaymentID", Payment.PaymentID   ),
                     SQLParam( "LinkKind",  PMLINK_KIND_MULTYPM ));
  rs = execSQLselect( select, params, TRUE );
  if(rs.moveNext)
    return rs.value(0);
  end;
  return 0;
end;

macro NeedIncludeInMultyPaym(Payment:RsbPayment)
  var AutoInclude, err;
  GetRegistryValue( PathAutoCreate, V_BOOL, AutoInclude, err );
  if( (IsNeedIncludeInMP()) or ( (AutoInclude ) and (Payment.DocKind != DLDOC_MULTYPM)) )
    return 1;
  end;
  return 0;
end;

macro GetPaymCountIncludedInMultyPM( PaymentID )
  var select;
  var params:TArray = TArray();
  VAR rs:RsdRecordset;
  var CountMultyPM = 0;

  select = "select count(*) from dpmlink_dbt link where link.t_InitialPayment = :PaymentID and link.t_LinkKind = 9";
  params = makeArray(SQLParam( "PaymentID", PaymentID ));
  rs = execSQLselect( select, params, TRUE );
  if(rs.moveNext)
    CountMultyPM = rs.value(0);
  end;
  return CountMultyPM;
end;

     
macro GetNameAndCorrAcc( ID, Name, CorAcc )
   FILE bankdprt( bankdprt ) key 0;
   ClearRecord(party);
   party.PartyID = ID;
   if(getEQ(party))
     SetParm(1, party.Name);
   end;
   ClearRecord(bankdprt);
   bankdprt.PartyID = ID;
   if(getEQ(bankdprt))
     SetParm(2, bankdprt.CorAcc);
   end;
end;                
      
macro GetCodeForTransp(AgentID, TpID, CodeKind:@integer, Code:@string)
    var select;
    var params:TArray = TArray();
    VAR rs:RsdRecordset;

    FILE tpCode(wltpcode) key 2;
    var b_continue;
    
    if(TpID)
      ClearRecord(tpCode);
      tpCode.TpID = TpID;
      tpCode.Primary = SET_CHAR;
      if (GetEQ(tpCode))
   
        select = " select pt.t_CodeKind, pt.t_Code " +
                 " from dobjcode_dbt pt " + 
                 " where pt.t_ObjectID = :AgentID " +
                 " and pt.t_ObjectType = :TYPE "+
                 " and pt.t_CodeKind = :CodeKind " + 
                 " and pt.t_state = 0 ";
        params = makeArray(SQLParam( "AgentID", AgentID ),
                           SQLParam( "TYPE", OBJTYPE_PARTY),
                           SQLParam( "CodeKind", tpCode.CodeKind));
        rs = execSQLselect( select, params, TRUE );
        if(rs.moveNext)
          SetParm(2,rs.value(0));
          SetParm(3,rs.value(1));
          return true;
        end;
      end;
      
      ClearRecord(tpCode);
      tpCode.TpID = TpID;
      tpCode.Primary = SET_CHAR;
      b_continue = GetGE(tpCode);
      while(b_continue)
        select = " select pt.t_CodeKind, pt.t_Code " +
                 " from dobjcode_dbt pt " + 
                 " where pt.t_ObjectID = :AgentID " +
                 " and pt.t_ObjectType = :TYPE "+
                 " and pt.t_CodeKind = :CodeKind " + 
                 " and pt.t_state = 0 ";
        params = makeArray(SQLParam( "AgentID", AgentID ),
                           SQLParam( "TYPE", OBJTYPE_PARTY),
                           SQLParam( "CodeKind", tpCode.CodeKind));
        rs = execSQLselect( select, params, TRUE );
        if(rs.moveNext)
          SetParm(2,rs.value(0));
          SetParm(3,rs.value(1));
          return true;
        end;
        b_continue = Next(tpCode);
      end;
      ClearRecord(party);
      party.PartyID = AgentID;
      GetEQ(party);
      msgbox(string("Не найден код вида ",tpCode.CodeKind," для субъекта ",party.ShortName));
      return false;  
    else
      setparm(2, "");
      setparm(3, "");
    end;
    return true;
end;

macro CreateMultyPayment(MultyPayment:@RsbMultyPayment, FIID:integer, Corschem:integer, 
                         ValueDate:date, Purpose:integer, TpID:integer, TpSchemID:integer, RslFormID:integer, SubKindMessage:integer, Payment:RsbPayment)

  var PayerBankCodeKind, PayerBankCode, PayerBankName, PayerCorrAccNostro,
      ReceiverBankCodeKind, ReceiverBankCode = "", ReceiverBankName = "", ReceiverCorrAccNostro = "",
      Payer, Receiver = 0;

  var ed108receiver = Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\AUTOPAYEEED108", V_BOOL, false);
  var FormName : string = GetUFBSMesFormName(RslFormID);


  MultyPayment.DocKind = DLDOC_MULTYPM;
  MultyPayment.Purpose = Purpose;
  MultyPayment.Ground = GetPurposeName( Purpose );
  cors.Number = Corschem;
  cors.FI_Kind = 1;
  cors.FIID = FIID;
  cors.State = 0;

  if(getEQ(cors))
    dp_dep.Code = cors.Department;
    if(getEQ(dp_dep))
      Payer = dp_dep.PartyID;
    end;    
  
    if( (FormName != "ED108" ) or (ed108receiver != false) )
      Receiver = cors.CorrID;
    end;
  end;


  MultyPayment.PayerMesBankID = Payer;
  MultyPayment.ReceiverMesBankID = Receiver;
  
  if(TpID)
    if(not GetCodeForTransp(Payer, TpID, PayerBankCodeKind, PayerBankCode))
      return 1;
    end;
  else
    PayerBankCodeKind = Payment.PayerBankCodeKind;
    PayerBankCode     = Payment.PayerBankCode;
  end;

  GetNameAndCorrAcc( Payer, PayerBankName, PayerCorrAccNostro );

  if(TpID and (Receiver != 0))
    if(not GetCodeForTransp(Receiver, TpID, ReceiverBankCodeKind, ReceiverBankCode))
      return 1;
    end;
  else
    ReceiverBankCodeKind = Payment.ReceiverBankCodeKind;
    ReceiverBankCode     = Payment.ReceiverBankCode;
  end;

  GetNameAndCorrAcc( Receiver, ReceiverBankName, ReceiverCorrAccNostro );

  MultyPayment.ValueDate = ValueDate;
  MultyPayment.PayerFIID = FIID;
  MultyPayment.ReceiverFIID = FIID; 
  MultyPayment.BaseFIID = FIID;
  MultyPayment.PayerAmount = 0;
  MultyPayment.ReceiverAmount = 0;
  MultyPayment.BaseAmount = 0;
  MultyPayment.PaymStatus = PM_READY_TO_SEND;
  MultyPayment.PayerIsSender = "X";
  MultyPayment.PayerBankCorrID = -1;
  MultyPayment.PayerOurCorrID = -1;
  MultyPayment.PayerCorschem = -1;

  MultyPayment.PayerName = Bnk_GetPartyName( Payer );
  GetPartyCodeEx( Payer, PTCK_INN, MultyPayment.PayerINN);

  MultyPayment.ReceiverBankCorrID = -1;
  MultyPayment.ReceiverOurCorrID = -1;
  
  MultyPayment.ReceiverCorschem = Corschem;
  MultyPayment.CorrPosType = PM_CORRPOS_TYPE_USER;
  MultyPayment.OutTransport = TpID;
  MultyPayment.OutTpSchem  = TpSchemID;
  MultyPayment.OutRlsForm  = RslFormID;
  MultyPayment.OutSubKindMessage = SubKindMessage;
  MultyPayment.PropStatus = PM_PROP_READY;  

  if( (FormName != "ED108" ) or (ed108receiver != false) )
    MultyPayment.ReceiverName = Bnk_GetPartyName( Receiver );
    GetPartyCodeEx( Receiver, PTCK_INN, MultyPayment.ReceiverINN );  
  end;

  if( (MultyPayment.OutTransport == 0) or (MultyPayment.OutTpSchem == 0) or (MultyPayment.OutRlsForm == 0))
    DefineRlsForm( MultyPayment, MultyPayment.OutTransport,MultyPayment.OutTpSchem,NULL,MultyPayment.OutRlsForm, MultyPayment.OutSubKindMessage );
  end;

  MultyPayment.SetPayerPI( PAYMENTS_GROUP_UNDEF, Payer, PayerBankCodeKind, PayerBankCode, PayerBankName, PayerCorrAccNostro, FIID, 1,"", Payer, PayerBankName  );
  MultyPayment.SetReceiverPI( PAYMENTS_GROUP_EXTERNAL, Receiver, ReceiverBankCodeKind, ReceiverBankCode, ReceiverBankName, ReceiverCorrAccNostro, FIID, 1,"", Receiver, ReceiverBankName  );
  
  MultyPayment.OutTransferDate = PmGetDefaultOutTransferDate( MultyPayment ); 

  GenerateReference( 133, MultyPayment.Number );  
  MultyPayment.Date = {curdate};
  MultyPayment.PayDate = {curdate};
  MultyPayment.ClientDate = {curdate};
  MultyPayment.PayerChargeOffDate = date(0,0,0);
  // ╙ёЄрэютшь ёЄрЄєё√
  MultyPayment.AddOprState( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_OUT );
  MultyPayment.AddOprState( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES );
  MultyPayment.AddOprState( OPR_PAYM_OUT_KVIT, OPR_PM_ST_UNKVIT );
  MultyPayment.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL );
  MultyPayment.AddOprState( OPR_PAYM_DO, OPR_PM_ST_DISCHARGE );
  MultyPayment.AddOprState( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO );

  array FlgRM;
  FlgRM[PNRMPM_COUNT] = 0;
  FlgRM[PNRMPM_AUTHSTAT] = 1;
  FlgRM[PNRMPM_INNP] = 1;  
  FlgRM[PNRMPM_NAMEP] = 1;
  FlgRM[PNRMPM_CODEP] = 1;
  FlgRM[PNRMPM_BANKP] = 1;
  FlgRM[PNRMPM_BANKACCP] = 1;
  FlgRM[PNRMPM_CODER] = 1;
  FlgRM[PNRMPM_BANKR] = 1;
  FlgRM[PNRMPM_BANKACCR] = 1;
  FlgRM[PNRMPM_INNR] = 1;
  FlgRM[PNRMPM_NAMER] = 1;
  FlgRM[PNRMPM_GROUND] = 1;
  FlgRM[PNRMPM_UIN] = 1;

  var stat =  0 ;

  if( GetDialogFlag() )
    stat = PM_ProcessPanel( MultyPayment, 1, NULL, FlgRM );
  end;
  
  return stat;
end;
macro CheckPaymentForIncludeInMulty( Payment:RsbPayment, MultyPayment:RsbMultyPayment)
  var MaxMultiPaym;

  if( Payment.DocKind == DLDOC_MULTYPM )
    msgbox("Сводный платеж не может быть включен в сводный");
    return false;
  end;

  if( not Payment.IsCredit )
    msgbox("Включить в сводный можно только кредитовые платежи");
    return false;
  end;
  if( (Payment.Purpose != MultyPayment.Purpose) and (MultyPayment.Purpose > 0) )
    msgbox("Назначение включаемого платежа не соответствует назначению сводного платежа");
    return false;
  end;
  if(( Payment.OutCorschem != MultyPayment.OutCorschem ) or ( Payment.OutCorschemFIID != MultyPayment.OutCorschemFIID ))
    msgbox("Схемы расчетов включаемого и сводного платежа должны совпадать");
    return false;
  end;
  if( Payment.ValueDate != MultyPayment.ValueDate )
    msgbox("Дата валютирования включаемого и сводного платежа должна совпадать");
    return false;
  end;
  
  GetRegistryValue( PathMaxPayment, V_INTEGER, MaxMultiPaym, 0 );
  if( (MaxMultiPaym) and (GetPaymCountIncludedInMultyPM( MultyPayment.PaymentID ) >= MaxMultiPaym))
    msgbox("Количество платежей, включенных в сводный, равно максимальному");
    return false;
  end;
  return true;
end;

macro IncludeInMultyPaym(PaymentObj:RsbPayment)
  var AutoInclude, err, MaxMultiPaym, PaymentID, stat, error;
  var MultyPaym:RsbMultyPayment;
  var select;
  var params:TArray = TArray();
  VAR rs:RsdRecordset;
  GetRegistryValue( PathAutoCreate, V_BOOL, AutoInclude, err );
 
  if( AutoInclude )
  
    GetRegistryValue( PathMaxPayment, V_INTEGER, MaxMultiPaym, error );
    
    select = " select pm.t_PaymentID" +
             " from dpmpaym_dbt pm, dpmprop_dbt cr" +
             " where  pm.t_PaymentID = cr.t_PaymentID"+
               " and  cr.t_DebetCredit = 1" +
               " and  pm.t_DocKind  = 311" +
               " and  (pm.t_Purpose  = :Purpose or pm.t_Purpose = 0)" +  
               " and  cr.t_Corschem = :Corschem" + 
               " and  cr.t_PayFIID  = :FIID" +
               " and  pm.t_ValueDate = :ValueDate" +
               " and  cr.t_PropStatus = 3000";

    if( MaxMultiPaym )
      select = select + " and  (select count(*) from dpmlink_dbt link where link.t_InitialPayment = pm.t_PaymentID and link.t_LinkKind = 9) < :MaxMultiPaym";
    end;
    
    params = makeArray(SQLParam( "Purpose",     PaymentObj.Purpose ),
                       SQLParam( "Corschem",    PaymentObj.OutCorschem ),
                       SQLParam( "FIID",        PaymentObj.OutCorschemFIID ), 
                       SQLParam( "ValueDate",   PaymentObj.ValueDate ));
    
    if(MaxMultiPaym)
      params[params.size] = SQLParam( "MaxMultiPaym",MaxMultiPaym )
    end;

    rs = execSQLselect( select, params, TRUE );

    if( rs.moveNext() )
      MultyPaym = RsbMultyPayment(rs.value(0));
    else
      stat = CreateMultyPayment(@MultyPaym,PaymentObj.BaseFIID, PaymentObj.OutCorschem, PaymentObj.ValueDate, PaymentObj.Purpose, 0, 0, 0, 0, PaymentObj );
      if( stat )
        return stat;
      end;
      MultyPaym.ConnectToOper = false;
      MultyPaym.LaunchOper    = true;
    end;
  else  
     stat = ListMultyPayment( PaymentID, NULL, PaymentObj.OutCorschemFIID, PaymentObj.OutCorschem, PaymentObj.ValueDate, PaymentObj.ValueDate, NULL, NULL, PaymentObj.Purpose);
     if( stat )
       return stat;
     end;

     MultyPaym = RsbMultyPayment( PaymentID );
  end;
   
   if(CheckPaymentForIncludeInMulty( PaymentObj, MultyPaym))
     
     if(MultyPaym.LinkPayment(PaymentObj, PMLINK_KIND_MULTYPM ) )
       msgbox(GetErrMsg());
       return 1;
     end;
     MultyPaym.BaseAmount     = MultyPaym.BaseAmount + PaymentObj.BaseAmount;
     MultyPaym.PayerAmount    = MultyPaym.PayerAmount + PaymentObj.BaseAmount;
     MultyPaym.ReceiverAmount = MultyPaym.ReceiverAmount + PaymentObj.BaseAmount;
     PaymentObj.OutTransport = MultyPaym.OutTransport;
     PaymentObj.OutTpSchem   = MultyPaym.OutTpSchem;
     PaymentObj.OutRlsForm   = MultyPaym.OutRlsForm;
     PaymentObj.PaymStatus   = PM_IS_SENDING;
     PaymentObj.PropStatus   = PM_PROP_DISCHARGED;
     PaymentObj.CreateWlPmOut();
     return 0;
   else
     return 1;
   end;

end;
