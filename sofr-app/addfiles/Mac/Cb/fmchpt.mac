/*
 $Name: fmchpt.mac
 $Module: Ядро ГКБО 
 $Description: Печать результатов контроля клиентов на причастность к террористической деятельности
 */
/*
 * Печать результатов контроля клиентов на причастность к террористической деятельности
 */
Import "globals.mac";

private const ListKFM = 1;
private const ListSDN = 2;
private const TERR_LIST_ADDRESS_KFM = -1;

private const NameListKFM = "ФСФМ РФ";
private const NameListSDN = "OFAC/SDN List";

/* Рижимы проверки */
private const FMCHPT_CheckAllClient = 0;
private const FMCHPT_CheckInput     = 1;


private macro ClearResultFileRecord( ResultFile : TBFile )
  ResultFile.Clear();
  ResultFile.rec.Percent = 100;
end;

private macro ConvertDateToString( d : date )
  var strDate = "00.00.0000";

  if( d != date(0, 0, 0) )
    strDate = string( d:f );
  end;

  return strDate;
end;

private macro GetNameOper( Oper : integer )
  file person("person.dbt");
  person.Oper = Oper;
  if( getEQ(person) ) return person.Name; end;
  return "";
end;

private macro GetListName( List : integer )
  var strList = "";
  if( List == 0 )
    strList = NameListKFM + ", " + NameListSDN;
  elif( List == ListKFM )
    strList = NameListKFM;
  elif( List == ListSDN )
    strList = NameListSDN;
  end;
  return strList;
end;


private macro KFMGetTerroristNumber( TerroristID : integer )
  var stat : bool;
  var Number : integer;
  var TerrFile = TBFile( "terr_kfm.dbt", "R", 0);

  Number = 0;

  TerrFile.Clear();
  TerrFile.rec.TerroristID = TerroristID;
  stat = TerrFile.GetEQ();

  if( stat  )
    Number = TerrFile.rec.Number;
  end;

  return Number;
end;



private macro KFMGetTerroristName( TerroristID : integer )
  var stat : bool;
  var Name : string;
  var TerrInfoFile = TBFile( "terr_kfm_aka.dbt", "R", 1);

  Name = "";

  TerrInfoFile.Clear();
  TerrInfoFile.rec.TerroristID = TerroristID;
  stat = TerrInfoFile.GetGE();
  while( (stat) and (TerrInfoFile.rec.TerroristID == TerroristID) )
    if(Name != "")
      Name = Name +"; ";
    end;
    Name = Name + TerrInfoFile.rec.Name;

    stat = TerrInfoFile.Next();
  end;

  return Name;
end;

private macro SDNGetTerroristName( EntNum : integer )
  var Name : string;
  var TerrFile = TBFile( "terror.dbt" );

  Name = "Запись не найдена";

  TerrFile.Clear();
  TerrFile.rec.EntNum = EntNum;
  if( TerrFile.GetEQ() )
    Name = TerrFile.rec.Name;
  end;
  
  return Name;
end;


macro PrintTableTitle( List : integer, ListName : string )
//  if( List == 0 )
[Совпадения со справочником #] ( ListName );
//  end;

[+--------+---------------------------+-------+------------------------------------------------------------------+];
[| Номер  |      Наименование         |   %   |              Совпало с данными о террористах                     |];
[|субъекта|        субъекта           |совпад.+----------+---------------------------+---------------------------+];
[|        |                           |       |    №     |       Наименование        |        Примечание         |];
[+--------+---------------------------+-------+----------+---------------------------+---------------------------+];
end;

macro PrintSimpleTableLine( line : string )
[|###############################################################################################################|]
( line:w );
[+--------+---------------------------+-------+----------+---------------------------+---------------------------+];
end;

macro PrintTableLine( ResultFile : TBFile, TerroristName : string, TerrNumber : integer )
[|########|###########################|  ###  |##########|###########################|###########################|]
 ( ResultFile.rec.PtCode:w
  ,ResultFile.rec.PtName:w
  ,ResultFile.rec.Percent
  ,TerrNumber
  ,TerroristName:w
  ,ResultFile.rec.Comment:w
 );
[+--------+---------------------------+-------+----------+---------------------------+---------------------------+];
end;

macro PrintTableStamp( nCoincidence : integer, ListName : string )
[];
[Всего совпадений со справочником #] ( ListName + ": "+ string(nCoincidence) );
end;



macro PrintTableTitleAddress( List : integer, ListName : string )
[Совпадения адресов со справочником #] ( ListName );

[+--------+---------------------------+------------------+------------------------------------------------------------------+];
[| Номер  |      Наименование         |       Адрес      |                Совпало с данными о террористах                   |];
[|субъекта|        субъекта           |     субъекта     +----------+---------------------------+---------------------------+];
[|        |                           |                  |    №     |       Наименование        |        Адрес              |];
[+--------+---------------------------+------------------+----------+---------------------------+---------------------------+];
end;

macro PrintSimpleTableLineAddress( line : string )
[|###########################################################################################################################|]
( line:w );
[+-------+----------------------------+------------------+----------+---------------------------+---------------------------+];
end;

macro PrintTableLineAddress( ResultFile : TBFile, TerroristName : string, TerrNumber : integer )
[|########|###########################|##################|##########|###########################|###########################|]
 ( ResultFile.rec.PtCode:w
  ,ResultFile.rec.PtName:w
  ,ResultFile.rec.PtAdress:w
  ,TerrNumber
  ,TerroristName:w
  ,ResultFile.rec.TerrAdress:w
 );
[+--------+---------------------------+------------------+----------+---------------------------+---------------------------+];
end;

private macro PrintTableSDN( ResultFile : TBFile, List : integer )

  var stat : bool;
  var nCoincidence : integer;
  
  nCoincidence = 0;

  ClearResultFileRecord( ResultFile );
  ResultFile.rec.List = ListSDN;
  stat = ResultFile.GetGE();
  if( (not stat) or (ResultFile.rec.List != ListSDN) )
  else
    PrintTableTitle( List, NameListSDN );
    while( (stat) and (ResultFile.rec.List == ListSDN) )
      nCoincidence = nCoincidence + 1;

      PrintTableLine( ResultFile, SDNGetTerroristName(ResultFile.rec.TerrNumber), ResultFile.rec.TerrNumber );

      stat = ResultFile.Next();
    end;
    PrintTableStamp( nCoincidence, NameListSDN );
  end;

  return nCoincidence;

end;

private macro PrintTableAddress( ResultFile : TBFile, List : integer )

  var stat : bool;
  var nCoincidence : integer;
  
  nCoincidence = 0;

  ClearResultFileRecord( ResultFile );
  ResultFile.rec.List = TERR_LIST_ADDRESS_KFM;
  stat = ResultFile.GetGE();
  if( (not stat) or (ResultFile.rec.List != TERR_LIST_ADDRESS_KFM) )
  else
    PrintTableTitleAddress( List, NameListKFM );
    while( (stat) and (ResultFile.rec.List == TERR_LIST_ADDRESS_KFM) )
      nCoincidence = nCoincidence + 1;

      PrintTableLineAddress( ResultFile, KFMGetTerroristName(ResultFile.rec.TerrNumber), KFMGetTerroristNumber(ResultFile.rec.TerrNumber));

      stat = ResultFile.Next();
    end;
    PrintTableStamp( nCoincidence, NameListKFM );
  end;

  return nCoincidence;

end;



private macro PrintTableKFMDegree( ResultFile : TBFile, Degree : integer, Title : string )

  var stat : bool;
  var nCoincidence : integer;
  
  nCoincidence = 0;

  ClearResultFileRecord( ResultFile );
  ResultFile.rec.List   = ListKFM;
  ResultFile.rec.Degree = Degree;
  stat = ResultFile.GetGE();
  if( (stat) and (ResultFile.rec.List == ListKFM) and (ResultFile.rec.Degree == Degree) )
    PrintSimpleTableLine( Title );

    while( (stat) and (ResultFile.rec.List == ListKFM) and (ResultFile.rec.Degree == Degree) )
      nCoincidence = nCoincidence + 1;

      PrintTableLine( ResultFile, KFMGetTerroristName(ResultFile.rec.TerrNumber), KFMGetTerroristNumber(ResultFile.rec.TerrNumber) );

      stat = ResultFile.Next();
    end;
    
  end;

  return nCoincidence;

end;

private macro PrintTableKFM( ResultFile : TBFile, List : integer )

  var stat : bool;
  var nCoincidence : integer, nAllCoincidence : integer;
  
  nAllCoincidence = 0;

  ClearResultFileRecord( ResultFile );
  ResultFile.rec.List = ListKFM;
  stat = ResultFile.GetGE();
  if( (not stat) or (ResultFile.rec.List != ListKFM) )
  else
    nCoincidence = 0;
    PrintTableTitle( List, NameListKFM );
    
    nAllCoincidence = 0;
    
    nCoincidence = PrintTableKFMDegree( ResultFile, 1, "НАИБОЛЕЕ ВЕРОЯТНОЕ СОВПАДЕНИЕ:" );
    if( nCoincidence != 0 )
      PrintSimpleTableLine( "Всего наиболее вероятных совпадений: " + string(nCoincidence) );
      nAllCoincidence = nAllCoincidence + nCoincidence; 
      PrintSimpleTableLine( "" );
    end;


    nCoincidence = PrintTableKFMDegree( ResultFile, 2, "МАЛОВЕРОЯТНОЕ СОВПАДЕНИЕ:" );
    if( nCoincidence != 0 )
      PrintSimpleTableLine( "Всего маловероятных совпадений: " + string(nCoincidence) );
      nAllCoincidence = nAllCoincidence + nCoincidence; 
    end;
    
    println;
    PrintTableStamp( nAllCoincidence, NameListKFM );
  end;

  return nAllCoincidence;

end;


/* Напечатать заголовок */
private macro PrintTitle( List : integer, DateUpdate : date, DateInsert : date, DateDelete : date, CheckMode : integer, PumpPartyType : string, PumpPartyKind : string, PumpServiceKind : string )
[ ПРОТОКОЛ КОНТРОЛЯ КЛИЕНТОВ НА ПРИЧАСТНОСТЬ К ТЕРРОРИСТИЧЕСКОЙ ДЕЯТЕЛЬНОСТИ ];
[                                                                            ];
[Дата и время контроля ##########  ########                                  ] ( date:f, time:f );
[Исполнитель: #### #                                                         ] ( {oper}, GetNameOper({oper}) );
  if( CheckMode == FMCHPT_CheckInput ) /* проверка при вводе */
[Используются справочники ######################                              ] ( GetListName(List) );
  else
[Используются справочники ###################### c даты обновления: ##########]
  ( GetListName(List), ConvertDateToString(DateUpdate) );
    if( (List == 0) or (List == ListKFM) )
[                                                     c даты ввода: ##########] ( ConvertDateToString(DateInsert) );
[                                                 по дату удаления: ##########] ( ConvertDateToString(DateDelete) );
    end;
[Виды субъектов для контроля:            ####################################] (PumpPartyType  ) ;
[Принадлежность субъектов для контроля:  ####################################] (PumpPartyKind  ) ;
[Вид обслуживания клиентов для контроля: ####################################] (PumpServiceKind) ;

  end;

end;

/*
 * Основная функция
 */
private macro PrintResults
(
  ResFileName : string
 ,List        : integer
 ,DateUpdate  : date
 ,DateInsert  : date
 ,DateDelete  : date
 ,AllNumber   : integer
 ,CheckMode   : integer
 ,PumpPartyType   : string
 ,PumpPartyKind   : string
 ,PumpServiceKind : string

)
  var ResultFile : TBFile;
  var nCoincidence : integer, nAllCoincidence : integer;

  ResultFile = TBFile( "fmchpt.tmp", null, null, ResFileName );

  nAllCoincidence = 0;

  PrintTitle( List, DateUpdate, DateInsert, DateDelete, CheckMode, PumpPartyType, PumpPartyKind, PumpServiceKind );

  if( ResultFile.NRecords() == 0 )
    println;
[Не найдено совпадений субъектов и адресов со справочниками лиц, причастных к террористической деятельности];
  else
    println;
    nCoincidence = PrintTableKFM( ResultFile, List );
    if( nCoincidence != 0 )
      nAllCoincidence = nAllCoincidence + nCoincidence;
      println;
    end;
    
    nCoincidence = PrintTableSDN( ResultFile, List );
    if( nCoincidence != 0 )
      nAllCoincidence = nAllCoincidence + nCoincidence;
      println;
    end;

    if( nAllCoincidence == 0 )
[Не найдено совпадений субъектов со справочниками лиц, причастных к терроризму.];
      println;
    end;

    nCoincidence = PrintTableAddress( ResultFile, TERR_LIST_ADDRESS_KFM );
    if( nCoincidence != 0 )
      nAllCoincidence = nAllCoincidence + nCoincidence;
      println;
    else
[Не найдено совпадений адресов со справочниками лиц, причастных к террористической деятельности.];
      println;
    end;

    println( "Всего обработано клиентов: " + string(AllNumber) );
    println( "Всего совпадений: " + string(nAllCoincidence) );
  end;

end;
