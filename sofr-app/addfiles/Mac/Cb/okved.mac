/*
  RS-Bank 5.10                                           R-Style Software Lab

  Загрузка новой категории субъектов "Код ОКВЭД" и списка ее признаков
  в БД (objgroup.dbt, objattr.dbt).

FIENAME: okved.MAC
CREATED: 04.03.2003 LCh
MODIFICATIONS:
NOTES:   Используется файл данных okved.dbf, который следует поместить 
         в один из каталогов макрофайлов.
         Перед запуском макроса рекомендуется сохранить резервные копии 
         обновляемых файлов objgroup.dbt, objattr.dbt 

└───────────────────────────────────────────────────────────────────────────*/

FILE objgroup (objgroup) write;     /* Категории признаков  */
FILE objattr (objattr) Key 2 write; /* Иерархия  признаков */

FILE f_data ( ) dbf;

CONST
      ObjTypePARTY   = 3,
      Group_ОКВЭД  = 17,
      Name_ОКВЭД   = "Код ОКВЭД",
      NameDataFile = "okved.dbf",
      LEN_COD    =   9;


/* Категории признаков субъектов */        
MACRO CheckGrClAttr( GroupID, Name )

    /* Проверка дублирования  */
   objgroup.ObjectType = ObjTypePARTY;
   objgroup.GroupID = GroupID;
   if ( GetEQ(objgroup) )

      if ( GetTrue( FALSE, "Уже есть запись с номером "+string(GroupID)+" \""+ objgroup.Name+"\".Очистить?") )
        if ( objgroup.Name != Name )
          objgroup.Name    = Name; 
          objgroup.Type    = "";     /* Наличие нескольких признаков */
          if ( Update(objgroup) )     
            println( " Обновлена категория субъектов \"", Name,"\"" );
          else  
            exit(0, " !!! Ошибка записи в файл objgroup.dbt");
          end;
        end;

      else
        exit(1);
      end;
   else  

    if ( not GetTrue( FALSE, "Обновить справочник категорий субъектов?") )
        exit(1);
    end;

    /* Запись в БД  */

    ClearRecord( objgroup );

    objgroup.ObjectType = ObjTypePARTY;
    objgroup.GroupID = GroupID;
    objgroup.Name    = Name; 
    objgroup.Order   = GroupID; 
    objgroup.Type    = "";     /* Наличие нескольких признаков */
    objgroup.System  = "X";     /* Признак системной группы  */

    if ( Insert(objgroup) )     
        println( " Добавлена категория субъектов \"", Name,"\"" );
    else  
        exit(0, " !!! Ошибка записи в файл objgroup.dbt");
    end;
   end;

   return TRUE;

END;


/* Добавить одну запись в список признаков категории */
MACRO AddClAttrib( p_cod, p_name, p_shname, AttrId )

    /* Проверка размеров полей */
    p_cod     = trim(p_cod);   
    p_shname  = trim(p_shname);
    p_name    = trim(p_name);  

    if ( strlen(p_cod) > LEN_COD ) 
      println( " Внимание! Слишком длинный код : ",p_cod, " для признака \"",p_shname,"\"");
      p_cod = substr(p_cod, 1, LEN_COD);
      println( "           Записан как         : ",p_cod );
    end;

    /* Проверка дублирования  */
    objattr.ObjectType = ObjTypePARTY;
    objattr.GroupID   =  Group_ОКВЭД;
    objattr.CodeList  =  p_cod;         
    objattr.NumInList =  "";
    if ( GetEQ(objattr) )
      println( " Внимание! Уже есть признак с кодом :",p_cod );
      println( "           Название старого \"",objattr.FullName,"\"");
      println( "           Название нового  \"",p_name,"\"");
      return FALSE;
    end;

    /* Запись в БД  */
    ClearRecord( objattr );

    objattr.ObjectType = ObjTypePARTY;
    objattr.GroupID   =  Group_ОКВЭД;
    objattr.CodeList  =  p_cod;         
    objattr.NumInList =  "";
    objattr.AttrID    =  AttrId;
    objattr.ParentID  =  0;
    objattr.Name            = p_shname; 
    objattr.FullName        = p_name;   

    if ( not Insert(objattr) )     
      exit(0, " !!! Ошибка записи в файл objattr.dbt");
    end;

    return TRUE;
END;

/* Удаление признаков в категории  */
MACRO RemoveClAttributes( GroupID )
   var stat, Count = 0;

    objattr.ObjectType = ObjTypePARTY;
    objattr.GroupID   =  GroupID;
    objattr.CodeList  =  "";         
    objattr.NumInList =  "";
    stat = GetGE(objattr);

    message( " Очистка справочника ...");

    while ( stat and (objattr.ObjectType == ObjTypePARTY) and (objattr.GroupID == GroupID) )
      if ( Delete(objattr) )     
        Count = Count +1;
      else  
        exit(0, " !!! Ошибка удаления записи в файле objattr.dbt" );
      end;
      stat = next(objattr);
    end;

    if ( Count > 0 )
      println( " Из списка признаков для категории субъектов \"", objgroup.Name, "\"",
               " удалено ",string(Count)," записей"  );
    end;

END;

/* Добавление признаков в категории */
MACRO  AddClAttributes(p_name)
  var stat = TRUE, Count = 0;

  if ( not open( f_data, p_name) )
     exit(0, " !!! Не открыт файл данных ", p_name  );
  end;

  while( next(f_data) )  
    message( " Добавление записи для ~", f_data.номер,"~");

    if ( AddClAttrib( f_data.номер, f_data.название, f_data.короткое_н, Count+1) )
      Count = Count +1;                                                     
    end;
  end;

  if ( Count > 0 )
    println( " В список признаков для категории субъектов \"", Name_ОКВЭД,"\"",
              " добавлено ",string(Count)," записей"  );
  end;

END;

/*_______________Вход в программу __________________ */

var FullNameData = FindPath( NameDataFile );

  if ( FullNameData == "" ) 
     MsgBox(  "Не найден файл данных ", NameDataFile);
     exit(1);
  end;

  println( "   Обновление справочника категорий субъектов" );
  println( "   ------------------------------------------" );

  if ( CheckGrClAttr( Group_ОКВЭД, Name_ОКВЭД ) )
    RemoveClAttributes( Group_ОКВЭД );
    AddClAttributes( FullNameData );
  end;

/*eof*/