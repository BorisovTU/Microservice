/*
$Name:           imp_kfm.mac
$Module:         Фин. мониторинг 
$Description:    Импорт справочника лиц, причастных к терроризму (КФМ РФ)
*/

Import "globals.mac", rsd, likepy, oralib ;

private file terr_kfm_tmp_file    ("terr_kfm.tmp"    ) write key 0;
private file terrinfo_tmp_file    ("terrinfo.tmp"    ) write;
private file terr_kfm_aka_tmp_file("terr_kfm_aka.tmp") write;
private file fmkfmchk_tmp_file    ("fmkfmchk.tmp"    ) write;


private file terr_kfm_file    ("terr_kfm.dbt"    ) write;
private file terrinfo_file    ("terrinfo.dbt"    ) write;
private file terr_kfm_aka_file("terr_kfm_aka.dbt") write;





private var Description  = TArray;
private var Citizen      = TArray;
private var Resolution   = TArray;
private var LegalAddress = TArray;
private var Address      = TArray;
private var Founder      = TArray;
private var Director     = TArray;
private var OKPO         = TArray;
private var RegNumber    = TArray;
private var PaperSeries  = TArray;
private var PaperNumber  = TArray;
private var NameDocum    = TArray;
private var BirthPlace   = TArray;

private macro GetNameOper( Oper : integer )
  file person("person.dbt");
  person.Oper = Oper;
  if( getEQ(person) ) return person.Name; end;
  return "";
end;

private macro toDate( dbfDate : variant )
  var bdDate : date;
  
  bdDate = date(0,0,0);

  if( valtype(dbfDate) == V_DATE )
    bdDate = dbfDate;
  else
    bdDate = date(dbfDate);
  end;

  return bdDate;
end;


macro GetSequentNewID( TableName ) // dterr_kfm_dbt , dterr_kfm_aka_dbt
  var strSql : string;
  var rs : RsdRecordset;

  var NewID = 0;

  strSql = "SELECT " + TableName +"_SEQ.NEXTVAL FROM DUAL";

  rs = RsdRecordset( strSql );
                                                                                                  
  if( rs.MoveNext() )
    NewID = rs.value(0);
  end;  

  return NewID;
end;   




/* Печать шапки протокола */
private macro PrintHeader( ImportFileName : string )
[                  ПРОТОКОЛ ОБНОВЛЕНИЯ СПРАВОЧНИКА ЛИЦ,
             ПРИЧАСТНЫХ К ТЕРРОРИСТИЧЕСКОЙ ДЕЯТЕЛЬНОСТИ (ФСФМ РФ)

Дата и время запуска процедуры импорта: ########## #####
Исполнитель: #### #
Файл импорта: #

] (string(date:f), string(time:f), {oper}, GetNameOper({oper}), ImportFileName );
end;

private macro AddString(list, num , str)
  var isExists = false;
  var i = 0;
  while (i < num)
   if(str == list(i))
     isExists = true;
   end;
     i = i + 1;
  end;

  if(isExists)
    list(num) = "";
  else
    list(num) = str;
  end;
end;


private macro SplitAndInsertName( Name:String, TerroristID  )
  var pos = 0;
  var TempStr = "";
  var SaveStr = "";

  var StrList = TArray;
  var numArray = 0;
  var i = 0;

  // Приводим строку к верхнему регистру.
  TempStr = StrUpr(Name);
  // Исключаем из строки все подстроки "A.K.A.".
  TempStr = StrSubst(TempStr, "A.K.A.", "");
  TempStr = StrSubst(TempStr, "(PREVIOUSLY LISTED AS)", "");
  TempStr = StrSubst(TempStr, "TRANSLITERATIONS:", "");
  TempStr = StrSubst(TempStr, "TRANSLATION:", "");
  TempStr = StrSubst(TempStr, "(FORMERLY LISTED AS)", "");
  TempStr = StrSubst(TempStr, "ЗАНКИШИЕВ ЭДУАРД (РОБЕРТ) ВЛАДИМИРОВИЧ", "ЗАНКИШИЕВ ЭДУАРД ВЛАДИМИРОВИЧ (ЗАНКИШИЕВ РОБЕРТ ВЛАДИМИРОВИЧ)");

  // Заменяем скобки на точки с запятой.
  TempStr = StrSubst(TempStr, "(", ";");
  TempStr = StrSubst(TempStr, ")", ";");

  // Исключаем точки с запятой в начале и конце строки.
  TempStr = Trim(TempStr);
  if (SubStr(TempStr, 1, 1) == ";")
    TempStr = SubStr(TempStr, 2 );
  end;

  if (SubStr(TempStr, StrLen(TempStr), 1) == ";")
    TempStr = SubStr(TempStr, 1, StrLen(TempStr) -1);
  end;

  // Исключаем двойные точки с запятой и лишние пробелы
  while (Index ( TempStr, ";;" ) )
    TempStr = StrSubst(TempStr, ";;", ";");
  end;
  while (Index ( TempStr, "  " ) )
    TempStr = StrSubst(TempStr, "  ", " ");
  end;

  pos = StrBrk ( TempStr, ";" );
  while(pos)
    SaveStr = SubStr(TempStr, 1, pos - 1); 
    SaveStr = Trim(SaveStr);
    if(StrLen(SaveStr))
      AddString(StrList, numArray , SaveStr);
      numArray = numArray + 1 ;
    end;

    TempStr = SubStr(TempStr, pos + 1);
    pos = StrBrk ( TempStr, ";" )
  end;

  SaveStr = Trim(TempStr); 

  if(StrLen(SaveStr))
    AddString(StrList, numArray , SaveStr);
    numArray = numArray + 1 ;
  end;

  i = 0;
  while (i < numArray)
    if(StrList(i) != "")
      ClearRecord( terr_kfm_aka_tmp_file );
      terr_kfm_aka_tmp_file.TerroristID = TerroristID;
      terr_kfm_aka_tmp_file.Name = StrList(i);
      terr_kfm_aka_tmp_file.NameID =  GetSequentNewID( "dterr_kfm_aka_dbt" ); 
      Insert( terr_kfm_aka_tmp_file );
    end;

    i = i + 1;
  end;

end;

private macro SplitString( list, num )
  var str = "";
  var i = 0;

  while (i < num)
    str = str + list(i);
    i = i + 1;
  end;

  return str;
end;

private macro FillRecord( Num : integer)
  terrinfo_tmp_file.Description  = SplitString(Description , Num);
                                                
  terrinfo_tmp_file.Citizen      = SplitString(Citizen     , Num);
                                                
  terrinfo_tmp_file.Resolution   = SplitString(Resolution  , Num);
                                                
  terrinfo_tmp_file.LegalAddress = SplitString(LegalAddress, Num);
  terrinfo_tmp_file.Address      = SplitString(Address     , Num);
                                                
  terrinfo_tmp_file.Founder      = SplitString(Founder     , Num);
  terrinfo_tmp_file.Director     = SplitString(Director    , Num);
                                                
  terrinfo_tmp_file.OKPO         = SplitString(OKPO        , Num);
  terrinfo_tmp_file.RegNumber    = SplitString(RegNumber   , Num);
                                                
  terrinfo_tmp_file.PaperSeries  = SplitString(PaperSeries , Num);
  terrinfo_tmp_file.PaperNumber  = SplitString(PaperNumber , Num);
  terrinfo_tmp_file.NameDocum    = SplitString(NameDocum   , Num);
  terrinfo_tmp_file.BirthPlace   = SplitString(BirthPlace  , Num);

end;

private macro FillListRecord( KFMList, Type : integer, Num : integer)

  AddString(Description, Num, Trim(KFMList.DESCRIPT) );
  
  if(FldIndex(KFMList,"CITIZEN") != -1)
    AddString(Citizen, Num, Trim(KFMList.CITIZEN));
  else
    Citizen(Num)    = "";
  end;

  AddString(Resolution, Num, Trim(KFMList.TERRTYPE));
  
  AddString(LegalAddress, Num, Trim(KFMList.AMR));
  AddString(Address, Num, Trim(KFMList.ADRESS));
  AddString(Founder, Num, Trim(KFMList.FOUNDER));
  AddString(Director, Num, Trim(KFMList.DIRECTOR));
    
  if( (Type == 1) or (Type == 2) )
    AddString(OKPO, Num, Trim(KFMList.SD));
    AddString(RegNumber, Num, Trim(KFMList.RG));

    PaperSeries(Num)  = "";
    PaperNumber(Num)  = "";
    NameDocum(Num)    = "";
    BirthPlace(Num)   = "";
  elif( Type == 3 )
    AddString(PaperSeries, Num, Trim(KFMList.SD));
    AddString(PaperNumber, Num, Trim(KFMList.RG));
    AddString(NameDocum, Num, Trim(KFMList.VD));
    AddString(BirthPlace, Num, Trim(KFMList.MR));

    OKPO(Num)      = "";
    RegNumber(Num) = "";
  end;
end;


private macro DefineCharType( terr_kfm_tmp_file )
  if( terr_kfm_tmp_file.Type == 3 ) terr_kfm_tmp_file.chType = "Ф"; end;
  return terr_kfm_tmp_file.chType = "Ю";
end;


private macro ProcessRecord( KFMList, DateUpdate : date, ProgressNumber : @integer )
  
  var stat : bool;
  var Number : integer;
  var SaveTerroristID = 0;
  var FullTerrName = "";
  var statins = true;

  Number = 0;

  /* Сначала заполним данные о террористе */
  ClearRecord( terr_kfm_tmp_file );
  terr_kfm_tmp_file.Number     = int(KFMList.NUMBER);
  terr_kfm_tmp_file.Type       = int(KFMList.TU);
  terr_kfm_tmp_file.TerrorType = int(KFMList.TERROR);
  
  terr_kfm_tmp_file.CodCR      = Trim(KFMList.KODCR);
  terr_kfm_tmp_file.CodCN      = Trim(KFMList.KODCN);
  terr_kfm_tmp_file.INN        = Trim(KFMList.ND);

  if( (terr_kfm_tmp_file.Type != 1) and (terr_kfm_tmp_file.Type != 2) )
    terr_kfm_tmp_file.CodeDocum  = Trim(KFMList.KD);
  end;

  terr_kfm_tmp_file.RegDate    = toDate(KFMList.GR);
  terr_kfm_tmp_file.BirthYear  = int(KFMList.YR);

  DefineCharType( terr_kfm_tmp_file );

  terr_kfm_tmp_file.DateInput  = toDate(KFMList.CB_DATE);
  terr_kfm_tmp_file.DateDelete = toDate(KFMList.CE_DATE);
  terr_kfm_tmp_file.DateUpdate = DateUpdate;
  terr_kfm_tmp_file.TerroristID =  GetSequentNewID( "dterr_kfm_dbt" );

  stat = Insert( terr_kfm_tmp_file );
  
  if(stat)
    while( (stat) and (terr_kfm_tmp_file.Number == int(KFMList.NUMBER)) )

      ProgressNumber = ProgressNumber + 1;
      UseProgress( ProgressNumber );
      FillListRecord( KFMList, terr_kfm_tmp_file.Type, Number );
      Number = Number + 1;

      SaveTerroristID = terr_kfm_tmp_file.TerroristID;

      if( (FullTerrName != "") and ( trim(KFMList.TERROR) != "") )
        FullTerrName = FullTerrName + ";";
      end;

      FullTerrName = FullTerrName + Trim(KFMList.NAMEU);  

      if( stat ) stat = next( KFMList ); end;
    
    end;
      ClearRecord( terrinfo_tmp_file );

      FillRecord( Number );

      terrinfo_tmp_file.TerroristID = terr_kfm_tmp_file.TerroristID;
      terrinfo_tmp_file.Number      = 1;
      statins = Insert( terrinfo_tmp_file );
      
      if(not statins )
        stat = statins;
      end;

      SplitAndInsertName( FullTerrName, SaveTerroristID ); 
  end;
  

  return stat;

end;

private macro DeleteTerorRec(TerroristID)

  var params:TArray;
  params = makeArray( SQLParam( "TerroristID", TerroristID) );

  execSQL( "DELETE FROM dterr_kfm_dbt WHERE t_TerroristID = :TerroristID", params, FALSE );
  execSQL( "DELETE FROM dterrinfo_dbt WHERE t_TerroristID = :TerroristID", params, FALSE );
  execSQL( "DELETE FROM dterr_kfm_aka_dbt WHERE t_TerroristID = :TerroristID", params, FALSE );
end;

private macro CopyTerorRec(TerroristID)
  var params:TArray;

  params = makeArray( SQLParam( "", "dterr_kfm_tmp"), 
                      SQLParam( "", "dterr_kfm_dbt"),
                      SQLParam( "", "WHERE t_TerroristID = " + TerroristID));

  execStoredFunc( "rsb_tools.copy_table", V_UNDEF, params);


  params = makeArray( SQLParam( "", "dterrinfo_tmp"), 
                      SQLParam( "", "dterrinfo_dbt"),
                      SQLParam( "", "WHERE t_TerroristID = " + TerroristID));

  execStoredFunc( "rsb_tools.copy_table", V_UNDEF, params);

  params = makeArray( SQLParam( "", "dterr_kfm_aka_tmp"), 
                      SQLParam( "", "dterr_kfm_aka_dbt"),
                      SQLParam( "", "WHERE t_TerroristID = " + TerroristID));

  execStoredFunc( "rsb_tools.copy_table", V_UNDEF, params);

end;

private macro UpdateTerrorist(DbtTerroristID, TmpTerroristID )
  var params:TArray;
  
  // Обновить во временных таблицах ид. террориста
  params = makeArray( SQLParam( "DTERRORISTID", DbtTerroristID ),
                      SQLParam( "TTERRORISTID", TmpTerroristID ) );

  execSQL( "UPDATE dterr_kfm_tmp SET t_TerroristID = :newTerroristID WHERE t_TerroristID = :oldTerroristID", params, FALSE );


  params = makeArray( SQLParam( "DTERRORISTID", DbtTerroristID ),
                      SQLParam( "TTERRORISTID", TmpTerroristID ) );

  execSQL( "UPDATE dterrinfo_tmp SET t_TerroristID = :newTerroristID WHERE t_TerroristID = :oldTerroristID", params, FALSE );

  execSQL( "UPDATE dterr_kfm_aka_tmp SET t_TerroristID = :newTerroristID WHERE t_TerroristID = :oldTerroristID", params, FALSE );

  // Заменить это значение на новое во всех записях результатов проверки субъектов на терроризм. 

//  println("OldID = ", DbtTerroristID, " TmpID = ", TmpTerroristID );

  // Удалить из постоянных таблиц данные, относящиеся к обрабатываемому террористу terr_kfm_Rec.TerroristID, и на их место копировать новые данные о террористе из временных таблиц  
  DeleteTerorRec(DbtTerroristID);
  CopyTerorRec(DbtTerroristID);

end;

private macro  ProcessDBRecords();
  var stat = 0;

  var params:TArray;
  var rs:object;
  var TmpTerroristID = 0;
  var CmpName = 0;
  var CmpInfo = 0;

  var Nrec = 0;
  rs = execSQLselect( " SELECT count(1) FROM dterr_kfm_dbt", NULL, FALSE );

  if( rs.moveNext() )
    Nrec = int(rs.value(0));
  end;

  var i = 0;

  InitProgress( Nrec, " ~Alt-Break~ Прервать", "Обработка записей, не изменившихся с момента прошлого импорта справочника" );


  var select = " SELECT tmp.T_TERRORISTID " +
               " FROM dterr_kfm_tmp tmp, dterr_kfm_dbt kfm " +
               " WHERE kfm.T_TERRORISTID = :TERRORIST  " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodCR) = RSI_RsbFMKernel.NormString(kfm.t_CodCR) " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodCN) = RSI_RsbFMKernel.NormString(kfm.t_CodCN) " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_INN)   = RSI_RsbFMKernel.NormString(kfm.t_INN) " +
               "   AND tmp.t_RegDate   =  kfm.t_RegDate " +
               "   AND tmp.t_BirthYear = kfm.t_BirthYear " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodeDocum) = RSI_RsbFMKernel.NormString(kfm.t_CodeDocum) " +
               "   AND RSI_RsbFMKernel.NormString((SELECT NVL(LISTAGG (t_Name) WITHIN GROUP (ORDER BY T_NAMEID), CHR(1)) " +
               "        FROM dterr_kfm_aka_dbt WHERE T_TERRORISTID = kfm.T_TERRORISTID )) = " +
               "        RSI_RsbFMKernel.NormString((SELECT NVL(LISTAGG (t_Name) WITHIN GROUP (ORDER BY T_NAMEID), CHR(1)) " +
               "        FROM dterr_kfm_aka_tmp WHERE T_TERRORISTID = tmp.T_TERRORISTID )) ";
                
                /*
  var select = " SELECT tmp.T_TERRORISTID " +
               " FROM dterr_kfm_tmp tmp, dterr_kfm_dbt kfm, dterr_kfm_aka_dbt aka_rec " +
               " WHERE kfm.T_TERRORISTID = :TERRORIST " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodCR) = RSI_RsbFMKernel.NormString(kfm.t_CodCR) " +  
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodCN) = RSI_RsbFMKernel.NormString(kfm.t_CodCN) " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_INN)   = RSI_RsbFMKernel.NormString(kfm.t_INN) " +
               "   AND tmp.t_RegDate   =  kfm.t_RegDate " +
               "   AND tmp.t_BirthYear = kfm.t_BirthYear " +
               "   AND RSI_RsbFMKernel.NormString(tmp.t_CodeDocum) = RSI_RsbFMKernel.NormString(kfm.t_CodeDocum) " +
               "   AND aka_rec.T_TERRORISTID = kfm.T_TERRORISTID " +
               "   AND aka_rec.T_NAMEID = (SELECT MIN(T_NAMEID) FROM dterr_kfm_aka_dbt aka3 WHERE  aka3.T_TERRORISTID = aka_rec.T_TERRORISTID) " +
               "   AND RSI_RsbFMKernel.NormString(aka_rec.t_Name) IN (SELECT RSI_RsbFMKernel.NormString(t_Name)  " +
               "                                   FROM dterr_kfm_aka_tmp aka " +
               "                                   WHERE aka.T_TERRORISTID = tmp.T_TERRORISTID " +
               "                                   AND T_NAMEID = (SELECT MIN(T_NAMEID) FROM dterr_kfm_aka_tmp aka2 WHERE  aka2.T_TERRORISTID = aka.T_TERRORISTID)) ";  


  
  var selectName = "SELECT count(1) " +
                   " FROM (SELECT COUNT (1) AS Num " +
                   "         FROM dterr_kfm_aka_dbt dbt, dterr_kfm_aka_tmp tmp " +
                   "        WHERE     dbt.T_TERRORISTID = :DTERRORISTID1 " +
                   "          AND tmp.T_TERRORISTID = :TTERRORISTID1 " +
                   "          AND RSI_RsbFMKernel.NormString(dbt.T_NAME) = RSI_RsbFMKernel.NormString(tmp.T_NAME)) countName," +
                   "      (SELECT COUNT (1) AS Num " +
                   "         FROM dterr_kfm_aka_dbt dbt " +
                   "        WHERE dbt.T_TERRORISTID = :DTERRORISTID2) countDbt, " +
                   "      (SELECT COUNT (1) AS Num " +
                   "         FROM dterr_kfm_aka_tmp tmp " +
                   "        WHERE tmp.T_TERRORISTID = :TTERRORISTID2) countTmp " +
                   " WHERE countName.Num = countDbt.Num AND countDbt.Num = countTmp.Num ";
*/


  var selectInfo = " SELECT count(1) " +
                   " FROM( " +
                   " SELECT NVL(LISTAGG (t_LegalAddress) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_LegalAddress, " +
                   " NVL(LISTAGG (t_Address) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_Address, " +
                   " NVL(LISTAGG (t_OKPO) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_OKPO, " +
                   " NVL(LISTAGG (t_RegNumber) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_RegNumber, " +
                   " NVL(LISTAGG (t_PaperSeries) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_PaperSeries, " +
                   " NVL(LISTAGG (t_PaperNumber) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_PaperNumber, " +
                   " NVL(LISTAGG (t_NameDocum) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_NameDocum " +
                   " FROM dterrinfo_dbt " +
                   " WHERE T_TERRORISTID = :DTERRORISTID " +
                   " ) dbt, " +
                   " ( " +
                   " SELECT NVL(LISTAGG (t_LegalAddress) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_LegalAddress, " +
                   " NVL(LISTAGG (t_Address) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_Address, " +
                   " NVL(LISTAGG (t_OKPO) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_OKPO, " +
                   " NVL(LISTAGG (t_RegNumber) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_RegNumber, " +
                   " NVL(LISTAGG (t_PaperSeries) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_PaperSeries, " +
                   " NVL(LISTAGG (t_PaperNumber) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_PaperNumber, " +
                   " NVL(LISTAGG (t_NameDocum) WITHIN GROUP (ORDER BY T_NUMBER), CHR(1)) AS t_NameDocum " +
                   " FROM dterrinfo_tmp " +
                   " WHERE T_TERRORISTID = :TTERRORISTID " +
                   " ) tmp " +
                   " WHERE RSI_RsbFMKernel.NormString(dbt.t_LegalAddress) = RSI_RsbFMKernel.NormString(tmp.t_LegalAddress) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_Address) = RSI_RsbFMKernel.NormString(tmp.t_Address) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_OKPO) = RSI_RsbFMKernel.NormString(tmp.t_OKPO) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_RegNumber) = RSI_RsbFMKernel.NormString(tmp.t_RegNumber) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_PaperSeries) = RSI_RsbFMKernel.NormString(tmp.t_PaperSeries) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_PaperNumber) = RSI_RsbFMKernel.NormString(tmp.t_PaperNumber) " +
                   "   AND RSI_RsbFMKernel.NormString(dbt.t_NameDocum) = RSI_RsbFMKernel.NormString(tmp.t_NameDocum) " ;



  ClearRecord( terr_kfm_file );
  stat = GetGE( terr_kfm_file );

  while( stat  )
    i = i + 1;
    UseProgress( i );

    TmpTerroristID = 0;
    CmpName = 0;
    CmpInfo = 0;

    params = makeArray( SQLParam( "TERRORIST", terr_kfm_file.TerroristID ));

    rs = execSQLselect( select, params, FALSE );
   
    if( rs.moveNext() )
      TmpTerroristID = rs.value(0);
    end;


    if(TmpTerroristID > 0)
      //  Проверить, наименования террориста 
/*
      params = makeArray( SQLParam( "DTERRORISTID1", terr_kfm_file.TerroristID ),
                          SQLParam( "TTERRORISTID1", TmpTerroristID            ),
                          SQLParam( "DTERRORISTID2", terr_kfm_file.TerroristID ),
                          SQLParam( "TTERRORISTID2", TmpTerroristID            ) );

      
      rs = execSQLselect( selectName, params, FALSE );

      if( rs.moveNext() )
        CmpName = rs.value(0);
      end;

      if(CmpName > 0)
*/
        // 4.1.2.1.3. Проверить, что информация в постоянной таблице dterr_kfm_dbt для terr_kfm_Rec.TerroristID, полностью совпадает с информацией по террористу terr_kfm_tmp_Rec.TerroristID во временной таблице dterr_kfm_tmp
        params = makeArray( SQLParam( "DTERRORISTID", terr_kfm_file.TerroristID ),
                            SQLParam( "TTERRORISTID", TmpTerroristID            ) );

        
        rs = execSQLselect( selectInfo, params, FALSE );

        if( rs.moveNext() )
          CmpInfo = rs.value(0);
        end;

        if(CmpInfo > 0)

          params = makeArray( SQLParam( "DateUpdate"  , terr_kfm_file.DateUpdate  ),
                              SQLParam( "TTERRORISTID", TmpTerroristID            ) );
          
          execSQL( "UPDATE dterr_kfm_tmp SET t_DateUpdate = :DateUpdate WHERE t_TerroristID = :oldTerroristID", params, FALSE );

          // Обновить во временных таблицах ид. террориста
          UpdateTerrorist(terr_kfm_file.TerroristID, TmpTerroristID );
         
        end;
//      end;

    end;
    
    stat = next( terr_kfm_file );
  end;

  RemProgress();

  return stat;
end;


private macro ProcessChangeRecords()

  var rs:object;
  var DbtTerroristID = 0;
  var TmpTerroristID = 0;


  var Nrec = 6;
  var i = 0;

  InitProgress( Nrec, " ~Alt-Break~ Прервать", "Обработка записей, изменившихся с момента прошлого импорта справочника" );

  // Поиск по t_INN
  var select = " SELECT dbt.t_TerroristID, tmp.t_TerroristID " +            
               " FROM dterr_kfm_dbt dbt, dterr_kfm_tmp tmp " +            
               " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3) " +            
               "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3) " +            
               "   AND dbt.t_INN <> chr(1) " +            
               "   AND dbt.t_INN =  tmp.t_INN " +            
               "   AND 1=(SELECT COUNT(1) " +            
               "            FROM dterr_kfm_dbt dbt2, dterr_kfm_tmp tmp2  " +            
               "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID " +            
               "             AND dbt2.t_INN <> chr(1) " +            
               "             AND dbt2.t_INN =  tmp2.t_INN " +            
               "             AND tmp2.t_TerroristID = tmp.t_TerroristID) " +            
               " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID ";

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  i = i + 1;
  UseProgress( i );

  // Поиск по t_OKPO 
  select = " SELECT DISTINCT dbt.t_TerroristID, tmp.t_TerroristID     " +            
           " FROM dterrinfo_dbt dbt, dterrinfo_tmp tmp                " +
           " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3) " +           
           "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3) " +            
           "   AND dbt.t_OKPO <> chr(1)                               " +
           "   AND dbt.t_OKPO =  tmp.t_OKPO                           " +
           "   AND 1=(SELECT COUNT(DISTINCT(tmp2.t_TerroristID))      " +
           "            FROM dterrinfo_dbt dbt2, dterrinfo_tmp tmp2   " +
           "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID " +
           "             AND dbt2.t_OKPO <> chr(1)                    " +
           "             AND dbt2.t_OKPO =  tmp2.t_OKPO               " +
           "             AND tmp2.t_TerroristID = tmp.t_TerroristID)  " +
           " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID            " ;

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  i = i + 1;
  UseProgress( i );

  // Поиск по t_PaperSeries и t_PaperNumber
  select = " SELECT DISTINCT dbt.t_TerroristID, tmp.t_TerroristID                                                           " +            
           " FROM dterrinfo_dbt dbt, dterrinfo_tmp tmp, dterr_kfm_aka_dbt aka_dbt, dterr_kfm_aka_tmp aka_tmp                " +
           " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3)                              " +           
           "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3)                              " +           
           "   AND dbt.t_PaperSeries <> chr(1)                                                                              " +
           "   AND dbt.t_PaperNumber <> chr(1)                                                                              " +
           "   AND dbt.t_PaperSeries =  tmp.t_PaperSeries                                                                   " +
           "   AND dbt.t_PaperNumber =  tmp.t_PaperNumber                                                                   " +
           "   AND aka_dbt.t_TerroristID =  dbt.t_TerroristID                                                               " +
           "   AND aka_tmp.t_TerroristID = tmp.t_TerroristID                                                                " +
           "   AND RSI_RsbFMKernel.NormString(aka_dbt.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp.T_NAME)                  " +
           "   AND 1=(SELECT COUNT(DISTINCT(tmp2.t_TerroristID))                                                            " +
           "            FROM dterrinfo_dbt dbt2, dterrinfo_tmp tmp2, dterr_kfm_aka_dbt aka_dbt2, dterr_kfm_aka_tmp aka_tmp2 " +
           "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID                                                       " +
           "             AND dbt2.t_PaperSeries <> chr(1)                                                                   " +
           "             AND dbt2.t_PaperNumber <> chr(1)                                                                   " +
           "             AND dbt2.t_PaperSeries =  tmp2.t_PaperSeries                                                       " +           
           "             AND dbt2.t_PaperNumber =  tmp2.t_PaperNumber                                                       " +
           "             AND aka_dbt2.t_TerroristID =  dbt2.t_TerroristID                                                   " +
           "             AND aka_tmp2.t_TerroristID = tmp2.t_TerroristID                                                    " +
           "             AND RSI_RsbFMKernel.NormString(aka_dbt2.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp2.T_NAME)      " +
           "             AND tmp2.t_TerroristID = tmp.t_TerroristID)                                                        " +
           " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID                                                                  " ;
           
  rs = execSQLselect( select, NULL, FALSE );
  
  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  i = i + 1;
  UseProgress( i );

  // Поиск по t_RegNumber
  select = " SELECT DISTINCT dbt.t_TerroristID, tmp.t_TerroristID                                                           " +            
           " FROM dterrinfo_dbt dbt, dterrinfo_tmp tmp, dterr_kfm_aka_dbt aka_dbt, dterr_kfm_aka_tmp aka_tmp                " +
           " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3)                              " +           
           "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3)                              " +           
           "   AND dbt.t_RegNumber <> chr(1)                                                                                " +
           "   AND dbt.t_RegNumber =  tmp.t_RegNumber                                                                       " +
           "   AND aka_dbt.t_TerroristID =  dbt.t_TerroristID                                                               " +
           "   AND aka_tmp.t_TerroristID = tmp.t_TerroristID                                                                " +
           "   AND RSI_RsbFMKernel.NormString(aka_dbt.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp.T_NAME)                  " +
           "   AND 1=(SELECT COUNT(DISTINCT(tmp2.t_TerroristID))                                                            " +
           "            FROM dterrinfo_dbt dbt2, dterrinfo_tmp tmp2, dterr_kfm_aka_dbt aka_dbt2, dterr_kfm_aka_tmp aka_tmp2 " +
           "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID                                                       " +
           "             AND dbt2.t_RegNumber <> chr(1)                                                                     " +           
           "             AND dbt2.t_RegNumber =  tmp2.t_RegNumber                                                           " +
           "             AND aka_dbt2.t_TerroristID =  dbt2.t_TerroristID                                                   " +
           "             AND aka_tmp2.t_TerroristID = tmp2.t_TerroristID                                                    " +
           "             AND RSI_RsbFMKernel.NormString(aka_dbt2.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp2.T_NAME)      " +
           "             AND tmp2.t_TerroristID = tmp.t_TerroristID)                                                        " +
           " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID                                                                  " ;

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  i = i + 1;
  UseProgress( i );

  // Поиск по t_LegalAddress  ?? Somalia
  select = " SELECT DISTINCT dbt.t_TerroristID, tmp.t_TerroristID                                                           " +            
           " FROM dterrinfo_dbt dbt, dterrinfo_tmp tmp, dterr_kfm_aka_dbt aka_dbt, dterr_kfm_aka_tmp aka_tmp                " +
           " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3)                              " +           
           "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3)                              " +           
           "   AND dbt.t_LegalAddress <> chr(1)                                                                             " +
           "   AND dbt.t_LegalAddress =  tmp.t_LegalAddress                                                                 " +
           "   AND aka_dbt.t_TerroristID =  dbt.t_TerroristID                                                               " +
           "   AND aka_tmp.t_TerroristID = tmp.t_TerroristID                                                                " +
           "   AND RSI_RsbFMKernel.NormString(aka_dbt.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp.T_NAME)                  " +
           "   AND 1=(SELECT COUNT(DISTINCT(tmp2.t_TerroristID))                                                            " +
           "            FROM dterrinfo_dbt dbt2, dterrinfo_tmp tmp2, dterr_kfm_aka_dbt aka_dbt2, dterr_kfm_aka_tmp aka_tmp2 " +
           "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID                                                       " +
           "             AND dbt2.t_LegalAddress <> chr(1)                                                                  " +           
           "             AND dbt2.t_LegalAddress =  tmp2.t_LegalAddress                                                     " +
           "             AND aka_dbt2.t_TerroristID =  dbt2.t_TerroristID                                                   " +           
           "             AND aka_tmp2.t_TerroristID = tmp2.t_TerroristID                                                    " +
           "             AND RSI_RsbFMKernel.NormString(aka_dbt2.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp2.T_NAME)      " +
           "             AND tmp2.t_TerroristID = tmp.t_TerroristID)                                                        " +
           " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID                                                                  " ;

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  i = i + 1;
  UseProgress( i );

  // Поиск по t_Address
  select = " SELECT DISTINCT dbt.t_TerroristID, tmp.t_TerroristID                                                           " +            
           " FROM dterrinfo_dbt dbt, dterrinfo_tmp tmp, dterr_kfm_aka_dbt aka_dbt, dterr_kfm_aka_tmp aka_tmp                " +
           " WHERE dbt.t_TerroristID NOT IN(SELECT tmp3.t_TerroristID FROM dterr_kfm_tmp tmp3)                              " +           
           "   AND tmp.t_TerroristID NOT IN(SELECT dbt3.t_TerroristID FROM dterr_kfm_dbt dbt3)                              " +           
           "   AND dbt.t_Address <> chr(1)                                                                                  " +
           "   AND dbt.t_Address =  tmp.t_Address                                                                           " +
           "   AND aka_dbt.t_TerroristID =  dbt.t_TerroristID                                                               " +
           "   AND aka_tmp.t_TerroristID = tmp.t_TerroristID                                                                " +
           "   AND RSI_RsbFMKernel.NormString(aka_dbt.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp.T_NAME)                  " +
           "   AND 1=(SELECT COUNT(DISTINCT(tmp2.t_TerroristID))                                                            " +
           "            FROM dterrinfo_dbt dbt2, dterrinfo_tmp tmp2, dterr_kfm_aka_dbt aka_dbt2, dterr_kfm_aka_tmp aka_tmp2 " +
           "           WHERE dbt2.t_TerroristID <> tmp2.t_TerroristID                                                       " +
           "             AND dbt2.t_Address <> chr(1)                                                                       " +           
           "             AND dbt2.t_Address =  tmp2.t_Address                                                               " +
           "             AND aka_dbt2.t_TerroristID =  dbt2.t_TerroristID                                                   " +           
           "             AND aka_tmp2.t_TerroristID = tmp2.t_TerroristID                                                    " +
           "             AND RSI_RsbFMKernel.NormString(aka_dbt2.T_NAME) = RSI_RsbFMKernel.NormString(aka_tmp2.T_NAME)      " +
           "             AND tmp2.t_TerroristID = tmp.t_TerroristID)                                                        " +
           " ORDER BY dbt.t_TerroristID, tmp.t_TerroristID                                                                  " ;

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    DbtTerroristID = rs.value(0);
    TmpTerroristID = rs.value(1);

    UpdateTerrorist(DbtTerroristID, TmpTerroristID );
  end;

  RemProgress();
end;

private macro ProcessNewRecords()

  var rs:object;
  var TmpTerroristID = 0;

  var Nrec = 0;
  rs = execSQLselect( " SELECT count(1) FROM dterr_kfm_tmp WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_dbt)", NULL, FALSE );

  if( rs.moveNext() )
    Nrec = int(rs.value(0));
  end;

  var i = 0;

  InitProgress( Nrec, " ~Alt-Break~ Прервать", "Обработка новых записей справочника" );

  var select = " SELECT t_TerroristID FROM dterr_kfm_tmp WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_dbt) " ;

  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    i = i + 1;
    UseProgress( i );

    TmpTerroristID = rs.value(0);

    CopyTerorRec(TmpTerroristID);
  end;

  RemProgress();
end;

private macro ProcessDelRecords()
  
  //  Удалить из постоянных таблиц - terr_kfm_aka.dbt, terrinfo.dbt, terr_kfm.dbt - все записи, для которых во временных таблицах нет соответстующего ид. террориста
  execSQL( "DELETE FROM dterr_kfm_dbt WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_tmp)", NULL, FALSE );
  execSQL( "DELETE FROM dterrinfo_dbt WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_tmp)", NULL, FALSE );
  execSQL( "DELETE FROM dterr_kfm_aka_dbt WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_tmp)", NULL, FALSE );

  // Удалить из постоянных таблиц, содержащих результаты проверки, (dfmkfmchk_dbt, dfmofacchk_dbt, dfmptadrchk_dbt, dfmpartychk_dbt) записи, относящиеся к террористам, 
  // удаленным из справочника ФСФМ, отсутствующим во временном файле dterr_kfm_tmp
  execSQL( "DELETE FROM dfmpartychk_dbt WHERE T_PARTYID IN (SELECT T_PARTYID FROM dfmkfmchk_dbt WHERE T_TERRORISTID NOT IN (SELECT t_TerroristID FROM dterr_kfm_dbt) )", NULL, FALSE );
  execSQL( "DELETE FROM dfmkfmchk_dbt WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_dbt)", NULL, FALSE );
  execSQL( "DELETE FROM dfmptadrchk_dbt WHERE t_TerroristID NOT IN (SELECT t_TerroristID FROM dterr_kfm_dbt)", NULL, FALSE );

end;

private macro ProcessTmpRecords(KFMList, DateUpdate)

  var stat : bool;
  var Nrec : integer, i : integer;

  stat = true;

  // Заполнение временных таблиц
  Nrec = NRecords( KFMList );
  i = 0;

  InitProgress( Nrec, " ~Alt-Break~ Прервать", "Импорт справочника во временные таблицы" );
  rewind( KFMList );
  stat = next( KFMList );
  while( stat )
      
    stat = ProcessRecord( KFMList, DateUpdate, @i );
    
  end;
  RemProgress();

end;

private macro RunImport( KFMList, DateUpdate : date, ListNumber : string, ListDate : date)

  // Импорт справочника во временные таблицы
  ProcessTmpRecords(KFMList, DateUpdate);

  // Обработать записи постоянных таблиц, не изменившиеся с момента прошлого импорта справочника
  ProcessDBRecords();
  
  // Обработать записи постоянных таблиц, изменившиеся с момента прошлого импорта справочника
  ProcessChangeRecords();

  // Обработать новые (добавленные) записи справочника, т.е. те записи временной таблицы, для которых ранее не нашлось соответствия в постоянных таблицах
  ProcessNewRecords();

  // Обработать старые (удаленные) записи справочника
  ProcessDelRecords();


//  SetDefaultRegistryValue( "ФИНМОНИТОРИНГ\\ПЕРЕЧЕНЬ_ДАТА_ОБНОВЛЕНИЯ",   string(DateUpdate:f) );
//  SetDefaultRegistryValue( "ФИНМОНИТОРИНГ\\ПЕРЕЧЕНЬ_ТЕРРОРИСТОВ_НОМЕР", string(ListNumber) );
  SetDefaultRegistryValue( "ФИНМОНИТОРИНГ\\ПЕРЕЧЕНЬ_ТЕРРОРИСТОВ_ДАТА",  string(DateUpdate:f) ); //  ListDate



  execSQL( "DELETE FROM DFMIMPKFMPRM_DBT", NULL, FALSE );
  var params = makeArray( SQLParam( "ListDate"  , ListDate  ),
                          SQLParam( "ListNumber", ListNumber) );

  execSQL("INSERT INTO DFMIMPKFMPRM_DBT (t_ListDate, t_ListNumber) VALUES(:ListDate, :ListNumber)", params, FALSE );

end;



private macro ImportFun( ImportFileName : string, DateUpdate : date, ListNumber : string, ListDate : date )
  
  file KFMList() dbf;
  var cmd : RsdCommand;


  PrintHeader( ImportFileName );
  if( not open(KFMList, ImportFileName) )
    msgbox( "Ошибка при открытии файла " + ImportFileName );
    exit(1);
  else
  
    if( not Clone(terrinfo_tmp_file) or not Clone(terr_kfm_tmp_file) or not Clone(terr_kfm_aka_tmp_file) /*or not Clone(fmkfmchk_file)*/ )
      msgbox( "Ошибка при пересоздании справочника" );
      exit(1);
    end;

    RunImport( KFMList, DateUpdate, ListNumber, ListDate);
  end;


end;

