/*
$Name:               mfrstep.mac
$Module:            РКО, ББ
$Description:      Шаг "Перевод по счетам МФР ЦАБС"
*/
//-----------------------------------------------------------------------------
// Блок      : 29001 - "Межфилиальные расчеты ЦАБС"
// Шаг       : 10    - "Перевод по счетам МФР ЦАБС"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------

import InsCarryDoc, pm_common, pm_setst, cbsttls, PaymInter, mfrstepmass,
       "bnk_common.mac";

var PaymentObj:RsbPayment;

// Нужно ли переоформлять платеж в ГО?
private macro NeedReissuePmInHead : bool
  var RegPath : string = "CB\\PAYMENTS\\PAYMENTBETWEENDEP\\CHANGEPAYMENTDOC";
  var IsChangePaymentDoc : bool = Bnk_GetRegistryValue(RegPath, V_BOOL, false);

  var Direction : integer = GetOprStatus(OPR_PAYM_DIRECT);
  var IsOutDirection : bool = InList(Direction, OPR_PM_ST_DIR_OUT, OPR_PM_ST_DIR_TRANZIT);

  return ( IsChangePaymentDoc and 
           not PaymentObj.DbFlag and
           IsOutDirection );
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep( doc, primdoc, KindDoc, ID_Operation, ID_Step )

  var stat    :integer = 0; // Статус ошибки
  var NumCarry:integer = 0; // Номер проводки

  /*Для того, чтобы при откате операции автоматически создавалась запись аннулирования платежа в dpmsend_dbt*/
  var isNeedAnalyseCarries = 1; // Нужно проверять проводки
  var backoutQuery = " DECLARE stat number := 0; BEGIN stat := WLD_GGPMSEND.PaymentAutomaticAnnulate( " + PaymentObj.PaymentID + ", " + isNeedAnalyseCarries + " ); END; ";
  if( Opr_ExecSQLQuery( "", backoutQuery ) )
    msgbox( "Ошибка при вставке SQL-запроса для отката" );
    return 1;
  end;
  
  // Если ничего проводить не надо
  if( PaymentObj.Department == PaymentObj.EndDepartment )
    if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    return 0;
  end;

  // Если вид предыдущего шага равен "Перенос на незавершенные"
  if( PM_PrevStepIsAccUnclosed(PaymentObj) )
    PaymentObj.ValueDate = {curdate};
  end;

  //входящий кредитовый тут никогда не может быть проведен
  if (not (PaymentObj.IsExternalIncoming and PaymentObj.IsCredit))
    PaymentObj.StatusInfo = "Исполнен";
  end;

  while( ( stat == 0 ) and ( PaymentObj.Department != PaymentObj.EndDepartment ) )
    stat = StepMFR_CABS( PaymentObj.PaymentID );
    NumCarry = NumCarry + 1;
  end;

  // Если сделали хоть одну проводку, то обнуляем ошибку и даем провести то, что есть
  if( ( stat != 0 ) and ( NumCarry > 1 ) )
    stat = 0;
  end;

  // Если все провели
  if( ( stat == 0 ) and ( PaymentObj.Department == PaymentObj.EndDepartment ) )
    if( NeedReissuePmInHead() )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_REISSUE ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    else
      if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
      end;
    end;
  end;
  
  return stat;

END;

