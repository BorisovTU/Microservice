import "globals.mac", rsd, FIInter;

/* получить ФИО операциониста */
private macro GetOperName( Oper : integer ) : string
  
  var OperName : string;

  file person("person.dbt") key 0;

  OperName = "";

  person.Oper = Oper;
  if( GetEQ(person) )
    
    OperName = person.Name;

  end;

  return OperName;

end;


macro RepRemObjHead(cdate :date, ctime :time )
[        Настройка объектов для изъятия данных по филиалу                        ];
[ #########################################################################      ]({Name_Bank}:w:c);
[                                                                                ];
[        Дата и время выполнения настройки: ########## ########                  ](cdate, ctime);
[        Операционист: ##### ################################################### ]({oper} , GetOperName({oper})); 
[--------------------------------------------------------------------------------];
end;

macro RepRemObj(ObjName, IsExclided, remdate:date, UserString )
 var strExclided;
 if(IsExclided == "X")
   strExclided = "Да"
 else 
   strExclided = "Нет"
 end;

[ Вид объекта: ################################################################# ](ObjName);
[ Объект-исключение: ####                                                        ](strExclided);
[      Дата очистки: ##########                                                  ](remdate);
println(UserString);
[--------------------------------------------------------------------------------];
end;

macro RepRemObjTail(count)
[ Всего видов объектов для очистки: #########                                    ](count);
end;


/*Проверочный отчет по очистке пользовательских полей объектов.*/


macro UsFldsTestReportGeneral( 
reportdate:date, 
reporttime:time, 
countCts:integer, 
countNotes:integer, 
countLnkObj:integer )

[┌────────────────     Проверочный отчет по очистке пользовательских полей объектов      ─────────────────┐];
[│                                                                                                        │];
[│╓─────────────────────────────────────────────────────────────────┬────────────────────────────────────╖│
 │║ Филиал                                                          │  ################################# ║│
 │╙─────────────────────────────────────────────────────────────────┴────────────────────────────────────╜│]
 ({Name_Bank});
  
[│╓─────────────────────────────────────────────────────────────────┬────────────────────────────────────╖│
 │║ Системные дата и время формирования отчета                      │  ############ ###########          ║│
 │╟─────────────────────────────────────────────────────────────────┼────────────────────────────────────╢│
 │║ Количество подлежащих удалению записей категорий                │  ##########                        ║│
 │╟─────────────────────────────────────────────────────────────────┼────────────────────────────────────╢│
 │║ Количество подлежащих удалению записей примечаний               │  ##########                        ║│
 │╟─────────────────────────────────────────────────────────────────┼────────────────────────────────────╢│
 │║ Количество подлежащих удалению записей связей объектов          │  ##########                        ║│
 │╙─────────────────────────────────────────────────────────────────┴────────────────────────────────────╜│]
 (reportdate, reporttime, countCts, countNotes, countLnkObj);
  
end;

/*информации по записям категорий, подлежащих удалению*/
macro CategForDelHead()

[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                                   Подлежащие удалению записи категорий                               ║│
 │╟───────────────┬──────────────────┬──────────────────────┬───────────┬────────────────────────────────╢│
 │║ Название типа │  Идентификатор   │    Наименование      │Дата начала│     Краткое название признака  ║│
 │║  объекта      │     объекта      │      категории       │ действия  │                                ║│]; 

end;


macro CategForDelBody(
  /*ObjTypeName:string,
  ObjID:string,
  CategName:string,
  ValidFromDate:date,
  ShortAttrName:string*/
  )
    
  var rs : RsdRecordset;

  var ObjTypeName:string;
  var ObjID:string;
  var CategName:string;
  var ValidFromDate:date;
  var ShortAttrName:string;

  array OTypeNameArray, ObjIDArray, CategNameArray, ShortNameArray; 
  
  
  rs = RSDRecordset( " SELECT  *  FROM dobjusflds_tmp  WHERE t_UserFldKind = 0 ");  
  
  while (rs.MoveNext)    
    ObjTypeName = rs.value("t_ObjTypeName");
    ObjID       = rs.value("t_ObjectID");    
    CategName   = rs.value("t_UserFldName");
/*    msgbox( "CategName", CategName ); */
    ValidFromDate = rs.value("t_ValidFromDate");
    ShortAttrName = rs.value("t_ShortAttrName");

    StrSplit( ObjTypeName, OTypeNameArray, 15, 15, 2);
    StrSplit( ObjID, ObjIDArray, 18, 18, 2);
    StrSplit( CategName, CategNameArray, 20, 20, 2);
    StrSplit( ShortAttrName, ShortNameArray, 31, 31, 2);

[│╟───────────────┼──────────────────┼──────────────────────┼───────────┼────────────────────────────────╢│
 │║###############│##################│######################│###########│################################║│]
    (OTypeNameArray(0), ObjIDArray(0), CategNameArray(0),  ValidFromDate, ShortNameArray(0) );     

[│║               │                  │                      │           │                                ║│
 │║###############│##################│######################│           │################################║│]
    (OTypeNameArray(1), ObjIDArray(1), CategNameArray(1),  ShortNameArray(1) );
  end;
 
end;

macro CategForDelTail()

[│╙───────────────┴──────────────────┴──────────────────────┴───────────┴────────────────────────────────╜│];

end;

/*информации по записям примечаний, подлежащих удалению*/
macro NotesForDelHead()

[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                                   Подлежащие удалению записи примечаний                              ║│
 │╟───────────────────────┬───────────────────────┬────────────────────────────────────────┬─────────────╢│
 │║ Название типа объекта │ Идентификатор объекта │      Наименование вида примечания      │ Дата начала ║│
 │║                       │                       │                                        │  действия   ║│];

end;


macro NotesForDelBody(
  /*
  ObjTypeName, /*Название типа объекта*/
  ObjID,        /*Идентификатор объекта*/
  NoteKindName, /*Наименование вида примечания*/
  ValidFromDate /*Дата начала действия*/  
  */
)

  var rs : RsdRecordset;

  var ObjTypeName:string;
  var ObjID:string;
  var NoteKindName:string;
  var ValidFromDate:date;  

  array OTypeNameArray, ObjIDArray, NoteKindNameArray;  
  
  rs = RSDRecordset( " SELECT  *  FROM dobjusflds_tmp  WHERE t_UserFldKind = 1 ");  
  
  while (rs.MoveNext)    
    ObjTypeName = rs.value("t_ObjTypeName");
    ObjID       = rs.value("t_ObjectID");    
    NoteKindName   = rs.value("t_UserFldName"); 
    ValidFromDate = rs.value("t_ValidFromDate");    

    StrSplit( ObjTypeName, OTypeNameArray, 23, 23, 2);
    StrSplit( ObjID, ObjIDArray, 23, 23, 2);
    StrSplit( NoteKindName, NoteKindNameArray, 38, 38, 2);    

[│╟───────────────────────┼───────────────────────┼────────────────────────────────────────┼─────────────╢│
 │║#######################│#######################│########################################│#############║│]  
  (OTypeNameArray(0), ObjIDArray(0), NoteKindNameArray(0),  ValidFromDate );
   
[│║                       │                       │                                        │             ║│
 │║#######################│#######################│########################################│             ║│]  
  (OTypeNameArray(1), ObjIDArray(1), NoteKindNameArray(1),  ValidFromDate ); 

  end;
 
end;

macro NotesForDelTail()

[│╙───────────────────────┴───────────────────────┴────────────────────────────────────────┴─────────────╜│];

end;


/*информации по записям связанных объектов, подлежащих удалению*/
macro ObjLinksForDelHead()

[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                            Подлежащие удалению записи связей объектов                                ║│
 │╟────────────────────┬───────────────┬──────────────────┬───────────────┬──────────────────┬───────────╢│
 │║      Связь         │ Тип объекта №1│  Идентификатор   │ Тип объекта №2│  Идентификатор   │Дата начала║│
 │║                    │               │   объекта №1     │               │   объекта №2     │ действия  ║│]; 



end;


macro ObjLinksForDelBody(   
  /*
  ObjTypeName,      /*Название типа объекта*/
  ObjID,            /*Идентификатор объекта*/
  LinkKindName,     /*Наименование вида связи*/
  ValidFromDate,        /*Дата начала действия*/
  ObjLinkTypeName,  /*Название типа объекта связи*/
  ObjLinkID         /*Идентификатор объекта связи*/
  */
)

  var rs : RsdRecordset;

  var ObjTypeName:string;
  var ObjID:string;
  var LinkKindName:string;
  var ValidFromDate:date;
  var ObjLinkTypeName:string;
  var ObjLinkID:string;

  array OTypeNameArray, ObjIDArray, LinkKindNameArray, ObjLinkTypeNameArray, ObjLinkIDArray;

  var i = 0; 
  
  
  rs = RSDRecordset( " SELECT  *  FROM dobjusflds_tmp  WHERE t_UserFldKind = 2 ");  
  
  while (rs.MoveNext)    
    ObjTypeName = rs.value("t_ObjTypeName");
    ObjID       = rs.value("t_ObjectID");    
    LinkKindName   = rs.value("t_UserFldName"); 
    ValidFromDate = rs.value("t_ValidFromDate");
    ObjLinkTypeName = rs.value("t_ObjLinkTypeName");
    ObjLinkID = rs.value("t_ObjLinkID");

    StrSplit( ObjTypeName, OTypeNameArray, 15, 15, 2);
    StrSplit( ObjID, ObjIDArray, 18, 18, 2);
    StrSplit( LinkKindName, LinkKindNameArray, 20, 20, 2);
    StrSplit( ObjLinkTypeName, ObjLinkTypeNameArray, 15, 15, 2);
    StrSplit( ObjLinkID, ObjLinkIDArray, 18, 18, 2);

[│╟────────────────────┼───────────────┼──────────────────┼───────────────┼──────────────────┼───────────╢│
 │║####################│###############│##################│###############│##################│###########║│]  
    (LinkKindNameArray(0), OTypeNameArray(0), ObjIDArray(0),    
      ObjLinkTypeNameArray(0),  ObjLinkIDArray(0), ValidFromDate );   

[│║                    │               │                  │               │                  │           ║│
 │║####################│###############│##################│###############│##################│           ║│] 
    (LinkKindNameArray(1), OTypeNameArray(1), ObjIDArray(1),   
     ObjLinkTypeNameArray(1),  ObjLinkIDArray(1) );

    i = 2;
    while( StrLen(LinkKindNameArray(i)) > 0 )
[│║                    │               │                  │               │                  │           ║│
 │║####################│               │                  │               │                  │           ║│] 
      (LinkKindNameArray(i) );
      i = i + 1;
    end;
  end;
 
end;

macro ObjLinksForDelTail()


[│╙────────────────────┴───────────────┴──────────────────┴───────────────┴──────────────────┴───────────╜│];

end;


/*Проверочный отчет по очистке операций и платежей.*/ 

macro OprsPmsRemTestReportGeneral( 
  reportdate:date, 
  reporttime:time,
  remdate:date   
  )

[┌────────────────────  Проверочный отчет по очистке операций и платежей  ─────────────────────────┐
 │                                                                                                 │
 │╓──────────────────────────────────────────────────────────┬────────────────────────────────────╖│
 │║ Филиал                                                   │  ################################# ║│
 │╙──────────────────────────────────────────────────────────┴────────────────────────────────────╜│]
 ({Name_Bank});
  
[│╓──────────────────────────────────────────────────────────┬────────────────────────────────────╖│
 │║ Системные дата и время формирования отчета               │  ############ ###########          ║│
 │╙──────────────────────────────────────────────────────────┴────────────────────────────────────╜│
 │╓───────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║      Список операций и платежей, которые должны быть удалены на дату очистки   ##########     ║│]
 
 ( reportdate, reporttime, remdate );
  
end;



macro PmsRemTestReportHead()

[│╓───────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║       Удаляемые платежи (DPMPAYM_DBT) филиала #################################               ║│
 │╟────┬───────────┬───────────────┬──────────────────┬────────────┬──────────────────┬───────────╢│
 │║ ID │ Назначение│   Плательщик  │ Счет плательщика │ Получатель │ Счет получателя  │Дата валюти║│
 │║    │           │               │                  │            │                  │  рования  ║│]
 ({Name_Bank});
 
end; 
  

macro PmsRemTestReportBody()

  var rs : RsdRecordset;

  var PmID:integer;
  var PmPurpose:string;
  var PmPayer:string;
  var PmPayerAccount:string;
  var PmReceiver:string;
  var PmReceiverAccount:string;
  var PmDate:date;

  array PmPurposeArray, PmPayerArray, PmPayerAccountArray, PmReceiverArray, PmReceiverAccountArray; 

  rs = RSDRecordset( " SELECT p.T_PAYMENTID as PmID, "+ 
                           " pu.T_SHORTNAME as PmPurpose, "+ 
                           " pay.T_SHORTNAME as PmPayer, "+        
                           " p.T_PAYERACCOUNT as PmPayerAccount, "+
                           " rcv.T_SHORTNAME as PmReceiver, "+
                           " p.T_RECEIVERACCOUNT as PmReceiverAccount, " +
                           " p.T_VALUEDATE as PmDate "+
                      " FROM DPMPAYM_DBT p "+
                      " JOIN DPMPURP_DBT pu ON (p.T_PURPOSE = pu.T_PURPOSE) "+
                      " JOIN DPARTY_DBT pay ON (p.T_PAYER = pay.T_PARTYID) "+
                      " JOIN DPARTY_DBT rcv ON (p.T_RECEIVER = rcv.T_PARTYID) "+
                      " WHERE T_PAYMENTID IN (SELECT T_OBJID FROM DREMPMOPRERR_TMP WHERE T_OBJKIND = 0 )" );     
    
  while (rs.MoveNext)    
    PmID = rs.value("PmID");
    PmPurpose       = rs.value("PmPurpose");    
    PmPayer   = rs.value("PmPayer"); 
    PmPayerAccount = rs.value("PmPayerAccount");
    PmReceiver = rs.value("PmReceiver");
    PmReceiverAccount = rs.value("PmReceiverAccount");
    PmDate = rs.value("PmDate");    

    StrSplit( PmPurpose, PmPurposeArray, 11, 11, 2);
    StrSplit( PmPayer, PmPayerArray, 15, 15, 2);
    StrSplit( PmPayerAccount, PmPayerAccountArray, 18, 18, 2);
    StrSplit( PmReceiver, PmReceiverArray, 12, 12, 2);
    StrSplit( PmReceiverAccount, PmReceiverAccountArray, 18, 18, 2);

[│╟────┼───────────┼───────────────┼──────────────────┼────────────┼──────────────────┼───────────╢│
 │║####│###########│###############│##################│############│##################│###########║│]   
    (PmID, PmPurposeArray(0), PmPayerArray(0), PmPayerAccountArray(0),  PmReceiverArray(0), 
      PmReceiverAccountArray(0),  PmDate );

[│║    │           │               │                  │            │                  │           ║│
 │║    │###########│###############│##################│############│##################│           ║│]   
    (PmPurposeArray(1), PmPayerArray(1), PmPayerAccountArray(1),  PmReceiverArray(1), 
      PmReceiverAccountArray(1) );

  end;
 
end;

macro PmsRemTestReportTail( countPms:integer )

[│╙────┴───────────┴───────────────┴──────────────────┴────────────┴──────────────────┴───────────╜│
 │ Итого: ######                                                                                   │
 │                                                                                                 │]
  ( countPms );

end;


macro OprsRemTestReportHead( )

[│╓───────────────────────────────────────────────────────────────────────────────────────────────╖│ 
 │║                         Удаляемые операции (DOPROPER_DBT) всех филиалов:                      ║│
 │╟────┬───────────────────────────────────┬─────────────────────┬──────────┬──────────┬──────────╢│
 │║ ID │    Вид операции                   │ Вид и идентификатор │ Признак  │   Дата   │  Дата    ║│
 │║    │                                   │      документа      │завершения│ создания │завершения║│];

end; 
  

macro OprsRemTestReportBody()

  var rs : RsdRecordset;

  var OpID:integer;
  var OpKind:string; /*varchar(79)*/
  var OpDoc:string;  /*varchar(34)*/
  var OpEnd:bool;
  var OpDateStart:date;
  var OpDateEnd:date;

  array OpKindArray, OpDocArray;

  rs = RSDRecordset( "SELECT o.T_ID_OPERATION as OpID, "+
                          " ko.T_NAME as OpKind, "+
                          " o.T_DOCUMENTID as OpDoc, "+
                          " o.T_COMPLETED as OpEnd, "+
                          " o.T_START_DATE as OpDateStart, "+
                          " o.T_END_DATE as OpDateEnd "+
                     " FROM DOPROPER_DBT o "+
                     " JOIN DOPRKOPER_DBT ko ON (o.T_KIND_OPERATION = ko.T_KIND_OPERATION) "+
                     " WHERE T_ID_OPERATION IN (SELECT T_OBJID FROM DREMPMOPRERR_TMP WHERE T_OBJKIND = 1 )" );       
    
  while (rs.MoveNext)    
    OpID = rs.value("OpID");
    OpKind    = rs.value("OpKind");    
    OpDoc     = rs.value("OpDoc");
    OpEnd   = rs.value("OpEnd"); 
    OpDateStart = rs.value("OpDateStart");
    OpDateEnd = rs.value("OpDateEnd");
    
    StrSplit( OpKind, OpKindArray, 34, 34, 2);
    StrSplit( OpDoc,  OpDocArray,  21, 21, 2);  

[│╟────┼───────────────────────────────────┼─────────────────────┼──────────┼──────────┼──────────╢│
 │║####│###################################│#####################│    #     │##########│##########║│]   
    (OpID, OpKindArray(0), OpDocArray(0), OpEnd,  OpDateStart,  OpDateEnd );

[│║    │                                   │                     │          │          │          ║│
 │║    │###################################│#####################│          │          │          ║│]   
    ( OpKindArray(1), OpDocArray(1) );

  end;

end;

macro OprsRemTestReportTail( countOprs:integer )

[│╙────┴───────────────────────────────────┴─────────────────────┴──────────┴──────────┴──────────╜│
 │ Итого: ######                                                                                   │]
  ( countOprs );
end;





/*Список "потерянных" ссылок в платежах и операциях по всем филиалам*/

var FAmount;
var WAmount;
var OAmount;

macro PmInvalidRefsHead( )

[│╓───────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║              Список "потерянных" ссылок в платежах и операциях по всем филиалам               ║│
 │╓───────────────────────────────────────────────────────────────────────────────────────────────╖│ 
 │║        Список прямых ссылок на несуществующие платежи (DPMPAYM_DBT.T_PAYMENTID):              ║│
 │╟───────────────────────────────────┬─────────────────┬─────────────────────┬───────────────────╢│
 │║          Сущность                 │    Таблица      │     Имя поля        │Ссылка на несущест-║│
 │║                                   │                 │                     │ вующий  платеж    ║│];
   

end; 


macro PmInvalidRefsBody()

  var rs : RsdRecordset;

  var FEnt:string;
  var FTab:string;
  var FCol:string;
  var FVal:integer;

  var i;

  array FEntArray;

  FAmount = 0;       

/*Список прямых ссылок на несуществующие значения DPMPAYM_DBT.T_PAYMENTID (SQL запрос): */
                     /* Свойства платежа DPMPROP_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
  rs = RSDRecordset( " SELECT 'Свойства платежа' as FEnt, 'DPMPROP_DBT' as FTab, "+ 
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMPROP_DBT WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Реквизиты платежа для R-макета DPMRMPROP_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Реквизиты платежа для R-макета' as FEnt, 'DPMRMPROP_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMRMPROP_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* История платежа DPMHIST_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'История платежа' as FEnt, 'DPMHIST_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMHIST_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* История изменений документа картотеки DPMINHIST_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'История изменений документа картотеки' as FEnt, 'DPMINHIST_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMINHIST_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION  "+
                     /* Привязка проводки к платежу DPMDOCS_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Привязка проводки к платежу' as FEnt, 'DPMDOCS_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMDOCS_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Выгрузка платежа DWLPM_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Выгрузка платежа' as FEnt, 'DWLPM_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DWLPM_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Связь квитовки DWLKVTLNK_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Связь квитовки' as FEnt, 'DWLKVTLNK_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DWLKVTLNK_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Гармошка комиссий платежа DPMSLEEVE_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Гармошка комиссий платежа' as FEnt, 'DPMSLEEVE_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMSLEEVE_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Связь дубликата с платежом DDUPPMLNK_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Связь дубликата с платежом' as FEnt, 'DDUPPMLNK_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DDUPPMLNK_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Журнал изменения суммы платежа DPMLOG_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Журнал изменения суммы платежа' as FEnt, 'DPMLOG_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMLOG_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Маршрут платежа внутри ЦАБС DPMDPHIST_DBT (T_PAYMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Маршрут платежа внутри' as FEnt, 'DPMDPHIST_DBT' as FTab, "+
                     " 'T_PAYMENTID' as FCol, T_PAYMENTID as FVal "+
                     " FROM DPMDPHIST_DBT "+
                     " WHERE T_PAYMENTID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Связь платежей DPMLINK_DBT (T_INITIALPAYMENT = <ID несуществующего платежа> или T_PURPOSEPAYMENT = <ID несуществующего платежа>): */
                     " SELECT 'Связь платежей' as FEnt, 'DPMLINK_DBT' as FTab, "+
                     " 'T_INITIALPAYMENT' as FCol, T_INITIALPAYMENT as FVal "+
                     " FROM DPMLINK_DBT "+
                     " WHERE T_INITIALPAYMENT NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     " SELECT 'Связь платежей' as FEnt, 'DPMLINK_DBT' as FTab, "+
                     " 'T_PURPOSEPAYMENT' as FCol, T_PURPOSEPAYMENT as FVal "+
                     " FROM DPMLINK_DBT "+
                     " WHERE T_PURPOSEPAYMENT NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Операции по легализации DOPCONTR_DBT (T_PRIMDOCKIND = 29 и T_DOCUMENTID = <ID несуществующего платежа>): */
                     " SELECT 'Операции по легализации платежа (T_PRIMDOCKIND = 29)' as FEnt, 'DOPCONTR_DBT' as FTab, "+
                     " 'T_DOCUMENTID' as FCol, TO_NUMBER(T_DOCUMENTID) as FVal "+
                     " FROM DOPCONTR_DBT "+
                     " WHERE T_PRIMDOCKIND = 29 "+
                     " AND TO_NUMBER(T_DOCUMENTID) NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                     /* Квитовка ЗК и РДД DBILRECONCILE_DBT (T_DOCKIND = 29 и T_DOCID = <ID несуществующего платежа>): */
                     " SELECT 'Квитовка ЗК и платежа (T_DOCKIND = 29)' as FEnt, 'DBILRECONCILE_DBT' as FTab, "+
                     " 'T_DOCID' as FCol, TO_NUMBER(T_DOCID) as FVal "+
                     " FROM DBILRECONCILE_DBT "+
                     " WHERE T_DOCKIND = 29 "+
                     " AND TO_NUMBER(T_DOCID) NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " UNION "+
                      /* Узел маршрута объекта DPMROUTE_DBT (T_OBJKIND = 501(OBJTYPE_PAYMENT) и T_OBJID = <ID несуществующего платежа>): */
                     " SELECT 'Узел маршрута платежа (T_OBJKIND = 501)' as FEnt, 'DPMROUTE_DBT' as FTab, "+
                     " 'T_OBJID' as FCol, T_OBJID as FVal "+
                     " FROM DPMROUTE_DBT "+
                     " WHERE T_OBJKIND = 501  AND T_OBJID NOT IN (SELECT T_PAYMENTID FROM DPMPAYM_DBT) "+
                " ORDER BY FTab, FCol, FVal ");



  while (rs.MoveNext)
    i = 1;  
    FEnt = rs.value("FEnt");
    FTab    = rs.value("FTab");    
    FCol     = rs.value("FCol");
    FVal   = rs.value("FVal");
    
    StrSplit( FEnt, FEntArray, 34 );

[│╟───────────────────────────────────┼─────────────────┼─────────────────────┼───────────────────╢│
 │║###################################│#################│#####################│###################║│]   
    ( FEntArray(0), FTab:c, FCol:c,  FVal:c );

    while ( StrLen(FEntArray(i) ) > 0) 
[│║                                   │                 │                     │                   ║│
 │║###################################│                 │                     │                   ║│]   
      ( FEntArray(i) );
      i = i + 1;
    end;
    FAmount = FAmount + 1;

  end;

end;

macro PmInvalidRefsTail( )

[│╙───────────────────────────────────┴─────────────────┴─────────────────────┴───────────────────╜│
 │ Итого: ######                                                                                   │
 │                                                                                                 │]
   ( FAmount );
end;  



/*Ошибки в данных выгрузки платежа (SQL запрос):*/
macro WgInvalidRefsHead( )

[│╓───────────────────────────────────────────────────────────────────────────────────────────────╖│ 
 │║                           Ошибки в данных выгрузки платежей:                                  ║│
 │╟───────────────────────────────────┬────────────────┬───────────┬──────────────────────────────╢│
 │║          Сущность                 │    Таблица     │  Имя поля │   Ссылка на несуществующее   ║│
 │║                                   │                │           │           значение           ║│];
                                                           
end; 


macro WgInvalidRefsBody()

  var rs : RsdRecordset;

  var WEnt:string;
  var WTab:string;
  var WCol:string;
  var WVal:string;

  array WEntArray;
  var i;

  WAmount = 0;
                    /* Связь сообщения с учетными объектами DWLMESLNK_DBT (DWLMESLNK_DBT.T_OBJID не входит в DWLPM_DBT.T_WLPMID): */
  rs = RSDRecordset( " SELECT 'Связь сообщения с учетными объектами' as WEnt, 'DWLMESLNK_DBT' as WTab, "+
                     " 'T_OBJID' as WCol, 'DWLPM_DBT.T_WLPMID = ' || T_OBJID as WVal "+
                     " FROM DWLMESLNK_DBT "+
                     " WHERE T_OBJID NOT IN (SELECT T_WLPMID FROM DWLPM_DBT) "+
               " UNION "+
                     /* Сообщение DWLMES_DBT (DWLMES_DBT.T_MESID не входит в DWLMESLNK_DBT.T_MESID): */
                     " SELECT 'Сообщение' as WEnt, 'DWLMES_DBT' as WTab, "+
                     " 'T_MESID' as WCol, 'DWLMESLNK_DBT.T_MESID = ' || T_MESID as WVal "+
                     " FROM DWLMES_DBT "+
                     " WHERE T_MESID NOT IN (SELECT T_MESID FROM DWLMESLNK_DBT) "+
               " UNION "+
                     /* Значение поля сообщения DWLMESVAL_DBT (DWLMESVAL_DBT.T_MESID не входит  в DWLMESLNK_DBT.T_MESID): */
                     " SELECT 'Значение поля сообщения' as WEnt, 'DWLMESVAL_DBT' as WTab, "+
                     " 'T_MESID' as WCol, 'DWLMESLNK_DBT.T_MESID = ' || T_MESID as WVal "+
                     " FROM DWLMESVAL_DBT "+
                     " WHERE T_MESID NOT IN (SELECT T_MESID FROM DWLMESLNK_DBT) "+
                " ORDER BY WTab, WCol, WVal " );

  while (rs.MoveNext)
    i = 1;  
    WEnt = rs.value("WEnt");
    WTab    = rs.value("WTab");    
    WCol     = rs.value("WCol");
    WVal   = rs.value("WVal");
    
    StrSplit( WEnt, WEntArray, 34 ); 

[│╟───────────────────────────────────┼────────────────┼───────────┼──────────────────────────────╢│
 │║###################################│################│###########│##############################║│]   
    ( WEntArray(0), WTab:c, WCol:c,  WVal:c );

    while ( StrLen(WEntArray(i) ) > 0) 
[│║                                   │                │           │                              ║│
 │║###################################│                │           │                              ║│]   
      ( WEntArray(i) );                                                     
      i = i + 1;
    end;
    WAmount = WAmount + 1;

  end;


end;
 

macro WgInvalidRefsTail( )

[│╙───────────────────────────────────┴────────────────┴───────────┴──────────────────────────────╜│
 │ Итого: ######                                                                                   │
 │                                                                                                 │]
   ( WAmount );
end; 



/*Список "потерянных" ссылок в платежах и операциях по всем филиалам*/
macro OprInvalidRefsHead( )

[│╓───────────────────────────────────────────────────────────────────────────────────────────────╖│ 
 │║   Список прямых ссылок на несуществующие экземпляры операции (DOPROPER_DBT.T_ID_OPERATION):   ║│
 │╟───────────────────────────────────┬─────────────────┬─────────────────────┬───────────────────╢│
 │║          Сущность                 │    Таблица      │     Имя поля        │Ссылка на несущест-║│
 │║                                   │                 │                     │ вующую  операцию  ║│];

end; 


macro OprInvalidRefsBody()

  var rs : RsdRecordset;

  var OEnt:string;
  var OTab:string;
  var OCol:string;
  var OVal:integer;

  array OEntArray;
  var i;

  OAmount = 0;

  /* Шаги операции DOPRSTEP_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
  rs = RSDRecordset( " SELECT 'Шаги операции' as OEnt, 'DOPRSTEP_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRSTEP_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Данные для отката операции DOPRDOCS_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Данные для отката операции' as OEnt, 'DOPRDOCS_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRDOCS_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Текущий статус экземпляра операции DOPRCURST_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Текущий статус экземпляра операции' as OEnt, 'DOPRCURST_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRCURST_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Значения даты для экземпляра операции DOPRDATES_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Значения даты для экземпляра операции' as OEnt, 'DOPRDATES_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRDATES_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Подтверждения на шагах операций DOPRRCPST_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Подтверждения на шагах операций' as OEnt, 'DOPRRCPST_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRRCPST_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Единовременные комиссии DOPRSFCOM_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Единовременные комиссии' as OEnt, 'DOPRSFCOM_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRSFCOM_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* Данные для отката плановой даты операции DOPRSTDBC_DBT (T_ID_CHANGINGOPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'Данные для отката плановой даты операции' as OEnt, 'DOPRSTDBC_DBT' as OTab, "+
                     " 'T_ID_CHANGINGOPERATION' as OCol, T_ID_CHANGINGOPERATION as Oval "+
                     " FROM DOPRSTDBC_DBT "+
                     " WHERE T_ID_CHANGINGOPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) "+
               " UNION "+
                     /* История изменения параметров операции DOPRSTHIST_DBT (T_ID_OPERATION не входит в DOPROPER_DBT.T_ID_OPERATION): */
                     " SELECT 'История изменения параметров операции' as OEnt, 'DOPRSTHIST_DBT' as OTab, "+
                     " 'T_ID_OPERATION' as OCol, T_ID_OPERATION as Oval "+
                     " FROM DOPRSTHIST_DBT "+
                     " WHERE T_ID_OPERATION NOT IN (SELECT T_ID_OPERATION FROM DOPROPER_DBT) ");


  while (rs.MoveNext)
    i = 1;  
    OEnt = rs.value("OEnt");
    OTab    = rs.value("OTab");    
    OCol     = rs.value("OCol");
    OVal   = rs.value("OVal");
    
    StrSplit( OEnt, OEntArray, 34 );

[│╟───────────────────────────────────┼─────────────────┼─────────────────────┼───────────────────╢│
 │║###################################│#################│#####################│###################║│]   
    ( OEntArray(0), OTab:c, OCol:c,  OVal:c );

    while ( StrLen(OEntArray(i) ) > 0) 
[│║                                   │                 │                     │                   ║│
 │║###################################│                 │                     │                   ║│]   
      ( OEntArray(i) );
      i = i + 1;
    end;
    OAmount = OAmount + 1;

  end; 

end;
 

macro oprInvalidRefsTail(  )

[│╙───────────────────────────────────┴─────────────────┴─────────────────────┴───────────────────╜│
 │ Итого: ######                                                                                   │
 │                                                                                                 │]
  ( OAmount );
end; 



/*Проверочный отчет по очистке л/с и проводок*/
/*лицевые счета*/ 
macro AccCarryTestReportGeneral( 
reportdate:date, 
reporttime:time, 
countAcc:integer, 
countCarry:integer )

[┌────────────────────────────────────     Проверочный отчет    ──────────────────────────────────────────┐];
[│                                                                                                        │];
[│╓─────────────────────────────────────────────────────────────────┬────────────────────────────────────╖│
 │║ Филиал                                                          │  ################################# ║│
 │╙─────────────────────────────────────────────────────────────────┴────────────────────────────────────╜│]
 ({Name_Bank});
  
[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                  Удаление лицевых счетов и проводок по закрытым счетам                               ║│];
[│╟────────────────────────────────────────────────────────────────────┬─────────────────────────────────╢│
 │║ Системные дата и время формирования отчета                         │  ############ ###########       ║│
 │╟────────────────────────────────────────────────────────────────────┼─────────────────────────────────╢│
 │║ Количество подлежащих удалению закрытых лицевых счетов             │  ##########                     ║│
 │╟────────────────────────────────────────────────────────────────────┼─────────────────────────────────╢│
 │║ Количество подлежащих удалению проводок по закрытым лицевым счетам │  ##########                     ║│ 
 │╙────────────────────────────────────────────────────────────────────┴─────────────────────────────────╜│]
 (reportdate, reporttime, countAcc, countCarry);
  
end;

macro AccRemTestReportHead()

[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                            Подлежащие удалению закрытые лицевые счета                                ║│
 │╟──────────────────────────┬─────────────┬─────────────┬───────────────────┬────────────┬──────────────╢│
 │║   Номер лицевого счета   │Дата открытия│Дата закрытия│  Балансовый счет  │ Код валюты │ Операционист ║│];
 
end;

macro AccRemTestReportBody()

  var cmd:RsdCommand, rs:RsdRecordset, strSql:string;
  var OpenDate:date, CloseDate:date;

  strSql = "SELECT t_Account, t_Open_Date, t_Close_Date, t_Balance, t_Code_Currency, t_Oper FROM dremaccerr_tmp";
  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );
    
  while (rs.MoveNext)
    OpenDate = rs.value(1);
    CloseDate = rs.value(2);    

[│╟──────────────────────────┼─────────────┼─────────────┼───────────────────┼────────────┼──────────────╢│
 │║##########################│ ############│ ############│###################│############│##############║│]   
    (rs.value(0), OpenDate, CloseDate, rs.value(3), ПолучитьКодФинИН( rs.value(4) ), rs.value(5) );

  end;
 
end;

macro AccRemTestReportTail( countAcc:integer )

[│╙──────────────────────────┴─────────────┴─────────────┴───────────────────┴────────────┴──────────────╜│];

end;     


/*проводки*/
macro CarryRemTestReportHead()

[│╓──────────────────────────────────────────────────────────────────────────────────────────────────────╖│
 │║                      Подлежащие удалению проводки по закрытым лицевым счетам                         ║│
 │╟─────────┬──────────┬──────────────────────────┬──────────────────────────┬────────────┬──────────────╢│
 │║  Номер  │   Дата   │        Счет дебета       │       Счет кредита       │  Сумма     │ Операционист ║│];

end;

macro CarryRemTestReportBody()

  var cmd:RsdCommand, rs:RsdRecordset, strSql:string;
  var CarryDate:date;

  strSql = "SELECT t_Numb_Document, t_Date_Carry, t_Account_Payer, t_Account_Receiver, t_Sum, t_Oper FROM dremcarryerr_tmp";
  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );                   
    
  while (rs.MoveNext)
    CarryDate = rs.value(1);

[│╟─────────┼──────────┼──────────────────────────┼──────────────────────────┼────────────┼──────────────╢│
 │║#########│##########│##########################│##########################│############│##############║│]  
  ( rs.value(0), CarryDate, rs.value(2), rs.value(3), rs.value(4), rs.value(5) );

  end;
 
end;

macro CarryRemTestReportTail( countAcc:integer )

[│╙─────────┴──────────┴──────────────────────────┴──────────────────────────┴────────────┴──────────────╜│];

end;

/*│║
 *┌─┬─┐ ╔═╦═╗ ╓─╥─╖╒═╤═╕
 *├─┼─┤ ╠═╬═╣ ╟─╫─╢╞═╪═╡
 *└─┴─┘ ╚═╩═╝ ╙─╨─╜╘═╧═╛
 */
