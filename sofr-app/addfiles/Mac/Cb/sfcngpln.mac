import "globals.mac", RsbDataSet;

var Success, Error;

macro ReportPrintHeader(SfPlanName, CngDate)
[           Протокол изменения тарифного плана в договорах обслуживания          ];
[                                                                                ];
[   Дата формирования: ##########                                                ] (string(date()));
[   Время формирования: ########                                                 ] (string(time()));
[   Операционист: ##### ######################################################## ] ({oper}, {Name_Oper});
[   Подразделение банка: ####################################################### ] ({Name_Bank});
[                                                                                ];
[   Параметры запуска:                                                           ];
[   Тарифный план - ############################################################ ] (SfPlanName);
[   Дата изменения - ##########                                                  ] (CngDate);
[                                                                                ];
[   Результаты выполнения:                                                       ];
[┌────────────────┬─────────────────────────────────────────────────────────────┐];
[│ Договора       │                      Результат                              │];
[│ обслуживания № │                                                             │];

  Success = Error = 0;
end;

macro ReportPrintLine(Number, Stat, Msg)
   array StringArray;
   var MessageStr, i;

   if(Stat == 0)
     MessageStr = "Тарифный план изменен";
     Success = Success + 1;
   else
     MessageStr = "Тарифный план не изменен. При изменении возникла ошибка: " + Msg;
     Error = Error + 1;
   end;

   StrSplit(MessageStr, StringArray, 60);
[├────────────────┼─────────────────────────────────────────────────────────────┤];
[│################│#############################################################│](Number, StringArray(0));
   i = 1;
   while(StrLen(StringArray(i)) > 0)
[│                │#############################################################│](StringArray(i));

     i = i + 1;
   end;
end;

macro ReportPrintInfoDscnt( CntrPlnID )
  
  var strMsg = "";
  var cmd = RsdCommand( " SELECT t_Number, t_ShortName FROM DSFDISCOUNT_DBT WHERE t_DiscountID " +
                        " IN (SELECT t_DiscountID FROM dsfcontrplandsc_dbt WHERE t_ContrPlanID = ? )" );

  cmd.addParam( "", RSDBP_IN, CntrPlnID );

  var rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    strMsg = "Назначен дисконт " + string(rs.value(0)) + " " + rs.value(1);
[│                │#############################################################│](strMsg);
  end;

end;

macro ReportPrintFooter()
[└────────────────┴─────────────────────────────────────────────────────────────┘];
[                                                                                ];
[ Итоговый результат:                                                            ];
[   Изменено тарифных планов: ########## договор обслуживания.                   ] (Success);
[   Не изменено тарифных планов: ########## договор обслуживания.                ] (Error);
end;