IMPORT  InsCarryDoc, BankInter, PaymInter, "mc_lib.mac", pm_common;

var Multy:RsbMultyDoc;

/*------------------------------------------------------------------------------------\
|                       Проводка мультивалютного документа                            |
\------------------------------------------------------------------------------------*/
macro   ExecuteStep( doc, multyDoc )

        RECORD  rec_wlconf( wlconf );
        var DKFlag = 1;
        
        var Payment:RsbMDPayment = Multy.Payment;
        var paymtr:RsbPaymTransaction = Payment.MakeTransaction();
        if( paymtr == NULL )
          MsgBox("Ошибка при создании проводки по платежу");
          return 1;
        end;

        paymtr.Chapter          = Multy.Chapter;
        paymtr.FIIDPayer        = Payment.PayerFIID;
        paymtr.FIIDReceiver     = Payment.ReceiverFIID;
        paymtr.SumPayer         = Payment.PayerAmount;
        paymtr.SumReceiver      = Payment.ReceiverAmount;
        paymtr.Date_Carry       = Payment.ValueDate;
        paymtr.Date_Document    = Payment.ValueDate;
        paymtr.Number_Pack      = Payment.NumberPack;
        paymtr.Department       = Payment.Department;
        paymtr.Numb_Document    = Payment.Number;
        paymtr.Ground           = Payment.Ground;
        paymtr.TypeDocument     = Multy.Type_Document;
        paymtr.UserField1       = Multy.UserField1;
        paymtr.UserField2       = Multy.UserField2;
        paymtr.UserField3       = Multy.UserField3;
        paymtr.UserField4       = Multy.UserField4;
        //paymtr.ResultCarry      = 1;
        paymtr.Shifr_Oper        = "09";
        
        // Добавим символы 
        paymtr.AddCashSymbol(Payment.CashSymbolDebet, Payment.PayerAmount, CASHSYMB_TYPE_DEBET);
        paymtr.AddCashSymbol(Payment.CashSymbolCredit, Payment.PayerAmount, CASHSYMB_TYPE_CREDIT);
        paymtr.AddCashSymbol(Payment.SymbNotBalDebet, Payment.PayerAmount, CASHSYMB_TYPE_NOTB_DEBET);
        paymtr.AddCashSymbol(Payment.SymbNotBalCredit, Payment.PayerAmount, CASHSYMB_TYPE_NOTB_CREDIT);
        
        if( not paymtr.Carry )
          MsgBox("Ошибка при актуализации платежа");
          return 1;
        end;
        
        // Статус платежа меняем на "Завершенный платеж"
        Payment.PaymStatus = PM_FINISHED;
        // Изменяем дату списания со счета плательщика
        Payment.PayerChargeOffDate = Payment.ValueDate;

        ClearRecord( rec_wlconf );
        /* Создание подтверждения о проводке */
        /* вызываем последовательно для подтверждений дебета и кредита по документу */
        while( DKFlag <= 2 )             
                                         
          if( DKFlag == 1 )
            rec_wlconf.FIID        = Payment.PayerFIID;
            rec_wlconf.Account     = Payment.PayerAccount;
            rec_wlconf.Sum         = Payment.PayerAmount;
          else
            rec_wlconf.FIID        = Payment.ReceiverFIID;
            rec_wlconf.Account     = Payment.ReceiverAccount;
            rec_wlconf.Sum         = Payment.ReceiverAmount;
          end;
          rec_wlconf.Corschem      = -1; /*ALLCORSCHEMNUMBER*/
          rec_wlconf.DocumentID    = Multy.AutoKey;
          rec_wlconf.DocKind       = CB_MULTYDOC;
          rec_wlconf.DateValue     = Payment.ValueDate;
          rec_wlconf.Number        = Payment.Number;
          
          rec_wlconf.DKFlag        = DKFlag;
          rec_wlconf.BankID        = GetClientByAccount(rec_wlconf.FIID, rec_wlconf.Account);
          rec_wlconf.CodeKind      = 1 /*PTCK_CONTR*/;
          //rec_wlconf.CodeName      = Payment.CodeName;
          rec_wlconf.Description   = Payment.Ground; 

          if(CreateConfFlagForCorschem(rec_wlconf))
          if( CreateConfirmation(rec_wlconf) == FALSE )
            msgbox("Ошибка при создании подтверждения по проводке");
            return 1;
          end;
          end;
          DKFlag = DKFlag + 1;
        end;


        RETURN 0;
END;