/**                                                                                                             
 @file sfacrpay.mac                                                                                           
 @brief ПЗО: Начисление и оплата комиссии
                                                                                                                
 #tag                                                                                                          
 - functional_block:Ядро ГКБО                
 - functional_block:ПЗО                                                                                                                                          
 - code_type:API 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.09 |Жиц М.В.       |DEF-59433           |Запрос отбора ДО по комиссии "ИнвестСоветник" исправлен на более универсальный                                      
 |2023.12.12 |Жиц М.В.       |BIQ-14887           |Реализация отбора ДО по комиссии "ИнвестСоветник", добавление описаний                                      
 |?          |Попов С.И.     |?                   |Создание                                                                                                                                                                                                                                                            
*/ 

import "sfcommon.mac", PSINTER, PTINTER, sfcomcat, "sf_lib.mac", "cb_sql.mac", "sfground.mac";

/**
 @brief Установка признака пропуска обработки во временной таблице начисленных/оплаченных комиссий,
        заполнение плановой даты оплаты
*/                    
macro skipSfDefCom()
  var cmd;

  cmd = RSDCommand("UPDATE dsfdeftmp_tmp sfdef " +
                   "   SET sfdef.t_Skip = CHR(88) " +  
                   " WHERE NOT EXISTS (SELECT 1 " + 
                   "                     FROM dsfcontr_dbt sfc, ddlcontrmp_dbt mp " + 
                   "                    WHERE sfc.t_ID = sfdef.t_ConID " + 
                   "                      AND mp.t_SfContrID = sfc.t_ID " +  
                   "                      AND sfc.t_ServKind = 1 " +
                   "                      AND sfc.t_ServKindSub = 8 " + 
                   "                      AND mp.t_MarketID = 2 " +
                   "                  ) " +
                   "   AND sfdef.t_FeeType = 1 " +   
                   "   AND sfdef.t_CommNumber IN (SELECT t_Number " + 
                   "                                FROM dsfcomiss_dbt " + 
                   "                               WHERE UPPER(t_Code) = UPPER('БрокерФикс') OR UPPER(t_Code) = UPPER('ИнвестСоветник') " +
                   "                             ) ");
  cmd.execute();

  cmd = RSDCommand("UPDATE dsfdeftmp_tmp sfdef " +
                   "   SET sfdef.t_PlanPayDate = CASE WHEN sfdef.t_CommNumber = (SELECT t_Number " + 
                   "                                                               FROM dsfcomiss_dbt " + 
                   "                                                              WHERE UPPER(t_Code) = UPPER('ИнвестСоветник')) " +
                   "                                  THEN NVL((SELECT MAX(serv.t_EndDate) " +
                   "                                              FROM ddlcontrmp_dbt mp, ddlcontrserv_dbt serv " +
                   "                                             WHERE mp.t_SfContrID = sfdef.t_ConID " +
                   "                                               AND serv.t_DlContrID = mp.t_DlContrID " +
                   "                                               AND serv.t_BeginDate < sfdef.t_DatePeriodEnd " +
                   "                                               AND serv.t_EndDate >= sfdef.t_DatePeriodBegin " +
                   "                                               AND serv.t_EndDate <= RSI_RSBCALENDAR.GetDateAfterWorkDay((ADD_MONTHS(TRUNC(sfdef.t_DatePeriodEnd, 'q'), 3)-1), 10, 10003) " +
                   "                                           ), " +
                   "                                           RSI_RSBCALENDAR.GetDateAfterWorkDay((ADD_MONTHS(TRUNC(sfdef.t_DatePeriodEnd, 'q'), 3)-1)/*Последний день текущего квартала*/, 10, 10003) " +
                   "                                          ) " +
                   "                                  ELSE RSI_RSBCALENDAR.GetDateAfterWorkDay(TRUNC(LAST_DAY(sfdef.t_DatePeriodEnd))/*Последний день текущего месяца*/, 10, 10003) " +
                   "                              END " +                                                                                                 
                   " WHERE sfdef.t_Skip <> CHR(88) " +
                   "   AND sfdef.t_FeeType = 1 " +   
                   "   AND sfdef.t_CommNumber IN (SELECT t_Number " + 
                   "                                FROM dsfcomiss_dbt " + 
                   "                               WHERE UPPER(t_Code) = UPPER('БрокерФикс') OR UPPER(t_Code) = UPPER('ИнвестСоветник') " +
                   "                             ) ");
  cmd.execute();
end;

/**
 @brief Корректировка параметров командной строки
 @param[in/out] все параметры командной строки
*/
macro SfCorrectShedParms( dep:@string, fi:@string, account:@string, oper:@string, 
                          group:@string, comnum:@integer, curonly:@string, 
                          pdate:@integer, cdate:@integer, 
                          charge:@string, paym:@string )
end;

/**
 @brief Получиение идентификатора биржи ММВБ
 @return идентификатор биржи ММВБ
*/                    
private macro u_MMVB_ID()
  var stat = 0;
  var MMVB_Code = "", _MMVB_ID = -1;

  GetRegistryValue("SECUR\\MICEX_CODE", V_STRING, MMVB_Code, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки SECUR\\MICEX_CODE");
  elif(MMVB_Code == "")
    stat = 1;
    msgbox("Не задано значение настройки SECUR\\MICEX_CODE");
  else
    _MMVB_ID = GetCodeParty(MMVB_Code,PTCK_CONTR);
  end;

  return _MMVB_ID;
end;

/**
 @brief Фильтр ДО, для создания комиссий, при запуске сервисной операции.
        Запрос выполняется по таблице dsfconcom_tmp с алиасом concom
 @param[in] SfSrvDocBuf структура сервисной операции начисления/оплаты комиссии
 @return sql условия для добавления в WHERE
*/                        
macro SfComissContrLink(SfSrvDocBuf):string
  record SfSrvDocRec(sfsrvdoc);
  SetBuff(SfSrvDocRec, SfSrvDocBuf);
  var StrWhere = "";

  if(GetComCode(SfSrvDocRec.CommNumber, SfSrvDocRec.FeeType) == COMMISS_INVEST_ADVISER_N)
    //Отбираем только договора обслуживания фондового рынка ММВБ, у которых на соответствующем ДБО заведена действующая на дату окончания периода процедуры услуга инвестиционного консультирования
    StrWhere = " concom.t_ObjectID IN (SELECT sfc.t_ID " +
               "                         FROM dsfcontr_dbt sfc, ddlcontrmp_dbt mp, ddlcontrserv_dbt serv " +
               "                        WHERE sfc.t_ServKind = " + PTSK_STOCKDL +
               "                          AND sfc.t_ServKindSub = 8 " +
               "                          AND mp.t_SfContrID = sfc.t_ID " +
               "                          AND mp.t_MarketID = " + u_MMVB_ID() + 
               "                          AND serv.t_DlContrID = mp.t_DlContrID " +
               "                          AND serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE +
               "                          AND serv.t_BeginDate < to_date('" + SfSrvDocRec.DatePeriodEnd + "','dd.mm.yyyy') "
               "                          AND (serv.t_EndDate >= to_date('" + SfSrvDocRec.DatePeriodEnd + "','dd.mm.yyyy') OR serv.t_EndDate = to_date('01.01.0001','dd.mm.yyyy') ) " +
               "                      ) ";
  end;

  return StrWhere;
end;