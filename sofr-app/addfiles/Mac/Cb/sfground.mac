/*Функция BuildGround()
  Формирование назначения платежа или основания ТО по данным удержанной комиссии и буферу ДО. 
  Для единоразовых комиссий генерим назначение платежа, для периодических -основание ТО. 
Принимаемые параметры:
  - ComBuf - буфер удержанной комиссии
  - SfComiss - буфер 
  - SfContr - буфер ДО
Возвращаемый параметр:
 - Строка основание РДД.*/

import SfInter, BankInter, PTInter, CTInter, FIInter, globals, RSD;

private const NOTEKIND_COMISS_GROUND = 1;
private const OBJTYPE_SFCOMISS = 650;

private macro GetCurrencyShortName( CCur ) : string

  var sqlString : string;
  var rs : RsdRecordset;

  sqlString = "SELECT t_Ccy FROM dfininstr_dbt WHERE t_fiid = " + CCur;

  rs = RsdRecordset( sqlString );

  if( rs.moveNext())
    return rs.value( 0 );
  end;

  return "";
end;

/*заменить спецпеременную без учета регистра*/
private macro ReplaceSpecVariable( ground, specVar, strToReplace )
  
  var VarIndex, groundOut = "";

  VarIndex = Index( StrUpr(ground), specVar );
  while( VarIndex > 0 )
    groundOut = groundOut + SubStr( ground, 1, VarIndex -1 ) + strToReplace;
    ground = SubStr( ground, VarIndex + strlen(specVar) );
    VarIndex = Index( StrUpr(ground), specVar );
  end;

  groundOut = groundOut + ground;

  return groundOut;

end;

/*Отрезать незначащие нули в дробной части. Если после точки все нули, то точку тоже вырезать */
private macro doubleValToStr( doubleVal )
  
  var Symbol, loop = true;
  var retStr = string(doubleVal); 

  while( loop )
    Symbol = substr( retStr, strlen(retStr), 1 );
    if( Symbol == "0" )
      retStr = substr( retStr, 1, strlen(retStr)-1 );
    elif( Symbol == "." )
      retStr = substr( retStr, 1, strlen(retStr)-1 );
      loop = false;
    else
      loop = false;
    end;
  end;

  if( strlen(retStr) == 0 )
    retStr = "0";
  end;

  return retStr;
end;

/*Пункт 2.2.3.1 */
private macro MoreAtrSub( party )
  
  file persn("persn.dbt") key 0;
  file objattr("objattr.dbt") key 0;
  var FindAttr:bool;
  if( persn.PersonID = party.PartyID)
   if( getEQ( persn ) )
    if ( party.LegalForm == PTLEGF_PERSN )
      if( (persn.IsEmployer == "X") 
        OR ((CheckObjAttrPresence ( FindAttr, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY), 16, 2, "", "", false ) == true) AND (FindAttr == true) ) 
        OR ((CheckObjAttrPresence ( FindAttr, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY), 16, 3, "", "", false ) == true ) AND (FindAttr == true) )
        )
        return true;
      end;
    end;
   end;
  end;

  return false;
end;

/*Пункт 2.2.3.2 */
private macro BankPropertyYuris( party )

  var cmd;
  var rs;
  
  var isPro = false;

  if( party.LegalForm != PTLEGF_PERSN )
    
    cmd = RsdCommand( "SELECT * FROM dpartyown_dbt WHERE t_PartyID = :PartyID AND (t_PartyKind = 2 OR t_PartyKind = 56)" );
    cmd.addParam( "", RSDBP_IN, party.PartyID );
       
    rs = RsdRecordset( cmd );
  
    if( rs.moveNext() )
      return isPro = false;
    else
      return isPro = true;
    end;
  end;

  return isPro;

end;

/*Добавление кода ВО в основание документа*/
macro SfGroundAddVO( ground:@string, ComFIID, PayerID, ReceiverID, DocDate )

  var Flag = false;
  var err = "";

  file party("party.dbt") key 0;

  GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, Flag, err);
  if( Flag )
    if( ComFIID == NATCUR ) 
      if( (PayerID == {HeadBankID}) OR (PayerID == {OurBank}) ) 
        party.PartyID = ReceiverID; 
      elif( (ReceiverID == {HeadBankID}) OR (ReceiverID == {OurBank}) )
        party.PartyID = PayerID;
      end;

      if( getEQ( party ) )
        if(( party.LegalForm != PTLEGF_PERSN ) AND( party.NotResident == "X" ) )
          if( DocDate < date(1,9,2012) )
            ground = "{VO99020} " + ground;
          else
            ground = "{VO80050} " + ground;
          end;
        end;
      end;
    
    elif( (ComFIID != NATCUR) AND (DocDate >= date(1,9,2012)) )
      if( (PayerID == {HeadBankID}) OR (PayerID == {OurBank}) )
        party.PartyID = ReceiverID;
      elif( (ReceiverID == {HeadBankID}) OR (ReceiverID == {OurBank}) )
        party.PartyID = PayerID;
      end;
      
      if( getEQ( party ) )
        if( (party.NotResident != "X") AND ( (MoreAtrSub( party ) == true) OR (BankPropertyYuris( party ) == true) ) ) 
          ground = "{VO80150} " + ground;
        end;
      end;
    end;
  end;

  return ground;
end;

macro BuildGroundTmpl( ComBuf,       // буфер удержанной комиссии
                       SfComiss,     // буфер вида комиссии
                       SfContr,      // буфер ДО
                       Template,     // шаблон основания - примечание вида "Назначение платежа"
                       fiidCCY,      //  Трехбуквенный код ISO валюты ComBuf.fiid_Sum
                       fiidTarsclCCY //  Трехбуквенный код ISO валюты ComBuf.fiid_Tarscl
                     )
  var dateStr = "", monthStr = "", sumStr = "", quantityStr = "", ndsStr = "", contractStr = "",
      percentStr = "", sumPerUnitStr = "";
  var month, year;

  var ground = Template; 

  /*Заменяем в полученном шаблоне спец. переменные их значениями из ComBuf и SfContr*/    
  if( SfComiss.feeType == SF_FEE_TYPE_PERIOD ) /*периодическая комиссия*/      
    /*{Date} datePeriodEnd*/
    dateStr = string( ComBuf.datePeriodEnd );

    /*{Month} - Месяц и год, в котором взимается комиссия (например, "март 2009г.") от {Date}*/
    DateSplit( ComBuf.datePeriodEnd, NULL, month, year);
    monthStr = monname(month) + " " + string(year) + " г.";
    
    /*{Sum} baseSum, fiid_comm_sum   Базовая сумма, используемая при расчете комиссии в валюте расчета комиссии 
    с указанием буквенного кода ISO валюты расчета комиссии (например, 2000 USD).*/
    sumStr = string(ComBuf.baseSum) + " " + fiidCCY;

    /*{Quantity}*/
    quantityStr = string(ComBuf.BaseQuant);

    /*{Nds} ndsSum, fiid_comm_sum 
    Сумма НДС по комиссии в валюте счета-получателя комиссии с указанием буквенного кода ISO валюты. Например "120.50 RUR"*/
    ndsStr = string(ComBuf.SumNDS) + " " + fiidCCY;

    /*{Contract}*/
    contractStr = "№" + SfContr.number + " от "  + string(SfContr.dateConc);

  elif( SfComiss.feeType == SF_FEE_TYPE_SINGLE ) /*единовременная*/
    /*{Date} dateFee*/
    dateStr = string( ComBuf.dateFee );
    
    /*{Month}*/
    DateSplit( ComBuf.dateFee, NULL, month, year );
    monthStr = monname(month) + " " + string(year) + " г.";
    
    /*{Sum} baseSum, fiid_sum */
    sumStr = string(ComBuf.baseSum) + " " + fiidCCY;

    /*{Quantity}*/
    quantityStr = "1";

    /*{Nds} sumNDS, fiid_sum */
    ndsStr = string(ComBuf.sumNDS) + " " + fiidCCY;

    /*{Contract}*/
    if( SfContr != null )
      contractStr = "№" + SfContr.number + " от "  + string(SfContr.dateConc);
    end;

  elif( SfComiss.feeType == SF_FEE_TYPE_ONCE ) /*разовая*/
    /*{Date} commDate*/
    dateStr = string( ComBuf.DateFee );
    
    /*{Month}*/
    DateSplit( ComBuf.DateFee, NULL, month, year );
    monthStr = monname(month) + " " + string(year) + " г.";
    
    /*{Sum} amountSum*/
    sumStr = string(ComBuf.BaseSum) + " " + fiidCCY;

    /*{Quantity}*/
    quantityStr = string(ComBuf.BaseQuant);

    /*{Nds} sumNDS*/
    ndsStr = string(ComBuf.sumNDS) + " " + fiidCCY;

    /*{Contract}*/
    if( SfContr != null )
      contractStr = "№" + SfContr.number + " от "  + string(SfContr.dateConc);
    end;
  end;

  /**/
  percentStr = doubleValToStr( ComBuf.Percent );
  
  sumPerUnitStr = string( money(ComBuf.Sum_Per_Unit) );
  if( ComBuf.Sum_Per_Unit > 0 )
    sumPerUnitStr = sumPerUnitStr + " " + fiidTarsclCCY;
  end;
  
  ground = ReplaceSpecVariable( ground, "{DATE}", dateStr );
  ground = ReplaceSpecVariable( ground, "{MONTH}", monthStr );
  ground = ReplaceSpecVariable( ground, "{SUM}", sumStr );
  ground = ReplaceSpecVariable( ground, "{QUANTITY}", quantityStr );
  ground = ReplaceSpecVariable( ground, "{NDS}", ndsStr );
  ground = ReplaceSpecVariable( ground, "{CONTRACT}", contractStr );
  ground = ReplaceSpecVariable( ground, "{PERCENT}", percentStr );
  ground = ReplaceSpecVariable( ground, "{SUM_PER_UNIT}", sumPerUnitStr );

  return ground;
end;

macro BuildGround( ComBuf, SfComiss, SfContr )
  var ground = "";
  var ndsSum;
  var fiidCCY = "";
  var fiidTarsclCCY = "";
    
  ground = readNoteForObject( OBJTYPE_SFCOMISS, MakeObjectID(OBJTYPE_SFCOMISS, NULL, SfComiss), NOTEKIND_COMISS_GROUND );
  if( ground == "" )
   /*генерим назначение по жестко прописанным правилам и возвращаем его.*/
   if((SfContr != null) and (SfContr.ID > 0))
     ground = "За услуги по договору " + SfContr.Number +" от " + SfContr.DateBegin + ". ";
   end;

   ground = ground + "Оплата комиссии " + SfComiss.code;

   ndsSum = ComBuf.SumNDS;
   fiidCCY = GetCurrencyShortName( ComBuf.fiid_sum );
   
   if( ndsSum > $0 )
     ground = ground + ". В том числе НДС " + ndsSum + " " + fiidCCY +".";
   else
     ground = ground + ". НДС не облагается.";
   end;

  else  
    fiidTarsclCCY = GetCurrencyShortName( ComBuf.fiid_Tarscl );
    fiidCCY       = GetCurrencyShortName( ComBuf.fiid_Sum );

    BuildGroundTmpl( ComBuf, SfComiss, SfContr, ground, fiidCCY, fiidTarsclCCY );
  end;

  return ground;

end;

macro BuildGroundWithVO( ComBuf, SfComiss, SfContr, PayerID, ReceiverID, DocDate )

  var ground = BuildGround( ComBuf, SfComiss, SfContr );
  ground = SfGroundAddVO( ground, ComBuf.fiid_sum, PayerID, ReceiverID, DocDate);

  return ground;   

end;

// Формирование кода ВО в пакетном режиме 
macro SfGroundAddVOBatch(FeeType, SfSrvDoc, BlockID, Number_Step)
  var cmd, rs, strSql;
  var Flag = false;
  var err = "";
  var ObjectType = 0;

  if(FeeType == SF_FEE_TYPE_PERIOD)  
    ObjectType = OBJTYPE_SFDEFCOM;
  elif (FeeType == SF_FEE_TYPE_SINGLE)
    ObjectType = OBJTYPE_OPRSFCOM;
  else
    return 0;
  end;

  var BankID = {OurBank};
  var OperID = {Oper};
  var HeadBankID = {HeadBankID};

  GetRegistryValue( "COMMON\\ВЕДЕНИЕ БД ВО В ПРИКЛАДНЫХ БО", V_BOOL, Flag, err);
  if( Flag == true )

    cmd = RsdCommand();
    cmd.NullConversion = true;


    strSql = " INSERT INTO dsfground_tmp (t_SfDefID, t_ComFIID, t_PartyID, t_DocDate, t_Ground) " +
             " SELECT " +
             " sfdef.t_id, sfdef.t_FIID_Sum, " +
             " DECODE (sidebet.t_PartyID, ?, sicredit.t_PartyID, 0, " +
             "               DECODE(sidebet.t_PartyID, ?, sicredit.t_PartyID, " +
             "                            DECODE(sicredit.t_PartyID, ?, sidebet.t_PartyID, " +
             "                                          DECODE(sicredit.t_PartyID, ?, sidebet.t_PartyID, 0) " +
             "                                        ) " +
             "                           ) " +
             "              ), " ;

    cmd.addParam( "", RSDBP_IN, BankID     );
    cmd.addParam( "", RSDBP_IN, HeadBankID );
    cmd.addParam( "", RSDBP_IN, BankID     );
    cmd.addParam( "", RSDBP_IN, HeadBankID );

    if(FeeType == SF_FEE_TYPE_PERIOD)  
      strSql = strSql + " ?, " ;
      cmd.addParam( "", RSDBP_IN, SfSrvDoc.DatePay );
    else
      strSql = strSql + " sfdef.t_DateFee, " ;
    end;

    strSql = strSql + " chr(1) ";

    if(FeeType == SF_FEE_TYPE_PERIOD)  
      strSql = strSql + " FROM dsfdef_dbt sfdef, doprtemp_tmp tmp, dsfsi_dbt sicredit, dsfsi_dbt sidebet " +
                        " WHERE sfdef.t_feetype = ? " +
                        "   AND sfdef.t_id = TO_NUMBER (tmp.t_documentid) ";
      cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
    else
      strSql = strSql + " FROM dsfdef_dbt sfdef, dsfoprcompay_tmp tmp, dsfsi_dbt sicredit, dsfsi_dbt sidebet " +
                        " WHERE sfdef.t_FeeType = ? " +
                        " AND sfdef.t_ID = tmp.t_SfDefID " +
                        " AND tmp.t_BlockID = ? " +
                        " AND tmp.t_Number_Step = ? " +
                        " AND tmp.t_AutoFormDocs = 'X' ";

      cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE );
      cmd.addParam( "", RSDBP_IN, BlockID );
      cmd.addParam( "", RSDBP_IN, Number_Step );
    end;
             
    strSql = strSql + " AND sicredit.t_objecttype = ?  AND sicredit.t_debetcredit = ?  " +
                      " AND sicredit.t_objectid = LPAD (TO_CHAR (sfdef.t_id), 10, '0') " +
                      " AND sidebet.t_ObjectType = ? AND sidebet.t_DebetCredit = ? " +
                      " AND sidebet.t_ObjectID = LPAD (TO_CHAR (sfdef.t_id), 10, '0') ";

    cmd.addParam( "", RSDBP_IN, ObjectType         ); 
    cmd.addParam( "", RSDBP_IN, BANK_CREDIT        );
    cmd.addParam( "", RSDBP_IN, ObjectType         ); 
    cmd.addParam( "", RSDBP_IN, BANK_DEBET         );

    cmd.CmdText = strSql;
    cmd.execute();   

    strSql = " UPDATE dsfground_tmp tmp " +
             " SET tmp.t_Ground = DECODE(LEAST(to_date('01 09 2012','dd.mm.yyyy'), tmp.t_DocDate), " +
             "                           tmp.t_DocDate, " +
             "                           '{VO99020}', " +
             "                           '{VO80050}') " +
             " WHERE tmp.t_ComFIID = ? " +
             "   AND EXISTS( SELECT 1 FROM dparty_dbt pt " +
             "       WHERE pt.t_PartyID = tmp.t_PartyID " +
             "         AND pt.t_LegalForm != ? " +
             "         AND pt.t_NotResident = 'X' ) " ;
             
    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, NATCUR  );
    cmd.addParam( "", RSDBP_IN, PTLEGF_PERSN  );

    cmd.execute();   


    strSql = " UPDATE dsfground_tmp tmp " +
             " SET tmp.t_Ground = '{VO80150}' " +
             " WHERE tmp.t_ComFIID != ? " +
             "   AND tmp.t_DocDate >= to_date('01 09 2012','dd.mm.yyyy') " + 
             "   AND EXISTS( SELECT 1 FROM dparty_dbt pt, dpersn_dbt persn " +
             "       WHERE pt.t_PartyID = tmp.t_PartyID AND persn.t_PersonID = pt.t_PartyID " +
             "         AND pt.t_NotResident != 'X' " +
             "           AND " +
             "           (pt.t_LegalForm = ? " +
             "           AND persn.t_IsEmployer = 'X' " +
             "        AND EXISTS (SELECT 1 FROM dobjatcor_dbt attr " +
             "              WHERE attr.t_ObjectType = ? " +
             "               AND attr.t_Object = to_char(pt.t_PartyID) " +
             "               AND attr.t_GroupID = 16 " + // ТИП СУБЪЕКТА
             "               AND (attr.t_AttrID = 2 " + // адвокат
             "                               OR attr.t_AttrID = 3)) " + // нотариус
             "         ) " +
             "         OR " +
             "         ( pt.t_LegalForm != ?  " +
             "        AND NOT EXISTS (SELECT 1 FROM dpartyown_dbt ptown " +
             "           WHERE ptown.t_PartyID = tmp.t_PartyID AND (ptown.t_PartyKind = 2 OR ptown.t_PartyKind = 56) " +
             "      ) " +
             "   )) " ;
        

    cmd = RsdCommand( strSql );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, NATCUR        );
    cmd.addParam( "", RSDBP_IN, PTLEGF_PERSN  );
    cmd.addParam( "", RSDBP_IN, OBJTYPE_PARTY );
    cmd.addParam( "", RSDBP_IN, PTLEGF_PERSN  );

    cmd.execute();   
  end;

end;

private var ComissIDArray = TArray;
private var GroundArray = TArray;

private macro GetGroundTemplateCache(SfComiss)
  var groundTemplate = ""; 
  var i = 0;
  var foundIndex = -1;
  
  while(i < ComissIDArray.size())
    if(ComissIDArray[i] == SfComiss.ComissID)
      foundIndex = i;
    end;
    i = i + 1;
  end;

  if(foundIndex >= 0)
    groundTemplate = GroundArray[foundIndex];
  else
    groundTemplate = readNoteForObject( OBJTYPE_SFCOMISS, MakeObjectID(OBJTYPE_SFCOMISS, NULL, SfComiss), NOTEKIND_COMISS_GROUND );
    ComissIDArray[ComissIDArray.size()] = SfComiss.ComissID;
    GroundArray[GroundArray.size()] = groundTemplate;
  end;

  return groundTemplate;
end;

private var FIIDArray = TArray;
private var CCYArray = TArray;

private macro GetCurrencyShortNameCache(FIID)
  var CCY = "";
  var i = 0;
  var foundIndex = -1;
  
  while(i < FIIDArray.size())
    if(FIIDArray[i] == FIID)
      foundIndex = i;
    end;
    i = i + 1;
  end;

  if(foundIndex >= 0)
    CCY = CCYArray[foundIndex];
  else
    CCY = GetCurrencyShortName( FIID );
    FIIDArray[FIIDArray.size()] = FIID;
    CCYArray[CCYArray.size()] = CCY;
  end;

  return CCY;
end;

macro BuildGroundBatch(groundVO:string, ComBuf, SfComiss, SfContr)
  var ground = "";
  var ndsSum;
  var fiidCCY = "";
  var fiidTarsclCCY = "";
    
  ground = GetGroundTemplateCache(SfComiss);
  if( ground == "" )
   /*генерим назначение по жестко прописанным правилам и возвращаем его.*/
   if((SfContr != null) and (SfContr.ID > 0))
     ground = "За услуги по договору " + SfContr.Number +" от " + SfContr.DateBegin + ". ";
   end;

   ground = ground + "Оплата комиссии " + SfComiss.code;

   ndsSum = ComBuf.SumNDS;

   fiidCCY = GetCurrencyShortNameCache(ComBuf.fiid_sum);
   
   if( ndsSum > $0 )
     ground = ground + ". В том числе НДС " + ndsSum + " " + fiidCCY +".";
   else
     ground = ground + ". НДС не облагается.";
   end;

  else  
    fiidTarsclCCY = GetCurrencyShortNameCache( ComBuf.fiid_Tarscl );
    fiidCCY       = GetCurrencyShortNameCache( ComBuf.fiid_Sum );

    BuildGroundTmpl( ComBuf, SfComiss, SfContr, ground, fiidCCY, fiidTarsclCCY );
  end;

  ground = groundVO + ground;

  return ground;
end;

