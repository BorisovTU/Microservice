/*
$Name:        ws_ssusage_spid.mac
$Module:      Web-общее
$Description: Макрос для вызова сервиса "Простые сервисы. Упрощенная идентификация" RS-Connect
*/

import XmlRpcInter;
import rcw;

////////////////////////////////////* Создание java-объектов и вызов сервисов *//////////////////////////
private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");

macro CreateHeaderSSASRequestJava( )
  var javaObject = jvm.CreateJavaObject( "ru.softlab.rsbank.SsSource","<init>" );
  return javaObject;
end;

/* Объект запрос УПРощенной ИДентификации */
macro CreateSSASRequestSPIDJava( )
  var javaObject = jvm.CreateJavaObject( "ru.softlab.rsbank.SsasRequestSPID","<init>" );
  return javaObject;
end;

/* Сервис УПРощенной ИДентификации */
macro callCheckSPID ( SSSource, SSASRequest, urlAddress : string )
  var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V",  urlAddress);
  var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
  var port = proxy.getIntegrationWSPort();
  var result = port.wsSsCheckSPID(SSSource, SSASRequest);
  return result;
end;

/* Сервис получения результатов УПРощенной ИДентификации */
macro callGetSPID (SSSource, ObjectID, MessageInitID, urlAddress : string )
  var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V",  urlAddress);
  var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
  var port = proxy.getIntegrationWSPort();
  var result = port.wsSsGetSPID(SSSource, ObjectID, MessageInitID);
  return result;
end;

////////////////////////////////////* Классы-обёртки *///////////////////////////////////////////////////
/* Заголовк запроса */
class SSHead
   var MessageID		:string;
   var SenderID			:string;
   var SenderPointID	:string,  
end;

/* Параметры УПРощенной ИДентификации */
class SSCheckSPIDParam
   	var lastName 		: string;
	var firstName 		: string;
	var patronymic 		: string;
	var passportSeries 	: string;
	var passportNumber 	: string;
	var SNILS 			: string;
	var INN 			: string;
end;

/* Параметры получения результатов УПРощенной ИДентификации */
class SSGetSPIDParam
   var ObjectID			: string;
   var MessageInitID 	: string;
end;

/* Параметры ответа сервиса*/
class SSDescription
	/* Структура ошибки СМЭВ */
	class SSExtError
		var returnCode 		: string;
		var returnText 		: string;
	end;
	/* Структура ответа СМЭВ*/
	class SSResponse
		var result 		: string;
	end;
	
	var messageRevID		: string;		// GUID ответа
	var messageSendID 		: string;		// GUID стартового сообщения
	var objectError			: string;		// Ошибка, возвращенная из Коннекта
	var objectId			: string;		// ИД запроса в Коннекте
	var extError			: SSExtError;	// Ошибка, полученная из СМЭВ
	var senderId			: string;
	var senderPointID		: string;
    var statusCode			: string;
    var statusName			: string;
	var response			: SSResponse;

end;

/////////////////////////////* Заполнение классов-обёрток *////////////////////////////////////////////
macro fill_SSHead (_MessageID, _SenderID, _SenderPointID) : SSHead
	var _sshead : SSHead;
		_sshead.MessageID 			= _MessageID;
		_sshead.SenderID 			= _SenderID;
		_sshead.SenderPointID 		= _SenderPointID;
	return _sshead;
end;

macro fill_SSCheckSPIDParam (_lastName : string, _firstName : string, _patronymic : string, _passportSeries : string, _passportNumber : string, _SNILS : string, _INN : string) : SSCheckSPIDParam
	var _checkParam : SSCheckSPIDParam;
		_checkParam.lastName 			= _lastName 		;
		_checkParam.firstName 			= _firstName 		;
		_checkParam.patronymic 			= _patronymic 		;
		_checkParam.passportSeries 	    = _passportSeries 	;
		_checkParam.passportNumber 	    = _passportNumber 	;
		_checkParam.SNILS 				= _SNILS 			;
		_checkParam.INN 				= _INN 				;
	return _checkParam;
end;

macro fill_SSGetSPIDParam (_ObjectID : string, _MessageInitID : string) : SSGetSPIDParam
	var _getParam : SSGetSPIDParam;
	_getParam.ObjectID = _ObjectID;
	_getParam.MessageInitID = _MessageInitID;
	return _getParam;
end;

macro fill_SSDescription (_SSASDescription) : SSDescription
	var _description : SSDescription;
	_description.messageRevID 			= _SSASDescription.messageRevID;
	_description.messageSendID 			= _SSASDescription.messageSendID;
	_description.objectError 			= _SSASDescription.objectError;
	_description.objectID 				= _SSASDescription.objectID;
	_description.extError.returnCode 	= _SSASDescription.SSExtError.returnCode;
	_description.extError.returnText 	= _SSASDescription.SSExtError.returnText;
	_description.senderID 				= _SSASDescription.senderID;
	_description.senderPointID 			= _SSASDescription.senderPointID;
	_description.statusCode 			= _SSASDescription.statusCode;
	_description.statusName 			= _SSASDescription.statusName;
	_description.response.result 		= _SSASDescription.SSASResponse.result;
	return _description;
end;
///////////////////////////////* Вызываемые функции *//////////////////////////////////////////////////
macro CheckSPID (urlAddress : string, head : SSHead, param : SSCheckSPIDParam
): SSDescription
	var _sshead = CreateHeaderSSASRequestJava();
		_sshead.messageID 			= head.MessageID;
		_sshead.senderID 			= head.SenderID;
		_sshead.senderPointID 		= head.SenderPointID;
	var _checkParam = CreateSSASRequestSPIDJava();
		_checkParam.lastName 		= param.lastName 		;
		_checkParam.firstName 		= param.firstName 		;
	      if (ValType(param.patronymic)!= V_UNDEF )
			_checkParam.patronymic 		= param.patronymic 		;
	      end;
		_checkParam.passportSeries 	= param.passportSeries 	;
		_checkParam.passportNumber 	= param.passportNumber 	;
	      if (ValType(param.SNILS)!= V_UNDEF )
			_checkParam.SNILS 			= param.SNILS 			;
		end;
	      if (ValType(param.INN)!= V_UNDEF )
		_checkParam.INN 			= param.INN 			;
		end;
	return fill_SSDescription(callCheckSPID(_sshead, _checkParam, urlAddress));
end;

macro GetSPIDResult (urlAddress : string, head : SSHead, param : SSGetSPIDParam): SSDescription
	var _sshead = CreateHeaderSSASRequestJava();
		_sshead.messageID 			= head.MessageID;
		_sshead.senderID 			= head.SenderID;
		_sshead.senderPointID 		= head.SenderPointID;
	return fill_SSDescription(callGetSPID(_sshead, param.ObjectID, param.MessageInitID, urlAddress));
end;

//////////////////////////////////* Пример использования */////////////////////////////////////////////
/*
////////////////////
var url = "http://localhost:8080/rs-connect-1.0.1/ws/integration";
var senderID = "PFR";
var senderPointID = "Пункт";
//////////////////// Первоначальный запрос УПРощенной ИДентификации
// создание объекта заголовка запроса
var a = fill_SSHead(substr(CreateGUID(), 2, 36), senderID, senderPointID);
// создание объекта тела запроса
//var b = fill_SSCheckSPIDParam ("ПЕТИНА", "ЕЛЕНА", "ВЛАДИМИРОВНА", "8888", "534534", "111-222-333 44", "323200001011");
var b = fill_SSCheckSPIDParam ("ПЕТИНА", "ЕЛЕНА", "ВЛАДИМИРОВНА", "8888", "534534", null, null);

// вызов сервиса УПРощенной ИДентификации
var checkresult = CheckSPID(url, a, b);
//
//////////////////// Получение результата по возвращенному objectID
// заполнение тела запроса результата по возвращенному objectID
var c = fill_SSGetSPIDParam (checkresult.objectID, "");
// вызов сервиса получения результата ранее запрошенной валидации (по objectID)
var getresultByObjId 	= GetSPIDResult(url, a, c);

//////////////////// Получение результата по GUID стартового сообщения 
// заполнение тела запроса результата по GUID стартового сообщения
var d = fill_SSGetSPIDParam ("", a.messageID);
// вызов сервиса получения результата ранее запрошенной валидации (по GUID стартового сообщения)
var getresultByInitID 	= GetSPIDResult(url, a, d);

*/