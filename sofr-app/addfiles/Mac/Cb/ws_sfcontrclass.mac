 /*
 $Name: ws_sfcontrclass.mac
 $Module: Главная книга
 $Description: Макрос виджета ServContractListingWidget 
 */
 
import rsd, cb_sql;
import RsbDataSet;

class TWsSfContr
  var ID             : integer;
  var Number_        : string;
  var Name_          : string;
  var DateConc       : date;
  var PayerID        : integer;
  var PayerName      : string;
  var ContrActorID   : integer;
  var ContrActorName : string;
  var ServKind       : integer;
  var Department     : integer;
end;

macro GetTWsSfContrByID(ID : integer) : TWsSfContr

  var result;

  var cmd = RSDCommand();
  var query = " SELECT contr.t_ID, contr.t_Number, contr.t_Name" +
              " FROM dsfcontr_dbt contr " +
              " WHERE contr.t_ID = " + ID;
              
  cmd.CmdText = query;
  cmd.NullConversion = true;

  println( cmd.CmdText );

  var rs = rsdRecordSet( cmd );  

  if( rs.moveNext() )
    var contr = TWsSfContr();
    
    contr.ID              = rs.value("t_ID");
    contr.Number_         = rs.value("t_Number");
    contr.Name_           = rs.value("t_Name");

    result = contr;
  end;  

  return result;

end;
 
 ////////////////////////////////////////////////////////////////////////////////////////////////////////////
// class SfContrList
////////////////////////////////////////////////////////////////////////////////////////////////////////////

class SfContrList()

  /* Аналог оператора ?: в си. */
  private macro IIF( condition, value_true, value_false )
     if( condition )
        return value_true;
     else 
        return value_false;
     end;
  end;

  private macro GetSortStr( OrderItems,            /* Параметры сортировки */
                            DefaultSortStr:string, /* Строка сортировки по умолчанию (первичному ключу) */
                            Table:string           /* Название таблицы c . */
                          ):String
                          
    var SortStr:string = "";

    if( (OrderItems == null) or (OrderItems.Size() <= 0) )
    
       SortStr = " " + DefaultSortStr + " ";
       
    else
    
       SortStr = " " + OrderItems[0].Column + " " + IIF(OrderItems[0].Direction == 0, "asc", "desc");
       var i:integer = 1;
       while( i < OrderItems.size() )
          SortStr = SortStr + ", " + OrderItems[i].Column + " " + IIF(OrderItems[i].Direction == 0, "asc", "desc");
          i = i + 1;
       end;
       SortStr = SortStr + " ";
       
    end;

    return SortStr;
    
  end;

  macro SetSfContrQuery
  (
    pagesize      : integer,
    pagenum       : integer,
    FilterParm,                 // параметры фильтрации 
    DateConcBegin : date,       // Начало диапазона дат заключения ДО
    DateConcEnd   : date,       // Конец диапазона дат заключения ДО
    WhereCond     : string,     // Дополнительное SQL-условие
    NumberLk      : string,     // пользовательский код ДО
    listLen       : integer,    // длина списка
    orderitems,
    cmd
  )
    var query = " SELECT contr.t_ID, contr.t_Number As Number_, contr.t_Name As Name_, contr.t_DateConc As DateConcStr,     " +
                  " contr.t_PartyID, payer_pt.t_Name As PayerName, " +
                  " contr.t_ContractorID, NVL(contr_pt.t_Name, chr(1)) As ContrActorName, " +
                  " contr.t_ServKind, contr.t_Department " +
                " FROM dsfcontr_dbt contr, dparty_dbt payer_pt, dparty_dbt contr_pt      " +
                " WHERE contr.t_PartyID = payer_pt.t_PartyID " +
                  " AND contr.t_ContractorID = contr_pt.t_PartyID(+) ";
                  
    if( NumberLk != NULL )
      query = query + " AND LOWER (contr.t_Number) LIKE LOWER ('%' || ? || '%') ";
      cmd.addParam( "", RSDBP_IN, NumberLk );
    end; 
    
    if( listLen != 0 )
      query = query + " AND ROWNUM < " + string(listLen);
    end;                

    if( FilterParm != null )
      
      if( (FilterParm.ID != null) AND (FilterParm.ID > 0) )       
        query = query + " AND contr.t_ID = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.ID );
      end;
      
      if( (FilterParm.Number_ != null) AND (FilterParm.Number_ > 0) )       
        query = query + " AND contr.t_Number = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.Number_ );
      end;    

      if( (FilterParm.ServKind != null) AND (FilterParm.ServKind > 0) )       
        query = query + " AND contr.t_ServKind = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.ServKind );
      end;


      if( (FilterParm.Department != null) AND (FilterParm.Department > 0) )       
        query = query + " AND contr.t_Department = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.Department );
      end;


      if( (FilterParm.PayerID != null) AND (FilterParm.PayerID > 0) )       
        query = query + " AND contr.t_PartyID = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.PayerID );
      end;

      if( (FilterParm.ContrActorID != null) AND (FilterParm.ContrActorID > 0) )       
        query = query + " AND contr.t_ContrActorID = ? ";
        cmd.addParam( "", RSDBP_IN, FilterParm.ContrActorID );
      end;

    end;

    // фильтрация по дате заключения из диапазона дат. 
    // Даты могут быть не заданы, а может быть задана только одна из дат, т.е.
    // диапазон дат может быть не закрыт с одного из концов
    
    if( DateConcBegin != date( 0, 0, 0 ) )
      query = query + " AND contr.t_DateConc >= ? ";
      cmd.addParam( "", RSDBP_IN, DateConcBegin );
    end;

    if( DateConcEnd != date( 0, 0, 0 ) )
      query = query + " AND contr.t_DateConc <= ? ";
      cmd.addParam( "", RSDBP_IN, DateConcEnd );
    end;

    if( WhereCond != null )
      query = query + " AND (" + WhereCond + ")";
    end;
    
    query = query + " ORDER BY " + GetSortStr(orderitems, "contr.t_Number", "contr.");

    if(pagesize != 0)
    
      query = SQL_WrapSelect( query, pagenum, pagesize);
      
    end; 
    
    return query;
  end;

  macro GetList
  (
    pagesize      : integer,
    pagenum       : integer,
    FilterParm,                 // параметры фильтрации 
    DateConcBegin : date,       // Начало диапазона дат заключения ДО
    DateConcEnd   : date,       // Конец диапазона дат заключения ДО
    WhereCond     : string,     // Дополнительное SQL-условие
    NumberLk      : string,     // пользовательский код ДО
    listLen       : integer,    // длина списка
    orderitems
  )
        
    var cmd = RSDCommand();
    
    var query = SetSfContrQuery(pagesize,
                                pagenum,
                                FilterParm,
                                DateConcBegin,
                                DateConcEnd,
                                WhereCond,
                                NumberLk,
                                listLen,
                                orderitems,
                                cmd);
   
    cmd.CmdText = query;
    cmd.NullConversion = true;

    println("*** SF_GetContrList ***");
    println( cmd.CmdText );

    var rs = rsdRecordSet( cmd );

    var result = TArray();

    while( rs.moveNext() )
      var contr = TWsSfContr;
      
      contr.ID              = rs.value("t_ID");
      contr.Number_         = rs.value("Number_");
      contr.Name_           = rs.value("Name_");
      contr.DateConc        = rs.value("DateConcStr");
      contr.PayerID         = rs.value("t_PartyID");
      contr.PayerName       = rs.value("PayerName");
      contr.ContrActorID    = rs.value("t_ContrActorID");
      contr.ContrActorName  = rs.value("ContrActorName");
      contr.ServKind        = rs.value("t_ServKind");
      contr.Department      = rs.value("t_Department");

      result.value(result.Size) = contr;
    end;

    return result;

  end;  

  macro GetCount
  (
    FilterParm,                 // параметры фильтрации 
    DateConcBegin : date,       // Начало диапазона дат заключения ДО
    DateConcEnd   : date,       // Конец диапазона дат заключения ДО
    WhereCond     : string,     // Дополнительное SQL-условие
    listLen       : integer,    // длина списка
    orderitems
  ):integer
  
    var result : integer;
    
    var cmd = RSDCommand();
    
    var query = "SELECT COUNT(*) C FROM ( SELECT * FROM ( ";
    
    query = query + SetSfContrQuery(0,
                                    0,
                                    FilterParm,
                                    DateConcBegin,
                                    DateConcEnd,
                                    WhereCond,
                                    null,
                                    listLen,
                                    orderitems,
                                    cmd);

    query = query + "))";
   
    cmd.CmdText = query;
    cmd.NullConversion = true;

    println("*** SF_GetContrCount ***");
    println( cmd.CmdText );

    var rs = rsdRecordSet( cmd );  

    if (rs.moveNext())

      result = rs.value("c");

    end;

    return result;

  end;

  macro GetRowNum
  (
    SfContrID     : integer,
    FilterParm,                 // параметры фильтрации 
    DateConcBegin : date,       // Начало диапазона дат заключения ДО
    DateConcEnd   : date,       // Конец диапазона дат заключения ДО
    WhereCond     : string,     // Дополнительное SQL-условие
    listLen       : integer,    // длина списка
    orderitems
  ):integer

    var result : integer = 0;
    
    var cmd = RSDCommand();
    
    var query = " SELECT RN " +
                "   FROM (SELECT ROWNUM RN, t_ID " +
                "           FROM ( ";
                
    query = query + SetSfContrQuery(0,
                                    0,
                                    FilterParm,
                                    DateConcBegin,
                                    DateConcEnd,
                                    WhereCond,
                                    null,
                                    listLen,
                                    orderitems,
                                    cmd);              
                
    query = query + ")) WHERE t_ID = " + string(SfContrID);
   
    cmd.CmdText = query;
    cmd.NullConversion = true;

    println("*** GetSfContrRowNum ***");
    println( cmd.CmdText );

    var rs = rsdRecordSet( cmd ); 

    if (rs.moveNext())

      result = rs.value("RN");

    end;

    return result;

  end;  

end;

/////////////////////////////////////////////////////////////////////////////////////////////////////////