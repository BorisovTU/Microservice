/*
$Name:           ptctdim.mac
$Module:         Ядро ГКБО
$Description:    Реализация процедуры загрузки справочников ОКАТО, ОКТМО, ОКВЭД
*/

import rsexts;
import dbf2ora, RSD, cb_sql, globals;
import rslx, osfile;
import dlmisc;
import bnk_common;
import "dlquery.mac";

FILE OBJGROUP    ("objgroup.dbt")WRITE ;
FILE OBJATTR     ("objattr.dbt") WRITE key 2;
record ObjAttrRec("objattr.dbt"); 

private var isOkato11DigitsFormat = Bnk_GetRegistryValue("CB\\PARTY\\ИМПОРТ СПРАВОЧНИКОВ\\ФОРМАТ ОКАТО 11 РАЗРЯДОВ", V_BOOL, false);
private var isOkatoNewFunctional = Bnk_GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДОП ФУНКЦИОНАЛ ОКАТО", V_BOOL, false);
private const FULL_OKATO_CODE_LENGTH = 11;

var CountOKATO : integer;
var NumInsert, NumDelete, NumReplace;

var CountOKTMO : integer;
var CodeOKTMOArray : TArray;
var NameOKTMOArray : TArray;

var CountOKVED2 : integer;
var RecIDOKVED2 = 0;
var RecIDOKVED2Array : TArray;
var CodeOKVED2Array : TArray;
var NameOKVED2Array : TArray;

var CountOKPD2 : integer;
var RecIDOKPD2 = 0;
var RecIDOKPD2Array : TArray;
var CodeOKPD2Array : TArray;
var NameOKPD2Array : TArray;

private var RemoveSpacesFromOktmo = true; //флаг удаления пробелов из кода ОКТМО

private var WordApp;

private var IsOKVED2ForParty = true;

MACRO PrintHead(Name, ImpFileName, ImpStatus, CreateReport:bool, StatisticsOnly:bool)
  var StrStatus = TArray;

  if (CreateReport == true)

  StrStatus(0) = "Оставить";
  StrStatus(1) = "Заменить";
  StrStatus(2) = "Дополнить";
  StrStatus(3) = "Обновить"; 


[Протокол загрузки справочника ######                                           ](Name);
[                                                                               ];
[ Дата формирования:    ##########                                              ] (date:f);
[ Время формирования:   ########                                                ] (time:f);
[ Операционист:         ##### #                                                 ] ({oper}, {Name_Oper});
[                                                                               ];

[ Файл импорта:         #                                                       ](ImpFileName);
[ Статус обновления категории: #                                                ](StrStatus(ImpStatus));

    if (StatisticsOnly == false)
[                                                                               ];
[+--------+-------------+-----------------------------------+----------+----------+];
[|Действие|  Номер      |              Название             | Открыто  | Закрыто  |];
[+--------+-------------+-----------------------------------+----------+----------+];
    end;

  end;

END;

private macro PrintHeadOKTMO(Name, ImpFileName, ImpStatus, CreateReport:bool, StatisticsOnly:bool, ImportPlaces:bool)
  var StrStatus = TArray;
  var DirList = TDirList;
  var i = 0;

  if (CreateReport == true)

  StrStatus(0) = "Оставить";
  StrStatus(1) = "Заменить";
  StrStatus(2) = "Дополнить";
  StrStatus(3) = "Обновить"; 

[Протокол загрузки справочника #####                                            ](Name);
[                                                                               ];
[ Дата формирования:    ##########                                              ] (date:f);
[ Время формирования:   ########                                                ] (time:f);
[ Операционист:         ##### #                                                 ] ({oper}, {Name_Oper});
[                                                                               ];

  DirList.List(ImpFileName, "F");
  i = 0;
  while(i < DirList.Count)

    if(i == 0)
[ Файл импорта:         #                                                       ](DirList.Name(i));
    else
[                       #                                                       ](DirList.Name(i));
    end;
    i = i + 1;
  end;  
/*
[ Статус обновления категории: #                                                ](StrStatus(ImpStatus));
*/
    if(ImportPlaces)
[ Населенные пункты: импортировать                                              ];
    else
[ Населенные пункты: не импортировать                                           ];
    end;

    if (StatisticsOnly == false)
[                                                                               ];
[+--------+--------------+-----------------------------------------------------+];
[|Действие|  Номер       |          Название                                   |];
[+--------+--------------+-----------------------------------------------------+];
    end;

  end;
end;

MACRO PrintBody(Action:string, Number:string, Name:string, OpenDate:date, CloseDate:date, CreateReport:bool, StatisticsOnly:bool)
  ARRAY  StringArray;
  var i = 1;
  var tCloseDate : date;


  if(Action == "Введено")
    NumInsert = NumInsert + 1;
  end;
    
  if(Action == "Удалено")
    NumDelete = NumDelete + 1;
  end;
             
  if((Action == "Заменено") or (Action == "Закрыто"))
    NumReplace = NumReplace + 1;
  end;

  if ((CreateReport == true) AND (StatisticsOnly == false))

    StrSplit(Name, StringArray, 35);

    if(CloseDate == date(31,12,2099)) 
       tCloseDate = date(0,0,0);
    else
      tCloseDate = CloseDate;
    end;

[|########|#############|###################################|##########|##########|] (Action:r, Number:l, StringArray(0):w, OpenDate:f, tCloseDate:f);

    while ( StrLen(StringArray(i) ) > 0)
[|        |             |###################################|          |          |] (StringArray(i):w);
    i = i + 1;
    end;
  end;
END;

private macro PrintBodyOKTMO(Action:string, Number:string, Name:string, OpenDate:date, CloseDate:date, CreateReport:bool, StatisticsOnly:bool)
  ARRAY  StringArray;
  var i = 1;
  /*var tCloseDate : date;*/

  if(Action == "Введено")
    NumInsert = NumInsert + 1;
  end;
    
  if(Action == "Удалено")
    NumDelete = NumDelete + 1;
  end;
             
  if((Action == "Заменено") or (Action == "Закрыто"))
    NumReplace = NumReplace + 1;
  end;

  if ((CreateReport == true) AND (StatisticsOnly == false))

    StrSplit(Name, StringArray, 53);
/*
    if(CloseDate == date(31,12,2099)) 
       tCloseDate = date(0,0,0);
    else
      tCloseDate = CloseDate;
    end;
*/
[|########|##############|#####################################################|] (Action:r, Number:l, StringArray(0):w);

    while ( StrLen(StringArray(i) ) > 0)
[|        |              |#####################################################|] (StringArray(i):w);
    i = i + 1;
    end;
  end;
end;

MACRO PrintTail(InFile:integer, InBase:integer, nInsert:integer, nDelete:integer, nReplace:integer, CreateReport:bool, StatisticsOnly:bool)

  if(CreateReport == true)
    if ( (StatisticsOnly == false))
[+--------+-------------+-----------------------------------+----------+----------+];
    end;
[                                                                               ];
[Загрузка завершена:                                                            ];
[ - в файле  ####### записей                                                    ](InFile:r);
[ - в базе   ####### записей                                                    ](InBase:r);
[ - введено  ####### записей                                                    ](nInsert:r);
[ - удалено  ####### записей                                                    ](nDelete:r);
[ - заменено ####### записей                                                    ](nReplace:r);
[                                                                               ];
  end;
END; 

private macro PrintTailOKTMO
(
  InFileDistricts:integer
 ,InFilePlaces:integer
 ,InBaseDistricts:integer
 ,InBasePlaces:integer
 ,nInsert:integer
 ,nDelete:integer
 ,nReplace:integer
 ,CreateReport:bool
 ,StatisticsOnly:bool
)

  if(CreateReport == true)
    if ( (StatisticsOnly == false))
[+--------+--------------+-----------------------------------------------------+];
    end;
[                                                                               ];
[Загрузка завершена:                                                            ];
[ - в файле  ####### муниципальных образований, ####### населенных пунктов      ](InFileDistricts:r, InFilePlaces:r);
[ - в базе   ####### муниципальных образований, ####### населенных пунктов      ](InBaseDistricts:r, InBasePlaces:r);
[ - введено  ####### записей                                                    ](nInsert:r);
[ - удалено  ####### записей                                                    ](nDelete:r);
[ - заменено ####### записей                                                    ](nReplace:r);
[                                                                               ];
  end;
end; 

MACRO GetMaxNumObjAttr(ObjectType, GroupID)
  var sqlString, rs, MaxNum = 0, IsMaxNumNULL; 
 
  sqlString = " SELECT MAX(T_ATTRID) NUM FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = "+ ObjectType +
              " AND T_GROUPID = " + GroupID;

  rs = RsdRecordset( sqlString );
  rs.moveNext();

  rs.value("NUM" , IsMaxNumNULL );
  
  if(IsMaxNumNULL != true)
    MaxNum = rs.value("NUM");
  else
    MaxNum = 0;
  end;

  return MaxNum;
END;


MACRO DeleteRecObjAttr(AttrID, ObjType, GroupID)

  var sqlString, cmd ; 
  sqlString = " DELETE FROM DOBJATCOR_DBT WHERE (T_OBJECTTYPE, T_GROUPID, T_ATTRID) IN " +
              " (SELECT T_OBJECTTYPE, T_GROUPID, " + AttrID + 
              "  FROM DOBJGROUP_DBT " +
              "  WHERE (T_ATTROBJECTTYPE = "+ ObjType + " AND T_ATTRGROUPID= "+GroupID+") OR (T_OBJECTTYPE = "+ObjType+" AND T_GROUPID = "+GroupID+" ))";


  cmd = RsdCommand( sqlString );
  cmd.execute();

  cmd = NULL;

  sqlString = " DELETE FROM DOBJATTR_DBT WHERE (T_OBJECTTYPE, T_GROUPID, T_ATTRID) IN " +
              " (SELECT T_OBJECTTYPE, T_GROUPID, " + AttrID + 
              "  FROM DOBJGROUP_DBT " +
              "  WHERE (T_ATTROBJECTTYPE = "+ ObjType + " AND T_ATTRGROUPID= "+GroupID+") OR (T_OBJECTTYPE = "+ObjType+" AND T_GROUPID = "+GroupID+" ))";


  cmd = RsdCommand( sqlString );
  cmd.execute();

END;


/*==========================================OKATO===============================================*/                         
MACRO IsExistRecOKATODBF(NumInList)
  var sqlString, rs, retval = 0, IsMaxNumNULL; 
  var FullNumInList, kod1, kod2, kod3;
  
  FullNumInList = StrSubst( String(NumInList:8), " ", "0" );
  kod1 = SubStr(FullNumInList,1,2);
  kod2 = SubStr(FullNumInList,3,3);
  kod3 = SubStr(FullNumInList,6,3);

  sqlString = " SELECT count(1)  FROM DOKATOEXPDBF_DBT WHERE T_RAZDEL = 1 "
              " AND T_KOD1 = '" + kod1 + "'" +
              " AND T_KOD2 = '" + kod2 + "'" +
              " AND T_KOD3 = '" + kod3 + "'";


  rs = RsdRecordset( sqlString );
  rs.moveNext();
  retval = rs.value(0);

  return retval;
END;

MACRO IsExistRecNewOKATODBF(NumInList)
  var sqlString, rs, retval = 0, IsMaxNumNULL; 
  var FullNumInList, kod, kod1, kod2;
  
  FullNumInList = StrSubst( String(NumInList:8), " ", "0" );
  kod = SubStr(FullNumInList,1,2);
  kod1 = SubStr(FullNumInList,3,3);
  kod2 = SubStr(FullNumInList,6,3);

  sqlString = " SELECT count(1)  FROM DOKATOEXPDBF_DBT WHERE T_RAZDEL = 1 "
              " AND T_TER =  '" + kod  + "'" +
              " AND T_KOD1 = '" + kod1 + "'" +
              " AND T_KOD2 = '" + kod2 + "'";


  rs = RsdRecordset( sqlString );
  rs.moveNext();
  retval = rs.value(0);

  return retval;
END;

private macro _CheckNeedInfoChangeNames(oldName, oldFullName, newName, newFullName)
  var NeedCngRep = false; /*Признак необходимости информирования об изменении*/
  ClearRecord(ObjAttrRec);
  ObjAttrRec.Name     = newName;
  ObjAttrRec.FullName = newName;
  if((oldName != ObjAttrRec.Name) OR (oldFullName != ObjAttrRec.FullName))
    NeedCngRep = true;
  end;
  return NeedCngRep;
end;

MACRO InsertObjAttrOKATORec(rs, UpdateFlag, CreateReport:bool, StatisticsOnly:bool)
  var stat= false;
  var AllKod : string;
  AllKod = trim(rs.value("T_KOD1"));
 
  if(trim(rs.value("T_KOD2")) != "000") 
    AllKod = AllKod + trim(rs.value("T_KOD2"));
  end;
  
  if(trim(rs.value("T_KOD3")) != "000") 
    AllKod = AllKod + trim(rs.value("T_KOD3"));
  end;

  if(UpdateFlag == 0) 
    stat = true;
    msgbox("Категория OKATO имеет статус обновления 'Оставить'. Загрузка категории из внешнего справочника невозможна")

  elif(UpdateFlag == 1)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = AllKod;
    OBJATTR.ObjectType = 3 ; //Справочник ОКАТО
    OBJATTR.GroupID    = 12; 

    if(not GetEQ(OBJATTR))
       OBJATTR.ObjectType = 3;
       OBJATTR.GroupID    = 12;
       OBJATTR.AttrID     = GetMaxNumObjAttr(3, 12) + 1;  
       OBJATTR.Name       = trim(rs.value("T_NAME_RUS"));
       OBJATTR.FullName   = trim(rs.value("T_NAME_RUS"));
       OBJATTR.NumInList  = AllKod;
       OBJATTR.NameObject = AllKod;
       OBJATTR.OpenDate   = rs.value("T_CB_DATE");                          
       OBJATTR.CloseDate  = rs.value("T_CE_DATE");                         
       PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
       Insert(OBJATTR);
    else
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("T_NAME_RUS")), trim(rs.value("T_NAME_RUS"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name       = trim(rs.value("T_NAME_RUS"));
        OBJATTR.FullName   = trim(rs.value("T_NAME_RUS"));
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    end;
  elif(UpdateFlag == 2)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = AllKod;
    OBJATTR.ObjectType = 3 ; //Справочник ОКАТО
    OBJATTR.GroupID    = 12; 

    if(not GetEQ(OBJATTR))
       OBJATTR.ObjectType = 3;
       OBJATTR.GroupID    = 12;
       OBJATTR.AttrID     = GetMaxNumObjAttr(3, 12) + 1;  
       OBJATTR.Name       = trim(rs.value("T_NAME_RUS"));
       OBJATTR.FullName   = trim(rs.value("T_NAME_RUS"));
       OBJATTR.NumInList  = AllKod;
       OBJATTR.NameObject = AllKod;
       OBJATTR.OpenDate   = rs.value("T_CB_DATE");                          
       OBJATTR.CloseDate  = rs.value("T_CE_DATE");                         
       PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
       Insert(OBJATTR);
    end;
  elif(UpdateFlag == 3)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = AllKod;
    OBJATTR.ObjectType = 3 ; //Справочник ОКАТО
    OBJATTR.GroupID    = 12; 

    if(GetEQ(OBJATTR))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("T_NAME_RUS")), trim(rs.value("T_NAME_RUS"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name       = trim(rs.value("T_NAME_RUS"));
        OBJATTR.FullName   = trim(rs.value("T_NAME_RUS"));
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    end;
  end;

  return stat;
END;

private macro _GetAttrCodeNameOKATO(rs, Code : @string, Name : @string)
  Code = trim(rs.value("t_ter"));
 
  if(trim(rs.value("t_kod1")) != "000") 
    Code = Code + trim(rs.value("t_kod1"));
  end;
  
  if(trim(rs.value("t_kod2")) != "000") 
    Code = Code + trim(rs.value("t_kod2"));
  end;

  Name = trim(rs.value("t_name1"));
end;

MACRO InsertObjAttrNewOKATORec
(
  rs
 ,UpdateFlag
 ,CreateReport:bool
 ,StatisticsOnly:bool
 ,ParentID : integer
 ,newAttrID : @integer
 ,newCode : @string
 ,newName : @string
)
  var stat = true;
  var ZeroDate = date(0, 0, 0);
  var rstatus = rs.value("t_status");
  
  _GetAttrCodeNameOKATO(rs, @newCode, @newName);

  var codeToStore = newCode;
  if (isOkato11DigitsFormat and isOkatoNewFunctional)
    codeToStore = codeToStore + SubStr("00000000000", StrLen(newCode) + 1, 11); // Дополнение ОКАТО до 11 символов
  end;

  ClearRecord(OBJATTR);
  OBJATTR.NumInList  = codeToStore;
  OBJATTR.ObjectType = 3 ; //Справочник ОКАТО
  OBJATTR.GroupID    = 12; 
  
  if(GetEQ(OBJATTR))
    if((rstatus == 0) or (rstatus == 2) or (rstatus == 3))
      // Пишем в протокол о замене только если действительно поменялось наименование
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, newName, newName) or ((rstatus == 3) and (OBJATTR.OpenDate != rs.value("t_datevved"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = newName;
        OBJATTR.FullName = newName;
        if(rstatus == 3)
          OBJATTR.OpenDate = rs.value("t_datevved");                          
        end;
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      end;
    elif(rstatus == 1)
      OBJATTR.CloseDate = rs.value("t_datevved");                          
      PrintBody("Закрыто", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
    end;
    OBJATTR.ParentID = ParentID;
    newAttrID = OBJATTR.AttrID;
    Update(OBJATTR);
  else
    if((rstatus == 0) or (rstatus == 2) or (rstatus == 3))
      newAttrID = GetMaxNumObjAttr(3, 12) + 1;
      OBJATTR.ObjectType = 3;
      OBJATTR.GroupID    = 12;
      OBJATTR.AttrID     = newAttrID;  
      OBJATTR.Name       = newName;
      OBJATTR.FullName   = newName;
      OBJATTR.NumInList  = codeToStore;
      OBJATTR.NameObject = codeToStore;
      OBJATTR.OpenDate   = rs.value("t_datevved");                          
      OBJATTR.CloseDate  = ZeroDate;                         
      OBJATTR.ParentID   = ParentID;
      PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Insert(OBJATTR);
    end;
  end;

  UseProgress(CountOKATO = CountOKATO + 1);
  
  return stat;
END;

private macro _CountEndNullOKATO(Code : string)
  var Count = 0;

  if(strlen(Code) > 0)
    if(substr(Code, strlen(Code)) == "0")
      Count = Count + 1;
      if(strlen(Code) > 1)
        Count = Count + _CountEndNullOKATO(substr(Code, 1, strlen(Code) - 1));
      end;
    end;
  end;

  return Count;
end;

private macro _CheckNextOKATORecIsChild
(
  rs
 ,PrevCode
 ,PrevName
)
  var IsChild = false;
  var IsPrevGroupRoot = false;
  var IsNextGroupRoot = false;
  var PrevCountEndNull = 0;
  var NextCountEndNull = 0;
  var NextCode : string;
  var NextName : string;
  _GetAttrCodeNameOKATO(rs, @NextCode, @NextName);

  if(strlen(PrevCode) < strlen(NextCode))
    IsChild = true;
  elif(strlen(PrevCode) == strlen(@NextCode))
    if(substr(PrevName, strlen(PrevName)) == "/")
      IsPrevGroupRoot = true;
    end;
    if(substr(NextName, strlen(NextName)) == "/")
      IsNextGroupRoot = true;
    end;

    PrevCountEndNull = _CountEndNullOKATO(PrevCode);
    NextCountEndNull = _CountEndNullOKATO(NextCode);

    if(IsPrevGroupRoot and ((PrevCountEndNull > NextCountEndNull) or ((PrevCountEndNull == NextCountEndNull) and (not IsNextGroupRoot))))
      IsChild = true;
    elif((not IsPrevGroupRoot) and (PrevCountEndNull >= NextCountEndNull) and IsNextGroupRoot)
      IsChild = true;
    elif((NextCountEndNull > 1) and (PrevCountEndNull > NextCountEndNull) and (not IsNextGroupRoot))
      IsChild = true;
    end;

  end;

  return IsChild;
end;

private macro _RecursionInsertObjAttrNewOKATORec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
 ,ParentID : integer
 ,PrevCode : string
 ,PrevName : string
 ,MoveNext : @bool
 ,Eof : @bool
)
  var stat = true;
  var newAttrID;
  var newCode;
  var newName;

  while(stat and (not Eof))

    if(MoveNext)
      stat = rs.moveNext();
      if(not stat)
        Eof = true;
      end;
      MoveNext = false;
    end;

    if(stat)
      if((ParentID == 0) or _CheckNextOKATORecIsChild(rs, PrevCode, PrevName))
        stat = InsertObjAttrNewOKATORec
        (
          rs
         ,UpdateFlag
         ,CreateReport
         ,StatisticsOnly
         ,ParentID
         ,@newAttrID
         ,@newCode
         ,@newName
        );
        if(stat)
          MoveNext = true;
        end;
      else
        stat = false;
      end;
    end;

    if(stat)
      stat = _RecursionInsertObjAttrNewOKATORec
      (
        rs
       ,UpdateFlag
       ,CreateReport
       ,StatisticsOnly
       ,newAttrID
       ,newCode
       ,newName
       ,@MoveNext
       ,@Eof
      );
      if(not MoveNext)
        stat = true;
      end;
    end;
  end;

  return stat;
end;

private macro _StartRecursionInsertObjAttrNewOKATORec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
)
  var ParentID = 0;
  var PrevCode = "";
  var PrevName = "";
  var MoveNext = true;
  var Eof = false;

  var stat = _RecursionInsertObjAttrNewOKATORec
  (
    rs
   ,UpdateFlag
   ,CreateReport
   ,StatisticsOnly
   ,ParentID
   ,PrevCode
   ,PrevName
   ,@MoveNext
   ,@Eof
  );

  return stat;
end;

MACRO ImpOKATOAll(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var pbar, Number :INTEGER, Count :INTEGER;

  var NumInFile, NumInBase;

  Number = 0; Count = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКАТО
  OBJGROUP.GroupID    = 12; 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = 3;
     OBJGROUP.GroupID    = 12;
     OBJGROUP.Name       = "Справочник ОКАТО";
     OBJGROUP.Type       = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 0;
     Insert(OBJGROUP);
  end;

  PrintHead("ОКАТО", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);


  sqlString = " SELECT count(1) " +
               " FROM DOKATOEXPDBF_DBT TB "+
               " WHERE TB.T_RAZDEL = 1 " +
               " AND SUBSTR(TRIM(TB.T_NAME_RUS), LENGTH(TRIM(TB.T_NAME_RUS)),1 ) <> '/' " +
               " AND TB.T_CE_DATE >= SYSDATE " +
               " AND TB.T_CE_DATE = (SELECT MAX(T_CE_DATE) FROM DOKATOEXPDBF_DBT WHERE T_KOD1 = TB.T_KOD1 AND T_KOD2 = TB.T_KOD2 AND T_KOD3 = TB.T_KOD3 AND T_RAZDEL = 1)";

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  Number = rs.value(0);

  if(OBJGROUP.UpdateFlag == 1)
     sqlString = " SELECT count(1) FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 12 ";
     rs = RsdRecordset( sqlString );
     rs.moveNext();
     Number = Number + rs.value(0);
     pbar=InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКАТО");

     delSqlString = "SELECT * FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 12";
     delrs = RsdRecordset( delSqlString );
    while( delrs.moveNext() ) 
      UseProgress (Count = Count + 1);
      if(NOT IsExistRecOKATODBF(delrs.value("T_NUMINLIST")))
        PrintBody("Удалено", delrs.value("t_NameObject"), delrs.value("t_FullName"), delrs.value("t_OpenDate"), delrs.value("t_CloseDate"), CreateReport, StatisticsOnly);
        DeleteRecObjAttr(delrs.value("T_ATTRID"), 3, 12);
      end;
    end;
  else
    pbar=InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКАТО");
  end;
 
  sqlString =  " SELECT TB.* " +
               " FROM DOKATOEXPDBF_DBT TB "+
               " WHERE TB.T_RAZDEL = 1 " +
               " AND SUBSTR(TRIM(TB.T_NAME_RUS), LENGTH(TRIM(TB.T_NAME_RUS)),1 ) <> '/' " +
               " AND TB.T_CE_DATE >= SYSDATE " +
               " AND TB.T_CE_DATE = (SELECT MAX(T_CE_DATE) FROM DOKATOEXPDBF_DBT WHERE T_KOD1 = TB.T_KOD1 AND T_KOD2 = TB.T_KOD2 AND T_KOD3 = TB.T_KOD3 AND T_RAZDEL = 1)";

  rs = RsdRecordset( sqlString );

  while( (rs.moveNext()) AND (stat == false))
     UseProgress (Count = Count + 1);
     stat = InsertObjAttrOKATORec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);
  end;


  sqlString = " SELECT count(1) " +
               " FROM DOKATOEXPDBF_DBT TB "+
               " WHERE TB.T_RAZDEL = 1 "; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInFile = rs.value(0);


  sqlString = "SELECT count(1) FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 12"; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInBase = rs.value(0);


  PrintTail(NumInFile, NumInBase, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

  RemProgress ();      

END;

/*=========================ОКВЭД==================================================*/

MACRO IsExistRecOKVEDDBF(NumInList)
  var sqlString, rs, retval = 0, IsMaxNumNULL; 

  sqlString = " SELECT count(1)  FROM DOKVEDEXPDBF_DBT WHERE "
              " trim(T_KOD) = '" + NumInList + "'" ;

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  retval = rs.value(0);

  return retval;
END;

MACRO ImpNewOKATOAll(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var pbar: Integer;
  var SourceRecsCount, SofrRecsCount;

  CountOKATO = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКАТО
  OBJGROUP.GroupID    = 12; 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = 3;
     OBJGROUP.GroupID    = 12;
     OBJGROUP.Name       = "Справочник ОКАТО";
     OBJGROUP.Type       = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 0;
     Insert(OBJGROUP);
  end;

  PrintHead("ОКАТО", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  sqlString = " SELECT COUNT(1) " +
              "   FROM dokatoexpdbf_dbt tb "+
              "  WHERE tb.t_razdel = 1 ";

  rs = RsdRecordset(sqlString);
  rs.moveNext();
  SourceRecsCount = rs.value(0);

  pbar = InitProgress(SourceRecsCount, " ~Alt-Break~ Прервать", "Загрузка справочника ОКАТО");

  sqlString =  " SELECT tb.* " +
               "   FROM dokatoexpdbf_dbt tb "+
               "  WHERE tb.t_razdel = 1 " +
               "  ORDER BY t_ter, t_kod1, t_kod2 " ;
  rs = RsdRecordset(sqlString);

  stat = _StartRecursionInsertObjAttrNewOKATORec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  sqlString = "SELECT count(1) FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 12"; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  SofrRecsCount = rs.value(0);

  PrintTail(SourceRecsCount, SofrRecsCount, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

  RemProgress ();      

END;


MACRO InsertObjAttrOKVEDRec(rs, UpdateFlag, CreateReport:bool, StatisticsOnly:bool)
  var stat= false;


  if(UpdateFlag == 0) 
    stat = true;
    msgbox("Категория ОКВЭД имеет статус обновления 'Оставить'. Загрузка категории из внешнего справочника невозможна");

  elif(UpdateFlag == 1)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = trim(rs.value("T_KOD"));
    OBJATTR.ObjectType = 3 ; //Справочник ОКВЭД
    OBJATTR.GroupID    = 17; 

    if(not GetEQ(OBJATTR))
       OBJATTR.ObjectType = 3;
       OBJATTR.GroupID    = 17;
       OBJATTR.AttrID     = GetMaxNumObjAttr(3, 17) + 1;  
       OBJATTR.Name       = trim(rs.value("T_NAME_RUS1"));
       OBJATTR.FullName   = trim(rs.value("T_NAME_RUS1"));
       OBJATTR.NumInList  = trim(rs.value("T_KOD"));
       OBJATTR.NameObject = trim(rs.value("T_KOD"));
       OBJATTR.OpenDate   = rs.value("T_CB_DATE");                          
       OBJATTR.CloseDate  = rs.value("T_CE_DATE");                         
       PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
       Insert(OBJATTR);
    else
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("T_NAME_RUS1")), trim(rs.value("T_NAME_RUS1"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name       = trim(rs.value("T_NAME_RUS1"));
        OBJATTR.FullName   = trim(rs.value("T_NAME_RUS1"));
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    end;
  elif(UpdateFlag == 2)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = trim(rs.value("T_KOD"));
    OBJATTR.ObjectType = 3 ; //Справочник ОКВЭД
    OBJATTR.GroupID    = 17; 

    if(not GetEQ(OBJATTR))
       OBJATTR.ObjectType = 3;
       OBJATTR.GroupID    = 17;
       OBJATTR.AttrID     = GetMaxNumObjAttr(3, 17) + 1;  
       OBJATTR.Name       = trim(rs.value("T_NAME_RUS1"));
       OBJATTR.FullName   = trim(rs.value("T_NAME_RUS1"));
       OBJATTR.NumInList  = trim(rs.value("T_KOD"));
       OBJATTR.NameObject = trim(rs.value("T_KOD"));
       OBJATTR.OpenDate   = rs.value("T_CB_DATE");                          
       OBJATTR.CloseDate  = rs.value("T_CE_DATE");                         
       PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
       Insert(OBJATTR);
    end;
  elif(UpdateFlag == 3)
    ClearRecord(OBJATTR);
    OBJATTR.NumInList = trim(rs.value("T_KOD"));
    OBJATTR.ObjectType = 3 ; //Справочник ОКВЭД
    OBJATTR.GroupID    = 17; 

    if(GetEQ(OBJATTR))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("T_NAME_RUS1")), trim(rs.value("T_NAME_RUS1"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name       = trim(rs.value("T_NAME_RUS1"));
        OBJATTR.FullName   = trim(rs.value("T_NAME_RUS1"));
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    end;
  end;

  return stat;
END;

MACRO InsertObjAttrNewOKVEDRec(rs, UpdateFlag, CreateReport:bool, StatisticsOnly:bool)
  var stat= false;
  var rstatus = rs.value("t_status");
  var oldName, oldFullName;

  ClearRecord(OBJATTR);
  OBJATTR.NumInList = trim(rs.value("t_kod"));
  OBJATTR.ObjectType = 3 ; //Справочник ОКВЭД
  OBJATTR.GroupID    = 17;
  if(GetEQ(OBJATTR))
    if((rstatus == 0) or (rstatus == 2))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("fullname")), trim(rs.value("fullname"))))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = trim(rs.value("fullname"));
        OBJATTR.FullName = trim(rs.value("fullname"));
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    elif(rstatus == 3)
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, trim(rs.value("fullname")), trim(rs.value("fullname"))) or (OBJATTR.OpenDate != rs.value("t_datevved")))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = trim(rs.value("fullname"));
        OBJATTR.FullName = trim(rs.value("fullname"));
        OBJATTR.OpenDate = rs.value("t_datevved");                          
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        Update(OBJATTR);
      end;
    elif(rstatus == 1)
      OBJATTR.CloseDate = rs.value("t_datevved");                          
      PrintBody("Закрыто", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Update(OBJATTR);
    end;
  else
    if((rstatus == 0) or (rstatus == 2) or (rstatus == 3))
      OBJATTR.ObjectType = 3;
      OBJATTR.GroupID    = 17;
      OBJATTR.AttrID     = GetMaxNumObjAttr(3, 17) + 1;  
      OBJATTR.Name       = trim(rs.value("fullname"));
      OBJATTR.FullName   = trim(rs.value("fullname"));
      OBJATTR.NumInList  = trim(rs.value("t_kod"));
      OBJATTR.NameObject = trim(rs.value("t_kod"));
      PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Insert(OBJATTR);
    end;
  end;

  return stat;
END;

MACRO ImpOKVEDAll(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var pbar, Number :INTEGER, Count :INTEGER;
  var NumInFile, NumInBase;

  Number = 0; Count = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКВЭД
  OBJGROUP.GroupID    = 17; 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = 3;
     OBJGROUP.GroupID    = 17;
     OBJGROUP.Name       = "Код ОКВЭД";
     OBJGROUP.Type       = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 0;
     Insert(OBJGROUP);
  end;

  PrintHead("ОКВЭД", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  Number = 0; Count = 0;

  sqlString = " SELECT count(1) " +
              " FROM DOKVEDEXPDBF_DBT TB "
              " WHERE TB.T_CE_DATE >= SYSDATE "
              " AND TB.T_CE_DATE = (SELECT MAX(T_CE_DATE) FROM DOKVEDEXPDBF_DBT WHERE T_KOD = TB.T_KOD)";


  rs = RsdRecordset( sqlString );
  rs.moveNext();
  Number = rs.value(0);


  if(OBJGROUP.UpdateFlag == 1)
    sqlString = " SELECT count(1) FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 17 ";
    rs = RsdRecordset( sqlString );
    rs.moveNext();
    Number = Number + rs.value(0);
    pbar=InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКВЭД");
  
     delSqlString = "SELECT * FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 17";
     delrs = RsdRecordset( delSqlString );
    while( delrs.moveNext() ) 
      UseProgress (Count = Count + 1);
      if(NOT IsExistRecOKVEDDBF(delrs.value("T_NUMINLIST")))
        PrintBody("Удалено", delrs.value("t_NameObject"), delrs.value("t_FullName"), delrs.value("t_OpenDate"), delrs.value("t_CloseDate"), CreateReport, StatisticsOnly);
        DeleteRecObjAttr(delrs.value("T_ATTRID"), 3, 17);
      end;
    end;

  else 
    pbar=InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКВЭД");
  end;

  sqlString = " SELECT TB.* " +
              " FROM DOKVEDEXPDBF_DBT TB "
              " WHERE TB.T_CE_DATE >= SYSDATE "
              " AND TB.T_CE_DATE = (SELECT MAX(T_CE_DATE) FROM DOKVEDEXPDBF_DBT WHERE T_KOD = TB.T_KOD)";

  rs = RsdRecordset( sqlString );

  while( (rs.moveNext()) AND (stat == false))
     UseProgress (Count = Count + 1);
     stat = InsertObjAttrOKVEDRec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);
  end;

  sqlString = " SELECT count(1) " +
               " FROM DOKVEDEXPDBF_DBT TB " ; 

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInFile = rs.value(0);


  sqlString = "SELECT count(1) FROM DOBJATTR_DBT WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 17"; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInBase = rs.value(0);


  PrintTail(NumInFile, NumInBase, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

  RemProgress ();      
 
END;

MACRO ImpNewOKVEDAll(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var pbar, Number :INTEGER, Count :INTEGER;
  var NumInFile, NumInBase;
  var Kod = "";
  var SaveKod = "";

  Number = 0; Count = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКВЭД
  OBJGROUP.GroupID    = 17; 
  if(not GetEQ(OBJGROUP))
    OBJGROUP.ObjectType = 3;
    OBJGROUP.GroupID    = 17;
    OBJGROUP.Name       = "Код ОКВЭД";
    OBJGROUP.Type       = "X";
    OBJGROUP.System     = "X";
    OBJGROUP.UpdateFlag = 0;
    Insert(OBJGROUP);
  end;

  PrintHead("ОКВЭД", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  Number = 0; Count = 0;

  sqlString = " SELECT COUNT(1) " +
              "   FROM dokvedexpdbf_dbt " +
              "  WHERE TRIM(t_kod) IS NOT NULL " +
              "  ORDER BY t_kod ";

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  Number = rs.value(0);

  pbar=InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКВЭД");

  sqlString = " SELECT CONCAT(TRIM(t_name11), CONCAT(' ', TRIM(t_name12))) AS fullname, ok.* " +
              "   FROM dokvedexpdbf_dbt ok " +
              "  WHERE TRIM(t_kod) IS NOT NULL " +
              "  ORDER BY t_kod ";

  rs = RsdRecordset( sqlString );

  while( (rs.moveNext()) AND (stat == false))
     UseProgress (Count = Count + 1);
     
     Kod = trim(rs.value("t_kod"));
     if(SaveKod != Kod)  
       stat = InsertObjAttrNewOKVEDRec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);
     end;
     SaveKod = Kod;  
  end;

  sqlString = " SELECT COUNT(1) " +
              "   FROM dokvedexpdbf_dbt tb "; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInFile = rs.value(0);

  sqlString = "SELECT COUNT(1) FROM dobjattr_dbt WHERE t_objecttype = 3 AND t_groupid = 17"; 
  rs = RsdRecordset( sqlString );
  rs.moveNext();
  NumInBase = rs.value(0);

  PrintTail(NumInFile, NumInBase, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

  RemProgress ();      
 
END;

PRIVATE MACRO dropTableOKATO( ):bool
  return execSQL( string( "DROP TABLE DOKATOEXPDBF_DBT;" ) );
ONERROR(x)
  return TRUE;
END;

PRIVATE MACRO NewVersionOkato( ):bool
  var sqlString, rs, stat = false;   

  sqlString = " SELECT T_TER FROM DOKATOEXPDBF_DBT TB " ; 

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  return TRUE;
ONERROR(x)
  return FALSE;
END;


PRIVATE MACRO NewVersionOkved( ):bool
  var sqlString, rs, stat = false;   

  sqlString = " SELECT T_ID FROM DOKVEDEXPDBF_DBT " ; 

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  return FALSE;
ONERROR(x)
  return TRUE;
END;

private macro _DropTableOKTMO( ) : bool
  return execSQL("DROP TABLE doktmoexpdoc_dbt");
OnError(x)
  return true;
end;

private macro _CreateTableOKTMO( ) : bool
  return execSQL("CREATE TABLE doktmoexpdoc_dbt (t_code VARCHAR2(20), t_name VARCHAR2(500))");
OnError(x)
  return true;
end;

private macro _CreateIndexOKTMO( ) : bool
  //return execSQL("CREATE TABLE doktmoexpdoc_dbt (t_code VARCHAR2(20), t_name VARCHAR2(500))");
  return SQL_Execute("CREATE INDEX doktmoexpdoc_dbt_idx0 ON doktmoexpdoc_dbt (t_code)", "Создание индексов...");
OnError(x)
  return true;
end;

private macro InitOKTMOArrays()
  CodeOKTMOArray = TArray;
  NameOKTMOArray = TArray;
end;

/* Добавить запись во временную таблицу */
private macro _AddToOKTMO(CodeOKTMO : string, NameOKTMO : string)
  CodeOKTMOArray(CodeOKTMOArray.Size) = CodeOKTMO;
  NameOKTMOArray(NameOKTMOArray.Size) = NameOKTMO;
end;

/* Добавить запись во временную таблицу */
private macro _InsertIntoOKTMO(Flush : bool) : bool
  var ins : RsbSQLInsert;
  var stat = true;
  if( Flush == null ) 
    Flush = false; 
  end;
  if((CodeOKTMOArray.Size == 5000) or ((Flush) and (CodeOKTMOArray.Size > 0)))
    ins = RsbSQLInsert("INSERT INTO doktmoexpdoc_dbt (t_code, t_name) VALUES (?, ?)", 2, CodeOKTMOArray.Size);
    ins.AddParam(V_STRING, CodeOKTMOArray, 20 );
    ins.AddParam(V_STRING, NameOKTMOArray, 500);
    stat = ins.Insert();
    CodeOKTMOArray.Size = 0;
    NameOKTMOArray.Size = 0;
  end;
  return stat;
end;

/* Открыть документ. Вернет открытый документ */
private macro _OpenWordDoc(DocFileName)
  var startAX;  
  var WordDoc = null;

  if(isStandAlone())
    WordApp = ActiveX("Word.Application", null, false);
  else
    if(startAX == null)
      startAX = CreateObject("rsax", "TRsAxServer", "FmAxServer", isStandAlone());
    end;
    WordApp = startAX.CreateComObject("Word.Application");
  end;
  
  if(WordApp != null) 
    /* Проверить, можно ли найти документ по указанному пути */
    if( not ExistFile(ToANSI("$" + DocFileName)))
       msgbox("Не найден файл-документ ", DocFileName, ".|Проверьте правильность пути");
    else
      WordDoc = WordApp.Documents.Open(DocFileName, false, true);
    
      if(WordDoc == null)
        msgbox("Ошибка открытия файла-документа ", DocFileName);
        WordApp.Quit;
      end;
    end;
  else
    msgbox("Не удалось создать объект Word.Application");
  end;

  return WordDoc;
end;

/*Убрать спец. символы из строки*/
private macro _Normalize(oldstr : string)
  var newstr, i, retstr; 
  newstr = oldstr;
  retstr = "";
  
  i = 0;
  while( i < StrLen(oldstr))
    i = i + 1;
    if(CodeFor(SubStr(oldstr, i, 1)) > 27)
      retstr = retstr + SubStr(oldstr, i, 1);
    end;
  end;
  return retstr;
end;

/*
Читает из документа таблицы с шапкой
"Код    | КЧ     | Наименование"
и переносит данные в БД
*/
private macro _ConvertSingleDocFile2TableOKTMO(PathFileOKTMO, ImportPlacesOKTMO)
  var i, j, k;
  var TablesCount, RowsCount, AllRowCount, CurrRowCount;
  var WordDoc = _OpenWordDoc(PathFileOKTMO);
  var Text;
  var IsColumn1Passed, IsColumn2Passed, IsColumn3Passed;
  var IsOKTMOTable;
  var CodeOKTMO, NameOKTMO;
  var file_name, file_ext;

  if(WordDoc != null)

    SplitFile(PathFileOKTMO, file_name, file_ext);
    file_name = MergeFile("", file_name, + file_ext);
    
    CodeOKTMOArray = TArray;
    NameOKTMOArray = TArray;

    TablesCount = WordDoc.Tables.Count;
    i = 1;
    AllRowCount = 0;
    CurrRowCount = 0;
    while(i <= TablesCount)
      AllRowCount = AllRowCount + WordDoc.Tables(i).Rows.Count;
      i = i + 1;
    end;

    InitProgress(AllRowCount, " ~Alt-Break~ Прервать", "Обработка файла импорта \"" + file_name + "\"");

    i = 1;
    while(i <= TablesCount)
      IsOKTMOTable = false;
      IsColumn1Passed = false;
      IsColumn2Passed = false;
      IsColumn3Passed = false;
      if((WordDoc.Tables(i).Rows.Count >= 1) and (WordDoc.Tables(i).Columns.Count >= 3))
        j = 1;
        
        RowsCount = WordDoc.Tables(i).Rows.Count;
        
        while(j <= RowsCount)
          k = 1;
          while(k <= 3)
            Text = WordDoc.Tables(i).Cell(j, k).Range.Text;
            Text = _Normalize(Text);
            //проверить заголовок таблицы
            if(j == 1)
              if((k == 1) and (trim(strupr(Text)) == strupr("Код")))
                IsColumn1Passed = true;
              end;
              if((k == 2) and (trim(strupr(Text)) == strupr("КЧ")))
                IsColumn2Passed = true;
              end;
              if((k == 3) and (trim(strupr(Text)) == strupr("Наименование")))
                IsColumn3Passed = true;
              end;
              if(IsColumn1Passed and IsColumn2Passed and IsColumn3Passed)
                IsOKTMOTable = true;
              end;
              k = k + 1;
            else
              if(IsOKTMOTable)
                if(k == 1)
                  CodeOKTMO = trim(Text);
                end;
                if(k == 3)
                  NameOKTMO = trim(Text);
                end;
              else
                break;
              end;
              k = k + 2;
            end;
          end;

          if((j > 1) and (CodeOKTMO != "") and ((strlen(CodeOKTMO) == 10) or (ImportPlacesOKTMO and (strlen(CodeOKTMO) == 14))))
            _AddToOKTMO(CodeOKTMO, NameOKTMO);
          end;

          _InsertIntoOKTMO(false);

          j = j + 1;
          CurrRowCount = CurrRowCount + 1;

          UseProgress(CurrRowCount);
        end;

      end;
    
      _InsertIntoOKTMO(true);
      
      i = i + 1;
    end;
  
    RemProgress();

    WordDoc.Close;

    WordApp.Quit;
  end;
end;

private macro _ConvertAllDocFiles2TableOKTMO(PathOKTMO, ImportPlacesOKTMO)
  var i;
  var DirList = TDirList;
  var file_path, file_name, file_ext;

  file_path = SplitFile(PathOKTMO, file_name, file_ext);
  
  if(isStandAlone())
    DirList.List(PathOKTMO, "F");
  else
    DirList.List("$" + PathOKTMO, "F");
  end;

  i = 0;
  
  while(i < DirList.Count)

    _ConvertSingleDocFile2TableOKTMO(file_path + DirList.Name(i), ImportPlacesOKTMO);

    i = i + 1;
  end;  
end;

private macro _GetAttrCodeNameOKTMO(rs, Code : @string, Name : @string)
  Code = trim(rs.value("t_code"));

  Name = trim(rs.value("t_name"));
end;

private macro _CountEndNullOKTMO(Code : string)
  var Count = 0;

  if(strlen(Code) > 0)
    if(substr(Code, strlen(Code)) == "0")
      Count = Count + 1;
      if(strlen(Code) > 1)
        Count = Count + _CountEndNullOKTMO(substr(Code, 1, strlen(Code) - 1));
      end;
    elif(substr(Code, strlen(Code)) == " ")
      if(strlen(Code) > 1)
        Count = Count + _CountEndNullOKTMO(substr(Code, 1, strlen(Code) - 1));
      end;
    end;
  end;

  return Count;
end;

private macro _CheckNextOKTMORecIsChild
(
  rs
 ,PrevCode
 ,PrevName
)
  var IsChild = false;
  var IsPrevGroupRoot = false;
  var IsNextGroupRoot = false;
  var IsPrevSubGroupRoot = false;
  var IsNextSubGroupRoot = false;
  var PrevCountEndNull = 0;
  var NextCountEndNull = 0;
  var NextCode : string;
  var NextName : string;
  _GetAttrCodeNameOKTMO(rs, @NextCode, @NextName);

  if(substr(PrevCode, 1, 2) == substr(NextCode, 1, 2))
    if(strlen(PrevCode) < strlen(NextCode))
      IsChild = true;
    elif(strlen(PrevCode) == strlen(@NextCode))
      if(substr(PrevName, strlen(PrevName)) == "/")
        IsPrevGroupRoot = true;
      end;
      if(substr(NextName, strlen(NextName)) == "/")
        IsNextGroupRoot = true;
      end;
      if(substr(PrevName, 1, 1) == "-")
        IsPrevSubGroupRoot = true;
      end;
      if(substr(NextName, 1, 1) == "-")
        IsNextSubGroupRoot = true;
      end;
    
      PrevCountEndNull = _CountEndNullOKTMO(PrevCode);
      NextCountEndNull = _CountEndNullOKTMO(NextCode);
    
      if(not (IsPrevSubGroupRoot and IsNextSubGroupRoot))
        if(IsPrevGroupRoot and ((PrevCountEndNull > NextCountEndNull) or ((PrevCountEndNull == NextCountEndNull) and (not IsNextGroupRoot))))
          IsChild = true;
        elif((not IsPrevGroupRoot) and (PrevCountEndNull >= NextCountEndNull) and IsNextGroupRoot)
          IsChild = true;
        elif((NextCountEndNull > 1) and (PrevCountEndNull > NextCountEndNull) and (not IsNextGroupRoot))
          IsChild = true;
        end;
      end;
    end;
  end;

  return IsChild;
end;

private macro _CutSpecSymbol(Name : string)
  var retVal = Name;
  if(index(retVal, "- ") == 1)
    retVal = substr(retVal, 3);
  end;
  if(index(retVal, "/") == strlen(retVal))
    retVal = substr(retVal, 1, strlen(retVal) - 1);
  end;
  return retVal;
end;

private macro _InsertObjAttrOKTMORec
(
  rs
 ,UpdateFlag
 ,CreateReport:bool
 ,StatisticsOnly:bool
 ,ParentID : integer
 ,newAttrID : @integer
 ,newCode : @string
 ,newName : @string
)
  var stat = true;
  var ZeroDate = date(0, 0, 0);
  
  _GetAttrCodeNameOKTMO(rs, @newCode, @newName);

  ClearRecord(OBJATTR);
  OBJATTR.NumInList  = newCode;
  OBJATTR.ObjectType = 3 ; //Справочник ОКТМО
  OBJATTR.GroupID    = 55; 
  
  if(not GetEQ(OBJATTR))
    if((UpdateFlag == 1) or (UpdateFlag == 2))
      newAttrID = GetMaxNumObjAttr(3, 55) + 1;
      OBJATTR.ObjectType = 3;
      OBJATTR.GroupID    = 55;
      OBJATTR.AttrID     = newAttrID;  
      OBJATTR.Name       = _CutSpecSymbol(newName);
      OBJATTR.FullName   = _CutSpecSymbol(newName);
      OBJATTR.NumInList  = newCode;
      OBJATTR.NameObject = newCode;
      OBJATTR.OpenDate   = {curdate};                          
      OBJATTR.CloseDate  = ZeroDate;                         
      OBJATTR.ParentID   = ParentID;
      PrintBodyOKTMO("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Insert(OBJATTR);
    end;
  else
    newAttrID = OBJATTR.AttrID;
    if((UpdateFlag == 1) or (UpdateFlag == 3))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, _CutSpecSymbol(newName), _CutSpecSymbol(newName)))
        PrintBodyOKTMO("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = _CutSpecSymbol(newName);
        OBJATTR.FullName = _CutSpecSymbol(newName);
        OBJATTR.OpenDate = {curdate};                          
        PrintBodyOKTMO("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      end;
      OBJATTR.ParentID = ParentID;
      Update(OBJATTR);
    end;
  end;

  UseProgress(CountOKTMO = CountOKTMO + 1);
  
  return stat;
end;

private macro _RecursionInsertObjAttrOKTMORec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
 ,ParentID : integer
 ,PrevCode : string
 ,PrevName : string
 ,MoveNext : @bool
 ,Eof : @bool
)
  var stat = true;
  var newAttrID;
  var newCode;
  var newName;

  while(stat and (not Eof))

    if(MoveNext)
      stat = rs.moveNext();
      if(not stat)
        Eof = true;
      end;
      MoveNext = false;
    end;

    if(stat)
      if((ParentID == 0) or _CheckNextOKTMORecIsChild(rs, PrevCode, PrevName))
        stat = _InsertObjAttrOKTMORec
        (
          rs
         ,UpdateFlag
         ,CreateReport
         ,StatisticsOnly
         ,ParentID
         ,@newAttrID
         ,@newCode
         ,@newName
        );
        if(stat)
          MoveNext = true;
        end;
      else
        stat = false;
      end;
    end;

    if(stat)
      stat = _RecursionInsertObjAttrOKTMORec
      (
        rs
       ,UpdateFlag
       ,CreateReport
       ,StatisticsOnly
       ,newAttrID
       ,newCode
       ,newName
       ,@MoveNext
       ,@Eof
      );
      if(not MoveNext)
        stat = true;
      end;
    end;
  end;

  return stat;
end;

private macro _StartRecursionInsertObjAttrOKTMORec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
)
  var ParentID = 0;
  var PrevCode = "";
  var PrevName = "";
  var MoveNext = true;
  var Eof = false;

  var stat = _RecursionInsertObjAttrOKTMORec
  (
    rs
   ,UpdateFlag
   ,CreateReport
   ,StatisticsOnly
   ,ParentID
   ,PrevCode
   ,PrevName
   ,@MoveNext
   ,@Eof
  );

  return stat;
end;

private macro _IsExistRecOKTMO(NumInList)
  var sqlString, rs, retval = 0; 
  
  sqlString = " SELECT COUNT(1) FROM doktmoexpdoc_dbt WHERE t_code = '" + NumInList + "'";

  rs = RsdRecordset( sqlString );
  rs.moveNext();
  retval = rs.value(0);

  return retval;
end;


private macro _ImpOKTMOAll(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string, ImportPlacesOKTMO:bool)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var Number : integer;

  var NumInFileDistrs, NumInFilePlaces, NumInBaseDistrs, NumInBasePlaces;

  Number = 0; CountOKTMO = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКТМО
  OBJGROUP.GroupID    = 55; 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = 3;
     OBJGROUP.GroupID    = 55;
     OBJGROUP.Name       = "Код ОКТМО";
     OBJGROUP.Type       = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 0;
     Insert(OBJGROUP);
  end;

  PrintHeadOKTMO("ОКТМО", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly, ImportPlacesOKTMO);

  sqlString = " SELECT COUNT(1) " +
              "   FROM doktmoexpdoc_dbt";

  rs = RsdRecordset(sqlString);
  rs.moveNext();
  Number = rs.value(0);

  if(OBJGROUP.UpdateFlag == 0) 
    stat = true;
    msgbox("Категория ОКТМО имеет статус обновления 'Оставить'. Загрузка категории из внешнего справочника невозможна")
  else
    if(OBJGROUP.UpdateFlag == 1)
      sqlString = " SELECT COUNT(1) " +
                  "   FROM dobjattr_dbt " +
                  "  WHERE t_ObjectType = 3 " +
                  "    AND t_GroupID = 55 " +
                  "    AND NOT EXISTS(SELECT 1 FROM doktmoexpdoc_dbt WHERE t_code = t_NumInList)";
      rs = RsdRecordset( sqlString );
      rs.moveNext();
      Number = Number + rs.value(0);
      
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКТМО");
   
      delSqlString = " SELECT * " +
                     "   FROM dobjattr_dbt " +
                     "  WHERE t_ObjectType = 3 " +
                     "    AND t_GroupID = 55 " +
                     "    AND NOT EXISTS(SELECT 1 FROM doktmoexpdoc_dbt WHERE t_code = t_NumInList)";
      delrs = RsdRecordset( delSqlString );
      while( delrs.moveNext() ) 
        PrintBodyOKTMO("Удалено", delrs.value("t_NameObject"), delrs.value("t_FullName"), delrs.value("t_OpenDate"), delrs.value("t_CloseDate"), CreateReport, StatisticsOnly);
        DeleteRecObjAttr(delrs.value("t_AttrID"), 3, 55);

        UseProgress (CountOKTMO = CountOKTMO + 1);
      end;
    else
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКТМО");
    end;
    
    if(RemoveSpacesFromOktmo)
      sqlString =  " SELECT * " +
                   "   FROM doktmoexpdoc_dbt "+
                   "  ORDER BY CASE WHEN LENGTH(t_code) = 8 THEN t_code || '000' ELSE t_code END";
    else
      sqlString =  " SELECT * " +
                   "   FROM doktmoexpdoc_dbt "+
                   "  ORDER BY CASE WHEN LENGTH(t_code) = 10 THEN t_code || ' 000' ELSE t_code END";
    end;
    rs = RsdRecordset(sqlString);


    stat = _StartRecursionInsertObjAttrOKTMORec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);
    if(RemoveSpacesFromOktmo)
      sqlString = " SELECT COUNT(1) " +
                  "   FROM doktmoexpdoc_dbt " +
                  "  WHERE LENGTH(t_code) = 8 ";
    else
      sqlString = " SELECT COUNT(1) " +
                  "   FROM doktmoexpdoc_dbt " +
                  "  WHERE LENGTH(t_code) = 10 ";
    end;
    rs = RsdRecordset(sqlString);
    rs.moveNext();
    NumInFileDistrs = rs.value(0);

    if(RemoveSpacesFromOktmo)
      sqlString = " SELECT COUNT(1) " +
                  "   FROM doktmoexpdoc_dbt " +
                  "  WHERE LENGTH(t_code) = 11 ";
    else
      sqlString = " SELECT COUNT(1) " +
                  "   FROM doktmoexpdoc_dbt " +
                  "  WHERE LENGTH(t_code) = 14 ";
    end;
    rs = RsdRecordset(sqlString);
    rs.moveNext();
    NumInFilePlaces = rs.value(0);

    if(RemoveSpacesFromOktmo)
      sqlString = "SELECT COUNT(1) " +
                  "  FROM dobjattr_dbt " +
                  "  WHERE t_objecttype = 3 AND t_groupid = 55 AND LENGTH(t_numinlist) = 8 "; 
    else
      sqlString = "SELECT COUNT(1) " +
                  "  FROM dobjattr_dbt " +
                  "  WHERE t_objecttype = 3 AND t_groupid = 55 AND LENGTH(t_numinlist) = 10 "; 
    end;
    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInBaseDistrs = rs.value(0);

    if(RemoveSpacesFromOktmo)
      sqlString = "SELECT COUNT(1) " +
                  "  FROM dobjattr_dbt " +
                  "  WHERE t_objecttype = 3 AND t_groupid = 55 AND LENGTH(t_numinlist) = 11 "; 
    else
      sqlString = "SELECT COUNT(1) " +
                  "  FROM dobjattr_dbt " +
                  "  WHERE t_objecttype = 3 AND t_groupid = 55 AND LENGTH(t_numinlist) = 14 "; 
    end;
    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInBasePlaces = rs.value(0);

    PrintTailOKTMO(NumInFileDistrs, NumInFilePlaces, NumInBaseDistrs, NumInBasePlaces, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

    RemProgress();
  end;
end;

private macro _DropTableOKVED2( ) : bool
  return execSQL("DROP TABLE dokved2expdoc_dbt");
OnError(x)
  return true;
end;

private macro _CreateTableOKVED2( ) : bool
  return execSQL("CREATE TABLE dokved2expdoc_dbt (t_recid NUMBER(10), t_code VARCHAR2(20), t_name VARCHAR2(500))");
OnError(x)
  return true;
end;

private macro _CreateIndexOKVED2( ) : bool
  return SQL_Execute("CREATE INDEX dokved2expdoc_dbt_idx0 ON dokved2expdoc_dbt (t_code)", "Создание индексов...");
OnError(x)
  return true;
end;

/* Добавить запись во временную таблицу */
private macro _AddToOKVED2(CodeOKVED2 : string, NameOKVED2 : string)
  RecIDOKVED2Array(RecIDOKVED2Array.Size) = RecIDOKVED2;
  RecIDOKVED2 = RecIDOKVED2 + 1;
  CodeOKVED2Array(CodeOKVED2Array.Size) = CodeOKVED2;
  NameOKVED2Array(NameOKVED2Array.Size) = NameOKVED2;
end;

/* Добавить запись во временную таблицу */
private macro _InsertIntoOKVED2(Flush : bool) : bool
  var ins : RsbSQLInsert;
  var stat = true;
  if( Flush == null ) 
    Flush = false; 
  end;
  if((CodeOKVED2Array.Size == 5000) or ((Flush) and (CodeOKVED2Array.Size > 0)))
    ins = RsbSQLInsert("INSERT INTO dokved2expdoc_dbt (t_recid, t_code, t_name) VALUES (?, ?, ?)", 3, CodeOKVED2Array.Size);
    ins.AddParam(V_INTEGER, RecIDOKVED2Array    );
    ins.AddParam(V_STRING , CodeOKVED2Array, 20 );
    ins.AddParam(V_STRING , NameOKVED2Array, 500);
    stat = ins.Insert();
    RecIDOKVED2Array.Size = 0;
    CodeOKVED2Array.Size  = 0;
    NameOKVED2Array.Size  = 0;
  end;
  return stat;
end;

private macro _GetTableCellText(WordDoc, i, j, k)
  var Text = WordDoc.Tables(i).Cell(j, k).Range.Text;
  return Text;
OnError(err)
  return "";
end;

/*Убрать спец. символы из строки*/
private macro _NormalizeOKVED2(oldstr : string)
  var newstr, i, retstr; 
  newstr = oldstr;
  retstr = "";
  i = 0;
  while(i < StrLen(oldstr))
    i = i + 1;
    if(CodeFor(SubStr(oldstr, i, 1)) > 27)
      retstr = retstr + SubStr(oldstr, i, 1);
    elif(StrLen(retstr) > 0)
      i = StrLen(oldstr);
    end;
  end;
  return retstr;
end;

private macro _ConvertSingleDocFile2TableOKVED2(PathFileOKVED2)
  var i, j, k;
  var TablesCount, RowsCount, AllRowCount, CurrRowCount;
  var WordDoc = _OpenWordDoc(PathFileOKVED2);
  var Text;
  var IsColumn1Passed, IsColumn2Passed, IsColumn3Passed;
  var IsOKVED2Table;
  var CodeOKVED2, NameOKVED2;
  var file_name, file_ext;

  if(WordDoc != null)

    SplitFile(PathFileOKVED2, file_name, file_ext);
    file_name = MergeFile("", file_name, + file_ext);
    
    RecIDOKVED2Array = TArray;
    CodeOKVED2Array = TArray;
    NameOKVED2Array = TArray;

    TablesCount = WordDoc.Tables.Count;
    i = TablesCount;
    AllRowCount = 0;
    CurrRowCount = 0;
    while(i <= TablesCount)
      AllRowCount = AllRowCount + WordDoc.Tables(i).Rows.Count;
      i = i + 1;
    end;

    InitProgress(AllRowCount, " ~Alt-Break~ Прервать", "Обработка файла импорта \"" + file_name + "\"");

    i = TablesCount;
    while(i <= TablesCount)
      IsOKVED2Table = true;
      if((WordDoc.Tables(i).Rows.Count >= 1) and (WordDoc.Tables(i).Columns.Count >= 2))
        j = 1;
        
        RowsCount = WordDoc.Tables(i).Rows.Count;
        
        while(j <= RowsCount)
          k = 1;
          while(k <= 2)
            Text = _GetTableCellText(WordDoc, i, j, k);
            Text = _NormalizeOKVED2(Text);
            if(IsOKVED2Table)
              if(k == 1)
                CodeOKVED2 = trim(Text);
              end;
              if(k == 2)
                NameOKVED2 = trim(Text);
              end;
            else
              break;
            end;
            k = k + 1;
          end;

          if(CodeOKVED2 != "")
            _AddToOKVED2(CodeOKVED2, NameOKVED2);
          end;

          _InsertIntoOKVED2(false);

          j = j + 1;
          CurrRowCount = CurrRowCount + 1;

          UseProgress(CurrRowCount);
        end;

      end;
    
      _InsertIntoOKVED2(true);
      
      i = i + 1;
    end;
  
    RemProgress();

    WordDoc.Close;

    WordApp.Quit;
  end;
end;

private macro _GetAttrCodeNameOKVED2(rs, Code : @string, Name : @string)
  Code = trim(rs.value("t_code"));

  Name = trim(rs.value("t_name"));
end;

private macro _CheckNextOKVED2RecIsChild
(
  rs
 ,PrevCode
 ,PrevName
)
  var IsChild = false;
  var PrevCodeLen = 0;
  var IsPrevGroupRoot = false;
  var IsNextGroupRoot = false;
  var IsPrevSubGroupRoot = false;
  var IsNextSubGroupRoot = false;
  var PrevCountEndNull = 0;
  var NextCountEndNull = 0;

  var NextCode : string;
  var NextName : string;
  _GetAttrCodeNameOKVED2(rs, @NextCode, @NextName);

  if((index(strupr(PrevCode), "РАЗДЕЛ") > 0) and (index(strupr(NextCode), "РАЗДЕЛ") == 0))
    IsChild = true;
  else
    PrevCodeLen = strlen(trim(PrevCode));
    if(trim(PrevCode) == substr(trim(NextCode), 1, PrevCodeLen))
      IsChild = true;
    end;
  end;

  return IsChild;
end;

private macro _InsertObjAttrOKVED2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport:bool
 ,StatisticsOnly:bool
 ,ParentID : integer
 ,newAttrID : @integer
 ,newCode : @string
 ,newName : @string
)
  var stat = true;
  var ZeroDate = date(0, 0, 0);
  
  _GetAttrCodeNameOKVED2(rs, @newCode, @newName);

  ClearRecord(OBJATTR);
  if(index(strupr(newCode), "РАЗДЕЛ") > 0)
    OBJATTR.NumInList = trim(substr(newCode, index(strupr(newCode), "РАЗДЕЛ") + 7));
  else
    OBJATTR.NumInList = trim(newCode);
  end;
  OBJATTR.ObjectType = IIF(IsOKVED2ForParty, 3, 207) ;
  OBJATTR.GroupID    = IIF(IsOKVED2ForParty, 64, 199) ; //Справочник ОКВЭД2 
  
  if(not GetEQ(OBJATTR))
    if((UpdateFlag == 1) or (UpdateFlag == 2))
      newAttrID = GetMaxNumObjAttr(IIF(IsOKVED2ForParty, 3, 207), IIF(IsOKVED2ForParty, 64, 199)) + 1;
      OBJATTR.ObjectType = IIF(IsOKVED2ForParty, 3, 207);
      OBJATTR.GroupID    = IIF(IsOKVED2ForParty, 64, 199);
      OBJATTR.AttrID     = newAttrID;  
      OBJATTR.Name       = newName;
      OBJATTR.FullName   = newName;
      if(index(strupr(newCode), "РАЗДЕЛ") > 0)
        OBJATTR.NumInList = trim(substr(newCode, index(strupr(newCode), "РАЗДЕЛ") + 7));
      else
        OBJATTR.NumInList = trim(newCode);
      end;
      OBJATTR.NameObject = OBJATTR.NumInList;
      OBJATTR.OpenDate   = {curdate};                          
      OBJATTR.CloseDate  = ZeroDate;                         
      OBJATTR.ParentID   = ParentID;
      PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Insert(OBJATTR);
    end;
  else
    newAttrID = OBJATTR.AttrID;
    if((UpdateFlag == 1) or (UpdateFlag == 3))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, newName, newName))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = newName;
        OBJATTR.FullName = newName;
        OBJATTR.OpenDate = {curdate};                          
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      end;
      OBJATTR.ParentID = ParentID;
      Update(OBJATTR);
    end;
  end;

  UseProgress(CountOKVED2 = CountOKVED2 + 1);
  
  return stat;
end;

private macro _RecursionInsertObjAttrOKVED2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
 ,ParentID : integer
 ,PrevCode : string
 ,PrevName : string
 ,MoveNext : @bool
 ,Eof : @bool
)
  var stat = true;
  var newAttrID;
  var newCode;
  var newName;

  while(stat and (not Eof))

    if(MoveNext)
      stat = rs.moveNext();
      if(not stat)
        Eof = true;
      end;
      MoveNext = false;
    end;

    if(stat)
      if((ParentID == 0) or _CheckNextOKVED2RecIsChild(rs, PrevCode, PrevName))
        stat = _InsertObjAttrOKVED2Rec
        (
          rs
         ,UpdateFlag
         ,CreateReport
         ,StatisticsOnly
         ,ParentID
         ,@newAttrID
         ,@newCode
         ,@newName
        );
        if(stat)
          MoveNext = true;
        end;
      else
        stat = false;
      end;
    end;

    if(stat)
      stat = _RecursionInsertObjAttrOKVED2Rec
      (
        rs
       ,UpdateFlag
       ,CreateReport
       ,StatisticsOnly
       ,newAttrID
       ,newCode
       ,newName
       ,@MoveNext
       ,@Eof
      );
      if(not MoveNext)
        stat = true;
      end;
    end;
  end;

  return stat;
end;

private macro _StartRecursionInsertObjAttrOKVED2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
)
  var ParentID = 0;
  var PrevCode = "";
  var PrevName = "";
  var MoveNext = true;
  var Eof = false;

  var stat = _RecursionInsertObjAttrOKVED2Rec
  (
    rs
   ,UpdateFlag
   ,CreateReport
   ,StatisticsOnly
   ,ParentID
   ,PrevCode
   ,PrevName
   ,@MoveNext
   ,@Eof
  );

  return stat;
end;

private macro _ImpOKVED2All(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var pbar, Number :INTEGER, Count :INTEGER;
  var NumInFile, NumInBase;

  Number = 0; CountOKVED2 = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = IIF(IsOKVED2ForParty, 3, 207); //Справочник ОКВЭД2
  OBJGROUP.GroupID    = IIF(IsOKVED2ForParty, 64, 199); 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = IIF(IsOKVED2ForParty, 3, 207);
     OBJGROUP.GroupID    = IIF(IsOKVED2ForParty, 64, 199);
     OBJGROUP.Order      = IIF(IsOKVED2ForParty, 64, 199);
     OBJGROUP.Name       = IIF(IsOKVED2ForParty, "Код ОКВЭД2", "ОКВЭД для 724ф");
     OBJGROUP.Type       = "";
     OBJGROUP.KeepOldValues = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 1;
     Insert(OBJGROUP);
  end;

  PrintHead(IIF(IsOKVED2ForParty, "Код ОКВЭД2", "ОКВЭД для 724ф"), ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  sqlString = " SELECT COUNT(1) " +
              "   FROM dokved2expdoc_dbt ";

  rs = RsdRecordset(sqlString);
  rs.moveNext();
  Number = rs.value(0);

  if(OBJGROUP.UpdateFlag == 0) 
    stat = true;
    msgbox("Категория "+ IIF(IsOKVED2ForParty, "Код ОКВЭД2", "ОКВЭД для 724ф") + " имеет статус обновления 'Оставить'. Загрузка категории из внешнего справочника невозможна")
  else
    if(OBJGROUP.UpdateFlag == 1)
      sqlString = " SELECT COUNT(1) " +
                  "   FROM dobjattr_dbt " +
                  "  WHERE t_ObjectType = " + IIF(IsOKVED2ForParty, 3, 207) +
                  "    AND t_GroupID = " + IIF(IsOKVED2ForParty, 64, 199) +
                  "    AND NOT EXISTS " +
                  "        (SELECT 1 " +
                  "           FROM dokved2expdoc_dbt " +
                  "          WHERE CASE " +
                  "                WHEN INSTR(UPPER(t_code), 'РАЗДЕЛ') > 0 " +
                  "                THEN TRIM(SUBSTR(t_code, INSTR(UPPER(t_code), 'РАЗДЕЛ') + 7)) " +
                  "                ELSE t_code " +
                  "                END = t_NumInList)";
      rs = RsdRecordset( sqlString );
      rs.moveNext();
      Number = Number + rs.value(0);
      
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника " + IIF(IsOKVED2ForParty, "Код ОКВЭД2", "ОКВЭД для 724ф"));
   
      delSqlString = " SELECT * " +
                     "   FROM dobjattr_dbt " +
                     "  WHERE t_ObjectType = " + IIF(IsOKVED2ForParty, 3, 207) +
                     "    AND t_GroupID = " + IIF(IsOKVED2ForParty, 64, 199) +
                     "    AND NOT EXISTS " +
                     "        (SELECT 1 " +
                     "           FROM dokved2expdoc_dbt " +
                     "          WHERE CASE " +
                     "                WHEN INSTR(UPPER(t_code), 'РАЗДЕЛ') > 0 " +
                     "                THEN TRIM(SUBSTR(t_code, INSTR(UPPER(t_code), 'РАЗДЕЛ') + 7)) " +
                     "                ELSE t_code " +
                     "                END = t_NumInList)";
      delrs = RsdRecordset( delSqlString );
      while( delrs.moveNext() ) 
        PrintBody("Удалено", delrs.value("t_NameObject"), delrs.value("t_FullName"), delrs.value("t_OpenDate"), delrs.value("t_CloseDate"), CreateReport, StatisticsOnly);
        DeleteRecObjAttr(delrs.value("t_AttrID"), IIF(IsOKVED2ForParty, 3, 207), IIF(IsOKVED2ForParty, 64, 199));

        UseProgress (CountOKVED2 = CountOKVED2 + 1);
      end;
    else
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника " + IIF(IsOKVED2ForParty, "Код ОКВЭД2", "ОКВЭД для 724ф"));
    end;
    
    sqlString =  " SELECT * " +
                 "   FROM dokved2expdoc_dbt "+
                 "  ORDER BY t_RecID ";
    rs = RsdRecordset(sqlString);
    stat = _StartRecursionInsertObjAttrOKVED2Rec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

    sqlString = " SELECT COUNT(1) " +
                "   FROM dokved2expdoc_dbt " ; 

    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInFile = rs.value(0);

    sqlString = "SELECT COUNT(1) FROM dobjattr_dbt WHERE T_OBJECTTYPE = " + IIF(IsOKVED2ForParty, 3, 207) + " AND T_GROUPID = " + IIF(IsOKVED2ForParty, 64, 199); 
    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInBase = rs.value(0);

    PrintTail(NumInFile, NumInBase, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

    RemProgress();
  end;
end;

private macro _DropTableOKPD2( ) : bool
  return execSQL("DROP TABLE dokpd2expdoc_dbt");
OnError(x)
  return true;
end;

private macro _CreateTableOKPD2( ) : bool
  return execSQL("CREATE TABLE dokpd2expdoc_dbt (t_recid NUMBER(10), t_code VARCHAR2(20), t_name VARCHAR2(500))");
OnError(x)
  return true;
end;

private macro _CreateIndexOKPD2( ) : bool
  return SQL_Execute("CREATE INDEX dokpd2expdoc_dbt_idx0 ON dokpd2expdoc_dbt (t_code)", "Создание индексов...");
OnError(x)
  return true;
end;

/* Добавить запись во временную таблицу */
private macro _AddToOKPD2(CodeOKPD2 : string, NameOKPD2 : string)
  RecIDOKPD2Array(RecIDOKPD2Array.Size) = RecIDOKPD2;
  RecIDOKPD2 = RecIDOKPD2 + 1;
  CodeOKPD2Array(CodeOKPD2Array.Size) = CodeOKPD2;
  NameOKPD2Array(NameOKPD2Array.Size) = NameOKPD2;
end;

/* Добавить запись во временную таблицу */
private macro _InsertIntoOKPD2(Flush : bool) : bool
  var ins : RsbSQLInsert;
  var stat = true;
  if( Flush == null ) 
    Flush = false; 
  end;
  if((CodeOKPD2Array.Size == 5000) or ((Flush) and (CodeOKPD2Array.Size > 0)))
    ins = RsbSQLInsert("INSERT INTO dokpd2expdoc_dbt (t_recid, t_code, t_name) VALUES (?, ?, ?)", 3, CodeOKPD2Array.Size);
    ins.AddParam(V_INTEGER, RecIDOKPD2Array    );
    ins.AddParam(V_STRING , CodeOKPD2Array, 20 );
    ins.AddParam(V_STRING , NameOKPD2Array, 500);
    stat = ins.Insert();
    RecIDOKPD2Array.Size = 0;
    CodeOKPD2Array.Size  = 0;
    NameOKPD2Array.Size  = 0;
  end;
  return stat;
end;

/*Убрать спец. символы из строки*/
private macro _NormalizeOKPD2(oldstr : string)
  var newstr, i, retstr; 
  newstr = oldstr;
  retstr = "";
  i = 0;
  while(i < StrLen(oldstr))
    i = i + 1;
    if(CodeFor(SubStr(oldstr, i, 1)) > 27)
      retstr = retstr + SubStr(oldstr, i, 1);
    elif(StrLen(retstr) > 0)
      i = StrLen(oldstr);
    end;
  end;
  return retstr;
end;

private macro _ConvertSingleDocFile2TableOKPD2(PathFileOKPD2)
  var i, j, k;
  var TablesCount, RowsCount, AllRowCount, CurrRowCount;
  var WordDoc = _OpenWordDoc(PathFileOKPD2);
  var Text;
  var IsColumn1Passed, IsColumn2Passed, IsColumn3Passed;
  var IsOKPD2Table;
  var CodeOKPD2, NameOKPD2;
  var file_name, file_ext;

  if(WordDoc != null)

    SplitFile(PathFileOKPD2, file_name, file_ext);
    file_name = MergeFile("", file_name, + file_ext);
    
    RecIDOKPD2Array = TArray;
    CodeOKPD2Array = TArray;
    NameOKPD2Array = TArray;

    TablesCount = WordDoc.Tables.Count;
    i = TablesCount;
    AllRowCount = 0;
    CurrRowCount = 0;
    while(i <= TablesCount)
      AllRowCount = AllRowCount + WordDoc.Tables(i).Rows.Count;
      i = i + 1;
    end;

    InitProgress(AllRowCount, " ~Alt-Break~ Прервать", "Обработка файла импорта \"" + file_name + "\"");

    i = TablesCount;
    while(i <= TablesCount)
      IsOKPD2Table = true;
      if((WordDoc.Tables(i).Rows.Count >= 1) and (WordDoc.Tables(i).Columns.Count >= 2))
        j = 1;
        
        RowsCount = WordDoc.Tables(i).Rows.Count;
        
        while(j <= RowsCount)
          k = 1;
          while(k <= 2)
            Text = _GetTableCellText(WordDoc, i, j, k);
            Text = _NormalizeOKPD2(Text);
            if(IsOKPD2Table)
              if(k == 1)
                CodeOKPD2 = trim(Text);
              end;
              if(k == 2)
                NameOKPD2 = trim(Text);
              end;
            else
              break;
            end;
            k = k + 1;
          end;

          if(CodeOKPD2 != "")
            _AddToOKPD2(CodeOKPD2, NameOKPD2);
          end;

          _InsertIntoOKPD2(false);

          j = j + 1;
          CurrRowCount = CurrRowCount + 1;

          UseProgress(CurrRowCount);
        end;

      end;
    
      _InsertIntoOKPD2(true);
      
      i = i + 1;
    end;
  
    RemProgress();

    WordDoc.Close;

    WordApp.Quit;
  end;
end;

private macro _GetAttrCodeNameOKPD2(rs, Code : @string, Name : @string)
  Code = trim(rs.value("t_code"));

  Name = trim(rs.value("t_name"));
end;

private macro _CheckNextOKPD2RecIsChild
(
  rs
 ,PrevCode
 ,PrevName
)
  var IsChild = false;
  var PrevCodeLen = 0;
  var IsPrevGroupRoot = false;
  var IsNextGroupRoot = false;
  var IsPrevSubGroupRoot = false;
  var IsNextSubGroupRoot = false;
  var PrevCountEndNull = 0;
  var NextCountEndNull = 0;

  var NextCode : string;
  var NextName : string;
  _GetAttrCodeNameOKPD2(rs, @NextCode, @NextName);

  if((index(strupr(PrevCode), "РАЗДЕЛ") > 0) and (index(strupr(NextCode), "РАЗДЕЛ") == 0))
    IsChild = true;
  else
    PrevCodeLen = strlen(trim(PrevCode));
    if(trim(PrevCode) == substr(trim(NextCode), 1, PrevCodeLen))
      IsChild = true;
    elif((PrevCodeLen == 12) and (substr(trim(PrevCode), 12) == "0") and (substr(trim(PrevCode), 1, 11) == substr(trim(NextCode), 1, 11)))
      IsChild = true;
    end;
  end;

  return IsChild;
end;

private macro _InsertObjAttrOKPD2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport:bool
 ,StatisticsOnly:bool
 ,ParentID : integer
 ,newAttrID : @integer
 ,newCode : @string
 ,newName : @string
)
  var stat = true;
  var ZeroDate = date(0, 0, 0);
  
  _GetAttrCodeNameOKPD2(rs, @newCode, @newName);

  ClearRecord(OBJATTR);

  if(index(strupr(newCode), "РАЗДЕЛ") > 0)
    OBJATTR.NumInList = trim(substr(newCode, index(strupr(newCode), "РАЗДЕЛ") + 7));
  else
    OBJATTR.NumInList = trim(newCode);
  end;
  OBJATTR.ObjectType = 3 ;
  OBJATTR.GroupID    = 65; //Справочник ОКВЭД2 
  
  if(not GetEQ(OBJATTR))
    if((UpdateFlag == 1) or (UpdateFlag == 2))
      newAttrID = GetMaxNumObjAttr(3, 65) + 1;
      OBJATTR.ObjectType = 3;
      OBJATTR.GroupID    = 65;
      OBJATTR.AttrID     = newAttrID;  
      OBJATTR.Name       = newName;
      OBJATTR.FullName   = newName;
      if(index(strupr(newCode), "РАЗДЕЛ") > 0)
        OBJATTR.NumInList = trim(substr(newCode, index(strupr(newCode), "РАЗДЕЛ") + 7));
      else
        OBJATTR.NumInList = trim(newCode);
      end;
      OBJATTR.NameObject = OBJATTR.NumInList;
      OBJATTR.OpenDate   = {curdate};                          
      OBJATTR.CloseDate  = ZeroDate;                         
      OBJATTR.ParentID   = ParentID;
      PrintBody("Введено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      Insert(OBJATTR);
    end;
  else
    newAttrID = OBJATTR.AttrID;
    if((UpdateFlag == 1) or (UpdateFlag == 3))
      /*Пишем в протокол о замене только если действительно поменялось наименование*/
      if(_CheckNeedInfoChangeNames(OBJATTR.Name, OBJATTR.FullName, newName, newName))
        PrintBody("Заменено", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
        OBJATTR.Name     = newName;
        OBJATTR.FullName = newName;
        OBJATTR.OpenDate = {curdate};                          
        PrintBody("на:", OBJATTR.NameObject, OBJATTR.FullName, OBJATTR.OpenDate, OBJATTR.CloseDate, CreateReport, StatisticsOnly);
      end;
      OBJATTR.ParentID = ParentID;
      Update(OBJATTR);
    end;
  end;

  UseProgress(CountOKPD2 = CountOKPD2 + 1);
  
  return stat;
end;

private macro _RecursionInsertObjAttrOKPD2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
 ,ParentID : integer
 ,PrevCode : string
 ,PrevName : string
 ,MoveNext : @bool
 ,Eof : @bool
)
  var stat = true;
  var newAttrID;
  var newCode;
  var newName;

  while(stat and (not Eof))

    if(MoveNext)
      stat = rs.moveNext();
      if(not stat)
        Eof = true;
      end;
      MoveNext = false;
    end;

    if(stat)
      if((ParentID == 0) or _CheckNextOKPD2RecIsChild(rs, PrevCode, PrevName))
        stat = _InsertObjAttrOKPD2Rec
        (
          rs
         ,UpdateFlag
         ,CreateReport
         ,StatisticsOnly
         ,ParentID
         ,@newAttrID
         ,@newCode
         ,@newName
        );
        if(stat)
          MoveNext = true;
        end;
      else
        stat = false;
      end;
    end;

    if(stat)
      stat = _RecursionInsertObjAttrOKPD2Rec
      (
        rs
       ,UpdateFlag
       ,CreateReport
       ,StatisticsOnly
       ,newAttrID
       ,newCode
       ,newName
       ,@MoveNext
       ,@Eof
      );
      if(not MoveNext)
        stat = true;
      end;
    end;
  end;

  return stat;
end;

private macro _StartRecursionInsertObjAttrOKPD2Rec
(
  rs
 ,UpdateFlag
 ,CreateReport : bool
 ,StatisticsOnly : bool
)
  var ParentID = 0;
  var PrevCode = "";
  var PrevName = "";
  var MoveNext = true;
  var Eof = false;

  var stat = _RecursionInsertObjAttrOKPD2Rec
  (
    rs
   ,UpdateFlag
   ,CreateReport
   ,StatisticsOnly
   ,ParentID
   ,PrevCode
   ,PrevName
   ,@MoveNext
   ,@Eof
  );

  return stat;
end;

private macro _ImpOKPD2All(CreateReport:bool, StatisticsOnly:bool, ImpFileName:string)
  var sqlString, rs, stat = false; 
  var delSqlString, delrs;
  var pbar, Number :INTEGER, Count :INTEGER;
  var NumInFile, NumInBase;

  Number = 0; CountOKPD2 = 0;

  ClearRecord(OBJGROUP);
  OBJGROUP.ObjectType = 3 ; //Справочник ОКПД2
  OBJGROUP.GroupID    = 65; 
  if(not GetEQ(OBJGROUP))
     OBJGROUP.ObjectType = 3;
     OBJGROUP.GroupID    = 65;
     OBJGROUP.Order      = 65;
     OBJGROUP.Name       = "Код ОКПД2";
     OBJGROUP.Type       = "";
     OBJGROUP.KeepOldValues = "X";
     OBJGROUP.System     = "X";
     OBJGROUP.UpdateFlag = 1;
     Insert(OBJGROUP);
  end;

  PrintHead("ОКПД2", ImpFileName, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

  sqlString = " SELECT COUNT(1) " +
              "   FROM dokpd2expdoc_dbt ";

  rs = RsdRecordset(sqlString);
  rs.moveNext();
  Number = rs.value(0);

  if(OBJGROUP.UpdateFlag == 0) 
    stat = true;
    msgbox("Категория ОКПД2 имеет статус обновления 'Оставить'. Загрузка категории из внешнего справочника невозможна")
  else
    if(OBJGROUP.UpdateFlag == 1)
      sqlString = " SELECT COUNT(1) " +
                  "   FROM dobjattr_dbt " +
                  "  WHERE t_ObjectType = 3 " +
                  "    AND t_GroupID = 65 " +
                  "    AND NOT EXISTS " +
                  "        (SELECT 1 " +
                  "           FROM dokpd2expdoc_dbt " +
                  "          WHERE CASE " +
                  "                WHEN INSTR(UPPER(t_code), 'РАЗДЕЛ') > 0 " +
                  "                THEN TRIM(SUBSTR(t_code, INSTR(UPPER(t_code), 'РАЗДЕЛ') + 7)) " +
                  "                ELSE t_code " +
                  "                END = t_NumInList)";
      rs = RsdRecordset( sqlString );
      rs.moveNext();
      Number = Number + rs.value(0);
      
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКПД2");
   
      delSqlString = " SELECT * " +
                     "   FROM dobjattr_dbt " +
                     "  WHERE t_ObjectType = 3 " +
                     "    AND t_GroupID = 65 " +
                     "    AND NOT EXISTS " +
                     "        (SELECT 1 " +
                     "           FROM dokpd2expdoc_dbt " +
                     "          WHERE CASE " +
                     "                WHEN INSTR(UPPER(t_code), 'РАЗДЕЛ') > 0 " +
                     "                THEN TRIM(SUBSTR(t_code, INSTR(UPPER(t_code), 'РАЗДЕЛ') + 7)) " +
                     "                ELSE t_code " +
                     "                END = t_NumInList)";
      delrs = RsdRecordset( delSqlString );
      while( delrs.moveNext() ) 
        PrintBody("Удалено", delrs.value("t_NameObject"), delrs.value("t_FullName"), delrs.value("t_OpenDate"), delrs.value("t_CloseDate"), CreateReport, StatisticsOnly);
        DeleteRecObjAttr(delrs.value("t_AttrID"), 3, 65);

        UseProgress (CountOKPD2 = CountOKPD2 + 1);
      end;
    else
      InitProgress(Number," ~Alt-Break~ Прервать","Загрузка справочника ОКПД2");
    end;
    
    sqlString =  " SELECT * " +
                 "   FROM dokpd2expdoc_dbt "+
                 "  ORDER BY t_RecID ";
    rs = RsdRecordset(sqlString);
    stat = _StartRecursionInsertObjAttrOKPD2Rec(rs, OBJGROUP.UpdateFlag, CreateReport, StatisticsOnly);

    sqlString = " SELECT COUNT(1) " +
                "   FROM dokpd2expdoc_dbt " ; 

    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInFile = rs.value(0);

    sqlString = "SELECT COUNT(1) FROM dobjattr_dbt WHERE T_OBJECTTYPE = 3 AND T_GROUPID = 65"; 
    rs = RsdRecordset( sqlString );
    rs.moveNext();
    NumInBase = rs.value(0);

    PrintTail(NumInFile, NumInBase, NumInsert, NumDelete, NumReplace, CreateReport, StatisticsOnly) ;

    RemProgress();
  end;
end;

/* CSV */

private macro _CreateTableOKATOCsv() : bool
  return execSQL(
    "CREATE TABLE DOKATOEXPDBF_DBT"+
    " ("+
    " T_TER VARCHAR2(2),"+
    " T_KOD1 VARCHAR2(3),"+
    " T_KOD2 VARCHAR2(3),"+
    " T_KOD3 VARCHAR2(3),"+
    " T_RAZDEL VARCHAR(1),"+
    " T_NAME1 VARCHAR2(250),"+
    " T_CENTRUM VARCHAR2(80),"+
    " T_NOMDESCR VARCHAR2(250),"+
    " T_NOMAKT VARCHAR2(3),"+
    " T_STATUS VARCHAR2(1),"+
    " T_DATEUTV DATE,"+
    " T_DATEVVED DATE"+
    " );");
OnError(x)
  return true;
end;


class CsvColumn(_type:integer, _value:TArray, _size:integer)
  var type : integer = _type;
  var val:TArray = _value;  // .Size == PackageSize
  var size :integer; 
  
  if((type == V_STRING) AND (valtype(_size) == V_INTEGER) and (_size > 0))
    size = _size + 1; // + \0 символ
  else
    size = null;
  end;
  
end;

class CSVReader(_path:string, _encoding:string, _colsInRow:integer)
  var path = _path;
  var encoding = _encoding;
  var columsCount = _colsInRow;
  
  private MACRO DefaultParm( parm, value )
      if ( parm == NULL )
          SetParm( 0, value );
      end;
  END;
  
  private MACRO SplitString( str, delim, save_delim, fieldsCount )
    DefaultParm(delim, ";");
    DefaultParm(save_delim, false);
    DefaultParm(fieldsCount, 15);

    var arr = TArray(fieldsCount, fieldsCount), i = StrBrk( str, delim );

    while( i > 0 )
        if (save_delim)
          arr( arr.size ) = substr( str, 1, i );
          str = substr( str, i + 1 );
        else
          arr( arr.size ) = trim( substr( str, 1, i - 1 ) );
          str = trim( substr( str, i + 1 ) );
        end;
        i = StrBrk( str, delim );
    end;

    if( str != "" )
      if (save_delim)
        arr( arr.size ) = str;
      else
        arr( arr.size ) = trim( str );
      end;
    end;

    return arr;
  END;
  
  macro ForeachRow(obj, processRow)

    var stat = true;
    var impfile = TStreamDoc(path, "R", encoding);
    
      var i = 0;
      while( stat AND impfile.ReadLine())
        stat = CallR2M(R2M(obj, processRow),i, SplitString(impfile.str, ";", false, columsCount));
        i = i + 1;
      end;
    return stat;
  end;
  
end;


class OKATOCsvImporter

  class OKATOColumns
    const TER      = 0;  // Код региона              
    const KOD1     = 1;  // Код района/города МО     
    const KOD2     = 2;  // Код поселения МО         
    const KOD3     = 3;  // Код населенного пункта МО   
    const RAZDEL   = 4;  // Код раздела              
    const NAME1    = 5;  // Наименование территории
    const Centrum  = 6;  // Дополнительная информация
    const NomDescr = 7;  // Описание                     
    const NomAkt   = 8;  // Номер изменения              
    const Status   = 9;  // Тип изменения                
    const DateUtv  = 10; // Дата принятия                
    const DateVved = 11; // Дата введения                
  end;              
  private var fld = OKATOColumns();
  
  private var _table:TArray; // TArray<CsvColumn>
  private var _packageMaxSize = 5000;
  private var _cacheSize = 0;
  
  //возвращает подстроку без первого " и последнего "
  private macro ReplaceQuotes(str: string)
    var s = str;
    if( (index(str, "\"") == 1) )               // s[0]    == '\"'
      s = substr(s, 2);
    end;
    if( (index(str, "\"", strlen(s)-1) > 0) )   // s[last] == '\"')
      s = substr(s, 1, strlen(s)-1);
    end;
    return s;
  end;
  private macro MaxStr(strval:string, fldType /*:OKTMOColumns*/)
    strval = ReplaceQuotes(trim(strval)); // удалим пробелы и кавычки   "01"
    if(strlen(strval) > _table(fldType).Size) 
      return substr(strval, 0, _table(fldType).Size);
    else
      return strval;
    end;
  end;
  
  private macro InitColumns()
    _table = TArray(); 
    
    _table(fld.TER     ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 2);
    _table(fld.KOD1    ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 3);
    _table(fld.KOD2    ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 3);
    _table(fld.KOD3    ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 3);
    _table(fld.RAZDEL  ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 1);
    _table(fld.NAME1   ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 250);
    _table(fld.Centrum ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 80);
    _table(fld.NomDescr) = CsvColumn(V_STRING, TArray(_packageMaxSize), 250);
    _table(fld.NomAkt  ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 3);
    _table(fld.Status  ) = CsvColumn(V_STRING, TArray(_packageMaxSize), 1);
    _table(fld.DateUtv ) = CsvColumn(V_DATE, TArray(_packageMaxSize));
    _table(fld.DateVved) = CsvColumn(V_DATE, TArray(_packageMaxSize));
  end;
  
  private macro AddRow(parm:TArray)
    
    _table(fld.TER     ).Val.Value(_cacheSize) = MaxStr(parm(fld.TER     ), fld.TER);
    _table(fld.KOD1    ).Val.Value(_cacheSize) = MaxStr(parm(fld.KOD1    ), fld.KOD1);
    _table(fld.KOD2    ).Val.Value(_cacheSize) = MaxStr(parm(fld.KOD2    ), fld.KOD2);
    _table(fld.KOD3    ).Val.Value(_cacheSize) = MaxStr(parm(fld.KOD3    ), fld.KOD3);
    _table(fld.RAZDEL  ).Val.Value(_cacheSize) = MaxStr(parm(fld.RAZDEL  ), fld.RAZDEL);
    _table(fld.NAME1   ).Val.Value(_cacheSize) = MaxStr(parm(fld.NAME1   ), fld.NAME1);
    _table(fld.Centrum ).Val.Value(_cacheSize) = MaxStr(parm(fld.Centrum ), fld.Centrum);
    _table(fld.NomDescr).Val.Value(_cacheSize) = MaxStr(parm(fld.NomDescr), fld.NomDescr);
    _table(fld.NomAkt  ).Val.Value(_cacheSize) = MaxStr(parm(fld.NomAkt  ), fld.NomAkt);
    _table(fld.Status  ).Val.Value(_cacheSize) = MaxStr(parm(fld.Status  ), fld.Status);
    _table(fld.DateUtv ).Val.Value(_cacheSize) =   date(parm(fld.DateUtv ));
    _table(fld.DateVved).Val.Value(_cacheSize) =   date(parm(fld.DateVved));

  end;
  
  private macro InsertRows()
    var stat = true;
    if(_cacheSize)
      var _cache = RsbSQLInsert(
        "INSERT INTO DOKATOEXPDBF_DBT"+
        " (t_TER     ,t_KOD1    ,t_KOD2    ,t_KOD3    ,t_RAZDEL  ,t_NAME1   ,t_Centrum ,t_NomDescr,t_NomAkt  ,t_Status  ,t_DateUtv ,t_DateVved)"+
        " VALUES (?,?,?,?,?,?,?,?,?,?,?,?)"
        , 12, _cacheSize);
        
      for(var col, _table)
        if(col != null)
          if(col.Type == V_STRING)
            _cache.AddParam(col.type, col.val, col.size + 1);
          else
            _cache.AddParam(col.type, col.val);
          end;
        end;
      end;
      
      stat = _cache.Insert();
      
      for(col, _table)
        if(col != null)
          col.Val.Size = 0;
        end;
      end;
      _cacheSize = 0;
    end;
    
    return stat;
  end;

  private macro SaveFileToAppServ(filepath:string):string
    
    if(not isStandAlone()) // когда мы на терминале, копируем файл на СП
      var file_name;
      SplitFile(filepath, file_name, null);      
      
      var path_appserv = "..\\workfile\\" + file_name +"."+ string(UserNumber);
      
      if(not CopyFile("$" + filepath, path_appserv, true, "Копируется файл на сервер приложений..."))
        RsbThrow("Ошибка при передаче файла на сервер приложений");
      end;
      filepath = path_appserv;
    end;
    
    return filepath;
  end;
  
  macro ProcessRow(index, strarr:TArray):bool
    
    var stat = true;
    if(     (strupr(strarr(fld.TER)) != strupr("ter"))
        AND (strupr(ReplaceQuotes(trim(strarr(fld.TER)))) != strupr("00")) ) // пропуcтим Headder и названия разделов
      
      if(_cacheSize >= _packageMaxSize)
        stat = InsertRows();        
      end;
      
      AddRow(strarr);
      
      _cacheSize = _cacheSize + 1;
    end;    
    
    return stat;
  onerror(e)
    RsbRethrow(e, "Ошибка при обработке строки "+string(index)+". Проверьте формат импортируемых данных.")
  end;
  
  macro ImportData(okatopath, encoding)
    InitColumns();
    
    var file_name;
    SplitFile(okatopath, file_name, null);
    
    okatopath = SaveFileToAppServ(okatopath); 

    BegAction(200, "Обработка файла импорта \"" + file_name + "\"", false);
        
    
    var r = CSVReader(okatopath, encoding); 
    var stat = r.ForeachRow(this, "ProcessRow");
    
    if(stat)
      stat = InsertRows(); // последние не вставленные
    end;
    
    EndAction();
    
    return stat;
  end;
  
end;

class (OKATOCsvImporter) OKTMOCsvImporter
  
  InitOKATOCsvImporter();
  
  class OKTMOColumns
    const TER      = 0;  // Код региона              
    const KOD1     = 1;  // Код района/города МО     
    const KOD2     = 2;  // Код поселения МО         
    const KOD3     = 3;  // Код населенного пункта МО   
    const KC       = 4;  // контрольное число
    const RAZDEL   = 5;  // Код раздела              
    const NAME1    = 6;  // Наименование территории
    const Centrum  = 7;  // Дополнительная информация
    const NomDescr = 8;  // Описание                     
    const NomAkt   = 9;  // Номер изменения              
    const Status   = 10; // Тип изменения                
    const DateUtv  = 11; // Дата принятия                
    const DateVved = 12; // Дата введения                
  end;   
  fld = OKTMOColumns();  // заменяем на формат полей ОКТМО
  
  _packageMaxSize = 5000;
  var ImportPlacesOKTMO = false;
  
  private macro ShapeStr(strval, length)
    if(strlen(strval) > length) 
      return substr(strval, 0, length);
    else
      return strval;
    end;
  end;
  
  private macro ReplZero(code:string)
    if((code =="00") or (code == "000"))
      code = "";
    end;
    return code;
  end;
  
  private macro AddRow(parm:TArray)
  
    var razdel:integer = int(ReplaceQuotes(trim(parm(fld.RAZDEL))));
    
    var CodeOKTMO = "";
    
    if(razdel == 1)
      CodeOKTMO = ReplaceQuotes(trim(parm(fld.TER))) + " " + ReplaceQuotes(trim(parm(fld.KOD1))) + " " + ReplaceQuotes(trim(parm(fld.KOD2)));
    elif( razdel == 2)
      CodeOKTMO = ReplaceQuotes(trim(parm(fld.TER))) + " " + ReplaceQuotes(trim(parm(fld.KOD1))) + " " + ReplaceQuotes(trim(parm(fld.KOD2)))  + " " + ReplaceQuotes(trim(parm(fld.KOD3)));
    end;
    
    var NameOKTMO = ReplaceQuotes(trim(parm(fld.NAME1)));
    
    if( (CodeOKTMO != "") and ((strlen(CodeOKTMO) == 10) or (ImportPlacesOKTMO and (strlen(CodeOKTMO) == 14))) )
      _AddToOKTMO(CodeOKTMO, ShapeStr(NameOKTMO, 500));  
    end;
    
  end;
  
  // Использует механизм импорта OKTMO из doc: _AddToOKTMO(), _InsertIntoOKTMO()
  macro ProcessRow(index, strarr:TArray):bool
    
    var stat = true;
    if(     (strupr(strarr(fld.TER)) != strupr("ter"))
        AND (strupr(ReplaceQuotes(trim(strarr(fld.TER)))) != strupr("00")) ) // пропутим Headder и названия разделов
      
      if(_cacheSize >= _packageMaxSize)
        _InsertIntoOKTMO(false);        
      end;
      
      AddRow(strarr);
      
      _cacheSize = _cacheSize + 1;
    end;    
    
    return stat;
  onerror(e)
    RsbRethrow(e, "Ошибка при обработке строки "+string(index)+". Проверьте формат импортируемых данных.")
  end;
  
  macro ImportData(oktmopath, encoding, _importplaces:bool)
    
    this.ImportPlacesOKTMO = _importplaces;
    
    var file_name;
    SplitFile(oktmopath, file_name, null);
    
    oktmopath = SaveFileToAppServ(oktmopath);
    
    BegAction(200, "Обработка файла импорта \"" + file_name + "\"", false);
    
    InitOKTMOArrays();
        
    
    var r = CSVReader(oktmopath, encoding); 
    var stat = r.ForeachRow(this, "ProcessRow");
    
    _InsertIntoOKTMO(true); // последние не вставленные
    
    
    EndAction();
    
    return stat;
  end;
  
end;



/* CSV end*/

/**
@brief Проверка формата хранения ОКАТО
@param[in] V_BOOL isFullFormat Тип проверяемого формата ОКАТО (isFullFormat = true - все записи ОКАТО в формате 11 символов)
@return V_BOOL true Если формат хранимых записей ОКАТО соответствует параметру isFullFormat, иначе false
*/
private macro CheckOkatoFormat(isFullFormat: Bool): Bool
  var cmd = DL_RSDCommand("SELECT 1 FROM dObjAttr_dbt WHERE t_objectType = ? AND t_groupID = ? AND LENGTH(t_nameObject) != ? AND LENGTH(t_numInList) != ? AND ROWNUM = 1");
  cmd.AddParam(OBJTYPE_PARTY);
  cmd.AddParam(PARTY_ATTR_GROUP_OKATO);
  cmd.AddParam(FULL_OKATO_CODE_LENGTH);
  cmd.AddParam(FULL_OKATO_CODE_LENGTH);
  var rs = cmd.Execute();
  
  if (rs.MoveNext())
    return (not isFullFormat);
  end;
  
  return isFullFormat;
end;

/**
@brief Конвертация всех существующих записей ОКАТО в формат 11 символов
*/
private macro ConvertOkatoToFullFormat()
  if (CheckOkatoFormat(false)) // Есть записи в коротком формате
    SQL_Execute("UPDATE dObjAttr_dbt SET t_nameObject = CONCAT(t_nameObject, SUBSTR('00000000000', LENGTH(t_nameObject) + 1)), " +
                                       " t_numInList = CONCAT(t_numInList, SUBSTR('00000000000', LENGTH(t_numInList) + 1)) " +
                " WHERE t_objectType = " + OBJTYPE_PARTY +
                  " AND t_groupID = " + PARTY_ATTR_GROUP_OKATO +
                  " AND LENGTH(t_nameObject) != " + FULL_OKATO_CODE_LENGTH +
                  " AND LENGTH(t_numInList) != " + FULL_OKATO_CODE_LENGTH, "", false);
  end;
end;

/**
@brief Конвертация всех существующих записей ОКАТО в короткий формат без нулей в конце кода (2, 5, 8 или 11 символов)
*/
private macro ConvertOkatoToShortFormat()
  if (CheckOkatoFormat(true)) // Все записи в полном формате (11 символов)
    SQL_Execute("UPDATE dObjAttr_dbt " +
                   "SET t_nameObject = CASE WHEN SUBSTR(t_nameObject, 3) = '000000000' " +
                                           "THEN SUBSTR(t_nameObject, 1, 2) " +
                                           "ELSE CASE WHEN SUBSTR(t_nameObject, 6) = '000000' " +
                                                     "THEN SUBSTR(t_nameObject, 1, 5) " +
                                                     "ELSE CASE WHEN SUBSTR(t_nameObject, 9) = '000' " +
                                                               "THEN SUBSTR(t_nameObject, 1, 8) " +
                                                               "ELSE t_nameObject " +
                                                          "END " +
                                                "END " +
                                        "END, " +
                       "t_numInList = CASE WHEN SUBSTR(t_numInList, 3) = '000000000' " +
                                          "THEN SUBSTR(t_numInList, 1, 2) " +
                                          "ELSE CASE WHEN SUBSTR(t_numInList, 6) = '000000' " +
                                                    "THEN SUBSTR(t_numInList, 1, 5) " +
                                                    "ELSE CASE WHEN SUBSTR(t_numInList, 9) = '000' " +
                                                              "THEN SUBSTR(t_numInList, 1, 8) " +
                                                              "ELSE t_numInList " +
                                                         "END " +
                                               "END " +
                                     "END " +
              " WHERE t_objectType = " + OBJTYPE_PARTY +
                " AND t_groupID = " + PARTY_ATTR_GROUP_OKATO +
                " AND LENGTH(t_nameObject) = " + FULL_OKATO_CODE_LENGTH +
                " AND LENGTH(t_numInList) = " + FULL_OKATO_CODE_LENGTH, "", false);
  end;
end;

/*================================================================================*/
MACRO ImpDictionary
(
  ImpOKATO : bool
 ,ImpOKTMO : bool
 ,ImpOKVED : bool
 ,ImpOKVED2: bool
 ,ImpOKPD2: bool
 ,CreateReportOKATO : bool
 ,StatisticsOnlyOKATO : bool
 ,CreateReportOKTMO : bool
 ,StatisticsOnlyOKTMO : bool
 ,ImportPlacesOKTMO : bool
 ,CreateReportOKVED : bool
 ,StatisticsOnlyOKVED : bool
 ,CreateReportOKVED2 : bool
 ,StatisticsOnlyOKVED2 : bool
 ,CreateReportOKPD2 : bool
 ,StatisticsOnlyOKPD2 : bool
 ,PathOKATO : string
 ,PathOKTMO : string
 ,PathOKVED : string
 ,PathOKVED2 : string
 ,PathOKPD2 : string
)

  var ERR_TEXT;

  if(ImpOKATO)
    dropTableOKATO();
   
    var file_ext;
    SplitFile(PathOKATO, null, file_ext);
    
    if(strupr(file_ext) == strupr(".csv"))
      _CreateTableOKATOCsv();      
      var imp = OKATOCsvImporter();
      if(not imp.ImportData(PathOKATO, "lcansi"))
        Exit(1);
      end; 
    elif( not dbf2ora(PathOKATO, "DOKATOEXPDBF_DBT", "Индексация справочника ОКАТО", err_text) )
      MsgBox( err_text );
      Exit(1);
    end;

    if (isOkatoNewFunctional)
      if (isOkato11DigitsFormat)
        ConvertOkatoToFullFormat();
      else
        ConvertOkatoToShortFormat();
      end;
    end;

    NumInsert =0; NumDelete = 0; NumReplace = 0;
    if(NewVersionOkato())
      SQL_Execute("create index DOKATOEXPDBF_DBT_IDX0 ON DOKATOEXPDBF_DBT(T_RAZDEL, T_TER, T_KOD1, T_KOD2)", "Создание индексов...");
      ImpNewOKATOAll(CreateReportOKATO, StatisticsOnlyOKATO,PathOKATO);
    else
      SQL_Execute("create index DOKATOEXPDBF_DBT_IDX0 ON DOKATOEXPDBF_DBT(T_RAZDEL, T_KOD1, T_KOD2, T_KOD3)", "Создание индексов...");
      ImpOKATOAll(CreateReportOKATO, StatisticsOnlyOKATO,PathOKATO);
    end;
  end;
  if(ImpOKTMO)
    _DropTableOKTMO();
    _CreateTableOKTMO();
   
    var file_ext2;
    SplitFile(PathOKTMO, null, file_ext2);
    
    if(strupr(file_ext2) == strupr(".csv"))           
      var imp2 = OKTMOCsvImporter();
      if(not imp2.ImportData(PathOKTMO, "lcansi", ImportPlacesOKTMO))
        Exit(1);
      end; 
    else

      _ConvertAllDocFiles2TableOKTMO(PathOKTMO, ImportPlacesOKTMO);

    end;
    NumInsert =0; NumDelete = 0; NumReplace = 0;
    SQL_Execute("DELETE FROM doktmoexpdoc_dbt WHERE SUBSTR(REPLACE(t_code, ' ', ''), 9) = '000'", "Создание индексов...");
    if(RemoveSpacesFromOktmo)
      SQL_Execute("UPDATE doktmoexpdoc_dbt SET t_code = REPLACE(t_code, ' ', '')", "Создание индексов...");
    end;

    _CreateIndexOKTMO();

    _ImpOKTMOAll(CreateReportOKTMO, StatisticsOnlyOKTMO, PathOKTMO, ImportPlacesOKTMO);
  end;

  if(ImpOKVED)
    if( not dbf2ora(PathOKVED, "DOKVEDEXPDBF_DBT", "Индексация справочника ОКВЭД", err_text) )
      MsgBox( err_text );
      Exit(1);
    end;

     NumInsert =0; NumDelete = 0; NumReplace = 0;
    if(NewVersionOkved())
      SQL_Execute("create index DOKVEDEXPDBF_DBT_IDX0 ON DOKVEDEXPDBF_DBT(T_KOD)", "Создание индексов...");
      ImpNewOKVEDAll(CreateReportOKVED, StatisticsOnlyOKVED,PathOKVED);
    else
      SQL_Execute("create index DOKVEDEXPDBF_DBT_IDX0 ON DOKVEDEXPDBF_DBT(T_KOD)", "Создание индексов...");
      ImpOKVEDAll(CreateReportOKVED, StatisticsOnlyOKVED,PathOKVED);
    end;
  end;

  if(ImpOKVED2)
    _DropTableOKVED2();
    _CreateTableOKVED2();
    _ConvertSingleDocFile2TableOKVED2(PathOKVED2);
    NumInsert = 0; NumDelete = 0; NumReplace = 0;IsOKVED2ForParty = true;
    _CreateIndexOKVED2();
    _ImpOKVED2All(CreateReportOKVED2, StatisticsOnlyOKVED2, PathOKVED2);
	 NumInsert = 0; NumDelete = 0; NumReplace = 0; IsOKVED2ForParty = false;
	_ImpOKVED2All(CreateReportOKVED2, StatisticsOnlyOKVED2, PathOKVED2);
  end;

  if(ImpOKPD2)
    _DropTableOKPD2();
    _CreateTableOKPD2();
    _ConvertSingleDocFile2TableOKPD2(PathOKPD2);
    NumInsert = 0; NumDelete = 0; NumReplace = 0;
    _CreateIndexOKPD2();
    _ImpOKPD2All(CreateReportOKPD2, StatisticsOnlyOKPD2, PathOKPD2);
  end;

  return true;
END;

//ImpDictionary(true, false, true, false, false, false, "D:\\VM\\Import\\Okato\\OKATO.DBF", "");
//ImpDictionary(false, true, false, false, true, false, false, true, false, false, "", "D:\\VM\\Import\\OKTMO_2013\\Test\\ОКТМО_Консультант_?_rtf.rtf", "");


