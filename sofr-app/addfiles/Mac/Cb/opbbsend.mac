/************************************************************************/
/*         Автоматизированная банковская система RS-Bank 5.1            */
/*                 Copyright (c) R-Style Software Lab 2001              */
/*                                                                      */
/*  Имя файла        : opbbsend.mac                                     */
/*                                                                      */
/*  Описание         : Выгрузка платежей ББ в АРМ Позиционера           */
/*                                                                      */
/*  Программист      : Алешин А.В.                                      */
/*                                                                      */
/*  Создан           : 21.12.01                                         */
/*                                                                      */
/************************************************************************/

import InsCarryDoc, BankInter;
import "pmmulti.mac";
import "paym_rec.mac";

/* Символ пустой? (пустая строка или только нули) */
PRIVATE MACRO isEmptySymbol( symbol ):bool
  return ( int(trim(symbol)) == 0 );
ONERROR(err)
  return TRUE;
END;

macro ExecuteStep( doc, primdoc )
  var d = RsbAccTransaction(); // RsbAccTransactionData
  record order("memorder.dbt", "bank.def");
  var TransferDate:date;

  SetBuff(order, primdoc);

  /* Для документов, поступивших из БО выполним плановую проводку */
  if( (order.Origin == MEMORDER_FDOC_LOANS) or (order.Origin == MEMORDER_FDOC_RETAIL) or (order.Origin == MEMORDER_FDOC_DP))
    
    /* Проверяем ДПП платежа */

    if(( (Pm_debet.PaymentID  == Pm_paym.PaymentID) and (Pm_debet.Group  == PAYMENTS_GROUP_EXTERNAL) ) or
       ( (Pm_credit.PaymentID == Pm_paym.PaymentID) and (Pm_credit.Group == PAYMENTS_GROUP_EXTERNAL) )) 

      if( (Pm_debet.PaymentID  == Pm_paym.PaymentID) and (Pm_debet.Group  == PAYMENTS_GROUP_EXTERNAL) )
        if( not PaymOutChangeDPP(Pm_paym, Pm_debet, NULL, 0, TransferDate ) )
          return 1;
        end;
      else
        if( not PaymOutChangeDPP(Pm_paym, NULL, Pm_credit, 0, TransferDate ) )
          return 1;
        end;
      end;

      if( TransferDate > {curdate} )
        MsgBox("Дата перечисления платежа для собственного платежа банка|не может отличаться от текущей" );
        return 1;
      end;

    end;

    /* Заполняем структуру проводки */
    d.Chapter       = 1;
    d.Kind_Oper     = " 1";
    d.ResultCarry   = 1;
    d.Date_Carry    = Pm_paym.ValueDate;
    d.Sum           = Pm_paym.Amount;
    d.FIID          = Pm_paym.FIID;
    d.Number_Pack   = Pm_paym.NumberPack;
    d.Numb_Document = Pm_rmprop.Number;
    d.Department    = Pm_paym.Department;
    d.Shifr_Oper    = Pm_rmprop.ShifrOper;
    d.Ground        = Pm_rmprop.Ground;

    /* Все проводки - по Future-счетам */
    d.AccountPayer    = Pm_paym.FuturePayerAccount;
    d.AccountReceiver = Pm_paym.FutureReceiverAccount;

    /* Получение массива символов */
    if( not isEmptySymbol(Pm_rmprop.SymbNotBal) )
      d.AddCashSymbol(Pm_rmprop.SymbNotBal, d.Sum);
    end;
    
    if( d.AccountReceiver == "" )
      MsgBox("В схеме расчетов не задан счет \"Незавершенные расчеты банка (списание)\"");
      return 1;
    end;
                                  
    /* Выполним плановую проводку со счета плательщика на корсчет */
    if( not d.Carry() )
      MsgBox("Ошибка при вставке рублевого документа");
      return 1;
    end;

  end;

  if( not InsertPaymentStat( 0, PM_READY_TO_SEND, PMMET_DPS, 0, 0, Pm_paym.Purpose, Pm_paym.SubPurpose ) )
    msgbox("Ошибка при вставке статуса платежа");
    return 1;
  end;

  /* Обработка сводных платежей */
  if( (Pm_debet.PaymentID  == Pm_paym.PaymentID) and (Pm_debet.Group  == PAYMENTS_GROUP_EXTERNAL) )
    if( ProcessPayment( Pm_debet.Corschem, Pm_debet.PayFIID, Pm_paym.ValueDate, Pm_paym.Department ) )
      return 1;
    end;
  elif( (Pm_credit.PaymentID  == Pm_paym.PaymentID) and (Pm_credit.Group  == PAYMENTS_GROUP_EXTERNAL) )
    if( ProcessPayment( Pm_credit.Corschem, Pm_credit.PayFIID, Pm_paym.ValueDate, Pm_paym.Department ) )
      return 1;
    end;
  end;

  return 0;
end;
