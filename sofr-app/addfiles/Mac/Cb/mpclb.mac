/*
$Name:        mpclb.mac
$Module:      Новые проценты. 
$Description: Общие константы и функции
*/
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpclb.mac

  Библиотека       : RSACCOUNT

  Описание         : Новые проценты. Общие константы и функции

  Программист      : Kirakozov Michael (KMB)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet, OprInter,  globals, Проценты, FIInter;

const PRC_ACCSCHEMA_NO_ADDCH           = 0, // доначисление не делаем
      PRC_ACCSCHEMA_ADDCH              = 1, // доначисление делаем
      PRC_ACCSCHEMA_PRIV               = 2, // привлечение. доначисление не делаем      
      PRC_ACCSCHEMA_PRIV_ADDCH         = 3, // привлечение. доначисление делаем     
      PRC_ACCSCHEMA_RAZM_BAL           = 4, // размещение.баланс     
      PRC_ACCSCHEMA_RAZM_NONBAL        = 5, // размещение.внебаланс     
      PRC_ACCSCHEMA_RAZM_BAL_OD        = 6, // размещение.баланс.просрочка     
      PRC_ACCSCHEMA_RAZM_NONBAL_OD     = 7, // размещение.внебаланс.просрочка  
      PRC_ACCSCHEMA_RAZM_BAL_NONBAL    = 8, // размещение.баланс и внебаланс
      PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD = 9; // размещение.баланс и внебаланс.просрочка


const PRC_KVITSTATE_PREP            = 0, //
      PRC_KVITSTATE_FINISHED        = 1, //  
      PRC_KVITSTATE_FINISHED_MANUAL = 2; //   

const PRC_PAYSTATUS_NOT_CALCULATED     = 0,     // не рассчитано
      PRC_PAYSTATUS_NOT_CLAIMED        = 1,     // не предъявлено
      PRC_PAYSTATUS_NOT_PAID           = 2,     // не оплачено
      PRC_PAYSTATUS_PARTLY_PAID        = 3,     // частично оплачено
      PRC_PAYSTATUS_PAID               = 4,     // оплачено
      PRC_PAYSTATUS_PARTLY_PAID_MANUAL = 5,     // частично оплачено ручной ввод
      PRC_PAYSTATUS_PAID_MANUAL        = 6,     // оплачено ручной ввод
      PRC_PAYSTATUS_SUSPENDED          = 7,     // начисление приостановлено
      PRC_PAYSTATUS_AWARDED            = 8;     // проценты начисляются

const PRC_RESKIND_PRIV        = 1, //
      PRC_RESKIND_RAZM        = 2; // 

const PRC_RATETYPE_ABSOLUTE      = 1,    // Абсолютная
      PRC_RATETYPE_USER          = 2,    // Пользовательская
      PRC_RATETYPE_FLOATING      = 3;    // Плавающая   

const PRC_REESTR_CATEG3       = "COMMON\\ПЕРЕМЕННЫЕ\\СУБЪЕКТ 3Й КАТЕГОРИИ КАЧЕСТВА";      
const PRC_REESTR_POSTDOC      = "BBANK\\PERCENT\\PERCPOSTDOC";
const PRC_REESTR_CHARGEBEFORE = "BBANK\\PERCENT\\НАЧИСЛ. ПЕРЕД ОПЛАТОЙ ПРИВЛ.";

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);

MACRO ИнитЗначенияНастройкиПроцентов(Настройка, ЗначениеНастройки:@bool)
      var stat = 0;

     GetRegistryValue(НАСТРОЙКА, V_BOOL, @ЗначениеНастройки, stat);
     if(stat)
        msgbox("Не задана настройка в реестре банка:|", Настройка);
     end;

     return stat;
END;


MACRO GetAccountDepartment(Account,Chapter,FIID, Dep:@integer)
   var   cmd,rs,
         stat = false;

   cmd = RSDCommand ("select T_DEPARTMENT from DACCOUNT_DBT " +
           "where T_ACCOUNT = ? and T_CHAPTER = ? and T_CODE_CURRENCY = ?");
   cmd.addParam( "", RSDBP_IN, Account );
   cmd.addParam( "", RSDBP_IN, Chapter );
   cmd.addParam( "", RSDBP_IN, FIID );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      Dep = rs.department; 
      stat = true;  
   end;
   
   return stat;
END;


MACRO GetAccountClient(Account, Client:@integer)
   var   cmd,rs,
         stat = false;

   cmd = RSDCommand ("select T_CLIENT from DACCOUNT_DBT where T_ACCOUNT = ? ");
   cmd.addParam( "", RSDBP_IN, Account );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      Client = rs.Client;   
      stat = true;  
   end;

   return stat;
END;


MACRO GetAccountFIID(Account, FIID:@integer)
   var   cmd,rs,
         stat = false;

   cmd = RSDCommand ("select T_CODE_CURRENCY from DACCOUNT_DBT where T_ACCOUNT = ? ");
   cmd.addParam( "", RSDBP_IN, Account );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      FIID = rs.Code_Currency;   
      stat = true;  
   end;

   return stat;
END;


MACRO GetPartyDpDep(Code, PartyID:@integer)
   var   cmd,rs,
         stat = false;

   cmd = RSDCommand ("select T_PARTYID FROM ddp_dep_dbt where T_CODE = ? ");
   cmd.addParam( "", RSDBP_IN, Code );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      PartyID = rs.PartyID;   
      stat = true;  
   end;
   
   return stat;
END;

// Читаем рассчитанную сумму за период
MACRO GetPeriodSumCalc(ContractID, SchedKind, SchedDate):money;
   var sumcalc = $0,
       cmd,rs,
       stat = 0;

   cmd = RSDCommand ("select T_SUMCALC from DPRCCNTSCHED_DBT "
         + "where T_CONTRACTID = ? and T_SCHEDULEKIND = ? and T_DATE = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, SchedKind );
   cmd.addParam( "", RSDBP_IN, SchedDate );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      sumcalc = rs.sumcalc;   
   end;

   if (ValType(sumcalc) == V_UNDEF)
      sumcalc = $0;
   end;
   
   return sumcalc;
END;


// Читаем даты периода 
MACRO GetDatesFromPeriod(ContractID, SchedKind, PeriodDate, PeriodBeginDate:@date, PeriodEndDate:@date)
   var   cmd,rs,
         stat = 0;

   cmd = RSDCommand ("select T_PERIODBEGINDATE, T_PERIODENDDATE FROM DPRCCNTSCHED_DBT " +
                     "where T_CONTRACTID = ? and T_SCHEDULEKIND = ? and T_DATE = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, SchedKind );
   cmd.addParam( "", RSDBP_IN, PeriodDate);
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      PeriodBeginDate = rs.periodbegindate; 
      PeriodEndDate = rs.periodenddate;
   end;

   return stat;   
END;


MACRO CalcPercentForPeriod(ID, BeginDate, EndDate):money
   var cmd,Sum = $0;

   Prc_CalcPrcValues(ID,BeginDate, EndDate, Sum);

   return money(Sum);
END; // end macro


// Функция определяет значение категории качества на дату
MACRO PrcGetClientQualityOnDate(ContractID, Date, QualValue:@integer)

   var cmd,rs,
   stat = 6829;

   cmd = RSDCommand ("select T_CLIENTQUALITY from DPRCCNTQUAL_DBT where T_CONTRACTID = ? and T_DATE = " +
                     "(select max(T_DATE) from DPRCCNTQUAL_DBT where T_CONTRACTID = ? and T_DATE <= ?)");
   cmd.addParam("", RSDBP_IN,ContractID );
   cmd.addParam("", RSDBP_IN,ContractID );
   cmd.addParam("", RSDBP_IN,Date ); 
   cmd.execute();
   
  rs = TRsbDataSet(cmd);
   if (rs.movenext())
      if ((ValType(rs.clientquality) != V_UNDEF) and (rs.clientquality != 0))
         QualValue = rs.clientquality;
         stat = 0;
      end;
   end;

   return stat;
END; 

MACRO IncomeIsDefinite(Quality)
   var ЗначениеНастройки,
       retVal = false;

   if ((Quality == 1) or (Quality == 2))
      retVal = true;
   elif ((Quality == 4) or (Quality == 5))
      retVal = false;
   elif (Quality == 3)
      ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CATEG3, @ЗначениеНастройки);            
      if (ЗначениеНастройки)             
         retVal = true;
      else             
         retVal = false;
      end;
      
   end;
   return retVal;
END;


MACRO GetAccountFromList(ContractID, AccTypeID, Account:@string, 
                          CatID:@integer, FIID:@integer, Chapter:@integer)
   var cmd,rs,
   stat = 0;

   cmd = RSDCommand ("select T_ACCOUNT,T_CATID,T_FIID,T_CHAPTER from DPRCCNTACC_DBT "
                   + "where T_CONTRACTID = ? and T_ACCTYPEID = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, AccTypeID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      if (ValType(rs.account) != V_UNDEF)
         Account = rs.account;
      else
         Account = "";
      end;
      if (ValType(rs.catid) != V_UNDEF)
         CatID = rs.catid;
      end;
      if ((ValType(rs.fiid) != V_UNDEF) and (rs.fiid != -1))
         FIID = rs.fiid;
      end;
      if ((ValType(rs.chapter) != V_UNDEF) and (rs.chapter != 0))
         Chapter = rs.chapter;
      end;
   else
      Account = "";
   stat = 1;
   end;
   
   return stat;
END;



MACRO GetCntSchedID(ContractID, ScheduleKind, Date)
   var cmd,rs,
   stat = 0,
   ID = 0;

   cmd = RSDCommand ("select T_CNTSCHEDID from DPRCCNTSCHED_DBT where T_CONTRACTID = ? "
         + "and T_SCHEDULEKIND = ? and T_DATE = ? and T_SPECIALTYPE = 0");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, ScheduleKind );
   cmd.addParam( "", RSDBP_IN, Date );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      ID = rs.cntschedid; 
   end;

   return ID;
END;


// Определяеем, существует ли период начисления с соотв. периодом 
MACRO CheckForEqualPeriod(ContractID, BeginDate, EndDate);
var cmd,rs,cmd2,rs2;

   cmd = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
   "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODBEGINDATE = ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
       cmd2 = RSDCommand ("select T_DATE from DPRCCNTSCHED_DBT " +
      "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODENDDATE = ?");
      cmd2.addParam( "", RSDBP_IN, ContractID );
      cmd2.addParam( "", RSDBP_IN, EndDate );
      cmd2.execute(); 
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())
         return true;
      end;
   end;

   return false;
END; 


// Определение даты последнего понижения качества 
PRIVATE MACRO GetLastDateDecreaseQual(ContractID, ChainBeginDate, ChainEndDate, DateDecr:@date)
   var cmd,rs,
       QualRec,
       PrevIncome = true;

   DateDecr = ChainBeginDate;
   
   cmd = RSDCommand ("select T_CLIENTQUALITY, T_DATE from DPRCCNTQUAL_DBT " +
                      "where T_CONTRACTID = ? and T_DATE >= ? and T_DATE <= ?" +
                      "order by T_DATE ASC");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, ChainBeginDate );
   cmd.addParam( "", RSDBP_IN, ChainEndDate );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   while (rs.movenext())
      QualRec = rs.clientquality; 
      if (IncomeIsDefinite(QualRec) == true)
         PrevIncome = true;
      else
         if (PrevIncome == true)
            DateDecr = rs.date;
         end;
         PrevIncome = false;
      end;
   end;

   return 0;  
END;


// Определение схемы проводок
MACRO PrcDefineTransSchema(ContractID, CntSchedID, DocDate, Schema:@integer, 
                            SumChargeFact:@money, SumChargeBalance:@money,SumChargeNonBalance:@money)
      var   cmd,rs,cmd2,rs2,
            ЗначениеНастройки,
            ChainBeginDate = "",
            ChainEndDate = "",
            stat = 0,
            BeginQual = 0,
            EndQual = 0,
            CurrQual = 0,
            DateDecr,
            prccontract = TBfile("prccontract.dbt"),
            cntsched = TBfile("prccntsched.dbt"),
            prccntopr = TBfile("prccntopr.dbt"),
            ResKind = 0;

      SumChargeFact = money(0);
      SumChargeBalance = money(0);
      SumChargeNonBalance = money(0);

      prccontract.KeyNum = 0;  
      prccontract.rec.ContractID = ContractID;
      if (not prccontract.GetEQ)
            return 1;
      end;

      ResKind = prccontract.rec.ResKind;

      cntsched.KeyNum = 0; 
      cntsched.rec.CntSchedID = CntSchedID;

      if (not cntsched.GetEQ)
            return 1;
      end; 

      // определяем даты начала и окончания цепочки периодов начисления 
      // и фактически начисленную сумму
      if (CheckForEqualPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate))
            cmd = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE, " +
                  "T_PERIODBEGINDATE, T_PERIODENDDATE from DPRCCNTSCHED_DBT " +
                  "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODBEGINDATE " +
                  ">= ? and T_PERIODENDDATE <= ?");
            cmd.addParam( "", RSDBP_IN, ContractID );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
      else         
            cmd = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE, " +
                  "T_PERIODBEGINDATE, T_PERIODENDDATE from DPRCCNTSCHED_DBT " +
                  "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and " +
                  "((T_SPECIALTYPE <> 1 and T_DATE >= ? and T_DATE <= ?) or " +
                  "(T_SPECIALTYPE = 1  AND T_PERIODBEGINDATE >= ? and T_PERIODENDDATE <= ?))");
            cmd.addParam( "", RSDBP_IN, ContractID );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
            cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
      end;      
   
      cmd.execute();
      rs = TRsbDataSet(cmd);
      
      while (rs.movenext())         
            if ((ValType(rs.DateReal) != V_UNDEF) and (rs.DateReal != date(0,0,0)) and (valtype(rs.sumfact) != V_UNDEF))
                  SumChargeFact = SumChargeFact + money(rs.sumfact);
            end;   

            if ((ChainBeginDate == "") or (rs.periodbegindate < ChainBeginDate))
                  ChainBeginDate = rs.periodbegindate;
            end; 

            if ((ChainEndDate == "") or (rs.periodenddate > ChainEndDate))
            ChainEndDate = rs.periodenddate;
            end;         
      end;
 
      // привлечение
      if (ResKind == PRC_RESKIND_PRIV)
            if (Schema == PRC_ACCSCHEMA_NO_ADDCH)
                  Schema = PRC_ACCSCHEMA_PRIV;
            else
                  Schema = PRC_ACCSCHEMA_PRIV_ADDCH;
            end;         
      end;
   
      // размещение
      if (ResKind == PRC_RESKIND_RAZM)
            Schema = 0;         
            if ({curdate} < ДАТА_ВВОДА)
                  stat = PrcGetClientQualityOnDate(ContractID, ChainBeginDate, @BeginQual);
                  if (stat)
                        return stat;
                  end;
            
                  stat = PrcGetClientQualityOnDate(ContractID, ChainEndDate, @EndQual);
                  if (stat)         
                        return stat;
                  end;
            
                  if ((SumChargeFact > 0) and (IncomeIsDefinite(BeginQual) == true) and (IncomeIsDefinite(EndQual) == false))
                        GetLastDateDecreaseQual(ContractID, ChainBeginDate, ChainEndDate, @DateDecr);

                        if (CheckForEqualPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate))
                              cmd2 = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE from DPRCCNTSCHED_DBT " +
                                    "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODBEGINDATE " +
                                    ">= ? and T_PERIODENDDATE <= ? and T_DATEREAL < ?");
                              cmd2.addParam( "", RSDBP_IN, ContractID );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
                              cmd2.addParam( "", RSDBP_IN, DateDecr );
                        else
                              cmd2 = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE from DPRCCNTSCHED_DBT " +
                                    "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and T_DATEREAL < ? and " +
                                    "((T_SPECIALTYPE <> 1 and T_DATE >= ? and T_DATE <= ?) or " +
                                    "(T_SPECIALTYPE = 1  AND T_PERIODBEGINDATE >= ? and T_PERIODENDDATE <= ?))");
                              cmd2.addParam( "", RSDBP_IN, ContractID );
                              cmd2.addParam( "", RSDBP_IN, DateDecr );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
                              cmd2.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
                        end;      
      
                        cmd2.execute();
                        rs2 = TRsbDataSet(cmd2);
            
                        while (rs2.movenext())         
                              if ((ValType(rs2.DateReal) != V_UNDEF) and (rs2.DateReal != date(0,0,0)) and (valtype(rs2.sumfact) != V_UNDEF))
                                    SumChargeBalance = SumChargeBalance + money(rs2.sumfact);
                              end;    
                        end;

                        SumChargeNonBalance = SumChargeFact - SumChargeBalance;
                        Schema = PRC_ACCSCHEMA_RAZM_BAL_NONBAL;
                  else
                        stat = PrcGetClientQualityOnDate(ContractID, DocDate, @CurrQual);
                        if (stat)         
                              return stat;
                        end;
            
                        if ((CurrQual == 1) or (CurrQual == 2))
                              Schema = PRC_ACCSCHEMA_RAZM_BAL;
                        end;

                        if ((CurrQual == 4) or (CurrQual == 5))
                              Schema = PRC_ACCSCHEMA_RAZM_NONBAL;
                        end;

                        if (CurrQual == 3)
                              if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CATEG3, @ЗначениеНастройки) != 0)
                                    return 1;
                              end;  

                              if (ЗначениеНастройки)             
                                    Schema = PRC_ACCSCHEMA_RAZM_BAL;
                              else             
                                    Schema = PRC_ACCSCHEMA_RAZM_NONBAL;
                              end;
                        end;
                  end;

                  if (DocDate > cntsched.rec.DelayDate)
                        if (Schema == PRC_ACCSCHEMA_RAZM_BAL)
                              Schema = PRC_ACCSCHEMA_RAZM_BAL_OD;
                        end;

                        if (Schema == PRC_ACCSCHEMA_RAZM_NONBAL)
                              Schema = PRC_ACCSCHEMA_RAZM_NONBAL_OD;
                        end;

                        if (Schema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL)
                              Schema = PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD;
                        end;
                  end;
            //Если дата операционного дня больше значения настройки 
            else
                  if (DocDate > cntsched.rec.DelayDate)
                        Schema = PRC_ACCSCHEMA_RAZM_BAL_OD; // Размещение.Баланс.Просрочка
                  else
                        Schema = PRC_ACCSCHEMA_RAZM_BAL; // Размещение.Баланс
                  end;
            end;
      end;

      return 0;
end;

// 
MACRO PrcSendMessage(ContractID, Message)

   var cmd,rs;

   if (IsOprMultiExec())   //
      Message = substr(Message, 1, 2000);
      cmd = RSDCommand ("update DPRCSRVACC_TMP set T_COMENT = ? where T_CONTRACTID = ?");
      cmd.addParam( "", RSDBP_IN, Message );
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.execute();           
   else                    // 
      msgbox(Message);
   end;

   return 0;
END;

// Формирование КТПС
MACRO GenerateRatesTable(ContractID, BeginDate, EndDate)
   var   cmd, cmd_fl1, cmd_fl1_0, cmd_fl1_1, cmd_fl1_2, cmd_fl1_3, cmd2, cmd_fl2, cmd_fl3, rs, c_sqlq,
         RateID   = 0,
         RateType = 0,
         ID = ContractID,
         stat = 0;

   cmd = RSDCommand ("select rate.T_RATEID, rate.T_RATETYPE " +
                      "from DPRCRATE_DBT rate, DPRCCONTRACT_DBT cnt " +
                      "where cnt.T_RATEID = rate.T_RATEID AND cnt.T_CONTRACTID = ?");
   cmd.addParam( "", RSDBP_IN, ID );
   cmd.execute();
  
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      RateType = rs.ratetype;
      RateID   = rs.rateid;   
   else
      return 1;
   end;

   // Если ставка абсолютная - то ничего не делаем
   if (RateType == PRC_RATETYPE_ABSOLUTE)
      return 0;
   end;
   
   // Если ставка плавающая - то запускаем расчет ставки через ХП
   if (RateType == PRC_RATETYPE_FLOATING)
   
      cmd_fl1 = RSDCommand ("delete from DPRCCNTRATE_TMP where T_CONTRACTID = ? and T_RATEDATE >= ? and  T_RATEDATE <= ?" );
      cmd_fl1.addParam("", RSDBP_IN,  ID);
      cmd_fl1.addParam("", RSDBP_IN,  BeginDate);
      cmd_fl1.addParam("", RSDBP_IN,  EndDate); 
      cmd_fl1.execute();

    // dprccontract_dbt dprccontract_isrv_tmp с ContractID
    // dprcrateval_dbt  dprcrateval_isrv_tmp

      cmd_fl1_0 = RSDCommand ("DELETE FROM dprccontract_isrv_tmp  WHERE t_contractid = ?" );
      cmd_fl1_0.addParam("", RSDBP_IN,  ID);
      cmd_fl1_0.execute();

      c_sqlq = "INSERT INTO dprccontract_isrv_tmp (t_contractid, t_chapter, t_fiid, t_account, t_calcid, t_rateid, t_begindate, " +
               "                                   t_enddate, t_nextdatecalc, t_nextdatecharge, t_nextdatepay, t_nextdateuser) "  +
               "SELECT t_contractid, t_chapter, t_fiid, t_account, t_calcid, t_rateid, t_begindate, "                             +
               "       t_enddate, t_nextdatecalc, t_nextdatecharge, t_nextdatepay, t_nextdateuser "                               +
               "  FROM dprccontract_dbt "                                                                                         +
               " WHERE t_contractid = ? ";

      cmd_fl1_1 = RSDCommand (c_sqlq);
      cmd_fl1_1.addParam("", RSDBP_IN,  ID);
      cmd_fl1_1.execute();

      cmd_fl1_2 = RSDCommand ("DELETE FROM dprcrateval_isrv_tmp WHERE t_rateid = ?");
      cmd_fl1_2.addParam("", RSDBP_IN,  RateID);
      cmd_fl1_2.execute();

      c_sqlq = "INSERT INTO dprcrateval_isrv_tmp (t_rateid, t_ratedate, t_rate, t_variableperiod, t_variabledays) " +
               "SELECT t_rateid, t_ratedate, t_rate, t_variableperiod, t_variabledays                             " +
               "  FROM dprcrateval_dbt                                                                            " +
               " WHERE t_rateid = ?                                                                               ";  

      cmd_fl1_3 = RSDCommand (c_sqlq);
      cmd_fl1_3.addParam("", RSDBP_IN,  RateID);
      cmd_fl1_3.execute();

      cmd2 = RSDCommand( "{ ? = call RSB_PERCENT.PRC_CALCRATETABLEFORFR (?,?,? ) }" );
      cmd2.addParam( "retval", RSDBP_OUT, V_INTEGER );
      cmd2.addParam( "", RSDBP_IN, ID );
      cmd2.addParam( "", RSDBP_IN, BeginDate );
      cmd2.addParam( "", RSDBP_IN, EndDate );
      cmd2.execute();

      if (cmd2.value("retval") != 0)
         return 1;
      end;

      // сохраняем данные в основной таблице
      cmd_fl2 = RSDCommand ("delete from DPRCCNTRATE_DBT where T_CONTRACTID = ? and T_RATEDATE >= ? and  T_RATEDATE <= ?" );
      cmd_fl2.addParam("", RSDBP_IN,  ID);
      cmd_fl2.addParam("", RSDBP_IN,  BeginDate);
      cmd_fl2.addParam("", RSDBP_IN,  EndDate);
      cmd_fl2.execute();

      cmd_fl3 = RSDCommand ("insert into DPRCCNTRATE_DBT select * from DPRCCNTRATE_TMP " +
                            "where T_CONTRACTID = ? and T_RATEDATE >= ? and  T_RATEDATE <= ?" );
      cmd_fl3.addParam("", RSDBP_IN,  ID);
      cmd_fl3.addParam("", RSDBP_IN,  BeginDate);
      cmd_fl3.addParam("", RSDBP_IN,  EndDate);
      cmd_fl3.execute();
   end;

   return 0;
END;// end macro

MACRO GetPrevRoundSumDelta( ID, ScheduleKind,  beg_date ): money
   var cmd,rs,RoundSumDelta = $0;

   cmd = RSDCommand (" SELECT NVL(t_RoundSumDelta, 0) AS RoundSumDelta " +
                     "   FROM dprccntsched_dbt " +
                     "  WHERE t_ContractID = ? "+
                     " AND t_ScheduleKind = ? " +
                     " AND t_PeriodBeginDate = " +
                     "  (SELECT MAX (t_PeriodBeginDate) " +
                     "     FROM dprccntsched_dbt " +
                     "    WHERE t_ContractID = ? " +
                     "       AND t_ScheduleKind = ? " +
                     "       AND t_PeriodBeginDate < ?) ");
   cmd.addParam( "", RSDBP_IN, ID );
   cmd.addParam( "", RSDBP_IN, ScheduleKind );
   cmd.addParam( "", RSDBP_IN, ID );
   cmd.addParam( "", RSDBP_IN, ScheduleKind );
   cmd.addParam( "", RSDBP_IN, beg_date );

   cmd.execute();

   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      RoundSumDelta = money(rs.roundsumdelta);
   end;

   return RoundSumDelta;
END;// end macro


