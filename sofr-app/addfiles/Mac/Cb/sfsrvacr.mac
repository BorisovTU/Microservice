/*
Макрос выполнения шага "Начисление" сервисной операции сопровождения ДО
*/

Import InsCarryDoc, "sfsrv_lib.mac";

record SfContr("sfcontr.dbt");
record SfConComRec("sfconcom.dbt");
record SfComissRec("sfcomiss.dbt");

record SfSrvDocRec("sfsrvdoc.dbt");

macro ExecuteStep( outBuff, primDoc )

  var stat = 0;

  SetBuff( SfContr, primDoc );  
  
  record SfDefCom("sfdef.dbt");
  record SfAccrue("sfaccrue.dbt");

  var PayerAccount = "", ReceiverAccount = "";
  var NDSPayerAccount : string, NDSReceiverAccount : string;

  var prevFIID_CommSum = -1;
  var PayerFIID = -1, ReceiverFIID = -1;

  var SfConComPD : SfConComPrimDoc;
  var IsNVPI:bool;

  var AccTrnID = 0, AccTrnID_NDS = 0;

  var Action = DEFCOM_ACTIONKIND_ACRUE;
  var Comment = "";

  var rs, cmd;
  cmd = RsdCommand( " SELECT * FROM DSFDEFTMP_TMP WHERE t_Action = ? AND t_AcrAmount > 0 AND t_Skip = chr(0) " );
  cmd.addParam( "", RSDBP_IN, Action );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )

    SfSrv_FillSfDefComRecord( rs, SfDefCom );
    SfSrv_FillSfAccrueRecord( rs, SfAccrue );

    if( prevFIID_CommSum != SfDefCom.FIID_Sum )
      prevFIID_CommSum = SfDefCom.FIID_Sum;
      IsNVPI = SfSrv_IsNVPICarry( SfContr.PayFIID, SfDefCom.FIID_Sum, SfContr.PayRateDateKind );

      SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConComRec, SfContr, SfDefCom.FIID_Sum);

      stat = SfSrv_GetCarryParms( IsNVPI, SfConComPD, SfDefCom, SfAccrue, SfContr, 
                                  @PayerFIID, @ReceiverFIID,
                                  @PayerAccount, @ReceiverAccount, @NDSPayerAccount, @NDSReceiverAccount, 0, 0 );
    end;
    
    if( stat == 0 )
      stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContr, SfConComRec, 
                               PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                               @AccTrnID, @AccTrnID_NDS, 0, 0 );
    end;

    if( (stat == 0) AND (SfDefCom.Status != SFDEFCOM_STATUS_ACCRUAL) )
      ChangeSfDefTmpStatus( SfDefCom, SFDEFCOM_STATUS_ACCRUAL );
    end;

    if( (stat == 0) AND ((SfAccrue.Amount != 0) OR (SfAccrue.NDSAmount != $0)) )
      stat = SfSrv_AddSfDocs( Action, SfDefCom, AccTrnID, AccTrnID_NDS );
    end;
    
    SfSrv_InsertAccrueLogRec( SfDefCom, SfAccrue, PayerAccount, ReceiverAccount, 0, stat, Comment );
    
  end;

  return stat;

end;

macro PostStepAction( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                primDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

   if( errTrn and (message == OP_EXECUTE_STEP) ) 
       /* Ошибка при выполнении шага */
     SetBuff( SfContr, primDoc );
       SfSrv_UpdateAccrueLogRec( SfContr, errTrn );
   end;   
   
   return 0;
end;

macro MassExecuteStep()

  var stat = 0;
  
  record SfDefCom("sfdef.dbt");
  record SfAccrue("sfaccrue.dbt");

  file SfContrFile( sfcontr ) key 0;
  file SfConComFile( sfconcom ) key 2;

  var PayerAccount = "", ReceiverAccount = "";
  var NDSPayerAccount : string, NDSReceiverAccount : string;

  var prevConID = -1, prevFIID_CommSum = -1, ConComID = 0, prevConComID = -1;
  var PayerFIID = -1, ReceiverFIID = -1;

  var SfConComPD : SfConComPrimDoc;
  var IsNVPI:bool, bNeedNewParms:bool = false;

  var AccTrnID = 0, AccTrnID_NDS = 0;

  var Action = DEFCOM_ACTIONKIND_ACRUE;
  var Comment = "";
  var ID_Operation = 0, ID_Step = 0;

  var rs, cmd;
  var StrQuery = "";

  stat = SfDefTmp_TransferToReal( false );

  if( stat == 0 )
    StrQuery = " SELECT * FROM DSFDEFTMP_TMP WHERE t_Action = ? AND t_AcrAmount > 0 AND t_Skip = chr(0) " +
               " ORDER BY t_ConID, t_ConComID, t_FIID_CommSum ";

    cmd = RsdCommand( StrQuery );
    cmd.addParam( "", RSDBP_IN, Action );

    rs = RsdRecordset( cmd );
    while( rs.moveNext() )

      SfSrv_FillSfDefComRecord( rs, SfDefCom );
      SfSrv_FillSfAccrueRecord( rs, SfAccrue );

      ConComID = rs.value("t_ConComID");
      
      ID_Operation = rs.value("t_ID_Operation");
      ID_Step = rs.value("t_ID_Step");    

      if( prevConID != SfDefCom.SfContrID )
        prevConID = SfDefCom.SfContrID;
        SfContrFile.ID = SfDefCom.SfContrID;
        if( not getEQ( SfContrFile ) )
          Comment = "Не найден ДО с ID = " + SfDefCom.SfContrID;
          stat = 1;
        end;
      end;

      if( stat == 0 )
        if( prevConComID != ConComID )
          prevConComID = ConComID;
          prevFIID_CommSum = SfDefCom.FIID_Sum;
          bNeedNewParms = true;
          SfConComFile.ID = ConComID;
          if( not getEQ( SfConComFile ) )
            Comment = "Не найдена комиссия для ДО с ID = " + ConComID;
            stat = 1;
          end;
        elif( prevFIID_CommSum != SfDefCom.FIID_Sum )
          prevFIID_CommSum = SfDefCom.FIID_Sum;
          bNeedNewParms = true;
        end;
      end;
        
      if( stat == 0 )
        if( bNeedNewParms )
          bNeedNewParms = false;
          IsNVPI = SfSrv_IsNVPICarry( SfContrFile.PayFIID, SfDefCom.FIID_Sum, SfContrFile.PayRateDateKind );
                  
          SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConComFile, SfContrFile, SfDefCom.FIID_Sum);

          stat = SfSrv_GetCarryParms( IsNVPI, SfConComPD, SfDefCom, SfAccrue, SfContrFile, 
                                      @PayerFIID, @ReceiverFIID,
                                      @PayerAccount, @ReceiverAccount, @NDSPayerAccount, @NDSReceiverAccount,
                                      ID_Operation, ID_Step );
        end;
      end;
      
      if( stat == 0 )
        stat = SfSrvCarryAccrue( PayerFIID, ReceiverFIID, SfDefCom, SfAccrue, SfContrFile, SfConComFile, 
                                 PayerAccount, ReceiverAccount, NDSPayerAccount, NDSReceiverAccount,
                                 @AccTrnID, @AccTrnID_NDS, ID_Operation, ID_Step );
      end;

      /*if( stat == 0 )
        ChangeSfDefTmpStatus( SfDefCom, SFDEFCOM_STATUS_ACCRUAL );
      end;*/

      if( (stat == 0) AND ((SfAccrue.Amount != 0) OR (SfAccrue.NDSAmount != $0)) )
        stat = SfSrv_AddSfDocs( Action, SfDefCom, AccTrnID, AccTrnID_NDS );
      end;
      
      SfSrv_InsertAccrueLogRec( SfDefCom, SfAccrue, PayerAccount, ReceiverAccount, 0, stat, Comment );
      
    end;
  end;

  return stat;
end;

