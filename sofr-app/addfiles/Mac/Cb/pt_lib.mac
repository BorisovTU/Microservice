/*
$Name:           pt_lib.mac
$Module:         Ядро ГКБО
$Description:    Библиотека для работы с субъектами
*/
/*
   Библиотека для работы с субъектами
*/

import rsd, oralib, likepy, cb_sql;

/*
   Определить код вида 1
   - передается буфер partcode.dbt
   
   Возвращает строку - код вида 1
*/
macro GetUserCode( Partcode )

  return Partcode.Code;

end;
/*
 Получить удостоверение личности
*/

macro GetPersonIdc( PersonID, persnidc )
  
  var stat;
  var strSql : string;
  var rs : RsdRecordset;
  file fpersnidc(persnidc) key 0;

  stat = 0;

  strSql = "SELECT t_PaperKind FROM dpersnidc_dbt t WHERE t.T_PersonID = " + PersonID + " AND t.T_IsMain = 'X' ;";

  rs = RsdRecordset( strSql );
                                                                                                  
  if( rs.MoveNext() )
    fpersnidc.PersonID = PersonID;
    fpersnidc.PaperKind = rs.value(0);
    if(GetEQ(fpersnidc))
      Copy(persnidc, fpersnidc);
      stat = 1;
    end;
  end;  

  return stat;

end;   


private macro _GetFiasAddrObj_GuidOkatoOktmo(Fias : string, AOGuid : @string, Okato : @string, Oktmo : @string)
  var stat = 0;
  var query = "";
  var rs, rsokato;
  var params : TArray;
  var paramsokato : TArray;
  var Code = substr(Fias, 1, 19);
  var AOLevel = 0;
  var ParentGuid = "";

  if(substr(Code, 16, 4) != "0000")
    AOLevel = 7;
  elif(substr(Code, 12, 4) != "0000")
    AOLevel = 65;
  elif(substr(Code, 9, 3) != "000")
    AOLevel = 6;
  elif(substr(Code, 6, 3) != "000")
    AOLevel = 4;
  elif(substr(Code, 3, 3) != "000")
    AOLevel = 3;
  elif(substr(Code, 1, 2) != "00")
    AOLevel = 1;
  end;

  query = query + "SELECT ";
  query = query + "  t_AOGuid ";
  query = query + " ,t_Okato ";
  query = query + " ,t_Oktmo ";
  query = query + " ,t_ParentGuid ";
  query = query + "  FROM das_addrobj_dbt ";
  query = query + " WHERE t_AOLevel = :AOLevel ";
  query = query + "   AND t_RegionCode = :RegionCode ";
  query = query + "   AND t_AreaCode = :AreaCode ";
  query = query + "   AND t_CityCode = :CityCode ";
  query = query + "   AND t_PlaceCode = :PlaceCode ";
  query = query + "   AND t_PlanCode = :PlanCode ";
  query = query + "   AND t_StreetCode = :StreetCode ";
  query = query + "   AND t_NextID = :NextID ";

  params = makeArray(SQLParam("AOLevel", AOLevel),
                     SQLParam("RegionCode", substr(Code, 1, 2)),
                     SQLParam("AreaCode", substr(Code, 3, 3)),
                     SQLParam("CityCode", substr(Code, 6, 3)),
                     SQLParam("PlaceCode", substr(Code, 9, 3)),
                     SQLParam("PlanCode", substr(Code, 12, 4)),
                     SQLParam("StreetCode", substr(Code, 16, 4)),
                     SQLParam("NextID", "")
                    );

  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    AOGuid = rs.value(0);
    Okato = rs.value(1);
    Oktmo = rs.value(2);
    ParentGuid = rs.value(3);
  else
    stat = 1;
  end;

  if((stat == 0) and (Okato == ""))
    query = "";
    query = query + "SELECT ";
    query = query + "  t_Okato ";
    query = query + "  FROM das_addrobj_dbt ";
    query = query + " WHERE t_AOGuid = :ParentGuid ";
    query = query + "   AND t_NextID = :NextID ";

    paramsokato = makeArray(SQLParam("ParentGuid", ParentGuid),
                            SQLParam("NextID", "")
                           );

    rsokato = execSQLselect(query, paramsokato, true);
    if(rsokato and rsokato.moveNext())
      Okato = rsokato.value(0);
    else
      stat = 1;
    end;
  end;

  return stat;
end;

private macro _GetFiasHouse_GuidOkatoOktmo(Fias : string, HouseGuid : @string, Okato : @string, Oktmo : @string)
  var stat = 0;
  var query = "";
  var rs;
  var params : TArray;
  var Code = substr(Fias, 1, 23);
  var AOGuid = "";
  
  stat = _GetFiasAddrObj_GuidOkatoOktmo(Fias, @AOGuid, @Okato, @Oktmo);

  if(not stat)
    query = query + "SELECT ";
    query = query + "  t_HouseGuid ";
    query = query + " ,t_Okato ";
    query = query + " ,t_Oktmo ";
    query = query + "  FROM das_house_dbt ";
    query = query + " WHERE t_AOGuid = :AOGuid ";
    query = query + "   AND TRIM(TO_CHAR(t_Counter, '0000')) = :Counter ";
    query = query + " ORDER BY t_EndDate DESC ";
    
    params = makeArray(SQLParam("AOGuid", AOGuid),
                       SQLParam("Counter", substr(Code, 20, 4))
                      );

    rs = execSQLselect(query, params, true);
    if(rs and rs.moveNext())
      HouseGuid = rs.value(0);
      Okato = rs.value(1);
      Oktmo = rs.value(2);
    else
      stat = 1;
    end;                         
  end;
  return stat;
end;


macro GetOkatoByKladr(Fias : string, Okato : @string, Oktmo : @string) : string
  var stat = 0;
  var retVal = "noKladr";
  var AOGuid = "";
  var HouseGuid = "";

  if(strlen(Fias) >= 23)
    if(substr(Fias, 20, 4) != "0000")
      stat = _GetFiasHouse_GuidOkatoOktmo(Fias, @HouseGuid, @Okato, @Oktmo);
    else
      stat = _GetFiasAddrObj_GuidOkatoOktmo(Fias, @AOGuid, @Okato, @Oktmo);
    end;

    if(not stat)
      retVal = Okato;
    else
      Okato = retVal;
      Oktmo = retVal;
    end;
  end;

  return retVal;

OnError(err);
  return retVal;
end;

