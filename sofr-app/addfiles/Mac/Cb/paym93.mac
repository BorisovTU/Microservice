/* Макрос печати результатов проверки заполнения ответных документов
   в соответствии с требованиями Инструкции № 93-И */
IMPORT BankInter, PtInter, FIInter, CTInter, "adress.mac", globals, "pt_lib.mac";

private record bank    (party); /* "Наш" банк */
private record pt      (party); /* банк, из которого пришел неверно заполненный документ */
private record RCC     (party); /* РКЦ,  через который работает банк */

private file   bankdprt(bankdprt);
private file   a       (account);

private ARRAY PARTYNAME, PARTYADDR;

private CONST NATCUR = 0;
private CONST ТипЗаграничногоАдреса = 101;
private CONST NoteKind_ИсточникПолученияВалюты      = 1;
private CONST NoteKind_МаксимальныйОбъемПоступлений = 2;

private CONST НастраиваемоеПримечание = "";
private CONST ALLCORSCHEMNUMBER = -1;

MACRO PrintMessage(text)

private ARRAY MES;
StrSplit(text, MES, 70);
[];
[#######################################################################](MES(0));
if(ValType(MES(1)) != V_UNDEF)
[#######################################################################](MES(1));
end;

END;

/* Печать ошибочного документа */
MACRO PrintDocument(_date,account,DocNumber,sum,text)

[##########. Документ по счету ######################### N #### на сумму #
             ################################################################################]
(_date,account:f,DocNumber,sum:a:f,text);

END;

/* Печать перехода на новую дату */
MACRO PrintNewDate(_date)

[Дата ##########](_date);

END;

/* Печать строки об отсутствии документов за дату */
MACRO PrintNoDoc(beginDate,endDate,ОтветныеДокументы)

var ВидДокументов = "Начальные";
if(ОтветныеДокументы) ВидДокументов = "Ответные"; end;
if(beginDate == endDate)
[######### документы по счетам типа "К","Н","Ф" за дату ########## не найдены](ВидДокументов,beginDate);
else
[######### документы по счетам типа "К","Н","Ф" c ########## по ########## не найдены](ВидДокументов,beginDate,endDate);
end;

END;

/* Печать строки об отсутствии ошибочных документов за дату */
MACRO PrintNoWrongDoc(_date, ОтветныеДокументы)
var ВидДокументов = "начальных";
if(ОтветныеДокументы) ВидДокументов = "ответных"; end;
[Ошибок в оформлении ######### документов по счетам типа "К","Н","Ф" за дату ########## не найдено](ВидДокументов,_date);

END;

/* Печать сообщения о превышении лимита за день */
MACRO PrintWrongAccDay(_date,account)

[##########. Счет N #########################](_date,account:f);
PrintMessage("Ошибка! Кредитовый оборот по счету за день превышает сумму лимита. Необходимо сформировать уведомление");

END;

/* Печать сообщения о превышении лимита за месяц */
MACRO PrintWrongAccMont(НаименованиеМесяца,Год,account,mes)

[######## #### г. Счет N #########################](НаименованиеМесяца,Год,account:f);
if(mes == "")
PrintMessage("Ошибка! Кредитовый оборот по счету за месяц превышает заданный максимум. Необходимо сформировать уведомление");
else
PrintMessage(mes);
end;

END;

MACRO НайтиРКЦ(НомерКорсхемы)

  private file RCC_partcode (partcode) key 1;
  private file corschem     (corschem) key 1; /* Ключ по номеру корсхемы */

  if(НомерКорсхемы != ALLCORSCHEMNUMBER)
    corschem.Number  = НомерКорсхемы;
    corschem.FI_Kind = FIKIND_CURRENCY;
    corschem.FIID    = NATCUR;
    if(not getEQ(corschem))
      msgbox("Не найдена корсхема");
      return FALSE;
    end;
    if(ПолучитьСубъекта(corschem.CorrID,RCC) != 0)
      msgbox("Не найден РКЦ");
      return FALSE;
    end;
  /* Ищем РКЦ, через который работает "Наш" банк*/
  else
    bankdprt.PartyID = {OurBank};
    if(not getEQ(bankdprt))
      msgbox("Не найден \"Наш\" банк");
      return FALSE;
    end;
    RCC_partcode.Code     = bankdprt.BIC_RCC;
    RCC_partcode.CodeKind = PTCK_BIC;
    if(not getEQ(RCC_partcode))
      msgbox("Не найден РКЦ");
      return FALSE;
    end;
    if(ПолучитьСубъекта(RCC_partcode.PartyID,RCC) != 0)
      msgbox("Не найден РКЦ");
      return FALSE;
    end;
  end;
  return TRUE;
END;

MACRO ФИОИсполнителя(НомерИсполнителя)

  file Исполнитель(person);     /* Операционист, запустивший процедуру */

  Исполнитель.Oper = НомерИсполнителя;
  if(not getEQ(Исполнитель))
    msgbox("Не найден операционист");
    return "";
  end;
  return Исполнитель.Name;
END;

MACRO НайтиСчет(НомерСчета)
  a.Account = НомерСчета;
  a.Chapter = 1;
  if(not getEQ(a))
    msgbox("Не найден счет");
    return FALSE;
  end;
  return TRUE;
END;

MACRO ФИОГлавногоБухгалтера()

  file ГлавБух(officer) key 2;  /* Главный бухгалтер */
  record Сотрудник(party);

  ГлавБух.PartyID = {OurBank};
  ГлавБух.IsSecondPerson = "X";
  if(not getEQ(ГлавБух))
    msgbox("Не найден главный бухгалтер");
    return "";
  end;
  if( (ПолучитьСубъекта(ГлавБух.PersonID,Сотрудник) != 0) )
    msgbox("Не найден субъект");
    return "";
  end;
  return Сотрудник.ShortName;
END;

MACRO ПечатьЗаголовка(RCC,кодОКПО,_date)
record RecordAdress ( adress  );
ClearRecord(RecordAdress);
НайтиЮридическийАдресСубъекта(RCC.PartyID,RecordAdress);
[ ];
[ ];
[                                                      наименование     ];
[                                                    территориального   ];
[                                                       учреждения      ];
[                                                      Банка России     ];
[                                               ########################](RCC.Name:wl);
[                                                                       ];
[                                                         почтовый адрес];
[                                               ########################](RecordAdress.Adress:wl);
[                                                                       ];
[     код ОКПО кредитной организации и дата                             ];
[     N ###############                                                 ](кодОКПО);
[     ########################                                          ](_date:m);
[                                                                       ];
[                                           УВЕДОМЛЕНИЕ                 ];
[                                                                       ];
END;

MACRO ПечататьПодвала()

[ ];
[     Главный бухгалтер     ##########################(Ф.И.О.)](ФИОГлавногоБухгалтера());
[ ];
[                        М.П.];
[     Исполнитель           ##########################(Ф.И.О.)](ФИОИсполнителя({oper}));
[ ];
[     Телефон               __________________________];
END;

/* Печать уведомления в РКЦ об отсутствии кода операции в документе */
MACRO ОтсутствиеКодаОперации(BankID,CorschemNum,_date)
/* BankID   - идентификатор банка, из которого пришел документ */
/* Corschem - идентификатор корсхемы, по которой был загружен документ */
  record RecordAdress ( adress  );
  
  if( (ПолучитьСубъекта(BankID,pt) != 0) or (ПолучитьСубъекта({OurBank},bank) != 0) )
    msgbox("Не найден банк");
    return FALSE;
  end;
  if(not НайтиРКЦ(CorschemNum))
    return FALSE;
  end;

StrSplit(pt.Name,     PARTYNAME, 68, 12);
ClearRecord(RecordAdress);
НайтиЮридическийАдресСубъекта(pt.PartyID,RecordAdress);
StrSplit(RecordAdress.Adress,  PARTYADDR, 41);

ПечатьЗаголовка(RCC,bank.OKPO,_date);
[        Настоящим уведомляем, что от кредитной организации ############](PARTYNAME(0));
if(ValType(PARTYNAME(1)) != V_UNDEF)
[     ##################################################################](PARTYNAME(1):w);
end;
[     -----------(полное наименование кредитной организации)------------];
[     расположенной по адресу: #########################################](PARTYADDR(0));
if(ValType(PARTYADDR(1)) != V_UNDEF)
[     ##################################################################](PARTYADDR(1));
end;
[     в нарушение пункта 4.7 Инструкции Банка России "О порядке открытия];
[     уполномоченными  банками  счетов  нерезидентов в валюте Российской];
[     Федерации и проведения  расчетов  в  валюте  Российской  Федерации];
[     между резидентами и нерезидентами" поступил расчетный документ без];
[     указания  в  поле  "Назначение  платежа"   цифрового   обозначения];
[     операции.];
ПечататьПодвала();
END;

/* Печать отчета "Информационная справка" по счету физического лица-нерезидента */
MACRO ПечататьИнформационнойСправки(Нерезидент,НашБанк,Счет)
/* Нерезидент - владелец счета - запись из party.dbt */
/* НашБанк - запись из party.dbt */
/* Счет    - запись из account.dbt */

file partcode (partcode); /* Ключ по номеру корсхемы */
file country  (country,"rsrfdb.def") key 2;  /* Страна нерезидента - Ключ по трехбуквенному код(лат.) */
file paprkind (paprkind); /* Ключ по виду документа */
file persn    (persn);
file adress   (adress);
file note     (notetext);
file partyreg (partyreg);   /* сведения о регистрации */

record persnidc("persnidc.dbt");
record PtRegParty(party); /* регистрирующий орган */
record RecordAdress ( adress  );

ARRAY TEXT_ИСТОЧНИК, TEXT_ПРИМЕЧАНИЕ;

var ИсточникПолученияВалюты = "";
var МаксимальныйОбъемПоступлений = $0;

partcode.PartyID  = НашБанк.PartyID;
partcode.CodeKind = PTCK_BIC;
if(not getEQ(partcode))
  msgbox("Не найден БИК банка");
end;
var NumInList;
if(not GetMainObjAttr(null,OBJTYPE_ACCOUNT,Счет,OBJ_ACCOUNT_GROUP_KIND,null,null,NumInList))
  msgbox("Не найдено значение категории счета");
END;
country.CodeLat3 = Нерезидент.NRCountry;
if(not getEQ(country))
  msgbox("Не найдена страна нерезидента");
end;
persn.PersonID = Нерезидент.PartyID;
if(getEQ(persn))
  GetPersonIdc(persn.PersonID , persnidc);
  paprkind.PaperKind = persnidc.PaperKind;
  if(not getEQ(paprkind))
    msgbox("Не найден вид документа, удостоверяющего личность");
  end;
else
  msgbox("Не найдено физическое лицо");
end;
adress.PartyID = Нерезидент.PartyID;
adress.Type    = ТипЗаграничногоАдреса;
if(not getEQ(adress))
  msgbox("Не найден адрес за границей");
end;

/*note.ObjectType = OBJTYPE_ACCOUNT;
note.DocumentID = makeObjectID(OBJTYPE_ACCOUNT,null,Счет);
note.NoteKind   = NoteKind_ИсточникПолученияВалюты;
if(getEQ(note))
  ИсточникПолученияВалюты = note.Text;
else
  msgbox("Не найдено примечание по источнику получения валюты|для счета " + Счет.Account);
end;*/
ИсточникПолученияВалюты = readNoteForObject( OBJTYPE_ACCOUNT,
                                             makeObjectID(OBJTYPE_ACCOUNT,null,Счет),
                                             NoteKind_ИсточникПолученияВалюты);
/*note.NoteKind   = NoteKind_МаксимальныйОбъемПоступлений;
if(getEQ(note))
  МаксимальныйОбъемПоступлений = note.SumNote;
else
  msgbox("Не найдено примечание по максимальному объему поступлений|для счета " + Счет.Account);
end;*/
МаксимальныйОбъемПоступлений = readNoteForObject( OBJTYPE_ACCOUNT,
                                                  makeObjectID(OBJTYPE_ACCOUNT,null,Счет),
                                                  NoteKind_МаксимальныйОбъемПоступлений);

var i = 0;
StrSplit(ИсточникПолученияВалюты,TEXT_ИСТОЧНИК,32);
while(i < 3)
  if(ValType(TEXT_ИСТОЧНИК(i)) == V_UNDEF) TEXT_ИСТОЧНИК(i) = "";end;
  i = i + 1;
end;

i = 0;
StrSplit(НастраиваемоеПримечание,TEXT_ПРИМЕЧАНИЕ,32);
while(i < 2)
  if(ValType(TEXT_ПРИМЕЧАНИЕ(i)) == V_UNDEF) TEXT_ПРИМЕЧАНИЕ(i) = "";end;
  i = i + 1;
end;

partyreg.PartyID   = Нерезидент.PartyID;
partyreg.OpenClose = "";
partyreg.PartyKind = PTLIST_MID;
if(getEQ(partyreg))
  if( (ПолучитьСубъекта(partyreg.PtRegParty,PtRegParty) != 0) )
    msgbox("Не найден регистрирующий орган");
  end;
else
  msgbox("Не найдена действующая регистрация");
end;

ClearRecord(RecordAdress);
НайтиЮридическийАдресСубъекта(Нерезидент.PartyID,RecordAdress);

[   Наименование кредитной организации (филиала): ###########################](НашБанк.Name);
[   БИК кредитной организации: #########](partcode.Code);
[   Тип счета "#" N счета ###################################################](Счет.Type_Account:1/*NumInList*/,Счет.Account);
[ ];
[ ];
[                   А. Сведения о владельце счета];
[┌──┬────────────────────────────────────────┬────────────────────────────────┐];
[│1 │ Фамилия, имя, отчество                 │################################│](Нерезидент.Name);
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│2 │ Гражданство (подданство)               │################################│](country.Name);
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│3 │ Сведения о паспорте или ином           │################################│](paprkind.Name);
[│  │ действительном документе,удостоверяющем│################################│](persnidc.PaperSeries);
[│  │ личность физического лица - нерезидента│################################│](persnidc.PaperNumber);
[│  │                                        │выдан                           │];
[│  │                                        │################################│](persnidc.PaperIssuer);
[│  │                                        │################################│](persnidc.PaperIssuedDate);
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│4 │ Место жительства за пределами          │################################│](adress.Adress);
[│  │ Российской Федерации                   │################################│]("");
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│5 │ Место пребывания на территории         │################################│](RecordAdress.PostIndex);
[│  │ Российской Федерации                   │################################│](RecordAdress.District);
[│  │                                        │################################│](RecordAdress.Place);
[│  │                                        │################################│](RecordAdress.Adress);
[│  │                                        │ул.#############################│](RecordAdress.Street);
[│  │                                        │д.### к.### кв.####             │](RecordAdress.House,RecordAdress.NumCorps,RecordAdress.Flat);
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│6 │ Сведения о въездной визе, включая срок │Серия ##########################│](partyreg.Series);
[│  │ действия визы                          │Номер ##########################│](partyreg.Number);
[│  │                                        │выдана ########## ##############│](partyreg.RegDate,PtRegParty.Name);
[│  │                                        │дейст.с ########## по ##########│](partyreg.RegDateStart,partyreg.RegDateEnd);
[│  │                                        │Регист.номер:###################│](partyreg.RegNumber);
[└──┴────────────────────────────────────────┴────────────────────────────────┘];
[ ];
[ Б. Сведения о предполагаемых операциях по счету и источниках получения валюты];
[                             Российской Федерации];
[┌──┬────────────────────────────────────────┬────────────────────────────────┐];
[│7 │ Предполагаемый источник получения      │################################│](TEXT_ИСТОЧНИК(0));
[│  │ валюты Российской Федерации(в том числе│################################│](TEXT_ИСТОЧНИК(1));
[│  │ наличной валюты Российской Федерации   │################################│](TEXT_ИСТОЧНИК(2));
[├──┼────────────────────────────────────────┼────────────────────────────────┤];
[│8 │ Предполагаемый максимальный объем      │################################│](максимальныйобъемпоступлений);
[│  │ поступления валюты Российской Федерации│                                │];
[│  │ (в том числе наличной) на счет в       │                                │];
[│  │ течениикалендарного месяца             │                                │];
[└──┴────────────────────────────────────────┴────────────────────────────────┘];
[ ];
[                   В. Обязательства владельца счета];
[┌──┬────────────────────────────────────────┬────────────────────────────────┐];
[│9 │ _______________________________________│________________________________│];
[│  │ (фамилия, имя, отчество физического    │(подпись физического            │];
[│  │            лица - нерезидента)         │ лица - нерезидента, являющегося│];
[│  │ обязуется не использовать счет типа "Ф"│ владельцем счета типа "Ф")     │];
[│  │ для осуществления операций,связанных с │                                │];
[│  │ предпринимательской деятельностью      │                                │];
[└──┴────────────────────────────────────────┴────────────────────────────────┘];
[ ];
[                   Г. Иные сведения];
[┌──┬────────────────────────────────────────┬────────────────────────────────┐];
[│10│ Иные сведения, в том числе полученные  │################################│](TEXT_ПРИМЕЧАНИЕ(0));
[│  │ со слов физического лица - нерезидента │################################│](TEXT_ПРИМЕЧАНИЕ(1));
[└──┴────────────────────────────────────────┴────────────────────────────────┘];
[ ];
[ ];
[ Представленные документы мною проверены,                                     ];
[                                                                              ];
[ Ответственное лицо Сбербанка России      ____________________                ];
[                                         (___________________________)        ];
[                                            Ф.И.О.                            ];
[                                                                              ];
[ Подпись физического лица - нерезидента   ____________________                ];
[                                         (___________________________)        ];
[                                            Ф.И.О.                            ];
END;

/* Печать уведомления в РКЦ о превышении лимита общего прихода по счету типа "Ф" за день */
MACRO ПревышениеЛимитаОбщегоПрихода(Account,CorschemNum,_date)
/* Account - номер лицевого счета, покоторому происходит печать */
/* Corschem - идентификатор корсхемы, по которой был проведен документ */

  if(not НайтиСчет(Account))
    return FALSE;
  end;
  if( (ПолучитьСубъекта(a.Client,pt) != 0) )
    msgbox("Не найден нерезидент");
    return FALSE;
  end;
  if( (ПолучитьСубъекта({OurBank},bank) != 0) )
    msgbox("Не найден банк");
    return FALSE;
  end;
  if(not НайтиРКЦ(CorschemNum))
    return FALSE;
  end;
StrSplit(pt.Name, PARTYNAME, 68, 12);

ПечатьЗаголовка(RCC,bank.OKPO,_date);
[        Настоящим уведомляем, что нерезидентом ########################](PARTYNAME(0));
if(ValType(PARTYNAME(1)) != V_UNDEF)
[     ##################################################################](PARTYNAME(1):w);
end;
[     в  нарушение  пункта  4.5  Инструкции  Банка  России  "О порядке открытия];
[     уполномоченными банками счетов нерезидентов в валюте Российской Федерации];
[     и  проведения  операций  по  этим  счетам" совершена приходная операция с];
[     превышением  установленного  минимума  в  три тысячи минимальных размеров];
[     оплаты труда.];
ПечататьПодвала();
[

];
/* Печать информационной справки */
ПечататьИнформационнойСправки(pt,bank,a);

END;

/* Печать уведомления в РКЦ о превышении заявленного максимума суммы прихода за месяц */
MACRO ПревышениеЗаявленногоМаксимума(Account,CorschemNum,Mont)
/* Account - номер лицевого счета, покоторому происходит печать */
/* Corschem - идентификатор корсхемы, по которой был проведен документ */

  if(not НайтиСчет(Account))
    return FALSE;
  end;
  if( (ПолучитьСубъекта(a.Client,pt) != 0) )
    msgbox("Не найден нерезидент");
    return FALSE;
  end;
  if( (ПолучитьСубъекта({OurBank},bank) != 0) )
    msgbox("Не найден банк");
    return FALSE;
  end;
  if(not НайтиРКЦ(CorschemNum))
    return FALSE;
  end;

StrSplit(pt.Name, PARTYNAME, 68, 12);

ПечатьЗаголовка(RCC,bank.OKPO,{curdate});
[        Настоящим уведомляем, что нерезидентом ########################](PARTYNAME(0));
if(ValType(PARTYNAME(1)) != V_UNDEF)
[     ##################################################################](PARTYNAME(1):w);
end;
[     в  нарушение  пункта  4.5  Инструкции  Банка  России  "О порядке открытия];
[     уполномоченными банками счетов нерезидентов в валюте Российской Федерации];
[     и  проведения  операций  по  этим  счетам" в течении календарного  месяца];
[     совершены приходные операции на общую сумму,  превышающую  предполагаемый];
[     максимальный объем поступлений, указанный нерезидентом  в  информационной];
[     справке.];
ПечататьПодвала();
[

];
/* Печать информационной справки */
ПечататьИнформационнойСправки(pt,bank,a);

END;
