/*
$Name: case_rln.mac
$Module: Ядро ГКБО
$Description: Макрос шага "Формирование резерва на возможные потери по ссудам"
*/

Import "case_cls.mac", "res_carry.mac";

record AccCaseParm("accasepm.dbt");

/*
    Пользовательская часть шага
*/
macro ExecuteStep( doc, primdoc, DocKind, ID_Operation )

  file   accaseFile("accase.dbt");
  record accasscs(accasscs);
  record accase(accase);
  
  var ReserveAccount : string;
  var accasePD : AccCasePrimdoc;
  var query, rs;
  var params : TArray;
  var ReserveType : integer;
  var ClassifReserveLoans : string;
  var IncomeCatCodePrm;
  var OutlayCatCodePrm;

  SetBuff( accasscs, primdoc );

  accaseFile.CaseID = accasscs.CaseID;

  if( GetEQ( accaseFile ) )
    Copy( accase, accaseFile );
  end;
   
  accasePD = AccCasePrimdoc( accase, AccCaseParm, accasscs );

  if(((accasscs.DateTo != date(0,0,0)) and (accasscs.DateTo <= AccOprServDoc.Date)) or
     ((ReserveLoss == $0) and
      (ReserveLoans == $0)))
    ReserveAccount = FindReserveAccount( accasePD, AccOprServDoc.Date );
    if( Trim(ReserveAccount) == "" )
      return 0;
    end;
  else
    ReserveAccount = OpenReserveAccount( accasePD, AccOprServDoc.Date );
  end;

  if( Trim(ReserveAccount) == "" )
    msgbox("Не найден счет резерва.");
    return 1;
  end;

  query =         "SELECT t_reservetype ";
  query = query + "  FROM daccassub_dbt ";
  query = query + " WHERE t_caseid = :CaseSubID ";
  query = query + "   AND t_datefrom = :DateFrom ";
  params = makeArray(SQLParam("CaseSubID", accasscs.CaseSubID)
                    ,SQLParam("DateFrom", accCaseParm.DateFrom)
                    );
  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    ReserveType = rs.value(0);
    ClassifReserveLoans = GetCaseReserveClassif(KindReserveLoans , ReserveType, accase);

    GetIncomeOutlayCatCodeLoans(ClassifReserveLoans, @IncomeCatCodePrm, @OutlayCatCodePrm, AccOprServDoc.Date);
  end;

  if((IncomeCatCodePrm == "") or (OutlayCatCodePrm == ""))
    msgbox("Не задана категория средств для классификации РВПС \"", ClassifReserveLoans, "\"");
    return 1;
  else
    if( not CreateReserveLoansCarry(accasePD, AccOprServDoc.Date, ReserveAccount, IncomeCatCodePrm, OutlayCatCodePrm) )
      msgbox("Не удалось создать проводку резерва.");
      return 1;
    end;
    
    if( not CreateCaseAdditionalCarry(accasscs, ID_Operation, AccOprServDoc.Date) )
      msgbox("Не удалось создать проводку восстановления резерва.");
      return 1;
    end;
  end;

  if((accasscs.DateTo != date(0,0,0)) and (accasscs.DateTo <= AccOprServDoc.Date) and (Trim(ReserveAccount) != ""))
    CloseReserveAccount( accasePD, AccOprServDoc.Date );
  end;
  
  return 0;

end;
