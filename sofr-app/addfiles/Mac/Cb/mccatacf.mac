/*
$Name:        mccatacf.mac
$Module:      Ядро ГКБО   
$Description: Общие функции для процедуры генерации номера счета при работе с катгориями учета
*/

/*************************************************************************/
/*         Автоматизированная банковская система RS-Bank                 */
/*                 Copyright (c) R-Style Software Lab 1998               */
/*                                                                       */
/*  Имя файла        : mccatacf.mac                                      */
/*                                                                       */
/*  Описание         : Общие функции для процедуры генерации номера счета*/
/*                        при работе с катгориями учета                  */
/*                                                                       */
/*  Создан           : //AV 15.03.01 (на основе reqinter.mac(Дудин В.А.))*/
/*                                                                       */
/*************************************************************************/

IMPORT CTInter, RsbDataSet, OprInter;

/*если длина Num < len -  дополняет строку слева нулями до нужной длины*/
/*если длина Num > len -  в зависимости от параметра TruncFirst отрезает первые символы или последние */
MACRO LZ_acc( num, len, LeftJustify )
   var str1, len1;
   str1 = trim( string( num ) );
   len1 = strlen( str1 );

   /*по умолчанию выравнивание по правому краю*/
   if( LeftJustify == null )
      LeftJustify = false;
   end;   

   if ( len1 >= len ) /*обрезаем - справа или слева*/
      if( LeftJustify )
         str1 = SubStr( str1, 1, len );            /*последние ((len1-len) символов отрезаем)*/
      else
         str1 = SubStr( str1, (len1-len+1), len ); /*первые (len1-len) символов отрезаем*/
      end;
   else  /*дополняем нулями*/ 
      if( LeftJustify )
         str1 = str1 + mkstr("0", len-len1 );  /* добавляем нули справа */
      else
         str1 = mkstr("0", len-len1 ) + str1;  /* добавляем нули слева */
      end;
   end;
   return str1;
END;

/* Определить параметры создания счета по КУ
   Categ          [IN]  - КУ. Параметр типа TBFile(mccateg), TRecHandler(mccateg), 
                          FILE(mccateg), RECORD(mccateg) или STRING - номер КУ.
   BackOutAccount [OUT] - Признак необходимости отката созданного счета при откате шага лперации
   ChangeOpenDate [OUT] - Признак переустановки даты открытия счета если счет уже есть, 
                          но его дата открытия больше, чем требуется
*/
MACRO MC_GetAccountOpenParms( Categ:VARIANT, BackOutAccount:@BOOL, ChangeOpenDate:@BOOL )
  VAR mccateg = TRecHandler( "mccateg" );

  BackOutAccount = false;
  ChangeOpenDate = false;

  if( ValType(Categ) == V_STRING )
     if( not MC_FindMCCATEG( Categ, mccateg ) )
        return;
     end;
  else
     copy( mccateg, Categ );  
  end;

/* ВР. BackOutAccount - всегда false. Созданные счета не удаляются - требования интеграции
  BackOutAccount = (mccateg.rec.UpdateMode == 0); /*ДА - для индивидуальных КУ*/
*/
  ChangeOpenDate = (mccateg.rec.UpdateMode != 0); /*ДА - для НЕиндивидуальных КУ*/
end;

/*  Поиск и создание лицевого счёта по категориям учёта
    с автоматическим определением параметров отката открытия счёта
    и переустановки даты открытия счёта
*/
MACRO MC_FindAndOpenAccountEx(
    CodeCat,            // код категории
    FD,                 // первичный документ
    ActionDate,         // дата действия
    IsMass,             // признак "тихой" работы
    IsOpen,             // режим поиска/создания счёта
    AccBuf,             // буфер для найденного/открытого счёта (account.dbt)
    CurryCurrency,      // ФИ создаваемого счёта
    ORScheme,           // схема переоценки
    PairAccMode,        // режим определения парного счёта
    NumberForAcc,       // номер счёта
    FIRole,             // роль финансового инструмента
    Result,             // результат
    AccDoc,             // найденный/открытый счёт (mcaccdoc.dbt)
    DepartmentID,       // номер узла ТС
    ActivateDate,       // дата начала действия счёта
    Initiator,          // владелец счета
    ID_Operation,
    ID_Step
)
var
    BackOutAccount = false, ChangeOpenDate  = false;
    MC_GetAccountOpenParms( CodeCat, @BackOutAccount, @ChangeOpenDate );

    return MC_FindAndOpenAccount(
        CodeCat,
        FD,
        ActionDate,
        IsMass,
        IsOpen,
        AccBuf,
        CurryCurrency,
        ORScheme,
        PairAccMode,
        NumberForAcc,
        FIRole,
        Result,
        AccDoc,
        DepartmentID,
        Initiator,
        ActivateDate,
        BackoutAccount,
        ChangeOpenDate,
        null,
        null,
        null,
        ID_Operation,
        ID_Step
    )
END;

/*Получить договор ДУ по договору ПЗО*/
MACRO CB_GetTSContrBySfContr( SfContrID:INTEGER, Order:TRecHandler ):BOOL
  var RS = TRsbDataSet( "SELECT * FROM dtsorder_dbt tsorder "+
                        " WHERE tsorder.t_SfContrID = " + string(SfContrID)
                      );
  if( RS.MoveNext() )
     RS.GetRecord().CopyTo( Order.rec );
     return true;
  else
     Order.Clear();
  end;
  return false;
END;

/*Код вида договора ДУ в счете*/
MACRO MC_TSOrder_GetKindCode( TSOrder:TRecHandler ):INTEGER
  VAR Code = -1;

  if( TSOrder.rec.DocKind == TS_DOC_IDDU )
     Code = 1; 
  elif( TSOrder.rec.DocKind == TS_DOC_OFBU )
     Code = 2; 
  elif( TSOrder.rec.DocKind == TS_DOC_DPOFBU )
     Code = 3; 
  end;
  return Code;
END;

/*Получить код договора ДУ в номере счета*/
MACRO MC_TSOrder_GetCodeInAccount( TSOrder:TRecHandler ):STRING
  return TSOrder.rec.AccCode;
END;
