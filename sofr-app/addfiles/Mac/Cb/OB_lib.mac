/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*            Copyright (c) R-Style Software Lab 1999                   */
/*                                                                      */
/*  Имя файла   : OB_lib.mac                                            */
/*                                                                      */
/*  Описание    : Библиотека для пользовательских операций              */
/*                Приход и Расход по Внебалансу                         */
/*                                                                      */
/*  Создан      : 05.04.1999                                            */
/*                                                                      */
/************************************************************************/
IMPORT OprInter, BankInter, CTInter, FIInter, globals;  /* в globals.mac описаны общебанковские глобальные переменные */

RECORD  uo_obcur ("uo_obcur", "USEROP.DEF");  /* Пользовательский первичный документ (польз. внебаланс) */

CONST
        OB_КодВалютыРубли       = 0,
        ОперацияПрихВнебаланс   = 5000,  /* номер вида операции "Приход по внебалансу"*/
        ОперацияРасхВнебаланс   = 5001,  /* номер вида операции "Приход по внебалансу"*/
        ПервДокВнебаланс        = 5000,  /* номер вида пользовательского первичного документа "Польз. внебаланс" */   
        ВидСчетВнеБалОВП        = 501,   /* номер вида счета ОВП по внебал. главе */
        ВидСчет99999            = 502,   /* номер вида счета 99999 */                                 
        ВидСчет99998            = 503;   /* номер вида счета 99998 */                                 


VAR
        Глава         = 3,
        СчетВнеБалОВП = "", 
        Счет99999 = "",  /* необходимо настроить на номер счета в банке */
        Счет99998 = "";  /* необходимо настроить на номер счета в банке */

FILE    OB_ФИ (fininstr);          /* справочник финансовых инструментов */
FILE    OB_accR ("account");       /* рублевые лицевые счета */


/************************************************************************/
/*   Функция ищет финансовый инструмент по польз. коду                  */
/************************************************************************/
MACRO OB_ПолучитьФИПоПользКоду (ПользКодФИ)
        keynum (OB_ФИ, 1);
        OB_ФИ.FI_Code = Trim (ПользКодФИ);
        if ( GetEQ(OB_ФИ) )
                Return TRUE;
        else
                Return False;
        end;
END;

/************************************************************************/
/*   Функция ищет финансовый инструмент по основному коду               */
/************************************************************************/
MACRO OB_ПолучитьФИПоКодуID (КодФИ)
        keynum (OB_ФИ, 0);
        OB_ФИ.FIID = КодФИ;
        if ( GetEQ(OB_ФИ) )
                Return TRUE;
        else
                Return False;
        end;
END;


/************************************************************************/
/*   Функция проверяет наличие лиц. счета                               */
/************************************************************************/
MACRO   OB_ЛСчетСуществует (Глава, КодВалюты, ЛСчет)
        OB_accR.Chapter        = Глава;
        OB_accR.Account        = ЛСчет;
        OB_accR.Code_Currency  = КодВалюты;
        if (GetEQ(OB_accR))
                Return TRUE;
        else
                Return FALSE;
        end;
END;



/************************************************************************/
/*   Функция возвращает ВИД лиц. счета  ()                               */
/************************************************************************/
MACRO   ВидЛСчета (Chapter, FIID, Account)

        OB_accR.Chapter        = Chapter;
        OB_accR.Account        = Account;
        OB_accR.Code_Currency  = FIID;
        if (not GetEQ(OB_accR))
                Return 0;
        else
                Return OB_accR.Kind_Account;
        end;

END;


/************************************************************************/
/*   Класс для определения счетов корреспонденции                       */
/************************************************************************/
class OB_PrimDoc( _KindAccount : string )

  var KindAccount : string;
  var error : integer;

  /* корректировка счета */
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    return true;
  end;

  /* определение FIRole */
  macro GetBasisFIRole( FIRole : integer ) : integer
    return FIROLE_UNDEF;
  end;

  /* определение параметров второго рода */
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole ) : integer
    
    var Parametr = -1;

    return Parametr;

  end;

  /* определение параметров первого рода */
  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole ) : integer
    
    var Parametr = -1;

    if( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND )
      
      if( KindAccount == "А" ) Parametr = 1;
      else                     Parametr = 2; end;

    end;

    return Parametr;

  end;

  
/* конструктор */  
  error = 0;

  KindAccount = _KindAccount;
  
end;

/* Получить счет для корреспонденции */
//-----------------------------------------------------------------------------
macro OB_GetCorrespondentAccount( KindAccount : string, DateCarry : date ) : string

  var PD : OB_PrimDoc = OB_PrimDoc( KindAccount );

  return MC_FindAndOpenCommonAccByFD( "ВнебалСчетКорресп", PD, DateCarry, IsOprMultiExec(), MC_OPENACC_CREATE, null, null, null, null, null, null );

end;
