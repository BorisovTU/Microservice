/*
   "Финансовый мониторинг"
   Макрос печати сообщения
*/
Import FMInter, BankInter, PTInter, CTInter, FIInter, "fm_str.mac", "globals.mac";

private const nFields = 3;
private const FullReportLength    = 78;
private const FugureFieldLength   = 4;
private const PropertyFieldLength = 34;

private var ValuesFieldLength = FullReportLength - FugureFieldLength - PropertyFieldLength - (nFields + 1);

private var CodeOK, CodeNO;

private const SecondHeadString = "                              ";

/* Сформировать строку из цифры - номер реквизита */
private macro MakeFigureName( Figure )
  var FigureName = "";
  if( valtype(Figure) == V_INTEGER )
    FigureName = string(Figure) + ".";
  end;
  return FigureName;
end;

/* Получить коды операции */
private macro GetCodes( opcontr )
  var ArrayCodeOK : TArray;
  var ArrayCodeNO : TArray;

  macro FormCodeString( ArrayCode )
    var i, N;
    var CodeString = "";
    N = ArrayCode.Size() ;
    if( N )
      CodeString = ArrayCode(0);
      i = 1;
      while( i < N )
        CodeString = CodeString + "," + ArrayCode(i);
        i = i + 1;
      end;
    end;
    return CodeString;
  end;
  
  CodeOK = ""; CodeNO = "";
  if( ПолучитьКодыОперации( opcontr, ArrayCodeOK, ArrayCodeNO ) )
    CodeOK = FormCodeString( ArrayCodeOK );
    CodeNO = FormCodeString( ArrayCodeNO );
  end;
end;

/* Получить название типа операции */
private macro СтрокаТипОперации( Тип )
  if( Тип == OPCONTR_TYPE_OK ) return "операция, подлежащая обязательному контролю"; end;
  if( Тип == OPCONTR_TYPE_NO ) return "необычная сделка";                            end;
  return "";
end;

/* Получить код и наименование валюты операции */
private macro GetCodeNameCurrency( Code_Currency )
  var Code, Name;
  Code = ""; Name = "";
  Code = ПолучитьКодФинИн( Code_Currency );
  Name = ПолучитьИмяФинИн( Code_Currency );
  
  return Code + " " + Name;
end;

/* Получить имя операциониста */
private macro GetOperName( oper )
  file person("person.dbt");

  person.Oper = oper;
  if( getEQ(person) ) return person.Name; end;
  return "";
end;

/* Получить имя операциониста, для которого текущий пользователь входит в группу подчиненных */
private macro Начальник( oper )
  file person("person.dbt");

  rewind( person );
  while( next( person ) )
      
    if( (person.cTypePerson == "Н") and (person.GroupOperFO <= oper) and (person.GroupOperLO >= oper) )
      return GetOperName( person.Oper );
    end;
    
  end;

  return GetOperName( oper );
end;

private macro SplitString( nString, Cage )
  var CurN = Cage.Split();
  if( nString < CurN )
    nString = CurN;
  end;
  return nString;
end;

/* Напечатать одну запись с разбитием на строки */
private macro PrintString( FugureCage, PropertyCage, ValueCage )
  
  var i, nString;
  var TabLine;
  array FigureArray, PropertyArray, ValuesArray;

  nString = 0;
  nString = SplitString( nString, FugureCage   );
  nString = SplitString( nString, PropertyCage );
  nString = SplitString( nString, ValueCage    );
  
  i = 0;
  while( i < nString )
    TabLine = "";

    TabLine = FM_AddToTabLine( TabLine, FugureCage.GetLine(i)  , FugureCage.GetCageLength()   );
    TabLine = FM_AddToTabLine( TabLine, PropertyCage.GetLine(i), PropertyCage.GetCageLength() );
    TabLine = FM_AddToTabLine( TabLine, ValueCage.GetLine(i)   , ValueCage.GetCageLength()    );

    println( TabLine );

    i = i + 1;
  end;

end;

/* Получить код клиента */
private macro ClientCode( opcntrpt )
  file partcode("partcode.dbt") key 0;
  var Code = "";
  
  if( opcntrpt.ClientID != -1 )
    partcode.PartyID = opcntrpt.ClientID;    
    partcode.CodeKind = PTCK_CONTR;
    if( getEQ(partcode) )
      Code = "Код клиента " + partcode.Code;
    end;
  end;
  
  return Code;
end;

private macro GetCountryName( CountryCode )
  file country("country.dbt","rsrfdb.def") key 3;
  country.CodeNum3 = CountryCode;
  if( getEQ(country) ) return country.Name; end;
  return "";
end;

/* добавить строку */
private macro add_string( FullString, AddString, Splitter )
  if( AddString != "" )
    if( FullString != "" )
      FullString = FullString + Splitter;
    end;
    FullString = FullString + AddString;
  end;
  return FullString;
end;

private macro add_string_head( FullString, Head, AddString, Splitter )
  if( AddString != "" )
    FullString = add_string( FullString, Head + " " + AddString, Splitter );
  end;
  return FullString;
end;


macro CountryString( Head, CountryCode, Territory )
  var str, terrCode;

  str = GetCountryName( CountryCode );
  if( str != "" )
    str = Head + " " + str;
    str = add_string( str, "территория", ", " );
    terrCode = Territory;
    if( terrCode == "" ) terrCode = "00"; end;
    str = add_string( str, terrCode, " " );
  end;

  return str;
end;

private macro CodeInstitut( opcntrpt )
  var CodeString = "";

  CodeString = add_string_head( CodeString, "ИНН"      , opcntrpt.INN             );
  CodeString = add_string_head( CodeString, "Рег.номер", opcntrpt.RegNumber, ", " );
  CodeString = add_string_head( CodeString, "ОКПО"     , opcntrpt.OKPO     , ", " );

  return CodeString;
end;

private macro MakeDocumentData( opcntrpt, DocumentName )
  var DocumentStr = DocumentName;
  if( DocumentStr != "" )
    DocumentStr = add_string_head( DocumentStr, "серия", opcntrpt.PaperSeries    , ", " );
    DocumentStr = add_string_head( DocumentStr, "номер", opcntrpt.PaperNumber    , ", " );
    DocumentStr = add_string_head( DocumentStr, "выдан", opcntrpt.PaperIssuer    , ", " );
    DocumentStr = add_string( DocumentStr, string(opcntrpt.PaperIssuedDate:f), ", " );
  end;
  return DocumentStr;
end;

/* Печать сведений об участнике */
private macro print_opcntrpt( opcntrpt )
  var PtKindName, TypeName, CodeDocumName;
  var PtString;

  НазваниеВидаУчастникаОперации( opcntrpt, PtKindName, TypeName, CodeDocumName );

  PtString = "";
  PtString = add_string( PtString, ClientCode( opcntrpt ), ""   );
  PtString = add_string( PtString, opcntrpt.Name         , "\n" );
  if( (opcntrpt.Type == "Ю") or (opcntrpt.Type == "Ф") or (opcntrpt.Type == "П") )
    PtString = add_string( PtString, TypeName, ", " );
  end;
  
  PtString = add_string(
              PtString
             ,CountryString( "Страна регистрации", opcntrpt.szCountryR, opcntrpt.szTerritoryR )
             , "\n" );
  PtString = add_string(
              PtString
             ,CountryString( "Страна нахождения" , opcntrpt.szCountryP, opcntrpt.szTerritoryP )
             , "\n" );
  
  if( opcntrpt.Type == "Ю" )
    PtString = add_string( PtString, CodeInstitut( opcntrpt ), "\n" );
  elif( (opcntrpt.Type == "Ф") or (opcntrpt.Type == "П") )
    PtString = add_string( PtString, MakeDocumentData( opcntrpt, CodeDocumName ), "\n" );
    PtString = add_string_head( PtString, "ИНН", opcntrpt.INN, "\n" );
  else
    PtString = add_string_head( PtString, "ИНН", opcntrpt.INN, "\n" );
  end;

  PrintString( FM_RepCage(""        , FugureFieldLength  , 1, 1)
              ,FM_RepCage(PtKindName, PropertyFieldLength, 4, 1)
              ,FM_RepCage(PtString  , ValuesFieldLength  , 1, 1)
             );
end;

/* Печать данные об участниках */
private macro PrintOpcontrParty( opcontr )
  file opcntrpt("opcntrpt.dbt") key 0;
  var stat;
  
  ClearRecord( opcntrpt );
  opcntrpt.OperationID = opcontr.OperationID;
  stat = GetGE( opcntrpt );
  while( (stat) and (opcntrpt.OperationID == opcontr.OperationID) )
    print_opcntrpt( opcntrpt );
    
    stat = next( opcntrpt );
  end;
end;

private macro print_account( Num, Account, NameAccount )
  PrintString( FM_RepCage(Num                                                   , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Счет, с использованием которого проводится операция:", PropertyFieldLength, 1, 1)
              ,FM_RepCage(string(Account:f)                                     , ValuesFieldLength  , 1, 1)
             );
  
  PrintString( FM_RepCage(""            , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Клиент счета", PropertyFieldLength, 4, 1)
              ,FM_RepCage(NameAccount   , ValuesFieldLength  , 1, 1)
             );
  
end;

/* Печать данных об одном счетe, участвующих в операции */
private macro PrintAccountData( Num, opcontr, Kind )
  file f_account ("account.dbt" );
  file f_accountC("account$.dbt");
  record opcntrpt("opcntrpt.dbt");
  var fileAccount, Account, NameAccount;

  ClearRecord( opcntrpt );
  ПолучитьУчастникаОперации( opcntrpt, opcontr, Kind );
  if( (Trim(opcntrpt.BankCode) == "") and (Trim(opcntrpt.BankName) == "") and
      (opcntrpt.Account != "") and (opcntrpt.ClientID != {OurBank}) )

    Account = opcntrpt.Account;
    if( opcontr.Code_Currency == NATCUR )
      fileAccount = f_account;
    else
      fileAccount = f_accountC;
    end;

    fileAccount.Chapter       = 1;
    fileAccount.Code_Currency = opcontr.Code_Currency;
    fileAccount.Account       = Account;
    if( (GetEQ(fileAccount)) and (opcntrpt.ClientID != {OurBank}) )
      NameAccount = fileAccount.NameAccount;
    else  
      NameAccount = opcntrpt.Name;
    end;
    print_account( Num, Account, NameAccount );
    SetParm( 0, "" );
  
  end

end;

/* Печать данных о счетах, участвующих в операции */
private macro PrintAccounts( opcontr, Num )
  PrintAccountData( Num, opcontr, 1 );
  PrintAccountData( Num, opcontr, 4 );
  if( Num != "" ) print_account( Num, "", "" ); end;
end;

private macro MonthName( mon )
  if( mon == 1  ) return "января";   end;
  if( mon == 2  ) return "февраля";  end;
  if( mon == 3  ) return "марта";    end;
  if( mon == 4  ) return "апреля";   end;
  if( mon == 5  ) return "мая";      end;
  if( mon == 6  ) return "июня";     end;
  if( mon == 7  ) return "июля";     end;
  if( mon == 8  ) return "августа";  end;
  if( mon == 9  ) return "сентября"; end;
  if( mon == 10 ) return "октября";  end;
  if( mon == 11 ) return "ноября";   end;
  if( mon == 12 ) return "декабря";  end;
end;

/* Печать штампа сообщения */
private macro PrintStamp( opcontr )
  var d, m, y;

  datesplit( {curdate}, d, m, y );

  println( " Дата составления: \"" + string(d:2:o) + "\" " + MonthName(m) + " " + y + " г." );
  println();

  println( " Сотрудник, составивший сообщение:           / " + GetOperName( opcontr.oper ) + " /" );
  println( "                        Должность:" );
  println();

  println( " Руководитель подразделения:           / " + Начальник( opcontr.oper ) + " /" );
  println();
end;

/*
   Печать сообщения
*/
macro ПечатьСообщения( _opcontr )
  
  var HeadLine, TabLine, SplitLine;
  record opcontr("opcontr.dbt");

  SetBuff( opcontr, _opcontr );
  
  println( FM_AddToTabLine( "", "Приложение № 4"                                , FullReportLength, "r", true ) );
  println( FM_AddToTabLine( "", "к Рекомендациям по разработке"                 , FullReportLength, "r", true ) );
  println( FM_AddToTabLine( "", "кредитными организациями"                      , FullReportLength, "r", true ) );
  println( FM_AddToTabLine( "", "правил внутреннего контроля"                   , FullReportLength, "r", true ) );
  println( FM_AddToTabLine( "", "в целях противодействия (легализации) доходов,", FullReportLength, "r", true ) );
  println( FM_AddToTabLine( "", "полученных преступным путем"                   , FullReportLength, "r", true ) );

  println();
  println();

  println( FM_AddToTabLine( SecondHeadString, "Ответственному за организацию"          , FullReportLength, "l", true ) );
  println( FM_AddToTabLine( SecondHeadString, "внутреннего контроля по противодействию", FullReportLength, "l", true ) );
  println( FM_AddToTabLine( SecondHeadString, "легализации (отмыванию) доходов,"       , FullReportLength, "l", true ) );
  println( FM_AddToTabLine( SecondHeadString, "полученных преступным путем"            , FullReportLength, "l", true ) );
  println();
  println();
  println( FM_AddToTabLine( SecondHeadString, "            (Фамилия И.О.)             ", FullReportLength, "l", true ) );

  println();

  println( FM_AddToTabLine( "", "СВЕДЕНИЯ ОБ ОПЕРАЦИИ (СДЕЛКЕ), ПОДЛЕЖАЩЕЙ КОНТРОЛЮ", FullReportLength, "c", true ) );

  println();

  HeadLine = "";
  HeadLine = FM_AddToSplitLine( HeadLine, FugureFieldLength + PropertyFieldLength  + 1 );
  HeadLine = FM_AddToSplitLine( HeadLine, ValuesFieldLength );
  println( HeadLine );

  TabLine = "";
  TabLine = FM_AddToTabLine( TabLine, " Реквизит сообщения", FugureFieldLength + PropertyFieldLength  + 1 );
  TabLine = FM_AddToTabLine( TabLine, " Значение"          , ValuesFieldLength );
  println( TabLine );
  
  SplitLine = "";
  SplitLine = FM_AddToSplitLine( SplitLine, FugureFieldLength   );
  SplitLine = FM_AddToSplitLine( SplitLine, PropertyFieldLength );
  SplitLine = FM_AddToSplitLine( SplitLine, ValuesFieldLength   );
  println( SplitLine );

  PrintString( FM_RepCage(MakeFigureName(1)               , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Сведения об операции (сделке):", PropertyFieldLength, 1, 1)
              ,FM_RepCage(""                              , ValuesFieldLength  , 1, 1)
             );

  PrintString( FM_RepCage(""                             , FugureFieldLength        )
              ,FM_RepCage("Тип операции"                 , PropertyFieldLength, 4, 1)
              ,FM_RepCage(СтрокаТипОперации(opcontr.Type), ValuesFieldLength  , 1, 1)
             );

  GetCodes( opcontr );
  PrintString( FM_RepCage(""                                       , FugureFieldLength        )
              ,FM_RepCage("Код операции для обязательного контроля", PropertyFieldLength, 4, 1)
              ,FM_RepCage(CodeOK                                   , ValuesFieldLength  , 1, 1)
             );

  PrintString( FM_RepCage(""                      , FugureFieldLength        )
              ,FM_RepCage("Код необычной операции", PropertyFieldLength, 4, 1)
              ,FM_RepCage(CodeNO                  , ValuesFieldLength  , 1, 1)
             );

  PrintString( FM_RepCage(""                  , FugureFieldLength        )
              ,FM_RepCage("Основание операции", PropertyFieldLength, 4, 1)
              ,FM_RepCage(opcontr.Ground      , ValuesFieldLength  , 1, 1)
             );

  println( SplitLine );

  PrintString( FM_RepCage(MakeFigureName(2)       , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Сумма операции"        , PropertyFieldLength, 1, 1)
              ,FM_RepCage(string(opcontr.SumCur:f), ValuesFieldLength  , 1, 1)
             );

  PrintString( FM_RepCage(""                                        , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Валюта операции"                         , PropertyFieldLength, 1, 1)
              ,FM_RepCage(GetCodeNameCurrency(opcontr.Code_Currency), ValuesFieldLength  , 1, 1)
             );

  println( SplitLine );

  PrintString( FM_RepCage(MakeFigureName(3)                 , FugureFieldLength  , 1, 1)
              ,FM_RepCage("Сведения об участниках операции:", PropertyFieldLength, 1, 1)
              ,FM_RepCage(""                                , ValuesFieldLength  , 1, 1)
             );

  PrintOpcontrParty( opcontr );

  println( SplitLine );

  PrintAccounts( opcontr, MakeFigureName(4) );

  println( SplitLine );

  PrintString(
    FM_RepCage(MakeFigureName(5)                                                                             , FugureFieldLength  , 1, 1)
   ,FM_RepCage("Причины отнесения операции к легализации доходов или возникшие затруднения при классификации", PropertyFieldLength, 1, 1)
   ,FM_RepCage(readNoteForObject( OBJTYPE_OPCONTR, MakeOpContrID(opcontr.OperationID), 1 )                   , ValuesFieldLength  , 1, 1)
             );

  println( SplitLine );

  println();

  PrintStamp( opcontr );
  
  println();

end;
