/*                                             
$Name:  fiwarnts.mac
$Module:  Ценные бумаги
$Description:  Макрос скроллинга купонов ценных бумаг
*/
IMPORT BankInter, FIInter, RsbDataSet, cb_sql;
import LoadRuData, oralib;
/*Режимы скроллинга*/
private const  FIREVIEW    = 0,   /* Только просмотр  */
               FIEDIT      = 1,   /* Редактирование   */
               FIEDITINPUT = 2;   /* Ввод новых записей и редактирование */


/* CheckPanelMode - функция для проверки перед вызовом панели купона/чп
  FI:TRecHandler - структура ФИ для которой вызван скроллинг чп/купонов, 
  SMode:INTEGER - режим скроллинга,
  IsInsert:BOOL - если панель вызывается для вставки то True 
  FIWarnt:TRecHandler - структура купона, передаваемая при редактировании (при вводе нового  - пустая)
  В случае вставки новой записи если ф-йия вернет 0 то появиться панель ввода новой записи 
  при любом другом значении панели не будет
  В случае просмотра если 0 то будет возможность редактирования иначе просто просмотр.
  */
private macro CheckPanelMode( FI:TRecHandler, SMode:INTEGER, IsInsert:BOOL, FIWarnt:TRecHandler )


  return 0;
end;

/*проверка - включен ли режим хранилища данных для НУ*/
private macro CheckTaxDepositoryMode()
   var ErrCode, DepMode;

   GetRegistryValue( "SECUR\\РЕЖИМ ХРАНИЛИЩА ДАННЫХ ДЛЯ НУ", V_BOOL, DepMode, ErrCode );

   return DepMode and (ErrCode == 0);
end;

/*проверка введенных дат составления перечня и получения для купонов и ЧП*/
MACRO CheckWarntsListFactDate( PtrToFiW:MEMADDR, PtrToOldFIW:MEMADDR )

   var fiwarnts = TRecHandler("fiwarnts.dbt");
   var oldfiwarnts = TRecHandler("fiwarnts.dbt");

   if (CheckTaxDepositoryMode() == false)
      fiwarnts.SetRecordAddr( PtrToFiW );
      oldfiwarnts.SetRecordAddr( PtrToOldFIW );

      if( fiwarnts.rec.DrawingDate != oldfiwarnts.rec.DrawingDate )

         if( (fiwarnts.rec.ListDate != null) and (fiwarnts.rec.ListDate != Date(0,0,0)) and (fiwarnts.rec.ListDate > fiwarnts.rec.DrawingDate) )
            fiwarnts.rec.ListDate = fiwarnts.rec.DrawingDate;
         end; 

      end;

      if( fiwarnts.rec.ListDate != oldfiwarnts.rec.ListDate )

         if( (fiwarnts.rec.ListDate != null) and (fiwarnts.rec.ListDate != Date(0,0,0)) and (fiwarnts.rec.ListDate > fiwarnts.rec.DrawingDate) )
            MsgBox("Дата составления перечня не может быть больше даты погашения");
            fiwarnts.rec.ListDate = oldfiwarnts.rec.ListDate;
            return 1;
         end; 

      end;
   end;

   return 0;
END;                                        

private MACRO ПолучитьДатуПоследнегоИзвестногоКупона(FIID:integer)
  var cmd, ds;
  var DrawingDate = Date(0,0,0);

  cmd = RSDCommand("SELECT RSI_RSB_FIInstr.FI_GetDateLastKnownCoupon(?) DrawingDate from dual ");
  cmd.AddParam("", RSDBP_IN, FIID);

  cmd.Execute();
  ds = TRsbDataSet(cmd);
  if(ds.moveNext())
    DrawingDate = SQL_ConvTypeDate(ds.DrawingDate);
  end;

  return DrawingDate;
END;

/*проверка введенных дат составления перечня и получения для анкеты выпуска*/
MACRO CheckAvoirListFactDate( PtrToAvr:MEMADDR, PtrToOldAvr:MEMADDR, PtrToFI:MEMADDR )
   var avr = TRecHandler("avoiriss.dbt");
   var oldavr = TRecHandler("avoiriss.dbt");
   var fininstr = TRecHandler("fininstr.dbt");

   if (CheckTaxDepositoryMode() == false)
      avr.SetRecordAddr( PtrToAvr );
      oldavr.SetRecordAddr( PtrToOldAvr );
      fininstr.SetRecordAddr( PtrToFI );
      
      if( avr.rec.ListDate != oldavr.rec.ListDate )

         if( (avr.rec.ListDate != null) and (avr.rec.ListDate != Date(0,0,0)) )
            var DrawingDate = fininstr.rec.DrawingDate;
            if( ((DrawingDate == null) or (DrawingDate == Date(0,0,0))) and 
                (avr.rec.Termless == "X"))
               DrawingDate = ПолучитьДатуПоследнегоИзвестногоКупона(fininstr.rec.FIID);
            end;

            if(avr.rec.ListDate > DrawingDate)
            MsgBox("Дата составления перечня не может быть больше даты погашения");
            avr.rec.ListDate = oldavr.rec.ListDate;
            return 1;
         end; 
         end; 

      end;
   end;

   return 0;
END;

/*проверка введенных дат составления перечня и получения для анкеты выпуска относительно даты погашения*/
MACRO CheckAvoirDrawingDate( PtrToAvr:MEMADDR, PtrToFI:MEMADDR, PtrToOldFI:MEMADDR )

   var avr = TRecHandler("avoiriss.dbt");
   var fininstr = TRecHandler("fininstr.dbt");
   var oldfininstr = TRecHandler("fininstr.dbt");

   if (CheckTaxDepositoryMode() == false)
      avr.SetRecordAddr( PtrToAvr );
      fininstr.SetRecordAddr( PtrToFI );
      oldfininstr.SetRecordAddr( PtrToOldFI );

      if( fininstr.rec.DrawingDate != oldfininstr.rec.DrawingDate )

         if( (avr.rec.ListDate != null) and (avr.rec.ListDate != Date(0,0,0)) and (avr.rec.ListDate > fininstr.rec.DrawingDate) )
            avr.rec.ListDate = fininstr.rec.DrawingDate;
         end; 

      end;
   end;

   return 0;
END;

/*Виды парных анкет*/
const PairAVOIR = 1,
              PairCOUPON = 2,
              PairPARTLY = 3;

macro ПогашениеКупона(FIID,Number_Coupon,Number_Partly)
  var SqlText, SqlQuery, DataQuery;

      SqlText = "SELECT tick.t_DealID " +
                "  FROM DDL_TICK_DBT tick, DDL_LEG_DBT leg "+
                " WHERE rsb_secur.IsRet_Coupon(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType,tick.t_BofficeKind))) = 1 " +
                "   AND tick.t_DealID = leg.t_DealID " +
                "   AND leg.t_LegID = 0 " +
                "   AND leg.t_LegKind = 0 " +
                "   AND tick.t_DealStatus = 0 " +
                "   AND leg.t_PFI = ?";

      if( Number_Coupon != null )
         SqlText = SqlText + "   AND tick.t_Number_Coupon = ? ";
      end;

      if( Number_Partly != null )
         SqlText = SqlText + "   AND tick.t_Number_Partly = ? ";
      end;

      SqlQuery = RSDCommand( SqlText ); 

      SqlQuery.addParam( "", RSDBP_IN, FIID );

      if( Number_Coupon != null )
         SqlQuery.addParam( "", RSDBP_IN, Number_Coupon );
      end;

      if( Number_Partly != null )
         SqlQuery.addParam( "", RSDBP_IN, Number_Partly );
      end;

      SqlQuery.execute();
      DataQuery = TRsbDataSet(SqlQuery);

      if(DataQuery.MoveNext())
        return true;
      end;

      return false;
end;

macro ПогашениеВыпуска(FIID,Number_Coupon,Number_Partly)
  var SqlText, SqlQuery, DataQuery;

      SqlText = "SELECT tick.t_DealID " +
                "  FROM DDL_TICK_DBT tick, DDL_LEG_DBT leg "+
                " WHERE rsb_secur.IsRet_Issue(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType,tick.t_BofficeKind))) = 1 " +
                "   AND tick.t_DealID = leg.t_DealID " +
                "   AND leg.t_LegID = 0 " +
                "   AND leg.t_LegKind = 0 " +
                "   AND tick.t_DealStatus = 0 " +
                "   AND leg.t_PFI = ? ";

      if( Number_Coupon != null )
         SqlText = SqlText + "   AND tick.t_Number_Coupon = ? ";
      end;

      if( Number_Partly != null )
         SqlText = SqlText + "   AND tick.t_Number_Partly = ? ";
      end;

      SqlQuery = RSDCommand( SqlText ); 

      SqlQuery.addParam( "", RSDBP_IN, FIID );

      if( Number_Coupon != null )
         SqlQuery.addParam( "", RSDBP_IN, Number_Coupon );
      end;

      if( Number_Partly != null )
         SqlQuery.addParam( "", RSDBP_IN, Number_Partly );
      end;

      SqlQuery.execute();
      DataQuery = TRsbDataSet(SqlQuery);

      if(DataQuery.MoveNext())
        return true;
      end;

      return false;
end;

MACRO ОбновитьСвязАнкетуКупона( PtrToFiWarnts:MEMADDR, Number_Coupon, KindPairAnk:INTEGER )
   var fininstr = TRecHandler("fininstr.dbt");
   var fiwarnts = TRecHandler("fiwarnts.dbt");

   var avoiriss = TRecHandler("avoiriss.dbt");
   var fipartly = TRecHandler("fiwarnts.dbt");

   var SqlQuery, DataQuery;

   fiwarnts.SetRecordAddr( PtrToFiWarnts );

   ПолучитьФинИн(fiwarnts.rec.FIID, fininstr);

   if( fiwarnts.rec.DrawingDate != fininstr.rec.DrawingDate )

      SqlQuery = RSDCommand( "SELECT * " +
                             "  FROM DFIWARNTS_DBT "+
                             " WHERE t_IsPartial = 'X' " +
                             "   AND t_DrawingDate = ? " +
                             "   AND t_FIID = ? "); 

      SqlQuery.addParam( "", RSDBP_IN, fiwarnts.rec.DrawingDate );
      SqlQuery.addParam( "", RSDBP_IN, fiwarnts.rec.FIID );
      SqlQuery.execute();

      DataQuery = TRsbDataSet(SqlQuery);

      if(DataQuery.MoveNext())
         
         if( ПогашениеКупона(fiwarnts.rec.FIID,fiwarnts.rec.Number,DataQuery.Number) == true )
            MsgBox("Погашение купона выполняется одновременно с частичным погашением.|"+
                   "В анкету частичного погашения из анкеты купона будут скопирована|"+
                   " дата составления перечня");
         else
//А.Киселев 24.01.2019 для массового обновления RUDAta
/*
            if( MsgBoxEx( "Подставить в анкету частичного погашения за дату " + fiwarnts.rec.DrawingDate + 
                          " дату составления перечня владельцев из анкеты купона?",
                           MB_YES+MB_NO, IND_NO, 
                           "", "") == IND_YES )
              setparm(1,DataQuery.Number);
              setparm(2,PairPARTLY);
            end;
*/
         end;
      end;

   else
      if( ПогашениеВыпуска(fiwarnts.rec.FIID,fiwarnts.rec.Number) == true )
            MsgBox("Погашение купона выполняется одновременно с погашением выпуска.|"+
                   "В анкету погашения выпуска из анкеты купона будет скопирована|"+
                   " дата составления перечня владельцев");
      else
//А.Киселев 24.01.2019 для массового обновления RUDAta
/*
            if( MsgBoxEx( "Подставить в анкету выпуска дату составления перечня из анкеты купона?",
                           MB_YES+MB_NO, IND_NO, 
                           "", "") == IND_YES )
               if( not ПолучитьФинИн(fiwarnts.rec.FIID, null, avoiriss))
                  setparm(1,"");
                  setparm(2,PairAVOIR);
               end;
            end;
*/
      end;
   end; 
 
   return 0;
END;

private macro ПоследнееЧПВыпуска(FIID,Number_Partly)
  var SqlQuery, DataQuery;

      SqlQuery = RSDCommand( "SELECT P.t_Number " +
                             "  FROM DFIWARNTS_DBT P "+
                             " WHERE P.t_IsPartial = 'X' " +
                             "   AND P.t_Number = ? " +
                             "   AND P.t_FIID = ? " + 
                             "   AND P.t_DrawingDate = (select MAX(MP.t_DrawingDate) " +
                             "                            from dfiwarnts_dbt MP " +
                             "                           where MP.t_IsPartial = 'X' " +
                             "                             and MP.t_FIID = P.t_FIID)");

      SqlQuery.addParam( "", RSDBP_IN, Number_Partly );
      SqlQuery.addParam( "", RSDBP_IN, FIID );

      SqlQuery.execute();
      DataQuery = TRsbDataSet(SqlQuery);

      if(DataQuery.MoveNext())
        return true;
      end;

      return false;
end;

MACRO ОбновитьСвязАнкетуЧП( PtrToFiPartly:MEMADDR, Number_Coupon, KindPairAnk:INTEGER )
   var fipartly = TRecHandler("fiwarnts.dbt");
   var fiwarnts = TRecHandler("fiwarnts.dbt");

   var SqlQuery, DataQuery;

   fipartly.SetRecordAddr( PtrToFiPartly );

   SqlQuery = RSDCommand( "SELECT * " +
                             "  FROM DFIWARNTS_DBT "+
                             " WHERE t_IsPartial = chr(0) " +
                             "   AND t_DrawingDate = ? " +
                             "   AND t_FIID = ? "); 

   SqlQuery.addParam( "", RSDBP_IN, fipartly.rec.DrawingDate );
   SqlQuery.addParam( "", RSDBP_IN, fipartly.rec.FIID );
   SqlQuery.execute();

   DataQuery = TRsbDataSet(SqlQuery);

   if( ПоследнееЧПВыпуска(fipartly.rec.FIID,fipartly.rec.Number) == false )
      if(DataQuery.MoveNext())
         if( ПогашениеКупона(fipartly.rec.FIID,DataQuery.Number,fipartly.rec.Number) == true )
            MsgBox("Частичное погашение выполняется одновременно с погашением купона.|"+
                   "В анкету купона из анкеты частичного погашения|"+
                   " будет скопирована дата составления перечня владельцев");
         else
//А.Киселев 24.01.2019 для массового обновления RUDAta
/*
            if( MsgBoxEx( "Подставить в анкету купона за дату " + fipartly.rec.DrawingDate + 
                          " дату составления перечня владельцев из анкеты частичного погашения?",
                           MB_YES+MB_NO, IND_NO, 
                           "", "") == IND_YES ) 
               setparm(1,DataQuery.Number);
               setparm(2,PairCOUPON); 
            end;
*/
         end;
      end;
   else
      if(DataQuery.MoveNext())
         if( ПогашениеВыпуска(fipartly.rec.FIID,DataQuery.Number,fipartly.rec.Number) == true )
            MsgBox("Погашение выпуска выполняется одновременно с погашением купона.|"+
                   "В анкету купона из анкеты последнего частичного погашения|"+
                   " будет скопирована дата составления перечня владельцев");
         else
//А.Киселев 24.01.2019 для массового обновления RUDAta
/*
            if( MsgBoxEx( "Подставить в анкету купона за дату " + fipartly.rec.DrawingDate + 
                          " дату составления перечня владельцев из анкеты частичного погашения?",
                           MB_YES+MB_NO, IND_NO, 
                           "", "") == IND_YES )
            setparm(1,DataQuery.Number);
               setparm(2,PairCOUPON); 
            end;
*/
         end;
      end;
   end; 
 
   return 0;
END;

MACRO ОбновитьСвязАнкетуВыпуска( PtrToFininstr:MEMADDR, Number_Coupon, KindPairAnk:INTEGER )
   var fininstr = TRecHandler("fininstr.dbt");
   var fiwarnts = TRecHandler("fiwarnts.dbt");

   var SqlQuery, DataQuery;

   fininstr.SetRecordAddr( PtrToFininstr );

   SqlQuery = RSDCommand( "SELECT * " +
                             "  FROM DFIWARNTS_DBT "+
                             " WHERE t_IsPartial = chr(0) " +
                             "   AND t_DrawingDate = ? " +
                             "   AND t_FIID = ? "); 

   SqlQuery.addParam( "", RSDBP_IN, fininstr.rec.DrawingDate );
   SqlQuery.addParam( "", RSDBP_IN, fininstr.rec.FIID );
   SqlQuery.execute();

   DataQuery = TRsbDataSet(SqlQuery);

   if(DataQuery.MoveNext())
      if( ПогашениеВыпуска(fininstr.rec.FIID,DataQuery.Number) == true )
         MsgBox("Погашение выпуска выполняется одновременно с погашением купона.|"+
                "В анкету купона из анкеты выпуска|"+
                " будет скопирована дата составления перечня владельцев");
      else
//А.Киселев 24.01.2019 для массового обновления RUDAta
/*
         if( MsgBoxEx( "Подставить в анкету купона за дату " + fininstr.rec.DrawingDate + 
                       " дату составления перечня владельцев из анкеты выпуска?",
                        MB_YES+MB_NO, IND_NO, 
                        "", "") == IND_YES )
            setparm(1,DataQuery.Number);
            setparm(2,PairCOUPON);
         end;
*/
      end;
   end;

   return 0;
END;

record Документ ( "fiwarnts.dbt" );
record СтарыйДокумент ( "fiwarnts.dbt" ); /* заполняется при редактировании */

/* Макрофункция инициализации новой операции 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  return 0;
END;


/* Макрофункция проверки операции при вводе, редактировании, удалении 
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Проверить_Документ( Режим:INTEGER )
/* Режимы проверки:
 OBJ_DELETE - удаление финансового инструмента;
 OBJ_INSERT - ввод финансового инструмента;
 OBJ_UPDATE - корректировка финансового инструмента;*/
  if (Режим == 1)   /*Удаление*/
  end;
  if (Режим == 2)   /*Ввод*/
  end;
  if (Режим == 3)   /*Редактирование*/
  end;
  return 0;
END;

/* Макрофункция вызывается по Ctrl-Z из скроллинга/панели */
PRIVATE MACRO Функция_Пользователя()
   var flag = true, amort = false,cmd1,col1, arnum, rs1;
   PRIVATE MACRO AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
      ar.value (ind * 6)     = fld;
      ar.value (ind * 6 + 1) = head;
      ar.value (ind * 6 + 2) = width;
      ar.value (ind * 6 + 3 ) = rdonly;   // fldType
      ar.value (ind * 6 + 4 ) = dp;//   decPoint
      ar.value (ind * 6 + 5 ) = 0;   // reserv
   END;

   macro EvProc(rs, cmd, id, key)
     var  CM_DEFAULT = null;
     if ((cmd == DLG_KEY)and(key == 13/*K_ENTER*/))
       /* if (rs.RecCount > 0)
          return CM_SELECT;
        end;*/
     elif ((cmd == DLG_KEY)and(key == 316/*K_F2*/))
        if ((not amort) and ({oper} == 1) and  (GetTrue(false, "Будут загружены недостающие купоны и обновлены купоны, дата погашения которых больше текущей операционной даты. | Выполнить действие?")))
           if ( execSqlSelect ("select 1 from user_tables where table_name = 'DFIWARNTS_DBT_BACKUP'").moveNext () )
           else
              if ( not execSql ("create table dfiwarnts_dbt_backup as select * from dfiwarnts_dbt where 1<>1") )
                  return false;
              end;
              if ( not execSql ("ALTER TABLE dfiwarnts_dbt_backup ADD (backuptime  timestamp)") )
                  return false;
              end;
           end;
           if ( not execSql ("insert into dfiwarnts_dbt_backup select T_FIID, T_NUMBER, T_NAME, T_DEFINITION, T_DRAWINGDATE, T_RELATIVEINCOME,   T_INCOMERATE, T_INCOMEVOLUME, T_ISCLOSED,    T_FIRSTDATE, T_ISPARTIAL, T_INCOMESCALE,    T_INCOMEPOINT, T_LISTDATE, T_FACTDATE,    T_INCOMEFI, T_SPISCLOSED, T_TSISCLOSED,    T_ID, T_PAYPERIOD, T_DIVIDENDSRATE,    T_VERSION, systimestamp from dfiwarnts_dbt t where t_ispartial = chr(0) and t_fiid = " + Документ.Fiid +";\n") )
               return false;
           end;
           LoadCoupon(Документ.Fiid, rs.Value("id_fintool"), 0);
        elif ((amort)  and ({oper} == 1) and  (GetTrue(false, "Будут загружены недостающие ЧП. | Выполнить действие?")))
           LoadAmortization(Документ.Fiid, rs.Value("id_fintool"), 0);
        end;
     elif ((cmd == DLG_KEY)and(key == 317))
        amort = not amort;
        flag = true;
        return 2; //CM_DEFAULT;
     end;
     return CM_DEFAULT;
   end;
   //MsgBox( "Выполняется макрофункция \"fiwarnts.mac\\ Функция_Пользователя\"." );
    while(flag)
      flag = false;
      if (amort)
         cmd1 = RsdCommand(" select t.* from SOFR_BOND_AMORTIZATIONS t where t.id_fintool = "
                                "     ( select c.t_code from dobjcode_dbt c where c.t_objecttype = 9 and c.t_codekind = 104 " + 
                                "        and c.t_objectid = :1 and c.t_state = 0) order by to_number(id_tranche)");
         cmd1.addParam(":1"  , RSDBP_IN, Документ.Fiid);

         col1 = TArray;   
         arnum = -1;
         AddCol (col1, arnum=arnum+1, "id_tranche", " id_coupon",   8, 0,0);
         AddCol (col1, arnum=arnum+1, "mty_date",    "mty_date",8, 0,0);
         AddCol (col1, arnum=arnum+1, "mty_part",    "mty_part",  8, 0);
         AddCol (col1, arnum=arnum+1, "fix_date",    "fix_date", 8, 0);
         AddCol (col1, arnum=arnum+1, "update_date", "update_date", 8, 0);
         AddCol (col1, arnum=arnum+1, "sysmoment",   "sysmoment",   8, 0);

      else

         cmd1 = RsdCommand(" select t.* from SOFR_BOND_COUPONS t where t.id_fintool = "
                                "     ( select c.t_code from dobjcode_dbt c where c.t_objecttype = 9 and c.t_codekind = 104 " + 
                                "        and c.t_objectid = :1 and c.t_state = 0) order by begin_period");

         cmd1.addParam(":1"  , RSDBP_IN, Документ.Fiid);

         col1 = TArray;   
         arnum = -1;
         AddCol (col1, arnum=arnum+1, "id_coupon",   "id_coupon",   8, 0,0);
         AddCol (col1, arnum=arnum+1, "begin_period","begin_period",8, 0,0);
         AddCol (col1, arnum=arnum+1, "end_period",  "end_period",  8, 0);
         AddCol (col1, arnum=arnum+1, "coupon_rate", "coupon_rate", 8, 0);
         AddCol (col1, arnum=arnum+1, "pay_per_bond","pay_per_bond",8, 0);
         AddCol (col1, arnum=arnum+1, "update_date", "update_date", 8, 0);
         AddCol (col1, arnum=arnum+1, "sysmoment",   "sysmoment",   8, 0);
      end;

      rs1 = RsdRecordSet(cmd1, rsdval_client, rsdval_static);
      if (RunScroll(rs1, arnum+1, col1, "RU DATA" ,"EvProc",ifthenelse(amort,"Частичные погашения", "Купоны")+" RU DATA", ifthenelse({oper}==1,"F2 Загрузить купоны F3 - Купоны/Частичные погашения","F3 - Частичные погашения"), true, 60,1,60,60))
      end;
   end;

   return 0;
END;
