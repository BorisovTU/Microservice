/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v6.00.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcpay40.mac

  Библиотека       : RSACCOUNT

  Описание         : Операция "Оплата по ПД". Шаг "Необходимо доначисление?".

  Программист      : Kirakozov Michael (KMV)

  Создан           : 12.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,  CTInter, InsCarryDoc, globals, mpclb;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);
var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    ContractID = 0,
    DocID = 0,
    TypeDocs = 0;


MACRO ExecuteCaseStep(OperationKind, StepNumber, Document, DocKind)

   var cmd,rs,
       ЗначениеНастройки,
       SumCalc = $0,
       SumFact = $0,
       CntSchedID = 0,
       ChainBeginDate = "",
       ChainEndDate = "";


   SetBuff( cntopr_buf, Document );
   ContractID = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   prccntopr.KeyNum = 0;	
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      //msgbox ("Не найдена операция оплаты");
      PrcSendMessage(ContractID, "Не найдена операция оплаты");
      return 1;
   end; 

   TypeDocs = prccntopr.rec.TypeDocs;
   
   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;	
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      //msgbox ("Не найден договор с ID = " + ContractID);
      PrcSendMessage(ContractID, "Не найден договор с ID = " + ContractID);
      return 1;
   end;   
   
   CntSchedID = GetCntSchedID(ContractID, 3, prccntopr.rec.OperDate);
   if (CntSchedID != 0)
      cntsched.KeyNum = 0;	
      cntsched.rec.CntSchedID = CntSchedID;
      if (not cntsched.GetEQ)
         //msgbox ("Не найден период оплаты на дату" + prccntopr.rec.OperDate);
         PrcSendMessage(ContractID, "Не найден период оплаты на дату" + prccntopr.rec.OperDate);
         return 1;
      end;
   else
      //msgbox ("Не найден период оплаты на дату" + prccntopr.rec.OperDate);
      PrcSendMessage(ContractID, "Не найден период оплаты на дату" + prccntopr.rec.OperDate);
      return 1;
   end;

   SumCalc = money(cntsched.rec.SumCalc);
   
   // 1.1
   if (CheckForEqualPeriod(ContractID, cntsched.rec.periodbegindate, cntsched.rec.periodenddate))
   
      cmd = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE, " +
            "T_PERIODBEGINDATE, T_PERIODENDDATE from DPRCCNTSCHED_DBT " +
            "where T_SCHEDULEKIND = 2 AND T_CONTRACTID = ? AND T_PERIODBEGINDATE " +
            ">= ? and T_PERIODENDDATE <= ?");
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
      ChainBeginDate = cntsched.rec.periodbegindate;
      ChainEndDate = cntsched.rec.periodenddate;
   else
      // 1.2
      cmd = RSDCommand ("select T_SUMFACT, T_DATEREAL, T_DATE, " +
            "T_PERIODBEGINDATE, T_PERIODENDDATE from DPRCCNTSCHED_DBT " +
            "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? and " +
            "((T_SPECIALTYPE <> 1 and T_DATE >= ? and T_DATE <= ?) or " +
            "(T_SPECIALTYPE = 1  AND T_PERIODBEGINDATE >= ? and T_PERIODENDDATE <= ?))");
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodbegindate );
      cmd.addParam( "", RSDBP_IN, cntsched.rec.periodenddate );
   end;       
   
   cmd.execute();
   rs = TRsbDataSet(cmd);
      
   while (rs.movenext())         
      if ((ValType(rs.DateReal) != V_UNDEF) and (rs.DateReal != date(0,0,0)) and (valtype(rs.sumfact) != V_UNDEF))
            SumFact = SumFact + money(rs.sumfact);
      end;   

      if ((ChainBeginDate == "") or (rs.periodbegindate < ChainBeginDate))
         ChainBeginDate = rs.periodbegindate;
      end; 
      if ((ChainEndDate == "") or (rs.periodenddate > ChainEndDate))
         ChainEndDate = rs.periodenddate;
      end; 
      
   end;
   

   // Приходится округлять, иначе RSL может для 2-х одинаковых чисел вернуть false
   if (round(SumFact,4) == round(SumCalc,4)) 
      
      PrcSetOperTypeDocs(DocID, PRC_ACCSCHEMA_NO_ADDCH, TypeDocs, ChainBeginDate, ChainEndDate);
      return "50";
      
   elif (round(SumFact,4) < round(SumCalc,4))
      
      if (ИнитЗначенияНастройкиПроцентов(PRC_REESTR_CHARGEBEFORE, @ЗначениеНастройки) != 0)
            return 1;
         end;
      
      if ((prccontract.rec.ResKind == 1) and (ЗначениеНастройки == false))
         PrcSetOperTypeDocs(DocID, PRC_ACCSCHEMA_ADDCH, TypeDocs, ChainBeginDate,ChainEndDate);
         return "50";
      else
         //msgbox("Необходимо выполнить операцию начисления процентов");
         PrcSendMessage(ContractID, "Необходимо выполнить операцию начисления процентов");
         return 1;
      end;
      
   else   
      
      //msgbox("Сумма начисленных процентов больше суммы процентов к оплате. " +
      //       "Необходимо выполнить операцию доначисления процентов или перерасчет процентов к оплате");
      PrcSendMessage(ContractID, "Сумма начисленных процентов больше суммы процентов к оплате. " +
                                  "Необходимо выполнить операцию доначисления процентов или перерасчет процентов к оплате");
      return 1;
      
   end;

END;


MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */
  exit(1); 

END;

