/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : scnprint.mac
  Created     : 27.04.01
  Description : Печать отчета о печати документов 

└───────────────────────────────────────────────────────────────────────────*/
import "pralcash.mac";

const Ошибка   = 0,
      Первый   = 1,
      Последний = 2,
      ReportLineWidth = 78;

macro Печать_ОтчетСканирования ( Вызов, firstdoc, Статус, Сообщение )
  record pscshdoc (pscshdoc);
  record pm_rmprop (pmrmprop);
  record pm_paym   (pmpaym);

  array  DescriptionArray;
  var    i:integer;

  setbuff (pscshdoc, firstdoc);

  if( Вызов == Первый )
    [                  Отчет о групповой печати документов.
    +---------+------+-----------------------------------------------------------+
    |  Номер  |Номер |                Сообщение                                  |
    |документа|ошибки|                                                           |
    +---------+------+-----------------------------------------------------------+];

  elif( Вызов==Ошибка )
    if( Статус == 0 )
      Сообщение = "Документ успешно напечатан"
    end;

    if ( FindPayment(
      0,      /* Индентификатор неизвестен    */
      PM_PURP_CASHBAL,      
      0,      /* Subpurpose                   */
      pscshdoc.DocKind, /* Вид первичного документа */ 
      pscshdoc.AutoKey,
      true,   /* Поиск по реальной базе       */
      pm_paym,
      null,
      null,
      pm_rmprop
      )       !=      0 )

      Статус = 1;
      Сообщение = "Не найдено платежное поручение";
    end;
    
    StrSplit(StrSubst(Сообщение, "|", " "), DescriptionArray, 59, 59, 1);
    [|#########|######|###########################################################|]
    (pm_rmprop.Number, Статус, DescriptionArray(0));
    i = 1;
    while(i < Asize(DescriptionArray))
      [|         |      |###########################################################|]
      (DescriptionArray(i));
      i = i + 1;
    end;

  elif( Вызов == Последний )
    [+---------+------+-----------------------------------------------------------+];
  end;
end;
