/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : check_nu.mac
  Programmer  : Зорина
  Description : Отчет "Проверка соответствия документов бухгалтерского и
                налогового учета".
  Comment     :
  Modify      : 15.04.02. Богданова
                Новая форма отчета.
└───────────────────────────────────────────────────────────────────────────*/
import BankInter, CTInter, globals;

/*--------- Настройка ------------*/

 ФильтрПоОперуНУ = TRUE;  /* Для проверки документов НУ */

/*--------------------------------*/

FILE tc      ( tc_nd ); /* Таблица соответствий*/
FILE fin     ( fininstr ); /* Финансовые инструменты */
FILE dkey    ( doc_key ) write; /* Врем.ключ.файл */
FILE obchap ( obchaptr );
FILE person ( person );
FILE alg    ( namealg );  /* Названия типов документов */

FILE doc ( document );
FILE adoc( arhdoc );
FILE post   ( postdoc);

FILE doc_v     ( "documnt$.dbt" );
FILE adoc_v    ( "arhdoc$.dbt"  );
FILE post_v    ( "postdoc$.dbt" );

record rec_doc ( document );

//var {oper}, {curdate};

CONST  NameFile = GetWorkFileName( "dkey" );

CONST Num_Chapters = 5, CHAPT6 = 6,
      TypeAlg = 3409;

CONST 
    N_DAT  = "1",  
    N_CHP  = "2",   
    N_CUR  = "3",  
    N_OPER = "4",  
    N_KIND = "5",  
    N_STAT = "6";  

CONST 
TURN_ALL = 1,
TURN_DEB = 2,
TURN_CRD = 3;


CONST 
DOCR   =  1,  /*Проведенные рублевые документы*/        
DOCV   =  2, /*Проведенные валютные документы*/        
ARHR   =  3, /*Архивные    рублевые документы*/        
ARHV   =  4, /*Архивные    валютные документы*/        
POSTR  =  5, /*Отложенные  рублевые документы*/  
POSTV  =  6, /*Отложенные  валютные документы*/  
OBDOCR =  7, /*Проведенные внебал. рублевые документы*/ 
OBDOCV =  8, /*Проведенные внебал. валютные документы*/ 
OBARHR =  9, /*Архивные    внебал. рублевые документы*/    
OBARHV = 10, /*Архивные    внебал. валютные документы*/    
OBPSR  = 11, /*Отложенные  внебал. рублевые документы*/  
OBPSV  = 12; /*Отложенные  внебал. валютные документы*/  

CONST OBJ_ARHDOC = 0,
      OBJ_DOC    = 1;

CONST   /*5.6.6.  Список сообщений.*/
note1 = "Документ находится в списке \"Отложенные\"",
note2 = "Связанный документ БУ не найден",
note3 = "Связанный документ НУ не найден",
note4 = "Несоответствие в статусах документов",
note5 = "Статус документа не соответствует типу обработки",
note6 = "Расхождение сумм документов БУ и НУ",
note7 = "Несоответствие счетов документа НУ данным ТС",
note8 = "Нет ссылки на документ БУ";

CONST
NU = "НУ",
BU = "БУ";

CONST
AllRec = 0,
ErrRec = 1,
ClearRec = 2;

var
  MinSumDoc = /*-$9999999999999.99,*/-$900000000000000.00,
  firstStr = TRUE,
  firstchap = True,
  firstoper = True,
  firstval = True,
  docRec   = FALSE,
  dat1 =  {curdate},
  dat2 =  {curdate},
  f_oper =  9999,
  f_currency =  0,
  CurTek = -1,
  ChapTek = -1,
  OperTek = -1,
  SortDoc = "",
  TipOt,
  CounTab = 0,
  КолБу, СумБу,
  КолНу, СумНу;

/*--------------------------------- */
/*  Дополняет строку num слева нулями до len знаков      */
MACRO LZ( num, len )
 var str1, len1;
    str1 = trim( string( num ) );
    len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else   return  mkstr("0", len-len1 ) + str1; /* слева */
    end;
END;

MACRO RestoreDocID( ObjID, f_rec, ObjType )
 var p_key = keynum( f_rec ), l;
 if( ObjType == OBJ_ARHDOC )
  keynum( f_rec, 5 );
 elif( ObjType == OBJ_DOC )
  keynum( f_rec, 4 );
 end;
 f_rec.iApplicationKind = int( substr( ObjID, 1, 5 ) );
 f_rec.ApplicationKey = LZ( substr( ObjID, 6 ), 6 );
 l = getEQ( f_rec );
 keynum( f_rec, p_key );
 return l;
END;

MACRO GetDocRec( fd, ObjectID, ObjType )
  var l;
  if ( not docRec )
    l = RestoreDocID( ObjectID, fd, ObjType );
/*    if ( not GetDirect( fd, Addr ) ) 
       msgbox("Ошибка чтения записи из файла документа");
       return FALSE; 
    else
       DocRec = TRUE;
    end;*/
    if( not l )
       msgbox("Ошибка чтения записи из файла документа");
       return FALSE;
    else
       DocRec = TRUE;
    end;
  end;
  return TRUE; 
END;


MACRO GetNameKind( kind)

   alg.iTypeAlg   = TypeAlg;
   alg.iNumberAlg = kind;

   if ( GetEq ( alg ) )
       return substr ( alg.szNameAlg, 1, 5 ); 
   else return "";
   end;

END;

MACRO GetNameCur ( Cur, Cod, Name)

  fin.FIID = Cur;
  if ( GetEQ( fin ) )
    setparm ( 1, fin.FI_CODE );
    setparm ( 2, fin.Name );
  else
    msgbox ("Не найден код валюты: " + string(Cur));
  end; 

END;

MACRO GetNameChap ( Chap)

    obchap.Chapter = Chap;
    if ( getEQ( obchap ) ) return obchap.symbol + ". " + obchap.Name;
    else return "";
    end;

END;

MACRO GetNameOper ( Oper)

   Person.Oper = Oper;
   if ( GetEQ( Person) )
      return ( Person.Name );
   else
    msgbox ("Не найден операционист: " + string(Oper));
    return "";
   end; 

END;

MACRO GetNameStatus( stat )
   if   ( stat == 1 ) return "Не скв."; 
   elif ( stat == 2 ) return "Скв.ч.";
   elif ( stat == 3 ) return "Скв.п.";
   else return "Не обр.";
   end;
END;

macro DateLZ( dat )
 var d,m,y;
  datesplit( dat, d, m, y );
  return LZ(y,4) + LZ(m,2) + LZ(d,2);
end;

/*--------------------------------- */
macro GetKindNUAcc(FIID, Account)
Var a;
FILE acc ( account );
FILE accur ( "account$.dbt" );

if ( FIID == 0 ) a = acc;
else           a = accur;
               a.Code_Currency = FIID;
end;   

a.Chapter = Chapt6;
a.Account = Account;
GetEq (a);

return ( a.Kind_Account );
end;

/*--------------------------------- */
macro CheckAccountMask( acc, mask )
   if ( (mask == "") or (CompareStrWithMasks( mask, acc ) == 0) ) 
    return true; 
   end;
   return false;
end;

/*--------------------------------- */
/* Найти корр. счет НУ */
/*macro GetCorAcc( tc, AccountNU )
Var role,rc;
RECORD fiobject (fininstr);
RECORD attr (account);

   if ( tc.DopCorAccNU != "" )
      return ( tc.DopCorAccNU );
   end;

   fiobject.FIID = tc.FIIDNU;
   
   if ( GetKindNUAcc(tc.FIIDNU, AccountNU) == "А" )
      role = OBJROLE_CUR_NUCORACCP;
   else
      role = OBJROLE_CUR_NUCORACCA;
   end;

   if (not GetLinkedObject( role, OBJTYPE_CURRENCY, fiobject, OBJTYPE_ACCOUNT, attr ))
      return( attr.Account );
   else
      return "";
   end;

end;  */

/*--------------------------------- */
/* Найти соотв. запись в ТС */
macro GetTCNU( docFU, docNU )

  macro CheckDocFU() 
       /* Проверка валюты */
   if ( tc.FIIDFU != docFU.Code_Currency ) return FALSE; end;     

       /* Проверка главы */
     if ( tc.ChapterFU != docFU.Chapter ) return FALSE; end;
       /* Проверка корреспонденции */
     if  ( ( tc.TurnKind == TURN_ALL ) or ( tc.TurnKind == TURN_DEB ) ) /* дебет */
       if ( (tc.AccountFU == docFU.Account_Payer) and 
             CheckAccountMask( docFU.Account_Receiver, tc.CorBalanceFU ))
         return TRUE; end;
     end;
     if ( ( tc.TurnKind == TURN_ALL ) or ( tc.TurnKind == TURN_CRD ) )/* кредит */
       if ( (tc.AccountFU == docFU.Account_Receiver) and 
             CheckAccountMask( docFU.Account_Payer, tc.CorBalanceFU ))
         return TRUE; end;
     end;
     return FALSE;
  end; 

  macro ScanTCNU( FIIDNU, AccountNU) 
    var l_next, role; 
    
    keynum( tc, 1);

    tc.FIIDNU    = FIIDNU;
    tc.AccountNU = AccountNU;

    l_next = getGE ( tc );
    while ( l_next and (tc.FIIDNU == FIIDNU) 
                   and (tc.AccountNU == AccountNU) )

      if ( (tc.NotUsed == "") and  CheckDocFU ) 
        return TRUE;
      else
        l_next = next( tc );
      end;
    end;
    return FALSE;
  end; 


  if   ( ScanTCNU( docNU.Code_Currency, docNU.Account_Payer ) ) 
       return TRUE; 
  elif ( ScanTCNU( docNU.Code_Currency, docNU.Account_Receiver )) 
       return TRUE; 
  else return FALSE;
  end;
end;


/*--------------------------------- */
macro FiltrDocRes( Result_Carry )
   if ( ( Result_Carry == 23 ) or
        ( Result_Carry == 14 ) or  /* рублевое покрытие не учитываем */
        ( Result_Carry == 38 ) )  return FALSE;
   else  return TRUE;
   end;
end;

/*--------------------------------- */
MACRO GetSort( fd, flagFile )
  var i = 0, st_sort = "";

  macro  GetOneField( p )
    if   ( p == N_DAT )
      if ( (flagFile ==DOCR) or (flagFile ==DOCV) or 
           (flagFile ==OBDOCR) or (flagFile ==OBDOCV))  
        return DateLZ({curdate});
      else
        return DateLZ(fd.Date_Carry);
      end;
    elif ( p == N_CHP )  
        return LZ( fd.Chapter, 8 );
    elif ( p == N_CUR )  return LZ( fd.Code_Currency, 8 );
    elif ( p == N_OPER ) return LZ( fd.Oper, 8 );
    elif ( p == N_KIND ) return LZ( fd.NU_Kind, 8 );
    elif ( p == N_STAT ) return LZ( fd.NU_Status, 8 );
    else return "";
    end;
  end;


   while( i < strlen(SortDoc) )
      i = i + 1;
      st_sort = st_sort + GetOneField( substr(SortDoc,i,1) );
   end;
 /*  println("sts=",st_sort);*/
   return st_sort;
END;

/*--------------------------------- */
macro  InitDocumentKey( fd, flagFile ) 
   var ObjectID = UniID( fd, OBJTYPE_DOCUMENT );
   dkey.flagFile = flagFile;
   dkey.ObjectID = ObjectID;
   if ( GetEQ( dkey ) ) return; end; /* Документ уже есть во временном файле */

   dkey.Sort = GetSort( fd, flagFile );
   dkey.flagFile = flagFile;
   dkey.ObjectID = ObjectID;
   if (not insert(dkey) )
     msgbox("Ошибка записи во временный файл");
     exit(1);
   end;
end;                  
/*--------------------------------- */

MACRO Header( Date_IN, Date_OUT )
[      Проверка соответствия результатов бухгалтерского и налогового учета
                за период с ########## по ##########.]

( Date_IN, Date_OUT );
END;

MACRO HeadCur( Cur )
Var CurCod, CurName;
GetNameCur (Cur, CurCod, CurName );
[

  Валюта  ###  ####################]

( CurCod, CurName );
END;

MACRO HeadChap( Chap )
[

  Глава  ########################################]

(  GetNameChap ( Chap ) );
END;

MACRO HeadOper( Oper )
[

  Операционист  ###################################]

( GetNameOper ( Oper ) );
END;

MACRO HeadTab (  )
[
 ┌──┬──────────────────────────────────────────────────────────────────────────────────────────────┐
 │Уч│                                            Документ                                          │
 │ет├──────────┬──────────────────────────┬─────────────────────────┬────────────────┬─────┬───────┤
 │  │   Дата   │             Дт           │             Кт          │      Сумма     │ Тип │Статус │];
END;

MACRO PrintItCur( Cur )

[  Итого по валюте    Документов БУ #########  на сумму ###########################
                      Документов НУ #########  на сумму ###########################]

( КолБу, СумБу, КолНу, СумНу );
END;

MACRO Footer
[└──────────────────────────────────────────────────────────────────┴────────────────┴─────┴───────┘];
END;

MACRO PrintNote( text )
  if ( firstStr )
[│Примечания: ######################################################│                │     │       │]
( text);
  else
[│            ######################################################│                │     │       │]
( text);
  end;
END;

MACRO PrintString(       dat, Payer, Receiver, Sum, Kind, Stat,
                         Ndat, NPayer, NReceiver, NSum, NKind, NStat, Note )

if (    (TipOt == AllRec) or
     (  (TipOt == ErrRec) and ( strlen( Note ) > 0 ) )  or
     (  (TipOt == ClearRec) and ( strlen( Note ) == 0 ) )  
   )


   if ( firstStr )
[├──┼──────────┼──────────────────────────┼─────────────────────────┼────────────────┼─────┼───────┤];

[│##│##########│##########################│#########################│################│#####│#######│]
( BU, dat,      Payer,                     Receiver,                 Sum,             Kind, Stat );
[│##│##########│##########################│#########################│################│#####│#######│]
( NU, Ndat,     NPayer,                    NReceiver,                NSum,            NKind,NStat );

      CounTab = CounTab + 1;
      if ( strlen (Payer) > 0)
          КолБу = КолБу + 1;
          СумБу = СумБу + Sum;
      end;

      if ( strlen (NPayer) > 0)
          КолНу = КолНу + 1;
          СумНу = СумНу + NSum;
      end;
   
   end;

   if ( strlen(Note) > 0 )
     PrintNote (Note);
   end;

end;

   if ( firstStr )
      firstStr = false;
   end;

END;


/*Проверка соответствия бухгалтерских документов налоговым*/
MACRO PutDocumentFU( docFU, docNU, dkey, flagNU );
 var  Payer, Receiver, FU_Date_Carry, AccountNU, NU_Date_Carry, stat;

 MACRO CheckRule1( stat )

   if( ( docFU.NU_Status == 1 ) and /* Не сквитован */
       ( docNU.NU_Status != 1 ) )
       /* Несоответствие в статусах */
      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note4 );
   end;

   if( ( ( docFU.NU_Kind == 0 ) and /* Кассовый */
         ( docFU.NU_Status == 2 ) ) or /* Сквитован частично */
         ( docNU.NU_Status == 2     ) )  /* Сквитован частично */
   /* Статус не соотв. типу обработки */
      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note5 );
   end;
        
  if(  ( docFU.NU_Kind == 0 ) and /* Кассовый */
       ( docFU.Sum != docNU.Sum ) ) 
   /* Расхождение сумм документов БУ и НУ */
      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note6 );
  end;

  if( not stat)  /* Несоответствие счетов документа НУ данным ТС */

      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note7 );

  end;

 END;
 
  Payer    = docFU.Account_Payer;
  Receiver = docFU.Account_Receiver;

 if ( ( dkey.FlagFile == ARHR) or 
      ( dkey.FlagFile == OBARHR) or 
      ( dkey.FlagFile == ARHV) or 
      ( dkey.FlagFile == OBARHV) ) 
      FU_Date_Carry = docFU.Date_Carry;
 else FU_Date_Carry = {curdate}; 
 end;

 firstStr = TRUE;

 if( ValType( docNU ) == V_Undef )
      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status),
                  "", "", "", "" ,"","",
                  note3 );
   /* документ НУ не найден */
  return;
 else

   NU_Date_Carry = docNU.Date_Carry;

     /* Найти соотв. запись в ТС */
   stat = GetTCNU( docFU, docNU);

/*   if ( stat )
       AccountNU = tc.AccountNU; 
   elif ( substr(docNU.Account_Payer,1,2) == "80" )
       AccountNU = docNU.Account_Receiver;
   else
       AccountNU = docNU.Account_Payer;
   end; */

   CheckRule1( stat ); 

  if( ( flagNU == OBPSR ) or ( flagNU == OBPSV ) )/* Отложенные */
     /* документ в списке Отложенные */

      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note1 );
  end;

      PrintString( FU_Date_Carry,
                   Payer, Receiver, docFU.Sum, GetNameKind(docFU.NU_Kind),
                  GetNameStatus(docFU.NU_Status), 
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  "" );

 end;

END;


/*------------Проверка соответствия налоговых документов бухгалтерским-------*/

/* Фильтр на корреспонденцию в документе  */

macro FiltrTCPayer( fd, tc )
  if ( not FiltrDocRes(fd.Result_Carry) ) return FALSE;
  elif ( (f_oper != 0 ) and ( fd.Oper != f_oper ) ) return FALSE;
  else  return CheckAccountMask( fd.Account_Payer, tc.CorBalanceFU ); 
  end;
end;

macro FiltrTCReceiver( fd, tc )
  if ( not FiltrDocRes(fd.Result_Carry) ) return FALSE;
  elif ( (f_oper != 0 ) and ( fd.Oper != f_oper ) ) return FALSE;
  else  return CheckAccountMask( fd.Account_Receiver, tc.CorBalanceFU ); 
  end;
end;

/*--------------------------------- */
macro FiltrDat( fd, dat )
  if ( fd.Date_Carry <= dat ) return TRUE;
  else return FALSE;
  end;
end;

macro FiltrDummy( fd, dat )
  return TRUE;
end;


macro FiltrPayer( fd, tc )
  if ( (fd.Account_Payer == tc.AccountFU ) and (fd.Chapter == tc.ChapterFU) )return TRUE;
  else return FALSE;
  end;
end;


macro FiltrPayer_V( fd, tc )
  if ( (fd.Account_Payer == tc.AccountFU) and (fd.Code_Currency == tc.FIIDFU) and
        (fd.Chapter == tc.ChapterFU) )
       return TRUE;
  else return FALSE;
  end;
end;


macro FiltrReceiver( fd, tc )
  if ( (fd.Account_Receiver == tc.AccountFU ) and (fd.Chapter == tc.ChapterFU) )return TRUE;
  else return FALSE;
  end;
end;

macro FiltrReceiver_V( fd, tc )
  if ( (fd.Account_Receiver == tc.AccountFU ) and (fd.Code_Currency == tc.FIIDFU) 
        and (fd.Chapter == tc.ChapterFU) ) return TRUE;
  else return FALSE;
  end;
end;


/*--------------------------------- */
macro ScanDebet(  fd, key_p, flagFile)
  var cont;
  keynum( fd, key_p );

    fd.Account_Payer = tc.AccountFU;
    fd.Chapter = tc.ChapterFU;
    fd.Sum = MinSumDoc;

  cont = getGE( fd );
  while ( cont and FiltrPayer( fd, tc )  and FiltrDat( fd, dat2 ) )
      if ( FiltrTCReceiver( fd, tc ) ) 
          InitDocumentKey( fd, flagFile); end;
      cont = next( fd );
  end;

end;

/*--------------------------------- */
macro ScanDebet_V(  fd, key_p, flagFile )
  var cont;
  keynum( fd, key_p );

    fd.Account_Payer = tc.AccountFU;
    fd.Chapter = tc.ChapterFU;
    fd.Code_Currency = tc.FIIDFU;
    fd.Sum = MinSumDoc;

  cont = getGE( fd );
  while ( cont and FiltrPayer_V( fd, tc ) and FiltrDat( fd, dat2 ) )
      if ( FiltrTCReceiver( fd, tc ) )  
          InitDocumentKey( fd, flagFile); end;
      cont = next( fd );
  end;

end;

/*--------------------------------- */
macro ScanKredit( fd, key_r, flagFile )
  var cont;

  keynum( fd, key_r );

    fd.Account_Receiver = tc.AccountFU;
    fd.Chapter = tc.ChapterFU;

  if ( tc.FIIDFU > 0 )
    fd.Code_Currency = tc.FIIDFU;
  end;
  fd.Sum = MinSumDoc;
  cont = getGE( fd );
  while ( cont and FiltrReceiver( fd, tc )    and FiltrDat( fd, dat2 ) )
      if ( FiltrTCPayer( fd, tc ) )  
          InitDocumentKey( fd, flagFile); end;
      cont = next( fd );
  end;

end;

/*--------------------------------- */
macro ScanKredit_V( fd, key_r, flagFile )
  var cont;

  keynum( fd, key_r );
    fd.Account_Receiver = tc.AccountFU;
    fd.Chapter = tc.ChapterFU;
    fd.Code_Currency = tc.FIIDFU;
    fd.Sum = MinSumDoc;

  cont = getGE( fd );
  while ( cont and FiltrReceiver_V( fd, tc )   and FiltrDat( fd, dat2 ) )
      if ( FiltrTCPayer( fd, tc ) ) 
          InitDocumentKey( fd, flagFile); end;
      cont = next( fd );
  end;

end;

/*--------------------------------- */
macro  ServRub ( DK, Chapter ) /*  Рубли  */
var flaga, flagd;

            if ( chapter == 1) 
               flaga = ARHR;
               flagd = DOCR;
            else
               flaga = OBARHR;
               flagd = OBDOCR;
            end;    

  if ( DK == 0 )
          if( dat1 < {curdate} )
            adoc.Chapter = Chapter;
            adoc.Date_Carry = dat1;
            ScanDebet(  adoc, 2, flaga );
          end;
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ReplaceMacro( "FiltrDat", "FiltrDummy" );
            ScanDebet(  doc, 2, flagd );
            ReplaceMacro( "FiltrDat" );
          end;
  else
          if( dat1 < {curdate} )
            adoc.Chapter = Chapter;
            adoc.Date_Carry = dat1;
            ScanKredit( adoc, 3, flaga );
          end;
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ReplaceMacro( "FiltrDat", "FiltrDummy" );
  ScanKredit( doc, 3, flagd );
            ReplaceMacro( "FiltrDat" );
          end;
  end;
end;                  

/*--------------------------------- */

macro  ServCur ( DK, Chapter ) /*  Валюта  */
var flaga, flagd;

            if ( chapter == 1) 
               flaga = ARHV;
               flagd = DOCV;
            else
               flaga = OBARHV;
               flagd = OBDOCV;
            end;    

  if ( DK == 0 )
          if( dat1 < {curdate} )
            adoc_v.Chapter = Chapter;
            adoc_v.Date_Carry = dat1;
            ScanDebet_V(  adoc_v, 2, flaga );
          end;
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ReplaceMacro( "FiltrDat", "FiltrDummy" );
            ScanDebet_V(  doc_v, 2, flagd );
            ReplaceMacro( "FiltrDat" );
          end;
  else
          if( dat1 < {curdate} )
            adoc_v.Date_Carry = dat1;
            ScanKredit_V( adoc_v, 3, flaga );
          end;
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ReplaceMacro( "FiltrDat", "FiltrDummy" );
            ScanKredit_V( doc_v, 3, flagd );
            ReplaceMacro( "FiltrDat" );
          end;
  end;
end;                  



/*--------------------------------- */
/* Обработка одной строки тавлицы */
MACRO  ServOneTable( tc )
   if ( tc.NotUsed != "" ) return; end;

   if  ( ( tc.TurnKind == TURN_ALL ) or ( tc.TurnKind == TURN_DEB ) ) /* дебет */
               if ( tc.FIIDFU == 0 ) /*  Рубли  */
                  ServRub ( 0, tc.ChapterFU );
               else                  /*  Валюта  */
                  ServCur( 0, tc.ChapterFU );
               end;
   end;

   if ( ( tc.TurnKind == TURN_ALL ) or ( tc.TurnKind == TURN_CRD ) )/* кредит */
               if ( tc.FIIDFU == 0 )     /*  Рубли */
                  ServRub ( 1, tc.ChapterFU );
               else                     /*  Валюта  */
                  ServCur ( 1, tc.ChapterFU );
               end;
   end;

END;


/*--------------------------------- */

/* Проверка соответствия бухгалтерских документов налоговым */

MACRO ScanFileFU( Chapter, Currency )
 var counter = 1, i, l_next;

 InitProgress( NRecords( tc ),"", "Обработка таблицы соответствия" );
 keynum( dkey, 1 );

 tc.AccountFU = "";
 tc.FIIDNU   = 0;
 tc.AccountNU = "";

 if( ( Chapter >  0 ) and ( Currency >= 0 ) ) /* По одной главе и одной валюте */
  tc.ChapterFU = Chapter;
  tc.FIIDFU    = Currency;
  l_next = getGE( tc ) and
           ( tc.ChapterFU == Chapter ) and
           ( tc.FIIDFU    == Currency );
  while( l_next )
   ServOneTable(  tc );
   l_next = next( tc ) and
            ( tc.ChapterFU == Chapter ) and
            ( tc.FIIDFU    == Currency );
   counter = counter + 1;
   UseProgress( counter );
  end;
 elif( ( Chapter >  0 ) and ( Currency <  0 ) ) /* По одной главе и всем  валютам */
  tc.ChapterFU = Chapter;
  tc.FIIDFU    = -1;
  l_next = getGE( tc ) and ( tc.ChapterFU == Chapter );
  while( l_next )
   ServOneTable( tc );
   l_next = next( tc ) and ( tc.ChapterFU == Chapter );
   counter = counter + 1;
   UseProgress( counter );
  end;

 elif( ( Chapter == 0 ) and ( Currency <  0 ) ) /* По всем главам и всем валютам */
  tc.ChapterFU = 1;
  tc.FIIDFU    = -1;
  l_next = getGE( tc ) and ( tc.ChapterFU <= Num_Chapters );
  while( l_next )
   ServOneTable( tc );
   l_next = next( tc ) and ( tc.ChapterFU <= Num_Chapters );
   counter = counter + 1;
   UseProgress( counter );
  end;

 elif( ( Chapter == 0 ) and ( Currency >= 0 ) ) /* По всем главам и одной валюте  */
  i = 1;
  while( i <= Num_Chapters ) /* По всем главам */
   tc.FIIDFU    = Currency;
   tc.ChapterFU = i;
   l_next = getGE( tc ) and
            ( tc.ChapterFU == i ) and
            ( tc.FIIDFU    == Currency );
   while( l_next )
    ServOneTable( tc );
    l_next = next( tc ) and
             ( tc.ChapterFU == i ) and
             ( tc.FIIDFU    == Currency );
    counter = counter + 1;
      UseProgress( counter );
   end;
   i = i + 1;
  end;
 end;
 RemProgress();

END;

/*--------------------------------- */
/* Печать документов БУ */

macro GetDocNU( nd, fd )
Var rc;
    nd.FU_iApplicationKind = fd.iApplicationKind;
    nd.FU_ApplicationKey   = fd.ApplicationKey;

    rc = GetGe ( nd);
    while ( rc and ( nd.FU_iApplicationKind == fd.iApplicationKind ) and
                   ( nd.FU_ApplicationKey   == fd.ApplicationKey ) and
                   ( nd.Chapter == Chapt6) )

        if ( not FiltrDocRes(nd.Result_Carry) )
            rc = next ( nd );
        else
            return True;
        end;
 
    end;

    return false;

end;


/*--------------------------------- */
macro  ServDoc(fd, dkey, ObjType )

   FILE f_obad (arhdoc) key 7;
   FILE f_obd (document) key 6;
   FILE f_obpd (postdoc) key 5;

   FILE f_obadc ("arhdoc$.dbt") key 7;
   FILE f_obdc  ("documnt$.dbt") key 6;
   FILE f_obpdc ("postdoc$.dbt") key 5;

  if ( not GetDocRec( fd, dkey.ObjectID, ObjType ) )  return;   end;

  if ( fd.Code_Currency  == 0 )
    if ( GetDocNU( f_obd, fd ) )   return PutDocumentFU( fd, f_obd,  dkey,OBDOCR  ); end;
    if ( GetDocNU( f_obad, fd ) )  return PutDocumentFU( fd, f_obad, dkey,OBARHR  ); end;
    if ( GetDocNU( f_obpd, fd ) )  return PutDocumentFU( fd, f_obpd, dkey,OBPSR ); end;
  else
    if ( GetDocNU( f_obdc, fd ) )  return PutDocumentFU( fd, f_obdc, dkey,OBDOCV ); end;
    if ( GetDocNU( f_obadc, fd ) ) return PutDocumentFU( fd, f_obadc,dkey,OBARHV ); end;
    if ( GetDocNU( f_obpdc, fd ) ) return PutDocumentFU( fd, f_obpdc,dkey,OBPSV ); end;
  end;

  PutDocumentFU( fd, null, dkey, 0 );

end;

/*--------------------------------- */
macro ServDocKeyFU( dkey )      
 docRec = FALSE;
 if   ( ( dkey.FlagFile == DOCR ) or (dkey.FlagFile == OBDOCR ) )  ServDoc( doc, dkey, OBJ_DOC );
 elif ( ( dkey.FlagFile == ARHR ) or (dkey.FlagFile == OBARHR ) )  ServDoc( adoc, dkey, OBJ_ARHDOC );
 elif ( ( dkey.FlagFile == DOCV ) or (dkey.FlagFile == OBDOCV ) )  ServDoc( doc_v, dkey, OBJ_DOC );
 elif ( ( dkey.FlagFile == ARHV ) or (dkey.FlagFile == OBARHV ) )  ServDoc( adoc_v, dkey, OBJ_ARHDOC );
 end;
end;


/*--------------------------------- */
/* Проверка соответствия налоговых документов бухгалтерским*/

macro GetDocFU( fd, nd )
Var rc;

    fd.iApplicationKind = nd.FU_iApplicationKind;
    fd.ApplicationKey   = nd.FU_ApplicationKey;

    rc = GetGe ( fd);
    while ( rc and ( fd.iApplicationKind == nd.FU_iApplicationKind ) and
                   ( fd.ApplicationKey   == nd.FU_ApplicationKey ) )

        if ( not FiltrDocRes(fd.Result_Carry) )
            rc = next ( fd );
        else
            return True;
        end;
 
    end;

    return false;

end;

/*--------------------------------- */
/* Обработка налоговых документов и запись во врем файл */
macro  ServDocNU(fd, flagFile )

  FILE f_ad (arhdoc) key 5;
  FILE f_d (document) key 4;

  FILE f_adc ("arhdoc$.dbt") key 5;
  FILE f_dc  ("documnt$.dbt") key 4;

  if( fd.FU_ApplicationKey == "" )
       return InitDocumentKey( fd, flagFile); 
  else

   if ( fd.Code_Currency  == 0 )  
    if ( not GetDocFU( f_d, fd ) and
         not GetDocFU( f_ad, fd ) )  
       return InitDocumentKey( fd, flagFile); 
    end;
   else
    if( not GetDocFU( f_dc, fd ) and
         not GetDocFU( f_adc, fd ) )  
       return InitDocumentKey( fd, flagFile); 
    end;
   end;
  end;
end;


/*--------------------------------- */
macro FiltrDocNU( fd )
   if ( not FiltrDocRes(fd.Result_Carry) ) return FALSE;
   elif ( ФильтрПоОперуНУ and ( fd.Oper != f_oper ) ) return FALSE;
   elif ( (f_currency > 0) and ( fd.Code_Currency != f_currency ) ) return FALSE;
   else  return TRUE;
   end;
end;

macro FiltrDatNU( fd )
   elif ( not FiltrDocRes(fd.Result_Carry) ) return FALSE;
   elif ( ФильтрПоОперуНУ and ( fd.Oper != f_oper ) ) return FALSE;
   elif ( (f_currency > 0) and ( fd.Code_Currency != f_currency ) ) return FALSE;
   else  return TRUE;
   end;
end;

/*--------------------------------- */

MACRO ScanFileNU( Chapter, Currency )
 var counter = 0;

 macro ScanNUDocum( fd, flagPs )
  var cont;
  keynum( fd, 1 );

  fd.Chapter = CHAPT6;
  fd.Oper = 0;
  fd.AutoKey = 0;

  cont = getGE( fd );
  while ( cont and ( fd.Chapter == CHAPT6 ) and FiltrDat( fd, dat2 ) )
      if ( FiltrDocNU(fd) )     ServDocNU( fd,flagPs   );      end;
      cont = next( fd );
    counter = counter + 1;
    UseProgress( counter );
  end;

 end;


 if ( Currency <= 0 ) /* по всем валютам или рублям */
   if( dat1 < {curdate} )
     counter =  counter + NRecords( adoc ); end;
   if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
     counter =  counter + NRecords( doc ); end;
   counter =  counter + NRecords( adoc );
 end;
 if ( (Currency < 0) or (Currency > 0 ) ) /* по  валюте  */
   if( dat1 < {curdate} )
     counter =  counter + NRecords( doc_v ); end;
   if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
     counter =  counter + NRecords( post_v ); end;
   counter =  counter + NRecords( post_v );
 end;

 InitProgress( counter,"", "Обработка документов..." );
 keynum( dkey, 1 );
          
 counter = 0;
 if ( Currency <= 0 ) /* по всем валютам или рублям */
          if( dat1 < {curdate} )
            adoc.Date_Carry = dat1;
            ScanNUDocum(  adoc, OBARHR ); 
          end;

          ReplaceMacro( "FiltrDat", "FiltrDummy" );
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ScanNUDocum(  doc, OBDOCR ); 
          end;

          ReplaceMacro( "FiltrDocNU", "FiltrDatNU" );
          ScanNUDocum(  post, OBPSR );       
          ReplaceMacro( "FiltrDocNU" );

          ReplaceMacro( "FiltrDat" );
 end;

 if ( (Currency < 0) or (Currency > 0 ) ) /* по  валюте  */
          if( dat1 < {curdate} )
            adoc.Date_Carry = dat1;
            ScanNUDocum(  adoc_v, OBARHV ); 
          end;

          ReplaceMacro( "FiltrDat", "FiltrDummy" );
          if( (dat1 <= {curdate}) and  (dat2 >= {curdate}) )
            ScanNUDocum(  doc_v, OBDOCV ); 
          end;

          ReplaceMacro( "FiltrDocNU", "FiltrDatNU" );
          ScanNUDocum(  post_v, OBPSV );     
          ReplaceMacro( "FiltrDocNU" );

          ReplaceMacro( "FiltrDat" );
 end;

 RemProgress();

END;

/*--------------------------------- */
/* Печать налоговых документов из врем файла  */

MACRO PutDocumentNU(  docNU, dkey, ObjType )
  var AccountNU = "", NU_Date_Carry;

  if ( not GetDocRec( docNU, dkey.ObjectID, ObjType ) )  return;   end;

/*  if ( substr(docNU.Account_Payer,1,2) == "80" )
        AccountNU = docNU.Account_Receiver;
  else  AccountNU = docNU.Account_Payer;
  end; */

   NU_Date_Carry = docNU.Date_Carry;

 firstStr = TRUE;


if ( (dkey.FlagFile == OBPSR) or  (dkey.FlagFile ==  OBPSV) )

       PrintString( "", "", "", "", "", "",
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind),
       GetNameStatus(docNU.NU_Status),
                  note1 );

end;

 if( docNU.FU_ApplicationKey == "" )

   /* Нет ссылки на документ БУ */
    if ( ( Int(docNU.NU_Kind) == 0 ) or
         ( Int(docNU.NU_Kind) == 1 ) or
         ( ( Int(docNU.NU_Kind) == 2 ) and ( Int(docNU.NU_Status) != 0 ) ) 
        )

       PrintString(  "", "", "", "", "", "",
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note8 );
    else
       PrintString(  "", "", "", "", "", "",
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  "" );
    end;



 else

   /* документ БУ не найден */
      PrintString( "", "", "", "", "", "",
       NU_Date_Carry, docNU.Account_Payer, docNU.Account_Receiver,docNU.Sum,GetNameKind(docNU.NU_Kind), 
                  GetNameStatus(docNU.NU_Status),
                  note2 );

 end;

END;


/*--------------------------------- */
macro  ServDocKeyNU( dkey )      
 docRec = FALSE;
 if   ( dkey.FlagFile == OBDOCR)   PutDocumentNU( doc,  dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBARHR)   PutDocumentNU( adoc, dkey, OBJ_ARHDOC );
 elif ( dkey.FlagFile == OBPSR)    PutDocumentNU( post,  dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBDOCV)   PutDocumentNU( doc_v, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBARHV)   PutDocumentNU( adoc_v, dkey, OBJ_ARHDOC );
 elif ( dkey.FlagFile == OBPSV)    PutDocumentNU( post_v,  dkey, OBJ_DOC );
 end;
end;


/*--------------------------------- */
/* Универсальн.обработчик записей врем файла при печати */

macro ServDocKeyALL( dkey )      

 macro ServDocAll( docNU, dkey, ObjType )
    if ( not GetDocRec( docNU, dkey.ObjectID ) )  return;   end;
    if ( docNU.Chapter != CHAPT6 )  return ServDoc( docNU, dkey, ObjType );
    else                            return PutDocumentNU( docNU, dkey);
    end;
 end;

 docRec = FALSE;
         /*  ServDocKeyFU( dkey )     */ 
 if   ( dkey.FlagFile == DOCR)     ServDoc( doc, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == ARHR)     ServDoc( adoc, dkey, OBJ_ARHDOC );
 elif ( dkey.FlagFile == DOCV)     ServDoc( doc_v, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == ARHV)     ServDoc( adoc_v, dkey, OBJ_ARHDOC );

 elif ( dkey.FlagFile == OBDOCR)   ServDocAll( doc, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBARHR)   ServDocAll( adoc, dkey, OBJ_ARHDOC );
 elif ( dkey.FlagFile == OBPSR)    ServDocAll( post, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBDOCV)   ServDocAll( doc_v, dkey, OBJ_DOC );
 elif ( dkey.FlagFile == OBARHV)   ServDocAll( adoc_v, dkey, OBJ_ARHDOC );
 elif ( dkey.FlagFile == OBPSV)    ServDocAll( post_v, dkey, OBJ_DOC );
 end;
end;

/*--------------------------------- */
macro   PrintHeadChapOper ( ic, io )
Var chap, oper;

macro ChangePar ( N, Tek, Akt, First, PF)

    if ( First )
         setparm (3, false);
    else
         if ( PF ) Footer; end;
    end;

    setparm (1, Akt);

    if ( N == N_CHP ) HeadChap ( Akt ); end;
    if ( N == N_OPER ) HeadOper ( Akt ); end;

end;

if ( ( ic == 0) and (io == 0 )) 
       return;
end;

if ( ic > 0 ) 
  chap = int(substr (  dkey.sort, 8*(ic-1)+1, 8 )) ;
end;

if ( io > 0 ) 
  oper = int(substr (  dkey.sort, 8*(io-1)+1, 8 )) ;
end;


if ( ic and not io )

       if ( ChapTek != chap )
            ChangePar( N_CHP, ChapTek, chap, FirstChap, true );
            HeadTab ();
       end;
end;


if  ( io and not ic )

       if ( OperTek != oper )
            ChangePar( N_OPER, OperTek, oper, FirstOper, true );
            HeadTab ();
       end;
end;



    if ( ic and ( ic < io ) )
       if ( ChapTek != chap )
           ChangePar( N_CHP, ChapTek, chap, FirstChap, true );
           ChangePar( N_OPER, OperTek, oper, FirstOper, false );

           HeadTab ();
       else
              if ( OperTek != oper )
                  ChangePar( N_OPER, OperTek, oper, FirstOper, true );
                  HeadTab ();
              end;

       end;
    end;

    if ( io and ( io < ic) )
       if ( OperTek != oper )
           ChangePar( N_OPER, OperTek, oper, FirstOper, true );
           ChangePar( N_CHP, ChapTek, chap, FirstChap, false );            

           HeadTab ();
       else
          if ( ChapTek != chap )
              ChangePar( N_CHP, ChapTek, chap, FirstChap, true );
              HeadTab ();
          end;
       end;
    end;
         
end;

/*--------------------------------- */
/* Печать из врем файла  */

MACRO PutReport()
 var counter = 0,
     ichap,
     ioper,
     Val;

 КолБу = 0;
 СумБу = $0;
 КолНу = 0;
 СумНу = $0;

 InitProgress( NRecords( dkey ),"", "Печать отчета из временного файла" );


 Header( dat1, dat2);/* Печать заголовка */

 keynum( tc, 1 );
 keynum( dkey, 0 );

 rewind( dkey );
 while( next (dkey ) )
  /*println("sort=",dkey.sort);*/
    Val = int( substr ( dkey.sort,1,8) );

    ichap = index (SortDoc, N_CHP);
    ioper = index (SortDoc, N_OPER);

    if ( CurTek != Val )
        if ( firstVal )
           FirstVal = false;
        else
           Footer;
           PrintItCur ( );
           КолБу = 0;
           СумБу = $0;
           КолНу = 0;
           СумНу = $0;
        end;
           CurTek = Val;
           HeadCur ( Val );
           if ( not ichap and not ioper )
                HeadTab();
           end;
           firstoper = true; OperTek = -1;
           firstchap = true; ChapTek = -1;
    end;



    PrintHeadChapOper ( ichap, ioper );
       
    ServDocKeyAll( dkey );      

    counter = counter + 1;
    UseProgress( counter );
 end;

 
 RemProgress();

 if( counter == 0  )
     println( "Нет данных для отчета." );
 else
     Footer;
     PrintItCur ( );
 end;

END;

/* --------Функция вызывается из программы ----------*/

MACRO PrintReport( Chapter, Currency, Oper, BegDate, EndDate, Sort, fFU, fNU, OtTip )

 dat1 =  BegDate;
 dat2 =  EndDate;
 f_oper =  Oper; 
 f_currency = Currency;

 SortDoc = N_CUR + string(Sort);
 TipOt = OtTip;

 if (not Create(dkey, NameFile)  or  not Open(dkey, NameFile))
     MsgBox ("Невозможно создать временный файл ",NameFile);
     exit(1);
 end;

 if ( fFU )
   ScanFileFU( Chapter, Currency );
   if ( not fNU )
     ReplaceMacro( "ServDocKeyAll", "ServDocKeyFU" );
   end;
 end;

 if ( fNU )

   if ( (f_oper==0) and ФильтрПоОперуНУ )  ФильтрПоОперуНУ = FALSE;
   end;

   ScanFileNU( Chapter, Currency );
   if ( not fFU )
     ReplaceMacro( "ServDocKeyAll", "ServDocKeyNU" );
   end;
 end;

 PutReport();

/* close(dkey);*/

END;

/*------------------------------*/

/*PrintReport( 0, -1, 0, Date(01,1,2002),Date(31,1,2002), 2465, 1,1, 1);*/
        