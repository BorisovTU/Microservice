/*
$Name:           fm_masschpt.mac
$Module:         Ядро ГКБО
$Description:    Дистрибутивные функции пакетной процедуры проверки на терроризм
*/
import XmlRpcInter, FmInter, PTInter, RSD;

import rsbformsinter;

private const FT_INT32  = 1;
private const FT_STRING = 7;
private const FT_DATE   = 9;

private const KEY_F2 = 316;
private const KEY_F3 = 317;
private const KEY_ESC = 27;
private const KEY_SPACE = 32;


class MassCheckPtTerrParm(_List, _PartyType, _PartyKind, _ServiceKind, _CheckTerr, _SkipCheckedTerr, _CheckAddr, _SkipCheckedAddr, _TerrorPercent, _DateUpdate, _DateInsert, _DateDelete)
   var List          = _List       ;
   var PartyType     = _PartyType  ;
   var PartyKind     = _PartyKind  ;
   var ServiceKind   = _ServiceKind;
   var CheckTerr       = _CheckTerr;
   var SkipCheckedTerr = _SkipCheckedTerr;
   var CheckAddr       = _CheckAddr      ;
   var SkipCheckedAddr = _SkipCheckedAddr;

   var TerrorPercent = _TerrorPercent;
   var DateUpdate    = _DateUpdate   ;
   var DateInsert    = _DateInsert   ;
   var DateDelete    = _DateDelete   ;
end;

        
macro GetPtCheckTerrParm(List, PartyType, PartyKind, ServiceKind, CheckTerr, SkipCheckedTerr, CheckAddr, SkipCheckedAddr, TerrorPercent, DateUpdate, DateInsert, DateDelete)
   var strParm  = "";
   var objParm = MassCheckPtTerrParm(List, PartyType, PartyKind, ServiceKind, CheckTerr, SkipCheckedTerr, CheckAddr, SkipCheckedAddr, TerrorPercent, DateUpdate, DateInsert, DateDelete);
   ConvertToXML(objParm, NULL, @strParm);

   return strParm;
end;



PRIVATE MACRO GetPtKindName(ID)

  var Name = "";
  var query:string =  "select T_NAME2 from dptkind_dbt WHERE T_PARTYKIND  = ?";

  var cmd = RSDCommand( query );
  cmd.addParam( "", RSDBP_IN, ID );
  var rs = RsdRecordset( cmd );

  if( rs and rs.moveNext() )
    Name = ( rs.value(0) );
  end;

  return Name;
END;

PRIVATE MACRO GetServKindName(ID)

  var Name = 0;
  var query:string =  "select T_NAME from dservkind_dbt WHERE T_SERVISEKIND = ?";

  var cmd = RSDCommand( query );
  cmd.addParam( "", RSDBP_IN, ID );
  var rs = RsdRecordset( cmd );

  if( rs and rs.moveNext() )
    Name = ( rs.value(0) );
  end;

  return Name;
END;



PRIVATE MACRO DefineDateUpdate(List)
  var query, cmd, rs;

  var DateUpdate = date(0,0,0);
  var DateUpdate2 = date(0,0,0);

  if( (List == 0) OR (List == 1) )
    query =  "select NVL(max(t_DateUpdate),to_date('01.01.0001', 'DD.MM.YYYY')) from dterr_kfm_dbt";

    cmd = RSDCommand( query );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if( rs and rs.moveNext() )
      DateUpdate = date( rs.value(0) );
    end;
  end;


  if( (List == 0) OR (List == 2) )
    query =  "select NVL(max(t_DateUpdate),to_date('01.01.0001', 'DD.MM.YYYY')) from dterror_dbt";

    cmd = RSDCommand( query );
    cmd.NullConversion = true;
    rs = RsdRecordset( cmd );

    if( rs and rs.moveNext() )
      DateUpdate2 = date( rs.value(0) );
    end;
  end;

  if(DateUpdate < DateUpdate2)
    DateUpdate = DateUpdate2;
  end;

  return DateUpdate;
END;

class (CRsbFormConv) FM_CheckPtTerrSel(_ReadOnly)
 initCRsbFormConv();
 var ReadOnly = _ReadOnly;

//  msgbox("ReadOnly =", ReadOnly);
 
  SetSize(72, 11);
  SetPosition(1, 17);

  var labelEditList:TRsbLabel = TRsbLabel( 2, 2, "Вид справочника лиц, причастных к терроризму:" );
  addLabel( labelEditList );
  
  var List = 1;
  var comboList = TRsbComboBox;

  comboList.addChoice( 0, "Все"          );
  comboList.addChoice( 1, "ФСФМ РФ"      );
  comboList.addChoice( 2, "OFAC/SDN List");

  comboList.setPosition(45, 2);
  comboList.setSize(15, 1);
  comboList.dataType = FT_STRING;
  comboList.bindValue( this, "List" );
  addControl( comboList );


  var labelPartyType:TRsbLabel = TRsbLabel( 2, 4, "Виды субъектов для контроля:" );
  addLabel( labelPartyType );

  var PartyType = 0;

  var comboPartyType = TRsbComboBox;

  comboPartyType.addChoice( 0, "Все"      );
  comboPartyType.addChoice( 1, "Юр. лица" );
  comboPartyType.addChoice( 2, "Физ. лица");

  comboPartyType.setPosition(40, 4);
  comboPartyType.setSize(20, 1);
  comboPartyType.dataType = FT_STRING;
  comboPartyType.bindValue( this, "PartyType" );
  addControl( comboPartyType );

  var labelPartyKind:TRsbLabel = TRsbLabel( 2, 5, "Принадлежность субъектов для контроля:" );
  addLabel( labelPartyKind );

  var PartyKind = 0;
  var StrPartyKind = "Все";

  var editPartyKind = TRsbEditField;
  editPartyKind.name       = "fldPartyKind";
  editPartyKind.dataType   = FT_STRING;
  if(ReadOnly == false)
    editPartyKind.rawType    = FBT;
  end;
  editPartyKind.textLength = 30;
  editPartyKind.setPosition(40, 5);
  editPartyKind.setSize    (20, 1);
  editPartyKind.bindValue( this, "StrPartyKind", 30 );
  addControl(editPartyKind);

  var labelServiceKind:TRsbLabel = TRsbLabel( 2, 6, "Вид обслуживания клиентов для контроля:" );
  addLabel( labelServiceKind );

  var ServiceKind     = 0;
  var StrServiceKind = "Все";

  var editServiceKind = TRsbEditField;
  editServiceKind.name     = "fldServiceKind";
  editServiceKind.dataType = FT_STRING;
  if(ReadOnly == false)
    editServiceKind.rawType = FBT;
  end;
  editServiceKind.textLength = 26;
  editServiceKind.setPosition(40, 6);
  editServiceKind.setSize(20, 1);
  editServiceKind.bindValue( this, "StrServiceKind", 26 );

  editServiceKind.Editable  = false;
  addControl( editServiceKind );
  
  var labelCheckTerr:TRsbLabel = TRsbLabel( 2, 8, "Проверять по наименованию:" );
  addLabel( labelCheckTerr );

  var CheckTerr = false;

  var editCheckTerr = TRsbCheckBox;
  editCheckTerr.setPosition(35, 8);
  editCheckTerr.bindValue( this, "CheckTerr" );
  addControl( editCheckTerr );

  var labelSkipCheckedTerr:TRsbLabel = TRsbLabel( 39, 8, "Пропускать проверенных:" );
  addLabel( labelSkipCheckedTerr );

  var SkipCheckedTerr = false;

  var editSkipCheckedTerr = TRsbCheckBox;
  editSkipCheckedTerr.setPosition(64, 8);
  editSkipCheckedTerr.bindValue( this, "SkipCheckedTerr" );
  addControl( editSkipCheckedTerr );


  var labelCheckAddr:TRsbLabel = TRsbLabel( 2, 9, "Проверять совпадение адресов:" );
  addLabel( labelCheckAddr );

  var CheckAddr = false;

  var editCheckAddr = TRsbCheckBox;
  editCheckAddr.setPosition(35, 9);
  editCheckAddr.bindValue( this, "CheckAddr" );
  addControl( editCheckAddr );

  var labelSkipCheckedAddr:TRsbLabel = TRsbLabel( 39, 9, "Пропускать проверенных:" );
  addLabel( labelSkipCheckedAddr );

  var SkipCheckedAddr = false;

  var editSkipCheckedAddr = TRsbCheckBox;
  editSkipCheckedAddr.setPosition(64, 9);
  editSkipCheckedAddr.bindValue( this, "SkipCheckedAddr" );
  addControl( editSkipCheckedAddr );



/*
  addThreads(2,10);
  addPacketSize(30,10);
  AddService(2,11);
*/
 addPacketSize(2,11);

 if(ReadOnly == true)
   comboList.Editable  = false;
   comboPartyType.Editable  = false;
   editPartyKind.Editable  = false;
   editServiceKind.Editable  = false;
   editCheckTerr.Editable  = false;
   editSkipCheckedTerr.Editable  = false;
   editCheckAddr.Editable  = false;
   editSkipCheckedAddr.Editable  = false;
   redraw();
 end; 

  addEventHandler( RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed") );

  macro onKeyPressed( RsbEvent:object )
  
    if( StrUpr(GenClassName(rsbEvent)) == StrUpr("RSB_EV_ENTER_WINDOW") )
      if(ReadOnly == true)
        comboList.Editable  = false;
        comboPartyType.Editable  = false;
        editPartyKind.Editable  = false;
        editServiceKind.Editable  = false;
        editCheckTerr.Editable  = false;
        editSkipCheckedTerr.Editable  = false;
        editCheckAddr.Editable  = false;
        editSkipCheckedAddr.Editable  = false;
        redraw();
      end; 
    end;

    if( StrUpr(GenClassName(rsbEvent)) == StrUpr("TRsbKeyPressEdEvent") )

      if( rsbEvent.KeyCode == KEY_ESC )
        close( rsbEvent.KeyCode );
      end;

      if( (rsbEvent.KeyCode == KEY_F3) AND (ReadOnly == false) )
         if(FocusedControl.Name == "fldPartyKind")
           var selPartyKind    = PartyKind; 
           var selStrPartyKind = StrPartyKind;
           if(PT_ListPTKIND(selPartyKind, selStrPartyKind) == true)
             PartyKind    = selPartyKind;
             editPartyKind.value = selStrPartyKind;

             if((PartyKind == 1) AND (ReadOnly == false))
               editServiceKind.Editable = true;
               editServiceKind.rawType = FBT;
             else
               editServiceKind.Editable = false;
               editServiceKind.rawType = FVT;
             end;
           end;
         elif(FocusedControl.Name == "fldServiceKind")
           var selServiceKind    = ServiceKind; 
           var selStrServiceKind = StrServiceKind;
           if(PT_ListSERVKIND(selServiceKind, selStrServiceKind) == true)
             ServiceKind    = selServiceKind   ;
             editServiceKind.value = selStrServiceKind;
           end;
         end;

         redraw();
      elif( (rsbEvent.KeyCode == KEY_SPACE) AND (ReadOnly == false) )
         if(FocusedControl.Name == "fldPartyKind")
           PartyKind = 0; 
           editPartyKind.value = "Все";

           if((PartyKind == 1) AND (ReadOnly == false))
             editServiceKind.Editable = true;
             editServiceKind.rawType = FBT;
           else
             editServiceKind.Editable = false;
             editServiceKind.rawType = FVT;
           end;

         elif(FocusedControl.Name == "fldServiceKind")
           ServiceKind = 0; 
           editServiceKind.value = "Все";
         end;

         redraw();
      end;
    end;

    return true;
  end;

  macro GetData()
    var retdata = MassCheckPtTerrParm(List, PartyType, PartyKind, ServiceKind, SkipCheckedTerr, NULL, NULL, NULL, NULL, NULL, NULL, NULL);
    return retdata;
  end;

  macro SetData(pObj/*:MassCheckPtTerrParm*/)
//      msgbox("pObj = ",pObj);
    List          = pObj.List       ;
    PartyType     = pObj.PartyType  ;
    PartyKind     = pObj.PartyKind  ;
    ServiceKind   = pObj.ServiceKind;

    var iProp = GenPropID( pObj, "CheckTerr" );
    if( iProp != -1 )
      CheckTerr  = pObj.CheckTerr;
    end;

    iProp = GenPropID( pObj, "SkipCheckedTerr" );
    if( iProp != -1 )
      SkipCheckedTerr  = pObj.SkipCheckedTerr;
    end;

    iProp = GenPropID( pObj, "CheckAddr" );
    if( iProp != -1 )
      CheckAddr  = pObj.CheckAddr;
    end;

    iProp = GenPropID( pObj, "SkipCheckedAddr" );
    if( iProp != -1 )
      SkipCheckedAddr  = pObj.SkipCheckedAddr;
    end;

    if(PartyKind > 0)
      StrPartyKind = GetPtKindName(PartyKind);
    else 
      StrPartyKind = "Все";
    end;

    if(ServiceKind > 0)
      StrServiceKind = GetServKindName(ServiceKind);
    else 
      StrServiceKind = "Все";
    end;

    comboList.currentChoice       = List;
    comboPartyType.currentChoice  = PartyType;

    editPartyKind.value   = StrPartyKind;
    editServiceKind.value = StrServiceKind;
    editSkipCheckedTerr.Checked = SkipCheckedTerr;

    if((PartyKind == 1) AND (ReadOnly == false))
      editServiceKind.Editable = true;
      editServiceKind.rawType = FBT;
    else
      editServiceKind.Editable = false;
      editServiceKind.rawType = FVT;
    end;

    redraw();
    return 0;
  end;

  macro Init()
    List            = 0;
    PartyType       = 0;
    PartyKind       = 0;
    ServiceKind     = 0;
    CheckTerr       = true;
    SkipCheckedTerr = false;
    CheckAddr       = false;
    SkipCheckedAddr = false;


    StrPartyKind = "Все";
    StrServiceKind = "Все";

    comboList.currentChoice     = List;     
    comboPartyType.currentChoice= PartyType;
    editPartyKind.value   = StrPartyKind;
    editServiceKind.value = StrServiceKind;

    editServiceKind.Editable = false;
    editServiceKind.rawType = FVT;

    redraw();
    return 0;
  end;
end;


class (CRsbFormConv) FM_CheckPtTerrExec(ReadOnly)
  initCRsbFormConv();

  SetSize(72, 11);
  SetPosition(1, 17);

  var labelDateUpdate:TRsbLabel = TRsbLabel( 2, 2, "Сканировать справочник тер. с даты обновления:" );
  addLabel( labelDateUpdate );

  var DateUpdate = date(0,0,0);
  DateUpdate = DefineDateUpdate(0);

  var editDateUpdate = TRsbEditField;
  editDateUpdate.setPosition(45, 2);
  editDateUpdate.setSize(10, 1);
  editDateUpdate.dataType = FT_DATE;
  editDateUpdate.bindValue( this, "DateUpdate" );
  addControl( editDateUpdate );


  var labelDateInsert:TRsbLabel = TRsbLabel( 32, 3, "с даты ввода:" );
  addLabel( labelDateInsert );

  var DateInsert = date(0,0,0);

  var editDateInsert = TRsbEditField;
  editDateInsert.setPosition(45, 3);
  editDateInsert.setSize(10, 1);
  editDateInsert.dataType = FT_DATE;
  editDateInsert.bindValue( this, "DateInsert" );
  addControl( editDateInsert );


  var labelDateDelete:TRsbLabel = TRsbLabel( 28, 4, "по дату удаления:" );
  addLabel( labelDateDelete );

  var DateDelete = date(0,0,0);

  var editDateDelete = TRsbEditField;
  editDateDelete.setPosition(45, 4);
  editDateDelete.setSize(10, 1);
  editDateDelete.dataType = FT_DATE;
  editDateDelete.bindValue( this, "DateDelete" );
  addControl( editDateDelete );

  var TerrorPercent = FM_GetTerrorPercent();


  addThreads(2,9);
//  addPacketSize(30,9);
  AddService(2,10);


  addEventHandler( RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed") );

  macro onKeyPressed( RsbEvent:object )
    if( StrUpr(GenClassName(rsbEvent)) == "TRSBKEYPRESSEDEVENT" )
      if( rsbEvent.KeyCode == KEY_ESC )
        close( rsbEvent.KeyCode );
      end;
    end;

    return true;
  end;

  macro GetData()
    var retdata = MassCheckPtTerrParm(0, NULL, NULL, NULL, NULL, NULL, NULL, NULL, TerrorPercent, DateUpdate, DateInsert, DateDelete);
    return retdata;
  end;

  macro SetData(pObj/*:MassCheckPtTerrParm*/)
//    msgbox("pObj = ",pObj);
    TerrorPercent = pObj.TerrorPercent;
    DateUpdate    = pObj.DateUpdate   ;
    DateInsert    = pObj.DateInsert   ;
    DateDelete    = pObj.DateDelete   ;

    editDateUpdate.value = DateUpdate;
    editDateInsert.value = DateInsert;
    editDateDelete.value = DateDelete;

    redraw();
    return 0;
  end;

  macro Init()
    TerrorPercent = FM_GetTerrorPercent();
    DateUpdate = date(0,0,0);
    DateInsert = date(0,0,0);
    DateDelete = date(0,0,0);
    
    DateUpdate = DefineDateUpdate(0);

    editDateUpdate.value = DateUpdate;
    editDateInsert.value = DateInsert;
    editDateDelete.value = DateDelete;

    redraw();
    return 0;
  end;
end;
