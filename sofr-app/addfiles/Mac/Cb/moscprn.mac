/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : moscan.mac
  Created     : 03.05.01
  Description : Отчет печати документов бухгалтерии банка

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;

RECORD mo_firstdoc (memorder);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2,
        ReportLineWidth = 79;

/* Выводим длинную строку Str в поле длиной LineWidth - StrLen( EmptyFlds ) */
/* EmptyFlds - предыдущие пустые колонки                                    */
/* LineWidth - общая длина строки отчета                                    */
macro WriteLongStr( Str, EmptyFlds, LineWidth, EndSymb )
  var i, FieldWidth = LineWidth - StrLen( EmptyFlds ) - 1, NumItems;
  array Strokes;

  macro AppendSpace( Str, Len, EndSymb )
    var slen = strlen( Str ), rstr;
    return Str + MkStr( " ", Len-slen ) + EndSymb;
  end;

  StrSplit( Str, Strokes, FieldWidth );
  println( AppendSpace( Strokes( 0 ), FieldWidth, EndSymb ) );
  i = 1;
  NumItems = Asize( Strokes );
  while( i < NumItems )
    println( AppendSpace( EmptyFlds + Strokes( i ), LineWidth - 1, EndSymb ) );
    i = i + 1;
  end;
end;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);

        setbuff (mo_firstdoc,firstdoc);

var stat;
        if (Вызов==Первый)
[            Отчет о групповой печати документов бухгалтерии банка.];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Документ успешно напечатан";
                end;
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_BANKPAYMENT,      
                        0,      /* Subpurpose                   */
                        mo_firstdoc.DocKind, /* Вид первичного документа */ 
                        mo_firstdoc.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                end;

      print("|", pm_rmprop.Number:9, "|", Статус:6, "|");

      /* Длинную строку Сообщения разбиваем по пробелам */
      WriteLongStr( StrSubst( Сообщение, "|", " " ), "|         |      |", ReportLineWidth, "|" );
                
        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;