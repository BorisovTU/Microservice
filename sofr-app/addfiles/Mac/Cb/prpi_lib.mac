/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : prpi_lib.mac
  Created     : 16.11.2007
  Programmer  : Попова О.
  Description : Библиотека для печати МО разнесения суммы
                по уточняющим записям платежа(ф № 277)

└───────────────────────────────────────────────────────────────────────────*/

import FIInter, PaymInter, likepy, globals, oralib, prpmbuff;

private const NATCUR = 0;

private MACRO DateStr( _date:date )
  
  var _dateStr, dd, mm, yy;

  datesplit(_date, dd, mm, yy);
  _dateStr = string(dd:2:o);
  
  if( mm == 1  ) _dateStr = _dateStr + " января "  ; end;              
  if( mm == 2  ) _dateStr = _dateStr + " февраля " ; end;
  if( mm == 3  ) _dateStr = _dateStr + " марта "   ; end;
  if( mm == 4  ) _dateStr = _dateStr + " апреля "  ; end;
  if( mm == 5  ) _dateStr = _dateStr + " мая "     ; end;
  if( mm == 6  ) _dateStr = _dateStr + " июня "    ; end;
  if( mm == 7  ) _dateStr = _dateStr + " июля "    ; end;
  if( mm == 8  ) _dateStr = _dateStr + " августа " ; end;
  if( mm == 9  ) _dateStr = _dateStr + " сентября "; end;
  if( mm == 10 ) _dateStr = _dateStr + " октября " ; end;
  if( mm == 11 ) _dateStr = _dateStr + " ноября "  ; end;
  if( mm == 12 ) _dateStr = _dateStr + " декабря " ; end;

  return _dateStr + string(yy) + " г.";
END;

private MACRO GetCurrencyBuf(fi:TRecHandler):integer

  var CurID;
  if( pr_pmpaym.rec.BaseFIID != NATCUR )
    CurID = pr_pmpaym.rec.BaseFIID;
  elif( pr_pmpaym.rec.FIID != NATCUR )
    CurID = pr_pmpaym.rec.FIID;
  elif( pr_pmpaym.rec.PayFIID != NATCUR )
    CurID = pr_pmpaym.rec.PayFIID;
  else
    CurID = NATCUR;
  end;

  return ПолучитьФинИн(CurID, fi); 
END;  

/* класс проводки по уточняющей записи */
CLASS PICarry(carry:TRecHandler, pi:TRecHandler, fiid:integer)

  private var _isCredit:integer  = pi.rec.DebetCredit,            
              _carry:TRecHandler = TRecHandler("acctrn.dbt",  "bank.def"),
              _pi   :TRecHandler = TRecHandler("pmaddpi.dbt", "bank.def"),
              _fiid:integer      = fiid;

  Copy(_carry, carry);
  Copy(_pi,    pi);

  macro СчетДебета(): string
    return IfThenElse( _isCredit, _carry.rec.Account_Payer, _pi.rec.Account );
  end;
  macro СчетКредита():string
    return IfThenElse( _isCredit, _pi.rec.Account , _carry.rec.Account_Receiver );
  end;

  macro СуммаВал(fiid:integer)
    return IfThenElse( _carry.rec.FIID_Payer != 0/*NATCUR*/, _carry.rec.Sum_Payer, _carry.rec.Sum_Receiver );
  end;  
  macro СуммаРуб()        
    return _carry.rec.Sum_NatCur;
  end;
  /*для ф 203*/
  macro Основание()
    return _pi.rec.Ground;
  end;  
  /*получает сумму дебета/кредита и соответствующие валюты*/
  macro GetAmountsAndFIID(DebetAmount, CreditAmount, DebetFIID, CreditFIID)
    SetParm(1, IfThenElse(_pi.rec.DebetCredit, _pi.rec.PmAmount, _pi.rec.Amount  ) );
    SetParm(2, IfThenElse(_pi.rec.DebetCredit, _pi.rec.Amount,   _pi.rec.PmAmount) );
    SetParm(3, IfThenElse(_pi.rec.DebetCredit, _pi.rec.PmFIID,   _pi.rec.FIID    ) );
    SetParm(4, IfThenElse(_pi.rec.DebetCredit, _pi.rec.FIID,     _pi.rec.PmFIID  ) );  
 end;
END;

// Получить атрибуты проводки по уточняющей записи
private macro GetSumCarryes( carry:TRecHandler, pi:TRecHandler ):integer

  var select:string = " select tr.t_sum_payer, tr.t_sum_receiver, tr.t_sum_natcur, tr.t_fiid_payer," +  
                      "        tr.t_account_payer, tr.t_account_receiver "
                      " from dpmaddpi_dbt pi, dpmdocs_dbt pd, dacctrn_dbt tr " +
                      " where pi.t_pmaddpiid = :id "+
                      "   and pd.t_paymentid = pi.t_paymentid " +
                      "   and tr.t_acctrnid = pd.t_acctrnid " +
                      "   and tr.t_state in (1,2) "+
                      "   and tr.t_result_carry <> 83 "+
                      "   and tr.t_sum_payer <> 0 "+
                      "   and tr.t_sum_receiver <> 0 ";
  if( pi.rec.DebetCredit )
    select = select + "   and tr.t_account_receiver = :acc "+
                      "   and tr.t_fiid_receiver    = :fiid ";
  else
    select = select + "   and tr.t_account_payer = :acc "+
                      "   and tr.t_fiid_payer    = :fiid ";
  end;

  var params:TArray = makeArray( SQLParam( "id"  , pi.rec.PmAddPIID ),
                                 SQLParam( "acc" , pi.rec.Account   ),
                                 SQLParam( "fiid", pi.rec.FIID      ) );
  
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    carry.rec.Sum_Payer      = rset.value(0);
    carry.rec.Sum_Receiver   = rset.value(1);
    carry.rec.Sum_NatCur     = rset.value(2);
    carry.rec.FIID_Payer     = rset.value(3);
    carry.rec.Account_Payer     = rset.value(4);
    carry.rec.Account_Receiver  = rset.value(5);
    return 0;
  else
    return 1;
  end;
  
ONERROR(x)
  MsgBox( x.Message );
end;

private macro ExistsCarryesOnPaym( PaymentID:integer )

  var select:string = " select 1 " +  
                      " from dpmpaym_dbt pm, dpmdocs_dbt pd, dacctrn_dbt tr " +
                      " where pm.t_paymentid = :id "+
                      "   and pd.t_paymentid = pm.t_paymentid " +
                      "   and tr.t_acctrnid = pd.t_acctrnid " +
                      "   and tr.t_state in (1,2) "+
                      "   and tr.t_result_carry <> 83 "+
                      "   and tr.t_sum_payer <> 0 "+
                      "   and tr.t_sum_receiver <> 0 ";

  var params:TArray = makeArray( SQLParam( "id", PaymentID ) );
  
  return existsSQLselect( select, params );

ONERROR(x)
  MsgBox( x.Message );
end;
/*класс документа с уточняющими записями*/
CLASS PmAddPIDoc()

  private var PmPaym:RsbPayment = RsbPayment(pr_pmpaym.rec.PaymentID),
              _fi:TRecHandler   = TRecHandler("fininstr.dbt", "bank.def"),
              _isNeedf203:bool  = false,
              _CarryList        = TArray,
              _err = 0;

  macro Номер()
    return pr_pmrmprop.rec.Number;
  end;
  macro Дата()
    return DateStr(pr_pmpaym.rec.ValueDate);
  end;
  macro Назначение()
    return pr_pmrmprop.rec.Ground; 
  end;

  macro fiName():string
    return _fi.rec.Name;
  end;

  macro isCurr():bool
    return _fi.rec.FIID;
  end;

  macro ISO_Number()
    return int(_fi.rec.ISO_Number);
  end;       

  macro СуммаВал()
   var SumCur = $0,
       i:integer;          
   for(i, 0, _CarryList.Size - 2 )
     SumCur = SumCur + _CarryList[i].СуммаВал();
   end;
   return  SumCur;
  end;

  macro СуммаРуб()
   var SumRub = $0,
       i:integer;  
   for(i, 0, _CarryList.Size - 2 )
     SumRub = SumRub + _CarryList[i].СуммаРуб();
   end;
   return  SumRub;
  end;

  macro GetCarry(Numb:integer)
    if( Numb <= _CarryList.Size )
      return _CarryList[Numb];
    else 
      return 0;
    end;
  end;
  macro isNeedf203()
    return _isNeedf203;
  end;
  private macro ЗаполнитьСписокПроводок():integer

    var carry = TRecHandler("acctrn.dbt"  , "bank.def"), /*проводка по документу*/
        pi    = TRecHandler("pmaddpi.dbt" , "bank.def"),       
        isCredit = -1, //По какой стороне берём уточняющие записи?
        isFirstStr = true,
        stat = 0;                                         

    if  (PmPaym.PIList( 0 ).Size() > 0)
          isCredit = 0;
    elif(PmPaym.PIList( 1 ).Size() > 0)
          isCredit = 1;
    end;
    
    if( isCredit == -1 ) 
      MsgBox("Разнесение суммы платежа не производилось");
      return 1;
    end;

    if( not ExistsCarryesOnPaym(PmPaym.PaymentID ) ) /*проводок еще не было - формировать отчет нельзя*/
      MsgBox("Отчет не может быть сформирован до выполнения всех |проводок по уточняющим записям в документе");
      return 1;
    else  /*находим уточняющие записи платежа*/
       
      if( PmPaym.PIList( isCredit ).First() == 0 )
        stat = PmPaym.PIList( isCredit ).Current( pi );
        /*если уточняющая запись мультивалютная - печатаем ф203*/
        if( isCurr() and (pi.rec.PmFIID != pi.rec.FIID)
                     and (pi.rec.PmFIID != NATCUR)
                     and (pi.rec.FIID   != NATCUR))
          _isNeedf203 = true;
        end;                        
        if( ( stat == 0 ) and not GetSumCarryes( carry, pi ) )/*заполняем первую запись списка проводок*/
          _CarryList[0] = PICarry(carry, pi, _fi.rec.FIID); 
        end;                 
      end;
      while( (PmPaym.PIList( isCredit ).Next == 0) and (stat == 0) )/*заполняем список проводок дальше*/   
        stat = PmPaym.PIList( isCredit ).Current( pi ); 
        if( isCurr() and (pi.rec.PmFIID != pi.rec.FIID)  
                     and (pi.rec.PmFIID != NATCUR)
                     and (pi.rec.FIID   != NATCUR))
          _isNeedf203 = true;
        end;                          
        if( not GetSumCarryes( carry, pi ) )
          /* очищаем, чтобы не выводить в последующих строках счет неразнесенной стороны */
          carry.rec.Account_Payer = carry.rec.Account_Receiver = "";  
          _CarryList[_CarryList.Size] = PICarry(carry, pi, _fi.rec.FIID);
        end;
      end;
    end;
    if( _CarryList.Size == 0 )  /*проводок еще не было - формировать отчет нельзя*/
      MsgBox("Отчет не может быть сформирован до выполнения всех |проводок по уточняющим записям в документе");
      return 1;
    end;
    return stat;
  end;

  macro GetErr()
    return _err;
  end;
  
  if( GetCurrencyBuf(_fi) != 0 )
    MsgBox("Ошибка при получении финансового инструмента платежа");
    _err = 1;
  end;
  _err = ЗаполнитьСписокПроводок();

END;

/*печать отчета*/

MACRO PrintHeaderReport(документ:PmAddPIDoc)
 [                                                                                         
                                                                                          
                        Мемориальный ордер № ########                              ф.№ 277 
                                                                                          
                                                                                          
                            ##################                                            
                                                                                           ](документ.Номер:l, документ.Дата:c);
  if( документ.isCurr )
 [   Валюта:  ############################################################################ ](документ.fiName:l);
  end;
 [   Назначение платежа (содержание операции):                                             
       ################################################################################### 
                                                                                           ](документ.Назначение:l:w);

END;

MACRO PrintHeaderTable(isCurr:bool)

  if(isCurr)
 [ ┌─────────────────────────┬─────────────────────────┬──────────────────┬──────────────────┐
   │                         │                         │                  │ Сумма в рублевом │
   │     Дебет счета         │      Кредит счета       │  Сумма в валюте  │    эквиваленте   │  
   ├─────────────────────────┼─────────────────────────┼──────────────────┼──────────────────┤
 ];                          
  else                       
 [ ┌─────────────────────────┬─────────────────────────┬──────────────────┐
   │      Дебет счета        │      Кредит счета       │    Сумма, руб    │
   ├─────────────────────────┼─────────────────────────┼──────────────────┤
 ];                                                                                       
  end;
END;

MACRO PrintLinesTable(проводка:PICarry, isCurr:bool)

  if(isCurr)
 [ │#########################│#########################│##################│##################│]
   ( проводка.СчетДебета(),   проводка.СчетКредита(),  string(проводка.СуммаВал():f):r,string(проводка.СуммаРуб():f):r  );    
  else
 [ │#########################│#########################│##################│]
   (  проводка.СчетДебета(),  проводка.СчетКредита(),   string(проводка.СуммаРуб():f):r);
  end;

END;
MACRO PrintFooterTable(документ:PmAddPIDoc)

  if(документ.isCurr)                                                                        
 [ ├─────────────────────────┴─────────────────────────┼──────────────────┼──────────────────┤
   │ Итого                                             │##################│##################│                                        
   └───────────────────────────────────────────────────┴──────────────────┴──────────────────┘]
                                               ( string(документ.СуммаВал:f):r,  string( документ.СуммаРуб:f):r );
  else                                                                    
 [ ├─────────────────────────┴─────────────────────────┼──────────────────┤
   │ Итого                                             │##################│
   └───────────────────────────────────────────────────┴──────────────────┘]
                                             ( string(документ.СуммаРуб:f):r);
  end;
END;
MACRO PrintTable(документ:PmAddPIDoc)

  PrintHeaderTable(документ.isCurr);
  
  var i = 0;
  while( документ.GetCarry(i) )
    PrintLinesTable(документ.GetCarry(i), документ.isCurr);
    i = i + 1;
  end;
  
  PrintFooterTable(документ);

END;
 
MACRO PrintFooterReport(документ:PmAddPIDoc)
 [ 
   Сумма прописью: #######################################################################
   
   Гл. бухгалтер:  __________________________                                              
 
   Бухгалтер:      __________________________                                             

   Контроллер:     __________________________                                             
 
 ](CurToStrAlt(документ.СуммаВал,null,null,документ.ISO_Number));

END;

MACRO PrPIReport_f277(документ:PmAddPIDoc)

  PrintHeaderReport( документ );
  PrintTable( документ );
  PrintFooterReport( документ );

END;
