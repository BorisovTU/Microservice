/*
$Name:        ws_ssusage_snils.mac
$Module:      Web-общее
$Description: Макрос для вызова сервиса "Простые сервисы. Валидация СНИЛС" RS-Connect
*/

import XmlRpcInter;
import rcw;

////////////////////////////////////* Создание java-объектов и вызов сервисов *//////////////////////////
private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");

macro CreateHeaderSSASRequestJava( )
  var javaObject = jvm.CreateJavaObject( "ru.softlab.rsbank.SsSource","<init>" );
  return javaObject;
end;

/* Объект запрос Валидации СНИЛС */
macro CreateSSASRequestSNILSJava( )
  var javaObject = jvm.CreateJavaObject( "ru.softlab.rsbank.SsasRequestSNILS","<init>" );
  return javaObject;
end;

/* Сервис валидации СНИЛС */
macro callCheckSnils ( SSSource, SSASRequest, urlAddress : string )
  var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V",  urlAddress);
  var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
  var port = proxy.getIntegrationWSPort();
  var result = port.wsSsCheckSNILS(SSSource, SSASRequest);
  return result;
end;

/* Сервис получения результатов валидации СНИЛС */
macro callGetSnils (SSSource, ObjectID, MessageInitID, urlAddress : string )
  var url = jvm.createJavaObject("java.net.URL", "<init>@(Ljava/lang/String;)V",  urlAddress);
  var proxy = jvm.createJavaObject("ru.softlab.rsbank.IntegrationWS", "<init>@(Ljava/net/URL;)V", url);
  var port = proxy.getIntegrationWSPort();
  var result = port.wsSsGetSNILS(SSSource, ObjectID, MessageInitID);
  return result;
end;

////////////////////////////////////* Классы-обёртки *///////////////////////////////////////////////////
/* Заголовк запроса */
class SSHead
   var MessageID		:string;
   var SenderID			:string;
   var SenderPointID	:string,  
end;

/* Параметры валидации СНИЛС */
class SSCheckSNILSParam
   var SNILS		: string;
   var Family		: string;
   var Name			: string;
   var Patronomic	: string;
   var Gender		: string;
   var Birthdate	: string;
end;

/* Параметры валидации СНИЛС */
class SSGetSNILSParam
   var ObjectID			: string;
   var MessageInitID 	: string;
end;

/* Параметры ответа сервиса*/
class SSDescription
	/* Структура ошибки СМЭВ */
	class SSExtError
		var returnCode 		: string;
		var returnText 		: string;
	end;
	/* Структура ответа СМЭВ*/
	class SSResponse
		var result 		: string;
	end;
	
	var messageRevID		: string;		// GUID ответа
	var messageSendID 		: string;		// GUID стартового сообщения
	var objectError			: string;		// Ошибка, возвращенная из Коннекта
	var objectId			: string;		// ИД запроса в Коннекте
	var extError			: SSExtError;	// Ошибка, полученная из СМЭВ
	var senderId			: string;
	var senderPointID		: string;
    var statusCode			: string;
    var statusName			: string;
	var response			: SSResponse;

end;

/////////////////////////////* Заполнение классов-обёрток *////////////////////////////////////////////
macro fill_SSHead (_MessageID, _SenderID, _SenderPointID) : SSHead
	var _sshead : SSHead;
	_sshead.MessageID 			= _MessageID;
	_sshead.SenderID 			= _SenderID;
	_sshead.SenderPointID 		= _SenderPointID;
	return _sshead;
end;

macro fill_SSCheckSNILSParam (_SNILS : string, _Family : string, _Name : string, _Patronomic : string, _Gender : string, _Birthdate : string) : SSCheckSNILSParam
	var _checkParam : SSCheckSNILSParam;
	_checkParam.SNILS 			= _SNILS;
	_checkParam.Family		= _Family;
	_checkParam.Name		= _Name;
	_checkParam.Patronomic 		= _Patronomic;
	_checkParam.Gender 			= _Gender;
	_checkParam.Birthdate 		= _Birthdate;
	return _checkParam;
end;

macro fill_SSGetSNILSParam (_ObjectID : string, _MessageInitID : string) : SSGetSNILSParam
	var _getParam : SSGetSNILSParam;
	_getParam.ObjectID = _ObjectID;
	_getParam.MessageInitID = _MessageInitID;
	return _getParam;
end;

macro fill_SSDescription (_SSASDescription) : SSDescription
	var _description : SSDescription;
	_description.messageRevID 			= _SSASDescription.messageRevID;
	_description.messageSendID 			= _SSASDescription.messageSendID;
	_description.objectError 			= _SSASDescription.objectError;
	_description.objectID 				= _SSASDescription.objectID;
	_description.extError.returnCode 	= _SSASDescription.SSExtError.returnCode;
	_description.extError.returnText 	= _SSASDescription.SSExtError.returnText;
	_description.senderID 				= _SSASDescription.senderID;
	_description.senderPointID 			= _SSASDescription.senderPointID;
	_description.statusCode 			= _SSASDescription.statusCode;
	_description.statusName 			= _SSASDescription.statusName;
	_description.response.result 		= _SSASDescription.SSASResponse.result;
	return _description;
end;
///////////////////////////////* Вызываемые функции *//////////////////////////////////////////////////
macro CheckSnils (urlAddress : string, head : SSHead, param : SSCheckSNILSParam
): SSDescription
	var _sshead = CreateHeaderSSASRequestJava();
		_sshead.messageID 			= head.MessageID;
		_sshead.senderID 			= head.SenderID;
		_sshead.senderPointID 		= head.SenderPointID;
	var _checkParam = CreateSSASRequestSNILSJava();
		_checkParam.SNILS 			= param.SNILS;
		_checkParam.lastName		= param.Family;
		_checkParam.firstName		= param.Name;
		_checkParam.patronymic 		= param.Patronomic;
		_checkParam.gender 			= param.Gender;
		_checkParam.birthdate 		= param.Birthdate;
	return fill_SSDescription(callCheckSnils(_sshead, _checkParam, urlAddress));
end;

macro GetSnilsResult (urlAddress : string, head : SSHead, param : SSGetSNILSParam): SSDescription
	var _sshead = CreateHeaderSSASRequestJava();
		_sshead.messageID 			= head.MessageID;
		_sshead.senderID 			= head.SenderID;
		_sshead.senderPointID 		= head.SenderPointID;
	return fill_SSDescription(callGetSnils(_sshead, param.ObjectID, param.MessageInitID, urlAddress));
end;

//////////////////////////////////* Пример использования */////////////////////////////////////////////

/*
//////////////////// Первоначальный запрос валидации СНИЛС
// создание объекта заголовка запроса
var a = fill_SSHead(substr(CreateGUID(), 2, 36), "1", "Пункт");
// создание объекта тела запроса
var b = fill_SSCheckSNILSParam ("027-733-198 62", "ПЕТИНА", "ЕЛЕНА", "ВЛАДИМИРОВНА", "F", "15.11.1980");
// вызов сервиса валидации СНИЛС
var checkresult = CheckSnils("http://serpkov:8084/RsConnect/ws/integration?wsdl", a, b);

//////////////////// Получение результата по возвращенному objectID
// заполнение тела запроса результата по возвращенному objectID
var c = fill_SSGetSNILSParam (checkresult.objectID, "");
// вызов сервиса получения результата ранее запрошенной валидации (по objectID)
var getresultByObjId 	= GetSnilsResult("http://serpkov:8084/RsConnect/ws/integration?wsdl", a, c);

//////////////////// Получение результата по GUID стартового сообщения 
// заполнение тела запроса результата по GUID стартового сообщения
var d = fill_SSGetSNILSParam ("", a.messageID);
// вызов сервиса получения результата ранее запрошенной валидации (по GUID стартового сообщения)
var getresultByInitID 	= GetSnilsResult("http://serpkov:8084/RsConnect/ws/integration?wsdl", a, d);
*/
