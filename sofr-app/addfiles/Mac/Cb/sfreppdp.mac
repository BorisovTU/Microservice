/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    Протокол оплаты ПЗО

+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfcommon;

file sfrepsim_write( "sfrepsim.tmp" ) write ;

var DepPartyID;

macro GetOperName(op)

   FILE person (person) key 0;

   person.Oper = op;
   if ( GetEQ(person) )
      return person.Name;
   end;
   return  "?" ;

end;

macro GetDepartName(PartyID)
	FILE party (party) key 0;

   party.PartyID = PartyID;
   if (GetEQ(party))
      return party.Name;
   end;
   return  "?" ;
end;

macro PayReportPrintCommonHeader(ComCode, ComProp, DateFee)
        
[Сокращенный протокол расчета комиссии];
[Дата формирования  - ##########](date());
[Время формирования - ########](time());
[Операционист: #### ############################################]({oper}, GetOperName({oper}));
[ ];
[Параметры запуска:];
[Филиал - по всем филиалам];
[Признак - ##############################](ComProp);
[Комиссия - ################](ComCode);
[Дата взимания - ##########](DateFee);
[ ];
[Результаты выполнения:];
[ ];
   
    return 0;
end;/*ReportPrintCommonHeader*/

macro PayReportPrintHeader( sfrepsim )

  file sfcomiss( sfcomiss )key 0;

  sfcomiss.FeeType = sfrepsim.FeeType;
  sfcomiss.Number  = sfrepsim.CommNumber;

  if( not GetEQ( sfcomiss ) )
    MsgBox( "Не найдена комиссия" );
    return 1;
  end;

[ ];
[Филиал - ####################################](GetDepartName(DepPartyID));
[ ];
[Ведомость оплаты комиссии ################]( sfcomiss.Code );
[+---------------------------+-------------------------+-------------------------------------+-----------------------+];
[| Объект начисления         |        Период           |             Сумма                   |    Сформированный     |];
[|                           |    с       |    по      |                                     |      документ         |];
[+---------------------------+------------+------------+-------------------------------------+-----------------------+];
    
  return 0;
end;/*PayReportPrintHeader*/


macro PayReportPrintLine( sfrepsim ) 
  file fininstr( fininstr )key 0;
  record contr(sfcontr);

  fininstr.FIID = sfrepsim.FIID_Sum;
  if( not GetEQ( fininstr ))
    MsgBox("Не найдена валюта ", sfrepsim.FIID_Sum );
    return 1;
  end;

  if( not SfGetContr(sfrepsim.SfcontrID, contr ) )
    MsgBox( "Не найден договор" );
    return 1;
  end;

[| ######################### | ########## | ########## | ################### ############### | ##################### |]
( sfrepsim.Object, sfrepsim.BeginDate, sfrepsim.EndDate, sfrepsim.CommSum + sfrepsim.NDSSum, fininstr.FI_Code, "Требование на оплату" );

  return 0;
end;/*PayReportPrintLine*/

macro PayReportPrintFooter( sfrepsim )
[+---------------------------+------------+------------+-------------------------------------+-----------------------+];
[  ];
  return 0;
end;/*PayReportPrintFooter*/


macro PayReportBegin(DepartPartyID)
  var stat = OpenReportGlobalFile( sfrepsim_write, true );
  DepPartyID = DepartPartyID;
  return  stat;
end;/*PayReportBegin*/


macro PayReportEnd()

  file sfrepsim( "sfrepsim.tmp" ) key 0;
  var FeeTypeCurrent = 0;
  var CommNumberCurrent=0;

  if( OpenReportGlobalFile( sfrepsim, false ) )
    return 1;
  end;
    
  if (NRecords(sfrepsim) == 0)
    println("Филиал - ", GetDepartName(DepPartyID));
    println("С указанными параметрами оплачиваемых комиссий нет");
    println;
  end;

  while( next( sfrepsim ) )

    if( ( sfrepsim.FeeType == FeeTypeCurrent ) and
         ( sfrepsim.CommNumber == CommNumberCurrent ) )

      if( PayReportPrintLine( sfrepsim ) )
         return 1;
      end;

    else
      if( FeeTypeCurrent != 0 )/**/
        if( PayReportPrintFooter( sfrepsim ) )
          return 1;
        end;
      end;

      FeeTypeCurrent = sfrepsim.FeeType;
      CommNumberCurrent = sfrepsim.CommNumber;

      if ( PayReportPrintHeader( sfrepsim ) )
         return 1;
      end;

      if( PayReportPrintLine( sfrepsim ) )
         return 1;
      end;

    end;

  end;/*while*/

  if( FeeTypeCurrent != 0 )/**/
    if( PayReportPrintFooter( sfrepsim ) )
      return 1;
    end;
  end;

  return 0;
end;/*PayReportEnd*/

macro PayReportInsertLine( sfdefcom_ )
  record sfdefcom( sfdefcom );
  file sfcontr( sfcontr ) key 0;

  SetBuff( sfdefcom, sfdefcom_ );

  sfcontr.ID = sfdefcom.conID;

  if( not getEQ( sfcontr ) )
    MsgBox( "Не найден договор обслуживания ", sfcontr.ID );
    return 1;
  end;

  ClearRecord( sfrepsim_write );

  sfrepsim_write.FeeType  = sfdefcom.FeeType;
  sfrepsim_write.CommNumber=sfdefcom.CommNumber;
  sfrepsim_write.SfcontrID = sfdefcom.conID;

  sfrepsim_write.Object   =sfcontr.Object;

  sfrepsim_write.BeginDate=sfdefcom.datePeriodBegin;
  sfrepsim_write.EndDate  =sfdefcom.dateFee;

  sfrepsim_write.baseSum  =sfdefcom.baseSum;
  sfrepsim_write.commSum  =sfdefcom.commSum;
  sfrepsim_write.NDSSum=sfdefcom.NDSSum;
  sfrepsim_write.FIID_sum = sfdefcom.FIID_commSum;

  var stat = Ins( sfrepsim_write );

  return stat;
end;/*PayReportInsertLine*/


macro PayReportInsertErrorLine( SFErrorMsg,FeeType,CommNumber,CID )

  file sfcomiss( sfcomiss )key 0;

  if (FeeType and CommNumber)
    sfcomiss.FeeType = FeeType;
    sfcomiss.Number  = CommNumber;

    if( not GetEQ( sfcomiss ) )
        SFErrorMsg = "Не найдена комиссия";
[Ведомость оплаты комиссии ################](SFErrorMsg );
    else
[Ведомость оплаты комиссии ################]( sfcomiss.Code );
    end;

[+---------------------------+---------------------------------------------------------------------------------------+];
[| Объект начисления         | Ошибка                                                                                |];
[|                           |                                                                                       |];
[+---------------------------+---------------------------------------------------------------------------------------+];
[| ######################### | ##################################################################################### |]
(CID:w,SFErrorMsg:w);
[+---------------------------+---------------------------------------------------------------------------------------+];
[  ];
  else
    println("С указанными параметрами оплачиваемых комиссий нет");
  end; /* end if */

  return 0;
end; /*PayReportInsertErrorLine*/
