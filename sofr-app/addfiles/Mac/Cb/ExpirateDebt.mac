/**                                                                                                             
 @file  EnrollDebtSumOp.mac                                                                                           
 @brief Макрос выполнения шага "Вынос на просрочку" операции по обработке периодической комиссии                                                                         
                                                                                                                
 #menu 
  - БО ЦБ\Договоры\Комиссии\Периодические комиссии\Внутренние комиссии
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.08.14 |Жиц М.В.       |BOSS-2231           |Создание макроса шага                                                                                                                                                 
*/ 
import "DebtSumOp_Common.mac", "sfcommon.mac", "sfcomcat.mac";

record SfDefComRec("sfdef.dbt");

MACRO ExecuteStep(Doc, FDoc, DocKind, ID_Operation)
  var stat:integer = 0;
  var FD = NULL;
  record DebtAcc(account);
  record ComAcc(account);
  var Ground:string = "";
  var opdate:date;

  SetBuff(SfDefComRec, FDoc);

  GetOprDate_Kind(SF_OPR_DK_DEBTOVERDUE, opdate);      
  if((ValType(opdate) == V_UNDEF) or (opdate == Date(0,0,0)))
    opdate = SfDefComRec.PlanPayDate;
  end; 

  if(SfDefIsDebtOverdue(SfDefComRec.FeeType, SfDefComRec.ID))
    MsgBox("Вынос на просрочку не требуется");
    return 0;
  end;

  if(({curdate} < SfDefComRec.PlanPayDate) or (opdate < SfDefComRec.PlanPayDate))
    MsgBox("Вынос на просрочку не осуществлен, Дата шага меньше плановой даты оплаты комиссии");
    return 0;
  end; 

  FD = DL_SubContrFD(SfDefComRec.SfContrID);

  if(not OpenDebtAccount(FD, opdate, SfDefComRec.FIID_Sum, DebtAcc))
    MsgBox("Ошибка при открытии счета по категории \"Треб. с н.с. брок\" " + GetErrMsg());
    return 1;            
  end;
  
  if(not FD.OpenAccount("+РасчетыКомисс1", NULL, true, ComAcc, opdate, SfDefComRec.FIID_Sum))
    MsgBox("Ошибка при открытии счета по категории \"+РасчетыКомисс1\" " + GetErrMsg());
    return 1;                                                                                                  
  end;

  if(GetComCode(SfDefComRec.CommNumber, SfDefComRec.FeeType) == COMMISS_INVEST_ADVISER_N)
    Ground = "Перенос на счет просроченной задолженности комиссии за услугу по инвест консультированию ";
    if(GetDay(date(SfDefComRec.DatePeriodBegin)) == GetDay(date(SfDefComRec.DatePeriodEnd)))
      Ground = Ground + "за " + string(GetDay(date(SfDefComRec.DatePeriodEnd)));
    else
      Ground = Ground + "c " + string(GetDay(date(SfDefComRec.DatePeriodBegin))) + " по " + string(GetDay(date(SfDefComRec.DatePeriodEnd)));
    end;
    Ground = Ground + " " + string(GetNameMonthRP(date(SfDefComRec.DatePeriodEnd))) + " " + string(GetYear(date(SfDefComRec.DatePeriodEnd))) + 
                        " по брок.дог. " + FD.GetMainContr().rec.Number + " от " + FD.GetMainContr().rec.DateBegin + ", клиент - " + FD.GetParty().rec.ShortName + ", EKK - " + GetEKKDBO(SfDefComRec.SfContrID);
  else //БРОКЕРФИКС
    Ground = "Перенос на счет просроченной задолженности ежемесячной комиссии по договору " + FD.GetMainContr().rec.Number + " за " + string(GetNameMonth(date(SfDefComRec.DatePeriodEnd))) + " " + string(GetYear(date(SfDefComRec.DatePeriodEnd)));
  end;

  stat = CountComissAsIncome(opdate, SfDefComRec.FIID_Sum, DebtAcc.Account, 
                             SfDefComRec.FIID_Sum, ComAcc.Account, SfDefComRec.Sum, Ground,
                             null, null, null, null, SFDOCS_LINKKIND_COMISSOVERDUE, OBJTYPE_SFDEFCOM, SfDefComRec);     

  if(not stat)
    var ExecQuery = "UPDATE dsfdef_dbt SET t_IsDebtOverdue = 'X' WHERE t_ID = "+SfDefComRec.ID;
    var BackOutQuery = "UPDATE dsfdef_dbt SET t_IsDebtOverdue = CHR(0) WHERE t_ID = "+SfDefComRec.ID;
    if(Opr_ExecSQLQuery(ExecQuery, BackOutQuery))
      msgbox("Ошибка при обновлении поля \"Вынесено на просрочку\"");
      return 1;
    end;
  end;

  return stat;
END;