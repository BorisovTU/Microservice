/*
 $Name: cb_doc.mac
 $Module: ББ/мемордера
 $Description: Макрос скроллинга мемордеров в ББ
 Блок      : Вне блока
 Шаг       : Вне шага
 Назначение: Макрос скроллинга
 Описание  : Макрос скроллинга мемордеров в ББ
-----------------------------------------------------------------------------
*/

import BankInter, check117, PaymInter, "pm_opr.mac", "pmchoper.mac", "pm_tools.mac", pm_chksave;
import "cb_FillFactura.mac", "pmedit.mac", pmbuff;

/*номера полей в панели*/
const fld_PAYERACC  = 8,    /* Счет плательщика */
      fld_RECEIVERACC  = 10; /* Счет получателя  */

MACRO Новый_документ()
 return 0;
END;


/************************************************************************/
/*   Функция проверяет наличие лиц. счета                               */
/************************************************************************/
private MACRO ЛСчетСуществует(Глава, КодВалюты, ЛСчет)
  FILE    acc ("account");
  acc.Chapter        = Глава;
  acc.Account        = ЛСчет;
  acc.Code_Currency  = КодВалюты;
  if (GetEQ(acc))
    Return TRUE;
  else
    Return FALSE;   
  end;
END;

/* Установка подсказки для скролингов из макроса */

private const Hint_ByStatus   :string = 
"/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper oprcurst) INDEX(t dcb_doc_dbt_idx4) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper oprcurst)*/";

private const Hint_ByValueDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) INDEX(t dcb_doc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByCloseDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) INDEX(t dcb_doc_dbt_idx0) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByStep     :string = 
"/*+FIRST_ROWS LEADING(t oproper cb_doc pmpaym pmrmprop oprcurst) INDEX(t doprstep_dbt_idx10) INDEX(cb_doc dcb_doc_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper cb_doc pmpaym pmrmprop oprcurst)*/";

MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string

  // Возможные значения ScrolStates:
  // -2 - Подготовленные
  // -1 - Все
  //  0 - Отложен
  //  1 - Обрабатывается
  //  2 - Отвергнут
  // 50 - Закрыт
  
  /* Отложенные, открытые, отвергнутые */
  if( ( ScrolStates == 0 ) or ( ScrolStates == 1 ) or ( ScrolStates == 2 ) )
  
    return Hint_ByStatus;

  /* Все, Закрытые */
  elif( ( ScrolStates == 50 ) or
        ( ScrolStates == -1 ) )
    
    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByStatus;
    end;

  /* Подготовленные */
  elif( ScrolStates == -2 )
    
    return Hint_ByStep;

  end;
 
  return DefaultHint;

END;

MACRO Проверить_документ(Режим)
  var stat = 0;

  /* Проверка использования кассовых документов */
  if( ((Режим == 2 ) or (Режим == 3) or (Режим == 8)) and index(r_cb_doc.Kind_Oper," 4") == 1 )
  
     /* Проверить по 117-И */
     if( r_cb_doc.Chapter == 1 )
       if( CheckOnSave_117( r_pmpaym, NULL, NULL, r_cb_doc, r_pmrmprop ) )
         return fld_PAYERACC;
       end;
     end;
     if(PM_CheckCO(r_pmpaym,r_pmrmprop,0,0))
       return 1;
     end;
  end;
  
  if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) // ввод/редактирование
    /* Общие проверки по списку */
    stat = BBMO_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, r_debet, r_credit, r_pmrmprop );
    if( stat != NOTERROR )
      return stat;
    end;
  end;

  if( Режим == 1 ) /*УДАЛЕНИЕ ДОКУМЕНТА*/
    if(not isDLMRuning())
        if(r_cb_doc.Origin == CB_DOC_ORIGIN_LOANS)
          if(not Index( "Ц", StrFor(GetIdentProgram())))
            msgbox("Документ порожден п/с \"Кредитование\".|Удаление запрещено.");
            stat = 1;
          end;
        elif(r_cb_doc.Origin == CB_DOC_ORIGIN_DEPOSIT)
          if(not Index( "Д", StrFor(GetIdentProgram())))
            msgbox("Документ порожден п/с \"Депозиты\".|Удаление запрещено.");
            stat = 1;
          end;
        elif(r_cb_doc.Origin == CB_DOC_ORIGIN_RETAIL)
          if(not Index( "ВБD", StrFor(GetIdentProgram())))
            msgbox("Документ порожден п/с \"Обслуж.физ.лиц\".|Удаление запрещено.");
            stat = 1;
          end;
        elif(r_cb_doc.Origin == CB_DOC_ORIGIN_INCOUNTING)
                 msgbox("Документ порожден п/с \"Incounting\".|Удаление запрещено.");
                 stat = 1;
        end;
     end;

     if(CheckDeletePayment(r_pmpaym.PaymentID))
       return 1;
     end;

  elif( Режим == 3 )  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
    /* При редактировании производим проверку важности внесенных изменений */
    /* Константы важности внесенных изменений:           */
    /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
    /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
    /* CHANG_NOTKEEP        - не сохранять изменения */
    /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
    /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
       и сохранение изменений можно производить без отката операции      */
    if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, r_debet, r_credit, r_pmrmprop, NULL,NULL, r_cb_doc), 
          TCheckUpdateParam(r_pmpaym_old, r_debet, r_credit, r_pmrmprop_old, NULL,NULL, r_cb_doc_old)) == CHANG_NOTKEEP)
      return CHANG_NOTKEEP;
    end;

  elif(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperMemorialOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_cb_doc, r_cb_doc_old, r_pmnvpi, r_pmnvpi_old);
  end;

  return stat;
END;

MACRO Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
 return 0;
END;

macro ProcessPanel(mode, key, field, panel)

  /* При входе в панель открыть на редактирование поле "Шифр операции" */
  if( key == 522 )
    panel.Fields.Item( 16 ).Enabled = true;
    panel.Refresh();
  end;

  return key;
end;
/*
var Precedence:integer = 0;
var PaymentKind:string = "";
macro    ЗапроситьРеквизиты()

  array MenuItems;
    
  MenuItems[0] = "Приоритет платежа";
  MenuItems[1] = "Вид платежа";
  
  var MenuResult = Menu(MenuItems, "Запросить реквизиты");
  
  if( MenuResult == 0 )
    GetInt(Precedence, "Ввод приоритета платежа");
  elif( MenuResult == 1 )
    PaymentKind = PM_ListPMPOPKND();
  end;
  return 0;
end;

macro    УстановитьРеквизиты( PaymentID )

  var Payment = RsbPayment( PaymentID );
  if(Precedence != 0)
    Payment.Precedence = Precedence;
  end;
  if(PaymentKind)
    Payment.PaymentKind = PaymentKind;
  end;
  
  return 0;
end;*/

