/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : moscan.mac
  Created     : 12.08.02
  Description : Отчет печати документов бухгалтерии банка

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter;

RECORD mo_firstdoc (oprstep);

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2,
        ReportLineWidth = 78;

/* Выводим длинную строку Str в поле длиной LineWidth - StrLen( EmptyFlds ) */
/* EmptyFlds - предыдущие пустые колонки                                    */
/* LineWidth - общая длина строки отчета                                    */
macro WriteLongStr( Str, EmptyFlds, LineWidth, EndSymb )
  var i, FieldWidth = LineWidth - StrLen( EmptyFlds ) - 1, NumItems;
  array Strokes;

  macro AppendSpace( Str, Len, EndSymb )
    var slen = strlen( Str ), rstr;
    return Str + MkStr( " ", Len-slen ) + EndSymb;
  end;

  StrSplit( Str, Strokes, FieldWidth );
  println( AppendSpace( Strokes( 0 ), FieldWidth, EndSymb ) );
  i = 1;
  NumItems = Asize( Strokes );
  while( i < NumItems )
    println( AppendSpace( EmptyFlds + Strokes( i ), LineWidth - 1, EndSymb ) );
    i = i + 1;
  end;
end;

MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)
        record pm_rmprop (pmrmprop);
        record pm_paym   (pmpaym);
        file op (oproper) key 0;
        file mo (memorder) key 0;

        setbuff (mo_firstdoc,firstdoc);

var stat;
        op.ID_operation = mo_firstdoc.ID_operation;
        if( GetEq(op))
           mo.OrderID = int(op.DocumentID);
           GetEq(mo);
        end;


        if (Вызов==Первый)
[Отчет о групповой печати документов бухгалтерии банка.];
[+---------+------+------------------------------------------------------------+];
[|  Номер  |Номер |                 Сообщение                                  |];
[|документа|ошибки|                                                            |];
[+---------+------+------------------------------------------------------------+];
        elif (Вызов==Ошибка)

                if (Статус == 0)
                        Сообщение = "Документ успешно напечатан";
                end;
                
                if ( FindPayment(
                        0,      /* Индентификатор неизвестен    */
                        PM_PURP_BANKPAYMENT,      
                        0,      /* Subpurpose                   */
                        mo.DocKind, /* Вид первичного документа */ 
                        mo.OrderID,
                        true,   /* Поиск по реальной базе       */
                        pm_paym,
                        null,
                        null,
                        pm_rmprop
                        )       !=      0 )

                        Статус = 1;
                        Сообщение = "Не найдено платежное поручение";
                end;

      print("|", pm_rmprop.Number:9, "|", Статус:6, "|");

      /* Длинную строку Сообщения разбиваем по пробелам */
      WriteLongStr( StrSubst( Сообщение, "|", " " ), "|         |      |", ReportLineWidth, "|" );
                
        elif (Вызов==Последний)
[+---------+------+------------------------------------------------------------+];
        end;
END;