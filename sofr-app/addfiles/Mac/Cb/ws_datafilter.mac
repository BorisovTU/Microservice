/*
$Name:        ws_datafilter.mac
$Module:      Web-общее
$Description: Функции для работы с условиями фильтрации
*/

import globals;
import BankInter;
import FIInter;
import "cb_sql.mac";

const FilterCondition_Undefined = -1;
const FilterCondition_Equal = 0; // = ? / like '?'
const FilterCondition_Null = 1; // is null
const FilterCondition_Include = 2; // in ( ? ) / like '%?%'
const FilterCondition_StartWith = 4; // >= ? / like '?%'
const FilterCondition_From = 8; // >= ?
const FilterCondition_To = 16; // <= ?
const FilterCondition_Between = 32; // between ? and ?
const FilterCondition_CurrentDate = 64; // = {curdate}
const FilterCondition_Greater = 128; // > ?
const FilterCondition_GreaterOrEqual = 256; // >= ?
const FilterCondition_Less = 512; // < ?
const FilterCondition_LessOrEqual = 1024; // <= ?
const FilterCondition_All = 2048; // not use

const FilterParameterType_Int = 0;
const FilterParameterType_Float = 1;
const FilterParameterType_Money = 2;
const FilterParameterType_String = 3;
const FilterParameterType_Date = 4;
const FilterParameterType_Time = 5;
const FilterParameterType_Bool = 6;
const FilterParameterType_UserType = 7;
const FilterParameterType_CheckList = 8;

class FilterConditionItem()
    var Id:Integer;
    var Condition:Integer;
    var Value = TArray();
    var IsNot:Bool;
    var ValueType:Integer;
end;

class FilterParser(
    _fltrCondItemArr
)
    private var fltrCondItemArr = _fltrCondItemArr;
    private var fldCondArr = TArray();

    private class FieldCondition()
        var Id:Integer;
        var TableName:String;
        var FieldName:String;
        var UserOperation:Variant;
        var IsNullOperand:String;
        var SQLCondition:String;
    end;

    private macro IIF(
        condition
       ,valueTrue
       ,valueFalse
    )
        var value;

        if( condition )
            value = valueTrue;
        else 
            value = valueFalse;
        end;

        return value;
    end;

    macro AddFieldCondition(
        id:Integer
       ,tableName:String
       ,fieldName:String
       ,isNullOperand:String
       ,userOperation:Variant
    )
        var fldCond = FieldCondition();

        fldCond.Id = id;
        fldCond.TableName = tableName;
        fldCond.FieldName = fieldName;
        fldCond.IsNullOperand = isNullOperand;
        fldCond.UserOperation = userOperation;
        fldCond.SQLCondition = "";

        fldCondArr[fldCondArr.Size] = fldCond;
    end;

    macro AddUserFieldCondition(
        id:Integer
       ,userOperation:Variant
    )
        AddFieldCondition( id, null, null, null, userOperation );
    end;

    private macro IsNumeric(
        type:Integer
    ):Bool
        return ( ValType( type ) == V_INTEGER )
              and ( ( type == FilterParameterType_Int )
                 or ( type == FilterParameterType_Float )
                 or ( type == FilterParameterType_Money ) );
    end;

    private macro IsString(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_String ) );
    end;

    private macro IsDate(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Date ) );
    end;

    private macro IsTime(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Time ) );
    end;

    private macro IsBool(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Bool ) );
    end;

    private macro IsUserType(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_UserType ) );
    end;

    private macro IsCheckListType(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_CheckList ) );
    end;

    private macro GetSQLNumeric(
        value
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_INTEGER )
         or ( ValType( value ) == V_MONEY )
         or ( ValType( value ) == V_DECIMAL )
         or ( ValType( value ) == V_DOUBLE ) )
            sqlValue = String( value:0:18 );
        else 
            sqlValue = " 0 ";
        end;

        return sqlValue;
    end;

    private macro GetSQLString(
        value:String
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_STRING ) and ( value != "" ) )
            sqlValue = value;
            sqlValue = StrSubst( sqlValue, "?", "_" );
            sqlValue = StrSubst( sqlValue, "*", "%" );
        else 
            sqlValue = " chr(1) ";
        end;

        return sqlValue;
    end;

    private macro GetSQLDate(
        value:Variant
    ):String
        var sqlValue:String = "";
        var dateValue:Date = Date(00, 00, 0000);
        var timeValue:Time = Time(00, 00, 00);

        if( ValType( value ) == V_DTTM )
            DtTmSplit( value, dateValue, timeValue );

            if( dateValue != Date(00, 00, 0000) )
                sqlValue = " TO_DATE('" + dateValue + " " + timeValue + "', 'dd.mm.yyyy HH24:MI:SS') ";
            else
                sqlValue = " TO_DATE('01.01.0001 00:00:00', 'dd.mm.yyyy HH24:MI:SS') ";
            end;
        elif( ValType( value ) == V_DATE )
            if( value != Date(00, 00, 0000) )
                sqlValue = " TO_DATE('" + value + "', 'DD.MM.YYYY') ";
            else
                sqlValue = " TO_DATE('01.01.0001', 'DD-MM-YYYY') ";
            end;
        else
            sqlValue = " TO_DATE('01.01.0001', 'DD-MM-YYYY') ";
        end;

        return sqlValue;
    end;

    private macro GetSQLTime(
        value:Time
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_TIME ) and ( value != Time(00, 00, 00) ) )
            sqlValue = " TO_DATE('01.01.0001 " + value + "', 'dd.mm.yyyy HH24:MI:SS')";
        else
            sqlValue = " TO_DATE('01.01.0001 00:00:00', 'dd.mm.yyyy HH24:MI:SS') ";
        end;

        return sqlValue;
    end;

    private macro GetSQLBool(
        value:Bool
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_BOOL ) and ( value == true ) )
            sqlValue = " chr(88) ";
        else
            sqlValue = " chr(0) ";
        end;

        return sqlValue;
    end;

    private macro GetSQLValue(
        value
       ,type:Integer
    ):String
        var sqlValue:String = "";

        if( IsNumeric( type ) )
            sqlValue = GetSQLNumeric( value );
        elif( IsString( type ) )
            sqlValue = GetSQLString( value );
        elif( IsDate( type ) )
            sqlValue = GetSQLDate( value );
        elif( IsTime( type ) )
            sqlValue = GetSQLTime( value );
        elif( IsBool( type ) )
            sqlValue = GetSQLBool( value );
        elif( IsUserType( type ) )
            sqlValue = " null ";
        elif( IsCheckListType( type ) )
            sqlValue = GetSQLNumeric( value );
        else
            sqlValue = " null ";
        end;

        return sqlValue;
    end;

    private macro HasValue(
        value
    ):Bool
        return ( ( ValType( value ) != V_UNDEF )
             and ( value.Size > 0 )
             and ( ValType( value[0] ) != V_UNDEF ) )
    end;

    private macro IsEmptyString(s)
        return (s == null) or (s == "");
    end;

    private macro InsertValueOperand(
        operand:String
       ,value1:String
       ,value2:String
    ):String
        var sqlValue:String = "";
        var symbPos:Integer = 0;

        symbPos = Index( operand, "?" );
        if( symbPos > 0 )
            sqlValue = SubStr( operand, 1, symbPos - 1 ) + value1 + SubStr( operand, symbPos + 1 );
        else
            sqlValue = operand;
        end;

        if( ValType( value2 ) != V_UNDEF )
            sqlValue = InsertValueOperand( sqlValue, value2 );
        end;

        return sqlValue;
    end;

    private macro GetSQLEnum(
        value
       ,type:Integer
    ):String
        var sqlValue:String = "";
        var valueCount:Integer = 0;

        if( HasValue( value ) )
            while( valueCount < value.Size )
                if( sqlValue != "" )
                    sqlValue = sqlValue + ", ";
                end;

                if (IsString( type ))
                    sqlValue = sqlValue + InsertValueOperand( " '?' ", StrLwr(GetSQLValue( value[valueCount], type )) );
                else
                    sqlValue = sqlValue + InsertValueOperand( " ? ", GetSQLValue( value[valueCount], type ) );
                end;

                valueCount = valueCount + 1;
            end;
        else
            sqlValue = " null "
        end;

        return sqlValue;
    end;

    private macro FormFieldSQLCondition(
        fldCond:FieldCondition
       ,fltrCondItem/*:FilterConditionItem*/
    ):String
        var strSQLCond:String = "";
        var fullFieldName:String = "";

        if( ( ValType( fltrCondItem.Condition ) != V_UNDEF )
        and ( ( fltrCondItem.Condition == FilterCondition_Null )
           or ( fltrCondItem.Condition == FilterCondition_CurrentDate )
           or ( ( fltrCondItem.Condition != FilterCondition_Undefined )
            and ( HasValue( fltrCondItem.Value ) ) ) ) )

            if( ValType( fldCond.FieldName ) != V_UNDEF )
                if( ValType( fldCond.TableName ) != V_UNDEF )
                    fullFieldName = fldCond.TableName + "." + fldCond.FieldName;
                else
                    fullFieldName = fldCond.FieldName;
                end;
            end;

            if( ValType( fldCond.UserOperation ) == V_PROC )
                strSQLCond = fullFieldName + " " + ExecMacro2( @fldCond.UserOperation, fltrCondItem.Condition, fltrCondItem.Value, fltrCondItem.ValueType );
            elif( ValType( fldCond.UserOperation ) == V_STRING )
                strSQLCond = fullFieldName + " " + InsertValueOperand( fldCond.UserOperation, GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
            else
                if( fltrCondItem.Condition == FilterCondition_Null ) // is null
                    if( ValType( fldCond.IsNullOperand ) != V_UNDEF )
                        strSQLCond = "((" + fullFieldName + fldCond.IsNullOperand + ") or (" + fullFieldName + " is null))";
                    else
                        if( ValType( fltrCondItem.ValueType ) != V_UNDEF )
                            strSQLCond = "((" + fullFieldName + InsertValueOperand( " = ? ", GetSQLValue( null, fltrCondItem.ValueType ) ) + ") or (" + fullFieldName + " is null))";
                        else
                            strSQLCond = fullFieldName + " is null ";
                        end;
                    end;
                elif( fltrCondItem.Condition == FilterCondition_CurrentDate ) // = {curdate}
                    strSQLCond = fullFieldName + InsertValueOperand( " = ? ", GetSQLValue( {curdate}, FilterParameterType_Date ) );
                elif( ( fltrCondItem.Condition != FilterCondition_Undefined )
                  and ( HasValue( fltrCondItem.Value ) ) )
                    if( fltrCondItem.Condition == FilterCondition_Equal ) // = ? / like '?'
                        if( fltrCondItem.Value.Size == 1 ) // single choice
                            if (IsString( fltrCondItem.ValueType ))
                                strSQLCond = "lower(" + fullFieldName + ")" + InsertValueOperand(" like '?' ", StrLwr(GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType )) );
                            else
                                strSQLCond = fullFieldName + InsertValueOperand(" = ? " , GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                            end;
                        elif( fltrCondItem.Value.Size > 1 ) // multiple choice
                            strSQLCond = IIF(IsString( fltrCondItem.ValueType ), "lower(" + fullFieldName + ")", fullFieldName) + InsertValueOperand( " in ( ? ) ", GetSQLEnum( fltrCondItem.Value, fltrCondItem.ValueType ) );
                        end;
                    elif( fltrCondItem.Condition == FilterCondition_Include ) // in ( ? ) / like '%?%'
                        if (IsString( fltrCondItem.ValueType ))
                            strSQLCond = "lower(" + fullFieldName + ")" + InsertValueOperand(" like '%?%' ", StrLwr(GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType )) );
                        else
                            strSQLCond = fullFieldName + InsertValueOperand(" in ( ? ) ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                        end;
                    elif( fltrCondItem.Condition == FilterCondition_StartWith ) // >= ? / like '?%'
                        if (IsString( fltrCondItem.ValueType ))
                            strSQLCond = "lower(" + fullFieldName + ")" + InsertValueOperand(" like '?%' ", StrLwr(GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType )) );
                        else
                            strSQLCond = fullFieldName + InsertValueOperand(" >= ? " , GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                        end;
                    elif( fltrCondItem.Condition == FilterCondition_From ) // >= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " >= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_To ) // <= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " <= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Between ) // between ? and ?
                        strSQLCond = fullFieldName + InsertValueOperand( " between ? and ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ), GetSQLValue( fltrCondItem.Value[1], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Greater ) // > ?
                        strSQLCond = fullFieldName + InsertValueOperand( " > ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_GreaterOrEqual ) // >= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " >= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Less ) // < ?
                        strSQLCond = fullFieldName + InsertValueOperand( " < ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_LessOrEqual ) // <= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " <= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    end;
                end;
            end;

            if (fltrCondItem.IsNot)
                strSQLCond = "not (" + strSQLCond + ")";
            end;
        end;

        return strSQLCond;
    end;

    private macro AddToFieldSQLCondition(
        _fltrCondItem/*:FilterConditionItem*/
    )
        var fltrCondItem = _fltrCondItem;
        var fldCondCount = 0;

        if( ValType( fltrCondItem ) != V_UNDEF )
            while( fldCondCount < fldCondArr.Size )
                if( fltrCondItem.Id == fldCondArr[fldCondCount].Id )
                    if (not IsEmptyString(fldCondArr[fldCondCount].SQLCondition))
                        fldCondArr[fldCondCount].SQLCondition = fldCondArr[fldCondCount].SQLCondition + " and ";
                    end;
                    fldCondArr[fldCondCount].SQLCondition = fldCondArr[fldCondCount].SQLCondition + FormFieldSQLCondition( fldCondArr[fldCondCount], fltrCondItem );
                end;
                fldCondCount = fldCondCount + 1;
            end;
        end;
    end;

    private macro ClearSQLCondition()
        var fldCondCount = 0;

        while( fldCondCount < fldCondArr.Size )
            fldCondArr[fldCondCount].SQLCondition = "";
            fldCondCount = fldCondCount + 1;
        end;
    end;

    private macro FormSQLCondition()
        var condCount = 0;

        if( ValType( fltrCondItemArr ) != V_UNDEF )
            while( condCount < fltrCondItemArr.Size )
                AddToFieldSQLCondition( fltrCondItemArr[condCount] );
                condCount = condCount + 1;
            end;
        end;
    end;

    macro GetFullSQLCondition():String
        var fldCondCount = 0;
        var strSQLCond:String = " 1 = 1 ";

        ClearSQLCondition();

        FormSQLCondition();

        while( fldCondCount < fldCondArr.Size )
            // все условия объединим через И
            if (not IsEmptyString(fldCondArr[fldCondCount].SQLCondition))
                strSQLCond = strSQLCond + " and " + fldCondArr[fldCondCount].SQLCondition;
            end;
            fldCondCount = fldCondCount + 1;
        end;

        return strSQLCond;
    end;

    macro GetFullSQLConditionDebug(
        outFileName:String
    )
        var fullOutFileName:String = "";
        FILE outFile() txt write;

        if( ValType( outFileName ) == V_STRING )
            if( Open( outFile, GetTxtFileName( outFileName ) ) )
                Insert( outFile, GetFullSQLCondition() );
                Close( outFile );
            end;
        end;
    end;
end;

/*
macro CallBack1(
    condition : Integer
   ,value// : TArray of <type>
   ,type : Integer
)
    return " = 1 ";
end;

macro CallBack2(
    condition : Integer
   ,value// : TArray of <type>
   ,type : Integer
)
    return " op( table.t_field2, " + value[0] + " ) > 0 ";
end;

var val = TArray();
var fc = FilterConditionItem();
var fcia = TArray();

// 1
val = TArray();
val[val.Size] = Date(01,01,2001);
val[val.Size] = Date(10,10,2010);

fc = FilterConditionItem();
fc.Id = 0;
fc.Condition = FilterCondition_Between;
fc.Value = val;
fc.IsNot = false;
fc.ValueType = FilterParameterType_Date;

fcia[fcia.Size] = fc;

// 2
val = TArray();
val[val.Size] = Date(02,02,2002);

fc = FilterConditionItem();
fc.Id = 0;
fc.Condition = FilterCondition_Equal;
fc.Value = val;
fc.IsNot = true;
fc.ValueType = FilterParameterType_Date;

fcia[fcia.Size] = fc;

// 3
val = TArray();
val[val.Size] = 3;

fc = FilterConditionItem();
fc.Id = 2;
fc.Condition = FilterCondition_Equal;
fc.Value = val;
fc.IsNot = false;
fc.ValueType = FilterParameterType_Int;

fcia[fcia.Size] = fc;

// 4
val = TArray();
val[val.Size] = 4;

fc = FilterConditionItem();
fc.Id = 3;
fc.Condition = FilterCondition_Equal;
fc.Value = val;
fc.IsNot = false;
fc.ValueType = FilterParameterType_UserType;

fcia[fcia.Size] = fc;

// 5
val = TArray();
val[val.Size] = "5_1";
val[val.Size] = "5_2";
val[val.Size] = "5_3";

fc = FilterConditionItem();
fc.Id = 4;
fc.Condition = FilterCondition_Equal;
fc.Value = val;
fc.IsNot = false;
fc.ValueType = FilterParameterType_String;

fcia[fcia.Size] = fc;

var fp = FilterParser( fcia );
fp.AddFieldCondition( 0, "table", "t_field0" );
fp.AddFieldCondition( 1, "table", "t_field1", " = -1 " );
fp.AddFieldCondition( 2, null, null, null, " op( table.t_field2, ? ) > 0 " );
fp.AddFieldCondition( 3, "table", "t_field2", null, @CallBack1 );
fp.AddFieldCondition( 3, null, null, null, @CallBack2 );
fp.AddUserFieldCondition( 3, @CallBack2 );
fp.AddFieldCondition( 4, "table", "t_field3" );
println(fp.GetFullSQLCondition());
fp.GetFullSQLConditionDebug( "ws_datafilter_debug" );
*/

macro GetSQLFIConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].FIID ) == V_INTEGER )
      and ( value[valueCount].FIID > -1 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].FIID ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLAvoirKindConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].AvoirKind ) == V_INTEGER )
      and ( value[valueCount].AvoirKind > 0 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        strSQLCond = strSQLCond + " RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", " + String( value[valueCount].AvoirKind ) + ", ";

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " ) = 1 ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLPartyConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].PartyID ) == V_INTEGER )
      and ( value[valueCount].PartyID > -1 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].PartyID ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLLlValuesConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Element ) == V_INTEGER )
      and ( value[valueCount].Element > -1 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].Element ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLNameAlgConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].NumberAlg ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].NumberAlg ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLTxDealConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Id ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].Id ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLTxAvoirssConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].AvoirissID ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].AvoirissID ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLTxMarketSectorConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].MarketSectorID ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].MarketSectorID ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

macro GetSQLTxReplDealConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].DealID ) == V_STRING ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].DealID ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

/*
     Фильтрация по строке
 */
macro GetFilterStringSQLCondition( FilterItem, SQLField : string, IsUpper : bool, IsMask : bool ) : string

  var SQLString = "";

  if( FilterItem.Condition == FilterCondition_Null ) /* не задано */
    
    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + "CHR(1)";
  
  else
  
    var _SQLField = SQLField;
    var _Value    = FilterItem.Value[0];

    if( IsUpper == true )
      _SQLField = "UPPER(" + SQLField + ")";
      _Value    = StrUpr(FilterItem.Value[0]);
    end;

    if( FilterItem.Condition == FilterCondition_Equal ) /* равно */

      if( (IsMask == true) and (IsAccountWithMask(_Value) == true) )

        var ConvertMask = ConvertMaskToSQLFormat( _Value, SQLField );

        if( ConvertMask == "" )
          ConvertMask = "1 = 1";
        end;

        if( FilterItem.IsNot == true )
          SQLString = SQLString + " NOT";
        end;

        SQLString = SQLString + " " + ConvertMask;

      else

        SQLString = SQLString + _SQLField;

        if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
        else                            SQLString = SQLString + " <> ";
        end;

        SQLString = SQLString + GetSQLString(_Value);

      end;

    elif( FilterItem.Condition == FilterCondition_StartWith ) /* начинается с */

      SQLString = SQLString + _SQLField;

      if( FilterItem.IsNot == true )
        SQLString = SQLString + " NOT";
      end;

      SQLString = SQLString + " LIKE " + GetSQLString(_Value) + " || '%'";

    elif( FilterItem.Condition == FilterCondition_Include ) /* содержит */

      SQLString = SQLString + _SQLField;

      if( FilterItem.IsNot == true )
        SQLString = SQLString + " NOT";
      end;

      SQLString = SQLString + " LIKE '%' || " + GetSQLString(_Value) + " || '%'";

    end;

  end;

  return SQLString;

end;

/*
     Фильтрация по дате
 */
macro GetFilterDateSQLCondition( FilterItem, SQLField : string ) : string

  var SQLString  = "";

  if( FilterItem.Condition == FilterCondition_Null ) /* не задано */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + GetSQLDate( date(0, 0, 0) );

  elif( FilterItem.Condition == FilterCondition_CurrentDate ) /* текущие операционный день */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + GetSQLDate( {curdate} );

   elif( FilterItem.Condition == FilterCondition_Equal ) /* равно */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + GetSQLDate( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_From ) /* ОТ */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " >= ";
    else                            SQLString = SQLString + " < ";
    end;

    SQLString = SQLString + GetSQLDate( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_To ) /* ДО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " <= ";
    else                            SQLString = SQLString + " > ";
    end;

    SQLString = SQLString + GetSQLDate( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_Between ) /* ОТ и ДО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == true ) SQLString = SQLString + " NOT "; end;

    SQLString = SQLString + " BETWEEN ";
    SQLString = SQLString + GetSQLDate( FilterItem.Value[0] );
    SQLString = SQLString + " AND ";
    SQLString = SQLString + GetSQLDate( FilterItem.Value[1] );

  end;

  return SQLString;

end;

/*
     Фильтрация по целочисленному атрибуту
 */
macro GetFilterIntegerSQLCondition( FilterItem, SQLField : string ) : string

  var SQLString  = "";

  if( FilterItem.Condition == FilterCondition_Equal ) /* равно */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_Between ) /* ОТ и ДО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == true ) SQLString = SQLString + " NOT "; end;

    SQLString = SQLString + " BETWEEN ";
    SQLString = SQLString + string( FilterItem.Value[0] );
    SQLString = SQLString + " AND ";
    SQLString = SQLString + string( FilterItem.Value[1] );

  elif( FilterItem.Condition == FilterCondition_Greater ) /* БОЛЬШЕ */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " > ";
    else                            SQLString = SQLString + " <= ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_GreaterOrEqual ) /* БОЛЬШЕ или РАВНО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " >= ";
    else                            SQLString = SQLString + " < ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_Less ) /* МЕНЬШЕ */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " < ";
    else                            SQLString = SQLString + " >= ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_LessOrEqual ) /* МЕНЬШЕ или РАВНО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " <= ";
    else                            SQLString = SQLString + " > ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  end;

  return SQLString;

end;

/*
     Фильтрация по денежному атрибуту
 */
macro GetFilterMoneySQLCondition( FilterItem, SQLField : string ) : string

  var SQLString  = "";

  if( FilterItem.Condition == FilterCondition_Equal ) /* равно */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " = ";
    else                            SQLString = SQLString + " <> ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_Between ) /* ОТ и ДО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == true ) SQLString = SQLString + " NOT "; end;

    SQLString = SQLString + " BETWEEN ";
    SQLString = SQLString + string( FilterItem.Value[0] );
    SQLString = SQLString + " AND ";
    SQLString = SQLString + string( FilterItem.Value[1] );

  elif( FilterItem.Condition == FilterCondition_Greater ) /* БОЛЬШЕ */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " > ";
    else                            SQLString = SQLString + " <= ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_GreaterOrEqual ) /* БОЛЬШЕ или РАВНО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " >= ";
    else                            SQLString = SQLString + " < ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_Less ) /* МЕНЬШЕ */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " < ";
    else                            SQLString = SQLString + " >= ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  elif( FilterItem.Condition == FilterCondition_LessOrEqual ) /* МЕНЬШЕ или РАВНО */

    SQLString = SQLString + SQLField;

    if( FilterItem.IsNot == false ) SQLString = SQLString + " <= ";
    else                            SQLString = SQLString + " > ";
    end;

    SQLString = SQLString + string( FilterItem.Value[0] );

  end;

  return SQLString;

end;


/*
     Фильтрация по операционисту
 */
macro GetFilterAppObjectSQLCondition( FilterItem, SQLField : string, GetObjectStringFuncName : string ) : string

  var SQLString = "";

  if( FilterItem.Condition == FilterCondition_Equal )

    if( FilterItem.Value.Size > 0 )

      var i = 0;

      SQLString = SQLString + " AND";

      if( FilterItem.IsNot )
        SQLString = SQLString + " NOT";
      end;

      SQLString = SQLString + " (";

      while( i < FilterItem.Value.Size )

        if( i > 0 )
          SQLString = SQLString + " OR ";
        end;
        
        SQLString = SQLString + SQLField;

        SQLString = SQLString + " = ";

        SQLString = SQLString + ExecMacro2( GetObjectStringFuncName, FilterItem.Value[i] );

        i = i + 1;

      end;

      SQLString = SQLString + ") ";

    end;

  end;

  return SQLString;

end;

/*
     Фильтрация по субъекту
 */

private macro getPartyString( PartyObject )
  return string(PartyObject.PartyID);
end;

macro GetFilterPartySQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getPartyString" );
end;

/*
     Фильтрация по финансовому инструменту
 */

private macro getFIString( FIObject )
  return string(FIObject.FIID);
end;

macro GetFilterFISQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getFIString" );
end;

/*
     Фильтрация по операционисту
 */
private macro getOperString( OperObject )
  return string(OperObject);
end;

macro GetFilterOperSQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getOperString" );
end;

/*
     Фильтрация по узлу ТС
 */

private macro getDprtString( DprtObject )
  return string(DprtObject.Code);
end;

macro GetFilterDprtSQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getDprtString" );
end;

/*
     Фильтрация по шифру операции
 */
private macro getCipherString( CipherObject )
  return "'" + string(CipherObject.Shifr_Oper) + "'";
end;

macro GetFilterCipherSQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getCipherString" );
end;

/*
     Фильтрация по виду проводки
 */
private macro getResCarryString( ResCarryObject )
  return string(ResCarryObject.ResultCarry);
end;

macro GetFilterResCarrySQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getResCarryString" );
end;

/*
     Фильтрация по фазе
 */
private macro getPhaseString( PhaseObject )
  return string(PhaseObject.Phase); 
end;

macro GetFilterPhaseSQLCondition( FilterItem, SQLField : string ) : string
  return GetFilterAppObjectSQLCondition( FilterItem, SQLField, "getPhaseString" );
end;



