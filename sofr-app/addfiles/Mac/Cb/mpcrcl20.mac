 /*
 $Name: mpcrcl20.mac
 $Module: RSACCOUNT
 $Description: Шаг "Расчет суммы".
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcrcl20.mac

  Библиотека       : RSACCOUNT

  Описание         : Расчет суммы

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet, CTInter, InsCarryDoc, globals, mpclb;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    DeltaSumCalc = $0,
    ContractID = 0;

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);


// Заполняем поля документа, по которому запущена операция
PRIVATE MACRO SetCntOprFieldsAfterReclass(DocID, OperDate, IDPeriodNew)
   var   cmd;

   cmd = RSDCommand ("update DPRCCNTOPR_DBT set T_OPERDATE = ?, T_ISRECALC = '', " +
                      "T_ISPREVPERIOD = '', T_ISCREATEDOC = 'X', T_NEWCNTSCHEDID = ? " +
                      "where T_DOCID = ?");
   cmd.addParam( "", RSDBP_IN, OperDate );
   cmd.addParam( "", RSDBP_IN, IDPeriodNew );
   cmd.addParam( "", RSDBP_IN, DocID );
   cmd.execute();

   cntopr_buf.NewCntSchedID = IDPeriodNew;

   return;
END;


// Обнуляем все последующие периоды
PRIVATE MACRO ZeroChargePeriods(ContractID,BeginDate)
   var cmd, rs, 
       NullDate = Date(0,0,0),
       ID = 0;

   cmd = RSDCommand ("select T_CNTSCHEDID from DPRCCNTSCHED_DBT " +
                      "where T_CONTRACTID = ? and T_SCHEDULEKIND = 2 and T_PERIODBEGINDATE >= ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   
   while (rs.movenext())
      ID = rs.cntschedid;
      PrcSetCntSchedSumByID(ID, money(0), NullDate, money(0));
   end;
   
END;




// Проверяем, отражено ли начисление за период в учете
PRIVATE MACRO CheckForFactSum(CntSchedID)
   var   stat = false,
         cmd,rs;

   cmd = RSDCommand ("select T_CNTSCHEDID from DPRCCNTSCHED_DBT " +
         "where T_CNTSCHEDID = ? and (T_DATEREAL IS NOT NULL and T_DATEREAL <> to_date('01.01.0001', 'DD.MM.YYYY'))" + 
         "and ((T_SUMFACT <>0) or (T_SUMCALC = 0 and T_SUMFACT = 0) or (T_SPECIALTYPE = 1))");
   cmd.addParam( "", RSDBP_IN, CntSchedID );
   cmd.execute();

   rs = TRsbDataSet(cmd);
   
   if (rs.movenext())
      stat = true;
   end;

   return stat;
END;

// Читаем даты периода 
PRIVATE MACRO GetPeriodDates(CntSchedID, PeriodBegin:@date, PeriodEnd:@date, PeriodDate:@date)

   var   cmd,rs;

   cmd = RSDCommand ("select T_PERIODBEGINDATE, T_PERIODENDDATE, T_DATE from DPRCCNTSCHED_DBT " +
         "where T_CNTSCHEDID = ?");
   cmd.addParam( "", RSDBP_IN, CntSchedID );
   cmd.execute();

   rs = TRsbDataSet(cmd);
   
   if (rs.movenext())
            PeriodBegin = rs.periodbegindate;
            PeriodEnd = rs.periodenddate;
            PeriodDate = rs.date;
   end;   

   return;
END;


// Определяем период, в который входит дата реклассификации
PRIVATE MACRO GetCntChargePeriods(ContractID, DocDate, IDPeriod1:@integer, IDPeriod0:@integer, 
                                   IDFirstPeriod:@integer, IDLastPeriod:@integer):bool

   var   stat = false,
         cmd,rs,
         firstPeriodBegin, lastPeriodEnd;

   IDPeriod1 = 0;
   IDPeriod0 = 0;
   IDFirstPeriod = 0; 
   IDLastPeriod = 0;

   cmd = RSDCommand ("select T_CNTSCHEDID, T_PERIODBEGINDATE, T_PERIODENDDATE from DPRCCNTSCHED_DBT " +
         "where T_CONTRACTID = ? and T_SCHEDULEKIND = 2 order by T_CNTSCHEDID ASC");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.execute();

   rs = TRsbDataSet(cmd);
   
   while (rs.movenext())
      
      if ((rs.periodbegindate <= DocDate) and (rs.periodenddate >= DocDate))
         if (IDPeriod1 < rs.cntschedid)
            IDPeriod0 = IDPeriod1;
            IDPeriod1 = rs.cntschedid;
            stat= true;
         end;
      end;
      
      if (rs.periodbegindate > DocDate)
         if ((IDFirstPeriod == 0) or (rs.periodbegindate < firstPeriodBegin))
            IDFirstPeriod = rs.cntschedid;
            firstPeriodBegin = rs.periodbegindate;
         end;
      end;

      if (rs.periodenddate < DocDate)
         if ((IDLastPeriod == 0) or (rs.periodenddate > lastPeriodEnd))
            IDLastPeriod = rs.cntschedid;
            lastPeriodEnd = rs.periodenddate;
         end;
      end;
      
   end; 

   return stat;
END;


MACRO ExecuteCaseStep(OperationKind, StepNumber, Document, DocKind)
   
   var   SumCalc = $0,
         SumCalc1 = $0,
         SumFact = $0,
         DeltaSum = $0,
         cmd,rs,
         Sum,
         IDPeriod1, IDPeriod0, IDFirst, IDLast, IDPeriodNew, 
         Period1Begin, Period1End, Period1Date,
         LastQuality, NewQuality;

      SetBuff( cntopr_buf, Document );

      ContractID = cntopr_buf.contractid;

      ClearRecord( contract_buf );

      prccontract.KeyNum = 0;  
      prccontract.rec.contractid = ContractID;
      if (not prccontract.GetEQ)
            msgbox ("Не найден договор с ID = " + ContractID);
            return 1;
      end;

      NewQuality = cntopr_buf.NewClientQuality;
      if ({curdate} > ДАТА_ВВОДА)
            PrcInsertClientQuality(ContractID, date(cntopr_buf.DocDate + 1), NewQuality);
            return "70";
      else
            if (PrcGetClientQualityOnDate(ContractID, date("31.12.9999"), @LastQuality) != 0)
                  MemoryError(6829);
                  DisplayError();
                  return 1;   
            end;

            if (IncomeIsDefinite(LastQuality) == IncomeIsDefinite(NewQuality))
                  PrcInsertClientQuality(ContractID, date(cntopr_buf.DocDate + 1), NewQuality);
                  return "70";
            elif ((IncomeIsDefinite(LastQuality) == true) and (IncomeIsDefinite(NewQuality) == false))
                  PrcSetOprReclassType(cntopr_buf.DocID, 0);
            else
                  PrcSetOprReclassType(cntopr_buf.DocID, 1);      
            end;   

            if (GetCntChargePeriods(ContractID, cntopr_buf.DocDate, @IDPeriod1, @IDPeriod0, @IDFirst, @IDLast) == false)
                  
                  if (IDLast != 0)
                  if (CheckForFactSum(IDLast))
                        return "50";
                  else
                        msgbox("Не отражены в учете начисленные проценты последнего периода. |" +
                              "Необходимо провести операцию начисления или пересмотреть дату реклассификации");
                        return "70";
                  end;
                  end;
            else

                  GetPeriodDates(IDPeriod1, @Period1Begin, @Period1End, @Period1Date);
            
                  if (Period1End == cntopr_buf.DocDate)
                  if (CheckForFactSum(IDPeriod1))
                        return "50";
                  else
                        PrcFillReclassOpr(cntopr_buf.DocID, Period1Date);
                        return "30";
                  end;   
                  end;  

                  if (CheckForFactSum(IDPeriod1))
                  msgbox("По договору были отражены в учете начисленные проценты до даты |" +
                              "более поздней, чем дата реклассификации. Необходимо откатить |" +
                              "операцию начисления или пересмотреть дату реклассификации");
                  return "70";
                  end;

                  if ((IDPeriod0 > 0) and (CheckForFactSum(IDPeriod0) == false))
                  msgbox("Не отражены в учете начисленные проценты последнего периода. |" +
                              "Необходимо провести операцию начисления или пересмотреть дату реклассификации");
                  return "70";
                  else
                  
                  if (PrcInsertReclassPeriod(cntopr_buf.DocID,IDPeriod1) != 0)
                        return 1;
                  end;
                  ZeroChargePeriods(ContractID, cntopr_buf.DocDate+1);         
                  
                  return "30";
                  end;      
            end;
      end;
 
   return "30";   
END;



MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

