/*
 $Name: csprepro.mac
 $Module: Ядро Banking
 $Description: Предобработка кассвого документа
 */
//-----------------------------------------------------------------------------
// Блок     : 29017 - "Предобработка кассового документа"
// Шаг      : 10    - "Предобработка"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, pm_syscont, pmchk117, cschkrst, pmfm, pmprepromass, pschkbnk;

var PaymentObj:RsbPayment;

MACRO ExecuteStep( doc, paymDoc )
  var stat:integer = 0;
  
  if( stat == 0 )
    
    //Контроль
    if( PaymentObj.Origin == PAYMENT_OR_AUTO )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL );
    else
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_NOTCONTROL );
    end;

    //Направление
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_DIRECT, OPR_PM_ST_DIR_INTERNAL );
    end;      
          
    //Уточнение причастности к терроризму - Не требуется
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NOTNEED );
    end;  
          
    //Квитовка - не сквитован
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_UNKVIT );
    end;

    //ЦАБС - проведен по счетам МФР 
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES );
    end;  
          
    //Обработка в БО - нет
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_BO_PROCESS, OPR_PAYM_ST_BO_NO );
    end;  
    
    // ПЗО
    if( stat == 0 )
      if( (PaymentObj.DocKind == CASH_PS_INCORDER) or (PaymentObj.DocKind == CASH_PS_OUTORDER) )
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_PZO, OPR_PAYM_ST_PZO_NEED );
      else
        stat = УстановитьСтатусыПлатежа( OPR_PAYM_PZO, OPR_PAYM_ST_PZO_NOTNEED );
      end;
    end;

    // РО - не требуется
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED );
    end;  

    // МФ
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_BRANCH, OPR_PAYM_ST_BRANCH_NO );
    end;      
          
    // КАРТ - нет
    if( stat == 0 )
      stat = УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_NO );
    end;
    
    if( stat != 0 )      
      msgbox("Ошибка при установке статуса платежа");
      return 1;
    end;
  end;
  
  if( stat == 0 ) // Системный контроль
    stat = ExecuteSysControlStep( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на причастность к терроризму
    stat = ПроверкаПричастностиТерроризму( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка на банкротство
    stat = ПроверитьСтатусБанкротства( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;

  if( stat == 0 ) // Контроль по 117-И
    stat = КонтрольПо117И( PaymentObj );
    if( stat < 0 )
      return 1;
    elif( stat > 0 )
      return 0;
    end;
  end;
  
  if( stat == 0 ) // Проверка остатков по счетам
    stat = CS_CheckAccRest( PaymentObj );

    // если резерв не был создан, завершить выполнение шага
    if(stat)
      return ConvertPrcChkRsrvStatForStep(stat);
    end;
  end;
  
  return stat;
END;


/* -----------------------------------------------------------------------------
   Массовое выполнение шага "Предобработка кассового документа"
   ----------------------------------------------------------------------------- */

/* -----------------------------------------------------------------------------
   Предтранзакционные действия 
   Все проверки отражаются только на временной таблице dpmprepro_tmp,
   никакие изменения в БД не делаются
   ----------------------------------------------------------------------------- */
macro PrepMassExecuteStep() 

  // Подготовительные действия
  var stat:integer = execStoredFunc( "PM_CSPREPRO.MassPreprocessPrepare", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproPrepare();
  end;
  
  // Системный контроль
  if( not stat )
    stat = execStoredFunc( "PM_CSPREPRO.MassSysControlPrepare", V_INTEGER );
  end;

  // Проверка остатка
  if( not stat )
    stat = execStoredFunc( "PM_CSPREPRO.MassCheckRestPrepare", V_INTEGER );
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

/* -----------------------------------------------------------------------------
   Транзакционные действия 
   Всё, что напроверяли, отражается на статусах документа, платежа, операции 
   ----------------------------------------------------------------------------- */
macro MassExecuteStep()
  
  var stat:integer = execStoredFunc( "PM_CSPREPRO.MassPreprocessExecute", V_INTEGER );

  if( not stat )
    stat = PM_MassPreproExecute();
  end;

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

/* -----------------------------------------------------------------------------
   Действия после транзакции
   Заполняем лог обработки платежей для отчета 
   ----------------------------------------------------------------------------- */
macro PostMassExecuteStep()

  var stat:integer = execStoredFunc( "PM_PREPRO.MassFillLog", V_INTEGER );

  if( stat )
    MemoryError( stat );
  end;

  return stat;

end;

