/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank v.6                                          R-Style Software Lab

  File Name   : prpmchnd.mac
  Created     : 11.11.2009
  Description : Печать отчета о массовой работе процедуры 
                смены узла платежа

└───────────────────────────────────────────────────────────────────────────*/
IMPORT  PaymInter, OprInter, globals;

const   ПечатьЗаписи = 0,
        Первый       = 1,
        Последний    = 2;

private macro GetNameNode(Code)
  var Tb_Dp_Dep:Tbfile = Tbfile("dp_dep.dbt", "bank.def", 0);
  var Tb_Party:Tbfile = Tbfile("party.dbt", "bank.def", 0);
  Tb_Dp_Dep.Clear();
  Tb_Dp_Dep.rec.Code = Code;
  if(Tb_Dp_Dep.GetEQ())
    Tb_Party.Clear();
    Tb_Party.rec.PartyID = Tb_Dp_Dep.rec.PartyID;
    if(Tb_Party.GetEQ())
      return Tb_Party.rec.Name;
    end;
  end;
  return "";
end;

macro PrintHeaderReport(NewNode)
  var Tb_Person:Tbfile = Tbfile("person.dbt", "bank.def", 0);
  Tb_Person.Clear();
  Tb_Person.rec.Oper = {oper};
  Tb_Person.GetEQ();

  var Tb_Dp_Dep:Tbfile = Tbfile("dp_dep.dbt", "bank.def", 0);
  Tb_Dp_Dep.Clear();
  Tb_Dp_Dep.rec.Code = NewNode;
  Tb_Dp_Dep.GetEQ();

  
[ Протокол процедуры передачи документов в подразделение #################################################################
  
  Операционист   : ######################################################################################
  Дата выполнения: #############

  ┌─────┬───────────┬─────────────┬───────┬────────────────────────────────────────────────────┐
  │Номер│Дата валют.│    Сумма    │Номер  │                   Сообщение                        │
  │     │           │             │ошибки │                                                    │
  ├─────┼───────────┼─────────────┼───────┼────────────────────────────────────────────────────┤](Tb_Dp_Dep.rec.Name +" "+ GetNameNode(NewNode), Tb_Person.rec.Oper +" "+ Tb_Person.rec.Name, {curdate}); 
end;

macro PrintRecordReport(Номер, ДатаВалют, Сумма, НомерОшибки, Сообщение)
 if(НомерОшибки == 0)
   Сообщение = "Подразделение документа изменено успешно";
 end;           

[ │#####│###########│#############│#######│####################################################│](Номер, ДатаВалют, Сумма, НомерОшибки, StrSubst(Сообщение, "|", " " ):w);
end;

macro PrintFooterReport()
[ └─────┴───────────┴─────────────┴───────┴────────────────────────────────────────────────────┘];
end;

MACRO Печать_ОтчетСканирования(Вызов,firstdoc,Статус,Сообщение, UserVal)
   
  record pm_rmprop (pmrmprop);
  record pm_paym (pmpaym);
  
  setbuff(pm_paym, firstdoc);

  if (Вызов==Первый)
    PrintHeaderReport(UserVal);
  elif (Вызов == ПечатьЗаписи)
    if ( FindPayment( pm_paym.PaymentID,      // Индентификатор 
                      0,    
                      0,    
                      0,    
                      0,
                      true,         // Поиск по реальной базе
                      pm_paym,
                      null,
                      null,
                      pm_rmprop
                      ) != 0 )

      Статус = 1;
      Сообщение = "Не найден платеж";
    end;

    PrintRecordReport(pm_rmprop.Number, pm_paym.ValueDate , pm_paym.BaseAmount, Статус, Сообщение);

  elif (Вызов==Последний)
    PrintFooterReport();
  end;

END;
