import PTInter, CTInter, "globals.mac", cb_sql;
import EAInter;
import rsd, oralib, likepy;
import "dlwreps.mac";
import "ea_lib.mac";

record RecEDSTUNITLOG(edstunitlog);
file FileEDSTUNITLOG(edstunitlog) key 0;

/*Таблица для ярлыка*/
const _LableTableId = 1;

/*Порядковые номера полей для размножения в таблице*/
const 
  _BankBlockRowNum     = 1
 ,_DocKindBlockRowNum  = 2
 ,_TotalBlockRowNum    = 3
 ,_SoftwareBlockRowNum = 4
 ,_HashBlockRowNum     = 5;

private macro _MultipleRowToEnd
(
  table : integer /*идентификатор таблицы*/
 ,num : integer   /*необходимое количество строк*/
 ,row : integer   /*номер ряда, который необходимо размножить*/
)
  [~MultipleRow]; 
  println(table); /*идентификатор таблицы*/      
  println(num);   /*необходимое количество строк*/
  println();      /*закладка для таблицы*/
  println(row);   /*номер ряда, который необходимо размножить*/
  println(1);     /*добавить в конец таблицы*/
end;

private macro _DeleteRows
(
  table : integer /*идентификатор таблицы*/
 ,first : integer /*номер первой строки*/
 ,last : integer  /*номер последней строки*/
)
  [~DeleteRows]; 
  println(table); /*идентификатор таблицы*/
  println("%");
  println(first); /*номер первой строки*/
  println(last);  /*номер последней строки*/
end;

/**
 * Заполнить блок сведений о банке.
 * Содержит закладки:
 * bank
 * num
 * filialid
 * filialname
 * edate
 */
private macro _PrintBankBlock(edstunitlog)
  var bank = GetDprtPartyName({OperDprt});
  var num = edstunitlog.Number;
  var filialid = edstunitlog.Department;
  var filialname = GetDprtPartyName(edstunitlog.Department);
  var edate = string(edstunitlog.StorageCompleteDate:f);

  [~bank1];
  println(bank);
  [~num1];
  println(num);
  [~filialid1];
  println(filialid);
  [~filialname1];
  println(filialname);
  [~edate1];
  println(edate);
end;

private macro _PrintBankBlockSingle(edstunitlog)
  _MultipleRowToEnd(_LableTableId, 1, _BankBlockRowNum);
  _PrintBankBlock(edstunitlog);
end;

/**
 * Заполнить блок сведений о видах документов.
 * Содержит закладки:
 * dockind
 * docnum
 */ 
private macro _PrintDocKindBlock(dockind, docnum)
  [~dockind1];
  println(dockind);
  [~docnum1];
  println(docnum);
end;

private macro _PrintDocKindBlockSingle(dockind, docnum)
  _MultipleRowToEnd(_LableTableId, 1, _DocKindBlockRowNum);
  _PrintDocKindBlock(dockind, docnum);
end;

private macro _PrintDocKindBlockPoly(edstunitlog)
  var dockind;
  var docnum;
  var query;
  var rs;
  var params : TArray;

  CaptureOutput;
[
SELECT edkind.t_Name, COUNT(*)
  FROM dedkind_dbt edkind, dedoc_dbt edoc
 WHERE     edkind.t_EDocKindID = edoc.t_EDocKindID 
       AND edoc.t_StoreUnitLogID = :StoreUnitLogID
 GROUP BY edkind.t_Name
 ORDER BY edkind.t_Name
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("StoreUnitLogID", edstunitlog.StoreUnitLogID)
                    );
  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    dockind = rs.value(0);
    docnum = int(rs.value(1));
    _PrintDocKindBlockSingle(dockind, docnum);
  end;
end;

/**
 * Заполнить блок сведений о количестве документов.
 * Содержит закладки:
 * doctotal
 * dbeg
 * dend
 * drec
 * trec
 */ 
private macro _PrintTotalBlock(edstunitlog)
  var doctotal = GetDocTotal(edstunitlog);
  var dbeg = string(edstunitlog.PeriodBeginDate:f);
  var dend = string(edstunitlog.PeriodEndDate:f);
  var drec = string(edstunitlog.RecordDate:f);
  var trec = string(edstunitlog.RecordTime:f);

  [~doctotal1];
  println(doctotal);
  [~dbeg1];
  println(dbeg);
  [~dend1];
  println(dend);
  [~drec1];
  println(drec);
  [~trec1];
  println(trec);
end;

private macro _PrintTotalBlockSingle(edstunitlog)
  _MultipleRowToEnd(_LableTableId, 1, _TotalBlockRowNum);
  _PrintTotalBlock(edstunitlog);
end;

/**
 * Заполнить блок сведений об ассоциациях файлов.
 * Содержит закладки:
 * fext
 * software
 */ 
private macro _PrintSoftwareBlock(fext, software)
  [~fext1];
  println(fext);
  [~software1];
  println(software);
end;

private macro _PrintSoftwareBlockSingle(fext, software)
  _MultipleRowToEnd(_LableTableId, 1, _SoftwareBlockRowNum);
  _PrintSoftwareBlock(fext, software);
end;

private macro _PrintSoftwareBlockPoly(edstunitlog)
  var fext;
  var software;
  var query;
  var rs;
  var params : TArray;

  CaptureOutput;
[
SELECT DISTINCT llvalues.t_Code, llvalues.t_Name
  FROM dllvalues_dbt llvalues
 WHERE llvalues.t_List = 3536
       AND llvalues.t_Code IN
              (SELECT SUBSTR (
                         LOWER (edoc.t_FileName),
                         NVL (INSTR (LOWER (edoc.t_FileName), '.', -1), 0)
                         + 1)
                 FROM dedoc_dbt edoc
                WHERE edoc.t_StoreUnitLogID = :StoreUnitLogID)
 ORDER BY llvalues.t_Code
];
  query = StopCaptureOutput;
  params = makeArray(SQLParam("StoreUnitLogID", edstunitlog.StoreUnitLogID)
                    );
  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    fext = rs.value(0);
    software = rs.value(1);
    _PrintSoftwareBlockSingle(fext, software);
  end;
end;

/**
 * Заполнить блок сведений о хеше.
 * Содержит закладки:
 * hash
 * recordername
 */ 
private macro _PrintHashBlock(edstunitlog)
  var hash = GetHashStringEDSTUNITLOG(edstunitlog);
  var recordername = GetOperName(edstunitlog.Recorder);

  [~hash1];
  println(hash);
  [~recordername1];
  println(recordername);
end;

private macro _PrintHashBlockSingle(edstunitlog)
  _MultipleRowToEnd(_LableTableId, 1, _HashBlockRowNum);
  _PrintHashBlock(edstunitlog);
end;

private macro _EA_GetPathArchDocFileName(edstunitlog)
  var stat;
  var vFileName = "";
  var vImageDir = "";

  stat = EA_GetImageDir(edstunitlog, vImageDir);
  stat = EA_CreateDir(ToANSI(vImageDir, true));
  stat = EA_GetLabelFileName(edstunitlog, vFileName);

  return vImageDir + vFileName;
end;

macro EA_CreateLabel(edstunitlog, CloseDoc)
  var RealFileName : string; /*реальное имя файла документа*/
  var ArchFileName : string; /*полное имя файла документа в архиве узла ТС*/
  
  SetBuff(RecEDSTUNITLOG, edstunitlog);

  PrintLn("EALabelTemplate.dot");
  PrintLn("EALabel.doc");

  [~KillBm];
  
  _PrintBankBlockSingle(RecEDSTUNITLOG);
  _PrintDocKindBlockPoly(RecEDSTUNITLOG);
  _PrintTotalBlockSingle(RecEDSTUNITLOG);
  _PrintSoftwareBlockPoly(RecEDSTUNITLOG);
  _PrintHashBlockSingle(RecEDSTUNITLOG);
  _DeleteRows(_LableTableId, _BankBlockRowNum, _HashBlockRowNum);

  DLWREPS_PrintReportFromTagFile( SetOutput (GetTxtFileName ("EALabelWord")), @RealFileName, CloseDoc, true );

  ArchFileName = _EA_GetPathArchDocFileName(RecEDSTUNITLOG);
  
  if(IsStandAlone) /*Двухзвенка*/
    if(not CopyFile(RealFileName, ArchFileName))
      msgbox("Ошибка при копировании файла|" + RealFileName + "|в каталог архива|" + ArchFileName);
    end;
  else /*Трехзвенка*/
    if(not CopyFile("$" + RealFileName, ArchFileName))
      msgbox("Ошибка при копировании файла|" + RealFileName + "|в каталог архива|" + ArchFileName);
    end;
  end;

  SetOutput(null, false);
end;

macro EA_ViewLabel(edstunitlog)
  var ArchFileName : string; /*полное имя файла документа в архиве узла ТС*/
  
  SetBuff(RecEDSTUNITLOG, edstunitlog);

  ArchFileName = _EA_GetPathArchDocFileName(RecEDSTUNITLOG);

  /*Если путь к архиву указан как относительный то добавить путь до рабочего каталога*/
  if((index(trim(ArchFileName), "\\\\") != 1) and (index(trim(ArchFileName), ":") == 0))
    ArchFileName = GetCWD + "\\" + ArchFileName;
  end;

  if(ExistFile(ArchFileName))
    println(ArchFileName);
  
    DLWREPS_MergeReportFromTagFile(SetOutput(GetTxtFileName ("EALabelWord")));

    SetOutput(null, false);
  else
    msgbox("Файл \"", ArchFileName, "\" не найден");
  end;
end;

macro EA_ViewLabelMassOnStart()
end;

macro EA_ViewLabelMassOnFinish()
  DLWREPS_MergeReportFromTagFile(SetOutput(GetTxtFileName ("EALabelWord")));

  SetOutput(null, false);
end;

macro EA_ViewLabelMassOnEachElem(edstunitlog)
  var ArchFileName : string; /*полное имя файла документа в архиве узла ТС*/

  SetBuff(RecEDSTUNITLOG, edstunitlog);

  ArchFileName = _EA_GetPathArchDocFileName(RecEDSTUNITLOG);

  /*Если путь к архиву указан как относительный то добавить путь до рабочего каталога*/
  if((index(trim(ArchFileName), "\\\\") != 1) and (index(trim(ArchFileName), ":") == 0))
    ArchFileName = GetCWD + "\\" + ArchFileName;
  end;

  if(ExistFile(ArchFileName))
    println(ArchFileName);
  end;
end;

/*eof*/