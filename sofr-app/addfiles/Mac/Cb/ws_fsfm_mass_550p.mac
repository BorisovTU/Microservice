
/*
 $Name: ws_fsfm_mass_550p.mac
 $Module: RS-Connect
 $Description: Проверка наличия отказов в обслуживании (массовая)
 */


import BankInter, PTInter, CTInter, "cb_sql.mac", "globals.mac", rslx, rcw, rsexts;
import "ws_fsfm_param_lib.mac", "TSimpleExtService.mac", "common_service_caller.mac";
import "CbReport_h.mac";

private class (CServiceCallerBase) CMassRejectQueryCaller(_Param)
   private var m_param = _Param;

   private macro SetRequestParameters(request : @TXRRequest)
      var inParams = TArray();
      inParams[inParams.size] = m_param;
      request.Parms = inParams;
   end;
   
   private macro GetSystemURL() : String
    var m_systemURL;
      GetRegistryValue ("RS-CONNECT\\SSUSAGE_SNILS\\URL", V_STRING, m_systemURL);
    return m_systemURL;
   end;

   InitCServiceCallerBase(CreateGUID(), GetSystemURL(), "ws_fsfm_rjsrvmsg_search", "FSFM_SearchOperationMassForPeriod", 300000);
end;

private macro CastMassAnswer(answer : Variant)
    var answerCast = TFSFMResponseSearchOperationMass();
    answerCast.AnswerParam.SenderID      = answer.AnswerParam.SenderID;
    answerCast.AnswerParam.MessageID     = answer.AnswerParam.MessageID;
    answerCast.AnswerParam.SenderPointID = answer.AnswerParam.SenderPointID;
    answerCast.AnswerParam.ObjectID      = answer.AnswerParam.ObjectID;
    answerCast.AnswerParam.PackageNum    = answer.AnswerParam.PackageNum;
    answerCast.AnswerParam.PackageCount  = answer.AnswerParam.PackageCount;
    answerCast.SelectResult = TArray;
      
    for (var info, answer.SelectResult)
        var infoCast        = TFSFMOperationInfoLongOut();
        infoCast.RowID      = info.RowID;
        infoCast.IdentMess  = info.IdentMess;
        infoCast.DateMess   = info.DateMess;
        infoCast.TypeRecord = info.TypeRecord;
        infoCast.SignUse    = info.SignUse;
        infoCast.TypeClient = info.TypeClient;
        infoCast.IsRezident = info.IsRezident;
        infoCast.INN        = info.INN;
        infoCast.KPP        = info.KPP;
        infoCast.FullName   = info.FullName;
        infoCast.LastName   = info.LastName;
        infoCast.FirstName  = info.FirstName;
        infoCast.Patronymic = info.Patronymic;
        infoCast.BirthDate  = info.BirthDate;
        infoCast.CodeDoc    = info.CodeDoc;
        infoCast.SeriesDoc  = info.SeriesDoc;
        infoCast.NumDoc     = info.NumDoc;
        infoCast.DateDoc    = info.DateDoc;
        infoCast.CodeReject = info.CodeReject;
        infoCast.DateReject = info.DateReject;
        infoCast.OperAttr   = info.OperAttr;
        infoCast.MoneyAttr  = info.MoneyAttr;
        infoCast.CodeNO     = TArray;
        for (var codeNO, info.CodeNO)
            infoCast.CodeNO[infoCast.CodeNO.size] = codeNO;
        end;

        infoCast.CodesPrO   = TArray;
        if(info.CodesPrO != NULL)
          for (var CodesPrO, info.CodesPrO)
              infoCast.CodesPrO[infoCast.CodesPrO.size] = CodesPrO;
          end;
        end;

        infoCast.PrUd = info.PrUd;

        answerCast.SelectResult[answerCast.SelectResult.size] = infoCast;
    end;
    return answerCast;
end;

private macro FillInformationTable(obj : @variant)
  var stat = true;

  InitProgress( obj.SelectResult.size, "~Alt-Break~ Прервать", "Заполнение временной таблицы" );                     

  var fmrjmsgserviceCache = RsbSQLInsert("fmrjmsgservice.tmp");
  var t = 0;
  var j = 0;
  var jt = 0;

  var fmrjmsgservice = TRecHandler("fmrjmsgservice.tmp");

  var sizea = obj.SelectResult.size;
  while (t < sizea)
      var i = t;
      ClearRecord (fmrjmsgservice);
      var SelectResult = obj.SelectResult[i];

      if(ValType(SelectResult.TypeClient) != V_UNDEF) fmrjmsgservice.rec.ClientType = SelectResult.TypeClient; end;
      if(ValType(SelectResult.FullName  ) != V_UNDEF) fmrjmsgservice.rec.FullName   = SelectResult.FullName;   end;
      if(ValType(SelectResult.INN       ) != V_UNDEF) fmrjmsgservice.rec.INN        = SelectResult.INN;        end;
      if(ValType(SelectResult.KPP       ) != V_UNDEF) fmrjmsgservice.rec.KPP        = SelectResult.KPP;        end;
      if(ValType(SelectResult.birthdate ) != V_UNDEF) fmrjmsgservice.rec.birthdate  = SelectResult.birthdate;  end;
      if(ValType(SelectResult.CodeDoc   ) != V_UNDEF) fmrjmsgservice.rec.CodeDoc    = SelectResult.CodeDoc;    end;
      if(ValType(SelectResult.SeriesDoc ) != V_UNDEF) fmrjmsgservice.rec.seriesDoc  = SelectResult.SeriesDoc;  end;
      if(ValType(SelectResult.NumDoc    ) != V_UNDEF) fmrjmsgservice.rec.NumDoc     = SelectResult.NumDoc;     end;
      if(ValType(SelectResult.IdentMess ) != V_UNDEF) fmrjmsgservice.rec.IdentMess  = SelectResult.IdentMess;  end;
      if(ValType(SelectResult.DateMess  ) != V_UNDEF) fmrjmsgservice.rec.DateMess   = SelectResult.DateMess;   end;
      if(ValType(SelectResult.TypeRecord) != V_UNDEF) fmrjmsgservice.rec.TypeRecord = SelectResult.TypeRecord; end;
      if(ValType(SelectResult.CodeReject) != V_UNDEF) fmrjmsgservice.rec.CodeReject = SelectResult.CodeReject; end;
      if(ValType(SelectResult.DateReject) != V_UNDEF) fmrjmsgservice.rec.DateReject = SelectResult.DateReject; end;
      if(ValType(SelectResult.Patronymic) != V_UNDEF) fmrjmsgservice.rec.Patronymic = SelectResult.Patronymic; end;
      if(ValType(SelectResult.LastName  ) != V_UNDEF) fmrjmsgservice.rec.LastName   = SelectResult.LastName  ; end;
      if(ValType(SelectResult.FirstName ) != V_UNDEF) fmrjmsgservice.rec.FirstName  = SelectResult.FirstName ; end;
      
      if(ValType(SelectResult.CodesPrO) != V_UNDEF )
        j = 0;
        jt = 0;
        while(j < SelectResult.CodesPrO.size)
          jt = j;
          if(jt > 0)
            fmrjmsgservice.rec.RejectReasonCode = fmrjmsgservice.rec.RejectReasonCode + ";";
          end;
          fmrjmsgservice.rec.RejectReasonCode = fmrjmsgservice.rec.RejectReasonCode + SelectResult.CodesPrO[jt];
          j = j + 1;
        end;
      end;

      fmrjmsgserviceCache.AddRecord( fmrjmsgservice );
      t = t + 1;
      UseProgress( t );
  end;

  if(obj.SelectResult.size > 0)
    if(not fmrjmsgserviceCache.Insert())
      MsgBox("Ошибка вставки в fmrjmsgservice.tmp");
      stat = false;
    end;
  end;
  RemProgress();

  return stat;
end;


private macro ComparePartyTable()
  var stat = true;

  InitProgress( 7, "~Alt-Break~ Прервать", "Идентификация субъектов" );                     

  var strQuery = "";  

  //Для субъектов ЮЛ (форматами для данного типа предусмотрены INN KPP и FullName):
  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(pt.t_PartyID, 0), 1 "    // "идентификация успешна"
             "                                                              FROM dpartcode_dbt pc1,  dparty_dbt pt "
             "                                                             WHERE pc1.t_CodeKind = 16 "
             "                                                               AND pc1.t_Code = concat(DECODE(ms.T_INN, chr(1), '', ms.T_INN), DECODE(ms.T_KPP, chr(1),'',concat('/', ms.T_KPP) )) "
             "                                                               AND pc1.t_State = 0 "
             "                                                               AND pt.t_PartyID = pc1.t_PartyID "
             "                                                               AND pt.t_Name = ms.t_FullName  "
             "                                                               AND ROWNUM <=1  ) "
             " WHERE t_ClientType = 1 "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X'"
             "   AND t_INN <> chr(1) "
             "   AND t_FullName <> chr(1)";

  SQL_Execute(strQuery);

  UseProgress( 1 );

  //Для субъектов ЮЛ (совпадение INN)
  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(pc1.t_PartyID, 0), 2 " // требуется Ручная идентификация
             "                                                              FROM dpartcode_dbt pc1 "
             "                                                             WHERE pc1.t_CodeKind = 16 " 
             "                                                               AND pc1.t_NormCode = lpad(ms.T_INN, 35,'0') "
             "                                                               AND pc1.t_State = 0 "
             "                                                               AND ROWNUM <=1  ) "
             " WHERE t_ClientType = 1  "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X' "
             "   AND t_INN <> chr(1) ";

  SQL_Execute(strQuery);


  UseProgress( 2 );

  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(pt.t_PartyID, 0), 1 "    // "идентификация успешна"
             "                                                              FROM dpartcode_dbt pc1,  dparty_dbt pt "
             "                                                             WHERE pc1.t_CodeKind = 16 "
             "                                                               AND pc1.t_Code = concat(DECODE(ms.T_INN, chr(1), '', ms.T_INN), DECODE(ms.T_KPP, chr(1),'',concat('/', ms.T_KPP) )) "
             "                                                               AND pc1.t_State = 0 "
             "                                                               AND pt.t_PartyID = pc1.t_PartyID "
             "                                                               AND pt.t_Name = ms.t_FullName  "
             "                                                               AND ROWNUM <=1  ) "
             " WHERE t_ClientType = 1 "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X'"
             "   AND t_INN <> chr(1) "
             "   AND t_FullName <> chr(1)";

  SQL_Execute(strQuery);

  UseProgress( 3 );

  // Для субъектов ФЛ, ИП, адвокатов и нотариусов
  // Если INN указан у субъекта
  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(t_PartyID, 0), 1 FROM dpartcode_dbt WHERE t_CodeKind = 16 AND t_Code = ms.T_INN AND T_State = 0 AND ROWNUM <=1  ) "
             " WHERE t_ClientType IN (2,3,4) "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X' "
             "   AND t_INN <> chr(1) ";
 
  SQL_Execute(strQuery);

  UseProgress( 4 );

  // ищем среди оставшихся записей по ФИО
  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(t_PersonID, 0), 2 FROM dpersn_dbt "
             "                                                           WHERE t_Name1 = ms.t_LastName "
             "                                                           AND t_Name2 = ms.t_FirstName "
             "                                                           AND t_Name3 = ms.t_Patronymic "
             "                                                           AND (t_Born = ms.t_birthdate OR ms.t_birthdate = to_date('01.01.0001','DD.MM.YYYY')) "
             "                                                           AND ROWNUM <=1  ) "
             " WHERE t_ClientType IN (2,3,4) "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X' "
             "   AND t_LastName <> chr(1) "
             "   AND t_FirstName <> chr(1) ";

  SQL_Execute(strQuery);

  UseProgress( 5 );

  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(t_PersonID, 0), 2 FROM dpersn_dbt "
             "                                                            WHERE trim(both ' ' from REPLACE(concat(concat(t_Name1,t_Name2), t_Name3), chr(1))) = REPLACE(ms.t_FullName, ' ') "
             "                                                            AND (t_Born = ms.t_birthdate OR ms.t_birthdate = to_date('01.01.0001','DD.MM.YYYY')) "
             "                                                            AND ROWNUM <=1  ) "
             " WHERE t_ClientType IN (2,3,4) "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X' "
             "   AND t_FullName <> chr(1) "
             "   AND t_LastName = chr(1) "
             "   AND t_FirstName = chr(1) ";
              
  SQL_Execute(strQuery);

  UseProgress( 6 );

  // Для субъектов ИНБОЮЛ
  strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET (t_PartyID, t_Status) = (SELECT NVL(pt.t_PartyID, 0), 2"
             "                                                              FROM dparty_dbt pt "
             "                                                             WHERE pt.t_Name = ms.t_FullName  "
             "                                                               AND ROWNUM <=1  ) "
             " WHERE t_ClientType = 5 "
             "   AND (t_PartyID = 0 OR t_PartyID IS NULL) "
             "   AND t_Complete <> 'X'"
             "   AND t_FullName <> chr(1)";


  SQL_Execute(strQuery);

  UseProgress( 7 );
  RemProgress();


  return stat;
end;
              
private macro AddNoteParty()
  var stat = true;

  var sString = " SELECT t_PartyID, t_IdentMess, t_DateReject, t_TypeRecord, T_STATUS  "
                " FROM DFMRJMSGSERVICE_TMP "
                " WHERE (t_PartyID <> 0 AND t_PartyID IS NOT NULL) "
                "   AND t_Complete <> 'X' "
                " ORDER BY t_PartyID ";


  var scmd = RsdCommand( sString );
  scmd.NullConversion = true;
  var srs = RsdRecordset( scmd );

  InitProgress( -1, "~Alt-Break~ Прервать", "Вставка примечаний субъекта" );                     
  var i  = 0;
  while( srs.moveNext() )
    var PartyID = srs.value("t_PartyID");       
    var IdentMess = srs.value("t_IdentMess");       
    var strDateReject = string(date(srs.value("t_DateReject")));       
    var TypeRecord = int(srs.value("t_TypeRecord"));       
    var Status = int(srs.value("T_STATUS"));  

    if(TypeRecord != 4)
      var AddNote = IdentMess+ "\\" + strDateReject;

      if (Status == 2)
        AddNote = AddNote + "\\@"
      end;
      
      AddNote = "<" + AddNote + ">";

      var FullNote = readNoteForObject( OBJTYPE_PARTY, string( PartyID:o:10 ), 71 );

      if(strlen(FullNote) > 0 )
        FullNote = FullNote + ";" + AddNote;
      else
        FullNote = AddNote;
      end;

      AddNoteForObject( OBJTYPE_PARTY, string( PartyID:o:10 ), 71, FullNote );

      i = i + 1;
      UseProgress( i );
    end;
  end;

  RemProgress();

  return stat;
end;

private macro SetComplete()
  var stat = true;

  var strQuery = " UPDATE DFMRJMSGSERVICE_TMP ms SET t_Complete = 'X'";
  
  SQL_Execute(strQuery);

  return stat;
end;

private macro GetQuestion()

  Array Text;
  Array Buttons;

  Text(0) = "Потеряна связь с RS-Connect. Обработана только часть данных. " ;

  Buttons(0)  = " Связаться повторно ";
  Buttons(1)   = " Завершить процедуру ";

  return ConfWin(Text,Buttons);

end;

private macro GetPackageInfo(SenderId, url, ObjectID, PackageNum:@integer, CompletedPackageNum)
  var stat = true;
  var mes = TFSFMRequestSearchOperationMass;

  mes.QueryParam = TFSFMHeaderInOut;
  mes.QueryParam.SenderID = SenderId;
  mes.QueryParam.MessageID = ObjectID;
  mes.QueryParam.SenderPointID = "";
      
  mes.SelectParam = TFSFMBodySearchOperationMassIn;
  mes.SelectParam.ObjectID = ObjectID;

  PackageNum = PackageNum+1;
  mes.SelectParam.PackageNum = PackageNum;
  mes.SelectParam.CompletedPackageNum = CompletedPackageNum;

  var retstat = false;
    var caller = CMassRejectQueryCaller(mes);
    var answer;

  while((retstat != true) AND (stat == true) )
     InitProgress( -1, "~Alt-Break~ Прервать", "Получение данных через транспортную компоненту." );                     
     retstat = caller.CallService(@answer, true);
     RemProgress();

     if (retstat != true)
       if(GetQuestion() != 0)
         stat = false;
       end;
     end;
   end;

  if((retstat == true) AND (stat == true))
      var obj;
      var retv = false;
      while((retv == false) AND (stat == true))
        InitProgress( -1, "~Alt-Break~ Прервать", "Получение данных через транспортную компоненту." );                     
            obj = CastMassAnswer(answer);
            retv = true;
        RemProgress();
      end;

      if((retv == true) AND (stat == true))
          if(PackageNum > 0 ) // Если не финализирующий запрос
            if (obj.AnswerParam.PackageCount == 0)
              RunError("", "Информация по запрошенным параметрам отсутствует");
            else
                stat = FillInformationTable(obj);

                if(stat == true)
                  stat = ComparePartyTable();
                end;

                if(stat == true)
                  stat = AddNoteParty();
                end;

                if(stat == true)
                  stat = SetComplete();
                end;
            end;
          end;
      end;
  else
    stat = false;
  end;

  return stat; 
end;

private macro PrintHeader0(BegDate, EndDate, Processed)
[
    Отчет о результатах проверки субъектов на наличие отказов в обслуживании в других организациях
                           за период с ########## по ##########
] (BegDate, EndDate);

  if(Processed == 0)
[                               Только новые сообщения об отказах];
  end;

end;

private macro PrintHeader1()
[───────┬─────────────┬──────────────┬──────────────┬────────────────────────────┬────────────────────────────┬─────────────┬─────────────┬─────────────────────────────  
  № п/п │ ID Субъекта │ Код субъекта │ ИНН субъекта │   Наименование субъекта    │         ID отказа          │ Тип записи  │ Код отказа  │      Коды причин отказа                       
 ───────┼─────────────┼──────────────┼──────────────┼────────────────────────────┼────────────────────────────┼─────────────┼─────────────┼─────────────────────────────];
  
end;

private macro PrintLine(N, ID, Code1, Code16, Name, ID_R, TZ, CodeReject, RejectReasonCode)
[ ######│ ########### │##############│##############│############################│############################│ ############│ ############│############################]
(N, ID, Code1, Code16, Name:w, ID_R, TZ, CodeReject, RejectReasonCode);
end;




private macro PrintReport(BegDate, EndDate, Processed)

  var PartyID;
  var Name;
  var IdentMess;
  var TypeRecord;
  var Code1;
  var Code16;
  var CodeReject;
  var RejectReasonCode;

  var sString = " SELECT count(t_PartyID) numparty"
                " FROM DFMRJMSGSERVICE_TMP "
                " WHERE (t_PartyID <> 0 AND t_PartyID IS NOT NULL) ";


  var scmd = RsdCommand( sString );
  scmd.NullConversion = true;
  var srs = RsdRecordset( scmd );

  if( srs.moveNext() )
    var Count = int( srs.value("numparty"));       
  end;

  if(Count == 0)
[ Сообщений об отказах по субъектам в соответствии с параметрами запроса не найдено] 
  else
    PrintHeader0(BegDate, EndDate, Processed);
  
[
  
  Успешная идентификация:];

    PrintHeader1();

    sString = " SELECT ms.t_PartyID, pt.t_Name, ms.t_IdentMess, ms.t_TypeRecord, ms.t_CodeReject, ms.t_RejectReasonCode, "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 1) , CHR(1)) t_Code1 , "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 16), CHR(1)) t_Code16 "
              " FROM DFMRJMSGSERVICE_TMP ms, dparty_dbt pt "
              " WHERE (ms.t_PartyID <> 0 AND ms.t_PartyID IS NOT NULL) "
              "   AND ms.t_PartyID = pt.t_PartyID "
              "   AND ms.t_Status = 1 "
              "   AND ms.t_TypeRecord <> 4 "
              " ORDER BY ms.t_PartyID ";

    scmd = RsdCommand( sString );
    scmd.NullConversion = true;
    srs = RsdRecordset( scmd );
    var NR_A = 0;
    while( srs.moveNext() )
      PartyID = srs.value("t_PartyID");       
      Name = srs.value("t_Name");       
      IdentMess = srs.value("t_IdentMess");       
      TypeRecord = srs.value("t_TypeRecord");       
      Code1 = srs.value("t_Code1");       
      Code16 = srs.value("t_Code16");
      CodeReject = srs.value("t_CodeReject");       
      RejectReasonCode = srs.value("t_RejectReasonCode");       

      NR_A = NR_A + 1;

      PrintLine(NR_A, PartyID, Code1, Code16, Name, IdentMess, TypeRecord, CodeReject, RejectReasonCode);
    end;
  

[
  
  Требуется ручная идентификация:];
    PrintHeader1();

    sString = " SELECT ms.t_PartyID, pt.t_Name, ms.t_IdentMess, ms.t_TypeRecord, ms.t_CodeReject, ms.t_RejectReasonCode, "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 1) , CHR(1)) t_Code1 , "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 16), CHR(1)) t_Code16 "
              " FROM DFMRJMSGSERVICE_TMP ms, dparty_dbt pt "
              " WHERE (ms.t_PartyID <> 0 AND ms.t_PartyID IS NOT NULL) "
              "   AND ms.t_PartyID = pt.t_PartyID "
              "   AND (ms.t_Status = 2 OR ms.t_TypeRecord = 4) "
              " ORDER BY ms.t_PartyID ";

    scmd = RsdCommand( sString );
    scmd.NullConversion = true;
    srs = RsdRecordset( scmd );
    VAR NU_R = 0;
    while( srs.moveNext() )
      PartyID = srs.value("t_PartyID");       
      Name = srs.value("t_Name");       
      IdentMess = srs.value("t_IdentMess");       
      TypeRecord = srs.value("t_TypeRecord");       
      Code1 = srs.value("t_Code1");       
      Code16 = srs.value("t_Code16");       
      CodeReject = srs.value("t_CodeReject");
      RejectReasonCode = srs.value("t_RejectReasonCode");          

      NU_R = NU_R + 1;
 
      PrintLine(NU_R, PartyID, Code1, Code16, Name, IdentMess, TypeRecord, CodeReject, RejectReasonCode);
    end;

[

───────────────────────────────────────────
Всего рассмотрено субъектов:           ##########

Всего найдено отказов:               ##########
Требуется уточнение:                 ##########

Дата и время проверки: ##########  ########
Исполнитель:           ##########  ##################################################
](Count, NR_A, NU_R, date(), time(), {oper}:l, {Name_Oper});
  end;


end;


private macro PrintReportTable1Xls(ObjPrint, NR_A:@integer )

  var PartyID;
  var Name;
  var IdentMess;
  var TypeRecord;
  var Code1;
  var Code16;
  var CodeReject;
  var RejectReasonCode;
  
  ObjPrint.SheetChange("Успешная идентификация");
  var Table = ObjPrint.RegisterTable("N1_MainTable");


  var sString = " SELECT ms.t_PartyID, pt.t_Name, ms.t_IdentMess, ms.t_TypeRecord, ms.t_CodeReject, ms.t_RejectReasonCode, "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 1) , CHR(1)) t_Code1 , "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 16), CHR(1)) t_Code16 "
              " FROM DFMRJMSGSERVICE_TMP ms, dparty_dbt pt "
              " WHERE (ms.t_PartyID <> 0 AND ms.t_PartyID IS NOT NULL) "
              "   AND ms.t_PartyID = pt.t_PartyID "
              "   AND ms.t_Status = 1 "
              "   AND ms.t_TypeRecord <> 4 "
              " ORDER BY ms.t_PartyID ";

  var scmd = RsdCommand( sString );
  scmd.NullConversion = true;
  var srs = RsdRecordset( scmd );
  NR_A = 0;
  while( srs.moveNext() )
    PartyID = srs.value("t_PartyID");       
    Name = srs.value("t_Name");       
    IdentMess = srs.value("t_IdentMess");       
    TypeRecord = srs.value("t_TypeRecord");       
    Code1 = string(srs.value("t_Code1"));       
    Code16 = string(srs.value("t_Code16"));
    CodeReject = srs.value("t_CodeReject");       
    RejectReasonCode = srs.value("t_RejectReasonCode");       

    NR_A = NR_A + 1;

    Table.SetValueCell("T1_NUM"                  ,  NR_A             );
    Table.SetValueCell("T1_PARTYID"              ,  PartyID          );
    Table.SetValueCell("T1_CODE"                 ,  Code1            );
    Table.SetValueCell("T1_INN"                  ,  Code16           );
    Table.SetValueCell("T1_NAMEPT"               ,  Name             );
    Table.SetValueCell("T1_RJID"                 ,  IdentMess        );
    Table.SetValueCell("T1_TYPER"                ,  TypeRecord       );
    Table.SetValueCell("T1_CODER"                ,  CodeReject       );
    Table.SetValueCell("T1_REJECTREASONCODE"     ,  RejectReasonCode );
    Table.AddStr(); 
  end;

  Table.EndTable();
end;




private macro PrintReportTable2Xls(ObjPrint, NU_R:@integer )

  var PartyID;
  var Name;
  var IdentMess;
  var TypeRecord;
  var Code1;
  var Code16;
  var CodeReject;
  var RejectReasonCode;
  
  ObjPrint.SheetChange("Требуется ручная идентификация");
  var Table = ObjPrint.RegisterTable("N2_MainTable");


  var sString = " SELECT ms.t_PartyID, pt.t_Name, ms.t_IdentMess, ms.t_TypeRecord, ms.t_CodeReject, ms.t_RejectReasonCode, "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 1) , CHR(1)) t_Code1 , "
              " NVL((SELECT t_Code FROM dpartcode_dbt WHERE t_PartyID = pt.t_PartyID AND t_CodeKind = 16), CHR(1)) t_Code16 "
              " FROM DFMRJMSGSERVICE_TMP ms, dparty_dbt pt "
              " WHERE (ms.t_PartyID <> 0 AND ms.t_PartyID IS NOT NULL) "
              "   AND ms.t_PartyID = pt.t_PartyID "
              "   AND (ms.t_Status = 2 OR ms.t_TypeRecord = 4) "
              " ORDER BY ms.t_PartyID ";

  var scmd = RsdCommand( sString );
  scmd.NullConversion = true;
  var srs = RsdRecordset( scmd );
  NU_R = 0;
  while( srs.moveNext() )
    PartyID = srs.value("t_PartyID");       
    Name = srs.value("t_Name");       
    IdentMess = srs.value("t_IdentMess");       
    TypeRecord = srs.value("t_TypeRecord");       
    Code1 = string(srs.value("t_Code1"));       
    Code16 = string(srs.value("t_Code16"));
    CodeReject = srs.value("t_CodeReject");       
    RejectReasonCode = srs.value("t_RejectReasonCode");       

    NU_R = NU_R + 1;

    Table.SetValueCell("T2_NUM"                  ,  NU_R             );
    Table.SetValueCell("T2_PARTYID"              ,  PartyID          );
    Table.SetValueCell("T2_CODE"                 ,  Code1            );
    Table.SetValueCell("T2_INN"                  ,  Code16           );
    Table.SetValueCell("T2_NAMEPT"               ,  Name             );
    Table.SetValueCell("T2_RJID"                 ,  IdentMess        );
    Table.SetValueCell("T2_TYPER"                ,  TypeRecord       );
    Table.SetValueCell("T2_CODER"                ,  CodeReject       );
    Table.SetValueCell("T2_REJECTREASONCODE"     ,  RejectReasonCode );

    Table.AddStr(); 
  end;

  Table.EndTable();
end;

private macro PrintReportXls(BegDate, EndDate, Processed)

  var Count = 0, NR_A = 0, NU_R = 0;
  var ObjPrint:CTemplateXLS = CTemplateXLS();                                           // основной объект отчёта
  
  ObjPrint.CreateTotalBook();

  var stat =  ObjPrint.OpenTemplate("fsfmmasschk550.xls");

  PrintReportTable1Xls(ObjPrint, @NR_A );
  
  PrintReportTable2Xls(ObjPrint, @NU_R );


  var sString = " SELECT count(t_PartyID) numparty"
                " FROM DFMRJMSGSERVICE_TMP "
                " WHERE (t_PartyID <> 0 AND t_PartyID IS NOT NULL) ";


  var scmd = RsdCommand( sString );
  scmd.NullConversion = true;
  var srs = RsdRecordset( scmd );

  if( srs.moveNext() )
    Count = int( srs.value("numparty"));       
  end;

  ObjPrint.SheetChange("Отчет");

  ObjPrint.SetValue_NameCell("Period", "за период с: " + String(BegDate) + " по: " + String(EndDate));

  if(Processed == 0)
    ObjPrint.SetValue_NameCell("OnlyNew", "Только новые сообщения об отказах");
  else
    ObjPrint.SetValue_NameCell("OnlyNew", "");
  end;

  ObjPrint.SetValue_NameCell("Count"    , Count);
  ObjPrint.SetValue_NameCell("NR_A"     , NR_A);
  ObjPrint.SetValue_NameCell("NU_R"     , NU_R);
  ObjPrint.SetValue_NameCell("Date_Time",  string(date()) + " " + string(time()) );
  ObjPrint.SetValue_NameCell("Oper_Name", string({oper}:l) + " " + string({Name_Oper}));

  stat = ObjPrint.CopyAllSheetInTotalBook(); // кидаем закладку в книгу

  ObjPrint.Close(FALSE);

  ObjPrint.SaveTotalBook("fsfmmasschk550_" + strsubst(string(date:f), ".", "") + "_" + strsubst(string(time:f), ":", ""));
end;



macro FSFM_SearchOperationMass(
    SenderId, url,
    BegDate,       // (K: 0-1) Дата начала периода проверки
    EndDate,       // (K: 0-1) Дата окончания периода проверки
    Processed      // 
)       
    var stat = true;
    var error : string;
    var paramArray = TArray;
    var errMsg : string;
    var service = TSimpleExtService("ws_fsfm_rjsrvmsg_search", "FSFM_SearchOperationMassForPeriod");
        
    var mes = TFSFMRequestSearchOperationMass;
    mes.QueryParam = TFSFMHeaderInOut;
    mes.QueryParam.SenderID = SenderId;
    var GUID = CreateGUID();
    var MessageID = substr(GUID, 2, strlen(GUID)-2);
    mes.QueryParam.MessageID = MessageID;
    mes.QueryParam.SenderPointID = "";
        
    mes.SelectParam = TFSFMBodySearchOperationMassIn;
    if(Processed > 0 )
      mes.SelectParam.NeedOldRecs = true;  // признак установлен, и новые и обработанные
    else
      mes.SelectParam.NeedOldRecs = false; // признак не установлен, только новые.
    end;
    mes.SelectParam.BegDate = BegDate;
    mes.SelectParam.EndDate = EndDate;
    
    mes.SelectParam.PackageSize = 0;

    mes.SelectParam.ObjectID = MessageID;
    mes.SelectParam.PackageNum = 0;
    mes.SelectParam.CompletedPackageNum = 0;
    
    var caller = CMassRejectQueryCaller(mes);
    var answer;
    var CallService_stat = caller.CallService(@answer);
    var obj = CastMassAnswer(answer);
    
    if (obj.AnswerParam.PackageCount == 0)
      RunError ("", "Информация по запрошенным параметрам отсутствует");
    else
        var PackageCount  = obj.AnswerParam.PackageCount;
        var CurPackageNum = obj.AnswerParam.PackageNum;

        var CompletedPackageNum = 0;
        InitProgress( PackageCount, "~Alt-Break~ Прервать", "Проверка наличия отказов в обслуживании" );
        while ( (CurPackageNum <= PackageCount) AND (stat == true) )
           stat = GetPackageInfo(SenderId, url, MessageID, @CurPackageNum, CompletedPackageNum);

           if(stat == true)
              CompletedPackageNum = CurPackageNum;
           end;

           UseProgress( CurPackageNum );
        end;

        // финализирующий запрос
        if(stat == true)
           CurPackageNum = -1;
           stat = GetPackageInfo(SenderId, url, MessageID, @CurPackageNum, CompletedPackageNum);
        end;

        RemProgress();
    end;


    
    var retv = 0;
    var RegVal = false;
    GetRegistryValue( "CB\\PARTY\\XLS_SEARCH_OPERATION_MASS", V_BOOL, RegVal, retv );

    if( retv != 0 )
      RunError("", " Ошибка чтения настройки CB\\PARTY\\XLS_SEARCH_OPERATION_MASS ");
    end;

    if(RegVal)
      PrintReportXls(BegDate, EndDate, Processed);
    else
      PrintReport(BegDate, EndDate, Processed);
    end;
    
    return stat;
OnError(err)
    if (ValType(err.Err) != V_UNDEF)
        MsgBox (err.Err);
    else
        MsgBox (err.Message);
    end;
    return false;
end;



