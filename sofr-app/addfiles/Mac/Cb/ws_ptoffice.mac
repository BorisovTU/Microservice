import globals;
import RsbDataSet;  
import CurrInter, XmlRpcInter, BankInter, CTInter, PTInter;   
import oralib;
import serviceaction;
import rcw;
import dlwreps;
import rsd;

import "ws_validation.mac";

class TWsObjKDoc()
  var objecttype        : integer;
  var name              : string;   // Полное наименование рег. документа
  var regdockind        : integer;  // Вид регистрационного документа
  var shortname         : string;
  var isregsertificate  : string;
end;

class TWsPtOffice()
  var PartyId         : integer;
  var OfficeId        : integer;
  var SuperiorID      : integer;
  var PartyName       : String;
  var Code            : String;
  var Name            : String;
  var Description     : String;
  var KPP             : String;
  var RegNumber       : String;
  var RegDate         : Date;
  var RegDoc;         //: TWsObjKDoc;
  var Series          : String;
  var Number          : String;
  var DateClose       : Date;
  var DocKindId       : integer;
  var Children;       //: List<TWsPtOffice>;
  var MoveOfficeMode  : bool;
end;

class TWsPtOffiSu()
  var PartyId       : Integer;
  var OfficeId      : Integer;
  var DateBegin     : Date;
  var SuperiorId    : Integer;
  var SuperiorName  : String;
  var Sort          : Integer;
end;

/* Вспомогательный класс, т.к. переменные сессии не реализованы в инструменте: */
class TWsCurrentSessionData
  var Oper       : integer;
  var OperName   : string;
  var CurDate    : date;
end;

private const NoSuperiorOffice = "Нет вышестоящего";

/* Вспомогательные функции для заполнения TWsPtOffice */

/* Вывод в файл с именем outFileName переменной dataString */
macro GetFullSQLConditionDebug( outFileName:String, dataString ) 
  var fullOutFileName:String = ""; 
  FILE outFile() txt write; 
  if( ValType( outFileName ) != V_UNDEF ) 
    fullOutFileName = GetTxtFileName( outFileName ); 

    if( Open( outFile, fullOutFileName ) == true ) 
      SetOutput( fullOutFileName ); 
      println( dataString ); 
      SetOutput( NULL, true ); 
      Close( outFile ); 
    end; 
  end; 
end;

macro getServiceError(errorCode, errorMessage, errorHelpPage, userObject)
    var responseBuilder = WebResponseBuilder;
    var error           = WsErrorMessage;

    error.Severity  = MessageSeverity.ValidationError;
    error.Code      = errorCode;
    error.Message   = errorMessage;
    error.Help      = errorHelpPage;

    responseBuilder.AddMessage(error); 

    responseBuilder.SetUserObject(userObject); 

    return responseBuilder.Result;
end;

macro GetCurDate()
  var sessionData = TWsCurrentSessionData();

  sessionData.CurDate = {curdate};

  return sessionData;
end;

macro GetPartyName(partyId)  
  var sqlText = "SELECT t_name FROM dparty_dbt WHERE t_partyid = ?";

  var cmd : RsdCommand, partyName, dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, partyId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    partyName = dataSet.value("t_name");
  end;

  return partyName;
end;

macro GetSuperiorName(partyId, officeId)  
  var sqlText = "SELECT t_officename FROM dptoffice_dbt WHERE t_partyid = ? AND t_officeid = ?";

  var cmd : RsdCommand, superiorName = "", dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, partyId);
  cmd.AddParam("", RSDBP_IN, officeId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    superiorName = dataSet.value("t_officename");
  end;

  if(superiorName != "")
    return superiorName;
  else
    return NoSuperiorOffice;
  end;
end;

macro GetRegDoc(docKindId)
  var sqlText = "SELECT t_regdockind, t_name FROM dobjkdoc_dbt WHERE t_regdockind = ? AND t_objectType = ?";

  var cmd : RsdCommand, tempObj = TWsObjKDoc(), dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, docKindId);
  cmd.AddParam("", RSDBP_IN, OBJTYPE_PARTY);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    tempObj.regdockind  = dataSet.value("t_regdockind");
    tempObj.name        = dataSet.value("t_name");
  end;

  return tempObj;
end;

// Из Record'а в объект класса
macro ReturnOffice(target)
  var source = TWsPtOffice();

  source.PartyId      = target.PartyID;
  source.OfficeId     = target.OfficeID;
  source.PartyName    = GetPartyName(target.PartyId);
  source.Code         = target.OfficeCode;
  source.Name         = target.OfficeName;
  source.Description  = target.Description;
  source.KPP          = target.KPP;
  source.RegNumber    = target.RegNumber;
  source.RegDate      = target.RegDate;
  source.RegDoc       = GetRegDoc(target.DocKindID);
  source.Series       = target.Series;
  source.Number       = target.Number;
  source.DateClose    = target.DateClose;
  source.DocKindId    = target.DocKindID;

  if(source.PartyId == 0)
    return null;
  else
    return source;
  end;
end;

// Из объекта класса в record
macro fillTableRecord(ptOfficeRecord, ptOfficeObject)
  ptOfficeRecord.PartyId      = ptOfficeObject.PartyId;
  ptOfficeRecord.OfficeId     = ptOfficeObject.OfficeId;
  ptOfficeRecord.OfficeCode   = ptOfficeObject.Code;
  ptOfficeRecord.OfficeName   = ptOfficeObject.Name;
  ptOfficeRecord.Description  = ptOfficeObject.Description;
  ptOfficeRecord.KPP          = ptOfficeObject.KPP;
  ptOfficeRecord.RegNumber    = ptOfficeObject.RegNumber;
  ptOfficeRecord.RegDate      = ptOfficeObject.RegDate;
  ptOfficeRecord.Series       = ptOfficeObject.Series;
  ptOfficeRecord.Number       = ptOfficeObject.Number;
  ptOfficeRecord.DateClose    = ptOfficeObject.DateClose;
  ptOfficeRecord.DocKindId    = ptOfficeObject.DocKindId;
end;


macro CB_GetPtOffices
(
  PartyId : integer,
  OnDate  : Date
)
  var ptOfficeTreeObj : PTOfficeTree = PTOfficeTree(PartyId, 0, OnDate);

  var ptOfficeList = TArray();
  
  var office : TWsPtOffice;
  var officeChildren : TWsPtOffice;
  
  record ptOffice("ptoffice.dbt");
  record ptOfficeChildren("ptoffice.dbt");
                                          
  ptOfficeTreeObj.first(ptOffice, ptOfficeChildren); 

  var firstOfficeInHierarchy = ReturnOffice(ptOffice);

  if(firstOfficeInHierarchy != null)
    ptOfficeList[ptOfficeList.size] = firstOfficeInHierarchy;  

    while(true)  
      if(ptOfficeTreeObj.next(ptOfficeChildren, ptOffice) == false)
        break;
      end;          
                           
      office = ReturnOffice(ptOffice);            
      officeChildren = ReturnOffice(ptOfficeChildren);

      if(Office.partyId != null)
        officeChildren.superiorid = ptOffice.officeId;
      end;

      if(officeChildren.partyId != null)
        ptOfficeList[ptOfficeList.size] = officeChildren;         
      end;    
    end;
  end;

  if(ptOfficeList.size == 0)
    return null;
  else
    return ptOfficeList;
  end;
end;

macro ChildOfficesHaveErrors
(
  parentPtOfficeObject
)
  if((ValType(parentPtOfficeObject) == V_UNDEF) OR (parentPtOfficeObject == NULL))
    return false;
  end;

  var status = 0;

  var sqlText = "SELECT offisu.*, office.* " +
                "FROM dptoffisu_dbt offisu, dptoffice_dbt office " + 
                "WHERE OFFISU.T_PARTYID = ? " + 

                   "AND office.t_partyid = offisu.t_partyid " + 
                   "AND offisu.t_superiorid = office.t_officeid " + 
                   
                   "AND " +
                   "(OFFICE.T_REGDATE < ? " +       /* parent.t_regdate */
                   "OR OFFICE.T_REGDATE > ? " +     /* parent.t_dateclose */
                   "OR OFFICE.T_DATECLOSE > ? " +   /* parent.t_dateclose */
                   "OR OFFICE.T_DATECLOSE < ?) " +  /* parent.t_regdate */      
                       
                "START WITH offisu.t_superiorid = ? " + 
                "CONNECT BY NOCYCLE offisu.t_superiorid = prior office.t_officeid";

  var cmd : RsdCommand, dataSet; 

  cmd.cmdText = sqlText;
  cmd.NullConversion = true;

  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.PartyId);

  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.RegDate);
  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.DateClose);

  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.DateClose);
  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.RegDate);

  cmd.AddParam("", RSDBP_IN, parentPtOfficeObject.OfficeId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    status = true;
  end;

  return status;
end;

macro CB_PtOfficeSave
(
  ptOffice, //: TWsPtOffice,
  superiorPtOffice,
  sortPosition
)                      
  var ValidationRetVal = NULL;
  var errMessage : string = "";
  record ptOfficeRecord("ptoffice.dbt");

  clearRecord(ptOfficeRecord);
  fillTableRecord(ptOfficeRecord, ptOffice);

  if(ptOffice.DateClose >= date(2, 1, 1))
    var result;

    if(ptOffice.DateClose < ptOffice.RegDate)
      ValidationRetVal = getServiceError(1, "Невозможно сохранить: дата расформирования должна быть больше даты регистрации", 0, NULL);
    end;

    if(ChildOfficesHaveErrors(ptOffice) == true)
      ValidationRetVal = getServiceError(2, "Невозможно сохранить: интервал действия подразделения не совпадает с интервалом действия вышестоящего подразделения", 0, NULL);
    else
      if(superiorPtOffice != null)
        if((ptOffice.RegDate < superiorPtOffice.RegDate) AND 
          (ptOffice.RegDate >= superiorPtOffice.DateClose))
            ValidationRetVal = getServiceError(3, "Невозможно сохранить: имеются подчиненные поздразделения", 0, NULL);
        end;
      end;
    end;
  end;

  if(ptOffice.OfficeId == 0)      
    var superiorOfficeId = ptOffice.SuperiorID;

    if(superiorOfficeId > 0)  // Если вставляем "До после подуровень"
      result = PTOfficeInsert(ptOfficeRecord, superiorOfficeId, sortPosition, errMessage);
    else
      result = PTOfficeInsert(ptOfficeRecord, NULL, NULL, errMessage);
    end;

    if(result == false)
      ValidationRetVal = getServiceError(4, errMessage, 0, NULL);
    end;                    
  else                           
    result = PTOfficeUpdate(ptOfficeRecord);

    if(result == false)
      ValidationRetVal = getServiceError(5, errMessage, 0, NULL);
    end;
  end;     
      
  return ValidationRetVal;
end;

macro CB_PtOfficeDelete
(
  PartyId   : integer,  // Идентификатор субъекта экономики
  OfficeId  : integer   // Идентификатор подразделения
)          
                             
  if(rslPTOfficeIsOfficeHasChilds(PartyId, OfficeId) == false)
    PTOfficeDelete(PartyId, OfficeId);
  else
    RunError("Удаление подразделения запрещено: имеются не расформированные подчиненные подразделения");
  end;

  return 0;
end;

macro CB_GetKindRegDocs
(
  ObjectType    : integer,
  RegPartyKind  : integer
)
  var sqlText = "select kdoc.t_ObjectType, kdoc.t_RegDocKind, kdoc.t_Name, kdoc.t_shortname, kdoc.t_isregsertificate from dobjkdoc_dbt kdoc, dobjalreg_dbt alreg " 
                "where alreg.t_ObjectType = ? AND alreg.t_RegPartyKind = ? "
                "AND alreg.t_RegDocKind = kdoc.t_RegDocKind";

  var cmd : RsdCommand, objectsArray = TArray(), tempObj = TWsObjKDoc(), dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, RegPartyKind);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj = TWsObjKDoc();    
      tempObj.objecttype        = dataSet.value(0L);
      tempObj.regdockind        = dataSet.value(1L);
      tempObj.name              = dataSet.value(2L);
      tempObj.shortname         = dataSet.value(3L);
      tempObj.isregsertificate  = dataSet.value(4L);

    objectsArray[objectsArray.size] = tempObj;
  end;

  return objectsArray;
end;

macro CB_GetPtOffiSu
(
  PartyId   : integer,
  OfficeId  : integer
)
  var sqlText = "select * from dptoffisu_dbt offisu, dptoffice_dbt office where offisu.t_PartyID = ? "
                "AND offisu.t_OfficeID = ? AND office.t_PartyId = offisu.t_PartyId "
                "AND office.t_OfficeID = offisu.t_OfficeID";

  var cmd : RsdCommand, objectsArray = TArray(), tempObj = TWsPtOffiSu(), dataSet;

  cmd.cmdText = sqlText;

  cmd.AddParam("", RSDBP_IN, PartyId);
  cmd.AddParam("", RSDBP_IN, OfficeId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  while(dataSet.moveNext())
    tempObj = TWsPtOffiSu();    
      tempObj.PartyId       = dataSet.value("t_PartyId");
      tempObj.OfficeId      = dataSet.value("t_OfficeId");
      tempObj.DateBegin     = dataSet.value("t_DateBegin");
      tempObj.SuperiorId    = dataSet.value("t_SuperiorId");
      tempObj.SuperiorName  = GetSuperiorName(tempObj.PartyId, tempObj.SuperiorId);
      tempObj.Sort          = dataSet.value("t_Sort");

    objectsArray[objectsArray.size] = tempObj;
  end;

  return objectsArray;
end;

macro CB_GetPtOfficesRec
(
  PartyId : integer,
  OfficeID : integer
)
  var source = TWsPtOffice();
  var sqlText = "SELECT * FROM dptoffice_dbt pto WHERE t_partyid = ? AND t_officeid = ?";

  var cmd : RsdCommand, superiorName = "", dataSet;

  cmd.cmdText = sqlText;
  cmd.NullConversion = true;

  cmd.AddParam("", RSDBP_IN, partyId);
  cmd.AddParam("", RSDBP_IN, officeId);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    source.PartyId      = dataSet.value("t_PartyID");
    source.OfficeId     = dataSet.value("t_OfficeID");
    source.PartyName    = GetPartyName(source.PartyId);
    source.Code         = dataSet.value("t_OfficeCode"); 
    source.Name         = dataSet.value("t_officename");
    source.Description  = dataSet.value("t_Description");
    source.KPP          = dataSet.value("t_KPP");
    source.RegNumber    = dataSet.value("t_RegNumber");
    source.RegDate      = dataSet.value("t_RegDate");
    source.RegDoc       = GetRegDoc(dataSet.value("t_DocKindID"));
    source.Series       = dataSet.value("t_Series");
    source.Number       = dataSet.value("t_Number");
    source.DateClose    = dataSet.value("t_DateClose");
    source.DocKindId    = dataSet.value("t_DocKindID");
  end;

  if((ValType(source.OfficeId) != V_UNDEF) AND (source.OfficeId > 0))
    return source;
  else
    return null;
  end;
end;

macro CB_GetPtOfficesRecWithChildren
(
  PartyId : integer,
  OfficeId : integer
)
  var ptOfficeWithChildren = TWsPtOffice();
  
  var cmd : RsdCommand, dataSet; 
  var getChildrenSqlText = "SELECT offisu.*, office.* " +
                          " FROM dptoffisu_dbt offisu, dptoffice_dbt office " +
                          " WHERE offisu.t_partyid = ? " +
                          " AND office.t_partyid = offisu.t_partyid " +
                          " AND offisu.t_superiorid = office.t_officeid " +
                          " AND offisu.t_superiorid = ?";

  ptOfficeWithChildren = CB_GetPtOfficesRec(PartyId, OfficeId);

  if((ValType(ptOfficeWithChildren) != V_UNDEF) AND (ptOfficeWithChildren.OfficeId > 0))
    cmd.cmdText = getChildrenSqlText;
    cmd.NullConversion = true;

    cmd.AddParam("", RSDBP_IN, partyId);
    cmd.AddParam("", RSDBP_IN, officeId);

    cmd.execute();

    dataSet = RsdRecordset(cmd);

    ptOfficeWithChildren.Children = TArray();

    while(dataSet.moveNext())
      var childrenObject = TWsPtOffice();

      childrenObject.PartyId      = dataSet.value("t_PartyID");
      childrenObject.OfficeId     = dataSet.value("t_OfficeID");
      childrenObject.PartyName    = GetPartyName(childrenObject.PartyId);
      childrenObject.Code         = dataSet.value("t_OfficeCode"); 
      childrenObject.Name         = dataSet.value("t_officename");
      childrenObject.Description  = dataSet.value("t_Description");
      childrenObject.KPP          = dataSet.value("t_KPP");
      childrenObject.RegNumber    = dataSet.value("t_RegNumber");
      childrenObject.RegDate      = dataSet.value("t_RegDate");
      childrenObject.RegDoc       = GetRegDoc(dataSet.value("t_DocKindID"));
      childrenObject.Series       = dataSet.value("t_Series");
      childrenObject.Number       = dataSet.value("t_Number");
      childrenObject.DateClose    = dataSet.value("t_DateClose");
      childrenObject.DocKindId    = dataSet.value("t_DocKindID");

      ptOfficeWithChildren.Children[ptOfficeWithChildren.Children.size] = childrenObject;  
    end;

    return ptOfficeWithChildren;
  else
    return NULL;
  end;

end;

macro CB_PtOfficeReplace
(
  Office,     //: TWsPtOffice
  Superior,   //: TWsPtOffice
  OrgDate,    //: Date
  UserChoice  //: Integer
)                      
  var stat : bool;

  var PartyId : integer;
  var OfficeId : integer;
  var SuperiorId : integer;
  var Position : integer;
  var ErrMsg : string;

  PartyId = Office.partyId;
  OfficeId = Office.OfficeId;
  SuperiorId = Superior.OfficeId;

  if  (UserChoice == 0)
    Position = OFFICE_RECORD_INSERT_BEFORE;
  elif(UserChoice == 1)
    Position = OFFICE_RECORD_INSERT_AFTER;
  else
    Position = OFFICE_RECORD_INSERT_CHILD;
  end;

  ErrMsg = "";

  stat = PTOfficeReplace(PartyId, OfficeId, SuperiorId, OrgDate, Position, ErrMsg);

  if(not stat)
    RunError(ErrMsg);
  end;
  
  return 0;
end;

/* Создание объекта TWsPtOffice через параметры */
macro CreatePtOfficeFromParam
(
  PartyId, 
  OfficeId, 
  SuperiorID, 
  PartyName, 
  Code, 
  Name, 
  Description, 
  KPP, 
  RegNumber, 
  RegDate, 
  RegDoc, 
  Series, 
  Number, 
  DateClose, 
  DocKindId, 
  Children, 
  MoveOfficeMode
)
  var ptOfficeItem : TWsPtOffice;

  ptOfficeItem.PartyId        = PartyId;
  ptOfficeItem.OfficeId       = OfficeId;
  ptOfficeItem.SuperiorID     = SuperiorID;
  ptOfficeItem.PartyName      = PartyName;
  ptOfficeItem.Code           = Code;
  ptOfficeItem.Name           = Name;
  ptOfficeItem.Description    = Description;
  ptOfficeItem.KPP            = KPP;
  ptOfficeItem.RegNumber      = RegNumber;
  ptOfficeItem.RegDate        = RegDate;
  ptOfficeItem.RegDoc         = RegDoc;
  ptOfficeItem.Series         = Series;
  ptOfficeItem.Number         = Number;
  ptOfficeItem.DateClose      = DateClose;
  ptOfficeItem.DocKindId      = DocKindId;
  ptOfficeItem.Children       = Children;
  ptOfficeItem.MoveOfficeMode = MoveOfficeMode;

  return ptOfficeItem;
end;

/* Создание объекта TWsPtOffice с подкачкой Name_Type из базы */
macro CreatePtOfficeFromAppServ(PartyId, OfficeId)
  return CB_GetPtOfficesRec(PartyId, OfficeId);
end;                                   