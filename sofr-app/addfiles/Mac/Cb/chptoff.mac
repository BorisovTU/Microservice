/*==============================================================================*\
|            Copyright (c) R-Style Software Lab Ltd. 2000                        |
|                                                                                |
|  System            : RS-Bank5.10                                               |
|  Description       :                                                           |
|  Subsystem         : Макросы                                                   |
|  File name         : ChPtOff.mac                          Date: 19.04.2001     |
|  Description       : Проверка на использование подразделения, если подразделен.|                    |
|                      используется в связанных БД, то возвращается TRUE,        |
|                      иначе FALSE                                               |
|  Header/Source name:                                                           |
|  Remarks           :                                                           |
|                                                                                |
|  Programmer        : Сельченко Игорь Анатольевич                               |
|================================================================================|
|                               History                                          |
|  Date      Description                                   Programmer            |
|                                                                                |
\*------------------------------------------------------------------------------*/
import globals;

var FlagProgress:bool = TRUE;/* Глобальный флажок для показа индикатора           */
var Comment:string    = "";  /* Глобальная переменная с описаниме файла в котором */
                             /* найден кодификатор                                */

/* Метод проверки подразделения в табеле */
macro FiltrTabel(FileID:tbfile, ID:integer ) : bool

      var stat:bool = TRUE;
      FileID.KeyNum = 6;

      /* Проверяем в суммарных данных */
      FileID.Rec.TypeRec  = 0;
      FileID.Rec.PartyID  = {OurBank};
      FileID.Rec.OfficeID = ID;
      FileID.Rec.Tnumb    = 0;
      FileID.Rec.Date     = Date(0, 0, 0);
      FileID.Rec.KindTime = 0;
      stat = FileID.GetGE();
      if( (stat) AND (FileID.Rec.OfficeID == ID) )  stat = TRUE ;
      else                                          stat = FALSE;
      end;
      if( NOT stat )
          /* Проверяем в ежеденвных данных */
          FileID.Rec.TypeRec  = 1;
          FileID.Rec.PartyID  = {OurBank};
          FileID.Rec.OfficeID = ID;
          FileID.Rec.Tnumb    = 0;
          FileID.Rec.Date     = Date(0, 0, 0);
          FileID.Rec.KindTime = 0;
          stat = FileID.GetGE();
          if( (stat) AND (FileID.Rec.OfficeID == ID) ) stat = TRUE;
          else                                         stat = FALSE;
          end;
      end;
      return stat;
end;

/* Метод проверки подразделения в Трудовой истории сотрудника */
macro FiltrHistory(FileID:tbfile, ID:integer ) : bool

      var stat:bool    = FALSE;
      var i   :integer = 1;
      FileID.KeyNum = 2;

      /* Цикл по событиям */
      while( (i <= 3) AND (NOT stat) )
          FileID.Rec.event    = i;
          FileID.Rec.PartyID  = {OurBank};
          FileID.Rec.OfficeID = ID;
          FileID.Rec.Date     = Date(0, 0, 0);
          stat = FileID.GetGE();
          if( (stat) AND (FileID.Rec.OfficeID == ID) )  stat = TRUE ;
          else                                          stat = FALSE;
          end;
          i = i + 1;
      end;
      return stat;
end;

macro NextMonth(OperDate:date) : date
   var Month:integer = 0,
       Year :integer = 0;
       DateSplit(OperDate, NULL,  Month,  Year);
       if( Month == 12 )
           return Date(1,       1, Year + 1);
       else
           return Date(1, Month+1, Year    );
       end;
end;

/* Метод проверки подразделения в фалах с ключом */
/* DateC, PartyID, OfficeID                      */
/* Ключ должен быть задан                        */
macro FiltrCommon(FileID:tbfile, ID:integer) : bool
      var stat    :bool = TRUE;
      var OperDate:date = Date(0, 0, 0);

      while( stat )
         FileID.Clear();
         FileID.Rec.DateC    = OperDate;
         FileID.Rec.PartyID  = {OurBank};
         FileID.Rec.OfficeID = ID;
         stat = FileID.GetGE();

         if( (stat) AND (FileID.Rec.OfficeID == ID      ) AND
                        (FileID.Rec.PartyID  == {OurBank})    )
              return stat;
         else
              if( OperDate == Date(0, 0, 0) )
                  OperDate = FileID.Rec.DateC;
              else
                  OperDate = NextMonth(OperDate);
              end;
         end;
      end;
      return stat;
end;

/* Метод проверки подразделения в данных по ЕСН сотрудников */
macro FiltrEsn_Paye(FileID:tbfile, ID:integer ) : bool

      FileID.KeyNum = 1;
      return FiltrCommon( FileID, ID )
end;

/* Метод проверки подразделения в начисл./удерж, методика расчета */
macro FiltrMethod(FileID:tbfile, ID:integer ) : bool

      FileID.KeyNum = 8;
      return FiltrCommon( FileID, ID )
end;

/* Метод проверки подразделения в результаты расчетов */
macro FiltrReznach(FileID:tbfile, ID:integer ) : bool

      FileID.KeyNum = 7;
      return FiltrCommon( FileID, ID )
end;

/* Метод проверки подразделения в лицевых карточек */
macro FiltrLshtflw(FileID:tbfile, ID:integer ) : bool
      FileID.KeyNum = 2;
      return FiltrCommon( FileID, ID )
end;

/* Метод проверки подразделения в Страховые взносы в ПФ */
macro FiltrPayPF(FileID:tbfile, ID:integer ) : bool
      FileID.KeyNum = 2;
      return FiltrCommon( FileID, ID )
end;

/* Метод проверки подразделения в списке сотрудников */
macro FiltrLsheet(FileID:tbfile, ID:integer ) : bool

      var stat    :bool = FALSE;
      var OperDate:date = Date(0, 0, 0);

      FileID.KeyNum = 4;

      FileID.Rec.OfficeID = ID;
      FileID.Rec.PartyID  = {OurBank};
      FileID.Rec.Tnumb    = 0;
      stat = FileID.GetGE();

      if( (stat) AND (FileID.Rec.OfficeID == ID) )  stat = TRUE ;
      else                                          stat = FALSE;
      end;
      return stat;
end;

/*______________ Структура файла с реквизитами файла для поиска ______________*/
CLASS CCheckFile( _FileName:string, _FieldName :string,
                  _Comment :string, _MacroFiltr:procref )

   var FileName   :string  = _FileName;     /* Имя файла                      */
   var FieldName  :string  = _FieldName;    /* Имя поля                       */
   var FileComment:string  = "";            /* Комментарий файла              */
   var MacroFiltr :procref = _MacroFiltr;   /* Указатель на функцию занимающуюся    */
                                            /* перебором записей в файле, если не   */
                                            /* задана, то используется стандартиная */

   if( ValType(_Comment) ) this.FileComment = _Comment; end;

END;


/* Структура содержит имя поле и его значение,  применяется для фильтрации    */
/* записей в проверяемой базе данных                                          */
CLASS CCheckField(_Field:string, _Value:variant )

   var Field:string  = _Field;
   var Value:variant = _Value;

END;

/*____________ Список файлов, в котором ведется поиск кодификатора ___________*/
CLASS CListFile()

   var List:TArray = TArray;

   macro add(FileName:string, FieldName:string, FileComment:string, MacroFiltr:procref)
         this.List[this.List.Size()] = CCheckFile(FileName, FieldName, FileComment, MacroFiltr);
   end;

END;


/*__________ Метод сравнения значения поля и значения кодификатора ___________*/
macro CompareFields( FileID:TBfile, Field:string, Code:variant, ID:integer ) : bool

      var FieldValue:variant = GenGetProp(FileID.Rec, Field);

      if( ValType(FieldValue) == V_INTEGER)
          if( FieldValue == ID )
              return TRUE;
          end;
      else
          if( FieldValue == Code )
              return TRUE;
          end;
      end;

      return FALSE;

end;

/*_____ Метод проверяет наличие значения Value в поле Field файла FileID _____*/
macro CheckUseValueInField(FileID:TBfile, Field:string, Value:variant,
                           ID:integer, CheckField:CCheckField, MacroFiltr:procref ) : bool

      var FlagCheckField:bool = FALSE;

      /* Задан метод перебора файла */
      if( ValType(MacroFiltr) != V_UNDEF )
          return ExecMacro2(@MacroFiltr, FileID, ID);
      end;

      if( ValType(CheckField) != V_UNDEF )
          FlagCheckField = TRUE;
      end;

      FileID.ReWind();
      while( FileID.Next() )

          if( FlagCheckField )
              if( GenGetProp(FileID.Rec, CheckField.Field) == CheckField.Value )
                  if( CompareFields(FileID, Field, Value, ID) ) return TRUE; end;
              end;
          else
              if( CompareFields(FileID, Field, Value, ID) ) return TRUE; end;
          end;

      end;
      return FALSE;

end;


/* Метод проверяет наличие кодификатора описанного в списке CListFile */
/* со значением value в списке файлов CListFile                       */
macro CheckKodif(List:CListFile, value:variant, ID:integer) : bool

   var i          :integer     = 0,
       Size       :integer     = List.List.Size();
   var Def        :string      = "zp.def";
   var CheckField :CCheckField = NULL;

   GetParm(3, CheckField);

   if(FlagProgress)
      InitProgress(Size, "Проверка использования кодификатора...",  "Проверка использования кодификатора...");
   end;
   while( i < Size )
       if(FlagProgress)
          UseProgress(i+1);
       end;
       if( CheckUseValueInField(TBFile(List.List[i].FileName  , "r", 0, List.List[i].FileName, Def),
                                List.List[i].FieldName,     /* Имя поля                 */
                                Value,                      /* Код кодификатора         */
                                ID,                         /* ID кодификатора          */
                                CheckField,                 /* Структура для фильтрации */
                                List.List[i].MacroFiltr ) ) /* Метод перебора файла     */
           if(FlagProgress)
              RemProgress();
           end;
           Comment = "\"" + List.List[i].FileName + ".dbt - " + List.List[i].FileComment + "\"";
           return TRUE;
       end;
       i = i + 1;
   end;
   if(FlagProgress)
      RemProgress();
   end;
   return FALSE;

end;


/*--------------------------------------------------------------------------*\
|*    Метод проверяет испрользование подразделение с идентификатором ID     *|
|*    в других базах данных                                                 *|
\*--------------------------------------------------------------------------*/
macro CheckUsePtOffice( OfficeID:integer ) : bool

   /* Список файлов, в которых используется Подразделение */
   var ls:CListFile = CListFile;
       ls.add("lsheet"  , "OfficeID", "Личные карточки"            , @FiltrLsheet  ); /* Список сотрудников                */
       ls.add("lshtflw" , "OfficeID", "Лицевые карточки"           , @FiltrLshtflw ); /* данные по сотруднику              */
       ls.add("esn_paye", "OfficeID", "Данные по ЕСН сотрудников"  , @FiltrEsn_Paye); /* Данные по ЕСН сотрудников         */
       ls.add("history" , "OfficeID", "Трудовая история сотрудника", @FiltrHistory ); /* Трудовая история сотрудника       */
       ls.add("tabel"   , "OfficeID", "Табельный учет"             , @FiltrTabel   ); /* табельный учет                    */
       ls.add("method"  , "OfficeID", "Методика расчета"           , @FiltrMethod  ); /* начисл./удерж, методика расчета   */
       ls.add("reznach" , "OfficeID", "Результаты расчетов"        , @FiltrReznach ); /* результаты расчетов               */
       ls.add("pay_pf"  , "OfficeID", "Страховые взносы в ПФ"      , @FiltrPayPF   ); /* Страховые взносы в ПФ             */

       ls.add("hs_lsht" , "OfficeID", "История изменения ЛК"             ); /* история изменения личной карточки */
       ls.add("duty"    , "OfficeID", "Список нарядов"                   ); /* Список нарядов                    */
       ls.add("facture" , "OfficeID", "Выработка по наряду"              ); /* Выработка по наряду               */
       ls.add("patrmeth", "OfficeID", "Методика расчета"                 ); /* начисл./удерж, методика расчета   */
       ls.add("paydep"  , "OfficeID", "Записи о выплате депонента"       ); /* записи о выплате депонента        */
       ls.add("paylist" , "OfficeID", "Ведомость на выплату"             ); /* ведомость на выплату              */
       ls.add("voluat"  , "OfficeID", "Справочник расценок"              ); /* справочник расценок               */
       ls.add("wrk_book", "Office"  , "Трудовые книжки"                  ); /* трудовые книжки                   */
       ls.add("shtat_ps", "OfficeID", "Список штатных позиций"           ); /* Список штатных позиций            */
       ls.add("hist_ps" , "OfficeID", "История изменения штатных позиций"); /* История изменения штатных позиций */

       return CheckKodif(ls, OfficeID, OfficeID);
end;


macro Check_PtOffice( Подразделение )

   if( CheckUsePtOffice(Подразделение.OfficeID) )
       MsgBox( "На подразделение \"" , Подразделение.OfficeCode, ":",
                                       Подразделение.OfficeName,
               "\" есть ссылки в \n"+ Comment +"!",
               "\nУдалять используемое подразделение нельзя!");
       return 1;
   end;

   return 0;
end;

/**/
