
import "globals.mac", adress, sfgetcat, RSD, SfInter, BilFacturaInter;

const SF_FEE_TYPE_COMMON = 0; /*сводный СФ*/

private const SFTYPE_DISTRIBUTED = 1;
private const SFTYPE_OBTAINED = 2;

macro SF_ToFullDocID( ID:string )
  
  const DocIdSize = 34;  
  var DocID = mkstr( "0", DocIdSize - strlen(ID) ) + ID;

  return DocID;
end;

private macro FillPartyInfo( PartyID, PartyName:@string, PartyINN:@string, bBothName )
  
  record party(party);
  if( ПолучитьСубъекта( PartyID, party ) == 0 )
    PartyName = party.Name;
    if( bBothName == true )
      PartyName = PartyName + " (" + party.ShortName + ")";
    end;
    PartyINN = GetPartyINN( PartyID, 1 );
  end;

end;

private macro SetPartyAddress( PartyID, Adress:@string )
  
  record RecordAdress ( adress  );  
  ClearRecord( RecordAdress );

  Adress = "";

  var bFounded = НайтиАдресСубъекта( PartyID, PTADDR_LEGAL, RecordAdress );
  if( bFounded )
    Adress = RecordAdress.Adress;
  end;

  if( (bFounded == false) OR (Adress == "") )
    if( НайтиАдресСубъекта(PartyID, PTADDR_REAL, RecordAdress) )
    Adress = RecordAdress.Adress;
  end;
  end;

end;

private macro GetFacturaDirection( feeType, commNumber )
  
  var Direction = 0;
  file sfcomiss( "sfcomiss.dbt" ) key 0;
  
  sfcomiss.feeType = feeType;
  sfcomiss.number = commNumber;
  
  if( getEQ(sfcomiss) )
    if(sfcomiss.ReceiverID == {OurBank})
      Direction   = SFTYPE_DISTRIBUTED;
    else
      Direction   = SFTYPE_OBTAINED;
    end;
  end;

  return Direction;
end;

macro SF_FillBilFactura( feeType, objBuff, BillDate, Dprt, DprtNode, PayerID, payFIID, facturaBuff, IsFromMacro )

  var stat = 0;

  const BILTYPE_BILFAC = 1;

  /**/
  var FacturaNumber = "";

  record sfinv( "sfinv.dbt" );
  record sfdef( "sfdef.dbt" );

  record factura("bilfactura.dbt");
  ClearRecord(factura);  

  if( IsFromMacro == NULL )
    setBuff( factura, facturaBuff );
  end;  

  if( feeType == SF_FEE_TYPE_INVOICE )
    
    if( objBuff == NULL )
      stat = 1;
    else
      SetBuff( sfinv, objBuff );

      if( sfinv.Direction == SFINV_COMTYPE_COMING )
        factura.Direction = SFTYPE_DISTRIBUTED;
      elif( sfinv.Direction == SFINV_COMTYPE_OUTLAY )
        factura.Direction = SFTYPE_OBTAINED;
      end;

      FacturaNumber = sfinv.DocNumber;
    end;

  elif( (feeType == SF_FEE_TYPE_PERIOD) OR (feeType == SF_FEE_TYPE_SINGLE) OR (feeType == SF_FEE_TYPE_ONCE) )
    
    if( objBuff == NULL )
      stat = 1;
    else
      SetBuff( sfdef, objBuff );      
      factura.Direction = GetFacturaDirection( feeType, sfdef.commNumber );
      FacturaNumber = string( sfdef.ID );
    end;

  elif( feeType == SF_FEE_TYPE_COMMON ) /*сводный СФ*/
    factura.Direction = SFTYPE_DISTRIBUTED;
  end;

  if( stat == 0 )
    if( factura.Direction == 0 )
      stat = 1;
    end;
  end;
  
  if( stat == 0 )
    factura.FacturaID  = 0;
    factura.SfTypeID   = BILTYPE_BILFAC;
    factura.Department = Dprt;
    if( DprtNode > 0 )
      factura.Branch = DprtNode;
    else
      factura.Branch = Dprt;
    end;

    factura.BankDate = BillDate;
    factura.SysDate  = date();
    factura.SysTime  = time();
    factura.Oper     = {oper};
    
    factura.CreationDate  = BillDate;

    if(factura.Direction != SFTYPE_DISTRIBUTED)
      factura.AcquisitionDate = BillDate;
    end;

    factura.RegDate       = BillDate;
    factura.FIID          = PayFIID;
    factura.Status        = 1;    
    factura.Assignment    = 1;

    if( factura.Direction == SFTYPE_DISTRIBUTED )
      factura.SupplierID = {HeadBankID};
      factura.ReceiverID = PayerID;
    else
      factura.SupplierID = PayerID;
      factura.ReceiverID = {HeadBankID};
      
      factura.FacturaNumber = FacturaNumber;

      factura.IsHanded = "X";  
      factura.AcquisitionDate = factura.CreationDate;
    end;

    FillPartyInfo( factura.SupplierID, @factura.SupplierName, @factura.SupplierINN, true );
    SetPartyAddress( factura.SupplierID, @factura.SupplierAddress );

    FillPartyInfo( factura.ReceiverID, @factura.ReceiverName, @factura.ReceiverINN, false );
    SetPartyAddress( factura.ReceiverID, @factura.ReceiverAddress );
  end;

  if( stat == 0 )
    if( (IsFromMacro != NULL) AND (IsFromMacro == true) )
      copy( facturaBuff, factura );  
    end;
  end;

  return stat;
end;

macro SF_CreateBilBookEntryForSfInv( facturaID, invoiceID, isIncluded, contractID, pmLinks )
  var sqlString, rs, cmd;
  var MinusNDS_Account = "";
  file SfContr( "sfcontr.dbt") key 0;

  var retVal = 0, bilbeID = 0, RegDate = {curdate};
  var bilfDocArray = TArray;

  if( pmLinks > 0 )
    /*ТО:  1. связь с платежами*/
    sqlString = " SELECT pm.t_DocKind docKind, to_char(pm.t_PaymentID) pmID, pm.t_BaseAmount pmAmount, pm.t_BaseFIID pmFIID " +
                " FROM dpmpaym_dbt pm WHERE pm.t_PaymStatus >= ? AND pm.t_feeType = ? AND pm.t_DefComID = ? " +
                " AND EXISTS (SELECT * FROM dpmdocs_dbt pmd WHERE pmd.t_PaymentID = pm.t_PaymentID AND pmd.t_Reason = 1)";
                
    cmd = RSDCommand( sqlString );
    cmd.addParam( "", RSDBP_IN, PM_FINISHED );
    cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_INVOICE );
    cmd.addParam( "", RSDBP_IN, invoiceID );

    rs = RsdRecordset( cmd );
    while( rs.moveNext() )      
      bilfDocArray[0] = TRecHandler("bilf_doc.rec");
      bilfDocArray[0].Clear();

      bilfDocArray[0].rec.DocKind = rs.value("docKind");    
      bilfDocArray[0].rec.DocID  = rs.value("pmID");
      bilfDocArray[0].rec.Amount = rs.value("pmAmount");
      bilfDocArray[0].rec.FIID   = rs.value("pmFIID");

      Opr_GetLastExecStep( bilfDocArray[0].rec.DocKind, SF_ToFullDocID(bilfDocArray[0].rec.DocID), 
                           bilfDocArray[0].rec.OperationId, bilfDocArray[0].rec.StepId );

      /*
      if( PM_GetPaymentExecutionDate(Int(bilfDocArray[0].rec.DocID), RegDate) != 0 )
        RegDate = {curdate};
      end;
      */
      
      if( not BFCreateBilBookEntry(facturaID, bilfDocArray, RegDate, bilbeID) )
        retVal = retVal + 1;      
      end;
    end;
  else     
    if( isIncluded == "X" )
      SfContr.Id = contractID;
      if( getEQ( SfContr ) )      
        MinusNDS_Account = SfGetCatAccount( SfContr, NATCUR, "-НДС", null, true );
      end;
    end;

    sqlString = "SELECT ? DocKind, LPAD(to_char(act.t_AccTrnID), 34, 0) docID, " +
         " act.t_Sum_Payer docAmount, act.t_FIID_Payer docFIID, act.t_Date_Carry CarryDate FROM dacctrn_dbt act " + 
         " WHERE act.t_AccTrnID IN " +
           " (SELECT oprd.t_AccTrnID FROM doprdocs_dbt oprd WHERE oprd.t_DocKind = ? AND t_ID_Operation IN " +
           " (SELECT opr.t_ID_Operation  FROM doproper_dbt opr " +
           " WHERE opr.t_DocKind = 59 AND opr.t_DocumentID = LPAD(to_char(?), 34, '0'))) ";
    if( isIncluded == "X" )                  
      sqlString = sqlString + " AND mc.t_Account_Receiver <> ? ";
    end;

    cmd = RSDCommand( sqlString );    

    /*for select from arhdoc*/
    cmd.addParam( "", RSDBP_IN, DLDOC_CARRY );
    cmd.addParam( "", RSDBP_IN, DLDOC_CARRY );    
    cmd.addParam( "", RSDBP_IN, invoiceID );
    if( isIncluded == "X" )
      cmd.addParam( "", RSDBP_IN, MinusNDS_Account );
    end;

    rs = RsdRecordset( cmd );
    while( rs.moveNext() )
      bilfDocArray[0] = TRecHandler("bilf_doc.rec");
      bilfDocArray[0].Clear();

      bilfDocArray[0].rec.DocKind = rs.value("docKind");    
      bilfDocArray[0].rec.DocID  = rs.value("docID");
      bilfDocArray[0].rec.Amount = rs.value("docAmount");
      bilfDocArray[0].rec.FIID   = rs.value("docFIID");

      RegDate = Date(rs.value("CarryDate"));

      Opr_GetLastExecStep( bilfDocArray[0].rec.DocKind, bilfDocArray[0].rec.DocID, 
                           bilfDocArray[0].rec.OperationId, bilfDocArray[0].rec.StepId );
      
      if( not BFCreateBilBookEntry(facturaID, bilfDocArray, RegDate, bilbeID) )
        retVal = retVal + 1;      
      end;
      
    end;
  end;

  return retVal;
end;

/*разовые и единовременные комиссии*/
macro SF_CreateBilBookEntry( feeType, defcomID, facturaID )

  var bilfDocArray = TArray;
  var retVal = 0, bilbeID = 0;
  var RegDate = {curdate};

  const PM_FINISHED = 32000;
   
  var sqlString, rs, cmd;
  
  sqlString = " SELECT pm.t_DocKind docKind, to_char(pm.t_PaymentID) pmID, pm.t_BaseAmount pmAmount, pm.t_BaseFIID pmFIID " +
              " FROM dpmpaym_dbt pm WHERE pm.t_PaymStatus >= ? AND pm.t_feeType = ? AND pm.t_DefComID = ? "+
              " AND EXISTS (SELECT * FROM dpmdocs_dbt pmd WHERE pmd.t_PaymentID = pm.t_PaymentID AND pmd.t_Reason = 1)";
  
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, PM_FINISHED );
  cmd.addParam( "", RSDBP_IN, feeType );
  cmd.addParam( "", RSDBP_IN, defcomID );  

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    
    bilfDocArray[0] = TRecHandler("bilf_doc.rec");
    bilfDocArray[0].Clear();

    bilfDocArray[0].rec.DocKind = rs.value("docKind");    
    bilfDocArray[0].rec.DocID  = rs.value("pmID");
    bilfDocArray[0].rec.Amount = rs.value("pmAmount");
    bilfDocArray[0].rec.FIID   = rs.value("pmFIID");

    Opr_GetLastExecStep( bilfDocArray[0].rec.DocKind, SF_ToFullDocID(bilfDocArray[0].rec.DocID), 
                           bilfDocArray[0].rec.OperationId, bilfDocArray[0].rec.StepId );
    
    /*!!!РБ должен переделать функцию
    if( PM_GetPaymentExecutionDate(Int(bilfDocArray[0].rec.DocID), RegDate) != 0 )
      RegDate = {curdate};
    end;
    */

    if( not BFCreateBilBookEntry(facturaID, bilfDocArray, RegDate, bilbeID) )
      retVal = retVal + 1;      
    end;

  end;  

  return retVal;
end;
