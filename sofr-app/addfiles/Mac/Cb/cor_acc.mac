import "cat_acc.mac","pm_tools.mac";//, OprInter, PSInter, PaymInter, oralib, likepy;

macro IsLOROAcc(corschem, Account, FIID, Chapter)
  if (Account == "") return false; end;
  var acc: TbFile = TbFile("account.dbt", "r");
  acc.rec.Chapter = Chapter;
  acc.rec.Account = Account;
  acc.rec.Code_currency = FIID;
  if (not acc.GetEQ() or not Index(acc.rec.Type_Account, "К"))
    return false;
  end;
  var params   :TArray;
  var rs       :object;

  var SelectStr = "select cor.t_Number" +
              "  from dcorschem_dbt cor          " +
              "where  cor.t_Account  = :Account  " +  
              "  and  cor.t_FIID     = :FIID     " +
              "  and  cor.t_State    = 0";

  params = makeArray( SQLParam( "Account", Account ),
                      SQLParam( "FIID"   , FIID));

  rs = execSQLselect( SelectStr, params, FALSE );

  if( rs and rs.moveNext() )
    if (not FindCorschem(corschem, rs.value(0), FIID) and corschem.rec.IsNostro == "")
      return true;
    end;
  end;

  return false;
end;


//-----------------------------------------------------------------------------
// Первичный документ для счета КУ "Внебалансовый счет картотеки корсчета" "ВнебалКартотекаКорсчёта"
//-----------------------------------------------------------------------------
CLASS (TCommonFirstDoc) Corr_Acc( Payment )

  var _Payment: RsbPayment;
  var TypeCorr: Integer = -1;
  MACRO InitParmArray
     if (ValType(_Payment) == V_GENOBJ)
       var corschem: TRechandler = TRecHandler( "corschem.dbt" );
       if (_Payment.PaymStatus >= PM_IS_SENDING)
         if (not FindCorschem(corschem, _Payment.GetCredit().rec.corschem, _Payment.GetCredit().rec.PayFIID)
             and (corschem.rec.IsNostro == "X"))
           TypeCorr = 1;
         end;
       else
         if (not FindCorschem(corschem, _Payment.GetDebet().rec.corschem, _Payment.GetDebet().rec.PayFIID)
             and (corschem.rec.IsNostro == ""))
           TypeCorr = 2;
         else
           if (IsLOROAcc(corschem, _Payment.FuturePayerAccount, _Payment.FuturePayerFIID, _Payment.Chapter) or
               IsLOROAcc(corschem, _Payment.FutureReceiverAccount, _Payment.FutureReceiverFIID, _Payment.Chapter))
             TypeCorr = 2;
           end;
         end;
       end;
     
       if (TypeCorr != -1) 
         ParmA[regList.registration(MC_TYPE_PARAMETR_CONTRACTOR) ] = corschem.rec.corrID;
         ParmA[regList.registration(MC_TYPE_PARAMETR_FIID  )     ] = corschem.rec.FIID;
         ParmA[regList.registration(MC_TYPE_PARAMETR_PAYCURRENCY)] = corschem.rec.FIID;
       end;
     end;
     FIRoleBArray[0] = 0;
  END;

//------------------------------------------------------------------------------
// вернуть параметр первого рода
//------------------------------------------------------------------------------
    MACRO GetParametrTemplate ( ObjectID, Classificator, OperDate, FIRole )
       if ((ObjectID == 2091/*OBJTYPE_TYPECORACC*/) and (Classificator == 1803))
         return TypeCorr;
       end;
       return -1;
    END;

  if (ValType(Payment) == V_INTEGER)
    _Payment = RsbPayment(Payment);
  elif ((ValType(Payment) == V_GENOBJ) and (IsEqClass("RsbPayment", Payment)))
    _Payment = Payment;
  else
    _Payment = NULL;
  end;
  InitTCommonFirstDoc();
  InitParmArray();
  
END;