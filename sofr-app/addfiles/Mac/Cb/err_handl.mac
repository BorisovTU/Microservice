 /*
 $Name: err_handl.mac
 $Module: Ядро
 $Description: Утилитные функции по обработке ошибок

  # tag
  - code_type: API 
*/

class CustomError(msg)
  var Message = msg;
end;

/**
  @brief - Получить строку полной ошибки. (Получение ошибок из вложенных объектов ошибок)
  @param[in] ObjError - объект ошибки
  @param[in] _textLimit - ограничение по выводимому тексту ошибки
  @return string текст ошибки
*/
macro GetFullErrorMessage(ObjError:object, _textLimit:integer)
  var textLimit = 0;
  if ((ValType(_textLimit) == V_INTEGER) and (_textLimit > 0))
    textLimit = _textLimit
  end;

  var errCode, errMes, count, i;

  if (IsEqClass ("TRslError",ObjError))
    errMes = ObjError.Message; 
    errCode = ObjError.Code; 
  else
    errMes = ObjError.getMessage(); 
    errCode = ObjError.getErrorCode(); 
  end;

  var Cause = ObjError.err;

  if (IsEqClass ("TRsComErr", Cause))
    count = Cause.Count;
    i = 0;
    while (i < count)
      errMes = errMes + ";"+ Cause.Message (i) + " (Code = " + Cause.Code (i) +
                                                ", Level = " + Cause.Level (i) + ")" ;
      i = i + 1
    end;
  end;

  if(IsEqClass("TDbError", Cause))
    errMes = errMes + ";" + Cause.Stat + "-" + Cause.Oper + "-" + Cause.Table + "-" + Cause.Message;
  end;

  if(isEqClass("TRsdError", Cause))
    i = 0;
    while(i < Cause.environment.ErrorCount)
      errMes = errMes + ";" + Cause.environment.Error(i).Descr;
      i = i + 1;
    end;
  end;

  if(isEqClass("CustomError", Cause))
    errMes = errMes + ";" + Cause.Message;
  end;

  if(isEqClass("c_usr_err_obj", Cause))
    errMes = errMes + ";" + Cause.v_err_msg;
  end;

  if(errCode != 17)
    var retStr = String("Code: " +string( errCode )+
                        "; Message: "+string( errMes )+
                        "; Module: "+string( ObjError.Module )+
                        "; Line: "+string( ObjError.Line )+
                        "; AxCode: "+string( ObjError.AxCode )+
                        "; Err: "+string( Cause )+
                        "; AxMes: "+string( ObjError.AxMes ));
    if (textLimit > 0)
      return SubStr(retStr,1,textLimit);
    else
      return retStr;
    end;
   else 
     return String("Прерывание пользователя!");
   end;
end;