/*
$Name:        ws_login.mac
$Module:      Web-общее
$Description: Макрос с реализацией сервисов входа в систему
*/


import BankInter, rsbdataset;
import "globals.mac";
import "ws_usermenu.mac";
import "ws_accnumber_mask.mac";

private var {Version};

class LoginResponse

  var LoginInfoStr;
  var OperData;
  var VersionInfo;

end;


class VersionInfo

  var ProgramVersion : string;   /* Версия программы */
  var DBVersion : string;   /* Версия БД */

end;

Macro CheckLogin
(
  Oper:integer,           // Номер пользователя
  Password:string       // Пароль
):string

  var RetStr = "Номер пользователя и пароль верен";

  var stat = CheckOperAndPswd( Oper, Password );
  var ErrMsg = GetErrMsg();

  if ( stat == 0 )
    return RetStr;
  else
    if ( ErrMsg )
      RunError( ErrMsg, stat );
    else
      RunError( "Номер пользователя или пароль неверен", stat );
    end;
  end;

end;

class ErrorInfo

  var Code;
  var Str = "";
  
  macro GetStr();
  
    var query = "SELECT T_CONTENTS FROM DBANK_MSG WHERE T_NUMBER = " + string(Code);

      var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

      if (ds.MoveNext())
        Str = ds.Contents;
      end;  
      
  end;

  macro SaveErrorInfo()
    
    if( (Code != null) and (Code != 0) )

      Str = GetErrMsg();
      if( Str == "" )
        GetStr();
      end

    end;

  end;
  
end;

macro GetBranchName(branch :integer, branchName: string, Name: string)
    var cmd, cmdText;

    cmdText = "SELECT dp.t_name branchname, p.t_name name FROM ddp_dep_dbt dp JOIN dparty_dbt p ON dp.t_partyid = p.t_partyid where dp.t_Code = ?";
    cmd = RsdCommand(cmdText);
    cmd.AddParam("", RSDBP_IN, branch);
    cmd.execute();

    var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (not ds.MoveNext())
        RunError("Подразделение с ID = " + branch + " не найдено");
    end;  

    SetParm(1, ds.BranchName);
    SetParm(2, ds.Name);
end;

class OperData

  var kbdlockmethod;
  var kbdlocktime;

  var BPromUse;
  var BranchCurDate;
  var Name_Oper;
  var OurBank;
  var curdate;
  var Name_Boss;
  var FIO_Boss;
  var Name_Book;
  var FIO_Book;
  var NumDprt;
  var MFO_Bank;
  var Name_Bank;
  var Post_Addr;
  var Real_Addr;
  var Legal_Addr;
  var CORAC_Bank;
  var INN_Bank;
  var MFO_RCC;
  var OperDprt;
  var OperDprtName;
  var OperDprtBranchName;
  var OperDprtNode;
  var OperDprtNodeName;
  var OperDprtNodeBranchName;
  var cTypePerson;
  var cRealTypePerson;
  var GroupOperL;
  var GroupOperF;
  var ISONatCur;
  var CCYNatCur;
  var ResidentCountryCode;
  var ResidentCountryCodeNum;
  var HeadBankID;
  var DprtCurDate;
  var OperMenu;
  var MaxRowsOnPageInDataGrid;
  var RowsOnPageInDataGrid;
  var IsRestoreLastTabEnabled;
  var IsSpellCheckingEnabled;
  var IsCriticalActionEnabled;
  var TermPath;
  var TermPathSettingName;
  var AccNumberTemplateList;
  var AccNumberTemplateDefault;
  
  macro GetRegValue(regpath, type)

    var result;
    var error = 0;

    GetRegistryValue( regpath, type, result, error, null, {oper} );

    return result;
  
  end;

end;


/*
 *  Сервис разблокировки экрана
 */
macro Unblock(InStr : string)
  
  return WebUnblock(InStr);

end;

/*
 *  Сервис выхода из системы
 */
macro Logout( SessionID : string )
  
  WebLogout( SessionID );

end;

/*
 *  Сервис информирования об активности Web-сессии
 */
macro Ping(InStr : string)
  
  return WebPing(InStr);

end;

macro GetOperData()

  var p = OperData();

  p.BPromUse = {BPromUse};
  p.BranchCurDate = {BranchCurDate};
  p.Name_Oper = {Name_Oper};
  p.OurBank = {OurBank};
  p.curdate = {curdate};
  p.Name_Boss = {Name_Boss};
  p.FIO_Boss = {FIO_Boss};
  p.Name_Book = {Name_Book};
  p.FIO_Book = {FIO_Book};
  p.NumDprt = {NumDprt};
  p.MFO_Bank = {MFO_Bank};
  p.Name_Bank= {Name_Bank};
  p.Post_Addr= {Post_Addr};
  p.Real_Addr= {Real_Addr};
  p.Legal_Addr= {Legal_Addr};
  p.CORAC_Bank= {CORAC_Bank};
  p.INN_Bank= {INN_Bank};
  p.MFO_RCC= {MFO_RCC};
  p.OperDprt= {OperDprt};
  GetBranchName({OperDprt}, p.OperDprtBranchName, p.OperDprtName);
  p.OperDprtNode= {OperDprtNode};
  GetBranchName({OperDprtNode}, p.OperDprtNodeBranchName, p.OperDprtNodeName);
  p.cTypePerson= {cTypePerson};
  p.cRealTypePerson= {cRealTypePerson};
  p.GroupOperL= {GroupOperL};
  p.GroupOperF= {GroupOperF};
  p.ISONatCur= {ISONatCur};
  p.CCYNatCur= {CCYNatCur};
  p.ResidentCountryCode= {ResidentCountryCode};
  p.ResidentCountryCodeNum= {ResidentCountryCodeNum};
  p.HeadBankID= {HeadBankID};
  p.DprtCurDate= {DprtCurDate};

  p.kbdlockmethod   = p.GetRegValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\KBDLOCKMETHOD", V_INTEGER);
  p.kbdlocktime     = p.GetRegValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\KBDLOCKTIME"  , V_INTEGER);
  
  p.RowsOnPageInDataGrid     = p.GetRegValue("WEBCLIENT\\СКРОЛЛИНГ\\СТРОК НА СТРАНИЦЕ"      , V_INTEGER);
  p.MaxRowsOnPageInDataGrid  = p.GetRegValue("WEBCLIENT\\СКРОЛЛИНГ\\СТРОК НА СТРАНИЦЕ МАКС" , V_INTEGER);
  p.IsRestoreLastTabEnabled  = p.GetRegValue("WEBCLIENT\\ПАНЕЛЬ\\СОХРАНЯТЬ ВКЛАДКУ"         , V_BOOL);

  p.IsSpellCheckingEnabled   = p.GetRegValue("WEBCLIENT\\ОРФОГРАФИЯ\\ВКЛЮЧИТЬ ПРОВЕРКУ"     , V_BOOL);
  p.IsCriticalActionEnabled  = p.GetRegValue("COMMON\\КРИТИЧНЫЕ ОПЕРАЦИИ\\ВКЛЮЧЕН_WEB"      , V_BOOL);

  p.TermPathSettingName = "WEBCLIENT\\EASYWIN_EXECUTION\\TERMINAL_PATH";
  p.TermPath = p.GetRegValue(p.TermPathSettingName, V_STRING);

  p.AccNumberTemplateList = TArray();

  GetAccountNumberMaskTemplate( @p.AccNumberTemplateDefault, p.AccNumberTemplateList );

  p.OperMenu = GetProgramsWithMenu();
  return p;

end;

macro GetVersionInfo()
    var info = VersionInfo();
    
    GetProgramInformation( info.ProgramVersion, info.DBVersion );
    
    return info;
end;

// 1, 2

macro CheckOperAndPwd(Oper, Pwd)

  var p = ErrorInfo();

  p.Code = CheckOperAndPswd(Oper, Pwd);
  
  if(p.Code > 0)
  
    p.GetStr();
  
  end;
  
  return p;

end;

/*
 *  Сервис установки нового пароля пользователя
 */
macro SetNewOperPwd(InStr : string)

  return WebSetNewOperPassword(InStr);

end;

// 4

macro CheckChangePwd(Oper)

  var p = ErrorInfo();

  p.Code = CheckChangePassword(Oper);
  
  if(p.Code > 0)
  
    p.SaveErrorInfo();
  
  end;
  
  return p;

end;

/*
 *  Сервис проверки пароля пользователя
 */
macro CheckPwd(InStr : string)

  return WebCheckPassword(InStr);

end;

// 6

macro OpWriteAuditLog(Oper)

  var p = ErrorInfo();

  p.Code = OperWriteAuditLog(Oper);
  
  if(p.Code > 0)
  
    p.GetStr();
  
  end;  
  
  return p;

end;
/*
 *  Сервис входа в систему
 */
macro Login(InStr : string)
  var response = LoginResponse();

  response.LoginInfoStr = WebLogin(InStr);
  response.OperData = GetOperData();
  response.VersionInfo = GetVersionInfo();

  return response;

end;
