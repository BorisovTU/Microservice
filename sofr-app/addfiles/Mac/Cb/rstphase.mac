import BankInter, globals;
import oralib, likepy;

record rstphase(rstphase, "bank.lbr") dialog;

private macro DeleteAccountMaskPhases(AccountMask)
  var query = "";
  var rs;
  var params : TArray;
  var SqlAccountMask : string;

  SqlAccountMask = ConvertMaskToSQLFormat(AccountMask, "t1.t_Account");
  
  if( SqlAccountMask == "" )
    SqlAccountMask = "1 = 1";
  end;

  query = query + "DELETE FROM dopdphase_dbt t                                                   ";
  query = query + "      WHERE (t.t_Account, t.t_Chapter, t.t_Currency, t.t_PhaseDate) IN        ";
  query = query + "            (SELECT t1.t_Account, t1.t_Chapter, t1.t_Currency, t1.t_PhaseDate ";
  query = query + "               FROM dopdphase_dbt t1, daccount_dbt t2                         ";
  query = query + "              WHERE     t1.t_Account = t2.t_Account                           ";
  query = query + "                    AND (" + SqlAccountMask + ")                              ";
  query = query + "                    AND t1.t_Chapter = t2.t_Chapter                           ";
  query = query + "                    AND t1.t_Currency = t2.t_Code_Currency                    ";
  query = query + "                    AND t1.t_PhaseDate <= t2.t_Open_Date)                     ";

  execSQL(query, params, true);

  println("Удалены некорректные фазы л/счетов по маске номера счета: ", AccountMask, ".");
end;

private macro RestoreAccountMaskPhases(AccountMask)
  var query = "";
  var rs;
  var params : TArray;
  var SqlAccountMask : string;

  SqlAccountMask = ConvertMaskToSQLFormat(AccountMask, "t.t_Account");
  
  if( SqlAccountMask == "" )
    SqlAccountMask = "1 = 1";
  end;

  query = query + "SELECT t.t_Account, t.t_Chapter, t.t_Code_Currency ";
  query = query + "  FROM daccount_dbt t                              ";
  query = query + " WHERE     (" + SqlAccountMask + ")                ";

  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    if(InsertAccountPhase(rs.value(0), rs.value(1), rs.value(2)) == 0)
      println("Для л/счета ", rs.value(0), " добавлена фаза на день открытия.");
    else
      println("Для л/счета ", rs.value(0), " не добавлена фаза на день открытия. Причина: ", GetErrMsg(), ".");
    end;
  end;                         
end;

private macro ProcessAccountMask(AccountMask)
  DeleteAccountMaskPhases(AccountMask);

  RestoreAccountMaskPhases(AccountMask);
end;

macro EventPan(dlg, cmd, id, key)
  var itemsel;

  if(cmd == DLG_INIT)
    UpdateFields(dlg);

  elif((CMD == DLG_KEY) and (key == 316/*K_F2*/))
    ProcessAccountMask(dlg.AccountMask);
    return CM_CANCEL;     

  else
    return CM_DEFAULT;
  end;
end;

RunDialog(rstphase, "EventPan");

//exit (1);
