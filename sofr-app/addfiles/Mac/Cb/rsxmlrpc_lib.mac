import treport, globals, rsexts;

File logFile() txt;

MACRO writeToLog(Event,
   logFName,
   RecordNumber,
   ReqId,
   LogId,
   Result,
   SysDate,
   SysTime,
   LogFolder,
   workFile,
   SignMsg)


   var day;
   var month;
   var year;
   var hh;
   var mm;
   var ss;
   var LogBody;
   var nmLen;

   var headPrinted = false;

   /* Если не задано имя обрабатываемого файла - ничего не делаем и выходим */
   if ((ValType(workFile) == V_UNDEF) OR (workFile == null) OR (workFile == ""))
      return 0;
   end;

   Var Rep = CTableReport(0, false, false);
   Rep.AddColumn("Номер строки", 15);
   Rep.AddColumn("Идентификатор запроса ReqId", 50);
   Rep.AddColumn("Результат", 70);


   /* Если передали пустое имя файла лога - формируем и возвращаем */
   if ((ValType(logFName) == V_UNDEF) OR (logFName == null) OR (logFName == ""))

      DateSplit(SysDate, day, month, year);
      TimeSplit(SysTime, hh, mm, ss);

      nmLen = strlen(workFile) - 4;
      logFname = substr(workFile, 1, nmLen);
      logFname = logFname + "_" + year + month + day + "_" + hh + "_" + mm + "_" + ss + ".log";

      logFName = LogFolder + "\\" + logFName;

      /*SetParm(3, V_STRING, logFName);*/
   end;

   /* Меняем файл вывода */
   var oldFile = SetOutput(logFName, true);

   if (Event == 2)/*$(Начало обработки файла)*/
      Rep.PrintFreeString("ПРОТОКОЛ ЗАГРУЗКИ XML-ЗАПРОСОВ ИЗ ФАЙЛА " + workFile);
      Rep.PrintFreeString("ПРОЦЕДУРА ЗАПУЩЕНА:");
      Rep.PrintFreeString("Системная дата:" + SysDate);
      Rep.PrintFreeString("Системное время:" + SysTime);
      Rep.PrintFreeString("Операционист:" + {oper});
[               ];
   elif(Event == 4)/*$(проверка ЭЦП не требуется)*/
      Rep.PrintFreeString("Проверка подписи не требуется");

   elif(Event == 5)/*$(ЭЦП файла верна)*/
      Rep.PrintFreeString("Проверка подписи выполнена успешно, подпись: '" + SignMsg + "'");

   elif(Event == 6)/*ошибка проверки ЭЦП*/
      Rep.PrintFreeString("При проверке ЭЦП произошла ошибка: '" + SignMsg + "'");

   elif(Event == 7)/*ошибка проверки ЭЦП*/
      Rep.PrintFreeString("Проверка подписи выполнена успешно, но ид. подписи " + SignMsg + " не соответствует настройкам");

   elif(Event == 8)/*ошибка проверки ЭЦП*/
      Rep.PrintFreeString("Ошибка проверки ЭЦП :" + SignMsg);

   elif(Event == 9)/*Ошибка формата файла*/
      Rep.PrintFreeString("Ошибочный формат файла");

   elif(Event == 10)/* Начало печати xml запросов */
[               ];
      Rep.PrintHead("ВЫПОЛНЕНИЕ XML-ЗАПРОСОВ");
   elif(Event == 11)
      /* Сформируем тело журнального сообщения */
      if (Result == 0)
         LogBody = "Обработан";
      elif ((Result == 1) or (Result == 2))
         LogBody = "Обработан повторно";
      elif (Result == 3)
         LogBody = "Ошибка. Номер в журнале " + LogId;
      elif (Result == 4)
         LogBody = "Ошибка повторно. Номер в журнале " + LogId;
      end;

      /* Пишем сообщение */
      Rep.PrintStringTransferByWord(RecordNumber, ReqId, LogBody);
      Rep.PrintSeparator();
   elif(Event == 12)/* Конец печати xml запросов */
   elif(Event == 13)/* Ошибка вызова XML RPC */
      Rep.PrintStringTransferByWord(RecordNumber, ReqId, SignMsg);
      Rep.PrintSeparator();
   elif(Event == 14)/* Фатальная ошибка ! */
      Rep.PrintFreeString("Ошибка: " + SignMsg);

   elif(Event == 0)/* Начало сеанса */
[               ];
   elif(Event == 1)/* Конец сеанса */
[               ];
   end;
   
   /* восстанавливаем файл вывода на стандартный */
   SetOutput (NULL,TRUE);

   return logFName;
   
END;

MACRO postProcessFile(fName, isError, inFolder, outFolder, errFolder)
   var res = true;
   /* Ошибочные файлы сваливаем в errFolder, успешные в outFolder */
   if (isError)
      res = CopyFile(inFolder + "\\" + fName,  errFolder + "\\" + fName);
   else
      res = CopyFile(inFolder + "\\" + fName,  outFolder + "\\" + fName);
   end;

   if (res) 
      res = RemoveFile(inFolder + "\\" + fName);
   end;

   return res;
END;

PRIVATE MACRO printFile(fullName)
   if (open(logFile, fullName))
      /* Просто выводим в стандартный вывод очередной файл лога */
      while (next (logFile))
         println (logFile.str)
      end;
      close(logFile);
   end;
END;

MACRO showGeneralReport(logFolder, logs)
var fullPath;

   var str;
   /* читаем все файлы из каталога логов logFolder*/
   var i = 0;
   while (i < logs.Size)
      fullPath = logs[i];
      printFile(fullPath);
      i = i + 1;
   end;
END;