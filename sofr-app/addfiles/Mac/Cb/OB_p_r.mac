/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*            Copyright (c) R-Style Software Lab 1999                   */
/*                                                                      */
/*  Имя файла   : OB_p_r.mac                                            */
/*                                                                      */
/*  Описание    : Приход\ расход по Внебалансу                          */
/*                (макрос ввода пользовательской операции)              */
/*                                                                      */
/*  Создан      : 05.04.1999                                            */
/*                                                                      */
/************************************************************************/
IMPORT "ob_lib.mac", Календарь;

RECORD FI (fininstr);   /* для "листалки" финансовых инструментов */
RECORD AC (account);    /* для "листалки" лицевых счетов */

RECORD OBPANEL (OB_p_r, "userop.lbr") dialog;
FILE  uo_obcur_FILE ("uo_obcur", "USEROP.DEF") write;  /* Пользовательский первичный документ (польз. внебаланс) */
file Опер( person );

CONST
        ПризнакПРИХОД = "ПРИХОД",
        ПризнакРАСХОД = "расход";

VAR
        КодВалюты       = 0,
        НомерДокумента  = "",
        ДатаПроводки    = {curdate},
        ПриходРасход    = ПризнакПРИХОД,
        Операция        = 0,
        Сумма           = $0.00,
        ЛСчет           = "",
        Основание       = "",
        Пачка           = 0,


        KeyEsc          = 0;

/************************************************************************/
/*  Панель ввода первичного пользовательского документа                 */
/************************************************************************/
MACRO ВнебалансПанель (pn, CMD, ID, KEY)
        VAR
                CurrentField;

        /* функция обновляет поля валюты */
        MACRO ОбновитьПоляВалюты (КодВалюты)
                if (OB_ПолучитьФИПоКодуID (КодВалюты))
                        pn.Code_Currency      = OB_ФИ.FI_Code; /* польз. код валюты (не ID) */
                        pn.Name_Code_Currency = OB_ФИ.Name;
                else
                        pn.Code_Currency      = "";
                        pn.Name_Code_Currency = "Не найдена";
                end;        
        END;
                
        /* функция проверяет правильность введенной валюты */
        MACRO ПроверитьВалюту (ПользКодВалюты)
                if (OB_ПолучитьФИПоПользКоду (ПользКодВалюты))
                        КодВалюты = OB_ФИ.FIID;
                else
                        КодВалюты = -1;
                end;
        END;
                

        /* начало обработки */
        CurrentField = FldName (OBPANEL, ID);
        if (CMD == DLG_INIT)
                ОбновитьПоляВалюты (КодВалюты);
                pn.Numb_Document        = НомерДокумента;
                pn.Chapter              = Глава;
                pn.Date_Carry           = ДатаПроводки;  
                pn.Приход_Расход        = ПриходРасход;  
                pn.Sum                  = Сумма;         
                pn.Account              = ЛСчет;           
                pn.Ground               = Основание;     
                pn.Number_Pack          = Пачка;         
                UpdateFields (OBPANEL);
                return CM_IGNORE;

        elif (CMD == DLG_REMFOCUS)
                if (CurrentField == "Code_Currency")
                        ПроверитьВалюту (pn.Code_Currency);
                        pn.Name_Code_Currency = "Не найдена";
                        UpdateFields (OBPANEL);
                        if (OB_ПолучитьФИПоКодуID (КодВалюты))
                                pn.Code_Currency      = OB_ФИ.FI_Code; /* польз. код валюты (не ID) */
                                pn.Name_Code_Currency = OB_ФИ.Name; 
                        else
                                if(KeyEsc != 1)
                                        msgbox("Неверный код валюты");
                                        return CM_CANCEL;
                                end;
                        end;    
                     /*   ОбновитьПоляВалюты (КодВалюты);       */
                end;
                UpdateFields (OBPANEL);
                return CM_DEFAULT;

        elif (CMD == DLG_SETFOCUS)
                UpdateFields (OBPANEL);
                return CM_DEFAULT;

        elif (CMD == DLG_KEY)
                if ( (KEY == 32) and (CurrentField == "Приход_Расход") )
                        if   (pn.Приход_Расход == ПризнакПРИХОД) 
                                pn.Приход_Расход = ПризнакРАСХОД; 
                        elif (pn.Приход_Расход == ПризнакРАСХОД)
                                pn.Приход_Расход = ПризнакПРИХОД; 
                        else
                                pn.Приход_Расход = ПризнакПРИХОД; 
                        end;
                        UpdateFields (OBPANEL);
                end;
                if (KEY == 27)
                        KeyEsc = 1; 
                        return CM_CANCEL; 
                end;

                if (KEY == 323)  /* Клавиша F9 */
                        return CM_IGNORE; 
                end;

                if ((KEY == 13) and (ID == FldIndex(OBPANEL,"Number_Pack"))) 
                        SetFocus(OBPANEL, FldIndex(OBPANEL,"Code_Currency"));
                        return CM_IGNORE; 
                end;

                if (KEY == 317)  /* Клавиша F3 */
                        /* вызов "листалки" валют */
                        if (ID == FldIndex (OBPANEL,"Code_Currency") )
                                if (ListFI (FIKIND_CURRENCY, 0, FI))
                                        КодВалюты = FI.FIID;
                                        pn.Code_Currency      = FI.FI_Code; /* польз. код валюты (не ID) */
                                        pn.Name_Code_Currency = FI.Name;
                                end;
                        end;

                        /* вызов "листалки" счетов */
                        if (ID == FldIndex(OBPANEL,"Account") )
                                if (ListAccount (AC, Глава, КодВалюты, pn.Account))
                                        pn.Account = AC.Account;
                                else
                                end;
                        end;

                        /* вызов календаря */
                        if (ID == FldIndex(OBPANEL,"Date_Carry") )
                                pn.Date_Carry = GetDateByCalendar( pn.Date_Carry );
                        end;
                        
                        UpdateFields (OBPANEL);
                        return CM_IGNORE;
                end;

                if (KEY == 316)  /* Клавиша F2 */
                        /* сохраняем введенные данные */
                        ПроверитьВалюту (pn.Code_Currency);
                        НомерДокумента   = pn.Numb_Document;
                        ДатаПроводки     = pn.Date_Carry   ;  
                        ПриходРасход     = pn.Приход_Расход;  
                        Сумма            = pn.Sum          ;      
                        ЛСчет            = pn.Account      ;        
                        Основание        = pn.Ground       ;     
                        Пачка            = pn.Number_Pack  ;      

                        /* проверяем введенные данные */
                        if (КодВалюты == -1)
                                MsgBox("Неверный код валюты");
                                SetFocus(OBPANEL, FldIndex(OBPANEL,"Code_Currency"));
                                Return CM_IGNORE; 
                        end;

                        if (Сумма <= $0.00)
                                MsgBox("Неверная сумма");
                                SetFocus(OBPANEL, FldIndex(OBPANEL,"Sum"));
                                Return CM_IGNORE;
                        end;

                        if (not OB_ЛСчетСуществует (Глава, КодВалюты, ЛСчет))
                                MsgBox("Счет не найден");
                                SetFocus(OBPANEL, FldIndex(OBPANEL,"Account"));
                                Return CM_IGNORE;
                        end;

                        if (pn.Приход_Расход == ПризнакПРИХОД) 
                            if (  (index (OB_accR.Type_Account, "У" ) > 0))   
                                MsgBox("Запрещено кредитование счета");
                                SetFocus(OBPANEL, FldIndex(OBPANEL,"Account"));
                                Return CM_IGNORE;
                            end;
                        end;

                        if (pn.Приход_Расход == ПризнакРАСХОД) 
                            if (  (index (OB_accR.Type_Account, "Т" ) > 0))   
                                MsgBox("Запрещено дебетование счета");
                                SetFocus(OBPANEL, FldIndex(OBPANEL,"Account"));
                                Return CM_IGNORE;
                            end;
                        end;


                        return CM_SAVE;
                end;
        else
                return CM_IGNORE;
        end; /* if*/
END;


/************************************************************************/
/* Начало операции                                                      */
/************************************************************************/
Опер.Oper = {oper};
getEQ( Опер );
if ( Опер.CTypePerson == "У")
 MSGBOX("Вам запрещено вызывать эту функцию");
 exit(1);
end;

while (true)
    /* инициализируем поля документа в цикле*/
    КодВалюты       = КодВалюты;
    if (  ( strlen(НомерДокумента) < 10 ) or ( ( strlen(НомерДокумента) == 10 ) and ( НомерДокумента < "2147483647" ) ) )
       НомерДокумента  = String(int(НомерДокумента) + 1);
    else
       НомерДокумента  = "1"
    end;
    ДатаПроводки    = {curdate};
    ПриходРасход    = ПриходРасход;
    Операция        = 0;
    Сумма           = $0.00;
    ЛСчет           = ЛСчет;
    Основание       = "";
    Пачка           = Пачка;

    /* запускаем панель ввода первичного документа */
    if (RunDialog (OBPANEL,"ВнебалансПанель"))
        /* формируем первичный документ */
        ClearRecord (uo_obcur_FILE);
        uo_obcur_FILE.Chapter                = Глава         ;
        uo_obcur_FILE.Date_Carry             = ДатаПроводки  ;
        uo_obcur_FILE.Oper                   = {oper}        ;
        uo_obcur_FILE.Sum                    = Сумма         ;
        uo_obcur_FILE.Number_Pack            = Пачка         ;
        uo_obcur_FILE.Code_Currency          = КодВалюты     ;
        uo_obcur_FILE.Numb_Document          = НомерДокумента;
        uo_obcur_FILE.Ground                 = Основание     ;

        /* сохраняем счет в обоих полях */
        uo_obcur_FILE.Account_Receiver = ЛСчет;
        uo_obcur_FILE.Account_Payer    = ЛСчет;

        if    (ПриходРасход == ПризнакПРИХОД)
                Операция = ОперацияПрихВнебаланс;
        elif  (ПриходРасход == ПризнакРАСХОД)
                Операция = ОперацияРасхВнебаланс;
        else
                Операция = 0;
        end;                
        uo_obcur_FILE.Kind_Operation = Операция;

        /* Запускаем операцию на выполнение */
        RunOperation (ПервДокВнебаланс, uo_obcur_FILE, TRUE, Операция);
    else
        Exit (1);
    end;
end;
END;
