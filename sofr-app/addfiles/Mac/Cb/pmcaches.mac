// Кеши для расчётного банка

import unicache, PTInter;

// Кеш ID субъектов по коду

class (TUniCache) TPartyIdsByCode

  InitTUniCache( V_STRING );

  private class (TUniCacheKey) TPartyIdsByCodeKey ( p_CodeKind:integer, p_Code:string )
    var m_CodeKind:integer = p_CodeKind,
        m_Code    :string  = p_Code;
    macro AsSimpleValue():variant
      return string( m_CodeKind:o:6, m_Code );
    end;
  end;

  macro Key( p_CodeKind:integer, p_Code:string ):TPartyIdsByCodeKey
    return TPartyIdsByCodeKey( p_CodeKind, p_Code );
  end;

  private macro Find( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool
    var tmpErrCode:integer;
    p_ErrText = "";
    p_Value = GetCodeParty( p_Key.m_Code, p_Key.m_CodeKind, tmpErrCode );
    p_ErrCode = tmpErrCode;
    return ( p_ErrCode == 0 );
  end;

end;

// Кеш кодов субъектов по ID

class (TUniCache) TPartyCodesByID

  InitTUniCache( V_STRING );

  private class (TUniCacheKey) TPartyCodesByIDKey ( p_CodeKind:integer, p_PartyID:integer )
    var m_CodeKind:integer = p_CodeKind,
        m_PartyID :integer = p_PartyID;
    macro AsSimpleValue():variant
      return string( m_CodeKind:o:6, m_PartyID:o:10 );
    end;
  end;

  macro Key( p_CodeKind:integer, p_PartyID:integer ):TPartyCodesByIDKey
    return TPartyCodesByIDKey( p_CodeKind, p_PartyID );
  end;

  private macro Find( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool
    var tmpErrCode:integer;
    p_ErrText = "";
    p_Value = GetCodeParty( p_Key.m_PartyID, p_Key.m_CodeKind, tmpErrCode );
    p_ErrCode = tmpErrCode;
    return ( p_ErrCode == 0 );
  end;

end;

/* Пример использования

var PtIdCache  :TPartyIdsByCode = TPartyIdsByCode();
var PtCodeCache:TPartyCodesByID = TPartyCodesByID();

var stat:bool;
var ID:integer;
var Code:string;

stat = PtIdCache.Get( PtIdCache.Key( 3, "047888670" ), @ID );
println( "stat = ", stat, " ID = ", ID );

var errCode:integer, errText:string;
stat = PtCodeCache.Get( PtCodeCache.Key( 3, 2386233 ), @Code, @errCode, @errText );
println( "stat = ", stat, " Code = ", Code, " errCode = ", errCode, " errText = ", errText );

*/
