/*
 $Name: ws_usermenu.mac
 $Module: ws_usermenu.mac
 $Description: ws_usermenu.mac
 */
 /*
        ws_usermenu.mac 
 */
import BankInter, cb_sql, rsd, oralib, likepy, globals, commonutil;

class TWsSubsystem
  var Id              : integer;  // Цифровой номер подсистемы
  var Symbol          : string ;  // Символ подсистемы
  var Name            : string ;  // Наименование подсистемы
  var HelpSubSystem   : string ;  // Адрес страницы помощи по подсистеме

  ID            = -1;
  Symbol        = "";
  Name          = "";
  HelpSubSystem = "";
end;

class TWsMenuItem
  var iIdentProgram   : integer; // Цифровой номер подсистемы
  var iNumberPoint    : integer; // Уникальный номер узла меню в рамках iIdentProgram
  var iNumberParent   : integer; // Номер вышестоящего пункта меню
  var iNumberLine     : integer; // Номер пункта в подменю
  var iCaseItem       : integer; // Системный номер выбора
  var cSystemItem     : string ; // Признак системного модуля
  var szNameItem      : string ; // Наименование пункта меню
  var szNamePrompt    : string ; // Подсказка к названию выбора (выводится в Тултип)
  var iHelp           : integer; // Номер страницы помощи
  var iProgItem       : integer; // Идентификатор приложения модуля
  var Parm            : string ; // Параметры вызова пункта меню. Лежат в ROW, передаем как строку.

  iIdentProgram   = 0;
  iNumberPoint    = 0;
  iNumberParent   = 0;
  iNumberLine     = 0;
  iCaseItem       = 0;
  cSystemItem     = "";
  szNameItem      = "";
  szNamePrompt    = "";
  iHelp           = 0;
  iProgItem       = 0;
  Parm            = "";
end;

class TWsProgramsWithMenu
  var Subsystem : TArray;
  var MenuItem  : TArray;
  var DefaultSubsystem : integer = 0; // Подсистема по-умолчанию
end;

private macro _GetListMenuItem(Subsystems : @TArray)
  var query;
  var rs;
  var params : TArray;
  var MenuItem : TWsMenuItem;
  var ListMenuItem = TArray;
  var NextIndex;

  CaptureOutput;
[
SELECT 
  mi.t_iIdentProgram AS t_iIdentProgram
 ,mi.t_iNumberPoint AS t_iNumberPoint
 ,mi.t_iNumberFather AS t_iNumberParent
 ,mi.t_iNumberLine AS t_iNumberLine
 ,mi.t_iCaseItem AS t_iCaseItem
 ,mi.t_cSystemItem AS t_cSystemItem
 ,mi.t_szNameItem AS t_szNameItem
 ,mi.t_szNamePrompt AS t_szNamePrompt
 ,mi.t_iHelp AS t_iHelp
 ,mi.t_iProgItem AS t_iProgItem
 ,rsb_struct.getString(TO_BLOB(NVL(mp.t_Parm, UTL_RAW.cast_to_raw(LPAD(CHR(0), 300, CHR(0)))))) AS t_Parm
  FROM dmenuitem_dbt mi, dmenuparm_dbt mp, dperson_dbt pn
 WHERE     pn.t_Oper = mi.t_objectid
       AND mp.t_ObjectID(+) = mi.t_ObjectID
       AND mp.t_IsTemplate(+) = mi.t_IsTemplate
       AND mp.t_iNumberPoint(+) = mi.t_iNumberPoint
       AND rsb_locale.oemcode_to_char(mi.t_iIdentProgram) = mp.t_cIdentProgram(+)
       AND mi.t_ObjectID = :Oper
       AND mi.t_IsTemplate = CHR(0)
       AND pn.t_szIdentProgram LIKE '%' || rsb_locale.oemcode_to_char(mi.t_iIdentProgram) || '%'
       AND rsb_locale.oemcode_to_char(mi.t_iIdentProgram) IN (
];       
  query = StopCaptureOutput;
  params = makeArray(SQLParam("Oper", {oper}));
  var i = 0;
  while (i < Subsystems.Size())
    if (i != 0)
        query = query + ",";
    end;
    var prmname = "SubSysPrm" + String(i);
    query = query + ":" + prmname;
    params[params.size] = SQLParam(prmname, Subsystems[i].Symbol);
    i = i + 1;
  end;
  query = query + ") ORDER BY mi.t_iIdentProgram,mi.t_iNumberPoint,mi.t_iNumberFather,mi.t_iNumberLine";
  
  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    MenuItem = TWsMenuItem;
    NextIndex = ListMenuItem.Size();

    CU_CopyRecordsetToClassObj(rs, MenuItem);

    ListMenuItem[NextIndex] = MenuItem;
  end;
  return ListMenuItem;
end;

private macro CountARNU()
    var query = "";
    var count : integer = 0;
    var params : TArray;
    var rs;
    CaptureOutput;
    [
     SELECT count(1)
     FROM dperson_dbt pn
     WHERE pn.t_Oper = :Oper
       AND pn.t_szIdentProgram LIKE '%T%'
    ];
    query = StopCaptureOutput;
    params = makeArray(SQLParam("Oper", {oper}));
    
    rs = execSQLselect(query, params, true);
    if (rs and rs.moveNext())
        count = rs.value(0);
    end;
    
    return count;
end;

private macro _GetListSubsystem()
  var query;
  var rs;
  var params : TArray;
  var Subsystem : TWsSubsystem;
  var ListSubsystem = TArray;
  var NextIndex;
  var val;
  var err;
  var hasarnu = false;

  GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\WEBSUBSYSTISOLATE", V_BOOL, val, err, false, {oper} );

  if( err != 0 )
    val = true;
  end;
  
  CaptureOutput;
[
SELECT 
  ta.t_Type_Account AS t_Symbol
 ,ss.t_Name AS t_Name
 ,ss.t_HelpSubSystem AS t_HelpSubSystem
  FROM dtypeac_dbt ta, dsubsystem_dbt ss, dperson_dbt pn
 WHERE     pn.t_Oper = :Oper
       AND ta.t_iNumType = :iNumType
       AND pn.t_szIdentProgram LIKE '%' || ta.t_Type_Account || '%'
       AND ss.t_SubsystemID = ta.t_Type_Account
];
  query = StopCaptureOutput;
  
  if ((val == true) and (CountARNU() == 1))
    query = query + " AND ta.t_Type_Account = 'T'";
  end;
  
  params = makeArray(SQLParam("Oper", {oper})
                    ,SQLParam("iNumType", 22)
                    );
  rs = execSQLselect(query, params, true);
  while(rs and rs.moveNext())
    Subsystem = TWsSubsystem;
    NextIndex = ListSubsystem.Size();

    CU_CopyRecordsetToClassObj(rs, Subsystem);

    Subsystem.ID = CodeFor(Subsystem.Symbol);

    ListSubsystem[NextIndex] = Subsystem;
  end;
  return ListSubsystem;
end;

macro _GetDefaultSubsystem()
  var cmd = RsdCommand("SELECT T_DEFAULTWEBSUBSYSTEM symbol FROM dperson_dbt where t_Oper = ?");
  cmd.AddParam("", RSDBP_IN, {oper});
  cmd.execute();
  
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  if (ds.moveNext())
    return CodeFor(ds.Symbol);
  end;
  return 0;
end;

macro GetProgramsWithMenu()
  var ProgramsWithMenu = TWsProgramsWithMenu;
  
  ProgramsWithMenu.Subsystem = _GetListSubsystem();
  
  if (ProgramsWithMenu.Subsystem.Size() == 0)
    ProgramsWithMenu.MenuItem = TArray;
  else
    ProgramsWithMenu.MenuItem = _GetListMenuItem(ProgramsWithMenu.Subsystem);
  end;
  
  if (ProgramsWithMenu.Subsystem.Size() > 1)
    ProgramsWithMenu.DefaultSubsystem = _GetDefaultSubsystem();
  end;

  return ProgramsWithMenu;
end;

//var xml;
//var stat;
//stat = ConvertToXML(GetProgramsWithMenu(), "TWsProgramsWithMenu", xml);
//println(stat);
//println(xml);
