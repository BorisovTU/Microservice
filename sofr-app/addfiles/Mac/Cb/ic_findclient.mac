/*
$Name: ic_findclient.mac
$Module: Ядро ГКБО 
$Description: Сервис "Идентифицировать клиента"
*/

import "bnk_common.mac", "ws_lib_fun.mac";

// ФИО ФЛ, раздельно
class FIOPerson
    var Surname;    // Фамилия
    var FirstName;
    var Patronymic; // Отчество
end;

// Документ удостоверяющий личность (ДУЛ)
class IDocPerson
    var TypeDoc; // Тип ДУЛ
    var SerDoc;  // Серия
    var NumDoc;  // Номер
end;

// Поисковые реквизиты клиента
class ReqClient                                
    var Type;      // Тип субъекта
    var FullName;  // Наименование/ФИО
    var FIO;       // ФИО ФЛ 
    var INN;       // ИНН
    var KPP;       // КПП
    var OGRN;      // ОГРН/ОРГНИП
    var BirthDate; // Дата рождения ФЛ
    var Snils;     // СНИЛС
    var IDoc;      // Удостоверение личности ФЛ
end;

// Входящие параметры
class ClientRequest
    var  ReqID ;        // Уникальный идентификатор запроса 
    var  ReqFindClient; // Поисковые реквизиты клиента
    var  Senders;       // Список кодов Абонентов, для которых надо произвести идентификацию
end;

// Исходящие параметры
class ClientResponse
    var  ReqID;                 // Уникальный идентификатор запроса
    var  SenderID    : string;  // ID абонента (АС)
    var  ResultCode  : string;  // Код результата идентификации
    var  ResultText  : string;  // Пояснительный текст (Не заполняем)
    var  CountResult : integer; // Количество найденных клиентов
    var  Clients;    // Найденные клиенты
end;

// Клиент
class Client
    var ClientID       : string;  // Глобальный идентификатор
    var SenderID       : string;  // Идентификатор Абонента 
    var ClientFullName : string;  // Наименование
    var LegalForm      : integer; // Форма клиента
    var IdentityText   : string;  // Дополнительная информация (Не заполняем)
    var Vip            : string;  // Категория клиента
    var IsCertain      : Bool;    // Признак однозначной идентификации
    var Twins;                    // Список клиентов-дублей
end;

class TwinClient
    var ClientID       : string;  // ID клиента в АС Абонента
    var SenderID       : string;  // Идентификатор Абонента 
    var ClientFullName : string;  // Наименование
    var LegalForm      : integer; // Форма клиента
    var IdentityText   : string;  // Дополнительная информация (Не заполняем)
    var IsCertain      : Bool;    // Признак однозначной идентификации
end; 

/////////////////////////////////////////////////////////////////

private class Pair(_type, _code)
    var type:integer = _type;
    var code:string = _code;
end;

private macro PersonFullName(rs:RsdRecordset)
    var FullName:string = "";
  
    if(rs.value("Surname") != "")
        FullName = rs.value("Surname");
    end;
  
    if(rs.value("FirstName") != "")
        if(FullName != "")
            FullName = FullName + " ";
        end;
    
        FullName = FullName + rs.value("FirstName");
    end;
  
    if(rs.value("Patronymic") != "")
        if(FullName != "")
            FullName = FullName + " ";
        end;
    
        FullName = FullName + rs.value("Patronymic");
    end;
  
    return FullName;
end;

private macro AddClientToResponse(response:@ClientResponse, vClient:@Client)
    if (ValType(vClient) != V_UNDEF)
        if (ValType(response.Clients) == V_UNDEF)
            response.Clients = TArray;
        end;
        response.Clients[response.Clients.size] = vClient;
    end;
end; 

private macro CheckNames(checkName, searchParameters, rs : @RsdRecordSet)
    var checked = false;
    if (checkName)
        if(searchParameters.ReqFindClient.Type != "1")
            if((searchParameters.ReqFindClient.FIO.SurName == rs.value("Surname")) and (searchParameters.ReqFindClient.FIO.FirstName == rs.value("FirstName")) and (searchParameters.ReqFindClient.FIO.Patronymic == rs.value("Patronymic")))
                checked = true;
            end;
        end;

        if (not checked)
            var DocumentName:string = "";
            var paramsCheckName:TArray; 

            DocumentName = "";
            if(searchParameters.ReqFindClient.FullName != NULL)
                DocumentName = searchParameters.ReqFindClient.FullName;
            else
                if(searchParameters.ReqFindClient.FIO.SurName != NULL)
                    DocumentName = searchParameters.ReqFindClient.FIO.SurName;
                end;
        
                if(searchParameters.ReqFindClient.FIO.FirstName != NULL)
                    if(DocumentName != "")
                        DocumentName = DocumentName + " ";
                    end;
          
                    DocumentName = DocumentName + searchParameters.ReqFindClient.FIO.FirstName;
                end;
        
                if(searchParameters.ReqFindClient.FIO.Patronymic != NULL)
                    if(DocumentName != "")
                        DocumentName = DocumentName + " ";
                    end;
          
                    DocumentName = DocumentName + searchParameters.ReqFindClient.FIO.Patronymic;
                end;
            end;

            if(DocumentName != "")
                paramsCheckName = TArray();
                paramsCheckName[0] = SQLParam("p_ClientID", rs.value("PartyID"));
                paramsCheckName[1] = SQLParam("p_ClientName", DocumentName);
                paramsCheckName[2] = SQLParam("p_CompareINN", 0);
                paramsCheckName[3] = SQLParam("p_ClientINN", "");
                paramsCheckName[4] = SQLParam("p_NistoryName", 1);
        
                if(not execStoredFunc( "PM_CLNNAMES.CompareNamesByClientID", V_INTEGER, paramsCheckName))
                    checked = true;
                end;
            end; 
        end;
    else
        checked = true;
    end;

    return checked;
end;

private macro CheckIsCertain(searchParameters)
    var IsCertain = false;
    
    if(searchParameters.ReqFindClient.Type != "1")
        if((searchParameters.ReqFindClient.IDoc != NULL) and 
                (searchParameters.ReqFindClient.IDoc.TypeDoc != NULL) and 
                ((searchParameters.ReqFindClient.IDoc.NumDoc != NULL) or 
                (searchParameters.ReqFindClient.IDoc.SerDoc != NULL)) and
                (searchParameters.ReqFindClient.BirthDate != NULL)
            )
            IsCertain = true;
        end;
    else
        if(searchParameters.ReqFindClient.INN != NULL)
            IsCertain = true;
        end;
    end;
    return IsCertain;
end;

private macro GetClientListIK_Body(searchParameters)
    var query:string = "SELECT "
        "party.t_PartyID AS PartyID,  "
        "party.t_LegalForm AS LegalForm,  "
        "party.t_Name AS FullNameInst, "
        "party.t_hasDoubler as hasDoubler,"
        "party.t_mainpartyid, "
        "party.t_isdoubler";
  
    if(searchParameters.ReqFindClient.Type != "1")
        query = query
            + ", persn.t_Name2 AS FirstName, persn.t_Name1 AS SurName, persn.t_Name3 AS Patronymic FROM dparty_dbt party "
            + " INNER JOIN dpersn_dbt persn ON persn.t_PersonID = party.t_PartyID ";
    else
        query = query + " FROM dparty_dbt party ";
    end;
  
    var IsCertain = CheckIsCertain(searchParameters);
    var where:string = "";
    var notNull:string = "";
    var orderBy:string = " order by party.t_hasDoubler DESC, party.t_partyID";
    var queryParams = TArray();
    var whereParams = TArray();
  
    var searchClosed:bool = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\SearchСlosedClients", V_BOOL, false);  
  
    if(not searchClosed)
        where = where + " party.t_Locked = chr(0)";
    end;
  
    if(searchParameters.ReqFindClient.Type == "1")
        if(where != "")
            where = where + " AND";
        end;    
        where = where + " party.t_LegalForm = :LegalForm";
        whereParams[whereParams.Size] = SQLParam("LegalForm", PTLEGF_INST);
    elif(searchParameters.ReqFindClient.Type == "2")
        if(where != "")
            where = where + " AND";
        end;
        where = where + " party.t_LegalForm = :LegalForm";
        whereParams[whereParams.Size] = SQLParam("LegalForm", PTLEGF_PERSN);
    elif(searchParameters.ReqFindClient.Type == "3")
        if(where != "")
            where = where + " AND";
        end;   
    
        where = where + " ( persn.t_IsEmployer = :IsEmployer";
        whereParams[whereParams.Size] = SQLParam("IsEmployer", "X");
    
        var searchLawyersNotaries:bool = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\SEARCH_LAWYERS_NOTARIES", V_BOOL, false);
        if(searchLawyersNotaries)
            where = where + " OR PM_COMMON.CheckObjAttrPresence ( " +
                      "  :OBJTYPE_PARTY, " +
                      "  rcb_objattr.MAKEPARTYID (party.t_PartyID), " +
                      "  :PARTY_ATTR_TYPE, " +
                      "  :PARTY_AT_NOTARY) = 1 " +
                      " OR PM_COMMON.CheckObjAttrPresence ( " +
                      "  :OBJTYPE_PARTYS, " +
                      "  rcb_objattr.MAKEPARTYID (party.t_PartyID), " +
                      "  :PARTY_ATTR_TYPES, " +
                      "  :PARTY_AT_LAWYER) = 1 ";
         
            whereParams[whereParams.Size] = SQLParam("OBJTYPE_PARTY", 3);         
            whereParams[whereParams.Size] = SQLParam("PARTY_ATTR_TYPE", 16);
            whereParams[whereParams.Size] = SQLParam("PARTY_AT_NOTARY", 2);
            whereParams[whereParams.Size] = SQLParam("OBJTYPE_PARTYS", 3);
            whereParams[whereParams.Size] = SQLParam("PARTY_ATTR_TYPES", 16);
            whereParams[whereParams.Size] = SQLParam("PARTY_AT_LAWYER", 3);
        elif( searchParameters.ReqFindClient.Type != "4")
            RunError("Передан Неверный тип клиента " + searchParameters.ReqFindClient.Type );
        end;
    
        where = where + ") ";
    end;
  
    var searchClosedCodes:bool = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\SearchСlosedCodes", V_BOOL, false);
    var codes = TArray();
  
    if(searchParameters.ReqFindClient.INN != NULL)
        codes[codes.Size] = Pair(PTCK_INN, searchParameters.ReqFindClient.INN);
    end;
  
    if(searchParameters.ReqFindClient.OGRN != NULL)
        codes[codes.Size] = Pair(PTCK_OGRN, searchParameters.ReqFindClient.OGRN);
    end;
  
    if(searchParameters.ReqFindClient.Snils != NULL)
        codes[codes.Size] = Pair(PTCK_SNILS, searchParameters.ReqFindClient.Snils);
    end;
  
    var item;
    var index = 0;
    var tableName = IfThenElse( searchClosedCodes, "partcode", "objcode" );
  
    var checkKPP:bool = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\CoincidenceKPP", V_BOOL, false);
  
    for( item, codes )
        query = query 
            + " LEFT OUTER JOIN d" + tableName + "_dbt " + tableName + index + " ON "
            + tableName + index + IfThenElse(tableName == "partcode", ".t_PartyID", ".t_ObjectID" ) + " = party.t_PartyID AND "
            + tableName + index + ".t_CodeKIND = :CodeKIND" + index;
        
        if(item.type == PTCK_INN)
            queryParams[queryParams.Size] = SQLParam("CODEKIND" + index, PTCK_INN);
        elif(item.type == PTCK_OGRN)
            queryParams[queryParams.Size] = SQLParam("CODEKIND" + index, PTCK_OGRN);
        elif(item.type == PTCK_SNILS)
            queryParams[queryParams.Size] = SQLParam("CODEKIND" + index, PTCK_SNILS);
        end;
    
        if(not searchClosedCodes)
            query = query + " AND " + tableName + index + ".t_State = 0 ";
        end;
    
        if(where != "")
            where = where + " AND";
        end;
    
        if(notNull != "")
            notNull = notNull + " OR ";
        end;
    
        where = where + " (( " + tableName + index + ".t_CodeKIND = :PTCK_INN" + index + " AND REGEXP_SUBSTR ( " + tableName + index + ".t_Code, '[^/]+') = :CODE" + index;

        if((checkKPP) and (item.type == PTCK_INN) and (searchParameters.ReqFindClient.KPP != NULL))
            where = where + " AND REGEXP_SUBSTR ( " + tableName + index + ".t_Code, '/(.+)',1,1,NULL,1) = :KPP ";
        end;

        where = where + ") " + " OR ( " + tableName + index + ".t_CodeKIND != :PTCK_INNS" + index + " AND " + tableName + index + ".t_Code = :CODES" + index + ") OR " + tableName + index + ".t_Code IS NULL ) ";
      
        notNull = notNull + tableName + index + ".t_Code IS NOT NULL ";
  
        whereParams[whereParams.Size] = SQLParam("PTCK_INN" + index, PTCK_INN);   
        whereParams[whereParams.Size] = SQLParam("CODE" + index, item.code);
        if((checkKPP) and (item.type == PTCK_INN) and (searchParameters.ReqFindClient.KPP != NULL))
            whereParams[whereParams.Size] = SQLParam("KPP", searchParameters.ReqFindClient.KPP);
        end;
        whereParams[whereParams.Size] = SQLParam("PTCK_INNS" + index, PTCK_INN);
        whereParams[whereParams.Size] = SQLParam("CODES" + index, item.code);
    
        index = index + 1;
    end;
  
    var searchClosedIDs = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\SearchСlosedID", V_BOOL, false);
  
    if((searchParameters.ReqFindClient.IDoc != NULL) and (searchParameters.ReqFindClient.IDoc.TypeDoc != NULL) and ((searchParameters.ReqFindClient.IDoc.NumDoc != NULL) or (searchParameters.ReqFindClient.IDoc.SerDoc != NULL)))
        query = query + " LEFT OUTER JOIN dpersnidc_dbt persnidc ON persnidc.t_PersonID = party.t_PartyID AND persnidc.t_PaperKind = :PaperKind ";
        queryParams[queryParams.Size] = SQLParam("PaperKind", int(searchParameters.ReqFindClient.IDoc.TypeDoc));
      
        if(where != "")
            where = where + " AND";
        end;
      
        if(notNull != "")
            notNull = notNull + " OR ";
        end;
        
        where = where + " (( persnidc.t_PaperNumber = :PaperNumber AND persnidc.t_PaperSeries = :PaperSeries ) OR persnidc.t_PaperNumber IS NULL )";
        notNull = notNull + " persnidc.t_PaperNumber IS NOT NULL ";  
        
        whereParams[whereParams.Size] = SQLParam("PaperNumber", searchParameters.ReqFindClient.IDoc.NumDoc);       
        whereParams[whereParams.Size] = SQLParam("PaperSeries", searchParameters.ReqFindClient.IDoc.SerDoc);
      
        if(searchClosedIDs)
            query = query
                + " LEFT OUTER JOIN "
                + " (select t_PersonID, t_PaperKind, t_paperSeries, t_paperNumber from dpersnidchist_dbt pih "
                + " where pih.t_PaperKind = :PaperKindH group by t_PersonID, t_PaperKind, t_paperSeries, t_paperNumber ) persnidchist ON persnidchist.T_PersonID = party.t_PARTYID "
                + " AND persnidchist.t_PaperKind = :PaperKindHS ";
        
            queryParams[queryParams.Size] = SQLParam("PaperKindH", int(searchParameters.ReqFindClient.IDoc.TypeDoc));
            queryParams[queryParams.Size] = SQLParam("PaperKindHS", int(searchParameters.ReqFindClient.IDoc.TypeDoc));
        
            if(where != "")
                where = where + " AND";
            end;
        
            if(notNull != "")
                notNull = notNull + " OR ";
            end;
          
            where = where + " (( persnidchist.t_PaperNumber = :PaperNumberH AND persnidchist.t_PaperSeries = :PaperSeriesH ) OR persnidchist.t_PaperNumber IS NULL ) ";
            notNull = notNull + " persnidchist.t_PaperNumber IS NOT NULL ";
          
            whereParams[whereParams.Size] = SQLParam("PaperNumberH", searchParameters.ReqFindClient.IDoc.NumDoc);       
            whereParams[whereParams.Size] = SQLParam("PaperSeriesH", searchParameters.ReqFindClient.IDoc.SerDoc);
        end;
    end;
  
    var checkName:bool = Bnk_GetRegistryValue("RS-CONNECT\\Идентификация клиентов\\CoincidenceName", V_BOOL, false);
  
    if(notNull != "")
        notNull = " AND ( " + notNull + " ) ";
    elif(notNull == "" )
        if((searchParameters.ReqFindClient.FullName != "") and ((searchParameters.ReqFindClient.Type == "1") or ( searchParameters.ReqFindClient.Type == "4")))
            if(where != "")
                where = where + " AND ";
            end;
        
            where = where + " party.t_Name = :FullName ";
            whereParams[whereParams.Size] = SQLParam("FullName", searchParameters.ReqFindClient.FullName);
        elif((searchParameters.ReqFindClient.Type != 1) and (searchParameters.ReqFindClient.FIO != NULL) and (searchParameters.ReqFindClient.FIO.FirstName != NULL) 
            or (searchParameters.ReqFindClient.FIO.Surname != NULL) 
            or (searchParameters.ReqFindClient.FIO.Patronymic != NULL))
            
            if(where != "")
                where = where + " AND ";
            end;
        
            where = where + " ( persn.t_Name2 = :FirstName AND persn.t_Name1 = :SurName AND persn.t_Name3 = :Patronymic ) ";
        
            whereParams[whereParams.Size] = SQLParam("FirstName", searchParameters.ReqFindClient.FIO.FirstName);       
            whereParams[whereParams.Size] = SQLParam("SurName", searchParameters.ReqFindClient.FIO.SurName);
            whereParams[whereParams.Size] = SQLParam("Patronymic", searchParameters.ReqFindClient.FIO.Patronymic);
        else // Условия не заданы
            RunError("Недостаточно переданных данных для идентификации клиента");
        end;
    end;
  
    if(where != "")
        where = " WHERE " + where;
    end;
  
    var prm;
    for(prm, whereParams)
        queryParams[queryParams.Size] = prm;
    end;
  
    var connectQuery = "with mainquery as (" + query + where + notNull + orderBy;
    connectQuery = connectQuery + ")"
        "SELECT LEVEL, MAINQUERY.* FROM MAINQUERY "
        "START WITH T_MAINPARTYID = 0 "
        "CONNECT BY PRIOR PARTYID = T_MAINPARTYID";

    var rs = execSQLselect(connectQuery, queryParams);
    var result = 0;  
  
    var DocumentName:string = "";
    var paramsCheckName:TArray; 
  
    var vClient;
    var response:ClientResponse;
    response.ReqID = searchParameters.ReqID;
  
    while(rs.MoveNext())
        var fAddClient = false;
        if (rs.value("level") == 1)
            AddClientToResponse(response, vClient);
            if(rs.value("t_isdoubler") != "X")
                if((checkName) and (notNull != ""))
                    if (CheckNames(checkName, searchParameters, rs))
                        fAddClient = true;
                    end;
                else
                    fAddClient = true;
                end;
            end;

            if(fAddClient)
                vClient = Client();
                vClient.ClientID = rs.value("PartyID");
                vClient.ClientFullName = IfThenElse(rs.value("LegalForm") == PTLEGF_INST, rs.value("FullNameInst"), PersonFullName(rs));
                vClient.LegalForm = rs.value("LegalForm");
                vClient.IsCertain = IsCertain;
                result = result + 1;  
            end;
        else
            if (ValType(vClient) != V_UNDEF)
                if((checkName) and (notNull != ""))
                    if (CheckNames(checkName, searchParameters, rs))
                        fAddClient = true;
                    end;
                else
                    fAddClient = true;
                end;

                if(fAddClient)
                    if (ValType(vClient.Twins) == V_UNDEF)
                        vClient.Twins = TArray;
                    end;
                    var Twin = TwinClient;
                    Twin.ClientID = rs.value("PartyID");
                    Twin.ClientFullName = IfThenElse(rs.value("LegalForm") == PTLEGF_INST, rs.value("FullNameInst"), PersonFullName(rs));
                    Twin.LegalForm = rs.value("LegalForm");
                    Twin.IsCertain = IsCertain;
                    vClient.Twins[vClient.Twins.size] = Twin;
                end;
            end;
        end;
    end;
    AddClientToResponse(response, vClient);
  
    if (result == 1)
        response.ResultCode = 0;
    elif(result == 0)
        response.ResultCode = 4;
    else
        if (result > 1)
            response.ResultCode = 1;
        end;
    end;
    response.ResultText = "";
    response.CountResult = result;
    return response;
end;

private macro FillTWsFIOPerson(ClientFIO : @variant, wsClientFIO, ParamN)
    WS_SetAttributeValue(@ClientFIO.Surname, ParamN, "ReqFindClient.FIO", "Surname", wsClientFIO, V_STRING, true, null);
    WS_SetAttributeValue(@ClientFIO.FirstName, ParamN, "ReqFindClient.FIO", "FirstName", wsClientFIO, V_STRING, true, null);
    WS_SetAttributeValue(@ClientFIO.Patronymic, ParamN, "ReqFindClient.FIO", "Patronymic", wsClientFIO, V_STRING, false, null);
end;

private macro FillTWsIDocPerson(ClientIDOC : @variant, wsClientIDOC, ParamN)
    WS_SetAttributeValue(@ClientIDOC.TypeDoc, ParamN, "ReqFindClient.IDoc", "TypeDoc", wsClientIDOC, V_STRING, true, null);
    WS_SetAttributeValue(@ClientIDOC.SerDoc, ParamN, "ReqFindClient.IDoc", "SerDoc", wsClientIDOC, V_STRING, true, null);
    WS_SetAttributeValue(@ClientIDOC.NumDoc, ParamN, "ReqFindClient.IDoc", "NumDoc", wsClientIDOC, V_STRING, true, null);
end;

private macro FillTReqFindClient(oReqFindClient : @variant, wsReqFindClient, ParamN)
    WS_SetAttributeValue(@oReqFindClient.Type, ParamN, "ReqFindClient", "Type", wsReqFindClient, V_STRING, true, null);
    WS_SetAttributeValue(@oReqFindClient.FullName, ParamN, "ReqFindClient", "FullName", wsReqFindClient, V_STRING, false, "");
    WS_SetAttributeValue(@oReqFindClient.INN, ParamN, "ReqFindClient", "INN", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.KPP, ParamN, "ReqFindClient", "KPP", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.OGRN, ParamN, "ReqFindClient", "OGRN", wsReqFindClient, V_STRING, false, null);
    WS_SetAttributeValue(@oReqFindClient.BirthDate, ParamN, "ReqFindClient", "BirthDate", wsReqFindClient, V_DATE, false, null);
    WS_SetAttributeValue(@oReqFindClient.Snils, ParamN, "ReqFindClient", "Snils", wsReqFindClient, V_STRING, false, null);

    WS_SetAttributeValue(@oReqFindClient.FIO, ParamN, "ReqFindClient", "FIO", wsReqFindClient, V_GENOBJ, true, null);
    if (oReqFindClient.FIO != null)
        oReqFindClient.FIO = FIOPerson;
        FillTWsFIOPerson(@oReqFindClient.FIO, wsReqFindClient.FIO, ParamN);
    end;
    
    WS_SetAttributeValue(@oReqFindClient.IDoc, ParamN, "ReqFindClient", "IDoc", wsReqFindClient, V_GENOBJ, true, null);
    if (oReqFindClient.IDoc != null)
        oReqFindClient.IDoc = IDocPerson;
        FillTWsIDocPerson(@oReqFindClient.IDoc, wsReqFindClient.IDoc, ParamN);
    end;
end;

private macro FillTWsClientIdent(oClientIdent, wsClientIdent : ClientRequest, insMode : bool)
    var ParamN     : integer;
    var ObjectName : string;
    var IsMandatory = insMode;
    ParamN     = 1;
    ObjectName = "";

    WS_SetAttributeValue(@wsClientIdent.ReqID, ParamN, ObjectName, "ReqID", oClientIdent, V_STRING, true, null);
    WS_SetAttributeValue(@wsClientIdent.ReqFindClient, ParamN, ObjectName, "ReqFindClient", oClientIdent, V_GENOBJ, true, null);

    if (wsClientIdent.ReqFindClient != null)
        wsClientIdent.ReqFindClient = ReqClient;
        FillTReqFindClient(@wsClientIdent.ReqFindClient, oClientIdent.ReqFindClient);
    end;
end;

macro GetClientListIK(oClientIdent)
    var response : ClientResponse;
    var wsClientIdent : ClientRequest;
    WS_CheckParameter(1, oClientIdent, true, V_GENOBJ);

    wsClientIdent = ClientRequest;
    FillTWsClientIdent(oClientIdent, wsClientIdent, false);
    response = GetClientListIK_Body(wsClientIdent);
    return response;
end;