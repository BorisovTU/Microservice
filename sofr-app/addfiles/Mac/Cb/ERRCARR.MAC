/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : errcarr.mac                                      07.07.1998
  Description : Отчет по ошибкам в рублевых счетах
  Comment     : Пункт 1801 "Системного сервиса"

                Отчет выводтся на печать на лазерном принтере
                при установке опций печати "Ланшафт + Сжатие по горизонтали"
└───────────────────────────────────────────────────────────────────────────*/
import globals;

 file ec ( errcarry ) key 0;  /* файл ошибок рублевых счетов */
 file oc ( obchaptr ) key 0;  /* файл глав счетов */

/* --------------- Печать заголовка отчета --------------- */
macro Title
[
 #  ----------------------------------------------------------------------------------------------------------------------------------------------  ##########

   ОТЧЕТ ПО ОШИБКАМ В РУБЛЕВЫХ СЧЕТАХ

   Глава счетов: ##################################################

 ┌────┬─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┐
 │Ста-│  Номер лицевого счета   │  Дебет за день  │  Кредит за день │ Остаток за день │   Планируемый   │  Дебет за месяц │ Кредит за месяц │      Остаток    │
 │тус │                         │                 │                 │                 │     остаток     │                 │                 │     за месяц    │
 ├────┼─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
 │  1 │           2             │        3        │        4        │        5        │        6        │        7        │        8        │        9        │
 ├────┼─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┤
]({Name_Bank}:15, date, oc.Name  );
end;

/* ----------------- Печать строки отчета ---------------- */
macro Line
[│  # │#########################│#################│#################│#################│#################│#################│#################│#################│
]( ec.Stat, ec.Account, ec.Debet, ec.Kredit, ec.Rest, ec.PlanRest, ec.DebetM, ec.KreditM, ec.RestM );
end;

/* -------------------- Печать отчета -------------------- */
/*                                                         */
/*     Параметр процедуры chapter - номер главы счетов     */
/*                                                         */
macro Report ( chapter )
  /* Поиск названия главы счетов */
  oc.Chapter = chapter;
  if ( not getEQ( oc ) ) oc.Name = "???"; end;  /* Не нашли главу счетов */
  Title;  /* Печать заголовка */
  /* Цикл вывода всех записей по данной главе счетов */
  while ( next( ec ) )
     if ( chapter == ec.Chapter )
        Line;  /* Печать строки отчета */
     end;
  end;
end;
