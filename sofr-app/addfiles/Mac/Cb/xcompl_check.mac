/*
 $Name: xcompl_check.mac
 $Module: RS-Connect
 $Description: Проверка на принадлежность к ПДЛ
 */
 /* 
	Проверка на принадлежность к ПДЛ
 */
import "cb_sql.mac", "globals.mac", rslx, rcw, rsexts, "ws_xcompl_lib.mac", "common_service_caller.mac";
import BankInter, PTInter, CTInter;

private class (CServiceCallerBase) CSinglePDLQueryCaller(_Param)
   private var m_param = _Param;

   private macro SetRequestParameters(request : @TXRRequest)
      var inParams = TArray();
      //m_param.QueryParam.MessageID = SubStr(request.ReqID, 2, 36);
      inParams[inParams.size] = m_param;
      request.Parms = inParams;
   end;
   
   private macro GetSystemURL() : String
    var m_systemURL;
      GetRegistryValue ("RS-CONNECT\\SSUSAGE_SNILS\\URL", V_STRING, m_systemURL);
    return m_systemURL;
   end;    

   InitCServiceCallerBase(CreateGUID(), GetSystemURL(), "ws_xcompl_check", "XCompliance_SearchOperation", 300000);
end;

private macro CastSingleAnswer(answer : Variant) : TXCompResponseSearchOperation
    var answerCast = TXCompResponseSearchOperation();
    answerCast.AnswPar.SenderID      = answer.AnswPar.SenderID;
    answerCast.AnswPar.MessageID     = answer.AnswPar.MessageID;
    answerCast.AnswPar.SenderPointID = answer.AnswPar.SenderPointID;
    answerCast.SelectResult = TArray;

    for (var info, answer.SelectResult)
        var infoCast        = TXCompOperationInfoShortOut();
        infoCast.CL_ID      = info.CL_ID;
        infoCast.RowID      = info.RowID;
        infoCast.IdentMess  = info.IdentMess;
        infoCast.DateMess   = info.DateMess;
        infoCast.AttrDel    = info.AttrDel;
        
        infoCast.FullName   = info.FullName;
        infoCast.BirthDate  = info.BirthDate;
        infoCast.NameDoc    = info.NameDoc;
        infoCast.SeriesDoc  = info.SeriesDoc;
        infoCast.NumDoc     = info.NumDoc;
        infoCast.Issue      = info.Issue;
        infoCast.CodeCat    = info.CodeCat;
        infoCast.DescrCat   = info.DescrCat;
        infoCast.Job        = info.Job;
        infoCast.Biography  = info.Biography;

        answerCast.SelectResult[answerCast.SelectResult.size] = infoCast;
    end;

    return answerCast;
end;

private macro FillInformationScroll(obj : @variant)
    file pdlservice("pdlservice.tmp") write;
    var i = 0;

    while (i < obj.SelectResult.size)
        ClearRecord (pdlservice);
        var SelectResult = obj.SelectResult[i];
        if(ValType(SelectResult.RowID)      != V_UNDEF) pdlservice.RowID           = SelectResult.RowID;     end;
        if(ValType(SelectResult.IdentMess)  != V_UNDEF) pdlservice.SystemId        = SelectResult.IdentMess; end;
        if(ValType(SelectResult.DateMess)   != V_UNDEF) pdlservice.UpDateAt        = SelectResult.DateMess;  end;
        if(ValType(SelectResult.AttrDel)    != V_UNDEF) pdlservice.IsDeletedPeople = SelectResult.AttrDel;   end;
        if(ValType(SelectResult.FullName)   != V_UNDEF) pdlservice.FullName        = SelectResult.FullName;  end;
        if(ValType(SelectResult.birthdate)  != V_UNDEF) pdlservice.BirthDate       = SelectResult.birthdate; end;
        if(ValType(SelectResult.NameDoc)    != V_UNDEF) pdlservice.PaperName       = SelectResult.NameDoc;   end;
        if(ValType(SelectResult.SeriesDoc)  != V_UNDEF) pdlservice.PaperSeries     = SelectResult.SeriesDoc; end;
        if(ValType(SelectResult.NumDoc)     != V_UNDEF) pdlservice.PaperNumber     = SelectResult.NumDoc;    end;
        if(ValType(SelectResult.Issue)      != V_UNDEF) pdlservice.PaperIssue      = SelectResult.Issue;     end;
        if(ValType(SelectResult.CodeCat)    != V_UNDEF) pdlservice.CategoryCode    = SelectResult.CodeCat;   end;
        if(ValType(SelectResult.DescrCat)   != V_UNDEF) pdlservice.CategoryName    = SelectResult.DescrCat;  end;
        if(ValType(SelectResult.Job)        != V_UNDEF) pdlservice.Post            = SelectResult.Job;       end;
        if(ValType(SelectResult.Biography)  != V_UNDEF) pdlservice.Biography       = SelectResult.Biography; end;

        Insert(pdlservice);
        i = i + 1;
    end;
end;

macro XCompliance_SearchForClient(SenderId, url, CL_ID, FullName, birthdate, DocArray)
    var GUID = CreateGUID();
    var MessageID = substr(GUID, 2, strlen(GUID) - 2);
    RECORD persnidc("persnidc.dbt");

    var mes = TXCompRequestSearchOperation();
    mes.QueryPar = TXCompHeaderInOut();
    mes.QueryPar.SenderID = SenderId;
    mes.QueryPar.MessageID = MessageID;
    mes.QueryPar.SenderPointID = "";
        
    mes.ParSelect = TXCompBodySearchOperationIn();
    mes.ParSelect.CL_ID = CL_ID;
    mes.ParSelect.FullName = FullName;
    mes.ParSelect.birthdate = birthdate;
    mes.ParSelect.ListDoc = TArray;
        
    var i = 0;
    while (i < DocArray.size)
        mes.ParSelect.ListDoc[i] = TXCompListDoc();
        var doc = mes.ParSelect.ListDoc[i];
        SetBuff(persnidc, DocArray[i]);
        doc.SeriesDoc = persnidc.paperseries;
        doc.NumDoc    = persnidc.papernumber;
        i = i + 1;
    end;

    var caller = CSinglePDLQueryCaller(mes);
    var answer;
    var CallService_stat = caller.CallService(@answer);
    var obj = CastSingleAnswer(answer);
    FillInformationScroll(obj);

    return true;
OnError(err)
    if (ValType(err.Err) != V_UNDEF)
        MsgBox (err.Err);
    else
        MsgBox (err.Message);
    end;
    return false;
end;