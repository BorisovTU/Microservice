/*
$Name:           pminret1.mac
$Module:         РКО
$Description:    Макрос шага
*/
/*
   Блок     : 29009 - "Возврат распоряжения"
   Шаг      : 10    - "Отзыв распоряжения"
*/
import pm_common, pm_setst, payinter, "pm_categ.mac", cbsttls, "pm_answerret.mac"; 
var PaymentObj:RsbPayment;

private macro ValueDateForRecallFromInd(Paym : RsbPayment) : date
  var ValueDate : date = {curdate};
  var DocKind = Paym.PrimDocKind;

  if( GetParentOrEqualDocKindFromList(DocKind, PMDOC_CLIENTPAYMENT) )
    ValueDate = PM_GetOperDay_BankServiceBalance(Paym.Department);
  elif( GetParentOrEqualDocKindFromList(DocKind, 
                                        PMDOC_BANKPAYMENT, PMDOC_BANKCLAIM, WL_INDOC) 
      )
    ValueDate = PM_GetOperDay_Balance(Paym.Department);
  end;

  return ValueDate;
end;

MACRO ExecuteStep( doc, paymDoc, DocKind:integer, ID_Operation:integer, ID_Step:integer )

  if(CheckDateStartOpr(ID_Operation))
    return 1;
  end;

  if(CheckRecallBan(PaymentObj))
    return 1;
  end;

  PaymentObj.ValueDate = ValueDateForRecallFromInd(PaymentObj);
  // Установить дату начала операции равной дате операционного дня пользователя
  SetOprDate(29000000, PaymentObj.ValueDate);
  // Объекты для КУ
  var FD_CorrAcc:NotBalCorrAcc_FirstDoc = NotBalCorrAcc_FirstDoc( "П" );

  // Счета КУ
  var СистемныйСчетДляКартотек:string = FD_CorrAcc.FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );

  var ВнебалСчетКартотеки2:string = "";

  var paymtr:RsbAccTransaction;

  // Если производится отзыв платежного ордера из картотеки НОСТРО
  if( ( GetOprStatus( OPR_PAYM_INDEX ) == OPR_PAYM_ST_INDEX_NOSTRO ) and
      ( PaymentObj.PartPaymNumber > 0 ) )
    // Пока не реализовано, т.к. нигде реально не используется
    return 0;
  end;

  // Если платеж в картотеке 2
  if( PaymentObj.PaymStatus == PM_I2PLACED )

    if( GetAccount90902( PaymentObj.PayerAccount,
                         PaymentObj.PayerFIID,
                         PaymentObj.BaseFIID,
                         ВнебалСчетКартотеки2  /* В случае успеха, сюда вернется номер "привязанного" счета */
                        ))
      return 1;
    end;

    // Выполнить проводку
    paymtr = PaymentObj.MakeTransaction();

    if( paymtr == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr.Chapter         = 3;                                            
    paymtr.Date_Carry      = PaymentObj.ValueDate;
    paymtr.Number_Pack     = PaymentObj.NumberPack;
    paymtr.Numb_Document   = PaymentObj.Number;
    paymtr.ResultCarry     = OBI2DELETE;
    paymtr.Kind_Oper       = " 1";
    paymtr.Ground          = "Отзыв из картотеки №2 "+ PaymentObj.Ground;
    paymtr.Department      = PaymentObj.Department;

    paymtr.FIIDPayer       = NATCUR; 
    paymtr.FIIDReceiver    = PaymentObj.BaseFIID;
    paymtr.AccountPayer    = СистемныйСчетДляКартотек;
    paymtr.AccountReceiver = ВнебалСчетКартотеки2;

    paymtr.SumReceiver     = PaymentObj.DenialAmount;
    if( ConvSum( paymtr.SumPayer, PaymentObj.DenialAmount, PaymentObj.ValueDate, PaymentObj.BaseFIID, NATCUR ) )
      msgbox("Ошибка конвертации суммы");
      return 1;
    end;

    if( not paymtr.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;
  // если в картотеке ожидающих исполнения  
  elif( GetOprStatus( OPR_PAYM_INDEX ) == OPR_PAYM_ST_INDEX_WAITEXEC )
    // Выполнить проводку
    paymtr = PaymentObj.MakeTransaction();

    if( paymtr == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr.Chapter         = 3;                                            
    paymtr.Date_Carry      = PaymentObj.ValueDate;
    paymtr.Number_Pack     = PaymentObj.NumberPack;
    paymtr.Numb_Document   = PaymentObj.Number;
    paymtr.ResultCarry     = OBIWEWITHDRAW;
    paymtr.Kind_Oper       = " 1";
    paymtr.Ground          = "Отзыв распоряжения №" + PaymentObj.Number + ", принятого к исполнению будущей датой " + PaymentObj.ValueDate + " к счету " + PaymentObj.PayerAccount + ".";
    paymtr.Department      = PaymentObj.Department;

    paymtr.FIIDPayer       = NATCUR; 
    paymtr.FIIDReceiver    = NATCUR;
    paymtr.AccountPayer    = СистемныйСчетДляКартотек;
    paymtr.AccountReceiver = TIndexWaitExec(PaymentObj).FindAndOpenSysAccount( "Ожид. Исполн.", 0, {curdate} );

    if( ConvSum( paymtr.SumPayer, PaymentObj.FutureBaseAmount, PaymentObj.ValueDate, PaymentObj.BaseFIID, NATCUR ) )
      msgbox("Ошибка конвертации суммы");
      return 1;
    end;
    paymtr.SumReceiver     = paymtr.SumPayer;

    if( not paymtr.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;
  end;

  if((PaymentObj.DenialAmount == PaymentObj.BaseAmount) and ( PaymentObj.PrimDocOrigin == PD_OR_PAYEEBANK ))
    var PaymStatusClaim, DocKindClaim, PaymentIDClaim;
    if ( GetESIDPrimPaym(PaymentObj.PaymentID,PaymStatusClaim, DocKindClaim, PaymentIDClaim ))
       if (ChoiseESIDProcesDoc(PaymStatusClaim, DocKindClaim, PaymentIDClaim))
         return 1;
       end;
    end;    
  end;
  
  
  if (PaymentObj.DenialAmount == PaymentObj.FutureBaseAmount)
    // Установить статус платежа
    PaymentObj.PaymStatus = PM_FINISHED;

    // Установить статус первички "Закрыт"
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );

    // Установить статус сегмента операции
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  else
    PaymentObj.FutureBaseAmount = PaymentObj.FutureBaseAmount - PaymentObj.DenialAmount; 
  end;

  PaymentObj.StatusInfo = "Отозван из картотеки";

  return 0;

END;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;
  var DenialAmount = $0, DenialGround = "";
  GetDenialAmountGround(PaymentObj.PaymentID, PAYMENTS_INDEX_2, @DenialAmount, @DenialGround);
  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if(DenialAmount > $0)
      var PsOrder : RsbPsPayOrder = RsbPsPayOrder(PaymentObj.PaymentID);
      if((DenialAmount == PaymentObj.BaseAmount) and (PsOrder.Origin == PSPO_OR_PAYEEBANK)) 

        var PaymStatusClaim, DocKindClaim, PaymentIDClaim;
        if ( GetESIDPrimPaym(PaymentObj.PaymentID,PaymStatusClaim, DocKindClaim, PaymentIDClaim ))
          var Narrative : string = "", Queries : string = "";
          Queries = "InfoCode:7";
          Narrative = DenialGround;
          CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
        end;    
      end;
    end;
  end;
  return stat;
end;
