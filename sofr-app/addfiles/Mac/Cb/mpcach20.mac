/*
 $Name: mpcach20.mac
 $Module: Ядро ГКБО 
 $Description: Расчет суммы
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpcach20.mac

  Библиотека       : RSACCOUNT

  Описание         : Расчет суммы

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet,  InsCarryDoc, CTInter,  globals, mpclb;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);

// Вид графика
const PRC_SCHEDKIND_USER         = 0, // пользовательский
      PRC_SCHEDKIND_CALC         = 1, // расчета
      PRC_SCHEDKIND_CHARGE       = 2, // начисления
      PRC_SCHEDKIND_PAY          = 3; // оплаты

var prccontract = TBfile("prccontract.dbt"),
    prccntopr = TBfile("prccntopr.dbt"),
    DeltaSumCalc = $0,
    DeltaSum = $0,
    ContractID = 0,
    DocID = 0;

private var ДАТА_ВВОДА;
GetRegistryValue ("COMMON\\МСФО\\ДАТА_ВВОДА", V_DATE, ДАТА_ВВОДА);
ДАТА_ВВОДА = Date(ДАТА_ВВОДА);


PRIVATE MACRO SaveResults()

   if (PrcSetOperSum(prccntopr.rec.DocID, money(DeltaSum)) != 0)
      return 1;
   end;

   if (DeltaSumCalc != 0)
      if (PrcInsertAddChargePeriod(ContractID, 
                                    prccntopr.rec.OperDate, 
                                    prccntopr.rec.BeginDate, 
                                    prccntopr.rec.EndDate,  
                                    money(DeltaSumCalc), 
                                    money(0), 
                                    prccntopr.rec.DocID,
                                    PRC_PAYSTATUS_AWARDED) != 0)
         return 1;
      end;      
      
   end;
   
   return 0;
END; // end macro

   
// Проверяем, что заданный период доначмсления входит в период оплаты
PRIVATE MACRO CheckForPayPeriod(ID, BeginDate, EndDate):bool
   var   cmd,rs;

   cmd = RSDCommand ("select count(T_CNTSCHEDID) as iCount from DPRCCNTSCHED_DBT " +
         "where T_CONTRACTID = ? and T_SCHEDULEKIND = 3 " +
         "and T_PERIODBEGINDATE <= ? and T_PERIODENDDATE >= ?");
   cmd.addParam( "", RSDBP_IN, ID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.execute();

   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      if (rs.icount != 0)
         return true;
      end;
   end; 

   return false;

END;   

// Проверка на существование периодов доначисления, пересекающихся с нашим
PRIVATE MACRO FindOverlappedPeriods(ID, BeginDate, EndDate):bool

   var   stat = false,
         cmd,rs;

   cmd = RSDCommand ("select count(T_CNTSCHEDID) as iCount from DPRCCNTSCHED_DBT " +
         "where T_CONTRACTID = ? and T_SCHEDULEKIND = 2 and T_SPECIALTYPE = 1 " +
         "and ((T_PERIODBEGINDATE > ? and T_PERIODBEGINDATE < ?) or " +
         "(T_PERIODENDDATE > ? and T_PERIODENDDATE < ?) or " + 
         "(T_PERIODBEGINDATE <= ? and T_PERIODENDDATE >= ?)) and " + 
         "not (T_PERIODBEGINDATE >= ? and T_PERIODENDDATE <= ?)");
   cmd.addParam( "", RSDBP_IN, ID );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.addParam( "", RSDBP_IN, BeginDate );
   cmd.addParam( "", RSDBP_IN, EndDate );
   cmd.execute();

   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      if (rs.icount != 0)
         return true;
      end;
   end; 

   return false;
END;

macro CheckPayState(ID, BeginDate, EndDate)
    var cmd = RSDCommand ("select count(*) as iCount from DPRCCNTSCHED_DBT where T_CONTRACTID = ? "
                        "AND T_PAYSTATE = 7 AND T_SCHEDULEKIND = 2 "
                        "AND T_PERIODBEGINDATE <= ? "
                        "AND T_PERIODENDDATE >= ?");
    cmd.addParam("", RSDBP_IN, ID);
    cmd.addParam("", RSDBP_IN, BeginDate);
    cmd.addParam("", RSDBP_IN, EndDate);
    
    var rs = TRsbDataSet(cmd);
    if (rs.movenext())
        if (rs.icount != 0)
            return true;
        end;
    end;

    return false;    
end;

private macro UpdatePayState(ID, BeginDate, EndDate)
    var cmd = RSDCommand ("select T_CNTSCHEDID, T_PAYSTATE, T_SCHEDULEKIND from DPRCCNTSCHED_DBT where T_CONTRACTID = ? " 
                      "   AND T_PAYSTATE = 7 "
                      "   AND T_PERIODBEGINDATE <= ? "
                      "   AND T_PERIODENDDATE >= ?");
    cmd.addParam("", RSDBP_IN, ID);
    cmd.addParam("", RSDBP_IN, BeginDate);
    cmd.addParam("", RSDBP_IN, EndDate);
    
    var rs = RsdRecordset(cmd);
    
    while (rs.MoveNext())
        var status = PRC_PAYSTATUS_AWARDED;
        if (rs.value("T_SCHEDULEKIND") == PRC_SCHEDKIND_PAY)
            status = PRC_PAYSTATUS_NOT_CLAIMED;
        end;
        PrcSetPayState(rs.value(0), status, rs.value(1));
    end;

    return true;    
end;

MACRO ExecuteStep(Buffer, Document)
   
   var   SumCalc = $0,
         SumCalc1 = $0,
         SumFact = $0,         
         RoundSumDelta = $0,
         cmd,rs,
         Sum,
         BeginQual,
         EndQual;

   SetBuff( cntopr_buf, Document );

   ContractID = cntopr_buf.contractid;
   DocID      = cntopr_buf.DocID;

   prccntopr.KeyNum = 0;  
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      msgbox ("Не найдена операция доначисления");
      return 1;
   end; 

   ClearRecord( contract_buf );

   prccontract.KeyNum = 0;  
   prccontract.rec.contractid = ContractID;
   if (not prccontract.GetEQ)
      msgbox ("Не найден договор с ID = " + ContractID);
      return 1;
   end;

   if (FindOverlappedPeriods(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate))
      msgbox("Доначисление процентов запрещено. Выбранный период |пересекается или полностью входит в период, по |которому уже проведена операция доначисления процентов");
      return 1;
   end;

   if (CheckForPayPeriod(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate) == false)
      msgbox("Доначисление процентов запрещено. Выбранный |период доначисления не соответствует периоду оплаты");
      return 1;
   end;

    if (CheckPayState(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate))
        var ret = MsgBoxEx ("В выбранном периоде начисление процентов приостановлено. Выполнить доначисление? ", 
            MB_YES + MB_NO, IND_NO);
        if (ret == IND_NO)
            InsertOprStatus(1040, 2);
            return 1;
        else
            UpdatePayState(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate);
        end;
    end;

    // Размещение
    if (prccontract.rec.ResKind == PRC_RESKIND_RAZM)
        if ({curdate} < ДАТА_ВВОДА)
            if (PrcGetClientQualityOnDate(ContractID, prccntopr.rec.BeginDate, @BeginQual))
                MemoryError(6829);
                DisplayError();
                return 1;
            end;   
        
            if (PrcGetClientQualityOnDate(ContractID, prccntopr.rec.EndDate, @EndQual))
                MemoryError(6829);
                DisplayError();
                return 1;
            end;
            
            if ((IncomeIsDefinite(BeginQual) == true) and (IncomeIsDefinite(EndQual) == false))
                msgbox("Доначисление процентов запрещено. В период |доначисления была выполнена операция реклассификации");
                return 1;
            end;
        end;
    end;

   cmd = RSDCommand ("select T_DATE, T_PERIODBEGINDATE, T_PERIODENDDATE, " +
                      "T_SUMCALC, T_SUMFACT, T_ROUNDSUMDELTA from DPRCCNTSCHED_DBT " +
                      "where T_SCHEDULEKIND = 2 and T_CONTRACTID = ? " + 
                      "and T_PERIODBEGINDATE >= ? and T_PERIODENDDATE <= ?");
   cmd.addParam( "", RSDBP_IN, ContractID );
   cmd.addParam( "", RSDBP_IN, prccntopr.rec.BeginDate );
   cmd.addParam( "", RSDBP_IN, prccntopr.rec.EndDate );
   cmd.execute();
   rs = TRsbDataSet(cmd);


   while (rs.movenext())
      if (ValType(rs.sumcalc) != V_UNDEF)
         SumCalc = SumCalc + rs.sumcalc;
      end;
      if (ValType(rs.sumfact) != V_UNDEF)
         SumFact = SumFact + rs.sumfact;
      end;
      if (ValType(rs.roundsumdelta) != V_UNDEF)
         RoundSumDelta = rs.roundsumdelta;
      end;
   end;     
   
   if (prccntopr.rec.IsReCalc == "X")
      GenerateRatesTable(ContractID, prccntopr.rec.BeginDate, prccntopr.rec.EndDate);
      SumCalc1 = CalcPercentForPeriod(ContractID,prccntopr.rec.BeginDate,prccntopr.rec.EndDate);
   else 
      SumCalc1 = SumCalc;
   end;

   
   SumCalc1 = money(round(SumCalc1,2));
   DeltaSum = money(SumCalc1 - SumFact);
   DeltaSumCalc = money(SumCalc1 - SumCalc);   


   if (SaveResults() != 0)
      return 1;
   end;
   
   return 0;
   
END;



MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)       /* Вид шага операции */ 

  exit(1); 

END;

