/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : paymmp.mac                                         */
/*                                                                      */
/*  Описание       : Отчет о массовой печати патежей.                   */
/*                                                                      */
/*  Программист    : Смирнов С.В. SMR PRN                               */
/*                                                                      */
/*  Создан         : 08.10.02                                           */
/*                                                                      */
/************************************************************************/

import prpmbuff;

const MSG_ERROR:integer = 0,
      MSG_FIRST:integer = 1,
      MSG_LAST :integer = 2;

MACRO getPMRMPROP(PaymentID:integer, pmrmprop:TRecHandler):bool
  var fpmrmprop:TBFile = TBFile("pmrmprop.dbt", "R", 0, "pmrmprop.dbt", "bank.def");
  fpmrmprop.rec.PaymentID = PaymentID;
  if(fpmrmprop.GetEQ())
    Copy(pmrmprop, fpmrmprop);
    return TRUE;
  else
    ClearRecord(pmrmprop);
    return FALSE;
  end;
END;
      
MACRO Печать_ОтчетСканирования(MessageType:integer, DocBuffer, MessageStat:integer, MessageText:string)
 record rpmpaym("pmpaym.dbt", "bank.def");
 array StringArray;

 var stat:integer, i:integer = 1;
 var pmrmprop:TRecHandler = TRecHandler("pmrmprop.dbt", "bank.def");

 SetBuff(rpmpaym, DocBuffer);
 Copy(pr_pmpaym, rpmpaym);
 getPMRMPROP(pr_pmpaym.rec.PaymentID, pmrmprop);

 if (MessageType==MSG_FIRST)

   [    Отчет о групповой печати платежей.
    +---------+------+------------------------------------------------------------+
    |  Номер  |Номер |                 Сообщение                                  |
    |документа|ошибки|                                                            |
    +---------+------+------------------------------------------------------------+];

  elif (MessageType==MSG_ERROR)

    if (MessageStat == 0)
      MessageText = "Документ напечатан успешно";
    end;
    StrSplit(StrSubst(MessageText, "|", " " ), StringArray, 56);                
    [|#########|######|############################################################|](pmrmprop.rec.Number, MessageStat, StringArray(0));
    while ( StrLen(StringArray(i) ) > 0)
      [|         |      |############################################################|](StringArray(i));
      i = i + 1;
    end;

  elif (MessageType==MSG_LAST)
    [+---------+------+------------------------------------------------------------+];
  end;

END;
