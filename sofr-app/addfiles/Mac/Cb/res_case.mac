/*
$Name: res_case.mac
$Module: Ядро ГКБО
$Description: Расчет резерва для портфеля однородных требований
*/

Import "res_calc.mac", "res_lib.mac";

/* подсчитать сумму по всем счетам массива */
private macro accArrayCalcSum( accArray, FunName, DateReserve, DateRate )

  record accBuff(account);
  var i, N;
  var Sum;
  var stat;

  N = accArray.Size();
  i = 0;

  Sum = $0;

  while( i < N )

    stat = GetAccountRecord( accBuff, accArray(i).Chapter, accArray(i).Account, accArray(i).Code_Currency );

    if( stat )
      Sum = Sum + ExecMacro2( FunName, accBuff, DateReserve, DateRate );
    end;

    i = i + 1;
  end;

  return Sum;

end;

/* подсчитать остаток на счете покрытия парного счета*/
private macro _CaseRestAccountCurrency( accBuff, DateReserve, DateRate )
  return GetAccCurrencyRest( accBuff, DateReserve );
end;

/* подсчитать остаток на счете */
private macro _CaseRestAccount( accBuff, DateReserve, DateRate )
  /*
  для рублевых счетов берем остаток на дату резервирования,
  для валютных берем остаток на дату резервирования, пересчитанный на дату курса
  */
  if(accBuff.Code_Currency == NATCUR)
    return GetAbsAccRest( accBuff.Account, DateReserve, accBuff.Chapter );
  else
    return GetAbsAccRestRate( accBuff.Account, DateReserve, DateRate, accBuff.Chapter, accBuff.Code_Currency );
  end;
end;

/* подсчитать текущую рыночную стоимость */
private macro _CaseMarketCost( accBuff, DateReserve, DateRate )
  return GetAccMarketCost( accBuff, DateReserve );
end;

/* подсчитать нерезервируемую сумму */
private macro _CaseFreeReserveSum( accBuff, DateReserve, DateRate )
  return GetAccFreeReserveSumRateDate( accBuff, DateReserve, DateRate );
end;

/* подсчитать остаток на счете обязательств */
private macro _CaseRestAccountObligation( accBuff, DateReserve, DateRate )
  return GetAccObligationRest( accBuff, DateReserve );
end;

/* подсчитать остаток на счете амортизации */
private macro _CaseRestAccountAmortization( accBuff, DateReserve, DateRate )
  return GetAccAmortizationRest( accBuff, DateReserve );
end;

/* подсчитать текущую рыночную стоимость на счетах амортизации */
private macro _CaseMarketCostAccountSecurity( accBuff, DateReserve, DateRate )
  return GetMarketCostAccountSecurity( accBuff, DateReserve );
end;

/* подсчитать текущую рыночную стоимость на счетах обеспечения c учетом категории обеспечения качества */
private macro _CaseMarketCostSecurityByCategory( accBuff, DateReserve, DateRate )
  return GetMarketCostSecurityByCategory( accBuff, DateReserve );
end;

/* подсчитать текущую рыночную стоимость на счетах обеспечения c учетом категории обеспечения качества */
private macro _CaseSumReserveCalcUser( accBuff, DateReserve, DateRate )
  var SumReserveCalcUser : money;
  SumReserveCalcUser = GetAccSumReserveCalcUser( accBuff, DateReserve );
  if(accBuff.Code_Currency != NATCUR)
    if(ConvSum(SumReserveCalcUser, 
               SumReserveCalcUser, 
               DateReserve, 
               accBuff.Code_Currency, 
               NATCUR))
      RunError("Ошибка при конвертации суммы");
    end;
  end;
  return SumReserveCalcUser;
end;


class (CalcReserve) CalcReserveAccCase( _accCase, _accCaseSub, _accCaseParm, _DateReserve : date, _ReserveType : integer, _RsvClass : string, _RsvClassLoans : string )

  private var accCase;     /* буфер портфеля */
  private var accCaseSub;  /* буфер субпортфеля */
  private var accCaseParm; /* буфер параметров портфеля */
  private var ReserveType; /* Классификация резерва субпортфеля */

  private var accArray : TArray;

/*** Конструктор ***/
  InitCalcReserve();

  accCase     = _accCase;
  accCaseSub  = _accCaseSub;
  accCaseParm = _accCaseParm;
  ReserveType = _ReserveType;

  /* классификация РВП */
  RsvClass         = _RsvClass;
  /* классификация РВПС */
  RsvClassLoans    = _RsvClassLoans;
  
  /* дата формирования резерва */
  DateReserve = _DateReserve;
  DateRate = GetDateRateForCase(accCase, accCaseParm, accCaseSub, DateReserve, @RestReserve);

  accArray = TArray;

  CreateCaseAccountArray( accCaseSub, accArray, DateReserve, ReserveType );
  
  /*Для классификации "3.1.1 (91318)" резерв не рассчитывается, а берется из примечания к счету*/
  if(SubStr(RsvClass, 1, 13) == "3.1.1 (91318)")
    SumReserveCalcUser_DateReserve = accArrayCalcSum( accArray, @_CaseSumReserveCalcUser, DateReserve, DateReserve );
    if(DateReserve != DateRate)
      SumReserveCalcUser_DateRate = accArrayCalcSum( accArray, @_CaseSumReserveCalcUser, DateReserve, DateRate );
    else
      SumReserveCalcUser_DateRate = SumReserveCalcUser_DateReserve;
    end;
    return;
  end;
  
  /* остаток на лицевом счете */
  Rest_DateReserve = accArrayCalcSum( accArray, @_CaseRestAccount, DateReserve, DateReserve );
  if(DateReserve != DateRate)
    Rest_DateRate = accArrayCalcSum( accArray, @_CaseRestAccount, DateReserve, DateRate );
  else
    Rest_DateRate = Rest_DateReserve;
  end;
  
  /* текущая рыночная стоимость */
  MarketCost = accArrayCalcSum( accArray, @_CaseMarketCost, DateReserve, DateRate );
  
  /* нерезервируемая сумма */
  FreeReserveSum_DateReserve = accArrayCalcSum( accArray, @_CaseFreeReserveSum, DateReserve, DateReserve );
  if(DateReserve != DateRate)
    FreeReserveSum_DateRate = accArrayCalcSum( accArray, @_CaseRestAccount, DateReserve, DateRate );
  else
    FreeReserveSum_DateRate = FreeReserveSum_DateReserve;
  end;
  
  /* остаток на счете амортизации */
  RestAmortization = accArrayCalcSum( accArray, @_CaseRestAccountAmortization, DateReserve, DateRate );
  
  /* остаток на счете обязательств */
  RestObligation = accArrayCalcSum( accArray, @_CaseRestAccountObligation, DateReserve, DateRate, true );
  
  /*остаток на счете покрытия парного счета*/
  RestCurrency = accArrayCalcSum( accArray, @_CaseRestAccountCurrency, DateReserve, DateRate );
  
  /* сумма текущих рыночная стоимостей для всех счетов обеспечения */
  MarketCostSecurity = accArrayCalcSum( accArray, @_CaseMarketCostAccountSecurity, DateReserve, DateRate );
  
  /* сумма текущих рыночная стоимостей для всех счетов обеспечения с учетом качества обеспечения */
  MarketCostSecurityByCategory = accArrayCalcSum( accArray, @_CaseMarketCostSecurityByCategory, DateReserve, DateRate );

  /* процент РВП (РВПС) */
  ProcentReserve = accCaseParm.ReservePercent;

  /* процент ОР */
  ProcentReserveEstimated = accCaseParm.LostPercent;
    
  /* Группа риска */
  GroupRisk = int(accCaseParm.RiskGroup);

  /* установить в качестве исходных данных для расчета резерва величины на дату резервирования */
  SetVariables_DateReserve();

end;
