/*
$Name: ws_progress.mac
$Module: WEB
$Description: Универсальный индикатор прогресса для интерфейсной работы и работы через сервис

  WEB. Макрос прогресс индикатора
  Универсальный индикатор прогресса для интерфейсной работы и работы через сервис

  Пример использования:
  var ProgressIndicator = CreateProgressIndicator(IsWebService);
  ProgressIndicator.Start(count, text, header);

  m_RS = TRsbDataSet(strSelect + DataQuery);
  while (m_RS.MoveNext())
     ObjReport.Print();
     index = index + 1;
     ProgressIndicator.Update(index); // всего 3 парамера Update(index, count, text)
  end;
  ProgressIndicator.Stop();

  второй вариант использовать функцию:
  UpdateBackgroundProcessState(value :integer, maxValue :integer, text :string)
*/

import rsd;
import RsbDataSet;
import XmlRpcInter, globals;

private const UPDATE_INTERVAL = time(0,0,3);

class TWsProcessState
    var Id : string = "";
    var Oper : integer = 0;
    var Value : integer = 0;
    var MaxValue : integer = 0;
    var Text : string = "";
    var Date : date = date();
end;

class CProgressIndicator()

  macro Start(maxValue :integer, text :string, header :string)
  end;

  macro Update(value :integer, maxValue :integer, text :string)
  end;

  macro Stop()
  end;
end;

//***************************************************************************************************************
class SqlQueryBuilder
  class ParamInfo(param_name :string, param_value :variant)
    var Name :string = param_name;
    var Value :variant = param_value;
  end;

  private var columns = TArray();
  macro AddColumn(name :string, value :variant)
    columns[columns.size] = ParamInfo(name, value);
  end;

  macro GetResult()
    var isfirst = true;
    var result = "";
    for (var info, columns)
      if (isfirst)
        isfirst = false;
      else
        result = result + ", ";
      end;
      result = result + info.Name + " = ? ";
    end;
    return result;
  end;

  macro AddParams(cmd)
    if (cmd == null)
      RunError("Не задана команда");
    end;

    for (var info, columns)
      cmd.AddParam("", RSDBP_IN, info.Value);
    end;
  end;
end;

private macro IIF(condition, value_true, value_false)
   if (condition)
      return value_true;
   else 
      return value_false;
   end;
end;

private macro CopyProcessState(procState :TWsProcessState, ds)
    procState.Id = ds.Id;
    procState.Oper = ds.Oper;
    procState.Value = ds.Value;
    procState.MaxValue = ds.MaxValue;
    procState.Text = ds.Text;
    procState.Date = ds.Date;
end;

private macro InsertProcStateRecord(id :string, oper: integer, value :integer, maxValue :integer, text :string)
  var cmd, cmdText;

  cmdText = "insert into DWEB_PROCSTATE_DBT(T_ID, T_OPER, T_VALUE, T_MAXVALUE, T_TEXT) VALUES(?, ?, ?, ?, ?)";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, id);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.AddParam("", RSDBP_IN, value);
  cmd.AddParam("", RSDBP_IN, maxValue);
  cmd.AddParam("", RSDBP_IN, text);
  cmd.execute();
end;

private macro UpdateProcStateRecord(id :string, value :integer, maxValue :integer, text :string)
    var cmd, cmdText;

    if ((value == null) and (maxValue == null) and (text == null))
      RunError("UpdateProcStateRecord: Не задан ни один обновляемый пареметр");
    end;

    var builder = SqlQueryBuilder();

    if (value != null)
      builder.AddColumn("T_VALUE", value);
    end;
    if (maxValue != null)
      builder.AddColumn("T_MAXVALUE", maxValue);
    end;
    if (text != null)
      builder.AddColumn("T_TEXT", text);
    end;

    cmdText = "UPDATE DWEB_PROCSTATE_DBT SET " + builder.GetResult() + " WHERE T_ID = ? ";
    cmd = RsdCommand(cmdText);
    builder.AddParams(cmd);
    cmd.AddParam("", RSDBP_IN, id);
    cmd.execute();
end;

private macro FindProcStateRecord(id :string) // : TWsProcessState
  var cmd, cmdText;

  if ((id == null) or (id == ""))
    RunError("id должен быть задан");
  end;

  cmdText = "SELECT * FROM DWEB_PROCSTATE_DBT WHERE T_ID = ?";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, id);
  cmd.execute();
  
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

  var result = null;
  if (ds.moveNext())
    result = TWsProcessState();
    CopyProcessState(result, ds);
  end;

  return result;
end;

private macro DeleteProcStateRecord(id :string)
  var cmd, cmdText;

  if ((id == null) or (id == ""))
    RunError("id должен быть задан");
  end;

  cmdText = "delete FROM DWEB_PROCSTATE_DBT WHERE T_ID = ?";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, id);
  cmd.execute();
  return true;
end;

private macro SelectProcStateRecordsByOper(oper :integer) // : TArray of TWsProcessState
  var cmd, cmdText;

  cmdText = "SELECT * FROM DWEB_PROCSTATE_DBT WHERE T_OPER = ?";
  cmd = RsdCommand(cmdText);

  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();
  
  var ds = TRsbDataSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
  var result = TArray();
  var state = null;

  while (ds.moveNext())
    state = TWsProcessState();
    CopyProcessState(state, ds);
    result[result.Size] = state;
  end;

  return result;
end;

macro UpdateBackgroundProcessStateById(reqId :string, value :integer, maxValue :integer, text :string)

  if ((reqId == null) or (reqId == ""))
    RunError("BackgroundProcessState: Не задан reqId")
  end;

  if ((value == null) and (maxValue == null) and (text == null))
    RunError("BackgroundProcessState: Не задан ни один обновляемый пареметр");
  end;

  var state = FindProcStateRecord(reqId);
  if (state == null)

    InsertProcStateRecord(reqId, {oper}, 
      IIF(value == null, 0, value), 
      IIF(maxValue == null, 0, maxValue), 
      IIF(text == null, "", text));

  elif (((value != null) and (state.value != value)) 
    or ((maxValue != null) and (state.maxValue != maxValue)) 
    or ((text != null) and (state.text != text)))

    UpdateProcStateRecord(reqId, value, maxValue, text);
  end;
end;

macro UpdateBackgroundProcessState(value :integer, maxValue :integer, text :string)
  UpdateBackgroundProcessStateById(XmlRpcRequestId(), value, maxValue, text);
end;

macro GetBackgroundProcessState(reqId :string)
  return FindProcStateRecord(reqId);
end;

macro ClearBackgroundProcessState(reqId :string)
  return DeleteProcStateRecord(reqId);
end;

macro GetBackgroundProcessStateListByOper(oper :integer)

  if ((oper == null) or (oper <= 0))
    RunError("Не задан операционист")
  end;

  return SelectProcStateRecordsByOper(oper);
end;

macro GetBackgroundProcessStateList()
  var oper = {oper};
  return GetBackgroundProcessStateListByOper(oper);
end;

//***************************************************************************************************************

class (CProgressIndicator) EwProgressIndicator()

  macro Start(maxValue :integer, text :string, header :string)
    InitProgress(maxValue, text, header);
  end;

  macro Update(value :bigint, maxValue :integer, text :string)
    UseProgress(value);
  end;

  macro Stop()
    RemProgress();
  end;
end;

class (CProgressIndicator) WebProgressIndicator()
  private var m_MaxValue:Integer = 0;
  private var m_CurValue:Integer = 0;
  private var m_ReqId:string = XmlRpcRequestId();
  private var m_Text:string = "";
  private var m_lastUpdateTime = null;

  macro Start(maxValue :integer, text :string, header :string)
    m_CurValue = 0;
    m_MaxValue = 0;
    m_Text = text;
    if (MaxValue >= 0)
      m_MaxValue = MaxValue;
    end;
    m_lastUpdateTime = null;
    Update(m_CurValue, m_MaxValue, m_Text);
  end;

  macro Update(value :integer, maxValue :integer, text :string)
    if ((value != null) and (value >= 0))
      m_CurValue = value;
    end;

    if ((maxValue != null) and (maxValue >= 0))
      m_MaxValue = maxValue;
    end;

    if (text != null)
      m_Text = text;
    end;

    if ((m_lastUpdateTime == null) or (time - m_lastUpdateTime > UPDATE_INTERVAL))
      m_lastUpdateTime = time;
      UpdateBackgroundProcessStateById(m_ReqId, m_CurValue, m_MaxValue, m_Text);
    end;
  end;

  macro Stop()
    m_lastUpdateTime = null;
    Update(m_MaxValue);
  end;

  macro GetReqID()
    return m_ReqId;
  end;
end;

macro CreateProgressIndicator(IsWebService:Bool) : CProgressIndicator
  if (IsWebService)
    return WebProgressIndicator();
  else
    return EwProgressIndicator();
  end;
end;

//**************************************************************************************************************************

/*

var reqId = "555";
UpdateBackgroundProcessStateById(reqId, 0 , 10, "comment");
UpdateBackgroundProcessStateById(reqId, 1);
UpdateBackgroundProcessStateById(reqId, null , 10);
UpdateBackgroundProcessStateById(reqId, null , null, "comment");

UpdateBackgroundProcessStateById("666", 1 , 10, "comment");
UpdateBackgroundProcessStateById("777", 1 , 10, "comment");

var state = GetBackgroundProcessState(reqId);
var list = GetBackgroundProcessStateList();
var i = 0;
*/
/*
var indicator = CreateProgressIndicator(true, 7777777);
indicator.Start(777);
indicator.Update(1);
indicator.Update(2);
indicator.Update(7);
var val = GetProgressValue(7777777);
indicator.Stop();
*/