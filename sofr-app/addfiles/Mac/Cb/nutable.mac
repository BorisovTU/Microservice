/* Макрофайл таблицы соответствия налогового учета */

import BankInter, CarryDoc, globals;

record NuTable ( "tc_nd.dbt" );

VAR opct_fname, opct_oldout, count, erropen;




macro OpenAccount( op )

   file txtout () txt;
   var len, notetext,err, errmsg;

   record account(account);

   file ac_c("account$.dbt") key 0;
   file ac_r(account) key 0 write;
   file balance(balance) key 0;
   file mes ("bank.msg");

   if ( op == 1 )

      opct_fname = GetTxtFileName( "nuaccrep" );
      opct_oldout = SetOutput(opct_fname);
      count = 0;
      erropen = 0;
      [Список открытых счетов налогового учета за дату ##########]({curdate});
      [ ];

   elif ( op == 3 )

      [ ];
      if ( ( count == 0 ) and ( erropen == 0 ) )
         [Объектов к открытию не найдено. Все лицевые счета присутствуют в базе.];
      else
         [Открыто новых лицевых счетов: #](count:l);
         [Ошибок выполнения: #](erropen:l);
      end;

      SetOutput( opct_oldout, TRUE );
      Open( txtout , opct_fname );
      ViewFile( txtout , TRUE );
      Close(txtout);
      DelFile( opct_fname );

   else

      ClearRecord(account);

      account.Chapter = 6;
      account.Account = NuTable.AccountNU;
      account.Code_Currency = NuTable.FIIDNU;
      account.Oper = {oper};
      account.Symbol = "0" ;
      account.Open_Date = {curdate};
      account.Type_Account = "О" ;

      balance.Chapter = 6;
      balance.iNumPlan = 0;
      balance.Balance = account.Account;

      
      if ( GetLE( balance ) )
         while ( ( Index( account.Account , balance.Balance ) != 1 ) and ( Prev(balance) ) and (balance.Chapter == 6) )

         end;
         if ( ( Index( account.Account , balance.Balance ) == 1 ) and ( balance.Chapter == 6 ) )
            account.Kind_Account = balance.Kind_Account;
            account.Balance      = balance.Balance;
         end;
      end;

      if ( NuTable.FIIDFU > 0 )
         ac_c.Chapter = NuTable.ChapterFU;
         ac_c.Code_Currency = NuTable.FIIDFU;
         ac_c.Account = NuTable.AccountFU;
         if ( GetEQ(ac_c) )
            account.Client = ac_c.Client;
            account.Department = ac_c.Department;
            account.NameAccount = ac_c.NameAccount;
         end;
      else
         ac_r.Chapter = NuTable.ChapterFU;
         ac_r.Account = NuTable.AccountFU;
         if ( GetEQ(ac_r) )
            account.Client = ac_r.Client;
            account.Department = ac_r.Department;
            account.NameAccount = ac_r.NameAccount;
         end;
      end;

      if ( NuTable.FIIDNU > 0 )
         account.Connect_Account = account.Account;
         account.Connect_Currency = 0;
         account.Connect_Chapter  = account.Chapter;
      end;

      err = Create_Account( account );

      if ( ( err == 0 ) and (NuTable.FIIDNU > 0 ) )
         ac_r.Account = account.Connect_Account;
         ac_r.Chapter = account.Connect_Chapter;
         if ( GetEQ(ac_r) )
            if ( Index( ac_r.Type_Account , "Н" ) == 0 )
               ac_r.Type_Account = ac_r.Type_Account + "Н" ;
               Update(ac_r);
            end;
         end;
      end;

      if ( err == 0 )
         count = count + 1;
         [К счету БУ N #################### открыт счет НУ N ####################](NuTable.AccountFU, NuTable.AccountNU);
      else
         erropen = erropen + 1;
         mes.Number = err;
         if ( ( err>1 ) and ( GetEQ(mes) ) )
            errmsg = mes.Contents;
         elif ( err == 1 )
            errmsg = "Ошибка работы с БД" ;
         else
            errmsg = "" ;
         end;
         [Ошибка открытия счета НУ N ####################. Код ошибки: #### #](NuTable.AccountNU, err, errmsg:l);
      end;

   end;

   return 0;

end;

/* Меню печати отчетных форм из скроллинга по F7  */
MACRO MenuPrintTC
Var menusel;
array menuArr;

menuArr(0) = "Список счетов таблицы соответствия НУ";
menuArr(1) = "Счета БУ, не вошедшие в таблицу соответствия";

menuSel= Menu (menuArr,"Выберите  отчет","Отчетные формы");

if (menuSel==0)
  system ( 8540, CodeFor("Г") );

elif (menuSel==1)
  system ( 8530, CodeFor("Г") );
end;

END;
