/*
 $Name:           ovest_summ_order.mac
 $Module:         Главная книга
 $Description:    Печать реестра документов по переоценке
*/
/*********************************************************************
  Печать реестра документов по переоценке
**********************************************************************/

Import rsd, BankInter, FIInter, CTInter, globals, "mo203inc.mac", "pr2161lib.mac",  cb_SQL, "consrep.mac";

private var MaxLinesMO      : integer;

private var IsEmptyReport   : bool;
private var OrderNumber     : integer;
private var PreviousChapter : integer;

private macro GetChapterString( Chapter : integer ) : string

  var ChapterString : integer;
  file obch("obchaptr.dbt") key 0;

  ChapterString = string(Chapter);

  obch.Chapter = Chapter;
  if( GetEQ(obch) )
    
    ChapterString = ChapterString + " - " + obch.Name;
  
  end;

  return ChapterString;

end;

private macro PrintMO( rs : RsdRecordset, Flag : integer, Chapter : integer, Account : string, RegDate : date, Count : integer, DprtID : Integer, RestTypeString : string )

  ARRAY SS;
  var TypeCarry : string;
  var Sum : money, TotalSum : money;
  var SumString : string;
  var PayerAccount : string, ReceiverAccount : string;
  var Ground : string;

  var Brk   : bool;
  var Lines : integer;

  
  Ground = "Назначение платежа (содержание операции): переоценка ";
  Ground = Ground + RestTypeString;
  Ground = Ground + " остатка счетов в иностранной валюте - ";

  if( Flag == 0 )
    Ground = Ground + "положительные разницы";
  else
    Ground = Ground + "отрицательные разницы";
  end;

  Ground = Ground + ", Глава ";
  Ground = Ground + GetChapterString(Chapter);

  while( Count > 0 )
    PrintLogDepartment( DprtID );

[                                                                              ];
[                  МЕМОРИАЛЬНЫЙ ОРДЕР № ################                 ф.№277]( OrderNumber:l );
[                                                                              ];
[                  #####################################                       ]( RegDate:m:c:f );
[                                                                              ];
[##############################################################################]( Ground:w );
[                                                                              ];
[+-------------------------+-------------------------+------------------------+];
[|        Дебет счета      |      Кредит счета       |       Сумма, руб.      |];
[|                         |                         |         в рублях       |];
[+-------------------------+-------------------------+------------------------+];

    Lines = 0;
    TotalSum = $0;

    Brk = false;
  
    while( (not Brk) and (Lines < MaxLinesMO) and (rs.moveNext()) )

      Sum = money(rs.value("t_Sum_NatCur"));
      PayerAccount    = rs.value("t_Account_Payer"   );
      ReceiverAccount = rs.value("t_Account_Receiver");

      if( Lines )
     
        if( Flag == 0 )
          ReceiverAccount = "";
        else
          PayerAccount = "";
        end;
    
      end;

[|#########################|#########################|########################|]
( PayerAccount:l, ReceiverAccount:l, Sum:f );
    
      Lines = Lines + 1;
      Count = Count - 1;
      TotalSum = TotalSum + Sum;

      if( (Count == 2 ) and (Lines == MaxLinesMO - 1) )
        /* в последнем СМО должно быть более 1 строки */
        Brk = true;
      end;

    end;

    SumString = CurToStrAlt( TotalSum, null, null, GetISOCode(NATCUR) );
    strsplit( SumString, SS, 1000,62,2 );

[+-------------------------+-------------------------+------------------------+];
[|           Итого                                   |########################|]( TotalSum );
[+---------------------------------------------------+------------------------+];
[                                                                              ];
[                                                                              ];
[Сумма прописью: ##############################################################]( SS(0) );
[##############################################################################]( SS(1):w );
[                                                                              ];
[Гл. бухгалтер: _________________________________                              ];
[                                                                              ];
[Бухгалтер:     _________________________________                              ];
[                                                                              ];
[Контролер:     _________________________________                              ];
[                                                                              ];
[                                                                              ];

    OrderNumber = OrderNumber + 1;

  end;

end;


/* Печать единичной проводок */
macro PrintSingleCarry( AccTrnID : integer )
  var acctrn:TBFile = TBFile( "acctrn", "R", 0 );
  acctrn.rec.AccTrnID = AccTrnID;
  if(acctrn.GetEQ())
    СформироватьОтчетДляПроводки( acctrn, true );
  end;
end;


macro ProcessAccount( OnLine : bool, Flag : integer, Chapter : integer, Account : string, RegDate : date, DprtID : Integer, Result_Carry : integer, RestTypeString : string )

   var rs : RsdRecordset;
   var SQLString : string;
   var FldAccount : string;
   var Count : integer;

   if( Chapter != PreviousChapter )
      OrderNumber = 1;
      PreviousChapter = Chapter;
   end;

   if( Flag == 0 )
      FldAccount = "t_Account_Receiver";
   else
      FldAccount = "t_Account_Payer";
   end;

   SQLString =          "FROM dacctrn_dbt ";
   SQLString = SQLString + "WHERE t_Chapter = " + string(Chapter) + " ";
   SQLString = SQLString + "AND t_Department = " + string(DprtID) + " ";
   SQLString = SQLString + "AND t_Result_Carry = " + string(Result_Carry) + " ";
   SQLString = SQLString + "AND " + FldAccount + " = '" + Account + "' ";
   SQLString = SQLString + "AND t_Date_Carry = TO_DATE( '" + string(RegDate) + "', 'DD.MM.YYYY') ";

   if( OnLine )
     SQLString = SQLString + "AND t_AccTrnID in (SELECT t_CarryID FROM dovest_carry_log_tmp) ";
   end;


   count = 0;

   rs = RsdRecordset( "SELECT COUNT(1) " + SQLString );

   if( rs.moveNext() )
      count = int(rs.value(0));
   end;

   SQLString = "SELECT * " + SQLString;

   rs = RsdRecordset( SQLString );

   if( count > 1 )
     
     PrintMO( rs, Flag, Chapter, Account, RegDate, count, DprtID, RestTypeString );

   elif( count == 1 )

     if( rs.moveNext() )

       PrintSingleCarry( rs.value("t_AccTrnID") );
     
     end;

   end;

   if( count != 0 ) IsEmptyReport = false; end;

end;

private macro _GetRegDatesRS()
  var query = "";
  var cmd, rs;

  query = query + "SELECT DISTINCT t_Date_Carry ";
  query = query + "  FROM dovest_carry_log_tmp ";
  
  cmd = RsdCommand(query);

  rs = RsdRecordset(cmd);

  return rs;
end;

macro procReportMO( OnLine : bool, Chapter : integer, RegDate : date, DprtID : integer, Result_Carry : integer, RestTypeString : string )

   var SQLString : string;
   var rs : RsdRecordset;
   var RegDatesRS;

   /* Сформируем запрос для выборки счетов курсовых разниц */
   SQLString = "";
   SQLString = SQLString + " select /*+ use_hash(dtmp,dacc)*/ distinct dacc.ch , dacc.ac";
   SQLString = SQLString + " from (select t_chapter  ch,t_account_minus ac from daccratediff_dbt ";
   SQLString = SQLString +          " union select t_chapter  ch, t_account_plus ac from daccratediff_dbt) dacc,";
   SQLString = SQLString +     " (select t_Account_Payer acc  from dovest_carry_log_tmp ";
   SQLString = SQLString +          " union select t_Account_Receiver from dovest_carry_log_tmp) dtmp ";
   SQLString = SQLString + " where dacc.ac = dtmp.acc";
   if( Chapter != 0 )
   SQLString = SQLString + "    and  dacc.ch = " + string(Chapter) ;
   end;
   SQLString = SQLString + " ORDER BY ch, ac ";

   RegDatesRS = _GetRegDatesRS();
   
   while((RegDate <= {BranchCurDate}) and (RegDatesRS.moveNext))

     rs = RsdRecordset( SQLString );

     RegDate = date(RegDatesRS.value(0));
     OrderNumber     = 1;
     PreviousChapter = 0;

     while( rs.moveNext() )
      
       ProcessAccount( OnLine, 0, int(rs.value("ch")), rs.value("ac"), RegDate, DprtID, Result_Carry, RestTypeString );

       ProcessAccount( OnLine, 1, int(rs.value("ch")), rs.value("ac"), RegDate, DprtID, Result_Carry, RestTypeString );
      
     end;
      
     OrderNumber = 1;

   end;

end;

macro ReportMO_SQL( OnLine : bool, Chapter : integer, RegDate : date, RestIn : bool, RestOut : bool, DprtID : integer )

   var SQLString : string;
   var rs : RsdRecordset;
   var err : integer;

   GetRegistryValue( "COMMON\\СВОДНЫЙ МО\\СТРОК В ТАБЛИЦЕ", V_INTEGER, MaxLinesMO, err );
   if ( err != 0 ) MaxLinesMO = 25; end;

   var out_rest_only = false;
   GetRegistryValue( "CB\\CONSENTACNT\\OUT_REST_ONLY", V_BOOL, out_rest_only, err );
   if ( err != 0 ) out_rest_only = false; end;

   IsEmptyReport = true;
   
   if( RestIn )
  
     procReportMO( OnLine, Chapter, RegDate, DprtID, DELTARATE, "входящего" );
    
   end;

   if( RestOut )
     
     var Result_Carry = REGRESTOUTCARRY;
     
     if( out_rest_only )
       Result_Carry = DELTARATE;
     end;
  
     procReportMO( OnLine, Chapter, RegDate, DprtID, Result_Carry, "исходящего" );
    
   end;

   if( IsEmptyReport )
     println("Нет данных для отчета");
   end;

end;
