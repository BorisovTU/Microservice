/******************************************************************************/
/* Печать сводного мемориального ордера по мультивалютной проводке            */
/******************************************************************************/
FILE accountR (account);
FILE accountC ("account$");
FILE mcar (multycar) key 1;
FILE clnt (party);

IMPORT prpm;

VAR FirstTime = true, l_res = 0;

/******************************************************************************/
/* Получить наименование поставщика/получателя по счету и валюте              */
/******************************************************************************/
MACRO getClientName( p_account, p_fiid )
 var l_file;

 if( p_fiid == 0 )
  l_file = accountR;
 else
  l_file = accountC;
 end;
 l_file.Chapter       = mcar.Chapter;
 l_file.Account       = p_account;
 l_file.Code_Currency = p_fiid;
 if( not getEQ( l_file ) )
  return "";
 end;

 clnt.PartyID = l_file.Client;

 if( not getEQ( clnt ) )
  return "";
 end;
 return trim( clnt.Name );
END;

/******************************************************************************/
MACRO PutMemOrder203(NumCopy)
 array l_sumS, l_payerS, l_bankS, l_receiverS, l_groundS;
 var Amount, AmountRUB_From, AmountRUB_To,
     l_accplus, l_accminus, l_n;

 if( ( NumCopy == 0                                          ) and
     ( not getInt( NumCopy, "Введите количество копий.", 3 ) ) )
  exit(1);
 end;

 Amount = 0;

 /* Получить сумму */
 if  ( mcar.FIID_From == 0 ) Amount = mcar.Amount_From;
 elif( mcar.FIID_To   == 0 ) Amount = mcar.Amount_To;
 else convSum( Amount, mcar.Amount_From, mcar.Date, mcar.FIID_From )
 end;

 strsplit( rubtostralt( Amount ), l_sumS,  76, 76, 2 );

 /* Наименование поставщика, получателя */
 strsplit( getClientName( mcar.Account_From, mcar.FIID_From ), l_payerS,     43, 39, 4 );
 strsplit( getClientName( mcar.Account_To,   mcar.FIID_To   ), l_receiverS,  43, 39, 4 );

 /* Рублевые суммы по каждой валюте */
 AmountRUB_From = 0;
 AmountRUB_To   = 0;
 convSum( AmountRUB_From, mcar.Amount_From, mcar.Date, mcar.FIID_From );
 convSum( AmountRUB_To,   mcar.Amount_To,   mcar.Date, mcar.FIID_To );

 /* Счета курсовых разниц */
 l_accplus  = "";
 l_accminus = "";

 if  ( AmountRUB_From > AmountRUB_To )
  l_accplus  = trim( mcar.Account1 );

 elif  ( AmountRUB_From < AmountRUB_To )
  l_accminus = trim( mcar.Account2 );
 end;

 /* Название нашего банка, основание документа */
 strsplit( {Name_Bank}, l_bankS,    43, 43, 2 );
 strsplit( mcar.Ground, l_groundS,  87, 46, 3 );

 /* Печать */
 l_n = NumCopy;
 while( l_n > 0 )
 [
  ┌──────────────────────────┬─────────────────┬────────────────────────────────────────────┐
  │    МЕМОРИАЛЬНЫЙ ОРДЕР N  │ ############### │                                Форма N 203 │
  │                          └─────────────────┘                                            │
  │  #######################                                                                │
  │ ─────────────────────────                                                               │
  │         /Дата/                                                                          │
  ├──────────┬──────────────────────────────────────────────────────────────────────────────┤
  │ Сумма    │ ############################################################################ │
  │ прописью │ ############################################################################ │
  ├──────────┴──────────────────────────────────┬──────────┬────────────────────────────────┤
  │ ИНН ####################################### │  Сумма   │ ############################   │
  │ ########################################### │          │                                │
  │ ########################################### ├──────────┼────────────────────────────────┤
  │ ########################################### │  Сч.N    │ #########################      │
  │ Плательщик                                  │          │ #########################      │
  ├─────────────────────────────────────────────┼──────────┤                                │
  │ ########################################### │  БИК     │ #################              │
  │ ########################################### ├──────────┤                                │
  │ Банк плательщика                            │  Сч.N    │ #########################      │
  ├─────────────────────────────────────────────┼──────────┼────────────────────────────────┤
  │ ########################################### │  БИК     │ #################              │
  │ ########################################### ├──────────┤                                │
  │ Банк получателя                             │  Сч.N    │ #########################      │
  ├─────────────────────────────────────────────┼──────────┤                                │
  │ ИНН ####################################### │  Сч.N    │ #########################      │
  │ ########################################### │          │ #########################      │
  │ ########################################### ├──────────┼────────┬──────────┬────────────┤
  │ ########################################### │ Вид.оп.  │ ###### │Срок плат.│            │
  │                                             ├──────────┤        ├──────────┤            │
  │                                             │Назн.плат.│        │Очер.плат.│            │
  │                                             ├──────────┤        ├──────────┤            │
  │ Получатель                                  │   Код    │        │Рез.поле  │            │
  ├─────────────────────────────────────────────┴──────────┴────────┴──────────┴────────────┤
  │ Назначение платежа (содержание операции) ############################################## │
  │ ####################################################################################### │
  │ ####################################################################################### │
  │                                                                                         │
  │            Подписи                           Отметки банка                              │
  │                                                                                         │
  │                                                                                         │
  │            ───────────────────────────────                                              │
  │                                                                                         │
  │            ───────────────────────────────                                              │
  │                                                                                         │
  └─────────────────────────────────────────────────────────────────────────────────────────┘

]
 ( trim( mcar.Numb_Document ):c, mcar.Date:m:c,
   l_sumS(0),
   l_sumS(1),
   l_payerS(0), Amount:l:f,
   l_payerS(1),
   l_payerS(2), 
   l_payerS(3), trim( mcar.Account_From ),
   l_accminus,
   l_bankS(0), GetCodeBik(mcar.Date,{MFO_Bank}),
   l_bankS(1), {CORAC_Bank},
   l_bankS(0), GetCodeBik(mcar.Date,{MFO_Bank}),
   l_bankS(1), {CORAC_Bank},
   l_receiverS(0), trim( mcar.Account_To ),
   l_receiverS(1), l_accplus,
   l_receiverS(2),
   l_receiverS(3), mcar.ShifrOper, 
   l_groundS(0),
   l_groundS(1),
   l_groundS(2) );

  l_n = l_n-1;
 end;
END;

/******************************************************************************/
/* Печать формы сводного мемориального ордера по мультивалютной проводке */
/* Возвращает true в случае успешного завершения, false в случае провала */
/******************************************************************************/
MACRO PutMultyDoc203(d, NumCopy)
  ARRAY aMenu;

  if ( FirstTime )
    aMenu(0) = "   Простой   ";
    aMenu(1) = "   Сводный   ";

    l_res = 0;

    if( GetDialogFlag() )
      l_res = menu( aMenu, "Выберите форму печати мемордера", "Мемордер" );
    end;

    FirstTime = false;
  end;

  mcar.iApplicationKind = d.ConnAppKind;
  mcar.ApplicationKey   = d.ConnAppKey;

  if( l_res < 0 )
   exit(1);

  elif( l_res == 0 )
   return FALSE;
  end;

  if( getEQ( mcar ) )
   if( mcar.MethodID != 1 ) /* пока только для "Конверсионных операций" */
    return FALSE;
   end;
   PutMemOrder203( NumCopy );
  else
   msgbox( "Нет данных для печати сводного мемориального ордера|Проверьте, выполнены ли все шаги мультивалютной проводки" );
   exit(1);
  end;

  return TRUE;
END;

