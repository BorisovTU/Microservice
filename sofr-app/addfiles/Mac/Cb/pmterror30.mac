 /*
 $Name:        pmterror30.mac
 $Module: 
 $Description: Макрос шага
 */
//-----------------------------------------------------------------------------
//  Назначение  : Макрос шага
//
//  Описание    : Операция - 4001  - "Платеж"
//                Блок     - 29043 - "Проверки по ФМ"
//                Шаг      - 30    - "Отказ по ФМ"
//-----------------------------------------------------------------------------
import PaymInter, "pmfm.mac", BankInter, likepy, "pm_answerret.mac", "pmlib.mac",
       "CbForms_h.mac";

var PaymentObj : RsbPayment;

private macro GetDenialReasonPaymCateg(Payment : RsbPayment) : integer
  var Cat : RsbObjCategories = Payment.Categories;

  var attr : TRecHandler = TRecHandler("objattr.dbt" , "bank.def");
  if( Cat.GetMainAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, {curdate}, attr) == 0 )
    return attr.rec.AttrID;
  end;

  return 0;
end;

private macro GetDenialReasonUserChoice(DenialReason : @integer) : integer
  var select : string = 
  "select t_AttrID, t_Name || ' ' as t_Name "
  "  from dobjattr_dbt "
  " where t_ObjectType = 700 "
  "   and t_GroupID = 5";

  return DL_ComboBox( Select, "t_Name", "t_AttrID", null, @DenialReason );
end;

// Определяем причину отказа
private macro GetDenialReason(DenialReason : @integer) : integer

  var Cat : RsbObjCategories = PaymentObj.Categories;

  var attr : TRecHandler = TRecHandler("objattr.dbt" , "bank.def");
  if( Cat.GetMainAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, {curdate}, attr) == 0 )
    DenialReason = attr.rec.AttrID;
  else
    msgbox("Не указана причина отказа от проведения операции");
    return 1;
  end;
  
  return 0; // раз нигде не отвалилось, значит, DenialReason определился

end;

private macro FreePaymentReserve() : integer
  var stat : integer = 0;

  if(PaymentObj.PayerAccount)
    stat = PaymentObj.FreeReserve( PaymentObj.PayerAccount, 
                                   PaymentObj.Chapter, 
                                   PaymentObj.PayerFIID );
    if(stat)
      msgbox("Ошибка при освобождении резерва по платежу");
      return stat;
    end;
  end;

  if( PaymentObj.PIList( PRT_Debet ).Size )

    var PIList : TArray;

    PIList = PaymentObj.PIList( PRT_Debet ).AsTArray(stat);
    if(stat)
      msgbox("Ошибка при получении массива записей разноски по дебету");
      return stat;
    end;

    for(var AddPI, PIList)
      stat = PaymentObj.FreeReserve( AddPI.rec.Account, 
                                     AddPI.rec.Chapter, 
                                     AddPI.rec.FIID,
                                     AddPI.rec.PmAddPIID );
      if(stat)
        msgbox( "Ошибка при освобождении резерва по уточняющей записи (номер счета " + 
                AddPI.rec.Account + ")" );
        return stat;
      end;
    end;

  end;

  return stat;

end;

private macro FinishPayment() : integer
  if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  PaymentObj.PaymStatus = PM_REJECTED;
  PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_CLOSED );
  return 0;
end;

macro ExecuteStep
  var stat : integer = 0;

  // Определяем причину отказа
  var DenialReason : integer = 0;
  if(not stat)
    stat = GetDenialReason(@DenialReason);
  end;

  if(PaymentObj.PrimDocKind != WL_INDOC)
    // Если по платежу был создан резерв, удалить его
    if(not stat)
      stat = FreePaymentReserve();
    end;

    // Завершить платеж
    if(not stat)
      stat = FinishPayment();
    end;
  else
    if( not stat and УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_UNKNOWN, 
                                               OPR_PAYM_STATE, OPR_PM_ST_OPEN )
      )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;

    if( not stat and УстановитьСтатусыПлатежа( OPR_PAYM_CABS, IfThenElse(PaymentObj.endDepartment == PaymentObj.StartDepartment, OPR_PM_ST_MFR_YES,OPR_PM_ST_MFR_NO) ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    PaymentObj.CheckTerror = CHT_NOTCHECK;
    
  end;

  if(not stat and (DenialReason != ATTR_PAYMENT_FMDENYREASON_FROZENPARTY) and (DenialReason != ATTR_PAYMENT_FMDENYREASON_UNDESIRABLE_ORG) )
    
    var ErrorMessage:string;
    stat = CreateRejectOperation( PaymentObj, @ErrorMessage );

    if(stat)
      MsgBox(ErrorMessage);
    end;
  end;

  return stat;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  // проверяем, что мы на выполнении шага
  if( ( errTrn != 0 ) or ( message != 1 ) )
    return 0;
  end;                                   

  // Формирование уведомлений о результатах приема
  if ((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ))
    var DocKind, Origin;
    var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                           " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                           makeArray(SQLParam("", PaymentObj.PaymentID)));
    if (rs and rs.MoveNext())
      DocKind = rs.value(0);
      Origin = rs.value(1);
      var Narrative : string = 
        "Уведомляем об аннулировании " + 
        IfThenElse( DocKind == PSPOKIND_REQUEST, 
                    "инкассового поручения", "платежного требования" ) + ". " + 
        "Отказано в исполнении документа на основании законодательства по противодействию легализации доходов, полученных преступным путем, и финансирования терроризма.";
      var Queries : string = "InfoCode:7";
      CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
    end;
  end;

  record wlmes( wlmes ); 
  var DenialReason : integer = 0;
  if( ДокументПорожденВходящимЭСИД(PaymentObj.PaymentID) and
      InList( (DenialReason = GetDenialReasonPaymCateg(PaymentObj)), ATTR_PAYMENT_FMDENYREASON_FROZENPARTY, ATTR_PAYMENT_FMDENYREASON_UNDESIRABLE_ORG )
    )
    // Параметры создаваемого подтверждения банка

    // Связанное входящее сообщение платежа pmlink.InitialPayment
    if( not ПолучитьСообщение( getInitialPaymentID(PaymentObj.PaymentID, PMLINK_KIND_EXECORDER), OBJTYPE_PAYMENT, 1/*WLD_MES_IN*/, wlmes, NULL ) )
      ClearRecord( wlmes );
    end;

    // Результат обработки
    var ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;

    // Описание результата обработки
    var ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

    // RsbWlError
    var ErrList = RsbWlError(0);

    var wlerr_buf = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.Code         = "35";
    wlerr_buf.rec.Description  = string
      ( "поручение налогового органа № ", PaymentObj.Number, 
        " от ", PaymentObj.Date,
        " на сумму ", PaymentObj.BaseAmount, 
        " не может быть исполнено в установленный срок в  связи с",
        IfThenElse
        ( DenialReason == ATTR_PAYMENT_FMDENYREASON_FROZENPARTY,
          " применением мер по замораживанию счета (счетов) " + PaymentObj.PayerAccount +
          " клиента " + PaymentObj.PayerName + " банка (филиала банка)",
          " участием в операции нежелательной иностранной или международной неправительственной организации " +
          PaymentObj.PayerName
        )
      );

    ErrList.Insert( wlerr_buf );

    // Собственно вставка подтверждения
    if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList ) )
      msgbox( "Ошибка при вставке подтверждения банка" );
    end;
  end;

  return 0;
end;

MACRO CheckStepAction( mes:integer ):integer
  var stat = 0;

  if( mes == OP_BACKOUT_STEP )
    stat = FM_Backout_RjSrvMsg(RJSRVMSG_MSGKIND_REJ_OPER, String(PaymentObj.PaymentID:34:o), OBJTYPE_PAYMENT );

    if((stat != 0) and (stat != 4))
      var bankMsg = AL_GetErrorInfo();
      MsgBox("Ошибка отката передачи сведений об отказе от операции в ФСФМ: " + bankMsg.Message);      
    else
      stat = 0;
    end;
  end;
 
  return stat;
END;

