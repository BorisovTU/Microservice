/*
$Name: wsqueuemesandproc.mac
$Module: Ядро ГКБО
$Description: API RSL Web Sphere
*/
import BankInter, Rsd, cryptdlm, "serviceaction.mac", "FmXmlWriter.mac", "email_notifyfun.mac", "XmlValidator.mac";
import "QueueWSLib.mac", "QueueWSClass.mac", "MessageWs.mac";
//import "PersonCreateXref.mac";
// BIQ-7006 Замена PersonCreateXref на CdiCreateXref
import "CdiCreateXref.mac";

const MC_HMAC_CONTEXT = "MC_HMAC|хеш";

var cryptoAPI = RsCryptoAPI();

/*private var PersonCreateXref_req = NULL;
private var PersonCreateXref_resp = NULL;*/
private var CdiCreateXref_resp = NULL;

private macro SaveXmlBuf(bufXml)
  file OutFile("") txt write;  

  var outStr = bufXml;

  var stat = Open( OutFile, ".\\0001.xml");
  if((stat == true))
     OutFile.str = OutStr;
     insert(OutFile);

     close(OutFile);
  end;
end;

private class TrnParm
  var ReqIDTrEn;
  var ReqIDMes;
  var ParentReqIDTrEn;
  var JMSMessageID;
  var QueueID;
  var QueueIDOut;
  var DirectionAnswer;
  var JMSCorrelationID;
  var Cover;
  var ParmBO;
  var ParmBO_ID;
  var XMLTrEn;  
  var obj;
  var Code;
  var Message;
  var Err;
  var Module;  
end;

var TrnPrm = TrnParm();

// ------------------------------------------------------------------------------------------------------

private macro SendMessageWrp
(
    QueueID, 
    ReqIDMes, 
    SenderID, 
    BranchNumber, 
    JMSMessageIDInit, 
    DataType,
    RequestText,
    Status,
    Description,
    Code,
    extensions_mes,
    FromSystem
)
//begin
    SendMessageToWS(QueueID, ReqIDMes, SenderID, BranchNumber, JMSMessageIDInit, DataType,
        RequestText, Status, Description, Code, extensions_mes, FromSystem);
end;

private macro ConvertToUTF8( strconv:string ):string
    var rs;
    var i = 1;
    var newxml = "";

    while (i < StrLen(strconv))
        var params:TArray = makeArray(SQLParam("", SubStr(strconv, i, 1024)));
        rs = execSQLselect("SELECT CONVERT (?, 'UTF8') FROM DUAL;", params, FALSE);

        if(rs.moveNext())
            newxml = newxml + rs.Value(0);
        end;
        i = i + 1024;                                               
    end;

    return newxml;
end;


private macro GetExtensionsFromRequest(xmlMes:string):string
    var pos, len;
    var pos_str;
    var temp_str;
    var ext_str = " ";

    private macro GetSubStr_include(xml, fromStr, toStr)
      var str = "";
      var from = index( xml, fromStr );

      if(from>0 )
        str = substr(xml, from);

        var to = index( str, toStr );

        if(to > 0)
          str = substr(str, 1, to-1);
          str = str+toStr;
        else
          str = "";
        end;
      end;

      return str;
    end;

    pos = Index(xmlMes,"<xci0:Extention");
    if (pos > 0)
       temp_str = Substr(xmlMes,pos);
       pos_str = "1";

       while (StrLen(pos_str) > 0)

         pos_str = GetSubStr_include(temp_str, "<xci0:Extention","</xci0:Extention>");
         len = StrLen(pos_str);
         temp_str = substr ( temp_str, len+1 );
         ext_str = String(ext_str,pos_str,"\n");
         pos = Index(temp_str,"<xci0:Extention");
         temp_str = Substr(temp_str,pos);
       end;
    end;
    return ext_str;

end;

private macro GetExtentionSubStr(xml)

  var str = "";

  if(xml != null)

    var pos_start = RsRegexSearch(xml, "<\\w+:Extention");

    var pos_end = 0;

    var pos = pos_start;

    var indx = RsRegex("<\\/\\w+:Extention>");

    while ((pos = indx.indexIn(xml, pos)) != -1)
        pos = pos + indx.matchedLength();
        pos_end = pos;
    end;

    str = substr(xml, pos_start, pos_end - pos_start + 1);
  
  end;

  return str;
  
end;



private macro FindMonTrEnWS(ParentReqIDTrEn, QueueDate, QueueTime)
    var cmd = RsdCommand("select * from dmontrenws_dbt where t_ReqIDTrEn = ? and t_IsTimeOut = chr(0)");
    cmd.addParam("", RSDBP_IN, ParentReqIDTrEn);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        var timeduration = WS_GetDateTimeIntervalMsec(Date(rs.value("t_SysDate")), Time(rs.value("t_SysTime")), QueueDate, QueueTime);

        if (timeduration > rs.value("t_TimeOut"))
            var upd = RsdCommand("update dmontrenws_dbt set t_IsTimeOut = CHR(88) where t_ID = ?");
            upd.addParam("", RSDBP_IN, rs.value("t_ID"));
            upd.execute();
        end;
    end;
end;

private macro GetDataTypeWSTimeOut(Service)
    var TimeOut = 0;

    var cmd = RsdCommand("select * from ddatatypews_dbt where t_ParmIP = ?");
    cmd.addParam("", RSDBP_IN, Service);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.MoveNext())
        TimeOut = Int(rs.value("t_TimeOut"));
    end;

    return TimeOut;
end;

private macro InsertMonTrEnWS(ReqIDTrEn, TimeOut)
    var MonTrEnWS = TBfile("montrenws.dbt", "W");
    MonTrEnWS.rec.ReqIDTrEn = ReqIDTrEn;
    MonTrEnWS.rec.SysDate = Date();
    MonTrEnWS.rec.SysTime = Time();
    MonTrEnWS.rec.TimeOut = TimeOut;

    return MonTrEnWS.insert();
end;


// ------------------------------------------------------------------------------------------------------

private macro GetQueueIDOut(QueueID)

  var p = 0;

  var cmd, rs;

  var query = "select qr.t_queueidout from dqueuerelws_dbt qr where qr.t_queueidin = ?";

  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, QueueID);
  
  rs = RsdRecordset(cmd);

  if(rs.movenext)

    p = SQL_ConvType(rs.value("t_queueidout"));

  end;

  return p;

end;

private macro TrnQueueBO()

  if (not WSInsertQueueLinks(TrnPrm.ReqIDTrEn, TrnPrm.ReqIDMes, TrnPrm.ParentReqIDTrEn, TrnPrm.JMSMessageID, TrnPrm.QueueID, TYPE_QUEUE_IN))
      RunError("", "Не удалось создать связь для сообщения, транспортных конвертов");
  end;

  var ParamJMSMessageID = TrnPrm.JMSMessageID;
/* 20190114 - kva - все равно не заполняется, JMSMessageID вполне можно использовать *//*
  if (TrnPrm.Cover.Direction == TrnPrm.DirectionAnswer)
      ParamJMSMessageID = TrnPrm.JMSCorrelationID;
  end;
*/
  TrnPrm.obj = ExecMacroFile("QueueBO.mac", TrnPrm.ParmBO,
          TrnPrm.ReqIDMes,
          TrnPrm.Cover.SenderID,
          TrnPrm.Cover.ToBranch,
          TrnPrm.ParmBO_ID,
          TrnPrm.Cover.Direction,
          ParamJMSMessageID,
          TrnPrm.Cover.XMLMes,
          TrnPrm.Cover.Status,
          TrnPrm.Cover.Code,
          TrnPrm.Cover.Description 
      );
   // ошибка, откат транзакции
   if (TrnPrm.obj.ResultCode == -101)
     TrnPrm.obj.ResultCode = 0;
     RunError("", "-101");
   end;


OnError( err )
  TrnPrm.Code = err.Code;
  TrnPrm.Message = err.Message;
  TrnPrm.Err = err.Err;
  TrnPrm.Module = err.Module;
   AbortTrn();
end;

private macro CryptXmlCheckBufferSignWS_JCP(xml, err)

   var cmd = RsdCommand("select t_keyinformation from dkeyinfows_dbt where t_iskeyactive = 'X' order by t_id desc");
   var pp = RsdRecordSet(cmd);

   cmd.execute();
   if( pp.moveNext())
      var jvm = createObject( "rsjvm", "TJavaHost" );   
      var hcp = jvm.CreateJavaObject( "ru.softlab.hmac.HmacCP" );
      hcp.setEncryptedKey(pp.value("t_keyinformation"));
      var res = hcp.verify(xml);
      return res;
   else
      RunError("Ошибка: нет активных ключей.");
   end;

OnError(error)
   err = error;

end;

macro GetQueueMesAndProcessBody
(
    QueueID,          // ИД очереди
    JMSMessageID,     // ИД транспортного конверта в Web Sphere
    JMSCorrelationID, // ИД транспортного конверта в Web Sphere для ответного сообщения
    XMLTrEn           // XML транспортного конверта
)
// begin
    var result = true;
    
    var QueueIDOut = GetQueueIDOut(QueueID);
    if (QueueIDOut == 0)
        RunError("", "Не задана связанная исходящая очередь");
    end;
    
    var ReqIDTrEn = CreateGUID();
    var TypeMsg = 0;
    var text = "", errorstring = "";
    var ReqIDMes;
    var ParentReqIDTrEn;
    WriteXrLogQueueWS(SRV_XR_KIND_IN, ReqIDTrEn, XMLTrEn, "");
    
    var QueueDate = Date();
    var QueueTime = Time();

    GetRegistryValue("COMMON\\QUEUE\\ТИП КОНВЕРТА", V_INTEGER, TypeMsg);

    if ((TypeMsg != 1) and (TypeMsg != 2))
        RunError("", "Не корректное значение настройки \"COMMON\\QUEUE\\ТИП КОНВЕРТА\"");
    end;

    var xml = CreateXMLParser();
    xml.validateOnParse = false;
    xml.async = false;
    var stat = xml.loadXML(XMLTrEn);

    if ( not stat )
        RunError("", "Ошибка создания экземпляра msxml");
    end;

    var Cover = WSReadCover(xml, TypeMsg);
    var Direction = "";
    if ((ValType(JMSCorrelationID) != V_UNDEF) and (JMSCorrelationID != ""))
        Direction = Cover.Direction;

        if (Direction == DirectionAnswer)
            var QueueLinks = TBfile("QueueLinks.dbt", "R", 3);
            QueueLinks.rec.JMSMessageID = JMSCorrelationID;
            QueueLinks.rec.QueueID = QueueIDOut;
            QueueLinks.rec.Type = TYPE_QUEUE_OUT;

            if (not QueueLinks.getEq())
                RunError("", "Не найдена связь сообщений, транспортных конвертов");
            end;

            ParentReqIDTrEn = QueueLinks.rec.ReqIDTrEn;

            FindMonTrEnWS(ParentReqIDTrEn, QueueDate, QueueTime);
        elif (Direction == DirectionRequest)
            var Service = Cover.Service;
            var TimeOut = GetDataTypeWSTimeOut(Service);

            if (not InsertMonTrEnWS(ReqIDTrEn, TimeOut))
                RunError("", "Ошибка вставки транспортного конверта в ожидающие ответа");
            end;
        else
            RunError("", "Не корректный транспортный конверт");
        end;
    end;

    // chesnokov 31.01.2022 MonTrEnWSTimeOutProc();

    // 13/06/2021 устарело, не используется
    // var extensions_mes = GetExtensionsFromRequest(xml.xml); // используется при любом вызове SendMessageWrp, включая сообщение об ошибке
    // универсальный код под любой префикс
    //var extensions_mes = GetExtentionSubStr(xml.xml);
    // DEF-13631 когда во входящем блоке описание xmlns не находится внутри тега Extention, код перестает быть универсальным, нужен заголовок

    var ParmBO_ID = 0;
    var ParmBO = GetQueueBO(Cover.Service, @ParmBO_ID);
    if (ParmBO == "")
        RunError("", "Не корректно заданы параметры сервиса БЭК-офиса");
    end;

// maa   Cover.HasMAC_info = false;
    if (Cover.HasMAC_info)
        var errcode = 0;

        //var XMLTrEnUTF8 = OemToUtf8(XMLTrEn);
        var XMLTrEnUTF8 = toAnsi(XMLTrEn);
        var err = null;
        //result = CB_CryptXmlCheckBufferSignWS(XMLTrEnUTF8, @errcode, @errorstring);
        CryptXmlCheckBufferSignWS_JCP(XMLTrEnUTF8, @err); 
        if (err != null)
           // обработка ошибок
           RunError(err.Message, err);
           errcode = err.Code;
           result = err.Code;
           errorstring = err.Message;
        end;

        if (not result)
            text = "Не прошла проверка метки целостности сообщения " + ReqIDTrEn;
            text = text + ". " + errorstring;
            SendEmail(E_MailTitle, text);
            SendMessageWrp(QueueIDOut, null, Cover.SenderID, Cover.FromBranch, JMSMessageID, ParmBO_ID, null, 
                "Error", text, 1006, Cover.Extention, Cover.FromSystem );
            RunError("", text);
        else
            WS_WriteAuditMessage(errorstring);
        end;
    end;

    if (not WSValidateMessageByXSD(Cover.XMLMes, Cover.Service, @errorstring))
        SendMessageWrp(QueueIDOut, null, Cover.SenderID, Cover.FromBranch, JMSMessageID, ParmBO_ID, null, 
                    "Error", errorstring, 1001, Cover.Extention, Cover.FromSystem);
        RunError("", errorstring);
    end;

    ReqIDMes = CreateGUID();
    WriteXrLogQueueWS(SRV_XR_KIND_OUT, ReqIDMes, XMLTrEn, "");
    
    TrnPrm.ReqIDTrEn        = ReqIDTrEn;
    TrnPrm.ReqIDMes         = ReqIDMes;
    TrnPrm.ParentReqIDTrEn  = ParentReqIDTrEn;
    TrnPrm.JMSMessageID     = JMSMessageID;
    TrnPrm.QueueID          = QueueID;
    TrnPrm.QueueIDOut       = QueueIDOut;
    TrnPrm.DirectionAnswer  = DirectionAnswer;
    TrnPrm.JMSCorrelationID = JMSCorrelationID;
    TrnPrm.Cover            = Cover;
    TrnPrm.ParmBO           = ParmBO;
    TrnPrm.ParmBO_ID        = ParmBO_ID;
    TrnPrm.XMLTrEn          = XMLTrEn;

    if( ProcessTrn(NULL,"TrnQueueBO") != true )
      if (TrnPrm.Err == "-101")  // запланированная ошибка с сформированным ответом
         TrnPrm.Code = 0;
         TrnPrm.Message = "";
         TrnPrm.Err = "";
         TrnPrm.Module = "";
      else

         var str = TrnPrm.Code + ":" + TrnPrm.Message;
       
         if (ValType(TrnPrm.Err) != V_UNDEF)
           str = str + " " + TrnPrm.Err;
         end;
         
         str = str + " " + TrnPrm.Module;
         
         RunError("", str);
      end;
    else

      //создание ссылки в CIF
      if ( (ValType(TrnPrm.obj.objectid) != V_UNDEF) and (TrnPrm.obj.objectid > 0) )
         // BIQ-7006 Замена PersonCreateXref на CdiCreateXref
         if (TrnPrm.obj.servicename == "PersonCreateXref")  // просто идентификатор, не будем менять
          /*  PersonCreateXref_req = c_PersonCreateXrefRequest;
            PersonCreateXref_req.MasterExternalId = TrnPrm.obj.MasterExternalId;
            if (ValType(PersonCreateXref_req) != V_UNDEF)
               PersonCreateXref_req.OperationalExternalId = c_IntegrationSymbolicIdentifierXType;
               PersonCreateXref_req.OperationalExternalId.ObjectId = String(TrnPrm.obj.objectid);
               PersonCreateXref_req.OperationalExternalId.SystemId = "SOFR";
               PersonCreateXref_req.OperationalExternalId.SystemNodeId = "SOFR";

               PersonCreateXref_resp = PersonCreateXref(PersonCreateXref_req, NULL, JMSMessageID);
            end; */
            CdiCreateXref_resp = CdiCreateXref (TrnPrm.obj.objectid, TrnPrm.obj.MasterExternalId, JMSMessageID );
         end;
      end;
      //отправка запроса на биржу
      if (ValType(TrnPrm.obj.dlcontrid) != V_UNDEF)
         ExecMacroFile("func_lib.mac", "InsertEventForMoex", TrnPrm.obj.dlcontrid );
      end;
    end;

    if(TrnPrm.obj != V_UNDEF)
      if (TrnPrm.obj.ResultCode != 0)
          SendEmail(E_MailTitle, TrnPrm.obj.ResultText);
          WriteXrLogQueueWS(SRV_XR_KIND_OUT, TrnPrm.ReqIDMes, TrnPrm.XMLTrEn, TrnPrm.obj.ResultCode);
          SendMessageWrp(TrnPrm.QueueIDOut, TrnPrm.ReqIDMes, TrnPrm.Cover.SenderID, TrnPrm.Cover.FromBranch, TrnPrm.JMSMessageID, TrnPrm.ParmBO_ID, null,
                      "Error", TrnPrm.obj.ResultText, TrnPrm.obj.ResultCode, Cover.Extention, Cover.FromSystem);
          RunError("", TrnPrm.obj.ResultText);
      else
          if ((TrnPrm.obj.ResponseText != "") and (ValType(TrnPrm.obj.ResponseText) != V_UNDEF))
              SendMessageWrp(TrnPrm.QueueIDOut, TrnPrm.ReqIDMes, TrnPrm.Cover.SenderID, TrnPrm.Cover.FromBranch, TrnPrm.JMSMessageID, TrnPrm.ParmBO_ID, TrnPrm.obj.ResponseText,
                      "Success", "", TrnPrm.obj.ResultCode, Cover.Extention, Cover.FromSystem);
          end;
      end;
    end;

    return result;
OnError(error)
    if (error.Err != "")
        if (ValType(error.Err) != V_UNDEF)
            SendEmail(E_MailTitle, error.Err);
            WS_WriteAuditMessage(error.Err);
        else
            SendEmail(E_MailTitle, error.Message);
            WS_WriteAuditMessage(error.Message);
        end;
    else
        SendEmail(E_MailTitle, error.Message);
        WS_WriteAuditMessage(error.Message);
    end;
    return false;
end;