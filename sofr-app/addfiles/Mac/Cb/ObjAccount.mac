/*
$Name:         ObjAccount.mac
$Module:       Главная книга
$Description:  
*/

Import CTInter, BankInter, oralib;

record AccountRec( "account.dbt" );

private class(ObjectFieldUse) AccountFieldUse(ObjType)
  InitObjectFieldUse(ObjType);

    macro CheckValueMask(Code, strValue)
     var retval = true;

     var Len = StrLen(strValue);

     var i = 1;
     var Char = "";

     if(Len > 0)
       while( i <= Len )
         Char = substr(strValue, i, 1);
             
         if(Index("0123456789*?-", Char) == 0)
           retval = false;
         end;
         i = i+1;
       end;
     end;

     return retval;
    end;

    macro CheckValueBal(Code, strValue)
     var retval = true;

     var Len = StrLen(strValue);

     var i = 1;
     var Char = "";

     if(Len > 5)
       retval = false;
     elif(Len > 0)
       while( i <= Len )
         Char = substr(strValue, i, 1);
             
         if(Index("0123456789*?-", Char) == 0)
           retval = false;
         end;
         i = i+1;
       end;
     end;

     return retval;
    end;
    
    
    macro checkConditionObject (Code, Operator, strValue)
      var ret = true;
      var tmpval;

   /*1 Если Code = "СчНомер", то выполняется сравнение с номером счета accounts.Account на соответствие заданной подстроке Value. Учесть, что в подстроке Value могут быть символы-заместители '?' и '*'.*/
      if(Code == "СчНомер")
        tmpval = CompareStrWithMasks(strValue, AccountRec.Account);
        if(tmpval == 0)
          ret = true;
        else
          ret = false;
        end;  
      end;

   /*2 Если Code = "СчБаланс", то выполняется сравнение с номером счета accounts.Balance на соответствие заданному значению Value.  
       2.1 Если Operator = '=', то условие верно, если accounts.Balance=Value. 
       2.2 Если Operator = '!', то условие верно, если accounts.Balance!=Value. 
       2.3 Если Operator = '*', то условие верно, если в accounts.Balance входит подстрока Value. При этом учесть, что в подстроке Value могут быть символы-заместители '?' и '*'.
   */

      if(Code == "СчБаланс")
        if(Operator == "=")
          if(strValue == AccountRec.Balance)
            ret = true;
          else
            ret = false;
          end;  
        elif(Operator == "!")
          if(strValue != AccountRec.Balance)
            ret = true;
          else
            ret = false;
          end;  
        elif(Operator == "*")
          tmpval = CompareStrWithMasks(strValue, AccountRec.Balance);
          if(tmpval == 0)
            ret = true;
          else
            ret = false;
          end;  
        end;
      end;

   /*3 Если Code = "СчВид", то выполняется сравнение вида счета accounts.Kind_Account с Value с помощью оператора Operator. */

      if(Code == "СчВид")
        if(Operator == "=")
          if(strValue == AccountRec.Kind_Account)
            ret = true;
          else
            ret = false;
          end;  
        elif(Operator == "!")
          if(strValue != AccountRec.Kind_Account)
            ret = true;
          else
            ret = false;
          end;  
        end;
      end;

   /*4 Если Code = "СчТип", то выполняется проверка строки accounts.Type_Account на вхождение/отсутствие в нее/ней значения Value в зависимости от оператора Operator. Т.е. проверяем есть ли указанный тип среди типов счета. */
      if(Code == "СчТип")
        tmpval = Index ( AccountRec.Type_Account, strValue );
        if(Operator == "=")
          if(tmpval > 0)
            ret = true;
          else
            ret = false;
          end;  
        elif(Operator == "!")
          if(tmpval == 0)
            ret = true;
          else
            ret = false;
          end;  
        end;
      end;

   /*5 Если Code = "СчТипП", то выполняется проверка строки accounts.UserTypeAccount также как и для "СчТип". */
      if(Code == "СчТипП")
        tmpval = Index ( AccountRec.UserTypeAccount, strValue );
        if(Operator == "=")
          if(tmpval > 0)
            ret = true;
          else
            ret = false;
          end;  
        elif(Operator == "!")
          if(tmpval == 0)
            ret = true;
          else
            ret = false;
          end;  
        end;
      end;


      return ret;
    end;


end; /* end of class PartyFieldUse */

private var FltrAccObj;

PRIVATE MACRO GetTypeAcList( iNumType1,  iNumType2 )

  var arr = TArray;
  var query:string =  " SELECT T_TYPE_ACCOUNT "  +
                        " FROM dtypeac_dbt " +
                       " WHERE t_inumtype = " + iNumType1 ;

  if(iNumType2 > 0)
    query = query + " OR t_inumtype = " + iNumType2  ; 
  end;

                              
  var rs:RsdRecordset = execSQLselect( query, null, true );
  var i = 0;

  while( rs and rs.moveNext() )
    arr[i] = string( rs.value(0) );
    i = i+1;
  end;

  return arr;
END;

macro Init(ObjectType)

 var arrOpr = TArray;
 FltrAccObj = AccountFieldUse(ObjectType);

 FltrAccObj.addCondition( "Маска номера счета" , 
                            "СчНомер",
                            V_STRING,
                            "*",
                            UFF_METHOD_INPUT_EDIT,
                            NULL,
                            "CheckValueMask"
 );

 FltrAccObj.addCondition( "Балансовый счет" , 
                            "СчБаланс",
                            V_STRING,
                            "=!*",
                            UFF_METHOD_INPUT_EDIT,
                            NULL,
                            "CheckValueBal"
 );

 arrOpr = TArray;
 arrOpr[0] = "П";
 arrOpr[1] = "А" ;
 arrOpr[2] = "АП";

 FltrAccObj.addCondition( "Вид счета" , 
                            "СчВид",
                            V_STRING,
                            "=!",
                            UFF_METHOD_INPUT_LIST,
                            arrOpr,
                            NULL
 );


 arrOpr = GetTypeAcList( 1, 0 );


 FltrAccObj.addCondition( "Тип счета" , 
                            "СчТип",
                            V_STRING,
                            "=!",
                            UFF_METHOD_INPUT_LIST,
                            arrOpr,
                            NULL
 );
 
  
 arrOpr = GetTypeAcList( 2, 5 );


 FltrAccObj.addCondition( "Польз. тип счета" , 
                            "СчТипП",
                            V_STRING,
                            "=!",
                            UFF_METHOD_INPUT_LIST,
                            arrOpr,
                            NULL
 );
 

 return FltrAccObj;
end; 

macro PropExist(obj/* , ... string */) : bool
  var parm:variant, i:integer = 1, sub = obj, ret = obj != null;  
  while( ret AND GetParm(i, parm)  )    
    if( (valtype(parm) == V_STRING) AND (GenPropID(sub, parm) != -1) )
      sub = GenGetProp(sub, parm);
      ret = true;
    else
      ret = false;
    end;    
    i = i + 1;
  end;
  return ret;
end;

// ObjData - TWebAccountObject
macro InitObject(ObjData) 
  
  if(PropExist(ObjData, "Account") )
    AccountRec.Account = ObjData.Account;
  end;
  if(PropExist(objData, "Balance", "Balance") )
    AccountRec.Balance = ObjData.Balance.Balance;
  end;
  if(PropExist(ObjData, "Kind_Account") )
    AccountRec.Kind_Account = ObjData.Kind_Account;
  end;
  if(PropExist(ObjData, "Type_Account") )
    AccountRec.Type_Account = ObjData.Type_Account;
  end;
  if(PropExist(ObjData, "UserTypeAccount") )
    AccountRec.UserTypeAccount = ObjData.UserTypeAccount;
  end;
  
end;


