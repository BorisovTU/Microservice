//------------------------------------------------------------------------------
// Класс свойства платежа по дебету или по кредиту.
//
// Позволяет одинаково обращаться к разноименным полям, имеющим одинаковое значение
// с разных сторон платежа (например FIID, PayFIID).
// Автоматически учитывает итрости вроде "перевернутых" в дебетовых 
// платежах *MesBankID.
//
//------------------------------------------------------------------------------
//
// CLASS TPaymentProps
// MACRO MakePaymentProps
//
//------------------------------------------------------------------------------

import globals, PaymInter, PTInter, OprInter, oralib, "bnk_ptlib.mac";

//------------------------------------------------------------------------------
// платеж является требованием?
//------------------------------------------------------------------------------
MACRO PM_IsClaim( pm, db, cr ):bool
  if( pm.DocKind==DLDOC_BANKCLAIM )
    return true;
  elif( (pm.DocKind==DLDOC_BANKPAYMENT) OR
        (pm.DocKind==PS_PAYORDER      ) OR
        (pm.DocKind==PS_CPORDER       ) OR
        (pm.DocKind==BBANK_CPORDER    ) OR
        (pm.DocKind==PS_INRQ          ) )
    return false;
  else
    return ( ( db.IsSender != "X" ) OR ( cr.IsSender == "X" ) );
  end;
end;

//------------------------------------------------------------------------------
// Платеж является требованием? (RsbPayment edition)
//------------------------------------------------------------------------------
MACRO PM_IsClaim2( pm:RsbPayment ):bool
  if( pm.DocKind==DLDOC_BANKCLAIM )
    return true;
  elif( (pm.DocKind==DLDOC_BANKPAYMENT) OR
        (pm.DocKind==PS_PAYORDER      ) OR
        (pm.DocKind==PS_CPORDER       ) OR
        (pm.DocKind==BBANK_CPORDER    ) OR
        (pm.DocKind==PS_INRQ          ) )
    return false;
  else
    return ( ( pm.PayerIsSender != "X" ) OR ( pm.ReceiverIsSender == "X" ) );
  end;
end;

//------------------------------------------------------------------------------
// Банк в нашей территориальной структуре?
//------------------------------------------------------------------------------
PRIVATE MACRO IsBankInTS( BankID:integer ):bool
  var select:string = "select 1 "
                    + "from ddp_dep_dbt dp "
                    + "where dp.t_PartyID = :BankID "
                    +  " and dp.t_status <> 3";
  var params:TArray = TArray();
  params[params.size] = SQLParam( "BankID" , BankID );
  return existsSQLselect( select, params );
END;

//------------------------------------------------------------------------------
// Класс свойства платежа по дебету или по кредиту.
// Параметры конструктора:
//  _pm, _rm, _db, _cr - буферы (RECORD) платежа
//  _side              - сторона платежа (PRT_Debet, PRT_Credit)
//------------------------------------------------------------------------------

CLASS TPaymentProps( _pm, _rm, _db, _cr, _side )

  private var pm   = _pm,   //буфер платежа
              rm   = _rm,   //буфер св-ва R-макета
              db   = _db,   //дебетовое свойство
              cr   = _cr,   //кредитовое свойство
              side = _side, //сторона платежа
              m_BankIsSB:variant;   //банк является Сбербанком?

  //Получить свойство платежа
  PRIVATE MACRO Pmprop( )
    if( side == PRT_Debet )
      return db;
    else
      return cr;
    end;
  END;

  //Получить "противоположное" свойство платежа
  PRIVATE MACRO OtherPmprop( )
    if( side == PRT_Debet )
      return cr;
    else
      return db;
    end;
  END;

  //Получить идентификатор банка
  MACRO BankID( ):integer
    if( side == PRT_Debet )
      return pm.PayerBankID;
    else
      return pm.ReceiverBankID;
    end;
  END;

  //Банк является Сбербанком?
  MACRO BankIsSBRF():bool
    if( m_BankIsSB == NULL )
      m_BankIsSB = BankIsSB( BankID() );
    end;
    return m_BankIsSB;
  END;

  //Доступ к идентификатору участника расчетов
  MACRO MesBankID( val:variant ):integer
    var claim:bool = PM_IsClaim( pm, db, cr );
    if( ((side == PRT_Debet ) AND (not claim)) OR
        ((side == PRT_Credit) AND claim      ) )
      if( ValType(val) != V_UNDEF )
        pm.PayerMesBankID = val;
      end;
      return pm.PayerMesBankID;
    else
      if( ValType(val) != V_UNDEF )
        pm.ReceiverMesBankID = val;
      end;
      return pm.ReceiverMesBankID;
    end;
  END;

  //Получить вид кода банка
  MACRO BankCodeKind( ):integer
    return Pmprop().CodeKind;
  END;

  //Получить код банка
  MACRO BankCode( ):string
    return Pmprop().BankCode;
  END;

  //Получить идентификатору клиента
  MACRO ClientID( ):integer
    if( side == PRT_Debet )
      return pm.Payer;
    else
      return pm.Receiver;
    end;
  END;

  //Доступ к названию клиента
  MACRO ClientName( val:variant ):string
    if( side == PRT_Debet )
      if( ValType(val) != V_UNDEF )
        rm.PayerName = val;
      end;
      return rm.PayerName;
    else
      if( ValType(val) != V_UNDEF )
        rm.ReceiverName = val;
      end;
      return rm.ReceiverName;
    end;
  END;

  //Получить номер счета
  MACRO Account( ):string
    if( side == PRT_Debet )
      return pm.PayerAccount;
    else
      return pm.ReceiverAccount;
    end;
  END;

  //Получить валюту счета
  MACRO FIID( ):integer
    if( side == PRT_Debet )
      return pm.FIID;
    else
      return pm.PayFIID;
    end;
  END;

  // Получить группу платежа
  MACRO Group():integer
    return Pmprop().Group;
  END;
  
  // Получить признак отправителя
  MACRO IsSender():bool
    return Pmprop().IsSender == "X";
  END;
  
  // Получить транспорт
  MACRO TpID():integer
    return Pmprop().TpID;
  END;

  //Получить коррсхему
  MACRO CorschemNum():integer
    return Pmprop().Corschem;
  END;

  //Получить банка-корреспондента по коррсхеме
  MACRO CorrBankID():integer
    var select:string;
    var params:TArray;
    var rset:RsdRecordset;
    if( CorschemNum() != -1 )
      select = "select t_CorrID "                 
             + "from dcorschem_dbt cs "
             + "where cs.t_Number = :Corschem and "
             + "cs.t_FI_Kind = 1 and "
             + "cs.t_FIID = :FIID";
      params = TArray();
      params[0] = SQLParam( "Corschem", CorschemNum() );
      params[1] = SQLParam( "FIID"    , FIID()        );
      rset = execSQLselect( select, params, false );
      if( rset AND rset.moveNext() )
        return rset.Value(0);
      end;    
    end;
    return -1;
  END;
  
  // Получить ID банка-держателя счета
  MACRO AccountOwnerID():integer
    if( Group()==PAYMENTS_GROUP_EXTERNAL )
      if( MesBankID() and BankIsSBRF() )
        return MesBankID();
      else
        return BankID();
      end;
    else
      return BankID();
    end;
  END;

  // Получить ID банка- владельца кода
  MACRO CodeOwnerID():integer
    var CodeKind:integer = 0;
    if( Group()==PAYMENTS_GROUP_EXTERNAL )
      return BankID();
    else
      CodeKind = BankCodeKind();
      if( not CodeKind )
        if( FIID() != 0 /*NATCUR*/ )
          CodeKind = OtherPmprop().CodeKind;
        end;
        if( not CodeKind )
          CodeKind = PTCK_BIC;
        end;
      end;
      return GetCodeOwnerID( CodeKind, BankCode() );
    end;
  END;

END;

//------------------------------------------------------------------------------
// Заполнить буферы платежа по объекту
//------------------------------------------------------------------------------
MACRO PM_FillBuffersByPayment( payment:RsbPayment, pm, rm, db, cr )

  ClearRecord( pm );
  ClearRecord( rm );
  ClearRecord( db );
  ClearRecord( cr );

  pm.PaymentID         = payment.PaymentID;
  pm.DocKind           = payment.DocKind;
  pm.PayerBankID       = payment.PayerBankID;
  pm.ReceiverBankID    = payment.ReceiverBankID;
  pm.PayerMesBankID    = payment.PayerMesBankID;
  pm.ReceiverMesBankID = payment.ReceiverMesBankID;
  pm.Payer             = payment.Payer;
  pm.Receiver          = payment.Receiver;
  pm.PayerAccount      = payment.PayerAccount;
  pm.ReceiverAccount   = payment.ReceiverAccount;
  pm.FIID              = payment.PayerFIID;
  pm.PayFIID           = payment.ReceiverFIID;
  pm.Amount            = payment.PayerAmount;
  pm.PayAmount         = payment.ReceiverAmount;
  pm.Department        = payment.Department;
  pm.StartDepartment   = payment.StartDepartment;
  pm.EndDepartment     = payment.EndDepartment;
  pm.DocKind           = payment.DocKind;

  rm.PaymentID    = payment.PaymentID;
  rm.PayerName    = payment.PayerName;
  rm.ReceiverName = payment.ReceiverName;

  db.PaymentID   = payment.PaymentID;
  db.DebetCredit = PRT_Debet;
  db.Group       = payment.PayerGroup;
  db.IsSender    = payment.PayerIsSender;
  db.CodeKind    = payment.PayerCodeKind;
  db.BankCode    = payment.PayerBankCode;

  cr.PaymentID   = payment.PaymentID;
  cr.DebetCredit = PRT_Credit;
  cr.Group       = payment.ReceiverGroup;
  cr.IsSender    = payment.ReceiverIsSender;
  cr.CodeKind    = payment.ReceiverCodeKind;
  cr.BankCode    = payment.ReceiverBankCode;

  if( payment.PayerIsSender )
    db.TpID     = payment.InTransport;
    db.Corschem = payment.InCorschem;
    cr.TpID     = payment.OutTransport;
    cr.Corschem = payment.OutCorschem;
  else
    db.TpID     = payment.OutTransport;
    db.Corschem = payment.OutCorschem;
    cr.TpID     = payment.InTransport;
    cr.Corschem = payment.InCorschem;
  end;

END;

//------------------------------------------------------------------------------
// Создать объект свойства платежа по объекту платежа
//  Payment        - объект обрабатываемого платежа
//  Side           - сторона платежа (PRT_Debet, PRT_Credit)
//  pm, rm, db, cr - буферы для хранения данных платежа 
//                   (пришлось передавать, а не объявлять здесь, т.к. иначе RSL падает)
//------------------------------------------------------------------------------
MACRO MakePaymentProps( Payment:RsbPayment, Side:integer, pm, rm, db, cr ):TPaymentProps
  PM_FillBuffersByPayment( Payment, pm, rm, db, cr );
  return TPaymentProps( pm, rm, db, cr, Side );
END;
