

VAR WorkUpgrader = false, /* При вызове из RS-Bank надо приравнять переменную false */
    dist_group: TBFile,
    cln_group: TBFile,
    dist_period: TBFile,
    cln_period: TBFile,
    cln_accdoc: TBFile,
    dist_categ: TBFile,
    cln_categ: TBFile,
    dist_templ: TBFile,
    cln_templ: TBFile,
    dist_doc: TBFile,
    cln_doc: TBFile;
const MAX_SYST_MCGROUP = 500;
const MAX_SYST_MCCATEG_GROUP = 500;
const MAX_SYST_MCCATEG_CATEG = 5000;
const SET_CHAR = "X";
const opInsert = 0,
      opUpdate = 1,
      opDelete = 2;
const MCCHARACT_DEAL = 31,
      MCCHARACT_FIID = 0,
      MCCHARACT_OWNER = 1,
      MCCHARACT_PLACE = 2,
      MCCHARACT_ISSUER = 3,
      MCCHARACT_CENTER = 7,
      MCCHARACT_FIID2 = 8,
      MCCHARACT_CONTR_CLIENT = 9,
      MCCHARACT_CONTR_BANK = 10,
      MCCHARACT_MARKET_PLACE = 11,
      MCCHARACT_INDEXDATE = 12,
      MCCHARACT_MARKET_PLACE_OFFICE = 13,
      MCCHARACT_CONTRACTOR = 14;

MACRO MakeBinary(intValue, BinValue)
  var i = 0;
  while ((i < 32))
    if (intValue > 0)
      BinValue[i] = mod(intValue, 2);
      intValue = (intValue - BinValue[i])/2;
    else
      BinValue[i] = 0;
    end;
    i = i + 1;
  end;
END;
MACRO OperateFile(F, operation, message)
  if (operation == opInsert)
    if (WorkUpgrader)
      if (Insert(F))
        println("Добавлена запись в " + F.TblName + "|" + message);
      else
        println("Ошибка добавления записи в " + F.TblName + "|" + message);
        return false;
      end;
    else
      println("Будет добавлена запись в " + F.TblName + "|" + message);
    end;
  elif (operation == opUpdate)
    if (WorkUpgrader)
      if (Update(F))
        println("Обновлена запись в " + F.TblName + "|" + message);
      else
        println("Ошибка обновления записи в " + F.TblName + "|" + message);
        return false;
      end;
    else
      println("Будет обновлена запись в " + F.TblName + "|" + message);
    end;
  elif (operation == opDelete)
    if (WorkUpgrader)
      if (Delete(F))
        println("Удалена запись в " + F.TblName + "|" + message);
      else
        println("Ошибка удаления записи в " + F.TblName + "|" + message);
        return false;
      end;
    else
      println("Будет удалена запись в " + F.TblName + "|" + message);
    end;
  end;
  
  return true;
END;

MACRO UpdateUpgrade(F)
  if ((F.rec.Upgrade != SET_CHAR) AND WorkUpgrader)
    F.rec.Upgrade = SET_CHAR;
    return Update(F);
  end;
  return true;
END;

MACRO CompareFiles(OldF, NewF, startPos, extPos)
  var nRec, i;
  var cmp = false;
  nRec = FldNumber(NewF);
  if (ValType(startPos) != V_INTEGER)
    startPos = 0;
  end;
  if (ValType(extPos) != V_INTEGER)
    extPos = -1;
  end;
  i = startPos;
  while(i < nRec)
    if((i != extPos) AND 
       (FldName(OldF, i) != "Upgrade") AND
       (OldF.Item(i) != NewF.Item(i)))
      return true;
    end;
    i = i + 1;
  end;
  return cmp;
END;

MACRO AddGroupAndPeriods()
  VAR ok;
  copy(cln_group, dist_group);
  cln_group.rec.Upgrade = SET_CHAR;
  if (not OperateFile(cln_group, opInsert, "Номер группы: " + dist_group.rec.Number))
    return false;
  end;

  dist_period.KeyNum = 3;
  dist_period.rec.GroupNum = dist_group.rec.Number;
  dist_period.rec.Number = 0;
  ok = GetGE(dist_period) AND (dist_period.rec.GroupNum == dist_group.rec.Number); 
  while (ok)
    copy(cln_period, dist_period);
    cln_period.rec.ID = 0;
    if (not OperateFile(cln_period, opInsert, "Номер группы: " + dist_group.rec.Number +
                                              "; Номер периода: " +dist_period.rec.Number))
      return false;
    end;
    ok = next(dist_period) AND (dist_period.rec.GroupNum == dist_group.rec.Number);
  end;
  return true;
END;

MACRO LinkMCACCDOC(PeriodID)
  cln_accdoc.KeyNum = 9;
  cln_accdoc.rec.PeriodID = PeriodID;
  if (GetEQ(cln_accdoc))
    return true;
  else
    return false;
  end;
END;

/*процедура обрабатывает период. Если на него нет ссылок из mcaccdoc, то
возвращает 0; если есть и есть такой же период в дистр., то 1, иначе 2.*/
MACRO ProcessPeriod()
  var ok;
  if (LinkMCACCDOC(cln_period.rec.ID))
    dist_period.KeyNum = 1;
    dist_period.rec.GroupNum = cln_period.rec.GroupNum;
    dist_period.rec.HighBoundUnit = cln_period.rec.HighBoundUnit;
    dist_period.rec.HighBound = cln_period.rec.HighBound;
    ok = GetGE(dist_period) AND
         (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
         (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
         (dist_period.rec.HighBound == cln_period.rec.HighBound);
    while (ok)
      if ((dist_period.rec.IsHighInclude == cln_period.rec.IsHighInclude) AND
          (dist_period.rec.LowBoundUnit == cln_period.rec.LowBoundUnit) AND
          (dist_period.rec.LowBound == cln_period.rec.LowBound) AND
          (dist_period.rec.IsLowInclude == cln_period.rec.IsLowInclude) AND
          (dist_period.rec.PeriodCode == cln_period.rec.PeriodCode))
        return 1;
      end;
      ok = next(dist_period) AND
           (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
           (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
           (dist_period.rec.HighBound == cln_period.rec.HighBound);
    end;
    return 2;
  else
    return 0;
  end;
END;

MACRO UpdateOnePeriod(UsedDistr, j)
  VAR saveID = 0,
      ok;
  dist_period.KeyNum = 1;
  dist_period.rec.GroupNum = cln_period.rec.GroupNum;
  dist_period.rec.HighBoundUnit = cln_period.rec.HighBoundUnit;
  dist_period.rec.HighBound = cln_period.rec.HighBound;

  ok = GetGE(dist_period) AND
       (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
       (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
       (dist_period.rec.HighBound == cln_period.rec.HighBound);
  while (ok)
    if ((dist_period.rec.IsHighInclude == cln_period.rec.IsHighInclude) AND
        (dist_period.rec.LowBoundUnit == cln_period.rec.LowBoundUnit) AND
        (dist_period.rec.LowBound == cln_period.rec.LowBound) AND
        (dist_period.rec.IsLowInclude == cln_period.rec.IsLowInclude) AND
        (dist_period.rec.PeriodCode == cln_period.rec.PeriodCode))
      UsedDistr = dist_period.rec.ID;
      if (CompareFiles(cln_period, dist_period, 1))
        saveID = cln_period.rec.ID;
        copy(cln_period, dist_period);
        cln_period.rec.ID = saveID;
        if (not OperateFile(cln_period, opUpdate, "Номер группы: " + dist_group.rec.Number +
                                                  "; Номер периода: " +dist_period.rec.Number))
          return false;
        end;
        UpdateUpgrade(cln_group);
      end;
      j = j + 1;
      SetParm(0, UsedDistr);
      SetParm(1, j);
      return true;
    end;
    ok = next(dist_period) AND
         (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
         (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
         (dist_period.rec.HighBound == cln_period.rec.HighBound);
  end;

  return true;
END;

MACRO DeleteOnePeriod(UsedDistr, j)
  var ok;
  dist_period.KeyNum = 1;
  dist_period.rec.GroupNum = cln_period.rec.GroupNum;
  dist_period.rec.HighBoundUnit = cln_period.rec.HighBoundUnit;
  dist_period.rec.HighBound = cln_period.rec.HighBound;

  ok = GetGE(dist_period) AND
       (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
       (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
       (dist_period.rec.HighBound == cln_period.rec.HighBound);
  while (ok)
    if ((dist_period.rec.IsHighInclude == cln_period.rec.IsHighInclude) AND
        (dist_period.rec.LowBoundUnit == cln_period.rec.LowBoundUnit) AND
        (dist_period.rec.LowBound == cln_period.rec.LowBound) AND
        (dist_period.rec.IsLowInclude == cln_period.rec.IsLowInclude) AND
        (dist_period.rec.PeriodCode == cln_period.rec.PeriodCode))
        
      if (CompareFiles(cln_period, dist_period, 1))
        if (not OperateFile(cln_period, opDelete, "Номер группы: " + cln_group.rec.Number +
                                                  "; Номер периода: " +cln_period.rec.Number))
          return false;
        end;
        UpdateUpgrade(cln_group);
      else
        UsedDistr = dist_period.rec.ID;
        j = j + 1;
        SetParm(0, UsedDistr);
        SetParm(1, j);
      end;
      return true;
    end;
    ok = next(dist_period) AND
         (dist_period.rec.GroupNum == cln_period.rec.GroupNum) AND
         (dist_period.rec.HighBoundUnit == cln_period.rec.HighBoundUnit) AND
         (dist_period.rec.HighBound == cln_period.rec.HighBound);
  end;

  if (not OperateFile(cln_period, opDelete, "Номер группы: " + cln_group.rec.Number +
                                            "; Номер периода: " +cln_period.rec.Number))
    return false;
  end;

  return true;
END;

MACRO UpdatePeriods()
  VAR Tmp = TArray,
      UsedDistr = TArray,
      i = 0, j = 0,
      ok;

  macro NotUse(ID)
    var k = 0;
    while (k < UsedDistr.Size)
      if (ID == UsedDistr[k])
        return false;
      end;
      k = k + 1;
    end;
    return true;
  end;
  cln_period.KeyNum = 1;
  cln_period.rec.GroupNum = cln_group.rec.Number;
  cln_period.rec.HighBoundUnit = 0;
  cln_period.rec.HighBound = 0;
  ok = GetGE(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number); 
  while (ok)
    Tmp[i] = ProcessPeriod();
    if (Tmp[i] == 2)
      println("Обновление группы # " + dist_group.rec.Number + " стандартным образом невозможно");
      if (WorkUpgrader)
        cln_group.rec.DontUpdate = SET_CHAR;
        Update(cln_group);
      end;
      return false;
    end;
    i = i + 1;
    ok = next(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number);
  end;

  cln_period.rec.GroupNum = cln_group.rec.Number;
  cln_period.rec.HighBoundUnit = 0;
  cln_period.rec.HighBound = 0;
  i = 0;
  ok = GetGE(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number);
  while (ok)
    if (Tmp[i])
      if (not UpdateOnePeriod(UsedDistr[j], j))
        return false;
      end;
    else
      if (not DeleteOnePeriod(UsedDistr[j], j))
        return false;
      end;
    end;
    i = i + 1;
    ok = next(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number);
  end;

  j = 0;
  dist_period.KeyNum = 1;
  dist_period.rec.GroupNum = cln_group.rec.Number;
  dist_period.rec.HighBoundUnit = 0;
  dist_period.rec.HighBound = 0;
  ok = GetGE(dist_period) AND (dist_period.rec.GroupNum == cln_group.rec.Number); 
  while (ok)
    if (NotUse(dist_period.rec.ID))
      copy(cln_period, dist_period);
      cln_period.rec.ID = 0;
      if (not OperateFile(cln_period, opInsert, "Номер группы: " + dist_group.rec.Number +
                                                "; Номер периода: " +dist_period.rec.Number))
        return false;
      end;
      UpdateUpgrade(cln_group);
    end;
    ok = next(dist_period) AND (dist_period.rec.GroupNum == cln_group.rec.Number);
  end;
  
  return true;
END;

MACRO UpdateGroupsPeriod()
  dist_group.KeyNum = 0;
  cln_group.KeyNum = 0;
  while (next(dist_group) AND (dist_group.rec.Number < MAX_SYST_MCGROUP))
    cln_group.rec.Number = dist_group.rec.Number;
    if (GetEQ(cln_group))
      if ((not cln_group.rec.DontUpdate) AND UpdatePeriods())
        if (CompareFiles(cln_group, dist_group, 0))
          copy(cln_group, dist_group);
          cln_group.rec.Upgrade = SET_CHAR;
          if (not OperateFile(cln_group, opUpdate, "Номер группы: " + dist_group.rec.Number))
            return false;
          end;
        end;
      elif (not cln_group.rec.DontUpdate)
        cln_group.rec.DontUpdate = SET_CHAR;
        Update(cln_group);
      end;      
    elif (not AddGroupAndPeriods())
      return false;
    end;
  end;
  return true;
END;

MACRO NotExistDistr()
  dist_categ.KeyNum = 2;
  dist_categ.rec.LevelType = cln_categ.rec.LevelType;
  dist_categ.rec.Number = cln_categ.rec.Number;
  if (GetEQ(dist_categ))
    return false;
  else
    return true;
  end;
END;
MACRO UpdateGroupsCateg()
  var ok, saveID = 0;
  
  macro ZeroLinks(GroupID)
    var pos, ok, key;
    pos = GetPos(cln_categ);
    key = cln_categ.KeyNum;
    cln_categ.KeyNum = 2;
    cln_categ.rec.LevelType = 1;
    cln_categ.rec.Number = 0;
    ok = GetGE(cln_categ) AND (cln_categ.rec.LevelType == 1);
    while (ok)
      if (cln_categ.rec.GroupID == GroupID)
        cln_categ.rec.GroupID = 0;
        if (not OperateFile(cln_categ, opUpdate, "Категория # " + cln_categ.rec.Number))
          return false;
        end;
      end;
      ok = next(cln_categ) AND (cln_categ.rec.LevelType == 1);
    end;
    cln_categ.KeyNum = key;
    if (pos)
      GetDirect(cln_categ, pos);
    end;
    return true;
  end;

  cln_categ.KeyNum = 2;
  cln_categ.rec.LevelType = 2;
  cln_categ.rec.Number = 0;
  ok = GetGE(cln_categ) AND (cln_categ.rec.Number < MAX_SYST_MCCATEG_GROUP);
  while (ok)
    if (NotExistDistr())
      if (ZeroLinks(cln_categ.rec.ID))
        if (not OperateFile(cln_categ, opDelete, "Группа # " + cln_categ.rec.Number))
          return false;
        end;
      else
        return false;
      end;
    end;
    ok = next(cln_categ) AND (cln_categ.rec.Number < MAX_SYST_MCCATEG_GROUP);
  end;
  
  dist_categ.KeyNum = 2;
  cln_categ.KeyNum = 2;
  dist_categ.rec.LevelType = 2;
  dist_categ.rec.Number = 0;
  ok = GetGE(dist_categ);
  while (ok)
    cln_categ.rec.LevelType = 2;
    cln_categ.rec.Number = dist_categ.rec.Number;
    if (GetEQ(cln_categ))
      if (CompareFiles(cln_categ, dist_categ, 1, FldIndex(cln_categ, "GroupID")))
        saveID = cln_categ.rec.ID;
        copy(cln_categ, dist_categ);
        cln_categ.rec.ID = saveID;
        if (not OperateFile(cln_categ, opUpdate, "Группа # " + cln_categ.rec.Number))
          return false;
        end;
      end;
    else
      copy(cln_categ, dist_categ);
      cln_categ.rec.ID = 0;
      if (not OperateFile(cln_categ, opInsert, "Группа # " + cln_categ.rec.Number))
        return false;
      end;
    end;
    ok = next(dist_categ);
  end;

  return true;
END;

MACRO UpdateCategs()
  macro GetGroupID()
    var pos_cln, pos_dist, 
        key_cln, key_dist, ret = 0;
    pos_cln = GetPos(cln_categ);
    pos_dist = GetPos(dist_categ);
    key_cln = cln_categ.KeyNum;
    key_dist = dist_categ.KeyNum;
    dist_categ.KeyNum = 0;
    dist_categ.rec.ID = dist_categ.rec.GroupID;
    if (GetEQ(dist_categ))
      cln_categ.KeyNum = 2;
      cln_categ.rec.LevelType = 2;
      cln_categ.rec.Number = dist_categ.rec.Number;
      if (GetEQ(cln_categ))
        ret = cln_categ.rec.ID;
      end;
    end;
    cln_categ.KeyNum = key_cln;
    dist_categ.KeyNum = key_dist;
    if (pos_cln)
      GetDirect(cln_categ, pos_cln);
    end;
    if (pos_dist)
      GetDirect(dist_categ, pos_dist);
    end;
    return ret;
  end;

  macro SetPairIdForCateg(catID, pairID)
    var pos_cln, key_cln;
    pos_cln = GetPos(cln_categ);
    key_cln = cln_categ.KeyNum;
    cln_categ.KeyNum = 0;
    cln_categ.rec.ID = catID;
    if (GetEQ(cln_categ))
      cln_categ.rec.PairCatID = pairID;
      if (not OperateFile(cln_categ, opUpdate, "Категория # " + cln_categ.rec.Number))
        return false;
      end;
    end;
    cln_categ.KeyNum = key_cln;
    if (pos_cln)
      GetDirect(cln_categ, pos_cln);
    end;
    return true;
  end;

  macro UpdatePairCateg(retval, NewCatId_cln, OldPairId_cln)
    var pos_cln, pos_dist,
        key_cln, key_dist,
        NewPairId_dist;
    pos_cln = GetPos(cln_categ);
    pos_dist = GetPos(dist_categ);
    key_cln = cln_categ.KeyNum;
    key_dist = dist_categ.KeyNum;

    NewPairId_dist = dist_categ.rec.PairCatID;
    
    dist_categ.KeyNum = 0;
    dist_categ.rec.ID = NewPairId_dist;
    if (GetEQ(dist_categ))
      cln_categ.KeyNum = 2;
      cln_categ.rec.LevelType = 1;
      cln_categ.rec.Number = dist_categ.rec.Number;
      if (not GetEQ(cln_categ))
        if (OldPairId_cln != 0)
          if (not SetPairIdForCateg(OldPairId_cln, 0))
            return 1;
          end;
        end;
        return -1;
      else
        if (OldPairId_cln != cln_categ.rec.ID)
          if ((OldPairId_cln != 0) AND (not SetPairIdForCateg(OldPairId_cln, 0)))
            return 1;
          end;
          if (cln_categ.rec.PairCatID != 0)
            if (not SetPairIdForCateg(cln_categ.rec.PairCatID, 0))
              return 1;
            end;
          end;
          cln_categ.rec.PairCatID = NewCatId_cln;
          if (not OperateFile(cln_categ, opUpdate, "Категория # " + cln_categ.rec.Number))
            return 1;
          end;
          SetParm(0, cln_categ.rec.ID);
          return 2;
        end;
      end;
    end;

    cln_categ.KeyNum = key_cln;
    dist_categ.KeyNum = key_dist;
    if (pos_cln)
      GetDirect(cln_categ, pos_cln);
    end;
    if (pos_dist)
      GetDirect(dist_categ, pos_dist);
    end;
    return 0;
  end;
  
  macro AddCategAndTemplsAndDocs()
    var ok, catid = 0, st = 0, TmpGrID;
    /*вставим категорию*/
    TmpGrID = GetGroupID();
    copy(cln_categ, dist_categ);
    cln_categ.rec.GroupID = TmpGrID;
    cln_categ.rec.ID = 0;
    cln_categ.rec.Upgrade = SET_CHAR;
    cln_categ.rec.PairCatID = 0;
    if (not OperateFile(cln_categ, opInsert, "Категория # " + cln_categ.rec.Number))
      return false;
    end;
    st = UpdatePairCateg(catid, cln_categ.rec.ID, 0);
    if (st == 1)
      return false;
    elif (st == 2)
      cln_categ.rec.PairCatID = catid;
      if (not OperateFile(cln_categ, opUpdate, "Категория # " + cln_categ.rec.Number))
        return false;
      end;
    end;

    dist_templ.KeyNum = 0;
    dist_templ.rec.CatID = dist_categ.rec.ID;
    dist_templ.rec.Number = 0;
    ok = GetGE(dist_templ) AND (dist_templ.rec.CatID == dist_categ.rec.ID);
    while (ok)
      copy(cln_templ, dist_templ);
      cln_templ.rec.CatID = cln_categ.rec.ID;
      if (not OperateFile(cln_templ, opInsert, "Категория # " + cln_categ.rec.Number +
                                               "; Шаблон # " + cln_templ.rec.Number))
        return false;
      end;
      ok = next(dist_templ) AND (dist_templ.rec.CatID == dist_categ.rec.ID);
    end;

    dist_doc.KeyNum = 0;
    dist_doc.rec.CatID = dist_categ.rec.ID;
    dist_doc.rec.DocKind = 0;
    ok = GetGE(dist_doc) AND (dist_doc.rec.CatID == dist_categ.rec.ID);
    while (ok)
      copy(cln_doc, dist_doc);
      cln_doc.rec.CatID = cln_categ.rec.ID;
      if (not OperateFile(cln_doc, opInsert, "Категория # " + cln_categ.rec.Number +
                                               "; Вид документа: " + cln_doc.rec.DocKind))
        return false;
      end;
      ok = next(dist_doc) AND (dist_doc.rec.CatID == dist_categ.rec.ID);
    end;
    return true;
  end;

  macro LinksToCategExist()
    cln_accdoc.KeyNum = 7;
    cln_accdoc.rec.CatID = cln_categ.rec.ID;
    cln_accdoc.rec.TemplNum = 0;
    if (GetGE(cln_accdoc) AND cln_accdoc.rec.CatID == cln_categ.rec.ID)
      return true;
    end;
    return false;
  end;

  macro UpdateAcc(clnAttr, distAttr)
    var ok, update = false;
    cln_accdoc.KeyNum = 7;
    cln_accdoc.rec.CatID = cln_categ.rec.ID;
    cln_accdoc.rec.TemplNum = 0;
    ok = GetGE(cln_accdoc) AND (cln_accdoc.rec.CatID == cln_categ.rec.ID);
    while (ok)
      update = false;
      if (distAttr[MCCHARACT_FIID] < clnAttr[MCCHARACT_FIID])
        cln_accdoc.rec.FIID = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_OWNER] < clnAttr[MCCHARACT_OWNER])
        cln_accdoc.rec.Owner = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_PLACE] < clnAttr[MCCHARACT_PLACE])
        cln_accdoc.rec.Place = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_ISSUER] < clnAttr[MCCHARACT_ISSUER])
        cln_accdoc.rec.Issuer = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_CENTER] < clnAttr[MCCHARACT_CENTER])
        cln_accdoc.rec.Centr = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_FIID2] < clnAttr[MCCHARACT_FIID2])
        cln_accdoc.rec.FIID2 = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_CONTR_CLIENT] < clnAttr[MCCHARACT_CONTR_CLIENT])
        cln_accdoc.rec.ClientContrID = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_CONTR_BANK] < clnAttr[MCCHARACT_CONTR_BANK])
        cln_accdoc.rec.BankContrID = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_MARKET_PLACE] < clnAttr[MCCHARACT_MARKET_PLACE])
        cln_accdoc.rec.MarketPlaceID = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_INDEXDATE] < clnAttr[MCCHARACT_INDEXDATE])
        cln_accdoc.rec.IndexDate = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_MARKET_PLACE_OFFICE] < clnAttr[MCCHARACT_MARKET_PLACE_OFFICE])
        cln_accdoc.rec.MarketPlaceOfficeID = -1;
        update = true;
      end;
      if (distAttr[MCCHARACT_CONTRACTOR] < clnAttr[MCCHARACT_CONTRACTOR])
        cln_accdoc.rec.Contractor = -1;
        update = true;
      end;
      if (update)
        if (not OperateFile(cln_accdoc, opUpdate, "Счет # " + cln_accdoc.rec.Account))
          if (not OperateFile(cln_accdoc, opDelete, "Счет # " + cln_accdoc.rec.Account))
            return false;
          end;
        end;
      end;
      ok = next(cln_accdoc) AND (cln_accdoc.rec.CatID == cln_categ.rec.ID);
    end;
    return true;
  end;

  macro DeleteCommonAcc()
    var ok;
    cln_accdoc.KeyNum = 7;
    cln_accdoc.rec.CatID = cln_categ.rec.ID;
    cln_accdoc.rec.TemplNum = 0;
    ok = GetGE(cln_accdoc) AND (cln_accdoc.rec.CatID == cln_categ.rec.ID);
    while (ok)
      if (cln_accdoc.rec.IsCommon)
        if (not OperateFile(cln_accdoc, opDelete, "Счет # " + cln_accdoc.rec.Account))
          return false;
        end;
      end;
      ok = next(cln_accdoc) AND (cln_accdoc.rec.CatID == cln_categ.rec.ID);
    end;
    return true;
  end;

  macro LinksToTemplExist()
    cln_accdoc.KeyNum = 7;
    cln_accdoc.rec.CatID = cln_categ.rec.ID;
    cln_accdoc.rec.TemplNum = cln_templ.rec.Number;
    return GetEQ(cln_accdoc);
  end;

  /*0 - ссылок нет; 1 - ссылки есть, но все ок; 2 - обновлять не можем; 3 - закрыть*/
  macro ProcessTempl()
    if (LinksToTemplExist())
      dist_templ.KeyNum = 0;
      dist_templ.rec.CatID = dist_categ.rec.ID;
      dist_templ.rec.Number = cln_templ.rec.Number;
      if (GetEQ(dist_templ))
        if ((dist_templ.rec.Chapter == cln_templ.rec.Chapter) AND
            (dist_templ.rec.Balance == cln_templ.rec.Balance) AND
            (dist_templ.rec.Kind_Account == cln_templ.rec.Kind_Account) AND
            (dist_templ.rec.AccountClient == cln_templ.rec.AccountClient) AND
            (dist_templ.rec.GroupNum == cln_templ.rec.GroupNum) AND
            (dist_templ.rec.HasPeriod == cln_templ.rec.HasPeriod))
          return 1;
        else
          return 2;
        end;
      else
        return 3;
      end;
    else
      return 0;
    end;
  end;

  macro UpdateOneTempl(UsedTempl, j)
    var saveCatID;
    dist_templ.KeyNum = 0;
    dist_templ.rec.CatID = dist_categ.rec.ID;
    dist_templ.rec.Number = cln_templ.rec.Number;
    if (getEQ(dist_templ))
      UsedTempl = dist_templ.rec.Number;
      if (CompareFiles(cln_templ, dist_templ, 1))
        saveCatID = cln_templ.rec.CatID;
        copy(cln_templ, dist_templ);
        cln_templ.rec.CatID = saveCatID;
        if (not OperateFile(cln_templ, opUpdate, "Категория # " + cln_categ.rec.Number +
                                                 "; Шаблон # " + cln_templ.rec.Number))
          return false;
        end;
        UpdateUpgrade(cln_categ);
      end;
      j = j + 1;
      SetParm(0, UsedTempl);
      SetParm(1, j);
    end;
    return true;
  end;

  macro DeleteOneTempl(UsedTempl, j)
    dist_templ.KeyNum = 0;
    dist_templ.rec.CatID = dist_categ.rec.ID;
    dist_templ.rec.Number = cln_templ.rec.Number;
    if (getEQ(dist_templ))
      if (CompareFiles(cln_templ, dist_templ, 1))
        if (not OperateFile(cln_templ, opDelete, "Категория # " + cln_categ.rec.Number +
                                                 "; Шаблон # " + cln_templ.rec.Number))
          return false;
        end;
      else
        UsedTempl = dist_templ.rec.Number;
        j = j + 1;
        SetParm(0, UsedTempl);
        SetParm(1, j);
      end;
    else
      if (not OperateFile(cln_templ, opDelete, "Категория # " + cln_categ.rec.Number +
                                               "; Шаблон # " + cln_templ.rec.Number))
        return false;
      end;
      UpdateUpgrade(cln_categ);
    end;

    return true;
  end;

  
  macro UpdateTempls()
    var ok,
        Tmp = TArray, UsedTempls = TArray,
        i = 0, j = 0;
    macro NotUseTempl(Number)
      var k  = 0;
      while (k < UsedTempls.Size)
        if (UsedTempls[k] == Number)
          return false;
        end;
        k = k + 1;
      end;
      return true;
    end;
    cln_templ.KeyNum = 0;
    cln_templ.rec.CatID = cln_categ.rec.ID;
    cln_templ.rec.Number = 0;
    ok = GetGE(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    while (ok)
      Tmp[i] = ProcessTempl();
      if (Tmp[i] == 2)
        println("Обновление категории " + cln_categ.rec.Number + "стандартным способом невозможно");
        if (WorkUpgrader)
          cln_categ.rec.DontUpdate = SET_CHAR;
          Update(cln_categ);
        end;
        return false;
      end;
      i = i + 1;
      ok = next(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    end;

    cln_templ.rec.CatID = cln_categ.rec.ID;
    cln_templ.rec.Number = 0;
    i = 0;
    ok = GetGE(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    while (ok)
      if (Tmp[i] == 1)
        if (not UpdateOneTempl(UsedTempls[j], j))
          return false;
        end;
      elif (Tmp[i] == 3)
        /*Закрыть шаблон*/
        cln_templ.rec.IsClose = SET_CHAR;
          if (not OperateFile(cln_templ, opUpdate, "Категория # " + cln_categ.rec.Number +
                                                 "; Шаблон # " + cln_templ.rec.Number +
                                                 " установлен закрытым"))
          return false;
        end;
      else
        if (not DeleteOneTempl(UsedTempls[j], j))
          return false;
        end;
      end;
      i = i + 1;
      ok = next(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    end;

    dist_templ.KeyNum = 0;
    dist_templ.rec.CatID = dist_categ.rec.ID;
    dist_templ.rec.Number = 0;
    ok = GetGE(dist_templ) AND (dist_templ.rec.CatID == dist_categ.rec.ID);
    while (ok)
      if (NotUseTempl(dist_templ.rec.Number))
        copy(cln_templ, dist_templ);
        cln_templ.rec.CatID = cln_categ.rec.ID;
        if (not OperateFile(cln_templ, opInsert, "Категория # " + cln_categ.rec.Number +
                                                 "; Шаблон # " + cln_templ.rec.Number))
          return false;
        end;
        UpdateUpgrade(cln_categ);
      end;
      ok = next(dist_templ) AND (dist_templ.rec.CatID == dist_categ.rec.ID);
    end;
    return true;
  end;

  macro UpdateDocs()
    var ok, 
        j = 0,
        UsedDocs = TArray;
    macro NotUseDoc(Number)
      var k  = 0;
      while (k < UsedDocs.Size)
        if (UsedDocs[k] == Number)
          return false;
        end;
        k = k + 1;
      end;
      return true;
    end;
    cln_doc.KeyNum = 0;
    cln_doc.rec.CatID = cln_categ.rec.ID;
    cln_doc.rec.DocKind = 0;
    ok = GetGE(cln_doc) AND (cln_doc.rec.CatID == cln_categ.rec.ID);
    while (ok)
      dist_doc.KeyNum = 0;
      dist_doc.rec.CatID = dist_categ.rec.ID;
      dist_doc.rec.DocKind = cln_doc.rec.DocKind;
      if (GetEQ(dist_doc))
        if (CompareFiles(cln_doc, dist_doc, 1))
          if (not OperateFile(cln_doc, opDelete, "Категория # " + cln_categ.rec.Number +
                                                 "; Вид документа: " + cln_doc.rec.DocKind))
            return false;
          end;
        else
          UsedDocs[j] = dist_doc.rec.DocKind;
          j = j + 1;
        end;
      else
        if (not OperateFile(cln_doc, opDelete, "Категория # " + cln_categ.rec.Number +
                                               "; Вид документа: " + cln_doc.rec.DocKind))
          return false;
        end;
        UpdateUpgrade(cln_categ);
      end;
      ok = next(cln_doc) AND (cln_doc.rec.CatID == cln_categ.rec.ID);
    end;

    dist_doc.KeyNum = 0;
    dist_doc.rec.CatID = dist_categ.rec.ID;
    dist_doc.rec.DocKind = 0;
    ok = GetGE(dist_doc) AND (dist_doc.rec.CatID == dist_categ.rec.ID);
    while (ok)
      if (NotUseDoc(dist_doc.rec.DocKind))
        copy(cln_doc, dist_doc);
        cln_doc.rec.CatID = cln_categ.rec.ID;
        if (not OperateFile(cln_doc, opInsert, "Категория # " + cln_categ.rec.Number +
                                               "; Вид документа: " + cln_doc.rec.DocKind))
          return false;
        end;
        UpdateUpgrade(cln_categ);
      end;
      ok = next(dist_doc) AND (dist_doc.rec.CatID == dist_categ.rec.ID);
    end;
    
    return true;
  end;

  macro DeleteCategAndTemplsAndDocs()
    var ok;
    cln_templ.KeyNum = 0;
    cln_templ.rec.CatID = cln_categ.rec.ID;
    cln_templ.rec.Number = 0;
    ok = GetGE(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    while (ok)
      if (not OperateFile(cln_templ, opDelete, "Категория # " + cln_categ.rec.Number +
                                               "; Шаблон # " + cln_templ.rec.Number))
        return false;
      end;
      ok = next(cln_templ) AND (cln_templ.rec.CatID == cln_categ.rec.ID);
    end;
    cln_doc.KeyNum = 0;
    cln_doc.rec.CatID = cln_categ.rec.ID;
    cln_doc.rec.DocKind = 0;
    ok = GetGE(cln_doc) AND (cln_doc.rec.CatID == cln_categ.rec.ID);
    while (ok)
      if (not OperateFile(cln_doc, opDelete, "Категория # " + cln_categ.rec.Number +
                                             "; Вид документа: " + cln_doc.rec.DocKind))
        return false;
      end;
      ok = next(cln_doc) AND (cln_doc.rec.CatID == cln_categ.rec.ID);
    end;
    if (not SetPairIdForCateg(cln_categ.rec.PairCatID, 0))
      return false;
    end;
    if (not OperateFile(cln_categ, opDelete, "Категория # " + cln_categ.rec.Number))
      return false;
    end;
    return true;
  end;
  
  VAR ok, st = 0,
      saveID, saveGrID, PairCatId = 0, CanContinue = true,
      clnAttr = TArray, distAttr = TArray, k = 0;
  dist_categ.KeyNum = 2;
  dist_categ.rec.LevelType = 1;
  dist_categ.rec.Number = 0;
  ok = GetGE(dist_categ) AND (dist_categ.rec.LevelType == 1);
  while (ok)
    k = 0;
    CanContinue = true;
    cln_categ.KeyNum = 2;
    cln_categ.rec.LevelType = dist_categ.rec.LevelType;
    cln_categ.rec.Number = dist_categ.rec.Number;
    if (GetEQ(cln_categ))
      if (not cln_categ.rec.DontUpdate)
        if (LinksToCategExist())
          MakeBinary(cln_categ.rec.AttrMask, clnAttr);
          MakeBinary(dist_categ.rec.AttrMask, distAttr);
          if((cln_categ.rec.PeriodKind != dist_categ.rec.PeriodKind) OR
             cln_categ.rec.PeriodGroupNum AND (cln_categ.rec.PeriodGroupNum != dist_categ.rec.PeriodGroupNum))
            CanContinue = false;
          end;
          if (CanContinue)
            while ((k < MCCHARACT_DEAL) AND (CanContinue))
              if (distAttr[k] > clnAttr[k])
                CanContinue = false;
              end;
              k = k + 1;
            end;
          end;
          if (CanContinue)
            if (distAttr[MCCHARACT_DEAL] > clnAttr[MCCHARACT_DEAL])
              if (not DeleteCommonAcc())
                return false;
              end;
            end;
          end;
          if (CanContinue)
            if (not UpdateAcc(clnAttr, distAttr))
              return false;
            end;
          end;
          if (CanContinue)
            if (distAttr[MCCHARACT_DEAL] < clnAttr[MCCHARACT_DEAL])
              CanContinue = false;
            end;
          end;
          if (not CanContinue)
            println("Обновление категории " + cln_categ.rec.Code + " стандартным способом невозможно");
            if (WorkUpgrader)
              cln_categ.rec.DontUpdate = SET_CHAR;
              Update(cln_categ);
            end;
          end;
        end;

        
        if (CanContinue)
          if (UpdateTempls())
            if (CompareFiles(cln_categ, dist_categ, 1, FldIndex(cln_categ, "GroupID")))
              saveGrID = GetGroupID();
              saveID = cln_categ.rec.ID;
              st = UpdatePairCateg(PairCatId, cln_categ.rec.ID, cln_categ.rec.PairCatID);
              copy(cln_categ, dist_categ);
              cln_categ.rec.ID = saveID;
              cln_categ.rec.Upgrade = SET_CHAR;
              cln_categ.rec.GroupID = saveGrID;
              if (st == -1)
                cln_categ.rec.PairCatID = 0;
              elif (st == 2)
                cln_categ.rec.PairCatID = PairCatId;
              elif (st == 1)
                return false;
              end;
              if (not OperateFile(cln_categ, opUpdate, "Категория # " + cln_categ.rec.Number))
                return false;
              end;
            end;
            UpdateDocs();
          end;
        end;
      end;
    else
      AddCategAndTemplsAndDocs();
    end;
    ok = next(dist_categ) AND (dist_categ.rec.LevelType == 1);
  end;
  /*удалим лишние*/
  cln_categ.KeyNum = 2;
  cln_categ.rec.LevelType = 1;
  cln_categ.rec.Number = 0;
  ok = GetGE(cln_categ) AND (cln_categ.rec.LevelType == 1) AND (cln_categ.rec.Number < MAX_SYST_MCCATEG_CATEG);
  while (ok)
    if (NotExistDistr())
      if (LinksToCategExist())
        println("Удаление категории " + cln_categ.rec.CatCode + " стандартным образом невозможно");
        if (WorkUpgrader)
          cln_categ.rec.DontUpdate = SET_CHAR;
          Update(cln_categ);
        end;
      else
        DeleteCategAndTemplsAndDocs();
      end;
    end;
    ok = next(cln_categ) AND (cln_categ.rec.LevelType == 1) AND (cln_categ.rec.Number < MAX_SYST_MCCATEG_CATEG);
  end;
  return true;
END;

MACRO DeletePeriods()
  macro NotExistDistrGroup()
    dist_group.KeyNum = 0;
    dist_group.rec.Number = cln_group.rec.Number;
    return not GetEQ(dist_group);
  end;
  macro LinksFromACCDOC()
    cln_accdoc.KeyNum = 8;
    cln_accdoc.rec.GroupNum = cln_group.rec.Number;
    return GetEQ(cln_group);
  end;
  macro LinksFromTEMPL()
    cln_templ.KeyNum = 1;
    cln_templ.rec.GroupNum = cln_group.rec.Number;
    return GetEQ(cln_templ);
  end;
  macro LinksFromCATEG()
    cln_categ.KeyNum = 4;
    cln_categ.rec.PeriodGroupNum = cln_group.rec.Number;
    return GetEQ(cln_categ);
  end;
  macro DeleteGroupAndPeriods()
    var ok;
    cln_period.KeyNum = 3;
    cln_period.rec.GroupNum = cln_group.rec.Number;
    cln_period.rec.Number = 0;
    ok = GetGE(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number);
    while (ok)
      if (not OperateFile(cln_period, opDelete, "Группа # " + cln_group.rec.Number +
                                                "; Период # " + cln_period.rec.Number))
        return false;
      end;
      ok = next(cln_period) AND (cln_period.rec.GroupNum == cln_group.rec.Number);
    end;
    if (not OperateFile(cln_group, opDelete, "Группа # " + cln_group.rec.Number))
      return false;
    end;
    return true;
  end;
  
  cln_group.KeyNum = 0;
  while (next(cln_group))
    if (NotExistDistrGroup())
      if (LinksFromACCDOC() OR
          LinksFromTEMPL() OR
          LinksFromCATEG())
        println("Невозможно удалить группу " + cln_group.rec.Number + " стандартным способом");
        if (WorkUpgrader)
          cln_group.rec.DontUpdate = SET_CHAR;
          Update(cln_group);
        end;
      else
        DeleteGroupAndPeriods();
      end;
    end;
  end;
  return true;
END;

MACRO ClearUpgradeFlag()
  cln_group.KeyNum = 0;
  while (next(cln_group))
    if (cln_group.rec.Upgrade == SET_CHAR)
      cln_group.rec.Upgrade = "";
      if (not Update(cln_group))
        return false;
      end;
    end;
  end;
  cln_categ.KeyNum = 0;
  while (next(cln_categ))
    if (cln_categ.rec.Upgrade == SET_CHAR)
      cln_categ.rec.Upgrade = "";
      if (not Update(cln_categ))
        return false;
      end;
    end;
  end;
  return true;
END;

MACRO OpenFiles(p_distDir, p_clnDir)
  if (substr(p_clnDir, strlen(p_clnDir)) != "\\")
    p_clnDir = p_clnDir + "\\";
  end;
  if (substr(p_distDir, strlen(p_distDir)) != "\\")
    p_distDir = p_distDir + "\\";
  end;

  /* открываем файлы */
  dist_group = TBFile("mcgroup.dbt", "R", 0, p_distDir + "mcgroup.dbt");
  if (dist_group == NULL)
    println("Ошибка открытия файла: " + p_distDir + "mcgroup.dbt");
    return false;
  end;
  cln_group = TBFile("mcgroup.dbt", "W", 0, p_clnDir + "mcgroup.dbt");
  if (cln_group == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mcgroup.dbt");
    return false;
  end;

  dist_period = TBFile("mcperiod.dbt", "R", 0, p_distDir + "mcperiod.dbt");
  if (dist_period == NULL)
    println("Ошибка открытия файла: " + p_distDir + "mcperiod.dbt");
    return false;
  end;
  cln_period = TBFile("mcperiod.dbt", "W", 0, p_clnDir + "mcperiod.dbt");
  if (cln_period == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mcperiod.dbt");
    return false;
  end;

  cln_accdoc = TBFile("mcaccdoc.dbt", "W", 0, p_clnDir + "mcaccdoc.dbt");
  if (cln_accdoc == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mcaccdoc.dbt");
    return false;
  end;

  dist_categ = TBFile("mccateg.dbt", "R", 0, p_distDir + "mccateg.dbt");
  if (dist_categ == NULL)
    println("Ошибка открытия файла: " + p_distDir + "mccateg.dbt");
    return false;
  end;
  cln_categ = TBFile("mccateg.dbt", "W", 0, p_clnDir + "mccateg.dbt");
  if (cln_categ == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mccateg.dbt");
    return false;
  end;

  dist_templ = TBFile("mctempl.dbt", "R", 0, p_distDir + "mctempl.dbt");
  if (dist_templ == NULL)
    println("Ошибка открытия файла: " + p_distDir + "mctempl.dbt");
    return false;
  end;
  cln_templ = TBFile("mctempl.dbt", "W", 0, p_clnDir + "mctempl.dbt");
  if (cln_templ == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mctempl.dbt");
    return false;
  end;

  dist_doc = TBFile("mcdoccat.dbt", "R", 0, p_distDir + "mcdoccat.dbt");
  if (dist_doc == NULL)
    println("Ошибка открытия файла: " + p_distDir + "mcdoccat.dbt");
    return false;
  end;
  cln_doc = TBFile("mcdoccat.dbt", "W", 0, p_clnDir + "mcdoccat.dbt");
  if (cln_doc == NULL)
    println("Ошибка открытия файла: " + p_clnDir + "mcdoccat.dbt");
    return false;
  end;

  
  return true;
END;

MACRO UpdateFiles(p_distDir, p_clnDir)
  if (not OpenFiles(p_distDir, p_clnDir))
    return false;
  end;
  if (WorkUpgrader AND (not ClearUpgradeFlag()))
    return false;
  end;
  
  if (not UpdateGroupsPeriod())
    return false;
  end;
  if (not UpdateGroupsCateg())
    return false;
  end;
  if (not UpdateCategs())
    return false;
  end;
 
  if (not DeletePeriods())
    return false;
  end;
  return true;
END;

