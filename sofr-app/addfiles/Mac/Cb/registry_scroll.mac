/*
$Name:        registry_scroll.mac
$Module:      ƒ« ¢­ ï ª­¨£ 
$Description: Œ ªà®á WEB. ……‘’ €‘’Ž…Š
*/

import BankInter;
import RsbDataSet, globals, oralib, likepy;
import "ws_validation.mac";
import "ws_dprt.mac";
import "ws_person.mac";

class TWebRegParm
  var KeyID;
  var ParentID;
  var RegName;
  var RegType;
  var IsGlobal;
  var IsBranch;
  var IsSecurity;
  var Description;
end;

class TWebRegVal
  var KeyID;
  var RegKind;
  var ObjectID;
  var BlockUserValue;
  var ExpDep;
  var IntValue;
  var DoubleValue;
  var StringValue;
  var FlagValue;
end;

class TWebRegSettingPanelObject
  var RegParm;
  var DefRegVal;
  var RegName;
  var ParentPath;
  var FullPath;
  var IsNoRegVal;
  var IsYesRegVal;
end;

class TWebRegSettingBranchValuesGridPanelObject
  var RegParm;
  var DefaultValue;
  var Path;
  var UserCanManageRegVal;
end;

class TWebRegSettingValuePanelObject
  var RegParm;
  var RegVal;
  var Path;
  var IsExpFilial;
  var Dprt;
  var Person;
  var IsNoRegVal;
  var IsYesRegVal;
end;

class TWebRegSettingUserValuesGridItem
  var DprtCode;
  var DprtName;
  var UserNum;
  var UserName;
  var IsBlocked;
  var StrRegValue;
end;

private const EREGPARMTYPE_INTEGER  = 0;
private const EREGPARMTYPE_DOUBLE   = 1;
private const EREGPARMTYPE_STRING   = 2;
private const EREGPARMTYPE_BINARY   = 3;
private const EREGPARMTYPE_FLAG     = 4;

PRIVATE VAR ResponseBuilder : WebResponseBuilder;

private macro AddErrorMessage(ErrorMessage : @WsErrorMessage)

  var objErr = AL_GetErrorInfo();

  ErrorMessage.Code     = objErr.stat;
  ErrorMessage.Message  = objErr.message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

private macro AddErrorMessageExt(ErrorMessage : @WsErrorMessage, stat, message)

  ErrorMessage.Code     = stat;
  ErrorMessage.Message  = message;
  ErrorMessage.Severity = MessageSeverity.RuntimeError;

  ResponseBuilder.AddMessage( ErrorMessage );

end;

macro WebCore_GetRegSettingsGridItemList(Branch, RootKeyPaths)

  var innerquery;
  var outerquery;

  var KeyIDs;
  var i;

  var result;

  innerquery =
          " SELECT LEVEL, "
          "        XMLELEMENT ( "
          "           RegParm, "
          "           XMLELEMENT (KeyID, rprm.t_KeyID), "
          "           XMLELEMENT (ParentID, rprm.t_ParentID), "
          "           XMLELEMENT ( "
          "              RegName, "
          "              CASE WHEN rprm.t_Name = CHR (1) THEN NULL ELSE rprm.t_Name END), "
          "           XMLELEMENT (RegType, rprm.t_Type), "
          "           XMLELEMENT ( "
          "              IsGlobal, "
          "              CASE "
          "                 WHEN rprm.t_Global = CHR (0) THEN NULL "
          "                 ELSE rprm.t_Global "
          "              END), "
          "           XMLELEMENT ( "
          "              IsBranch, "
          "              CASE "
          "                 WHEN rprm.t_IsBranch = CHR (0) THEN NULL "
          "                 ELSE rprm.t_IsBranch "
          "              END), "
          "           XMLELEMENT ( "
          "              IsSecurity, "
          "              CASE "
          "                 WHEN rprm.t_Security = CHR (0) THEN NULL "
          "                 ELSE rprm.t_Security "
          "              END), "
          "           XMLELEMENT ( "
          "              Description, "
          "              CASE "
          "                 WHEN rprm.t_Description = CHR (1) THEN NULL "
          "                 ELSE rprm.t_Description "
          "              END), "
          "           XMLELEMENT ( "
          "              BranchValueNum, "
          "              (SELECT COUNT (*) "
          "                 FROM dregval_dbt rval "
          "                WHERE     rval.t_KeyID = rprm.t_KeyID ";
  innerquery = innerquery +
          "                      AND rval.t_RegKind = " + REGVAL_KIND_DPRT;
  innerquery = innerquery +
          "                      AND rval.t_ObjectID IN "
          "                             (    SELECT dep.t_Code "
          "                                    FROM ddp_dep_dbt dep ";
  innerquery = innerquery +
          "                              START WITH dep.t_Code = " + Branch;
  innerquery = innerquery +
          "                              CONNECT BY PRIOR dep.t_Code = dep.t_ParentCode))), "
          "           XMLELEMENT ( "
          "              UserValueNum, "
          "              (SELECT COUNT (*) "
          "                 FROM dregval_dbt rval "
          "                WHERE     rval.t_KeyID = rprm.t_KeyID ";
  innerquery = innerquery +
          "                      AND rval.t_RegKind = " + REGVAL_KIND_OPER;
  innerquery = innerquery +
          "                      AND rval.t_ObjectID IN "
          "                             (    SELECT dep.t_Code "
          "                                    FROM ddp_dep_dbt dep ";
  innerquery = innerquery +
          "                              START WITH dep.t_Code = " + Branch;
  innerquery = innerquery +
          "                              CONNECT BY PRIOR dep.t_Code = dep.t_ParentCode))), "
          "           XMLELEMENT (DefaultValue, "
          "                       (CASE "
          "                           WHEN (SELECT RSB_Common.RSI_GetRegValue ( "
          "                                           rprm.t_KeyID, "
          "                                           0, "
          "                                           0, "
          "                                           rprm.t_Type) "
          "                                   FROM DUAL) = CHR (0) "
          "                           THEN "
          "                              NULL "
          "                           ELSE "
          "                              (SELECT RSB_Common.RSI_GetRegValue ( "
          "                                         rprm.t_KeyID, "
          "                                         0, "
          "                                         0, "
          "                                         rprm.t_Type) "
          "                                 FROM DUAL) "
          "                        END))) "
          "    FROM dregparm_dbt rprm "
          " WHERE 1=1 ";

  if((RootKeyPaths != NULL) AND (RootKeyPaths.size > 0))

    innerquery = innerquery + " AND rprm.t_KeyID IN ";

    i = 0;

    KeyIDs = "";

    while(i < RootKeyPaths.size)

      KeyIDs = KeyIDs + " (SELECT RSB_Common.RSI_GetRegParm(''" + RootKeyPaths[i] + "'') FROM dual) ";

      if( i != RootKeyPaths.size - 1)

        KeyIDs = KeyIDs + ", ";

      end;

      i = i + 1;

    end;

    innerquery = innerquery +
  "                       (    SELECT t.t_KeyID "
  "                            FROM dregparm_dbt t "
  "                      START WITH t.t_KeyID IN "
  "                                    (" + KeyIDs + ") "
  "                      CONNECT BY t.t_KeyID = PRIOR t.t_ParentID "
  "                      UNION "
  "                          SELECT t.t_KeyID "
  "                            FROM dregparm_dbt t "
  "                      START WITH t.t_KeyID IN "
  "                                    (" + KeyIDs + ") "
  "                      CONNECT BY PRIOR t.t_KeyID = t.t_ParentID) ";

  end;

  innerquery = innerquery +
                "         START WITH rprm.t_ParentID = 0 "
                "       CONNECT BY PRIOR rprm.t_KeyID = rprm.t_ParentID "
                "ORDER SIBLINGS BY rprm.t_Name ";

  outerquery =
              "    SELECT 1, "
              "   XMLELEMENT(registry_list,  "
              "    (SELECT DBMS_XMLGEN.GETXMLTYPE ";
  outerquery = outerquery +
              "      (DBMS_XMLGEN.NEWCONTEXTFROMHIERARCHY  ('" + innerquery + "') ) ";
  outerquery = outerquery +
              "  FROM DUAL)).GETCLOBVAL() XMLDOC  FROM DUAL";

  var ds = TRsbDataSet( outerquery );

  if( ds.MoveNext() )
    result = ds.XMLDOC;
  end;

  return result;

end;

macro WebCore_DeleteRegSetting(KeyID)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = CB_DeleteRegSetting(KeyID);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

private macro GetRegParm( KeyID )

  var cmd, rs;

  var obj = TWebRegParm();

  var query = " select T_KEYID, "
              "   T_PARENTID, "
              "   T_NAME, "
              "   T_TYPE, "
              "   T_GLOBAL, "
              "   T_DESCRIPTION, "
              "   T_SECURITY, "
              "   T_ISBRANCH "
              " from dregparm_dbt "
              "  where t_keyid = " + string(KeyID);

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  if(rs.movenext)
    obj.KeyID       = SQL_ConvType(rs.value("T_KEYID"));
    obj.ParentID    = SQL_ConvType(rs.value("T_PARENTID"));
    obj.RegName     = SQL_ConvType(rs.value("T_NAME"));
    obj.RegType     = SQL_ConvType(rs.value("T_TYPE"));
    obj.IsGlobal    = SQL_ConvTypeBool(rs.value("T_GLOBAL"));
    obj.IsBranch    = SQL_ConvTypeBool(rs.value("T_ISBRANCH"));
    obj.IsSecurity  = SQL_ConvTypeBool(rs.value("T_SECURITY"));
    obj.Description = SQL_ConvType(rs.value("T_DESCRIPTION"));
  end;

  return obj;

end;

private macro WebCore_GetRegVal(KeyID, RegKind, ObjectID, RegType)

  var pIntValue:SQLParam;
  var pDoubleValue:SQLParam;
  var pStringValue:SQLParam;
  var pFlagValue:SQLParam;
  var pExpDep:SQLParam;
  var params:TArray;
  var stat:integer;

  var cmd, rs;

  var regvalObj = TWebRegVal();

  var query = " select T_KEYID, "
              "   T_REGKIND, "
              "   T_OBJECTID, "
              "   T_BLOCKUSERVALUE, "
              "   T_EXPDEP "
              " from dregval_dbt "
              "  where t_keyid = ? and t_regkind = ? and t_objectid = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, KeyID );
  cmd.addParam( "", RSDBP_IN, RegKind );
  cmd.addParam( "", RSDBP_IN, ObjectID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    regvalObj.KeyID           = SQL_ConvType(rs.value("T_KEYID"));
    regvalObj.RegKind         = SQL_ConvType(rs.value("T_REGKIND"));
    regvalObj.ObjectID        = SQL_ConvType(rs.value("T_OBJECTID"));
    regvalObj.BlockUserValue  = SQL_ConvTypeBool(rs.value("T_BLOCKUSERVALUE"));
    regvalObj.ExpDep          = SQL_ConvType(rs.value("T_EXPDEP"));
    regvalObj.IntValue        = 0;
    regvalObj.DoubleValue     = 0;
    regvalObj.StringValue     = "";
    regvalObj.FlagValue       = false;

    if(RegType == 0 /* INTEGER */)

      pIntValue = SQLParam( "pIntValue", V_INTEGER, RSDBP_OUT );
      pExpDep   = SQLParam( "pExpDep", V_INTEGER, RSDBP_OUT );

      params = makeArray( pIntValue,
                          SQLParam("p_KeyID",    KeyID),
                          SQLParam("p_RegKind",  RegKind),
                          SQLParam("p_ObjectID", ObjectID),
                          pExpDep );

      stat = execStoredFunc("RSB_Common.RSI_ProcessIntegerValue", V_INTEGER, params);

      regvalObj.IntValue = pIntValue.value;
      regvalObj.ExpDep   = pExpDep.value;

    else

      if(RegType == 1 /* DOUBLE */)

        pDoubleValue = SQLParam( "pDoubleValue", V_DOUBLE, RSDBP_OUT );
        pExpDep      = SQLParam( "pExpDep", V_INTEGER, RSDBP_OUT );

        params = makeArray( pDoubleValue,
                            SQLParam("p_KeyID",    KeyID),
                            SQLParam("p_RegKind",  RegKind),
                            SQLParam("p_ObjectID", ObjectID),
                            pExpDep );

        stat = execStoredFunc("RSB_Common.RSI_ProcessDoubleValue", V_INTEGER, params);

        regvalObj.DoubleValue = pDoubleValue.value;
        regvalObj.ExpDep      = pExpDep.value;

      else

        if(RegType == 2 /* STRING */)

          pStringValue = SQLParam( "pStringValue", V_STRING, RSDBP_OUT );
          pExpDep      = SQLParam( "pExpDep", V_INTEGER, RSDBP_OUT );

          params = makeArray( pStringValue,
                              SQLParam("p_KeyID",    KeyID),
                              SQLParam("p_RegKind",  RegKind),
                              SQLParam("p_ObjectID", ObjectID),
                              pExpDep );

          stat = execStoredFunc("RSB_Common.RSI_ProcessStringValue", V_INTEGER, params);

          regvalObj.StringValue = pStringValue.value;
          regvalObj.ExpDep      = pExpDep.value;

        else

          if(RegType == 4 /* FLAG */)

            pFlagValue  = SQLParam( "pFlagValue ", V_STRING, RSDBP_OUT );
            pExpDep     = SQLParam( "pExpDep", V_INTEGER, RSDBP_OUT );

            params = makeArray( pFlagValue,
                                SQLParam("p_KeyID",    KeyID),
                                SQLParam("p_RegKind",  RegKind),
                                SQLParam("p_ObjectID", ObjectID),
                                pExpDep );

            stat = execStoredFunc("RSB_Common.RSI_ProcessFlagValue ", V_INTEGER, params);

            if(pFlagValue.value == "X")

              regvalObj.FlagValue = true;

            else

              regvalObj.FlagValue = false;

            end;

            regvalObj.ExpDep    = pExpDep.value;

          end;

        end;

      end;

    end;

  end;

  return regvalObj;

end;

macro WebCore_GetRegSettingParam(KeyID, ParentID)

  var params:TArray;

  var panelObj = TWebRegSettingPanelObject();

  if(KeyID == 0)

    panelObj.RegParm = TWebRegParm();

    panelObj.RegParm.KeyID        = 0;
    panelObj.RegParm.ParentID     = ParentID;
    panelObj.RegParm.RegName      = "";
    panelObj.RegParm.RegType      = 0;
    panelObj.RegParm.IsGlobal     = false;
    panelObj.RegParm.IsBranch     = false;

    if({cTypePerson} == TP_ADM_SEC)

      panelObj.RegParm.IsSecurity = true;

    else

      panelObj.RegParm.IsSecurity = false;

    end;

    panelObj.RegParm.Description  = "";

    panelObj.DefRegVal = TWebRegVal();

    // panelObj.DefRegVal.KeyID;
    // panelObj.DefRegVal.RegKind;
    // panelObj.DefRegVal.ObjectID;
    // panelObj.DefRegVal.BlockUserValue;
    // panelObj.DefRegVal.ExpDep;

    panelObj.DefRegVal.IntValue    = 0;
    panelObj.DefRegVal.DoubleValue = 0;
    panelObj.DefRegVal.StringValue = "";
    panelObj.DefRegVal.FlagValue   = false;

    params = makeArray( SQLParam("p_ParentID",    ParentID) );

    panelObj.ParentPath = execStoredFunc("RSB_Common.RSI_GetRegKeyPath ", V_STRING, params);

  else

    panelObj.RegParm = GetRegParm( KeyID );

    panelObj.DefRegVal = WebCore_GetRegVal(panelObj.RegParm.KeyID, 0, 0, panelObj.RegParm.RegType);

    params = makeArray( SQLParam("p_KeyID",    KeyID) );

    panelObj.ParentPath = execStoredFunc("RSB_Common.RSI_GetRegKeyPath ", V_STRING, params);

  end;

  panelObj.RegName = panelObj.RegParm.RegName;

  if(panelObj.RegParm.RegType == 4 /* FLAG */)

    if(panelObj.DefRegVal.FlagValue == true)

      panelObj.IsYesRegVal = true;
      panelObj.IsNoRegVal = false;

    else

      panelObj.IsYesRegVal = false;
      panelObj.IsNoRegVal = true;

    end;

  end;

  return panelObj;

end;

macro WebCore_InsertUpdateRegSetting(RegPrm, RegValue, RegType)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  file prm("regparm.dbt");

  prm.KeyID       = RegPrm.KeyID;
  prm.ParentID    = RegPrm.ParentID;
  prm.Name        = RegPrm.RegName;
  prm.Type        = RegPrm.RegType;

  if(RegPrm.IsGlobal == true)
    prm.Global      = "X";
  else
    prm.Global      = "";
  end;

  if(RegPrm.IsBranch == true)
    prm.IsBranch      = "X";
  else
    prm.IsBranch      = "";
  end;

  if(RegPrm.IsSecurity == true)
    prm.Security      = "X";
  else
    prm.Security      = "";
  end;

  prm.Description = RegPrm.Description;

  res = CB_InsertUpdateRegSetting(prm, RegValue, RegType);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_GetRegSettingValuesGridPanelParam(KeyID)

  var params:TArray;

  var panelObj  = TWebRegSettingBranchValuesGridPanelObject();

  panelObj.RegParm = GetRegParm( KeyID );

  params = makeArray( SQLParam("p_KeyID", panelObj.RegParm.KeyID),
                      SQLParam("p_RegKind", 0),
                      SQLParam("p_ObjectID", 0),
                      SQLParam("p_RegType", panelObj.RegParm.RegType));

  panelObj.DefaultValue = execStoredFunc("RSB_Common.RSI_GetRegValue", V_STRING, params);

  params = makeArray( SQLParam("p_KeyID", panelObj.RegParm.KeyID) );

  panelObj.Path = execStoredFunc("RSB_Common.RSI_GetRegKeyPath ", V_STRING, params);

  panelObj.UserCanManageRegVal = false;

  if({cTypePerson} == TP_ADM)

    if((panelObj.RegParm.IsSecurity == false) AND (CheckActionRestriction(81) == true))

      panelObj.UserCanManageRegVal = true;

    end;

  else

    if({cTypePerson} == TP_ADM_SEC)

      if((panelObj.RegParm.IsSecurity == true) AND (CheckActionRestriction(80) == true))

        panelObj.UserCanManageRegVal = true;

      end;

    end;

  end;

  return panelObj;

end;

macro WebCore_GetRegSettingBranchValuesGridItemList(KeyID, RegType, Branch, DprtStatusList)

  var innerquery;
  var outerquery;

  var KeyIDs;
  var i;

  var result;

  innerquery =
  "    SELECT LEVEL, "
  "           XMLELEMENT ( "
  "              REGPARMBRANCH, "
  "              XMLELEMENT (Code, dep.t_Code), "
  "              XMLELEMENT (Name, dep.t_Name), "
  "              XMLELEMENT (NodeType, dep.t_NodeType), "
  "              XMLELEMENT (PartyName, pt.t_Name), "
  "              XMLELEMENT (Status, dep.t_Status), "
  "              XMLELEMENT (RegType, " + RegType + "), ";
  innerquery = innerquery +
  "              XMLELEMENT (HasBranchValue, DECODE (rv.t_KeyID, NULL, 0, 1)), "
  "              XMLELEMENT (ExpDep, NVL (rv.t_ExpDep, 0)), "
  "              XMLELEMENT ( "
  "                 BlockUserValue, "
  "                 CASE "
  "                    WHEN rv.t_BlockUserValue = CHR (0) THEN NULL "
  "                    ELSE rv.t_BlockUserValue "
  "                 END), "
  "              XMLELEMENT (StrRegValue, "
  "                        (CASE "
  "                           WHEN (SELECT RSB_Common.RSI_GetRegValue ( "
  "                                           " + KeyID + ", ";
  innerquery = innerquery +
  "                                           1, "
  "                                           dep.t_Code, "
  "                                           " + RegType + ") ";
  innerquery = innerquery +
  "                                   FROM DUAL) = CHR (0) "
  "                           THEN "
  "                              NULL "
  "                           ELSE "
  "                              (SELECT RSB_Common.RSI_GetRegValue ( "
  "                                           " + KeyID + ", ";
  innerquery = innerquery +
  "                                           1, "
  "                                           dep.t_Code, "
  "                                           " + RegType + ") ";
  innerquery = innerquery +
  "                                 FROM DUAL) "
  "                        END))) "
  "      FROM ddp_dep_dbt dep "
  "           INNER JOIN dparty_dbt pt ON pt.t_PartyID = dep.t_PartyID "
  "           LEFT JOIN "
  "           dregval_dbt rv "
  "              ON     rv.t_KeyID = " + KeyID;
  innerquery = innerquery +
  "                 AND rv.t_RegKind = " + REGVAL_KIND_DPRT;
  innerquery = innerquery +
  "                 AND rv.t_ObjectID = dep.t_Code "
  "     WHERE EXISTS "
  "              (SELECT 1 "
  "                 FROM dregval_dbt rv2 "
  "                WHERE     rv2.t_KeyID =  " + KeyID;
  innerquery = innerquery +
  "                      AND rv2.t_RegKind = " + REGVAL_KIND_DPRT;
  innerquery = innerquery +
  "                      AND t_ObjectID IN "
  "                             (    SELECT dep2.t_Code "
  "                                    FROM ddp_dep_dbt dep2 "
  "                                   WHERE dep2.t_Status IN (" + DprtStatusList + ") ";
  innerquery = innerquery +
  "                              START WITH dep2.t_Code = dep.t_Code "
  "                              CONNECT BY PRIOR dep2.t_Code = dep2.t_ParentCode)) "
  " START WITH dep.t_Code = " + Branch;
  innerquery = innerquery +
  " CONNECT BY PRIOR dep.t_Code = dep.t_ParentCode ";

  outerquery =
              "    SELECT 1, "
              "   XMLELEMENT(registrybranch_list,  "
              "    (SELECT DBMS_XMLGEN.GETXMLTYPE ";
  outerquery = outerquery +
              "      (DBMS_XMLGEN.NEWCONTEXTFROMHIERARCHY  ('" + innerquery + "') ) ";
  outerquery = outerquery +
              "  FROM DUAL)).GETCLOBVAL() XMLDOC  FROM DUAL";

  var ds = TRsbDataSet( outerquery );

  if( ds.MoveNext() )
    result = ds.XMLDOC;
  end;

  return result;

end;

macro WebCore_DeleteRegSettingBranchUserValue(KeyPath, RegKind, ObjectID)

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  res = DeleteRegistryValue (KeyPath, ObjectID, RegKind);

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_GetRegSettingValuePanelParam(KeyID, RegKind, ObjectID, IsNewRec)

  var params:TArray;

  var panelObj = TWebRegSettingValuePanelObject();

  panelObj.RegParm = GetRegParm( KeyID );

  params = makeArray( SQLParam("p_KeyID", KeyID) );

  panelObj.Path = execStoredFunc("RSB_Common.RSI_GetRegKeyPath ", V_STRING, params);

  if(ObjectID != 0)

    if(RegKind == REGVAL_KIND_DPRT)

      panelObj.Dprt = GetTWsDepartmentByCode(ObjectID);

    else

      if (RegKind == REGVAL_KIND_OPER)

        panelObj.Person = FindPersonByNumber(ObjectID);

      end;

    end;

    if(IsNewRec == false)

      panelObj.RegVal = WebCore_GetRegVal(KeyID, RegKind, ObjectID, panelObj.RegParm.RegType);

      if(panelObj.RegVal.FlagValue == true)

        panelObj.IsYesRegVal = true;
        panelObj.IsNoRegVal = false;

      else

        panelObj.IsYesRegVal = false;
        panelObj.IsNoRegVal = true;

      end;

    end;

  end;

  if(IsNewRec == true)

    panelObj.RegVal = TWebRegVal();

    panelObj.RegVal.KeyID           = KeyID;
    panelObj.RegVal.RegKind         = RegKind;
    panelObj.RegVal.ObjectID        = ObjectID;
    panelObj.RegVal.BlockUserValue  = false;
    panelObj.RegVal.ExpDep          = 1;
    panelObj.RegVal.IntValue        = 0;
    panelObj.RegVal.DoubleValue     = 0.00;
    panelObj.RegVal.StringValue     = "";
    panelObj.RegVal.FlagValue       = false;
    panelObj.IsYesRegVal = false;
    panelObj.IsNoRegVal = true;

  end;

  return panelObj;

end;

macro WebCore_HasObjectRegSettingValue(KeyID, RegKind, ObjectID)

  var res = false;

  var cmd, rs;

  var query = " select T_KEYID, "
              "   T_REGKIND, "
              "   T_OBJECTID, "
              "   T_BLOCKUSERVALUE, "
              "   T_EXPDEP "
              " from dregval_dbt "
              "  where t_keyid = ? and t_regkind = ? and t_objectid = ? ";

  cmd = RsdCommand(query);

  cmd.addParam( "", RSDBP_IN, KeyID );
  cmd.addParam( "", RSDBP_IN, RegKind );
  cmd.addParam( "", RSDBP_IN, ObjectID );

  rs = RsdRecordset(cmd);

  if(rs.movenext)

    res = true;

  end;

  return res;

end;

macro WebCore_SaveRegSettingValue(RegType, RegValObj, IsNewRec )

  var res = false;

  ResponseBuilder = WebResponseBuilder();

  var ErrorMessage : WsErrorMessage = WsErrorMessage();

  file rparm("regparm.dbt");
  file rval("regval.dbt");

  if(IsNewRec == true)

    rval.KeyID          = RegValObj.KeyID;
    rval.RegKind        = RegValObj.RegKind;
    rval.ObjectID       = RegValObj.ObjectID;

    if(RegValObj.BlockUserValue == true)
      rval.BlockUserValue      = "X";
    else
      rval.BlockUserValue      = "";
    end;

    rval.ExpDep         = RegValObj.ExpDep;
    rval.LIntValue      = 0;
    rval.LDoubleValue   = 0.00;

  else

    rval.KeyID          = RegValObj.KeyID;
    rval.RegKind        = RegValObj.RegKind;
    rval.ObjectID       = RegValObj.ObjectID;

    res = GetEQ(rval);

    if(RegValObj.BlockUserValue == true)
      rval.BlockUserValue      = "X";
    else
      rval.BlockUserValue      = "";
    end;

    rval.ExpDep         = RegValObj.ExpDep;

  end;

  var RegValue;

  if(RegType == EREGPARMTYPE_INTEGER)

    RegValue = RegValObj.IntValue;

  end;

  if(RegType == EREGPARMTYPE_DOUBLE)

    RegValue = RegValObj.DoubleValue;

  end;

  if(RegType == EREGPARMTYPE_STRING)

    RegValue = RegValObj.StringValue;

  end;

  if(RegType == EREGPARMTYPE_FLAG)

    RegValue = RegValObj.FlagValue;

  end;

  rparm.KeyID = RegValObj.KeyID;

  res = GetEQ(rparm);

  if(res == true)

    res = CB_SaveRegval(rparm, rval, RegValue);

  end;

  if(res != true)

    AddErrorMessage(@ErrorMessage);

  end;

  responseBuilder.SetUserObject(res);

  return responseBuilder.Result;

end;

macro WebCore_GetRegSettingUserValuesGridItemList(KeyID, RegType, Branch, UseHierarchy)

  var cmd, rs;

  var Arr = TArray;

  var query_select =
  " SELECT dp.t_Code AS DprtCode, "
  "        pt.t_Name AS DprtName, "
  "        prs.t_Oper AS UserNum, "
  "        prs.t_Name AS UserName, "
  "        rgv.t_blockuservalue AS IsBlocked, "
  "        RSB_Common.RSI_GetRegValue (rgv.t_KeyID, "
  "                                    " + REGVAL_KIND_OPER + ", "
  "                                    prs.t_Oper, "
  "                                    " + RegType + " ) "
  "         AS StrRegValue ";
  var query_from =
  "  FROM dregval_dbt rgv "
  "       JOIN dperson_dbt prs ON prs.t_Oper = rgv.t_ObjectID "
  "       JOIN ddp_dep_dbt dp ON dp.t_Code = prs.t_CodeDepart "
  "       JOIN dparty_dbt pt ON pt.t_PartyID = dp.t_PartyID ";
  var query_where =
  " WHERE     rgv.t_keyid = " + KeyID + " "
  "       AND rgv.t_regkind = " + REGVAL_KIND_OPER;
  if(UseHierarchy == true)
    query_where = query_where +
  "       AND prs.t_CodeDepart IN "
  "              (    SELECT dp2.t_Code "
  "                     FROM ddp_dep_dbt dp2 "
  "               START WITH dp2.t_Code = " + Branch + " "
  "               CONNECT BY PRIOR dp2.t_Code = dp2.t_ParentCode) ";
  else
    query_where = query_where +
  "       AND prs.t_CodeDepart = " + Branch;
  end;
  
  var priv = 0;
  
  if({cTypePerson} == TP_ADM_SEC)

    priv = 71;

  else

    priv = 72;

  end;
  
  var privFROM : string = "";
  var privWHERE : string = "";
  
  if( GetObjectRestriction(privWHERE, priv, {oper}, "prs", 0,"person.dbt", privFROM ) )
    if(privFROM != "")
      privFROM = ", " + privFROM + " ";
    end;
    if(privWHERE != "")
      privWHERE = " AND " + privWHERE;
    end;
  end;
 
  var query = query_select + query_from + privFROM + query_where + privWHERE + " ORDER BY rgv.t_objectid ";

  cmd = RsdCommand(query);

  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWebRegSettingUserValuesGridItem;
    
    obj.DprtCode    = SQL_ConvType( rs.value("DprtCode") );
    obj.DprtName    = SQL_ConvType( rs.value("DprtName") );
    obj.UserNum     = SQL_ConvType( rs.value("UserNum") );
    obj.UserName    = SQL_ConvType( rs.value("UserName") );
    obj.IsBlocked   = SQL_ConvTypeBool( rs.value("IsBlocked") );
    obj.StrRegValue = SQL_ConvType( rs.value("StrRegValue") );

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;

macro WebCore_GetRegSettingValueUserList(KeyID, Branch, FilterMode)

  var cmd, rs;

  var Arr = TArray;
  
  var PrivID;
  
  if({cTypePerson} == TP_ADM_SEC)

    PrivID = 5;

  else

    PrivID = 0;

  end;

  var query =
  "  SELECT t.t_Oper AS Num, "
  "         t.t_Name AS Name_, "
  "         t.t_CodeDepart AS DprtNode, "
  "         NVL (dp_dep.t_Name, CHR (1)) AS DprtName, "
  "         DECODE (NVL (persondeputy.t_Oper, 0), 0, CHR (0), 'X') AS HasDeputy "
  "    FROM dperson_dbt t, ddp_dep_dbt dp_dep, dpersondeputy_dbt persondeputy "
  "   WHERE     t.t_CodeDepart = dp_dep.t_Code(+) "
  "         AND t.t_Oper = persondeputy.t_Oper(+) AND " + SetOperFilterCondition(Branch, FilterMode, PrivID) +
  "         AND t.t_Oper NOT IN "
  "                (SELECT rgv.t_ObjectID "
  "                   FROM dregval_dbt rgv "
  "                  WHERE rgv.t_KeyID = " + KeyID + " AND rgv.t_RegKind = " + REGVAL_KIND_OPER + ") "
  "ORDER BY t.t_Oper ";

  cmd = RsdCommand(query);

  cmd.NullConversion = true;

  rs = RsdRecordset(cmd);

  while(rs.movenext)

    var obj = TWsPerson;
    
    obj.Number    = SQL_ConvType( rs.value("Num") );
    obj.Name_     = SQL_ConvType( rs.value("Name_") );
    obj.DprtNode  = SQL_ConvType( rs.value("DprtNode") );
    obj.DprtName  = SQL_ConvType( rs.value("DprtName") );
    obj.HasDeputy = SQL_ConvTypeBool( rs.value("HasDeputy") );

    Arr[Arr.size] = obj;

  end;

  return Arr;
  
end;