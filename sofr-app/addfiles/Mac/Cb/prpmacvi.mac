/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmacvi.mac                                       */
/*                                                                      */
/*  Описание       : Печать аккредитива (WR)                            */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 27.10.04                                           */
/*                                                                      */
/************************************************************************/
import prpm, gnd120p;
import "BnkTemplReport.mac";


class (BnkTemplReport)PayAkkr0401060Report(PayerP:TPartyProperties, ReceiverP:TPartyProperties)

   var ReceiverProps = ReceiverP;
   var PayerProps = PayerP;

   var pmpaym   :TRecHandler = TRecHandler( "pmpaym.dbt",   "bank.def" );
   var debet    :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var credit   :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var pmrmprop :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   var pmakkr   :TRecHandler = TRecHandler( "pmakkr.dbt",   "bank.def" );
   var ftyp     :TBFile = TBFile("typeac.dbt",   "R", 0, "typeac.dbt",   "bank.def");
   
   copy( pmpaym    , pr_pmpaym   );
   copy( debet     , pr_debet    );
   copy( credit    , pr_credit   );
   copy( pmrmprop  , pr_pmrmprop );
   copy( pmakkr    , pr_pmakkr   );
   copy( ftyp      , ftypeac0    );

   macro Create()
      var accType       :string,
      condition         :string;
  
      record rpspayord( "pspayord.dbt", "bank.def" );
      Copy(rpspayord, pr_pspayord);

      ftyp.rec.iNumType = 160;
      ftyp.rec.Type_Account = pmakkr.rec.Type;
      if(ftyp.getEQ())
        accType = ftyp.rec.Contens;
      else
        accType = "";
      end;
      condition = IfThenElse( pmakkr.rec.PayCondition == 0, НазваниеСАкцептом, НазваниеБезАкцепта );

      var pINN, rINN;
      splitINN_KPP(PayerProps.INN, pINN);
      splitINN_KPP(ReceiverProps.INN, rINN);

      SetSheetName( pmrmprop.rec.Number );

      SetValues( "Number", pmrmprop.rec.Number,
                 "Data", string(pmrmprop.rec.Date:f),
                 "PaymentKind", GetPaymentKind(pmrmprop.rec.PaymentKind),
                 "pINN", pINN,
                 "SumInPrint", RubToStrAlt(pmpaym.rec.Amount),
                 "Summa", string(pmpaym.rec.Amount:f),
                 "PayerName", PayerProps.Name,
                 "PayerAccount", PayerProps.Account,
                 "PayerBankName", PayerProps.BankName,
                 "BIC_PB", PayerProps.BankBIC,
                 "PayerBankCorrAccount", PayerProps.BankCorrAccount,
                 "ReceiverBankName", ReceiverProps.BankName,
                 "ReceiverBankBIC", ReceiverProps.BankBIC,
                 "ReceiverBankCorrAccount", ReceiverProps.BankCorrAccount,
                 "rINN", rINN,
                 "ReceiverAccount", ReceiverProps.Account,
                 "ReceiverName", ReceiverProps.Name,
                 "ShifrOper", pmrmprop.rec.ShifrOper,
                 "ShifrNazn", "",
                 "ShifrCode", "",
                 "Srok", string(pmakkr.rec.Date:f),
                 "ResultPole", "",
                 "Ground", InterpretGround( pmrmprop.rec.Ground ),
                 "KindAccred", accType, 
                 "Uslovie", condition,
                 "PayPredst", pmakkr.rec.Representation,
                 "DopUsl", pmakkr.rec.AddCondition,
                 "ClientAccount", string(pmakkr.rec.AccRealReceiver:l),
                 "OtBankPl", string(pmpaym.rec.PayerBankMarkDate),
                 "AddDocs", string(akkr_adddocs)
              );

   end;
   
   initBnkTemplReport("PayAkkr0401063.xls");
end;

MACRO PrintAkkreditivRSF( ncopy:integer ):bool

  var PayerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  return PayAkkr0401060Report(PayerProps, ReceiverProps).Print();

END;