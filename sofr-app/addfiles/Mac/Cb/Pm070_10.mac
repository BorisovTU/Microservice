/*
$Name:             Pm070_10.mac
$Module:           РКО, ББ
$Description:      Шаг "Переоформление платежа в конечном филиале"
*/
//-----------------------------------------------------------------------------
// Блок      : 29001 - "Переоформление межфилиального платежа"
// Шаг       : 10    - "Переоформление платежа в конечном филиале"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------

import InsCarryDoc, PaymInter, "globals.mac", "bnk_ptlib.mac", PTInter, FIInter, 
       BankInter, "pm_const.mac", OprInter, "pmlib.mac", "pm_common.mac";

var PaymentObj : RsbPayment;

private macro GetPrimDoc : object
  var PrimDoc : object = null;

  if( (PaymentObj.PayerFIID == NATCUR) and
      (PaymentObj.ReceiverFIID == NATCUR) and 
      (PaymentObj.BaseFIID == NATCUR) )

    PrimDoc = RsbBankPayment();
    PrimDoc.Status = MEMORDER_STATUS_POST;
    PrimDoc.Origin = MEMORDER_FDOC_MFR;
  else
    PrimDoc = RsbBbCpOrder();
    PrimDoc.CurrentState = CP_ST_DEFERRED;
    PrimDoc.Origin = CP_OR_MFR;
  end;

  PrimDoc.Kind_Operation = 0;
  PrimDoc.Oper = {oper};

  return PrimDoc;
end;

private macro FillPaymFields(NewPaym : RsbPayment)
  NewPaym.Number = PaymentObj.Number;
  NewPaym.PaymStatus = PM_PREPARING;

  NewPaym.Date = PaymentObj.date;
  NewPaym.PayerBankEnterDate = PaymentObj.PayerBankEnterDate;

  NewPaym.Oper = {Oper};
  NewPaym.BaseAmount = PaymentObj.BaseAmount;
  NewPaym.BaseFIID = PaymentObj.BaseFIID;
  NewPaym.ReceiverAmount = PaymentObj.ReceiverAmount;
  NewPaym.ReceiverFIID = PaymentObj.ReceiverFIID;
  NewPaym.PaymentKind = PaymentObj.PaymentKind;
  NewPaym.ShifrOper = PaymentObj.ShifrOper;
  NewPaym.Priority = PaymentObj.Priority;
  NewPaym.Ground = PaymentObj.Ground;
  NewPaym.TaxAuthorState = PaymentObj.TaxAuthorState;
  NewPaym.BTTTICode = PaymentObj.BTTTICode;
  NewPaym.OKATOCode = PaymentObj.OKATOCode;
  NewPaym.TaxPmGround = PaymentObj.TaxPmGround;
  NewPaym.TaxPmPeriod = PaymentObj.TaxPmPeriod;
  NewPaym.TaxPmNumber = PaymentObj.TaxPmPeriod;
  NewPaym.TaxPmDate = PaymentObj.TaxPmDate;
  NewPaym.TaxPmType = PaymentObj.TaxPmType;
  NewPaym.PayType = PaymentObj.PayType;
end;

private macro CheckPrimOrigin(IsPrimOriginTranzit : @bool, IsPrimOriginReturnUnkn : @bool)
  var TableName : string = "", OrigTranzit : integer = 0, OrigReturnUnkn : integer = 0;

  if(PaymentObj.DocKind == DLDOC_BANKPAYMENT)
    TableName = "dmemorder_dbt";
    OrigTranzit = MEMORDER_FDOC_TRANZIT;
    OrigReturnUnkn = MEMORDER_FDOC_RETURN;
  elif(PaymentObj.DocKind == BBANK_CPORDER)
    TableName = "dbbcpord_dbt";
    OrigTranzit = CP_OR_TRANZIT;
    OrigReturnUnkn = CP_OR_RMRETURN;
  else
    IsPrimOriginTranzit = false;
    IsPrimOriginReturnUnkn = false;
    return;
  end;

  var Origin : integer = 0;
  var rs : RsdRecordset = execSQLselectPrm
    ( "select t_Origin "
      "  from " + TableName +
      " where t_OrderID = :OrderID ",
      SQLParam("OrderID", PaymentObj.PaymentID)
    );
  if(rs and rs.moveNext())
    Origin = rs.value(0);
  else
    RunError("Не найдена запись о первичном документе в БД");
  end;

  IsPrimOriginTranzit = (Origin == OrigTranzit);
  IsPrimOriginReturnUnkn = (Origin == OrigReturnUnkn);
end;

private macro GetPmPayerINN(PaymentID : integer) : string
  var rs : RsdRecordset = execSQLselectPrm
    ( "select t_PayerINN "
      "  from dpmrmprop_dbt "
      " where t_PaymentID = :PaymentID ",
      SQLParam("PaymentID", PaymentID)
    );
  if(rs and rs.moveNext())
    return rs.value(0);
  end;

  RunError("Не найдена запись о свойствах R-макета платежа");
end;

private macro GetNewPaymPayerNameINN(PayerName : @string, PayerINN : @string)
  var ParentPaymentID : integer = getInitialPaymentID(PaymentObj.PaymentID, PMLINK_KIND_RETREDIR),
      IsLinkRetRedir : bool = ( ParentPaymentID > 0 ), 
      IsPrimOriginTranzit : bool = false,
      IsPrimOriginReturnUnkn : bool = false;
  if(IsLinkRetRedir)
    CheckPrimOrigin(@IsPrimOriginTranzit, @IsPrimOriginReturnUnkn);
  end;

  // PayerINN
  if(IsLinkRetRedir and IsPrimOriginTranzit)
    PayerINN = GetPmPayerINN(ParentPaymentID);
  else
    PayerINN = PaymentObj.PayerINN;
  end;

  // PayerName
  if( IsLinkRetRedir and (IsPrimOriginTranzit or IsPrimOriginReturnUnkn) )
    PayerName = PaymentObj.PayerName;
  else
    PayerName = PaymentObj.PayerName + " сч. " + PaymentObj.PayerAccount + ", " +
                PaymentObj.PayerBankName;
  end;
end;

private macro SetDebitPI(NewPaym : RsbPayment)
  var PayerINN : string = "", PayerName : string = "";
  GetNewPaymPayerNameINN(@PayerName, @PayerINN);

  var PayerBankID : integer = GetDprtPartyID(PaymentObj.EndDepartment),
      PayerBankCodeKind : integer = PTCK_BIC,
      PayerBankCode : string = ПолучитьКодСубъекта(PayerBankID, PayerBankCodeKind, null, 1),
      PayerBankName : string = Bnk_GetPartyName(PayerBankID);

  var stat : integer = NewPaym.SetPayerPI
    ( PAYMENTS_GROUP_UNDEF, 
      PayerBankID, 
      PayerBankCodeKind, 
      PayerBankCode, 
      PayerBankName, 
      "", // CorrAcc 
      PaymentObj.FuturePayerFIID, 
      PaymentObj.Chapter, 
      PaymentObj.FuturePayerAccount, 
      null, 
      PayerName, 
      PayerINN );

  if(stat)
    RunError("Ошибка при установке платежной инструкции по дебету");
  end;
end;

private macro SetCreditPI(NewPaym : RsbPayment)
  var stat : integer = NewPaym.SetReceiverPI
    ( PaymentObj.ReceiverGroup, 
      PaymentObj.ReceiverBankID, 
      PaymentObj.ReceiverBankCodeKind, 
      PaymentObj.ReceiverBankCode, 
      PaymentObj.ReceiverBankName, 
      PaymentObj.ReceiverCorrAccNostro,
      PaymentObj.ReceiverFIID, 
      PaymentObj.Chapter, 
      PaymentObj.ReceiverAccount, 
      PaymentObj.Receiver, 
      PaymentObj.ReceiverName, 
      PaymentObj.ReceiverINN,
      PaymentObj.ReceiverCodeKind, 
      PaymentObj.ReceiverCode, 
      PaymentObj.GetCREDIT().rec.Corschem,
      PaymentObj.GetCREDIT().rec.CorrPosType
    );

  if(stat)
    RunError("Ошибка при установке платежной инструкции по кредиту");
  end;

  if( (NewPaym.GetCREDIT().rec.CorrPosType != PM_CORRPOS_TYPE_SYSTEM) or
      (NewPaym.OutTransport <= 0) or
      (NewPaym.OutTpSchem <= 0) or
      (NewPaym.OutRlsForm <= 0) or
      (NewPaym.OutTransferDate == date(0,0,0))
    )
    NewPaym.OutTransport = PaymentObj.OutTransport;
    NewPaym.OutTpSchem = PaymentObj.OutTpSchem;
    NewPaym.OutRlsForm = PaymentObj.OutRlsForm;
    NewPaym.OutTransferDate = PaymentObj.OutTransferDate;
  end;

end;

private macro CreateNewPaym : RsbPayment
  var Doc : object = GetPrimDoc();
  var NewPaym : RsbPayment = Doc.Payment;

  FillPaymFields(NewPaym);

  SetDebitPI(NewPaym);

  SetCreditPI(NewPaym);

  // Т.к. нужен Department, то даты устанавливаем после SetPI
  NewPaym.ValueDate = PM_GetOperDay_Balance(NewPaym.Department);
  NewPaym.PayDate = NewPaym.ValueDate;

  if( NewPaym.Actuate() != 0 )
    RunError("Ошибка при актуализации платежа");
  end;

  CopyPaymentNotes(NewPaym, PaymentObj);
  CopyPaymentCategories(NewPaym, PaymentObj);

  // Запустить операцию обработки по дочернему платежу
  Doc.LaunchOper = true;

  return NewPaym;
end;

MACRO ExecuteStep()
  // Создать дочерний документ по текущему
  var NewPaymentObj : RsbPayment = CreateNewPaym();

  // Связать созданный платеж с обрабатываемым по виду связи "Переоформление межфилиального платежа"
  if( PaymentObj.LinkPayment( NewPaymentObj, PMLINK_KIND_MFPAYMENT ) )
    msgbox( GetErrMsg() );
    return 1;
  end;

  return 0;

ONERROR(er)
  ExeptionMessage( er );
  return 1;
END;

