/*
 $Name: mpckvit.mac
 $Module: RSACCOUNT
 $Description: Квитовка
 */
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : mpckvit.mac

  Библиотека       : RSACCOUNT

  Описание         : Квитовка

  Программист      : Kirakozov Michael (KMB)

  Создан           : 26.02.2009
-------------------------------------------------------------------------*/
IMPORT RsbDataSet, CTInter, PTInter, globals, cryptdlm, CurrInter, 
       mpcactacc, mpccarry, mpclb;


PRIVATE MACRO GetPaymentValueDate(PaymentID, ValueDate:@date)
var   cmd, rs;
   cmd = RSDCommand ("select T_VALUEDATE from DPMPAYM_DBT where T_PAYMENTID = ? ");
   cmd.addParam( "", RSDBP_IN, PaymentID );
   cmd.execute();
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      ValueDate = rs.valuedate;
   end;
   
END;



PRIVATE MACRO GetPaymentSumInCntCurr(ContractID, ChildPaymentID, ChildSum:@money, AmountSum:@money)
var   cmd, rs, cmd2, rs2,
      FIID = 0, PayFIID = 0, ContrFIID = 0, 
      Amount = $0, PayAmount = $0,
      stat = 0;


   cmd = RSDCommand ("select T_FIID, T_PAYFIID, T_AMOUNT, T_PAYAMOUNT " + 
         "from DPMPAYM_DBT where T_PAYMENTID = ? ");
   cmd.addParam( "", RSDBP_IN, ChildPaymentID );
   cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      FIID = rs.fiid;
      PayFIID = rs.payfiid;
      Amount = rs.amount;
      PayAmount = rs.payamount;
   else 
      stat = 1;
   end;

   if (stat == 0)
      cmd2 = RSDCommand ("select T_FIID from DPRCCONTRACT_DBT where T_CONTRACTID = ? ");
      cmd2.addParam( "", RSDBP_IN, ContractID );
      cmd2.execute();
   
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())
         ContrFIID = rs2.fiid;
      end;


      if ((ContrFIID != FIID) and (ContrFIID != PayFIID))
         if (ConvSumCross (ChildSum, Amount, {curdate}, FIID, ContrFIID) == false)
            stat = 1;
         end;
      elif (ContrFIID == FIID)
         ChildSum = Amount;
      else
         ChildSum = PayAmount;
      end;

      AmountSum = Amount;
      
   end;
   
   return stat;
END;



PRIVATE MACRO ActuateAccKvit(ContractID, CatID, Account:@string, FIID:@integer, Chapter:@integer)
   var   cmd,rs,
         CatCode = "",
         CodeCurr,Chapt,
         stat = 0;

   cmd = RSDCommand ("select T_CODE from DMCCATEG_DBT where T_ID = ? ");
   cmd.addParam( "", RSDBP_IN, CatID );
   stat = cmd.execute();
   
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CatCode = rs.code;   
   end;

   Account = MPC_ActAccountsForFD( ContractID, CatCode, @CodeCurr, @Chapt);
   
   if (ValType(Account) == V_UNDEF)
      Account = "";
   end;

   if (ValType(CodeCurr) != V_UNDEF)
      FIID = CodeCurr;
   end;

   if (ValType(Chapt) != V_UNDEF)
      Chapter = Chapt;
   end;
   
   return stat;
END;


// Функция, вызываемая после полной обработки документа РБ при автоматической квитовке
MACRO PrcCorrSchedFactSum(PaymentID )
   var   prccontract = TBfile("prccontract.dbt"),
         pmpaym = TBfile("pmpaym.dbt"),
         cmd,rs,
         CntSchedID = 0, ContractID = 0, CntOprDocID = 0,
         CntCurrSum, AmountSum, SumFact, SumCalc,
         DebetCarrySum,CreditCarrySum,
         PayState,OldPayState,
         TransSchema,
         ValueDate, DelayDate, BeginDate, EndDate, OldRealDate,
         Debet="",Credit="",Credit3,Credit4, ReceiverAccount,      
         CreditCatID = 0,CreditTypeID,
         CreditFIID,DebetFIID,ContrFIID,
         Chapter = 0,
         fd,
         SumChargeFact, 
         SumChargeBalance,
         SumChargeNonBalance,
         НазначениеПроводки = "",
         ЗначениеНастройки,
         stat = 0;

   record acc_buf (account);
   if (stat == 0)
      pmpaym.KeyNum = 0;  
      pmpaym.rec.paymentid = PaymentID;
      if (not pmpaym.GetEQ)
         msgbox ("Не найден платеж");
         stat = 1;
      end;
   end;

   if(stat == 0)
     cmd = RSDCommand ("select q.T_CNTSCHEDID, q.T_CNTOPRDOCID, s.T_CONTRACTID, s.T_SUMFACT, s.T_DATEREAL, " + 
           "s.T_SUMCALC, s.T_DELAYDATE, s.T_PERIODBEGINDATE, s.T_PERIODENDDATE, s.T_PAYSTATE " + 
           "from DPRCQUIT_DBT q, DPRCCNTSCHED_DBT s " +
           "where q.T_CNTSCHEDID = s.T_CNTSCHEDID and q.T_PAYMENTID = ? ");
     cmd.addParam( "", RSDBP_IN, PaymentID );
     cmd.execute();
  
     rs = TRsbDataSet(cmd);
     if (rs.movenext())
        CntSchedID     = rs.cntschedid;
      CntOprDocID    = rs.cntoprdocid;
        ContractID     = rs.contractid;
        SumFact        = rs.sumfact;
        SumCalc        = rs.sumcalc;
        DelayDate      = rs.delaydate;
        BeginDate      = rs.periodbegindate;
        EndDate        = rs.periodenddate;
        OldPayState    = rs.paystate;
        OldRealDate    = date(rs.datereal);
     else 
        msgbox ("Ошибка при поиске графика");
        stat = 1;
     end;
   end;

   if (stat == 0)
      prccontract.KeyNum = 0; 
      prccontract.rec.contractid = ContractID;
      if (not prccontract.GetEQ)
         msgbox ("Не найден процентный договор");
         stat = 1;
      end;
   end;

   ReceiverAccount = pmpaym.rec.ReceiverAccount; 

   if (stat == 0)   
      GetPaymentValueDate(PaymentID, @ValueDate);
      stat = GetPaymentSumInCntCurr(ContractID, PaymentID, @CntCurrSum, @AmountSum);
   end;

   if (stat == 0)
      SumFact = SumFact + CntCurrSum;
      
      SumFact = abs(SumFact);
      SumCalc = abs(SumCalc);
      
      if (SumFact == SumCalc)
         PayState = PRC_PAYSTATUS_PAID;
      elif (SumFact < SumCalc)
         PayState = PRC_PAYSTATUS_PARTLY_PAID;
      else
         msgbox("Ошибка! Сквитованная сумма больше рассчитанной");
         return 1;
      end;         
   end;

   if ( (stat == 0) and (PrcSetCntSchedFactSumByID(CntSchedID, money(SumFact), ValueDate, OldRealDate) != 0) )
      return 1;
   end;
   
   if ( (stat == 0) and (PrcSetOperSum(CntOprDocID, money(SumFact)) != 0) )
      return 1;
   end;
   

   // Определяем схему отражения проводок в учете
   if (stat == 0)
      PrcDefineTransSchema(ContractID, CntSchedID,ValueDate, @TransSchema, 
                         @SumChargeFact, @SumChargeBalance, @SumChargeNonBalance);
   end;

   GetAccountFromList(ContractID, 3, @Credit3, @CreditCatID, @CreditFIID, @Chapter);  
   GetAccountFromList(ContractID, 4, @Credit4, @CreditCatID, @CreditFIID, @Chapter);
   
   // Для схем "Внебаланс" и "Внебаланс.Просрочка" выполняем проводки
   if ((stat == 0) and ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL) or 
                         (TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL_OD) or                          
                         ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL) and (ReceiverAccount == Credit3)) or
                         ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD) and (ReceiverAccount == Credit4)))
       )        

      if ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL) or 
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL) and (ReceiverAccount == Credit3))
         )
         Chapter = 3;
         CreditTypeID = 5;
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);  
         НазначениеПроводки = "Списание начисленных процентов за период с " + BeginDate +
                              " по " + EndDate + " для договора " +  prccontract.rec.Number + " по счету " +  prccontract.rec.Account ;                           
      end;

      if ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL_OD) or 
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD) and (ReceiverAccount == Credit4))
         )
         Chapter = 3;
         CreditTypeID = 6;
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);                              
         НазначениеПроводки = "Списание просроченных начисленных процентов " +
                              "для договора " +  prccontract.rec.Number + " по счету " +  prccontract.rec.Account ;
      end;

      if ((Credit == "") and (CreditCatID == 0))
         msgbox("Не определен счет кредита и не задана соотв.КУ для договора");
         stat = 1;
      else
         if (Credit == "")
            ActuateAccKvit(ContractID, CreditCatID, @Credit, @CreditFIID, @Chapter);
            if (Credit == "")
               msgbox("Не удалось актуализировать счет кредита");
               stat = 1; 
            else
               PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
            end;
         end;
      end;

      fd = PrcContractFD(ContractID);
      Debet = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        acc_buf,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            /* дата начала действия счета */
                                       );
      DebetFIID = acc_buf.code_currency;

      ContrFIID = prccontract.rec.FIID;

      if (DebetFIID != ContrFIID)
         ConvSum(DebetCarrySum, CntCurrSum, {curdate}, ContrFIID, DebetFIID );
      else
         DebetCarrySum  = CntCurrSum;
      end;
      if (CreditFIID != ContrFIID)
         ConvSum(CreditCarrySum, CntCurrSum, {curdate}, ContrFIID, CreditFIID );
      else
         CreditCarrySum = CntCurrSum; 
      end;
      
      
      if (stat == 0)   
         stat = Prc_DocCarry( 
                              Chapter,                   // Глава
                              DebetFIID,                 // Дебет
                              CreditFIID,                         // Валюта
                              Debet,                     // Дебет
                              Credit,                    // Кредит
                              DebetCarrySum, 
                              CreditCarrySum,            // сколько
                              ValueDate,                 // За какое число
                              "09",
                              NULL,
                              0,                            // CarryResult
                              НазначениеПроводки
                           );
         if (stat)
            msgbox("Ошибка формирования проводки");
         end;            
      end;
      
   end;  
   
   if (stat == 0)
      if (PrcUpdateQuitLink(CntSchedID, PaymentID, CntCurrSum, AmountSum, ValueDate, 1, CntOprDocID) != 0)
         return 1;
      end;
   end;

   return 0;
END;


// Установка связи между оплатой и РДД
MACRO PrcKvitLinkPayments(ParentPaymentID, ChildPaymentID)
   var   cmd,rs,cmd2,
         CntSchedID = 0, ContractID = 0,
         ChildSum, AmountSum,
         stat = 0;

   cmd = RSDCommand ("select q.T_CNTSCHEDID, s.T_CONTRACTID " + 
         "from DPRCQUIT_DBT q, DPRCCNTSCHED_DBT s, DPRCCNTOPR_DBT p where q.T_PAYMENTID = ? and s.T_CNTSCHEDID = q.T_CNTSCHEDID " +
         " and q.T_CNTOPRDOCID = p.T_DOCID");
   cmd.addParam( "", RSDBP_IN, ParentPaymentID );
   cmd.execute();
    
   rs = TRsbDataSet(cmd);
   if (rs.movenext())
      CntSchedID = rs.cntschedid;
      ContractID = rs.contractid;
   else 
      stat = 1;
   end;

   if (stat == 0)   
      stat = GetPaymentSumInCntCurr(ContractID, ChildPaymentID, @ChildSum, @AmountSum);
   end;

   if (stat == 0)
      cmd = RSDCommand ("insert into DPRCQUIT_DBT set T_CNTSCHEDID = ?, T_PAYMENTID = ?, " +
                     "T_STATE = 0, T_DATE = ?, T_SUMCNT = ?, T_SUMDOC = ?");
      cmd.addParam( "", RSDBP_IN, CntSchedID );
      cmd.addParam( "", RSDBP_IN, ChildPaymentID );
      cmd.addParam( "", RSDBP_IN, {curdate} );
      cmd.addParam( "", RSDBP_IN, ChildSum);
      cmd.addParam( "", RSDBP_IN, AmountSum);
      stat = cmd.execute();
   end;


END;

// Замена счета получателя в документах
MACRO PrcReceiverAccountChange(ContractID, DocID, OperDate)
   var   prccontract = TBfile("prccontract.dbt"),
         cntsched = TBfile("prccntsched.dbt"),
         prccntopr = TBfile("prccntopr.dbt"),
         PaymentID = 0,
         OperKind = 0,
         Account_Cat1, Account_Cat2, Account_Cat3, Account_Cat4,
         CatID1, CatID2, CatID3, CatID4, 
         FIID1, FIID2, FIID3, FIID4, 
         Chapter1, Chapter2, Chapter3, Chapter4,
         Payment:RsbPayment,
         CryptoAPI = RsCryptoAPI(), rsms,
         cmd,rs,cmd2,
         stat;

   prccntopr.KeyNum = 0;  
   prccntopr.rec.DocID = DocID;
   if (not prccntopr.GetEQ)
      msgbox ("Не найдена операция");
      return 1;
   end;

   OperKind = prccntopr.rec.OperKind;

   if (OperKind == 4524)   // реклассификация
      cmd = RSDCommand ("select q.T_PAYMENTID from DPRCQUIT_DBT q, DPRCCNTSCHED_DBT s " + 
                  "where q.T_CNTSCHEDID = s.T_CNTSCHEDID and s.T_CONTRACTID = ? and q.T_STATE = 0");
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.execute();
      
   elif (OperKind == 4525) // вынос на просрочку     
      cmd = RSDCommand ("select q.T_PAYMENTID from DPRCQUIT_DBT q, DPRCCNTSCHED_DBT s " + 
                  "where q.T_CNTSCHEDID = s.T_CNTSCHEDID and s.T_CONTRACTID = ? and q.T_STATE = 0 " +
                  "and s.T_DELAYDATE <= ?");
      cmd.addParam( "", RSDBP_IN, ContractID );
      cmd.addParam( "", RSDBP_IN, OperDate );
      cmd.execute();
      
   else
      msgbox ("Неверный тип операции");
      return 1;
   end;
      
   rs = TRsbDataSet(cmd);
   
   // получаем лицевые счета по процентному договору 
   GetAccountFromList(ContractID, 1, @Account_Cat1, @CatID1, @FIID1, @Chapter1); 
   GetAccountFromList(ContractID, 2, @Account_Cat2, @CatID2, @FIID2, @Chapter2); 
   GetAccountFromList(ContractID, 3, @Account_Cat3, @CatID3, @FIID3, @Chapter3); 
   GetAccountFromList(ContractID, 4, @Account_Cat4, @CatID4, @FIID4, @Chapter4); 

   // обходим все платежи
   while (rs.movenext())
      PaymentID = rs.PaymentID;
      Payment = RsbPayment(PaymentID );

      if (OperKind == 4524)   // реклассификация

         if ((Account_Cat3 == "") and (CatID3 == 0)) 
            msgbox("Не определен счет доходов (расходов) срочных процентов |и не задана соотв.КУ для договора");
            return 1;
         else
            if (Account_Cat3 == "")
               ActuateAccKvit(ContractID, CatID3, @Account_Cat3, @FIID3, @Chapter3);
               if (Account_Cat3 == "")
                  msgbox("Не удалось актуализировать счет доходов (расходов) срочных процентов");
                  return 1;
               else
                  PrcSetCntAcc(ContractID, 3, FIID3, Chapter3, Account_Cat3);
               end;   
            end;
         end;

         if ((Account_Cat4 == "") and (CatID4 == 0)) 
            msgbox("Не определен счет доходов (расходов) просроченных процентов |и не задана соотв.КУ для договора");
            return 1;
         else
            if (Account_Cat4 == "")
               ActuateAccKvit(ContractID, CatID4, @Account_Cat4, @FIID4, @Chapter4);
               if (Account_Cat4 == "")
                  msgbox("Не удалось актуализировать счет доходов (расходов) просроченных процентов");
                  return 1;
               else
                  PrcSetCntAcc(ContractID, 4, FIID4, Chapter4, Account_Cat4);
               end;   
            end;
         end;

      
         if (Payment.ReceiverAccount == Account_Cat3)
            
            if ((Account_Cat1 == "") and (CatID1 == 0)) 
               msgbox("Не определен счет срочных процентов для балансового учета |и не задана соотв.КУ для договора");
               return 1;
            else
               if (Account_Cat1 == "")
                  ActuateAccKvit(ContractID, CatID1, @Account_Cat1, @FIID1, @Chapter1);
                  if (Account_Cat1 == "")
                     msgbox("Не удалось актуализировать счет срочных процентов для балансового учета");
                     return 1;
                  else
                     PrcSetCntAcc(ContractID, 1, FIID1, Chapter1, Account_Cat1);
                  end;   
               end;
            end;
            Payment.SetReceiverAccount(FIID1, Chapter1, Account_Cat1);
            
         elif (Payment.ReceiverAccount == Account_Cat4)
            
            if ((Account_Cat2 == "") and (CatID2 == 0)) 
               msgbox("Не определен счет просроченных процентов для балансового учета |и не задана соотв.КУ для договора");
               return 1;
            else
               if (Account_Cat2 == "")
                  ActuateAccKvit(ContractID, CatID2, @Account_Cat2, @FIID2, @Chapter2);
                  if (Account_Cat2 == "")
                     msgbox("Не удалось актуализировать счет просроченных процентов для балансового учета");
                     return 1;
                  else
                     PrcSetCntAcc(ContractID, 2, FIID2, Chapter2, Account_Cat2);
                  end;   
               end;
            end;
            Payment.SetReceiverAccount(FIID2, Chapter2, Account_Cat2);
            
         end;
         
      elif (OperKind == 4525)   // вынос на просрочку

         if ((Account_Cat3 == "") and (CatID3 == 0)) 
            msgbox("Не определен счет доходов (расходов) срочных процентов |и не задана соотв.КУ для договора");
            return 1;
         else
            if (Account_Cat3 == "")
               ActuateAccKvit(ContractID, CatID3, @Account_Cat3, @FIID3, @Chapter3);
               if (Account_Cat3 == "")
                  msgbox("Не удалось актуализировать счет доходов (расходов) срочных процентов");
                  return 1;
               else
                  PrcSetCntAcc(ContractID, 3, FIID3, Chapter3, Account_Cat3);
               end;   
            end;
         end;

         if ((Account_Cat1 == "") and (CatID1 == 0)) 
            msgbox("Не определен счет срочных процентов для балансового учета |и не задана соотв.КУ для договора");
            return 1;
         else
            if (Account_Cat1 == "")
               ActuateAccKvit(ContractID, CatID1, @Account_Cat1, @FIID1, @Chapter1);
               if (Account_Cat1 == "")
                  msgbox("Не удалось актуализировать счет срочных процентов для балансового учета");
                  return 1;
               else
                  PrcSetCntAcc(ContractID, 1, FIID1, Chapter1, Account_Cat1);
               end;   
            end;
         end;
         
         if (Payment.ReceiverAccount == Account_Cat3)
            
            if ((Account_Cat4 == "") and (CatID4 == 0)) 
               msgbox("Не определен счет доходов (расходов) просроченных процентов |и не задана соотв.КУ для договора");
               return 1;
            else
               if (Account_Cat4 == "")
                  ActuateAccKvit(ContractID, CatID4, @Account_Cat4, @FIID4, @Chapter4);
                  if (Account_Cat4 == "")
                     msgbox("Не удалось актуализировать счет доходов (расходов) просроченных процентов");
                     return 1;
                  else
                     PrcSetCntAcc(ContractID, 4, FIID4, Chapter4, Account_Cat4);
                  end;   
               end;
            end;
            Payment.SetReceiverAccount(FIID4, Chapter4, Account_Cat4); 
            
         elif (Payment.ReceiverAccount == Account_Cat1)
            
            if ((Account_Cat2 == "") and (CatID2 == 0)) 
               msgbox("Не определен счет просроченных процентов для балансового учета |и не задана соотв.КУ для договора");
               return 1;
            else
               if (Account_Cat2 == "")
                  ActuateAccKvit(ContractID, CatID2, @Account_Cat2, @FIID2, @Chapter2);
                  if (Account_Cat2 == "")
                     msgbox("Не удалось актуализировать счет просроченных процентов для балансового учета");
                     return 1;
                  else
                     PrcSetCntAcc(ContractID, 2, FIID2, Chapter2, Account_Cat2);
                  end;   
               end;
            end;
            Payment.SetReceiverAccount(FIID2, Chapter2, Account_Cat2);
         end;
      end; 
      
      Payment.CryptoAction( string("Автоматическое_формирование_платежей") );
   end;

   return 0;   
   
END;


/************************************************************************/
/* Ручная квитовка                                                      */
/************************************************************************/
MACRO MpcRunManualKvit(PaymentID, ContractID, CntSchedID, KvitSum, KvitDate, QuitID: @integer )

   var   prccontract = TBfile("prccontract.dbt"),
         cntsched = TBfile("prccntsched.dbt"),
         pmpaym = TBfile("pmpaym.dbt"),
         TransSchema = 0,                   // Схема проводок
         BeginDate, EndDate,                // Даты начала и окончания периода оплаты
         CreditFIID, DebetFIID,
         ContrFIID,
         DocFIID,
         DebetCarrySum, CreditCarrySum,     // Суммы формируемых проводок
         Debet="", Credit="", Credit3, Credit4, ReceiverAccount,      
         CreditCatID = 0, CreditTypeID,
         Chapter = 0,
         fd,
         SumChargeFact, 
         SumChargeBalance,
         SumChargeNonBalance,
         НазначениеПроводки = "",
         ЗначениеНастройки,
         Method,
         tr,
         stat = 0,
         AccTrnID,
         cmd, cmd2, rs2, cmd3,
         DocSum,                       // Сумма квитовки в валюте документа
         NewSumFact, NewPayState;      // Новая фактическая сумма и статус периода оплаты

   record acc_buf (account);
   
   // находим ПД
   prccontract.KeyNum = 0;  
   prccontract.rec.ContractID = ContractID;
   if (not prccontract.GetEQ)
      PrcSendMessage(ContractID, "Не найден договор с ID = " + ContractID);
      return 1;
   end;

   ContrFIID = prccontract.rec.FIID;

   // находим платеж
   pmpaym.KeyNum = 0; 
   pmpaym.rec.paymentid = PaymentID;
   if (not pmpaym.GetEQ)
      msgbox ("Не найден платеж");
     return 1;
   end;   

   DocFIID = pmpaym.rec.FIID;

   // рассчитываем сумму платежа в валюте документа
   if (DocFIID != ContrFIID)
      ConvSum(DocSum, KvitSum, KvitDate, ContrFIID, DocFIID);      
   else
      DocSum = KvitSum;
   end;

    // находим период оплаты
   cntsched.KeyNum = 0; 
   cntsched.rec.CntSchedID = CntSchedID;
   if (not cntsched.GetEQ)
      PrcSendMessage(ContractID, "Не найден период оплаты");
      return 1;
   end;   
   
   BeginDate      = cntsched.rec.periodbegindate;
   EndDate        = cntsched.rec.periodenddate;

   stat = PrcDefineTransSchema(ContractID, CntSchedID, KvitDate, @TransSchema, 
                                @SumChargeFact, @SumChargeBalance, @SumChargeNonBalance);

   GetAccountFromList(ContractID, 3, @Credit3, @CreditCatID, @CreditFIID, @Chapter);  
   GetAccountFromList(ContractID, 4, @Credit4, @CreditCatID, @CreditFIID, @Chapter);
   
   // для схем "Внебаланс" и "Внебаланс.Просрочка" выполняем проводки
   if ((stat == 0) and ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL) or 
                         (TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL_OD) or                          
                         ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL) and (ReceiverAccount == Credit3)) or
                         ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD) and (ReceiverAccount == Credit4)))
       )        

      if ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL) or 
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL) and (ReceiverAccount == Credit3))
         )
         Chapter = 3;
         CreditTypeID = 5;
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);  
         НазначениеПроводки = "Списание начисленных процентов за период с " + BeginDate +
                              " по " + EndDate + " для договора " +  prccontract.rec.Number + " по счету " +  prccontract.rec.Account ;
      end;

      if ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL_OD) or 
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD) and (ReceiverAccount == Credit4))
         )
         Chapter = 3;
         CreditTypeID = 6;
         GetAccountFromList(ContractID, CreditTypeID, @Credit, @CreditCatID, @CreditFIID, @Chapter);   
         НазначениеПроводки = "Списание просроченных начисленных процентов" +
                              " для договора " +  prccontract.rec.Number + " по счету " + prccontract.rec.Account;                           
      end;

      if ((Credit == "") and (CreditCatID == 0))
         msgbox("Не определен счет кредита и не задана соотв.КУ для договора");
         stat = 1;
      else
         if (Credit == "")
            ActuateAccKvit(ContractID, CreditCatID, @Credit, @CreditFIID, @Chapter);
            if (Credit == "")
               msgbox("Не удалось актуализировать счет кредита");
               stat = 1; 
            else
               PrcSetCntAcc(ContractID, CreditTypeID, CreditFIID, Chapter, Credit);
            end;
         end;
      end;

      fd = PrcContractFD(ContractID);
      Debet = MC_FindAndOpenAccountEx("ВнебалСчетКорресп",
                                        fd,
                                        {curdate},
                                        NULL,
                                        MC_OPENACC_CREATE,
                                        acc_buf,
                                        NATCUR,
                                        NULL,
                                        MC_PAIRACCMODE_AUTO,
                                        NULL,
                                        NULL,
                                        null, null, null, null,
                                       {curdate}            /* дата начала действия счета */
                                       );
      DebetFIID = acc_buf.code_currency;
      

      if (DebetFIID != ContrFIID)
         ConvSum(DebetCarrySum, KvitSum, KvitDate, ContrFIID, DebetFIID );
      else
         DebetCarrySum  = KvitSum;
      end;
      if (CreditFIID != ContrFIID)
         ConvSum(CreditCarrySum, KvitSum, KvitDate, ContrFIID, CreditFIID );
      else
         CreditCarrySum = KvitSum; 
      end;
      
      
      if (stat == 0)   
         stat = Prc_DocCarry( 
                              Chapter,                   // Глава
                              DebetFIID,                 // Дебет
                              CreditFIID,                         // Валюта
                              Debet,                     // Дебет
                              Credit,                    // Кредит
                              DebetCarrySum, 
                              CreditCarrySum,            // сколько
                              KvitDate,                 // За какое число
                              "09",
                              NULL,
                              0,                            // CarryResult
                              НазначениеПроводки,
                              0,
                              Method,
                              tr                              
                           );
         if (stat)
            msgbox("Ошибка формирования проводки");         
         end;            
      end;
      
   end; 

   if (stat == 0)
      cmd = RSDCommand ("insert into DPRCQUIT_DBT (T_CNTSCHEDID,T_PAYMENTID,T_STATE, " +
                     " T_DATE,T_SUMCNT,T_SUMDOC) values(?,?,2,?,?,?)");
      cmd.addParam( "", RSDBP_IN, CntSchedID );
      cmd.addParam( "", RSDBP_IN, PaymentID );
      cmd.addParam( "", RSDBP_IN, KvitDate );
      cmd.addParam( "", RSDBP_IN, KvitSum);
      cmd.addParam( "", RSDBP_IN, DocSum);      
      cmd.execute();

      cmd2 = RSDCommand ("select T_QUITID from DPRCQUIT_DBT where T_CNTSCHEDID = ? and T_PAYMENTID = ?");
      cmd2.addParam( "", RSDBP_IN, CntSchedID );
      cmd2.addParam( "", RSDBP_IN, PaymentID );     
      cmd2.execute();
      rs2 = TRsbDataSet(cmd2);
      if (rs2.movenext())
         QuitID = rs2.quitid;
      end;

      if ((TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL) or 
          (TransSchema == PRC_ACCSCHEMA_RAZM_NONBAL_OD) or                          
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL) and (ReceiverAccount == Credit3)) or
          ((TransSchema == PRC_ACCSCHEMA_RAZM_BAL_NONBAL_OD) and (ReceiverAccount == Credit4)))
         
         AccTrnID = tr.AccTrnID;

         cmd = RSDCommand ("insert into DPRCQUITDOC_DBT (T_QUITID,T_ACCTRNID) values (?,?)");
         cmd.addParam( "", RSDBP_IN, QuitID );
         cmd.addParam( "", RSDBP_IN, AccTrnID );
         cmd.execute();
      end;

      NewSumFact = cntsched.rec.SumFact + KvitSum;
      if (NewSumFact == cntsched.rec.SumCalc)
         NewPayState = PRC_PAYSTATUS_PAID_MANUAL;
      else
         NewPayState = PRC_PAYSTATUS_PARTLY_PAID_MANUAL;
      end;

      cmd3 = RSDCommand ("update DPRCCNTSCHED_DBT set T_PAYSTATE = ?, T_SUMFACT = ? where T_CNTSCHEDID = ?");
      cmd3.addParam( "", RSDBP_IN, NewPayState);
      cmd3.addParam( "", RSDBP_IN, NewSumFact);
      cmd3.addParam( "", RSDBP_IN, CntSchedID);
      cmd3.execute();
      
   end;

      
   return stat;
   
END;

