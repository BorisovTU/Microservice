/*
  $Name:             multydoc.mac
  $Module:           Бухгалтерия банка Banking
  $Description:      Макрос скроллинга мультивалютных документов в ББ
*/

import FIInter, globals, PaymInter, "pm_opr.mac", "pmchoper.mac", "pm_tools.mac";
import "cb_FillFactura.mac", "pmedit.mac", pmbuff;

/*
      Макросы:
       -  Check()
      - Проверить_Документ (...)
      должны возвращать номер поля в котором произошла ошибка
*/

var НомерПоля =  -1; /*ошибки нет*/

FILE   f_account  ("account.dbt" );

const
    Тип_Документа_Любой      = 0,
    Тип_Документа_Отложенный = 1,
    Тип_Документа_Открытый   = 2,
    Тип_Документа_Закрытый   = 3,

    FLD_MCD_CHAPTER          = 1,
    FLD_MCD_CHAPTERNAME      = 2,
    FLD_MCD_DOCNUMBER        = 3,
    FLD_MCD_OPRID            = 4,
    FLD_MCD_OPRNAME          = 5,
    FLD_MCD_DOCDATE          = 6,
    FLD_MCD_DEBFIID          = 7,
    FLD_MCD_KREDFIID         = 8,
    FLD_MCD_DEBFIIDNAME      = 9,
    FLD_MCD_KREDFIIDNAME     = 10,
    FLD_MCD_DEBACCOUNT       = 11,
    FLD_MCD_KREDACCOUNT      = 12,
    FLD_MCD_DEBSUM           = 13,
    FLD_MCD_KREDSUM          = 14,
    FLD_MCD_DEBNAME          = 15,
    FLD_MCD_KREDNAME         = 16,
    FLD_MCD_PTDEBNAME        = 17,
    FLD_MCD_PTKREDNAME       = 18,
    FLD_MCD_SymbDebet        = 19,
    FLD_MCD_SymbNotBalDebet  = 20,
    FLD_MCD_SymbCredit       = 21,
    FLD_MCD_SymbNotBalCredit = 22,
    FLD_MCD_RATETYPE         = 23,
    FLD_MCD_RATETYPENAME     = 24,
    FLD_MCD_RATE             = 25,
    FLD_MCD_RATEFIID_TO      = 26,
    FLD_MCD_RATESCALE        = 27,
    FLD_MCD_RATEFIID_FROM    = 28,
    FLD_MCD_RATEPOINT        = 29,
    FLD_MCD_ISFIXPMRATE      = 30,
    FLD_MCD_TYPEDOC          = 31,
    FLD_MCD_GROUND           = 32,
    FLD_MCD_NPACK            = 33,
    FLD_MCD_DEPARTMENT       = 34,
    FLD_MCD_CARDATE          = 35,
    FLD_MCD_RECALCDIR        = 36,
    FLD_MCD_SHIFROPER        = 37,
    FLD_MCD_KINDOPER         = 38,
    FLD_MCD_MCMETHODNAME     = 39,

    СчетДебетования          = 0,
    СчетКредитования         = 1;



/* макрос проверяет счет и ругается если ему что-то не понравится*/
/* возвращает 1 - если ошибка */
macro ПроверитьСчет (FIID, Account, ТипСчета)
   var TypeAccount = "";

   f_account.Chapter       = r_multydoc.Chapter;
   f_account.Account       = Account ;
   f_account.Code_Currency = FIID ;
   if( GetEQ ( f_account ) != TRUE )
     MsgBox ("Неизвестный номер счета");
     return 1;
   else
     TypeAccount = f_account.Type_Account;
   end;
   if( ТипСчета == СчетДебетования )
     if( Index (TypeAccount, "ЧТ") > 0 )
       MsgBox("Счет закрыт для дебетования");
       return 1;
     end;
   end;
   if( ТипСчета == СчетКредитования )
     if ( Index (TypeAccount, "ЧУ") > 0 )
       MsgBox("Счет закрыт для кредитования");
       return 1;
     end;
   end;

   return 0;
end;


macro Check()
  var error;
/*     Неверно задана сумма */
  if( r_pmpaym.Amount == 0 )  
     НомерПоля = FLD_MCD_DEBSUM;
     MsgBox ("Неверно задана сумма");
     return НомерПоля;
  end;

/*     Неверно задана сумма */
  if( r_pmpaym.PayAmount == 0 )  
     НомерПоля = FLD_MCD_KREDSUM;
     MsgBox("Неверно задана сумма");
     return НомерПоля;
  end;

/*     Неизвестный тип валюты */
  ПолучитьКодФинИн( r_pmpaym.FIID, error );
  if( error )
     НомерПоля = FLD_MCD_DEBFIID;
     MsgBox("Неизвестный тип валюты");
     return НомерПоля;
  end;

/*     Неизвестный тип валюты   */
  ПолучитьКодФинИн( r_pmpaym.PayFIID, error );
  if( error )
     НомерПоля = FLD_MCD_KREDFIID;
     MsgBox("Неизвестный тип валюты");
     return НомерПоля;
  end;

/*     При одинаковых типах валюты суммы должны совпадать  */
  if( (r_pmpaym.FIID == r_pmpaym.PayFIID) and (r_pmpaym.Amount != r_pmpaym.PayAmount) )
     НомерПоля = FLD_MCD_DEBSUM;
     MsgBox("При одинаковых типах валюты суммы должны совпадать");
     return НомерПоля;
  end;

/*     Номера счетов должны различаться */
  if( ( r_pmpaym.FIID == r_pmpaym.PayFIID ) and        
      ( r_pmpaym.PayerAccount == r_pmpaym.ReceiverAccount ) )
     НомерПоля = FLD_MCD_DEBACCOUNT;
     MsgBox("Номера счетов должны различаться");
     return НомерПоля;
  end;

  if( ПроверитьСчет(r_pmpaym.FIID, r_pmpaym.PayerAccount, СчетДебетования) )
     НомерПоля = FLD_MCD_DEBACCOUNT;
     return НомерПоля;
  end;

  if( ПроверитьСчет(r_pmpaym.PayFIID, r_pmpaym.ReceiverAccount, СчетКредитования) )
     НомерПоля = FLD_MCD_KREDACCOUNT;
     return НомерПоля;
  end;

  return 0;
end;


macro Новый_Документ( )
  return 0;
end;

/* Установка подсказки для скролингов из макроса */

private const Hint_ByValueDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx11) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByCloseDate:string = 
"/*+FIRST_ROWS LEADING(pmpaym t pmrmprop oproper oprcurst) INDEX(pmpaym dpmpaym_dbt_idx15) USE_NL(pmpaym t pmrmprop oproper oprcurst)*/";

private const Hint_ByStatus   :string = 
"/*+FIRST_ROWS LEADING(t pmpaym pmrmprop oproper oprcurst) INDEX(t dmultydoc_dbt_idx1) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t pmpaym pmrmprop oproper oprcurst)*/";

private const Hint_ByStep     :string = 
"/*+FIRST_ROWS LEADING(t oproper multydoc pmpaym pmrmprop oprcurst) INDEX(t doprstep_dbt_idx10) INDEX(multydoc dmultydoc_dbt_idx0) INDEX(pmpaym dpmpaym_dbt_idx0) USE_NL(t oproper multydoc pmpaym pmrmprop oprcurst)*/";

MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStates:integer ):string
  /* Возможные значения ScrolStates:
     0 - Все
     1 - Отложенные
     2 - Открытые
     3 - Закрытые
     4 - Отвергнутые
     5 - Подготовленные к шагу */
  
  /* Все, Закрытые */
  if( ( ScrolStates == 0 ) or
      ( ScrolStates == 3 ) )

    var dtflt:PMScrolDateFltr = PMScrolDateFltr();

    if( dtflt.IsSet( DTFL_CLOSEDATE ) )
      return Hint_ByCloseDate;
    elif( dtflt.IsSet( DTFL_VALUEDATE ) )
      return Hint_ByValueDate;
    else
      return Hint_ByStatus;
    end;
  
  /* Отложенные, Открытые, Отвергнутые */
  elif( ( ScrolStates == 1 ) or 
        ( ScrolStates == 2 ) or 
        ( ScrolStates == 4 ) )
    return Hint_ByStatus;

  /* Подготовленные к шагу */
  elif( ScrolStates == 5 )
    return Hint_ByStep;

  end;

  return DefaultHint;

END;


/* Режим - отложенный, открытый, закрытый*/
macro    Проверить_документ( Режим )
   var stat = 0, num, i = 0, цикл = true,
       CHANG_IMPORTANT = -11,
       CHANG_NOTIMPORTANT = -10;
   var errors:TArray = TArray();

   if( (Режим == 2 ) or (Режим == 3) or (Режим == 8) ) 
        if( StrLen( r_pmrmprop.Number ) == 0 )
          MsgBox("Не задан номер документа");
          return FLD_MCD_DOCNUMBER;
        end;

        if( r_pmpaym.Amount <= 0 )
            MemoryError( 407 );
            return FLD_MCD_DEBSUM;
        end;

        if( r_pmpaym.PayAmount <= 0 )
            MemoryError( 407 );
            return FLD_MCD_KREDSUM;
        end;
        /* Для документов по счетам с НВПИ проверим заполненность сумм в валюте-эквиваленте */
        if( PM_CheckAccount_Type( r_pmpaym.PayerAccount, r_pmpaym.Chapter, r_pmpaym.FIID, "В" ) and PM_CheckAccount_Type( r_pmpaym.ReceiverAccount, r_pmpaym.Chapter, r_pmpaym.PayFIID, "В" ) )
          if( r_pmnvpi.DbSumEq == $0 )
            MsgBox("Сумма дебета в валюте-эквиваленте не может быть равна нулю.");
            return 1;
          end;

          if( r_pmnvpi.CrSumEq == $0 )
            MsgBox("Сумма кредита в валюте-эквиваленте не может быть равна нулю.");
            return 1;
          end;
        end;

        if( StrLen( r_pmrmprop.Ground ) == 0 )
          MsgBox("Введите основание документа");
          return FLD_MCD_GROUND;
        end;
        // проверка корректности фактического курса
        if( CheckCorrectRateTypeOnDate( r_pmpaym, errors ) )
          msgbox( errors[0].name );
          return 1;
        end;
   end;

/* Если не отложенный документ, то проверяем иначе нельзя будет сохранить всё, что угодно*/
   if( (Режим == 2) or (Режим == 3) ) 

     if( not stat )
       stat = Check();

       if( stat )
          return stat;
       end;
     end;


     if(PM_CheckCO(r_pmpaym,r_pmrmprop,0,0))
        return 1;
     end;
   end;

  if( Режим == 1 ) /*УДАЛЕНИЕ ДОКУМЕНТА*/
    if( r_multydoc.Origin == MULTYDOC_ORIGIN_LOANS )
      if(not Index( "Ц", StrFor(GetIdentProgram())))
        msgbox("Документ порожден п/с \"Кредитование\".|Удаление запрещено.");
        stat = 1;
      end;
    elif( r_multydoc.Origin == MULTYDOC_ORIGIN_DEPOSIT )
      if(not Index( "Д", StrFor(GetIdentProgram())))
        msgbox("Документ порожден п/с \"Депозиты\".|Удаление запрещено.");
        stat = 1;
      end;
    elif( r_multydoc.Origin == MULTYDOC_ORIGIN_RETAIL )
      if(not Index( "ВБD", StrFor(GetIdentProgram())))
        msgbox("Документ порожден п/с \"Обслуж.физ.лиц\".|Удаление запрещено.");
        stat = 1;
      end;
    end;
    if(CheckDeletePayment(r_pmpaym.PaymentID))
      return 1;
    end;
  elif( Режим == 2 )  /* ВВОД ДОКУМЕНТА */
    if( r_multydoc.Kind_Operation ) /* Вставка документа одновременно с началом операции */
      if( r_pmpaym.ValueDate == Date(0,0,0000) ) /* Дата валютирования*/
        r_pmpaym.ValueDate = {curdate};
      end;
    end;
  elif( Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
       /*При редактировании производим проверку важности внесенных изменений */
       /* Константы важности внесенных изменений:           */
       /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
       /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
       /* CHANG_NOTKEEP        - не сохранять изменения */
       /* Если возвращаемое значение  > 1, то cчитается, что произошла ошибка (код ошибки)*/
       /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно*/
       /* и сохранение изменений можно производить без отката операции      */
       /* Для мультивалютных считаем любые изменения важными */
    if (StandartCheckUpdate(TCheckUpdateParam(r_pmpaym, NULL, NULL, r_pmrmprop), 
        TCheckUpdateParam(r_pmpaym_old, NULL, NULL, r_pmrmprop_old)) == CHANG_NOTKEEP)
      return CHANG_NOTKEEP;
    end;
  elif( Режим == 7 ) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/
  elif( Режим == 8 )  /*ВВОД В ОТЛОЖЕННЫЕ*/
  elif(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperMultyDoc(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_multydoc, r_multydoc_old, r_pmnvpi, r_pmnvpi_old);
  end;

  return stat;
end;

macro Функция_Пользователя( Режим:integer )
 /*
 Возможные значения Режим:
  UFN_PANEL_INPUT(1) - функция вызвана из панели ввода объекта;
  UFN_PANEL_EDIT(2) - функция вызвана из панели корректировки объекта;
  UFN_SCROL(3) - функция вызвана из панели скролинга, единичный вызов, любая корректировка объекта запрещена;
  UFN_SCROL_FMASS(4) - функция вызвана из панели скролинга, работа по нескольким записям, вызов до начала обработки, любая корректировка объекта запрещена;
 
// Пример работы: 
 if( Режим == UFN_SCROL )
   return UPDTPAGE;    // Обновить страницу записей и область скролинга
   //return UPDTREC;     // Обновить текущую запись, загрузив из файла
 end;
 */
 return 0;
end;

