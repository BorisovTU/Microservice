import "ob_lib.mac", BankInter, FIInter, OprInter, globals;

RECORD mo_vw_pan (MEM_O, "userop.lbr") dialog;
RECORD ob_vw_pan (OB_p_r, "userop.lbr") dialog;

RECORD uo_mem_rec( "uo_mem_o", "USEROP.DEF" );
RECORD uo_obcr_rec("uo_obcur", "USEROP.DEF") write;  /* Пользовательский первичный документ (польз. внебаланс) */

private FILE accR ("account");       /* рублевые лицевые счета */
private FILE accC ("account$");      /* валютные лицевые счета */
PRIVATE file ch( obchaptr );


PRIVATE VAR     ВидДок,
        ПриходРасход,
        ЛСчет,
        КодВалюты,
        Операция,
        ПризнакПРИХОД = "ПРИХОД",
        ПризнакРАСХОД = "расход";

PRIVATE MACRO FindAcc( Глава, КодВалюты, ЛСчет )
  if (КодВалюты)
     accC.Chapter        = Глава;
     accC.Account        = ЛСчет;
     accC.Code_Currency  = КодВалюты;
     GetEQ(accC)
  else
     accR.Chapter        = Глава;
     accR.Account        = ЛСчет;
     GetEQ(accR)
  end;
END;

PRIVATE MACRO AddPrmFld( pn, CMD, ID, KEY )
  
   if  ( ВидДок == 5001 )
      
      pn.Code_Currency        = ПолучитьКодФинИн( КодВалюты );      
      FindAcc( pn.Chapter, КодВалюты, pn.Account_Payer ); 
      if( КодВалюты )
         pn.NameAP = accC.NameAccount;
         pn.Department =  accC.Department;
      else
         pn.NameAP = accR.NameAccount;
         pn.Department =  accR.Department;
      end;        
      FindAcc( pn.Chapter, КодВалюты, pn.Account_Receiver );
      if( КодВалюты )
         pn.NameAR = accC.NameAccount;
      else
         pn.NameAR = accR.NameAccount;
      end;       
      ch.Chapter = pn.Chapter;
         if( getEQ( ch ) )
            pn.ChapterName = ch.ShortName;
         else
            pn.ChapterName = "";
      end;

   elif( ВидДок == 5000 )
      if (OB_ПолучитьФИПоКодуID (КодВалюты))
         pn.Code_Currency      = OB_ФИ.FI_Code; 
         pn.Name_Code_Currency = OB_ФИ.Name;
      end;
      
      if    ( Операция == 5000 )
         ПриходРасход = ПризнакПРИХОД;
         ЛСчет        = uo_obcr_rec.Account_Payer;
      elif  ( Операция == 5001 )
         ПриходРасход = ПризнакРАСХОД;
         ЛСчет        = uo_obcr_rec.Account_Receiver;
      end;
      pn.Приход_Расход        = ПриходРасход;            
      pn.Account              = ЛСчет;
   end;

END;

PRIVATE MACRO InitPanelPrimaryDoc( pn, CMD, ID, KEY )
   
   if  ( ВидДок == 5001 )
      КодВалюты               = uo_mem_rec.Code_Currency;
      pn.Numb_Document        = uo_mem_rec.Numb_Document;
      pn.Chapter              = uo_mem_rec.Chapter;
      pn.Date_Carry           = uo_mem_rec.Date_Carry;
      pn.Sum                  = uo_mem_rec.Sum;
      pn.Account_Payer        = uo_mem_rec.Account_Payer;
      pn.Account_Receiver     = uo_mem_rec.Account_Receiver;
      pn.Ground               = uo_mem_rec.Ground;
      pn.Number_Pack          = uo_mem_rec.Number_Pack;
      pn.Kind_Operation       = uo_mem_rec.Kind_Operation;
      pn.Department           = uo_mem_rec.Department;
      AddPrmFld(pn, CMD, ID, KEY);
      DisableFields ( pn );
   
   elif( ВидДок == 5000 )
      Операция                = uo_obcr_rec.Kind_Operation;
      КодВалюты               = uo_obcr_rec.Code_Currency;
      AddPrmFld(pn, CMD, ID, KEY);
      pn.Numb_Document        = uo_obcr_rec.Numb_Document;
      pn.Chapter              = uo_obcr_rec.Chapter;
      pn.Date_Carry           = uo_obcr_rec.Date_Carry;  
      pn.Sum                  = uo_obcr_rec.Sum;                    
      pn.Ground               = uo_obcr_rec.Ground;     
      pn.Number_Pack          = uo_obcr_rec.Number_Pack;         
      DisableFields ( pn );
   end;
END;

MACRO ВнебалансПанельПросмотр( pn, CMD, ID, KEY )
   if   ( CMD == DLG_INIT )
      message("Esc Выход");
      return InitPanelPrimaryDoc( pn, CMD, ID, KEY );
   elif ( KEY == 27 ) 
      return CM_CANCEL;
   else
      return CM_IGNORE;     
   end;
END;

MACRO МемордерПанельПросмотр( pn, CMD, ID, KEY )
   if( CMD == DLG_INIT )
      message("Esc Выход");
      return InitPanelPrimaryDoc( pn, CMD, ID, KEY );
   elif ( KEY == 27 ) 
      return CM_CANCEL;
   else
      return CM_IGNORE;     
   end;
END;

 
MACRO ShowPrimaryDoc(Kind_Operation,DocKind,prdoc)  
   ВидДок = DocKind;
   if  ( ВидДок == 5000 )
     setbuff( uo_obcr_rec, prdoc );
     RunDialog (ob_vw_pan, "ВнебалансПанельПросмотр");
   elif( ВидДок == 5001 )
     setbuff( uo_mem_rec, prdoc );
     RunDialog (mo_vw_pan, "МемордерПанельПросмотр");
   end;
   return 0;
END;

MACRO InitOperation(Kind_Operation,DocKind,prdoc)
  return 0;
END; 

