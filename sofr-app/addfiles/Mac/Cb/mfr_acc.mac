//-----------------------------------------------------------------------------
// Блок      : 29001 - "Межфилиальные расчеты ЦАБС"
// Шаг       : 10    - "Перевод по счетам МФР ЦАБС"
// Назначение: Различные функции
// Описание  : Определение счетов МФР
//-----------------------------------------------------------------------------
import "cat_acc.mac", OprInter, PSInter, PaymInter, oralib, likepy;

//-----------------------------------------------------------------------------
// Первичный документ для счета КУ "Счет МФР"
//-----------------------------------------------------------------------------
CLASS (TCommonFirstDoc) PM_AccMFR_FD( parm1, parm2, parm3, parm4, parm5 )

  VAR AccKind   :string,
      AccFIID   :integer,
      AccDep    :integer,
      AccCorrDep:integer,
      IsKlient  :integer;

  MACRO InitParmArray

     ParmA[regList.registration(MC_TYPE_PARAMETR_CURRENCY  )    ] = AccFIID;
     ParmA[regList.registration(MC_TYPE_PARAMETR_DEPARTMENT)    ] = AccDep;
     ParmA[regList.registration(MC_TYPE_PARAMETR_CORRDEPARTMENT)] = AccCorrDep;

     FIRoleBArray[0] = 0;
  END;

  // Вернуть параметр второго рода
  MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
     VAR Parametr = -1;
     if( regList.isMY(ParmKind) ) return ParmA[ParmKind]; end;
     return Parametr;
  END;

  // Вернуть параметр первого рода
  MACRO GetParametrTemplate ( ObjectID, Classificator, OperDate, FIRole )         
    VAR Parametr = -1;
    if ( (ObjectID==OBJTYPE_UNCL_PAYMTYPE) AND (Classificator==LLCLASS_UNCS_PAYMTYPE) )
      return IsKlient;
    end;
    return Parametr;
  END;

  // Конструктор
  AccKind    = parm1; // Активный/пассивный
  AccFIID    = parm2; // Валюта
  AccDep     = parm3; // Филиал
  AccCorrDep = parm4; // Филиал-корреспондент
  IsKlient   = parm5; // ID платежа

  InitTCommonFirstDoc();
  InitParmArray();

  Id         = 0;
  Kind       = 0;

END;

//-----------------------------------------------------------------------------
// Получить пассивный счет МФР
//-----------------------------------------------------------------------------
macro MFR_GetAccountPassive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string

  var FD:PM_AccMFR_FD = PM_AccMFR_FD( "П", FIID, Dep, CorrDep, IsKlient );
  
  return FD.FindAndOpenSysAccount( "+Счет МФР", IsOprMultiExec(), {curdate} );
end;

//-----------------------------------------------------------------------------
// Получить активный счет МФР
//-----------------------------------------------------------------------------
macro MFR_GetAccountActive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string

  var FD:PM_AccMFR_FD = PM_AccMFR_FD( "А", FIID, Dep, CorrDep, IsKlient );

  return FD.FindAndOpenSysAccount( "-Счет МФР", IsOprMultiExec(), {curdate} );
end;
//-----------------------------------------------------------------------------


//Молчаливые версии функций

//-----------------------------------------------------------------------------
// Получить пассивный счет МФР
//-----------------------------------------------------------------------------
macro silent_MFR_GetAccountPassive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string

  var FD:PM_AccMFR_FD = PM_AccMFR_FD( "П", FIID, Dep, CorrDep, IsKlient );
  
  return FD.FindAndOpenSysAccount( "+Счет МФР", 1, {curdate} );
end;

//-----------------------------------------------------------------------------
// Получить активный счет МФР
//-----------------------------------------------------------------------------
macro silent_MFR_GetAccountActive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string

  var FD:PM_AccMFR_FD = PM_AccMFR_FD( "А", FIID, Dep, CorrDep, IsKlient );

  return FD.FindAndOpenSysAccount( "-Счет МФР", 1, {curdate} );
end;

/* -----------------------------------------------------------------------------
   Кеш счетов МФР внутри ЦАБС
   ----------------------------------------------------------------------------- */

class ( PSRepMap ) TRsbMfrAccountCache

  InitPSRepMap( V_STRING );

  private macro createKey( Kind:string, Dep:integer, CorrDep:integer, FIID:integer ):string
    return string( Kind, Dep:o:10, CorrDep:o:10, FIID:o:10 );
  end;
  
  macro GetAccountPassive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string
    var k:string = createKey( "П", Dep, CorrDep, FIID );
    var v:variant = Value( k );
    if( v == NULL )
      v = MFR_GetAccountPassive( Dep, CorrDep, FIID, IsKlient );
      Add( k, v )
    end;
    return v;
  end;

  macro GetAccountActive( Dep:integer, CorrDep:integer, FIID:integer, IsKlient:integer ):string
    var k:string = createKey( "А", Dep, CorrDep, FIID );
    var v:variant = Value( k );
    if( v == NULL )
      v = MFR_GetAccountActive( Dep, CorrDep, FIID, IsKlient );
      Add( k, v )
    end;
    return v;
  end;

end;

