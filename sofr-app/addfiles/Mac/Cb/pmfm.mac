 /*
 $Name:        pmfm.mac
 $Module:      Ядро Банкинг
 $Description: Функции и константы для проверок платежей в ФМ
 */

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание    : Функции и константы для проверок платежей в ФМ
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.01.2014
//
// --------------------------------------------------------------------

import PaymInter, CTInter, likepy, oralib, globals, OprInter, "pmlib.mac", 
       "pm_common.mac", "pm_chkrst.mac", FMInter, "bnk_common.mac", "AccOperations.mac", "wlmnstls.mac";
      
const НомерШагаПриостановлениеПоФМ : string = "10",
      НомерШагаДополнительныйКонтрольПоФМ : string = "20",
      НомерШагаОтказПоФМ : string = "30";

private const SET_CHAR   = "X";
private const UNSET_CHAR = "";

MACRO IsPmCategFMDeny(Payment : RsbPayment, AttrID : integer) : bool
  if( Payment.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENY, AttrID, null, null, false, {curdate}) )
    return true;
  end;

  return false;
END;

MACRO IsPaymentDeniableFM(Payment : RsbPayment) : bool

  // вид документа платежа  содержится в настройке CB\PAYMENTS\CHECKTERROR
  // (формат настройки: значения  вида <pmpaym.Dockind>[:<pspayord.Dockind>] через запятую
  MACRO IsFMDeniableByDocKind(DocKind : integer, SubDocKind : integer) : bool
    var sDocKind   : string = string(DocKind), 
        sDocKindEx : string = "";
    if(SubDocKind > 0)
      sDocKindEx = sDocKind + ":" + string(SubDocKind);
    end;
  
    var DocKindStr : string = Bnk_GetRegistryValue
                              ( "CB\\PAYMENTS\\CHECKTERROR",
                                V_STRING,
                                "15, 16, 17, 27, 70, 74, 201:1, 201:2, 201:3, 201:4, 201:5, 202, 203, 286, 288, 400, 410, 420, 430, 440, 450"
                              );
    var DocKindArray : TArray = split(DocKindStr, ",");
  
    for(var sDocKindReg, DocKindArray)
      sDocKindReg = Trim(sDocKindReg);
      if( (sDocKindReg == sDocKind) or 
          (sDocKindEx and (sDocKindReg == sDocKindEx))
        )
        return TRUE;
      end;
    end;
  
    return FALSE;
  END;
  
  // категория ФМ платежа "Отказать по ФМ?" установлена в значение 
  // "Отказать" или "Дополнительный контроль"
  MACRO IsFMDeniableByCateg(Payment : RsbPayment) : bool
    if( IsPmCategFMDeny(Payment, ATTR_PAYMENT_FMDENY_DENY) or
        IsPmCategFMDeny(Payment, ATTR_PAYMENT_FMDENY_ADDCONTROL)
      )
      return true;
    end;
  
    return false;
  END;


  if( IsFMDeniableByDocKind(Payment.DocKind, GetPSDocKind(Payment)) and
      IsFMDeniableByCateg(Payment)
    )
    return TRUE;
  end;

  return FALSE;
END;

macro IsCloseTransePayment( PaymentObj:RsbPayment ):bool

  var objFDoc;

  if( ( PaymentObj.DocKind == DLDOC_BANKPAYMENT ) )                  /* Рублевые платежи ББ         */

    objFDoc = GenObject( "RsbBankPayment", PaymentObj.DocumentID );
    return ( objFDoc.Origin == MEMORDER_FDOC_CLOSTRANS );

  elif( ( PaymentObj.DocKind == BBANK_CPORDER     ) )                  /* Валютные платежи ББ         */

    objFDoc = GenObject( "RsbBbCpOrder", PaymentObj.DocumentID );
    return ( objFDoc.Origin == CP_OR_CLOSTRANS );

  end;      

  return false;
end;

MACRO ПроверкаНачалаЗакрытия( Payment:RsbPayment, ВходящийПлатеж: bool ): integer
  var stat:integer, i = 0,
      regStr: string;
  array Text;
  array Buttons;
  GetRegistryValue( "COMMON\\ПРЕДОБРАБОТКА\\CheckTerror_PermitPayType", V_STRING, regStr, stat );
  if( stat != 0 )
    regStr = "1,2";
  end;
  
  var val: TArray = split(regStr, ",");
  var need = true;

  if (ВходящийПлатеж)
    if( InList(Payment.DemandIsESID, IS_ESID_PAYMENT, ESID_KGN) )
      while (i < val.size)
        if (1 == int(val[i]))
          need = false;
          break;
        end;
        i = i + 1;
      end;
    end;
  else
    while (i < val.size)
      if (Payment.PayType == int(val[i]))
        need = false;
        break;
      end;
      i = i + 1;
    end;
  end;
  if ((not ВходящийПлатеж) and need and (IsPrcPayment(Payment) or IsCloseTransePayment(Payment)))
    need = false;
  end;
  if(need)
    regStr = "select 1 from dreqclosa_dbt where t_Code_currency = ? and t_Account = ? and t_OrderKind = 2 and t_CurrentState <> 0";
    var rs, Acc = "";
    if (not ВходящийПлатеж)
      if (Payment.PayerGroup == 2)
        rs = execSQLselect(regStr, makeArray(SQLParam("", Payment.PayerFIID), SQLParam("", Payment.PayerAccount)));
        if (rs and rs.moveNext())
          Acc = "плательщика " + Payment.PayerAccount;
        end;
      end;
    end;
    if ((Acc == "" ) and (Payment.ReceiverGroup == 2))
      rs = execSQLselect(regStr, makeArray(SQLParam("", Payment.ReceiverFIID), SQLParam("", Payment.ReceiverAccount)));
      if (rs and rs.moveNext())
        Acc = "получателя " + Payment.ReceiverAccount;
      end;
    end;
    if (Acc != "")
      var res = IfThenElse(ВходящийПлатеж and (Payment.DemandIsESID != IS_ESID_PAYMENT), 2, 1);
      if (not IsOprMultiExec())
        Text(0) = string("Счет ", Acc, " закрывается на основании п.5.2 ст.7 закона №115-ФЗ ",
               "\"О противодействии легализации (отмыванию) доходов, полученных преступным путем, и финансированию терроризма\".|",
               "Операции с участием такого счета могут быть незаконны.|Продолжить выполнение операции по платежу?");
        Buttons(0) = "Продолжить";
        Buttons(1) = "Отвергнуть";
        if ( ВходящийПлатеж )
          Buttons(2) = "На ручную обработку";
        end;
        res = ConfWin(Text, Buttons, res);
      end;
      if (res == 1)
        stat = RejectPayment(Payment, string(IfThenElse(ВходящийПлатеж, "35;", ""),"Счет ", Acc, " закрывается на основании п.5.2 ст.7 закона №115-ФЗ ",
                      "\"О противодействии легализации (отмыванию) доходов, полученных преступным путем, и финансированию терроризма\""));
        if (stat == 0)
          return 1;
        else
          return -1;
        end;
      elif (res == 2)
        stat = PM_ToManual(Payment, "Счет ", Acc, " закрывается на основании п.5.2 ст.7 закона №115-ФЗ ",
                      "\"О противодействии легализации (отмыванию) доходов, полученных преступным путем, и финансированию терроризма\"");
        if (stat == 0)
          return 1;
        else
          return -1;
        end;
      end;
    end;
  end;
  return 0;
END;

private macro NeedCheckOperationStop(Payment : RsbPayment, IsWlIncoming : bool) : bool

  if(Payment.CheckTerror == CHT_NOTCHECK)

    if(IsWlIncoming)
      if( not InList(Payment.DemandIsESID, IS_ESID_PAYMENT, ESID_KGN) )
        return true;
      end;
    else
      if(not Payment.ToBackOffice or
         not IsPerson(Payment.Receiver)
        )
        return true;
      end;
    end;

  end;

  return false;
end;

// Эта функция вызывается из сишника, при изменении прототипа надо править сишник
macro SetAttrFmDenyReason(Payment : RsbPayment, MessageFM : string) : integer
  var stat : integer = 0;

  var ReasonID : integer = 0;

  if( index(MessageFM, "заморож") )
    ReasonID = ATTR_PAYMENT_FMDENYREASON_FROZENPARTY; // "Заморожены средства плательщика/получателя"
  elif( index(MessageFM, "нежелательная иностранная или международная неправительственная организация") )
    ReasonID = ATTR_PAYMENT_FMDENYREASON_UNDESIRABLE_ORG; // Плательщик/получатель - нежелательная НПО
  end;

  if(ReasonID)
    stat = Payment.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_FMDENYREASON, ReasonID, null, null, {curdate});

    if(stat)
      msgbox("Ошибка при установке значения категории ФМ \"Причина отказа по ФМ\"");
    end;
  end;

  return stat;
end;

// Контроль в целях ПОД/ФТ
MACRO ПроверкаПричастностиТерроризму( Payment:RsbPayment, IsWlIncoming : bool ):integer
  var stat: integer = 0;

  // Проверка на возможность отказа по 115-ФЗ
  if( not IsWlIncoming and IsPaymentDeniableFM(Payment) )
    if( УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NEED ) )
      MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
      return -1;
    end;
    return 1;
  end;

  // Проверка счетов на инициированность операции закрытия счета 
  // по подозрению в причастности к терроризму
  stat = ПроверкаНачалаЗакрытия( Payment, IsWlIncoming );
  if (stat != 0)
    return stat;
  end;

  // Проверка платежа на замороженность/приостановление операции.
  if( NeedCheckOperationStop(Payment, IsWlIncoming) )
    var MessageFM : string = "";

    stat = FM_CheckPayment
      ( Payment.PaymentID, 
        null, 
        null, 
        1/*HUMBEN_CHECK_MODE_NOT_CHECKED*/,
        MessageFM
      );

    if( stat == OPCONTR_CHECK_DEFER )
      if( УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NEED ) )
        MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
        return -1;
      end; 

      if(IsWlIncoming)
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_UNFIN ) )
          MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
          return -1;
        end; 
      end;

      // ПОД/ФТ. Результаты проверки на соответствие законодательству
      if(MessageFM)
        if( Payment.Notes.AddNote( NOTEKIND_PAYM_FM_CHECK_MESSAGE, MessageFM ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return -1;
        end;
      end;

      Payment.CheckTerror = CHT_APPROVED;
      return 1;
    elif( stat == OPCONTR_CHECK_STOP )
      var val;
      if( (GetRegistryValue("ФИНМОНИТОРИНГ\\OLD_TERROR_CHECK", 0, val)!=V_UNDEF) and val ) //если старый режим проверки, то просто прервать шаг
        return -1;
      end;

      if( УстановитьСтатусыПлатежа( OPR_PAYM_TERR, OPR_PAYM_ST_TERR_NEED ) )
        MsgBox("Ошибка при установке сегментов статуса экземпляра операции");
        return -1;
      end;


      // ПОД/ФТ. Результаты проверки на соответствие законодательству
      if(MessageFM)
        if( Payment.Notes.AddNote( NOTEKIND_PAYM_FM_CHECK_MESSAGE, MessageFM ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return -1;
        end;
      end;

      // Установить категорию ФМ "Отказать по ФМ?" в значение "Отказать"
      stat = Payment.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_FMDENY, ATTR_PAYMENT_FMDENY_DENY, null, null, {curdate});
      if(stat)
        msgbox("Ошибка при установке значения категории ФМ \"Отказать по ФМ?\"");
        return -1;
      end;

      // категорию ФМ "Причина отказа по ФМ"
      stat = SetAttrFmDenyReason(Payment, MessageFM);
      if(stat)
        return -1; // Сообщение об ошибке выводит сама SetAttrFmDenyReason
      end;

      Payment.CheckTerror = CHT_APPROVED;
      return 1;
    elif( stat == 8 /*OPCONTR_CHECK_NEED_HAND*/ )
      var Name = "";
      if( IsWlIncoming and (Payment.PayerGroup == PAYMENTS_GROUP_EXTERNAL) )
        Name = Payment.PayerName;
      else
        Name = Payment.ReceiverName;
      end;
      // ПОД/ФТ. Результаты проверки на соответствие законодательству
      stat = Payment.Notes.AddNote(NOTEKIND_PAYM_FM_CHECK_MESSAGE, "В перечне ФСФМ найден(ы) субъект(ы), похожий(е) на " + Name + ". Операцию по платежу необходимо выполнить в единичном режиме из списка отвергнутых");
      if(stat)
        msgbox("Ошибка при установке примечания \"ПОД/ФТ. Результаты проверки на соответствие законодательству\"");
        return -1;
      end;

      stat = RejectPayment( Payment );
      if(stat)
        return -1; // Сообщение об ошибке выводит функция RejectPayment
      end;

      return 1;
    elif( stat != OPCONTR_CHECK_OK )
      var RejectMsg : string = "";
      if(MessageFM)
        RejectMsg = MessageFM + ".";
      else
        RejectMsg = "Ошибка при проверке платежа в фин.мониторинге.";
      end;

      var LogMsg : string = RejectMsg + " Платеж направлен в список отвергнутых";
      if(not IsOprMultiExec()) 
        MsgBox(LogMsg);
      else
        CreateWarning(CWM_NOPREFIX, WLD_WARN_MACRO, LogMsg);
      end;

      // ПОД/ФТ. Результаты проверки на соответствие законодательству
      if( Payment.Notes.AddNote( NOTEKIND_PAYM_FM_CHECK_MESSAGE, RejectMsg ) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return -1;
      end;

      stat = RejectPayment( Payment );
      if(stat)
        return -1; // Сообщение об ошибке выводит функция RejectPayment
      end;

      return 1;
    end;

  end;

  Payment.CheckTerror = CHT_APPROVED;

  return 0;
END;

private macro MassSetNotesForFMStopped()

  var query:string = "select t.t_PaymentID "
                           ",t.t_ID_Operation "
                           ",t.t_ID_Step "
                           ",pmt.t_Message "
                       "from V_PMMASSOP t, dpmforterr_tmp pmt "
                      "where t.t_SkipDocument = 0 "
                        "and t.t_ErrorStatus  = 0 "
                        "and pmt.t_PaymentID  = t.t_OrderID "
                        "and pmt.t_Result <> 0 /* OPCONTR_CHECK_OK */ ";

  var rs : RsdRecordset = execSQLselect( query );

  while( rs and rs.moveNext() )
    var NoteText : string = rs.Value("t_Message");

    if( NoteText and
        AddNoteForObject( OBJTYPE_PAYMENT, 
                          string( rs.Value("t_PaymentID"):o:10 ),
                          NOTEKIND_PAYM_FM_CHECK_MESSAGE, 
                          NoteText,
                          date(),/*{curdate} - не инициализировано*/
                          rs.Value("t_ID_Operation"),
                          rs.Value("t_ID_Step") )
      )
      PM_SetErrorStatus( rs.Value("t_PaymentID"), 1, "Ошибка при вставке примечания \"ПОД/ФТ. Результаты проверки на соответствие законодательству\"" );
    end;

  end;

end;

macro MassCheckTerrorPrepare(IsWlIncoming : bool)

  var StorFunc1 : string = "PM_TERROR.MassCheckTerrorPrepare1",
      StorFunc2 : string = "PM_TERROR.MassCheckTerrorPrepare2";
  if(IsWlIncoming)
    StorFunc1 = "PM_TERROR.MassCheckTerrorPrepare_WlIn1";
    StorFunc2 = "PM_TERROR.MassCheckTerrorPrepare_WlIn2";
  end;

  var stat:integer = execStoredFunc( StorFunc1, V_INTEGER );

  if((stat == 0) and (FM_CheckTerorDocumentMass() == false))
    stat = 1;
  end;  
 
  if(stat == 0)
    stat = execStoredFunc( StorFunc2, V_INTEGER );
  end;

  return stat;
end;

macro MassCheckTerrorExecute(IsWlIncoming : bool)
  
  var StorFunc : string = "PM_TERROR.MassCheckTerrorExecute";
  if(IsWlIncoming)
    StorFunc = "PM_TERROR.MassCheckTerrorExecute_WlIn";
  end;

  var stat:integer = execStoredFunc( StorFunc, V_INTEGER );

  if(not stat)
    MassSetNotesForFMStopped();
  end;

  if((stat == 0) and (FM_InsertTerorOperationMass() == true))
    stat = 1;
  end;

  return stat;

end;

private macro GetDocumentID(PaymentID : integer)
  record pmpaym(pmpaym);  
  pmpaym.PaymentID = PaymentID;
  return makeObjectID( OBJTYPE_PAYMENT, 0, pmpaym );
end;

private macro GetFmOpSumEquivalent(Sum : moneyL, FIID : integer) : moneyL
  if(FIID == NATCUR)
    return Sum;
  end;

  var SumNatCur : moneyL = $0;
  ConvSum(SumNatCur, Sum, date(), FIID, NATCUR);
  return SumNatCur;
end;

// субъект pmpaym.Payer является физлицом-предпринимателем (persn.IsEmpoyer =SET_CHAR) 
// и в счете pmpaym.PayerAccount   (accounts.TypeAcc) есть тип "Ч" (Расчетный)*/
private macro IsPayer_FPF_BE(Payment : RsbPayment) : bool
  var qAcc = "select 1 "
             "  from daccount_dbt "
             " where t_Account = :Account "
             "   and t_Code_Currency = :FIID "
             "   and t_Chapter = :Chapter "
             "   and t_Type_Account like '%Ч%' ";

  if( IsIndividualEmployer(Payment.Payer)
      and
      existsSQLselect( qAcc, makeArray( SQLParam("Account", Payment.PayerAccount),
                                        SQLParam("FIID",    Payment.PayerFIID),
                                        SQLParam("Chapter", Payment.Chapter) ) )
    )
    return TRUE;
  end;

  return FALSE;
end;

private macro RejectGroundAccQuery( isNeedCached: bool): string
  var OldDialogFlag = SetDialogFlag(1);
  var Ground = "";
  GetString( Ground, "Введите обоснование принятого решения об отказе от выполнения операции:" );
  SetParm( 0, true );
  SetDialogFlag(OldDialogFlag);
  return Ground;
end;

// Создать операцию ФМ для отказа от платежа по основаниям Фин. Мониторинга
macro CreateFMOperationDenyPayment(PaymentObj : RsbPayment, NeedAsk) : integer
  var FM_Operation : RsbFMOperation = RsbFMOperation;

  FM_Operation.OperationType           = FM_OPER_TYPE_REJ_OPR;// ОО (Отказ от проведения операции)
  FM_Operation.DocKind                 = PaymentObj.PrimDocKind;
  FM_Operation.DocumentID              = GetDocumentID(PaymentObj.PaymentID);
  FM_Operation.FIID                    = PaymentObj.BaseFIID;
  FM_Operation.Oper                    = PaymentObj.Oper;
  FM_Operation.Department              = PaymentObj.Department;
  FM_Operation.Sum                     = PaymentObj.FutureBaseAmount;
  FM_Operation.SumEquivalent           = GetFmOpSumEquivalent(PaymentObj.FutureBaseAmount, PaymentObj.BaseFIID);
  FM_Operation.Ground                  = PaymentObj.Ground;
  FM_Operation.PaymDocNumber           = PaymentObj.Number;
  FM_Operation.PaymDocDate             = PaymentObj.Date;
  if( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENYREASON, ATTR_PAYMENT_FMDENYREASON_NODOCS, null, null, false, {curdate}) ) // "Не предоставлены документы"
    FM_Operation.RejectCode            = 7; // "НетДокДляФиксИнф"
  elif( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENYREASON, ATTR_PAYMENT_FMDENYREASON_MISTRUSTOPER, null, null, false, {curdate}) ) // "Операция подозрительна"
    FM_Operation.RejectCode            = 8; // "ОперЦельЛегалТер"
  end;

  if (NeedAsk)
    var Msg = "";
    if (IsOprMultiExec())
      Msg = GetCachedVar("FMRejectGround", "RejectGroundAccQuery")
    else
      var tmp;
      Msg = RejectGroundAccQuery(tmp);
    end;
    FM_Operation.RejectGround = Msg;
  end;
  FM_Operation.RejectAccount = PaymentObj.PayerAccount;

  FM_Operation.OprParty(_FM_PARTY_CLN_OPR_REJ).PartyID = PaymentObj.Payer;
  if( IsPayer_FPF_BE(PaymentObj) )
    FM_Operation.OprParty(_FM_PARTY_CLN_OPR_REJ).PartyType = _FM_EMPLOYER;
    FM_Operation.OprParty(_FM_PARTY_CLN_OPR_REJ).ForeignPublicFunctionary = _FM_FPF_BE;
  end;

  if( not FM_Operation.Save() )
    msgbox( FM_Operation.ErrorMessage() );
    return 1;
  end;

  return 0;
end;

private macro GetISOCode( FIID )
  record fi ( fininstr );

  if( FIID != ALLFININSTR )
    if( ПолучитьФинИн( FIID, fi ) == 0 )
      return int(fi.ISO_Number);
    else
      return "";
    end;
  else
    return "";
  end;
end;

private macro IsDocKindOrParent( DocKindCheck, DocKind )
  
  var query = "SELECT 1 FROM DOPRKDOC_DBT WHERE T_DOCKIND IN " +
              " (SELECT T_DOCKIND FROM DOPRKDOC_DBT START WITH T_DOCKIND = :DOCKIND " +
              " CONNECT BY PRIOR T_DOCKIND = T_PARENTDOCKIND) AND T_DOCKIND = :DOCKINDCHECK;";  

  var params:TArray = makeArray( SQLParam( "DOCKIND", DocKind ),
                            SQLParam( "DOCKINDCHECK", DocKindCheck ));

  var rs = execSQLselect( query, params, FALSE );
  
  if( rs and rs.moveNext())
    return true;
  end;
  
  return false;
  
end;

private macro GetBankBIC(BankID:integer)

  var query = "select t_code from dpartcode_dbt where t_PartyID = :PartyID and t_CodeKind = :CodeKind;";

  var params:TArray = makeArray( SQLParam( "PartyID", BankID ),
                            SQLParam( "CodeKind", PTCK_BIC ));

  var rs = execSQLselect( query, params, TRUE );
  
  if(rs and rs.moveNext())
    return rs.value("t_code");
  end;
  
  params = makeArray( SQLParam( "PartyID", BankID ),
                   SQLParam( "CodeKind", PTCK_SWIFT ));

  rs = execSQLselect( query, params, TRUE );
  
  if(rs and rs.moveNext())
    return rs.value("t_code");
  end;
  
  query = "select t_NotResident from dparty_dbt where t_PartyID = :PartyID;";

  params = makeArray( SQLParam( "PartyID", BankID ));

  rs = execSQLselect( query, params, TRUE );
  
  if(rs and rs.moveNext() and rs.value("t_NotResident") == "X")
    return "НР";
  end;
  
  return "";

end;

private macro GetBankName(BankID:integer)

  var query = "select t_name from dparty_dbt where t_PartyID = :PartyID;";

  var params:TArray = makeArray( SQLParam( "PartyID", BankID ));

  var rs = execSQLselect( query, params, TRUE );
  
  if(rs and rs.moveNext())
    return rs.value("t_name");    
  end;
  
  return "";

end;

private macro GetNotResident(BankID:integer, PartyID:integer, Account:string )

  var query = "select t_notresident from dparty_dbt where t_partyid = :PartyID";
  var params:TArray = makeArray( SQLParam( "PartyID", BankID ) );
  VAR rs = execSQLselect( query, params, TRUE );

  if( rs and rs.moveNext() )
    if(rs.value("t_notresident") == "X")
      params = makeArray( SQLParam( "PartyID", PartyID ) );
      rs = execSQLselect( query, params, TRUE );
      
      if(rs and rs.moveNext() and rs.value("t_notresident") == "X" )
        return true;
      end;
    end;
  end;
  
  var mask:string = Bnk_GetRegistryValue("PS\\REQOPENACC\\173-ФЗ счета нерезидентов\\Все счета", V_STRING, "");  

  if(CompareStrWithMasks( mask, Account ) == 0)
    return true;
  end;
  
  return false;

end;

const E_PMPTFM:integer = 0,
      E_PAYER:integer = 1,
      E_RECEIVER:integer = 2;

class Splitter( stringForSplit:string, delimiter:string )
  var splittedStrings:TArray = TArray;
  var currentIdx = 0;
  
  macro Reset(stringForSplit:string, delimiter:string)
   
    splittedStrings = TArray;
   
    if( ValType(delimiter) == V_UNDEF )
      delimiter = " ";
    end;
    
    if( ValType(stringForSplit) != V_UNDEF )
   
      var idx = 1;
    
      while(Index(stringForSplit, delimiter, idx + 1))
        var oldIdx = idx;
        idx = Index(stringForSplit, delimiter, idx + 1);        
        splittedStrings(splittedStrings.size()) = SubStr(stringForSplit, oldIdx, idx - oldIdx );
      end;
      
      if(idx == 1)
        splittedStrings(0) = stringForSplit;
      else
        splittedStrings(splittedStrings.size()) = SubStr(stringForSplit, idx + 1);
      end;
    end;

    currentIdx = 0;    
    
  end;
  
  macro Next()
    if( currentIdx >= splittedStrings.size() )
      return "";
    end;
    
    var str = splittedStrings(currentIdx);
    currentIdx = currentIdx + 1;
    return str;
  end;
  
  Reset(stringForSplit, delimiter);
  
end;

private macro FillParty( PartyType:integer, Party:@RsbFMRejectServiceParty, ErrorMessage:@string, PartySource:TRecHandler, PaymentObj:RsbPayment )

  var INN:string;
  var ind:integer;
   
  if(Party.ClientType == 0)
    if(PartyType == E_PMPTFM)
      
      INN = PartySource.rec.INN;
      ind = Index(INN, "/");
      
      if(ind > 0)
        INN = Substr(INN, 1, ind - 1);
      end;
        
      Party.ClientType = execStoredFunc("PM_TERROR.GetClientType", 
                                  V_INTEGER, 
                                  makeArray( 
                                    SQLParam( "" , PartySource.rec.ClientID ), 
                                    SQLParam( "" , PartySource.rec.Type), 
                                    SQLParam( "" , INN),
                                    SQLParam( "" , ""),
                                    SQLParam( "" , RJSRVMSG_MSGKIND_REJ_OPER)));
    elif(PartyType == E_PAYER)
    
      INN = PaymentObj.PayerINN;
      ind = Index(INN, "/");
      
      if(ind > 0)
        INN = Substr(INN, 1, ind - 1);
      end;
    
      Party.ClientType = execStoredFunc("PM_TERROR.GetClientType", V_INTEGER, 
                                  makeArray( 
                                    SQLParam( "" , PaymentObj.Payer ), 
                                    SQLParam( "" , ""), 
                                    SQLParam( "" , INN), 
                                    SQLParam( "" , PaymentObj.PayerAccount),
                                    SQLParam( "" , RJSRVMSG_MSGKIND_REJ_OPER)));
    elif(PartyType == E_RECEIVER)
    
      INN = PaymentObj.ReceiverINN;
      ind = Index(INN, "/");
      
      if(ind > 0)
        INN = Substr(INN, 1, ind - 1);
      end;
    
        Party.ClientType = execStoredFunc("PM_TERROR.GetClientType", V_INTEGER, 
                                  makeArray( 
                                    SQLParam( "" , PaymentObj.Receiver ), 
                                    SQLParam( "" , ""), 
                                    SQLParam( "" , INN), 
                                    SQLParam( "" , PaymentObj.ReceiverAccount),
                                    SQLParam( "" , RJSRVMSG_MSGKIND_REJ_OPER)));
    end;
  end;
  
  if(Party.PartyID == 0)    
    if(PartyType == E_PMPTFM)
      Party.PartyID = PartySource.rec.ClientID;
    elif(PartyType == E_PAYER)
      Party.PartyID = PaymentObj.Payer;
    elif(PartyType == E_RECEIVER)      
      Party.PartyID = PaymentObj.Receiver;
    end;   
  end;
  
  if(Party.State == 0)    
    if(PartyType == E_PAYER)      
      Party.State = IfThenElse(PaymentObj.GetDEBET().rec.Group == PAYMENTS_GROUP_INTERNAL, 1, 2);
    elif(PartyType == E_RECEIVER)      
      Party.State = IfThenElse(PaymentObj.GetCREDIT().rec.Group == PAYMENTS_GROUP_INTERNAL, 1, 2);
    end;   
  end;
  
  if(Party.PtOperKind == 0)    
    if(PartyType == E_PAYER)      
      Party.PtOperKind = 510;
    elif(PartyType == E_RECEIVER)      
      Party.PtOperKind = 270;
    end;
  end;
  
  if(Party.BIC == "")    
    if(PartyType == E_PAYER)
      Party.BIC = GetBankBIC(PaymentObj.PayerBankID);    
    elif(PartyType == E_RECEIVER)
      Party.BIC = GetBankBIC(PaymentObj.ReceiverBankID);
    end;
  end;
  
  if(Party.AccountNumber == "")    
    if(PartyType == E_PAYER)      
      Party.AccountNumber = PaymentObj.PayerAccount;
    elif(PartyType == E_RECEIVER)      
      Party.AccountNumber = PaymentObj.ReceiverAccount;
    end;
  end;
  
  if(Party.BankName == "")    
    if(PartyType == E_PAYER)      
      Party.BankName = GetBankName(PaymentObj.PayerBankID);
    elif(PartyType == E_RECEIVER)      
      Party.BankName = GetBankName(PaymentObj.ReceiverBankID);
    end;
  end;
  
  if( (Party.IsNotResident == false) and (Party.ClientType != 0) )    
    if(PartyType == E_PAYER)      
      Party.IsNotResident = GetNotResident(PaymentObj.PayerBankID, PaymentObj.Payer, PaymentObj.PayerAccount);
    elif(PartyType == E_RECEIVER)      
      Party.IsNotResident = GetNotResident(PaymentObj.ReceiverBankID, PaymentObj.Receiver, PaymentObj.ReceiverAccount);
    end;
  end;
       
  if( Party.ClientType != 0 )
       
    if(Party.Name == "" )
      if(PartyType == E_PMPTFM)
        Party.Name = PartySource.rec.Name;
      elif(PartyType == E_PAYER)
        Party.Name = PaymentObj.PayerName;
      elif(PartyType == E_RECEIVER)
        Party.Name = PaymentObj.ReceiverName;
      end;
    end; 
  
    if(Party.INN == "" )  
      var PartySplitterINN;
          
      if(PartyType == E_PMPTFM)
        PartySplitterINN = Splitter(PartySource.rec.INN, "/");
      elif(PartyType == E_PAYER)
        PartySplitterINN = Splitter(PaymentObj.PayerINN, "/");
      elif(PartyType == E_RECEIVER)
        PartySplitterINN = Splitter(PaymentObj.ReceiverINN, "/");
      end;
    
      Party.INN = PartySplitterINN.Next();
      Party.KPP = PartySplitterINN.Next();
    end;  
  
    if(PartyType == E_PMPTFM)
      if(Party.OKPO == "")   
        Party.OKPO = PartySource.rec.OKPO;
      end;
    end;
  
    if( InList(Party.ClientType, 2,3,4) )  
      
     var partySplitterName; 
      
      if(PartyType == E_PMPTFM)
        partySplitterName = Splitter( PartySource.rec.Name );
      else
        var name:string = IfThenElse(PartyType == E_PAYER, PaymentObj.PayerName, PaymentObj.ReceiverName );  
        var idx = Index(name, "//");    
        
        if(idx > 0)
          name = Substr(name, 1, idx - 1);
        end;
        
        PartySplitterName = Splitter( name );
      end;
      
      if(Party.Name1 == "")
        Party.Name1 = PartySplitterName.Next();
      else
        PartySplitterName.Next();
      end;
      
      if(Party.Name2 == "")
        Party.Name2 = PartySplitterName.Next();
      else
        PartySplitterName.Next();
      end;
      
      if(Party.Name3 == "")
        Party.Name3 = PartySplitterName.Next();
      else
        PartySplitterName.Next();
      end;
    end;
  end;

  if( (Party.ClientType != 0) and (PartyType == E_PMPTFM) )  
    if(Party.BirthDate == date(0,0,0) )
      Party.BirthDate = PartySource.rec.birthDay;
    end;
    
    if(Party.PlaceName == "")
      Party.PlaceName = PartySource.rec.BirthPlace;
    end;
    
    if(Party.IsPublicOfficial == "")
      Party.IsPublicOfficial = SubStr( PartySource.rec.ForeignPublicFunctionary, 2, 1);
    end;
  end;

  if( PartyType == E_PMPTFM )  

    if((Party.ClientType != 0) and ((Party.PaperKind == 0) and (Party.PaperName == "") 
       and (Party.PaperSeries == "") and (Party.PaperNumber == "")
       and (Party.PaperIssuedDate == date(0,0,0)) and (Party.PaperIssuer == "")
       and (Party.PaperIssuerCode == "")))
       
      var query = "select t_PaperKind from dpaprkind_dbt where dpaprkind_dbt.t_CodeDocum = :CodeDocum;";  

      var params:TArray = makeArray( SQLParam( "CodeDocum", PartySource.rec.CodeDocum ));

      var rs = execSQLselect( query, params, FALSE );
  
      if( rs.moveNext() )
        Party.PaperKind = rs.value("t_PaperKind");
      end;   
    
      Party.PaperSeries = PartySource.rec.PaperSeries;
      Party.PaperNumber = PartySource.rec.PaperNumber;
      Party.PaperIssuedDate = PartySource.rec.PaperIssuedDate;
      Party.PaperIssuer = PartySource.rec.PaperIssuer;
      Party.PaperIssuerCode = PartySource.rec.PaperIssuerCode;
    
    end;

    if((Party.PartyID == 0) and (Party.ClientType != 0))
      if((PartySource.rec.szCountryP != "") or (PartySource.rec.FactAddrOKATO != "") or
         (PartySource.rec.FactAddrRegion != "") or (PartySource.rec.FactAddrPlaceName != "") or
         (PartySource.rec.FactAddrStreet != "") or (PartySource.rec.FactAddrHouse != "") or
         (PartySource.rec.FactAddrBuilding != "") or (PartySource.rec.FactAddrOffice != ""))
        var Address = Party.AddAddress();
        Address.CountryISOCode = PartySource.rec.szCountryP;
        Address.OKATO  = PartySource.rec.FactAddrOKATO;
        Address.DistrictName  = PartySource.rec.FactAddrRegion;
        Address.PlaceName  = PartySource.rec.FactAddrPlaceName;
        Address.StreetName  = PartySource.rec.FactAddrStreet;
        Address.House  = PartySource.rec.FactAddrHouse;
        Address.Corps  = PartySource.rec.FactAddrBuilding;
        Address.Flat  = PartySource.rec.FactAddrOffice;
        
        if(Party.ClientType != 2)
          Address = party.AddRegPlace();
          Address.CountryISOCode = PartySource.rec.szCountryP;
          Address.OKATO  = PartySource.rec.FactAddrOKATO;
          Address.DistrictName  = PartySource.rec.FactAddrRegion;
          Address.PlaceName  = PartySource.rec.FactAddrPlaceName;
          Address.StreetName  = PartySource.rec.FactAddrStreet;
          Address.House  = PartySource.rec.FactAddrHouse;
          Address.Corps  = PartySource.rec.FactAddrBuilding;
          Address.Flat  = PartySource.rec.FactAddrOffice;
        end;
      end;
    end;
    
  end;
  
  return 0;

end;

macro FillRejectAccount( ReqOpenAObj:RsbReqOpenAcc, ErrorMessage:@string, rejectAccountObj:@RsbFMRejectServiceMessage ):integer

  var ErrorStatus:integer = 0;  

  if(not ErrorStatus)
    rejectAccountObj.RecType = RJENTCONTRACC_PRIMARY;
  
    var CheckTerror:integer = Bnk_GetRegistryValue("PS\\REQOPENACC\\OPERATION\\Проверки_Терроризм", V_INTEGER, 0);

    if( CheckTerror != 1 )
      rejectAccountObj.RejectDate = {curdate};    
    end;
    
    var note = ReqOpenAObj.Notes.ReadNote(REQOPENA_NOTE_KIND_PASSING_TO_FM_GROUND, {curdate});

    if( (note != null) and (note != "") )
      rejectAccountObj.AddInfo = note;
    end;
    
    rejectAccountObj.ObjectType = OBJTYPE_REQOPENA;
    rejectAccountObj.ObjectID = String(ReqOpenAObj.RequestID:34:o);
    
    var clientObj = rejectAccountObj.AddClient();
    ErrorStatus = rejectAccountObj.GetLastError(ErrorMessage);    
  end;
    
  if( not ErrorStatus)
    
    if(clientObj.ClientType == 0)    
    
      var INN:string = "";
      var select = "SELECT CODE.T_CODE T_CODE " +
                  "   FROM DPARTY_DBT PARTY, DPARTCODE_DBT CODE " +
                  " WHERE     CODE.T_CODEKIND = :CODE_TYPE " +
                  " AND PARTY.T_PARTYID = :PARTYID AND CODE.T_PARTYID = PARTY.T_PARTYID; ";

      var prm = makeArray(SQLParam("CODE_TYPE", PTCK_INN),
                     SQLParam("PARTYID", ReqOpenAObj.ClientID));

      var rs = execSQLselect( select, prm, TRUE );
  
      if( rs.moveNext() )
        INN = rs.value("T_CODE");
      end;
      
      var PartySplitterINN = Splitter(INN, "/"); 
          
      clientObj.ClientType = execStoredFunc("PM_TERROR.GetClientType", 
                                  V_INTEGER, 
                                  makeArray( 
                                    SQLParam( "" , ReqOpenAObj.ClientID ), 
                                    SQLParam( "" , ""), 
                                    SQLParam( "" , PartySplitterINN.Next()),
                                    SQLParam("", ReqOpenAObj.Account),
                                    SQLParam("", RJSRVMSG_MSGKIND_REJ_ACC_CNTR)));
    end;
    
    if(ClientObj.PartyID == 0)
      ClientObj.PartyID = ReqOpenAObj.ClientID;
    end;

  end;

  var objFMRjRsns = ReqOpenAObj.GetObjFMRjRsn();
  var objFMRjRsnSource = TRecHandler("objfmrjrsn.dbt");
  var objFMRjRsn;

  var isNext =  not objFMRjRsns.First( objFMRjRsnSource );  
  
  while(isNext)
  
    rejectAccountObj.AddFailureCode(objFMRjRsnSource.rec.Code);
  
    if(objFMRjRsnSource.rec.Code == "0000")
      rejectAccountObj.OtherReason = objFMRjRsnSource.rec.Note;      
    end;    
        
    isNext = not objFMRjRsns.Next( objFMRjRsnSource );
  end;
  

  if((not ErrorStatus) and (rejectAccountObj.Save() != true))
    ErrorStatus = 1;
    ErrorMessage = "Ошибка при сохранении объектов ФМ";
  end;
  
  return ErrorStatus;

end;

macro FillTerminateAccount( ReqClosAObj:RsbReqCloseAcc, ErrorMessage:@string, terminateAccountObj:@RsbFMRejectServiceMessage ):integer

  var ErrorStatus:integer = 0;
    
  if(not ErrorStatus)
    terminateAccountObj.RecType = RJENTCONTRACC_PRIMARY;
    
    var SfCntr : TRecHandler = TRecHandler("sfcontr.dbt");
    var IsSfContr : bool = ( ПолучитьДоговорОбслуживания(ReqClosAObj.Code_Currency, ReqClosAObj.Account, SfCntr) == 0 );

    terminateAccountObj.RejectDate = ReqClosAObj.Date;
    
    var note = ReqClosAObj.Notes.ReadNote(REQCLOSA_NOTE_KIND_PASSING_TO_FM_GROUND, {curdate});

    if( (note != null) and (note != "") )
      ReqClosAObj.AddInfo = note;
    end;
      
    terminateAccountObj.ObjectType = OBJTYPE_REQCLOSA;
    terminateAccountObj.ObjectID = String(ReqClosAObj.RequestID:34:o);
    
    var clientObj = terminateAccountObj.AddClient();
    ErrorStatus = terminateAccountObj.GetLastError(ErrorMessage);    
  end;
    
  if( not ErrorStatus)
    
    if(clientObj.ClientType == 0) 

      var ClientID:integer;

      var select = "SELECT account.t_client t_Client" +
               "  FROM daccount_dbt account " +
               "WHERE     account.t_account = :Account " +
               "AND account.t_chapter = :Chapter "
               "AND ACCOUNT.T_CODE_CURRENCY = :CURRENCY; ";

      var prm = makeArray(SQLParam("Account", ReqClosAObj.Account),
                     SQLParam("Chapter", 1),
                     SQLParam("Currency", ReqClosAObj.Code_Currency));      

      var rs = execSQLselect( select, prm, TRUE );
  
      if( rs.moveNext() )
        ClientID = rs.value("T_Client");
      end;    
    
      var INN:string = "";
      select = "SELECT CODE.T_CODE T_CODE " +
                  "   FROM DPARTY_DBT PARTY, DPARTCODE_DBT CODE " +
                  " WHERE     CODE.T_CODEKIND = :CODE_TYPE " +
                  " AND PARTY.T_PARTYID = :PARTYID AND CODE.T_PARTYID = PARTY.T_PARTYID; ";

      prm = makeArray(SQLParam("CODE_TYPE", PTCK_INN),
                     SQLParam("PARTYID", ClientID));

      rs = execSQLselect( select, prm, TRUE );
  
      if( rs.moveNext() )
        INN = rs.value("T_CODE");
      end;
      
      var PartySplitterINN = Splitter(INN, "/"); 
          
      clientObj.ClientType = execStoredFunc("PM_TERROR.GetClientType", 
                                  V_INTEGER, 
                                  makeArray( 
                                    SQLParam( "" , ClientID ), 
                                    SQLParam( "" , ""), 
                                    SQLParam( "" , PartySplitterINN.Next()),
                                    SQLParam("", ReqClosAObj.Account),
                                    SQLParam("", RJSRVMSG_MSGKIND_TERM_ACC_CNTR)));
    end;
    
    if(clientObj.PartyID == 0)
      clientObj.PartyID = ClientID;
    end;

  end;

  if((not ErrorStatus) and (terminateAccountObj.Save() != true))
    ErrorStatus = 1;
    ErrorMessage = "Ошибка при сохранении объектов ФМ";
  end;
  
  return ErrorStatus;

end;

macro CreateRejectOperation( PaymentObj:RsbPayment, ErrorMessage:@string ):integer

  var rejectOperationtObj = RsbFMRejectServiceMessage( RJSRVMSG_MSGKIND_REJ_OPER );
  var ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);
  
  if(not ErrorStatus)
  
    rejectOperationtObj.RecType = RJENTCONTRACC_PRIMARY;

    if( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENYREASON, ATTR_PAYMENT_FMDENYREASON_NODOCS, null, null, false, {curdate}) )
      rejectOperationtObj.RejectGround = "1";  
    elif( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENYREASON, ATTR_PAYMENT_FMDENYREASON_MONEY_LAUNDERING, null, null, false, {curdate}) )
      rejectOperationtObj.RejectGround = "2";
    elif( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_FMDENYREASON, ATTR_PAYMENT_FMDENYREASON_TERROR_FINANCING, null, null, false, {curdate}) )
      rejectOperationtObj.RejectGround = "3";
    end;

    rejectOperationtObj.CurrencyISONumber = GetISOCode( PaymentObj.BaseFIID );

    rejectOperationtObj.RejectDate = {curdate}; 
  end;
  
  if(not ErrorStatus)
    rejectOperationtObj.SumNat = GetFmOpSumEquivalent(PaymentObj.BaseAmount, PaymentObj.BaseFIID);
    rejectOperationtObj.SumCur = PaymentObj.BaseAmount;
    rejectOperationtObj.IsStuff = UNSET_CHAR;
    
    if( IsDocKindOrParent(PaymentObj.PrimDocKind, PMDOC_CASHDOCUMENT) )
      rejectOperationtObj.MoneyKind = 1;
    elif( IsDocKindOrParent(PaymentObj.PrimDocKind, DLDOC_PAYMENT) )
      rejectOperationtObj.MoneyKind = 2;
    //else "электронные" - пока не поддерживаем
      //rejectOperationtObj.MoneyKind = 3;
    end;

    var note:string;

    note = PaymentObj.Notes.ReadNote(NOTEKIND_PAYM_REJECTEDOPERATION, {curdate});

    if( (note != null) and (note != "") )
      rejectOperationtObj.OperationInfo =  note;
    end;

    note = PaymentObj.Notes.ReadNote(NOTEKIND_PAYM_FM_CHECK_MESSAGE, {curdate});

    if( (note != null) and (note != "") )
      rejectOperationtObj.AddInfo = note;
    end;

    rejectOperationtObj.ObjectType = OBJTYPE_PAYMENT;
    rejectOperationtObj.ObjectID = String(PaymentObj.PaymentID:34:o);

    var parties = PaymentObj.GetParties();
    var partySource = TRecHandler("pmptfm.dbt");
    var party;
    
    if((not ErrorStatus) and (not parties.First(partySource)))
      party = rejectOperationtObj.addParty();      
      ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);      
      
      if(ErrorStatus)
        break;
      end;
      
      ErrorStatus = FillParty( E_PMPTFM, @party, @ErrorMessage, partySource );
    end;


    while((not ErrorStatus) and (not parties.next(partySource)))
      party = rejectOperationtObj.addParty();      
      ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);      
      
      if(ErrorStatus)
        break;
      end;
      
      ErrorStatus = FillParty( E_PMPTFM, @party, @ErrorMessage, partySource );
    end;

  end;
    
  if(not ErrorStatus)
    party = rejectOperationtObj.addParty();    
    ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);
  end;
    
  if(not ErrorStatus)
    ErrorStatus = FillParty( E_PAYER, @party, @ErrorMessage, null, PaymentObj );
  end;

  if(not ErrorStatus)
    party = rejectOperationtObj.addParty();    
    ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);
  end;
    
  if(not ErrorStatus)
    ErrorStatus = FillParty( E_RECEIVER, @party, @ErrorMessage, null, PaymentObj );
  end;
  
  if(not ErrorStatus)
    
    var document = rejectOperationtObj.AddDocument();    
    ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);
    
  end;
  
  if(not ErrorStatus)    
    if(IsDocKindOrParent(PaymentObj.PrimDocKind, PMDOC_MEMORIALDOCUMENT))
      document.DocKind = "05";
    elif(PaymentObj.PrimDocKind == CASH_BOF_INCORDER)
      document.DocKind = "10";
    elif((PaymentObj.PrimDocKind == CASH_PS_OUTORDER) or (PaymentObj.PrimDocKind == CASH_BOF_OUTORDER))
      document.DocKind = "11";
    elif(PaymentObj.PrimDocKind == CASH_PS_INCORDER)
      document.DocKind = "12";
    elif(PaymentObj.ShifrOper == "01")
      document.DocKind = "13";
    elif(PaymentObj.ShifrOper == "06")
      document.DocKind = "14";
    elif(PaymentObj.ShifrOper == "02")
      document.DocKind = "15";
    elif(PaymentObj.ShifrOper == "16")
      document.DocKind = "16";
    end;
    
    document.DocDate = PaymentObj.Date;
    document.DocNumber = PaymentObj.Number;
    document.DocSummary = PaymentObj.Ground;    
       
  end; 

  var category = TRecHandler( "objattr" );
  if(not PaymentObj.Categories.GetFirst(OBJ_PAYMENT_GROUP_CODE_UNUSUAL_OPR, {curdate}, category))
    rejectOperationtObj.AddUOCode(category.rec.NameObject);       
    ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);
  end;
  
  while((not ErrorStatus) and (not PaymentObj.Categories.GetNext(category)))
    rejectOperationtObj.AddUOCode(category.rec.NameObject);       
    ErrorStatus = rejectOperationtObj.GetLastError(ErrorMessage);      
  end;

  
  var objFMRjRsns = PaymentObj.GetObjFMRjRsn();
  var objFMRjRsnSource = TRecHandler("objfmrjrsn.dbt");

  var isNext =  not objFMRjRsns.First( objFMRjRsnSource );  
  
  while(isNext)
  
    rejectOperationtObj.AddFailureCode(objFMRjRsnSource.rec.Code);
  
    if(objFMRjRsnSource.rec.Code == "0000")
      rejectOperationtObj.OtherReason = objFMRjRsnSource.rec.Note;      
    end;    
        
    isNext = not objFMRjRsns.Next( objFMRjRsnSource );
  end;

  
  if((not ErrorStatus) and (rejectOperationtObj.Save() != true))
    ErrorStatus = 1;
    ErrorMessage = "Ошибка при сохранении объектов ФМ";
  end; 

  rejectOperationtObj = null;

  return ErrorStatus;

end;