/*
 $Name: TSimpleExtService.mac
 $Module: RS-Connect
 $Description: Класс обертка для вызова сервисов
 */
 /*
	Класс обертка для вызова сервисов
 */
import ServiceAction;

class TSimpleExtService(pMacroName, pMacroFunc)
    var method = "XMLRPCCall";
    var encoding;
    
    var timeOut = 0;
    var priority;
    
    var macroName = pMacroName;
    var macroFunc = pMacroFunc;
    var direction = "1";
    
    var outResponse;
    var outParams;
    
    private var mLastErr : string = "";
    
    macro CallService(urlAddress, paramArray, errMsg : @string)
        var xmlReq = TXRRequest();
        xmlReq.reqId = CreateGUID();
        xmlReq.macroName = macroName;
        xmlReq.macroFunc = macroFunc;
        xmlReq.Parms     = paramArray;
        xmlReq.direction = direction;
        
        var inParams          = TArray;
        var inParam_request       = RslSoapPrm;
        inParam_request.Name  = "request";
        inParam_request.Value = xmlReq.GenerateMessage(false);
        inParams[0]           = inParam_request;

        var inParam_timeout = RslSoapPrm;
        inParam_timeout.Name = "timeout";
        inParam_timeout.Value = timeOut;
        inParams[1] = inParam_timeout;

        var inParam_priority = RslSoapPrm;
        inParam_priority.Name = "priority";
        inParam_priority.Value = 0;
        inParams[2] = inParam_priority;
        var reqId = CreateGUID();
        var stat = CallSimpleExtService(reqId, urlAddress, method, inParams, "", "", timeOut, outParams, encoding, outResponse, errMsg);
        mLastErr = errMsg;
        return stat;
    end;
    
    macro CallResult(obj : @variant, message : @string)
        var xmlOut = CreateXMLParser();
        xmlOut.validateOnParse = true;
        xmlOut.async = false;
        var res = xmlOut.loadXML(outResponse);

        if (not res)
            message = "Ошибка разбора ответа SOAP";
            mLastErr = message;
            return false;
        end;

        var XMLRPCCallResult = xmlOut.getElementsByTagName("XMLRPCCallResult");
        var XMLResult = XMLRPCCallResult.item(0).text;
        res = xmlOut.loadXML(XMLResult);
        
        if (not res)
            message = "Ошибка разбора ответа XML-RPC";
            mLastErr = message;
            return false;
        end;

        ConvertToRsl(XMLResult, obj);
        var faultTag = xmlOut.getElementsByTagName("fault");

        if (faultTag.length != 0)
            message = obj.faultString;
            mLastErr = message;
            return false;
        else
            message = "result = " + obj;
            return true;
        end;
        return true;
    end;
    
    macro IsTimeOutResult()
        var hr = false;
        if (Index (mLastErr, "Futures timed out"))
            hr = true;
        end;
        return hr;
    end;
    
    macro GetLastError()
        return mLastErr;
    end;
    
    macro Clear()
        method = "XMLRPCCall";
        encoding = NULL;
        timeOut = 0;
        priority = NULL;
        macroName = NULL;
        macroFunc = NULL;
        direction = NULL;
        outResponse = NULL;
        outParams = NULL;
        mLastErr = "";
    end;
end;