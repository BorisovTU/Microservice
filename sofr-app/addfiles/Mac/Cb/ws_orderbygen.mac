/*
  ws_orderbygen.mac
  Avdaschenko D. V.
  01.04.2014
*/

import BankInter;

const ListSortDirection_Ascending = 0;
const ListSortDirection_Descending = 1;

class OrderItem
  var Column:String;
  var Direction:Integer;
  var Number:Integer;
end;

macro SP_GetSortStr(
  orderItems//:TArray of OrderItem  // Параметры сортировки
 ,defaultSQLSortStr:String          // Строка сортировки по умолчанию
):String

  var sortStr:String = "";
  var itemCount:Integer = 0;

  if( ( orderItems != null ) and ( orderItems.Size > 0 ) )
    while( itemCount < orderItems.Size )
      if( orderItems[itemCount] != null )
        if( sortStr != "" )
          sortStr = sortStr + ", ";
        end;
        sortStr = sortStr + " t_" + orderItems[itemCount].Column;
        if( orderItems[itemCount].Direction == ListSortDirection_Ascending )
          sortStr = sortStr + " ASC ";
        else
          sortStr = sortStr + " DESC ";
        end;
      end;
      itemCount = itemCount + 1;
    end;
  elif( defaultSQLSortStr != null )
    sortStr = " " + defaultSQLSortStr + " ";
  end;

  return sortStr;
end;

class OrderByGenerator(
  _orderItems//:TArray of OrderItem
 ,_defaultSQLSortOrder:String
 ,_includeOrderBy:Bool
 ,_formByColumn:Bool
)
  private var orderItems = _orderItems;
  private var defaultSQLSortOrder:String = _defaultSQLSortOrder;
  private var includeOrderBy:Bool = _includeOrderBy;
  private var formByColumn:Bool = _formByColumn;
  private var objSortArr = TArray();
  private var sqlSortOrder:String = "";

  if( defaultSQLSortOrder == null )
    defaultSQLSortOrder = "";
  end;

  if( includeOrderBy == null )
    includeOrderBy = false;
  end;

  if( formByColumn == null )
    formByColumn = true;
  end;

  private class ObjectSorting()
    var Key:String;
    var Obj:String;
  end;

  macro AddObjectSorting(
    key:String
   ,obj:String
  )
    var objSort = ObjectSorting();

    objSort.Key = key;
    objSort.Obj = obj;

    objSortArr[objSortArr.Size] = objSort;
  end;

  private macro FormSQLSortOrder(
    obj:String
   ,direction:Integer
  )
    if( sqlSortOrder != "" )
      sqlSortOrder = sqlSortOrder + ", ";
    end;
    sqlSortOrder = sqlSortOrder + obj;
    if( direction == ListSortDirection_Ascending )
      sqlSortOrder = sqlSortOrder + " ASC ";
    elif( direction == ListSortDirection_Descending )
      sqlSortOrder = sqlSortOrder + " DESC ";
    end;
  end;

  private macro FormForOrderItem(
    _ordrItem//:OrderItem
  )
    var ordrItem = _ordrItem;
    var objSort:ObjectSorting = null;
    var objSortCount:Integer = 0;
    var matchIsFound:Bool = false;

    if( ordrItem != null )
      while( ( not matchIsFound )
         and ( objSortCount < objSortArr.Size ) )
        if( ( ordrItem.Column == objSortArr[objSortCount].Key )
        and ( objSortArr[objSortCount].Obj != "" ) )
          objSort = objSortArr[objSortCount];
          matchIsFound = true;
        end;
        objSortCount = objSortCount + 1;
      end;
      if( matchIsFound )
        FormSQLSortOrder( objSort.Obj, ordrItem.Direction );
      elif( formByColumn )
        FormSQLSortOrder( " t_" + ordrItem.Column, ordrItem.Direction );
      end;
    end;
  end;

  private macro FormFullSQLSortOrder()
    var orderItemCount:Integer = 0;

    while( ( orderItems != null )
       and ( orderItemCount < orderItems.Size ) )
      FormForOrderItem( orderItems[orderItemCount] );
      orderItemCount = orderItemCount + 1;
    end;
  end;

  macro GetFullSQLSortOrder():String
    sqlSortOrder = "";

    FormFullSQLSortOrder();

    if( ( sqlSortOrder == "" )
    and ( defaultSQLSortOrder != "" ) )
      sqlSortOrder = " " + defaultSQLSortOrder + " ";
    end;

    if( ( includeOrderBy )
    and ( sqlSortOrder != "" ) )
      sqlSortOrder = " ORDER BY " + sqlSortOrder;
    end;

    return sqlSortOrder;
  end;

  macro GetFullSQLSortOrderDebug(
    outFileName:String
  )
    var fullOutFileName:String = "";
    FILE outFile() txt write;

    if( ValType( outFileName ) == V_STRING )
      if( Open( outFile, GetTxtFileName( outFileName ) ) )
        Insert( outFile, GetFullSQLSortOrder() );
        Close( outFile );
      end;
    end;
  end;
end;

/*var oi = null;
var oiarr = TArray();
var obg = null;
// 1
oi = OrderItem();
oi.Column = "col1";
oi.Direction = ListSortDirection_Ascending;
oi.Number = 1;
oiarr[oiarr.Size] = oi;
// 2 Error
oi = OrderItem();
oi.Column = "col1";
oi.Direction = ListSortDirection_Descending;
oi.Number = 2;
oiarr[oiarr.Size] = oi;
// 3
oi = OrderItem();
oi.Column = "col2";
oi.Direction = ListSortDirection_Ascending;
oi.Number = 3;
oiarr[oiarr.Size] = oi;
// 4
oi = OrderItem();
oi.Column = "col3";
oi.Direction = ListSortDirection_Descending;
oi.Number = 4;
oiarr[oiarr.Size] = oi;

obg = OrderByGenerator( oiarr, "col0 DESC", true );

obg.AddObjectSorting( "col1", "table1.t_column1" );
obg.AddObjectSorting( "col3", "table1.t_column3" );

obg.GetFullSQLSortOrder();
obg.GetFullSQLSortOrderDebug( "_ws_orderbygen_test_" );*/
