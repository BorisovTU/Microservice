/*
$Name:         spissues.mac
$Module:       Ядро ГКБО
$Description:  Отчет по справочнику ценных бумаг
*/

import FIInter, PTInter, "globals.mac";
import deposerv;
import RsbDataSet;

record party("party");

private const ALL = 0,
              IS_EMISS = 1,
              IS_NOT_EMISS = 2;

private const DPSERV_ALL = 0,         //все
              DPSERV_ONSERV = 1,      //на обслуживании в депозитарии
              DPSERV_REMOVESERV = 2;  //сняты с обслуживания в депозитарии

private const AVOIRISSTATE_NOSERV  = 0,  // Не обслуживается
              AVOIRISSTATE_INCLUDE = 10, // Подключена
              AVOIRISSTATE_ONSERV  = 20, // На обслуживании
              AVOIRISSTATE_BLOCK   = 30;  // Блокирован
private const QualifiedKind_ALL = 0,         //все
              QualifiedKind_Qualified = 1,   //квалифицированные
		QualifiedKind_NotQualified = 2;//неквадифицированные


var HeadTable = "┌───────────────┬───────────┬──────────────────────────────┬──────────────┬──────────────┬──────────────┬──────────────────────────────┬────────────────────┬──────────┬──────────┬──────────┬──────────────┐\n"+
                "│  Код ц/б      │ Тип ц/б   │ Эмитент                      │ Номер гос.   │ ISIN         │ CFI          │ Название                     │      Номинал       │ Валюта   │ Дата вв. │ Дата пог.│ Комментарий  │\n"+
                "│               │           │                              │ регистрации  │              │              │                              │                    │ номинала │          │          │              │\n"+
                "├───────────────┼───────────┼──────────────────────────────┼──────────────┼──────────────┼──────────────┼──────────────────────────────┼────────────────────┼──────────┼──────────┼──────────┼──────────────┤";

var HeadTableDepo = "┌───────────────┬───────────┬──────────────────────────────┬──────────────┬──────────────┬──────────────┬──────────────────────────────┬────────────────────┬──────────┬──────────┬──────────┬───────────────┬──────────────┬──────────────┬───────────────┐\n"+
                    "│  Код ц/б      │ Тип ц/б   │ Эмитент                      │ Номер гос.   │ ISIN         │ CFI          │ Название                     │      Номинал       │ Валюта   │ Дата вв. │ Дата пог.│ Принят на     │ Снят с       │ Комментарий  │ Квалификация  │\n"+
                    "│               │           │                              │ регистрации  │              │              │                              │                    │ номинала │          │          │ обслуживание  │ обслуживания │              │ в качестве ЦБ │\n"+
                    "├───────────────┼───────────┼──────────────────────────────┼──────────────┼──────────────┼──────────────┼──────────────────────────────┼────────────────────┼──────────┼──────────┼──────────┼───────────────┼──────────────┼──────────────┼───────────────┤"; 

var tablewidth;
var Rep;
const RepFontStyleTitel =  "ex_FS(b)";

class SourceData()
var EmissiveCode:integer, 
    avr_kind = TArray(), 
    emi_id = TArray(), 
    RepDate:date;

    macro Construct(_EmissiveCode:integer, _avr_kind:Variant, _emi_id:Variant, _RepDate:date)
       EmissiveCode = _EmissiveCode; 
       avr_kind     = _avr_kind; 
       emi_id       = _emi_id; 
       RepDate      = _RepDate;
    end;
end;

private var Data = SourceData();


private macro ЧислоCЗаданнойТочностью( _var, _point:integer) 
  var TmpStr:string;
  if( (valtype (_var) == V_DOUBLE) OR (valtype (_var) == V_DOUBLEL) OR (valtype (_var) == V_MONEY))
    TmpStr = "String(_var:0" + ":" + string(_point) + ")";
    return ExecExp(TmpStr);
  else
    return String(_var);
  end;
end;


macro WriteLine(FI_Code, AvrkShortName, emi_name, LSIN, ISIN, CFI, Name, FaceValue,
                Issued:date, DrawingDate:date, code_nom, StartServDate:date, RemoveServDate:date, Comment:string, KindQualifiedName)
  DP_AddPrintCell( Rep,  FI_Code, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  AvrkShortName, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  emi_name, 0, 0, "l:w:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  LSIN, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  ISIN, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  CFI, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Name, 0, 0, "l:w:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  FaceValue, 0, 0, "r:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  code_nom, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  string(Issued:f), 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  string(DrawingDate:f), 0, 0, "l:" + RepFontStyleTitel );

  if( GetIdentProgram() == CodeFor("Z") )
    DP_AddPrintCell( Rep,  string(StartServDate:f), 0, 0, "c:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  string(RemoveServDate:f), 0, 0, "c:" + RepFontStyleTitel );
  end;

  DP_AddPrintCell( Rep,  Comment, 0, 0, "l:w:" + RepFontStyleTitel );
  
  if( GetIdentProgram() == CodeFor("Z") )
    DP_AddPrintCell( Rep,  KindQualifiedName, 0, 0, "l:" + RepFontStyleTitel );
  end;
  

  Rep.AddStr();
end;


/*Должность и имя опера*/
macro Footer()
   var OperId;
   var Name, Post;
   
   var person  = TBFile("person");
   var officer = TBFile("officer");

/*Подпись исполнителя*/
macro FooterSign()
   DP_AddPrintCell( Rep,  " " + Post                     + " _____________ " + Name, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  " " + MkStr(" ", strlen(Post)) + "    подпись", tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddEmptyStr();
end;

   Rep.AddEmptyStr();
   DP_AddPrintCell( Rep,  " Составлено:   ", 15, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  {curdate}, 10, 0, "l:f:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddEmptyStr();
   
    /*Подпись главного босса*/
   if( {Name_Boss} )
     Post = {Name_Boss};
   else
     Post = "Директор банка";
   end;
   Name = {FIO_Boss};
   FooterSign();
   
   /*Подпись главного бухгалтера*/
   if( {Name_Book} )
      Post = {Name_Book};
   else
      Post = "Главный бухгалтер банка"
   end;
   Name = {FIO_Book};
   FooterSign();
   
   /* Информация об операционисте */
   person.rec.oper = {oper};
   if( person.GetEQ )
      Name = person.rec.Name;
      officer.rec.PartyID  = {OurBank};
      officer.rec.PersonID = person.rec.PartyID;
      if (officer.GetEQ)
        Post = officer.rec.Post;
      else
        Post = "Ответственный сотрудник";
      end;
   else
      Name = "";
   end;
   FooterSign();
end;

/* Отчет по справочнику ценных бумаг */
macro issues(EmissiveCode:integer, avr_kind:Variant, emi_id:Variant, RepDate:date, ToExcel:bool, DepoServiceState:integer, KindQualified:integer,IsWebService:bool, menuItem/*:WsMenuItem*/)
     var b_continue, emi_name, code_nom, FaceValue, RemoveServDate = date(0,0,0),StartServDate = date(0,0,0);
     var DocVoid = true, OK = true;
     var query, DataSet;
     record pt(party);

     var cfin,LSIN,ISIN,CFI,NAME,Nominal,NominalFICode,Comment,KindQualifiedName;

     var FullReportFileName : String = "";
     var ReportFileName = "spissues";

     var AvrKindList = null;
     var AvrKindAddWhere : String = "";
     var AvrKindCount : Integer = 0;

     var EmiList = null;
     var EmiAddWhere : String = "";
     var EmiCount : Integer = 0;

     if( not IsWebService )
       Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     else
       Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
     end;

     if( GetIdentProgram() == CodeFor("Z") )
       tablewidth = Index(HeadTableDepo, "\n");
       Rep = CMakeReport(HeadTableDepo);
     else
       tablewidth = Index(HeadTable, "\n");
       Rep = CMakeReport(HeadTable);
     end;

     if( IsWebService )
       Rep.SetFileName( GetUniqAdd( ReportFileName ) ); 
       Rep.SetFlagShowReport( false );

       if( not ToExcel )
         Rep.SetWinRepOutput( WINREP_OUTPUT_IE );
       end;
     end;

     if( not IsWebService )
        AvrKindList = TArray();
        AvrKindList[AvrKindList.Size] = avr_kind;
        EmiList = TArray();
        EmiList[EmiList.Size] = emi_id;
     else
        AvrKindList = avr_kind;
        EmiList = emi_id;
     end;

     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     Data.Construct(EmissiveCode, AvrKindList, EmiList, RepDate);

     if ( ПолучитьСубъекта({OurBank},pt) != 0 )
       MsgBox("Не установлен наш банк");
       ClearRecord(pt);
     end;

     DP_AddPrintCell( Rep,  "               Отчет по справочнику ценных бумаг", tablewidth, 0, "c:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell( Rep,  "               Коммерческий банк " + pt.ShortName, tablewidth, 0, "c:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell( Rep,  "               Дата создания " + string(Data.RepDate:f), tablewidth, 0, "c:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddEmptyStr();
     
     
     query =   " select fin.t_FIID, fin.t_FI_Code, fin.t_AvoirKind, fin.t_Name, fin.t_Point, fin.t_Issued, RSI_RSB_FIInstr.FI_GetNominalDrawingDate(fin.T_FIID, iss.t_Termless) t_DrawingDate, "
             + "        avrk.t_IsIndividual, avrk.t_ShortName as AvrkShortName, "
             + "        NVL(srv.t_ServiceStartDate, "+GetSQLDate(date(0,0,0))+") as ServiceStartDate, "
             + "        NVL(srv.t_ServiceStateDate, "+GetSQLDate(date(0,0,0))+") as ServiceStateDate, "
             + "        NVL(srv.t_ServiceState, 0) as ServiceState, "
             + "        NVL(adm1.t_OperDate, "+GetSQLDate(date(0,0,0))+") as RegAdmoDate, "
             + "        NVL(adm2.t_OperDate, "+GetSQLDate(date(0,0,0))+") as CloseAdmoDate, "
             + "        rsb_depo.GetAvoirServStateOnDate(iss.t_FIID, "+GetSQLDate(Data.RepDate)+", "+{OperDprt}+") as OnServ, "
			 + "        RSB_FIINSTR.FI_ISQUALIFIED(fin.t_FIID, " + GetSQLDate(Data.RepDate) + ") as Qualified "
             + "   from davoiriss_dbt iss, davrkinds_dbt avrk, "
             + "        dfininstr_dbt fin "
             + "           left join davoirsrv_dbt srv on srv.t_FIID = fin.t_FIID and srv.t_Department = "+{OperDprt}+" "
             + "           left join ddepoadmo_dbt adm1 on adm1.t_AdmoprID = rsb_depo.GetObjPrevAdmoprOnDate(0, fin.t_FIID, CHR(1), "+GetSQLDate(Data.RepDate)+", "+SPAO_CON_REGISTERING+", "+{OperDprt}+") "
             + "           left join ddepoadmo_dbt adm2 on adm2.t_AdmoprID = rsb_depo.GetObjNextAdmoprOnDate(0, fin.t_FIID, CHR(1), "+GetSQLDate(Data.RepDate-1)+", "+SPAO_CON_CLOSING+", "+{OperDprt}+") "
             + "  where fin.t_FI_Kind = " + FIKIND_AVOIRISS
             + "    and iss.t_FIID = fin.t_FIID "
             + "    and fin.t_Issued <= " + GetSQLDate(Data.RepDate)
             + "    and fin.t_IsClosed = CHR(0) "
             + "    and avrk.t_FI_Kind = fin.t_FI_Kind "
             + "    and avrk.t_AvoirKind = fin.t_AvoirKind ";

     if( GetIdentProgram() == CodeFor("Z") )
       query = query + " and 1 = (case when (avrk.t_IsIndividual = CHR(0) and NOT srv.t_FIID IS NULL) or (avrk.t_IsIndividual = 'X' and Exists (select 1 from dinvcard_dbt ic, dv_ficert_dbt vficert where vficert.t_FiCertID = ic.t_FiCertID and vficert.t_FIID = fin.t_FIID and vficert.t_Department = "+{OperDprt}+")) then 1 "
                     + "               else 0 end ) ";
       
       if(DepoServiceState == DPSERV_ONSERV)  
         query = query + " and rsb_depo.GetAvoirServStateOnDate(iss.t_FIID, "+GetSQLDate(Data.RepDate)+", "+{OperDprt}+") <> 0";
       elif(DepoServiceState == DPSERV_REMOVESERV)
         query = query + " and rsb_depo.GetAvoirServStateOnDate(iss.t_FIID, "+GetSQLDate(Data.RepDate)+", "+{OperDprt}+") = 0 "
                       + " and Exists (select 1 from ddepoadmo_dbt adm where adm.t_Item = "+SPDP_AVOIRIS+" and adm.t_FIID = fin.t_FIID and adm.t_OperDate <= "+GetSQLDate(Data.RepDate)+")";
       end;
     end;

     if( ( ValType( Data.avr_kind ) != V_UNDEF ) and ( Data.avr_kind.Size > 0 ) )
        while( AvrKindCount < Data.avr_kind.Size )
           if( ( ValType( Data.avr_kind[AvrKindCount] ) == V_INTEGER )
           and ( Data.avr_kind[AvrKindCount] > ALL ) )
              if( AvrKindAddWhere == "" )
                 AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
              else
                 AvrKindAddWhere = AvrKindAddWhere + " OR ";
              end;
              AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, " + String( Data.avr_kind[AvrKindCount] ) + ", fin.t_AvoirKind) = 1 ";
           end;
           AvrKindCount = AvrKindCount + 1;
        end;
        if( AvrKindAddWhere != "" )
           AvrKindAddWhere = AvrKindAddWhere + " ) ";
        end;
        query = query + AvrKindAddWhere;
     end;

     if( ( ValType( Data.emi_id ) != V_UNDEF ) and ( Data.emi_id.Size > 0 ) )
        while( EmiCount < Data.emi_id.Size )
           if( ( ValType( Data.emi_id[EmiCount] ) == V_INTEGER )
           and ( Data.emi_id[EmiCount] > -1 ) )
              if( EmiAddWhere == "" )
                 EmiAddWhere = EmiAddWhere + " AND ( ";
              else
                 EmiAddWhere = EmiAddWhere + " OR ";
              end;
              EmiAddWhere = EmiAddWhere +  " RSI_RSB_FIInstr.FI_GetIssuerOnDate( fin.t_FIID, " + GetSQLDate(Data.RepDate) + " )  = " + String( Data.emi_id[EmiCount] );
           end;
           EmiCount = EmiCount + 1;
        end;
        if( EmiAddWhere != "" )
           EmiAddWhere = EmiAddWhere + " ) ";
        end;
        query = query + EmiAddWhere;
     end;

     if(Data.EmissiveCode == IS_EMISS)
       query = query + " and avrk.t_IsEmissive = 'X' ";
     elif(Data.EmissiveCode == IS_NOT_EMISS)
       query = query + " and avrk.t_IsEmissive = CHR(0) ";
     end;
     
	 if (KindQualified != QualifiedKind_ALL)
	  if (KindQualified == QualifiedKind_Qualified)
	   query = query + " and RSB_FIINSTR.FI_ISQUALIFIED(fin.t_FIID, " + GetSQLDate(Data.RepDate) + ") = 1 "
	  end;
	  if (KindQualified == QualifiedKind_NotQualified)
	   query = query + " and RSB_FIINSTR.FI_ISQUALIFIED(fin.t_FIID, " + GetSQLDate(Data.RepDate) + ") != 1 "
	  end;
	 end;
	 
	 
     query = query + " order by fin.t_FIID ";

     DataSet = TRsbDataSet(query);

     while ( DataSet.moveNext() )
       cfin = CDPFinInstr(DataSet.FIID, Data.RepDate);

       LSIN = cfin.LSIN();
       ISIN = cfin.ISIN();
       CFI  = cfin.CFI();

       NAME = cfin.NAME();

       Comment = cfin.Definition();

       StartServDate  = date(0,0,0);
       RemoveServDate = date(0,0,0);
       if( GetIdentProgram() == CodeFor("Z") )
         if(DataSet.OnServ == 0)
           if((date(DataSet.CloseAdmoDate) != date(0,0,0)) and (date(DataSet.CloseAdmoDate) < date(DataSet.ServiceStateDate)))
             RemoveServDate = date(DataSet.CloseAdmoDate);
           elif(DataSet.ServiceState == AVOIRISSTATE_NOSERV)
             RemoveServDate = date(DataSet.ServiceStateDate);
           end;

           if(RemoveServDate > Data.RepDate)
             RemoveServDate = date(0,0,0);
           end;
         end;

         if((date(DataSet.RegAdmoDate) != date(0,0,0)) and (date(DataSet.RegAdmoDate) < date(DataSet.ServiceStartDate)))
           StartServDate = date(DataSet.RegAdmoDate);
         else
           StartServDate = date(DataSet.ServiceStartDate);
         end;
		 
		 if (DataSet.Qualified == QualifiedKind_Qualified)
		 KindQualifiedName="Да";
		 else
		 KindQualifiedName="Нет";
		 end;
       end;

       emi_name = cfin.IssuerName();
       
       Nominal = money(cfin.FaceValue());
       NominalFICode   = cfin.FaceValueFICode();

       WriteLine(DataSet.FI_Code, DataSet.AvrkShortName, emi_name, LSIN, ISIN, CFI, Name, 
                 ЧислоCЗаданнойТочностью( Nominal, DataSet.Point+2 ), 
                 date(DataSet.Issued), date(DataSet.DrawingDate), NominalFICode, StartServDate, RemoveServDate, Comment, KindQualifiedName);
       DocVoid = false;
     end;
     
  if(DocVoid == true)
     if(not DP_ProtocolIsEmpty()) //если протокол не пуст, печатаем пустой отчет с протоколом
       Rep.ClearAllStr();
       DP_AddPrintCell(Rep, "В справочнике нет данных, удовлетворяющих параметрам отчета!", 65, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
       Rep.AddStr();

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();

         FullReportFileName = Rep.ReportPath + "\\" + Rep.ReportFileName_xls;
       else
         if( IsWebService )
           Rep.PrintWinRep();
           Rep.ShowWinRep();

           FullReportFileName = Rep.ReportPath + "\\" + Rep.ReportFileName_html;
         else
           Rep.PrintRep();
         end;
       end;
     else
       DP_EndProtocol();                 //закончить протоколирование
     end;

     MsgBox(" В справочнике нет данных, удовлетворяющих параметрам отчета!");
     return FullReportFileName;
  end;
  
  Footer();

  DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
  DP_EndProtocol();                 //закончить протоколирование

  if( ToExcel )
    Rep.SetZoomType( ZOOM_TYPE_A4L );
    Rep.PrintWinRep();
    Rep.ShowWinRep();

    FullReportFileName = Rep.ReportPath + "\\" + Rep.ReportFileName_xls;
  else
    if( IsWebService )
      Rep.SetZoomType( ZOOM_TYPE_A4L );
      Rep.PrintWinRep();
      Rep.ShowWinRep();

      FullReportFileName = Rep.ReportPath + "\\" + Rep.ReportFileName_html;
    else
      Rep.PrintRep();
    end;
  end;

  return FullReportFileName;
end;
