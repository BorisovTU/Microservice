Import BankInter, rsd , "globals.mac";

private macro GetOperString( Oper : integer ) : string
  
  var OperName : string;

  file person("person.dbt") key 0;

  OperName = Oper;

  person.Oper = Oper;
  if( GetEQ(person) )
    
    OperName = OperName + " " + person.Name;

  end;

  return OperName;

end;

/* вывод блока выполненных переносов */
private macro PrintSuccessLog( MoveRestParm, Department : integer )

  var bNeedHead : bool;

  var SourceAccount : string;
  var TargetAccount : string;
  var RestAccount : money;

  var rs : RsdRecordset;

  rs = RsdRecordset( "SELECT * FROM dc302log_dbt WHERE t_Success = 'X' AND t_ParmID = " + MoveRestParm.ParmID + " AND t_Department = " + Department );

  bNeedHead = true;

  while( rs.moveNext() )

    if( bNeedHead )
[                                                                              ];
[                                                                              ];
[+---------------------------+-----------------------------+------------------+];
[| Номер лицевого счета      | Номер лицевого счета,       | Перенесенный     |];
[| по старому плану счетов   | на который выполнен перенос | входящий остаток |];
[+---------------------------+-----------------------------+------------------+];
      bNeedHead = false;
    end;

    SourceAccount = rs.value("t_SourceAccount"); if( SourceAccount == StrFor(1) ) SourceAccount = ""; end;
    TargetAccount = rs.value("t_TargetAccount"); if( TargetAccount == StrFor(1) ) TargetAccount = ""; end;

    RestAccount = money(rs.value("t_Rest"));


[| ######################### | ########################### |##################|] (SourceAccount:f, TargetAccount:f, RestAccount);
[+---------------------------+-----------------------------+------------------+];

  end;

end;

/* вывод блока ошибок переноса */
private macro PrintErrorLog( MoveRestParm, Department : integer )

  var bNeedHead : bool;

  var SourceAccount : string;
  var ErrorMsg : string;

  var rs : RsdRecordset;

  rs = RsdRecordset( "SELECT * FROM dc302log_dbt WHERE t_Success <> 'X' AND t_ParmID = " + MoveRestParm.ParmID + " AND t_Department = " + Department );

  bNeedHead = true;

  while( rs.moveNext() )

    if( bNeedHead )
[                                                                              ];
[                                                                              ];
[                         ПРОТОКОЛ ОШИБОК                                      ];
[                                                                              ];
[+---------------------------+------------------------------------------------+];
[| Номер лицевого счета      | Причина невыполнения переноса                  |];
[| по старому плану счетов   |                                                |];
[+---------------------------+------------------------------------------------+];
      bNeedHead = false;
    end;

    SourceAccount = rs.value("t_SourceAccount"); if( SourceAccount == StrFor(1) ) SourceAccount = ""; end;
    ErrorMsg      = rs.value("t_ErrorMsg"     ); if( ErrorMsg      == StrFor(1) ) ErrorMsg      = ""; end;

[| ######################### |################################################|] (SourceAccount:f, ErrorMsg:w);
[+---------------------------+------------------------------------------------+];

  end;

end;

/* Вывод протокола процедуры переноса остатков лицевых счетов */
private macro _PrintMoveRestLog( MoveRestParm, Department : integer )

  var DprtName : string;

  CB_GetDepartmentCodeAndName( Department, null, null, DprtName );

[                                   ВЕДОМОСТЬ                                  ];
[      ПЕРЕНОСА ОСТАТКОВ ЛИЦЕВЫХ СЧЕТОВ ПРИ ПЕРЕХОДЕ НА НОВЫЙ ПЛАН СЧЕТОВ      ];
[                                 НА ##########                                ] (MoveRestParm.BankDate:f);
[                                                                              ];
[#                                                                             ] (string("Филиал: ", DprtName):w);

  PrintSuccessLog( MoveRestParm, Department );

  PrintErrorLog( MoveRestParm, Department );

[                                                                              ];
[                                                                              ];
[Дата: ##########  Отв.исполнитель: #                                          ] (MoveRestParm.SysDate:f, GetOperString(MoveRestParm.Oper) );
[                                                                              ];

end;

/* Вывод протокола процедуры переноса остатков лицевых счетов */
private macro PrintMoveRestLog( MoveRestParm )

  record MoveRestParmRec("c302parm.dbt");

  SetBuff( MoveRestParmRec, MoveRestParm );

  var rs : RsdRecordset;

  if( (MoveRestParmRec.Department != {NumDprt}) or (MoveRestParmRec.Subordinate == "") )

    _PrintMoveRestLog( MoveRestParmRec, MoveRestParmRec.Department );

  else

    rs = RsdRecordset( "SELECT t_Code FROM ddp_dep_dbt WHERE t_Status = 2 " +
                       "START WITH t_Code = " + {NumDprt} + " " +
                       "CONNECT BY t_ParentCode = PRIOR t_Code AND t_NodeType = 1"
                     );

    while( rs.moveNext() )
      _PrintMoveRestLog( MoveRestParmRec, rs.value("t_Code") );
[                                                                              ];
    end;

  end;

end;
