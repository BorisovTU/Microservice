/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : sfinvfrm.mac
  Created     : Chernov Anton 19-05-2005
  Description : Печать протокола формирования ТО
└───────────────────────────────────────────────────────────────────────────*/

import SfInter, FIInter, PTInter, CTInter, "cb_sql", RsbDataSet;

private var iCount = 0;

private macro GetLLValuesName( List, Element )
  record llv( "llvalues" );

  ClearRecord( llv );
  if( LL_FindLLVALUES(List, Element, llv) == 0 )
    return "-";
  end;

  return llv.Name;
end;


private macro GetFinInstr( FIID )
  return GetFICode(FIID, null, FICK_USERCODE) + " " + GetFIName(FIID, null, FICK_USERCODE);
end;

private macro GetParty( PartyID )
  record party("party");

  if( ПолучитьСубъекта(PartyID, party) != 0)
    return "-";
  end;

  return party.Name;
end;

private macro GetServKind( ServKind )
  file svkind(servkind);

  clearrecord(svkind);
  svkind.servisekind = ServKind;
  if( not GetEQ(svkind) )
    return "-";
  end;

  return svkind.Name;
end;

private macro GetServKindSub( ServKind, ServKindSub )
  file svksub(servksub);

  clearrecord(svksub);
  svksub.ServiceKind    = ServKind;
  svksub.ServiceKindSub = ServKindSub;
  if( not GetEQ(svksub) )
    return "-";
  end;

  return svksub.Name;
end;

private macro GetSfContrByID( Id )
  file sfcontr(sfcontr);

  clearrecord(sfcontr);
  sfcontr.Id = Id;
  if( not GetEQ(sfcontr) )
    return "-";
  end;

  return sfcontr.number;
end;

private macro GetDPDEP( Code )
  file dpdep(dp_dep);

  clearrecord(dpdep);
  dpdep.Code = Code;
  if( not GetEQ(dpdep) )
    return "-";
  end;

  return dpdep.Name;
end;

/* печать заголовка с параметрами формирования ТО */
private macro PrintHeader( FrmPrm_ : memaddr )
  record FormParm("sfinvformprm.rec");

  SetBuff( FormParm, FrmPrm_ );

[ Параметры формирования ТО: ];

  if( FormParm.Department )
[   Подразделение:        #]( GetDPDEP(FormParm.Department) );
  end;

  if( FormParm.ServKind )
[   Вид обслуживания:     #]( GetServKind(FormParm.ServKind) );
  end;

  if( FormParm.ServKindSub )
[   Вид обслуживания:     #]( GetServKindSub(FormParm.ServKind, FormParm.ServKindSub) );
  end;

  if( FormParm.PayerID != -1 )
[   Плательщик:           #]( GetParty(FormParm.PayerID) );
  end;

  if( FormParm.PayerAccount != "" )
[   Счет плательщика:     #]( FormParm.PayerAccount );
  end;

  if( FormParm.PayerBankID != -1 )
[   Банк плательщика:     #]( GetParty(FormParm.PayerBankID) );
  end;

  if( FormParm.BeneID != -1 )
[   Получатель:           #]( GetParty(FormParm.BeneID) );
  end;

  if( FormParm.BeneAccount != "" )
[   Счет получателя:      #]( FormParm.BeneAccount );
  end;

  if( FormParm.BeneBankID != -1 )
[   Банк получателя:      #]( GetParty(FormParm.BeneBankID) );
  end;

  if( FormParm.Contract != "" )
[   Номер договора:       #]( FormParm.Contract );
  end;

  if( FormParm.FIA != -1 )
[   Валюта начисления:    #]( GetFinInstr(FormParm.FIA) );
  end;

  if( FormParm.FIP != -1 )
[   Валюта оплаты:        #]( GetFinInstr(FormParm.FIP) );
  end;

  if( FormParm.VATStatus )
[   Облагается НДС:       #]( GetLLValuesName(OBJTYPE_COMATTITUDENDS, FormParm.VATStatus) );
  end;

[   Период с ########## по ########## ]( FormParm.DStart, FormParm.DEnd );

  println();

end;

/* печать ошибки о несозданных ТО */
private macro PrintError()

  println( " По заданным параметрам не сформировано ни одного ТО" );

end;

/* печать одельного ТО со списком включенных в него комиссий */
private macro PrintInvoice( InvoiceID : integer )
  file sfinv("sfinv");
  var sQuery = "", DataSet;
  var IsPrintedComm = false;

  ClearRecord( sfinv );

  sfinv.InvoiceID = InvoiceID;
  if( not GetEQ(sfinv) )
    return;
  end;

  if( iCount == 0 )
[ Сформированы следующие ТО: ];
    println();
  else
    println();
  end;

  iCount = iCount + 1;

[   Порядковый №#]( iCount );
[     Номер ТО:       #]( sfinv.DocNumber );
[     Плательщик:     #]( GetParty(sfinv.PayerID) );
[     Получатель:     #]( GetParty(sfinv.BeneID) );
[     Номер договора: #]( GetSfContrByID(sfinv.ContractID) );
[     Сумма ТО:       #]( sfinv.TotalAmount:l );
[     Сумма НДС:      #]( sfinv.NDSAmount:l );
[       Включенные комиссии :];

  sQuery = " select comiss.t_name as name, defcom.t_datePeriodBegin as begin, "
           " defcom.t_Sum as Sum, defcom.t_SumNDS as NDSSum "
           " from dsfcomiss_dbt comiss, dsfdef_dbt defcom "
           " where defcom.t_InvoiceID = " + sfinv.InvoiceID + 
           " and comiss.t_feeType = defcom.t_feeType and comiss.t_number = defcom.t_commNumber ";

  DataSet = TRSBDataSet(sQuery);
  while( DataSet.MoveNext() )
    if( IsPrintedComm )
[         ---------------- ];
    else
      IsPrintedComm = true;
    end;
[         Название:        #]( DataSet.name );
[         Начиcляется с:   #]( SQL_ConvTypeDate(DataSet.begin) );
[         Сумма начислено: #]( DataSet.Sum:l );
[         Сумма НДС:       #]( DataSet.NDSSum:l );
  end;

end;
