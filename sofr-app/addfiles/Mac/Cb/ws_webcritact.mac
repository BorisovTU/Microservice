/*
$Name:        ws_webcritact.mac
$Module:      Web-общее
$Description: Макрос с реализацией сервисов работы с критическими действиями
*/

import RsbDataSet, cb_sql;
import XmlRpcInter, globals;
import ws_llvalues, commonutil;

private const CAViewObjType = 1007;
private const CAStateObjType = 1008;

private const Binsert       = 2;   // Inserts a new record into a file
private const Bupdate       = 3;   // Updates the current record

// Статусы критичного действия
class TCriticalActionError
    const IncorrectData = 10001;   // Некорректные данные
end;
const CAError = TCriticalActionError;

// Статусы критичного действия
class TCriticalActionState
    const Waiting = 0;   // Ожидание ответа супервизора
    const Permitted = 1; // КД разрешено
    const Refused = 2;   // КД запрещено
    const Withdrawn = 3; // Запрос отозван
    const NoSupervisors = 4; // Не назначено супервизоров
end;
const CriticalActionState = TCriticalActionState;

// Формат описания критичного действия
class TCAInfoFormat
    const Text = 1;   // Просмотр выполняется в многострочном текстовом поле
    const List = 2;   // Просмотр выполняется в скроллинге из двух колонок: наименование параметра и значение параметра
    const Web  = 3;   // Описание показывается, как документ в специальной панели в окне web-браузера
end;
const CAInfoFormat = TCAInfoFormat;

// Режим получения списка КД
class TCAGettingMode
    const All    = 1;   // Все
    const Active = 2;   // Активные
    const Single = 3;   // Конкретное КД
end;
const CAGettingMode = TCAGettingMode;

// Критическое действие
class CriticalAction
    var ID : integer; // ID КД
    var SessionID : string; // ID исходной сессии инициатора
    var OperID : integer; // Номер пользователя, инициировавшего КД
    var OperName : string; // ФИО пользователя
    var Name : string; // Наименование типа КД
    var ObjectTypeID : integer; // Вид целевого объекта
    var ObjectName : string; // Наименование целевого объекта
    var ObjectID : string; // Целевой объект
    var CreateTime : datetime; // Дата и время совершения КД
    var Description : string; // Краткое описание КД
    var Comment : string; // Обоснование запроса
    var InfoKind : integer; // Формат полного описания КД
    var Info : string; // Полное описание КД
    var State : integer; // Статус
    var StateName : string; // Текстовое описание статуса
    var StateComment : string; // Обоснование статуса
    var StateTime : datetime; // Дата и время ответа (изменения статуса)
    var ManagerOperID : integer; // Номер cупервизора, давшего ответ
    var ManagerOperName : string; // ФИО cупервизора, давшего ответ
end;

class SupervisorStatus
    var DoNotAcceptRequests; // флаг "Не принимать запросы"
    var IsPossibleManager; // является ли пользователь потенциальным супервизором
end;

class CriticalActionInitResult
    var Critact;
    var IsLock;
end;

private macro IsNullOrEmptyString(s)
  return (s == null) or (s == "");
end;

// Получает имя операцониста
private macro GetOperName(oper : integer) : string

  var cmdText = "SELECT T_NAME FROM DPERSON_DBT WHERE T_OPER = ?";
  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();
  var rs = TRsbDataSet(cmd);

  if (rs.moveNext())
    return rs.Name;
  end;

  return "";
end;

// получает значение справочника
private macro GetLlvalue(list :integer, element :integer) // : TWsLlvalues
   return CreateLLValueFromAppServ(list, element);
end;

// получает имя типа объекта
private macro GetObjectTypeName(objectType)

  var cmd = RsdCommand("SELECT t_name FROM DOBJECTS_DBT WHERE t_objecttype = ?");
  cmd.AddParam("", RSDBP_IN, objectType);
  var ds = TRsbDataSet(cmd);

  if (ds.MoveNext())
    return ds.name;
  end;

  return "";
end;

// Получает список возможных супервизоров
private macro GetPossibleManagers(privID) // : TArray of int

  var cmdText = "SELECT T_MANAGEROPERID FROM DCA_POSSIBLEMANAGER_DBT WHERE T_PRIVID = ? AND T_MANAGEROPERID <> ?";
  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, privID);
  cmd.AddParam("", RSDBP_IN, {oper});
  cmd.execute();
  var rs = TRsbDataSet(cmd);

  var result = TArray();

  while (rs.moveNext())
    result[result.size] = rs.ManagerOperID;
  end;

  return result;
end;

// Ищет вид КД
private macro FindCriticalActionType(typeid :Integer) // : TRsbDataSet

  if (typeid <= 0)
    RunError("filterid должен быть > 0");
  end;

  var cmdText = "SELECT * FROM DCA_TYPE_DBT WHERE T_CATYPEID = ?";
  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, typeid);
  cmd.execute();
  var rs = TRsbDataSet(cmd);

  if (rs.moveNext())
    return rs;
  end;

  return null;
end;

// Ищет КД
private macro FindCriticalAction(caid :Integer) // : TRsbDataSet

  if (caid <= 0)
    RunError("caid должен быть > 0");
  end;

  var cmdText = "SELECT T_CAID,T_OPERID,T_SESSIONID,T_CATYPEID,T_CREATEDATE,T_CREATETIME,T_OBJECT,T_DESCRIPTION,NVL(TO_CHAR(T_INFO),chr(1)) T_INFO "
                  ",T_COMMENT,T_STATE,T_STATEDATE,T_STATETIME,T_STATECOMMENT,T_MANAGEROPERID FROM DCA_CRITACT_DBT WHERE T_CAID = ?";
  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, caid);
  cmd.execute();
  var rs = TRsbDataSet(cmd);

  if (rs.moveNext())
    return rs;
  end;


  return null;
end;

// Подкачивает поля КД
private macro ParseCriticalAction(ca) // : CriticalAction

    var caTypeId = SQL_ConvTypeInteger(ca.catypeid);
    var catype = FindCriticalActionType(caTypeId);
    if (catype == null)
        RunError("Не найден вид КД");
    end;

    var critact = CriticalAction();

    critact.ID = SQL_ConvTypeInteger(ca.caID);
    critact.SessionID = SQL_ConvTypeStr(ca.SessionID);

    var OperID = SQL_ConvTypeInteger(ca.OperID);
    critact.OperID = OperID;
    critact.OperName = GetOperName(OperID);

    critact.Name = SQL_ConvTypeStr(catype.Name);

    var ObjectTypeID = SQL_ConvTypeInteger(catype.ObjectType);
    critact.ObjectTypeID = ObjectTypeID;
    critact.ObjectName = GetObjectTypeName(ObjectTypeID);

    critact.ObjectID = SQL_ConvTypeStr(ca.Object);
    critact.CreateTime = DtTm(SQL_ConvTypeDate(ca.CreateDate), SQL_ConvTypeTime(ca.CreateTime));
    critact.Description = SQL_ConvTypeStr(ca.Description);
    critact.Comment  = SQL_ConvTypeStr(ca.Comment);
    critact.InfoKind = SQL_ConvTypeInteger(catype.caInfoKindID);
    critact.Info  = SQL_ConvTypeStr(ca.Info);
    critact.State = SQL_ConvTypeInteger(ca.State);
    critact.StateComment = SQL_ConvTypeStr(ca.StateComment);
    critact.StateTime = DtTm(SQL_ConvTypeDate(ca.StateDate), SQL_ConvTypeTime(ca.StateTime));

    var ManagerOperID = SQL_ConvTypeInteger(ca.ManagerOperID);
    critact.ManagerOperID = ManagerOperID;
    critact.ManagerOperName = GetOperName(ManagerOperID);

    var llvalue = GetLlvalue(CAStateObjType, ca.State);
    if (llvalue != null)
        critact.StateName = llvalue.Name;
    end;

    return critact;
end;

// Сервис получения КД
macro GetCriticalAction(caid :Integer) // : CriticalAction

    var ca = FindCriticalAction(caid);
    if (ca == null)
        RunError("Не найдено КД");
    end;

    return ParseCriticalAction(ca);
end;

// Вставляет новую запись КД
private macro InsertCriticalAction(OperID, caTypeID, Object, Description, Info, Comment, SessionID, State, StateComment)

    var cmdText = "insert into DCA_CRITACT_DBT(T_OPERID, T_SESSIONID, T_CATYPEID, T_OBJECT, T_DESCRIPTION, T_INFO, T_COMMENT, T_STATE, T_STATECOMMENT) "
                      " VALUES( ?, ?, ?, ?, ?, ?, ?, ?, ?) RETURNING T_CAID into ? ";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, OperID);
    cmd.AddParam("", RSDBP_IN, SessionID);  
    cmd.AddParam("", RSDBP_IN, caTypeID);    
    cmd.AddParam("", RSDBP_IN, Object);
    cmd.AddParam("", RSDBP_IN, Description);
    cmd.AddParam("", RSDBP_IN, Info);
    cmd.AddParam("", RSDBP_IN, Comment);
    cmd.AddParam("", RSDBP_IN, State);
    cmd.AddParam("", RSDBP_IN, StateComment);

    cmd.AddParam("caid", RSDBP_OUT, V_INTEGER);
    cmd.execute();
   
    return cmd.value("caid");
end;

// Вставляет связь КД и супервизора
private macro InsertCriticalActionManager(caid, manager)

    var cmdText = "insert into DCA_MANAGER_DBT(T_CAID, T_MANAGEROPERID) VALUES( ?, ? )";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, caid);
    cmd.AddParam("", RSDBP_IN, manager);  
    cmd.execute();
end;

// Является ли операционист супервизором по КД
private macro IsOperCriticalActionManager(caid, manager)

  var cmdText =  "SELECT 1 FROM DCA_MANAGER_DBT WHERE T_CAID = ? and T_MANAGEROPERID = ?";

  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, caid);
  cmd.AddParam("", RSDBP_IN, manager);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.movenext)
    return true;
  end;
  
  return false;
end;

// Состояние флага "Не принимает запросов на подтверждение критичных действий"
private macro IsDoNotAcceptRequests(oper)

  var cmdText = "SELECT 1 FROM DPERSON_DBT WHERE t_Oper = ? and t_SkipCritActRequest = 'X'";

  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.movenext)
    return true;
  end;

  return false;
end;

// Является ли операционист потенциальным супервизором
private macro IsOperPossibleManager(oper)

  var cmdText =  "select 1 from DCA_POSSIBLEMANAGER_DBT where T_MANAGEROPERID = ?";

  var cmd = RsdCommand(cmdText);
  cmd.AddParam("", RSDBP_IN, oper);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.movenext)
    return true;
  end;
  
  return false;
end;

// Изменяет состояние КД и супервизора
private macro ChangeCriticalActionState(caid, state, comment, manager)
    record caRec(ca_critact);
    file caFile(ca_critact) key 0;
    caFile.caid = caid;
    if (not GetEQ(caFile))
        RunError("Не найдено КД");
    end;
    // старое состояние
    copy(caRec, caFile);

    CaptureOutput;

    [ UPDATE DCA_CRITACT_DBT set T_STATE = ?, T_STATECOMMENT = ? ];

    if (manager != null)
      [, T_MANAGEROPERID = ? ];
    end;

    [ WHERE T_CAID = ? ];

    var cmdText = StopCaptureOutput;
    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, state);
    cmd.AddParam("", RSDBP_IN, comment);

    if (manager != null)
      cmd.AddParam("", RSDBP_IN, manager);
    end;

    cmd.AddParam("", RSDBP_IN, caid);
    cmd.execute();

    // новое состояние
    if (not GetEQ(caFile))
        RunError("Не найдено КД");
    end;

    if (not WriteAuditLogChanges(Bupdate, 0, string(caid), caFile, caRec))
      RunError("Ошибка записи в журнал");
    end;
end;

class CriticalActionProcessor()
  var m_oper, m_caTypeId, m_object, m_description, m_info, m_comment, m_sessionID, m_state, m_stateComment, m_supervisors;
  var m_caid = 0;

  macro InsertTrn()
    m_caid = InsertCriticalAction(m_oper, m_caTypeId, m_object, m_description, m_info, m_comment, m_sessionID, m_state, m_stateComment);

    file caFile(ca_critact) key 0;
    caFile.caid = m_caid;
    if (not GetEQ(caFile))
        RunError("Не найдено КД");
    end;

    // Создаем в Журнале аудита типовое событие Ввод записи для записи ca_critact.dbt.
    if (not WriteAuditLogChanges(Binsert, 0, string(m_caid), caFile))
      RunError("Ошибка записи в журнал");
    end;

    for (var item, m_supervisors)
        InsertCriticalActionManager(m_caid, item);
    end;
  end;

  macro Insert(oper, caTypeId, object, description, info, comment, sessionID, state, stateComment, supervisors)

    m_oper = oper; 
    m_caTypeId = caTypeId; 
    m_object = object; 
    m_description = description;
    m_info = info;
    m_comment = comment;
    m_sessionID = sessionID;
    m_state = state;
    m_stateComment = stateComment;
    m_caid = 0;
    m_supervisors = supervisors;

    ProcessTrn(0, R2M(this, "InsertTrn"));
    return m_caid;
  end;

end;
// Сервис создания КД
macro CreateCriticalAction(caTypeId :integer, object :string, description :string, comment :string, info :string)
    var catype = FindCriticalActionType(caTypeId);
    if (catype == null)
        RunError("Не найден вид КД");
    end;

    if ((catype.ObjectType != 0) and IsNullOrEmptyString(object))
      RunError("Некорректные данные", CAError.IncorrectData);
    end;

    // определим наличие супервизоров
    var possibleManagers = TArray();
    var PrivID = SQL_ConvTypeInteger(catype.PrivID);

    if (PrivID != 0)
      possibleManagers = GetPossibleManagers(PrivID);
    end;

    var state = 0;
    var stateComment = "";
    var supervisors = TArray(); // операционисты, прошедшие проверку

    // проверим потенциальных супервизоров, проверим их привилегии
    for (var oper, possibleManagers)
      if (0 == ACS_CheckAccessForObject(catype.PrivID, catype.ObjectType, object, oper, true))
          supervisors[supervisors.size] = oper;
      end;
    end;

    // если нет супервизоров или не подходят по привилегии
    if (supervisors.size == 0)
        state = CriticalActionState.NoSupervisors;
        stateComment = "Не назначено супервизоров";
    end;

    // даже если нет супервизоров надо создать запись КД
    var caProcessor = CriticalActionProcessor();
    var caid = caProcessor.Insert({oper}, caTypeId, object, description, info, comment, {WebSessionID}, state, stateComment, supervisors);

    return GetCriticalAction(caid);
end;

// Инициализация КД
macro InitCriticalAction(caTypeID :integer)

    var catype = FindCriticalActionType(caTypeId);
    if (catype == null)
        RunError("Не найден вид КД");
    end;

    var critact = CriticalAction();

    critact.Name = SQL_ConvTypeStr(catype.Name);
    var ObjectTypeID = SQL_ConvTypeInteger(catype.ObjectType);
    critact.ObjectTypeID = ObjectTypeID;
    critact.ObjectName = GetObjectTypeName(ObjectTypeID);
    critact.InfoKind = SQL_ConvTypeInteger(catype.caInfoKindID);

    var result = CriticalActionInitResult();
    result.Critact = critact;
    result.IsLock = SQL_ConvTypeBool(catype.IsLock);

    return result;
end;

private macro CheckOperPrivilege(caTypeId, objectId, oper) // : bool

  var catype = FindCriticalActionType(caTypeId);
  if (catype == null)
      RunError("Не найден вид КД");
  end;

  return 0 == ACS_CheckAccessForObject(catype.PrivID, catype.ObjectType, objectId, oper, true);
end;

// Сервис обновления состояния КД
macro UpdateCriticalActionState(caid :integer, state :integer, comment :string) // : CriticalAction
    
    if (((caid == null) or (caid < 0)) 
      or ((state == null) or (state < 0)))
      RunError("Некорректные данные", CAError.IncorrectData);
    end;

    var ca = FindCriticalAction(caid);
    if (ca == null)
        RunError("Не найдено КД");
    end;

    // Если статус КД ca_critact.State не равен 0, то выходим, т.к. статус уже был кем-то изменен
    if (ca.State != 0)
        return GetCriticalAction(caid);
    end;

    // если пользователь инициатор КД, то состояние может быть только "Отозван"
    if (ca.OperID == {oper})
        if (state != CriticalActionState.Withdrawn)
            RunError("Некорректные данные", CAError.IncorrectData);
        end;
        ChangeCriticalActionState(caid, CriticalActionState.Withdrawn, comment);

    // если вызов пришел от супервизора, то состояние может быть только "Разрешено" или "Запрещено"
    elif (IsOperCriticalActionManager(caid, {oper}))
        if ((state != CriticalActionState.Permitted) and (state != CriticalActionState.Refused))
            RunError("Некорректные данные", CAError.IncorrectData);
        end;

        // проверить привилегии
        if (not CheckOperPrivilege(ca.caTypeId, ca.Object, {oper}))
            RunError("Недостаточно прав");
        end;
        ChangeCriticalActionState(caid, state, comment, {oper});
    else
        // если это не инициатор или супервизор
        RunError("Некорректные данные", CAError.IncorrectData);
    end;

    return GetCriticalAction(caid);
end;

private macro GetCriticalActionListQuery(isActive, oper)

    CaptureOutput;
    [
      SELECT * FROM (
        SELECT ca.T_CAID,
               T_OPERID,
               T_SESSIONID,
               T_CATYPEID,
               T_CREATEDATE,
               T_CREATETIME,
               T_OBJECT,
               T_DESCRIPTION,
               NVL (TO_CHAR (T_INFO), CHR (1)) T_INFO,
               T_COMMENT,
               T_STATE,
               T_STATEDATE,
               T_STATETIME,
               T_STATECOMMENT,
               ca.T_MANAGEROPERID
          FROM DCA_CRITACT_DBT ca, dca_manager_dbt mgr
         WHERE CA.T_CAID = MGR.T_CAID AND MGR.T_MANAGEROPERID = #
    ](oper:l);

    if (isActive)
      [ AND CA.T_STATE = 0 ];
    end;
             
    [       
        UNION
          SELECT ca.T_CAID,
                 T_OPERID,
                 T_SESSIONID,
                 T_CATYPEID,
                 T_CREATEDATE,
                 T_CREATETIME,
                 T_OBJECT,
                 T_DESCRIPTION,
                 NVL (TO_CHAR (T_INFO), CHR (1)) T_INFO,
                 T_COMMENT,
                 T_STATE,
                 T_STATEDATE,
                 T_STATETIME,
                 T_STATECOMMENT,
                 ca.T_MANAGEROPERID
          FROM DCA_CRITACT_DBT ca WHERE CA.T_OPERID = #
    ](oper:l);

    if (isActive)
      [ AND CA.T_STATE = 0 ];
    end;

    [ ) order by T_CREATEDATE desc, T_CREATETIME desc ];

    var cmdText = StopCaptureOutput;
    return cmdText;
end;


// Выбирает список КД по текущему пользователю
private macro SelectCriticalAction(isActive) // : RsdCommand
    
  var cmdText = GetCriticalActionListQuery(isActive, {oper});
  var cmd = RsdCommand(cmdText);
  cmd.execute();

  return cmd;
end;

// Сервис получения списка запросов КД
macro GetCriticalActionList(mode, caid) // : Array of CriticalAction

    // По умолчанию = Активные
    if (mode == null)
        mode = CAGettingMode.Active;
    end;

    if ((mode != CAGettingMode.All) and (mode != CAGettingMode.Active) and (mode != CAGettingMode.Single))
        RunError("Некорректные данные", CAError.IncorrectData);
    end;

    // если выборка по конкретному КД, но ID не задан
    if ((mode == CAGettingMode.Single) and ((caid == null) or (caid < 0)))
        RunError("Некорректные данные", CAError.IncorrectData);
    end;

    var result = TArray();
    var ca = null;
    if (mode == CAGettingMode.Single)
        ca = GetCriticalAction(caid);
        result[result.size] = ca;

    elif ((mode == CAGettingMode.All) or (mode == CAGettingMode.Active))

        var cmd = SelectCriticalAction(mode == CAGettingMode.Active);
        var caidList = TRsbDataSet(cmd);
        while (caidList.movenext)
            ca = ParseCriticalAction(caidList);
            result[result.size] = ca;
        end;
    end;

    return result;
end;

// Сервис получения списка запросов КД
macro GetCriticalActionListPaged(selectActiveOnly, pageSize, pageIndex) // : Array of CriticalAction

    var query = GetCriticalActionListQuery(selectActiveOnly, {oper});

    query = SQL_WrapSelect(query , pageIndex, pageSize);
    var cmd = RsdCommand(query);
    cmd.execute();
    var rs = TRsbDataSet(cmd);

    var result = TArray();

    while(rs.MoveNext())
      var item = ParseCriticalAction(rs);
      result[result.Size] = item;
    end;   

    return result;
end;

// получает количество записей
macro GetCriticalActionListItemCount(selectActiveOnly)
    var query = GetCriticalActionListQuery(selectActiveOnly, {oper});
  return SQL_GetNRecs(query);
end;

// Получает статус пользователя как супервизора
macro GetSupervisorStatus()
    var status = SupervisorStatus();

    status.DoNotAcceptRequests = IsDoNotAcceptRequests({oper});
    status.IsPossibleManager = IsOperPossibleManager({oper});

    return status;
end;

// Устанавливает признак "не принимать запросы КД" для операциониста
macro SetOperDoNotAcceptRequests(value)
  
  var SkipCritActRequest = "";

  if( value )
    SkipCritActRequest = "X";
  end;

  var cmd = RsdCommand("UPDATE DPERSON_DBT SET t_SkipCritActRequest = ? WHERE t_Oper = ?");

  cmd.nullConversion = true;

  cmd.AddParam( "", RSDBP_IN, SkipCritActRequest );
  cmd.AddParam( "", RSDBP_IN, {oper} );  
  
  cmd.execute();

  return 0;

end;
