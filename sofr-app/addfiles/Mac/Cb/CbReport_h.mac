/*
$Name:        CbReport_h.mac
$Module:      Ядро ГКБО
$Description: Базовые классы для создания отчетов
*/
import BankInter, "cb_rep_h.mac", "CbForms_h.mac", "cbRepFun.mac", "or_tpl_h.mac", "ws_progress.mac";
import RsbDataSet, globals, SPInter;

PRIVATE CONST DEBUG_COPY  = false/*true*/;

VAR __CalculateTimeExecute__ = false;

/*Режимы вывода отчета на экран*/
CONST DL_OUTREPORT_STD   = 0, /*Стандартный*/
      DL_OUTREPORT_EXCEL = 1, /*Excel*/
      DL_OUTREPORT_TXT   = 2, /*Текстовый файл с разделителями*/
      DL_OUTREPORT_EXCEL_NO_FORMAT = 3, /*Excel без форматирования*/
      DL_OUTREPORT_XML   = 4; /*XML*/

/* Режимы логирования в журнал аудита */
CONST REPORT_START       = 0,
      REPORT_FINISH      = 1,
      REPORT_NO_DATA     = 2,
      REPORT_FILE_ERROR  = 3;

PRIVATE FILE ReportOutFile() txt write; /*файл вывода для отчета*/

PRIVATE VAR LogSheetName = "Протокол выполнения отчета";
PRIVATE VAR TableHeaderErrors = /*Шапка таблицы с ошибками*/
"┌────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
"│                               Ошибки при выполнении отчета                                                 │\n" +
"├────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";

PRIVATE VAR isErrorMacReplaced = false;

MACRO GetisErrorMacReplaced()
   return isErrorMacReplaced; 
END; 

/* Вывод сообщения в журнал аудита */
MACRO AuditLog(Mode:INTEGER, Msg:@STRING, FileName:STRING)
  if (Msg == null) return; end;

  var LogDate = Date;
  var LogTime = Time;

  if (Mode == REPORT_START)
    Msg = Msg + "Запуск отчета:      " + LogDate + "  " + LogTime + "\n";
  else
    Msg = Msg + "Завершение отчета:  " + LogDate + "  " + LogTime + "\n\n";

    if (FileName == null) FileName = "???"; end;

    if (Mode == REPORT_FINISH)
      Msg = Msg + "Отчет сохранен:     " + FileName;
    elif (Mode == REPORT_NO_DATA)
      Msg = Msg + "Нет данных для отчета.";
    elif (Mode == REPORT_FILE_ERROR)
      Msg = Msg + "Ошибка файла:       " + FileName;
    end;
  end;
  
  AuditLogReport(Mode, Msg);
end;

/*Замена стандартного MsgBox на время выполнения отчета*/
PRIVATE VAR ErrorMesage = TArray(1000,1000);
MACRO DL_MsgBoxFromReport( Msg:VARIANT )
   VAR i = 1, CurVal, FormMessage = string(Msg);

   while( GetParm( i, CurVal) )
      FormMessage = FormMessage + string(CurVal);
      i = i + 1;
   end;
   ErrorMesage[ErrorMesage.Size] = FormMessage;
END;

MACRO WEB_isStandalone( )
   return true; // в WEB нет 3х звенки
END;

MACRO WEB_GetLocaleInfo(  id, code, isLocal );
   return GetLocaleInfo( id, code, true );
END;

PRIVATE MACRO AddItog( Itog:@VARIANT, Summa:VARIANT )
   if( (Summa != "") AND (Summa != null) )
      Itog = Itog + Summa;
   end;
END;

PRIVATE VAR __LocaleDecimalSeparator = NULL;

PRIVATE CLASS DL_CReportBase( Form:DL_CPanel )
  PRIVATE VAR m_Form        = Form,
              m_OutMode     = DL_OUTREPORT_STD,
              m_IsWebService = false,
              m_ReportHashCode = 0,
              m_UsePOI = false,
              m_UseBigReportMode = false,
              m_FullReportFileNames = TArray(),
              ShowErrorsLog = true,
              AuditMsg = null,
              m_IsShowAppl = true;

  VAR m_CellNumber = 0; /*кол-во ячеек в текущей строке*/

  VAR m_NumCellAutoSumm = TArray(), m_ValueAutoSumm = TArray(), m_PointAutoSumm = TArray();
  PRIVATE VAR m_btime = TArray(), m_btime_index, m_TimeMessages:STRING;

  if( __LocaleDecimalSeparator == NULL )
     // __LocaleDecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL,(NOT IsStandAlone()));
__LocaleDecimalSeparator = ","; // т.к. в 3х звенке неверно определяет !!!
  end;

  MACRO ReportHasError()
    return (ErrorMesage.Size > 0);
  END;

  MACRO FormTimeMessage( Message:STRING, ExecTime:TIME )
     if( ExecTime != null )
        m_TimeMessages = m_TimeMessages + string(Message:60:l) + "  " + string(ExecTime:m) + "\n";
     else
        m_TimeMessages = m_TimeMessages + string(Message:60:l) + "\n";
     end;
  END;

  MACRO BeginMarkTime()
     m_btime[m_btime_index] = Time();
     m_btime_index = m_btime_index + 1;
  END;

  MACRO EndMarkTime( Message:STRING )
     VAR etime = Time()-m_btime[m_btime_index-1];

     m_btime_index = m_btime_index - 1;
     FormTimeMessage( Message, etime );
  END;

  MACRO ShowExecuteTime()
     FormTimeMessage( "\n\nОбщее время работы отчета: ", Time()-m_btime[0] );

     println( m_TimeMessages );
     MsgBox( m_TimeMessages );
  END;

  MACRO SetPrintMode( OutMode:INTEGER )
     m_OutMode = OutMode;
  END;

  /*******************************************************************************
   Методы, которые необходимо переопределять в производных классах
   *******************************************************************************/
  /*Вернуть режим вывода отчета (DL_OUTREPORT_...)*/
  MACRO PrintMode():INTEGER
     return m_OutMode;
  END;

  MACRO CReport_DataExist() // есть данные в выборке?
     return true; // по умолчанию, считаем, что есть.
  END;

  MACRO CReport_NoDataMsg() // здесь можно отобразить сообщение, если данных нет и отчет не сформирован
  END; // по умолчанию, ничего не делает.

  /*создать отчет*/
  PRIVATE MACRO Create( NumCopy:INTEGER )
  END;

  /*Начало отчета. Тут можно печатать шапку, например*/
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
  END;

  /*Завершение отчета. Тут можно печатать подвал и т.п.*/
  PRIVATE MACRO EndReport( NumCopy:INTEGER )
  END;

  /*******************************************************************************/

  MACRO AddStandardFooter()
  END;

  /*Показать отчет на экран*/
  MACRO CReport_Show( NumCopy:INTEGER )
  END;

  MACRO GetFullReportFileNames()
     return m_FullReportFileNames;
  END;

  MACRO CReport_PrintLog()
  END;

  MACRO CReport_Clear( IsFirstCopy:BOOL, NotClearLog:BOOL )
     if( (NotClearLog == NULL) OR (NotClearLog == false) )
        ErrorMesage.Size = 0;
     end;
     m_NumCellAutoSumm.Size = 0;
     m_ValueAutoSumm.Size = 0;
     m_PointAutoSumm.Size = 0;
  END;

  MACRO AlreadyInit():BOOL
     return false;
  END;

  MACRO FinishLine()
  END;

  /*Получить данные из панели ввода*/
  MACRO FormData()
     return m_Form.Data().Rec;
  END;

  PRIVATE MACRO OnBeforeAddSheet() 
  END;

  PRIVATE MACRO OnAfterAddSheet() 
  END;

  PRIVATE MACRO OnAfterChangeFileName()
  END;

  PRIVATE MACRO OnAfterFirstAutoFill()
  END;

  PRIVATE MACRO OnAfterReOpenTempleFile()
  END;

  PRIVATE MACRO CReport_ExecuteReport( NumCopy:INTEGER, IsFirstCopy:BOOL )

     CReport_Clear( IsFirstCopy );
     BeginReport( NumCopy );

     if( __CalculateTimeExecute__ )
        BeginMarkTime();
     end;

     Create( NumCopy );

     if( __CalculateTimeExecute__ )
        EndMarkTime( "\n\nВремя создания отчета (CReport_ExecuteReport/Create):" );
     end;

      EndReport( NumCopy );

  OnError( RslErrObj )
     VAR Err = "Ошибка при выполнении отчета.\n";

     if( RslErrObj.Module != "" )
        Err = Err + "Модуль : " + RslErrObj.Module + "\n";
     end;

     if( RslErrObj.Line != 0 )
        Err = Err + "Строка : " + RslErrObj.Line + "\n";
     end;

     if( RslErrObj.Message != "" )
        Err = Err + RslErrObj.Message + "\n";
     end;

     /*больше 3х никогда не бывает, если меньше - не страшно, ф-я просто ничего не сделает*/
     RemProgress();
     RemProgress();
     RemProgress();

     /*если WinRep еще не проинициализирован, не использовать его, т.к. иначе он отвалится при попытке печати*/
     if( AlreadyInit() == false )
        println( Err );
        RunError();
     end;

//     FinishLine();
     MsgBox( Err );
  END;

  /*Запустить на выполнение*/
  MACRO Run( NumCopy:INTEGER, _AuditMsg:STRING)
     VAR RetVal = true, CurrentCopy;
     AuditMsg = _AuditMsg;

     if( NumCopy == null )
        NumCopy = 1;
     end;
     CurrentCopy = NumCopy;

     while( RetVal )
        /*форму ввода данных показать только 1 раз для всех копий отчета*/
        if( CurrentCopy == NumCopy )
           if(m_Form != null)
             RetVal = m_Form.Run();
             if (AuditMsg != null) 
                AuditMsg = this.GetAuditMsg();
             end;
           end;
        end;

        if( RetVal )

           if( __CalculateTimeExecute__ )
              m_btime.Size   = 0;
              m_btime_index  = 0;
              m_TimeMessages = "";
              BeginMarkTime();
           end;

           ReplaceMacro ( "msgbox", "DL_MsgBoxFromReport" );
           ReplaceMacro ( "RunError", "DL_MsgBoxFromReport" );
           isErrorMacReplaced = true;

           if( m_IsWebService == true )
              ReplaceMacro ( "isStandalone", "WEB_isStandalone" );
              ReplaceMacro ( "GetLocaleInfo", "WEB_GetLocaleInfo" );
           end;

           AuditLog(REPORT_START, @AuditMsg);

           CReport_ExecuteReport( CurrentCopy, CurrentCopy == NumCopy );

           ReplaceMacro ( "msgbox", NULL );
           ReplaceMacro ( "RunError", NULL );
           isErrorMacReplaced = false;

           CReport_PrintLog();
           CReport_Show( CurrentCopy );
           CurrentCopy = CurrentCopy - 1;
           if( CurrentCopy <= 0 )
              if(m_Form != null)
                 CurrentCopy = NumCopy; /*снова показать форму ввода данных*/
              else
                 RetVal = false;
              end;
           end;

           if( __CalculateTimeExecute__ )
              ShowExecuteTime();
           end;

           if( m_IsWebService == true )
              ReplaceMacro ( "isStandalone", NULL );
              ReplaceMacro ( "GetLocaleInfo", NULL );
           end;
        end;
     end;

  END;

  PRIVATE MACRO GetAutoSumma( NumCell:INTEGER )
     if( m_NumCellAutoSumm[NumCell] != null )
        return m_ValueAutoSumm[NumCell];
     end;
     return 0;
  END;

  PRIVATE MACRO GetPointAutoSumma( NumCell:INTEGER )
     if( m_PointAutoSumm[NumCell] != null )
        return m_PointAutoSumm[NumCell];
     end;
     return -1;
  END;

  MACRO SetCellAutoSumma( /*...*/ )
     m_NumCellAutoSumm.Size = 0;
     m_ValueAutoSumm.Size = 0;
     m_PointAutoSumm.Size = 0;
  END;

  MACRO SetCellAutoSummaPoint( )
     m_NumCellAutoSumm.Size = 0;
     m_ValueAutoSumm.Size = 0;
     m_PointAutoSumm.Size = 0;
  END;

  MACRO PrintLineAutoSumma( NumStr:INTEGER )
  END;

  MACRO GetHashCode()
    return m_ReportHashCode;
  END;

END;

CLASS (DL_CReportBase) CB_CReport( Form:DL_CPanel, TableHeader:STRING )
  PRIVATE VAR m_TableHeader = TableHeader,
              m_Report;

  MACRO SetTableHeader( Name:STRING, Header:STRING )
     m_TableHeader = Header;
     if( Name != NULL )
        m_Report.AddNewSheetBreak( Name, Header, false );
     end;
     m_Report.SetHeaderStr( Header );
  END;

  /*Показать отчет на экран*/
  MACRO CReport_Show(NumCopy:INTEGER, AuditMsg:STRING)
     VAR ReportFileName, errcode, errtext;
     VAR OutMode = PrintMode();

     if( (OutMode == DL_OUTREPORT_EXCEL) OR (OutMode == DL_OUTREPORT_EXCEL_NO_FORMAT) )
        m_Report.PrintWinRep();
        m_Report.ShowWinRep();
     else
        if( NumCopy == 1 )
           ReportFileName = GetTxtFileName( "" );
           if( Open( ReportOutFile, ReportFileName ) == false )
              errcode = status( errtext );
              AuditLog(REPORT_FILE_ERROR, @AuditMsg, ReportFileName);
              MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
              break;
           end;
           m_FullReportFileNames[m_FullReportFileNames.Size] = ReportFileName;
           SetOutput( ReportFileName );

           if( OutMode == DL_OUTREPORT_TXT )
              m_Report.IsDebug(); /*с разделителями*/
              m_Report.PrintWinRep();
           else
              m_Report.PrintRep();
           end;

           SetOutput( NULL, true );
           close( ReportOutFile );
           AuditLog(REPORT_FINISH, @AuditMsg, ReportFileName);
           ViewFile( ReportOutFile );
        end;
     end;
  END;

  MACRO CReport_PrintLog()
     VAR count = 0;

     if( (ShowErrorsLog == true) AND (ErrorMesage.Size > 0) )
        m_Report.AddNewSheetBreak( LogSheetName, TableHeaderErrors, false );

//        m_Report.SetSheetColor( XlColorIndex_RED );
/*        m_Report.SetExecute( "iBook.Sheets("+m_Report.GetCountSheet()+").Tab.ColorIndex = " + string(XlColorIndex_RED) );*/

        while( count < ErrorMesage.Size )

           m_Report.AddPrintCell( StrSubst( ErrorMesage[count], "|", "\n" ), 0, 0, "l");
           m_Report.AddStr();

           count = count + 1;
        end;
     end;
  END;

  MACRO CReport_Clear( IsFirstCopy:BOOL )
     CReport_Clear( IsFirstCopy );
     if( IsFirstCopy )
        m_Report = CDLMakeReport( m_TableHeader );
     else
        m_Report.SetHeaderStr( m_TableHeader );
     end;
  END;

  MACRO AlreadyInit():BOOL
     return (m_Report != NULL) AND (m_Report.GetNumLastStr( false ) <= 0 );
  END;

  MACRO WinRep():CDLMakeReport
     return m_Report;
  END;

  /*Добавить ячейку вне основной таблицы отчета*/
  MACRO AddCell( CellValue:VARIANT, Format:STRING, CellLen:INTEGER, NumPoint:INTEGER )
     if( (CellLen == NULL) OR (CellLen == 0) )
        if( PrintMode() == DL_OUTREPORT_STD )
           CellLen = strlen( string(CellValue) )+1;
        else
           CellLen = m_Report.GetColWidthTable(m_CellNumber)+1;
        end;
     end;
     m_Report.AddPrintCell( CellValue, CellLen, NumPoint, Format, REP_ELEM_STR );
     m_CellNumber = m_CellNumber + 1;
  END;

  /*Добавить ячейку в основную таблицу отчета*/
  MACRO AddCellTable( CellValue:VARIANT, Format:STRING, NumPoint:INTEGER )
     if( CellValue == NULL )
        CellValue = "";
     end;

     m_Report.AddPrintCell( CellValue, null, NumPoint, Format );

     if( m_NumCellAutoSumm[m_CellNumber] != null )
        AddItog( @m_ValueAutoSumm[m_CellNumber], CellValue );
     end;
     m_CellNumber = m_CellNumber + 1;
  END;

  /*Вставить строку, набитую AddCell'ами*/
  MACRO FinishLine()
     m_Report.AddStr();
     m_CellNumber = 0;
  END;

  /*Добавить Number пустых строк*/
  MACRO AddEmptyString( Number:INTEGER )
     m_Report.AddEmptyStr( Number );
  END;

  MACRO AddStandardFooter( IsCurDate:BOOL, NoBook:BOOL )
     after_44_footer( m_Report, NULL, IsCurDate, NoBook );
  END;

  /*задать номера ячеек, по которым будет вычисляться сумма при добавлении в основную таблицу отчета*/
  MACRO SetCellAutoSumma( /*...*/ )
     VAR i = 1, CurVal;

     SetCellAutoSumma(); // из базового класса

     while( GetParm( i, CurVal) )
        m_NumCellAutoSumm[CurVal] = true;
        m_ValueAutoSumm[CurVal]  = 0;
        i = i + 1;
     end;
  END;

  /*задать номера ячеек, по которым будет вычисляться сумма при добавлении в основную таблицу отчета*/
  MACRO SetCellAutoSummaPoint( /*...*/ )
     VAR i = 1, CurVal, CurPoint;

     SetCellAutoSummaPoint(); // из базового класса

     while( GetParm( i, CurVal) )
        m_NumCellAutoSumm[CurVal] = true;
        m_ValueAutoSumm[CurVal]  = 0;
        GetParm( (i+1), CurPoint);
        m_PointAutoSumm[CurVal]  = CurPoint;
        i = i + 2;
     end;
  END;

  MACRO PrintLineAutoSumma( NumStr:INTEGER )
     VAR i = 0;

     while( i < m_NumCellAutoSumm.Size )
        if( m_NumCellAutoSumm[i] != NULL )
           if( NumStr == null )
              if( GetPointAutoSumma(i) == -1 )
              m_Report.AddPrintCell( GetAutoSumma(i), null, null, null );
              else
                 m_Report.AddPrintCell( WR_ЧислоCЗаданнойТочностью(GetAutoSumma(i), GetPointAutoSumma(i) ), null, null, null );
              end;
           elif( NumStr >= 0 )
              if( GetPointAutoSumma(i) == -1 )
              m_Report.SetCellValue( NumStr, i, GetAutoSumma(i) );
              else
                 m_Report.SetCellValue( NumStr, i, WR_ЧислоCЗаданнойТочностью(GetAutoSumma(i), GetPointAutoSumma(i) ) );
              end;
           end;
        elif( NumStr == null )
           m_Report.AddPrintCell( "", null, null, null );
        end;

        i = i + 1;
     end;

     if( NumStr == null )
        FinishLine();
     end;
  END;

  InitDL_CReportBase( Form );
END;

/*Создание отчета
  Параметры конструктора:
     Form        - форма для ввода данныхотчета
     TableHeader - шапка заголовка таблицы отчета
     UseCSV      - использовать заполнение тпблицы через CSV-файл
*/
/*Похоже на BUG в RSL - значения переменных вышестоящих классов во вложенных классов всегда UNDEFINE
  __BUG_Global_m_ObjTemplateXLS - для обхода данной проблемы
*/

/*ak PRIVATE ~ak*/ VAR __BUG_Global_m_ObjTemplateXLS;

CLASS (DL_CReportBase) CB_CReportTemplate( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, UseBigReportMode:BOOL, FocreFormatRep:INTEGER )
  PRIVATE VAR m_ObjWinRep, m_ObjTemplateXLS = NULL, m_UseTemplate, m_TemplateName = TemplateName;
  PRIVATE VAR m_UsedSheet = TArray();
  PRIVATE VAR m_ItogLineName = null;
  PRIVATE VAR m_ReportTable:object = NULL, m_ReportTableName;
  PRIVATE VAR m_ReportLineCellNames = TArray();
  PRIVATE VAR m_FormulaSumm = TArray();
  PRIVATE VAR m_UseCSV, m_CSVFile:C_CSVFile, m_CurrentCSV;
  PRIVATE VAR m_CurrentRow, m_TotalCurrentRow, m_SheetCurrentRow;
  PRIVATE VAR m_CurNC = 0, m_UseTotalBook = false;
  PRIVATE VAR m_FirstSheetName;
  PRIVATE VAR m_FirstAddressDiapazon;
  PRIVATE VAR m_UseApp = false;
  PRIVATE VAR m_CellsFormat= TArray(1000,1000);
  PRIVATE VAR m_ProgressIndicator = CreateProgressIndicator(IsWebService);
  PRIVATE VAR m_LastTotalBookName = "";
  PRIVATE VAR m_FocreFormatRep = null;
  PRIVATE VAR m_Row_Flush_Count = null;

  MACRO SetRowFlushCount( MaxRows:INTEGER )
      m_Row_Flush_Count = MaxRows;
  END;

  MACRO Destructor()
     //__BUG_Global_m_ObjTemplateXLS = NULL; /* что бы вызвался деструктор, для закрытия excel */
     if((m_UseCSV == true) and (m_CSVFile != NULL))
       m_CSVFile.FileClose();
     end;
  END; /* Destructor */

  PRIVATE CONST FORMAT_PI_STEP = 1000;

  PRIVATE MACRO SetFormat( CSVFileName:STRING )
     VAR i = 0;
     var pi_val = 0;

     if( m_OutMode == DL_OUTREPORT_EXCEL_NO_FORMAT )
        return;
     end;

     if( __CalculateTimeExecute__ )
        BeginMarkTime();
     end;

     m_ProgressIndicator.Start( m_CellsFormat.Size, "Выполнение форматирования Excel", "Выполнение форматирования Excel" );
     if( m_UseTemplate ) /*сделать форматирование*/
        while( i < m_CellsFormat.Size )
           if( CSVFileName == m_CellsFormat[i].m_CSVFileName )
              m_CellsFormat[i].SetFormat( m_ReportTable.AddressDiapazon );
           end;
           i = i + 1;
           if (i - pi_val == FORMAT_PI_STEP) 
             pi_val = i;
             m_ProgressIndicator.Update(pi_val, m_CellsFormat.Size );
           end;
        end;
     end;
     m_ProgressIndicator.Stop();

     if( __CalculateTimeExecute__ )
        EndMarkTime( "Время выполнения форматирования (SetDiapazon/SetFont):" );
     end;
  END;

  PRIVATE MACRO NC( CellName:STRING ):INTEGER
     VAR TmpNC = m_CurNC;

     /*просмотреть от текущего до конца*/
     while( m_CurNC < m_ReportLineCellNames.Size )
        if( StrUpr(m_ReportLineCellNames[m_CurNC]) == StrUpr(CellName) )
           return m_CurNC;
        end;
        m_CurNC = m_CurNC + 1;
     end;

     /*просмотреть от начала до текущего */
     m_CurNC = 0;
     while( m_CurNC < TmpNC )
        if( StrUpr(m_ReportLineCellNames[m_CurNC]) == StrUpr(CellName) )
           return m_CurNC;
        end;
        m_CurNC = m_CurNC + 1;
     end;

     return -1;
  END;

  CLASS CFormat( CSVFileName:STRING, Row:INTEGER, CellID:INTEGER )
     VAR m_Row         = Row,
         m_CellID      = CellID,
         m_CSVFileName = CSVFileName;

     MACRO SetFormat( AddressDiapazon:OBJECT )
     END;
  END;

  CLASS (CFormat) CFontFormat( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER, Size:INTEGER, Bold:BOOL, Italic:BOOL, UnderLine:integer, FrColor:integer, BkColor:integer )
     VAR m_Size       = Size,
         m_Bold       = Bold,
         m_Italic     = Italic,
         m_UnderLine  = UnderLine,
         m_FrColor    = FrColor,
         m_BkColor    = BkColor;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        VAR Address;

        if( m_CellID == NULL ) /*Для всей строки*/
           Address = AddressDiapazon.GetRow( m_Row );
        else
           Address = AddressDiapazon.GetCell( m_Row, m_CellID );
        end;

        __BUG_Global_m_ObjTemplateXLS.SetDiapazon( Address );
        __BUG_Global_m_ObjTemplateXLS.SetFont( NULL, m_Size, m_Bold, m_Italic, m_UnderLine, m_FrColor, m_BkColor );
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row, CellID );
  END;

  CLASS (CFormat) CCellFormat( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER, Point:integer )
     VAR m_Point = Point;

     MACRO SetFormat( AddressDiapazon:OBJECT )
        
        VAR ThousandsSeparator = " ";
        
        if( __BUG_Global_m_ObjTemplateXLS.UsePoi() )
            ThousandsSeparator = ",";
            __LocaleDecimalSeparator = ".";
        else
            ThousandsSeparator = __BUG_Global_m_ObjTemplateXLS.Application.International(4/*xlThousandsSeparator*/);
            __LocaleDecimalSeparator = GetLocaleInfo(0, LOCALE_SDECIMAL, false); //vlv 25/03/20   506872
        end; 

        VAR StrFormat = "#" + string(ThousandsSeparator) + "##0";
        VAR Address = AddressDiapazon.GetCell( m_Row, m_CellID );

        if( (m_Point != null) and (m_Point > 0) )
           StrFormat = StrFormat + __LocaleDecimalSeparator + mkStr( "0", m_Point);
        end;

        __BUG_Global_m_ObjTemplateXLS.SetCellFormat(Address, StrFormat);

     ONERROR
       return;
     END;

     initCFormat( CurrentCSV, Row, CellID );
  END;

    CLASS (CFormat) CCellMerge( CurrentCSV:STRING, CellID_1:INTEGER, CellID_2:INTEGER, Row:INTEGER, RowHeight:DOUBLE )

     VAR m_RowHeight = RowHeight;
     VAR m_CellID_1 = CellID_1, m_CellID_2 = CellID_2;

     MACRO SetFormat(AddressDiapazon:OBJECT)
         if( m_RowHeight == null )
           m_RowHeight = 20;
         end;

         if( __BUG_Global_m_ObjTemplateXLS.UsePoi() )
           __BUG_Global_m_ObjTemplateXLS.SetMerge(AddressDiapazon.GetCell( m_Row, m_CellID_1 )+":"+AddressDiapazon.GetCell( m_Row, m_CellID_2 ));
           __BUG_Global_m_ObjTemplateXLS.RowHeight(m_Row, m_RowHeight);
           return;
         end;

         var Save_DisplayAlerts  = __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts; // Запомним состояние флажка, чтобы после смены восстановить его первоначальное состояние
         var Range = __BUG_Global_m_ObjTemplateXLS.sheet.Range( AddressDiapazon.GetCell( m_Row, m_CellID_1 )+":"+AddressDiapazon.GetCell( m_Row, m_CellID_2 ) );

        __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts = false; // Чтобы MSExcel не задавал лишних вопросов
        Range.Merge();
        Range.RowHeight = m_RowHeight;
      __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts = Save_DisplayAlerts;
     ONERROR
       return;
     END;

     initCFormat( CurrentCSV, Row, null );
  END;

   CLASS (CFormat) CCellMergeRow( CurrentCSV:STRING, CellID_1:INTEGER, CellID_2:INTEGER, Row:INTEGER, Row_2:INTEGER, RowHeight:DOUBLE )

     VAR m_RowHeight = RowHeight;
     VAR m_CellID_1 = CellID_1, m_CellID_2 = CellID_2;
     VAR m_Row_2    = Row_2;

     MACRO SetFormat(AddressDiapazon:OBJECT)
         if( m_RowHeight == null )
           m_RowHeight = 20;
         end;

         if( __BUG_Global_m_ObjTemplateXLS.UsePoi() )
           __BUG_Global_m_ObjTemplateXLS.SetMerge(AddressDiapazon.GetCell( m_Row, m_CellID_1 )+":"+AddressDiapazon.GetCell( m_Row_2, m_CellID_2 ));
           __BUG_Global_m_ObjTemplateXLS.RowHeight(m_Row, m_RowHeight);
           return;
         end;

         var Save_DisplayAlerts  = __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts; // Запомним состояние флажка, чтобы после смены восстановить его первоначальное состояние
         var Range = __BUG_Global_m_ObjTemplateXLS.sheet.Range( AddressDiapazon.GetCell( m_Row, m_CellID_1 )+":"+AddressDiapazon.GetCell( m_Row_2, m_CellID_2 ) );

        __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts = false; // Чтобы MSExcel не задавал лишних вопросов
        Range.Merge();
        Range.RowHeight = m_RowHeight;
      __BUG_Global_m_ObjTemplateXLS.Application.DisplayAlerts = Save_DisplayAlerts;
     ONERROR
       return;
     END;

     initCFormat( CurrentCSV, Row, null );
  END;

  CLASS (CFormat) CClearCellFormat( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER )
     MACRO SetFormat( AddressDiapazon:OBJECT )
        VAR Address;

        if( m_CellID == NULL ) /*Для всей строки*/
           Address = AddressDiapazon.GetRow( m_Row );
        else
           Address = AddressDiapazon.GetCell( m_Row, m_CellID );
        end;

        if( __BUG_Global_m_ObjTemplateXLS.UsePoi() )
        // todo
        else
           __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).ClearContents();
        end;
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row, CellID );
  END;

  CLASS (CFormat) CAlignCellFormat( CurrentCSV:STRING, Row:INTEGER, CellID:INTEGER, Alignment:integer )
      var m_Alignment = Alignment;
     MACRO SetFormat( AddressDiapazon:OBJECT )
        VAR Address;

        if( (m_CellID == NULL) or (m_CellID == -1) ) /*Для всей строки*/
           Address = AddressDiapazon.GetRow( m_Row );
        else
           Address = AddressDiapazon.GetCell( m_Row, m_CellID );
        end;
        if( __BUG_Global_m_ObjTemplateXLS.UsePoi() )
        // todo
        else
           __BUG_Global_m_ObjTemplateXLS.sheet.Range( Address ).HorizontalAlignment = m_Alignment;
        end;
     ONERROR
       return;
     END;
     initCFormat( CurrentCSV, Row, CellID );
  END;

  MACRO Merge( Cell1:STRING, Cell2:STRING, RowHeight:DOUBLE)
     if(m_UseTemplate)
       if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
         m_CellsFormat[m_CellsFormat.Size] = CCellMerge( m_CurrentCSV, NC(Cell1), NC(Cell2), m_CurrentRow, RowHeight  );
       end;
     else
       m_ObjWinRep.WinRep.SetCellWidth(m_CurrentRow+1, NC(Cell1), 100);

     end;

  END;

  MACRO getCurrentRow()
     return m_SheetCurrentRow;
  END;

  MACRO MergeRow( Cell1:STRING, Cell2:STRING, Row_1:INTEGER, Row_2:INTEGER, RowHeight:DOUBLE)
     if(m_UseTemplate)
       if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
         m_CellsFormat[m_CellsFormat.Size] = CCellMergeRow( m_CurrentCSV, NC(Cell1), NC(Cell2), Row_1, Row_2, RowHeight  );
       end;
     else
//       m_ObjWinRep.WinRep.SetCellWidth(m_CurrentRow+1, NC(Cell1), 100); Пока не нужна
     end;
  END;

  MACRO SetNumberPage(Namep:string, Nump )
    if( m_UseTemplate )
      if( m_ObjTemplateXLS.UsePoi() )
          m_ObjTemplateXLS.SetPageNumber(Namep, Nump);
          return true;
      end;
      m_ObjTemplateXLS.Sheet.PageSetup.CenterFooter    = Namep+MARGIN_ENG_PAGE;
      m_ObjTemplateXLS.Sheet.PageSetup.FirstPageNumber = Nump;
    end;
  END;

  MACRO DeleteRow(NumFrom:integer, NumTo:integer)
    var strRange = string(NumFrom)+":"+string(NumTo);
    if( m_UseTemplate )
      m_ObjTemplateXLS.Sheet.Rows(strRange).Delete();
    end;
  END;

  MACRO SetRowHeight(NumRow:integer, RHeight:integer)
    var strRange = string(NumRow)+":"+string(NumRow);
    if( m_UseTemplate )
      m_ObjTemplateXLS.RowHeight(NumRow, RHeight);
    end;
  END;

  MACRO SetActiveSheet( SheetName:STRING ):BOOL
     if( m_UseTemplate )
        if( m_ObjTemplateXLS.SheetChange( SheetName ) == true )
           m_UsedSheet[m_UsedSheet.Size] = SheetName;
           return true;
        end;
     else
        m_UsedSheet[m_UsedSheet.Size] = SheetName;
        m_ObjWinRep.WinRep.AddNewSheetBreak( SheetName );
        m_ObjWinRep.WinRep.SetFlagPrintFullHeader( true ); /*Печатать последнюю строку шапки на новой странице*/
        m_ObjWinRep.WinRep.SetFlagShowGrid( false );        /* установка флага показа разделительной сетки в Excel*/
        m_ObjWinRep.WinRep.SetFlagShowZeroValue( false );   /*установка флага показа нулевых значений в Excel*/
        return true;
     end;
     return false;
  ONERROR
     MsgBox( "В шаблоне \""+ m_TemplateName +"\"" + " не найден лист с именем \""+ SheetName +"\"" );
     return false;
  END;

  MACRO ExistsSheet( SheetName:STRING ):BOOL
    if( m_ObjTemplateXLS.UsePoi() )
       return m_ObjTemplateXLS.GetWorkbook().existsSheet(SheetName);
    end;
    for( var i, 1, m_ObjTemplateXLS.Book.Sheets.Count )
       if( m_ObjTemplateXLS.Book.Sheets(i).Name == SheetName )
          return true;
       end;
    end;
    return false;
  END;

  PRIVATE MACRO SheetUsed( SheetName:STRING ):BOOL
    VAR i = 0;

    while( i < m_UsedSheet.Size )
       if( StrUpr(m_UsedSheet[i]) == StrUpr(SheetName) )
          return true;
       end;
       i = i + 1;
    end;
    return false;
  END;

  PRIVATE MACRO DeleteNotUsedSheet()
     VAR i = 0;

     if( m_ObjTemplateXLS.SheetCount > 1 )
        while( i < m_ObjTemplateXLS.SheetCount )

           if( (m_ObjTemplateXLS.SheetChange( i+1 ) != true) OR
               (SheetUsed( m_ObjTemplateXLS.SheetName() ) != false) OR
               (m_ObjTemplateXLS.SheetDelete( i + 1 ) != true )
             )
              i = i + 1;
           end;
        end;
     end;
  END;

  /*Показать отчет на экран*/
  MACRO CReport_Show( NumCopy:INTEGER )
     VAR FileName : STRING = "";

    if(not CReport_DataExist()) // если нет данных, не надо ничего показывать,
      AuditLog(REPORT_NO_DATA, @AuditMsg);
      CReport_NoDataMsg();      // кроме, может быть, сообщения пользователю.
      return;
    end;

     if( __CalculateTimeExecute__ )
        BeginMarkTime();
     end;

     if( m_UseTemplate )
        if( m_UseTotalBook == false and (not m_ObjTemplateXLS.UseBigReport()) )
           DeleteNotUsedSheet();
        end;

        if( m_IsWebService == true )
          m_IsShowAppl = false;
        end;

        if( m_LastTotalBookName != "" )
           SplitFile( m_LastTotalBookName, FileName );
        else
           SplitFile( GetUniqAdd( m_TemplateName ), FileName );
        end;
        if( not m_ObjTemplateXLS.UseBigReport() )
           m_ObjTemplateXLS.SheetChange( 1 );
        end;
        m_ObjTemplateXLS.SaveAsTemplate( FileName, m_IsShowAppl );

        AuditLog(REPORT_FINISH, @AuditMsg, m_ObjTemplateXLS.ReportFileName_xls);
        m_FullReportFileNames[m_FullReportFileNames.Size] = m_ObjTemplateXLS.ReportPath + "\\" + m_ObjTemplateXLS.ReportFileName_xls;
/*        
        if( m_ObjTemplateXLS.UseMasterTotalBook() )
            m_ObjTemplateXLS.SaveMasterTotalBook(FileName, m_FullReportFileNames, m_IsShowAppl);
        end;
*/
        /*вызвать деструкторы что бы отконнетится. Актуально только если отвалились при выполнении по exception*/
        if( WR_GetLoadNewApplicationMode() or (NumCopy == 1)/*последняя*/ )
           m_ObjTemplateXLS = __BUG_Global_m_ObjTemplateXLS = NULL;
        end;
     else
        m_ObjWinRep.CReport_Show(NumCopy, AuditMsg);
        /*все должно быть в одном тектовом файле, поэтому только один объект на все копии*/
        if( NumCopy == 1 )
           m_FullReportFileNames = m_ObjWinRep.GetFullReportFileNames();
           m_ObjWinRep = NULL;
        end;
     end;

     if( __CalculateTimeExecute__ )
        EndMarkTime( "Время показа в Excel (CReport_Show):" );
     end;
  END;

  MACRO CReport_PrintLog()
     VAR count = 0;

     if( __CalculateTimeExecute__ )
        BeginMarkTime();
     end;

     if( m_UseTemplate )
        if( (ShowErrorsLog == true) AND (ErrorMesage.Size > 0) )
           m_ObjTemplateXLS.SheetAdd();
           m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );
           m_ObjTemplateXLS.SheetName( LogSheetName );
           SetActiveSheet( LogSheetName );
           m_ObjTemplateXLS.SetValue( 0,0,"Ошибки при выполнении отчета");
           m_ObjTemplateXLS.ColWidth(0, 100);
           m_ObjTemplateXLS.SetAlign(0,0,ALIGN_CENTER );
           m_ObjTemplateXLS.SetBkColor( 0, 0, 12451773 );
           m_ObjTemplateXLS.SetDiapazon(0, 0, 0, 0);
           m_ObjTemplateXLS.SetFont( null, null, true, null, null );
           while( count < ErrorMesage.Size )
              m_ObjTemplateXLS.SetValue( count+1, 0, StrSubst( ErrorMesage[count], "|", "\n" ) );
              m_ObjTemplateXLS.SetDiapazon( count + 1, 0, count + 1, 0 );
              m_ObjTemplateXLS.SetBorder();
              count = count + 1;
           end;
        end;
     else
        m_ObjWinRep.CReport_PrintLog();
     end;

     if( __CalculateTimeExecute__ )
        EndMarkTime( "Время печати лога (CReport_PrintLog):" );
     end;
  END;

  MACRO GetErroMessageArr()
    return ErrorMesage;
  END;

  MACRO CReport_Clear( IsFirstCopy:BOOL, NotCreateNewTemplate:BOOL, NotClearLog:BOOL )
     VAR i, ErrStr = "";
     CReport_Clear( IsFirstCopy, NotClearLog );

     m_OutMode = PrintMode();
     if( (m_OutMode == DL_OUTREPORT_EXCEL) OR (m_OutMode == DL_OUTREPORT_EXCEL_NO_FORMAT) )

        if( (NotCreateNewTemplate == NULL) OR (NotCreateNewTemplate == false) )
           if( WR_GetLoadNewApplicationMode() OR (IsFirstCopy AND (m_ObjTemplateXLS == NULL)) )
               if (m_UseBigReportMode AND (GetTemplateFileFormat(m_TemplateName) == WINREP_FORMAT_XLSX) )
                   m_ObjTemplateXLS = __BUG_Global_m_ObjTemplateXLS = CTemplateSXLSX(NULL);
               else
                   m_ObjTemplateXLS = __BUG_Global_m_ObjTemplateXLS = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, NULL, m_UsePOI, m_UseBigReportMode, m_FocreFormatRep);
               end;
           end;
        end;
        m_UseTemplate    = m_ObjTemplateXLS.OpenTemplate( m_TemplateName );
        //GAA: 513778
        if ( (m_TemplateName != "") AND (m_TemplateName != null) and (m_TemplateName == "registeria.xlsx"))  
           m_ObjTemplateXLS.SetXLSXFormat();
        end;
        //
        if( m_UseTemplate == false )
           i = 0;
           ErrStr = "Ошибка при открытии шаблона.\n";
           ReplaceMacro ( "msgbox", NULL );
           ReplaceMacro ( "RunError", NULL );
           isErrorMacReplaced = false;
           if( m_IsWebService == true )
              ReplaceMacro ( "isStandalone", NULL );
              ReplaceMacro ( "GetLocaleInfo", NULL );
           end;
           while( i < ErrorMesage.Size )
              ErrStr = ErrStr + ErrorMesage[i] + "\n";
              i = i + 1;
           end;
           MsgBox( ErrStr+"\nОтчет будет выпущен в текстовом виде." );
           ReplaceMacro ( "msgbox", "DL_MsgBoxFromReport" );
           ReplaceMacro ( "RunError", "DL_MsgBoxFromReport" );
           isErrorMacReplaced = true;
           if( m_IsWebService == true )
              ReplaceMacro ( "isStandalone", "WEB_isStandalone" );
              ReplaceMacro ( "GetLocaleInfo", "WEB_GetLocaleInfo" );
           end;
           m_OutMode = DL_OUTREPORT_STD;
        end;
     else
        m_UseTemplate = false;
     end;

     if( m_UseTemplate == false )
        /*все должно быть в одном тектовом файле, поэтому только один объект*/
        if( IsFirstCopy )
           m_ObjWinRep = CB_CReport( m_Form );
        end;
        m_ObjWinRep.CReport_Clear( IsFirstCopy );
        m_ObjWinRep.SetPrintMode( m_OutMode );
     end;

     m_UsedSheet.Size = 0;
  END;

  MACRO AlreadyInit():BOOL
     if( m_UseTemplate )
        return ( (m_ObjTemplateXLS != NULL) AND (m_ObjTemplateXLS.Book != NULL) );
     else
        return ( (m_ObjWinRep != NULL) AND m_ObjWinRep.AlreadyInit() );
     end;
  END;

  MACRO AddStandardFooter( CellPrefix:STRING, IsCurDate:BOOL, NoBook:BOOL )

     MACRO НачальникОтдела()
         var RS = TRsbDataSet("SELECT party.t_Name " +
                              "  FROM dofficer_dbt officer, dparty_dbt party " +
                              " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                              "       officer.t_OfficeID = " + string(SelfDivision) + " AND " +
                              "       officer.t_IsFirstOfficePerson = 'X' AND "
                              "       party.t_PartyID = officer.t_PersonID "
                             );

         if( RS.MoveNext() )
            return RS.Name;
         end;
         return "";
     END;

     MACRO Операционист()
         var RS = TRsbDataSet("SELECT person.t_Name " +
                              "  FROM dperson_dbt person " +
                              " WHERE person.t_Oper = " + string({oper})
                             );

         if( RS.MoveNext() )
            return RS.Name;
         end;
         return "";
     END;

     MACRO ДолжностьОперациониста()
         return "Исполнитель";
         var RS = TRsbDataSet("SELECT officer.t_Post " +
                              "  FROM dofficer_dbt officer, dperson_dbt person " +
                              " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                              "       person.t_Oper = " + string({oper}) + " AND " +
                              "       officer.t_PersonID = person.t_PartyID "
                             );

         if( RS.MoveNext() )
            return RS.Post;
         end;
         return "Ответственный сотрудник";
     END;

     MACRO ТелефонОперациониста()
         var PhoneNumber = "";
         var RS = TRsbDataSet("SELECT person.t_PartyID "
                              "  FROM dperson_dbt person " +
                              " WHERE person.t_Oper = " + string({oper})
                             );

         if( ( RS.MoveNext() ) and ( FindAdressContactValue( RS.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber ) == 0 ) )
            return PhoneNumber;
         end;
         return "";
     END;

     var RepDate, PostBoss, PostBook;

     if( m_UseTemplate )
        if( CellPrefix == null )
           CellPrefix = "";
        end;

        // IsCurDate добавлен по запросу 102303
        if( IsCurDate )
           RepDate = {curdate};
        else
           RepDate = Date();
        end;

        if( {Name_Boss} )
           PostBoss = {Name_Boss};
        else
           PostBoss = "Директор банка";
        end;

        if( {Name_Book} )
           PostBook = {Name_Book};
        else
           PostBook = "Главный бухгалтер банка"
        end;

        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ДолжностьДиректора",     PostBoss + " _____________ " );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"Директор",               {FIO_Boss} );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ДолжностьГлавБух",       PostBook + " _____________ " );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ГлавБух",                {FIO_Book} );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"НачальникОтдела",        НачальникОтдела() );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ДолжностьОперациониста", ДолжностьОперациониста() + " _____________ " );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"Операционист",           Операционист() );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ТелефонОперациониста",   ТелефонОперациониста() );
        m_ObjTemplateXLS.SetValue_NameCell( CellPrefix+"ДатаСоставления",        DL_DateToStr( RepDate ) );
     else
        m_ObjWinRep.AddStandardFooter( IsCurDate, NoBook );
     end;
  END;

  MACRO MoveStandartFooter(Footer:string, NumbSheet:integer)
     var Range  :object  = NULL;  
     var Address:string  = "";

     // Если указан некорректный номер листа, то ничего не делаем 
     if (m_ObjTemplateXLS.UsePoi())
       if ((NumbSheet == m_ObjTemplateXLS.GetWorkbook().getActiveSheetIndex()) or (NumbSheet > m_ObjTemplateXLS.SheetCount()))
         return;
       end;
     else
     if( (NumbSheet == m_ObjTemplateXLS.Sheet.Index) or (NumbSheet > m_ObjTemplateXLS.SheetCount()))
        return;
     end; 
     end;

     if( m_UseTemplate and Footer != "" )
        if( not m_ObjTemplateXLS.UsePoi() )
           m_ObjTemplateXLS.Sheet.Range(Footer).Copy;

           Address = m_ReportTable.AddressDiapazon.GetCell( m_ReportTable.AddressDiapazon.GetNumbRow() + 1, 0 );
           Range = m_ObjTemplateXLS.Book.Sheets.Item(NumbSheet).Range(Address);

           Range.PasteSpecial(xlPasteColumnWidths); 
           Range.PasteSpecial(xlPasteAll); 

           m_ObjTemplateXLS.Sheet.Range("A1").Copy();
           //m_ObjTemplateXLS.Sheet.Range("A1").Select();

           m_ObjTemplateXLS.Sheet.Range(Footer).Delete(xlUp); 
        end;
     end;
       
  END;


  PRIVATE MACRO SplitCellName( FullCellName:STRING, CellNameNoFormat:@STRING, CellFormat:@STRING, RunForExecMacro:BOOL )
     VAR Pos = Index( FullCellName, ":" );

     if( Pos > 0 )
        CellNameNoFormat = SubStr( FullCellName, 1, Pos-1);
        CellFormat       = SubStr( FullCellName, Pos+1, strlen(FullCellName) );
        if( RunForExecMacro )
           CellFormat = "\""+ CellFormat + "\"";
        end;
     else
        CellNameNoFormat = FullCellName;
        if( RunForExecMacro )
           CellFormat       = "null";
        else
           CellFormat       = null;
        end;
     end;
  END;

  PRIVATE MACRO ConvertValueToStringParm( SourceValue:VARIANT ):STRING
     VAR Day, Mon, Year;

     if( ValType(SourceValue) == V_STRING )
        return "\"" + StrSubst( SourceValue, "\n", "\"+\"\\n\"+\"" ) + "\"";
     elif( ValType(SourceValue) == V_DATE )
        DateSplit( SourceValue, Day, Mon, Year );
        return "Date("+ string(Day) + "," + string(Mon) + "," + string(Year) + ")";
     elif( ValType(SourceValue) == V_TIME )
        TimeSplit( SourceValue, Day, Mon, Year);
        return "Time("+ string(Day) + "," + string(Mon) + "," + string(Year) + ")";
     else
        return string(SourceValue);
     end;
     return null;
  END;

  MACRO PrintFormatString( FormatString:STRING /*...*/)
     VAR i = 2, CellName, CellValue, Parms, CellFormat;

     if( m_UseTemplate )
        while( GetParm( i, CellName) AND GetParm( i+1, CellValue) )
           SplitCellName( CellName, @CellName, null, true );
           m_ObjTemplateXLS.SetValue_NameCell( CellName , CellValue );
           CellValue = NULL;
           i = i + 2;
        end;
     else
        FormatString = m_ObjWinRep.WinRep.ChangeSpecialSymbol( FormatString, true );
        Parms = "\"["+StrSubst( FormatString, "\n", "\"+\"\\n\"+\"" )+"]()\"";

        while( GetParm( i, CellName) AND GetParm( i+1, CellValue) )
           SplitCellName( CellName, null, @CellFormat, true );
           CellValue = m_ObjWinRep.WinRep.ChangeSpecialSymbol( CellValue, true );
           Parms = Parms + "," + ConvertValueToStringParm(CellValue) + "," + CellFormat;/*форматирование не надо*/
           CellValue = NULL;
           i = i + 2;
        end;

        ExecExp( "ExecMacro( R2M(m_ObjWinRep.WinRep,\"AutoScan\"), "+ Parms +")" );
     end;
  END;

  PRIVATE MACRO IsNumber( StrVal:STRING ):BOOL
     StrVal = trim( StrVal );
     var StrValLen = strlen( StrVal );
     if( StrValLen > 32 )
        return false;
     end;

     var pp = index( StrVal, "." );
     if( pp > 0 )
        StrSet( StrVal, pp, "0" );
     end;

     var fch = SubStr( StrVal, 1, 1 );
     if( (fch == "-") OR (fch == "+") )
        StrSet( StrVal, 1, "0" );
     end;

     var i = StrValLen;
     while( i > 0 )
       if( index( "0123456789", SubStr( StrVal, i, 1 ) ) <= 0 )
          return false;
       end;
       i = i - 1;
     end;

     return true;
  END;

  PRIVATE MACRO CorrectNumberStr( CellValue:VARIANT ):VARIANT
     VAR CellType;

     if( (CellValue != null) AND (__LocaleDecimalSeparator != ".") )
        if( ValType( CellValue ) == V_STRING )
           if( IsNumber(CellValue) )
              var pp = index(CellValue, ".");
              if( pp > 0 )
                 StrSet( CellValue, pp, __LocaleDecimalSeparator );
              end;
           end;
        end;
     end;

     return CellValue;
  END;

  PRIVATE MACRO SetCellFormat( CellNumber:INTEGER, CellValue:VARIANT, Point:INTEGER )
     VAR CellType;

     if( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT )
        if( CellValue != null )
           CellType = ValType( CellValue );
           if( (Point != null) or
               ((m_UseApp == true) and ((CellType == V_DOUBLE)  or
                                        (CellType == V_DOUBLEL) or
                                        (CellType == V_MONEY)   or
                                        (CellType == V_MONEYL)  or
                                        (CellType == V_INTEGER)) )
             )
              if( (not ( ( ((CellType == V_DOUBLE) OR (CellType == V_DOUBLEL)) AND (Point == 4) ) OR
                         ( ((CellType == V_MONEY ) OR (CellType == V_MONEYL )) AND (Point == 2) ) )) OR (m_UseApp == true)
                )
                  m_CellsFormat[m_CellsFormat.Size] = CCellFormat( m_CurrentCSV, m_CurrentRow, CellNumber, Point );
              end;
           end;
        end;
     end;
  END;

  MACRO ExternalSetCellPoint( CellName:STRING, Point:INTEGER )
     if( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT )

        var ThousandsSeparator = "";

        if( m_ObjTemplateXLS.UsePoi() )
            ThousandsSeparator = ",";
            __LocaleDecimalSeparator = "."
        else
            ThousandsSeparator = m_ObjTemplateXLS.Application.International(4/*xlThousandsSeparator*/);;
        end;
        
        var StrFormat = "#" + string(ThousandsSeparator) + "##0";

        if( (Point != null) and (Point > 0) )
           StrFormat = StrFormat + __LocaleDecimalSeparator + mkStr( "0", Point);
        end;

        m_ObjTemplateXLS.SetCellFormat(CellName, StrFormat);
     end
  END;

  MACRO SetCellFont( CellName:STRING, Size:INTEGER, Bold:BOOL, Italic:BOOL, UnderLine:integer, FrColor:integer, BkColor:integer )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CFontFormat( m_CurrentCSV, m_CurrentRow, NC(CellName), Size, Bold, Italic, UnderLine, FrColor, BkColor );
        else
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.GetCurAddress(CellName) );
           m_ObjTemplateXLS.SetFont( NULL, Size, Bold, Italic, UnderLine, FrColor, BkColor );
        end;
     end;
  END;

  MACRO SetCurrentLineFont( Size:INTEGER, Bold:BOOL, Italic:BOOL, UnderLine:integer, FrColor:integer, BkColor:integer )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ))
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CFontFormat( m_CurrentCSV, m_CurrentRow, NULL, Size, Bold, Italic, UnderLine, FrColor, BkColor );
        else
           if( NOT m_ObjTemplateXLS.UsePoi() )
           m_ObjTemplateXLS.SetDiapazon( m_ReportTable.RangeTable.Rows.Row-1, 0, m_ReportTable.RangeTable.Rows.Row, m_ReportTable.RangeTable.Columns.Count-1 );
           m_ObjTemplateXLS.SetFont( NULL, Size, Bold, Italic, UnderLine, FrColor, BkColor );
        end;
     end;
     end;
  END;

  MACRO SetClearCell( /*...*/ )
      var CellName:STRING;
      var i = 1;
      if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
         if( m_UseCSV )
            while(GetParm( i, CellName))
               m_CellsFormat[m_CellsFormat.Size] = CClearCellFormat( m_CurrentCSV, m_CurrentRow, NC(CellName) );
               i =  i + 1;
            end;
         end;
      end;
  END;

  MACRO SetCellAlignment( CellName:STRING, Alignment:integer )
     if( m_UseTemplate AND ( m_OutMode != DL_OUTREPORT_EXCEL_NO_FORMAT ) )
        if( m_UseCSV )
           m_CellsFormat[m_CellsFormat.Size] = CAlignCellFormat( m_CurrentCSV, m_CurrentRow, NC(CellName), Alignment );
        end;
     end;
  END;

  MACRO SetLimitRow( RowNum:INTEGER )
    if( m_UseTemplate )
        if( m_UseCSV == true )
            m_CSVFile.SetLimitRow( RowNum )
        end;
    end;
  END;

  MACRO PrintTableLine( /*...*/ )
     VAR i = 1, CellName, CellValue, CellPoint, CellFormat, FldNumber, CurFldNumber = 0;

     if( m_UseTemplate )
        if( m_UseCSV == true )
/*ak*/
           var v_SetCellFormat = GetGlobalParameter("SetCellFormat", true);
           if(valtype(v_SetCellFormat) != V_BOOL)
             v_SetCellFormat = true;
           end;
/*~ak*/
           while( GetParm( i, CellName) AND GetParm( i+1, CellValue) AND GetParm( i+2, CellPoint))
              SplitCellName( CellName, @CellName, null, false );

              FldNumber = NC( CellName );
              while( CurFldNumber < FldNumber )
                 m_CSVFile.AddCell(null);
                 CurFldNumber = CurFldNumber + 1;
              end;

              if( CellValue == "" )
                 CellValue = null;
              end;

              m_CSVFile.AddCell( CorrectNumberStr( CellValue ), CellPoint );
/*ak*/
              if(v_SetCellFormat)
/*~ak*/
                SetCellFormat( FldNumber, CellValue, CellPoint );
/*ak*/
              end;
/*~ak*/
              CurFldNumber = CurFldNumber + 1;
              CellValue = NULL;
              i = i + 3;
           end;

           while( CurFldNumber < m_ReportLineCellNames.Size )
              m_CSVFile.AddCell(null);
              CurFldNumber = CurFldNumber + 1;
           end;
        else
           /*точность не используется, т.к. нет возможности в инструменте для этого*/
           while( GetParm( i, CellName) AND GetParm( i+1, CellValue) )
              if( CellName != NULL )
                 SplitCellName( CellName, @CellName, null, false );
                 m_ReportTable.SetValueCell( CellName , CellValue );
                 CellValue = NULL;
              end;
              i = i + 3;
           end;
        end;
     else
        while( GetParm( i, CellName) AND GetParm( i+1, CellValue) AND GetParm( i+2, CellPoint))
           SplitCellName( CellName, @CellName, @CellFormat, false );

           FldNumber = NC( CellName );
           while( m_ObjWinRep.m_CellNumber < FldNumber )
              m_ObjWinRep.AddCellTable( null, null, null );
           end;

           m_ObjWinRep.AddCellTable( CellValue, CellFormat, CellPoint );
           CellValue = NULL;
           i = i + 3;
        end;

        while( m_ObjWinRep.m_CellNumber < m_ReportLineCellNames.Size )
           m_ObjWinRep.AddCellTable( null, null, null );
        end;
     end;

     this.FinishLine();
  END;

    MACRO PrintTableLine_2point( /*...*/ )
     VAR i = 1, CellName, CellValue, ValPoint, CellPoint, CellFormat, FldNumber, CurFldNumber = 0;

     if( m_UseTemplate )
        if( m_UseCSV == true )
           while( GetParm( i, CellName) AND GetParm( i+1, CellValue) AND GetParm( i+2, ValPoint) and (GetParm( i+3, CellPoint)))
              SplitCellName( CellName, @CellName, null, false );

              FldNumber = NC( CellName );
              while( CurFldNumber < FldNumber )
                 m_CSVFile.AddCell(null);
                 CurFldNumber = CurFldNumber + 1;
              end;

              if( CellValue == "" )
                 CellValue = null;
              end;

              m_CSVFile.AddCell( CorrectNumberStr( CellValue ), ValPoint );
              SetCellFormat( FldNumber, CellValue, CellPoint );

              CurFldNumber = CurFldNumber + 1;
              CellValue = NULL;
              i = i + 4;
           end;

           while( CurFldNumber < m_ReportLineCellNames.Size )
              m_CSVFile.AddCell(null);
              CurFldNumber = CurFldNumber + 1;
           end;
        else
           /*точность не используется, т.к. нет возможности в инструменте для этого*/
           while( GetParm( i, CellName) AND GetParm( i+1, CellValue) )
              if( CellName != NULL )
                 SplitCellName( CellName, @CellName, null, false );
                 m_ReportTable.SetValueCell( CellName , CellValue );
                 CellValue = NULL;
              end;
              i = i + 3;
           end;
        end;
     else
        while( GetParm( i, CellName) AND GetParm( i+1, CellValue) AND GetParm( i+2, CellPoint))
           SplitCellName( CellName, @CellName, @CellFormat, false );

           FldNumber = NC( CellName );
           while( m_ObjWinRep.m_CellNumber < FldNumber )
              m_ObjWinRep.AddCellTable( null, null, null );
           end;

           m_ObjWinRep.AddCellTable( CellValue, CellFormat, CellPoint );
           CellValue = NULL;
           i = i + 3;
        end;

        while( m_ObjWinRep.m_CellNumber < m_ReportLineCellNames.Size )
           m_ObjWinRep.AddCellTable( null, null, null );
        end;
     end;

     this.FinishLine();
  END;


  MACRO AddCell( CellValue:VARIANT, CellPoint:INTEGER, CellFormat:STRING )
     if( m_UseTemplate )
        if( m_UseCSV == true )
           m_CSVFile.AddCell( CellValue, CellPoint );
           SetCellFormat( m_CellNumber, CellValue, CellPoint );
           m_CellNumber = m_CellNumber + 1;
        else
           /*точность не используется, т.к. нет возможности в инструменте для этого*/
           m_ReportTable.AddStr();  // Добавляем строку в таблицу
        end;
     else
        m_ObjWinRep.AddCellTable( CellValue, CellFormat, CellPoint );
     end;
  END;

  MACRO FinishLine()
     if( m_UseTemplate )
        if( m_UseCSV == true )
           // DEF-75936. Никаких AddRow(true) здесь быть не должно, игнорирование MAXROWSHEET приводит в POI к нехватке памяти  
           m_CSVFile.AddRow();
        else
           m_ReportTable.AddStr();
        end;
        m_CellNumber = 0;
     else
        m_ObjWinRep.FinishLine();
     end;
     m_CurrentRow = m_CurrentRow + 1;
     m_SheetCurrentRow = m_SheetCurrentRow + 1;
  END;

  PRIVATE MACRO GetCurrentCSV( FullName:STRING ):STRING
     VAR  Name = "", Ext = "";
     SplitFile( FullName, Name, Ext );
     return Name+Ext;
  END;

  MACRO GetFullFileNameCSV()
    if(m_CSVFile != NULL)
      return m_CSVFile.m_FileName;
    end;

    return "";
  END;

  MACRO PutRowToCSV(NewRow)
    if(m_CSVFile != NULL)
      m_CSVFile.PutRow(NewRow);
    end;
  END;

  /*По замечаниям ГПБ убрано наименование листа из формулы*/
  MACRO GetFormulaSummREG()
     VAR i = 0, StrSumm = "", BeginRow, CellAddr;

     while( i < m_NumCellAutoSumm.Size )

        if( m_FormulaSumm[i] == null ) /*для первой вкладки*/
           BeginRow = 1;
        else
           BeginRow = 0;
        end;

        CellAddr = m_ReportTable.AddressDiapazon.GetCell( 0, NC( m_NumCellAutoSumm[i] ) );
        if( m_ItogLineName == NULL ) // в последней строке таблицы
          if( m_ReportTable.AddressDiapazon.GetNumbRow() > 2 )
             StrSumm = m_ReportTable.GetFormulaSummREG( CellAddr, null, m_ReportTable.bRowTable, m_ReportTable.bRowTable+m_ReportTable.AddressDiapazon.GetNumbRow()-2 );
          end;
        else  // в первой строке таблицы
          if( m_ReportTable.AddressDiapazon.GetNumbRow() > 1 )
             StrSumm = m_ReportTable.GetFormulaSummREG( CellAddr, null, m_ReportTable.bRowTable+BeginRow, m_ReportTable.bRowTable+m_ReportTable.AddressDiapazon.GetNumbRow()-1 );
          else
             StrSumm = "";
          end;
        end;
        if( m_FormulaSumm[i] == null ) /*для первой вкладки*/
           m_FormulaSumm[i] = "";
        elif( substr(StrSumm, 1,1) == "=" )
           StrSet( StrSumm, 1, "+" );
        end;
        m_FormulaSumm[i] = m_FormulaSumm[i] + StrSumm;
        i = i + 1;
     end;
  END;

  PRIVATE MACRO GetFormulaSumm()
     VAR i = 0, StrSumm = "", BeginRow, CellAddr;

     while( i < m_NumCellAutoSumm.Size )

        if( m_FormulaSumm[i] == null ) /*для первой вкладки*/
           BeginRow = 1;
        else
           BeginRow = 0;
        end;

        CellAddr = m_ReportTable.AddressDiapazon.GetCell( 0, NC( m_NumCellAutoSumm[i] ) );
        if( m_ItogLineName == NULL ) // в последней строке таблицы
          if( m_ReportTable.AddressDiapazon.GetNumbRow() > 2 )
             StrSumm = m_ReportTable.GetFormulaSumm( CellAddr, null, m_ReportTable.bRowTable, m_ReportTable.bRowTable+m_ReportTable.AddressDiapazon.GetNumbRow()-2 );
          end;
        else  // в первой строке таблицы
          if( m_ReportTable.AddressDiapazon.GetNumbRow() > 1 )
             StrSumm = m_ReportTable.GetFormulaSumm( CellAddr, null, m_ReportTable.bRowTable+BeginRow, m_ReportTable.bRowTable+m_ReportTable.AddressDiapazon.GetNumbRow()-1 );
          else
             StrSumm = "";
          end;
        end;

        if( m_FormulaSumm[i] == null ) /*для первой вкладки*/
           m_FormulaSumm[i] = "";
        elif( substr(StrSumm, 1,1) == "=" )
           StrSet( StrSumm, 1, "+" );
        end;
        m_FormulaSumm[i] = m_FormulaSumm[i] + StrSumm;
        i = i + 1;
     end;
  END;

  MACRO AfterChangeFileName()
      m_CurrentCSV = GetCurrentCSV(m_CSVFile.m_FileName);
      if( m_SheetCurrentRow >= MAXROWSHEET)
         m_SheetCurrentRow = 0;
      end;
      m_TotalCurrentRow = m_TotalCurrentRow + m_CurrentRow;
      m_CurrentRow = 0;
      OnAfterChangeFileName();
  END;

  MACRO BeforeAddSheet( pThis:variant, CSVFileName:STRING )
      OnBeforeAddSheet();
  END;

  MACRO AfterAddSheet( pThis:variant, CSVFileName:STRING )
      OnAfterAddSheet();
      m_UsedSheet[m_UsedSheet.Size] = m_ObjTemplateXLS.SheetName();
  END;

  MACRO AfterAutoFill( pThis:variant, CSVFileName:STRING, Address_AutoFill:STRING )
     /*применить форматирование к вкладке*/
     if( m_FirstSheetName == NULL )
        m_FirstSheetName       = m_ObjTemplateXLS.SheetName();
        m_FirstAddressDiapazon = Address_AutoFill;

        pThis.AddressDiapazon.Save();
        pThis.AddressDiapazon.OffSet_Bottom(pThis.addRowCount);
        GetFormulaSumm();
        PrintLineAutoSumma();
        pThis.AddressDiapazon.Restore();

        OnAfterFirstAutoFill();
     end;

     SetFormat( GetCurrentCSV(CSVFileName) );
  END;

  MACRO AfterReOpenTempleFile( pThis:variant, CSVFileName:STRING )
      OnAfterReOpenTempleFile();
  END;

  /* Name       - Имя диапазона со строкой таблицы
     HeaderName - Имя диапазона с шапкой таблицы
  */
  PRIVATE MACRO InitTable( Name:STRING, Header:STRING )
     VAR CSVFileName = "";

     m_CurrentRow = 0;
     m_TotalCurrentRow = 0;
     m_SheetCurrentRow = 0;

     if( m_UseTemplate )
        if( m_UseCSV == true )
           m_CSVFile = C_CSVFile();

           if( m_UseBigReportMode )
               if (m_Row_Flush_Count)
                  m_CSVFile.SetLimitRow(m_Row_Flush_Count);
               else
                  m_CSVFile.SetLimitRow(ROW_FLUSH_COUNT);
               end;
           end;

           CSVFileName = m_CSVFile.CreateFullFileName( "MainTableData.txt" );

           if( m_CSVFile.FileOpen( CSVFileName, true) != true )
              RunError( "Ошибка при открытии файла " + CSVFileName );
           end;
           AfterChangeFileName();
           m_CSVFile.Set_Func_AddSheet( R2M( this, "AfterChangeFileName") );
        end;

        m_ReportTable     = m_ObjTemplateXLS.RegisterTable( Name, Header );
        m_ReportTableName = Name;
        /*в первой строке шаблона обычно есть значения для наглядности*/
        m_ReportTable.ClearCurRow();
        m_NumCellAutoSumm.Size = 0;
        m_ValueAutoSumm.Size   = 0;
        m_PointAutoSumm.Size   = 0;
        m_CellsFormat.Size     = 0;
        m_FormulaSumm.Size     = 0;
        m_FirstAddressDiapazon = NULL;
        m_FirstSheetName       = null;
        m_ReportTable.Set_Func_BeforeAddSheet( R2M( this, "BeforeAddSheet") );
        m_ReportTable.Set_Func_AfterAddSheet ( R2M( this, "AfterAddSheet") );
        m_ReportTable.Set_Func_AfterAutoFill(  R2M( this, "AfterAutoFill") );
        m_ReportTable.Set_Func_AfterReOpenTempleFile( R2M( this, "AfterReOpenTempleFile") );

     else
        m_ObjWinRep.SetTableHeader( Name, Header );

        m_ObjWinRep.m_NumCellAutoSumm.Size = 0;
        m_ObjWinRep.m_ValueAutoSumm.Size   = 0;
        m_ObjWinRep.m_PointAutoSumm.Size   = 0;
     end;
  END;

  MACRO RegisterTable( Name:STRING, Header:STRING /*...*/ )
     VAR i = 3, CellName;

     m_ReportLineCellNames.Size = 0;
     while( GetParm( i, CellName) )
        m_ReportLineCellNames[m_ReportLineCellNames.Size] = CellName;
        i = i + 1;
     end;

     InitTable( Name, Header );
  END;

  MACRO EndTable()
     if( m_UseTemplate )
        if( m_UseCSV == true )

           if( __CalculateTimeExecute__ )
              BeginMarkTime();
           end;

           m_CSVFile.FileClose();
           m_ReportTable.FillTabelFromCSV( m_CSVFile.GetlsFileName() );

           if( __CalculateTimeExecute__ )
              FormTimeMessage( "\nКоличество строк отчета: " + string(m_TotalCurrentRow) );
              EndMarkTime( "Время заполнения таблицы из CSV (FillTabelFromCSV):" );
           end;
        end;
     end;

     if( m_UseTemplate )
        m_ReportTable.EndTable();
     end;
  END;

  /*задать номера ячеек, по которым будет вычисляться сумма при добавлении в основную таблицу отчета*/
  MACRO SetCellAutoSumma( ItogLineName:STRING/*...*/ )
     VAR i = 2, CellName, FldNumber, j = 0;

     SetCellAutoSumma(); // из базового класса
     m_ItogLineName = ItogLineName;

     if( m_UseTemplate )
        while( GetParm( i, CellName) )
           m_NumCellAutoSumm[ j ] = CellName; /*номер полей с автосуммой*/
           i = i + 1;
           j = j + 1;
        end;
     else
        while( GetParm( i, CellName) )
           FldNumber = NC( CellName );
           m_ObjWinRep.m_NumCellAutoSumm[FldNumber] = true; /*номер полей с автосуммой*/
           m_ObjWinRep.m_ValueAutoSumm[FldNumber]   = 0;
           i = i + 1;
        end;
     end;
  END;

  /*задать номера ячеек, по которым будет вычисляться сумма при добавлении в основную таблицу отчета*/
  MACRO SetCellAutoSummaPoint( ItogLineName:STRING/*...*/ )
     VAR i = 2, CellName, FldNumber, j = 0, CellPoint;
     SetCellAutoSummaPoint(); // из базового класса
     m_ItogLineName = ItogLineName;

     if( m_UseTemplate )
        while( GetParm( i, CellName) )
           m_NumCellAutoSumm[ j ] = CellName; /*номер полей с автосуммой*/
           GetParm( i+1, CellPoint);
           m_PointAutoSumm[ j ] = CellPoint; /*номер полей с автосуммой*/
           i = i + 2;
           j = j + 1;
        end;
     else
        while( GetParm( i, CellName) )
           FldNumber = NC( CellName );
           m_ObjWinRep.m_NumCellAutoSumm[FldNumber] = true; /*номер полей с автосуммой*/
           m_ObjWinRep.m_ValueAutoSumm[FldNumber]   = 0;
           GetParm( i+1, CellPoint);
           m_ObjWinRep.m_PointAutoSumm[FldNumber]   = CellPoint;
           i = i + 2;
        end;
     end;
  END;

  MACRO ModifyValue( LineNumber:INTEGER, CellName:STRING, NewValue:VARIANT )
     VAR Address, CellNumber = NC( CellName );

     if( m_UseTemplate )
        m_ObjTemplateXLS.SetValue_NameCell( m_ReportTable.AddressDiapazon.GetCell(LineNumber, CellNumber), NewValue );
     else
        m_ObjWinRep.WinRep().SetCellValue( LineNumber, CellNumber, NewValue );
     end;
  END;

   /* Метод заносит Value в ячейки. Координаты могут быть заданы индексами (2, 0), или названими "В3"*/
  MACRO SetNumberFormat( Address:STRING, CellFormat:INTEGER )

     if( CellFormat == V_STRING)
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = "@";
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = "@";
     elif(CellFormat == V_INTEGER )
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = 0;
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = 0;
     elif((CellFormat == V_DOUBLE) OR (CellFormat == V_DOUBLEL))
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = "# ##0" + __LocaleDecimalSeparator + "0000";
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = "# ##0" + __LocaleDecimalSeparator + "0000";
     elif((CellFormat == V_MONEY)or(CellFormat == V_MONEYL))
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = DoubleFormat;
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = DoubleFormat;
     elif(CellFormat == V_DATE)
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = DateFormat;
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = DateFormat;
     elif(CellFormat == V_TIME)
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormat      = TimeFormat;
          m_ObjTemplateXLS.Sheet.Range(Address).NumberFormatLocal = TimeFormat;
     end;
  ONERROR
     return;
  END;

  PRIVATE MACRO PrintLineAutoSumma()
     VAR NumStr = null, Address;
     VAR i = 0, OldSheet;

     if( m_UseTemplate )
        if( m_ItogLineName != NULL ) // в первой строке таблицы
           OldSheet = m_ObjTemplateXLS.SheetName();
           m_ObjTemplateXLS.SheetChange( m_FirstSheetName );
        end;

        while( i < m_NumCellAutoSumm.Size )

           if( m_ItogLineName == NULL ) // в последней строке таблицы
              Address = m_ReportTable.AddressDiapazon.GetCell( m_ReportTable.AddressDiapazon.GetNumbRow()-1, NC( m_NumCellAutoSumm[i] ) );
           else  // в первой строке таблицы
              Address = CAddressDiapazon(m_FirstAddressDiapazon).GetCell(0, NC( m_NumCellAutoSumm[i] ) );
           end;

           m_ObjTemplateXLS.SetValue_NameCell( Address, m_FormulaSumm[i] );
           i = i + 1;
        end;

        m_FormulaSumm.Size = 0;
        if( m_ItogLineName != NULL ) // в первой строке таблицы
           m_ObjTemplateXLS.SheetChange( OldSheet );
        end;
     else
        if( m_ObjWinRep.m_NumCellAutoSumm.Size > 0 )
           if( m_ItogLineName != NULL )
              m_ObjWinRep.WinRep.FindCellValue( m_ItogLineName, 0, m_ObjWinRep.WinRep.GetCountColTable(), 0, m_ObjWinRep.WinRep.GetNumLastStr(), NumStr );
           else
              NumStr = null;
           end;
           m_ObjWinRep.PrintLineAutoSumma( NumStr );
        end;
     end;
  END;

  MACRO CreateLineByTemplate( FirstColName:STRING, ColSize:INTEGER, NameDiapazon:STRING )

    VAR i = 1,
        FirstRange = m_ObjTemplateXLS.sheet.Range( FirstColName ),
        FirstAddr  = CAddressDiapazon( FirstRange.Address + ":"+ FirstRange.Address);

    FirstRange.AutoFill( m_ObjTemplateXLS.sheet.Range( FirstColName + ":" + FirstAddr.GetCell( 0, ColSize-1) ), xlFillFormats );

    while( i < ColSize )
       m_ObjTemplateXLS.Sheet.Names.Add( String( FirstColName,i ), m_ObjTemplateXLS.sheet.Range( FirstAddr.GetColumn(i) ) );
       i = i + 1;
    end;

    if( NameDiapazon != NULL )

       m_ObjTemplateXLS.Sheet.Names.Add( NameDiapazon, m_ObjTemplateXLS.sheet.Range( FirstAddr.GetCell(0, 0) + ":" + FirstAddr.GetCell(0, ColSize-1) ) );
    end;
  END;

  MACRO CreateTable( LabelName:STRING, LabelHeader:STRING, LabelNumber:STRING, LabelValue:STRING,
                     ScrolName:STRING
                     /*... имена полей таблицы, значение по умолчанию*/ )

     VAR i, j, CellName, N_CellName, H_CellName, V_CellName, CellFormat,
         FirstRange = m_ObjTemplateXLS.sheet.Range( LabelValue ),
         FirstAddr  = CAddressDiapazon( FirstRange.Address + ":"+ FirstRange.Address),
         ConstParmNum = 6,  /*6 - кол-во постоянных параметров + this */
         ColNum = parmcount() - ConstParmNum; /*кол-во пар поле в шапке, значение*/

     this.CreateLineByTemplate( LabelHeader, ColNum/2 );
     if( LabelNumber != NULL )
        this.CreateLineByTemplate( LabelNumber, ColNum/2 );
     end;
     this.CreateLineByTemplate( LabelValue, ColNum/2, "DynMainTable" );

     if( LabelName != NULL )
        m_ObjTemplateXLS.SetValue_NameCell( LabelName, ScrolName );
     end;

     m_ReportLineCellNames.Size = 0;
     j = i = 0;
     while( (i < ColNum) AND (GetParm( i+ConstParmNum, CellName)) AND (GetParm( i+1+ConstParmNum, CellFormat)))
        this.SetNumberFormat( FirstAddr.GetColumn(j), CellFormat );

        if( LabelNumber != NULL )
           if( j > 0 )
              N_CellName = LabelNumber + string(j);
           else
              N_CellName = LabelNumber;
           end;
           m_ObjTemplateXLS.SetValue_NameCell( N_CellName, string(j+1) );
        end;

        if( j > 0 )
           H_CellName = LabelHeader + string(j);
           V_CellName = LabelValue  + string(j);
        else
           H_CellName = LabelHeader;
           V_CellName = LabelValue;
        end;
        m_ObjTemplateXLS.SetValue_NameCell( H_CellName, string(CellName) );
        m_ReportLineCellNames[m_ReportLineCellNames.Size] = V_CellName;

        i = i + 2;
        j = j + 1;
        CellName     = NULL;
        CellFormat = NULL;
     end;

     InitTable( "DynMainTable", ScrolName );
  END;

  MACRO CopySheetTo( SheetName:STRING ):BOOL
     VAR SourceName, Address;

     /*запомнить текущую страницу*/
     SourceName = m_ObjTemplateXLS.SheetName();
     /*скопировать текущую страницу*/

/*  только таблица с данными
     m_ObjTemplateXLS.sheet.Range( "_Header1" + ":" +
                                   m_ReportTable.AddressDiapazon.GetCell( m_CurrentRow, 0 )
                                 ).Copy();  // Скопируем данные
*/
     Address = CAddressDiapazon( m_ObjTemplateXLS.sheet.UsedRange.Address );
     m_ObjTemplateXLS.sheet.Range( Address.Get_bRow()+1 + ":" + Address.Get_eRow()+1 ).Copy();  // Скопируем данные

     /*перейти на страницу, куда надо скопировать */
     SetActiveSheet( SheetName );

     Address = CAddressDiapazon( m_ObjTemplateXLS.sheet.UsedRange.Address );

     m_ObjTemplateXLS.sheet.Rows( Address.Get_eRow()+3 ).Select();  // Скопируем данные
     m_ObjTemplateXLS.sheet.Paste();
/*
     Address = CAddressDiapazon( m_ObjTemplateXLS.sheet.UsedRange.Address );
     m_ObjTemplateXLS.sheet.Rows( Address.Get_eRow()+3 ).Select();  // Скопируем данные
     m_ObjTemplateXLS.sheet.Paste();

     Address = CAddressDiapazon( m_ObjTemplateXLS.sheet.UsedRange.Address );
     m_ObjTemplateXLS.sheet.Rows( Address.Get_eRow()+3 ).Select();  // Скопируем данные
     m_ObjTemplateXLS.sheet.Paste();
*/
     /*восстановить текущую */
     SetActiveSheet( SourceName );
     m_ObjTemplateXLS.SheetClear(m_ObjTemplateXLS.sheet.index);
  END;

  MACRO OpenTemplate()
      m_UseTemplate  = m_ObjTemplateXLS.OpenTemplate( m_TemplateName );
  END;

  MACRO CreateTotalBook()
    if(m_UseTemplate)
      m_ObjTemplateXLS.CreateTotalBook();
      m_UseTotalBook = true;
    end;
  END;

  MACRO ReSetUseTotalBook()
    m_UseTotalBook = false;
  END;

  MACRO UseNewSheetInTotalBook()
    if(m_UseTemplate)
      m_ObjTemplateXLS.NeedNewSheet = true;
    end;
  END;

  MACRO RenameSheet( NewName:STRING )
    if(m_UseTemplate)
       VAR i = 0;

       while( i < m_UsedSheet.Size )
          if( StrUpr(m_UsedSheet[i]) == StrUpr(m_ObjTemplateXLS.SheetName()) )
             m_UsedSheet[i] = StrUpr(NewName);
             break;
          end;
          i = i + 1;
       end;

       m_ObjTemplateXLS.SheetName(NewName);
    end;
  END;

  MACRO GetTableRange()
    if(m_UseTemplate)
      return m_ReportTable.GetTableRange();//
    end;
  END;

private var dbg_time = null;
private var dbg_num = 0;

private macro dbg(msg)
  if (DEBUG_COPY)
    if (msg == null)
      dbg_num = dbg_num + 1;
      msg = string(dbg_num);
    else
      dbg_num = 0;
    end;
    if (dbg_time != null)
      msg = string(time() - dbg_time) + " " + string(msg);
    end;
    println(msg);
    dbg_time = time();
  end;
end;

  MACRO CopyAllSheetInTotalBook(SheetName:string,//Имя закладки,по умолчанию имя закладки из шаблона
                                _FlagSheet:bool,// true - на новую закладку, на существующую
                                _CopyPos:string,//Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                _PastePos:variant, // сколько строчек отступить (int) или куда на новом листе копировать, например, "A1"
                                _PasteSheetName:variant, // Имя закладки, в которую копировать
                                _NotUseActivation:bool) : bool //Не использовать активацию закладки в шаблоне (нужно при размножении вкладок в новой общей книге)
    dbg(">>> CopyAllSheetInTotalBook");

    if(m_UseTemplate)
      if( not m_ObjTemplateXLS.UseBigReport() )
        dbg(">>> DeleteNotUsedSheet");
        DeleteNotUsedSheet();
        dbg("DeleteNotUsedSheet >>>");
        if(not ((ValType(_NotUseActivation) == V_BOOL) and (_NotUseActivation == true)))
          if( SheetName )
            dbg(">>> SetActiveSheet");
            SetActiveSheet( SheetName );
            dbg("SetActiveSheet >>>");
          end;
        end;
      end;
      dbg(">>> CopyAllSheetInTotalBook");
      m_ObjTemplateXLS.CopyAllSheetInTotalBook(SheetName, _FlagSheet, _CopyPos, _PastePos, _PasteSheetName);
      dbg("CopyAllSheetInTotalBook >>>");
      dbg(">>> Close");
      m_ObjTemplateXLS.Close();
      dbg("Close >>>");
    end;

    dbg(">>> CReport_Clear");
    CReport_Clear( false,true, true);
    dbg("CReport_Clear >>>");

    if(m_UseTemplate)
      if(not ((ValType(_NotUseActivation) == V_BOOL) and (_NotUseActivation == true)))
        if( SheetName )
          dbg(">>> SetActiveSheet");
          SetActiveSheet( SheetName );
          dbg("SetActiveSheet >>>");
        end;
      end;
    end;

    dbg("CopyAllSheetInTotalBook >>>");

  END;

  MACRO ReplaceAndCalculate( FromStr:string, ToStr:string )
    if( m_ObjTemplateXLS.UsePoi() )
        m_ObjTemplateXLS.ReplaceCellString(FromStr, ToStr);
        m_ObjTemplateXLS.Calculate();
        return;
    end;

    var Save_DisplayAlerts = m_ObjTemplateXLS.Application.DisplayAlerts,
        Save_Calculation   = m_ObjTemplateXLS.Application.Calculation;

    m_ObjTemplateXLS.Application.DisplayAlerts = false; /* Чтобы MSExcel не задавал лишних вопросов */
    m_ObjTemplateXLS.Application.Calculation   = -4135; /* xlManual */

    m_ProgressIndicator.Start( m_ObjTemplateXLS.Book.Sheets.Count, "Обработка и пересчет формул шаблона", "Обработка и пересчет формул шаблона" );
    var i = 1;
    while( i <= m_ObjTemplateXLS.Book.Sheets.Count )
       m_ObjTemplateXLS.Book.Sheets(i).Cells.Replace(FromStr, ToStr, 2/*xlPart*/, 1/*xlByRows*/, false, false, false);
       i = i + 1;
       m_ProgressIndicator.Update( i, m_ObjTemplateXLS.Book.Sheets.Count );
    end;
    m_ProgressIndicator.Stop();

    m_ObjTemplateXLS.Application.Calculation   = Save_Calculation;

    m_ObjTemplateXLS.Application.Calculate();
    m_ObjTemplateXLS.Application.DisplayAlerts = Save_DisplayAlerts;

  END;

  macro GetLastPosition( SheetName:string ):integer

     var RetVal:integer = 0;

     if(m_UseTemplate)
       RetVal = m_ObjTemplateXLS.GetLastPosition( SheetName );
     end;

     return RetVal;
  end;

  MACRO SaveTotalBook()
    if(m_UseTemplate)
      m_ObjTemplateXLS.Close();
      if( not m_ObjTemplateXLS.UsePoi() or (m_ObjTemplateXLS.UsePoi() and (not m_ObjTemplateXLS.UseBigReport()) ) )
        m_ObjTemplateXLS.SaveTotalBook( "itogs", false );
      else
        m_ObjTemplateXLS.CopyTotalBookToWorkBook();
      end;
    end;
  END;

  MACRO SaveSubTotalBook(NameFile:string, IsShowAppl:bool)
    if(m_UseTemplate)
      var p_NameFile   = "itogs";
      var p_IsShowAppl = false;
      
      if( ValType(NameFile) == V_STRING )
         p_NameFile = NameFile;
      end;
      if( ValType(IsShowAppl) == V_BOOL )
         p_IsShowAppl = IsShowAppl;
      end;

      if( m_IsWebService )
        p_IsShowAppl = false;
      end;

      m_ObjTemplateXLS.SaveSubTotalBook( p_NameFile, p_IsShowAppl );
      m_FullReportFileNames[m_FullReportFileNames.Size] = m_ObjTemplateXLS.ReportPath + "\\" + m_ObjTemplateXLS.ReportFileName_xls;
    end;
  END;

  MACRO NeedMasterTotalBook()
    if(m_UseTemplate)
      m_ObjTemplateXLS.NeedMasterTotalBook();
    end;
  END;

  MACRO SetLastTotalBookName( LastTotalBookName:string )
    if(m_UseTemplate)
      m_LastTotalBookName = LastTotalBookName;
    end;
  END;
 

  MACRO SetSubTotal(StartStrNumber, EndStrNumber /*...*/ )
     var Formula = "";
     var SheetName = "";
     var CellName = "", ColumnName = "", TmpAddress="";
     var Pos = 0, i = 3;

     if(m_UseTemplate)
       if( m_ObjTemplateXLS.UsePoi() )
           SheetName = m_ObjTemplateXLS.GetWorkbook().getSheetName();
       else
           SheetName = m_ObjTemplateXLS.SheetName();
       end;

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "SUBTOTAL";
       else                                                                  Formula = "ПРОМЕЖУТОЧНЫЕ.ИТОГИ";
       end;

       while(GetParm( i, CellName))
         if( m_ObjTemplateXLS.UsePoi() )
             TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
         else
         TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
         end;
         if( (Pos = index(TmpAddress, "$")) != 0 )
           TmpAddress = SubStr(TmpAddress, Pos+1);
           if( (Pos = index(TmpAddress, "$")) != 0 )
             ColumnName = SubStr(TmpAddress, 1, Pos-1);
           end;
         end;

         m_ObjTemplateXLS.SetValue_NameCell( CellName, "="+Formula+"(9;'"+SheetName+"'!$"+ColumnName+"$"+StartStrNumber+":"+"$"+ColumnName+"$"+EndStrNumber+")" );
         i = i + 1;
       end;
     end;
  END;

  MACRO SetSubTotalEx( HeaderEndStrNumber, StartStrNumber /*...*/ )
     var Formula = "", FormulaTxt = "";
     var SheetName = "";
     var CellName = "", ColumnName = "", TmpAddress="";
     var Pos = 0, i = 3;
     var FromSheetNum = 0, j = 0;

     if(m_UseTemplate)
       if( m_ObjTemplateXLS.UsePoi() )
           SheetName = m_ObjTemplateXLS.GetWorkbook().getSheetName();
       else
           SheetName = m_ObjTemplateXLS.SheetName();
       end;


       while( j < m_UsedSheet.Size )
          if( StrUpr(m_UsedSheet[j]) == StrUpr(SheetName) )
             FromSheetNum = j;
          end;
          j = j + 1;
       end;

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "SUBTOTAL";
       else                                                                  Formula = "ПРОМЕЖУТОЧНЫЕ.ИТОГИ";
       end;
       while(GetParm( i, CellName))
         if( m_ObjTemplateXLS.UsePoi() )
             TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
         else
         TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
         end;
         if( (Pos = index(TmpAddress, "$")) != 0 )
           TmpAddress = SubStr(TmpAddress, Pos+1);
           if( (Pos = index(TmpAddress, "$")) != 0 )
             ColumnName = SubStr(TmpAddress, 1, Pos-1);
           end;
         end;

         j = FromSheetNum;
         FormulaTxt = "";
         while( j < m_UsedSheet.Size )
            if( FormulaTxt == "" )
               FormulaTxt = "="+Formula+"(9;"+ColumnName+StartStrNumber+":"+ColumnName+string(HeaderEndStrNumber+MAXROWSHEET)+")";
            else
               FormulaTxt = FormulaTxt +"+"+ Formula+"(9;"+ColumnName+string(m_ReportTable.bRowTable)+":"+ColumnName+string(1+MAXROWSHEET)+")";
            end;
            j = j + 1;
         end;

         m_ObjTemplateXLS.SetValue_NameCell( CellName, FormulaTxt  );

         i = i + 1;
       end;
     end;
  END;
 /*PDV*/

   MACRO SetSubTotalEx1( HeaderEndStrNumber, StartStrNumber /*...*/ )                                                                             
      var Formula = "", FormulaTxt = "";                                                                                                         
      var SheetName = "";                                                                                                                        
      var CellName = "", ColumnName = "", TmpAddress="";                                                                                         
      var Pos = 0, i = 3;                                                                                                                        
      var FromSheetNum = 0, j = 0;                                                                                                               
                                                                                                                                                 
      if(m_UseTemplate)                                                                                                                          
        /*if( m_ObjTemplateXLS.UsePoi() )                                                                                                          
          return;                                                                                                                                
        end;*/                                                                                                                                     
                                                                                                                                                 
        if( m_ObjTemplateXLS.UsePoi() )
           SheetName = m_ObjTemplateXLS.GetWorkbook().getSheetName();
        else
           SheetName = m_ObjTemplateXLS.SheetName();
        end;                                                                                                
                                                                                                                                                 
        while( j < m_UsedSheet.Size )                                                                                                            
           if( StrUpr(m_UsedSheet[j]) == StrUpr(SheetName) )                                                                                     
              FromSheetNum = j;                                                                                                                  
           end;                                                                                                                                  
           j = j + 1;                                                                                                                            
        end;                                                                                                                                     
                                                                                                                                                 
        if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "SUBTOTAL";                                              
        else                                                                  Formula = "ПРОМЕЖУТОЧНЫЕ.ИТОГИ";                                   
        end;
                                                                                                                                     
        while( GetParm( i, CellName) )
          if( m_ObjTemplateXLS.UsePoi() )
             TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
          else
             TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
          end;
                                                                                                                                                           
          if( (Pos = index(TmpAddress, "$")) != 0 )                                                                                              
             TmpAddress = SubStr(TmpAddress, Pos+1);                                                                                              
             if( (Pos = index(TmpAddress, "$")) != 0 )                                                                                            
                ColumnName = SubStr(TmpAddress, 1, Pos-1);                                                                                         
             end;                                                                                                                                 
          end;                                                                                                                                   
                                                                                                                                                 
          j = FromSheetNum;                                                                                                                      
          FormulaTxt = "";                                                                                                                       
          while( j < m_UsedSheet.Size )                                                                                                          
             if( FormulaTxt == "" )                                                                                                              
                FormulaTxt = "="+Formula+"(9;"+ColumnName+StartStrNumber+":"+ColumnName+string(HeaderEndStrNumber+MAXROWSHEET)+")";              
             else                                                                                                                                
                FormulaTxt = FormulaTxt +"+"+ Formula+"(9;"+ColumnName+string(m_ReportTable.bRowTable)+":"+ColumnName+string(1+MAXROWSHEET)+")"; 
             end;                                                                                                                                
             j = j + 1;                                                                                                                          
          end;                                                                                                                                   
                                                                                                                                                 
          m_ObjTemplateXLS.SetValue_NameCell( CellName, FormulaTxt  );                                                                           
                                                                                                                                                 
          i = i + 1;                                                                                                                             
        end;                                                                                                                                     
      end;                                                                                                                                       
   END;                                                                                                                                          

  MACRO SetUseApp( UseApp:bool )
     m_UseApp = UseApp;
  END;

  MACRO SetXLSXFormat()
     if(m_UseTemplate)
        m_ObjTemplateXLS.SetXLSXFormat();
     end;
  END;

  /*BEA begin*/
  /* FirstColumn "Sign" SecondColumn*/
  MACRO SetOneSignFormula( FirstColumn, SecondColumn, Sign, point )
     var SheetName = "";
     var Pos = 0, i = 3;
     var Formula = "";

     if (point == null) point = 2; end;

     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND";
     else                                                                  Formula = "ОКРУГЛ";
     end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();
       return "="+Formula+"("+FirstColumn+Sign+SecondColumn+"; "+point+")";
     end;
  END;

  /* FirstColumn "OneSign" SecondColumn "TwoSign" ThirdColumn*/
  MACRO SetTwoSignFormula( FirstColumn, SecondColumn, ThirdColumn, OneSign, TwoSign, point )
     var SheetName = "";
     var Pos = 0, i = 3;
     var Formula = "";

     if (point == null) point = 2;  end;

     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND";
     else                                                                  Formula = "ОКРУГЛ";
     end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();
       return "="+Formula+"("+FirstColumn+OneSign+SecondColumn+TwoSign+ThirdColumn+"; "+point+")";
     end;

  END;

  /* FirstColumn "OneSign" SecondColumn "TwoSign" ThirdColumn "ThreeSign" FouthColumn*/
  MACRO SetThreeSignFormula( FirstColumn, SecondColumn, ThirdColumn, FouthColumn, OneSign, TwoSign, ThreeSign, point )
     var SheetName = "";
     var Pos = 0, i = 3;
     var Formula = "";

     if (point == null) point = 2;  end;

     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND";
     else                                                                  Formula = "ОКРУГЛ";
     end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();
       return "="+Formula+"("+FirstColumn+OneSign+SecondColumn+TwoSign+ThirdColumn+ThreeSign+FouthColumn+"; "+point+")";
     end;

  END;

  /*IF(FirstColumn "Condition" ;SecondColumn;ThirdColumn)*/
  MACRO SetIFFormula( FirstColumn, SecondColumn, ThirdColumn, Condition, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2;  end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(IF";
       else                                                                  Formula = "ОКРУГЛ(ЕСЛИ";
       end;

       return "="+Formula+"("+FirstColumn+Condition+";"+SecondColumn+";"+ThirdColumn+")"+"; "+point+")";
     end;
  END;

    /*IF(FirstColumn "Condition" ;SecondColumn;ThirdColumn)*/
  MACRO SetIFFormulaShort( FirstColumn, SecondColumn, ThirdColumn, Condition )
     var Formula = "";
     var SheetName = "";

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "IF";
       else                                                                  Formula = "ЕСЛИ";
       end;

       return "="+Formula+"("+FirstColumn+Condition+";"+SecondColumn+";"+ThirdColumn+")";
     end;
  END;

  /* SUM(FirstColumn;SecondColumn)*/
  MACRO SetSUMFormula( FirstColumn, SecondColumn, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2; end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(SUM";
       else                                                                  Formula = "ОКРУГЛ(СУММ";
       end;

       return "="+Formula+"("+FirstColumn+":"+SecondColumn+")"+"; "+point+")";
     end;
  END;

  /* SUMIFS(FirstColumn;SecondColumn;ThirdColumn;FouthColumn;FifhtColumn)*/
  MACRO SetSUMIFSFormula( FirstColumn, SecondColumn, ThirdColumn, FouthColumn, FifthColumn, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2; end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(SUMIFS";
       else                                                                  Formula = "ОКРУГЛ(СУММЕСЛИМН";
       end;

       return "="+Formula+"("+FirstColumn+";"+SecondColumn+";"+ThirdColumn+";"+FouthColumn+";"+FifthColumn+")"+"; "+point+")";
     end;
  END;

  /* SUMIFS(FirstColumn;SecondColumn;ThirdColumn;FouthColumn;FifhtColumn) "OneSign" SixColumn*/
  MACRO SetSUMIFSOneSignFormula( FirstColumn, SecondColumn, ThirdColumn, FouthColumn, FifthColumn, SixColumn, OneSign, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2; end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(SUMIFS";
       else                                                                  Formula = "ОКРУГЛ(СУММЕСЛИМН";
       end;

       return "="+Formula+"("+FirstColumn+";"+SecondColumn+";"+ThirdColumn+";"+FouthColumn+";"+FifthColumn+")"+OneSign+SixColumn+"; "+point+")";
     end;
  END;

  /* SUMIFS(FirstColumn;SecondColumn;ThirdColumn)*/
  MACRO SetSUMIFSShortFormula( FirstColumn, SecondColumn, ThirdColumn, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2; end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(SUMIFS";
       else                                                                  Formula = "ОКРУГЛ(СУММЕСЛИМН";
       end;

       return "="+Formula+"("+FirstColumn+";"+SecondColumn+";"+ThirdColumn+")"+"; "+point+")";
     end;
  END;

  MACRO SetOLD_SUMIFSFormula(FirstColumn, SecondColumn, ThirdColumn, 
                             FouthColumn, FifthColumn, point )
    var Formula = "";
    var SheetName = "";

    if (point == null) point = 2; end;

    if (m_UseTemplate)
      SheetName = m_ObjTemplateXLS.SheetName();

      if (m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS) 
        Formula = "ROUND(SUMPRODUCT";
      else
        Formula = "ОКРУГЛ(СУММПРОИЗВ";
      end;

      return "="+Formula+"(("+SecondColumn+ThirdColumn+")*("+FouthColumn+FifthColumn+");"+FirstColumn+");"+point+")";
    end;
  END;

  MACRO SetOLD_SUMIFSFormula_3_cond(FirstColumn, SecondColumn, ThirdColumn, 
                                    FouthColumn, FifthColumn,
                                    Column6, Column7, point )
    var Formula = "";
    var SheetName = "";

    if (point == null) point = 2; end;

    if (m_UseTemplate)
      SheetName = m_ObjTemplateXLS.SheetName();

      if (m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS) 
        Formula = "ROUND(SUMPRODUCT";
      else
        Formula = "ОКРУГЛ(СУММПРОИЗВ";
      end;

      return "="+Formula+"(("+SecondColumn+ThirdColumn+")*("+FouthColumn+FifthColumn+")*("+Column6+Column7+");"+FirstColumn+");"+point+")";
    end;
  END;

  /* ISNUMBER(FirstColumn)*/
  MACRO SetIsNumberFormula( FirstColumn, point )
     var Formula = "";
     var SheetName = "";

     if (point == null) point = 2; end;

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ROUND(ISNUMBER";
       else                                                                  Formula = "ОКРУГЛ(ЕЧИСЛО";
       end;

       return "="+Formula+"("+FirstColumn+")"+"; "+point+")";
     end;
  END;

  MACRO SetIsNumberFormulaShort( FirstColumn )
     var Formula = "";
     var SheetName = "";

     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "ISNUMBER";
       else                                                                  Formula = "ЕЧИСЛО";
       end;

       return "="+Formula+"("+FirstColumn+")";
     end;
  END;
/*BEA end*/

  // Показывать файл после выпуска отчета
  MACRO SetIsShowAppl( IsShowAppl:bool )
     m_IsShowAppl = IsShowAppl;
  END;

  InitDL_CReportBase( Form );
  if( UseCSV == null )
     UseCSV = true;
  end;
  m_UseCSV = UseCSV;
  m_IsWebService = IsWebService;
  m_ReportHashCode = ReportHashCode;
  m_UsePOI = UsePOI;
  m_UseBigReportMode = UseBigReportMode;
  m_FocreFormatRep = FocreFormatRep; 
END;

