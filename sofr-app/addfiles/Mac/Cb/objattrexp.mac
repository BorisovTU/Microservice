/*
$Name:        objattrexp.mac
$Module:      Финансовый мониторинг
$Description: Функция экспорта категорий объекта
*/

import "FmXmlWriter.mac", BankInter, rsexts;
import CTInter, oralib, likepy, globals, "globals.mac";
import rsexts;

const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;
      
private file MessageXMLFile() txt write;

private var ExportedCategory = TArray;

private macro FormatDate(indate : Date)
    var str;
    var day = 0, mon = 0, year = 0;
    DateSplit (indate, day, mon, year);
    
    str = String(day:2:o) + "-" +  String(mon:2:o) + "-" + String(year:4:o);
    return str;
end;

private macro FormatTime(intime : Time)
    var str;
    var hour, min, sec;
    TimeSplit (intime, hour, min, sec);
    
    str = String(hour:2:o) + ":" +  String(min:2:o) + ":" + String(sec:2:o);
    return str;
end;

private macro Признак(Parent : @FmXmlNodeElement, attr)
    var node = Parent.addChild("Признак");

    node.setAttribute("номер", attr.value("t_attrid"));
    node.setAttribute("дейстс", FormatDate(attr.value("T_validfromdate")));
    node.setAttribute("дейстпо", FormatDate(attr.value("T_validtodate")));
    node.setAttribute("основной", attr.value("T_general"));
    node.setAttribute("пользователь", attr.value("T_oper"));
    node.setAttribute("дата", FormatDate(attr.value("T_sysdate")));
    node.setAttribute("время", FormatTime(attr.value("T_systime")));
    node.setAttribute("автоматически", attr.value("T_isauto"));
end;

private macro Объект(node : @FmXmlNodeElement, objecttype, groupid, Object)    
    var cmd = RsdCommand("  SELECT COR.*, ATTR.T_NAME " +
        "FROM DOBJATCOR_DBT COR, DOBJATTR_DBT ATTR " +
        "   WHERE COR.T_OBJECTTYPE = ? AND COR.T_GROUPID = ? AND COR.T_OBJECT = ? " +
        "   AND ATTR.T_OBJECTTYPE = COR.T_OBJECTTYPE AND COR.T_GROUPID = ATTR.T_GROUPID AND COR.T_ATTRID = ATTR.T_ATTRID " +
        "ORDER BY COR.T_GROUPID");
    cmd.AddParam ("", RSDBP_IN, objecttype);
    cmd.AddParam ("", RSDBP_IN, groupid);
    cmd.AddParam ("", RSDBP_IN, Object);
    
    var rs = RsdRecordset(cmd);
    var LastGroupID = 0;
    var Категория;
    while(rs.moveNext())
        if (LastGroupID != rs.value("T_GROUPID"))
            Категория = node.addChild("Категория");
            Категория.setAttribute("номер", rs.value("T_GROUPID"));
            LastGroupID = rs.value("T_GROUPID");
        end;
        
        ExportedCategory[ExportedCategory.size] = rs.value("T_NAME");
        Признак(Категория, rs);
    end;
end;

macro RunObjAttrExport(sFileName, outObjectType : @Integer, outGroupId : @Integer, outObject : @string, outCategory : @TArray)
    var xml : FmXmlDocumentWriter = FmXmlDocumentWriter();
    var stat = 0;

    xml.createProcessingInstruction("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
    var root : FmXmlNodeElement = xml.getRootElement("Выгрузка");
    root.setAttribute("дата", FormatDate(Date()));
    root.setAttribute("время", FormatTime(Time()));
    root.setAttribute("пользователь", {oper});
    
    var cmd = RsdCommand("SELECT * FROM DOBJATTREXPORT_TMP");
       
    var rs = RsdRecordset(cmd);
    var node;
    var oldObjectType;
    var oldObject;
    
    while (rs.MoveNext())
        outObjectType = rs.value(0); 
        outGroupId = rs.value(1);
        outObject = rs.value(2);
        
        if ((oldObjectType != outObjectType) and (oldObject != outObject))
            node = root.addChild("Объект");
            node.setAttribute("вид", outObjectType);
            node.setAttribute("идентификатор", outObject);
            
            oldObjectType = outObjectType;
            oldObject = outObject;
        end;
        
        Объект(node, rs.value(0), rs.value(1), rs.value(2));
    end;
       
    outCategory = ExportedCategory;

    close(MessageXMLFile);
    DelFile(MessageXMLFile);
    if(open(MessageXMLFile, sFileName, "utf8"))
        xml.saveToFile(MessageXMLFile);
        close(MessageXMLFile);

        if ((not IsStandAlone) and (IsGUI == true)) /*Трехзвенка*/
            if(substr(sFileName,1,2) != "\\\\") // если не на машину по сетке 
                CopyFile (sFileName,"$"+sFileName);
            end;
        end;
    else
        stat = 31694;
        CreateErrMsg(stat, sFileName);
    end;

    return stat;
end;

macro Печать_ОтчетСканирования (Вызов,doc,Статус,Сообщение)
    array StringArray;
    var i = 1;
    var stat;
    
    if (Вызов==Первый)
[                       Отчет о групповом выполнении.                             ];
[┌────────┬────────────────────────────────────────────────────────┐];
[│   №    │                       Сообщение                        │];
[│ ошибки │                                                        │];
[├────────┼────────────────────────────────────────────────────────┤];
    elif (Вызов==Ошибка)
         if (Статус == 0)
            Сообщение = "Операция выполнена успешно"
         end;

         if(Статус == 19303)  // Те сообщения, которые не являются ошибками
           Статус = 0;
         end;

         StrSplit(StrSubst(Сообщение, "│", " " ), StringArray, 56);
[│#############│########│########################################################│](Статус, StringArray(0));

         while ( StrLen(StringArray(i) ) > 0)
[│             │        │########################################################│](StringArray(i));
           i = i + 1;
         end;

        elif (Вызов==Последний)         
[└─────────────┴────────┴────────────────────────────────────────────────────────┘];
    end;
end;