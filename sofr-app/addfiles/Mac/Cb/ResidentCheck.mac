/*
 $Name: ResidentCheck.mac
 $Module: Ядро ГКБО 
 $Description: Сделать обязательным указание для клиента страны резидентности 
 */
 /*
        Сделать обязательным указание для клиента страны резидентности 
 */
import BankInter, rsd;
private var PartySql : string = "";
private var UpdateSql : string = "";
var mode;

private macro CHECKCLIENTRESIDENCY()
    var Mode;
    var ErrCode = 0;
    GetRegistryValue("CB\\PARTY\\CHECKCLIENTRESIDENCY",V_INTEGER,Mode,ErrCode);
    
    if(ErrCode != 0)
        Mode = 2;
    end;
    
    return Mode;
end;

private macro СТРАНАРЕЗИДЕНТНОСТИ()
    var Mode = "";
    var ErrCode = 0;
    GetRegistryValue("COMMON\\ПЕРЕМЕННЫЕ\\СТРАНА РЕЗИДЕНТНОСТИ",V_STRING, Mode, ErrCode);

    return Mode;
end;

private macro PartySqlHandler(str)
    PartySql = PartySql + str;
end;

private macro PrintProtocolHeader()
[Протокол процедуры проверки клиентов на резидентность

Клиенты, у которых не определена страна резидентности, и не взведен флаг "Лицо без гражданства"

+--------------+-----------------------------------------------------+--------+--------------------------+
| Код          | Наименование                                        |   Ид.  |Результат работы процедуры|
|              |                                                     |субъекта+-------------+------------+
|              |                                                     |        |Поле <Страна>|Взведен флаг|
|              |                                                     |        |  заполнено  | <Лицо без  |
|              |                                                     |        |  значением  |гражданства>|
|              |                                                     |        |   страны    |            |
|              |                                                     |        |резидентности|            |
+--------------+-----------------------------------------------------+--------+-------------+------------+];
end;

private macro Init()
    mode = CHECKCLIENTRESIDENCY();
    PrintProtocolHeader();
    
    var cmd = RsdCommand("delete from DPTISSTATELES_TMP;");
    cmd.execute();
    
    SetOutHandler(@PartySqlHandler);
[INSERT INTO DPTISSTATELES_TMP
   (SELECT PT.T_PARTYID,
           PT.T_LEGALFORM,
           PT.T_NAME,
           CD.T_CODE,
           PS.T_ISSTATELESS,
           PT.T_NRCOUNTRY
      FROM DPARTY_DBT PT, DPERSN_DBT PS, DOBJCODE_DBT CD
     WHERE     PT.T_LOCKED = CHR (0)
           AND PT.T_PARTYID = PS.T_PERSONID(+)
           AND CD.T_CODEKIND = 1
           AND CD.T_OBJECTTYPE = 3
           AND CD.T_OBJECTID = PT.T_PARTYID
           AND PT.T_PARTYID IN (SELECT T_PARTYID
                                  FROM DPARTYOWN_DBT
                                 WHERE T_PARTYKIND = 1)
           AND (   (PT.T_LEGALFORM = 1 AND PT.T_NRCOUNTRY = CHR (1))
                OR (    PT.T_LEGALFORM = 2
                    AND PT.T_NRCOUNTRY = CHR (1)
                    AND PS.T_ISSTATELESS = CHR (0)
                    AND PT.T_NOTRESIDENT = CHR (0))))
];
    SetOutHandler(NULL);
    
    cmd = RsdCommand(PartySql);
    cmd.execute();
end;

private macro PrintProtocolLine(Code, Name, Id, NRCountry, ISSTATELESS)
[|##############|#####################################################|########|      #      |      #     |]
(Code, Name, Id, NRCountry,ISSTATELESS);
[+--------------+-----------------------------------------------------+--------+-------------+------------+];
end;

private macro BatchUpdate()
    var NR = СТРАНАРЕЗИДЕНТНОСТИ();
    var cmd2;
    if (mode == 1)
        cmd2 = RsdCommand("UPDATE DPARTY_DBT SET T_NRCOUNTRY = ?, T_NOTRESIDENT = CHR(0) WHERE T_LEGALFORM = 1 and T_PARTYID in (select T_PARTYID from DPTISSTATELES_TMP)");
        cmd2.addParam("", RSDBP_IN, NR);
        cmd2.execute();
        
        cmd2 = RsdCommand("UPDATE dpersn_dbt ps " + 
        "   SET ps.t_IsStateless = 'X' " + 
        " WHERE EXISTS " + 
        "          (SELECT 1 " + 
        "             FROM dparty_dbt pt " + 
        "            WHERE ps.T_PERSONID = pt.T_PartyID AND pt.T_LEGALFORM = 2 and pt.T_PARTYID in (select T_PARTYID from DPTISSTATELES_TMP))");
        cmd2.execute();
    end;
    
    if (mode == 2)
        cmd2 = RsdCommand("UPDATE DPARTY_DBT SET T_NRCOUNTRY = ?, T_NOTRESIDENT = CHR(0) WHERE T_PARTYID in (select T_PARTYID from DPTISSTATELES_TMP)");
        cmd2.addParam("", RSDBP_IN, NR);
        cmd2.execute();
    end;
end;

macro Main()
    Init();
    BatchUpdate();
    
    var cmd = RsdCommand("SELECT ST.T_PARTYID, "
        "         ST.T_LEGALFORM, "
        "         ST.T_NAME, "
        "         ST.T_CODE, "
        "         PS.T_ISSTATELESS, "
        "         PT.T_NRCOUNTRY "
        "    FROM DPTISSTATELES_TMP ST, DPARTY_DBT PT, DPERSN_DBT PS "
        "   WHERE ST.T_PARTYID = PT.T_PARTYID AND ST.T_PARTYID = PS.T_PERSONID(+) "
        "ORDER BY ST.T_PARTYID");
        
    cmd.NullConversion = true;
    cmd.execute();
    
    var rs = RsdRecordset(cmd);
    while (rs.MoveNext())
        var A = "", // Поле Страна заполнено значением страны резидентности
            B = ""; // Взведен флаг без гражданства

        if (Strlen(String(rs.value("T_NRCOUNTRY"))) > 0)
            A = "X";
        end;
        
        if ((Int(rs.value("T_LEGALFORM")) == 2) and (rs.value("T_ISSTATELESS") == "X"))
            B = "X";
        end;
        
        if (mode == 1)
            PrintProtocolLine(rs.value(3), rs.value(2),rs.value(0), A, B);
        end;
        
        if (mode == 2)
            PrintProtocolLine(rs.value(3), rs.value(2),rs.value(0), A, "");
        end;
        
        if (mode == 3)
            PrintProtocolLine(rs.value(3), rs.value(2),rs.value(0), "", "");
        end;
    end;
end;

Main();