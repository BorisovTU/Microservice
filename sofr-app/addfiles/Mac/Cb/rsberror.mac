 /*
 $Name: rsberror.mac
 $Module: Ядро ГКБО
 $Description: Функции и классы обработки ошибок
 */

import BankInter;

// Класс исключения RS-Bank
class TRsbError( parent:object,  // Объект ислючения более низкого уровня
                 code  :integer, // Номер ошибки в dbank_msg
                 text  :string   // Текст ошибки
               )

  private var m_ParentError:object  = parent,
              m_Code       :integer = code,   
              m_Text       :string  = text;

  // Текстовое представление ошибки
  macro ToString():string
    var mes:string = "";
    if( m_Code )
      MemoryError( m_Code, m_Text );
      mes = GetErrMsg();
    else
      mes = m_Text;
    end;
    if( m_ParentError )
      mes = string( mes, "|", m_ParentError );
    end;
    return mes;
  end;

end;

// Выбросить исключение RS-Bank
macro RsbThrow( mesOrCode:variant )
  var code:integer = 0, text:string = "";
  if( ValType( mesOrCode ) == V_INTEGER )
    code = mesOrCode;
  elif( ValType( mesOrCode ) != V_UNDEF )
    text = string( mesOrCode );
  else
    text = "";
  end;
  RunError( text, TRsbError( null, code, text ) );
end;

// Прокинуть исключение RS-Bank
macro RsbRethrow( x:object /*TRslError*/, mesOrCode:variant )
  var code:integer = 0, text:string = "";
  if( ValType( mesOrCode ) == V_INTEGER )
    code = mesOrCode;
  elif( ValType( mesOrCode ) != V_UNDEF )
    text = string( mesOrCode );
  else
    text = "";
  end;
  var parent:TRsbError = null;
  if( GenClassName( x.err ) == "TRSBERROR" )
    parent = x.err;
  elif( ValType( x.err ) == V_STRING )
    parent = TRsbError( null, 0, x.err );
  else
    parent = TRsbError( null, 0, x.message );
  end;
  if( code or text )
    RunError( text, TRsbError( parent, code, text ) );
  else
    RunError( text, parent );
  end;
end;

// Получение описания ошибки времени выполнения
macro RsbGetError( x:object /*TRslError*/ ):string
  if( GenClassName( x.err ) == "TRSBERROR" )
    return string( x.err );
  elif( ValType( x.err ) == V_STRING )
    return x.err;
  elif( x.message )
    return x.message;
  else
    return string( "Модуль: ", x.Module, " Строка: ", x.Line );
  end;
end;

// Обработка ошибки времени выполнения
macro RsbShowError( x:object /*TRslError*/ )
  MsgBox( RsbGetError( x ) );
end;

macro RsbGetErrorEx( er:object /*TRslError*/ ):string
  return String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line);
end;

/* Обработка exception (ошибок времени выполнения) */
macro ExeptionMessage(er)
  //RsbShowError(er);
  MsgBox( RsbGetErrorEx(er) );
end;

macro GetErrMsgEx(stat : integer, DefaultErr : string) : string
  var ErrMsg : string = "";

  if(stat)
    MemoryError(stat);
  end;

  ErrMsg = GetErrMsg();

  if(not ErrMsg)
    ErrMsg = DefaultErr;
  end;

  return ErrMsg;
end;
