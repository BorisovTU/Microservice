 /*
 $Name: ws_file.mac
 $Module: WEB
 $Description: Контейнер для передачи файлов
 */

import BankInter;
import rsexts;
import rsd;

private const COMPRESS_FILE_CONTAINER = true;
private const ENCODE_HTML_TO_UTF8 = true;
private const USE_LARGE_FILE_DIR = false;
private const LARGE_FILE_LIMIT = 20 * 1024 * 1024; // размер после которого считаем файл большим

class FileContainer
  var FullName = "";
  var FileExtension = ".txt";
  var Body = "";
  var IsCompressed = false;
  var IsLargeFile = false;
  var StorageId = 0;

  MACRO SaveToStorage()
    var cmd, cmdText;

    cmdText = "insert into DFILE_STORAGE_DBT(T_FILE_CONTENT, T_FILE_NAME, T_SESSIONID) "
                      " VALUES( ?, ?, ?) RETURNING T_ID into ? ";

    cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, Body);
    cmd.AddParam("", RSDBP_IN, FullName);  

    // пока записываем незначащий ID
    cmd.AddParam("", RSDBP_IN, "111");    

    cmd.AddParam("T_ID", RSDBP_OUT, V_INTEGER);
    cmd.execute();

    StorageId = cmd.value("T_ID");
    
    return StorageId;

  END;
end;

macro GetFileSize(filePath :string) : integer
  var size = 0;
  if (not GetFileInfo(filePath, null, null, @size, null))
    RunError("Файл " + filePath + " не найден");
  end;

  return size;
end;

macro CreateFileContainer(filePath :string, needCompress: bool, data: string, isLargeFile: bool) : FileContainer
    if (filePath == "")
        RunError("Не задан путь файла");
    end;

    var name, ext;
    SplitFile(filePath, name, ext);

    const fc = FileContainer();
    // пока так сделать в общем случае нельзя, т.к. в некоторых местах после создания контейнера файл удаляют
    // если задана явная строка данных, то размер файла не вычисляем
    const asLargeFile = (data == null) 
      and USE_LARGE_FILE_DIR
      and isLargeFile
      and (GetFileSize(filePath) > LARGE_FILE_LIMIT);

    if (data != null)
      fc.Body = RslBinaryData(data).asBase64;
    else
      fc.Body = EncodeFileToBase64(ToAnsi(filePath, true), false);
    end;

    fc.FileExtension = ext;
    fc.FullName = name + ext;
    fc.IsCompressed = false;

    fc.SaveToStorage();

    // больше содержимое файла на клиент не передаем - все скачиваем по имени с FileDownload.ashx или из FileStorage
    fc.Body = "";

    return fc;
end;

macro CreateLargeFileContainer(filePath :string, forceData) : FileContainer
  return CreateFileContainer(filePath, false, null, true, forceData);
end;

macro GetTextFile(fileName:string) : FileContainer
  return CreateFileContainer(fileName);
end;

macro fprintln(
  outFileName : String
 ,str         : String
)
  FILE outFile() txt write;// append;

  if( Open( outFile, GetTxtFileName( outFileName ) ) )
    Insert( outFile, str );
    Close( outFile );
  end;
end;

private macro GetHtmlFileEncoding()
    if (ENCODE_HTML_TO_UTF8)
        return "utf-8";
    end;
    return "cp866";
end;

private macro EncodeString(s)
    if (ENCODE_HTML_TO_UTF8)
        return OemToUtf8(s);
    end;
    return s;
end;

macro ConvertTxt2Html(
  TxtFileFullName:String
):String

  var Stat:Integer = 0;
  var ErrMsg:String = "";
  var HtmlFileFullName:String = "";
  FILE HtmlFile() txt write;
  FILE TxtFile() txt;

  if(not ((ValType(TxtFileFullName) == V_STRING) and (TxtFileFullName != "")))
    RunError("Не указано имя");
  end;

  HtmlFileFullName = TxtFileFullName + ".html";

  if(not Open(HtmlFile, HtmlFileFullName))
    Stat = Status(ErrMsg);
    RunError("Ошибка при создании файла|" + HtmlFileFullName + "|(" + Stat + ") " + ErrMsg);
  end;

  if(not Open(TxtFile, TxtFileFullName))
    Stat = Status(ErrMsg);
    RunError("Ошибка при открытии файла|" + TxtFileFullName + "|(" + Stat + ") " + ErrMsg);
  end;

  Insert(HtmlFile,
    "<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Transitional//EN\" \"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd\">\n" +
      "<html xmlns=\"http://www.w3.org/1999/xhtml\" xml:lang=\"en-gb\" lang=\"en-gb\">\n" +
        "<head>\n" +
          "<meta http-equiv=\"Content-type\" CONTENT=\"text/html; charset=" + GetHtmlFileEncoding() + "\">\n" +
        "</head>\n" +
        "<body>\n" +
          "<pre>\n");

  Rewind(TxtFile); 
  while(Next(TxtFile)) 
    Insert(HtmlFile, EncodeString(TxtFile.str));
  end;

  Insert(HtmlFile, 
          "</pre>\n" +
        "</body>\n" +
      "</html>");

  Close(HtmlFile);
  Close(TxtFile);
  RemoveFile(TxtFileFullName);

  return HtmlFileFullName;
end;