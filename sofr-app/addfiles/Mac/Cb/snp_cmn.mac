/************************************************************************
*                             RS-Balance ver.2.80
*                        Справочник населенных пунктов
*                           Общие функции и переменные
*                        (C)2002 R-Style SofLab Ltd.
*
*                                                    19.09.2002 В.Жарков
*************************************************************************/


const ADDR_COUNTRY = 1;
const ADDR_STATE   = 2;
const ADDR_DISTRICT= 3;
const ADDR_TOWN    = 4;
const ADDR_PLACE   = 5;
const ADDR_STREET  = 6;
const ADDR_BUILDING =7;


const CADR_TYPEADDRELEM:integer = 40; /* Тип справочника видов элементов адреса */

FILE SNP     ("snp.dbt",      "rsrfdb.def")   KEY 1000 write;
FILE DBTKodif("kodif_cd.dbt", "zp.def")       KEY 1 write;

var Russia_ID;
var AutomaticUpdate = FALSE;

/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro ReportError()
  var i = 0;
  var parm;

  print("Error: ");
  while(GetParm(i, parm))
    print(parm);
    i = i+1;
  end;
  println("");
end;
/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro ReportError_stat()
  var i = 0;
  var parm;
  var szMsg:string = "";
  var szDesc:string = "";

  while(GetParm(i, parm))
    szMsg = szMsg + parm;
    i = i+1;
  end;

  szMsg = szMsg + " (код ошибки:" + Status(szDesc) + "):" + szDesc;
  ReportError(szMsg);
end;



/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro OpenFile(ff, Dir, Name, SilentMode)
  if(open(ff, Dir + "\\" + Name))
    return TRUE;
  else
    if(SilentMode!=TRUE)
      ReportError("Невозможно открыть файл " + Dir + "\\" + Name);
    end;
    return FALSE;
  end;
end;

macro CreateFile(ff, Dir, Name)
  if(Create(ff, Dir + "\\" + Name))
    return TRUE;
  else
    ReportError("Невозможно создать файл " + Dir + "\\" + Name);
    return FALSE;
  end;
end;


macro InsertRecord(ff)
  if(Insert(ff))
    return TRUE;
  else
    ReportError_stat("невозможно добавить запись в файл ", FileName(ff));
    return FALSE;
  end;
end;

macro UpdateRecord(ff)
 if(Update(ff))
    return TRUE;
  else
    ReportError_stat("невозможно обновить запись в файле ", FileName(ff));
    return FALSE;
  end;
end;




/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro GetLevelName(Level:integer)

  if(Level == ADDR_COUNTRY)
      return "ADDR_COUNTRY";
  elif(Level == ADDR_STATE)
        return "ADDR_STATE";
  elif(Level == ADDR_DISTRICT)
        return "ADDR_DISTRICT";
  elif(Level == ADDR_TOWN)
        return "ADDR_TOWN";
  elif(Level == ADDR_PLACE)
        return "ADDR_PLACE";
  elif(Level == ADDR_STREET)
        return "ADDR_STREET";
  elif(Level == ADDR_BUILDING)
        return "ADDR_BUILDING";
  else 
        return Level + " (неизвестный уровень)";
  end;
end;

/*-----------------------------------------------------------------------------
  Определяет позицию кода элемента в общем строковом коде
-----------------------------------------------------------------------------*/
macro GetOffsetSubCode(Level)
    if(Level == ADDR_STATE)    return 1;
  elif(Level == ADDR_DISTRICT) return 3;
  elif(Level == ADDR_TOWN)     return 6;
  elif(Level == ADDR_PLACE)    return 9;
  elif(Level == ADDR_STREET)   return 12;
  else  return 0;
  end;
end;

/*-----------------------------------------------------------------------------
Получить часть строкового кода для уровня Level
-----------------------------------------------------------------------------*/
macro GetSubCode(Level, szFullCode)

  if(  Level == ADDR_STREET)
    return SubStr(szFullCode, 12/*GetOffsetSubCode(Level)*/,  4);       /*...........XXXX*/
  elif(Level == ADDR_PLACE)
    return SubStr(szFullCode,  9/*GetOffsetSubCode(Level)*/,  3);       /*........XXX....*/
  elif(Level == ADDR_TOWN)
    return SubStr(szFullCode,  6/*GetOffsetSubCode(Level)*/,  3);       /*.....XXX.......*/
  elif(Level == ADDR_DISTRICT)
    return SubStr(szFullCode,  3/*GetOffsetSubCode(Level)*/,  3);       /*..XXX..........*/
  elif(Level == ADDR_STATE)
    return SubStr(szFullCode,  1/*GetOffsetSubCode(Level)*/,  2);       /*XX.............*/
  else
    return "";
  end;
end;

/*-----------------------------------------------------------------------------
  Преобразовывает числовой код элемента к строковому коду
-----------------------------------------------------------------------------*/
macro SubCodeToStr(SubCode: integer, Level:integer)

var szCode:string = "";
    
  if(Level == ADDR_STATE)
      if(SubCode < 10)
        szCode = "0";
      end;
  elif((Level == ADDR_DISTRICT) or (Level == ADDR_TOWN) or (Level == ADDR_PLACE))
      if(SubCode < 10)       szCode = "00";
      elif(SubCode < 100)    szCode = "0";
      end;
  elif(Level == ADDR_STREET)
      if(SubCode < 10)       szCode = "000";
      elif(SubCode < 100)    szCode = "00";
      elif(SubCode < 1000)   szCode = "0";
      end;
  else
    return "";
  end;

  szCode = szCode + String(SubCode);
  return szCode;
end;

/*-----------------------------------------------------------------------------
По строковому коду определить уровень элемента
-----------------------------------------------------------------------------*/
macro GetLevel(szFullCode)
  if(not strlen (szFullCode))
    ReportError("некорректная длина строкового кода");
    return 0;
  end;

  if(GetSubCode(ADDR_STREET, szFullCode) == "0000")
    if(GetSubCode(ADDR_PLACE, szFullCode) == "000")
      if(GetSubCode(ADDR_TOWN, szFullCode) == "000")
        if(GetSubCode(ADDR_DISTRICT, szFullCode) == "000")
          if(GetSubCode(ADDR_STATE, szFullCode) == "00")
            ReportError("пустой строковый код");
            return 0;
          else
            return ADDR_STATE;
          end;
        else
          return ADDR_DISTRICT;
        end;
      else
        return ADDR_TOWN;
      end;
    else
      return ADDR_PLACE;
    end;
  else
    return ADDR_STREET;
  end;
end;

/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro GetRussia_ID(Dir)
  file    ff( "country.dbt", "rsrfdb.def")       KEY 2;  /* CodeLat3 */

  if(not OpenFile(ff, Dir, "country.dbt"))      return 0; end;
  
  ff.CodeLat3 = "RUS";
  if( GetEQ( ff ) )
    return  ff.CountryID;
  else
    return 0;
  end;
end;

/*-----------------------------------------------------------------------------
Содержит ли строковый код что-то кроме "0"
-----------------------------------------------------------------------------*/
macro IsEmptySubCode(SubCode:string)

  var Len:integer = StrLen(SubCode);
  var Pos:integer = 1;

  while(Pos <= Len)
    if(SubStr(SubCode, Pos, 1) != "0")
      return FALSE;
    end;
    Pos = Pos+1;
  end;

  return TRUE;
end;
/*-----------------------------------------------------------------------------
Ищет уровень, на котором должен находиться родитель для  элемента,
лежащего на уровне Level и имеющего код szFullCode
(т.к. код может иметь пропуски,
 то родитель не обязательно должен лежать на соседнем уровне)
-----------------------------------------------------------------------------*/
macro FindFirstParentLevel(Level:integer, szFullCode:string)

  var SubCode:string;

  if(Level <= ADDR_COUNTRY)
    ReportError("для страны не может быть родительского элемента");
    return 0;
  end;

  if((Level == ADDR_STATE) or (Level == ADDR_DISTRICT))
    return Level - 1;
  end;

  Level = Level - 1;
  SubCode = GetSubCode(Level, szFullCode);

  while((Level > ADDR_STATE) and IsEmptySubCode(SubCode))
    Level = Level - 1;/*Здесь нужно в цикле добраться до первого непустого кода родителя, и там уже работать.*/
    SubCode = GetSubCode(Level, szFullCode);
  end;

  if(IsEmptySubCode(SubCode))
    ReportError("не найден код родителя для уровня ", GetLevelName(Level), " (строковый код ", szFullCode, ")");
    return 0;
  end;

  return Level;
end;

/*-----------------------------------------------------------------------------
Найти ID родительского элемента
для элемента на уровне Level и имеющего код szFullCode
-----------------------------------------------------------------------------*/
macro FindExistingParentID(Level, szFullCode)

  var SubCodeParentCurr:string;
  var LevelParent:integer;
  var LevelCurr:integer = ADDR_STATE;
  var ParentIDCurr:integer = Russia_ID;


  if(Level == ADDR_STATE)
    return Russia_ID;
  end;


  LevelParent   = FindFirstParentLevel(Level, szFullCode);

  if(LevelParent)
    /*Будем искать начиная с верхнего уровня*/

    while(LevelCurr <= LevelParent)
      SubCodeParentCurr = GetSubCode(LevelCurr, szFullCode);

      if(not IsEmptySubCode(SubCodeParentCurr))
        SNP.Level    = LevelCurr;
        SNP.ParentID = ParentIDCurr;
        SNP.Code     = SubCodeParentCurr;

        if(getEQ(SNP))
          if(LevelCurr == LevelParent)
            return SNP.ID;
          end;
          ParentIDCurr = SNP.ID;
        else
          return 0; /*parent not found - this is not an error*/
        end;
      end;
      LevelCurr    = LevelCurr + 1;
    end;
    /*unreachable*/
    ReportError("ошибка поиска родителя - выход из цикла");
  else
    ReportError("ошибка поиска родителя - некорректный уровень");
    return 0;
  end;
end;

/*-----------------------------------------------------------------------------
Ищет ID родителя для элемента, находящегося на уровне Level.
При необходимости создает пустого родителя и возвращает его ID.
Возможно рекурсивное создание пустых родителей.
-----------------------------------------------------------------------------*/
macro GetParentID(Level, szFullCode)

  var ParentID:integer = FindExistingParentID(Level, szFullCode);
  var LevelParent:integer;

  if(ParentID)
    return ParentID;
  end;

  /*Если существующего родителя не найдено, создается пустой*/
  LevelParent = FindFirstParentLevel(Level, szFullCode);

  if(LevelParent == ADDR_COUNTRY)
    return Russia_ID;
  elif(LevelParent < ADDR_COUNTRY)
    ReportError("невозможно создать пустого родителя на уровне ", GetLevelName(LevelParent));
    return 0;
  end;

  

  ParentID = GetParentID(LevelParent/*!!!*/, szFullCode); /*рекурсивный вызов*/
  if(not SNP.ParentID)
    ReportError("невозможно создать пустого родителя на уровне ", GetLevelName(LevelParent), " - нет следующего родителя");
    return 0;
  end;

  ClearRecord(SNP);
  SNP.ParentID = ParentID;
  SNP.Level    = LevelParent;
  SNP.Code     = GetSubCode(LevelParent, szFullCode);

  if(not SNP.Code)
    ReportError("невозможно создать пустого родителя на уровне ", GetLevelName(LevelParent), " - некорректный код");
    return 0;
  end;

  if(not InsertRecord(SNP))   return 0;  end;

  return SNP.ID;
end;

/*-----------------------------------------------------------------------------
Возвращает ID записи в кодификаторе для сокращенного наименования типа.
Если такой записи еще нет, создает ее.
-----------------------------------------------------------------------------*/
macro GetSocrID(szSocr)

  KeyNum(DBTKodif, 1);

  DBTKodif.TypeKodif = CADR_TYPEADDRELEM;
  DBTKodif.Code      = StrUpr(szSocr);
  if(not GetEQ(DBTKodif))
      ClearRecord(DBTKodif);
      DBTKodif.TypeKodif = CADR_TYPEADDRELEM;
      DBTKodif.Code      = StrUpr(szSocr);
      DBTKodif.Name      = szSocr;

      if(not InsertRecord(DBTKodif)) return 0; end;
  end;

  return DBTKodif.ID;
end;

/*-----------------------------------------------------------------------------
Возвращает имя записи в кодификаторе по ее ID.
-----------------------------------------------------------------------------*/
macro GetSocrName(ID)
  KeyNum(DBTKodif, 0);
  DBTKodif.ID      = ID;
  if(GetEQ(DBTKodif))
    return DBTKodif.Code;
  else
    return "";
  end;
end;

/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro IsValidCode(szFullCode:string)

  var Len:integer = StrLen(szFullCode);
  var Pos:integer = 1;
  var CharCode:integer;
  var CodeStart = CodeFor("0");
  var CodeEnd   = CodeFor("9");
  
  if(Len != 15)
    ReportError("cтроковый код ", szFullCode, " имеет некорректную длину");
    return FALSE;
  end;


  /*Проверяем, что код содержит только цифры - достаточно долгая операция - можно отключить*/
  while(Pos <= Len)
    CharCode = CodeFor(SubStr(szFullCode, Pos, 1));

    if((CharCode < CodeStart) or (CharCode > CodeEnd))
      ReportError("строка кода может содержать только цифры");
      return FALSE;
    end;
    Pos = Pos+1;
  end;
  
  /*Код верхнего уровня должен быть непустым*/
  if(IsEmptySubCode(GetSubCode(ADDR_STATE, szFullCode)))
    ReportError("cтроковый код ", szFullCode, " является некорректным - код региона пустой");
    return FALSE; 
  end;

  return TRUE;
end;

/*-----------------------------------------------------------------------------
Получить полный строковый код записи по ее ID
-----------------------------------------------------------------------------*/
MACRO GetFullCode(ID:integer)
  var LevelCurr:integer     = SNP.Level;
  var ParentIDCurr:integer  = SNP.ParentID;

  var szFullCode = "000000000000000";


  KeyNum(SNP,0); /*ID*/


  /*1. Собираем все коды верхних уровней*/
  while(LevelCurr > ADDR_STATE)
    
    SNP.ID = ParentIDCurr;
    if(getEQ(SNP))
      ParentIDCurr = SNP.ParentID;
      LevelCurr    = SNP.Level;
      StrSet(szFullCode, GetOffsetSubCode(LevelCurr), SubCodeToStr(SNP.Code, LevelCurr));
    else
      ReportError("не найден родитель для элемента с ID=", ParentIDCurr);
      return "";
    end;
    LevelCurr = LevelCurr - 1;
  end;

  SNP.ID = ID;
  if(not getEQ(SNP))
    ReportError("невозможно возвратиться к текущей записи с ID=", ID);
    return "";
  end;


  /*2. Добавляем код текущего уровня*/
  LevelCurr    = SNP.Level;
  StrSet(szFullCode, GetOffsetSubCode(LevelCurr), SubCodeToStr(SNP.Code, LevelCurr));
 
  return szFullCode;
END;
/*-----------------------------------------------------------------------------
Инициализация общих переменных
-----------------------------------------------------------------------------*/
MACRO SNP_Init(PathDBT:string)

  if(not OpenFile(SNP,      PathDBT, "snp.dbt"))      return FALSE; end;
  if(not OpenFile(DBTKodif, PathDBT, "kodif_cd.dbt")) return FALSE; end;

  Russia_ID = GetRussia_ID(PathDBT);
  if(not Russia_ID)
    ReportError("Cannot find code of Russia in country.dbt!");
    return FALSE;
  end;

  return TRUE;
END;
/*-----------------------------------------------------------------------------
Воссоздать строковый код из полей CodeRegion + CodeRaion + CodeTown + CodePlace
-----------------------------------------------------------------------------*/
macro ConstructCodeFromKladrDbt(ff)
  return SubCodeToStr(Int(ff.CodeRegion), ADDR_STATE) +
         SubCodeToStr(    ff.CodeRaion,   ADDR_DISTRICT) +
         SubCodeToStr(    ff.CodeTown,    ADDR_TOWN) +
         SubCodeToStr(    ff.CodePlace,   ADDR_PLACE) +
         SubCodeToStr(    0,   ADDR_STREET);
end;
/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro AddLink_kladr(ff_kladr, ff_link)

  ClearRecord(ff_link);

  ff_link.Type = 1;/*LINK_KLADR*/
  ff_link.old_ID   = ff_kladr.PlaceID;
  ff_link.new_ID   = SNP.ID;

  return InsertRecord(ff_link);
end;
/*-----------------------------------------------------------------------------
Заглушка
-----------------------------------------------------------------------------*/
macro Stub()
  return;
end;

/*-----------------------------------------------------------------------------
Информация об исходном файле
-----------------------------------------------------------------------------*/
class CConvContext(src)
  var m_IsKladrDBF:bool;
  var m_IsKladrDBT:bool;
  var m_IsStreetDBF:bool;
  var m_IsStreetDBT:bool;

  m_IsKladrDBF   = (Index(FileName(src), "kladr.dbf")  != 0); /*импорт из КЛАДР*/
  m_IsKladrDBT   = (Index(FileName(src), "kladr.dbt")  != 0); /*конвертация kladr.dbt*/
  m_IsStreetDBF  = (Index(FileName(src), "street.dbf") != 0); 
  m_IsStreetDBT  = (Index(FileName(src), "street.dbt") != 0);

  macro IsKladrDBF    return m_IsKladrDBF;  end;
  macro IsKladrDBT    return m_IsKladrDBT;  end;
  macro IsStreetDBF   return m_IsStreetDBF; end;
  macro IsStreetDBT   return m_IsStreetDBT; end;
end;

/*-----------------------------------------------------------------------------
Информация из записи исходного файла
-----------------------------------------------------------------------------*/
class SrcInfo(src, context)
/*variables*/
  var FullCode: string;
  var SocrID:     integer;
  var Name:     string;
  var GNI_Numb:   string;
  var Index:    string;
  var Code:     string;
  var Level:    integer;
  var Socr:     string;
  var ParentID: integer;
  var m_IsBuilding:bool;

/*constructor*/
  if(context.IsKladrDBF())
    FullCode = Trim(src.Code) + "0000"/*SubCodeToStr(0, ADDR_STREET)*/;
    Socr     = Trim(src.Socr);
  elif(context.IsStreetDBF())
    FullCode = Trim(src.Code);
    Socr     = Trim(src.Socr);
  elif(context.IsKladrDBT())
    FullCode = ConstructCodeFromKladrDbt(src);
    Socr     = GetSocrName(src.TypeID);
    if(Socr=="")   ReportError("не найдена запись кодификатора с ID=", src.TypeID); end;
  end;

  if(context.IsKladrDBT())
    GNI_Numb = Trim(src.CodeGNI);
  else
    GNI_Numb = Trim(src.GNINmb);
  end;

  SocrID = GetSocrID(Socr);
  Name = Trim(src.Name);
  Index = Trim(src.Index);
  
  Level = GetLevel(FullCode);
  if(not Level)    ReportError("Ошибка обработки строкового кода ",FullCode);  return FALSE;  end;

  Code = GetSubCode(Level, FullCode);

  ParentID = GetParentID(Level, FullCode);
  if(not ParentID) ReportError("ошибка создания родительского элемента");    return FALSE;  end;

  m_IsBuilding = (context.IsStreetDBF() and (Socr == "ДОМ"));

/*functions*/
  macro IsBuilding()
    return m_IsBuilding;
  end;

  macro CopyToSNP() /*аналог SNPChange::Apply*/
      SNP.Level     = Level;
      SNP.ParentID  = ParentID;
      SNP.Code      = Code;
      SNP.Name      = Name;
      SNP.Index     = Index;
      SNP.GNI_Numb  = GNI_Numb;
      SNP.SocrID    = SocrID;
  end;
end;

/*-----------------------------------------------------------------------------
Информация об изменении данных записи при ее обновлении
------------------------------------------------------------------------------*/
class SNPChange(src_info)
/*variables*/
  var old_Code    :integer,  new_Code    :integer,
      old_Name    :string,   new_Name     :string,
      old_Index   :string,   new_Index    :string,
      old_GNI_Numb:string,   new_GNI_Numb :string,
      old_SocrID  :integer,  new_SocrID   :integer;

/*constructor*/
    old_Code      = Int(SNP.Code);
    old_Name      = SNP.Name;
    old_Index     = SNP.Index;
    old_GNI_Numb  = SNP.GNI_Numb;
    old_SocrID    = SNP.SocrID;

    
    new_Code      = Int(src_info.Code);
    new_Name      = src_info.Name;
    new_Index     = src_info.Index;
    new_GNI_Numb  = src_info.GNI_Numb;
    new_SocrID    = src_info.SocrID;
  

/*functions*/
    macro Apply()
      SNP.Code      = new_Code;
      SNP.Name      = new_Name;
      SNP.Index     = new_Index;
      SNP.GNI_Numb  = new_GNI_Numb;
      SNP.SocrID    = new_SocrID;
    end;

    /*форматирует сообщение для многострочного MsgBox()*/
    macro GetDesc()
      var msg:string = "";
      if(old_Name     != new_Name)      msg = msg + "|Name: \""     + old_Name                + "\"->\"" + new_Name                 + "\"";
      else                              msg = msg + "|Name= \""     + new_Name                                                      + "\"";  end;
      if(old_SocrID   != new_SocrID)    msg = msg + "|Socr: \""     + GetSocrName(old_SocrID) + "\"->\"" + GetSocrName(new_SocrID)  + "\"";  end;
      if(old_Code     != new_Code)      msg = msg + "|Code: \""     + old_Code                + "\"->\"" + new_Code                 + "\"";  end;
      if(old_Index    != new_Index)     msg = msg + "|Index: \""    + old_Index               + "\"->\"" + new_Index                + "\"";  end;
      if(old_GNI_Numb != new_GNI_Numb)  msg = msg + "|GNI_Numb: \"" + old_GNI_Numb            + "\"->\"" + new_GNI_Numb             + "\"";  end;
      msg = msg + "|(ID записи=" + SNP.ID + ")";
      return msg;
    end;

    /*форматирует сообщение для вывода в строку отчета*/
    macro GetDesc_Line()
      var msg:string = "";
      msg = msg + "(ID=" + SNP.ID + ")";
      if(old_Name     != new_Name)      msg = msg + "\tName: \""     + old_Name                + "\"->\"" + new_Name                + "\"";
      else                              msg = msg + "\tName=\""      + new_Name                                                     + "\"";   end;
      if(old_SocrID   != new_SocrID)    msg = msg + "\tSocr: \""     + GetSocrName(old_SocrID) + "\"->\"" + GetSocrName(new_SocrID) + "\"";   end;
      if(old_Code     != new_Code)      msg = msg + "\tCode: \""     + old_Code                + "\"->\"" + new_Code                + "\"";   end;
      if(old_Index    != new_Index)     msg = msg + "\tIndex: \""    + old_Index               + "\"->\"" + new_Index               + "\"";   end;
      if(old_GNI_Numb != new_GNI_Numb)  msg = msg + "\tGNI_Numb: \"" + old_GNI_Numb            + "\"->\"" + new_GNI_Numb            + "\"";   end;
      return msg;
    end;


    macro WillBeChangedOnUpdate()
     return((old_Code     != new_Code)      or
            (old_Name     != new_Name)      or
            (old_Index    != new_Index)     or
            (old_GNI_Numb != new_GNI_Numb)  or 
            (old_SocrID   != new_SocrID));
    end;
end;

macro IsEmptyParent()
  return ((SNP.Name == "") and (SNP.Index == "") and (SNP.GNI_Numb == "") and (SNP.SocrID == 0));
end;

/*---------------------------------------------------------------------------
Можно (и нужно ли) обновлять текущую запись
---------------------------------------------------------------------------*/
macro Check_CanUpdateRecord(change)
var key;
  
  if(AutomaticUpdate)
    return TRUE;
  end;

  if(not IsEmptyParent() and change.WillBeChangedOnUpdate())
     
    if(not AutomaticUpdate)
      key = MsgBoxEx("В записи будут изменены:|" + change.GetDesc() + 
                      "|Обновить запись?|.|Да - обновить только эту запись|Нет - оставить существующую|OK-обновить эту запись и все последующие",
                        MB_YES+MB_NO+MB_OK,
                        IND_YES);
      if(key==IND_OK) /*Да для всех*/
        AutomaticUpdate = TRUE;
        return TRUE;
      elif(key==IND_YES)
        return TRUE;
      else
        return FALSE;
      end;
    end;
  end;

  return FALSE;
end;

/*-----------------------------------------------------------------------------

-----------------------------------------------------------------------------*/
macro FindRecByNameWithZeroCode(Level, ParentID, Name)

  var savkey = KeyNum(SNP);
  var bFound:bool = FALSE;
  KeyNum(SNP, 2); /*Level,Parent,Name*/
  SNP.Level    = Level;
  SNP.ParentID = ParentID;
  SNP.Name     = Name;


  if(GetGE(SNP))
    if((SNP.Level==Level)and(SNP.ParentID==ParentID)and(SNP.Name==Name))
      if(SNP.Code==0)
        bFound = TRUE;
      else
        /*крутимся, пока совпадают поля и код ненулевой*/
        while(Next(SNP)and(SNP.Level==Level)and(SNP.ParentID==ParentID)and(SNP.Name==Name)and(not (bFound=(SNP.Code==0))))
          /*empty*/
        end;
      end;
    end;
  end;

  KeyNum(SNP, savkey);
  return bFound;
end;

/*-----------------------------------------------------------------------------
Умеет импортировать данные из kladr.dbf, street.dbf и kladr.dbt
-----------------------------------------------------------------------------*/
macro ImportData(src, InterfaceMode, file_link/*необяз.*/)
  var i:integer = 0;
  var src_context = CConvContext(src);/*вид исходного файла - определяется один раз в начале процедуры*/
  var src_info; /*информация взятая из текущей записи источника*/
  var change;   /*информация об изменении записи по сравнению с текущим состоянием буфера*/
  
  var IsZeroCodeRecord:bool = FALSE;
  var UpdateCurr:bool = FALSE;
  var key;

  var b_IsEmptyParent:bool = FALSE;

  if(not InterfaceMode)
    ReplaceMacro("InitProgress", "Stub");
    ReplaceMacro("UseProgress",  "Stub");
    ReplaceMacro("RemProgress",  "Stub");
  end;
  
  InitProgress(NRecords(src), " Импорт " + FileName(src), "Импорт данных в Справочник населенных пунктов...");

  KeyNum(SNP, 1);
  Rewind(src);
  while( Next(src) )
    src_info = SrcInfo(src, src_context);
    UpdateCurr = FALSE;

    SNP.Level    = src_info.Level;
    SNP.ParentID = src_info.ParentID;
    SNP.Code     = src_info.Code;
    if(getEQ(SNP))
      change = SNPChange(src_info);
      
      /*если это дом в файле street.dbf, то найденная запись улицы должна стать для него родительской*/
      if(src_info.IsBuilding())
        change.Apply();
        SNP.System    = "X";
        SNP.Level     = ADDR_BUILDING;
        SNP.ParentID  = SNP.ID;
        SNP.Code      = 0; /*на уровне ADDR_BUILDING не используется*/
        SNP.ID        = 0;

        if(not InsertRecord(SNP))
          return FALSE;
        end;
      else /*обновление существующей записи*/
        UpdateCurr = Check_CanUpdateRecord(change);
        b_IsEmptyParent = IsEmptyParent();
        if(UpdateCurr or (SNP.System!="X") or b_IsEmptyParent)
          change.Apply();
          SNP.System  = "X";
          if(UpdateRecord(SNP))
            if(not b_IsEmptyParent)
              println("Обновлена (поиск по коду): " + change.GetDesc_line());
            end;
          else
            ReportError_stat("невозможно обновить запись в файле ", FileName(SNP));
            return FALSE;
          end;
        else
          if(change.WillBeChangedOnUpdate())
            println("Оставлено без изменений: " + change.GetDesc_line());
          end;
        end;
      end;
    else   /*Запись не найдена по коду. Возможно, она имеет нулевой код - тогда будем искать ее по названию
            Это не относится к домам, т.к. у них коды всегда пустые*/
      IsZeroCodeRecord = FALSE;
      if(not src_info.IsBuilding())
        if(FindRecByNameWithZeroCode(src_info.Level, src_info.ParentID, src_info.Name))
          IsZeroCodeRecord = TRUE;
          change = SNPChange(src_info);
          UpdateCurr = Check_CanUpdateRecord(change);
          if(UpdateCurr or (SNP.System!="X"))
            change.Apply();
            SNP.System   = "X";
            if(UpdateRecord(SNP))
              println("Обновлена (поиск по имени): " + change.GetDesc_line());
            else
              ReportError_stat("невозможно обновить запись с нулевым кодом");
              return FALSE;
            end;
          else
            if(change.WillBeChangedOnUpdate())
              println("Оставлено без изменений: " + change.GetDesc_line());
            end;
          end;
        end;
      end;
      
      if(not IsZeroCodeRecord)
        if(src_info.IsBuilding())
          src_info.Level = ADDR_BUILDING;
          src_info.ParentID = GetParentID(src_info.Level, src_info.FullCode);
          if(not src_info.ParentID) ReportError("ошибка создания родительского элемента");    return FALSE;  end;
          src_info.Code = ""; /*на уровне ADDR_BUILDING не используется*/
        end;

        ClearRecord(SNP);
        src_info.CopyToSNP();
        SNP.System    = "X";

        if(not InsertRecord(SNP))  return FALSE;   end;
      end;
    end;

    if(src_context.IsKladrDBT() and (not AddLink_kladr(src, file_link)))      return FALSE;   end;

    i = i + 1;
    UseProgress(i);
  end;

  RemProgress();

  /*restore*/
  if(not InterfaceMode)
    ReplaceMacro("InitProgress");
    ReplaceMacro("UseProgress");
    ReplaceMacro("RemProgress");
  end;

  return TRUE;
end;

