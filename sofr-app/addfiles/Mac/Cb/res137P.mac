/* Вспомогательные функции для формирования резерва по 137-П */

Import BankInter, FIInter, CTInter, globals;

/* Категории */
const КлассифПо137П = 3;
const ГруппаРиска = 13;
/* Виды привязанных объектов */
const СчетРезерва = 26;
/* Виды примечаний */
const ПроцентРезерва = 3;
const ТекРынСтоим = 5;

const Res137P_СчетРасходов = "70209810000002910300";
const Res137P_СчетДоходов  = "70107810000001710300";
const Ground = "Формирование резерва по 137-П";
const Number_Pack = 999;


/* Проверка правильности задания Процента резерва по 137-П */
MACRO CheckNumbSimbol(ControlStr, acc);

   var CompareStr = "0123456789.";
   var SizeStr = StrLen(ControlStr);
   var i = 1;
   var PointPos;

   if(SizeStr == 0)
      msgbox( "Не задано примечание Процент резерва по 137-П для счета " + acc.Account );
      Return False;                                                    
   end;

   while(i <= SizeStr)
        if(Not StrBrk(SubStr(ControlStr,i,1),CompareStr))
             msgbox( "Процент резерва по 137-П должен быть числом. Счет " + acc.Account );
             Return False;
        end;
        i = i + 1;
   end;

   PointPos = Index(ControlStr,".");
   if( Index(SubStr(ControlStr,PointPos+1),".") > 0 )
      msgbox( "Процент резерва по 137-П должен быть числом. Счет "  + acc.Account );
      Return False;
   End;

   Return True;

END;


/* Получить дату, за которую определена группа риска */
macro GetDateRisk( DateRisk, DateReserv, ObjID )
  var stat, retval;
  var d, m, y;
  var ReturnData;
  file ocor(objatcor) key 3;
  
  macro ValidObjAtCor()
    if(
        (ocor.ObjectType    == OBJTYPE_ACCOUNT) and
        (ocor.Object        == ObjID          ) and
        (ocor.GroupID       == ГруппаРиска    ) and
        (ocor.ValidFromDate <= DateReserv     ) ) /* Дата не должна выйти за пределы месяца */
      ReturnData = ocor.ValidFromDate;
      return true;
    end;
    return false;
  end;
  
  DateSplit( DateReserv, d, m, y );
  retval = 0;
  ReturnData = Date(0, 0, 0);
  /* Сначала пытаемся найти за месяц */
  ocor.ObjectType    = OBJTYPE_ACCOUNT;
  ocor.Object        = ObjID;
  ocor.GroupID       = ГруппаРиска;
  ocor.ValidFromDate = Date(1, m, y); /* первое число месяца */
  ocor.ValidToDate   = Date(0, 0, 0);
  ocor.AttrID        = 0;

  stat = GetGE( ocor );
  
  if( (stat == true) and (ValidObjAtCor() == true) ) /* Нашли за текущий месяц */
    
    retval = 1;
    while( (stat == true) and (ValidObjAtCor() == true) )
      DateSplit( ocor.ValidToDate, d, m, y );
      if( ocor.ValidToDate < DateReserv )
        stat = next( ocor );
      else
        stat = false;
      end;
    end;
  
  else
    
    /* Иначе ищем за предыдущий период */
    ocor.ObjectType    = OBJTYPE_ACCOUNT;
    ocor.Object        = ObjID;
    ocor.GroupID       = ГруппаРиска;
    ocor.ValidFromDate = Date(1, m, y); /* первое число месяца */
    ocor.ValidToDate   = Date(31, 12, 9999);
    ocor.AttrID        = 10000;
    
    stat = GetLE( ocor );
    if( (stat == true) and
        (ocor.ObjectType == OBJTYPE_ACCOUNT) and
        (ocor.Object     == ObjID          ) and
        (ocor.GroupID    == ГруппаРиска    ) )
      retval = -1;
      ReturnData = ocor.ValidFromDate;
    end;

  end;

  SetParm( 0, ReturnData );
  return retval;
end;

macro GetMethod( NumInList )
  if( (NumInList == "2.1.1.") or
      (NumInList == "2.1.2.") or
      (NumInList == "2.1.3.") or
      (NumInList == "2.1.5.") or
      (NumInList == "2.2."))
    return 1;
  end;
  return 0;
end;


/* Рассчитать величину S */
macro CalculateS( SS, acc, NumInList, DateRisk, ObjID )
  
  file   AccountRisk(account);
  var rest;    /* остаток на счете или счете покрытия */
  var procent; /* процент на формирование резерва */
  var cur;     /* текущая рыночная стоимость */
  var S;       /* искомая величина */
  var prc;
  var str;

  /* Получаем остаток на счете за дату */
  if( acc.Code_Currency == NatCur )
    rest = RestA( acc.Account, DateRisk, null, acc.Chapter );
  else
    rest = RestA( acc.Connect_Account, DateRisk, null, acc.Chapter /*NatCur*/ ); /* !!! */
  end;
  
  if( rest < $0 )
    rest = -rest; /* подправляем остаток */
  end;

/*  prc = money( readNoteForObject(OBJTYPE_ACCOUNT, ObjID, ПроцентРезерва, DateRisk) );
    procent = prc / 100;
*/
  str = readNoteForObject(OBJTYPE_ACCOUNT, ObjID, ПроцентРезерва, DateRisk);
  if( CheckNumbSimbol( str, acc ) == false ) return false; End;
  procent = double( str );

  if( GetMethod(NumInList) == 1 )
    S = rest * procent / 100;
  else
    cur = readNoteForObject(OBJTYPE_ACCOUNT, ObjID, ТекРынСтоим, DateRisk);
    S = (rest - cur) * procent / 100;
  end;

  if( S < $0 )
/*    S = -S;*/  S = $0; /* подправляем величину S */
  end;

  SetParm(0,S);
  return true;

end;

/* Сформировать проводку */
macro CreateCarry( acc, doc, DateReserv )

  file resAccount(account) key 0;
  file rest(restdate) key 0;
  
  var ObjID;
  var stat, return_value;
  var restValue, restDate, DateRisk, DateCarry;
  var AttrID, NumInList;
  var S;

  /* Проверить, открыт ли счет */
  if ( (acc.Open_Close == "З" ) and (acc.Close_Date < DateReserv) )
    msgbox( "Счет " + acc.Account + " закрыт на дату " +DateReserv);
    return false;
  end;


  ObjID = UniID(acc, OBJTYPE_ACCOUNT);
  /* Получить номер по 137-П */
  stat = GetMainObjAttr( return_value, OBJTYPE_ACCOUNT, ObjID, КлассифПо137П,
                         AttrID, null, NumInList );
  if( stat == false )
    msgbox( "Счет " + acc.Account + " не классифицирован по 137-П" );
    return false;
  end;
  /* Получить дату, за которую определена группа риска */
  return_value = GetDateRisk(DateRisk, DateReserv, ObjID);
  if( return_value == 0 )
    msgbox( "Не найдена привязка группы риска для счета " + acc.Account );
    return false;
  end;
  /* Получаем величину S */
  if( CalculateS( S, acc, NumInList, DateRisk, ObjID ) == false )
      return false;
  End;
  /* Получаем номер счета резерва */
  if( GetLinkedObject( СчетРезерва, OBJTYPE_ACCOUNT, acc, OBJTYPE_ACCOUNT, resAccount ) != 0 )
    msgbox( "К счету " + acc.Account + " не привязан счет резерва" );
    return false;
  end;
  /* Счет резерва должен быть рублевым */
  if( resAccount.Code_Currency != NATCUR )
    msgbox( "Счет резерва должен быть рублевым" );
    return false;
  end;
  /* Пытаемся найти счет в базе */
  if( not GetGE(resAccount)  )
    msgbox( "Не найден счет " + resAccount.Account );
    return false;
  end;
  
  /* Остаток на счете резерва */
  restValue = resAccount.R0;
  restDate  = {curdate} + 1;
  
  rest.Chapter       = resAccount.Chapter;
  rest.Account       = resAccount.Account;
  rest.Code_Currency = resAccount.Code_Currency;
  rest.Date_Value    = {curdate};
  if( GetLE(rest) and
     (rest.Chapter       == resAccount.Chapter) and
     (rest.Account       == resAccount.Account) and
     (rest.Code_Currency == resAccount.Code_Currency) )
    if( restValue == rest.Rest ) /* остаток не изменился с некоторых пор */
      restDate = rest.Date_Value; /* тогда запоминаем дату  */
    end;
  end;

  doc.Sum = 0; /* это будет флажок, сигнализирующий о том, что проводку и не надо формировать */
  
  if( return_value == 1 )

    if( restValue == 0 ) /* если нет остатка */
    
      doc.Sum              = S;                       /* сумма         */
      doc.Date_Carry       = DateReserv;              /* дата проводки */
      doc.Account_Payer    = Res137P_СчетРасходов;    /* счет дебета   */
      doc.Account_Receiver = resAccount.Account;      /* счет кредита  */
  
    elif( restDate <= DateReserv )
    
      doc.Sum = S - restValue; /* сумма */
      if( doc.Sum > 0 )
        doc.Date_Carry       = DateReserv;            /* дата проводки */
        doc.Account_Payer    = Res137P_СчетРасходов;  /* счет дебета   */
        doc.Account_Receiver = resAccount.Account;    /* счет кредита  */
      else
        doc.Sum              = -doc.Sum;              /* сумма         */
        doc.Date_Carry       = DateReserv;            /* дата проводки */
        doc.Account_Payer    = resAccount.Account;    /* счет дебета   */
        doc.Account_Receiver = Res137P_СчетДоходов;   /* счет кредита  */
      end;
    
    end;

  else
  
    doc.Sum              = restValue;               /* сумма         */
    doc.Date_Carry       = {curdate};               /* дата проводки */
    doc.Account_Payer    = resAccount.Account;      /* счет дебета   */
    doc.Account_Receiver = Res137P_СчетДоходов;     /* счет кредита  */

  end;
  
  doc.Result_Carry = RESERV137P_CARRY;
  doc.Ground       = Ground;
  doc.Number_Pack  = Number_Pack;

  return true;

end;
