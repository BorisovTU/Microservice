 /*
 $Name: ws_partylist.mac
 $Module: Главная книга
 $Description: Макрос листалки субъектов
 */

import "ws_partylistclass.mac";
import "web_scroll_lib.mac";
import "ws_getparty.mac";

///////////////////////////////////////////////////////////
// Получить информацию о субъекте вместе с кодом (краткую)
// заданного вида
///////////////////////////////////////////////////////////

macro GetPartyByIDForList( partyid             : integer,
                           listkind            : integer,
                           namekind            : integer,
                           codekind            : integer,
                           partykindcollection : string,
                           isallparty          : bool ):TWsPartyEx

  var p = TWsPartyEx();
  
  var obj = PartyList(isallparty, true);  

  var query = obj.GetPartySQL(listkind, namekind, codekind, -1) + " ) AA ) tt WHERE tt.T_PARTYID = " + string(PartyId);

  if( partykindcollection != "" )

    query = query + " AND tt.T_PARTYID IN " +
                    " (SELECT PO.T_PARTYID " +
                    " FROM DPARTYOWN_DBT PO " +
                    " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";

  end;

  PT_PrintDebug( "ws_getparty.mac.log", "*** GetPartyByID ***");
  PT_PrintDebug( "ws_getparty.mac.log", query);

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  ds.NullConversion = true;

  if (ds.MoveNext())

    SetParty(p, ds);

  end;

  return p;

end;

///////////////////////////////////////////////////////////
// Получить список пользователей постранично
// PageSize - размер страницы
// PageNum - номер страницы НАЧИНАЯ С 0 !!!
///////////////////////////////////////////////////////////

macro GetPartyList( pagesize            : integer,
                    pagenum             : integer,
                    listkind            : integer,
                    namekind            : integer,
                    codekind            : integer,
                    paperkind           : integer,
                    partykindcollection : string,
                    filteritems,
                    orderitems,
                    param,
                    isallparty          : bool,
                    PartyEx,
                    IsGetCodeParents    : bool):TArray

  var obj = PartyList(isallparty, true);

  return obj.GetList( pagesize, pagenum, listkind, namekind, codekind, paperkind, partykindcollection, filteritems, orderitems, PartyEx, IsGetCodeParents);


end;

///////////////////////////////////////////////////////////
// Получить общее количество субъектов (для паджинации)
///////////////////////////////////////////////////////////

macro GetPartyCount(listkind            : integer,
                    namekind            : integer,
                    codekind            : integer,
                    partykindcollection : string,
                    filteritems,
                    param,
                    isallparty          : bool,
                    PartyEx ):integer

  var obj = PartyList(isallparty, true);

  return obj.GetCount( listkind, namekind, codekind, partykindcollection, filteritems, PartyEx );

end;

///////////////////////////////////////////////////////////
// Получить объект TWsPartyEx по CodeName
///////////////////////////////////////////////////////////

macro CheckPartyByCodeName(listkind            : integer,
                           mode                : integer,
                           namekind            : integer,
                           codeKind            : integer,
                           Str                 : string,
                           param,
                           isallparty          : bool):bool
  
  var obj = PartyList(isallparty, true);    
  return obj.GetByCheck(listkind, mode, namekind, codeKind, Str);                              

end;

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по
// частичному совпадению значения кода вида кода codeKind или наименования
///////////////////////////////////////////////////////////

macro LookupPartyByCodeName(mode                : integer,
                            listkind            : integer,
                            namekind            : integer,
                            codeKind            : integer,
                            partykindcollection : string,
                            PartyEx,
                            sub                 : string,
                            listLen             : integer,
                            param,
                            isallparty          : bool,
                            IsGetCodeParents    : bool):TArray

  var obj = PartyList(isallparty, true);

  return obj.Lookup(mode, listkind, namekind, codeKind, partykindcollection, PartyEx,sub, listLen, IsGetCodeParents);

end;

macro PT_PartyBankruptWarning(isallparty, PartyIDList):WebResponseBuilder

  var obj = PartyList(isallparty, true);

  return obj.WebCore_PT_PartyBankruptWarning(PartyIDList);

end;

macro WebCore_GetPartyBankruptStatus(PartyID, OnDate)

    if((OnDate == null) or (OnDate == Date(0,0,0))) 
        OnDate = Date;
    end;
    
    var obj = PartyList();
    return obj.WebCore_PT_GetPartybankruptStatus(PartyID, Date);    
    
end;
////////////////////////////////////////////////
// Возвращает номер строки для partyid
////////////////////////////////////////////////

macro GetPartyRowNum(partyid             : integer,
                     listkind            : integer,
                     namekind            : integer,
                     codekind            : integer,
                     partykindcollection : string,
                     param,
                     isallparty          : bool) : integer

  var obj = PartyList(isallparty, true);

  return obj.GetRowNum(partyid, listkind, namekind, codekind, partykindcollection);

end;

////////////////////////////////////////////////
// Возвращает PartyListInfo (PartyListCollection, PartyCount)
////////////////////////////////////////////////

macro GetPartyListInfoListAndCount( pagesize,
                                    pagenum,
                                    listkind,
                                    namekind,
                                    codekind,
                                    partykindcollection,
                                    filteritems,
                                    orderitems ) : PartyListInfo

  var p = PartyListInfo();

  p.PartyListCollection = GetPartyList( pagesize,
                                        pagenum,
                                        listkind,
                                        namekind,
                                        codekind,
                                        partykindcollection,
                                        filteritems,
                                        orderitems );

  p.PartyCount = GetPartyCount( listkind,
                                namekind,
                                codekind,
                                partykindcollection,
                                filteritems );

  return p;

end;

/////////////////////////////////////////////////
// Получить условия для встраивания в SQL-запрос
/////////////////////////////////////////////////
macro WebParty_GetQueryParam( select : @string, from : @string, where : @string, alias : string, PartyID : string, CodeKind : integer )

  var aliasPtCode = string( alias, "_PtCode" );

  WebScroll_AddSelect( @select, string(alias, ".t_PartyID"  , " ", alias, "_PartyID"  ) );
  WebScroll_AddSelect( @select, string(alias, ".t_LegalForm", " ", alias, "_LegalForm") );
  WebScroll_AddSelect( @select, string(alias, ".t_Name"     , " ", alias, "_Name"     ) );
  WebScroll_AddSelect( @select, string(alias, ".t_ShortName", " ", alias, "_ShortName") );

  WebScroll_AddSelect( @select, string(aliasPtCode, ".t_Code", " ", aliasPtCode, "_Code") );

  WebScroll_AddFrom( @from, string("dparty_dbt "   , alias      ) );
  WebScroll_AddFrom( @from, string("dpartcode_dbt ", aliasPtCode) );

  WebScroll_AddWhere( @where, string(PartyID, " = ", alias, ".t_PartyID(+)") );

  WebScroll_AddWhere( @where, string(alias, ".t_PartyID", " = ", aliasPtCode, ".t_PartyID(+)") );

  WebScroll_AddWhere( @where, string(aliasPtCode, ".t_CodeKind(+)", " = ", CodeKind) );

end;

////////////////////////////////////////////////////////////
// Получить условие сортировки для встраивания в SQL-запрос
////////////////////////////////////////////////////////////
macro WebParty_GetSortParam( alias : string )

  return string(alias, "_ShortName");

end;

//////////////////////////////////////////////////////////////////////
// Создать объект TWsDepartment по результатам выполнения SQL-запроса
//////////////////////////////////////////////////////////////////////
macro WebParty_GetObjectFromRecordset( alias : string, rs : RsdRecordset )

  var p = null;

  var aliasPtCode = string( alias, "_PtCode" );

  var IsPartyNull : bool;

  var PartyID = rs.value( string(alias, "_PartyID"), IsPartyNull );

  if( IsPartyNull == false )

    p = TWsPartyEx();

    p.partyid   = PartyID;
    p.legalform = SQL_ConvType( rs.value(string(alias, "_LegalForm")) );
    p.name      = SQL_ConvType( rs.value(string(alias, "_Name"     )) );
    p.partyname = SQL_ConvType( rs.value(string(alias, "_Name"     )) );
    p.shortname = SQL_ConvType( rs.value(string(alias, "_ShortName")) );

    p.code      = SQL_ConvType( rs.value(string(aliasPtCode, "_Code")) );

  end;

  return p;

end;


///////////////////////////////////////////////////////////////////////////////////////////////////
// PartyListInfo
///////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////
// Возвращает PrdKind
////////////////////////////////////////////////

macro GetPrdKindList()

  var cmd, rs;

  var Rec = TRecHandler("prdkind");
  var Arr = TArray;

  var query = "select * from dprdkind_dbt";

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

    var p = TRecHandler("prdkind");
    copy(p, Rec);

    Arr[Arr.size] = p.rec;

  end;

  return Arr;

end;

////////////////////////////////////////////////
// Возвращает PrdProduct
////////////////////////////////////////////////

macro GetPrdProductList()

  var cmd, rs;

  var Rec = TRecHandler("prdproduct");
  var Arr = TArray;

  var query = "select * from dprdproduct_dbt";

  cmd = RsdCommand(query);
  rs = RsdRecordset(cmd);

  while(rs.movenext)

    _CopyRecordsetToRecHandler(rs, Rec);

    var p = TRecHandler("prdproduct");
    copy(p, Rec);

    Arr[Arr.size] = p.rec;

  end;

  return Arr;

end;

private macro GetRestrictionList()

  var Arr = TArray;

  Arr[Arr.size] = CheckActionRestriction(30);
  Arr[Arr.size] = CheckActionRestriction(31);
  Arr[Arr.size] = CheckActionRestriction(151);

  return Arr;
  
end;

private macro GetSfPlanCollection()

  var Arr = TArray;

  var query = " select s.t_sfplanid sfplanid, s.t_num num, s.t_name name " +
              " from dsfplan_dbt s ";

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while (ds.MoveNext())

    var p = TWSSfPlanDS();

    p.sfplanid    = ds.sfplanid;
    p.num         = ds.num;
    p.name        = ds.name;

    Arr[Arr.size] = p;

  end;

  return Arr;

end;

////////////////////////////////////////////////
// Возвращает PartyListInfo (Partylst, PartyKNameCollection, CodeKindCollection)
////////////////////////////////////////////////

macro GetPartyListInfo(ObjectType : integer, ListKind : integer) : PartyListInfo

  var res, err;
  
  var p = PartyListInfo();

  p.Partylst             = GetPartylstByListKind( ListKind );
  p.PartyKNameCollection = GetPartyKNameList();
  p.CodeKindCollection   = GetObjkcodeList( ObjectType );
  p.ObjkrgptCollection   = GetObjkrgptList( ObjectType );
  p.ObjkdocCollection    = GetObjkdocList( ObjectType );
  p.ObjalregCollection   = GetObjalregList( ObjectType );
  p.ServKindCollection   = GetServKindList();
  p.SfPlanCollection     = GetSfPlanCollection();
  p.PrdKindCollection    = GetPrdKindList();
  p.PrdProductCollection = GetPrdProductList();
  p.Restriction          = GetRestrictionList();
  
  GetRegistryValue( "CB\\PARTY\\WEB\\FOTOSIGNUSE", V_BOOL, res, err ,null, {oper});

  if(err == 0)
    p.FotoSignUse = res;
  else
    RunError("Ошибка при чтении настройки реестра!");
  end;

  return p;

end;
