/*
$Name:        ws_IdentPartyList.mac
$Module:      WEB - Общее
$Description: Макрос с реализаций класса для отбора субъектов в виджете поиска субъектов
*/

import "ws_partylistclass.mac";

class (PartyList) IdentPartyList

  InitPartyList(true);

  macro GetList(pagesize            : integer,
                pagenum             : integer,
                listkind            : integer,
                namekind            : integer,
                codekind            : integer,
                paperkind           : integer,
                partykindcollection : string,
                filteritems,
                orderitems,
                param):TArray

    var result = TArray();

    var strAddWhere:string = "";

    var p;

    var query = " select * from (SELECT * FROM ( ";
    
    query = query + GetPartySQL(listkind, namekind, codekind, paperkind);
    
    strAddWhere = GetPartyAddWhereCondition( FilterItems );

    if( strAddWhere != "" )
       query = query + strAddWhere;
    end;

    if( partykindcollection != "" )

      query = query + " AND t.T_PARTYID IN " +
                      " (SELECT PO.T_PARTYID " +
                      " FROM DPARTYOWN_DBT PO " +
                      " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";

    end;
    
    if( param != "" )

      query = query + " AND " + " ( " + string(param) + " ) ";

    end;
    
    query = query + " ORDER BY t.t_partyid ";

    query = query + " ) AA ) tt ";
    
    query = query + " ) ";
    
    if( (orderitems == null) or (OrderItems.Size() > 0) )

      query = query + " ORDER BY " + PT_GetSortStr(orderitems, "");
    
    end;
    
    query = query + " ) ";

    if( pagesize > 0 )
      query = query + " WHERE RN BETWEEN " + string(PageSize*(PageNum) + 1) + " AND " + string(PageSize*(PageNum + 1)) ;
    end;

    PT_PrintDebug( "ws_partylistclass.mac.log", "*** GetList ***");
    PT_PrintDebug( "ws_partylistclass.mac.log", query);

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
    ds.NullConversion = true;

    while (ds.MoveNext())

      p = TWsPartyEx();

      SetParty(p, ds);

      result.value(result.Size) = p;

    end;

    return result;

  end;
  
  macro GetCount( listkind            : integer,
                  namekind            : integer,
                  codekind            : integer,
                  partykindcollection : string,
                  filteritems,
                  param):integer

    var result : integer;

    var strAddWhere:string = "";

    var query = "SELECT COUNT(*) C FROM ( ";

    query = query + GetPartySQL(listkind, namekind, codekind, -1);

    strAddWhere = GetPartyAddWhereCondition( FilterItems );

    if( strAddWhere != "" )
      query = query + strAddWhere;
    end;

    if( partykindcollection != "" )

      query = query + " AND t.T_PARTYID IN " +
                      " (SELECT PO.T_PARTYID " +
                      " FROM DPARTYOWN_DBT PO " +
                      " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";

    end;
    
    if( param != "" )

      query = query + " AND " + " ( " + string(param) + " ) ";

    end;    

    query = query + " ) AA ) tt ";

    query = query + ")";

    PT_PrintDebug( "ws_partylistclass.mac.log", "*** GetCount ***");
    PT_PrintDebug( "ws_partylistclass.mac.log", query);

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
    ds.NullConversion = true;

    if (ds.MoveNext())

      result = ds.c;

    end;

    return result;

  end;  

end;

///////////////////////////////////////////////////////////
// Получить список пользователей постранично
// PageSize - размер страницы
// PageNum - номер страницы НАЧИНАЯ С 0 !!!
///////////////////////////////////////////////////////////

macro GetPartyList( pagesize            : integer,
                    pagenum             : integer,
                    listkind            : integer,
                    namekind            : integer,
                    codekind            : integer,
                    paperkind            : integer,
                    partykindcollection : string,
                    filteritems,
                    orderitems,
                    param):TArray
                    
  var obj = IdentPartyList();    
  
  return obj.GetList( pagesize, pagenum, listkind, namekind, codekind, paperkind, partykindcollection, filteritems, orderitems, param );
                    

end;

///////////////////////////////////////////////////////////
// Получить общее количество субъектов (для паджинации)
///////////////////////////////////////////////////////////

macro GetPartyCount(listkind            : integer,
                    namekind            : integer,
                    codekind            : integer,
                    partykindcollection : string,
                    filteritems,
                    param):integer
                    
  var obj = IdentPartyList();    
    
  return obj.GetCount( listkind, namekind, codekind, partykindcollection, filteritems, param);                    

end;

///////////////////////////////////////////////////////////
// Получить объект TWsPartyEx по CodeName
///////////////////////////////////////////////////////////

macro CheckPartyByCodeName(listkind            : integer,
							  mode                : integer,
                              namekind            : integer,
                              codeKind            : integer,
                              Str                 : string,
                              param):bool
  
   var obj = IdentPartyList();    
  return obj.GetByCheck(listkind, mode, namekind, codeKind, Str );                              

end;

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по
// частичному совпадению значения кода вида кода codeKind или наименования
///////////////////////////////////////////////////////////

macro LookupPartyByCodeName(mode                : integer,
                            listkind            : integer,
                            namekind            : integer,
                            codeKind            : integer,
                            partykindcollection : string,
                            sub                 : string,
                            listLen             : integer,
                            param):TArray
  
  var obj = IdentPartyList();  
  
  return obj.Lookup(mode, listkind, namekind, codeKind, partykindcollection, sub, listLen);                            

end;

////////////////////////////////////////////////
// Возвращает номер строки для partyid
////////////////////////////////////////////////

macro GetPartyRowNum(partyid             : integer,
                     listkind            : integer,
                     namekind            : integer,
                     codekind            : integer,
                     partykindcollection : string,
                     param) : integer
                     
  var obj = IdentPartyList();
  
  return obj.GetRowNum(partyid, listkind, namekind, codekind, partykindcollection);

end;