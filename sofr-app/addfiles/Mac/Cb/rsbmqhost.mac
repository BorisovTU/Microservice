import rcw, rsd, globals;

CLASS RslMQHost
   var Host:STRING;
   var Port:INTEGER;
   var queueManager:STRING;
   var queueName:STRING;

   private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");
   private var jhost = jvm.createJavaObject ("ru.softlab.integration.mq.RsbMQHost");
   private var bConnected = false;

   PRIVATE MACRO WriteLog(QueueMgr:STRING, QueueName:STRING, Msg:OBJECT, ErrObject:OBJECT)
      var insSql = "INSERT INTO DXR_LOG_DBT (T_ROLE, T_KIND, T_REQID, T_SESSIONID, T_OPER, T_DATE, T_SYSDATE, T_SYSTIME, T_MESSAGE, T_MACRO, T_FUNC, T_URL, T_LOGIN, T_METHOD, T_ERRORCODE) " +
			"VALUES (?, ?, ?, ?, ?, ?, (to_date(to_char( sysdate, 'DDMMYYY' ), 'DDMMYYY')), to_date( '01010001' || to_char( sysdate, 'hh24miss' ), 'ddmmyyyyhh24miss' ), ?, ?, ?, ?, ?, ?, ?)";

      var txtMes = ""; 
      var MesId = ""; 
      var MesCorrId = ""; 
      var errMes = ""; 
      var errCode = 0; 
      var count = 0;
      var i = 0;

      if (Msg != null)
         MesId = Msg.getJMSMessageID(); 
         MesCorrId = Msg.getJMSCorrelationID(); 
         txtMes = Msg.getText(); 
      end;

      if (ErrObject != null)
         if (IsEqClass ("TRslError",ErrObject))
            errMes = ErrObject.Message; 
            errCode = ErrObject.Code; 
         else
            errMes = ErrObject.getMessage(); 
            errCode = ErrObject.getErrorCode(); 
         end;

         if (IsEqClass ("TRsComErr", ErrObject.err))
            count = ErrObject.err.Count;
            i = 0;
            while (i <= count)
               errMes = errMes + "\n "+ ErrObject.err.Message (i) + " (Code = " + ErrObject.err.Code (i) +
                                                                 ", Level = " + ErrObject.err.Level (i) + ")" ;
               i = i + 1
            end;
         end;
      end;

      var mes = "<?xml version='1.0' encoding='windows-1251'?>" +
    	"\n<MQOperation>" +
    	"\n    <Type>QRY_PUT</Type>" +
    	"\n    <QMgrName>" + QueueMgr + "</QMgrName>" +
    	"\n    <QueryName>" + QueueName + "</QueryName>" +
    	"\n    <MsgId>" + MesId + "</MsgId>" +
    	"\n    <CorrelId>" + MesCorrId + "</CorrelId>" +
    	"\n    <Message><![CDATA[" + txtMes + "]]></Message>" +
    	"\n    <ErrCode>" + ErrCode + "</ErrCode>" +
    	"\n    <ErrMessage><![CDATA[" + errMes + "]]></ErrMessage>" +
    	"\n</MQOperation>";
	
      var cmd =  RsdCommand(insSql);

  	cmd.addParam( "", RSDBP_IN, 2 );
  	cmd.addParam( "", RSDBP_IN, 60 );
  	cmd.addParam( "", RSDBP_IN, MesId );
  	cmd.addParam( "", RSDBP_IN, 0 );
  	cmd.addParam( "", RSDBP_IN, {oper} );
	
  	cmd.addParam( "", RSDBP_IN, {curDate} );
  	cmd.addParam( "", RSDBP_IN, mes );
  	cmd.addParam( "", RSDBP_IN, "" );
  	cmd.addParam( "", RSDBP_IN, "" );
  	cmd.addParam( "", RSDBP_IN, "" );
	
  	cmd.addParam( "", RSDBP_IN, "" );
  	cmd.addParam( "", RSDBP_IN, "" );
  	cmd.addParam( "", RSDBP_IN, ErrCode );
	
      cmd.execute();
      
   END;

   MACRO Connect()
   
      jhost.host = Host;
      jhost.port = Port;
      jhost.queueManager = queueManager;
      jhost.queueName = queueName;

      jhost.connect();
      bConnected = true;

   OnError(Err)
      WriteLog(queueManager, queueName, null, Err);
   END;

   MACRO CreateMessage(text:STRING):OBJECT
      if (bConnected != true)
         return null;
      end; 

      return jhost.createMessage(text);
   END;

   MACRO SendMessage(msg:OBJECT)
      if (bConnected != true)
         return;
      end; 
      jhost.sendMessage(msg);
      WriteLog(queueManager, queueName, msg, null);
   OnError(Err)
      WriteLog(queueManager, queueName, msg, Err);
   END;
END;

