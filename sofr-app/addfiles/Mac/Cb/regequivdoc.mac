/*
 * Макрос для определения счетов курсовых разниц при переоценке НВПИ
 */
Import CTInter, FIInter, rsd;

/* определить вид финансового инструмент */
private macro GetSideBalance( conn_account : TRecHandler ) : integer
  
  var SideBalance : integer;

  SideBalance = 2;

  if( Trim(StrUpr(conn_account.rec.Kind_Account)) == "А" )
    SideBalance = 1;
  end;

  return SideBalance;

end;

/*
 *  Класс первичного документа для определения счетов курсовых разниц при переоценке НВПИ
 */
class PDocRegEquiv( p_rec_account : TRecHandler )

  var rec_account : TRecHandler;

  var error : integer;

  /* корректировка счета */
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    return true;
  end;

  /* определение FIRole */
  macro GetBasisFIRole( FIRole : integer ) : integer
    return FIROLE_UNDEF;
  end;

  /* определение параметров второго рода */
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole ) : integer
    
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = rec_account.rec.Department;
    elif( ParmKind == MC_TYPE_PARAMETR_FIID )
      if( rec_account.rec.Chapter == 4 ) Parametr = rec_account.rec.Code_Currency;
      else                               Parametr = rec_account.rec.CurrencyEq;
      end;
    end;

    return Parametr;

  end;

  /* определение параметров первого рода */
  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole ) : integer
    
    var Parametr = -1;

    if( Classificator == LLCLASS_KIND_MARG_FUTURES ) /* ВидМаржСроч */
      Parametr = 6; /* Изменение валютного курса */
    elif( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND ) /* ВидКорреспСчета */
      Parametr = GetSideBalance( rec_account );
    end;

    return Parametr;

  end;

  
/* конструктор */  
  error = 0;

  rec_account = p_rec_account;
  
end;
