Import BankInter, FIInter, PTInter, GateInter;

private const WS_ERROR = 1;

/**
 *  Функция генерации ошибки Web-сервиса
 */
macro WS_CreateError( ErrNum : integer )
  CreateErrMsg( ErrNum );
  RunError( GetErrMsg(), ErrNum );
end;

/**
 *  Функция проверки корректности задания параметра Web-сервиса
 *
 *    @param ParamNumber  Номер параметра, начиная с 1
 *    @param ParamValue   Значение параметра
 *    @param IsMandatory  Признак обязательного параметра
 *    @param ParamType    Требуемый тип значения параметра
 */
macro WS_CheckParameter( ParamNumber : integer, ParamValue : variant, IsMandatory : bool, ParamType : integer )
  
  var ErrNum : integer;

  if( (IsMandatory == true) and (ParamValue == null) )
    ErrNum = 31404;
    CreateErrMsg( ErrNum, ParamNumber );
    RunError( GetErrMsg(), ErrNum );
  end;

  if( (ParamType != null) and (ParamValue != null) and (ValType(ParamValue) != ParamType) )
    ErrNum = 31405;
    CreateErrMsg( ErrNum, ParamNumber );
    RunError( GetErrMsg(), ErrNum );
  end;

end;

/**
 *  Функция проверки корректности задания атрибута
 *
 *    @param AttribValue  Значение атрибута, выходной параметр
 *    @param ParamN       Номер параметра, начиная с 1, которому принадлежит атрибут
 *    @param ObjectName   Название объекта Web-сервиса, свойством которого является атрибут
 *    @param AttribName   Название атрибута
 *    @param SrcObject    Объект Web-сервиса, свойством которого является атрибут
 *    @param AttribType   Требуемый тип значения атрибута
 *    @param IsMandatory  Признак обязательного параметра
 *    @param EmptyValue   Значение атрибута, которое является не заданным
 */
macro WS_SetAttributeValue( AttribValue : @variant, ParamN : integer, ObjectName : string, AttribName : string, SrcObject : object, AttribType : integer, IsMandatory : bool, EmptyValue : variant, ForceSet : bool )
  
  var ErrNum : integer;

  var AttribFullName :string;
  
  AttribFullName = ObjectName;
  
  if( AttribFullName != "" )
    AttribFullName = AttribFullName + ".";
  end;

  AttribFullName = AttribFullName + AttribName;

  if( (AttribValue == null) OR ((ForceSet != NULL) AND (ForceSet == true)) )
  
    var PropValue : variant;

    var iProp = GenPropID( SrcObject, AttribName );

    if( iProp == -1 )

      if( IsMandatory )
      
        /* не указан обязательный атрибут */
        ErrNum = 31406;
        CreateErrMsg( ErrNum, ParamN, AttribFullName );
        RunError( GetErrMsg(), ErrNum );
      
      end;

    else

      PropValue = GenGetProp( SrcObject, AttribName );

      if( (AttribType != null) and (ValType(PropValue) != AttribType) and (PropValue != null) ) 
        
        /* указан некорректный тип атрибута */
        ErrNum = 31407;
        CreateErrMsg( ErrNum, ParamN, AttribFullName );
        RunError( GetErrMsg(), ErrNum );
      
      end;

      if( (EmptyValue != null) and (PropValue == EmptyValue) )

        /* указано некорректное значение атрибута */
        ErrNum = 31406;
        CreateErrMsg( ErrNum, ParamN, AttribFullName );
        RunError( GetErrMsg(), ErrNum );

      end;

    end;

    /* заносим полученное значение свойства в переданный параметр */
    AttribValue = PropValue;

  end;

end;

/**
 *  Функция получения идентификатора финансового инструмента из параметров Web-сервиса
 */
macro WS_GetFIID( wsFIID_Code : variant, iAppID : integer ) : integer

  record fininstr("fininstr.dbt");

  var ErrNum : integer;

  var FIID : integer;

  if( (ValType(wsFIID_Code) == V_INTEGER) )
    
    FIID = wsFIID_Code;
  
  elif( ValType(wsFIID_Code) == V_STRING )
    
    if( wsFIID_Code == "0" )

      FIID = NATCUR;
    
    else

      if( iAppID == null )
      
        if( ПолучитьФинИнПоISO(wsFIID_Code, fininstr) )
          FIID = fininstr.FIID;
        else
          ErrNum = 31451;
          CreateErrMsg( ErrNum, wsFIID_Code );
          RunError( GetErrMsg(), ErrNum );
        end;
    
      else
      
        var stat : integer;

        var FIID_Code : string;
      
                        stat = GtConvertObjectCode( FIID_Code, GT_APP_RSBANKV6_SRC, wsFIID_Code, iAppID, RG_CURRENCY );
        if( stat != 0 ) stat = GtConvertObjectCode( FIID_Code, GT_APP_RSBANKV6_SRC, wsFIID_Code, iAppID, RG_METAL    ); end;
        if( stat != 0 ) stat = GtConvertObjectCode( FIID_Code, GT_APP_RSBANKV6_SRC, wsFIID_Code, iAppID, RG_AVOIRISS ); end;

        if( stat != 0 )
          WS_CreateError( stat );
        end;

        FIID = int(FIID_Code);

      end;

    end;

  end;

  return FIID;

end;

/**
 *  Функция получения идентификатора субъекта из параметров Web-сервиса
 */
macro WS_GetPartyID( wsPartyCode : variant, CodeKind : integer, iAppID : integer ) : integer

  var ErrNum : integer;

  var PartyID : integer;

  if( ValType(wsPartyCode) == V_INTEGER )
    
    PartyID = wsPartyCode;
  
  elif( ValType(wsPartyCode) == V_STRING )
    
    var stat : integer;

    if( iAppID == null )
      
      if( CodeKind == null ) CodeKind = PTCK_CONTR; end;

      PartyID = GetCodeParty( wsPartyCode, CodeKind, stat );

      if( stat != 0 )
        ErrNum = 31452;
        CreateErrMsg( ErrNum, wsPartyCode, CodeKind );
        RunError( GetErrMsg(), ErrNum );
      end;

    else
      
      var ClientCode : string;

      stat = GtConvertObjectCode( ClientCode, GT_APP_RSBANKV6_SRC, wsPartyCode, iAppID, RG_PARTY );

      if( stat != 0 )
        WS_CreateError( stat );
      end;

      PartyID = int(ClientCode);

    end;

  end;

  return PartyID;

end;
