/*
 $Name: prcret20.mac
 $Module: Возврат излишне начисленных процентов
 $Description: Расчет суммы
 */
 /*
	Блок      : 452700 - "Возврат излишне начисленных процентов"
    Шаг       : 20    - "Расчет суммы"
    Назначение: Макрос шага
    Описание  : Макрос шага
 */

IMPORT RsbDataSet, InsCarryDoc, BankInter, pm_common,
       PTInter, CurrInter, ReptCBCommon;

record contract_buf (prccontract);
record cntopr_buf (prccntopr);

var prccontract = TBfile("prccontract.dbt"),
    cntsched = TBfile("prccntsched.dbt"),
    prccntopr = TBfile("prccntopr.dbt");
    
// Вид графика расчета счета
const PC_SCHEDTYPE_ABSOLUTE       = 1, // абсолютный
      PC_SCHEDTYPE_RELATIVE       = 2, // относительный
      PC_SCHEDTYPE_CALCDAY        = 3, // по дню расчета
      PC_SCHEDTYPE_UNSET          = 4, // не задан
      PC_SCHEDTYPE_DAILY          = 5; // ежедневный

// Вид процентной ставки счета процентов
const PC_RATETYPE_ABSOLUTE       = 1, // абсолютная
      PC_RATETYPE_RELATIVE       = 2, // относительная
      PC_RATETYPE_CONTRACT       = 3; // от договора

// Тип ставки
const PC_RATEALG_DATE            = 1, // ставка по дате
      PC_RATEALG_COMPL           = 2; // сложная     

//--------  НОВЫЕ ПРОЦЕНТЫ:  ----------

// Вид графика
const PRC_SCHEDKIND_USER         = 0, // пользовательский
      PRC_SCHEDKIND_CALC         = 1, // расчета
      PRC_SCHEDKIND_CHARGE       = 2, // начисления
      PRC_SCHEDKIND_PAY          = 3; // оплаты
      
// Тип графика
const PRC_SCHEDTYPE_ABSOLUTE      = 1, // абсолютный
      PRC_SCHEDTYPE_RELATIVE      = 2; // относительный


// Тип процентной ставки
const PRC_RATETYPE_ABSOLUTE      = 1, // абсолютная
      PRC_RATETYPE_USER          = 2, // пользовательская
      PRC_RATETYPE_FLOATING      = 3; // плавающая

// Статус оплаты
const PRC_PAYSTATUS_NOT_CALCULATED     = 0,     // не рассчитано
      PRC_PAYSTATUS_NOT_CLAIMED        = 1,     // не предъявлено
      PRC_PAYSTATUS_NOT_PAID           = 2,     // не оплачено
      PRC_PAYSTATUS_PARTLY_PAID        = 3,     // частично оплачено
      PRC_PAYSTATUS_PAID               = 4,     // оплачено
      PRC_PAYSTATUS_PARTLY_PAID_MANUAL = 5,     // частично оплачено ручной ввод
      PRC_PAYSTATUS_PAID_MANUAL        = 6;     // оплачено ручной ввод

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep( Buffer, Document )
    SetBuff(cntopr_buf, Document);
    
    var stat = 0;
    var ContractID = cntopr_buf.ContractID;
    var DocID      = cntopr_buf.DocID;
  
    var Bdate = cntopr_buf.BeginDate;
    var Edate = cntopr_buf.EndDate;
    var dS0 = $0.0;

    ClearRecord( contract_buf );

    prccontract.KeyNum = 0;  
    prccontract.rec.ContractID = ContractID;
    if (not prccontract.GetEQ)
        msgbox(ContractID, "Не найден договор с ID = " + ContractID);
        return 1;
    end;

    var ПогрешностьSнач;
    var ПогрешностьS0;
    // 2.1
    // Выбрать из графика оплат строки, у которых
    var cmd = RsdCommand("SELECT * FROM DPRCCNTSCHED_DBT SH WHERE SH.T_PERIODBEGINDATE >= :Bdate " + 
        "AND SH.T_PERIODENDDATE <= :Edate AND SH.T_DATEREAL <> TO_DATE('01.01.0001', 'dd.mm.yyyy') " + 
        "AND SH.T_SPECIALTYPE <> 3 AND SH.T_SCHEDULEKIND = 3 AND SH.T_CONTRACTID = :cntContract ORDER BY SH.T_PERIODBEGINDATE");
    cmd.AddParam("Bdate", RSDBP_IN, Bdate);
    cmd.AddParam("Edate", RSDBP_IN, Edate);
    cmd.AddParam("cntContract", RSDBP_IN, ContractID);
  
    cmd.execute();	
    var rs = RsdRecordset(cmd);
    var found = false;
    var Sр = $0.0;
    var Bdate1 = Bdate, Edate1;
    
    while (rs.MoveNext())
        found = true;
        Sр = Sр + rs.value("T_SUMFACT");
    end;
    
    if (found)
        Edate1 = Date(rs.value("T_PERIODENDDATE"));
        var Sн = $0.0;
        stat = Prc_CalcPeriod (ContractID, Bdate1, Edate1, false, 0, cntopr_buf.RecalcRateID, Sн);
        
        var S0round = Round (Sн, 2);
        ПогрешностьS0 = ПогрешностьОкругления(Sн, S0round);
        dS0 = S0round - Sр;
        
        if (dS0 != $0.0)
            Bdate = Edate1 + 1;
        end;
    end;

    // 3 Определим сумму излишне начисленных процентов Sнач
    // Выбрать из графика оплат строки, у которых
    var Sнач = $0.0;
    var cmd2 = RsdCommand("SELECT * FROM DPRCCNTSCHED_DBT SH WHERE SH.T_PERIODBEGINDATE >= :Bdate " + 
        "AND SH.T_PERIODENDDATE <= :Edate AND SH.T_DATEREAL <> TO_DATE('01.01.0001', 'dd.mm.yyyy') " + 
        "AND SH.T_SPECIALTYPE <> 3 AND SH.T_SCHEDULEKIND = 2 AND SH.T_CONTRACTID = :ContractID ORDER BY SH.T_PERIODBEGINDATE");
    cmd2.AddParam("Bdate", RSDBP_IN, Bdate); 
    cmd2.AddParam("Edate", RSDBP_IN, Edate);
    cmd2.AddParam("ContractID", RSDBP_IN, ContractID);
    cmd2.execute();	
    var rs2 = RsdRecordset(cmd2);
    var Bdate2 = Date();
    var Edate2 = Edate;
    var fFirst = true;
    found = false;
    Sр = $0.0;
    while (rs2.MoveNext())
        if (fFirst)
            fFirst = false;
            Bdate2 = Date(rs2.value("T_PERIODBEGINDATE"));
        end;
        found = true;
        Sр = Sр + rs2.value("T_SUMFACT");
        Edate2 = Date(rs2.value("T_PERIODENDDATE"));
    end;
    Sн = $0.0;
    
    if (found == true)
        stat = Prc_CalcPeriod (ContractID, Bdate2, Edate2, false, 0, cntopr_buf.RecalcRateID, Sн);
        var Sнround = Round (Sн, 2);
        ПогрешностьSнач = ПогрешностьОкругления(Sн, Sнround);
        Sнач = Sнround - Sр;
    end;
    
    if ((dS0 == $0.0) and (Sнач == $0.0))
        msgbox ("Ошибка в учете! По договору нет ни начисленных, ни оплаченных процентов");
        return 1;
    end;
    
    if (cntopr_buf.IsCreateDoc == "X")
        var msg = "";
        if (dS0 != $0.0)
            msg = msg + "Рассчитанная сумма для возврата оплаченных процентов за период " 
                + String(Bdate1) + " - " + String(Edate1) + " - " + String(dS0) + "\n";
        end;
        
        if (Sнач != $0.0)
            msg = msg + "Рассчитанная сумма для возврата начисленных процентов за период " 
                + String(Bdate2) + " - " + String(Edate2) + " - " + String(Sнач)  + "\n";
        end;
        msgbox(msg);
    end;
    
    if (dS0)
        PrcInsertReturning(ContractID, PRC_SCHEDKIND_PAY, 3, cntopr_buf.OPERDATE,
            cntopr_buf.BeginDate, cntopr_buf.EndDate, PRC_PAYSTATUS_NOT_CLAIMED, dS0, ПогрешностьS0);
    end;
    
    if (Sнач)
        PrcInsertReturning(ContractID, PRC_SCHEDKIND_CHARGE, 3, cntopr_buf.OPERDATE,
            cntopr_buf.BeginDate, Edate2, PRC_PAYSTATUS_NOT_CALCULATED, Sнач, ПогрешностьSнач);
    end;
    return 0;
END;