 /*
 $Name: pmdeltrn.mac
 $Module: МБР
 $Description: Удаление проводок платежа на шаге операции
 */

import PaymInter, InsCarryDoc, oralib, globals;

//при выполнении шага "Возврат клиенту" выполнение обратных проводок(мультивалютных)

PRIVATE MACRO PM_CheckCarry( payment:RsbPayment, pmdocs:TRecHandler, BeginDelDate:date, trnTypes:string ):bool
  
  var select:string = "select t.t_chapter, t.t_date_carry, t.t_number_pack, t.t_numb_document, " +
                      "t.t_result_carry, t.t_kind_oper, t.t_shifr_oper, t.t_ground, t.t_department, t.t_account_payer, " +
                      "t.t_fiid_payer, t.t_sum_payer, t.t_account_receiver, t.t_fiid_receiver, t.t_sum_receiver, t.t_typedocument " +
                      "from dacctrn_dbt t " +
                      "where t.t_acctrnid = :acctrnid and t.t_result_carry <> 83 ";

  var params:TArray = TArray();
  
  var paymtr:RsbPaymTransaction = NULL;
  params[0] = SQLParam( "t_acctrnid", pmdocs.rec.AccTrnID );

  var rs:RsdRecordset = execSQLselect( select, params );

  if ( rs.moveNext() )
    if ( (rs.Value(1) < BeginDelDate) and (rs.Value(0) == 1))
      paymtr = payment.MakeTransaction();
      paymtr.Chapter         = rs.Value("t_chapter");
      paymtr.Date_Carry      = payment.ValueDate;
      paymtr.Number_Pack     = rs.Value("t_number_pack");
      paymtr.Numb_Document   = rs.Value("t_numb_document");
      paymtr.ResultCarry     = rs.Value("t_result_carry");
      paymtr.Kind_Oper       = rs.Value("t_kind_oper");
      paymtr.Shifr_Oper      = rs.Value("t_shifr_oper");
      paymtr.Ground          = "Проводка обратная проводке:"+rs.Value("t_ground");
      paymtr.Department      = rs.Value("t_department");
      paymtr.AccountPayer    = rs.Value("t_account_receiver");
      paymtr.FIIDPayer       = rs.Value("t_fiid_receiver");
      paymtr.SumPayer        = rs.Value("t_sum_receiver");
      paymtr.AccountReceiver = rs.Value("t_account_payer");
      paymtr.FIIDReceiver    = rs.Value("t_fiid_payer");
      paymtr.SumReceiver     = rs.Value("t_sum_payer");                     
      /*
      var trn55indx = Index( rs.Value("t_typedocument"), "" ); // Не реализовано удаление символа проводки 5.5,
      if( not trn55indx )  */                                  // так как по 209771 еще не реализован символ
        paymtr.TypeDocument  = rs.Value("t_typedocument");
    /*  else
        paymtr.TypeDocument  = SubStr( rs.Value("t_typedocument"), 1, trn55indx - 1 ) 
                             + SubStr( rs.Value("t_typedocument"), trn55indx + 1 );   
      end; */
      if( trnTypes and trnTypes != "" )
        paymtr.TypeDocument = paymtr.TypeDocument + trnTypes;  
      end;

      if( not paymtr.Carry() )
        MsgBox("Ошибка при актуализации платежа");
        return 1;
      end;

      return false;

    end;
  end; 

  return true;

  
END;

//при выполнении шага "Возврат клиенту" выполнение обратных проводок(фактических)
//------------------------------------------------------------------------------
// Удалить поводку, привязанную к платежу
//  payment  - объект платежа
//  pmdocs   - привязка проводки к платежу
//  Возвращает True в случае успеха
//------------------------------------------------------------------------------
PRIVATE MACRO _PM_DeletePaymTransaction( payment:RsbPayment, 
                                         pmdocs:TRecHandler, 
                                         errtext:@string, 
                                         BeginDelDate:date,
                                         trnTypes:string  ):bool
  
  errtext = "";
  
  if( pmdocs.rec.PaymentID != payment.PaymentID )
    errtext = "Проводка не привязана к платежу";
    return false;
  end;
  //Либо удаляем, либо делаем обратную проводку
  if (PM_CheckCarry(payment, pmdocs, BeginDelDate, trnTypes ))
    if( not Opr_DeleteCarry( pmdocs.rec.AccTrnID ) )
      errtext = "Ошибка удаления проводки";
      return false;
    end;
  end;

  // Future* поля не переставляем, т.к. по МФР-ным проводкам все равно переставить не сможем
  return true;

END;

//------------------------------------------------------------------------------
// Получить корсчет по корсхеме платежа
//------------------------------------------------------------------------------
PRIVATE MACRO _PM_GetCorrAccount( payment:RsbPayment, side:integer ):string
  
  var corschem:integer = -1,
      fiid    :integer = -1;
  
  if( side == PRT_Credit )
    if( payment.PayerIsSender )
      corschem = payment.OutCorschem;
    else
      corschem = payment.InCorschem;
    end;
    fiid = payment.ReceiverFIID;
  else
    if( payment.PayerIsSender )
      corschem = payment.InCorschem;
    else
      corschem = payment.OutCorschem;
    end;
    fiid = payment.PayerFIID;
  end;

  var select:string = "select t.t_Account " +
                      "from dcorschem_dbt t " +
                      "where t.t_FI_Kind = 1 " +
                        "and t.t_FIID = :FIID " +
                        "and t.t_Number = :CorSchem";
  var params:TArray = TArray();
  params[0] = SQLParam( "FIID"    , fiid     );
  params[1] = SQLParam( "CorSchem", corschem );
  var rs:RsdRecordset = execSQLselect( select, params, false );

  if( rs and rs.moveNext() )
    return rs.Value(0);
  else
    return "";
  end;

END;

//------------------------------------------------------------------------------
// Удалить все проводки, привязанные к платежу
//  payment  - объект платежа
//  Возвращает True в случае успеха
//------------------------------------------------------------------------------
MACRO PM_DeleteAllPaymTransactions( payment:RsbPayment, 
                                    errtext:@string, 
                                    BeginDelDate:date,
                                    trnTypes:string  ):bool

  var select:string = "select t.t_AccTrnID " +
                      "from dpmdocs_dbt t, dacctrn_dbt acc  " +
                      "where t.t_PaymentID = :PaymentID     " +
                      "and t.t_AccTrnID    = acc.t_AccTrnID " +
                      "and acc.t_state     <  4             " +
                      "order by t.t_AutoKey desc";
  var params:TArray = TArray();
  params[0] = SQLParam( "PaymentID", payment.PaymentID );
  var rs:RsdRecordset = execSQLselect( select, params );
  var stat:bool = true;
  var pmdocs:TRecHandler = TRecHandler( "pmdocs.dbt" );
  while( stat and rs.moveNext() )
    pmdocs.rec.PaymentID       = payment.PaymentID;
    pmdocs.rec.AccTrnID        = rs.Value(0);
    stat = _PM_DeletePaymTransaction( payment, pmdocs, @errtext, BeginDelDate, trnTypes );
  end;

  // Вернем Future* поля в начальное состояние
  if( stat )
    
    payment.FuturePayerFIID    = payment.PayerFIID;
    payment.FutureReceiverFIID = payment.ReceiverFIID;

    if( payment.PayerGroup != PAYMENTS_GROUP_EXTERNAL )
      payment.FuturePayerAccount = payment.PayerAccount;
    else
      payment.FuturePayerAccount = _PM_GetCorrAccount( payment, PRT_Debet );
    end;

    if( payment.ReceiverGroup != PAYMENTS_GROUP_EXTERNAL )
      payment.FutureReceiverAccount = payment.ReceiverAccount;
    else
      payment.FutureReceiverAccount = _PM_GetCorrAccount( payment, PRT_Credit );
    end;

  end;

  return stat;
END;
