/*
$Name:           sfrepacc.mac
$Module:         Ядро ГКБО
$Description:    Протокол начисления комиссий
*/


import BankInter, sfrepcommon;

var PrevObject = "";
var PrevDprt = -1;

private macro PrintHeader( bPay:bool )
  var head = "Протокол начисления комиссий";
  if( bPay == true )
    head = "Протокол доначисления комиссий"
  end;
  
  [
  

                     ###############################](head);
end;

private macro PrintParm( Dprt, Attr, Comiss, EndDate, TrnDate, bPay )

  var opKind = "начисления:";
  if( bPay == true )
    opKind = "оплаты:"
  end; 
[
   Параметры запуска операции ###########](opKind);
[   Филиал - #########################################################################] ( Dprt );
[   Признак - ########################################################################] ( Attr );
[   Комиссия - #######################################################################] ( Comiss );
[   Дата окончания периода - ##########] ( EndDate );
[   Дата проводки - ##########] ( TrnDate );

end;

private macro GetNameDBO(dogid, dognum) 
  var sql, cmd, rs;
  sql = " select sf.* from ddlcontrmp_dbt mp, ddlcontr_dbt dl, dsfcontr_dbt sf  where mp.t_sfcontrid = ? and dl.t_dlcontrid = mp.t_dlcontrid and sf.t_id = dl.t_sfcontrid";
  cmd = RSDCommand(sql);
  cmd.AddParam("dogid", RSDBP_IN, dogid);
  rs = RSDRecordSet(cmd);
  if (rs.MoveNext())
     return rs.value("t_number");
  else
     return dognum;
  end;
end;

private macro PrintAccrue( AllDprt, Dprt, rs : RsdRecordSet )

  var DprtStr = "";
  var CCurStr = "";

  var SfDefComID;
  if( rs.value(0) == NULLVAL )
    SfDefComID = "";
  else
    SfDefComID = rs.value(0);
  end;

  var Amount;
  if( rs.value(4) == NULLVAL )
    Amount = "";
  else
    Amount = money( rs.value(4) );
  end;

  var BeginDate;
  if( rs.value(1) == NULLVAL )
    BeginDate = "";
  else
    BeginDate = string( rs.value(1) );
  end;

  var EndDate;
  if( rs.value(2) == NULLVAL )
    EndDate = "";
  else
    EndDate = string( rs.value(2) );
  end;

  var Debet;
  if( rs.value(6) == NULLVAL )
    Debet = "";
  else
    Debet = rs.value(6);    
  end;

  var Credit;
  if( rs.value(7) == NULLVAL )
    Credit = "";
  else
    Credit = rs.value(7);
  end;

  var Comment;
  if( rs.value(8) == NULLVAL )
    Comment = "";
  else
    Comment = rs.value(8);
  end;

  var object;
  if( PrevDprt == -1 )

[
  Результаты выполнения:];
  
  end;

  if(( PrevDprt == -1 ) or (( AllDprt == true ) and ( PrevDprt != rs.value( 12 ))))
    PrevDprt = rs.value( 12 );

    DprtStr = SfRep_GetDepartmentName( rs.value(12) );
    
[
  Филиал - #####################################################################################] ( DprtStr );
[+-------------------------+------------------------------+----------+----------+---------+------+-----------------------------------+-----------------------------------+-----------------------------+];
[|Объект договора          |Комиссия                      |Дата      |Дата      |Сумма    |Валюта|Дт                                 |Кт                                 |Комментарий                  |];
[|                         |                              |начала    |окончания |         |      |                                   |                                   |                             |];
[|                         |                              |периода   |периода   |         |      |                                   |                                   |                             |];
[|                         |                              |начисления|начисления|         |      |                                   |                                   |                             |];
[+-------------------------+------------------------------+----------+----------+---------+------+-----------------------------------+-----------------------------------+-----------------------------+];
  
  end;

  if(( strlen( PrevObject ) == 0 ) or ( PrevObject != rs.value( 14 )))
    PrevObject = rs.value( 14 );
  if(PrevObject == "")
     PrevObject = GetNameDBO(rs.value( 17 ), rs.value( 16 ));
  end;

[|#########################|                              |          |          |         |      |                                   |                                   |                             |] 
  ( PrevObject/*rs.value( 14 )*/);
[+-------------------------+------------------------------+----------+----------+---------+------+-----------------------------------+-----------------------------------+-----------------------------+];

  end;

  if( rs.value(13) == NULLVAL )
    CCurStr = "";
  else
    CCurStr = SfRep_GetCurrencyShortName( rs.value( 13 ));
  end;

[|                         |##############################|##########|##########|#########|  ### |###################################|###################################|#############################|] 
  ( rs.value( 15 ), BeginDate, EndDate, Amount, CCurStr, Debet, Credit, Comment:w );
[+-------------------------+------------------------------+----------+----------+---------+------+-----------------------------------+-----------------------------------+-----------------------------+];

end;

private macro MakeSqlQuery( Department ) : string

  var sqlString : string;

  sqlString = "SELECT " +
              " sfra.t_SfDefComID, " +
              " sfra.t_BeginDate, " +
              " sfra.t_EndDate, " +
              " sfra.t_TransactionDate, " +
              " sfra.t_Amount, " +
              " sfra.t_InvoiceID, " +
              " sfra.t_Debit, " +
              " sfra.t_Credit, " +
              " sfra.t_Comment, " +
              " sfra.t_ContrID, " +              
              " sfra.t_FeeType, " +
              " sfra.t_ComissNumber, " +
              " sfra.t_Department, " +               
              " sfdc.t_FIID_CommSum, " +
              " sfc.t_Object, " +
              " sfcm.t_Code, sfc.t_number, sfc.t_id " +
              "FROM dsfrepacc_tmp sfra, dsfdefcom_dbt sfdc, dsfcontr_dbt sfc, dsfcomiss_dbt sfcm " +
              " WHERE sfra.t_Kind = 0 AND sfdc.t_ID (+) = sfra.t_SfDefComID AND sfc.t_ID = sfra.t_ContrID " + 
              " AND   sfcm.t_FeeType = sfra.t_FeeType AND sfcm.t_Number = sfra.t_ComissNumber " +
              " AND (sfra.t_Amount <> 0 OR sfra.t_ErrorCode <> 0) " +
              " ORDER BY sfra.t_Department, sfc.t_Object, sfcm.t_Code, sfra.t_BeginDate ";


  return sqlString;        

end;

private macro PrintContext( AllDprt, Dprt )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;
  
  strSql = MakeSqlQuery();  

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())
    PrintAccrue( AllDprt, Dprt, rs );
  end;

end;

macro PrintReport( AllDprt, Dprt, Attr, Comiss, EndDate, TrnDate, bPay )
  PrintHeader( bPay );

  SfRep_PrintTop();

  PrintParm( Dprt, Attr, Comiss, EndDate, TrnDate, bPay );

  PrintContext( AllDprt, Dprt );
end;