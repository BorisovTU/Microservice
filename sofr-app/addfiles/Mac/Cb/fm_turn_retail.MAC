/*
  RS-Bank 6.0
  RS-Retail
  115-ФЗ "Контроль доходов, полученных преступным путем"
  Анкета клиентов

  Подсчет оборотов по клиенту
 Функция RSRetail_TurnForClient возвращает модулю "Финансовый мониторинг"
 оброты по заданному клиенту, найденные по операциям RS-Retail
------------------------------------------------------------
  Входные параметры функции RSRetail_TurnForClient
 - PartyID - идентификатор клиента
 - Year    - год для расчета оборотов
 - Quarter - квартал для расчета оборотов (1,2,3.4)
             Обороты считаются по заданному клиенту за
             заданный квартал заданного года.
  Выходные параметры функции RSRetail_TurnForClient
 - Number_I - количество операций типа "И" (приходных)
 - Summa_I  - общая сумма операций типа "И" (приходных)
 - Number_O - количество операций типа "О" (расходных)
 - Summa_O  - общая сумма операций типа "О" (расходных)
  Функция возвращает код возврата. 0 - без ошибки

  Ромодин Александр Васильевич
  08.08.2005
*/
Import "opercode.mac",FIinter,rsd;

//private var SBDEPDOC = TBFile ("sbdepdoc.dbt", "w", 2);
//private var DEPOSITR = TBFile ("depositr.dbt", "w", 4);
// *********************************************************

macro ToCodeFI (Code_Currency)
//
// Перевод валюты в коде Retail
// в код финансового инструмента Ядва
//
  var fiid = 0;
  var cmd = RsdCommand( "select t_fiid as fiid from dfininstr_dbt " +
                         "where t_fi_code = "+
                         "(select t_externalcode from dcurrency_dbt "+
                         "where t_code_currency = ?)");
  var rs = RsdRecordset (cmd);
  cmd.addParam ("Code_Currency", RSDBP_IN);
  cmd.value ("Code_Currency" ) = Code_Currency;

  cmd.execute;
  if (rs.moveNext)
    fiid = rs.value ("fiid");
  end;
  return fiid;
end;
// *********************************************************

macro MakeDepDoc (Number_I, Summa_I,
                  Number_O, Summa_O
                  ,inSBDEPDOC_ApplicationKey
                  ,inSBDEPDOC_InSum
                  ,inSBDEPDOC_OutSum
                  ,inSBDEPDOC_TypeOper
                  ,inSBDEPDOC_ApplType
                  ,inSBDEPDOC_Code_Currency
                  ,inSBDEPDOC_DepDate_Document
                  );
//
// Добавление оборотов по документу
//
  var stat = 0;
  var CreditSumCUR:moneyl = 0,
      CreditSumRUR:moneyl = 0,
      CreditNum    = 0,
      DebetNum     = 0,
      DebetSumCUR:moneyl  = 0,
      DebetSumRUR:moneyl  = 0;
  var fiid;
  var tm = null;

  if( inSBDEPDOC_ApplicationKey != "" )
    tm = Time( int( substr( inSBDEPDOC_ApplicationKey, 11, 2 ) ),
               int( substr( inSBDEPDOC_ApplicationKey, 13, 2 ) ),
               int( substr( inSBDEPDOC_ApplicationKey, 15, 2 ) ) );
  end;

  if ((inSBDEPDOC_InSum != 0) OR (inSBDEPDOC_OutSum != 0))
    if ( (inSBDEPDOC_TypeOper != Зачисление_Процентов)
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != Безналичная_Комиссия))
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != Налог_Нерезидентов))
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != Налог_Ставки_Рефинансирования))
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != 16))
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != 17))
     AND ((inSBDEPDOC_TypeOper != Списание_Простое) OR (inSBDEPDOC_ApplType != 18))
     AND (inSBDEPDOC_TypeOper != Переплата_Процентов)
     AND (inSBDEPDOC_TypeOper != Недоплата_Процентов) )
      if (inSBDEPDOC_InSum != 0)
        if (inSBDEPDOC_InSum > 0)
          CreditNum    = CreditNum    + 1;
          CreditSumCUR = CreditSumCUR + inSBDEPDOC_InSum;
        else
          DebetNum    = DebetNum    + 1;
          DebetSumCUR = DebetSumCUR - inSBDEPDOC_InSum;
        end;
      end;
      if (inSBDEPDOC_OutSum != 0)
        if (inSBDEPDOC_OutSum > 0)
          DebetNum    = DebetNum    + 1;
          DebetSumCUR = DebetSumCUR + inSBDEPDOC_OutSum;
        else
          CreditNum    = CreditNum    + 1;
          CreditSumCUR = CreditSumCUR - inSBDEPDOC_OutSum;
        end;
      end;
      if (inSBDEPDOC_Code_Currency > 0)
        fiid = ToCodeFI (inSBDEPDOC_Code_Currency);
        if (fiid != 0)
          stat = ConvSum (CreditSumRUR, CreditSumCUR,
                          inSBDEPDOC_DepDate_Document,
                          fiid);
          if (stat == 0)
            stat = ConvSum (DebetSumRUR, DebetSumCUR,
                            inSBDEPDOC_DepDate_Document,
                            fiid);
          end;
        else
          stat = 1;
//          MsgBox("stat = 1, fiid=",fiid,"  для inSBDEPDOC_Code_Currency=",inSBDEPDOC_Code_Currency);
        end;
      else
        CreditSumRUR = CreditSumCUR;
        DebetSumRUR  = DebetSumCUR;
      end;
//    Заполнение результата
      Number_I = Number_I + CreditNum;
      Summa_I  = Summa_I  + CreditSumRUR;
      Number_O = Number_O + DebetNum;
      Summa_O  = Summa_O  + DebetSumRUR;
    end;
  end;
  SetParm (0, Number_I);
  SetParm (1, Summa_I);
  SetParm (2, Number_O);
  SetParm (3, Summa_O);
  return stat;
end;
// ------------------------------------------------------

macro CalcTurn (PartyID,
                BeginDate,EndDate,
                Number_I, Summa_I,
                Number_O, Summa_O)
//
//  Расчет оборотов по клиенту
//
  var stat = 0;
  var st, st2, st3;

  //объединённый запрос
  var cmdSelect = RsdCommand;
  var rsSelect;
  var sql="";

  Number_I = 0;
  Summa_I  = 0;
  Number_O = 0;
  Summa_O  = 0;

  //scr#89737
  sql =
  " SELECT * FROM dsbdepdoc_dbt "+
  " WHERE t_Referenc IN "+
  " ("+
  "         SELECT t_Referenc FROM ddepositr_dbt"+
  "         WHERE t_CodClient = "+ string(PartyID)+ //--PartyID
  " )"+
  " AND"+
  " (         t_DepDate_Document BETWEEN "+
  "       TO_DATE( '" + string(BeginDate) + "','dd.mm.yyyy' )"+ //--BeginDate
  "       AND "+
  "       TO_DATE( '" + string(EndDate) + "','dd.mm.yyyy' )"+  //--EndDate
  " ) ";

  cmdSelect.CmdText = sql;
  
//  msgbox("fm_trun_retail.mac объединённый запрос:",sql);

  cmdSelect.execute;
  rsSelect = RsdRecordset( cmdSelect );

  while( rsSelect.moveNext )
      st = MakeDepDoc (Number_I, Summa_I,
                       Number_O, Summa_O
                       ,rsSelect.Value("t_ApplicationKey")
                       ,rsSelect.Value("t_InSum")
                       ,rsSelect.Value("t_OutSum")
                       ,rsSelect.Value("t_TypeOper")
                       ,rsSelect.Value("t_ApplType")
                       ,rsSelect.Value("t_Code_Currency")
                       ,rsSelect.Value("t_DepDate_Document")
                       );


      if ((st != 0) AND (stat == 0))
        stat = st;
      end;
  end;

  SetParm (3, Number_I);
  SetParm (4, Summa_I);
  SetParm (5, Number_O);
  SetParm (6, Summa_O);
  return stat;
end;
// *********************************************************

macro DefineLimitsOfPeriod (Year,Quarter,BeginDate,EndDate)
//
// Определение границ периода по номеру квартала
//
  if (Quarter == 1)
    BeginDate = Date(1,1,Year);
    EndDate   = Date(31,3,Year);
  elif (Quarter == 2)
    BeginDate = Date(1,4,Year);
    EndDate   = Date(30,6,Year);
  elif (Quarter == 3)
    BeginDate = Date(1,7,Year);
    EndDate   = Date(30,9,Year);
  elif (Quarter == 4)
    BeginDate = Date(1,10,Year);
    EndDate   = Date(31,12,Year);
  else
    BeginDate = Date(0,0,0);
    EndDate   = Date(0,0,0);
  end;
  SetParm (2,BeginDate);
  SetParm (3,EndDate);
end;


// ***********************************************************
// ##### Точка входа #########################################
// ***********************************************************

macro RSRetail_TurnForClient
(         // ВХОД
 PartyID,   // Идентификатор клиента
 Year,      // Год для расчета оборотов (4-значный)
 Quarter,   // Номер квартала для расчета оборотов (1,2,3.4)
          // Выход
 Number_I,  // Количество операций типа "И" (приходных)
 Summa_I,   // Общая сумма операций типа "И" (приходных)
 Number_O,  // Количество операций типа "О" (расходных)
 Summa_O    // Общая сумма операций типа "О" (расходных)
)
  var BeginDate,
      EndDate;
  var stat = 0;
// Определение границ периода расчета оборотов
  DefineLimitsOfPeriod (Year,Quarter,BeginDate,EndDate);
// Обработка клиента
  stat = CalcTurn (PartyID,
                   BeginDate,EndDate,
                   Number_I, Summa_I,
                   Number_O, Summa_O);

  SetParm (3,Number_I);
  SetParm (4,Summa_I);
  SetParm (5,Number_O);
  SetParm (6,Summa_O);

  return stat;
end;
/* ----- Конец файла ----- */
