/*
$Name: ic_getaccclient.mac
$Module: Ядро Banking
$Description: Сервис "Запрос сведений по счетам клиентов"
*/

import globals, PaymInter, "bnk_common.mac", "ic_findclient.mac", "ws_lib_fun.mac";

//Сумма претензий ареста по счету
private macro GetSumArrest(account:string, chapter:integer, Code_Currency:integer, DateReq:date )

  var Sum = $0;
  var PlanRest = $0;
  var paramsFullArest = TArray;

  paramsFullArest[0] = SQLParam("p_Account", account);
  paramsFullArest[1] = SQLParam("p_Chapter", chapter );
  paramsFullArest[2] = SQLParam("p_FIID", Code_Currency );
  paramsFullArest[3] = SQLParam("p_BankDate", DateReq );

  if(execStoredFunc( "RSB_ACCLAIM.IsAccountFullyArrested", V_INTEGER, paramsFullArest ))
    if(CB_GetAccountRest(chapter, account, Code_Currency, DateReq, Sum, PlanRest))
      RunError("Ошибка получения остатка по счету " + account + " за дату " + DateReq);
    end;
  else
    var paramsSumArrestClaim = TArray;

    paramsSumArrestClaim[0] = SQLParam("p_Account", account);
    paramsSumArrestClaim[1] = SQLParam("p_Chapter", chapter );
    paramsSumArrestClaim[2] = SQLParam("p_FIID", Code_Currency );
    paramsSumArrestClaim[3] = SQLParam("p_BankDate", {curdate});
    paramsSumArrestClaim[4] = SQLParam("p_Priority", -1);
    paramsSumArrestClaim[5] = SQLParam("p_Sum", V_DOUBLE, RSDBP_OUT);

    if(execStoredFunc( "RSB_ACCLAIM.GetSumArrestClaim", V_INTEGER, paramsSumArrestClaim ) )
      RunError("Ошибка получения суммы претензий ареста по счету " + account + " за дату " + {curdate});
    end;

    Sum = paramsSumArrestClaim.Value(5).value;

  end;

  return Sum;

end;

//Определение финансовых сведений по счету

private macro GetSummsArestAndRest(account:string, chapter:integer, Code_Currency:integer, DateReq:date, SummaArestVal:@money, RealRestVal:@money)
  var ShowSummaArest = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\ShowSummaArest", V_INTEGER, 1);

  SummaArestVal = GetSumArrest(account, chapter, Code_Currency, DateReq );

  var paramsSumKORAndK2 = TArray;
  var PlanRest = $0;

  if(ShowSummaArest == 1)
    var paramsFullArest = TArray;

    paramsFullArest[0] = SQLParam("p_Account", account);
    paramsFullArest[1] = SQLParam("p_Chapter", chapter );
    paramsFullArest[2] = SQLParam("p_FIID", Code_Currency );
    paramsFullArest[3] = SQLParam("p_BankDate", DateReq );

    if(execStoredFunc( "RSB_ACCLAIM.IsAccountFullyArrested", V_INTEGER, paramsFullArest ))
      if(CB_GetAccountRest(chapter, account, Code_Currency, DateReq, SummaArestVal, PlanRest))
        RunError("Ошибка получения остатка по счету " + account + " за дату " + DateReq);
      end;
    else
      paramsSumKORAndK2 = TArray;  

      paramsSumKORAndK2[0] = SQLParam("p_Account", account);
      paramsSumKORAndK2[1] = SQLParam("p_FIID", Code_Currency );            
      paramsSumKORAndK2[2] = SQLParam("p_Date", DateReq );
      paramsSumKORAndK2[3] = SQLParam("p_Req", "X" );
      paramsSumKORAndK2[4] = SQLParam("p_iAmount", V_DOUBLE, RSDBP_OUT);

      if(execStoredFunc( "PM_RESTFUN.GetSumKORAndK2", V_INTEGER, paramsSumKORAndK2 ) == 1)
        SummaArestVal = SummaArestVal + paramsSumKORAndK2.Value(4).value;
      end;

    end;
  end;

  var ShowRealRest = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\ShowRealRest", V_INTEGER, 1);

  var paramsFreeAmount = TArray;

  paramsFreeAmount[0] = SQLParam("p_FreeAmount", V_DOUBLE, RSDBP_OUT);
  paramsFreeAmount[1] = SQLParam("p_FreeLimitAmount", V_DOUBLE, RSDBP_OUT);
  paramsFreeAmount[2] = SQLParam("p_Account", account );            
  paramsFreeAmount[3] = SQLParam("p_Chapter", chapter );
  paramsFreeAmount[4] = SQLParam("p_FIID", Code_Currency );
  paramsFreeAmount[5] = SQLParam("p_BankDate", DateReq );
  paramsFreeAmount[6] = SQLParam("p_Priority", 0 );
  paramsFreeAmount[7] = SQLParam("p_SkipArrest", 0 );
  paramsFreeAmount[8] = SQLParam("p_ClaimID", 0 );
  paramsFreeAmount[9] = SQLParam("p_ReasonID", V_INTEGER, RSDBP_OUT);

  execStoredFunc("RSI_RsbAccTransaction.AccGetFreeAmount", V_UNDEF, paramsFreeAmount);

  RealRestVal = paramsFreeAmount.Value(0).value;

  if((ShowRealRest == 1) or (ShowRealRest == 2))
    paramsSumKORAndK2 = TArray;  

    paramsSumKORAndK2[0] = SQLParam("p_Account", account);
    paramsSumKORAndK2[1] = SQLParam("p_FIID", Code_Currency );            
    paramsSumKORAndK2[2] = SQLParam("p_Date", DateReq );
    paramsSumKORAndK2[3] = SQLParam("p_Req", IfThenElse(ShowRealRest == 1, "X", "") );
    paramsSumKORAndK2[4] = SQLParam("p_iAmount", V_DOUBLE, RSDBP_OUT);

    if(execStoredFunc( "PM_RESTFUN.GetSumKORAndK2", V_INTEGER, paramsSumKORAndK2 ) == 1)
      RealRestVal = RealRestVal - paramsSumKORAndK2.Value(4).value;
    end;
  end;

  if(RealRestVal < $0)
    RealRestVal = $0;
  end;

  if(SummaArestVal < $0)
    SummaArestVal = $0;
  end;

end;

// Определение кода с учетом иерархии

macro GetPartCode(PartyID:integer, CodeKind:integer)
    var paramsPartCode = TArray;  

    paramsPartCode[0] = SQLParam("p_PartyID", PartyID);
    paramsPartCode[1] = SQLParam("p_CodeKind", CodeKind );            
    paramsPartCode[2] = SQLParam("p_Code", V_STRING, RSDBP_OUT );
    paramsPartCode[3] = SQLParam("p_CodeOwnerID", V_INTEGER, RSDBP_OUT );

    if(not execStoredFunc("RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, paramsPartCode) )
      return paramsPartCode.Value(2).value;
    else
      return "";
    end;    
end;

private const ACCCLAIM:integer = 0;
private const PSPOREQUEST:integer = 1;

class Splitter( stringForSplit:string, dlm:string )
  var splittedStrings:TArray = TArray;
  var currentIdx = 0;
  var delimiter = dlm;

  macro Reset(stringForSplit:string, delimiter:string)

    splittedStrings = TArray;

    if( ValType(delimiter) == V_UNDEF )
      delimiter = " ";
    end;

    if( ValType(stringForSplit) != V_UNDEF )

      var idx = 1;

      while(Index(stringForSplit, delimiter, idx + 1))
        var oldIdx = idx;
        idx = Index(stringForSplit, delimiter, idx + 1) + strlen(delimiter);        
        splittedStrings(splittedStrings.size()) = SubStr(stringForSplit, oldIdx, idx - oldIdx - strlen(delimiter) );

        if(idx == strlen(stringForSplit) + strlen(delimiter))
          break;
        end;
      end;

      if(idx == 1)
        splittedStrings(0) = stringForSplit;
      elif(idx != strlen(stringForSplit) + strlen(delimiter))
        splittedStrings(splittedStrings.size()) = SubStr(stringForSplit, idx);
      end;
    end;

    currentIdx = 0;    

  end;

  macro Next()
    if( currentIdx >= splittedStrings.size() )
      return "";
    end;

    var str = splittedStrings(currentIdx);
    currentIdx = currentIdx + 1;
    return str;
  end;

  Reset(stringForSplit, delimiter);

end;

macro RemoveDelimiterFromString(stringSplitter:@Splitter)

  var temp:string = stringSplitter.Next();
  var str:string = "";

  while(temp != "")

    if(Trim(temp) != "")

      str = str + Trim(temp);     

    end;

    temp = stringSplitter.Next();

  end;
  
  if(str != "")
    str = ".*[" + str + "].*";
  end;

  return str;

end;

class TypeAccountToFSSPTypeAccount(sysTypeAccounts:string)
  var codes:TArray = TArray;
  
  macro Add(str:string)
    
    var sSplitter = Splitter(str, ";");

    var temp = sSplitter.Next();

    while(temp != "")
      temp = Trim(temp);

      if(temp != "")
        codes[codes.Size] = temp;
      end;

      temp = sSplitter.Next();
    end;
  end;

  macro Reset(sysTypeAccounts:string)

    Add(sysTypeAccounts);

  end;
  
  macro GetFSSPCode(code:string)
    
    for(var item, codes)
    
      if(item == code)
        return item;
      end;
    
    end;
    
    return "";
  end;

  Reset(sysTypeAccounts );

end;

class ReqOrganisation                                            // Параметры исходного запроса внешней АС - основание
  var           ReqOrg :                string =            "";  // Код внешней АС - источника запроса
  var       ReqOrgName :                string =            "";  // Наименование внешней АС - источника запроса
  var      InternalKey :                string =            "";  // Идентификатор исходного запроса во внешней АС
end;

class AccountRequest                                             // Входящие параметры
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var          OrigReq :       ReqOrganisation;                  // Определяет параметры исходного Запроса от внешней АС
  var           TypeID :                string =            "";  // Код вида идентификатора клиента
  var         ClientID :                string =            "";  // Идентификатор клиента
  var    ReqFindClient :             ReqClient;                  // Идентификационные реквизиты
  var          DateReq :                  date = date(0, 0, 0);  // Дата, по состоянию на которую формируются сведения
  var     TypeCloseAcc :                string =           "O";  // Признак счета
  var      TypeCurRate :                string =            "";  // Признак, по какому курсу считать
  var            NoVip :                string =            "";  // Признак не обрабатывать для VIP
  var            Twins :                TArray =        TArray;  // Список клиентов - дублеров
end;

class ContractorData                                             // Исполнитель
  var              FIO :                string =            "";  // ФИО
  var            Phone :                string =            "";  // Телефон
end;

class      AccountData                                           // Описание счета    
  var      AccountType :                string =            "";  // Тип счета
  var              Acc :                string =            "";  // Номер счета
  var      PersonalAcc :                string =            "";  // Номер субсчета
  var      AccountName :                string =            "";  // Наименование счета
  var          BicBank :                string =            "";  // БИК банка
  var         BankOGRN :                string =            "";  // ОГРН банка
  var          BankINN :                string =            "";  // ИНН банка
  var          BankKPP :                string =            "";  // КПП банка
  var   BankRegionCode :                string =            "";  // Код региона
  var      BankAddress :                string =            "";  // Адрес банка
  var         DeptCode :                string =            "";  // Филиал
  var   ContractNumber :                string =            "";  // Номер договора счета
  var ContractStartDate :                 date = date(0, 0, 0);  // Дата начала действия договора счета
  var  ContractFinDate :                  date = date(0, 0, 0);  // Дата окончания действия договора счета
  var     CurrencyCode :                string =            "";  // Валюта счета
  var         DateMove :                  date = date(0, 0, 0);  // Дата последнего движения по счету

  var         SummaVal :                 money =            $0;  // Сумма в валюте счета
  var         SummaRUB :                 money =            $0;  // Сумма в рублях
  var      RealRestVal :                 money =            $0;  // Доступный остаток в валюте счета
  var      RealRestRUB :                 money =            $0;  // Доступный остаток в рублях
  var    SummaArestVal :                 money =            $0;  // Сумма обременения в валюте
  var    SummaArestRUB :                 money =            $0;  // Сумма обременения в рублях
  var       DateChange :                  date = date(0, 0, 0);  // Дата курса
  var      BaseCurRate :                double =             0;  // Базовая валюта
  var          RubRate :                double =             0;  // Котируемая валюта
end;

//Ответ сервиса
class AccountResponse
  var            ReqID ;                                         // Уникальный идентификатор запроса
  var         SenderID :                string =            "";  // Идентификатор (код) АС, источник сведений
  var     RealDateTime :              DateTime = date(0, 0, 0);  // Дата, время актуальности сведений
  var       Contractor :        ContractorData;                  // Исполнитель
  var      CountClient :               integer =            0;   // Количество клиентов
  var         ClientID :                string =            "";  // Уникальный ИД клиента в АС
  var   ClientFullName :                string =            "";  // Наименование/ФИО
  var              Vip :                string =            "";  // Особый статус
  var         CountAcc :               integer =             0;  // Количество счетов в ответе
  var         Accounts :                TArray =        TArray;  // Список счетов
end;


macro GetAccClientIC(searchParameters)

  var ClientID:string = "";
  var Twins:TArray = TArray;
  
  var ParamN = 1, ObjectName = "";
  var TypeMandatory = false;
  
  var DateReq = {curdate};
  var TypeID;

  var response:AccountResponse;
  WS_SetAttributeValue(@response.ReqID, ParamN, ObjectName, "ReqID", searchParameters, V_STRING, true, null);
  response.SenderID = Bnk_GetRegistryValue("RS-CONNECT\\SENDERID\\Banking", V_STRING, "");  
  response.Contractor.FIO = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\FSSPSignatureName", V_STRING, "");
  response.Contractor.Phone = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\FSSPSignaturePhone", V_STRING, "");
  response.CountClient = 1;
  
  WS_SetAttributeValue(@TypeID, ParamN, ObjectName, "TypeID", searchParameters, V_STRING, false, null);
  if( ValType(TypeID) != V_UNDEF )
    TypeMandatory = true;
  else
    TypeMandatory = false;
  end;
  
  WS_SetAttributeValue(@response.ClientID, ParamN, ObjectName, "ClientID", searchParameters, V_STRING, TypeMandatory, null);
  
  WS_SetAttributeValue(@DateReq, ParamN, ObjectName, "DateReq", searchParameters, V_DATE, false, null);
  if( DateReq == null )
    DateReq = {curdate};
  end;
    
  //response.ClientID = searchParameters.ClientID;

  if(searchParameters.ClientID != "")
    var fullNameQuery = 
                        " SELECT CASE " +
                        " WHEN party.t_LegalForm = :PTLEGF_INST THEN party.t_Name " +
                        " ELSE persn.t_Name1 || ' ' || persn.t_Name2 || ' ' || persn.t_Name3 " +
                        " END " +
                        " AS FullName " +
                        " FROM dparty_dbt party " +
                        " LEFT OUTER JOIN dpersn_dbt persn ON persn.t_PersonID = party.t_PartyID " +
                        " WHERE party.t_PartyID = :PartyID ";

    var fnRS = execSQLselect(fullNameQuery, makeArray(SQLParam("PTLEGF_INST", PTLEGF_INST), SQLParam("PartyID", searchParameters.ClientID )));

    if(fnRS and (fnRS.MoveNext()))
      response.ClientFullName = fnRS.value("FullName");
    end;

  end;

  if((searchParameters.ClientID == "") and (searchParameters.Twins.Size == 0) )
    var request:ClientRequest;
    request.ReqFindClient = searchParameters.ReqFindClient;
    var resp = GetClientListIK(request);

    response.ClientID = resp.ClientID;
    response.CountClient = resp.CountRezult;
    response.ClientFullName = resp.ClientFullName;

    if(resp.CountRezult > 0)
      ClientID = resp.ClientID;
      Twins = resp.Twins;
    end;
  else
    ClientID = searchParameters.ClientID;
    Twins = searchParameters.Twins;
  end;

  if((ClientID != "") or (Twins.Size > 0))
    var query = 
       " SELECT accoun.t_Type_Account AS SysTypeAccount, " +
       " accoun.t_UserTypeAccount AS UserTypeAccount, " +
       " accoun.t_AccountID AS AccountID, " +
       " party.t_LegalForm AS LegalForm, " +
       " accoun.t_Account AS Acc, " +
       " accoun.t_NameAccount AS AccountName, " +
       " dp_dep.t_PartyID AS Department, " +
       " NVL(objcode.t_Code, CHR(1)) AS OGRN, " +
       " NVL(REGEXP_SUBSTR (objcodeINN.t_Code, '[^/]+'), CHR(1)) AS INN, " +
       " NVL(REGEXP_SUBSTR (objcodeINN.t_Code, '/(.+)',1,1,NULL,1), CHR(1)) AS KPP, " +
       " NVL(adress.t_REGIONNUM, CHR(1)) AS LCodeRegion, " +
       " NVL(adress_p.t_REGIONNUM, CHR(1)) AS PCodeRegion, " +
       " NVL(LTRIM(regexp_substr(objcodeREGNUM.t_Code, '/(.+)', " +
       "              1, " +
       "              1, " +
       "              NULL, " +
       "              1), '0'), CHR(1)) AS DeptCode, " +
       " CASE " +
       "    WHEN adress_p.t_Country <> CHR (1) AND adress_p.t_Country IS NOT NULL " +
       "    THEN " +
       "       NVL ( " +
       "             adress.t_Country " +
       "          || ',' " +
       "          || adress.t_PostIndex " +
       "          || ',' " +
       "          || adress.t_Region " +
       "          || ',' " +
       "          || adress.t_Province " +
       "          || ',' " +
       "          || adress.t_District " +
       "          || ',' " +
       "          || adress.t_Place " +
       "          || ' ' " +
       "          || adress.t_CodePlace " +
       "          || ',' " +
       "          || adress.t_Street " +
       "          || ' ' " +
       "          || adress.t_CodeStreet " +
       "          || ',' " +
       "          || adress.t_House " +
       "          || ',' " +
       "          || adress.t_NumCorps " +
       "          || ',' " +
       "          || adress.t_Flat " +
       "          || '.', " +
       "          CHR(1)) " +
       "    ELSE " +
       "       NVL(adress_p.t_adress, CHR(1)) " +
       " END " +
       "    AS BankAddress, " +
       " NVL (sfcontr.t_Number, CHR(1)) AS ContractNumber, " +
       " accoun.t_Open_Date AS ContractStartDate, " +
       " accoun.t_Close_Date AS ContractFinDate, " +
       " NVL(fininstr.t_Iso_Number, CHR(1)) AS CurrencyCode, " +
       " CASE " +
       "    WHEN accoun.t_Final_date <= :FinalDate " +
       "    THEN " +
       "       accoun.t_Final_date " +
       "    ELSE " +
       "       NVL((SELECT MAX (restdate.t_restdate) " +
       "          FROM drestdate_dbt restdate " +
       "         WHERE     restdate.t_restdate <= " +
       "                      :RestDate " +
       "               AND restdate.t_accountID = accoun.t_AccountID), :ZeroDate) " +
       " END " +
       "    AS DateMove, " +
       " accoun.t_Chapter AS Chapter,"
       " accoun.t_Code_Currency AS FIID"
       " FROM daccount_dbt accoun " +
      "   INNER JOIN dparty_dbt party ON party.t_PartyID = accoun.t_Client " +
      "   INNER JOIN ddp_dep_dbt dp_dep ON dp_dep.t_Code = accoun.t_Department " +
      "   LEFT OUTER JOIN dadress_dbt adress " +
      "       ON     adress.t_PartyID = dp_dep.t_PartyID " +
      "       AND adress.t_Type = :PTADDR_LEGAL " +
      "   LEFT OUTER JOIN dadress_dbt adress_p " +
      "       ON     adress.t_PartyID = dp_dep.t_PartyID " +
      "       AND adress.t_Type = :PTADDR_POST " +
      "   LEFT OUTER JOIN dsfcontr_dbt sfcontr " +
      "       ON     sfcontr.t_ServKind = :PTSK_PAY " +
      "       AND sfcontr.t_ObjectType = :SF_ACCOUNT " +
      "       AND sfcontr.t_FIID = accoun.t_code_Currency " +
      "       AND sfcontr.t_Object = accoun.t_Account " +
      "   LEFT OUTER JOIN dfininstr_dbt fininstr " +
      "       ON fininstr.t_FIID = accoun.t_code_Currency  " +
      "   LEFT OUTER JOIN dobjcode_dbt objcode  " +
      "       ON     objcode.t_ObjectID = dp_dep.t_PartyID  " +
      "       AND objcode.t_CodeKInD = :PTCK_OGRN  " +
      "       AND objcode.t_State = 0  " +
      "   LEFT OUTER JOIN dobjcode_dbt objcodeINN  " +
      "       ON     objcodeINN.t_ObjectID = dp_dep.t_PartyID  " +
      "       AND objcodeINN.t_CodeKInD = :PTCK_INN  " +
      "       AND objcodeINN.t_State = 0  " +
      "   LEFT OUTER JOIN dobjcode_dbt objcodeREGNUM " +
      "       ON     objcodeREGNUM.t_ObjectID = dp_dep.t_PartyID " +
      "       AND objcodeREGNUM.t_CodeKInD = :PTCK_BANKREGNUM " +
      "       AND objcodeREGNUM.t_State = 0 " +
      " WHERE accoun.t_Client IN( ";
                
    var queryParam:TArray = TArray;
    
    queryParam[queryParam.Size] = SQLParam("FinalDate", DateReq);
    queryParam[queryParam.Size] = SQLParam("RestDate", DateReq);
    queryParam[queryParam.Size] = SQLParam("ZeroDate", date(0,0,0));
    queryParam[queryParam.Size] = SQLParam("PTADDR_LEGAL", PTADDR_LEGAL);
    queryParam[queryParam.Size] = SQLParam("PTADDR_POST", PTADDR_POST);
    queryParam[queryParam.Size] = SQLParam("PTSK_PAY", PTSK_PAY);
    queryParam[queryParam.Size] = SQLParam("SF_ACCOUNT", 1);
    queryParam[queryParam.Size] = SQLParam("PTCK_OGRN", PTCK_OGRN);
    queryParam[queryParam.Size] = SQLParam("PTCK_INN", PTCK_INN);
    queryParam[queryParam.Size] = SQLParam("PTCK_BANKREGNUM", PTCK_BANKREGNUM);

    if(ClientID != "")
      query = query + ":Client ";
      queryParam[queryParam.Size] = SQLParam("Client", int(ClientID));
    end;   

    var item;
    var idx = 0;

    if( ValType(Twins) != V_UNDEF )
      for(item, Twins)

        if(queryParam.Size != 0)
          query = query + ", ";
        end;

        query = query + ":Client" + idx;
        queryParam[queryParam.Size] = SQLParam("Client" + idx, int(item.LocalID));

        idx = idx + 1;
      end;
    end;

    query = query + ") ";

    var SysTypeAccount = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\SysTypeAccount", V_STRING, "");
    var UserTypeAccount = Bnk_GetRegistryValue("RS-CONNECT\\ФССП\\BANKING\\USERTYPEACCOUNT", V_STRING, "");

    var typeAccountSplitter = Splitter(SysTypeAccount, "," );
    var params:TArray = TArray;

    var sysTypeAccountCondition = RemoveDelimiterFromString(typeAccountSplitter);

    if(sysTypeAccountCondition != "")
      queryParam[queryParam.Size] = SQLParam("sysTypeAccountCondition", sysTypeAccountCondition);
      sysTypeAccountCondition = " REGEXP_LIKE( accoun.t_Type_Account, :sysTypeAccountCondition ) ";
    end;

    typeAccountSplitter.reset(UserTypeAccount, "," );

    var userTypeAccountCondition = RemoveDelimiterFromString(typeAccountSplitter);

    if(userTypeAccountCondition != "")      
      queryParam[queryParam.Size] = SQLParam("userTypeAccountCondition", userTypeAccountCondition);
      userTypeAccountCondition = " REGEXP_LIKE( accoun.t_UserTypeAccount, :userTypeAccountCondition ) ";
    end;

    if((userTypeAccountCondition != "") and (sysTypeAccountCondition != ""))
      query = query + " AND ( " + sysTypeAccountCondition + " OR " + userTypeAccountCondition + " ) ";
    elif(sysTypeAccountCondition != "")
      query = query + " AND " + sysTypeAccountCondition;
    elif(userTypeAccountCondition != "")
      query = query + " AND " + userTypeAccountCondition;
    end;

    if(searchParameters.TypeCloseAcc == "O")
      query = query + " AND accoun.t_Open_Date <= :DateReqO AND ( accoun.t_Close_Date >= :DateReqC OR accoun.t_Close_Date = :ZerodateC ) ";
      queryParam[queryParam.Size] = SQLParam("DateReqO", DateReq);
      queryParam[queryParam.Size] = SQLParam("DateReqC", DateReq);
      queryParam[queryParam.Size] = SQLParam("ZeroDateO", date(0,0,0));
    elif(searchParameters.TypeCloseAcc == "C")
      query = query + " AND accoun.t_Close_Date < :DateReqC AND accoun.t_Close_Date <> :ZerodateC ";
      queryParam[queryParam.Size] = SQLParam("DateReqC", DateReq);
      queryParam[queryParam.Size] = SQLParam("ZeroDateC", date(0,0,0));
    end;

    var rs = execSQLselect( query, queryParam );

    var count = 0;
    
    while(rs.MoveNext())
    
      var accData = AccountData();
      
      if( (accData.AccountType == "") and (CompareStrWithMasks(SysTypeAccount, rs.value("SysTypeAccount")) == 0) )
        accData.AccountType = rs.value("SysTypeAccount");
      end;      

      if( (accData.AccountType == "") and (CompareStrWithMasks(UserTypeAccount, rs.value("UserTypeAccount")) == 0) ) 
        accData.AccountType = rs.value("UserTypeAccount");
      end;
      
      if(accData.AccountType == "")
        if(rs.value("LegalForm") == PTLEGF_INST)
          if(rs.value("FIID") == NATCUR)
            accData.AccountType = "Ч";
          else
            accData.AccountType = "X";
          end;
        elif(rs.value("LegalForm") == PTLEGF_PERSN)
          accData.AccountType = "W";
        end;
      end;
      
      accData.Acc = rs.value("Acc");
      accData.AccountName = rs.value("AccountName");
      accData.BicBank = GetPartCode(rs.value("Department"), PTCK_BIC);
      accData.BankOGRN = rs.value("OGRN");
      accData.BankINN = rs.value("INN");
      accData.BankKPP = rs.value("KPP");
      accData.BankRegionCode = rs.value("LCodeRegion");
      accData.DeptCode = rs.value("DeptCode");
      
      if(accData.BankRegionCode == "")
        accData.BankRegionCode = rs.value("PCodeRegion");
      end;

      accData.BankAddress = rs.value("BankAddress");
      
      accData.ContractNumber = rs.value("ContractNumber");
      accData.ContractStartDate = rs.value("ContractStartDate");
      
      if(rs.value("ContractFinDate") != date(0,0,0))
        accData.ContractFinDate = rs.value("ContractFinDate");
      end;
      
      accData.CurrencyCode = rs.value("CurrencyCode");
      accData.DateMove = rs.value("DateMove");
      
      accData.SummaVal = GetRestAByID(rs.value("AccountID"), DateReq, rs.value("FIID"));
      accData.SummaRub = GetRestAByID(rs.value("AccountID"), DateReq, NATCUR);
      
      GetSummsArestAndRest(rs.value("Acc"), rs.value("Chapter"), rs.value("FIID"), DateReq, @accData.SummaArestVal, @accData.RealRestVal);
      
      var ConvSum2Param = makeArray(SQLParam("SumB", accData.RealRestVal),
                                    SQLParam("pFromFI", rs.value("FIID")),
                                    SQLParam("pToFI", NATCUR),
                                    SQLParam("pbdate", DateReq),
                                    SQLParam("pRound", 0),
                                    SQLParam("pRateType", 0, RSDBP_IN_OUT),
                                    SQLParam("pRate", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pScale", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pPoint", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pIsInverse", V_STRING, RSDBP_OUT));
      
      accData.RealRestRUB = execStoredFunc("RSI_RSB_FIInstr.ConvSum2", V_DOUBLE, ConvSum2Param);
      
      ConvSum2Param = makeArray(SQLParam("SumB", accData.SummaArestVal),
                                    SQLParam("pFromFI", rs.value("FIID")),
                                    SQLParam("pToFI", NATCUR),
                                    SQLParam("pbdate", DateReq),
                                    SQLParam("pRound", 0),
                                    SQLParam("pRateType", 0, RSDBP_IN_OUT),
                                    SQLParam("pRate", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pScale", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pPoint", V_DOUBLE, RSDBP_OUT),
                                    SQLParam("pIsInverse", V_STRING, RSDBP_OUT));
                                                                                           
      accData.SummaArestRUB = execStoredFunc("RSI_RSB_FIInstr.ConvSum2", V_DOUBLE, ConvSum2Param);                                                                                   
      accData.DateChange = DateReq;
      
      if(rs.value("FIID") != NATCUR)
        if(ConvSum2Param.Value(9).value == "X")
          accData.BaseCurRate = pow( 10, -ConvSum2Param.Value(8).value ) * ConvSum2Param.Value(6).value;
          accData.RubRate = ConvSum2Param.Value(7).value;
        else
          accData.BaseCurRate = ConvSum2Param.Value(7).value;
          accData.RubRate = pow( 10, -ConvSum2Param.Value(8).value ) * ConvSum2Param.Value(6).value;
        end;
      end;
      
      response.Accounts[response.Accounts.Size] = accData;      
    
      count = count + 1;
    end;

    response.CountAcc = count;
    response.RealDateTime = DtTm(Date(), Time());

  end; 
  
  return response;  
end;
