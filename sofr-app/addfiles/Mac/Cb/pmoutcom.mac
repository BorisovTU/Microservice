/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.10          */
/*                 Copyright (c) R-Style Software Lab 1999              */
/*                                                                      */
/*  Имя файла        : pmoutcom.mac                                     */
/*                                                                      */
/*  Описание         : Единовременные комиссии исходящего платежа       */
/*  Программист      : Бабин А.П.                                       */
/*                                                                      */
/*  Создан           : 27.03.02                                         */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */
import  "pmtlscom.mac", InsCarryDoc;

/*                    КОМИССИЯ НАШЕГО БАНКА ЗА НАШ СЧЕТ                       */
/*               (сумма платежа)                                              */
/*         Клиент        ->      Транзитный счет     (проводка)               */
/*               (сумма комиссии)                                             */
/*         Клиент        ->      Счет доходов банка  (платеж комиссии)        */
/*                                                                            */
/*                    КОМИССИЯ НАШЕГО БАНКА ЗА СЧЕТ БЕНЕФИЦИАРА               */
/*          сумма платежа = сумма платежа - сумма комиссии                    */
/*                 (сумма платежа)                                            */
/*         Клиент          ->      Транзитный счет     (проводка)             */
/*                (сумма комиссии)                                            */
/*         Клиент          ->      Счет доходов банка  (платеж комиссии)      */
/*                                                                            */
/*                   КОМИССИЯ КОРРЕСПОНДЕНТА ЗА НАШ СЧЕТ                      */
/*         сумма платежа = сумма платежа + сумма комиссии                     */
/*                                                                            */
/*                   КОМИССИЯ КОРРЕСПОНДЕНТА ЗА НАШ БЕНЕФИЦИАРА               */
/*         дополнительных действий с нашей стороны не производится            */

/*  Макрос обработки комиссий */     
macro ExecuteSingCommission( ID_Operation, /* Операция */ 
                         ps,           /* Буфер первички (должен быть уже установлен) */
                         Kind,         /* Вид первички */
                         PrimPmpaym,   /* Буфер платежа */
                         PrimPmprop,   /* Буфер свойств платежа */
                         ComissCharges,

                         /*    Следующие параметры будут расчитаны здесь    */
                         PaySumPaym, /*  Сумма платежа в валюте платежа     */
                         PaySumDoc,  /*  Сумма проводки в валюте платажа    */
                         SumPaym,    /*  Сумма платежа в валюте актива      */
                         SumDoc )    /*  Сумма проводки в валюте актива     */

  RECORD combuf( oprsfcom );  

  PaySumPaym = PrimPmpaym.PayAmount;
  PaySumDoc  = PaySumPaym;
  SumPaym    = PrimPmpaym.Amount;
  SumDoc     = SumPaym;

  var b_continue;

  /*                    КОМИССИЯ НАШЕГО БАНКА                               */
  ClearRecord(combuf); 
  /* Перебираем комиссии нашего банка */
  b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  while( b_continue )
     if ( ComissCharges==BEN )  /* За наш счет */
        /* Наши расходы за счет бенефициара - уменьшаем сумму платежа */
        /* Сумму проводки на транзитный тоже уменьшаем             */
        if ( PrimPmpaym.PayFIID == combuf.FIID_Sum )
            PaySumPaym = PaySumPaym - combuf.Sum - combuf.sumNDS;
            PaySumDoc = PaySumDoc - combuf.Sum - combuf.sumNDS;
        else
            /* Пересчитываем сумму */
            PaySumPaym = PaySumPaym - GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
            PaySumDoc = PaySumDoc - GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
        end;
        if ( PrimPmpaym.FIID == combuf.FIID_Sum )
            SumPaym = SumPaym - combuf.Sum - combuf.sumNDS;
            SumDoc = SumDoc - combuf.Sum - combuf.sumNDS;
        else
            /* Пересчитываем сумму */
            SumPaym = SumPaym - GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
            SumDoc = SumDoc - GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
        end;
     end;
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, OWN, combuf );
  end;
  
  /*                   КОМИССИЯ КОРРЕСПОНДЕНТА                              */
  ClearRecord(combuf); 
  /* Перебираем комиссии корреспондента */
  b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, CORRESP, combuf );
  while( b_continue )
     if ( ComissCharges==OUR )  /* За наш счет */
        /* Увеличиваем сумму плаетежа и проводки */
        if ( PrimPmpaym.PayFIID == combuf.FIID_Sum )
            PaySumPaym = PaySumPaym + combuf.Sum + combuf.sumNDS;
            PaySumDoc = PaySumDoc + combuf.Sum + combuf.sumNDS;
        else
            /* Пересчитываем сумму */
            PaySumPaym = PaySumPaym + GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
            PaySumDoc = PaySumDoc + GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.PayFIID);
        end;
        if ( PrimPmpaym.FIID == combuf.FIID_Sum )
            SumPaym = SumPaym + combuf.Sum+combuf.sumNDS;
            SumDoc  = SumDoc  + combuf.Sum+combuf.sumNDS;
        else
            /* Пересчитываем сумму */
            SumPaym = SumPaym + GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
            SumDoc  = SumDoc  + GetSumConvert(combuf.Sum+combuf.sumNDS, combuf.FIID_Sum, PrimPmpaym.FIID);
        end;
     end;
     b_continue = GetCommission( ID_Operation, ID_CURRENT_STEP, CORRESP, combuf );
  end;

  if ( (PaySumPaym<$0) OR (SumPaym<$0) )
     msgBox("Сумма комиссии больше суммы платежа");
     return 1;
  end;

  /*                  Обновляем сумму платежа в реальной базе                */
  if( (PrimPmpaym.Amount    != SumPaym) or
      (PrimPmpaym.PayAmount != PaySumPaym) )
    if ( not UpdateSumRealPayment( PrimPmpaym.PaymentID, PaySumPaym, SumPaym ) )
      msgBox( "Ошибка обновления платежа" );
      return 1;
    end;
  end;

  SetParm( 7,  PaySumPaym );
  SetParm( 8,  PaySumDoc );
  SetParm( 9,  SumPaym );
  SetParm( 10, SumDoc );

  return 0;

  OnError(er) /* Обработка ошибок времени выполнения */
    MsgBox(String(er.Message, " Строка: ", er.Line));
    return 1;
end;