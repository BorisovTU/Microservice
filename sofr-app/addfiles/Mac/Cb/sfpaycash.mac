/*
$Name:        sfpaycash.mac
$Module:      РКО (ПЗО)  
$Description: Формирование приходного кассового ордера
*/





import sfpay_lib;

macro SfCheckCashAccountType( Account, FIID, IsCashType:@bool )
  
  record AccRec ("account");

  IsCashType = false;
  if( SfCommon_GetAccount(AccRec, Account, FIID, 1) == false )
    MsgBox( "Не найден счет " + Account + " в валюте " + FIID + "." );
    return SFPAY_ERROR_COMMON;
  else
    if( index(AccRec.Type_Account, "А") > 0 )
      IsCashType = true;
    end;
  end;

  return 0;
end;

macro SfCheckCashDocParams( sicredit, sidebet )

  var IsCashType;
  var stat = 0;

  if( sicredit.Rec.FIID != sidebet.Rec.FIID )
    stat = SFPAY_ERROR_CASHORDER_DIFFERCURRENCIES;
  else
    stat = SfCheckCashAccountType( sicredit.Rec.Account, sicredit.Rec.FIID, @IsCashType );
    if( stat == 0 )
      if( IsCashType )
        stat = SFPAY_ERROR_BOTHACCOUNTS_HAS_CASHTYPE;
      elif( sicredit.rec.Department == 0 )
        stat = SFPAY_ERROR_RECEIVERACC_NOTAINOURBANK_NOCABS;
      end;      
    end;
  end;

  return stat;
end;

macro SfFormCashOrderExt( sidebet, sicredit, sfcomiss, payParams:TSfPayParams, IsBatchMode, oprchild )
  
  var CashOrder = GenObject( "RsbBBIncCashOrder", 0 );
  CashOrder.Oper       = {oper};  
  CashOrder.LaunchOper = false;

  var Payment = CashOrder.Payment;
  Payment.DocKind = CashOrder.DocKind;
  Payment.Purpose  = PM_PURP_CASHBAL; //14
  Payment.CashSymbolDebet = "32";
  Payment.CashSymbolCredit = "32";

  //валюты счета получателя и счета плательщика должны быть в одной валюте
  //корректируем сумму, если разные валюта счетов и валюта оплаты  
  if( sidebet.Rec.FIID != payParams.payFIID )
    if( ConvSum(payParams.paySum, payParams.paySum, payParams.PayDate, payParams.payFIID, sidebet.Rec.FIID) != 0 )
      payParams.SetError(SFPAY_ERROR_COMMON, "Ошибка конвертации суммы");
      MsgBox( "Ошибка конвертации суммы" ); // ??
      return SFPAY_ERROR_COMMON;
    end;

    if( ConvSum(payParams.taxSum, payParams.taxSum, payParams.PayDate, payParams.payFIID, sidebet.Rec.FIID, 0) != 0 )
      payParams.SetError(SFPAY_ERROR_COMMON, "Ошибка конвертации суммы");
      MsgBox( "Ошибка конвертации суммы" );
      return SFPAY_ERROR_COMMON;
    end;
  end;

  payParams.payFIID = sidebet.Rec.FIID;
  if( SfFillPaymentExt(Payment, sidebet, sicredit, sfcomiss, payParams) ) 
    return SFPAY_ERROR_COMMON; 
  end;

  Payment.Ground = payParams.ground;
  Payment.FeeType = payParams.feeType;
  if( payParams.objectID != null )
    Payment.DefComID = payParams.objectID;
  end;

  SfPayDocConnectToOperation( CashOrder, IsBatchMode, oprchild, payParams );

  //Payment.Actuate();

  return 0;
end;

macro SfFormCashOrder( sidebet, sicredit, sfcomiss, PayDate, ground, paySum, taxSum, payFIID,
                       PlusCalc_Account, PlusCalcNDS_Account, feeType, DefComID,
                       IsBatchMode, ID_Operation, ID_Step, ChildDocKind:@integer, ChildDocID:@string )
  
  var payParams = TSfPayParams;
  
  payParams.payDate = payDate;
  payParams.paySum = paySum; 
  payParams.taxSum = taxSum;
  payParams.payFIID = payFIID;  

  payParams.PlusCalc_Account = PlusCalc_Account;
  payParams.PlusCalcNDS_Account = PlusCalcNDS_Account;
  
  payParams.feeType = feeType;
  payParams.objectID = DefComID;

  return SfFormCashOrderExt( sidebet, sicredit, sfcomiss, payParams,
                             IsBatchMode, ID_Operation, ID_Step, @ChildDocKind, @ChildDocID );
end;

