/*
$Name: estimdoc.mac
$Module: Ядро ГКБО
$Description: Макрос для определения счетов нереализованых курсовых разниц
*/

/*
 * Макрос для определения счетов нереализованых курсовых разниц
 */
Import CTInter, FIInter, rsd, RsbDataSet;
Import "rsb_const.mac";
Import "ex_rate_lib.mac";


/* определить вид финансового инструмент */
private macro GetFIKind( Currency : integer ) : integer

  var FIKind : integer;
  var rs = RsdRecordset( "SELECT rsi_rsb_fiinstr.fi_getrealfikind(t_fiid) AS fikind FROM dfininstr_dbt WHERE t_FIID = " + string(Currency) );

  FIKind = -1;

  if( rs.moveNext() )

    FIKind = rs.value(0);

  end;

    if( FIKind == FIKIND_CURRENCY ) FIKind = 1;
  elif( FIKind == FIKIND_METAL    ) FIKind = 2;
  end;

  return FIKind;

end;

/* определить вид финансового инструмент */
private macro GetSideBalance( conn_account : TRecHandler ) : integer
  
  var SideBalance : integer;

  SideBalance = 2;

  if( Trim(StrUpr(conn_account.rec.Kind_Account)) == "А" )
    SideBalance = 1;
  end;

  return SideBalance;

end;


class PDocOverEstimate( _Chapter : integer, _Currency : integer, _Account : string, _conn_account )

  var Chapter  : integer;
  var Currency : integer;
  var Account  : string;

  var conn_account : TRecHandler;

  var FI_Kind    : integer;
  var ParentFIID : integer;
  var FI_Name    : string; 
  var LSIN       : string; 

  var error : integer;

  /* корректировка счета */
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    if(((categ.rec.Code == "+ПереоценкаА") or (categ.rec.Code == "-ПереоценкаА")) and (ParentFIID > 0))
      account.rec.nameaccount = "Изменение в капитале от переоценки средств в ин.валюте по облиг. "+FI_Name+", гос.рег.№ "+LSIN;
    end;

    return true;
  end;

  /* определение FIRole */
  macro GetBasisFIRole( FIRole : integer ) : integer
    return FIROLE_UNDEF;
  end;

  /* определение параметров второго рода */
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole ) : integer
    
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_FIID )
      if(ParentFIID > 0)
        Parametr = ParentFIID;
      else
        Parametr = Currency;
      end;
    elif( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = conn_account.rec.Department;
    end;

    return Parametr;

  end;

  /* определение параметров первого рода */
  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole ) : integer
    
    var Parametr = -1;

    if( Classificator == LLCLASS_OVERVALUE_ACC )

      Parametr = conn_account.rec.ORScheme; /* схема переоценки */

    elif( (Classificator == LLCLASS_FI_KIND) or (Classificator == LLCLASS_KIND_AKT_BANK) )

      if(FI_Kind == FIKIND_AVOIRISS)
        Parametr = 3;
      else
        Parametr = GetFIKind( Currency );
      end;

    elif( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND )

      Parametr = GetSideBalance( conn_account );

    elif( Classificator == LLCLASS_SYMBOLNUMBERS )

      if( OperDate >= DateBegin446P )
        Parametr = 0;
      end;

    elif( Classificator == LLCLASS_CODE_FI )

      if( OperDate >= DateBeginМСФО )
        Parametr = 0;
      else
        Parametr = ExRate_GetParameter_CodeFI( Currency );
      end;
    elif( Classificator == 5065 )
      if(substr(Account, 1, 1) == "5")
        Parametr = 1;
      else
        Parametr = 2;
      end;
    end;

    return Parametr;

  end;

  private macro LoadParentFI()
    var query, cmd, DataSet;

    query =   " with q as (select mcd.t_FIID"
            + "              from dmcaccdoc_dbt mcd"
            + "             where mcd.t_Account = ? "
            + "               and mcd.t_Chapter = ? "
            + "               and mcd.t_Currency = ? "
            + "               and mcd.t_IsCommon = 'X' "
            + "               and ROWNUM = 1"
            + "           )"
            + " select fin.t_FIID, fin.t_FI_Kind, fin.t_Name, avr.t_LSIN "
            + "   from q, dfininstr_dbt fin, davoiriss_dbt avr "
            + "  where fin.t_FIID = q.t_FIID "
            + "    and avr.t_FIID = fin.t_FIID "
            + "    and avr.t_Subordinated = 'X' "
            + "    and RSB_SECUR.GetMainObjAttrNoDate("+OBJTYPE_AVOIRISS+", LPAD(avr.t_FIID, 10, '0'), "+OBJGROUP_SUBORDCVALTYPE+") = " + OBJATTR_SUBORDCVALTYPE_DI;

    cmd = RSDCommand(query); 

    cmd.addParam( "", RSDBP_IN, Account );
    cmd.addParam( "", RSDBP_IN, Chapter );
    cmd.addParam( "", RSDBP_IN, Currency );
    
    cmd.Execute();

    DataSet = TRsbDataSet(cmd);

    if( DataSet.moveNext())
      ParentFIID = DataSet.FIID;
      FI_Kind    = int(DataSet.FI_Kind);
      FI_Name    = DataSet.Name;
      LSIN       = DataSet.LSIN; 
    end;
  end;

  
/* конструктор */  
  error = 0;

  Chapter  = _Chapter ;
  Currency = _Currency;
  Account  = _Account ;

  conn_account = _conn_account;

  ParentFIID = -1;
  FI_Kind    = 0;
  FI_Name    = "";
  LSIN       = "";
  if(substr(Account,1,2) == "52")
    LoadParentFI();
  end;
  
end;
