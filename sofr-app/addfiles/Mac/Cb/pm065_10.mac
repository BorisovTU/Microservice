 /*
 $Name:        pm065_10.mac
 $Module:      Ядро Banking
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок      : 29065 - "Отказ в исполнении поручения на списание"
// Шаг       : 10    - "Отказ в исполнении поручения на списание"
// Описание  : Макрос шага
//-----------------------------------------------------------------------------

import MesInter, pm_tools, wltools, WldInter, "pm_answerret.mac";

var PaymentObj:RsbPayment;

// ----------------------------------------------------------------------------
// Выполнение шага
// ----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )

  var DenialGround = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
  var ErrorNumber = "";
  /* Связанное входящее сообщение платежа */
  record wlmes( wlmes );
  if( not ПолучитьСообщение( PaymentObj.PaymentID, OBJTYPE_PAYMENT, 1/*WLD_MES_IN*/, wlmes, NULL ) )
    ClearRecord( wlmes );
  end;

  if( int( SubStr( DenialGround, 1, Index( DenialGround, ";" ) - 1 ) ) >= 100 )
    if( not IsOprMultiExec() )
      if( RsbGetTrue( False, False,"Причина ошибки, указанная в примечании платежа, не может служить основанием для отказа в исполнении поручения.|Продолжить?") == False )
        return 1;
      end;
    end;
    /* Заменим код ошибки >= 100 на 35 */
    if( GetMesFormatType(wlmes.MesID) == WLD_FORMAT_XML )
      ErrorNumber         = "31";
    else
      ErrorNumber         = "35";
    end;
    PaymentObj.Notes.DelNote( PM_NOTEKIND_DENIALGROUND );
    PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, ErrorNumber + SubStr( DenialGround, Index( DenialGround, ";" ) ), {curdate} );
    DenialGround = PaymentObj.Notes.ReadNote( PM_NOTEKIND_DENIALGROUND, {curdate} );
  end;

  var ErrList = RsbWlError(0);

  var wlerr_buf = TRecHandler("wlerror.dbt");
  wlerr_buf.rec.Code         = SubStr( DenialGround, 1, Index( DenialGround, ";" ) - 1 );
  wlerr_buf.rec.Description  = SubStr( DenialGround, Index( DenialGround, ";" ) + 1 );

  ErrList.Insert( wlerr_buf );
  
  /* Параметры создаваемого подтверждения банка */
  var ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;
  var ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode); 

  if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList ) )
    msgbox( "Ошибка при вставке подтверждения банка" );
    return 1;
  end;

  return 0;
end;