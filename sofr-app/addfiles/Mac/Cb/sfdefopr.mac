import SfInter, BankInter, OprInter, RsbDataSet;

class SfDefOpr
  var SfDef = TRecHandler("sfdef.dbt");
  var OldStatus = 0;
  var OldIsIncluded = "";
  var Error = 0;
  var oprID_Operation = 0;
  var oprID_Step      = 0;
  var IsNDSSumReset  = false;  // Признак сброса суммы НДС: устанавливается если сумма НДС комиссии в валюте УК (SfDef.rec.SumNDS) не равно 0, 
                               // а ее эквивалент в валюте оплаты/начисления равен 0
  var IsPayFromCFT:bool = false; //Признак списания комиссии с текущего счета от ЦФТ
  var AccountCFT:string = ""; //Текущий счет списания от ЦФТ
  var DebtSumCFT:money = 0; //Сумма списания с текущего счеат от ЦФТ                                                        
end;


class SfDefOprStatus
  var SfDefID         = 0;
  var FeeType         = 0;
  var Status          = 0;
  var OldStatus       = 0;
  var oprID_Operation = 0;
  var oprID_Step      = 0;
  var DatePay:date    = date(0,0,0); /* DEF-56737*/
end;

macro UpdateSfDefStatus( DefOprArray:TArray ) // массив SfDefOprStatus
  var stat = 0;
  var StatusArray     = TArray;
  var DefIDArray      = TArray;
  var FeeTypeArray    = TArray;
  var DatePayArray    = TArray;  /* DEF-56737*/

  var i = 0;     
  var sizea = 0;
  while(i < DefOprArray.size)
    if(DefOprArray[i] != NULL)
      if((DefOprArray[i].Status != DefOprArray[i].OldStatus))
        StatusArray[i]     = DefOprArray[i].Status;
        DefIDArray[i]      = DefOprArray[i].SfDefID;
        FeeTypeArray[i]    = DefOprArray[i].FeeType;
        DatePayArray[i]    = DefOprArray[i].DatePay;   /* DEF-56737*/

        // Привяжем статус к операции.   
        // sprintf( DocID,"%010d%05d%010d%05d%02d", (int)sfParm->SfDefComID, (int)sfParm->OldStatus, (int)sfParm->ID_Operation, (int)sfParm->ID_Step, (int)sfParm->FeeType );
        var StatDocID = String(DefOprArray[i].SfDefID:10)+String(DefOprArray[i].OldStatus:5)+String(DefOprArray[i].oprID_Operation:10)+String(DefOprArray[i].oprID_Step:5)+String(DefOprArray[i].FeeType:2);
        StatDocID = StrSubst( StatDocID, " ", "0" );
        AddDocumentToStepMass(2001, StatDocID, DefOprArray[i].oprID_Operation, DefOprArray[i].oprID_Step); // SFDDEFCOM_CHANGE_STATUS = 2001

        sizea = sizea + 1;
      end;
    end;
    i = i + 1;
  end;

  if(sizea > 0)
    var strSql = "UPDATE dsfdef_dbt SET t_Status = ?, t_DatePay = ? WHERE t_FeeType = ? and t_id = ?" ;  /* DEF-56737*/

    var upd = RsbSQLInsert(strSql, 4, sizea );
    upd.AddParam( V_INTEGER, StatusArray   );
    upd.AddParam( V_DATE   , DatePayArray  );  /* DEF-56737*/
    upd.AddParam( V_INTEGER, FeeTypeArray  );
    upd.AddParam( V_INTEGER, DefIDArray    );

    if(not upd.Insert())
      stat = 1;
    end;
  end;

  return stat;
end;

macro UpdateSfDefStatusSfOpr( sfdefArray:TArray ) // массив SfDefOpr
  var DefOprArray = TArray;
  var stat = 0;
  var i = 0;     

  while(i < sfdefArray.size)
    if(sfdefArray[i] != NULL)
      if((sfdefArray[i].Error == 0) AND (sfdefArray[i].SfDef.rec.Status != sfdefArray[i].OldStatus))

        var sfdef = SfDefOprStatus();

        sfdef.SfDefID          = sfdefArray[i].SfDef.rec.ID;
        sfdef.FeeType          = sfdefArray[i].SfDef.rec.FeeType;
        sfdef.Status           = sfdefArray[i].SfDef.rec.Status;
        sfdef.OldStatus        = sfdefArray[i].OldStatus;
        sfdef.oprID_Operation  = sfdefArray[i].oprID_Operation;
        sfdef.oprID_Step       = sfdefArray[i].oprID_Step;
        sfdef.DatePay          = sfdefArray[i].SfDef.rec.DatePay; /*DEF-56637*/    

        DefOprArray[DefOprArray.size()] = sfdef;
      end;
    end;
    i = i + 1;
  end;

  if(DefOprArray.size() > 0)
    stat = UpdateSfDefStatus( DefOprArray );
  end;

  return stat;
end;