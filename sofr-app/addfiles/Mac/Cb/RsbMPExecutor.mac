/*
$Name:           RsbMPExecutor.mac
$Module:         Ядро ГКБО
$Description:    Реализация класса RslMPExecutor
*/
import rcw;

private var ax;

class RslMPExecutor(url:string, maxThreads:integer)

  private var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  private var executor = jvm.createJavaObject("ru.softlab.core.mpe.Executor", "<init>@(Ljava/lang/String;I)V",
      url, maxThreads);


  // добавляет задание в очередь
  macro Add(request:string, timeOut:integer, priority:integer)
    if (timeOut == null)
      timeOut = 0;
    end;

    if (priority == null)
      priority = 0;
    end;

    executor.addRequest(request, timeOut, priority);
  end;

  // добавляет задание в очередь с приоритезацией запросов
  macro AddRequestWithConvPriority(request:string, timeOut:integer, priority:integer, convPriority: integer)
    if (timeOut == null)
      timeOut = 0;
    end;

    if (priority == null)
      priority = 0;
    end;

    if (convPriority == null)
      convPriority = 0;
    end;

    executor.addRequestWithConvPriority(request, timeOut, priority, convPriority);
  end;

  // запускает выполнение всех заданий из очереди
  macro Run()
    executor.run();
  end;


  // выполняет проверку окончания выполнения
  macro IsFinish()
    return executor.isFinish();
  end;


  // приостановка выполнения заданий
  macro Pause()
    executor.pause();
  end;


  // полностью "обнуляет" класс и готовит его для новой процедуры распараллеливания
  macro Reset()
    executor.reset();
  end;


  // очищает очередь заданий
  macro clearQueue()
    executor.clearQueue();
  end;


  // Сколько пакетов находится в очереди на выполнение
  macro GetWaitingTaskCount()
    return executor.getWaitingTaskCount();
  end;


  // Сколько сервисов выполнено успешно
  macro GetSuccessTaskCount()
    return executor.getSuccessTaskCount();
  end;


  // Сколько сервисов выполнено с ошибкой
  macro GetFaultTaskCount()
    return executor.getFaultTaskCount();
  end;


  // Сколько сервисов в работе
  macro GetWorkingTaskCount()
    return executor.getWorkingTaskCount();
  end;

  
  // Получить спсок ошибок
  macro GetFaultTasks( clearList )
     var i = 0;
     var ArrErr = TArray;
     var RetStr = executor.getErrorStrFirst();
     while (RetStr != "")
        ArrErr[i] = RetStr;
        RetStr = executor.getErrorStrNext();
        i = i + 1;
     end;

     if(clearList == true)
       executor.clearErrList();
     end;

     return ArrErr;
  end;

  macro Destructor
    if(executor != NULL)
      executor.reset();
    end;
  end;

end;