/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                           R-Style Software Lab


  Печать мемориального ордера для "Казначейства"

CREATED :
FILE_NAME : TR_MO.MAC
MODIFICATIONS:
NOTES:
└───────────────────────────────────────────────────────────────────────────*/

IMPORT globals;

RECORD d( document );

FILE officer ( officer ) key 2;
FILE persn   ( persn   );
FILE person  ( person  );
FILE account ( account );
FILE fininstr( fininstr) key 0;


VAR NumCopy = 0;

CONST _SET_CHAR_ = "X",
      POINT    = "." ;
      /*natcur   =  0;*/

MACRO PrintMoForTreasury( doc )

  /* Подготовка строк к печати */
  ARRAY SG;
  VAR ЛицевойПоДебету, ЛицевойПоКредиту, dateDoc, БуквенныйКодВалюты,
      Главбух, ncopy, КодВалюты;

  setbuff( d, doc );

  if( NumCopy == 0 )
    NumCopy = 1;
    if ( not GetInt(NumCopy,"Введите количество копий.",3))
      exit(1);
    end;
  end;

  ncopy = NumCopy;

  /*Буквенный код валюты*/
  fininstr.FIID = d.Code_Currency;
  /*fininstr.FIID = natcur;*/
  if( GetEQ(fininstr) ) /*по дебету*/
      КодВалюты = fininstr.FI_Code;
      БуквенныйКодВалюты = fininstr.Ccy;
  else
      msgbox("Не найдена валюта");
      exit(1);
  end;

  /*Основание*/
  strsplit( d.Ground, SG, 74,64,3 );

  /*Счета*/
  account.Chapter = d.Chapter;
  account.Account = d.Account_Payer;
  if( GetEQ(account) ) /*по дебету*/
      ЛицевойПоДебету = account.Account;
  else
      msgbox("Не найден счет плательщика: " + d.Account_Payer );
      exit(1);
  end;

  account.Account = d.Account_Receiver;
  if( GetEQ(account) ) /*по кредиту*/
      ЛицевойПоКредиту = account.Account;
  else
      msgbox("Не найден счет получателя: " + d.Account_Receiver);
      exit(1);
  end;

  /*ФИО главбуха*/
  officer.PartyID = {OurBank};
  officer.IsSecondPerson = _SET_CHAR_;
  if( GetEQ(officer) )
      persn.PersonID = officer.personID;
      if( GetEQ(persn) )
          Главбух = persn.Name1 + " " +
                    SubStr(persn.Name2, 1, 1) + POINT +
                    SubStr(persn.Name3, 1, 1) + POINT;
      else
          msgbox("Не найден главный бухгалтер");
          /*exit(1);*/
      end;

  else
      msgbox("Не найден главный бухгалтер");
      /*exit(1);*/
  end;

  /*Имя операциониста*/
  person.Oper = {oper};
  if( GetEQ(person) == FALSE )
      msgbox("Не найден операционист.");
      /*exit(1);*/
  end;

  if( d.Date_Carry > date(0, 0, 0) )
    dateDoc = d.Date_Carry;
  else
    dateDoc = {curdate};       /* Документ текущего дня */
  end;

  while( ncopy != 0 )

[#############################################################################
 Мемориальный ордер (проводка) № #########################
 ╓────────────────────────────────────────────────────────────────────────────╖
 ║ дата проводки               сумма                           валюта         ║
 ║ #######################     ############################    ### (###)      ║
 ║                                                                            ║
 ║ кореспонденция счетов                                                      ║
 ║                                                                            ║
 ║                                                                            ║
 ║ дебет  #########################                                           ║
 ║ кредит #########################                                           ║
 ╙────────────────────────────────────────────────────────────────────────────╜
   Основание:################################################################
   ##########################################################################
   ##########################################################################


   Главный бухгалтер______________  ####################################

   Исполнитель      ______________  ####################################

   #######################

](  {Name_Bank}:l,
    d.Numb_Document:l,
    dateDoc:m:l,
    d.Sum:a:l,
    КодВалюты,
    БуквенныйКодВалюты,
    ЛицевойПоДебету,
    ЛицевойПоКредиту,
    SG(0), SG(1),SG(2),
    Главбух:l,
    person.Name:l,
    {curdate}:m:l
 );

    ncopy = ncopy - 1;

  end;

END;


/*eof*/