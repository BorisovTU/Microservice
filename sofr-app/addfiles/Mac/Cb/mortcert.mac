/*
$Name:             mortcert.mac
$Module:           Депозитарий
$Description:      Отчет "ИК закладной"
*/

import FIInter, CTInter, "or_rep_h.mac", cb_sql, "globals.mac";
import RsbDataSet;

record invcard("invcard.dbt");
record mort("mortgage");
var vficert;

var _Excel = 0;

var CertRep = CMakeReport();
const FontStyleFields = "ex_FS(bi):ex_FN(Arial):ex_FZ(10)";
const FontStyleText =   "ex_FN(Arial):ex_FZ(10)";

private macro GetNameAlg( TypeAlg, NumberAlg)
  file na("namealg");

  na.iTypeAlg   = TypeAlg;
  na.iNumberAlg = NumberAlg;
  if( GetEQ(na))
    return na.szNameAlg;
  else
    return "";
  end;

  return "";
end;

private macro ПолучитьНазваниеСтраны(Code)
   var Query, select;
   select = RSDCommand("select t_Name " +
                        " from dcountry_dbt " +
                        "where t_CodeLat3 = ? ");
   select.addParam( "", RSDBP_IN, Code );
   select.execute();
   Query = TRsbDataSet(select);
   if( Query.MoveNext() )
      return Query.Name;
   else
      return "";
   end;
end;

private macro PrintAmountHistory()
   var DataSet, Select;
   var FirstTime;
   Select = RSDCommand("select mp.t_DrawingDate t_Date, " +
                       "       cast(? /*1*/ - (SELECT SUM(mp2.t_IncomeVolume) " +
                       "                         FROM dmortpay_dbt mp2 " +
                       "                        WHERE mp2.t_mortgageid = mp.t_mortgageid " +
                       "                          AND mp2.t_IsClosed = mp.t_IsClosed " +
                       "                          AND mp2.t_DrawingDate <= mp.t_DrawingDate) as NUMBER(32,12)) t_Rest " +
                       "  from dmortpay_dbt mp " +
                       " where mp.t_Mortgageid = ? /*2*/ " +
                       "   and mp.t_IsClosed = 'X' " +
                       " order by t_Date asc ");

   Select.addParam( "Amount", RSDBP_IN, mort.Amount ); /*1*/
   Select.addParam( "Mortgageid", RSDBP_IN, mort.Mortgageid );
   Select.Execute();
   DataSet = TRsbDataSet(Select);

   FirstTime = true;
   while( DataSet.MoveNext() )
     if(FirstTime)
       CertRep.AddPrintCell( "7б. История изменения:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
       CertRep.AddPrintCell( "Дата", 10, 0, "c:" + FontStyleText, REP_ELEM_STR );
       CertRep.AddPrintCell( "Сумма:", 25, 0, "c:" + FontStyleText, REP_ELEM_STR );
       CertRep.AddStr();
       CertRep.AddPrintCell( "", 20, 0, "c:" + FontStyleFields, REP_ELEM_STR );
       CertRep.AddPrintCell( date(vficert.issueDate), 10, 0, "c:" + FontStyleFields, REP_ELEM_STR );
       CertRep.AddPrintCell( mort.Amount, 25, 2, "r:" + FontStyleFields, REP_ELEM_STR );
       CertRep.AddStr();
       FirstTime = false;
     end;

     CertRep.AddPrintCell( "", 20, 0, "c:" + FontStyleFields, REP_ELEM_STR );
     CertRep.AddPrintCell( date(DataSet.Date), 10, 0, "c:" + FontStyleFields, REP_ELEM_STR );
     CertRep.AddPrintCell( DataSet.Rest, 25, 2, "r:" + FontStyleFields, REP_ELEM_STR );
     CertRep.AddStr();

   end;

   if(NOT FirstTime)
     CertRep.AddEmptyStr();
   end;
end;

macro ПечататьИКЗакладной()

   file fin("fininstr");
   record llvalues( "llvalues.dbt" );
   var FiCode = "", FiName = "", IsElectronicText = "", MortgageIssuer = 0, IsPawnerIssuer = "", IsDebtorIssuer = "",
       СписокЗалогодателей = "", СписокКодовСтран = "", СписокНаименованийСтран = "", ЧислоДолей = 0,
       СписокДожников = "", CurrCode = "", CurrName = "", СуммаОценки = $0, PrCurrCode = "", PrCurrName = "";
   var Query, Select, RecNum;

   CertRep.AddPrintCell( "Инвентарная карточка закладной №", 80, 0, "r:" + "ex_FS(bi):ex_FN(Arial):ex_FZ(14)", REP_ELEM_STR );
   //А00 - Номер инвентарной карточки
   CertRep.AddPrintCell( invcard.Number, 20, 0, "c:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "1. Тип ценной бумаги:", 20, 0, "l:" + FontStyleText, REP_ELEM_STR );
   //А01.1 - код ФИ {закладная}
   //А01.2 - наименование ФИ
   fin.FIID = vficert.FIID;
   if( GetEQ(fin))
      FiCode = fin.Fi_Code;
      FiName = fin.Name;
   end;
   CertRep.AddPrintCell( FiCode, 9, 0, "w:c:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( " ", 1, 0, "c:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( FiName, 25, 0, "l:" + FontStyleFields, REP_ELEM_STR );

   CertRep.AddPrintCell( "1а. Вид закладной:", 25, 0, "l:" + FontStyleText, REP_ELEM_STR );
   //А01.3 - 1а. Вид закладной:
   if (mort.IsElectronic == "X")
      IsElectronicText = "Электронная";
   else
      IsElectronicText = "Документарная";
   end;

   CertRep.AddPrintCell( IsElectronicText, 20, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   //А02.0, А02.1 - Залогодатель
   if( ( GetRegistryValue( "DEPO\\MORTGAGEISSUER", V_INTEGER, MortgageIssuer ) == V_INTEGER )
   and ( MortgageIssuer == 1 ) )
      IsPawnerIssuer = "(признан эмитентом)";
      IsDebtorIssuer = "";
   else
      IsPawnerIssuer = "";
      IsDebtorIssuer = "(признан эмитентом)";
   end;

   ЧислоДолей = 1;
   Select = RSDCommand("select vs.t_AgentName, pt.t_NRCountry " +
                        " from dvsircag_dbt vs, dparty_dbt pt " +
                        "where vs.t_AgentRole = 7 " + /*IRCAG_ROLE_PAWNER залогодатель*/
                        "  and vs.t_BcID = ? " +
                        "  and vs.t_Agent = pt.t_PartyID " +
                        "order by vs.t_ID ");
   Select.addParam( "", RSDBP_IN, mort.MortgageID );
   Select.execute();
   Query = TRsbDataSet( Select );
   while( Query.MoveNext() )
      ЧислоДолей = ЧислоДолей + 1;

      if( СписокЗалогодателей != "" )
         СписокЗалогодателей = СписокЗалогодателей + ", ";
      end;
      СписокЗалогодателей = СписокЗалогодателей + Query.AgentName;

      if( Query.NRCountry != "" )
         if( СписокКодовСтран != "" )
            СписокКодовСтран = СписокКодовСтран + ", ";
         end;
         СписокКодовСтран = СписокКодовСтран + Query.NRCountry;

         if( СписокНаименованийСтран != "" )
            СписокНаименованийСтран = СписокНаименованийСтран + ", ";
         end;
         СписокНаименованийСтран = СписокНаименованийСтран + ПолучитьНазваниеСтраны( Query.NRCountry );
      end;
   end;

   CertRep.AddPrintCell( "2. Залогодатель: " + IsPawnerIssuer, 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   CertRep.AddPrintCell( СписокЗалогодателей, 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "2а.Гражданство/          юрисдикция Залогодателя:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А02.2 - Цифровой код страны гражданства.....
   CertRep.AddPrintCell( СписокКодовСтран, 10, 0, "w:c:" + FontStyleFields, REP_ELEM_STR );
   //А02.3 - Название страны гражданства.....
   CertRep.AddPrintCell( СписокНаименованийСтран, 25, 0, "w:l:" + FontStyleFields, REP_ELEM_STR );

   CertRep.AddPrintCell( "2б. Количество долей в собственности:", 25, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А02.4 - Количество долей в собственности
   CertRep.AddPrintCell( ЧислоДолей, 20, 0, "c:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "3. Первоначальный залогодержатель:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А03.1 - Первоначальный залогодержатель
   CertRep.AddPrintCell( mort.OwnerShortName, 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "4. Должник: " + IsDebtorIssuer, 19, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   CertRep.AddPrintCell( " ", 1, 0, "l:" + FontStyleText, REP_ELEM_STR );
   //А04.1 - Фамилия, имя, отчество "эмитента"
   Select = RSDCommand("select vs.t_AgentName " +
                        " from dvsircag_dbt vs " +
                        "where vs.t_AgentRole = 16 " + /*IRCAG_ROLE_DEBTOR залогодатель*/
                        "  and vs.t_BcID = ? " +
                        "order by vs.t_ID ");
   Select.addParam( "", RSDBP_IN, mort.MortgageID );
   Select.execute();
   Query = TRsbDataSet( Select );
   while( Query.MoveNext() )
      if( СписокДожников != "" )
         СписокДожников = СписокДожников + ", ";
      end;
      СписокДожников = СписокДожников + Query.AgentName;
   end;
   CertRep.AddPrintCell( СписокДожников, 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "5. Государственная регистрация права:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   Select = RSDCommand("select mo.t_Lsin, mo.t_LsinDate, mo.t_RegisterShortName " +
                        " from dmortobj_dbt mo " +
                        "where mo.t_MortgageID = ? " +
                        "order by mo.t_AutoKey ");
   Select.addParam( "", RSDBP_IN, mort.MortgageID );
   Select.execute();
   Query = TRsbDataSet( Select );
   RecNum = 0;
   while( Query.MoveNext() )
      RecNum = RecNum + 1;
      if( RecNum > 1 )
         CertRep.AddPrintCell( "", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
      end;
      //А05.1, А05.2, А05.3 - данные государственной регистрации ипотеки
      CertRep.AddPrintCell( "№ " + Query.Lsin + " от " + date( Query.LsinDate ) + " (" + Query.RegisterShortName + ")", 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddStr();
   end;
   if( RecNum == 0 )
      CertRep.AddPrintCell( "№  от  ()", 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddStr();
   end;
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "6. Договор об ипотеке:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А06.1, А06.2, А06.3, А06.4 - данные по договору по ипотеке
   LL_FindLLVALUES( 1050, mort.GroundKind, llvalues );
   CertRep.AddPrintCell( llvalues.Name + " № " + mort.GroundAltXld + " от " + mort.GroundSignedDate + ", " + mort.GroundIssuePlace, 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "6а. Обязательства по договору:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А06.5 - Размер ссудной задолженности
   CertRep.AddPrintCell( mort.Amount, 10, 2, "r:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( "валюта:", 25, 0, "r:" + FontStyleText, REP_ELEM_STR );
   //А06.6, А06.7 - данные по валюте
   fin.FIID = mort.AmountFIID;
   if( GetEQ(fin))
      CurrCode = fin.Fi_Code;
      CurrName = fin.Name;
   end;
   CertRep.AddPrintCell( CurrCode, 10, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( CurrName, 35, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "6б. Срок погашения обязательств:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А06.8 - Дата окончания срока исполнения обязательства
   CertRep.AddPrintCell( date(vficert.DrawingDate), 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "6в. Местонахождение предмета ипотеки:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   Select = RSDCommand("select mo.t_Address, mo.t_Amount " +
                        " from dmortobj_dbt mo " +
                        "where mo.t_MortgageID = ? " +
                        "order by mo.t_AutoKey ");
   Select.addParam( "", RSDBP_IN, mort.MortgageID );
   Select.execute();
   Query = TRsbDataSet( Select );
   RecNum = 0;
   while( Query.MoveNext() )
      RecNum = RecNum + 1;
      if( RecNum > 1 )
         CertRep.AddPrintCell( "", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
      end;
      //А06.9 - Местонахождение имущества, на которое установлена ипотека
      CertRep.AddPrintCell( Query.Address, 80, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddStr();
   end;
   if( RecNum == 0 )
      CertRep.AddStr();
   end;
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "6г. Денежная оценка предмета ипотеки:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   Select = RSDCommand("select mo.t_Amount, fi.t_Fi_Code, fi.t_Name " +
                        " from dmortobj_dbt mo, dfininstr_dbt fi " +
                        "where mo.t_MortgageID = ? " +
                        "  and fi.t_FIID = mo.t_AmountFIID "
                        "order by mo.t_AutoKey ");
   Select.addParam( "", RSDBP_IN, mort.MortgageID );
   Select.execute();
   Query = TRsbDataSet( Select );
   RecNum = 0;
   while( Query.MoveNext() )
      RecNum = RecNum + 1;
      if( RecNum > 1 )
         CertRep.AddPrintCell( "", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
      end;
      //А06.10 - Атрибут "Сумма" агента обращения закладной вида "оценщик"
      CertRep.AddPrintCell( Query.Amount, 10, 2, "r:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddPrintCell( "валюта:", 25, 0, "r:" + FontStyleText, REP_ELEM_STR );
      //А06.11, А06.12 - данные по валюте
      CertRep.AddPrintCell( Query.Fi_Code, 10, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddPrintCell( Query.Name, 35, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddStr();
   end;
   if( RecNum == 0 )
      CertRep.AddPrintCell( "", 10, 2, "r:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddPrintCell( "валюта:", 25, 0, "r:" + FontStyleText, REP_ELEM_STR );
      CertRep.AddPrintCell( "", 45, 0, "l:" + FontStyleFields, REP_ELEM_STR );
      CertRep.AddStr();
   end;
   CertRep.AddEmptyStr();

   CertRep.AddPrintCell( "7а. Текущий остаток по ипотеке:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А07.01 - Размер текущего остатка
   CertRep.AddPrintCell( money( vficert.FaceValue ), 10, 2, "r:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( "валюта:", 25, 0, "r:" + FontStyleText, REP_ELEM_STR );
   //А07.02, А07.03 - данные по валюте
   CertRep.AddPrintCell( CurrCode, 10, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( CurrName, 35, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   PrintAmountHistory();

   CertRep.AddPrintCell( "Состояние карточки:", 20, 0, "w:l:" + FontStyleText, REP_ELEM_STR );
   //А08.01 - Код состояния ИК
   CertRep.AddPrintCell( GetNameAlg( 5211, invcard.Status ), 16, 0, "w:l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( "установлено:", 13, 0, "r:" + FontStyleText, REP_ELEM_STR );
   //А08.02 - Дата установки текущего состояния ИК
   CertRep.AddPrintCell( date( invcard.StatusDate ), 10, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddPrintCell( "ИК составлена:", 16, 0, "c:" + FontStyleText, REP_ELEM_STR );
   //А08.03 - Дата открытия инвентарной карточки
   CertRep.AddPrintCell( date( invcard.RegistrDate ), 20, 0, "l:" + FontStyleFields, REP_ELEM_STR );
   CertRep.AddStr();
   CertRep.AddEmptyStr();

   if( _Excel )
     CertRep.PrintWinRep();
     CertRep.ShowWinRep();
   else
     CertRep.PrintRep();
   end;
end;

macro ИК_Закладной( _invcard, _vficert, _mortgage, Excel )

  copy(invcard, _invcard);
  copy(mort, _mortgage);
  vficert = _vficert;

  _Excel     = Excel;

  ПечататьИКЗакладной();

  return 0;
end;
