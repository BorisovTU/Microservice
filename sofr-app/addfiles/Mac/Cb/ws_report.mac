 /*
 $Name: ws_report.mac
 $Module: Инструмент генерации отчетов для Web в пакетном режиме
 $Description: Классы обертки генерации отчетов для Web
 */

import "or_rep_h.mac";
import ws_file;
import dlwreps;

macro WebReportGeneratorIsStandAlone():Bool
  return true;
end;

macro PackWebReport(FullReportFileName : string) : FileContainer
  if (FullReportFileName != "")
      return CreateFileContainer(FullReportFileName);
  end;

  return null;
end;

class WebReportGenerator(_ReportObj)

    private var reportObj = null;

    private macro MakeFullPath(dir, name)
      if ((dir == "") or (name == ""))
        RunError("Не задано имя сгенерированного файла отчета");
      end;
      return MergeFile(dir, name);
    end;

    private macro PrintReport()
      ReplaceMacro("isStandAlone", "WebReportGeneratorIsStandAlone");

      reportObj.PrintWinRep();
      reportObj.ShowWinRep();

      ReplaceMacro("isStandAlone", NULL);
      OnError
        ReplaceMacro("isStandAlone", NULL);
        RunError;
    end;

   macro Generate(isExcel :bool, reportName) : string
    if (reportName == null)
      reportName = "WebReport";
    end;

    reportObj.SetFileName(GetUniqAdd(reportName));
    reportObj.SetFlagShowReport(false);

    // если не в excel то в html
    if (isExcel)
      reportObj.SetZoomType(ZOOM_TYPE_A4L);
    else
      reportObj.SetWinRepOutput(WINREP_OUTPUT_IE);
    end;  

    PrintReport();

    var FullReportFileName = "";
    if (isExcel)
      FullReportFileName = MakeFullPath(reportObj.ReportPath, reportObj.ReportFileName_xls);
    else
      FullReportFileName = MakeFullPath(reportObj.ReportPath, reportObj.ReportFileName_html);
    end;
    return FullReportFileName;
  end;

  macro PackReport(fullReportFileName) // : FileContainer
    return PackWebReport(FullReportFileName);;
  end;

  macro GenerateAndPack(isExcel :bool, reportName) // : FileContainer

    var fullReportFileName = Generate(isExcel, reportName);
    return PackReport(FullReportFileName);
  end;

  if (_ReportObj == null)
    RunError("Не задан объект отчета");
  end;
  reportObj = _ReportObj;
end;

class WebWordReportGenerator

  // инструмент DLWREP генерирует имя файла не с расширением .doc. При передаче в Web скорректируем расширение.
  macro PackReport(fullReportFileName) // : FileContainer
    var fc = PackWebReport(fullReportFileName);
    if (fc != null)
      fc.FileExtension = ".doc";
    end;
    return fc;
  end;

  macro GenerateFromTagFile(fullservertagfilename) : string
    var realFileName = "";

    ReplaceMacro("isStandAlone", "WebReportGeneratorIsStandAlone");

    var ok = DLWREPS_PrintReportFromTagFile(fullservertagfilename, @realFileName, true, false);

    if (not ok)
      RunError("Ошибка генерации отчета Word");
    end;

    ReplaceMacro("isStandAlone", NULL);
    return realFileName;

    OnError
      ReplaceMacro("isStandAlone", NULL);
      RunError;
  end;

  macro GenerateFromTagFileAndPack(fullservertagfilename) // : FileContainer

    var fullReportFileName = GenerateFromTagFile(fullservertagfilename);
    return PackReport(fullReportFileName);
  end;

end;
