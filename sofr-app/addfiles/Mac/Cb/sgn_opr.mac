/*
 *
 * ЭЦП. Отчет "Использование ЭЦП в разрезе операционистов"
 *
 */

Import "sgn_util.mac";

var TypeAction;

var PrevKindOperation;
var PrevNumberStep   ;
var PrevSignKind     ;
var PrevTypeAction   ;

/* Уровни отступа */
const INDENT_OPRSTEP  = 1;  /* печать шага     */
const INDENT_SIGNKIND = 2;  /* печать роли     */
const INDENT_ABONENT  = 3;  /* печать абонента */

/* Сформировать полный отступ перед строкой */
macro DefineIndent( LevelIndent )
  var i, FullIndent;
  FullIndent = "";
  i = 0;
  while( i < LevelIndent )
    FullIndent = FullIndent + Indent;
    i = i + 1;
  end;
  return FullIndent;
end;


/*
   Печать заголовка отчета
*/
macro PrintHeader( _TypeAction, _Department )
  
  TypeAction = _TypeAction;

  PrintSplit();
  println( "          Использование ЭЦП в разрезе операций" );
  println( "Действие над ЭЦП : " + GetTypeActionName( TypeAction ) );

  PrintDepartmentName( _Department );

  PrevKindOperation = 0;
  
end;

/*
   Печать данных о ключе
*/
macro PrintKeyData( _oprkoper, _oprkdsgn, _sgnaccss, _sgnkey )
  record oprkoper("oprkoper.dbt");
  record oprkdsgn("oprkdsgn.dbt");
  record sgnaccss("sgnaccss.dbt");
  record sgnkey  ("sgnkey.dbt"  );
  var ActionString, AbonentString;

  SetBuff( oprkoper, _oprkoper );
  SetBuff( oprkdsgn, _oprkdsgn );
  SetBuff( sgnaccss, _sgnaccss );
  SetBuff( sgnkey  , _sgnkey   );

  if( PrevKindOperation != oprkdsgn.Kind_Operation )
    println( "\nОперация: " + oprkdsgn.Kind_Operation + Indent + oprkoper.Name );
    
    PrevNumberStep = 0;
  end;

  if( PrevNumberStep != oprkdsgn.Number_step )
    println( DefineIndent(INDENT_OPRSTEP)
           + "Шаг: " + oprkdsgn.Number_step
           + " " + GetStepName( oprkdsgn.Kind_Operation, oprkdsgn.Number_Step ) );
    
    PrevSignKind   = 0;
    PrevTypeAction = 0;
  end;

  if( (PrevSignKind != oprkdsgn.SignKind) or (PrevTypeAction != oprkdsgn.TypeAction) )
    ActionString = DefineIndent(INDENT_SIGNKIND) + "Роль: " + GetSignKindName( sgnaccss.DocKind, sgnaccss.SignKind );
    if( TypeAction == 0 )
      ActionString = ActionString + Indent + string(GetTypeActionName( oprkdsgn.TypeAction ):9 );
    end;
  /*  if( RoleAction == 0 )
      ActionString = ActionString + Indent + string(GetRoleActionName( oprkdsgn.RoleAction ):14 );
    end;*/
    println( ActionString );
  end;

  AbonentString = DefineIndent( INDENT_ABONENT ) + string(GetPartyName( sgnkey.PartyID ):20);

  AbonentString = AbonentString + Indent + string(GetCryptoSysName(sgnkey.CryptoSysID):20) + Indent + sgnkey.KeyID;

  println( AbonentString );
  
  PrevKindOperation = oprkdsgn.Kind_Operation;
  PrevNumberStep    = oprkdsgn.Number_step   ;
  PrevSignKind      = oprkdsgn.SignKind      ;
  PrevTypeAction    = oprkdsgn.TypeAction    ;

end;

macro PrintFooter()
  PrintSplit( "\n" );
end;
