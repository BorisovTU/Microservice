/*
 $Name:        prpm_common.mac
 $Module:      ББ
 $Description: Инициализация параметров для печати ордеров по передаче ценностей
*/

// Программист: TDV 2014

import prpm, prcs, bnk_common;

const DPRT_TYPE_FILIAL = 1;  /*филиал*/
const DPRT_TYPE_VSP    = 2;     /*ВСП*/

macro getFIINameOrCYY( FIID, isName ):string
  file fiinstr(fininstr);
  if( (pr_pmpaym.rec.PayFIID != NATCUR) OR (pr_pmpaym.rec.FIID != NATCUR) )
    ClearRecord(fiinstr);
    fiinstr.FIID = FIID;
    var res = "";
    if( GetEQ(fiinstr) )
      if( isName )
        res = fiinstr.Name;
      else
        res = fiinstr.CCY;
      end;
    end;
    return res;
  else
    return "";
  end;
end;

CLASS TInOutOrderPrintData
  var Number               : string,
      DateStr              : string,
      BankName             : string,
      DebAcc               : string,
      DFI                  : string,
      DAm                  : string,
      OwnerName            : string,
      CFI                  : string,
      CredAcc              : string,
      CredAmount           : string,
      Sh                   : string,
      Content              : string,
      ValName              : string,
      Valcount             : string,
      ValSum               : string,
      Propis               : string,
      FIO                  : string,
      RegDoc               : string,
      order                : RsbValueTransOrder;

  var CCy_Code             : string,
      CCy_Name             : string;

  var RegKeyFDate          : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ФОРМАТ_ДАТЫ",          dateFormat,
      nameVal,
      RegKeySSum           : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\РАЗДЕЛИТЕЛЬ_СУММЫ",    sepSumm,
      RegKeyToDigit        : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ",toDigit;


  /*Функция для оформления с = или копейками через -*/
  macro MoneyStyle(sum:money):string          
    var newsum:string;
  if (sum !=0)
    newsum = sum;
    newsum = StrSubSt(newsum, ".00", "=");
    newsum = StrSubSt(newsum, ".", "-"); 
  else
    newsum = "";
  end;
  return newsum;
  end;

  private macro getRegVal()
    GetRegistryValue( RegKeyFDate,   V_INTEGER, dateFormat, "" );
    nameVal = 0;
    GetRegistryValue( RegKeySSum,    V_STRING,  sepSumm,    "" );
    GetRegistryValue( RegKeyToDigit, V_BOOL,    toDigit,    "" );
  end;

  private macro FillDateString( Date : date )
    return  string( SubStr( string(Date:m:f), 1, strlen(string(Date:m:f) ) - 1 ), "ода" ) ;
  end;

  private macro GetRegDoc( pmvaltr:TRecHandler ):string
  
    var strRegDoc:string = GetPaperKindName( pmvaltr.rec.PaperKind );
    if( strRegDoc )
      if( trim( pmvaltr.rec.PaperSeries ) )
        strRegDoc = strRegDoc + " серии " + trim( pmvaltr.rec.PaperSeries );
      end;
      if( trim( pmvaltr.rec.PaperNumber ) )
        strRegDoc = strRegDoc + " номер " + trim( pmvaltr.rec.PaperNumber );
      end;
      if( trim( pmvaltr.rec.PaperIssuer ) )
        strRegDoc = strRegDoc + ", выдан " + trim( pmvaltr.rec.PaperIssuer );
      end;
      if( pmvaltr.rec.PaperIssuedDate != date(0,0,0) )
        strRegDoc = strRegDoc + string( ", ", pmvaltr.rec.PaperIssuedDate, " года." );
      end;
    end;

    if( FIO == "" )
      strRegDoc = "";
    end;

    return strRegDoc;
  end;

  /*Заполнение наименования банков*/
  private macro FillBankName(Department,OperNode):string
    var cmd, RS;
    var BankNameFIL: string = "",
        BankNameVSP:  string = "";

    cmd = RSDCommand( "SELECT prt.t_Name, prt.t_ShortName, dp.t_NodeType "
                      "  FROM dparty_dbt prt, ddp_dep_dbt dp "
                      " WHERE prt.t_PartyID = dp.t_PartyID "
                      "     AND dp.t_Code IN (?,?) " ) ; 
    cmd.addParam( "", RSDBP_IN, Department );
    cmd.addParam( "", RSDBP_IN, OperNode );
    cmd.execute();
    RS = TRsbDataSet(cmd);

    while( RS.movenext() )
      if( Rs.NodeType == DPRT_TYPE_FILIAL )
        if( (Rs.Name != "") AND (strlen(Rs.Name) <= 73) )
          BankNameFIL = Rs.Name;
        else
          BankNameFIL = Rs.ShortName;
        end;
      elif (Rs.NodeType == DPRT_TYPE_VSP )
        if( (Rs.Name != "") AND (strlen(Rs.Name) <= 73) )
          BankNameVSP = Rs.Name;
        else
          BankNameVSP = Rs.ShortName;
        end;
      end;
    end;
    return (BankNameFIL + IfThenElse( BankNameVSP, ", " + BankNameVSP, "" ));
  end;

  macro SplitAmount( _Money:money, _Chapter:integer, _FIID:integer ):string

    VAR StrRub  :string = "";
    VAR StrKop  :string = "";
    VAR RetValue:string = ""; 
    var statReg;

    if( _Chapter == 5)

      var select = "SELECT fi.t_SumPrecision " +
                 "FROM dfininstr_dbt fi, davrinvst_dbt av " +
                 "WHERE av.t_FIID = :FIID AND av.t_FIID = fi.t_FIID; ";

      var prm = makeArray(SQLParam("FIID", _FIID));

      var rs = execSQLselect( select, prm, false );

      if( rs.MoveNext() )

        var precision = rs.value("t_SumPrecision");

        if ( _Money != $0 )
          if(ValType(_Money) == V_MONEY)
            StrRub = Trim(String((Floor(Double(_Money))):16:0));
            StrKop = Trim(String((pow(10, precision) * (Double(_Money) - Floor(Double(_Money)))):16:0));
            
            while(StrLen(StrKop) < precision )
              StrKop = "0" + StrKop;
            end;
      
            RetValue = StrRub;
            if( not (not toDigit and ( StrKop == "00" )) )
              RetValue = StrRub + "-" + StrKop;
            end;
          end;
        end;
      end;
    end;

    if( RetValue == "" )  
      if ( _Money != $0 )
        if(ValType(_Money) == V_MONEY)
          StrRub = Trim(String((Floor(Double(_Money))):16:0));
          StrKop = Trim(String((100.0 * (Double(_Money) - Floor(Double(_Money)))):16:0));
          if(StrLen(StrKop) < 2)
            StrKop = "0" + StrKop;
          end;
      
          RetValue = StrRub;
          if( not (not toDigit and ( StrKop == "00" )) )
            RetValue = StrRub + IfThenElse( _Chapter != 5, sepSumm, "-" ) + StrKop;
          end;
        end;
      end;
    end; 
    return RetValue;   
  end;

  /* -----------------------------------
  Проинициализировать данными мемориального ордера
  ----------------------------------- */
  MACRO InitByCashOrder( pmpaym:TRecHandler, pmrmprop:TRecHandler, pmstval:TRecHandler, pmvaltr:TRecHandler )

    Number     = pmrmprop.rec.Number;
    DateStr    = IfThenElse( dateFormat==0, string(pmrmprop.rec.Date:f), FillDateString( pmrmprop.rec.Date ) );

    BankName   = FillBankName( pmpaym.rec.Department, pmpaym.rec.OperNode );
    order      = RsbValueTransOrder( pmpaym.rec.PaymentID );

    // выводить если нет разноски иначе из расноски
    DebAcc     = pmpaym.rec.PayerAccount;
    DFI        = IfThenElse(pr_pmpaym.rec.Chapter == 5, "", pmpaym.rec.FIID );
    DAm        = SplitAmount( pmpaym.rec.Amount, pmpaym.rec.Chapter, pmpaym.rec.FIID );
    Propis     = MoneyStyle( pmpaym.rec.PayAmount );
    CFI        = pmpaym.rec.PayFIID;
    CredAcc    = pmpaym.rec.ReceiverAccount;
    CredAmount = IfThenElse(pr_pmpaym.rec.Chapter == 5, "", IfThenElse((pr_pmpaym.rec.PayFIID != pr_pmpaym.rec.FIID) or (order.PIList(PRT_Debet).size()), SplitAmount( pmpaym.rec.PayAmount ), "") );
    //
    OwnerName  = pmvaltr.rec.OwnerValues;
    Sh         = pmrmprop.rec.ShifrOper;
    Content    = pmrmprop.rec.Ground;
    ValName    = pmstval.rec.NameValue;
    Valcount   = pmstval.rec.CountValue;
    ValSum     = pmstval.rec.AmountValue;

    FIO        = pmvaltr.rec.ReceiverNameValues;

    RegDoc     = GetRegDoc( pmvaltr );

  END;

  getRegVal();
  
END;
