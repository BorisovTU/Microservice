/*
$Name:             pmlib.mac
$Module:           Платежи
$Description:      Общие функции и переменные для платежей
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2008              */
/*                                                                      */
/*  Имя файла      : pmlib.mac                                          */
/*                                                                      */
/*  Описание       : Общие функции и переменные для платежей            */
/*                                                                      */
/*  Создан         : 14.04.08                                           */
/*                                                                      */
/************************************************************************/
import oralib, likepy, BankInter, CTInter, FIInter, PTInter, globals, "bnk_dttmfrmt.mac", PaymInter, OprInter;

/* Поиск типа сообщения по номеру счета */
private macro Found_Type_Clir(Pm_paym, Rm_prop, Debet, Credit)

  file PartCode ( partcode ) key 1; /*  Код контрагента (по коду)       */
  file PartCode_Kind ( partcode ) key 0; /*  Код контрагента (по виду кода)  */

  macro fndtype(Account,BankCode,CodeKind)
    if( (CodeKind != 0) and (BankCode != "") )
      if(CodeKind != PTCK_BIC)
        /* Ищем свой БИК или вышестоящей организации */
        PartCode.CodeKind = CodeKind;
        PartCode.Code     = BankCode;
        if( not GetEQ( PartCode ) )
          BankCode = "";
        else
          PartCode_Kind.CodeKind = PTCK_BIC;
          PartCode_Kind.PartyID  = PartCode.PartyID;
          if( not GetEQ( PartCode_Kind ) ) BankCode = ""; end;
        end;
      end;
      if (substr(BankCode,strlen(BankCode)-2)=="002") /* "002" - признак полевого учереждения ЦБ */
        return("122")
      end;
    end;
    
    if (Rm_prop.Instancy and Rm_prop.NeedNotify)
      return ("139");
    elif (Rm_prop.Instancy)
      return ("137");
    elif (Rm_prop.Priority==1)
      return("195");
    elif (Rm_prop.Priority==2)
      return("196");
    elif (Rm_prop.Priority==3)
      if   (substr(Account,1,5)=="40104")
        return("199");
      elif (substr(Account,1,5)=="40402")
        return("200");
      elif (substr(Account,1,5)=="40405")
        return("201");
      elif (substr(Account,1,5)=="40403")
        return("202");
      elif (substr(Account,1,5)=="40404")
        return("202");
      else
        return("198");
      end;
    elif (Rm_prop.Priority==4)
      return("141");
    elif (Rm_prop.Priority==5)
      return("197");
    elif (Rm_prop.Priority==6)
      return("193");
    end;
    return("193");
  end;

  if(Credit.BankCode != "")
    return(fndtype(Pm_paym.ReceiverAccount,Credit.BankCode,Credit.CodeKind));
  else
    return(fndtype(Pm_paym.PayerAccount,Debet.BankCode,Debet.CodeKind));
  end;
end;

private macro MsgCreateNotInstancy(paym, rm)

  Array Text;
  Array Buttons;
                                                         /*в формате ДД.ММ.ГГ}*/
  Text(0) = "Платеж № " + rm.Number + " от " + rm.Date + " на сумму " + paym.Amount +
             " не может быть проведен как срочный."+
             " Провести его как обычный платеж?";                                                       

  Buttons(0) = " Да ";
  Buttons(1) = " Отменить "; 

  if(ConfWin(Text,Buttons) == 0)
    return true;
  end;
  return false;
end;
/* Является ли платеж внешним по заданному свойству */
private macro HavePaymProp( prop )
  if( prop == NULL )
    return false;
  end;

  if( (prop.BankCode != "") and (prop.CodeKind != 0) )
    return true;
  else
    return false;
  end;

  return false;
end;

//Проверка возможности проведения срочного платежа
macro CheckInstancyPM(paym, rm, debet, credit, wlmes)
  file Corschem(corschem) key 1;   
  record AssigneeParty(party);
  record Party(party);
  var stat = true;
  var GroupID, ObjectID;
  var params:TArray;
  var rs:RsdRecordset;
  var select;
  var PartyID;
  var PayerBankID = IfThenElse(paym.PayerBankID, paym.PayerBankID, {OurBank});
  var ReceiverBankID = IfThenElse(paym.ReceiverBankID, paym.ReceiverBankID, {OurBank});

  //если входящий или внутренний, то не проверяем
  if( ( (not HavePaymProp(credit)) and (not HavePaymProp(debet))) or (wlmes != NULL))
    return true;
  end;

  if(rm.Instancy)
  
    if( paym.ReceiverMesBankID > 0 )
      PartyID = paym.ReceiverMesBankID;
    else
      ClearRecord(Corschem);
      if(HavePaymProp(credit))
        Corschem.Number  = credit.Corschem;
        Corschem.FIID    = credit.PayFIID;
        Corschem.FI_Kind = 1;
      else
        Corschem.Number  = debet.Corschem;
        Corschem.FIID    = debet.PayFIID;      
        Corschem.FI_Kind = 1;
      end; 
      if ( GetEQ( Corschem ))
        Party.PartyID = Corschem.CorrID;
        if( GetLinkedObject( OBJROLE_PARTY_ASSIGNEE, OBJTYPE_PARTY, Party, OBJTYPE_PARTY, AssigneeParty ) == 0 )
          PartyID = AssigneeParty.PartyID;
        else
          PartyID = Corschem.CorrID;
        end;
      end;

    end;

    select = "select 1 "+
               "from dual "+
              "where exists(select 1 "+
                             "from dobjatcor_dbt oap, dobjatcor_dbt oaa, daccount_dbt acc "+ 
                            "where oap.t_ObjectType = 3 and "+ //OBJTYPE_PARTY
                                  "oap.t_Object = lpad(:PayerBankID, 10, '0') and "+
                                  "oap.t_GroupID = :GroupIDP and "+
                                  "oap.t_AttrID = 1 and "+
                                  "oap.t_ValidToDate >= :curdateP and "+
                                  "acc.t_Account = :PayerAccount and  "+
                                  "acc.T_CHAPTER = 1 and "+
                                  "acc.T_CODE_CURRENCY = :Code_Currency and "+
                                  "((oaa.t_ObjectType = 4 and "+ //OBJTYPE_ACCOUNT
                                    "oaa.t_Object = lpad(acc.T_CHAPTER, 2, '0') || lpad(acc.T_CODE_CURRENCY, 7, '0') || acc.t_Account and "+
                                    "oaa.t_GroupID = 18 and "+
                                    "oaa.t_AttrID = 1 and "+
                                    "oaa.t_ValidToDate >= :curdateA) or (acc.T_CLIENT = :OurBank))) ";  
    
    if((paym.FIID == NATCUR) and  
       (paym.PayFIID == NATCUR))
    
      GroupID = PARTY_ATTR_GROUP_INSTANCYPM;
    else
      GroupID = PARTY_ATTR_GROUP_CURINSTANCYPM;
    end;
    params = MakeArray( SQLParam("PayerBankID", PartyID),
                        SQLParam("GroupIDP", GroupID),      
                        SQLParam("curdateP", {curdate}));      
    params[params.Size] = SQLParam("PayerAccount", paym.PayerAccount);
    params[params.Size] = SQLParam("Code_Currency", paym.FIID); 
    params[params.Size] = SQLParam("curdateA", {curdate});      
    params[params.Size] = SQLParam("OurBank", {OurBank});

    rs = execSQLselect( select, params, false );
  
    if(rs and not rs.moveNext() )
        MsgBox(string("Платеж № " + rm.Number + " от " + rm.Date + " на сумму " + paym.Amount +
             " не может быть проведен как срочный."+
             " Признаки срочности и уведомления будут сняты"));

        rm.Instancy = 0;
        rm.NeedNotify = "";
        rm.MessageType = Found_Type_Clir(paym, rm, debet, credit);
        if(wlmes)
          wlmes.Importance = 0;
        end;
    end;
  end;
  
  return stat;
end;

// Формирование строки с содержанием операции
macro GetContentOperationString( Note_ContentOper: string,  // значение примечания платежа "Содержание операции"
                                 pmpaym,                    // RECORD или TRecHandler
                                 IsPrint:bool               // алгоритм вызывается для печатной формы
                               ): string

  RECORD pm(pmpaym);
  Copy(pm, pmpaym);

  var ContentOper: string = Note_ContentOper;

  if ((pm.PartPaymShifrMain == "16") and (Note_ContentOper == "ЧИПО"))
    var DateStr = "";
    if(IsPrint)
      DateStr = DDpMMpYYYY(pm.PartPaymDateMain); // DD.MM.YYYY
    else
      DateStr = YYYY_MM_DD(pm.PartPaymDateMain); // YYYY-MM-DD
    end;

    ContentOper = ContentOper + " " + DateStr;
  end;

  return ContentOper;
end;

MACRO getInitialPaymentID( PurposePaymentID : integer, LinkKind : integer ) : integer

  var InitialPaymentID:integer = 0;
  var select       :string  = "";
  var params       :TArray  = TArray();
  var rset         :object;

  select = "select pmlink.t_InitialPayment " +
           "from   dpmlink_dbt pmlink      " +
           "where  pmlink.t_PurposePayment  = :PurposePaymID " +
           "  and  pmlink.t_InitialPayment != pmlink.t_PurposePayment " +
           "  and  pmlink.t_LinkKind        = :LinkKind ";

  params = makeArray( SQLParam( "PurposePaymID" , PurposePaymentID  ),
                      SQLParam( "LinkKind", LinkKind));

  rset = execSQLselect( select, params, false );

  if( rset AND rset.moveNext() )
    InitialPaymentID = rset.Value( 0 );
  end;
  
  return InitialPaymentID;
END;

MACRO getPurposePaymentRs( InitialPaymentID:integer, LinkKind:integer ): RsdRecordset
  var select : string  = "";
  var params : TArray  = TArray();

  select = "select pmlink.t_PurposePayment " +
           "  from dpmlink_dbt pmlink      " +
           " where pmlink.t_InitialPayment  = :InitialPaymID " +
           "   and pmlink.t_LinkKind        = :LinkKind " +
           "   and pmlink.t_InitialPayment != pmlink.t_PurposePayment ";

  params = makeArray( SQLParam( "InitialPaymID", InitialPaymentID ),
                      SQLParam( "LinkKind",      LinkKind        ) );

  return execSQLselect( select, params );
END;

MACRO existsPurposePayment( InitialPaymentID:integer, LinkKind:integer ): bool
  var rs : RsdRecordset = getPurposePaymentRs(InitialPaymentID, LinkKind);
  if(rs and rs.moveNext() and (rs.value(0) > 0))
    return true;
  end;

  return false;
END;

// извлечь строку адреса из наименования плательщика (получателя) в платеже
// текст между //... //
macro GetAddressFromPaymSideName(Name : string)
  var ind1 = index(Name, "//");
  if(ind1)
    var ind2 = index(Name, "//", ind1 + 2);
    if(ind2 == 0)
      ind2 = strlen(Name) + 1;
    end;

    return substr(Name, ind1 + 2, ind2 - ind1 - 2);
  end;

  return "";
end;

// выделить из наименования плательщика (получателя) в платеже
// только наименование без адреса
macro RemoveAddressFromPaymSideName(Name : string)

  var ind_slash = Index(Name,"//");

  if ( ind_slash != 0 ) 
    return SubStr(Name, 1, ind_slash-1);
  end;

  return Name;
end;

// Составить Name//Address//
macro ConstructPaymSideName( Name : string, Address : string )
    if ( not Address )
       return Name;
    end;
    return ( Name + "//" + Address + "//" );
end;

// Найти шаг, готовый к выполнению у платежа
// Если заданы символы, то ищем только шаги с указанными символами
macro FindPmReadyStepFromList(Payment : RsbPayment/*, StepSymbol : string*/) : string
  var DocKind = 0;
  if(Payment.PrimDocKind)
    DocKind = Payment.PrimDocKind;
  elif(Payment.DocKind == WL_WIPM)
    DocKind = WL_INDOC;
  else
    DocKind = Payment.DocKind;
  end;
  var strSQL = "Select step.t_Symbol " +
               "  from doprstep_dbt step, doproper_dbt oproper " +
               " where step.t_ID_Operation = oproper.t_ID_Operation " +
               "   and oproper.t_DocKind = :DocKind " +
               "   and oproper.t_DocumentID = LPad( :PaymentID, 34, '0' ) " +
               "   and step.t_IsExecute = 'R' ";

  var params = makeArray( SQLParam("DocKind",   DocKind),
                          SQLParam("PaymentID", Payment.PaymentID)
                        );
  var i : integer = 1, 
      symb : string = "";
  if( GetParm(i, symb) )
    strSQL = strSQL + " and step.t_Symbol in ( :Symbol ";
    params[ params.size ] = SQLParam("Symbol", symb);
    i = i + 1;

    while( GetParm(i, symb) )
      strSQL = strSQL + "," + string("         :Symbol", i);
      params[ params.size ] = SQLParam("Symbol"+i, symb);
      i = i + 1;
    end;
    strSQL = strSQL + "                      )";
  end;

  var rs : RsdRecordset = execSQLselect(strSQL, params);
  if(rs and rs.moveNext())
    return rs.value("t_Symbol");
  else
    return "";
  end;
end;

macro PM_DefaultMaxPriority() : integer
  var MaxPriority : integer = 0, err : integer = 0, TypeVal : integer = V_UNDEF;

  TypeVal = GetRegistryValue( "CB\\PAYMENTS\\MAXPRIORITY", V_INTEGER, MaxPriority, err );
  if( ( TypeVal == V_UNDEF ) OR ( err != 0 ) )
    MaxPriority = 5;
  end;

  return MaxPriority;
end;
  
MACRO GetPSDocKind(Payment : RsbPayment) : integer
  if(Payment.DocKind == PS_PAYORDER)
    var q : string = "select t_DocKind "
                     "  from dpspayord_dbt "
                     " where t_OrderID = :OrderID ";
  
    var params : TArray = makeArray( SQLParam("OrderID", Payment.PaymentID) );
  
    var rs : RsdRecordset = execSQLselect(q, params);
  
    if(rs and rs.moveNext())
      return rs.value("t_DocKind");
    end;
  end;
  
  return 0;
END;

macro GetRegValueOldDoc( PmDocKind:integer, PoDocKind:integer, IsNatCur:bool ):integer
  
  var OldDoc:integer = 0;
  var err:integer = 0;
  
  // Требование банка
  if( PmDocKind == DLDOC_BANKCLAIM )
    if( IsNatCur )
      GetRegistryValue("BBANK\\PAYORDER\\CLAIM\\OldDoc", V_INTEGER, OldDoc, err);
    else
      GetRegistryValue("BBANK\\CPORDER\\CLAIM\\OldDoc", V_INTEGER, OldDoc, err);
    end;
   
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;
  // Платеж банка
  if( PmDocKind == DLDOC_BANKPAYMENT )
    GetRegistryValue("BBANK\\PAYORDER\\PAYMENT\\OldDoc", V_INTEGER, OldDoc, err);
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;
  // Валютный платеж банка
  if( PmDocKind == BBANK_CPORDER )
    GetRegistryValue("BBANK\\CPORDER\\ORDER\\OldDoc", V_INTEGER, OldDoc, err);
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;
  // Клиентский платеж
  if( PmDocKind == PS_PAYORDER )
    if( (PoDocKind == PSPOKIND_DEMAND) or (PoDocKind == PSPOKIND_REQUEST ))
      GetRegistryValue("PS\\PAYORDER\\DEMAND\\OldDoc", V_INTEGER, OldDoc, err);
    elif ( PoDocKind == PSPOKIND_AKKREDITIV ) 
      GetRegistryValue("PS\\PAYORDER\\LETTEROFCREDIT\\OldDoc", V_INTEGER, OldDoc, err);
    else
      GetRegistryValue("PS\\PAYORDER\\ORDER\\OldDoc", V_INTEGER, OldDoc, err);
    end;
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end; 
  // Валютный клиентский платеж
  if( PmDocKind == PS_CPORDER )
    GetRegistryValue("PS\\CPORDER\\ORDER\\OldDoc", V_INTEGER, OldDoc, err);
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;
  // Инкассовое поручение
  if( PmDocKind == PS_INRQ )
    GetRegistryValue("PS\\INRQCUR\\OldDoc", V_INTEGER, OldDoc, err);
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;

  if( InList( PmDocKind, DLDOC_OUT_PMCLAIM, DLDOC_OUT_PMCOL ) )
    GetRegistryValue("PS\\PAYORDER\\DEMAND\\OLDDOC", V_INTEGER, OldDoc, err);
    if( OldDoc > 0 )
      return OldDoc;
    end;
  end;

end;

macro CheckUIN(ReceiverAccount : string, UIN : string)
  var stat : integer = 0;
  var ErrMsg : string = "";

  var params : TArray = makeArray
    ( SQLParam("p_ReceiverAccount", ReceiverAccount),
      SQLParam("p_UIN", UIN),
      SQLParam("p_ErrMsg", V_STRING, RSDBP_OUT)
    );

  if( not stat )
    stat = execStoredFunc( "PM_COMMON.CheckUIN", V_INTEGER, params );
    ErrMsg = params.Value(2).value;

    if( stat )
      RunError(ErrMsg);
    end;
  end;

end;

macro GetPaymStatus(PaymentID : integer) : integer
  var rs = execSQLselectPrm
  ( "Select t_PaymStatus "
    "  from dpmpaym_dbt "
    " where t_PaymentID = :PaymentID ",
    SQLParam("PaymentID", PaymentID)
  );

  if(rs and rs.moveNext())
    return rs.value("t_PaymStatus");
  end;

  return -1;
end;

private macro CopyRsToRec(rs : RsdRecordset, buff)

  var BuffFldCount : integer = FldNumber(buff);

  for(var i : integer, 0, rs.FldCount - 1)
    var FieldName : string = StrLwr( SubStr(rs.Fld(i).Name, 3) );

    for(var j : integer, 0, BuffFldCount - 1)
      if( FldName(buff, j) == FieldName )
        if( rs.Fld(i).Value )
          buff(j) = rs.Fld(i).Value;
        end;
      end;
    end;
  end;

end;

macro GetOperationParticipantsFromRoute
( PaymentID : integer,
  p_PrevInstrAgent, p_SenderBank, p_ExecutorBank, 
  p_IntermediaryBank, p_PayerBank, p_ReceiverBank
)
  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);

  ClearRecord( PayerBank );
  ClearRecord( PrevInstrAgent );
  ClearRecord( SenderBank );
  ClearRecord( ExecutorBank );
  ClearRecord( IntermediaryBank );
  ClearRecord( ReceiverBank );

  var rs = execSQLselectPrm
    ( "SELECT * FROM TABLE( PM_COMMON.GetOperParticipantsFromRoute( ? ) ) ",
      SQLParam("p_PaymentID", PaymentID)
    );

  while(rs and rs.moveNext())
    if( rs.value("t_Type") == PART_PAYERBANK )
      CopyRsToRec(rs, PayerBank);
    elif( rs.value("t_Type") == PART_PREVINSTRUCTINGAGENT )
      CopyRsToRec(rs, PrevInstrAgent);
    elif( rs.value("t_Type") == PART_SENDERBANK )
      CopyRsToRec(rs, SenderBank);
    elif( rs.value("t_Type") == PART_EXECUTORBANK )
      CopyRsToRec(rs, ExecutorBank);
    elif( rs.value("t_Type") == PART_INTERMEDIARYBANK )
      CopyRsToRec(rs, IntermediaryBank);
    elif( rs.value("t_Type") == PART_RECEIVERBANK )
      CopyRsToRec(rs, ReceiverBank);
    end;
  end;

  copy(p_PayerBank, PayerBank);
  copy(p_PrevInstrAgent, PrevInstrAgent);
  copy(p_SenderBank, SenderBank);
  copy(p_ExecutorBank, ExecutorBank);
  copy(p_IntermediaryBank, IntermediaryBank);
  copy(p_ReceiverBank, ReceiverBank);
end;

macro CheckPaymentPrecedence( PaymentPrecedence:string, PaymentKind:string ):bool
  if(
      (PaymentPrecedence != "") and (StrLen( PaymentPrecedence ) == 2)
      and 
      ( 
        ( (PaymentKind == "С") and (
                                   (int(PaymentPrecedence) == 13) 
                                    or 
                                  ((int(PaymentPrecedence) >= 50) and (int(PaymentPrecedence) <= 59)) 
                                    or 
                                  ((int(PaymentPrecedence) >= 60) and (int(PaymentPrecedence) <= 69))
                                  )
        )
      or 
        ( 
          InList(PaymentKind, "", "Н", "П", "Т") and (int(PaymentPrecedence) >= 70) and (int(PaymentPrecedence) <= 79) 
        )
      )
    )
    return true;
  else
    return false;
  end;
end;

macro PM_HasOutProp(Payment : RsbPayment) : bool
  if( (Payment.PayerGroup == PAYMENTS_GROUP_EXTERNAL) and
      (not Payment.PayerIsSender)
    )
    return true;
  end;

  if( (Payment.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL) and
      (not Payment.ReceiverIsSender)
    )
    return true;
  end;

  return false;
end;
