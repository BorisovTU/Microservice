 /*
 $Name: regpair.mac
 $Module: Главная книга
 $Description: Печать реестра проводок по выведению единого результата
 */

/*---------------------------------------------------------------------------\
|                       Файл    regpair.mac                                  |
|              Печать отчета по выведению единого результата                 |
\---------------------------------------------------------------------------*/

Import rsd, FIInter, "reg_rep_lib.mac", "globals.mac", "consrep.mac";

private var i : integer, count : integer;
private var sqlString : string;
private var rs : RsdRecordset;

/**/
private var TotalSum       : money;
private var TotalSumNatCur : money;

private var OperName : string;
private var OperPost : string;

/* вспомогательные переменные для печати */
private var PreviousDate     : date;
private var PreviousChapter  : integer;
private var PreviousCurrency : integer;

/* Печать заголовка реестра */
private macro PrintLogTop( RegDate : date, DprtID : integer, Backout : bool )

  PrintLogDepartment( DprtID );

[                               РЕЕСТР                                         ];
  if(Backout)
[   удаленных документов по выведению единого результата на ##########         ]( RegDate:f );
  else
[        документов по выведению единого результата на ##########              ]( RegDate:f );
  end;
[                                                                              ];

end;

/* Печать отсутствия проводок выведения единого результата */
private macro PrintEmptyLog( DprtID : integer, Backout : bool);
  
  PrintLogTop( {curdate}, DprtID, Backout );
[Счетов для выведения единого результата не найдено];

end;

/* Печать заголовка таблицы */
private macro PrintLogTableTop( RegDate : date, Chapter : integer, Currency : integer, Number_Pack : integer, DprtID : integer, Backout : bool )

  PrintLogTop( RegDate, DprtID, Backout );

[Глава:                 #                                                      ]( GetChapterString(Chapter)   );
[Финансовый инструмент: #                                                      ]( GetCurrencyString(Currency) );
[Пачка:                 #                                                      ]( string(Number_Pack)       );
[                                                                              ];
 
  if( Currency == NATCUR )
[+---------+-----------------------+-----------------------+---------------+   ];
[|    №    |        Дебет          |       Кредит          |     Сумма     |   ];
[|  докум  |                       |                       |    в рублях   |   ];
[+---------+-----------------------+-----------------------+---------------+   ];
  else
[+---------+-----------------------+-----------------------+---------------+---------------+];
[|    №    |        Дебет          |       Кредит          |     Сумма     |     Сумма     |];
[|  докум  |                       |                       |    в валюте   |    в рублях   |];
[+---------+-----------------------+-----------------------+---------------+---------------+];
  end;

  TotalSum       = $0;
  TotalSumNatCur = $0;

end;

/* Печать нижней части таблицы */
private macro PrintLogTableBot( Currency : integer )

 if( Currency != ALLFININSTR )
   
   if( Currency == NATCUR )
[+---------+-----------------------+-----------------------+---------------+   ];
[|Итого    |                                               |###############|   ]( TotalSum );
[+---------+-----------------------------------------------+---------------+   ];
    else
[+---------+-----------------------+-----------------------+---------------+---------------+];
[|Итого    |                                               |###############|###############|]( TotalSum, TotalSumNatCur );
[+---------+-----------------------------------------------+---------------+---------------+];
    end;

[                                                                              ];
[##############################     #######                                    ]( OperPost:w, "(" + string({oper}) + ")" );
[                                    #                                         ]( OperName );
[                                                                              ];
[Контролер                          (                        )                 ];
[                                                                              ];
[                                                                              ];

  end;

end;

/* Печать заголовка реестра */
private macro PrintLogLine( rs : RsdRecordset, DprtID : integer, Backout : bool )

  var Date_Carry       : date;
  var Chapter          : integer;
  var Currency         : integer;
  var Numb_Document    : string;
  var Account_Payer    : string;
  var Account_Receiver : string;
  var Sum              : money;
  var SumNatCur        : money;
  var Number_Pack      : integer;

  Date_Carry       = rs.value("t_Date_Carry"      );
  Chapter          = rs.value("t_Chapter"         );
  Currency         = rs.value("t_Currency"        );
  Numb_Document    = rs.value("t_Numb_Document"   );
  Account_Payer    = rs.value("t_Account_Payer"   );
  Account_Receiver = rs.value("t_Account_Receiver");
  Sum              = rs.value("t_Sum"             );
  SumNatCur        = rs.value("t_SumNatCur"       );
  Number_Pack      = rs.value("t_Number_Pack"     );

  if( (PreviousDate != Date_Carry) or (PreviousChapter != Chapter) or (PreviousCurrency != Currency) )

    PrintLogTableBot( PreviousCurrency );
    PrintLogTableTop( Date_Carry, Chapter, Currency, Number_Pack, DprtID, Backout );

  end;

  TotalSum = TotalSum + Sum;

  if( Currency == NATCUR )

[|#########|#######################|#######################|###############|   ]
(Numb_Document:r, Account_Payer:f, Account_Receiver:f, Sum);
  
  else

    if( SumNatCur == $0 )

      ConvSum( SumNatCur, Sum, Date_Carry, Currency, NATCUR );

    end;

    TotalSumNatCur = TotalSumNatCur + SumNatCur;

[|#########|#######################|#######################|###############|###############|]
(Numb_Document:r, Account_Payer:f, Account_Receiver:f, Sum, SumNatCur);

  end;

  PreviousDate     = Date_Carry;
  PreviousChapter  = Chapter;
  PreviousCurrency = Currency;

end;

private macro PrintCarryLog( DprtID : integer, Backout : bool )

  sqlString = "SELECT count(1) FROM drp_carry_log_tmp WHERE t_DprtID = " + String(DprtID);

rs = RsdRecordset( sqlString );

rs.moveNext();

count = rs.value(0);

if( count == 0 )

  /* Выводим пустой отчет */
    PrintEmptyLog( DprtID, Backout );

else
  
    sqlString = " SELECT * FROM drp_carry_log_tmp " +
                " WHERE t_DprtID = " + String(DprtID) + 
                " ORDER BY t_Date_Carry, t_Chapter, t_Currency, t_Sum";

  rs = RsdRecordset( sqlString );

  PreviousDate     = date(0, 0, 0);
  PreviousChapter  = 0;
  PreviousCurrency = ALLFININSTR;

  OperName = GetOperString( {oper}, @OperPost );

  InitProgress( count, null, "Формирование реестра" );
  i = 0;
  while( rs.moveNext() )
    
    UseProgress( i );

    PrintLogLine( rs, DprtID, Backout );

    i = i + 1;

  end;

  PrintLogTableBot( PreviousCurrency );

  RemProgress();
  end;
end;

macro PrintCarryLogExtPair(DprtID : integer, Backout : bool)
  PrintCarryLog(DprtID, Backout);
end;
