/*
 $Name: fm4077uOutOpcontr.mac
 $Module: Ядро ГКБО 
 $Description: ФМ.4077-У: Выгрузка сообщения об отказах в dopcontr_dbt
 */

import FMInter, BankInter;
import rsexts, likepy;
import rsd, oralib;

private macro GetCurrencyByISO( CurrencyISONumber ) : integer

  var sqlString : string;
  var rs : RsdRecordset;

  if(CurrencyISONumber != "")
    sqlString = "SELECT t_fiid FROM dfininstr_dbt WHERE t_ISO_Number = '" + CurrencyISONumber + "'";

    rs = RsdRecordset( sqlString );

    if( rs.moveNext())
      return rs.value( 0 );
    end;
  end;

  return 0;
end;

private macro GetPaperCode(Element)
  var query = "";
  var rs;
  var params : TArray;
  var Code = "";

  query = query + " SELECT t_Code FROM DFMPAPERLINK_DBT WHERE T_LinkKind IN(1, 2) AND t_DocKind IN (SELECT t_Flag FROM DLLVALUES_DBT WHERE t_List = 3566 AND t_Element = :Element )";

  params = makeArray(SQLParam("Element", Element));

  rs = execSQLselect(query, params, true);
  if(rs and rs.moveNext())
    Code = rs.value(0);
  end;

  return Code;
end;


macro FM_RjSrvMsg_Opcontr_Out(MsgKind, ID)
  var RejectService = RsbFMRejectServiceMessage(MsgKind, ID);

  var Msg = ""; 
  var stat = RejectService.GetLastError(Msg);

  if(stat > 0)
    MsgBox(Msg);
  end;

  if(stat == 0)
    var FM_Operation : RsbFMOperation = RsbFMOperation();

    // OPCONTR            // FMRejectServiceMessage
    if(MsgKind == RJSRVMSG_MSGKIND_REJ_ACC_CNTR) // отказы от заключения договоров счета
      FM_Operation.OperationType = FM_OPER_TYPE_REJ_ACC; // - ОС (Отказ от заключения договора банковского сче-та)     

      // Дата операции 5.1.3.1.6 "Дата отказа"
      FM_Operation.DateCarry = RejectService.RejectDate;

      // Тип отправки  5.1.3.1.4 "Тип записи"
      FM_Operation.Action = RejectService.RecType;

      //Дата отправки <не задано>
      // Заявление <не задано>
      // ID заявления на открытие счета  <не задано>


      // Основание отказа от закл.договора банк.счета  "3" (Подозрение, что цель открытия счета - легализация или терроризм)
      FM_Operation.RejectCode = FM_REJECTCODE_ACC_TERROR;

      // Исполнитель 5.1.1.1.5 "Пользователь создавший"
      FM_Operation.Executer = RejectService.CreateOper;

      // Контролер 5.1.1.1.7 "Пользователь изменивший"
      FM_Operation.Controler = RejectService.ChangeOper; 

      // Операционист  <не задано>

      // Обоснование 5.1.3.1.8 "Дополнительные сведения"
      FM_Operation.RejectGround = RejectService.AddInfo;

    elif(MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)   // отказы в совершении операций
      FM_Operation.OperationType = FM_OPER_TYPE_REJ_OPR; // - ОО (Отказ от проведения операции)                      
      // ...
      // Номер операции 0

      // Дата операции 5.1.4.1.7 "Дата отказа"
      FM_Operation.DateCarry = RejectService.RejectDate;

      // Тип отправки  5.1.4.1.4 "Тип записи"
      FM_Operation.Action = RejectService.RecType;

      //Дата отправки <не задано>
      /*
      Основание для отказа в проведении операции  В зависимости от значения атрибута 5.1.4.1.6:
      " 1: "7" (Не представлены документы, необходимые для фиксирования информации)
      " 2 или 3: "8" (Подозрение, что цель выполнения операции - легализация или терроризм)
      */

      if(RejectService.RejectGround == RJCNCLCARRYOPER_GRND_NO_DOCUMENTS)
        FM_Operation.RejectCode = FM_REJECTCODE_DOC_ABS;
      elif((RejectService.RejectGround == RJCNCLCARRYOPER_GRND_TRN_LAUNDERING) OR (RejectService.RejectGround == RJCNCLCARRYOPER_GRND_TRN_TERROR))
        FM_Operation.RejectCode = FM_REJECTCODE_OPR_TERROR;
      end;

      // Первичный документ  <не задано>
      // Номер платежного документа  <не задано>
      // Дата платежного документа <не задано>
      
      // Код валюты  5.1.4.1.8 " Код валюты"
      FM_Operation.FIID = GetCurrencyByISO( RejectService.CurrencyISONumber );

      // Счет  <не задано>
      // Сумма 5.1.4.1.9 " Сумма операции в валюте "
      if(RejectService.SumCur != $0)
        FM_Operation.Sum = RejectService.SumCur;
      end;

      // Сумма в рублях  5.1.4.1.10 " Сумма в рублях"
      if(RejectService.SumNat != $0)
        FM_Operation.SumEquivalent = RejectService.SumNat;
      end;

      // Исполнитель 5.1.1.1.5 "Пользователь создавший"
      FM_Operation.Executer = RejectService.CreateOper;

      // Контролер 5.1.1.1.7 "Пользователь изменивший"
      FM_Operation.Controler = RejectService.ChangeOper;
      
      // Операционист  <не задано>

      // Основание операции  5.1.4.1.13 "Характеристика операции"
      FM_Operation.Ground = RejectService.OperationInfo;

      // Обоснование 5.1.4.1.14 "Дополнительные сведения"
      FM_Operation.RejectGround = RejectService.AddInfo;

    // RsbFMOperation
    elif(MsgKind == RJSRVMSG_MSGKIND_TERM_ACC_CNTR)   // расторжения договоров счета
      FM_Operation.OperationType = FM_OPER_TYPE_REJ_CON; // - ОД (Отказ от договора банковского счета)               

      // Номер операции  0
     
      // Дата операции 5.1.5.1.6 "Дата расторжения"
      FM_Operation.DateCarry = RejectService.RejectDate;

      // Тип отправки  5.1.5.1.4 "Тип записи"
      FM_Operation.Action = RejectService.RecType;

      // Дата отправки <не задано>
      // Основание для расторжения договора счета  "9" (Расторжение договора счета после двух отказов вып.операций в год)
      FM_Operation.RejectCode = FM_REJECTCODE_OPR_REJ;

      // Исполнитель 5.1.1.1.5 "Пользователь создавший"
      FM_Operation.Executer = RejectService.CreateOper;

      // Контролер 5.1.1.1.7 "Пользователь изменивший"
      FM_Operation.Controler = RejectService.ChangeOper; 

      // Операционист  <не задано>

      // Обоснование 5.1.5.1.8 "Дополнительные сведения"
      FM_Operation.RejectGround = RejectService.AddInfo;
    end;

    // Сведения об участнике операции

    var OprParty = NULL;  

    var client = NULL;
    var indx = 0;
    if((MsgKind == RJSRVMSG_MSGKIND_REJ_ACC_CNTR) OR (MsgKind == RJSRVMSG_MSGKIND_TERM_ACC_CNTR)) // отказы от заключения договоров счета
      client = RejectService.GetClient();
    else
      client = RejectService.GetParty(indx);
    end;

    // CRsbFMOprParty  FMRejectServiceParty
    while(client != null)
      if(MsgKind == RJSRVMSG_MSGKIND_REJ_ACC_CNTR) // отказы от заключения договоров счета
        OprParty = FM_Operation.OprParty(_FM_PARTY_DEC_ACC_REJ); // заявитель по отказному договору счета (для операции ОС) 
      elif(MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)   // отказы в совершении операций
        OprParty = FM_Operation.OprParty(_FM_PARTY_CLN_OPR_REJ); // клиент по отказной операции (для операции ОО)           
      elif(MsgKind == RJSRVMSG_MSGKIND_TERM_ACC_CNTR)   // расторжения договоров счета
        OprParty = FM_Operation.OprParty(_FM_PARTY_CLN_CON_REJ); // клиент по расторгнутому договору счета                  
      end;
      
      //Участник  5.1.6.1.2 "Субъект экономики"
      OprParty.PartyID = client.PartyID; 

      /*Тип участника расчетов  В зависимости от значения атрибута 5.1.6.1.3 "Тип клиента":
      " 1 или 5: "Ю" (Юридическое лицо);
      " 2 или 4: "Ф" (Физическое лицо);
      " 3: "П" (Предприниматель).
      */
      if((client.ClientType == RJSRVCLNT_CT_LEGAL) OR (client.ClientType == RJSRVCLNT_CT_FOREIGN_ENT))
        OprParty.PartyType = "Ю"; 
      elif((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))
         OprParty.PartyType = "Ф"; 
      elif(client.ClientType == RJSRVCLNT_CT_INDIVID )
         OprParty.PartyType = "П"; 
      end;

      // № из Спр.ФСФМ РФ  <не задано>
      // Признак участника <не задано>


      /*
      Принадлежность к ИПДЛ В зависимости от значения атрибута 5.1.8.1.9 "Признак принадлежности к публичным лицам":
      " 1 или 5: "01" (является ИПДЛ);
      " 2: "02" (является родственником ИПДЛ);
      " "00" (в ином случае) - в остальных случаях.
      Заполняется только если значение атрибута 5.1.6.1.3 "Тип клиента" равно:
      " 2: физическое лицо;
      " 4: физическое лицо, занимающееся частной практикой.
      */
      if((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))
        if((client.IsPublicOfficial == RJSRVPERSON_IPO_FOREIGN_PUBLIC_OFFICIAL) OR (client.IsPublicOfficial == RJSRVPERSON_IPO_RUS_PUBLIC_PUB_OFFICIAL))
          OprParty.ForeignPublicFunctionary = "01" ; 
        elif(client.IsPublicOfficial == RJSRVPERSON_IPO_REL_FOREIGN_PUB_OFFICIAL)
          OprParty.ForeignPublicFunctionary = "02" ; 
        else
          OprParty.ForeignPublicFunctionary = "00" ; 
        end;
      end;

      /*
      ИНН В зависимости от значения атрибута 5.1.6.1.3 "Тип клиента":
      " 1 (юр.лицо): 5.1.7.1.3 "ИНН";
      " 2, 3 или 4 (физ.лица) : 5.1.8.1.5 "ИНН";
      " 5 (иностр.структура) - <не задано>. 
      */
      if(client.ClientType != RJSRVCLNT_CT_FOREIGN_ENT)
        OprParty.INN = client.INN; 
      end;

      /*
      ОКПО  В зависимости от значения атрибута 5.1.6.1.3 "Тип клиента":
      " 1 (юр.лицо): 5.1.7.1.5 "ОКПО";
      " остальные - <не задано>. 
      */
      if(client.ClientType == RJSRVCLNT_CT_LEGAL)
        OprParty.OKPO = client.OKPO; 
      end;
      /*
      Рег.номер В зависимости от значения атрибута 5.1.6.1.3 "Тип клиента":
      " 1 (юр.лицо): 5.1.7.1.6 "ОГРН";
      " 3 (ИП) : 5.1.10.1.2 "ОГРНИП";
      " остальные - <не задано>. 
      */
      if(client.ClientType == RJSRVCLNT_CT_LEGAL)
        OprParty.RegNumber = client.OGRN; 
      end;

      if(client.ClientType == RJSRVCLNT_CT_INDIVID)
        OprParty.RegNumber = client.OGRNIP; 
      end;

      // Упрощенная идентификация участника  <не задано>
      // Дата регистрации  <не задано>
      /*
      Дата рождения 5.1.8.1.7 "Дата рождения".
      Заполняется только если значение атрибута 5.1.6.1.3 "Тип клиента" равно:
      " 2: физическое лицо;
      " 3: индивидуальный предприниматель; 
      " 4: физическое лицо, занимающееся частной практикой. 
      */
      if((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_INDIVID) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))      
        OprParty.Birthday =client.BirthDate ; 
      end;
      /*
      Место рождения  Строка, объединённая через запятую с пробелом из значений атрибутов: 5.1.8.1.18 "Код страны", 5.1.8.1.19 "Наименование страны", 5.1.8.1.20 "Код субъекта РФ по ОКАТО", 5.1.8.1.21 "Район", 5.1.8.1.22 "Населенный пункт".
      Заполняется только если значение атрибута 5.1.6.1.3 "Тип клиента" равно:
      " 2: физическое лицо;
      " 3: индивидуальный предприниматель;
      " 4: физическое лицо, занимающееся частной практикой. 
      */
      if((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_INDIVID) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))      
        var Adres = client.BirthCountryISOCode;

        if(client.CountryName != "")
          if(Adres != "") 
            Adres = Adres + ",";
          end;
          Adres = Adres + client.CountryName;      
        end;

        if(client.OKATO != "")
          if(Adres != "") 
            Adres = Adres + ",";
          end;
          Adres = Adres + client.OKATO;
        end;

        if(client.DistrictName != "")
          if(Adres != "") 
            Adres = Adres + ",";
          end;
          Adres = Adres + client.DistrictName;
        end;

        if(Adres + client.PlaceName != "")
          if(Adres != "") 
            Adres = Adres + ",";
          end;
          Adres = Adres + client.PlaceName;
        end;

        OprParty.BirthPlace = Adres; 
      end;
      
      /*
      Гражданство 5.1.8.1.8
      Заполняется только если значение атрибута 5.1.6.1.3 "Тип клиента" равно:
      " 2: физическое лицо;
      " 3: индивидуальный предприниматель;
      " 4: физическое лицо, занимающееся частной практикой. 
      */
      if((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_INDIVID) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))      
        OprParty.CitizenShipCountry = client.CountryISOCode; 
      end;


      /*
        Документ, удостоверяющий личность
        Заполняется только если значение атрибута 5.1.6.1.3 "Тип клиента" равно:
        " 2: физическое лицо;
        " 3: индивидуальный предприниматель;
        " 4: физическое лицо, занимающееся частной практикой.
      */
      if((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_INDIVID) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))      
          OprParty.CodeDocum = GetPaperCode(client.PaperKind);  // Код документа  
          OprParty.PaperSeries = client.PaperSeries;  // Серия                         
          OprParty.PaperNumber = client.PaperNumber;  // Номер                         
          OprParty.PaperIssuedDate = client.PaperIssuedDate;  // Дата выдачи
          OprParty.PaperIssuer = client.PaperIssuer;  // Кем выдан
          //OprParty.SetPaperIssuerCode(client.PaperIssuerCode);  // Код подразделения !! 
      end;

      // Сведения о кредитной организации участника операции
      //Заполняется только при наличии экземпляра сущности 5.1.13 "Участник операции".
      if(MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)
        // OprParty.CodeDocum(client.PaperKind);  // Код документа  

        // № счета участника операции  5.1.13.1.6 "Номер счета в банке"
        OprParty.Account = client.AccountNumber ;
        // Код 5.1.13.1.5 "БИК"
        OprParty.BankCode = client.BIC;
        // Наименование  5.1.13.1.7 "Наименование банка"
        OprParty.BankName = client.BankName;
      end;

      /*
        Адрес места регистрации
        В зависимости от значения атрибута 5.1.6.1.3 "Тип клиента" заполяется данными:
        " 1 (юридическое лицо): 5.1.7.1.8 "Адрес юридического лица";
        " 2, 3 или 4 (физическое лицо): 5.1.8.1.10 "Адрес физического лица"; 
        " 5 (иностранная структура): 5.1.9.1.4 " Место ведения основной деятельности".
      */
      var Address = NULL;
      if(client.ClientType == RJSRVCLNT_CT_LEGAL)
        Address = client.GetAddress();
      elif((client.ClientType == RJSRVCLNT_CT_PERSN) OR (client.ClientType == RJSRVCLNT_CT_INDIVID) OR (client.ClientType == RJSRVCLNT_CT_PERSN_PRA))      
        Address = client.GetAddress();                     // FMRJSRVEMPLOYER ??
      elif(client.ClientType == RJSRVCLNT_CT_FOREIGN_ENT)    
        Address = client.GetAddress();
      end;

      if(Address != null)
        // Страна
        OprParty.RegCountry = Address.CountryISOCode;
        // Субъект РФ
        OprParty.RegAddrOKATO = Address.OKATO;
        // Район (регион)
        OprParty.RegAddrRegion = Address.DistrictName;
        // Населенный пункт
        OprParty.RegAddrPlaceName = Address.PlaceName;
        // Улица
        OprParty.RegAddrStreet = Address.StreetName;
        // Дом
        OprParty.RegAddrHouse = Address.House;
        // Корпус
        OprParty.RegAddrBuilding = Address.Corps;
        // Квартира
        OprParty.RegAddrOffice = Address.Flat;
      end;
      // CRsbFMOprParty  FMRejectServiceParty FMRejectServiceAddress

      if(MsgKind == RJSRVMSG_MSGKIND_REJ_OPER)
        indx = indx + 1;
        client = RejectService.GetParty(indx);
      else
        client = NULL;
      end;
    end;

  end;

  if(stat == 0)
    if(not FM_Operation.Save())
      var MsgStr = FM_Operation.ErrorMessage();
      stat = 19304;  // Ошибка сохранения: %s
      CreateErrMsg(stat, MsgStr);
    end;
  end;

  return stat;
end;