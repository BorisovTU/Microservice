/*********************************************************/
/*                                                       */
/*       макрос генерации сводных счетов-фактур          */
/*                                                       */
/*********************************************************/

import "globals.mac", sfbilf;

private macro ClearTmpTable()
  var cmd = RsdCommand("TRUNCATE TABLE dbilsfgen_tmp");
  cmd.execute();
end;

private macro AddSfDefComisses( FeeType, DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo )
  
  var cmd = RsdCommand;
  cmd.CmdText = 
    " INSERT INTO dbilsfgen_tmp( t_Dprt, t_DprtNode, t_PayerID, t_PayFIID, " +
    " t_FeeType, t_CommNumber, t_ComissName, t_ProductID, t_ComID, t_ComSum, t_ComSumNDS, t_NDSRateValue, t_invoiceID ) " +
    " SELECT sfdef.t_Department, ";
  
  // Узел ТС и плательщик
  if( FeeType == SF_FEE_TYPE_ONCE )
    cmd.CmdText = cmd.CmdText +  
      " DECODE( sfdef.t_SfContrID, 0, 0, (SELECT contr.t_Branch FROM dsfcontr_dbt contr WHERE contr.t_ID = sfdef.t_SfContrID) ), " +
      " (SELECT acc.t_Client FROM daccount_dbt acc " +
        " WHERE acc.t_Account = sfdef.t_AccountPayer AND acc.t_Code_Currency = sfdef.t_FIIDPayer AND acc.t_Chapter = 1), ";    
  else
    cmd.CmdText = cmd.CmdText + " contr.t_Branch, contr.t_PartyID, ";
  end;

  cmd.CmdText = cmd.CmdText +  
      " sfdef.t_FIID_Sum, " +
      " sfdef.t_FeeType, sfdef.t_CommNumber, com.t_Name, com.t_ProductID, " +
      " sfdef.t_ID, sfdef.t_Sum, sfdef.t_SumNDS, sfdef.t_NDSRateValue, 0 ";

  if( FeeType == SF_FEE_TYPE_ONCE )
    cmd.CmdText = cmd.CmdText +  " FROM dsfdef_dbt sfdef, dsfcomiss_dbt com " ;
  else
    cmd.CmdText = cmd.CmdText +  " FROM dsfdef_dbt sfdef, dsfcomiss_dbt com, dsfcontr_dbt contr " ;
  end;

  cmd.CmdText = cmd.CmdText +  " WHERE sfdef.t_FeeType = com.t_FeeType AND sfdef.t_CommNumber = com.t_Number ";
  
  if( FeeType != SF_FEE_TYPE_ONCE )
    cmd.CmdText = cmd.CmdText + " AND contr.t_ID = sfdef.t_SfContrID ";
  end;

  cmd.CmdText = cmd.CmdText +
      " AND sfdef.t_InvoiceID = 0 AND sfdef.t_FacturaID = 0 AND com.t_paynds != ? AND sfdef.t_FeeType = ? "  
      " AND com.t_ReceiverID = ? AND (sfdef.t_Status = ? OR sfdef.t_Status = ?) AND sfdef.t_Department = ? ";

  cmd.addParam( "", RSDBP_IN, SF_NOT_TAX );
  cmd.addParam( "", RSDBP_IN, FeeType );
  cmd.addParam( "", RSDBP_IN, {HeadBankID} );
  cmd.addParam( "", RSDBP_IN, SFDEFCOM_STATUS_FOR_PAY );
  cmd.addParam( "", RSDBP_IN, SFDEFCOM_STATUS_PAYED );
  cmd.addParam( "", RSDBP_IN, DprtID );
  
  if( FeeType == SF_FEE_TYPE_ONCE )
    cmd.CmdText = cmd.CmdText + " AND sfdef.t_DateFee >= ? AND sfdef.t_DateFee <= ? ";
  elif( FeeType == SF_FEE_TYPE_SINGLE )
    cmd.CmdText = cmd.CmdText + 
      " AND (SELECT step.t_Fact_Date FROM doprstep_dbt step WHERE step.t_ID_Operation = sfdef.t_ID_Operation AND step.t_ID_Step = sfdef.t_ID_Step) " + 
      " BETWEEN ? AND ? ";
  else
    cmd.CmdText = cmd.CmdText + 
      " AND sfdef.t_DatePeriodBegin >= ? AND sfdef.t_DatePeriodEnd <= ? ";
  end;
  cmd.addParam( "", RSDBP_IN, ServDateFrom );
  cmd.addParam( "", RSDBP_IN, ServDateTo );


  if( DprtID != DprtNodeID )
    if( FeeType == SF_FEE_TYPE_ONCE )
      cmd.CmdText = cmd.CmdText + " AND ? = (SELECT contr.t_Branch FROM dsfcontr_dbt contr WHERE contr.t_ID = sfdef.t_SfContrID) ";
    else    
      cmd.CmdText = cmd.CmdText + " AND ? = contr.t_Branch ";
    end;
    cmd.addParam( "", RSDBP_IN, DprtNodeID );
  end;

  if( ServKind > 0 )
    cmd.CmdText = cmd.CmdText + " AND com.t_ServiceKind = ? ";
    cmd.addParam( "", RSDBP_IN, ServKind );
  end;

  if( ClientID > 0 )    
    if( FeeType == SF_FEE_TYPE_ONCE )
      cmd.CmdText = cmd.CmdText + 
        " AND ? = (SELECT acc.t_Client FROM daccount_dbt acc WHERE acc.t_Account = sfdef.t_AccountPayer " +
                 " AND acc.t_Code_Currency = sfdef.t_FIIDPayer AND acc.t_Chapter = 1) ";
    else
      cmd.CmdText = cmd.CmdText + " AND ? = contr.t_PartyID ";
    end;
    cmd.addParam( "", RSDBP_IN, ClientID );
  end;

  cmd.execute();

end;

//Отбираем все релевантные УПК входящие в ТО
private macro AddSfInvComisses( DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo )
  
  var cmd = RsdCommand;
  cmd.CmdText = 
    " INSERT INTO dbilsfgen_tmp( t_Dprt, t_DprtNode, t_PayerID, t_PayFIID, " +
    " t_FeeType, t_CommNumber, t_ComissName, t_ProductID, t_ComID, t_ComSum, t_ComSumNDS, t_NDSRateValue, t_invoiceID ) " +
    " SELECT sfdef.t_Department, "
      " (SELECT contr.t_Branch FROM dsfcontr_dbt contr WHERE contr.t_ID = inv.t_ContractID ), " +
      " inv.t_PayerID, sfdef.t_FIID_Sum, " +
      " sfdef.t_FeeType, sfdef.t_CommNumber, com.t_Name, com.t_ProductID, " +
      " sfdef.t_ID, sfdef.t_Sum, sfdef.t_SumNDS, sfdef.t_NDSRateValue, inv.t_InvoiceID "
    " FROM dsfdef_dbt sfdef, dsfinv_dbt inv, dsfcomiss_dbt com " +
    " WHERE sfdef.t_InvoiceID = inv.t_InvoiceID AND com.t_paynds != ? AND inv.t_FacturaID = 0 " +
      " AND sfdef.t_FeeType = com.t_FeeType AND sfdef.t_CommNumber = com.t_Number "
      " AND EXISTS (SELECT dep.t_Code FROM ddp_dep_dbt dep WHERE dep.t_PartyID = inv.t_BeneID AND dep.t_AccessMode <> 0 )" +       
      " AND inv.t_Department = ?  AND inv.t_InvoiceDate >= ?  AND inv.t_InvoiceDate <= ? ";

  cmd.addParam( "", RSDBP_IN, SF_NOT_TAX );
  cmd.addParam( "", RSDBP_IN, DprtID );
  cmd.addParam( "", RSDBP_IN, ServDateFrom );
  cmd.addParam( "", RSDBP_IN, ServDateTo );

  if( DprtID != DprtNodeID )
    cmd.CmdText = cmd.CmdText + " AND ? = (SELECT contr.t_Branch FROM dsfcontr_dbt contr WHERE contr.t_ID = inv.t_ContractID ) ";
    cmd.addParam( "", RSDBP_IN, DprtNodeID );
  end;

  if( ServKind > 0 )
    cmd.CmdText = cmd.CmdText + " AND inv.t_ServiceKind = ? ";
    cmd.addParam( "", RSDBP_IN, ServKind );
  end;

  if( ClientID > 0 )    
    cmd.CmdText = cmd.CmdText + " AND inv.t_PayerID = ? ";
    cmd.addParam( "", RSDBP_IN, ClientID );
  end;

  cmd.execute();

end;

private macro FillTmpTable( DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo )

  ClearTmpTable();

  AddSfDefComisses( SF_FEE_TYPE_ONCE, DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo );
  AddSfDefComisses( SF_FEE_TYPE_SINGLE, DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo );
  AddSfDefComisses( SF_FEE_TYPE_PERIOD, DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo );

  AddSfInvComisses( DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo );
       
end;

private macro ResultLogBF_Success( FacturaID, BillDate )
  file factura (bilfactura) key 0;
  var FacturaNumber = "";
  factura.FacturaID = FacturaID;
  if( getEQ(factura) )
    FacturaNumber = factura.FacturaNumber;
  end;

[     Сформирован СФ № ################################## от ########## ](FacturaNumber, BillDate);
end;

private macro ResultLogEntryBook_NotSuccess( ErrMsg )
[            Необходимые записи книг продаж не сформированы             ];
  println("            " + ErrMsg);
end;

private macro ResultLogBF_NotSuccess( ErrMsg )

  var i = 1;
  array StringArray;
  StrSplit( StrSubst(ErrMsg, "|", " " ), StringArray, 37);

[     СФ не сформирован. ###############################################](StringArray(0));

  while ( StrLen(StringArray(i) ) > 0)
[                        ###############################################]
    ( StringArray(i) );
    i = i + 1;
  end;  
end;

private macro ResultLogClientName( ClientName )
[                                                                       ];
[     Клиент: #######################################################   ](ClientName);

end;

private macro GetFacturaLines( BillDate, Dprt, DprtNode, PayerID, PayFIID, OprCodeId )
  var sqlString, rs, cmd, i = 0;
  var facturaLineAr = TArray;  
  var feeType, comissName, productID, taxSchema, comID, comSum, comSumNDS, NDSRateValue;

  sqlString = " SELECT bil.t_FeeType, bil.t_CommNumber, bil.t_ComissName, bil.t_ProductID, " + 
              " (SELECT t_TaxSchema FROM dbilproduct_dbt pr WHERE pr.t_ProductID = bil.t_ProductID) TaxSchema, "
              " bil.t_ComID, bil.t_ComSum, bil.t_ComSumNDS, bil.t_NDSRateValue " 
              " FROM dbilsfgen_tmp bil " + 
              " WHERE bil.t_Dprt = ? AND bil.t_DprtNode = ? AND bil.t_PayerID = ? AND bil.t_PayFIID = ? ";
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, DprtNode );
  cmd.addParam( "", RSDBP_IN, PayerID );
  cmd.addParam( "", RSDBP_IN, PayFIID );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    
    feeType = rs.value("t_FeeType");
    /*rs.value("t_CommNumber");*/
    comissName = rs.value("t_ComissName");
    productID  = rs.value("t_ProductID");
    taxSchema  = rs.value("TaxSchema");
    comID = rs.value("t_ComID");
    comSum = rs.value("t_ComSum");
    comSumNDS = rs.value("t_ComSumNDS");
    NDSRateValue = rs.value("t_NDSRateValue");
        
    /*Заполняем спецификацию СФ*/
    facturaLineAr[i] = TRecHandler("bilfacturaline.dbt");
    facturaLineAr[i].Clear();    
    
    facturaLineAr[i].rec.LineNo = 1;    
    facturaLineAr[i].rec.ProductID = productID;    
    facturaLineAr[i].rec.ProductName = comissName + ", " + string(BillDate);
    
    if( taxSchema != NULLVAL )
      facturaLineAr[i].rec.TaxSchema = taxSchema;
    end;   
    
    facturaLineAr[i].rec.Amount   = comSum;         
    facturaLineAr[i].rec.AmountWithNDS = comSum + comSumNDS;    
    facturaLineAr[i].rec.NDSAmount     = comSumNDS;
    
    if( NDSRateValue != 0 )
      facturaLineAr[i].rec.NDSRate       = NDSRateValue;
    else
      if( (comSumNDS != $0) AND (comSum != $0) )
        facturaLineAr[i].rec.NDSRate  = double( round(comSumNDS/comSum) ) * 100.0;
      end;
    end;
        
    facturaLineAr[i].rec.ComisID      = comID;
    facturaLineAr[i].rec.ComisType    = feeType;
    facturaLineAr[i].rec.OprCodeId    = OprCodeId;

    i = i + 1; 
  end;

  return facturaLineAr;

end;

private macro UpdateDefComisses( facturaID, Dprt, DprtNode, PayerID, PayFIID )

  var sqlString, cmd;

  /*разовые комиссии и единовременные комиссии*/
  sqlString = "UPDATE dsfdef_dbt sfdef SET sfdef.t_FacturaID = ? WHERE (sfdef.t_ID, sfdef.t_FeeType) IN " +
        " (SELECT bil.t_comID, bil.t_FeeType FROM dbilsfgen_tmp bil WHERE (bil.t_FeeType = ? OR bil.t_FeeType = ? OR bil.t_FeeType = ?) AND bil.t_comID > 0 " +
        " AND bil.t_Dprt = ? AND bil.t_DprtNode = ? AND bil.t_PayerID = ? AND bil.t_PayFIID = ? )";
  
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, facturaID );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, DprtNode );
  cmd.addParam( "", RSDBP_IN, PayerID );
  cmd.addParam( "", RSDBP_IN, PayFIID );

  cmd.Execute();

  /*ТО*/
  sqlString = "UPDATE dsfinv_dbt inv SET inv.t_FacturaID = ? WHERE inv.t_InvoiceID IN " +
        " (SELECT Distinct bil.t_InvoiceID FROM dbilsfgen_tmp bil WHERE bil.t_FeeType = ? AND bil.t_InvoiceID > 0 " +
        " AND bil.t_Dprt = ? AND bil.t_DprtNode = ? AND bil.t_PayerID = ? AND bil.t_PayFIID = ? AND bil.t_InvoiceID > 0 )";
  
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, facturaID );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, DprtNode );
  cmd.addParam( "", RSDBP_IN, PayerID );
  cmd.addParam( "", RSDBP_IN, PayFIID );

  cmd.Execute();

end;

private macro CreateBilFacturaForClient( ClientName, BillDate, Dprt, DprtNode, PayerID, PayFIID, OprCodeId, 
                                         facturaID:@integer, bilfDocArray:@TArray )
  
  var retVal = false;
  const BILTYPE_BILFAC = 1;
  const SFTYPE_DISTRIBUTED = 1;

  record factura("bilfactura.dbt");
  ClearRecord(factura);

  SF_FillBilFactura( SF_FEE_TYPE_COMMON, NULL, BillDate, Dprt, DprtNode, PayerID, payFIID, factura, true );

  var facturaLineAr = GetFacturaLines( BillDate, Dprt, DprtNode, PayerID, PayFIID, OprCodeId);
    
  if( BFCreateBilFactura(factura, facturaLineAr, facturaID) )   
    UpdateDefComisses( facturaID, Dprt, DprtNode, PayerID, PayFIID );
    ResultLogBF_Success( facturaID, BillDate );
    retVal = true;
  end;  
  
  //ResultIntoLog( ClientName, factura.FacturaNumber, factura.CreationDate );  

  return retVal;
end;

private macro SF_CreateBilBookEntryCommon( facturaID, Dprt, DprtNode, PayerID, PayFIID, BillDate )
  
  var bilfDocArray = TArray;
  var retVal = 0, retValTmp = 0, bilbeID = 0;
  
  var RegDate = BillDate;

  const PM_FINISHED = 32000;
   
  var sqlString, rs, cmd;

  /*разовые и единовременные комиссии*/
  sqlString = " SELECT pm.t_DocKind docKind, to_char(pm.t_PaymentID) pmID, pm.t_Amount pmAmount, pm.t_FIID pmFIID " +
              " FROM dpmpaym_dbt pm, dbilsfgen_tmp bil " + 
              " WHERE bil.t_Dprt = ? AND bil.t_DprtNode = ? AND bil.t_PayerID = ? AND bil.t_PayFIID = ? " +
                " AND (bil.t_feeType = ? OR bil.t_feeType = ? OR bil.t_feeType = ?) " +
                " AND pm.t_feeType = bil.t_feeType AND pm.t_DefComID = bil.t_comID AND pm.t_PaymStatus >= ? "
                " AND EXISTS (SELECT * FROM dpmdocs_dbt pmd WHERE pmd.t_PaymentID = pm.t_PaymentID AND pmd.t_Reason = 1)";
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, DprtNode );
  cmd.addParam( "", RSDBP_IN, PayerID );
  cmd.addParam( "", RSDBP_IN, PayFIID );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_ONCE );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_SINGLE );
  cmd.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );
  cmd.addParam( "", RSDBP_IN, PM_FINISHED );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    
    bilfDocArray[0] = TRecHandler("bilf_doc.rec");
    bilfDocArray[0].Clear();

    bilfDocArray[0].rec.DocKind = rs.value("docKind");    
    bilfDocArray[0].rec.DocID  = rs.value("pmID");
    bilfDocArray[0].rec.Amount = rs.value("pmAmount");
    bilfDocArray[0].rec.FIID   = rs.value("pmFIID");

    Opr_GetLastExecStep( bilfDocArray[0].rec.DocKind, SF_ToFullDocID(bilfDocArray[0].rec.DocID), 
                           bilfDocArray[0].rec.OperationId, bilfDocArray[0].rec.StepId );
    
    if( PM_GetPaymentExecutionDate(Int(bilfDocArray[0].rec.DocID), RegDate) != 0 )
      RegDate = BillDate;
    end;

    if( not BFCreateBilBookEntry(facturaID, bilfDocArray, RegDate, bilbeID) )
      retVal = retVal + 1;      
    end;
  end;
  
  /*ТО*/
  var invoiceID = 0, isIncluded = "", contractID = 0, pmLinks = 0;
  sqlString = "SELECT DISTINCT inv.t_InvoiceID invID, inv.t_IsIncluded isInc, inv.t_ContractID conID, " +
              " (SELECT count (*) FROM dsfinvlnk_dbt lnk WHERE lnk.t_InvoiceID = inv.t_InvoiceID) pmLnk " +
              " FROM dsfinv_dbt inv, dbilsfgen_tmp bil WHERE inv.t_InvoiceID = bil.t_invoiceID " + 
              " AND (inv.t_InvoiceStatus = ? OR inv.t_InvoiceStatus = ?) " +
              " AND bil.t_Dprt = ? AND bil.t_DprtNode = ? AND bil.t_PayerID = ? AND bil.t_PayFIID = ? " ;
  cmd = RSDCommand( sqlString );
  cmd.addParam( "", RSDBP_IN, SFINV_STATUS_PARTPAYED );
  cmd.addParam( "", RSDBP_IN, SFINV_STATUS_PAYED );
  cmd.addParam( "", RSDBP_IN, Dprt );
  cmd.addParam( "", RSDBP_IN, DprtNode );
  cmd.addParam( "", RSDBP_IN, PayerID );
  cmd.addParam( "", RSDBP_IN, PayFIID );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    invoiceID  = rs.value("invID");
    isIncluded = rs.value("isInc");
    contractID = rs.value("conID");
    pmLinks = rs.value("pmLnk");
    
    retValTmp = SF_CreateBilBookEntryForSfInv( facturaID, invoiceID, isIncluded, contractID, pmLinks );
    retVal = retVal + retValTmp;
  end;  

  return retVal;
end;

private macro getDefaultOprCodeId()
  var oprCodeId = 0;

  var sqlString = "SELECT t_id FROM DBILOPRCODE_DBT WHERE t_code = ?";
  var cmd = RSDCommand(sqlString);
  cmd.addParam("", RSDBP_IN, "01");
        
  var rs = RsdRecordset(cmd);
  
  if(rs.moveNext())
    oprCodeId = rs.value(0);
  end;
        
  return oprCodeId;
end;

private macro GenerateCompositeBills( BillDate, BF_Count:@integer, NoBF_Count:@integer, NoBook_Count:@integer )
  var sqlString, rs, cmd;  
  var Dprt, DprtNode, PayerID, PayFIID, ClientName, OprCodeId;
  
  var facturaID = 0, bilbeID = 0, bilfDocArray = TArray;

  OprCodeId = getDefaultOprCodeId();

  sqlString = " SELECT  bil.t_Dprt, bil.t_DprtNode, bil.t_PayerID, bil.t_PayFIID, " +
              " (SELECT t_ShortName FROM dparty_dbt p WHERE p.t_PartyID = bil.t_PayerID) ClientName " +
              " FROM dbilsfgen_tmp bil GROUP BY bil.t_Dprt, bil.t_DprtNode, bil.t_PayerID, bil.t_PayFIID ";  
  cmd = RSDCommand( sqlString );
  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
    
    Dprt = rs.value("t_Dprt");    

    if( rs.value("t_DprtNode") == NULLVAL )
      DprtNode = 0;
    else
      DprtNode = rs.value("t_DprtNode");
    end;

    PayerID  = rs.value("t_PayerID");
    PayFIID  = rs.value("t_PayFIID");
    ClientName = rs.value("ClientName");

    ResultLogClientName( ClientName );

    if( CreateBilFacturaForClient(ClientName, BillDate, Dprt, DprtNode, PayerID, PayFIID, OprCodeId, @facturaID) )
      BF_Count = BF_Count + 1;
      if( SF_CreateBilBookEntryCommon(facturaID, Dprt, DprtNode, PayerID, PayFIID, BillDate) > 0 )
        NoBook_Count = NoBook_Count + 1;
        ResultLogEntryBook_NotSuccess( GetErrMsg() );
      end;
    else
      NoBF_Count = NoBF_Count + 1;
      ResultLogBF_NotSuccess( GetErrMsg() );
    end;  
  end;
   
end;

private macro PrintLogFooter(BF_Count, NoBF_Count, NoBook_Count)
[                                                                       ];
[     Итоговый результат:                                               ];
[     Сформировано СФ:    #######                                       ](BF_Count); 
[     Не сформировано СФ: #######                                       ](NoBF_Count);
[     СФ, по которым не удалось сформировать записи книги продаж: ######](NoBook_Count);
end;

private macro PrintLogHeader( DprtName, DprtNodeName, ServKindName, ClientName, ServDateFrom, ServDateTo, BillDate )
  

[            Протокол формирования сводных СФ по услугам банка         
                                                                        ];
[     Дата формирования:   ##########                                   ](date());
[     Время формирования:  ########                                     ](time());
[     Операционист:        ##### ###################################### ]({oper}, {Name_Oper});
[     Подразделение банка: ############################################ ]({Name_Bank});
[          
      Параметры запуска:                                                ];
[     Филиал:             ############################################  ](DprtName);
[     Подразделение ТС:   ############################################  ](DprtNodeName);
[     Вид обслуживания:   ############################################  ](ServKindName);
[     Клиент:             ############################################  ](ClientName);
[     Дата оказания услуги: с ########## по ##########                  ](ServDateFrom, ServDateTo);
[     Дата счета-фактуры: ##########                                    ](BillDate);
[                                                                       ];
[     Результаты выполнения:                                            ];
  

end;


macro CompositeBillGenerationProc( DprtID, DprtName, DprtNodeID, DprtNodeName, ServKind, ServKindName,  
                                   ClientID, ClientName, ServDateFrom, ServDateTo, BillDate )

  var BF_Count = 0, NoBF_Count = 0, NoBook_Count = 0;
  
  PrintLogHeader( DprtName, DprtNodeName, ServKindName, ClientName, ServDateFrom, ServDateTo, BillDate );

  FillTmpTable( DprtID, DprtNodeID, ServKind, ClientID, ServDateFrom, ServDateTo );

  GenerateCompositeBills( BillDate, @BF_Count, @NoBF_Count, @NoBook_Count  );

  PrintLogFooter( BF_Count, NoBF_Count, NoBook_Count );

end;






