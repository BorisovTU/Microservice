/* ╔══════════════════════════════════════════════════════════════════╗ */
/*         Автоматизированная банковская система RS-Bank v5.0           */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Имя файла        : pspoinit.mac                                     */
/*                                                                      */
/*  Описание         : Макрос инициализации операции для платежа,       */
/*                     требования и требования-поручения банка          */
/*                                                                      */
/*  Программист      : Попова О.И.                                      */
/*                                                                      */
/*  Создан           : 11.06.07                                         */
/*                                                                      */
/* ╚══════════════════════════════════════════════════════════════════╝ */


import BankInter, PaymInter, PSInter, PTInter, OprInter, CashInter,oralib, likepy, FIInter, globals;

PRIVATE record memorder_paym ( pmpaym );
PRIVATE record memorder_prop ( pmprop );
PRIVATE record memorder_rmprop ( pmrmprop );

PRIVATE const PAYMENT_KIND_MAIL = "П";  // почтой
PRIVATE const PAYMENT_KIND_TELG = "T";  // телеграф
PRIVATE const PAYMENT_KIND_ELEC = "Э";  // электронно
PRIVATE const PAYMENT_KIND_INST = "С";  // срочно

PRIVATE CLASS TBankDprt( )
  
  VAR PARTYID:INTEGER = -1; 
  VAR BIC_RCC:STRING  = "";
  VAR UER:INTEGER     = -1;

END;

//------------------------------------------------------------------------------
// Получить bankdprt по Party
//------------------------------------------------------------------------------
MACRO getBankDprtByParty( PartyID:integer, bankdprt:TBankDprt ):bool
  var bankdp:TBankDprt = TBankDprt();
  var query:string = "SELECT bdpt.T_PARTYID, bdpt.T_BIC_RCC, bdpt.T_UER " +
                     " FROM dbankdprt_dbt bdpt " +
                     " WHERE bdpt.T_PARTYID = :PARTYID " ;

  if( PartyID == 0 )
     PartyID = {OurBank};
  end;
  var params:TArray = makeArray( SQLParam( "PARTYID", PartyID  ) );
  var rs:RsdRecordset = execSQLselect( query, params, TRUE );
  if( rs and rs.moveNext() )
    bankdprt.PARTYID = rs.value(0);
    bankdprt.BIC_RCC = rs.value(1);
    bankdprt.UER     = rs.value(2);
    return TRUE;
  else
    bankdprt = bankdp;
    return FALSE;
  end;

ONERROR(x)
  
  MsgBox( x.Message );
  bankdprt = bankdp;
  return FALSE;

END;

//------------------------------------------------------------------------------
// Получить bankdprt по коду
//------------------------------------------------------------------------------
PRIVATE MACRO getBankDprtByCode( Code:string, bankdprt:TBankDprt ):bool
  var bnkdprt:TBankDprt = TBankDprt();
  var query:string = "SELECT BDP.T_PARTYID, BDP.T_BIC_RCC, BDP.T_UER " +
                     "FROM DBANKDPRT_DBT BDP              " +
                     "WHERE BDP.T_PARTYID =               " +
                     " ( SELECT PARTCODE.T_PARTYID        " +
                     "     FROM DPARTCODE_DBT PARTCODE    " +
                     "    WHERE PARTCODE.T_CODEKIND = 3   " +
                     "      AND PARTCODE.T_CODE = :CODE ) ";
                     
  var params:TArray = makeArray( SQLParam( "CODE", Code  ) );
  var rs:RsdRecordset = execSQLselect( query, params, TRUE );

  if( rs and rs.moveNext() )
    bankdprt.PARTYID = rs.value(0);
    bankdprt.BIC_RCC = rs.value(1);
    bankdprt.UER     = rs.value(2);
    return TRUE;
  else
    bankdprt = bnkdprt;
    return FALSE;
  end;

ONERROR(x)
  
  MsgBox( x.Message );
  bankdprt = bnkdprt;
  return FALSE;

END;

//------------------------------------------------------------------------------
// Получить БИК субъекта
//------------------------------------------------------------------------------
PRIVATE MACRO ПолучитьБИК( PartyID:integer ):string

  var select:string = "select t_Code from dpartcode_dbt where t_CodeKind=:CodeKind and t_PartyID =:PartyID";

  if( PartyID == 0 )
      PartyID = {OurBank};
  end;
  var params:TArray = makeArray( SQLParam( "CodeKind", 3 ),
                                 SQLParam( "PartyID" , PartyID ) );
  var rset:RsdRecordset = execSQLselect( select, params, FALSE );
  if( rset and rset.moveNext() )
    return rset.Value( 0 );
  end;

  return "";
END;

//------------------------------------------------------------------------------
// Контрагент РКЦ?
//------------------------------------------------------------------------------
PRIVATE MACRO IsКонтрагентРКЦ():bool

  var query:string = "SELECT BDP.T_UERTYPE     " +
                     "  FROM DBANKDPRT_DBT BDP " +                     
                     " WHERE BDP.T_PARTYID =   " +
                     "   (SELECT CORR.T_CORRID           " +
                     "    FROM DCORSCHEM_DBT CORR        " +
                     "    WHERE CORR.T_NUMBER = :TNUMBER " +
                     "        AND CORR.T_FIID = :TFIID)  " ;

  var params:TArray = makeArray( SQLParam( "TNUMBER", memorder_prop.Corschem  ), 
                                 SQLParam( "TFIID"  , memorder_prop.PayFIID ) );

  var rs:RsdRecordset = execSQLselect( query, params, TRUE );
  if( rs and rs.moveNext() )
    return ( rs.value(0) == PT_KIND_PAYM_CASH_CENTRE );
  else
    return FALSE;
  end;

ONERROR(x)
  
  MsgBox( "Ошибка при определении контрагента|" + x.Message );
  return FALSE;

END;

//------------------------------------------------------------------------------
// Получить РКЦ или ГРКЦ, если flag == TRUE
//------------------------------------------------------------------------------
PRIVATE MACRO ПолучитьРКЦ( Code:string, _bankdprt:TBankDprt, flag:bool ):bool
  var stat:bool = TRUE;

  stat = getBankDprtByCode( Code, _bankdprt );
  if( stat AND flag AND ( SubStr( Code, 7 ) == "000" ) )
    stat = getBankDprtByCode( _bankdprt.BIC_RCC, _bankdprt );
  end;

  return stat;             
  
END;

//------------------------------------------------------------------------------
// Банк подразделение ЦБР?
//------------------------------------------------------------------------------
PRIVATE MACRO IsБанкЦБР( BankCode:string ):bool
  var code:string = SubStr( BankCode, 7 );
  return ( ( code == "001" ) or
           ( code == "002" ) or
           ( code == "000" ) );
END;

//------------------------------------------------------------------------------
// Документ дебетовый?
//------------------------------------------------------------------------------
PRIVATE MACRO IsDebet( KindDoc ):bool
  return ( KindDoc != DLDOC_BANKPAYMENT );
END;

//------------------------------------------------------------------------------
// Платеж исходящий?
//------------------------------------------------------------------------------
PRIVATE MACRO ПлатежИсходящий( KindDoc ):bool

  if( IsDebet(KindDoc) )
     return ( memorder_paym.PayerBankID != {OurBank} );
  end;
  return ( memorder_paym.ReceiverBankID != {OurBank} );

END;

//------------------------------------------------------------------------------
// Платеж внутрирегиональный?
//------------------------------------------------------------------------------
PRIVATE MACRO ПлатежВнутрирегиональный(bic1:string, bic2:string):bool
   return  ( SubStr( bic1, 3, 2) == SubStr( bic2, 3, 2) ) ;  
END;

MACRO InitOperation( KindOp, KindDoc, primdoc )

  var bankdprtPayer:TBankDprt    = TBankDprt();
  var bankdprtReceiver:TBankDprt = TBankDprt();
  var РазрешеноЭлектронно1 = FALSE, 
      РазрешеноЭлектронно2 = FALSE, 
      РазрешеноЭлектронно3 = FALSE; 
  var РазрешеноСрочно1 = FALSE, 
      РазрешеноСрочно2 = FALSE, 
      РазрешеноСрочно3 = FALSE;
  var PayerBankID:integer,
      ReceiverBankID:integer;
  var PayerBIC:string,
      ReceiverBIC:string;

  if( (memorder_paym.FIID == NATCUR ) and (memorder_paym.PayFIID == NATCUR) and
      (ПлатежИсходящий( KindDoc ))    and
      (IsКонтрагентРКЦ()) )

       if( not IsDebet(KindDoc) )
           PayerBankID    = memorder_paym.PayerBankID;
           ReceiverBankID = memorder_paym.ReceiverBankID;
       else /* для дебетовых во всех проверках вместо банка-плательщика - банк-получатель и наоборот*/
           PayerBankID    = memorder_paym.ReceiverBankID;
           ReceiverBankID = memorder_paym.PayerBankID;
       end;

       PayerBIC    = ПолучитьБИК(PayerBankID);
       ReceiverBIC = ПолучитьБИК(ReceiverBankID);

       if( (not getBankDprtByParty( PayerBankID,    bankdprtPayer    ) ) or
           (not getBankDprtByParty( ReceiverBankID, bankdprtReceiver ) )   )              
              MsgBox("Ошибка определения РКЦ");
              return 1;
       elif( (bankdprtPayer.BIC_RCC == "") or (bankdprtReceiver.BIC_RCC == "") )
              MsgBox("Не задан БИК РКЦ");
              return 1;
       end;

    // 1.Проверка кода участника нашего банка

       if  ( bankdprtPayer.UER == 2 )    РазрешеноЭлектронно1 = TRUE;
       elif( bankdprtPayer.UER == 3 )    РазрешеноЭлектронно1 = РазрешеноСрочно1 = TRUE;
       elif( bankdprtPayer.UER == 4 )                           РазрешеноСрочно1 = TRUE;
       end;

    // 2.Проверка ГРКЦ

       if( not ПолучитьРКЦ( bankdprtPayer.BIC_RCC, bankdprtPayer, TRUE ) )
           MsgBox("Ошибка определения ГРКЦ");
           return 1;
       end;

       if  ( bankdprtPayer.UER == 3 )    РазрешеноЭлектронно2 = TRUE;
       elif( bankdprtPayer.UER == 4 )    РазрешеноЭлектронно2 = РазрешеноСрочно2 = TRUE;
       elif( bankdprtPayer.UER == 1 )
          if( not ПлатежВнутрирегиональный( ПолучитьБИК(bankdprtPayer.PartyID), ReceiverBIC) )
                                    РазрешеноЭлектронно2 = TRUE;
          end;
       elif((bankdprtPayer.UER == 2)   or
            (bankdprtPayer.UER == 7))                                
          if( ПлатежВнутрирегиональный( ПолучитьБИК(bankdprtPayer.PartyID), ReceiverBIC) )
                                    РазрешеноЭлектронно2 = TRUE;
          end;
       end;

    // 3.Проверка кода участника банка-получателя

       if( not IsБанкЦБР( ReceiverBIC ) )   
          if( not ПолучитьРКЦ( bankdprtReceiver.BIC_RCC, bankdprtReceiver, FALSE ) )
              MsgBox("Ошибка определения РКЦ");
              return 1;
          end;
       end;

       if  (  bankdprtReceiver.UER == 3 )   РазрешеноЭлектронно3 = TRUE;
       elif(  bankdprtReceiver.UER == 4 )   РазрешеноЭлектронно3 = РазрешеноСрочно3 = TRUE;
       elif(  bankdprtReceiver.UER == 1 )
          if( not ПлатежВнутрирегиональный(PayerBIC, ReceiverBIC) )
                                            РазрешеноЭлектронно3 = TRUE;
          end;
       elif( (bankdprtReceiver.UER == 2 ) or
             (bankdprtReceiver.UER == 7 ) )
          if( ПлатежВнутрирегиональный( PayerBIC, ReceiverBIC ) )
                                            РазрешеноЭлектронно3 = TRUE;
          end;
       end;

       if( (РазрешеноЭлектронно1) and (РазрешеноЭлектронно2) and (РазрешеноЭлектронно3) )
           if( (memorder_rmprop.PaymentKind == PAYMENT_KIND_MAIL)  or
               (memorder_rmprop.PaymentKind == PAYMENT_KIND_TELG)  or
               (memorder_rmprop.PaymentKind == ""               ) )
             memorder_rmprop.PaymentKind = PAYMENT_KIND_ELEC; 
           end;                           
       elif( (memorder_rmprop.PaymentKind == PAYMENT_KIND_ELEC)  or
             (memorder_rmprop.PaymentKind == ""               ) )
                      MsgBox("Запрещено проводить электронные платежи");
                      return 1;
       end;
       if( (not ( РазрешеноСрочно1 and РазрешеноСрочно2 and РазрешеноСрочно3 ))  and
                ( memorder_rmprop.PaymentKind == PAYMENT_KIND_INST) )
                      MsgBox("Запрещено проводить срочные платежи");
                      return 1;       
       end;

  end;
  return 0;
END;
