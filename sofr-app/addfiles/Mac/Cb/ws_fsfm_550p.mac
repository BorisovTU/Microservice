/*
 $Name: ws_fsfm_550p.mac
 $Module: RS-Connect
 $Description: Проверка наличия отказов в обслуживании
 */
 /* 
	Проверка наличия отказов в обслуживании
 */
import BankInter, "cb_sql.mac", "globals.mac", rslx, rcw, rsexts;
import "ws_fsfm_param_lib.mac", "TSimpleExtService.mac", "cb_sql.mac", "common_service_caller.mac";

private class (CServiceCallerBase) CSingleRejectQueryCaller(_Param)
   private var m_param = _Param;

   private macro SetRequestParameters(request : @TXRRequest)
      var inParams = TArray();
      m_param.QueryParam.MessageID = SubStr(request.ReqID, 2, 36);
      inParams[inParams.size] = m_param;
      request.Parms = inParams;
   end;
   
   private macro GetSystemURL() : String
    var m_systemURL;
      GetRegistryValue ("RS-CONNECT\\SSUSAGE_SNILS\\URL", V_STRING, m_systemURL);
    return m_systemURL;
   end;   

   InitCServiceCallerBase(CreateGUID(), GetSystemURL(), "ws_fsfm_rjsrvmsg_search", "FSFM_SearchOperationForClient", 300000);
end;

private macro CastSingleAnswer(answer : Variant) : TFSFMOperationInfoShortOut
    var answerCast = TFSFMResponseSearchOperation();
    answerCast.AnswerParam.SenderID      = answer.AnswerParam.SenderID;
    answerCast.AnswerParam.MessageID     = answer.AnswerParam.MessageID;
    answerCast.AnswerParam.SenderPointID = answer.AnswerParam.SenderPointID;
    answerCast.SelectResult = Tarray;
    
    
    for (var info, answer.SelectResult)
        var infoCast        = TFSFMOperationInfoShortOut();
        infoCast.CL_ID      = info.CL_ID;
        infoCast.RowID      = info.RowID;
        infoCast.IdentMess  = info.IdentMess;
        infoCast.DateMess   = info.DateMess;
        infoCast.TypeRecord = info.TypeRecord;
        infoCast.SignUse    = info.SignUse;
        infoCast.TypeClient = info.TypeClient;
        infoCast.IsRezident = info.IsRezident;
        infoCast.INN        = info.INN;
        infoCast.KPP        = info.KPP;
        infoCast.FullName   = info.FullName;
        infoCast.LastName   = info.LastName;
        infoCast.FirstName  = info.FirstName;
        infoCast.Patronymic = info.Patronymic;
        infoCast.BirthDate  = info.BirthDate;
        infoCast.CodeDoc    = info.CodeDoc;
        infoCast.SeriesDoc  = info.SeriesDoc;
        infoCast.NumDoc     = info.NumDoc;
        infoCast.DateDoc    = info.DateDoc;
        infoCast.CodeReject = info.CodeReject;
        infoCast.DateReject = info.DateReject;
        infoCast.OperAttr   = info.OperAttr;
        infoCast.MoneyAttr  = info.MoneyAttr;
        infoCast.DULStr     = info.DULStr;
        infoCast.CodeNO     = TArray;
        
        for (var codeNO, info.CodeNO)
            infoCast.CodeNO[infoCast.CodeNO.size] = codeNO;
        end;

        infoCast.CodesPrO     = TArray;
        if(info.CodesPrO != NULL)
          for (var CodesPrO, info.CodesPrO)
              infoCast.CodesPrO[infoCast.CodesPrO.size] = CodesPrO;
          end;
        end;

        infoCast.PrUd  = info.PrUd;

        answerCast.SelectResult[answerCast.SelectResult.size] = infoCast;
      end;
      
      return answerCast;
end;

private macro FillInformationScroll(obj : @variant)
    file fmrjmsgservice("fmrjmsgservice.tmp") write;
    var i = 0;
    var j = 0;

    while (i < obj.SelectResult.size)
        ClearRecord (fmrjmsgservice);
        var SelectResult = obj.SelectResult[i];
        fmrjmsgservice.ClientType = SelectResult.TypeClient;
        fmrjmsgservice.INN = SelectResult.INN;
        fmrjmsgservice.KPP = SelectResult.KPP;
        fmrjmsgservice.birthdate = SQL_ConvTypeDate(SelectResult.birthdate);
        fmrjmsgservice.CodeDoc = SelectResult.CodeDoc;
        fmrjmsgservice.seriesDoc = SelectResult.SeriesDoc;
        fmrjmsgservice.NumDoc = SelectResult.NumDoc;
        fmrjmsgservice.DULStr = SelectResult.DULStr;
        fmrjmsgservice.IdentMess = SelectResult.IdentMess;
        fmrjmsgservice.DateMess = SQL_ConvTypeDate(SelectResult.DateMess);
        fmrjmsgservice.TypeRecord = SelectResult.TypeRecord;
        fmrjmsgservice.CodeReject = SelectResult.CodeReject;
        fmrjmsgservice.DateReject = SQL_ConvTypeDate(SelectResult.DateReject);
        
        if (strlen(SelectResult.FullName) == 0)
            fmrjmsgservice.FullName = SelectResult.LastName + " " + SelectResult.FirstName + " " + SelectResult.Patronymic;
        else
            fmrjmsgservice.FullName = SelectResult.FullName;
        end;

        j = 0;
        while(j < SelectResult.CodesPrO.size)
          if(j > 0)
            fmrjmsgservice.RejectReasonCode = fmrjmsgservice.RejectReasonCode + ";";
          end;
          fmrjmsgservice.RejectReasonCode = fmrjmsgservice.RejectReasonCode + SelectResult.CodesPrO[j];
          j = j + 1;
        end;

        Insert(fmrjmsgservice);
        i = i + 1;
    end;
end;

macro FSFM_SearchOperationForClient(
    SenderId, url,
    CL_ID,         // (K: 0-1) ID клиента в АС
    BegDate,       // (K: 0-1) Дата начала периода проверки
    EndDate,       // (K: 0-1) Дата окончания периода проверки
    TypeClient,    // K: (1) Тип клиента
    INN,           // (K: 0-1) ИНН клиента
    KPP,           // (K: 0-1) КПП клиента
    FullName,      // (K: 0-1) Наименование (Может использоваться как для Юр. Лица, так и для Физ. лица)
    LastName,      // (K: 0-1) Фамилия
    FirstName,     // (K: 0-1) Имя
    Patronymic,    // (K: 0-1) Отчество
    birthdate,     // (K: 0-1) Дата рождения
    CodeDoc,       // (K: 0-1) Код документа, удостоверяющего личность в соответствии со справочником кодов видов ДУЛ
    NumDoc,        // (K: 0-1) Номер документа, удостоверяющего личность 
    DateDoc,
    NoGui,
    errtext : @string
)       
//begin
    var mes = TFSFMRequestSearchOperation;
    mes.QueryParam.SenderID = SenderId;
    mes.QueryParam.MessageID = CreateGUID();
    mes.QueryParam.SenderPointID = "";
        
    mes.SelectParam = TFSFMBodySearchOperationIn;
    mes.SelectParam.CL_ID = CL_ID;
    mes.SelectParam.BegDate = BegDate;
    mes.SelectParam.EndDate = EndDate;
    mes.SelectParam.TypeClient = TypeClient;
    if (Index(INN, "/") == 0)
        mes.SelectParam.INN = INN;
        mes.SelectParam.KPP = KPP;
    else
        SplitFullINN(INN, mes.SelectParam.INN, mes.SelectParam.KPP);
    end;
    
    mes.SelectParam.FullName = FullName;
    mes.SelectParam.LastName = LastName;
    mes.SelectParam.FirstName = FirstName;
    mes.SelectParam.Patronymic = Patronymic;
    mes.SelectParam.birthdate = birthdate;
    mes.SelectParam.CodeDoc = CodeDoc;
    mes.SelectParam.NumDoc = NumDoc;
    mes.SelectParam.DateDoc = DateDoc;
    
    var caller = CSingleRejectQueryCaller(mes);
    var answer;
    var CallService_stat = caller.CallService(@answer);
    var obj = CastSingleAnswer(answer);
    FillInformationScroll(obj);
    
    return true;
OnError(err)
    if (ValType(err.Err) != V_UNDEF)
        errtext = err.Err;
    else
        errtext = err.Message;
    end;

    if (not NoGui)
        MsgBox (errtext);
    end;
    return false;
end;