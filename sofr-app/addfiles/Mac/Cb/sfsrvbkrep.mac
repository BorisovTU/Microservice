/*
$Name:           sfsrvbkrep.mac
$Module:         Ядро ГКБО
$Description:    ПЗО: Протокол отката начисления и оплаты обслуживания
*/

import RSD;

private macro PrintHeader()
[                   Протокол отката начисления и оплаты обслуживания];
end;


private macro PrintLogRecord( ContrNum, ContrDate:date, ErMsg, ErrCode )

  var LogText = "Откат операции по Договору №" + ContrNum + " от " + ContrDate;
  array StringArray;
  var i = 0;

  if( ErrCode == 0 )
    LogText = LogText + " выполнен.";
  else
    LogText = LogText + " не выполнен. Ошибка: " + ErMsg +".";    
  end;

  StrSplit( LogText, StringArray, 100 );
  while( StrLen(StringArray(i) ) > 0 )    
[######################################################################################################] ( StringArray(i) );
    i = i + 1;
  end;

end;

macro SfSrvBackOutReport()

  var rs, cmd;

  PrintHeader();

  var opKind = 10; /*откат*/

  cmd = RsdCommand( " SELECT t_ContrNum, t_BeginDate, t_Comment, t_ErrorCode "
    " FROM DSFREPACC_TMP WHERE t_Kind = ? " );
  cmd.addParam( "", RSDBP_IN, opKind );

  rs = RsdRecordset( cmd );
  while( rs.moveNext() )
     PrintLogRecord( 
       rs.value("t_ContrNum"),
       rs.value("t_BeginDate"),
       rs.value("t_Comment"),
       rs.value("t_ErrorCode")
       );
  end;

end;

////////////
private const BACK_OUT_ACR = 10; //откат начисления
private const BACK_OUT_PAY = 11; //откат оплаты
private const DEL_DEF_COM = 12;  //удаление комиссии
private const DEL_DEF_SRV_LNK = 13;  //удаление связи УПК с операцией

private macro SfDef_PrintHeader()
[   
      Протокол отката начисления и оплаты обслуживания ];
end;

private macro SfDef_PrintContrInfo( ContrNum )
[  
    Откат комиссий по ДО №################]( ContrNum );
end;

private macro SfDef_PrintLogRecord( Kind, SfComiss, BeginDate:Date, EndDate:date, ErMsg, ErrCode )

  var LogText = "", Tail = " выполнен.";

  if( Kind == BACK_OUT_ACR )
    LogText = "Откат начисления комиссии ";
  elif( Kind == BACK_OUT_PAY )
    LogText = "Откат оплаты комиссии ";
  elif( Kind == DEL_DEF_COM)
    LogText = "Удаление комиссии ";
    Tail = " выполнено.";
  else
    LogText = "Откат комиссии ";
    Tail = " выполнен.";
  end;

  LogText = LogText + SfComiss.Code;
  LogText = LogText + ", удержанной за период с " + BeginDate + " по " + EndDate + ",";

  array StringArray;
  var i = 0;

  if( ErrCode == 0 )
    LogText = LogText + Tail;
  else
    LogText = LogText + " не" + Tail + " Ошибка: " + ErMsg +".";    
  end;

  StrSplit( LogText, StringArray, 120 );
  while( StrLen(StringArray(i) ) > 0 )    
[     #######################################################################################################################] 
( StringArray(i) );
    i = i + 1;
  end;

end;


macro SfDefSrvBackOutReport()

  SfDef_PrintHeader();

  var cmd = RsdCommand( 
    " SELECT t_ContrID, t_Kind, t_BeginDate, t_EndDate, t_ContrNum, t_FeeType, t_ComissNumber, t_Comment, t_ErrorCode " +
    " FROM DSFREPACC_TMP  ORDER BY t_ContrID, t_SfDefComID, t_BeginDate DESC, t_Kind " );

  var rs = RsdRecordset( cmd );

  file SfComiss( sfcomiss ) key 0;

  SfComiss.ComissID = 0;
  var prevComissID = -1, prevContrID = -1;

  while( rs.moveNext() )

    if( prevComissID != SfComiss.ComissID )
      SfComiss.FeeType = rs.value("t_FeeType");
      SfComiss.Number  = rs.value("t_ComissNumber");

      if( not getEQ( sfcomiss ) )
        SfComiss.Code = "";
      else
        prevComissID = SfComiss.ComissID;      
      end;
    end;

    var ContrID = rs.value("t_ContrID");
    if( prevContrID != ContrID )
      SfDef_PrintContrInfo( rs.value("t_ContrNum") );
      prevContrID = ContrID;
    end;
     
    SfDef_PrintLogRecord( 
      rs.value("t_Kind"),
      SfComiss,
      rs.value("t_BeginDate"),
      rs.value("t_EndDate"),
      rs.value("t_Comment"),
      rs.value("t_ErrorCode")
    );
  end;

end;

