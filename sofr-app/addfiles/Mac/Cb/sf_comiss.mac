/*
$Name:        sf_comiss.mac
$Module:      Главная книга
$Description: Макрос скроллинга комиссий 
*/

import BankInter, PTInter, RsbDataSet;

private const OBJTYPE_SFPLAN = 57;
private const OBJTYPE_DEPARTMENT = 80;
private const OBJTYPE_SFCONTR = 659;
private const OBJTYPE_SFCOMISS = 650; // комиссия ПЗО

record DO(sfcontr);
record Old_DO(sfcontr);

record SfContrParm   ("SfContrRefParm.rec");
record SfContrParmOld ("SfContrRefParm.rec");

private macro Функция_Пользователя()
  var arr_choice = TArray();
  var choice;
  var Dt, cmd, msgtxt;

  arr_choice(arr_choice.size) = "Удалить связку договора с ДБО (для удаления ДО)";

  choice = menu (arr_choice, "Выберите действие","Выберите действие");

  if (choice == 0)
     Dt = TRsbDataSet(" select count(1) cnt from ddl_tick_dbt where t_clientcontrid = " + Do.id);
     if (Dt.movenext())
        if (Dt.cnt > 0)
           MsgBox("Нельзя удалять связку, т.к. по договору есть операции. Обратитесь к администратору системы.");
           return 0;
        end;
     end;
     Dt = null;
     Dt = TRsbDataSet(" select count(1) cnt from dpmwrtsum_dbt where t_contract = " + Do.id);
     if (Dt.movenext())
        if (Dt.cnt > 0)
           MsgBox("Нельзя удалять связку, т.к. по договору есть лоты ценных бумаг. Обратитесь к администратору системы.");
           return 0;
        end;
     end;
     Dt = null;
     Dt = TRsbDataSet(" select distinct(t_account) acc from dmcaccdoc_dbt where t_clientcontrid = " + Do.id);
     if (Dt.movenext())
        msgtxt = Dt.acc;
        while(Dt.movenext())
           msgtxt = msgtxt + "|" + Dt.acc;
        end;
        MsgBox("Будут удалены следующие привязанные счета |"+msgtxt+".");
       // return 0;
     end;

     if (GetTrue(false,"Действительно хотите удалить связку договора обслуживания с ДБО? | Откатить действие будет невозможно"))
        cmd = RsdCommand(" delete from ddlcontrmp_dbt where t_sfcontrid = " + Do.id);
        cmd.Execute();
        MsgBox("Связка с ДБО удалена. Теперь можно удалить договор.");
     end;
  end;

  return 0;
end;

private macro IsExistsConCom(SfConcom)
  if((SfConcom != NULL) AND (SfConcom.rec.FeeType != 0) AND (SfConcom.rec.CommNumber != 0))
    return true;
  else
    return false;
  end;
end;

private macro IsExistConCom(FeeType:integer, CommNumber:integer, ServiceKind:integer, ObjectType:integer, SfPlanID:integer)
  var countr = 0;
  var Sql= "SELECT count(1) numr "
           "  FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           " WHERE com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " +
           "   AND concom.t_FeeType = ?" +
           "   AND concom.t_CommNumber = ?" +
           "   AND concom.t_ObjectType = ? " +
           "   AND concom.t_SfPlanID = ?" +
           "   AND com.t_ServiceKind = ?";


  var Com = RSDCommand( sql );

  Com.AddParam("FeeType",     RSDBP_IN, FeeType );     /*1*/
  Com.AddParam("CommNumber",  RSDBP_IN, CommNumber );  /*2*/
  Com.AddParam("ObjectType",  RSDBP_IN, ObjectType );  /*3*/
  Com.AddParam("SfPlanID",    RSDBP_IN, SfPlanID );    /*4*/
  Com.AddParam("ServiceKind", RSDBP_IN, ServiceKind ); /*5*/

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;

  if(countr > 0)
    return true;
  else
    return false;
  end;
end;


private macro IsExistConComIsBankExpenses(FeeType:integer, CommNumber:integer, ServiceKind:integer, ObjectType:integer)
  var countr = 0;
  var Sql= "SELECT count(1) numr "
           "  FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           " WHERE com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " +
           "   AND concom.t_FeeType = ?" +
           "   AND concom.t_CommNumber = ?" +
           "   AND concom.t_ObjectType = ? " +
           "   AND concom.t_SfPlanID <> 0" +
           "   AND concom.t_IsBankExpenses = 'X' " +
           "   AND com.t_ServiceKind = ?";


  var Com = RSDCommand( sql );

  Com.AddParam("FeeType",    RSDBP_IN, FeeType );     /*1*/
  Com.AddParam("CommNumber", RSDBP_IN, CommNumber );  /*2*/
  Com.AddParam("ObjectType", RSDBP_IN, ObjectType );  /*3*/
  Com.AddParam("ServiceKind", RSDBP_IN, ServiceKind ); /*4*/

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;

  if(countr > 0)
    return true;
  else
    return false;
  end;
end;

private macro IsNotExistConComIsBankExpenses(FeeType:integer, CommNumber:integer, ServiceKind:integer, ObjectType:integer, IsBankExpenses:string, IsCompensationCom:string)
  var countr = 0;
  var Sql= "SELECT count(1) AS numr "+
           "  FROM dsfconcom_dbt concom, dsfcomiss_dbt com, (SELECT DISTINCT concom.t_ObjectID AS SfPlanID "+                                                                                  
           "                                                   FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           "                                                  WHERE com.t_feetype = concom.t_feetype " +
           "                                                    AND com.t_number = concom.t_commnumber " + 
           "                                                    AND concom.t_FeeType = ? "+
           "                                                    AND concom.t_CommNumber = ? "+
           "                                                    AND concom.t_ObjectType = ? "+
           "                                                    AND com.t_ServiceKind = ? " +
           "                                                    AND concom.t_SfPlanID = 0 ) pln "+
           " WHERE com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " + 
           "   AND concom.t_ObjectType = ? "+
           "   AND concom.t_ObjectID = pln.SfPlanID "+
           "   AND com.t_ServiceKind = ? " ; 
   if(IsCompensationCom != "X")
     Sql= Sql+ "AND NOT (concom.t_feetype = ? AND concom.t_CommNumber = ?)";
   end;

   Sql= Sql+ " AND concom.t_SfPlanID = 0 "+
             " AND (SELECT count(1) " ;
           
   if(IsBankExpenses == "X") // Учет записи, которую еще не сохранили
      Sql= Sql+ " + 1 ";
   end;

   Sql= Sql+ "      FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           "                               WHERE com.t_feetype = concom.t_feetype " +
           "                                 AND com.t_number = concom.t_commnumber " +
           "                                 AND concom.t_ObjectType = ? "
           "                                 AND concom.t_ObjectID = pln.SfPlanID "+
           "                                 AND com.t_ServiceKind = ? " +
           "                                 AND NOT (concom.t_feetype = ? AND concom.t_CommNumber = ?)"+  
           "                                 AND concom.t_IsBankExpenses = 'X' ) = 0 ";

  var Com = RSDCommand( sql );

  Com.AddParam("FeeType",    RSDBP_IN, FeeType );       /*1*/
  Com.AddParam("CommNumber", RSDBP_IN, CommNumber );    /*2*/
  Com.AddParam("ObjectType1", RSDBP_IN, ObjectType );   /*3*/
  Com.AddParam("ServiceKind", RSDBP_IN, ServiceKind );  /*4*/
  Com.AddParam("ObjectType2", RSDBP_IN, ObjectType );   /*5*/
  Com.AddParam("ServiceKind2", RSDBP_IN, ServiceKind ); /*6*/
  if(IsCompensationCom != "X")
    Com.AddParam("FeeType2",     RSDBP_IN, FeeType );     /*9*/
    Com.AddParam("CommNumber2",  RSDBP_IN, CommNumber );  /*10*/
  end;
  Com.AddParam("ObjectType3",  RSDBP_IN, ObjectType );  /*7*/
  Com.AddParam("ServiceKind3", RSDBP_IN, ServiceKind ); /*8*/
  Com.AddParam("FeeType3",     RSDBP_IN, FeeType );     /*9*/
  Com.AddParam("CommNumber3",  RSDBP_IN, CommNumber );  /*10*/


  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;

  if(countr > 0)
    return true;
  else
    return false;
  end;
end;

private macro IsNotExistConComIsBankExpensesDel(FeeType:integer, CommNumber:integer, ServiceKind:integer, ObjectType:integer)
  var countr = 0;

  var Sql= "SELECT count(1)  AS numr " +
           "FROM dsfconcom_dbt concom, " +
           "     dsfcomiss_dbt com, " +
           "(SELECT DISTINCT pln.SfPlanID  " +
           "  FROM dsfconcom_dbt concom, " +
           "       dsfcomiss_dbt com, " +
           "       (SELECT DISTINCT concom.t_ObjectID AS SfPlanID " +
           "          FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           "         WHERE     com.t_feetype = concom.t_feetype " +
           "               AND com.t_number = concom.t_commnumber " +
           "               AND concom.t_FeeType = ? " +
           "               AND concom.t_CommNumber = ? " +
           "               AND concom.t_ObjectType = ? " +
           "               AND com.t_ServiceKind = ? " +
           "               AND concom.t_SfPlanID = 0) pln " +
           " WHERE     com.t_feetype = concom.t_feetype " +
           "       AND com.t_number = concom.t_commnumber " +
           "       AND concom.t_ObjectType = ? " +
           "       AND concom.t_ObjectID = pln.SfPlanID " +
           "       AND com.t_ServiceKind = ? " +
           "       AND concom.t_SfPlanID = 0 " +
           "       AND concom.t_IsCompensationCom = 'X' " +
           "       AND NOT (concom.t_feetype = ? AND concom.t_CommNumber = ?) " +
           ") plna        " +
           "WHERE  com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " +
           "   AND concom.t_ObjectType = ? " +
           "   AND concom.t_ObjectID = plna.SfPlanID " +
           "   AND com.t_ServiceKind = ? " +
           "   AND concom.t_IsCompensationCom = 'X' " +
           "   AND NOT (concom.t_feetype = ? AND concom.t_CommNumber = ?) " +
           "   AND NOT EXISTS " +
           "                  (SELECT 1 " +
           "                     FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           "                    WHERE     com.t_feetype = concom.t_feetype " +
           "                          AND com.t_number = concom.t_commnumber " +
           "                          AND concom.t_ObjectType = ? " +
           "                          AND concom.t_ObjectID = plna.SfPlanID " +
           "                          AND com.t_ServiceKind = ? " +
           "                          AND concom.t_IsBankExpenses = 'X' " +
           "                          AND NOT (concom.t_feetype = ? AND concom.t_CommNumber = ?))  " ;

  var Com = RSDCommand( sql );

  Com.AddParam("FeeType1",    RSDBP_IN, FeeType    );       
  Com.AddParam("CommNumber1", RSDBP_IN, CommNumber );    
  Com.AddParam("ObjectType1", RSDBP_IN, ObjectType );   
  Com.AddParam("ServiceKind1",RSDBP_IN, ServiceKind);  

  Com.AddParam("ObjectType2", RSDBP_IN, ObjectType );   
  Com.AddParam("ServiceKind2",RSDBP_IN, ServiceKind); 
  Com.AddParam("FeeType2",    RSDBP_IN, FeeType    );       
  Com.AddParam("CommNumber2", RSDBP_IN, CommNumber );    

  Com.AddParam("ObjectType3", RSDBP_IN, ObjectType );  
  Com.AddParam("ServiceKind3",RSDBP_IN, ServiceKind); 
  Com.AddParam("FeeType3",    RSDBP_IN, FeeType    );      
  Com.AddParam("CommNumber3", RSDBP_IN, CommNumber );   

  Com.AddParam("ObjectType4", RSDBP_IN, ObjectType );  
  Com.AddParam("ServiceKind4",RSDBP_IN, ServiceKind); 
  Com.AddParam("FeeType4",    RSDBP_IN, FeeType    );      
  Com.AddParam("CommNumber4", RSDBP_IN, CommNumber );   

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;

  if(countr > 0)
    return true;
  else
    return false;
  end;
end;


private macro IsExistComNotORB(ObjectType:Integer, ObjectID:integer, ServiceKind:integer, NameField:string, countnum, SfPlanID ) // NameField = "t_IsCompensationCom" или "t_IsBankExpenses"
  var countr = 0;
  var Sql= "SELECT count(1) numr " +
           "  FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           " WHERE com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " +
           "   AND concom.t_ObjectType = ? " +
           "   AND concom.t_ObjectID = ? " +        
           "   AND concom."+NameField+" != 'X' " +
           "   AND com.t_ServiceKind = ?" +
           "   AND com.t_receiverid in (2, 151337)" +
           "   AND com.t_number <1000" +
           "   AND com.t_feetype = 1";


  if(SfPlanID != NULL)
    Sql = Sql + "   AND concom.t_SfPlanID = ?";
  end;

  var Com = RSDCommand( sql );

  Com.AddParam("ObjectType", RSDBP_IN,  ObjectType );  /*1*/
  Com.AddParam("ObjectID"  , RSDBP_IN,  ObjectID );    /*2*/
  Com.AddParam("ServiceKind", RSDBP_IN, ServiceKind ); /*3*/

  if(SfPlanID != NULL)
    Com.AddParam("SfPlanID", RSDBP_IN, SfPlanID );     /*4*/
  end;

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;



  if(countr > 0)
    return true;
  else
    return false;
  end;
end;

private macro IsExistComFlag(ObjectType:Integer, ObjectID:integer, ServiceKind:integer, NameField:string, countnum, SfPlanID ) // NameField = "t_IsCompensationCom" или "t_IsBankExpenses"
  var countr = 0;
  var Sql= "SELECT count(1) numr " +
           "  FROM dsfconcom_dbt concom, dsfcomiss_dbt com " +
           " WHERE com.t_feetype = concom.t_feetype " +
           "   AND com.t_number = concom.t_commnumber " +
           "   AND concom.t_ObjectType = ? " +
           "   AND concom.t_ObjectID = ? " +        
           "   AND concom."+NameField+" = 'X' " +
           "   AND com.t_ServiceKind = ?";

  if(SfPlanID != NULL)
    Sql = Sql + "   AND concom.t_SfPlanID = ?";
  end;

  var Com = RSDCommand( sql );

  Com.AddParam("ObjectType", RSDBP_IN,  ObjectType );  /*1*/
  Com.AddParam("ObjectID"  , RSDBP_IN,  ObjectID );    /*2*/
  Com.AddParam("ServiceKind", RSDBP_IN, ServiceKind ); /*3*/

  if(SfPlanID != NULL)
    Com.AddParam("SfPlanID", RSDBP_IN, SfPlanID );     /*4*/
  end;

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;


  var compnum = 0;
  if(countnum != NULL)
    compnum = countnum; 
  end;

  if(countr > compnum)
    return true;
  else
    return false;
  end;
end;

private macro IsPartyMarket(ReceiverID)
  var countr = 0;
  var Sql= "SELECT count(1) numr " +
           "  FROM dpartyown_dbt ptown " +
           " WHERE ptown.t_PartyID = ?" +
           "   AND ptown.t_PartyKind = ? " ; // PTK_MARKETPLASE

  var Com = RSDCommand( sql );

  Com.AddParam("PartyID",   RSDBP_IN, ReceiverID      );   /*1*/
  Com.AddParam("PartyKind", RSDBP_IN, PTK_MARKETPLASE );   /*2*/

  var rsd = TRsbDataSet(Com);
  if( rsd.MoveNext() )
    countr = rsd.numr;
  end;

  if(countr > 0)
    return true;
  else
    return false;
  end;
end;


private macro InitComiss(SfComiss:TRecHandler, SfConcom:TRecHandler)
  return 0;
end;

// Mode:
//  OBJ_DELETE (1) - удаление объекта;
//  OBJ_INSERT (2) - ввод объекта в систему;
//  OBJ_UPDATE (3) - корректировка объекта;
private macro CheckComiss(Mode:integer, SfComiss:TRecHandler, SfConcom:TRecHandler, SfComissOld:TRecHandler, SfConcomOld:TRecHandler)
    var stat = 0;

  if(Mode == OBJ_INSERT)
    //  Необходимо установить ограничение на добавление признака ОРБ (отнесение на расходы банка). Флаг можно устанавливать только комиссиям, в которых получатель = Биржа
    if((stat == 0) AND (((NOT IsExistsConCom(SfConcom)) AND (SfComiss.rec.IsBankExpenses == "X")) OR (IsExistsConCom(SfConcom) AND (SfConcom.rec.IsBankExpenses == "X"))) AND
       (NOT IsPartyMarket(SfComiss.rec.ReceiverID))
      )
      
      Msgbox("Изменение запрещено");
      stat = 1;
    end;                                                                                                                                   
  end;


  if(Mode == OBJ_UPDATE)
    //  Необходимо установить ограничение на добавление признака ОРБ (отнесение на расходы банка). Флаг можно устанавливать только комиссиям, в которых получатель = Биржа
    if((stat == 0) AND (((NOT IsExistsConCom(SfConcom)) AND (NOT IsExistsConCom(SfConcomOld)) AND (SfComiss.rec.IsBankExpenses == "X") ) OR
                        (IsExistsConCom(SfConcom) AND IsExistsConCom(SfConcomOld) AND (SfConcom.rec.IsBankExpenses == "X") )) AND
                        (NOT IsPartyMarket(SfComiss.rec.ReceiverID))
      )
      
      Msgbox("Изменение запрещено");
      stat = 1;
    end;                                                                                                                                   


    //  Необходимо установить ограничение на снятие признака ОРБ (отнесение на расходы банка), если в тарифном плане есть компенсационная комиссия комиссия по этому же виду обслуживания

    if((stat == 0) AND IsExistsConCom(SfConcom) AND IsExistsConCom(SfConcomOld) AND (SfConcom.rec.ObjectType == OBJTYPE_SFPLAN)
      )
       if((stat == 0) AND (SfConcom.rec.IsBankExpenses == "") AND  (SfConcomOld.rec.IsBankExpenses == "X")  
           AND (IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsCompensationCom", 0 ))  
         )

         Msgbox("Нельзя снять признак 'Отнесение на расходы банка' с комиссии при наличии в тарифном плане коспенсационной комиссии этого же вида осблуживания");
         stat = 1;
       end;
    end;


    // Запрещаем изменяить флаги для локализованной комиссии ДО
    if((stat == 0) AND IsExistsConCom(SfConcom) AND IsExistsConCom(SfConcomOld) AND (SfConcom.rec.ObjectType == OBJTYPE_SFCONTR) AND // (SfConcom.rec.SfPlanID == 0) AND 
       ((SfConcom.rec.IsBankExpenses != SfConcomOld.rec.IsBankExpenses) OR (SfConcom.rec.IsCompensationCom != SfConcomOld.rec.IsCompensationCom)) 
      )
      Msgbox("Изменение запрещено");
      stat = 1;
    end;                                                                                                                                   

    // 
    if((stat == 0) AND IsExistsConCom(SfConcom) AND IsExistsConCom(SfConcomOld) AND (SfConcom.rec.ObjectType == OBJTYPE_DEPARTMENT) AND (SfConcom.rec.SfPlanID > 0) AND
       ((SfConcom.rec.IsBankExpenses != SfConcomOld.rec.IsBankExpenses) OR (SfConcom.rec.IsCompensationCom != SfConcomOld.rec.IsCompensationCom)) 
      ) 
      // для Компенсационной комиссии обязательно наличие комиссии с установленным флагом "Отнесение на расходы банка"
      if((stat == 0) AND (SfConcom.rec.IsBankExpenses == "") AND  (SfConcomOld.rec.IsBankExpenses == "X")  
          AND (IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsCompensationCom", 0, SfConcom.rec.SfPlanID ))  
          AND (NOT IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses", 1, SfConcom.rec.SfPlanID)) // 1 - проверяем, что удаляем не последнюю
        )

        Msgbox("Изменение запрещено");
        stat = 1;
      end;

      // для Компенсационной комиссии обязательно наличие комиссии с установленным флагом "Отнесение на расходы банка"
      if((stat == 0) AND (SfConcom.rec.IsCompensationCom == "X") AND  (SfConcomOld.rec.IsCompensationCom == "")  
          AND (NOT IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses", 0, SfConcom.rec.SfPlanID))
        )

        Msgbox("Изменение запрещено");
        stat = 1;
      end;
    end;

    // Нельзя установить признак <ОРБ> или <компенсационная> на комиссию, если она включена в список комиссий  хотя бы одного договора с индивидуальными условиями. 
    if((stat == 0) AND (NOT IsExistsConCom(SfConcom)) AND ((SfComiss.rec.IsBankExpenses != SfComissOld.rec.IsBankExpenses) OR (SfComiss.rec.IsCompensationCom != SfComissOld.rec.IsCompensationCom))) 
      if(IsExistConCom(SfComiss.rec.FeeType, SfComiss.rec.Number, SfComiss.rec.ServiceKind, OBJTYPE_SFCONTR, 0))
        Msgbox("Изменение запрещено");
        stat = 1;
      end;
    end;

    //  Нельзя установить признак <Компенсационная комиссия> на комиссию, если она входит в состав хотя бы одного ТП, где нет комиссии с признаком ОРБ  
    if((stat == 0) AND (NOT IsExistsConCom(SfConcom)) AND (((SfComiss.rec.IsBankExpenses  == "") AND (SfComissOld.rec.IsBankExpenses == "X")) OR ( (SfComiss.rec.IsCompensationCom == "X" ) AND ( SfComissOld.rec.IsCompensationCom == "")))
       AND (NOT((SfComiss.rec.IsBankExpenses == "X") AND (SfComiss.rec.IsCompensationCom == "X")))  ) 
      if(IsNotExistConComIsBankExpenses(SfComiss.rec.FeeType, SfComiss.rec.Number, SfComiss.rec.ServiceKind,  OBJTYPE_SFPLAN, SfComiss.rec.IsBankExpenses, SfComiss.rec.IsCompensationCom))
        Msgbox("Изменение запрещено");
        stat = 1;
      end;
    end;

    //   Нельзя снять признак ОРБ с комиссии, если после этого хотя бы в одном ТП, содержащем компенсационную комиссию,  не останется ни одной комиссии с ОРБ. 
    if((stat == 0) AND (NOT IsExistsConCom(SfConcom)) AND ( (SfComiss.rec.IsBankExpenses == "" ) AND ( SfComissOld.rec.IsBankExpenses == "X"))) 
      if(IsNotExistConComIsBankExpensesDel(SfComiss.rec.FeeType, SfComiss.rec.Number, SfComiss.rec.ServiceKind, OBJTYPE_SFPLAN))
        Msgbox("Изменение запрещено");
        stat = 1;
      end;
    end;

    //  Нельзя установить признак <Компенсационная комиссия> на комиссию, если она входит в состав ТП, где нет комиссии с признаком ОРБ  по этому виду обслуживания
    if((stat == 0) AND IsExistsConCom(SfConcom) AND IsExistsConCom(SfConcomOld) AND (SfConcom.rec.ObjectType == OBJTYPE_SFPLAN))
       if((stat == 0) AND (SfConcom.rec.IsCompensationCom == "X") AND  (SfConcomOld.rec.IsCompensationCom == "")
           AND (IsExistComNotORB(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses"))
         )
         Msgbox("Нельзя установить признак \"Компенсационная\", на комиссию если она входит в состав ТП, где нет комиссии с признаком \"Отнесение на расходы Банка\" по этому виду обслуживания");
         stat = 1;
       end;
    end;

  end;

  if(Mode == OBJ_DELETE)
    if((stat == 0) AND IsExistsConCom(SfConcom) AND (SfConcom.rec.ObjectType == OBJTYPE_SFPLAN) AND (SfConcom.rec.IsBankExpenses == "X") 
        AND (IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsCompensationCom")) 
        AND (NOT IsExistComFlag(SfConcom.rec.ObjectType, SfConcom.rec.ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses", 1)) // 1 - проверяем, что удаляем не последнюю
      )
      Msgbox("Нельзя исключить из ТП комиссию с признаком \"Отнесение на расходы Банка\", имеющем в своем составе компенсационную комиссию, если после этого в ТП не останется ни одной комиссии с таким признаком");
      stat = 1;
    end;
  end;

  return stat;
end;

private macro CheckSelectComiss(SfComiss:TRecHandler, ObjectType:Integer, ObjectID:integer, SfPlanID:integer)
  var stat = 0;

  if((stat == 0) AND (ObjectType == OBJTYPE_SFCONTR) AND (SfPlanID == 0) AND ((SfComiss.rec.IsBankExpenses == "X") OR (SfComiss.rec.IsCompensationCom == "X")))
    Msgbox("Не допускается включение в состав плана с индивидуальными условиями комиссий, имеющих установленный признак \"Компенсационная комиссия\" или \"Отнесение на расходы Банка\"");
    stat = 1;
  end;
/*
  if((stat == 0) AND (ObjectType == OBJTYPE_SFPLAN) AND (SfComiss.rec.IsCompensationCom == "X") AND (NOT IsExistComFlag(ObjectType, ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses")))
    Msgbox("Нельзя включить в ТП комиссию с признаком \"Компенсационная\", если в нем нет ни одной комиссии с признаком \"Отнесение на расходы Банка\"");
    stat = 1;
  end;
*/

  if((stat == 0) AND (ObjectType == OBJTYPE_SFPLAN) AND (SfComiss.rec.IsCompensationCom == "X") AND (IsExistComNotORB(ObjectType, ObjectID, SfComiss.rec.ServiceKind, "t_IsBankExpenses")))
    Msgbox("Нельзя включить в ТП комиссию с признаком \"Компенсационная\", если в нем есть комиссия без признака \"Отнесение на расходы Банка\" того же вида обслуживания");
    stat = 1;
  end;

  
  return stat;
end;



