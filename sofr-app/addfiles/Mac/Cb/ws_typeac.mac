import globals;
import RsbDataSet;  
import CurrInter, XmlRpcInter;   
import oralib;
import serviceaction;
import rcw;
import dlwreps;
import rsd;


class TWsTypeAc()
  var iNumType;
  var cType;
  var Name_Type;
end;

/* Создание объекта TWsTypeAc через параметры */
macro CreateTypeAcFromParam(iNumType, cType, Name_Type)
  var typeAcItem : TWsTypeAc;

  typeAcItem.iNumType   = iNumType;
  typeAcItem.cType      = cType;
  typeAcItem.Name_Type  = Name_Type;

  return typeAcItem;
end;

/* Создание объекта TWsTypeAc с подкачкой Name_Type из базы */
macro CreateTypeAcFromAppServ(iNumType, cType)
  var typeAcItem : TWsTypeAc = NULL;

  var cmd : RsdCommand, dataSet;

  cmd.cmdText = "select t_name_type from dtypeac_dbt WHERE t_inumtype = ? and t_type_account = ?";

  cmd.AddParam("", RSDBP_IN,  iNumType);
  cmd.AddParam("", RSDBP_IN,  cType);

  cmd.execute();

  dataSet = RsdRecordset(cmd);

  if(dataSet.moveNext())
    typeAcItem = TWsTypeAc();

    typeAcItem.Name_Type  = dataSet.value(0);

    typeAcItem.iNumType   = iNumType;
    typeAcItem.cType      = cType;
  end;

  return typeAcItem;
end;
    
/* Получение списка из базы */       
macro GetListTypeAc(iNumType : integer)                   
  var cmd : RsdCommand, outArray = TArray(), tempObj = TWsTypeAc(), dataSet;
                                   
  if(iNumType != NULL)

    cmd.cmdText = "select t_type_account, t_name_type from dtypeac_dbt WHERE t_inumtype = ?";

    cmd.AddParam("", RSDBP_IN,  iNumType);

    cmd.execute();

    dataSet = RsdRecordset(cmd);

    while(dataSet.moveNext())
      tempObj           = TWsTypeAc();
      tempObj.iNumType  = iNumType;
      tempObj.cType     = dataSet.value(0);
      tempObj.Name_Type = dataSet.value(1);

      outArray[outArray.size] = tempObj;
    end;
    
  else              
    var err = "Не задан номер типа";
                                                                 
    RunError(err);
  end;
                                        
  return outArray;  
end;
