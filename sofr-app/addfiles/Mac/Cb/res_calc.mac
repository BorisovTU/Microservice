/*
$Name:           res_calc.mac
$Module:         Ядро ГКБО
$Description:    Расчет резерва
*/

Import "res_fun.mac", "globals.mac";

/* Класс - расчет резерва */
class CalcReserve

  /* Значения резерва */
  var Reserve              : money;
  var ReserveLossValue     : money;
  var ReserveLoansValue    : money;
  var ReserveOffshoreValue : money;
  var ReserveEstimatedValue : money;

  /* Параметры, необходимые для расчета всех видов резерва */
  var DateReserve : date; /* Дата резервирования */
  var DateRate    : date; /* Дата курса */
  var RestReserve : money; /* Остаток по счету резерва на дату резервирования */

  var AccountReserve : string /* Счет резерва */;

  var RsvClass         : string; /* Классификация по РВП */
  var RsvClassLoans    : string; /* Классификация по РВПС */
  var RsvClassOffshore : string; /* Классификация по РОФШ */

  var Rest : money; /* Остаток объекта, по которому создается резерв */
  var Rest_DateReserve : money; /* Остаток объекта, по которому создается резерв на дату резервирования */
  var Rest_DateRate    : money; /* Остаток объекта, по которому создается резерв на дату курса          */

  var ProcentReserve : double; /* Процент резервирования */
  var ProcentReserveContr : double; /* Процент резервирования по контрагенту */
  var ProcentReserveEstimated : double; /* Процент оценочного резерва */
  var RsvRepoCostCB : integer; /* Значение настройки CB\РЕЗЕРВЫ\РВП ПО ПР. РЕПО */

  /* Дополнительные параметры для расчета РВП */
  var MarketCost         : money; /* текущая рыночная стоимость */
  var MarketCostSecurity : money; /* сумма текущих рыночная стоимостей для всех счетов обеспечения */
  var RestObligation     : money; /* остаток на счете обязательств  */
  var RestAmortization   : money; /* остаток на счете амортизации  */
  var RestCurrency       : money; /* остаток за дату формирования резерва по счету, парному тому, по которому создается резерв для двухвалютных сделок - конверсия валюты за валюту */

  var RestPosRevalue   : money; /* остаток на счете положительной переоценки */
  var RestNegRevalue   : money; /* остаток на счете отрицательной переоценки */

  var RestAccountProcent : money; /* остаток на счете процентов */

  var FreeReserveSum     : money; /* нерезервируемая сумма */
  var FreeReserveSum_DateReserve : money; /* нерезервируемая сумма на дату резервирования */
  var FreeReserveSum_DateRate    : money; /* нерезервируемая сумма на дату курса          */

  var ConsiderMinPercent            : bool; /*учитывать минимальный процент резерва для л/с*/
  var MinPercentReserve             : double; /* минимальный процент резерва */
  var MinPercentReserve_DateReserve : double; /* минимальный процент резерва на дату резервирования */
  var MinPercentReserve_DateRate    : double; /* минимальный процент резерва на дату курса          */

  /* Дополнительные параметры для расчета РВПС */
  
  /* категория качества обеспечения для счета, по которому рассчитывается резерв */
  var EnsuringCategory : integer;
  /* сумма текущих рыночная стоимостей для всех счетов обеспечения с учетом качества обеспечения */
  var MarketCostSecurityByCategory : money;
  
  /* Дополнительные параметры для расчета РОФШ */
  /* (кроме этих параметров необходимы также все, используемые при расчете РВПС) */
  var GroupRisk              : integer; /* Группа риска */
  var ProcentReserveOffshore : double;  /* Процент резервирования - офшор */
  var MaxRest                : money;   /* Максимальный остаток объекта, по которому создается резерв, за отчетный период */
  var DebetTurn              : money;   /* среднедневной расчетный дебетовый оборот */
  var MaxRest30WorkDay       : money;   /* максимальный остаток по счету в течение последних 30 календарных дней (согласно 1584-У)*/
  var DebetTurn30Day         : money;   /* среднедневной расчетный дебетовый оборот за последние 30 календарных дней (согласно 1584-У)*/

  var RestLoanOverdraft      : money;   /* остаток на счете ссудной задолженности  */
  /* сумма текущих рыночная стоимостей для всех счетов обеспечения с учетом качества обеспечения для счета ссудной задолженности */
  var MarketCostSecurityByCategoryLoanOverdraft : money;

  var РасчетРезерваПросроченныхТребований : bool;
  var РасчетРезерваНепросроченныхТребований : bool;
  var СуммаПросроченныхТребований             : money; /* Cумма простроченных требований                        */
  var СуммаПросроченныхТребований_DateReserve : money; /* Cумма простроченных требований на дату резервирования */
  var СуммаПросроченныхТребований_DateRate    : money; /* Cумма простроченных требований на дату курса          */
  var ПроцентРезерваПросроченныхТребований : double;
  var КлиентСчета : integer;
  var ReserveKind : integer;
  var PartyTaxGroup : integer; /*клиент счета - Участник консолидированной группы налогоплательщиков?*/

  var SumReserveCalcUser : money; /*значение примечания счета "Сумма резерва, рассчитанная вручную"*/
  var SumReserveCalcUser_DateReserve : money; /*значение примечания счета на дату резервирования */
  var SumReserveCalcUser_DateRate    : money; /*значение примечания счета на дату курса          */
  
  var СуммаОстатковСчетовРазмещенныхСредств : money; /*остатки счетов, для которых резервируемый счет является счетом обеспечения*/

  var SolidDeposit : integer; /*значение категории Права на ц/б учитываются в надежном депозитарии*/
  
/* Частные функции класса */
  private macro ValidClass( ClassCode : string ) : bool
    return ((ClassCode != null) and (ClassCode != ""));
  end;

/* Общие функции класса */
  
  /*
   * Расчет резерва на возможные потери
   */
  macro CalcReserveLoss() : money

    var ReserveTmp : money;

    if( RsvClass != null )

      if(DateReserve < date(01,10,2005) )
      /*действуют 232-П*/
        if( (SubStr(RsvClass, 1, 3) == "2.1")
         or (SubStr(RsvClass, 1, 3) == "2.2")
         or (SubStr(RsvClass, 1, 3) == "2.3")
         or (SubStr(RsvClass, 1, 3) == "2.5")
         or (SubStr(RsvClass, 1, 3) == "5.1")
         or (SubStr(RsvClass, 1, 3) == "1.9")
          )
        
          ReserveLossValue = money( Rest * ProcentReserve / 100 );
  
        elif( (SubStr(RsvClass, 1, 3) == "4.1") )
        
          ReserveLossValue = money( (Rest - MarketCost) * ProcentReserve / 100 );
  
        elif( (SubStr(RsvClass, 1, 3) == "2.4") )
  
          ReserveLossValue = money( (Rest - RestAmortization) * ProcentReserve / 100 );
  
        elif( (SubStr(RsvClass, 1, 5) == "3.1.1") )
  
          ReserveLossValue = money( (Rest - MarketCostSecurity) * ProcentReserve / 100 );
  
        end;
  
      elif(DateReserve < DateBegin446P)  
      /*действуют 283-П*/
        if( ((SubStr(RsvClass, 1, 3) == "2.1") and (SubStr(RsvClass, 1, 9) != "2.1 (502)") and (SubStr(RsvClass, 1, 9) != "2.1 (507)"))
         or (SubStr(RsvClass, 1, 3) == "2.2")
         or (SubStr(RsvClass, 1, 3) == "2.3")
         or ((SubStr(RsvClass, 1, 3) == "2.4") and (SubStr(RsvClass, 1, 9) != "2.4 (604)") and (SubStr(RsvClass, 1, 9) != "2.4 (603)"))
         or ((SubStr(RsvClass, 1, 3) == "2.5") and (SubStr(RsvClass, 1, 9) != "2.5 (601)"))
         or ((SubStr(RsvClass, 1, 3) == "2.7") and (SubStr(RsvClass, 1, 9) != "2.7 (604)"))
         or (SubStr(RsvClass, 1, 9) == "2.9 (202)")
          )
        
          ReserveLossValue = money( Rest * ProcentReserve / 100 );
  
        elif((SubStr(RsvClass, 1, 9) == "2.4 (604)") or (SubStr(RsvClass, 1, 9) == "2.7 (604)"))
  
          ReserveLossValue = money( (Rest - RestAmortization) * ProcentReserve / 100 );
  
        elif((SubStr(RsvClass, 1, 9) == "2.4 (603)"))
  
          ReserveLossValue = money( (Rest - FreeReserveSum) * ProcentReserve / 100 );
  
        elif(SubStr(RsvClass, 1, 13) == "3.1.1 (91318)")
  
          ReserveLossValue = SumReserveCalcUser;
  
        elif(   (SubStr(RsvClass, 1, 9) == "2.1 (502)")
             or (SubStr(RsvClass, 1, 9) == "2.1 (507)")
             or (SubStr(RsvClass, 1, 9) == "2.5 (601)")
             or (SubStr(RsvClass, 1, 12) == "2732-У (501)")
             or (SubStr(RsvClass, 1, 12) == "2732-У (506)")
             or (SubStr(RsvClass, 1, 12) == "2732-У (502)")
             or (SubStr(RsvClass, 1, 12) == "2732-У (507)")
            )
          
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          elif(SolidDeposit != SolidDeposit_Yes)
            ReserveLossValue = money( (Rest + RestPosRevalue - RestNegRevalue) * ProcentReserve / 100 );
          else
            ReserveLossValue = $0;
          end;

        elif(   (SubStr(RsvClass, 1, 9) == "2.6 (479)")
             or (SubStr(RsvClass, 1, 11) == "2.6 (60106)")
            )
  
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          elif(SolidDeposit != SolidDeposit_Yes)
            ReserveLossValue = money( Rest * ProcentReserve / 100 );
          else
            ReserveLossValue = $0;
          end;

        elif(   (SubStr(RsvClass, 1, 11) == "2.8 (50118)")
             or (SubStr(RsvClass, 1, 11) == "2.8 (50218)")
             or (SubStr(RsvClass, 1, 11) == "2.8 (50318)")
             or (SubStr(RsvClass, 1, 11) == "2.8 (50618)")
             or (SubStr(RsvClass, 1, 11) == "2.8 (50718)")
            )
  
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          else
            if(RsvRepoCostCB == 1)
              ReserveLossValue = money( max($0, (Rest + RestPosRevalue - RestNegRevalue - RestObligation)) * max(ProcentReserve, ProcentReserveContr) / 100 + RestObligation * ProcentReserve / 100 );
            elif(RsvRepoCostCB == 2)
              ReserveLossValue = money( max($0, (Rest + RestPosRevalue - RestNegRevalue - RestObligation)) * ProcentReserveContr / 100 );
            end;

            if(SolidDeposit != SolidDeposit_Yes)
              ReserveTmp = money( max(0, (Rest + RestPosRevalue - RestNegRevalue)) * 50 / 100 );
          
              if(ReserveLossValue < ReserveTmp)
                ReserveLossValue = ReserveTmp;
              end;
            end;
          end;

        elif(    (SubStr(RsvClass, 1, 3) == "2.8")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50118)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50218)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50318)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50618)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50718)")
            )
  
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          else
            if(RsvRepoCostCB == 1)
              ReserveLossValue = money( max($0, (Rest - RestObligation)) * max(ProcentReserve, ProcentReserveContr) / 100 + RestObligation * ProcentReserve / 100 );
            elif(RsvRepoCostCB == 2)
              ReserveLossValue = money( max($0, (Rest - RestObligation)) * ProcentReserveContr / 100 );
            end;
          end;
        
        elif(   (SubStr(RsvClass, 1, 14) == "2732-У (91311)")
            )
  
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          elif(SolidDeposit != SolidDeposit_Yes)
            if(СуммаОстатковСчетовРазмещенныхСредств < Rest)
              ReserveLossValue = money( СуммаОстатковСчетовРазмещенныхСредств * ProcentReserve / 100 );
            else
              ReserveLossValue = money( Rest * ProcentReserve / 100 );
            end;
          else
            ReserveLossValue = $0;
          end;

        elif(   (SubStr(RsvClass, 1, 12) == "2732-У (601)")
             or (SubStr(RsvClass, 1, 14) == "2732-У (91314)")
            )
  
          if(SolidDeposit != SolidDeposit_Yes)
            ReserveLossValue = money( Rest * ProcentReserve / 100 );
          else
            ReserveLossValue = $0;
          end;

        elif((SubStr(RsvClass, 1, 5) == "3.1.1"))
  
          ReserveLossValue = money( (Rest - MarketCostSecurityByCategory) * ProcentReserve / 100 );
  
        elif((SubStr(RsvClass, 1, 7) == "5 (938)"))
  
          ReserveLossValue = money( (Rest - MarketCost) * ProcentReserve / 100 );

        elif((SubStr(RsvClass, 1, 5) == "5.1.1"))
          
          if( RestLoanOverdraft )
            ReserveLossValue = money( (1 - MarketCostSecurityByCategoryLoanOverdraft / RestLoanOverdraft) * Rest * ProcentReserve / 100 );
          else
            ReserveLossValue = money( Rest * ProcentReserve / 100 );
          end;

        elif((SubStr(RsvClass, 1, 1) == "5") or (SubStr(RsvClass, 1, 5) == "5.1.2") or (SubStr(RsvClass, 1, 5) == "5.1.3"))
  
          ReserveLossValue = money( Rest * ProcentReserve / 100 );

        end;
  
      else
      /*действуют 446-П*/
        if( ((SubStr(RsvClass, 1, 3) == "2.1") and (SubStr(RsvClass, 1, 9) != "2.1 (502)") and (SubStr(RsvClass, 1, 9) != "2.1 (507)"))
         or (SubStr(RsvClass, 1, 3) == "2.2")
         or (SubStr(RsvClass, 1, 3) == "2.3")
         or ((SubStr(RsvClass, 1, 3) == "2.5") and (SubStr(RsvClass, 1, 9) != "2.5 (601)"))
         or (SubStr(RsvClass, 1, 9) == "2.9 (202)")
          )
        
          ReserveLossValue = money( Rest * ProcentReserve / 100 );
  
        elif((SubStr(RsvClass, 1, 9) == "2.4 (603)"))
  
          ReserveLossValue = money( (Rest - FreeReserveSum) * ProcentReserve / 100 );
  
        elif((SubStr(RsvClass, 1, 9) == "2.4 (604)") or (SubStr(RsvClass, 1, 3) == "2.7"))
  
          ReserveLossValue = money( (Rest - RestAmortization - FreeReserveSum) * ProcentReserve / 100 );
  
        elif(SubStr(RsvClass, 1, 13) == "3.1.1 (91318)")
  
          ReserveLossValue = SumReserveCalcUser;
  
        elif(    (SubStr(RsvClass, 1, 3) == "2.8")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50118)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50218)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50318)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50618)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (50718)")
             and (SubStr(RsvClass, 1, 11) != "2.8 (60118)")
            )
  
          if(abs(SumReserveCalcUser) > $0)
            ReserveLossValue = SumReserveCalcUser;
          end;
        
        elif((SubStr(RsvClass, 1, 5) == "3.1.1"))
  
          ReserveLossValue = money( (Rest - MarketCostSecurityByCategory) * ProcentReserve / 100 );
  
        elif(   (SubStr(RsvClass, 1, 11) == "5.1.1 (203)") 
             or (SubStr(RsvClass, 1, 11) == "5.1.1 (325)") 
             or (SubStr(RsvClass, 1, 11) == "5.1.1 (474)")
             or (SubStr(RsvClass, 1, 11) == "5.1.1 (459)")
            )

          ReserveLossValue = money((Rest - (MarketCostSecurityByCategoryLoanOverdraft - RestLoanOverdraft - RestAccountProcent)) * ProcentReserve / 100 );

        elif((SubStr(RsvClass, 1, 7) == "5 (938)"))
  
          ReserveLossValue = money( (Rest - MarketCost) * ProcentReserve / 100 );

        elif((SubStr(RsvClass, 1, 5) == "5.1.1"))
          
          if( RestLoanOverdraft )
            ReserveLossValue = money( (1 - MarketCostSecurityByCategoryLoanOverdraft / RestLoanOverdraft) * Rest * ProcentReserve / 100 );
          else
            ReserveLossValue = money( Rest * ProcentReserve / 100 );
          end;

        elif((SubStr(RsvClass, 1, 5) == "5.1.2") or (SubStr(RsvClass, 1, 5) == "5.1.3"))
  
          ReserveLossValue = money( Rest * ProcentReserve / 100 );

        end;

      end;
  
      if(ConsiderMinPercent and (ReserveLossValue < money(Rest * MinPercentReserve / 100)))
        ReserveLossValue = money(Rest * MinPercentReserve / 100);
      end;

      if( РасчетРезерваПросроченныхТребований )
        
        if(( not РасчетРезерваНепросроченныхТребований ) or ( КлиентСчета == {OurBank} ))
        
          if( СуммаПросроченныхТребований <= Rest )

            ReserveLossValue = money( СуммаПросроченныхТребований * ПроцентРезерваПросроченныхТребований / 100 ) +
                               money(( Rest - СуммаПросроченныхТребований ) * ProcentReserve / 100 ); 

          else

            ReserveLossValue = money( Rest * ПроцентРезерваПросроченныхТребований / 100 );

          end;
        else
      
          ReserveLossValue = money( Rest * ProcentReserve / 100 );

        end;
      end;

      if( ReserveLossValue < 0 ) ReserveLossValue = $0; end;
  
    end;
  
    return ReserveLossValue;
  
  end;
  
  /*
   * Расчет резерва на возможные потери по ссудам
   */
  macro CalcReserveLoans() : money
    var EnsuringCoefficient : double;
  
    if( RsvClassLoans != null )
      
      ReserveLoansValue = $0;

      /*действуют 283-П*/
      /*действуют 446-П*/
      if(PartyTaxGroup != PtCategory_TaxGroup_Yes)
        if( Rest )
          ReserveLoansValue = money( Rest * ProcentReserve / 100 * max(0, (1 - MarketCostSecurityByCategory / Rest)) );
        end;
      end;
          
      if(ConsiderMinPercent and (ReserveLoansValue < money(Rest * MinPercentReserve / 100)))
        ReserveLoansValue = money(Rest * MinPercentReserve / 100);
      end;

      if( ReserveLoansValue < $0 ) ReserveLoansValue = $0; end;
  
    end;

    return ReserveLoansValue;

  end;

  /*
   * Расчет резерва на возможные потери по операциям с резидентами офшорных зон
   */
  macro CalcReserveOffshore() : money

    if( RsvClassOffshore != null )

      if(DateReserve < date(01,10,2005) )
        if( SubStr(RsvClassOffshore, 1, 3) == "2.1" )
      
          if( GroupRisk == 5 )
            ReserveOffshoreValue = money((max(Rest, DebetTurn) + Rest - 2 * ReserveLoansValue) * ProcentReserveOffshore / 100);
          else
            ReserveOffshoreValue = money(max(Rest, DebetTurn) * ProcentReserveOffshore / 100);
          end;

        elif( (SubStr(RsvClassOffshore, 1, 3) == "2.2")
           or (SubStr(RsvClassOffshore, 1, 3) == "2.3")
           or (SubStr(RsvClassOffshore, 1, 3) == "2.8")
           )
      
          ReserveOffshoreValue = money( max(Rest, DebetTurn) * ProcentReserveOffshore / 100 );

        elif( (SubStr(RsvClassOffshore, 1, 3) == "2.4")
           or (SubStr(RsvClassOffshore, 1, 3) == "2.5")
           or (SubStr(RsvClassOffshore, 1, 3) == "2.6")
           or (SubStr(RsvClassOffshore, 1, 3) == "2.9")
            )

          ReserveOffshoreValue = money( Rest * ProcentReserveOffshore / 100 );

        elif( SubStr(RsvClassOffshore, 1, 3) == "2.7" )
      
          ReserveOffshoreValue = money( MaxRest * ProcentReserveOffshore / 100 );

        end;

        if( ReserveOffshoreValue < $0 ) ReserveOffshoreValue = $0; end;
      else
      /*действуют 283-П*/
      /*действуют 446-П*/
        if((( SubStr(RsvClassOffshore, 1, 7) == "2 (474)" ) and ( ReserveKind == KindReserveLoss )) or ( SubStr(RsvClassOffshore, 1, 3) == "2.1" ))

          ReserveOffshoreValue = money(Rest * ProcentReserveOffshore / 100);

        elif (SubStr(RsvClassOffshore, 1, 3) == "2.2")

          ReserveOffshoreValue = money( MaxRest30WorkDay * ProcentReserveOffshore / 100 );
        elif ((( SubStr(RsvClassOffshore, 1, 7) == "2 (474)" ) and ( ReserveKind == KindReserveLoans )) or (SubStr(RsvClassOffshore, 1, 3) == "2.3"))

          ReserveOffshoreValue = money( max(Rest, DebetTurn30Day) * ProcentReserveOffshore / 100 );

        end;

        if( ReserveOffshoreValue < $0 ) ReserveOffshoreValue = $0; end;

      end;

    end;

    return ReserveOffshoreValue;

  end;

  /* Расчет оценочного резерва */
  macro CalcReserveEstimated() : money
    if((ProcentReserveEstimated != null) and (ProcentReserveEstimated >= 0))
      ReserveEstimatedValue = Rest * ProcentReserveEstimated / 100;
    end;

    return ReserveEstimatedValue;
  end;

  /*
   * Расчет резерва
   */
  macro CalcReserve() : money
    
    if( ValidClass(RsvClassOffshore) )
      
      if( ValidClass(RsvClass) )
        Reserve = max(ReserveLossValue, ReserveOffshoreValue);
      elif( ValidClass(RsvClassLoans) )
        Reserve = max(ReserveLoansValue, ReserveOffshoreValue);
      end;
    
    else
      
      if( ValidClass(RsvClass) )
        Reserve = ReserveLossValue;
      elif( ValidClass(RsvClassLoans) )
        Reserve = ReserveLoansValue;
      end;
    
    end;

    return Reserve;
  
  end;

  /* установить в качестве исходных данных для расчета резерва величины на дату курса */
  macro SetVariables_DateRate()
    Rest = Rest_DateRate;
    FreeReserveSum = FreeReserveSum_DateRate;
    MinPercentReserve = MinPercentReserve_DateRate;
    СуммаПросроченныхТребований = СуммаПросроченныхТребований_DateRate;
    SumReserveCalcUser = SumReserveCalcUser_DateRate;
  end;

  /* установить в качестве исходных данных для расчета резерва величины на дату резервирования */
  macro SetVariables_DateReserve()
    Rest = Rest_DateReserve;
    FreeReserveSum = FreeReserveSum_DateReserve;
    MinPercentReserve = MinPercentReserve_DateReserve;
    СуммаПросроченныхТребований = СуммаПросроченныхТребований_DateReserve;
    SumReserveCalcUser = SumReserveCalcUser_DateReserve;
  end;

  macro GetRestReserve() : money
    return RestReserve;
  end;

/* Конструктор */
  DateReserve = {curdate};
  DateRate    = {curdate};
  RestReserve = $0;

  GroupRisk = 0;

  Reserve              = $0;
  ReserveLossValue     = $0;
  ReserveLoansValue    = $0;
  ReserveOffshoreValue = $0;
  ReserveEstimatedValue = $0;

  RsvClass         = null;
  RsvClassLoans    = null;
  RsvClassOffshore = null;

  GroupRisk = 0;

  Rest = $0;
  Rest_DateRate    = $0;
  Rest_DateReserve = $0;
  ProcentReserve         = 0.0;
  ProcentReserveContr    = 0.0;
  ProcentReserveOffshore = 0.0;
  RsvRepoCostCB = 1;
  
  MaxRest   = $0;
  DebetTurn = $0;
  
  MarketCost         = $0;
  MarketCostSecurity = $0;
  RestObligation     = $0;
  RestAmortization   = $0;
  RestCurrency       = $0;

  RestPosRevalue     = $0; 
  RestNegRevalue     = $0; 

  RestAccountProcent = $0;

  FreeReserveSum             = $0;
  FreeReserveSum_DateReserve = $0;
  FreeReserveSum_DateRate    = $0;

  ConsiderMinPercent            = false;
  MinPercentReserve             = 0.0;
  MinPercentReserve_DateReserve = 0.0;
  MinPercentReserve_DateRate    = 0.0;

  EnsuringCategory = 0;

  MarketCostSecurityByCategory = $0;

  RestLoanOverdraft = $0;
  MarketCostSecurityByCategoryLoanOverdraft = $0;

  РасчетРезерваПросроченныхТребований = false;
  РасчетРезерваНепросроченныхТребований = false;
  СуммаПросроченныхТребований = $0;
  СуммаПросроченныхТребований_DateReserve = $0;
  СуммаПросроченныхТребований_DateRate    = $0;
  ПроцентРезерваПросроченныхТребований = 0.0;
  КлиентСчета = -1;
  ReserveKind = 0;

  SumReserveCalcUser             = $0;
  SumReserveCalcUser_DateReserve = $0;
  SumReserveCalcUser_DateRate    = $0;

  СуммаОстатковСчетовРазмещенныхСредств = $0;

  SolidDeposit = SolidDeposit_Undef;
end;
