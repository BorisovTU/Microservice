/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab 2005              */
/*  Имя файла        : cbsfinv5.mac                                     */
/*  Описание         : Оплата ТО. Шаг учета комиссии в доход            */
/*  Программист      : CолАН                                            */
/*  Создан           : 27ю07.2007                                       */
/************************************************************************/

import OprInter, SFInter, "sfcomcat.mac";

private const RashetCatCode = "+Расчеты"; 
private const NdsCatCode = "-НДС"; 

macro ExecuteStep( d, sfinv )
  var ClaimAccountUse;
  var isCarryPossible:bool;
  var stat;
  
  record sfinv_buf("sfinv.dbt");

  record sfdefcomRec ("sfdefcom.dbt");
  file   sfdefcomFile ("sfdefcom.dbt");

  record sfcontrRec ("sfcontr.dbt");
  file   sfcontrFile( "sfcontr.dbt") key 0;  

  file sfcomissFile( "sfcomiss.dbt"  ) key 0;

  record AccountReceiverRec( account );

  setbuff( sfinv_buf, sfinv );  

  /*_средства = НаличиеСредствНаСчете_r20 (счет плательщика по ТО (DSFINV_DBT.T_PAYERACCOUNT), 
    сумма ТО (DSFINV_DBT.T_TOTALAMOUNT)*/
  var IsMoneyAvailable = IsMoneyAvailableOnAccount( sfinv_buf.InvoiceFIID, sfinv_buf.PayerAccount, sfinv_buf.TotalAmount );  

  /* Получить значение настройки _настройка = "COMMON\Комиссии\Не использовать счет требования" */
  GetRegistryValue( "COMMON\\КОМИССИИ\\НЕ ИСПОЛЬЗОВАТЬ СЧЕТ ТРЕБОВАНИЯ", V_BOOL, ClaimAccountUse, stat );
  if(stat != 0)
    msgbox("Ошибка при определении настройки: |COMMON\\КОМИССИИ\\НЕ ИСПОЛЬЗОВАТЬ СЧЕТ ТРЕБОВАНИЯ");
    return 1;
  end;     

  /*sfdefcomFile.InvoiceID =  sfinv_buf.InvoiceID;*/

  var sidebet  = TRecHandler("sfsi.dbt");
  var sicredit = TRecHandler("sfsi.dbt");  

  var SfComPD : SfComPrimDoc;
  var RashetAccount : string, ndsAccount:string;
  var SumWithoutNDS;

  var rs:object;
  var select : string;
  var Ground : string, strDate: string;
  
  select = "SELECT * FROM dsfdefcom_dbt WHERE T_INVOICEID = " + sfinv_buf.InvoiceID;   
  rs = RsdRecordset(select);

  if( rs.moveNext() )    
    sfdefcomFile.ID = rs.value(0);    
    if( getEQ( sfdefcomFile ) )      
      /*Получить платежные инструкции дебета (sidebet) и кредита (sicredit):*/     
      sidebet.Clear;
      sicredit.Clear;
      Copy( sfdefcomRec, sfdefcomFile ); 
      /*SfGetSI( 663, sfdefcomRec, sidebet, sicredit ); */
      
      /*Условия выполнения проводки
         Комиссия должна быть учтена в доход если выполняется хотя бы одно из условий приведенных ниже.
         Таблица 5 Условие учета комиссии в доход
         --------------------------------------------------------------------------------------------
         Значения настройки                |  Наличие средств      |
         "Не использовать счет требования" |  на счете-плательщика | Дата оплаты
         ----------------------------------|-----------------------|---------------------------------
         No                                |      есть             | Считаем, что         
         Не смотрим на настройку           |      нет              | совпадает с датой начисления         
         --------------------------------------------------------------------------------------------      
      */       
       isCarryPossible = false;
       if( IsMoneyAvailable == true )
         if( ClaimAccountUse == false )
           isCarryPossible = true;
         end;
       else
         isCarryPossible = true;        
       end;    

      /*Если направление ТО "к клиенту"  и выполняется условие учета комиссии в доход (п. 6.2)*/
      if( (sfinv_buf.Direction == SFINV_COMTYPE_COMING) AND (isCarryPossible == true) )
        /*Проводка 1: Дт. "+Расчеты" по договору обслуживания (значение параметра БэкОфисКУ 
                          выбирается по виду обслуживания договора, контрагентом является контрагент 
                          по договору) 
                      Кт. счет плательщика (DSFINV_DBT.T_BENEACCOUNT) 
                      на сумму ТО без НДС (DSFINV_DBT.T_TOTALAMOUNT - DSFINV_DBT.T_NDSAMOUNT) 
                      текущим операционным днем филиала. 
                      Основание проводки "За услуги по договору <"№"> от <дата> 
                      за период с <дата начала периода> по <дата окончания периода>".*/
        sfcontrFile.Id = sfdefcomRec.ConId;
        if( not getEQ( sfcontrFile ) )
           MsgBox("Не найден договор обслуживания: ", sfdefcomRec.ConId);
           return 1;
        end;

        SfComPD = SfComPrimDoc( 87 /*DLDOC_SFCONTRACT*/, sfcontrFile );
        RashetAccount = SfComPD.FindAndOpenAccount( RashetCatCode, {curdate}, sfdefcomRec.FIID_CommSum );
                
        Copy( sfcontrRec, sfcontrFile );         
         /*на сумму ТО без НДС (DSFINV_DBT.T_TOTALAMOUNT - DSFINV_DBT.T_NDSAMOUNT)*/ 
        SumWithoutNDS = sfinv_buf.TotalAmount - sfinv_buf.NDSAmount;        


        if (sfcontrFile.DATECLOSE != Date(0,0,0)) 
          strDate = string(sfcontrFile.DATECLOSE:f);
        else
          strDate = "01.01.0001";
        end;
        Ground = "За услуги по договору \"" +sfcontrFile.Number + "\"  от " + string(sfcontrFile.DATECONC:f) + " за период с " + string(sfcontrFile.DATEBEGIN:f)+ " по " + strDate;

         /*Проводка текущим операционным днем филиала*/
        CountComissAsIncome(sfdefcomRec.FIID_CommSum, RashetAccount, sfinv_buf.PayFIID, sfinv_buf.BeneAccount, SumWithoutNDS, Ground );

        /*Если у ТО есть сумма НДС DSFINV_DBT.T_NDSAMOUNT > 0 */
        if( sfinv_buf.NDSAmount > $0 )       
          /*Проводка 2:
             Дт. "+Расчеты" по договору обслуживания (значение параметра БэкОфисКУ выбирается по 
                 виду обслуживания договора, контрагентом является контрагент по договору) 
             Кт. счет НДС комиссии (SfGetCategoryAccount( comiss, OBJROLE_SFCOMISS_TAX_ACC, &chapterID, &FIID, &account )) 
             на сумму НДС ТО (DSFINV_DBT.T_NDSAMOUNT) 
             текущим операционным днем. 
             Основание проводки "НДС по комиссии <DSFCOMISS.T_CODE>". */

          sfcomissFile.FeeType = sfdefcomRec.FeeType;
          sfcomissFile.Number  = sfdefcomRec.CommNumber;

          if( not getEQ( sfcomissFile ) )
             MsgBox("Не найдена комиссия");
             return 1;
          end;

          Ground = "НДС по комиссии "+sfcomissFile.Code+ ".";

          /*ndsAccount = SfComPD.FindAndOpenAccount( NdsCatCode, {curdate}, sfdefcomRec.FIID_CommSum );*/
          SfGetCategoryAccount( sfcomissFile, /*OBJROLE_SFCOMISS_TAX_ACC*/ 2, AccountReceiverRec );                  
          CountComissAsIncome( sfdefcomRec.FIID_CommSum, RashetAccount, AccountReceiverRec.Code_Currency, AccountReceiverRec.account, sfinv_buf.NDSAmount, Ground);
        end;

         /*Выставить признак "Учтена в доход" (DSFDEFCOM_DBT.T_ISINCLUDED) для всех комиссий ТО*/
         SfDefSetFlagIsInc( /*sfinv_buf.InvoiceID*/ );
      end;
    end;
  end;

  return 0;

end;

  
