/*
$Name:             dv_closingaccounts_203.mac
$Module:           ФИССиКО
$Description:      BIQ-15022:Закрытие счетов и списание остатков 
*/

import dlmisc, dlquery;

PRIVATE MACRO PrintTableHeader()
[┌───────────┬─────────────────────────┬───────────────────────────────────────────┬────────────────────────┬───────────────────────┬──────────────────────────────┬────────────────────────────────────┬────────────────────────────┐];
[│   № п/п   │      Код владельца      │           Наименование /ФИО               │      Лицевой счет      │  Дата открытия счета  │    Остаток в валюте счета    │    Остаток в валюте-эквиваленте    │  Результат закрытия счета  │];
[│           │                         │                                           │                        │                       │       на дату переноса       │          на дату переноса          │                            │];
[├───────────┼─────────────────────────┼───────────────────────────────────────────┼────────────────────────┼───────────────────────┼──────────────────────────────┼────────────────────────────────────┼────────────────────────────┤];
END;

PRIVATE MACRO PrintLine (A1, A2, A3, A4, A5, A6, A7, A8)
[│###########│#########################│###########################################│########################│#######################│##############################│####################################│############################│] (A1:c, A2:w, A3:w, A4:w, A5:w, A6:w, A7:w, A8:w);
END;

PRIVATE MACRO Footer() 
[└───────────┴─────────────────────────┴───────────────────────────────────────────┴────────────────────────┴───────────────────────┴──────────────────────────────┴────────────────────────────────────┴────────────────────────────┘];
END;

PRIVATE CLASS LineProtocol()
  var LineNumber:integer, CodeClient:string, NameClient:string, Account:string, OpenDate:date, Rest_Acc:money, Rest_Nat:money, Result:string;
  Macro Clear()
    LineNumber = 0;
    CodeClient = "";
    NameClient = "";
    Account = "";
    OpenDate = date(0,0,0);
    Rest_Acc = $0;
    Rest_Nat = $0;
    Result = "";
  END;
END;

PRIVATE MACRO ОбнулитьОстаток(Account)
  var cmd = RsdCommand( "MERGE INTO drestdate_dbt frest " + 
                        "     USING (SELECT r2.* " + 
                        "              FROM (SELECT DISTINCT " + 
                        "                           rest.t_accountid, " + 
                        "                           rest.t_restcurrency, " + 
                        "                           MAX ( " + 
                        "                              t_restdate) " + 
                        "                           OVER ( " + 
                        "                              PARTITION BY rest.t_accountid, " + 
                        "                                           rest.t_restcurrency) " + 
                        "                              t_restdate " + 
                        "                      FROM drestdate_dbt rest " + 
                        "                           JOIN daccount_dbt acc " + 
                        "                              ON rest.t_accountid = acc.t_accountid " + 
                        "                     WHERE acc.t_account = \'" + Account + "\') r " + 
                        "                   JOIN drestdate_dbt r2 " + 
                        "                      ON     r2.t_accountid = r.t_accountid " + 
                        "                         AND r2.t_restcurrency = r.t_restcurrency " + 
                        "                         AND r2.t_restdate = r.t_restdate " + 
                        "             WHERE (r2.t_rest != 0 OR r2.t_planrest != 0)) r2 " + 
                        "        ON (    frest.t_accountid = r2.t_accountid " + 
                        "            AND frest.t_restcurrency = r2.t_restcurrency " + 
                        "            AND frest.t_restdate = r2.t_restdate) " + 
                        "WHEN MATCHED " + 
                        "THEN " + 
                        "   UPDATE SET frest.t_rest = 0, frest.t_planrest = 0 "
                      );
  cmd.execute();
END;

PRIVATE MACRO ОбнулитьИЗакрыть(i:@integer,DataSet,line_for_protocol:@LineProtocol)
  var party = TRecHandler("party.dbt");
  if(DataSet.Account != line_for_protocol.Account)
    if ((line_for_protocol.Rest_Nat == 0) and (line_for_protocol.Rest_Acc != 0))
      PrintLine(line_for_protocol.LineNumber, line_for_protocol.CodeClient, line_for_protocol.NameClient, line_for_protocol.Account, line_for_protocol.OpenDate, line_for_protocol.Rest_Acc, line_for_protocol.Rest_Nat, line_for_protocol.Result);
    else
      line_for_protocol.Clear();
      
      line_for_protocol.LineNumber = i + 1;

      ПолучитьСубъекта(DataSet.client, party);
      line_for_protocol.CodeClient = ПолучитьКодСубъекта(party.rec.PartyID, PTCK_CONTR); 
      line_for_protocol.NameClient = party.rec.Name;
      line_for_protocol.Account = DataSet.account;
      line_for_protocol.OpenDate = SQL_ConvTypeDate(DataSet.open_date);
      line_for_protocol.Rest_Acc = DataSet.rest;

      ОбнулитьОстаток(line_for_protocol.Account);

      if( CB_CloseAccount(DataSet.chapter, DataSet.code_currency, DataSet.account, {curdate}, null) != 0 )
        line_for_protocol.Result = "Ошибка закрытия";
      else
        line_for_protocol.Result = "Успешное закрытие";
      end;
      i = i + 1;
      UseProgress(i);
    end;
  else
    if(DataSet.restcurrency == NATCUR)
      line_for_protocol.Rest_Nat = DataSet.rest;
    end;
    PrintLine(line_for_protocol.LineNumber, line_for_protocol.CodeClient, line_for_protocol.NameClient, line_for_protocol.Account, line_for_protocol.OpenDate, line_for_protocol.Rest_Acc, line_for_protocol.Rest_Nat, line_for_protocol.Result);
  end;
END;

PRIVATE MACRO ПоискСчетов(toDate:Date)
  var query, cmd, DataSet, DS_acc, DS_nat;
  var cnt = 0;
  var i = 0;
  var line_for_protocol = LineProtocol();
  line_for_protocol.Clear();
  var IsFirst = true;

  query = " SELECT r.t_account, r.t_client, r.t_open_date, r.t_code_currency, r.t_chapter, r2.* " + 
          "    FROM (SELECT DISTINCT " + 
          "                 acc.t_account, " + 
          "                 acc.t_chapter, " + 
          "                 acc.t_client, " + 
          "                 acc.t_open_date, " + 
          "                 acc.t_code_currency, " + 
          "                 rest.t_accountid, " + 
          "                 rest.t_restcurrency, " + 
          "                 MAX (t_restdate) " + 
          "                    OVER (PARTITION BY rest.t_accountid, rest.t_restcurrency) " + 
          "                    t_restdate " + 
          "            FROM drestdate_dbt rest " + 
          "                 JOIN daccount_dbt acc ON rest.t_accountid = acc.t_accountid " + 
          "           WHERE (   (acc.t_account LIKE '20309%') " + 
          "                  OR (acc.t_account LIKE '20310%'))) r " + 
          "         JOIN drestdate_dbt r2 " + 
          "            ON     r2.t_accountid = r.t_accountid " + 
          "               AND r2.t_restcurrency = r.t_restcurrency " + 
          "               AND r2.t_restdate = r.t_restdate " + 
          "   WHERE (r2.t_rest != 0 OR r2.t_planrest != 0) " + 
          "ORDER BY r.t_account, r.t_restcurrency DESC"; // Сортируем по счету и валюте остатка, т.к. в выборку попадает остаток в нац.валюте и в валюте счета
  
  cmd = DL_RSDCommand(query);

  cnt = int(round(SQL_GetNRecs(query) / 2, 0)); // делим на два исходя из того, что нашли ранее два остатка одного счета

  InitProgress( cnt, "Идет закрытие неактуальных лицевых счетов по учету драгметаллов", "Выполняется закрытие неактуальных лицевых счетов по учету драгметаллов" );

  DataSet = cmd.Execute();

  while (DataSet.MoveNext())
    if (IsFirst)
      PrintTableHeader();
      IsFirst = false;
    end;
    ОбнулитьИЗакрыть(@i, DataSet, @line_for_protocol);
  end;

  if (not IsFirst)
    Footer();
  end;
  RemProgress();
END;


PRIVATE MACRO ЗапускЗакрыетияСчетов()
  SetOutPut(gettxtfilename("Protocol_closingaccounts_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","")),false);

  ПоискСчетов({curdate});

  ViewFile(SetOutPut(null, true));
  SetOutPut(null, true)
END;

ЗапускЗакрыетияСчетов();