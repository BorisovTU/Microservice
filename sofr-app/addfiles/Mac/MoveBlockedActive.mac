/*
$Name:        MoveBlockedActive.mac
$Module:      Ценные бумаги
$Description: Создание рублевых счетов и перенос на них остатков по ЦБ, признанными невозмещаемыми
*/
import BankInter, "dlcontrfunc.mac"; 
import rsbformsinter;

private const FIID_LIST = "23092, 31314, 36256, 36923, 42569";  //идентификаторы ЦБ для обработки

private VAR tableHeader = 
 "┌──────────────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
 "│        ISIN          │                                                                                      Операция                                                                                                        │\n" +
 "├──────────────────────┼──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";


private var trnCnt = 0, accCnt = 0, errCnt = 0;
private var PrevAcc = "",
            PrevISIN = "";

FILE ReportOutFile() txt write; /*Файл вывода*/

//класс панели параметров отчета
PRIVATE CLASS(TRsbPanel) Dlg()
    PRIVATE VAR
        fld_decisiondate  = null, 
        fld_execdate      = null;

    MACRO Get_DecisionDate()
        return fld_decisiondate.Value;
    END;

    MACRO Get_ExecDate()
        return fld_execdate.Value;
    END;

    PRIVATE MACRO InitDefValue()
        fld_decisiondate.Value = date (31,12,2023);
        fld_execdate.Value = {curdate};
    END;


    MACRO eventHandler(EV)
        if (EV.keyCode == 316/*K_F2*/)
            close(-EV.keyCode);
        end;
    END;
    

    //constructor
    InitTRsbPanel();
    caption = "Перенос остатков";
    setPosition(40, 15);
    setSize(31, 3);
    status = "~F2~ Выполнить ~Esc~ Выход";

    // инициализация полей панели

    addLabel(TRsbLabel(2, 1, "Дата фиксации курса:"));

    fld_DecisionDate      = TRsbEditField(V_DATE);
    fld_DecisionDate.name = "fld_DecisionDate";
    fld_DecisionDate.setPosition(20, 1);
    fld_DecisionDate.setSize(10, 1);
    addControl(fld_DecisionDate);

    addLabel(TRsbLabel(2, 2, "Дата проводок переноса:"));

    fld_ExecDate      = TRsbEditField(V_DATE);
    fld_ExecDate.name = "fld_ExecDate";
    fld_ExecDate.setPosition(20, 2);
    fld_ExecDate.setSize(10, 1);
    addControl(fld_ExecDate);

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

    InitDefValue();
END;

private macro PrintHeader()
  println("Протокол обработки валютных счетов по невозмещаемым активам");
  println (tableHeader);
end;

private macro PrintRepLine(AvrCode, Message);
  var Code = AvrCode;
  if (AvrCode == PrevISIN)
    Code = "";
  end;
  //[├──────────────────────┼────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤                                                                                                                   │    
  [│  ################### │ #################################################################################################################################################################################################### │]
  (Code, Message);
  PrevISIN = AvrCode;
end;

private macro PrintEnd()
  [└──────────────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
end;

private macro DDMMYYYY( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(day:2, mon:2, string(year));
  str = StrSubst ( str, " ", "0" );
  return str;
end;


private macro GetRubAccNum(Account, DepartmentID)
  var RubAcc = "", DprtPartyCode = "";
  RubAcc = substr(Account, 1, 5) + "8100" + substr(Account, 10);
  CB_GetDprtPartyCode( DepartmentID, PTCK_BIC, DprtPartyCode );
  return GetKey(RubAcc, DprtPartyCode);
end;


private macro GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
end;

private macro CreateRubAcc(AccDocID, ISIN, DecisionDate, ExecDate)

  macro ProcessAccsTrn()

    var RubAcc = "", OldAcc = "", OldAccCurr, ErrDescription = "", Rest = $0, RestRub = $0, FIID, tr;
    var AccDocBuf = TBfile("mcaccdoc", "W");
    AccDocBuf.KeyNum = 0;
    AccDocBuf.rec.ID = AccDocID; 

    if (not AccDocBuf.GetEQ())
      PrintRepLine(FIID, "Не удалось получить запись по валютному счету из таблицы daccount_dbt");
      errCnt = errCnt + 1;
      return false;
    end; 
    FIID = AccDocBuf.rec.FIID;
    OldAcc = AccDocBuf.rec.Account;
    OldAccCurr = AccDocBuf.rec.Currency;
    // формирование нового номера счета
    RubAcc =  GetRubAccNum(AccDocBuf.rec.Account, AccDocBuf.rec.DepartmentID);

    // Помечаем старый счет неактивным в таблице dmcaccdoc_dbt;
    AccDocBuf.rec.DisablingDate = ExecDate;
    AccDocBuf.rec.IsUsable = "";
    if (not AccDocBuf.Update())
      PrintRepLine(ISIN, "Не удалось обновить счет " + AccDocBuf.rec.Account + " в таблице dmcaccdoc_dbt");
      return false;
    end;

    // Если нового счета нет, то добавляем в таблицу  dmcaccdoc_dbt
    var sql = "SELECT 1 FROM dmcaccdoc_dbt where t_dockind = ? and t_docid = ? and t_catid = ? and t_account = ? and t_currency = 0 ";
    if (AccDocBuf.rec.IsCommon == "X")
       sql = sql + "and t_iscommon = 'X'";
    else
       sql = sql + "and t_iscommon != 'X'";
    end;
    var cmd = RSDCommand(sql);
    cmd.AddParam("1", RSDBP_IN, AccDocBuf.rec.DocKind);
    cmd.AddParam("2", RSDBP_IN, AccDocBuf.rec.DocID);
    cmd.AddParam("3", RSDBP_IN, AccDocBuf.rec.CatID);
    cmd.AddParam("4", RSDBP_IN, RubAcc);
    var rs = RSDRecordset (cmd, rsdval_client, rsdval_static);
    if (not rs.movenext)

      AccDocBuf.rec.ID = 0;
      AccDocBuf.rec.DisablingDate = date(0,0,0);
      AccDocBuf.rec.IsUsable = "X";
      AccDocBuf.rec.Currency = NATCUR;
      AccDocBuf.rec.Account = RubAcc;
      AccDocBuf.rec.ActivateDate =   
      AccDocBuf.rec.ActionDate = ExecDate;

      if (not AccDocBuf.Insert())
        PrintRepLine(ISIN, "Не удалось вставить счет " + AccDocBuf.rec.Account + " в таблицу dmcaccdoc_dbt");
        errCnt = errCnt + 1;
        return false;
      else
        var msg = "Добавлен счет " + RubAcc + ", первичный валютный счет: " + OldAcc;
        if (AccDocBuf.rec.IsCommon == "X")
           msg = msg + " (глобальный)"; 
        else
           msg = msg + " (документ t_dockind = " + AccDocBuf.rec.DocKind + ", t_docid = " + AccDocBuf.rec.DocID + ")"; 
        end;
        PrintRepLine(ISIN, msg);
        accCnt = accCnt + 1;
      end;
    
    end;

    record NewAccRec("account.dbt");
    ClearRecord( NewAccRec );

    record accblnc("accblnc.dbt");
    ClearRecord( accblnc );


    Rest = RestA(OldAcc, ExecDate, NULL, AccDocBuf.rec.Chapter, OldAccCurr);
    if (Rest != 0)

      // Создаем новый счет в таблице daccount_dbt
      var AccBuf = TBfile("account");
      AccBuf.KeyNum = 0;
      AccBuf.rec.Chapter       = AccDocBuf.rec.Chapter;
      AccBuf.rec.Account       = OldAcc;
      AccBuf.rec.Code_Currency = OldAccCurr;


      if (not AccBuf.GetEQ())
        PrintRepLine(FIID, "Не удалось найти счет " + AccDocBuf.rec.Account + " в таблице daccount_dbt");
        errCnt = errCnt + 1;
        return false;
      end;

      copy(NewAccRec, AccBuf);

      AccBuf.rec.Account       = RubAcc;
      AccBuf.rec.Code_Currency = NATCUR;


      // Счет уже может быть создан, если был такой счет в dmcaccdoc_dbt
      if (not AccBuf.GetEQ())

        NewAccRec.AccountID = 0;
        NewAccRec.Oper = {oper};
        NewAccRec.Account = RubAcc;
        NewAccRec.Code_Currency = NATCUR;
        NewAccRec.Open_Date = ExecDate;
 
        if (NewAccRec.PairAccount != "")
          NewAccRec.PairAccount = GetRubAccNum(NewAccRec.PairAccount, AccDocBuf.rec.DepartmentID);
        end;

        accblnc.Chapter       = AccBuf.rec.Chapter;
        accblnc.Code_Currency = AccBuf.rec.Code_Currency;
        accblnc.Account       = AccBuf.rec.Account;
        accblnc.Balance0      = AccBuf.rec.Balance;

  
        if( Create_Account(NewAccRec, accblnc, ErrDescription) )
          PrintRepLine(ISIN, "Ошибка создания счета " + RubAcc + " на основе счета " + AccDocBuf.rec.Account + ": " + ErrDescription);
          errCnt = errCnt + 1;
          return false;
        end;

    end;


      ConvSum (RestRub, Rest, DecisionDate, OldAccCurr, NATCUR);

      tr = RsbAccTransaction();
      tr.Chapter         = AccDocBuf.rec.Chapter;
      tr.Date_Carry      = ExecDate;
      tr.ResultCarry     = INPCARRY;
      tr.Kind_Oper       = "";
      tr.Ground          = "Технический перенос остатков по невозмещаемым бумагам. Выпуск " + ISIN;
      tr.Department      = {OperDprt};
      tr.Oper            = {oper};
      if (Rest < 0)
        tr.AccountPayer    = RubAcc;
        tr.AccountReceiver = OldAcc;
        tr.SumPayer        = ABS(RestRub);
        tr.SumReceiver     = ABS(Rest);
        tr.FIIDPayer       = NATCUR;
        tr.FIIDReceiver    = OldAccCurr;
      else
        tr.AccountPayer    = OldAcc;
        tr.AccountReceiver = RubAcc;
        tr.SumPayer        = ABS(Rest);
        tr.SumReceiver     = ABS(RestRub);
        tr.FIIDPayer       = OldAccCurr;
        tr.FIIDReceiver    = NATCUR;   
      end;
 
      PrintRepLine(ISIN, "Проводка от " + ExecDate + ", дебет " + tr.AccountPayer + " на сумму " + tr.SumPayer + " " + GetCCY(tr.FIIDPayer) + ", кредит " + tr.AccountReceiver + " на сумму " + tr.SumReceiver + " " + GetCCY(tr.FIIDReceiver));
      PrintRepLine(ISIN, "Назначение проводки: " + tr.Ground);

      if( not tr.Carry(null, ErrDescription) )
        errCnt = errCnt + 1;
        PrintRepLine(ISIN, "Ошибка при выполнении проводки: " + ErrDescription);
        return false;
      else
        trnCnt = trnCnt + 1;
      end;
    end; 
 
  
    AddNoteForObject(OBJTYPE_AVOIRISS, L_Z(AccDocBuf.rec.FIID, 10), 108, DecisionDate, DecisionDate);
    PrevAcc = OldAcc;

    return true;
  end;

  if (not ProcessTrn(0, "ProcessAccsTrn"))
    return false  
  end;

  return true;
end;


var panel = Dlg();
if (panel.run() != -316)
  return;
end;

if (not (GetTrue(true, "Вы желаете выполнить перенос остатков с валютных счетов на рублёвые по невозмещаемым активам?")))
  return;
end;

var ReportFileName = GetTxtFileName("MoveBlockedActive_" + DDMMYYYY({curdate}) + ".txt");
if( Open(ReportOutFile, ReportFileName) == false )
  MsgBox("Ошибка при открытии файла |"+ ReportFileName);
  return;
end;
SetOutput(ReportFileName);
/*                                                       
var regValue = "";
var errCode:Integer = 0;
GetRegistryValue( "РСХБ\\НЕВОЗМЕЩАЕМЫЕ АКТИВЫ", V_STRING, regValue, errCode );
if (errCode)
   println("Ошибка при получении значения настройки РСХБ\\НЕВОЗМЕЩАЕМЫЕ АКТИВЫ");
   return;
end;

if (regValue == "")
  println("Настройка 'РСХБ\\НЕВОЗМЕЩАЕМЫЕ АКТИВЫ' не заполнена");
  return;
end;*/

printHeader();
var cmd = RSDCommand(                                                                 
    "SELECT avr, ac, id, rest, (select t_isin from davoiriss_dbt where t_fiid = avr) isin " +
    "FROM (SELECT mc.t_fiid avr, "                                                     +
    "             mc.t_account ac, "                                                   +
    "             mc.t_id id, "                                                        +
    "             NVL ( "                                                              +
    "                (SELECT rd.t_Rest  "                                              +
    "                   FROM drestdate_dbt rd, daccount_dbt acnt "                     +
    "                  WHERE     acnt.t_Chapter = mc.t_Chapter "                       +
    "                        AND acnt.t_Code_Currency = mc.t_currency "                +
    "                        AND acnt.t_Account = mc.t_account "                       +
    "                        AND rd.t_AccountID = acnt.t_AccountID "                   +
    "                        AND rd.t_RestCurrency = acnt.t_Code_Currency "            +
    "                        AND rd.t_RestDate = "                                     +
    "                               (SELECT MAX (t.t_RestDate) "                       +
    "                                  FROM drestdate_dbt t "                          +
    "                                 WHERE     t.t_AccountID = rd.t_accountID "       +
    "                                       AND t.t_RestCurrency = rd.t_RestCurrency " +
    "                                       AND t.t_RestDate <= TO_DATE ('31122023', 'ddmmyyyy'))), " +
    "                0) "                                                              +
    "                Rest "                                                            +
    "        FROM dmcaccdoc_dbt mc "                                                   +
    "       WHERE mc.t_FIID IN (" + FIID_LIST + ") "                                   +
    "             AND mc.t_Currency != mc.t_FIID AND mc.t_Currency != 0) "             +
    "GROUP BY avr, ac, id, rest "                                        +
    "ORDER BY avr, ac, id ");

var rs = RSDRecordset (cmd, rsdval_client, rsdval_static);
while (rs.movenext)
  CreateRubAcc(rs.value("id"), rs.value("isin"), panel.Get_DecisionDate(), panel.Get_ExecDate());
end;

[└──────────────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
println("Открыто счетов: " + accCnt);
println("Выполнено проводок: " + trnCnt);
println("Количество ошибок: " + errCnt);


var sqlUpd = "UPDATE dmcaccdoc_dbt SET t_isusable = chr(0), t_disablingdate = " + GetSQLDate(panel.Get_ExecDate()) + " WHERE t_iscommon != 'X' AND t_ActivateDate < TO_DATE ('31122023', 'ddmmyyyy') AND t_dockind = 5 AND t_docid IN (" + FIID_LIST + ") AND t_catid IN (13, 514)";
var rs2 = ExecSql(sqlUpd);


