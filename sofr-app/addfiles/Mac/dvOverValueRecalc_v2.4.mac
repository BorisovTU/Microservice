import "dv_car.mac";

macro ExecuteRecalc(execDate: date, makeTransaction:bool, trnDate:date)

 [ Исправительные проводки переоценки внебаланса конверсионных операций: ];
 [┌──────────────────┬────────────────────┬────────────────────┬────────────────────┐];
 [│    Код сделки    │        Дебет       │        Кредит      │   Сумма проводки   │];

  var outBalCategMinus = 5021;
  var outBalCategPlus = 5022;

  var overValMinus = 5073;
  var overValPlus = 5072;
 

  var AccPlus = TRecHandler("account");
  var AccMinus = TRecHandler("account");

  var AccPlusM = TRecHandler("account");
  var AccMinusM = TRecHandler("account");

  var Ground = "Исправительная проводка отражения курсовой разницы по сделке ";

  var query = 
    "SELECT (SELECT SUM ( "
   +"                  CASE "
   +"                     WHEN acctrn.T_ACCOUNT_PAYER IN (baEqQuery.t_PlusAccount, "
   +"                                                     baEqQuery.t_MinusAccount) "
   +"                     THEN "
   +"                        acctrn.T_SUM_NATCUR "
   +"                     ELSE "
   +"                        acctrn.T_SUM_NATCUR * -1 "
   +"                  END) "
   +"                  AS t_Rest "
   +"          FROM doprdocs_dbt docs, "
   +"               dacctrn_dbt acctrn, "
   +"               DOPRSTEP_DBT ostep, "
   +"               dacctrn_state_dbt acctrn_state, "
   +"               DOPROPER_DBT oper "
   +"         WHERE     OPER.T_DOCUMENTID = LPAD (baEqQuery.t_docid, 34, '0') "
   +"               AND OPER.t_dockind = 4813 "
   +"              and ostep.T_ID_OPERATION = OPER.T_ID_OPERATION "
   +"              and ostep.T_ID_STEP = docs.T_ID_STEP "
   +"              and ostep.T_SYMBOL != chr(161) "
   +"               and ACCTRN.T_DATE_CARRY <= ? "
   +"               AND (   acctrn.T_ACCOUNT_PAYER IN (baEqQuery.t_PlusAccount, "
   +"                                                  baEqQuery.t_MinusAccount) "
   +"                    OR acctrn.T_ACCOUNT_RECEIVER IN (baEqQuery.t_PlusAccount, "
   +"                                                     baEqQuery.t_MinusAccount)) "
   +"               AND docs.t_id_operation = OPER.T_ID_OPERATION "
   +"               AND (    docs.t_DocKind = 1 "
   +"                    AND docs.t_AccTrnID = acctrn.t_AccTrnID "
   +"                    AND (   (acctrn.t_State = 1 OR acctrn.t_State = 2) "
   +"                         OR acctrn.t_State = 3) "
   +"                    AND acctrn.t_State = acctrn_state.t_State(+))) "
   +"          AS t_restOver, "
   +"       baEqQuery.* "
   +"  FROM (SELECT ABS (RSI_RSB_FIInstr.ConvSum ( "
   +"                       mainQuery.t_rest, "
   +"                       mainQuery.T_CURRENCY, "
   +"                       0, "
   +"                       ?, "
   +"                       2)) "
   +"                  AS t_BAEQ, "
   +"               ABS (RSI_RSB_FIInstr.ConvSum ( "
   +"                       mainQuery.t_CASum, "
   +"                       mainQuery.t_CAFiid, "
   +"                       0, "
   +"                       ?, "
   +"                       2)) "
   +"                  AS t_CAEQ, "
   +"               mainQuery.* "
   +"          FROM (SELECT rsb_account.restac ( "
   +"                          MCDOC.T_ACCOUNT, "
   +"                          MCDOC.T_CURRENCY, "
   +"                          ?, "
   +"                          MCDOC.T_CHAPTER, "
   +"                          0) "
   +"                          AS t_rest, "
   +"                       NFI.T_COST AS t_CASum, "
   +"                       NFI.T_PRICEFIID AS t_CAFiid, "
   +"                       DEAL.T_TYPE AS t_DealType, "
   +"                       mcdoc.*, "
   +"                       PLUSACC.T_ACCOUNT AS t_PlusAccount, "
   +"                       minusAcc.T_ACCOUNT AS t_MinusAccount "
   +"                  FROM DMCACCDOC_DBT mcdoc, "
   +"                       DDVNFI_DBT nfi, "
   +"                       DDVNDEAL_DBT deal, "
   +"                       DMCACCDOC_DBT plusAcc, "
   +"                       DMCACCDOC_DBT minusAcc "
   +"                 WHERE     mcdoc.t_docid = NFI.T_DEALID "
   +"                       AND MCDOC.T_CURRENCY = nfi.t_fiid "
   +"                       AND mcdoc.T_CATNUM IN (?, ?) "
   +"                       AND deal.T_ID = mcdoc.t_docid "
   +"                       AND deal.T_DOCKIND = mcdoc.T_DOCKIND "
   +"                       AND mcdoc.T_DOCKIND = 4813 "
   +"                       AND mcdoc.T_CURRENCY <> 0 "
   +"                       AND plusAcc.T_DOCID(+) = mcdoc.t_docid "
   +"                       AND plusAcc.T_CATNUM(+) = ? "
   +"                       AND plusAcc.T_DOCKIND(+) = 4813 " 
   +"                       AND minusAcc.T_DOCID(+) = mcdoc.t_docid "
   +"                       AND minusAcc.T_CATNUM(+) = ? "
   +"                       AND minusAcc.T_DOCKIND(+) = 4813 "
   +"                       AND nfi.T_EXECDATE >= ?) mainQuery "
   +"         WHERE mainQuery.t_rest <> 0) baEqQuery ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(execDate);
  cmd.addParam(execDate);
  cmd.addParam(execDate);
  cmd.addParam(execDate-1);
  cmd.addParam(outBalCategMinus);
  cmd.addParam(outBalCategPlus);
  cmd.addParam(overValPlus);
  cmd.addParam(overValMinus);
  cmd.addParam(execDate);

  BegAction();

  var dataSet = cmd.execute();

  while (dataset.moveNext())
    var curRestOverVal = SQL_ConvTypeSum(dataset.restOver);
    var curBAEq = SQL_ConvTypeSum(dataset.BAEQ);
    var curCAEq = SQL_ConvTypeSum(dataset.CAEQ);

    var docId = SQL_ConvTypeInteger(dataset.docid);
    var docKind = SQL_ConvTypeInteger(dataset.dockind);

    var FD = DVFirstDocFXDeal( docKind, docId );

    var needOverVal = curBAEq - curCAEq;

    if ((not FD.IsBNote()) and (not FD.IsBuyPart()))
      needOverVal = needOverVal * (-1);
    end;

    var overValDiff = needOverVal - curRestOverVal;

    var stat = 0;
    var errMsg = "";

    var accDeb = "", accCred = "";

    if ((Round(overValDiff,1) != $0) and (curRestOverVal != $0))
      if( ( (FD.IsBuyPart() and ((FD.ВалютаКонтрАктива()==NATCUR) OR (FD.ВалютаКонтрАктива()!=NATCUR))) AND (overValDiff > 0) ) OR
          ( (not FD.IsBuyPart()) and ((FD.ВалютаКонтрАктива()==NATCUR) OR (FD.ВалютаКонтрАктива()!=NATCUR)) AND (overValDiff < 0) ) OR
          ( FD.IsBNote() and (overValDiff > 0) )
          )
        if( (not FD.IsExistAccount("+Переоц.,поставка ИВ и ДМ", null, true, FIROLE_FIRST_OVERVAL, AccPlus, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет +Переоц.,поставка ИВ и ДМ";
        end;
        if( (not FD.IsExistAccount("+Маржа, ИВ и ДМ", null, true, FIROLE_BA, AccPlusM, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет +Маржа, ИВ и ДМ";
        end;

        accDeb = AccPlus.rec.account;
        accCred = AccPlusM.rec.account;

        if((stat == 0) and makeTransaction and (not ПроводкаПоКатегориямУчета( FD,
                                            AccPlus, AccPlusM,
                                            trnDate,
                                            1,
                                            NATCUR, abs(overValDiff),
                                            NULL,
                                            INPCARRY, "", "",
                                            Ground + FD.DealCode(),
                                            null,
                                            null, FIROLE_BA 
                                        ))
        )
          stat = 1;
        end;
      else
        if( (not FD.IsExistAccount("-Переоц.,поставка ИВ и ДМ", null, true, FIROLE_FIRST_OVERVAL, AccMinus, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет -Переоц.,поставка ИВ и ДМ";
        end;
        if( (not FD.IsExistAccount("-Маржа, ИВ и ДМ", null, true, FIROLE_BA, AccMinusM, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет -Маржа, ИВ и ДМ";
        end;

        accDeb = AccMinusM.rec.account;
        accCred = AccMinus.rec.account;

        if((stat == 0) and makeTransaction and (not ПроводкаПоКатегориямУчета( FD,
                                            AccMinusM, AccMinus,
                                            trnDate,
                                            1,
                                            NATCUR, abs(overValDiff),
                                            NULL,
                                            INPCARRY, "", "",
                                            Ground + FD.DealCode(),
                                            null,
                                            FIROLE_BA
                                        ))
        )
            return 1;
        end;
      end;
      [├──────────────────┼────────────────────┼────────────────────┼────────────────────┤];
      [│##################│####################│####################│####################│#](FD.DealCode():c,accDeb,accCred,abs(overValDiff),errMsg);
    end;
  end;

  [└──────────────────┴────────────────────┴────────────────────┴────────────────────┘];
  EndAction();
end;



ExecuteRecalc(date(12,01,2021), false, date(11,03,2021));