/*
$Name:               fnsgmbosx.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения по BOS XML
*/

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "mnsbxtool.mac";

macro CheckObj(addrRegdec)
  SetBuff(wlregdec, addrRegdec);
  return CheckObj_FNSINFO_Xml( wlregdec );
end;

private macro IsKespAllAccounts(DecisionID : integer) : bool
  var ExistsKesp : bool = false, ExistsNotKesp : bool = false;

  var rs = execSQLselect
    ( "select acclnk.t_Type " +
      "  from dwlacclnk_dbt acclnk " +
      " where acclnk.t_ObjectType = :OBJTYPE_FNSINFO " +
      "   and acclnk.t_ObjectID = :DecisionID " +
      " group by acclnk.t_Type ",
      MakeArray( SQLParam("", OBJTYPE_FNSINFO), 
                 SQLParam("", DecisionID) ) );

  while(rs and rs.moveNext())
    if( rs.value("t_Type") == WLACCLNK_TYPE_KESP )
      ExistsKesp = true;
    else
      ExistsNotKesp = true;
    end;
  end;

  if(ExistsKesp and not ExistsNotKesp)
    return TRUE;
  end;

  return FALSE;
end;

private macro ЗаписатьПолеВидСправ( wlregdec )
  var ВидСправ : string = "";

  if(wlregdec.LnkDocType == OBJTYPE_REQ)
    ВидСправ = string(wlregdec.LnkTypeInfo);
  elif( IsKespAllAccounts(wlregdec.DecisionID) )
    ВидСправ = "4";
  else
    ВидСправ = "2";
  end;

  ЗаписатьПолеЛог("ВидСпр", ВидСправ);
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record wlregdec(wlregdec);
  SetBuff(wlregdec, addrRegdec);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BOS");

  var rs;
  // чтение глобализмов
  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьФайл("СПРБНОСТАТ");

  // Файл \ СПРБНОСТАТ
  StartBlockLogXML("СПРБНОСТАТ");

  // атрибуты элемента "СПРБНОСТАТ"
  ЗаписатьПолеЛог("НомСправ", string(wlregdec.Number));
  var ТипСправ : string = ЗаписатьПоле_ТипСправ(wlregdec);
  ЗаписатьПолеЛог("НомЗР", string(wlregdec.LnkDocNumber));
  ЗаписатьПолеЛог("ДатаЗР", YYYY_MM_DD(wlregdec.LnkDocDate));
  ЗаписатьНаимПодтв(wlregdec);
  ЗаписатьПолеВидСправ(wlregdec);
  ЗаписатьПолеЛог("ДейстПоСост", YYYY_MM_DD(wlregdec.Date));
  ЗаписатьПолеЛог("ДатаФорм", YYYY_MM_DD(wlregdec.InputDate));
  ЗаписатьПолеЛог("ДатаНапр", YYYY_MM_DD(date()));

  ПереченьФилиалов(ТипСправ, wlregdec.OriginatorID);//блоки "Самостоят"

  СведенияОНалоговомОргане("СвНО", wlregdec);

  СведенияОБанке("СвБанк", wlmes.InsideAbonentID);

  ЗаписатьПлательщикаВСправкуФНС(wlregdec);

  var IncludeCloseAcc = 1, err;
  GetRegistryValue(RegOpenAcc + "ВКЛЮЧАТЬ_ЗАКР_СЧЕТА_СПРАВОСТ", V_INTEGER, IncludeCloseAcc, err);
  if (err)
    IncludeCloseAcc = 1;
  end;

  rs = execSQLselect
    ( "select acclnk.* " +
      "  from dwlacclnk_dbt acclnk " +
      " where acclnk.t_ObjectType = ? " +
      "   and acclnk.t_ObjectID = ? " +
      IfThenElse(IncludeCloseAcc == 2, "", " and LOWER( acclnk.t_Description ) NOT LIKE '%счет закрыт%' ") +
      "   and acclnk.t_Department = ( " +
      "                   SELECT includedDprt.t_code " +
      "                     FROM ddp_dep_dbt includedDprt " +
      "                    WHERE includedDprt.t_PartyID = ?) ", 
      MakeArray( SQLParam("", OBJTYPE_FNSINFO), 
                 SQLParam("", wlregdec.DecisionID), 
                 SQLParam("", wlmes.InsideAbonentID) ) );

  var acclnkRec : TRecHandler = TRecHandler("wlacclnk");


  while (rs.moveNext())
    CopyRSetToFBuff(acclnkRec, rs);

    if ( not IsLnkAccountPrcError(acclnkRec, WLD_TYPE_REGDEC_IOS) )// если не было ошибок при обработке счета
        StartBlockLogXML("Остатки");

        ЗаписатьПолеЛог("НомСч", rs.value("t_Account"));
        ЗаписатьПолеЛог("ВидСч", rs.value("t_TypeAcc"));
        ЗаписатьПолеЛог("КодВал", GetISO_Number(rs.value("t_FIID")));

        var AccRest : money = GetAccRestForFnsInfoOS(wlregdec.LnkDocType, acclnkRec.rec.RestIn, acclnkRec.rec.RestOut);
        ЗаписатьПолеЛог("Остаток", string(AccRest));

        FinishBlockLogXML("Остатки");
    end;
  end;

  ЗаписатьПредБанка("ПредБанка", wlmes.InsideAbonentID, true);

  FinishBlockLogXML("СПРБНОСТАТ");

  FinishBlockLogXML("Файл");

  /*_НомерОтправ*/
  FnsWriteSendNum(OldMes);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
