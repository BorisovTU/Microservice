 /*
 $Name: fnsprzall.mac
 $Module: МБР
 $Description: Общие функции для печати сообщений ZNS, ZNO, ZVS
 */

import "globals.mac", BankInter, FIInter, PtInter, "likepy.mac", "wlmnstls.mac", 
       "fnsreadmall.mac", "bnk_ptlib.mac", "mnsbxtool.mac", "mnsznsal.mac";

private var NeedDepartment = -1;
 
class TRecMergener(tableA, tableB) // merge two tables and returns TRecHandler on all fields
                                   // if found the equal fields - preferred fields first table
  private var _tableA = tableA,
              _tableB = tableB,
              _all = TArray,
              _size;

  private macro init
      var fAcc = TRecHandler(_tableA).GetFldInfo;
      var fAccLnk = TRecHandler(_tableB).GetFldInfo;
      var item_acc = fAcc.createEnum;

      while( item_Acc.Next )
        _all[_all.size()] = item_acc.Item; item_Acc.Next;
        _all[_all.size()] = item_acc.Item; item_Acc.Next;
        _all[_all.size()] = item_acc.Item; item_Acc.Next;
        _all[_all.size()] = item_acc.Item; item_Acc.Next;
        _all[_all.size()] = item_acc.Item;
      end;

      var isFound = false;
      var item_acclnk = fAccLnk.createEnum;
      while( item_acclnk.Next )
        for( item_acc, fAcc )
          if( item_acclnk.Item == item_acc )
            isFound = true;
            break;
          end;
        end;
        if( not isFound )
          _all[_all.size] = item_acclnk.Item; item_acclnk.Next;
          _all[_all.size] = item_acclnk.Item; item_acclnk.Next;
          _all[_all.size] = item_acclnk.Item; item_acclnk.Next;
          _all[_all.size] = item_acclnk.Item; item_acclnk.Next;
          _all[_all.size] = item_acclnk.Item;
        else
          isFound = false;
        end;
      end;
      _size = _all.size;
  end;

  macro copy(rechandler, tbl1, tbl2 ) // copys without rewrite fields in contrast from standart copy
    var p1 = GetObjProps(tbl1.rec);
    var p2 = GetObjProps(tbl2.rec);
    for( var it, p2 )
      GenSetProp(rechandler.rec, it, GenGetProp(tbl2.rec,it));
    end;
    for( it, p1 )
      GenSetProp(rechandler.rec, it, GenGetProp(tbl1.rec,it)); // the main - first table
    end;
  end;

  macro createMergeTables
    return TRecHandler("merge", _all);
  end;

  macro size:integer
    return _size;
  end;

  init;

END;

macro isAccNotFound(Acc, ObjID):bool
  var select = " select t_description from dwlacclnk_dbt where t_account = :ACC and t_ObjectID = :ObjID ";
  var rs = execSQLselect( select, makeArray(SQLParam("ACC",Acc),
                                            SQLParam("ObjID", ObjID)
                                            ), FALSE);
  if( rs.moveNext() )
    SubStr(strlwr(rs.value("t_Description")), 1, strlen("Счет не найден")) == strlwr("Счет не найден");
    return SubStr(strlwr(rs.value("t_Description")), 1, strlen("Счет не найден")) == strlwr("Счет не найден");
  end;
  return false;
end;

macro GetError(Acc, ObjID)
  var rs = execSQLSelect( " select t_description from dwlacclnk_dbt "
                          " where t_account = :Acc and t_objectID = :ObjID",
                          makeArray(SQLParam("Acc", Acc),
                                    SQLParam("ObjID", ObjID)));
  if( rs.MoveNext() )
    if(strlen(rs.value(0)) > 0)
      return rs.value(0);
    end;
  end;
  
  return "";
end;

macro GetOp(Acc, ObjID):string
  var Op = "";
  for( var i, 0, Acc.size()-1 )
    if( isAccNotFound(Acc(i).rec.Account, ObjID) )
      Op = "X";
      break;
    end;
  end;
  return Op;
end;

macro checkAccState(Acc, ObjID, State) : bool
  var select = " select t_State from dwlacclnk_dbt where t_Account = :Acc and t_ObjectID = :ObjID and t_State = :State ";
  return existsSQLselect( select, MakeArray(SQLParam("Acc", Acc),
                                            SQLParam("ObjID", ObjID),
                                            SQLParam("State", State)));
end;

macro GetYes(Acc, ObjID) : string
  
  for( var i, 0, Acc.size()-1 )
    if( checkAccState(Acc(i).rec.Account, ObjID, "100") )
      return "X";
    end;
  end;
  
  return "";
end;

private macro СортировкаПоКодуВалюты( Acc1:TRecHandler, Acc2:TRecHandler )
  // сначала все рублевые, потом валютные по возрастанию кода валюты
  if ( Acc1.rec.Department == Acc2.rec.Department )
    var Code1 = IfThenElse( Acc1.rec.Code_Currency == 0, "000", GetFICode( Acc1.rec.Code_Currency ) );
    var Code2 = IfThenElse( Acc2.rec.Code_Currency == 0, "000", GetFICode( Acc2.rec.Code_Currency ) );
      if( Code1 < Code2 ) return -1;
    elif( Code1 > Code2 ) return  1;
    elif( Acc1.rec.Account > Acc2.rec.Account ) return  1;
    elif( Acc1.rec.Account < Acc2.rec.Account ) return -1;
    end;
    return 0;
  elif (Acc1.rec.Department == NeedDepartment) return -1;
  elif (Acc2.rec.Department == NeedDepartment) return 1;
  elif (Acc1.rec.Department < Acc2.rec.Department) return -1;
  else return 1;
  end;
end;

macro СортироватьСчета( Acc: TArray, Department)
  if (Department == null)
    NeedDepartment = -1;
  else
    NeedDepartment = Department;
  end;
  Qsort( Acc, @СортировкаПоКодуВалюты );
  return Acc;
end;


macro GetLnkAccounts(WlRegDecID: integer):TArray
  var retArr = TArray();
  var r_acl:TRecHandler = TRecHandler( "wlacclnk", "bank.def" );
  var WlAccList:RsbWlAccLnk = RsbWlAccLnk( WlRegDecID, OBJTYPE_FNSINFO );
  var accs = TRecMergener("account", "wlacclnk");
  if( not WlAccList.First( r_acl ) )
    retArr[retArr.size] = accs.createMergeTables;
    accs.copy( retArr[retArr.size-1], r_acl, GetAccRecHandler( r_acl ) );
    while( not WlAccList.Next( r_acl ) )
      retArr[retArr.size] = accs.createMergeTables;
      accs.copy( retArr[retArr.size-1], r_acl, GetAccRecHandler( r_acl ) );
    end;
  end;
  return retArr;
end;

// Вывести строки для наименования и адреса в печатных формах для ПФР и ФСС
macro PrintSplitLine( Line : String )
  array LineArr;
  StrSplit( Line, LineArr, 50, 50, 1);
  var i = 0;
  if( StrLen( Line ) == 0 )
  [                                                                                                                                               
                                                                                      ──────────────────────────────────────────────────
  ];
  end;
  while( LineArr[i] )
  [                                                                                   ##################################################          
                                                                                      ──────────────────────────────────────────────────
  ]( LineArr[i] );
    i = i + 1;  
  end;
end;

// Клиент, по которому создается сообщение
private class ZNSParty( ID:integer )
  var PartyID   :integer;
  var LegalForm :integer;
  var FullName  :string;
  var FullINN   :string;

  macro ZNSParty( ID:integer )
    private var PartyObj = RsbParty( ID );
    PartyID    = ID;
    LegalForm  = PartyObj.LegalForm;
    if( LegalForm == PTLEGF_INST )
      FullName =  PartyObj.FullName;
    else
      FullName = string( PartyObj.LastName, ",", PartyObj.FirstName, ",", PartyObj.Patronymic );
    end;
    FullINN    = PartyObj.Code(PTCK_INN);
  end;
  ZNSParty(ID);
end;

class (MessageReader) FNSPrint(_WlRegDecID)
  var КлиентПоСчету: ZNSParty;
  var Error = 0, ErrorMes = "", IsPSReport, ClientID = 0;
  var RepBankID = {OurBank};
  var LnkTypeInfo: integer = 1;
  var НаимНО,
      АдрНО,
      НаимКО,
      ДатаЗапроса,
      НомерЗапроса,
      F1,
      F2,
      F3,
      Cause,
      DepInfo,
      DepINN,
      Changes = TArray,
      Ps,
      НаимНП,
      ИНННП,
      КППНП,
      ФИОИП,
      WlRegDecID = _WlRegDecID,
      Счет,
      ВидСправки,
      ВидЗапроса,
      КонецПериода,
      НачалоПериода,
      RepPriority       = 3,
      RepClaimID        = 0,
      ПоСостояниюНаДату = date( 0, 0, 0 ),
      RegPartyKind,
      ClientRegNum = "",
      ClientSubCode = "";

  var pr_wlregdec:TRecHandler  = TRecHandler( "wlregdec",  "bank.def" );

  var CreatorAnswer = 0;
  GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\365-П\\Централизованный_обмен\\Создатель_Ответа", V_INTEGER, CreatorAnswer, null );

  private const HAS_OWNER_REQ = 1; // Имеется связанный запрос, который также имеет родительский запрос по виду связи "передача запроса в филиал"
  private const HAS_CHILD_REQ = 2; // Имеется связанный запрос, который также имеет дочерние запросы по виду связи "передача запроса в филиал"
  // Имеется связанный запрос, который также имеет <родительский запрос/дочерние запросы> по виду связи "передача запроса в филиал" ?
  private macro IsExistsLnkRequest( requestType ) : bool
    var select =         " select count(1) from dwlreqlnk_dbt lnk, dwlregdec_dbt regdec, dwlreq_dbt req "
                         "                where regdec.t_DecisionID = :DecisionID "  // ?
                         "                  and regdec.t_LnkObjType = :Request ";
    if( requestType == HAS_OWNER_REQ )
      select = select +  "                  and regdec.t_LnkDocID = lnk.t_ObjID ";
    elif( requestType == HAS_CHILD_REQ )
      select = select +  "                  and regdec.t_LnkDocID = lnk.t_ReqID ";
    end;       
      select = select +  "                  and lnk.t_ObjKind = regdec.t_LnkObjType "
                         "                  and lnk.t_ReqID = req.t_ReqID ";
    var rs = execSQLSelect( select,
                            makeArray( SQLParam( "DecisionID", WlRegDecID ),
                                       SQLParam( "Request", OBJTYPE_REQ ) ), FALSE );
    return rs.moveNext();
  end;

  private macro IsExistsAccNotCreatedInfoFNS( OriginatorID ) // В списке счетов нет счетов филиалов != создателю справки/выписки
    var rs = execSQLSelect( " select count(1) from ddp_dep_dbt dep, dwlacclnk_dbt lnk "
                            "  where lnk.t_Department = dep.t_Code "
                            "    and dep.t_PartyID <> :OriginatorID ",
                            makeArray( SQLParam( "OriginatorID", OriginatorID ) ), FALSE );
    return not rs.moveNext();
  end;

  private macro IsLnkReqHasChildReq() //Связанный запрос справки/выписки имеет дочерние запросы по     
                                      //виду связи "передача запроса в филиал" для всех филиалов в ЦАБС
    var rs = execSQLSelect( " select count(1) from ddp_dep_dbt dep, dwlreq_dbt req, dwlregdec_dbt regdec, dwlreqlnk_dbt lnk "
                            "                where dep.t_AccessMode = 1 " // online
                            "                  and dep.t_NodeType = 1 "   // Filial
                            "                  and dep.t_PartyID in ( req.t_RecipientID, regdec.t_OriginatorID) "
                            "                  and lnk.t_ObjID = req.t_ReqID "
                            "                  and regdec.t_LnkObjType = :Request "
                            "                  and regdec.t_LnkDocID = lnk.t_ReqID "
                            "                  and regdec.t_DecisionID = :DecisionID "  // ?
                            "                  and lnk.t_ObjKind = regdec.t_LnkObjType "
                            "                  and lnk.t_ObjID = req.t_ReqID ",
                            makeArray( SQLParam( "Request", OBJTYPE_REQ ),
                                       SQLParam( "DecisionID", WlRegDecID ) ), FALSE );
    return rs.moveNext();
  end;

  private macro IsExistsSubFilCABS() //Есть подчиненные филиалы в ЦАБС, в которые не переданы запросы И в списке счетов 
                                     //нет ни одного счета этих филиалов                                                
    var rs = execSQLSelect( " select count(1) from ddp_dep_dbt dep, dwlreq_dbt req, dwlregdec_dbt regdec, dwlreqlnk_dbt lnk "
                            "                where dep.t_AccessMode = 1 " // online
                            "                  and dep.t_NodeType = 1 "   // Filial
                            "                  and dep.t_PartyID not in ( req.t_RecipientID, regdec.t_OriginatorID) "
                            "                  and lnk.t_ObjID = req.t_ReqID "
                            "                  and regdec.t_LnkObjType = :Request "
                            "                  and regdec.t_DecisionID = :DecisionID " // ?
                            "                  and regdec.t_LnkDocID = lnk.t_ReqID "
                            "                  and lnk.t_ObjKind = regdec.t_LnkObjType "
                            "                  and lnk.t_ObjID = req.t_ReqID "
                            "                  and lnk.t_Department not in (dep.t_Code)",
                            makeArray( SQLParam( "Request", OBJTYPE_REQ ), 
                                       SQLParam( "DecisionID", WlRegDecID ) ), FALSE );
    return rs.moveNext();
  end;

  private macro FillDepInfoAndPs()
    var rgd = pr_wlregdec;

    var needFillPs = false;
      // Проверить Тип запроса
      if( rgd.rec.LnkReqType == FNS_REQTYPE_BANK_WITHOUT_DEPS )
        DepInfo = "указанного банка без учета информации филиалов";
      elif( rgd.rec.LnkReqType == FNS_REQTYPE_ONE_DEP )
        DepInfo = "указанного филиала банка";
      elif( rgd.rec.LnkReqType == FNS_REQTYPE_BANK_WITH_DEPS )
        var rs = execSQLselect( " select count(1) from ddp_dep_dbt where :OriginID = t_PartyID and t_ParentCode = 0 ", 
                                  makeArray( SQLParam( "OriginID", rgd.rec.OriginatorID ) ), FALSE );
        if( rs.moveNext() ) //Создатель справки/выписки == ГО ?
          if( CreatorAnswer == 1 ) // Создатель_Ответа
            if( IsExistsLnkRequest( HAS_CHILD_REQ ) ) // Имеется связанный запрос, который также имеет дочерние запросы по виду связи "передача запроса в филиал" ?
              if( IsLnkReqHasChildReq() ) //Связанный запрос справки/выписки имеет дочерние запросы по     
                                          //виду связи "передача запроса в филиал" для всех филиалов в ЦАБС
                if( IsExistsAccNotCreatedInfoFNS( rgd.rec.OriginatorID ) ) // В списке счетов нет счетов филиалов != создателю справки/выписки
                  DepInfo = "указанного банка без учета информации филиалов банка. "
                            "Информация филиалов будет представлена ими самостоятельно";
                else
                  DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                            "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
                end;
              else
                if( IsExistsSubFilCABS() )  //Есть подчиненные филиалы в ЦАБС, в которые не переданы запросы И в списке счетов 
                                            //нет ни одного счета этих филиалов    
                  DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                            "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
                else
                  if( IsExistsAccNotCreatedInfoFNS( rgd.rec.OriginatorID ) ) // В списке счетов нет счетов филиалов != создателю справки/выписки
                    DepInfo = "указанного банка без учета информации филиалов банка. "
                              "Информация филиалов будет представлена ими самостоятельно";
                  else
                    DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                              "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
                  end;
                end;                                            
              end;
              needFillPs = true;
            else
              rs = execSQLSelect( " select count(1) from ddp_dep_dbt "
                                  "                where t_AccessMode = 1 " // online
                                  "                  and t_NodeType = 1 "   // Filial
                                  "                  and t_PartyID <> :ObjID ",
                                  makeArray( SQLParam( "ObjID", rgd.rec.OriginatorID ) ), FALSE );
              if( rs.moveNext() ) // Есть подчиненные филиалы вне ЦАБС ?
                if( IsExistsSubFilCABS() ) //Есть подчиненные филиалы в ЦАБС, в которые не переданы запросы И в списке счетов 
                                           //нет ни одного счета этих филиалов
                  DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                            "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
                  needFillPs = true;
                else
                  if( not IsExistsAccNotCreatedInfoFNS( rgd.rec.OriginatorID ) ) // В списке счетов есть счета филиалов != создателю справки/выписки
                    DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                              "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
                    needFillPs = true;
                  else
                    DepInfo = "";
                  end;
                end;
              else
                if( IsExistsLnkRequest( HAS_OWNER_REQ ) ) // Имеется связанный запрос, который также имеет родительский запрос по виду связи "передача запроса в филиал"
                  DepInfo = "";
                else
                  DepInfo = "указанного банка с учетом информации всех филиалов банка";
                end;
              end;
            end;
          else // Создатель_Ответа != 1
            if( IsExistsAccNotCreatedInfoFNS( rgd.rec.OriginatorID ) ) // В списке счетов нет счетов филиалов != создателю справки/выписки
              DepInfo = "указанного  банка без учета информации филиалов банка. "
                        "Информация филиалов будет представлена ими самостоятельно";
            else
              DepInfo = "указанного банка с учетом информации отдельных филиалов банка. "
                        "Информация в части оставшихся филиалов будет представлена ими самостоятельно";
            end;
            needFillPs = true;
          end;
        else // Создатель справки/выписки != ГО ?
          rs = execSQLSelect( " select count(1) from dwlreqlnk_dbt lnk, dwlregdec_dbt regdec, dwlreq_dbt req "
                              "  where regdec.t_LnkObjType = :Request "
                              "    and regdec.t_LnkDocID = lnk.t_ReqID "
                              "    and lnk.t_ObjKind = regdec.t_LnkObjType "
                              "    and lnk.t_ReqID = req.t_ReqID "
                              "    and req.t_RecipientID = :DecisionID ",
                              makeArray( SQLParam( "Request", OBJTYPE_REQ ),
                                         SQLParam( "DecisionID", rgd.rec.DecisionID ) ), FALSE );
          if( rs.moveNext() ) //Имеется связанный родительский запрос и филиал является его получателем
            if( CreatorAnswer == 2 ) // Создатель_Ответа
              DepInfo = "указанного филиала банка";
            else
              if( IsLnkReqHasChildReq() ) //Связанный запрос справки/выписки имеет дочерние запросы по 
                                          //виду связи "передача запроса в филиал" для всех филиалов в ЦАБС
                DepInfo = "указанного филиала банка";
              else
                DepInfo = "";
              end;
            end;
          else
            if( IsExistsLnkRequest( HAS_OWNER_REQ ) ) // Имеется связанный запрос, который также имеет родительский запрос по виду связи "передача запроса в филиал"
              DepInfo = "указанного филиала банка";
            else
              DepInfo = "";
            end;
          end;
        end;
      else
        DepInfo = "";
      end;
      if( (rgd.rec.LnkReqType == FNS_REQTYPE_BANK_WITH_DEPS) and needFillPs )
        Ps = "X";
      else
        Ps = "";
      end;

  end;

  private macro FillMethodsReceivingRequest
  
    var rgdSelect : Bool;
    var rs = execSQLselect( " select t_LnkDocID "
                                 " from dwlregdec_dbt "
                                 " where t_DecisionID = :DecisionID "
                                 " and t_LnkDocType = :DocType ",
                                 MakeArray( SQLParam("DecisionID", WlRegDecID),
                                            SQLParam("DocType", OBJTYPE_REQ)));
    rgdSelect = rs.MoveNext();
    F1 = F2 = F3 = "";
    Cause = "";
    var lnkSelect = existsSQLselect( " select 1 "
                                 " from dwlreqlnk_dbt wlreqlnk "
                                 " where wlreqlnk.t_ObjID = :ObjID and wlreqlnk.t_ObjKind = :ObjKind ",
                                 MakeArray( SQLParam("ObjID", rs.value(0)),
                                            SQLParam("ObjKind", OBJTYPE_REQ)));
    if( rgdSelect and (not lnkSelect))
      
        F2 = "X";

        var ObjCat = RsbObjCategories( OBJTYPE_FNSINFO, UniID( pr_wlregdec, OBJTYPE_FNSINFO ) );
        for( var attrid, 1, 4 )
          if( ObjCat.IsAttrPresense( 1, attrid, null, null, null, {curdate} ) )
            break;
          end;
        end;
        if( attrid != 4 )
          var params = makeArray( SQLParam( "ObjType", OBJTYPE_FNSINFO ),
                                  SQLParam( "AttrID" , attrid   ) );
       
        rs = execSQLselect( " select t_FullName from dobjattr_dbt "
                                  "  where t_ObjectType = :ObjType "
                                  "    and t_GroupID = 1 "
                                  "    and t_AttrID = :AttrID ", params, FALSE );
          if( rs.moveNext() )
            Cause = rs.value(0);
          end;
        end;
      else
      rgdSelect = existsSQLselect( " select 1 "
                                " from dwlregdec_dbt wlregdec, dwlreqlnk_dbt wlreqlnk "
                                " where wlregdec.t_LnkDocType = :DocType "
                                " and wlregdec.t_LnkDocID = wlreqlnk.t_ObjID "
                                " and wlreqlnk.t_ObjKind = :ObjKind ",
                                MakeArray( SQLParam("DocType", OBJTYPE_REQ),
                                           SQLParam("ObjKind", OBJTYPE_REQ)));
      if( rgdSelect )
        F3 = "X";
      else
        F1 = "X";
      end;
    end;
  end;

  macro FillAcc(account, FIID):string
    var isExists = false;
    var rs:object;
    var select = " select t_Date, t_OldAccount, t_NewAccount "
                      "   from dreqchnga_dbt "
            "  where t_NewAccount = :ACC "
            "    and t_NewAccountFIID = :FIID ";
    rs = execSQLselect( select, makeArray(SQLParam("ACC", account),
                 SQLParam("FIID", FIID)), FALSE );
    if( not rs.moveNext() )
      select = " select t_Change_Date, t_Old_Account, t_New_Account "
                    "   from dmovacnt_dbt "
                    "  where t_New_Account = :ACC "
               "    and t_Code_Currency = :FIID ";
      rs = execSQLselect( select, makeArray(SQLParam("ACC", account),
                                                 SQLParam("FIID", FIID)), FALSE );
      isExists = rs.moveNext();
    else
      isExists = true;
    end;
    if( not isExists )
      return "";
    end;
    return string( DDpMMpYYYY(rs.value(0)), ";", rs.value(1), "/", rs.value(2) );
  end;

  macro FillChanges:TArray
    if( Счет.size() > 0 )
      for( var i, 0, Счет.size()-1 )
        if( Счет[i].TblName == "account.dbt" )
          changes[changes.size()] = FillAcc( Счет[i].rec.Account, Счет[i].rec.Code_Currency );
        elif( Счет[i].TblName == "merge" )
          changes[changes.size()] = FillAcc( Счет[i].rec.Account, Счет[i].rec.FIID );
        end;
      end;
    else
      changes[changes.size()] = "";
    end;
  end;

  macro GetAccName(ClientID):string
    var select = " select t_Name from dparty_dbt "
                 "  where t_PartyID = :ClientID ";
    var rs = execSQLselect( select, makeArray(SQLParam("ClientID", ClientID)), FALSE );
    if( rs.moveNext() )
      return rs.value(0);
    end;
    return "";
  end;

  macro GetDepositDateEnd( rec ):string
    var WlAccList:RsbWlAccLnk = RsbWlAccLnk( WlRegDecID, OBJTYPE_FNSINFO );
    var r_acl:TRecHandler = TRecHandler( "wlacclnk", "bank.def" );
    var r_acc:TRecHandler = TRecHandler( "account", "bank.def" );
    copy(r_acc, rec);
    if( ( WlAccList.size > 0 ) and ( WlAccList.Find( r_acl, r_acc.rec.Account, r_acc.rec.Chapter, r_acc.rec.Code_Currency ) == 0 ) )
      return DDpMMpYYYY(r_acl.rec.DepositDateEnd);
    end;
    return DDpMMpYYYY(date(0,0,0));
  end;
  
  macro GetLnkAccRest( Account:string, Chapter:integer, FIID:integer ):money

    var WlAccList:RsbWlAccLnk = RsbWlAccLnk( WlRegDecID, OBJTYPE_FNSINFO );
    var r_acl:TRecHandler = TRecHandler( "wlacclnk", "bank.def" );
    if( ( WlAccList.size > 0 ) and ( WlAccList.Find( r_acl, Account, FIID, Chapter ) == 0 ) )
      return r_acl.rec.RestIn;
    end;
    return $0;
  end;

  macro GetLnkAccRestTax( Account:string, Chapter:integer, FIID:integer ):money
    var WlAccList:RsbWlAccLnk = RsbWlAccLnk( WlRegDecID, OBJTYPE_FNSINFO );
    var r_acl:TRecHandler = TRecHandler( "wlacclnk", "bank.def" );
    if( ( WlAccList.size > 0 ) and ( WlAccList.Find( r_acl, Account, FIID, Chapter ) == 0 ) )
      return GetAccRestForFnsInfoOS(pr_wlregdec.rec.LnkDocType, r_acl.rec.RestIn, r_acl.rec.RestOut);
    end;
    return $0;
  end;

  // Возвращает массив из счетов (accounts) клиента, уже отсортированный
  macro ВыбратьСчетаКлиента( Department ):TArray

    var Acc :TArray = TArray();
    var facc:TBFile;
    var RegTypeAcc:string = ""; GetRegValForOPENAC( "ТИПЫ_СЧЕТОВ", V_STRING, RegTypeAcc );

    var OpenDate  = date( 0, 0, 0 ),
        CloseDate = date( 0, 0, 0 );
    if( (ВидСправки == "NS") or (ВидЗапроса == "1") )
      OpenDate = IfThenElse( IsPSReport(), ПоСостояниюНаДату, date( ДатаЗапроса ) ); 
    elif( (ВидСправки == "VS") or (ВидЗапроса == "3") )
      OpenDate  = IfThenElse( IsPSReport(), КонецПериода, {curdate} ); 
      CloseDate = IfThenElse( IsPSReport(), НачалоПериода, date( ДатаЗапроса ) );
    else
      OpenDate  = date( ДатаЗапроса );
      CloseDate = date( ДатаЗапроса );
    end;
    var select  = "  select t_Account, t_Chapter, t_Code_Currency, t_Type_Account      "+
                  "    from daccount_dbt                                               "+
                  "   where t_Client       = :ClientID                                 ";
    if( Department == null ) 
      select = select +
                  "     and t_Department   = :OperDep                                  ";
      Department = {OperDprt};
      NeedDepartment = -1;
    else 
      select = select +
        "   and t_Department in ( " +
        "                   SELECT includedDprt.t_code\n" +
        "                     FROM ddp_dep_dbt includedDprt\n" +
        "               CONNECT BY PRIOR includedDprt.t_code =\n" +
        "                             includedDprt.t_parentCode\n" +
        "               START WITH includedDprt.t_code = :OperDep)";
      NeedDepartment = Department;
    end;
    select = select +
                  "     and t_Chapter      = 1                                         "+
                  "     and t_Open_Date   <= :OpenDate                                 ";
    if( (ВидСправки == "OS") or (ВидЗапроса == "2") ) select = select +
                  "     and ( t_Close_Date  =  :ZeroDate                               "+
                  "        or t_Close_Date  >  :CloseDate )                            ";
    elif( (ВидСправки == "VS") or (ВидЗапроса == "3") ) select = select +
                  "     and ( t_Close_Date  =  :ZeroDate                               "+
                  "        or t_Close_Date  >= :CloseDate )                            ";
    end;
    select = select + " and t_Type_Account not like '%П%'                              ";

    var params = makeArray( SQLParam( "ClientID", ClientID     ),
                            SQLParam( "OperDep" , Department   ),
                            SQLParam( "OpenDate", OpenDate     ) );
    if( (ВидСправки != "NS") and (ВидЗапроса != "1") )
      params[params.size] = SQLParam( "ZeroDate"  , date(0,0,0) );
      params[params.size] = SQLParam( "CloseDate" , CloseDate   );
    end;
    var rs = execSQLselect( select, params, FALSE );

    while( ( rs.moveNext() ) )

      if( ( StrBrk( rs.value(3), "НМU" ) == 0 ) and
          ( StrBrk( RegTypeAcc, rs.value(3) ) > 0 ) )
        facc = TBFile( "account.dbt" ,  "R", 0, "account.dbt" ,  "bank.def" );
        facc.KeyNum = 0;
        facc.rec.Account         = rs.value(0);
        facc.rec.Chapter         = rs.value(1);
        facc.rec.Code_Currency   = rs.value(2);
        if( facc.GetEQ() )
          Acc( Acc.Size() ) = TRecHandler("account");
          Copy( Acc( Acc.Size() - 1 ), facc );
        end;
      end;
    end;
    Qsort( Acc, @СортировкаПоКодуВалюты ); 
    return Acc;
  end;

  macro ЗаголовокСправки_ММ_3_06_178(КНД)
    if (КНД == NULL)
      КНД = "1114306";
    end;
    [
      Форма по КНД #######

                                                               ########################################################
                                                               ────────────────────────────────────────────────────────
                                                                         наименование налогового органа(территориально
                                                                         обособленного подразделения налогового органа)

                                                               ########################################################
                                                               ────────────────────────────────────────────────────────
                                                                                    место нахождения(адрес)

    ]( КНД, НаимНО:w, АдрНО:w );
  end;

  macro ДанныеФилиала(BlockNum: integer, BlockSubNum: integer, Department: integer, ReqDate:date)
    var DepName = "";
    array DepINN, DepKPP, DepBIC, RegNDep, RegNKO;
    var rs = execSQLSelect("select t_PartyID, decode(dparty_dbt.t_ShortName, chr(1), dparty_dbt.t_Name, dparty_dbt.t_ShortName) from ddp_dep_dbt join dparty_dbt using (t_PartyID) where t_Code = ?", 
                        MakeArray(SQLParam("", Department)));

    if (rs.moveNext())
      StrSplit( RemoveKPP( ПолучитьКодСубъекта( rs.value(0), PTCK_INN ) ), DepINN, 1, 1,  10 );
      StrSplit( RemoveINN( ПолучитьКодСубъекта( rs.value(0), PTCK_INN ) ), DepKPP, 1, 1,  9 );
      StrSplit( ПолучитьКодСубъекта( rs.value(0), PTCK_BIC ), DepBIC, 1, 1, 9 );
      StrSplit( RemoveINN( ПолучитьКодСубъекта( rs.value(0), PTCK_BANKREGNUM ) ), RegNDep, 1, 1, 4 );
      StrSplit( RemoveKPP( ПолучитьКодСубъекта( rs.value(0), PTCK_BANKREGNUM ) ), RegNKO, 1, 1, 4 );
      DepName = rs.value(1);
    else
      StrSplit( "", DepINN, 1, 1, 10 );
      StrSplit( "", DepKPP, 1, 1, 9 );
      StrSplit( "", DepBIC, 1, 1, 9 );
      StrSplit( "", RegNDep, 1, 1, 4 );
      StrSplit( "", RegNKO, 1, 1, 4 );
    end;
    this.DepINN = DepINN;

    [
      ################# в:
      ─────────────────────────────────────────────────────────────────────────────────────────────────────────
      ## # 
      ─────────────────────────────────────────────────────────────────────────────────────────────────────────
                          сокращенное наименование банка (филиала банка, оператора по переводу денежных средств)

      ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐ ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐ ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐     ┌─┬─┬─┬─┐            ┌─┬─┬─┬─┐
      │#│#│#│#│#│#│#│#│#│#│ │#│#│#│#│#│#│#│#│#│ │#│#│#│#│#│#│#│#│#│     │#│#│#│#│            │#│#│#│#│
      └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘ └─┴─┴─┴─┴─┴─┴─┴─┴─┘ └─┴─┴─┴─┴─┴─┴─┴─┴─┘     └─┴─┴─┴─┘            └─┴─┴─┴─┘
               ИНН                  КПП                 БИК        рег.№ банка по КГРКО  порядк.№ филиала по КГРКО
    ]( string( "\"", substr(YYYYMMDD(ReqDate),7,2), "\" ",
               Месяца(substr(YYYYMMDD(ReqDate),5,2)):c, " ",
               substr(YYYYMMDD(ReqDate),1,4), " г." 
             ),
       BlockNum, DepName,
       DepINN(0), DepINN(1), DepINN(2), DepINN(3), DepINN(4), DepINN(5), DepINN(6), DepINN(7), DepINN(8), DepINN(9),
       DepKPP(0), DepKPP(1), DepKPP(2), DepKPP(3), DepKPP(4), DepKPP(5), DepKPP(6), DepKPP(7), DepKPP(8),
       DepBIC(0), DepBIC(1), DepBIC(2), DepBIC(3), DepBIC(4), DepBIC(5), DepBIC(6), DepBIC(7), DepBIC(8),
       RegNKO(0), RegNKO(1), RegNKO(2), RegNKO(3),
       RegNDep(0), RegNDep(1), RegNDep(2), RegNDep(3)
     );
  end;

  macro ДанныеБанкаИ_Клиента()
    
    var INN = "", BIC = {MFO_Bank}, NameBank = {Name_Bank};
    array BankINN, BankKPP, ClientINN, ClientKPP, BankBIC;
    
    INN = ПолучитьКодСубъекта( RepBankID, PTCK_INN );
    StrSplit( RemoveKPP( INN ), BankINN, 1, 1, 10 );
    StrSplit( RemoveINN( INN ), BankKPP, 1, 1,  9 );
    StrSplit( BIC, BankBIC, 1, 1, 9 );

    array RegNKO, RegNDep;
    var NKO = ПолучитьКодСубъекта( RepBankID, PTCK_BANKREGNUM );
    StrSplit( RemoveKPP( NKO ), RegNKO,  1, 1, 4 );
    StrSplit( RemoveINN( NKO ), RegNDep, 1, 1, 4 );

    if( IsPSReport() )
      NameBank = НаимКО;
      BIC = ПолучитьКодСубъекта( RepBankID, PTCK_BIC );
    end;
    StrSplit( ИНННП, ClientINN, 1, 1, 12 );
    StrSplit( КППНП, ClientKPP, 1, 1,  9 );

    [    

      Банк (филиал банка)  #
                         ─────────────────────────────────────────────────────────────────────────────────────
                        сокращенное наименование банка (филиала банка, оператора по переводу денежных средств), 
                        представляющего информацию
     ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐ ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐ ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐      ┌─┬─┬─┬─┐          ┌─┬─┬─┬─┐
     │#│#│#│#│#│#│#│#│#│#│ │#│#│#│#│#│#│#│#│#│ │#│#│#│#│#│#│#│#│#│      │#│#│#│#│          │#│#│#│#│
     └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘ └─┴─┴─┴─┴─┴─┴─┴─┴─┘ └─┴─┴─┴─┴─┴─┴─┴─┴─┘      └─┴─┴─┴─┘          └─┴─┴─┴─┘
              ИНН                  КПП                 БИК        рег.№ банка по КГРКО порядк.№ филиала по КГРКО
    ]( {Name_Bank}, 
      BankINN(0),BankINN(1),BankINN(2),BankINN(3),BankINN(4),BankINN(5),BankINN(6),BankINN(7),BankINN(8),BankINN(9),   
      BankKPP(0),BankKPP(1),BankKPP(2),BankKPP(3),BankKPP(4),BankKPP(5),BankKPP(6),BankKPP(7),BankKPP(8),
      BankBIC(0),BankBIC(1),BankBIC(2),BankBIC(3),BankBIC(4),BankBIC(5),BankBIC(6),BankBIC(7),BankBIC(8),
      RegNKO(0), RegNKO(1), RegNKO(2), RegNKO(3),
      RegNDep(0), RegNDep(1), RegNDep(2), RegNDep(3));
      
    if(F1 != "X")
    [

                                                                                                         ┌───┐
      В соответствии с запросом налогового органа от            №                                        │   │
                                                     ───────────  ──────────────────────────────────────,└───┘
      полученным от налогового органа на бумажном носителе
    ];
    else
    [

                                                                                                         ┌───┐
      В соответствии с запросом налогового органа от #          №  #                                     │ # │
                                                     ───────────  ──────────────────────────────────────,└───┘
      полученным от налогового органа на бумажном носителе
    ] (ДатаЗапроса, НомерЗапроса, F1);
    end;
    if(F2 != "X")
    [
                                                                                                         ┌───┐
      полученным от налогового органа в электронной форме. При этом в электронной форме представить      │   │
      настоящую справку не представляется возможным по причине                                           └───┘
       
      ──────────────────────────────────────────────────────────────────────────────────────────────────  
    ];
    else
    [
                                                                                                         ┌───┐
      полученным от налогового органа в электронной форме. При этом в электронной форме представить      │ # │
      настоящую справку не представляется возможным по причине                                           └───┘
      #
      ──────────────────────────────────────────────────────────────────────────────────────────────────  
    ](F2, Cause);
    end;
    if(F3 != "X")
    [

      полученным от банка для самостоятельного представление сведений филиалом банка по информации       ┌───┐
      данного филиала                                                                                    │   │
                                                                                                         └───┘
    ];
    else
    [

      полученным от банка для самостоятельного представление сведений филиалом банка по информации       ┌───┐
      данного филиала                                                                                    │ # │
                                                                                                         └───┘
    ](F3);
    end;     

    [ представляет (сообщает) информацию #                                                               
                                         ─────────────────────────────────────────────────────────────── 
                                                  сведения о представляемой (сообщаемой) информации                                                       

                                                                                                                
                                                                                                              
      в отношении #
                 ───────────────────────────────────────────────────────────────────────────────────────
       сокращенное наименование организации (Ф.И.О. индивидуального предпринимателя физического лица, не 
       являющегося предпринимателем, нотариуса, занимающегося частной практикой, адвоката, учредившего 
       адвокатский кабинет)

              ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐            ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
      ИНН/КИО │#│#│#│#│#│#│#│#│#│#│#│#│        КПП │#│#│#│#│#│#│#│#│#│
              └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘            └─┴─┴─┴─┴─┴─┴─┴─┴─┘
    ](
      DepInfo,
      IfThenElse( НаимНП != "", НаимНП, ФИОИП ), 
      ClientINN(0),ClientINN(1),ClientINN(2),ClientINN(3),ClientINN(4),ClientINN(5),ClientINN(6),ClientINN(7),ClientINN(8),
      ClientINN(9),ClientINN(10),ClientINN(11),
      ClientKPP(0),ClientKPP(1),ClientKPP(2),ClientKPP(3),ClientKPP(4),ClientKPP(5),ClientKPP(6),ClientKPP(7),ClientKPP(8)
     );
  end;

  macro ДатаПодписьПредставителяБанка()

    var error = 0;                                   
    var RegIFNSPost:string; if(GetRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", V_STRING, RegIFNSPost ))  RegIFNSPost = ""; end;
    var RegIFNS_FIO:string; if(GetRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО" , V_STRING, RegIFNS_FIO ))  RegIFNS_FIO = ""; end;   

  [

    Представитель банка   
    (оператора по переводу
    денежных средств)      ######################################## ########################################                ##########
    ───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                        (должность)                               (Ф.И.О)                      (подпись)    (дата)
    ──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                                                                                           М.П.
    ───────────────────────────────────────────────────────────────────────────────────────────────────────────
  ]( RegIFNSPost, RegIFNS_FIO, DDpMMpYYYY({curdate}));

  end;

  macro GetNameTypeAcc( TypeAcc : string, FIID : integer )

    if( ( (ВидСправки != "OS") and (ВидЗапроса != "2") ) and
        (Index(TypeAcc, "X") > 0) and (FIID > 0) )
      return "текущий счет";
    elif( (Index(TypeAcc, "Ч") > 0) or 
          ( (Index(TypeAcc, "X") > 0) and ( (ВидСправки == "OS") or (ВидЗапроса == "2") ) ) )
      return "расчетный счет";
    elif( Index(TypeAcc, "Б") > 0 )
      return "бюджетный счет";
    elif( Index(TypeAcc, "К") > 0 )
      return "корреспондентский счет";
    elif( Index(TypeAcc, "Я") > 0 )
      return "специальный банковский счет";
    elif( Index(TypeAcc, "Q") > 0 )
      return "специальный брокерский счет";
    elif( ( Index(TypeAcc, "J") > 0 ) or
          ( Index(TypeAcc, "G") > 0 ) )
      return "вклад(депозит)";
    elif( Index(TypeAcc, "М" ) > 0 )
      return "Номинальный счет";
    elif( Index(TypeAcc, "Э" ) > 0 )
      return "Счет эскроу";
    else
      return "";
    end;

  end;

  private macro CheckBeforePrint()
    var PartyID = ПолучитьКодСубъекта( string( ИНННП + IfThenElse( КППНП != "", "/", "" ) + КППНП ), PTCK_INN, Error );
    if( ( Error > 0 ) and ( КППНП != "" ) )
      Error = 0;  // Не нашли субъекта по полному коду - поищем только по ИНН
      PartyID = GetLegalPersonIDByINN( ИНННП );
      if( PartyID > 0 ) 
        КлиентПоСчету = ZNSParty( PartyID );
        if( КлиентПоСчету.LegalForm == PTLEGF_INST )
          ErrorMes = "КПП клиента не совпадает с указанным в запросе ФНС";// это не ошибка, только предупреждение
        end;
      end;
    end;
    if( ( PartyID <= 0 ) and ( ВидСправки != "NS" ) and (ВидЗапроса != "1") )
      Error = 1;  // это ошибка для всех форм, кроме NS
      ErrorMes = "Не найден клиент с ИНН " + ИНННП;
    elif( PartyID > 0 )
      if( КлиентПоСчету == NULL )
        КлиентПоСчету = ZNSParty( PartyID );
      end;
      if( ( ( КлиентПоСчету.LegalForm == PTLEGF_INST  ) and ( НаимНП != КлиентПоСчету.FullName ) ) or
          ( ( КлиентПоСчету.LegalForm == PTLEGF_PERSN ) and ( ФИОИП  != КлиентПоСчету.FullName ) ) )
        ErrorMes = IfThenElse( ErrorMes == "", "Наименование клиента не совпадает с указанным в запросе ФНС", ErrorMes );
      end;
    end;
    return IfThenElse( ( Error != 0 ) or ( ErrorMes != "" ), 1, 0 ); 
  end;

  macro PrintDoc()
    Array Text, Buttons;
    if( ClientID > 0 )
      КлиентПоСчету = ZNSParty( ClientID );
    end;
    if( (ClientID == 0) and CheckBeforePrint() ) // проверку субъекта делаем только для сообщений ZNS
      if( GetDialogFlag() and ( Error == 0 ) ) // на все предупреждения выдадим запрос
        Text(0) = ErrorMes + ".|Продолжить печать?";
        Buttons( 0 ) = "Да";
        Buttons( 1 ) = "Нет";
        Error = IfThenElse( ConfWin( Text, Buttons ) == 1, 1, 0 );
      else
        msgbox( ErrorMes );
      end;
      if( Error )
        return 0; // ошибка уже обработана
      end;
    end;

    if( Error == 0 )
      if( ValType( КлиентПоСчету ) == V_GENOBJ )
        ClientID = КлиентПоСчету.PartyID;
      elif( ( (ВидСправки == "NS") or (ВидЗапроса == "1") ) and not IsPSReport )
        ClientID = -1; // в этом случае выводим справку об отсутствии счетов
      elif( ClientID > 0 )  
        КлиентПоСчету = ZNSParty( ClientID );
      end;
      //Error = this.PrintReport(ClientID);
      
      if( RegPartyKind == REGPT_PENSFOND ) //ПФР
        if( (ВидСправки == "OS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_OS ) )
          Error = ExecMacroFile( "zpsosprn.mac", "PrintReportInfoAboutRestAccounts", this, ПоСостояниюНаДату );
        elif( (ВидСправки == "NS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_NS) )
          Error = ExecMacroFile( "zpsnsprn.mac", "PrintReportInfoAboutExistenceAccounts", this, ДатаЗапроса );
        elif( (ВидСправки == "VS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_VS) )
          Error = ExecMacroFile( "zpsvsprn.mac", "PrintReportStatementOprAccounts", this, ДатаЗапроса );
        end;
      elif( RegPartyKind == REGPT_FUND_SOCIAL_INSURANCE ) //ФСС
        if( (ВидСправки == "OS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_OS ) )
          Error = ExecMacroFile( "zfsosprn.mac", "PrintReportInfoAboutRestAccounts", this, ПоСостояниюНаДату );
        elif( (ВидСправки == "NS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_NS) )
          Error = ExecMacroFile( "zfsnsprn.mac", "PrintReportInfoAboutExistenceAccounts", this, ДатаЗапроса );
        elif( (ВидСправки == "VS") and (LnkTypeInfo == WLD_SUBKIND_FNSINFO_VS) )
          Error = ExecMacroFile( "zfsvsprn.mac", "PrintReportStatementOprAccounts", this, ДатаЗапроса );
        end;
      elif( RegPartyKind == REGPT_TAXINSTITUTE ) //ФНС
        
        if( (ВидСправки == "NS") or (ВидЗапроса == "1") )
          if (LnkTypeInfo == WLD_SUBKIND_FNSINFO_NS2)
            Error = ExecMacroFile( "znsnsdprn.mac", "PrintReportInfoAboutExistenceAccounts", this, ДатаЗапроса );
          else
            Error = ExecMacroFile( "znsnsprn.mac", "PrintReportInfoAboutExistenceAccounts", this, ДатаЗапроса );
          end;
        elif( (ВидСправки == "OS") or (ВидЗапроса == "2") )

          if (LnkTypeInfo == WLD_SUBKIND_FNSINFO_OS2)
            Error = ExecMacroFile( "znsosdprn.mac", "PrintReportInfoAboutRestAccounts", this, ПоСостояниюНаДату );
          else
            Error = ExecMacroFile( "znsosprn.mac", "PrintReportInfoAboutRestAccounts", this, ПоСостояниюНаДату );
          end;
        elif( (ВидСправки == "VS") or (ВидЗапроса == "3") )
          if (LnkTypeInfo == WLD_SUBKIND_FNSINFO_VS2)
            Error = ExecMacroFile( "znsvsdprn.mac", "PrintReportStatementOprAccounts", this, ДатаЗапроса );
          else
            Error = ExecMacroFile( "znsvsprn.mac", "PrintReportStatementOprAccounts", this, ДатаЗапроса );
          end;
        end;
      end;
    end;             
    return Error;
  end;

  private macro pumpParams
    var rgd : Tbfile = Tbfile("wlregdec.dbt", "r", 0);
    rgd.rec.DecisionID = WlRegDecID;
    if( getEQ(rgd) )
      Copy(pr_wlregdec, rgd);
    else
      RunError("Не найдена справка/выписка ФНС с ID=" + WlRegDecID);
    end;

    FillDepInfoAndPs;
    FillMethodsReceivingRequest;
  end;

  pumpParams;
  initMessageReader();
end;

// Печать справок/выписок в ФНС по сообщениям форм BNS, BOS, BV
macro PrintMesFNSINFO( addrMes, MassCopy, ВидСправки, ВидЗапроса ):bool

  SetBuff( wlmes, addrMes );
;
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  
  FILE MsgTmpFile() txt;
  var stat:bool = true;

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  if( wlmes.Kind != MESKIND_MNS )
    stat = false;
  end;

  if( stat == true )
    var DecisionID : integer = GetObjIDByMesLink(wlmes.MesID, OBJTYPE_FNSINFO);
    if( DecisionID > 0 )
      var rgd : Tbfile = Tbfile("wlregdec.dbt", "r", 0);
      var prn: FNSPrint = FNSPrint(DecisionID);
      rgd.rec.DecisionID = DecisionID;
      if( getEQ(rgd) )
        prn.LnkTypeInfo         = rgd.rec.LnkTypeInfo;
        prn.НаимНО              = rgd.rec.RecipientName;
        prn.АдрНО               = GetNOAdress( rgd.rec.RecipientID );
        prn.RepBankID           = rgd.rec.OriginatorID;
        prn.НаимКО              = rgd.rec.OriginatorName;
        prn.ДатаЗапроса         = rgd.rec.LnkDocDate;
        prn.НомерЗапроса        = rgd.rec.LnkDocNumber;
        prn.НаимНП              = rgd.rec.ClientName;
        prn.ИНННП               = rgd.rec.ClientINN;
        prn.КППНП               = rgd.rec.ClientKPP;
        prn.ВидСправки          = ВидСправки;
        prn.ВидЗапроса          = ВидЗапроса;
        prn.Счет                = GetLnkAccounts(rgd.rec.DecisionID);
        prn.FillChanges;
        prn.ClientID            = rgd.rec.ClientID;
        
        if( (prn.ВидСправки == "NS") or (prn.ВидСправки == "OS") or
            (prn.ВидЗапроса == "1")  or (prn.ВидЗапроса == "2") )
          prn.ПоСостояниюНаДату   = rgd.rec.Date;
        elif( (prn.ВидСправки == "VS") or (prn.ВидЗапроса == "3") )
          prn.НачалоПериода       = rgd.rec.Date;
          prn.КонецПериода        = rgd.rec.EndDate;
        end;

        stat = prn.PrintDoc() == 0;
      end;
    end;
  end;

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  if( stat == true )
    wlmes.IsPrinted = "X";
    if( not UpdateMes(wlmes) )
      msgbox("Ошибка при обновлении сообщения");
      stat = false;
    end;
  end;
  if( not GetDialogFlag() and ( GetMNSWarning() != "" ) )// выводим предупреждения в протокол
    msgbox( GetMNSWarning() );
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage(er);
  return false;
end;

macro printCarryes( Account:string, FIID:integer, D1:date, D2:date, RegKind, RegDecID )
  var N = 0,
      TrDate = "",
      Kind = 0,
      NDoc = "",
      DocDate = "",
      CorrAcc = "",
      CBankName = "",
      BIC = "",
      ClName = "",
      ClINN = "",
      ClKPP = "",
      ClAcc = "",
      Amount_D = "",
      Amount_K = "",
      Ground = "";
 
  private var select = " SELECT d.t_Date_Carry as t_OprDate,                                                       "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when pm.t_DocKind in (430,440)                                       "+
                       "                 then chr(1)                                                               "+
                       "                 else rm.t_ShifrOper                                                       "+
                       "                 end)                                                                      "+
                       "          else d.t_Shifr_Oper                                                              "+
                       "          end ) as t_ShifrOper,                                                            "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then rm.t_Number                                                                 "+
                       "          else d.t_Numb_Document                                                           "+
                       "          end ) as t_Number,                                                               "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then rm.t_Date                                                                   "+
                       "          else d.t_Date_Carry                                                              "+
                       "          end ) as t_DocDate,                                                              "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account1                                   "+
                       "                 then rm.t_ReceiverCorrAccNostro                                           "+
                       "                 else rm.t_PayerCorrAccNostro                                              "+
                       "                 end)                                                                      "+
                       "          else chr(1)                                                                      "+
                       "          end ) as t_CorAcc,                                                               "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account2                                   "+
                       "                 then rm.t_ReceiverBankName                                                "+
                       "                 else rm.t_PayerBankName                                                   "+
                       "                 end)                                                                      "+
                       "          else chr(1)                                                                      "+
                       "          end ) as t_BankName,                                                             "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account3                                   "+
                       "                 then cr.t_BankCode                                                        "+
                       "                 else db.t_BankCode                                                        "+
                       "                 end)                                                                      "+
                       "          else chr(1)                                                                      "+
                       "          end ) as t_BIC,                                                                  "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account4                                   "+
                       "                 then pm.t_Receiver                                                        "+
                       "                 else pm.t_Payer                                                           "+
                       "                 end)                                                                      "+
                       "          else ( case when d.t_Account_Payer = :Account5                                   "+
                       "                 then (select t_Client                                                     "+
                       "                         from daccount_dbt                                                 "+
                       "                        where t_Account = d.t_Account_Receiver                             "+
                       "                          and t_Code_Currency = d.t_FIID_Payer                             "+
                       "                      )                                                                    "+
                       "                 else (select t_Client                                                     "+
                       "                         from daccount_dbt                                                 "+
                       "                        where t_Account = d.t_Account_Payer                                "+
                       "                          and t_Code_Currency = d.t_FIID_Receiver                          "+
                       "                      )                                                                    "+
                       "                 end)                                                                      "+
                       "          end ) as t_ClientID,                                                             "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account6                                   "+
                       "                 then rm.t_ReceiverName                                                    "+
                       "                 else rm.t_PayerName                                                       "+
                       "                 end)                                                                      "+
                       "          else chr(1)                                                                      "+
                       "          end ) as t_ClName,                                                               ";
  if( (RegKind == REGPT_TAXINSTITUTE) or (RegKind == REGPT_PENSFOND) )
    Select = Select +  "        ( case when NVL (pd.t_paymentid, 0) <> 0 and d.t_Result_Carry <> 71                "+
                       "          then ( case when d.t_Account_Payer = :Account7                                   "+
                       "                 then pm.t_ReceiverAccount                                                 "+
                       "                 else pm.t_PayerAccount                                                    "+
                       "                 end)                                                                      "+
                       "          else ( case when d.t_Account_Payer = :Account8                                   "+
                       "                 then d.t_Account_Receiver                                                 "+
                       "                 else d.t_Account_Payer                                                    "+
                       "                 end)                                                                      "+
                       "          end ) as t_Account,                                                              ";
  elif(RegKind == REGPT_FUND_SOCIAL_INSURANCE)
      Select = Select +  "        ( case when conf.t_DocKind = 420                                                 "+
                         "          then '-'                                                                       "+
                         "          else conf.t_ReceiverAccount                                                    "+
                         "          end ) as t_Account,                                                            ";
  end;
  if( RegKind == REGPT_TAXINSTITUTE )
    Select = Select +  "        ( case when d.t_Account_Payer = :Account9                                          "+
                       "          then d.t_Sum_Payer                                                               "+
                       "          else 0                                                                           "+
                       "          end ) as t_AmountD,                                                              "+
                       "        ( case when d.t_Account_Receiver = :Account10                                      "+
                       "          then d.t_Sum_Receiver                                                            "+
                       "          else 0                                                                           "+
                       "          end ) as t_AmountC,                                                              ";
  else                     
    Select = Select +  "        ( case when conf.t_DKFlag = 1                                                      "+
                       "          then conf.t_Sum                                                                  "+
                       "          else 0                                                                           "+
                       "          end ) as t_AmountD,                                                              "+
                       "        ( case when conf.t_DKFlag = 2                                                      "+
                       "          then conf.t_Sum                                                                  "+
                       "          else 0                                                                           "+
                       "          end ) as t_AmountC,                                                              ";
  end;
    Select = Select +  "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then rm.t_Ground                                                                 "+
                       "          else d.t_Ground                                                                  "+
                       "          end ) as t_Ground,                                                               "+
                       "        NVL (pd.t_paymentid, 0) AS t_PaymentID,                                            "+
                       "        ( case when NVL (pd.t_paymentid, 0) <> 0                                           "+
                       "          then ( case when d.t_Account_Payer = :Account11                                  "+
                       "                 then rm.t_ReceiverINN                                                     "+
                       "                 else rm.t_PayerINN                                                        "+
                       "                 end)                                                                      "+
                       "          else ( case when d.t_Account_Payer = :Account12                                  "+
                       "               then  (select t_code                                                        "+
                       "                      from  dpartcode_dbt par                                              "+
                       "                       where par.t_partyid = d.t_accountid_receiver                        "+
                       "                       and par.t_codekind = :PINN1)                                        "+
                       "               else (select t_code                                                         "+
                       "                       from  dpartcode_dbt par                                             "+
                       "                       where par.t_partyid = d.t_accountid_payer                           "+
                       "                       and par.t_codekind = :PINN2)                                        "+
                       "               end)                                                                        "+
                       "          end ) as t_FullINN                                                               "+
                       "   FROM dpmdocs_dbt pd,                                                                    "+
                       "        dacctrn_dbt     d,                                                                 ";
  if( RegKind != REGPT_TAXINSTITUTE )
    Select = Select +  "        dwlconf_dbt     conf,                                                               ";
  end;
    Select = Select +  "        dpmpaym_dbt     pm,                                                                "+
                       "        dpmrmprop_dbt   rm,                                                                "+
                       "        dpmprop_dbt     db,                                                                "+
                       "        dpmprop_dbt     cr                                                                 "+
                       "   WHERE d.t_date_carry between :Date1 and :Date2                                          "+
                       "     AND d.t_chapter = 1                                                                   "+
                       "     AND d.t_state = 1                                                                     "+
                       "     AND ( d.t_Account_Payer    = :Account13 AND d.t_FIID_Payer    = :FIID1 OR             "+
                       "           d.t_Account_Receiver = :Account14 AND d.t_FIID_Receiver = :FIID2 )              "+
                       "     AND d.t_acctrnid = pd.t_acctrnid(+)                                                   "+
                       "     AND pm.t_PaymentID(+) = pd.t_PaymentID                                                "+
                       "     AND rm.t_PaymentID(+) = pd.t_PaymentID                                                "+
                       "     AND db.t_PaymentID(+) = pd.t_PaymentID                                                "+
                       "     AND db.t_DebetCredit(+) = 0                                                           "+
                       "     AND cr.t_PaymentID(+) = pd.t_PaymentID                                                "+
                       "     AND cr.t_DebetCredit(+) = 1                                                           ";
  if( RegKind != REGPT_TAXINSTITUTE )
    Select = Select +  "     AND conf.t_HeadID = :DecisionID                                                       "+
                       "     AND conf.t_Account = :Account15                                                       "+
                       "     AND conf.t_CarryID = d.t_acctrnid                                                     ";
  end;
    Select = Select +  "  ORDER BY d.t_Date_Carry                                                                  ";
    
  private var params;
  if( RegKind == REGPT_PENSFOND ) 
    params = makeArray( SQLParam( "Account1" , Account   ),  SQLParam( "Account2" , Account   ),
                        SQLParam( "Account3" , Account   ),  SQLParam( "Account4" , Account   ),
                        SQLParam( "Account5" , Account   ),  SQLParam( "Account6" , Account   ),
                        SQLParam( "Account7" , Account   ),  SQLParam( "Account8" , Account   ),
                        SQLParam( "Account11", Account   ),  SQLParam( "Account12", Account   ),
                        SQLParam( "PINN1"    , PTCK_INN  ),  SQLParam( "PINN2"    , PTCK_INN  ),
                        SQLParam( "Date1"    , D1        ),  SQLParam( "Date2"    , D2        ),
                        SQLParam( "Account13", Account   ),  SQLParam( "FIID1"    , FIID      ),
                        SQLParam( "Account14", Account   ),  SQLParam( "FIID2"    , FIID      ),
                        SQLParam( "DecisionID", RegDecID ), SQLParam( "Account15", Account   ));
  elif( RegKind == REGPT_TAXINSTITUTE )
    params = makeArray( SQLParam( "Account1" , Account   ),  SQLParam( "Account2" , Account   ),
                        SQLParam( "Account3" , Account   ),  SQLParam( "Account4" , Account   ),
                        SQLParam( "Account5" , Account   ),  SQLParam( "Account6" , Account   ),
                        SQLParam( "Account7" , Account   ),  SQLParam( "Account8" , Account   ),
                        SQLParam( "Account9" , Account   ),  SQLParam( "Account10", Account   ),
                        SQLParam( "Account11", Account   ),  SQLParam( "Account12", Account   ),
                        SQLParam( "PINN1"    , PTCK_INN  ),  SQLParam( "PINN2"    , PTCK_INN  ),
                        SQLParam( "Date1"    , D1        ),  SQLParam( "Date2"    , D2        ),
                        SQLParam( "Account13", Account   ),  SQLParam( "FIID1"    , FIID      ),
                        SQLParam( "Account14", Account   ),  SQLParam( "FIID2"    , FIID      ) );
  else
    params = makeArray( SQLParam( "Account1" , Account   ),  SQLParam( "Account2" , Account   ),
                        SQLParam( "Account3" , Account   ),  SQLParam( "Account4" , Account   ),
                        SQLParam( "Account5" , Account   ),  SQLParam( "Account6" , Account   ),
                        SQLParam( "Account11", Account   ),  SQLParam( "Account12", Account   ),
                        SQLParam( "PINN1"    , PTCK_INN  ),  SQLParam( "PINN2"    , PTCK_INN  ),
                        SQLParam( "Date1"    , D1        ),  SQLParam( "Date2"    , D2        ),
                        SQLParam( "Account13", Account   ),  SQLParam( "FIID1"    , FIID      ),
                        SQLParam( "Account14", Account   ),  SQLParam( "FIID2"    , FIID      ),
                        SQLParam( "DecisionID", RegDecID ), SQLParam( "Account15", Account   ));
  end;
  
  var rs = execSQLselect( select, params, FALSE );
  
  N = 0;
  
  var i = 1;
  while( rs and  rs.moveNext()  )
    N = i;
    var FullINN = rs.value("t_FullINN");
    TrDate = pyOr(string( rs.value("t_OprDate") ), "-");
    Kind = pyOr(rs.value("t_ShifrOper"), "-");
    NDoc = pyOr(rs.value("t_Number"), "-");
    DocDate = pyOr(string( rs.value("t_DocDate") ), "-");
    CorrAcc = pyOr(IfThenElse( FullINN == 0, {CORAC_Bank}, rs.value("t_CorAcc") ), "-");
    CBankName = pyOr(IfThenElse( FullINN == 0, {Name_Bank} , rs.value("t_BankName") ), "-");
    BIC = pyOr(IfThenElse( FullINN == 0, {MFO_Bank}  , rs.value("t_BIC") ), "-");
    ClName = pyOr(rs.value("t_ClName"), "-");
    ClINN = pyOr(RemoveKPP(rs.value("t_FullINN")), "-");
    ClKPP = pyOr(RemoveINN(rs.value("t_FullINN")), "-");
    ClAcc = pyOr(rs.value("t_Account"), "-");
    Amount_D = IfThenElse( rs.value("t_AmountD") == 0, "-", StrSubst(string( rs.value("t_AmountD"):16:2 ), ".", "-" ));
    Amount_K = IfThenElse( rs.value("t_AmountC") == 0, "-", StrSubst(string( rs.value("t_AmountC"):16:2 ), ".", "-" ));
    Ground = pyOr(rs.value("t_Ground"), "-");
   //│    │  дата    │ вид  │номер │   дата   │номер корресп.счета │наименовани│   БИК   │Наимено- │  ИНН / КИО │   КПП    │    номер счета     │  по дебету     │ по кредиту     │основание  │
   [ │####│##########│######│######│##########│####################│###########│#########│#########│############│##########│####################│################│################│###########│]
   (N:c,  
    TrDate:c, 
    Kind:c, 
    NDoc:w, 
    DocDate:c,
    CorrAcc:c, 
    CBankName:w, 
    BIC:c, 
    ClName:w,
    ClINN:c, 
    ClKPP:c, 
    ClAcc:c, 
    Amount_D:r, 
    Amount_K:r,
    Ground:w);
    
    i = i + 1;
  end;
  return i - 1;

end;
