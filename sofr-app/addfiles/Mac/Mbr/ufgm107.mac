/*
  $Name:         ufgm107.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED107 транспорта УФЭБС
*/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac", "pmlib.mac", "pm_const.mac";

const ED107v3 = 434;

private macro CheckCorPZN( Payment )
  var rs;
  var SelectStr = "";
  var ParticipantID = 0;
  var CorrID = 0;
  
  rs = execSQLSelect( " Select cor.t_CorrID "
                      "   From dcorschem_dbt cor "
                      "  Where cor.t_Number  = :CorNumber "
                      "    and cor.t_FIID = :FIID "
                      "    and cor.t_FI_Kind = :FI_Kind ",
                      MakeArray( SQLParam("CorNumber", Payment.OutCorschem),
                                 SQLParam("FIID", Payment.OutCorschemFIID),
                                 SQLParam("FI_Kind", FIKIND_CURRENCY)));
  if( rs and rs.MoveNext )
    CorrID = rs.value("t_CorrID");
    if( not БанкУБР(CorrID) )
      return true;
    end;
  end;
  
  SelectStr = " Select dep.t_PartyID "
              "   From ddp_dep_dbt dep, dcorschem_dbt cor "
              "  Where dep.t_Code = cor.t_Department "
              "    and cor.t_CorrID = :CorrID ";
              
  rs = execSQLSelect( SelectStr, MakeArray( SQLParam("CorrID", CorrID)));
  
  if( rs and rs.MoveNext() )
    ParticipantID = rs.value("t_PartyID");
  end;
  
  rs = null;
  rs = execSQLSelect( " Select dprt.t_PZN "
                      "   From dbankdprt_dbt dprt "
                      "  Where dprt.t_PartyID = :PartyID ",
                      MakeArray(SQLParam("PartyID", ParticipantID)));
                      
  if( rs and rs.MoveNext() )
    if( rs.value("t_PZN") == "40" )
      return false;
    else
      return true;
    end;
  end;
  
  return true;
  
end;

macro CheckPaym( addr )

  SetBuff( wlpmpaym,  addr );
  var RsPaym: RsbPayment = RsbPayment( wlpmpaym.PaymentID );

  var err_msg : string = "", err : integer = 0;

  var rlsFormID : integer = 0;
  if( IsDebetPaym( RsPaym ) )
    rlsFormID = RsPaym.GetDEBET().rec.RlsFormID;
  else
    rlsFormID = RsPaym.GetCREDIT().rec.RlsFormID;
  end;
  if( InList( rlsFormID, ED107v3 ) )
    var PaymentPrecedence = RsPaym.GetPMRMPROP().rec.Precedence;
    var PaymentKind = RsPaym.PaymentKind;
    if( not CheckPaymentPrecedence( PaymentPrecedence, PaymentKind ) )
      err = 1;
      err_msg = "Некорректное значение приоритета распоряжения. Для срочного сервиса допустимы значения \"13\" или в диапазонах \"50-59\", \"60-69\". Для несрочного сервиса только значения в диапазоне \"70-79\""; 
    end;
  end;

  if( not CheckCorPZN(RsPaym) )
    err_msg = "В адрес ПУ (клиента ПУ) не может быть направлено ЭПС " + "ED107";
  end;

  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;

end;

private macro IsCorrectSwiftBIC( swift_bic:string, need_check:bool  ) : bool

  if( need_check )
    var rs_swift = execSQLselect( "select REGEXP_INSTR( :swift_bic, '^[A-Z]{4}[A-Z]{2}[A-Z0-9]{2}([A-Z0-9]{3})*$') from dual", 
                                  makeArray( SQLParam("swift_bic", swift_bic) ), false );
 
    if( ( rs_swift and  rs_swift.moveNext() ) and ( rs_swift.value(0) == 1 ) )
      return true;
    end;
  else
    return true;
  end;

  return false;
end;

private macro IsCodeDefined( code_value:string, code_kind:integer ):bool
  
  if( (code_value == "") or (not InList( code_kind, PTCK_BIC, PTCK_SWIFT) ) or (( code_kind == PTCK_SWIFT ) and ( not IsCorrectSwiftBIC(code_value, true ) ) ) )
    return false;
  end;

  return true;
end;

macro GenMesV3( addrMes, addrPm )
  
  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);
  
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  
  var RsPaym: RsbPayment = RsbPayment( wlpmpaym.PaymentID );
  
  var err = 0;
  var swift = "";

  var need_check =  Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED107_ПРОВЕРЯТЬ_SWBIC", V_BOOL, false);
  
  var ed = EDNoDateAuthor();
  ed.ConstructByTrn( wlmes.TRN );
  
  ЗаписатьПолеЛог( "EDNo",     ed.EDNo );
  ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(ed.EDDate) );
  ЗаписатьПолеЛог( "EDAuthor", ed.EDAuthor );
  
  ЗаписатьПолеЛог( "TransKind", "01" );
  
  if( RsPaym.Priority != 5 )
    var ErrMes = "Недопустимое значение очередности платежа. Для поручения банка допускается только значение 5";
    RunError( ErrMes, ErrMes);
  else
    ЗаписатьПолеЛог( "Priority", string(RsPaym.Priority) );
  end;
  
  var Error;
  var SystemCode;
  if( GetSystemCodeForGenMes( "ED107", wlmes, @Error, @SystemCode ) )
    if( strlen(Error) != 0 )
      RunError(Error);
    else
      ЗаписатьПолеЛог("SystemCode",  SystemCode );
    end;
  end;
  
  var UIN : string = RsPaym.GetPMRMPROP().rec.UIN;
  if(UIN and (UIN != ""))
    CheckUIN(RsPaym.ReceiverAccount, UIN);
    ЗаписатьПолеЛог("PaymentID", UIN);
  end;
  
  var PaytKind = GetPaytKind( "ED107", RsPaym.PaymentKind, wlmes.TpSchemID );
 
  if(PaytKind and (PaytKind != ""))
    ЗаписатьПолеЛог("PaytKind", PaytKind);
  end;
  
  ЗаписатьПолеЛог("Sum", MoneyToStrUFBS(wlpmpaym.BaseAmount));

  var PaymentPrecedence : string = RsPaym.GetPMRMPROP().rec.Precedence;
  if( PaymentPrecedence != "" )
    ЗаписатьПолеЛог( "PaymentPrecedence", PaymentPrecedence );
  end;
  
  // Запрошенная (требуемая) дата исполнения распоряжения
  var ReqSettlementDate : date = GetReqSettlementDate
    ( RsPaym.OutTransferDate, 
      ed.EDDate,
      RsPaym.PaymentKind );

  if(ReqSettlementDate != date(0,0,0))
    ЗаписатьПолеЛог( "ReqSettlementDate", YYYYMMDD(ReqSettlementDate) );
  end;

  УстановитьКонтекстБлокаЛог("AccDoc");
  ЗаписатьПолеЛог( beginField, "AccDoc" );
  ЗаписатьПолеЛог("AccDocNo",   GetLastSymbols(RsPaym.Number, PM_DOCNO_NONZERO_LEN) );
  ЗаписатьПолеЛог("AccDocDate", YYYYMMDD(RsPaym.Date) );  
  ЗаписатьПолеЛог( endField, "AccDoc" ) ;
  УстановитьКонтекстБлокаЛог("..");
  
  if( RsPaym.PartPaymNumMain != "" )
    УстановитьКонтекстБлокаЛог("RelatedDoc");
    ЗаписатьПолеЛог( beginField, "RelatedDoc" );
    ЗаписатьПолеЛог("RelatedDocNo",   GetLastSymbols(RsPaym.PartPaymNumMain, 16) );
    ЗаписатьПолеЛог("RelatedDocDate", YYYYMMDD(RsPaym.PartPaymDateMain) );
    ЗаписатьПолеЛог( endField, "RelatedDoc" ) ;
    УстановитьКонтекстБлокаЛог("..");
  end;  
  
  ClearRecord( PrevInstrAgent );
  ClearRecord( SenderBank );
  ClearRecord( ExecutorBank );
  ClearRecord( IntermediaryBank );
  ClearRecord( PayerBank );
  ClearRecord( ReceiverBank );
  
  if( RsPaym.GetOperationParticipantsFromRoute(PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, PayerBank, ReceiverBank ) )
    
    var isNeedFillOrderingBank = false; // Нужно ли заполнять блок банка-плательщика
    if( not IsCodeDefined(PayerBank.CodeValue, PayerBank.CodeKind)  )
      if( PayerBank.InAccount == "" )
        isNeedFillOrderingBank = false;
      else
        isNeedFillOrderingBank = true;
      end;
    else
      isNeedFillOrderingBank = true;
    end;
    
    if( isNeedFillOrderingBank and ( SenderBank.PartyID != PayerBank.PartyID ) )
      if( PayerBank.PartyID == 0 )
        RunError("Не задан банк-плательщика");
      else
        УстановитьКонтекстБлокаЛог("OrderingBank");
        ЗаписатьПолеЛог( beginField, "OrderingBank" );      
        
        if( PayerBank.CodeKind == PTCK_BIC )
          ЗаписатьПолеЛог("BIC", PayerBank.CodeValue );
        elif( ( PayerBank.CodeKind == PTCK_SWIFT ) and IsCorrectSwiftBIC( PayerBank.CodeValue, need_check ) )
          ЗаписатьПолеЛог("SWBIC", PayerBank.CodeValue );
        end;
        
        ЗаписатьПолеЛог("BankAccount", RsPaym.PayerAccount);
        
        if( (PayerBank.Name != "") and (not InList( PayerBank.CodeKind, PTCK_BIC, PTCK_SWIFT )) )      
          УстановитьКонтекстБлокаЛог("Name");
          ЗаписатьПолеЛог( beginField, "Name" );              
          ЗаписатьПолеЛог("Name", PayerBank.Name );        
          ЗаписатьПолеЛог( endField, "Name" ) ;
          УстановитьКонтекстБлокаЛог("..");
        end;

        ЗаписатьПолеЛог( endField, "OrderingBank" ) ;
        УстановитьКонтекстБлокаЛог("..");
      end;
    end;

    var isNeedFillPrevInstrAgent = false; // Нужно ли заполнять блок сведений о предыдущем инструктирующем банке
    if( isNeedFillOrderingBank )
      if( not IsCodeDefined(PrevInstrAgent.CodeValue, PrevInstrAgent.CodeKind) )
        if( PrevInstrAgent.OutAccount == "" )
          isNeedFillPrevInstrAgent = false;
        else
          isNeedFillPrevInstrAgent = true;
        end;
      else
        isNeedFillPrevInstrAgent = true;
      end;
    else
      isNeedFillPrevInstrAgent = false;
    end;

    if( isNeedFillPrevInstrAgent and (PrevInstrAgent.PartyID != 0) )
      УстановитьКонтекстБлокаЛог("PrevInstrAgent");
      ЗаписатьПолеЛог( beginField, "PrevInstrAgent" );
            
      if( PrevInstrAgent.CodeKind == PTCK_BIC )
        ЗаписатьПолеЛог("BIC", PrevInstrAgent.CodeValue );
      elif( ( PrevInstrAgent.CodeKind == PTCK_SWIFT ) and IsCorrectSwiftBIC( PrevInstrAgent.CodeValue, need_check ) )
        ЗаписатьПолеЛог("SWBIC", PrevInstrAgent.CodeValue );
      end;
      
      ЗаписатьПолеЛог("BankAccount", PrevInstrAgent.OutAccount);
      
      if( (PrevInstrAgent.Name != "") and (not InList( PrevInstrAgent.CodeKind, PTCK_BIC, PTCK_SWIFT )) )      
        УстановитьКонтекстБлокаЛог("Name");
        ЗаписатьПолеЛог( beginField, "Name" );              
        ЗаписатьПолеЛог("Name", PrevInstrAgent.Name );        
        ЗаписатьПолеЛог( endField, "Name" ) ;
        УстановитьКонтекстБлокаЛог("..");
      end;      

      ЗаписатьПолеЛог( endField, "PrevInstrAgent" ) ;
      УстановитьКонтекстБлокаЛог("..");
    end;

    if(SenderBank.CodeKind != PTCK_BIC)
      RunError("Для банка-отправителя необходимо указать код вида 3 \"БИК (ЦБ РФ)\"");
    elif( SenderBank.PartyID == 0 )
      RunError("Не задан банк-отправителя");
    else

      УстановитьКонтекстБлокаЛог("InstructingAgent");
      ЗаписатьПолеЛог( beginField, "InstructingAgent" );      
      
      ЗаписатьПолеЛог("BIC", SenderBank.CodeValue );
      
      err = 0;      
      swift = ПолучитьКодСубъекта( SenderBank.PartyID, PTCK_SWIFT, err );
      
      if( ( err == 0 ) and IsCorrectSwiftBIC( swift, need_check ) )
        ЗаписатьПолеЛог("SWBIC", swift );
      end;
      
      ЗаписатьПолеЛог("CorrespAcc", SenderBank.OutAccount);   

      ЗаписатьПолеЛог( endField, "InstructingAgent" ) ;
      УстановитьКонтекстБлокаЛог("..");
    end;
    
    if(ExecutorBank.CodeKind != PTCK_BIC)
      RunError("Для банка-исполнителя необходимо указать код вида 3 \"БИК (ЦБ РФ)\"");
    elif( ExecutorBank.PartyID == 0 )
      RunError("Не задан банк-исполнителя");
    else

      УстановитьКонтекстБлокаЛог("InstructedAgent");
      ЗаписатьПолеЛог( beginField, "InstructedAgent" );      
      
      ЗаписатьПолеЛог("BIC", ExecutorBank.CodeValue );
      
      err = 0;      
      swift = ПолучитьКодСубъекта( ExecutorBank.PartyID, PTCK_SWIFT, err );
      
      if( ( err == 0 ) and IsCorrectSwiftBIC( swift, need_check ) )
        ЗаписатьПолеЛог("SWBIC", swift );
      end;
      
      ЗаписатьПолеЛог("CorrespAcc", ExecutorBank.OutAccount);   

      ЗаписатьПолеЛог( endField, "InstructedAgent" ) ;
      УстановитьКонтекстБлокаЛог("..");
    end;

    var isNeedFillBeneficiary = false; // Нужно ли заполнять блок сведений о банке-получателе
    if( not IsCodeDefined(ReceiverBank.CodeValue, ReceiverBank.CodeKind) )
      if( ReceiverBank.InAccount == "" )
        isNeedFillBeneficiary = false;
      else
        isNeedFillBeneficiary = true;
      end;
    else
      isNeedFillBeneficiary = true;
    end;

    var isNeedFillAcctWithInst = false; // Нужно ли заполнять блок сведений об агенте банка-получателя
    if( isNeedFillBeneficiary and ( ( ReceiverBank.PartyID != 0 ) and ( ExecutorBank.PartyID != ReceiverBank.PartyID ) ) )
      if( not IsCodeDefined(IntermediaryBank.CodeValue, IntermediaryBank.CodeKind) )
        if( IntermediaryBank.OutAccount == "" )
          isNeedFillAcctWithInst = false;
        else
          isNeedFillAcctWithInst = true;
        end;
      else
        isNeedFillAcctWithInst = true;
      end;
    else
      isNeedFillAcctWithInst = false;
    end;

    if( isNeedFillAcctWithInst and (IntermediaryBank.PartyID != 0 ) )
      УстановитьКонтекстБлокаЛог("AcctWithInst");
      ЗаписатьПолеЛог( beginField, "AcctWithInst" );
      
      if( IntermediaryBank.CodeKind == PTCK_BIC )
        ЗаписатьПолеЛог("BIC", IntermediaryBank.CodeValue );
      elif( ( IntermediaryBank.CodeKind == PTCK_SWIFT ) and IsCorrectSwiftBIC( IntermediaryBank.CodeValue, need_check ) )
        ЗаписатьПолеЛог("SWBIC", IntermediaryBank.CodeValue );
      end;
      
      ЗаписатьПолеЛог("BankAccount", IntermediaryBank.OutAccount);
      
      if( (IntermediaryBank.Name != "") and (not InList( IntermediaryBank.CodeKind, PTCK_BIC, PTCK_SWIFT )) )      
        УстановитьКонтекстБлокаЛог("Name");
        ЗаписатьПолеЛог( beginField, "Name" );              
        ЗаписатьПолеЛог("Name", IntermediaryBank.Name );        
        ЗаписатьПолеЛог( endField, "Name" ) ;
        УстановитьКонтекстБлокаЛог("..");
      end;      

      ЗаписатьПолеЛог( endField, "AcctWithInst" ) ;
      УстановитьКонтекстБлокаЛог("..");
    end;

    if( isNeedFillBeneficiary )
      if( ReceiverBank.PartyID == 0 )
        RunError("Не задан банк-получателя");
      else
        УстановитьКонтекстБлокаЛог("Beneficiary");
        ЗаписатьПолеЛог( beginField, "Beneficiary" );
        
        if( ReceiverBank.CodeKind == PTCK_BIC )
          ЗаписатьПолеЛог("BIC", ReceiverBank.CodeValue );
        elif( ( ReceiverBank.CodeKind == PTCK_SWIFT ) and IsCorrectSwiftBIC( ReceiverBank.CodeValue, need_check ) )
          ЗаписатьПолеЛог("SWBIC", ReceiverBank.CodeValue );
        end;
        
        ЗаписатьПолеЛог("BankAccount", RsPaym.ReceiverAccount);
        
        if( (ReceiverBank.Name != "") and (not InList( ReceiverBank.CodeKind, PTCK_BIC, PTCK_SWIFT )) )      
          УстановитьКонтекстБлокаЛог("Name");
          ЗаписатьПолеЛог( beginField, "Name" );              
          ЗаписатьПолеЛог("Name", ReceiverBank.Name );        
          ЗаписатьПолеЛог( endField, "Name" ) ;
          УстановитьКонтекстБлокаЛог("..");
        end;      

        ЗаписатьПолеЛог( endField, "Beneficiary" ) ;
        УстановитьКонтекстБлокаЛог("..");
      end;
    end;
  end;
  
  if( RsPaym.Ground != "" )
    УстановитьКонтекстБлокаЛог("Sndr2RecvrInfo");
    ЗаписатьПолеЛог( beginField, "Sndr2RecvrInfo" );      
    ЗаписатьПолеЛог("Sndr2RecvrInfo", RsPaym.Ground);
    ЗаписатьПолеЛог( endField, "Sndr2RecvrInfo" ) ;
    УстановитьКонтекстБлокаЛог("..");
  end;


  
  if( RsPaym.Reference != "" )
    
    var query = 
      " SELECT 1 " +
      "   FROM dwlmes_dbt wlmes " +
      " INNER JOIN dwlsess_dbt wlsess " +
      "   ON wlmes.t_SessionID = wlsess.t_SessionID " +
      " WHERE wlmes.t_TRN = :TRN AND wlsess.t_TpID = :TpID";

    var rs = execSQLselect( query, makeArray( SQLParam("TRN", RsPaym.Reference ), SQLParam("TpID", TRANSP_UFBS )), false );
    
    if(rs and rs.moveNext())
      УстановитьКонтекстБлокаЛог("InitialED");
      ЗаписатьПолеЛог( beginField, "InitialED" );
      
      var initialED = EDNoDateAuthor();
      initialED.ConstructByTrn( RsPaym.Reference );
  
      ЗаписатьПолеЛог( "EDNo",     initialED.EDNo );
      ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(initialED.EDDate) );
      ЗаписатьПолеЛог( "EDAuthor", initialED.EDAuthor );
      ЗаписатьПолеЛог( endField, "InitialED" ) ;
      УстановитьКонтекстБлокаЛог("..");

    end;
        
  end;                                     
  
  return true;

  OnError( er )
    var Message = "";
    if(strlen(er.Err) != 0)
      Message = "Error: " + er.Err;
    else
      Message = er.Message;
    end;
    std.msg(String(Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return false; 
end;
