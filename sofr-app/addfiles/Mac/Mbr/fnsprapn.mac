/*
$Name:          fnsprapn.mac
$Module:        МБ Расчеты
$Description:   Печать сообщения APN - о приостановлении поручения НО
*/

import "wldoc.mac", "fnsprapall.mac", "fnsreadmall.mac";

class ( MessageReader ) PrintAPN
  var НомРеш = "",
      ДатаРеш = "",
      НаимНо = "",
      БанкПл = "",
      БИКБПл = "",
      Фамилия = " ",
      Имя = " ",
      Отчество = "",
      FIO = "",
      ВидПоруч = "",
      ДатаПоруч = "",
      НомПоруч = "",
      ИНННП = "",
      КППНП = "",
      КласЧин = "",
      НаимБ = "",
      
  ListReg = TArray(),
  AccKindList = TArray(),
  AccList = TArray();
      
  macro ВидСч( value )
    AccKindList[AccKindList.size] = value;
  end;
  
  macro НомСчПл( value )
    AccList[AccList.size] = value;
  end;
  
  macro _ConvertBlock_( name )
    if ( name == "Руководитель" )
      FIO = Фамилия + " " + Имя + " " + Отчество;
    end;
    
    if( name == "ПриостПоруч" )
    var TempStr = "";
      if( ВидПоруч == 1 )
        TempStr = "на списание и перечисление денежных средств со счетов налогоплательщика (плательщика сбора," + 
                 " плательщика страховых взносов, налогового агента) в бюджетную систему Российской Федерации";
      elif( ВидПоруч == 2 )
        TempStr = "на перевод электронных денежных средств со счетов налогоплательщика (плательщика сбора, " +
                 "плательщика страховых взносов, налогового агента) в бюджетную систему Российской Федерации"
      end;
      TempStr = TempStr + " от " + ДатаПоруч + " N " + НомПоруч;
      
      ListReg[ListReg.Size] = TempStr; 
      
    end;
    
    
  end;
  
  macro _Normalize_()
    if (ДатаРеш == "")
      ДатаРеш = DDpMMpYYYY( ToDateYYYY_MM_DD(ДатаРеш) );
    end;
    
    
    
  end;
  
  
end;

macro Draw( APN : PrintAPN, mesId : integer )

  [
                                                        Решение N #
                                                                 ───────
                            о приостановлении действия поручений на списание и перечисление денежных
                               средств со счетов налогоплательщика (плательщика сбора, плательщика
                               страховых взносов, налогового агента), а также на перевод электронных
                               денежных средств налогоплательщика (плательщика сбора, плательщика
                                    страховых взносов, налогового агента) в бюджетную систему
                                                      Российской Федерации

                                                   от "##" ######### #### г.
                                                      ───────────────────
  ]
  (
    APN.НомРеш,
    substr( APN.ДатаРеш, 9, 2),
    Месяца( substr( APN.ДатаРеш, 6, 2 ) ):c,
    substr( APN.ДатаРеш, 1, 4 )
  );
  
  PrintTaxNameHead( APN );
  
  
  [                                        РЕШИЛ:

    Приостановить в банке ############################################################################################# действие  
                          ─────────────────────────────────────────────────────────────────────────────────────────────
                                            (полное наименование банка, БИК)
                                            
  ]
  (
    APN.БанкПл + ", " + APN.БИКБПл
  );
  
  PrintBodyAP( APN, mesId );
  
  return true;

end; 


macro PrintDoc( addrMes, MassCopy )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;


  SetBuff( wlmes, addrMes );

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpfnsprnapn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var APN = PrintAPN();
  APN.ReadMessage();

  Draw( APN, wlmes.MESID );

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  return true;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage( er );
  return false;
end;