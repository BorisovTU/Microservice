/*
 $Name:   swgm900r.mac
 $Module: MBR
 $Description: Генерация сообщения по SWIFT MT900 стандарт RUR5
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT900 стандарт RUR5                         */
/*                                                                          */
/*  Имя файла: swgm900r.mac                                                 */
/*  Создан:  12.09.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

macro GenMes( addrMes, addrConf )
  var field_value;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog(2,"Генерация сообщения по МТ900 по стандарту RUR5");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  field_value = FillRelatedReferenceForDebitCreditConfirmation(wlconf);
  ЗаписатьПолеЛог( RelatedReferenceField, field_value );

  /* 25 - счет подтверждения */
  ЗаписатьПолеЛог( AccountIdentificationField, wlconf.Account );
  
  /* 13D - Date/Time Indication */
  field_value = GetSWIFTDate( date() ) + GetSWIFTTime(Time()) + GetSessTimeZone();
  ЗаписатьПолеЛог( DateTimeIndicationField, field_value );
  
  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( wlconf.DateValue ) +
                ПолучитьКодВалютыЛог( wlconf.FIID ) +
                GetSWIFTAmount( wlconf.Sum );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );  

  /* 52a - банк приказодателя */
  if( (wlconf.CodeValue!="") OR (wlconf.BankName!="") )
    ClearRecord(Route);
    Route.PartyID  = wlconf.BankID;
    Route.CodeKind = wlconf.CodeKind;
    Route.CodeName = wlconf.CodeName;
    Route.CodeValue = wlconf.CodeValue;
    Route.Name = wlconf.BankName;
    Route.InAccount = "";
    Route.OutAccount = "";
    ЗаписатьПолеМаршрутаКонтекст0Лог( OrderingInstitutionField, "52a", Route, "", "", ST_RUR5 );
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  /* 72 - информация для банка-получателя */
  field_value = "";
  field_value = FillNZP_byPaymentGround( field_value, wlconf.Description, ST_RUR5 );
  ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;