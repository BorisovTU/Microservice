/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*       Макрофункции для формирования уведомления по срочному платежу      */
/*                                                                          */
/*  Имя файла: wlinpm.mac                                                   */
/*  Создан:  21.04.2008                                                     */
/****************************************************************************/
import "globals.mac", PTInter, likepy, oralib, PaymInter, InsCarryDoc;

const TYPETEMPLINFO_OK         = 1; //Средства зачислены на счет
const TYPETEMPLINFO_UNKNOWNSUM = 2; //Средства зачислены на счет невыясненых сумм

private macro ОпределитьТранспортСообщения(PaymentID, Trn)
  var TpID = 0;

  var select = "select wts.t_TpID, wm.t_Trn "+
                 "from dWlpm_dbt wp, dWlmeslnk_dbt wml, dWlmes_dbt wm, dwltpshem_dbt wts "+
                "where wp.T_PAYMENTID = :PaymentID "+
                  "and wp.t_Direct = chr(88) "+
                  "and wml.t_ObjID = wp.t_WlpmID "+
                  "and wml.t_ObjKind = 501 "+  //OBJTYPE_PAYMENT
                  "and wm.t_MesID = wml.t_MesID "+
                  "and wts.t_TpShemID = wm.t_TpSchemID";

  var params = MakeArray( SQLParam("PaymentID", PaymentID));      
  
  var rs = execSQLselect( select, params, false );
    
  if(rs and rs.moveNext() )
    TpID = rs.value(0);
    SetParm(1, rs.value(1));
  else
    SetParm(1, "");
  end;
  
  return TpID;
end;

macro ФормированиеУведомленияПоСрочномуПлатежу(PaymentID, TypeTempl)
  
  var Trn = "",
      StrInfo = "",
      err = 0,
      TpID = ОпределитьТранспортСообщения(PaymentID, Trn); 

  record pmpaym (pmpaym);
  record debet  (pmprop);
  record credit (pmprop);
  record rm     (pmrmprop);
  record Wlinfo (wlinfo);
  record Party  (Party);
  
  if( FindPayment( PaymentID, 0, 0, 0, 0, true, pmpaym, debet, credit, rm))
    msgbox("Не найден платеж ID = " + PaymentID);
    return false;
  end;

  Wlinfo.Direct = "";//WLD_MES_OUT
  Wlinfo.Trn = "";
  Wlinfo.RelatedRef  = Trn;        
  Wlinfo.TypeOper = "139";
  
  //Заполняем отправителя
  Wlinfo.OriginatorID = pmpaym.ReceiverMesBankID;
  Wlinfo.OriginatorCodeKind = PTCK_CLIRING;
  Wlinfo.OriginatorCode = GetCodeParty(Wlinfo.OriginatorID, Wlinfo.OriginatorCodeKind, err);
  
  if(err != 0)
    msgbox("Не найден код №", Wlinfo.OriginatorCodeKind, " у субъекта с ID = ", Wlinfo.OriginatorID);
    return false;
  end;

  if(ПолучитьСубъекта (Wlinfo.OriginatorID, Party) != 0)
    msgbox("Не найден субъект с ID = ", Wlinfo.OriginatorID);
    return false;
  end;

  Wlinfo.OriginatorName = Party.Name;

  //Заполняем получателя
  Wlinfo.RecipientID       = pmpaym.PayerMesBankID;
  Wlinfo.RecipientCodeKind = PTCK_CLIRING;
  Wlinfo.RecipientCode     = GetCodeParty(Wlinfo.RecipientID, Wlinfo.RecipientCodeKind, err);
  
  if(err != 0)
    msgbox("Не найден код №",Wlinfo.RecipientCodeKind, " у субъекта с ID = ", pmpaym.ReceiverMesBankID);
    return false;
  end;

  if(ПолучитьСубъекта (Wlinfo.RecipientID, Party) != 0)
    msgbox("Не найден субъект с ID = ", Wlinfo.RecipientID);
    return false;
  end;

  Wlinfo.RecipientName = Party.Name;
  
  //Заполняем подразделение
  Wlinfo.OfficeID = Wlinfo.OriginatorID;             
  Wlinfo.OfficeCodeKind = Wlinfo.OriginatorCodeKind;                   
  Wlinfo.OfficeCode = Wlinfo.OriginatorCode;

  //Узнаем сколько сейчас времени
  var curTime = Time(), hour, min;
  TimeSplit(curTime, hour, min);
  
  //Найдем ФИО опера
  var Person = Tbfile("person.dbt", "r", "bank.def");
  Person.rec.Oper = {oper};
  Person.GetEQ();
  
  if(TypeTempl == TYPETEMPLINFO_OK)
    StrInfo = "Средства по СПП N " + rm.Number + " от " + rm.Date +" "+ rm.PayerName + " на сумму " + pmpaym.Amount +
              " в " + hour + " ч. " + min + " мин. "+ date() + " зачислены на счет N " + pmpaym.ReceiverAccount+ " "+
              GetNameClient(NULL,pmpaym.ReceiverAccount,1,pmpaym.PayFIID) + ". " + Person.rec.Name;
  else
    StrInfo = "Средства по СПП N " + rm.Number + " от " + rm.Date +" "+ rm.PayerName + " на сумму " + pmpaym.Amount +
              " в " + hour + " ч. " + min + " мин. "+ date() + " зачислены на счет невыясненных сумм. " + 
              Person.rec.Name;
  end;

  if(CreateInfoMess(Wlinfo, StrInfo, TpID))
    msgbox("Ошибка создания информационного сообщения.");
    return false;
  end;

  return true;
end;
