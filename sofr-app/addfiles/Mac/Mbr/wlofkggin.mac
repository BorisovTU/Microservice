/*
 $Name:        wlofkggin.mac
 $Module:      MBR
 $Description: Импорт сообщений ГИС ГМП
*/
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                      Импорт  сообщений  ГИС ГМП                          */
/*                                                                          */
/*  Имя файла: wlofkggin.mac                                                */
/*  Создан:  23.04.13                                                       */
/****************************************************************************/

import "wlimport.mac", "wlufsign.mac", "wlofkggtools.mac";

const CONTEXTID_CHECK_SIGN_GG = "ГИСГМП|импорт";

private macro ToDateGG( strYYYYMMDD )
  return date( int(substr(strYYYYMMDD, 9, 2)), int(substr(strYYYYMMDD, 6, 2)), int(substr(strYYYYMMDD, 1, 4)) );
end;

private macro CorrectMesField( fname:string ) : string
  var fields = makeArray( 
                          "ID", 
                          "TimeStamp",
                          "Faultcode",
                          "Faultstring",
                          "Detail", // block
                          "MessageId"
                        );
  for( var correct, fields, 1 )
    if( strLwr(correct) == strLwr(fname) )
      return correct;
    end;
  end;
  return fname;
end;

private macro GetNode(node:object, name:string):object
  var child:object = NULL, 
      find:object = NULL;
  var chNodeList:object = node.childNodes();

  if( chNodeList.length > 0 )
    var i = 0;
    while( (child = chNodeList.item( i )) != NULL )
      if( child.nodeType == CHILD_NODE )
        if( GetNodeName( child.NodeName ) == Name )
          return child;
        else
          find = GetNode( child, name );
          if( find )
            break;
          end;
        end;
      end;
      i = i + 1;
    end;
  end;
  return find;
end;

private macro HandleSourceMessage( node:object )
  УстановитьКонтекстБлока( "SourseMessage"/*GetNodeName( node.nodename )*/ );
    ЗаписатьПоле( beginField, node.nodename );

    var child = node.childNodes();
    if( child.length() > 0 )
      child = child.item(0);
      if( child.nodeType == CDATA_SECTION_NODE )
        var child_xml:object = ActiveX( "MSXML2.DOMDocument" );
        child_xml.async = false;

        if( not child_xml.loadXML( child.nodeValue ) )
          ErrImport( string( "Неверный формат тега <SourceMessage>|Невозможно обработать дочерний элемент" ) );
          if( child_xml.parseError.errorCode != 0 )
            ErrImport( child_xml.parseError.reason );
          end;
        else
          child = GetNode(child_xml, "RequestMessage");
          if( child )
            ЗаписатьПоле( "RequestMessageID", IfThenElse( child.attributes.length(), child.attributes.item(0).nodeValue, "" ) );

            while( ( child = GetNode(child, "FinalPayment") ) != NULL )
              УстановитьКонтекстБлока( "FinalPaymentID" );
                ЗаписатьПоле( beginField, "FinalPaymentID" );
                ЗаписатьПоле( "FinalPaymentID", IfThenElse( child.attributes.length(), child.attributes.item(0).nodeValue, "" ) );
                ЗаписатьПоле( endField, "FinalPaymentID" );
              УстановитьКонтекстБлока( ".." );
            end;
          end;
        end;
      end;
    end;

    ЗаписатьПоле( endField, node.nodename );
  УстановитьКонтекстБлока( ".." );
end;

macro ОбработатьДочернийЭлемент( node:object, isRoot:bool, isNeedProcAttr:bool )
  var i, child:object;
  var chNodeList:object;
  var attributes:object;
  i=0;
  chNodeList = node.childNodes();
  if( chNodeList.length() )
    i = 0;
    while( ( child = chNodeList.item( i ) ) != NULL )
      if( child.nodeType == CHILD_NODE )
        if( not isRoot and (i == 0 ) )      
          УстановитьКонтекстБлока( /*debug*/CorrectMesField(GetNodeName( node.nodeName )) );
          ЗаписатьПоле( beginField, GetNodeName( node.nodeName ) );
          if( isNeedProcAttr and (node.attributes != NULL) and (node.attributes.length() > 0) )
            attributes = node.attributes;
            for( var attr, attributes, 1 )
              ЗаписатьПоле( CorrectMesField( GetNodeName( attr.nodeName ) ), attr.nodeValue );
            end;
          end;
        end;
        if(GetNodeName(child.nodeName) == "SourceMessage");
          HandleSourceMessage( child );
        else
          ОбработатьДочернийЭлемент( child, false, isNeedProcAttr );
        end;
      elif( child.nodeType == TEXT_NODE )
        ЗаписатьПоле( CorrectMesField(GetNodeName( node.nodeName )), child.nodeValue );
      end;
      i=i+1;
    end;
    child = chNodeList.item( i - 1 );
    if( not isRoot and ( child.nodeType == CHILD_NODE ) ) 
      ЗаписатьПоле( endField, GetNodeName( node.nodeName ) );
      УстановитьКонтекстБлока("..");
    end;
  end;
end;

/* Поиск абонента по коду и виду кода*/
private macro FindAbonent( CodeKind:integer, Code:string ):integer

  var select = "select obj.t_ObjectID from dobjcode_dbt obj "
               "where  obj.t_CodeKind = :CodeKind "
               "  and  obj.t_Code     = :Code     ";  
  var params = makeArray( SQLParam( "CodeKind", CodeKind ),
                          SQLParam( "Code"    , Code     ) );  

  var rs = execSQLselect( select, params, TRUE );

  if( rs.MoveNext() )
    return rs.Value(0);
  end;
  return 0;
end;

private macro ConvertToUTF8( strconv:string ):string
  
  var select = "";
  var rs;
  var i = 1;
  var newxml = "";

  while ( i < StrLen(strconv) )
    var params:TArray = makeArray( SQLParam( "", SubStr( strconv, i, 1024 ) ) );
    select = "SELECT CONVERT (?, 'UTF8') FROM DUAL;";
    rs = execSQLselect( select, params, FALSE );
    if( rs.moveNext() )
      newxml = newxml + rs.Value(0);
    end;
    i = i + 1024;                                               
  end;

  return newxml;
end;


/* Импорт сообщений ГИС ГМП 1.16 */
macro InCommonProc( TpID, ImportFileName, addrSess, isFormat1_16 )
  var i:integer;
  var Code    = "", 
      MesKind = "", 
      trn     = "",
      НомерФормы = "", 
      РелизФормы = "", 
      refer   = "",
      OutsideAbonentDate = date(0,0,0),
      InsideAbonentCode = 0;
  var RslFormID = 0,
      TpShemID  = 0;
  var xml:object = ActiveX( "MSXML2.DOMDocument" ), 
      child:object, 
      node:object,
      chNodeList:object;
  var CryptoAPI = RsCryptoAPI();
  var rightSign = false;

  SetBuff( wlsess, addrSess );  
  
  if( not xml.load( ImportFileName ) )
    ErrImport( string( "Неверный формат файла импорта|", ImportFileName,  "|Невозможно загрузить xml-документ" ) );
    if( xml.parseError.errorCode != 0 )
      ErrImport( xml.parseError.reason );
    end;
    return false;
  end;

  node = xml.DocumentElement;

  i = 0;
  if( xml.NodeType == DOCUMENT_NODE )
    while( i < xml.childNodes.length )
      child = xml.childNodes.item( i );
      if( child and ( child.nodeType == CHILD_NODE ) )
        node = child;
        break;
      end;
      i = i+1;
    end;
  end;
  
  var isFormNameTICKETPACKAGE = false;
  if( StrUpr( node.BaseName ) == "ENVELOPE" )
    if( FindChildNode( node, "PackageProcessResult", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket" ) )
      isFormNameTICKETPACKAGE = true;
    end;
    if( FindChildNode( node, "Ticket", IfThenElse( isFormat1_16, "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage",
                                                                 "Body/UnifoTransferMsgResponse/MessageData/AppData/ImportDataResponse" ) ) or
        (isFormNameTICKETPACKAGE == true) )
      // код абонента 
      Code = ReadValue( node, "Code", IfThenElse( isFormat1_16, "Body/GISGMPTransferMsg/Message/Sender/Code",
                                                                "Body/UnifoTransferMsgResponse/Message/Sender/Code" ) );

      if( isFormat1_16 )
        // дата абонента
        OutsideAbonentDate = ToDateGG( ReadValue( node, "Date", "Body/GISGMPTransferMsg/Message/Date" ) );
        InsideAbonentCode = ReadValue( node, "Code", "Body/GISGMPTransferMsg/Message/Recipient/Code" );
      else
        OutsideAbonentDate = ToDateGG( ReadValue( node, "Date", "Body/UnifoTransferMsgResponse/Message/Date" ) );
        InsideAbonentCode = ReadValue( node, "Code", "Body/UnifoTransferMsgResponse/Message/Recipient/Code" );
      end;

      // транспортная схема
      НомерФормы = ОпределитьФорму( TpID, IfThenElse( not isFormNameTICKETPACKAGE, "TICKET", "TCKTPCKG" ), MesKind );
      var findCode = FindAbonent( PTCK_SMEV, Code );
      TpShemID = ОпределитьТранспортнуюСхему( findCode, -1, -1, TpID, НомерФормы, РелизФормы, NULL, NULL, wlsess.TpFrmtID );

      if( TpShemID == -1 )
        ErrImport( string( "Не определена транспортная схема для респондента: ", Code) );
        return false;
      end;

      // референс
      var select = "select mes.t_RefID from dwlmesrls_dbt mes "
                   "where  mes.t_RlsFormID = :RlsFormID ";  
      var params = makeArray( SQLParam( "RlsFormID", РелизФормы ) );  

      var rs = execSQLselect( select, params, TRUE );

      if( rs.MoveNext() )
        RslFormID = rs.Value(0);
        if ( not GenerateReference( RslFormID, refer ) )
         trn = refer;
        end;
      end;

     /* Если уже было такое сообщение - не закачиваем */
      var TpFieldID = 0;
 
      // t_TpFieldID = Fault \ MessageId
      rs = execSQLselect( "select t_TpFieldID from dwltpfld_dbt where t_TpID = 14 and t_Name = 'MessageId'", null, TRUE );

      if( rs.MoveNext() )
        TpFieldID = rs.Value(0);
      end;

      select = "select mes.t_TRN "
                 "from dwlmes_dbt mes, "
                      "dwlmesval_dbt val "
                 "where mes.t_RlsFormID = :RlsFormID "
                   "and mes.t_Department = :OperD "     
                   "and mes.t_MesID      = val.t_MesID " 
                   "and val.t_TpFieldID  = :TpFieldID " 
                   "and val.t_Value      = :MessageID ";

      params = makeArray( SQLParam( "RslFormID", RslFormID  ),
                          SQLParam( "OperD"    , {OperDprt} ),
                          SQLParam( "TpFieldID", TpFieldID  ),
                          SQLParam( "MessageID", ReadValue( node, "MessageId", "Envelop/Body/Fault/MessageId" ) ) );
      rs = execSQLselect( select, params, TRUE );

      if( rs.MoveNext() )
        ErrImport( string( "Сообщение с номером  |", rs.Value(0), "| уже было принято ранее - игнорируется " ) );
        return false;
      end;

      var checksign = false, RegErr = 0;
      GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\CheckSignSMEV", V_BOOL, checksign, RegErr);

      var xmlutf8 = ConvertToUTF8(xml.xml);
      if( CryptoAPI.IsCryptoActionNeeded( CONTEXTID_CHECK_SIGN_GG, 0 /*370*/ ) and checksign )
        if( not CryptoAPI.CheckBufferSign( CONTEXTID_CHECK_SIGN_GG, xmlutf8 ) )
          PrintLog( 2, "Ошибка проверки ЭП сообщения-ответа web-сервера СМЭВ " + trn );
        else
          rightSign = true;
        end;
      end;

    elif( isFormat1_16 and FindChildNode( node, "Fault", "Body" ) )
      // код абонента 
      Code = "СМЭВ";
      // дата абонента
      OutsideAbonentDate = date();

      var errcode = 0;
      InsideAbonentCode = ПолучитьКодСубъекта( {ourbank}, PTCK_SMEV, errcode );
      if( errcode )
        InsideAbonentCode = ПолучитьКодСубъекта( {ourbank}, PTCK_BIC, errcode );
      end;

      // транспортная схема
      НомерФормы = ОпределитьФорму( TpID, "FAULT", MesKind );
      TpShemID = ОпределитьТранспортнуюСхему( {ourbank}, -1, -1, TpID, НомерФормы, РелизФормы, NULL, NULL, wlsess.TpFrmtID );
      
      if( TpShemID == -1 )
        ErrImport( string( "Не определена транспортная схема для респондента: ", Code) );
        return false;
      end;

      // референс
      select = "select mes.t_RefID from dwlmesrls_dbt mes "
               "where  mes.t_RlsFormID = :RlsFormID ";  
      params = makeArray( SQLParam( "RlsFormID", РелизФормы ) );  

      rs = execSQLselect( select, params, TRUE );

      if( rs.MoveNext() )
        RslFormID = rs.Value(0);
        if ( not GenerateReference( RslFormID, refer ) )
         trn = refer;
        end;
      end;

     /* Если уже было такое сообщение - не закачиваем */
      TpFieldID = 0;

      rs = execSQLselect( "select t_TpFieldID from dwltpfld_dbt where t_TpID = 14 and t_Name = 'MessageId'", null, TRUE );

      if( rs.MoveNext() )
        TpFieldID = rs.Value(0);
      end;

      select = "select mes.t_TRN "
                 "from dwlmes_dbt mes, "
                      "dwlmesval_dbt val "
                 "where mes.t_RlsFormID = :RlsFormID "
                   "and mes.t_Department = :OperD "     
                   "and mes.t_MesID      = val.t_MesID " 
                   "and val.t_TpFieldID  = :TpFieldID " 
                   "and val.t_Value      = :MessageID ";

      params = makeArray( SQLParam( "RslFormID", RslFormID  ),
                          SQLParam( "OperD"    , {OperDprt} ),
                          SQLParam( "TpFieldID", TpFieldID  ),
                          SQLParam( "MessageID", ReadValue( node, "MessageId", "Header/Header/MessageId" ) ) );
      rs = execSQLselect( select, params, TRUE );

      if( rs.MoveNext() )
        ErrImport( string( "Сообщение с номером  |", rs.Value(0), "| уже было принято ранее - игнорируется " ) );
        return false;
      end;

      xmlutf8 = ConvertToUTF8(xml.xml);
      if( CryptoAPI.IsCryptoActionNeeded( CONTEXTID_CHECK_SIGN_GG, 0 /*370*/ ) )
        if( not CryptoAPI.CheckBufferSign( CONTEXTID_CHECK_SIGN_GG, xmlutf8 ) )
          PrintLog( 2, "Ошибка проверки ЭП сообщения-ответа web-сервера СМЭВ " + trn );
        else
          rightSign = true;
        end;
      end;
      node = GetNode(node, "Body");
    else
      ErrImport( string( "Не удалось определить форму сообщения" ) );
      return false;
    end;
  end;

  // Заполняем ответное сообщение
  ClearRecord( wlmes );
  wlmes.TpSchemID           = TpShemID;
  wlmes.RlsFormID           = РелизФормы;
  wlmes.Kind                = MESKIND_GISGMP;
  wlmes.TRN                 = trn;
  wlmes.OutsideAbonentDate  = OutsideAbonentDate;
  wlmes.Date                = wlmes.OutsideAbonentDate;
  wlmes.OutsideAbonentCodeKind = wlmes.AgentCodeKind = PTCK_SMEV;
  wlmes.OutsideAbonentCode  = wlmes.AgentCode = Code;
  wlmes.OutsideAbonentID    = wlmes.AgentID = FindAbonent( wlmes.OutsideAbonentCodeKind, wlmes.OutsideAbonentCode );
  wlmes.InsideAbonentCodeKind = PTCK_SMEV;
  wlmes.InsideAbonentCode   = InsideAbonentCode;
  wlmes.InsideAbonentID     = FindAbonent( wlmes.InsideAbonentCodeKind, wlmes.InsideAbonentCode );
  if( rightSign )
    Wlmes.Signed = SET_CHAR;
  end;

  if( not СоздатьЗапись( wlmes ) )
    RunError( "Невозможно создать запись по форме: " + node.NodeName() );
  end;
  ОбработатьДочернийЭлемент( node, true, isFormat1_16 );
  AddRepElem(wlmes.Kind, IfThenElse( isFormNameTICKETPACKAGE, "TCKTPCKG" , "TICKET" ), "RUR", money(0) );
  PrintImportReport();
 
  return true;     
end;

/* Импорт сообщений ГИС ГМП */
macro InProc( TpID, ImportFileName, addrSess )
  return InCommonProc( TpID, ImportFileName, addrSess );
end;

/* Импорт сообщений ГИС ГМП 1.16 */
macro InProc_1_16( TpID, ImportFileName, addrSess )
  return InCommonProc( TpID, ImportFileName, addrSess, true /*use format 1.16*/ );
end;
