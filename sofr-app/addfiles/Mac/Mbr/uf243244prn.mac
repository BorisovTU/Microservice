/*
 $Name: uf243244prn.mac
 $Module: MBR
 $Description: Функции для печати сообщений ED243, ED244
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для печати сообщений ED243, ED244
//
// Программист  : Чукина Т.А.
//
// Создан       : 18.03.2014
//
//-----------------------------------------------------------------------------

import MesInter, "wltools.mac", "uf243244.mac";

macro PrintReqAnsCode(fieldname : string, fieldvalue : string)
  var Code : string = "", CodeType : integer = 0, CodeName : string = "";

  if  (fieldname == "EDDefineRequestCode")
    Code = "/УТ"+ fieldvalue + "/";
    CodeType = TYPE_CODE_UFBS_ED243;
    CodeName = "Код запроса:";
  elif(fieldname == "EDDefineAnswerCode")
    Code = "/ОТ"+ fieldvalue + "/";
    CodeType = TYPE_CODE_UFBS_ED244;
    CodeName = "Код ответа:";
  else
    return;
  end;

  var CodeDescr : string = GetWlcodeDescription(CodeType, Code);

        [
          ############ ## ####################################################################]
        ( CodeName, fieldvalue, CodeDescr:w );
end;

private macro CheckPrintRequisiteHeader(fieldname : string, fieldvalue : string, blockname : string, 
                                checkblock : string, header : string, IsPrinted : @bool)
  if ( not IsPrinted and 
       index(blockname, checkblock) and
       (fieldname != beginField) and (fieldname != "") and (fieldvalue != "") 
     )
      [ 
        # ](header);
    IsPrinted = true;      
  end;
end;

private macro isFCodeEQUFName( code:string, name:string ) : bool
  var names = makeArray( makeArray( "AccDocDate",    "Дата документа"           ),
                         makeArray( "AccDocNo",      "Номер документа"          ),
                         makeArray( "PayerAcc",      "Счет плательщика"         ),
                         makeArray( "PayeeAcc",      "Счет получателя"          ),
                         makeArray( "PayerINN",      "ИНН плательщика"          ),
                         makeArray( "PayeeINN",      "ИНН получателя"           ),
                         makeArray( "Sum",           "Сумма"                    ),
                         makeArray( "EnterDate",     "Дата представления документов в банк получателя" ),
                         makeArray( "PayeeCorrAcc",  "Счет банка получателя"    ),
                         makeArray( "PayeeBIC",      "БИК банка получателя"     ),
                         makeArray( "PayerLongName", "Наименование плательщика" ),
                         makeArray( "PayeeLongName", "Наименование получателя"  ),
                         makeArray( "Purpose",       "Назначение платежа"       ),
                         makeArray( "Address"        "Адрес плательщика"        ),
                         makeArray( "PayerName",     "Наименование плательщика" ),
                         makeArray( "PayeeName",     "Наименование получателя"  )
                       );
  for( var n, names )
    if( (code == n[0]) AND (name == n[1]) )
      return true;
    end;
  end;
  return false;
end;

private macro GetPrintFieldName(Type : string, FieldNoOrName : string, AddFlds : RsbAddFld) : string
  class (TRsbAddFldIterator) TNamePrn( p_Type : string, 
                                       p_FieldNoOrName : string, 
                                       p_AddFlds : RsbAddFld )

    InitTRsbAddFldIterator(p_AddFlds);
    var Type : string = p_Type,
        FieldNoOrName : string = p_FieldNoOrName,
        PrintName : string = FieldNoOrName;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( (FldRec.rec.Type == Type) and
          ((FldRec.rec.Number == FieldNoOrName) OR ( not FldRec.rec.Number AND isFCodeEQUFName(FieldNoOrName,FldRec.rec.Name) ))
        )
        if(FldRec.rec.Name)
          if(Type == "П")
            PrintName = FldRec.rec.Name;
          else
            PrintName = FieldNoOrName + " (" + FldRec.rec.Name + ")";
          end;
        end;

        Finish();
      end;
    end;

  end;

  var NamePrn : TNamePrn = TNamePrn(Type, FieldNoOrName, AddFlds);
  NamePrn.Go();

  return NamePrn.PrintName;
end;

private macro PrintFieldListItem
( blockname : string,
  fieldname : string,
  fieldvalue : string,
  CurrFieldNo : @string, // IN OUT
  AddFlds : RsbAddFld
)

  macro GetPrintFName(Type : string, FieldNo : string, AddFlds : RsbAddFld) : string
    var PrintFName : string = GetPrintFieldName(Type, FieldNo, AddFlds) + ":";

    // Делаем отступ для записей вложенных платежей
    if(Type == "Р")
      PrintFName = "  " + PrintFName;
    end;

    return PrintFName;
  end;

  var PrintFName : string = "",
      AddFldType : string = "";

  if( index(blockname, "EDFieldList") )
    AddFldType = "У";
  elif( index(blockname, "EDReestrInfo") )
    AddFldType = "Р";
  else
    return;
  end;

      if( fieldname == "FieldNo" )
        if(CurrFieldNo)
          PrintFName = GetPrintFName(AddFldType, CurrFieldNo, AddFlds);
        [    ############################################  ](PrintFName:w);
        end;

        CurrFieldNo = fieldvalue;
      elif( (fieldname == "FieldValue") and fieldvalue )
        PrintFName = GetPrintFName(AddFldType, CurrFieldNo, AddFlds);
        [    ############################################ # ](PrintFName:w, fieldvalue);

        CurrFieldNo = "";
      elif( (fieldname == endField) and not index(blockname, "FieldNo") and CurrFieldNo )
        PrintFName = GetPrintFName(AddFldType, CurrFieldNo, AddFlds);
        [    ############################################  ](PrintFName:w);

        CurrFieldNo = "";
      end;

end;

// Реквизиты ЭПС, поясняющие запрос/ответ
private macro PrintEDDefineReqAnsInfo
( FormName : string, 
  blockname : string,
  fieldname : string, 
  fieldvalue : string,
  AddFlds : RsbAddFld,
  CurrFieldNo : @string, // IN OUT
  IsRequisiteHeaderPrinted : @bool, // IN OUT
  IsFieldListHeaderPrinted : @bool // IN OUT
)

  var InfoBlockName : string, TextFieldName : string, IsTextNodeFun : ProcRef,
      RequisiteHeader : string, TextHeader : string;

  GetFldDiff243244(FormName, @InfoBlockName, @TextFieldName, @IsTextNodeFun);

  if(FormName == "ED243")
    RequisiteHeader = "Реквизиты ЭПС, поясняющие запрос:";
    TextHeader = "Текст запроса:";
  elif(FormName == "ED244")
    RequisiteHeader = "Реквизиты ЭПС, поясняющие ответ:";
    TextHeader = "Текст ответа:";
  else
    RunError("Макропроцедура PrintEDDefineReqAnsInfo поддерживает только формы ED243, ED244");
  end;

  var IsTextNode : bool,
      PrintFName : string = "";

    CheckPrintRequisiteHeader(fieldname, fieldvalue, blockname, InfoBlockName, RequisiteHeader, @IsRequisiteHeaderPrinted);
    if ( ( (blockname == InfoBlockName) or 
           (IsTextNode = ExecMacro2(IsTextNodeFun, fieldname))
         )
         and
         (substr(fieldname, 1, 1) != "_")
         and
         (fieldvalue != "")
       )
      PrintFName = GetPrintFieldName("П", fieldname, AddFlds) + ":";

        [    ############################################ ################################################################## ]
        (PrintFName:w, fieldvalue:w);

    elif ( (fieldname == TextFieldName) AND (fieldvalue != "") )
        [
             # ](TextHeader);
        [         ################################################################# ](fieldvalue:w);
    elif ( index(blockname, "EDFieldList") )
      CheckPrintRequisiteHeader(fieldname, fieldvalue, blockname, "EDFieldList", "Уточняемые реквизиты ЭПС:", @IsFieldListHeaderPrinted);

      PrintFieldListItem(blockname, fieldname, fieldvalue, @CurrFieldNo, AddFlds);
    elif ( index(blockname, "EDReestrInfo") )
      CheckPrintRequisiteHeader(fieldname, fieldvalue, blockname, "EDReestrInfo", "Уточняемые реквизиты записей реестра:", @IsFieldListHeaderPrinted);

      if(fieldname == "TransactionID")
        [    Номер записи реестра:                        # ]( fieldvalue );
      end;

      PrintFieldListItem(blockname, fieldname, fieldvalue, @CurrFieldNo, AddFlds);
    end;
end;

private macro PrintEDNoDateAuthor(Header : string, ed_nda : EDNoDateAuthor)
  Array atmpstr;

  if(ed_nda.EDNo)
  [
    # ](Header);
  [    Номер ЭС:     ######### ]( ed_nda.EDNo ); 
  [    Дата ЭС:      ########## ]( DDpMMpYYYY(ed_nda.EDDate) );
    if (ed_nda.EDAuthor)
      StrSplit( GetPartyNameByUIS(ed_nda.EDAuthor), atmpstr, 46, 46, 2 );
  [    Составитель:  ########## ##############################################
                                ############################################## ]( ed_nda.EDAuthor, atmpstr(0), atmpstr(1) );
    end;
  end;

end;

private class TMesInfo
  var OriginalEPD : EDNoDateAuthor = EDNoDateAuthor(),
      InitialED : EDNoDateAuthor = EDNoDateAuthor();
end;

macro PrintFields243244(FormName : string, wlmes)

  var fieldname = "", fieldvalue = "", blockname = "", MesInfo = TMesInfo();

  var IsRequisiteHeaderPrinted : bool = false, IsFieldListHeaderPrinted : bool = false;
  var CurrFieldNo : string = "",
      ReqID : integer = GetObjIDByMesLink(wlmes.MesID, OBJTYPE_REQ, wlmes.Direct),
      AddFlds : RsbAddFld = RsbAddFld(ReqID);

  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
  
    // Код запроса. Код ответа
    PrintReqAnsCode(fieldname, fieldvalue);
  
    // Идентификаторы исходного ЭПС
    if ( blockname == "OriginalEPD" )
      ReadEDNoDateAuthorField(fieldname, fieldvalue, MesInfo.OriginalEPD);
    end;

    // Идентификаторы исходного ЭСИС
    if ( blockname == "InitialED" )
      ReadEDNoDateAuthorField(fieldname, fieldvalue, MesInfo.InitialED);
    end;

    // Реквизиты ЭПС, поясняющие запрос/ответ
    PrintEDDefineReqAnsInfo( FormName, blockname, fieldname, fieldvalue, AddFlds, 
                             @CurrFieldNo, @IsRequisiteHeaderPrinted, @IsFieldListHeaderPrinted );
  end;

  // Идентификаторы исходного ЭПС
  PrintEDNoDateAuthor("Идентификаторы исходного ЭПС", MesInfo.OriginalEPD);

  // Идентификаторы исходного ЭСИС
  PrintEDNoDateAuthor("Идентификаторы исходного ЭСИС", MesInfo.InitialED);

end;
