 /*
 $Name:        mnsgdpob.mac
 $Module:      МБР
 $Description: Генерация Извещения об ошибках в сообщении банка об открытии или о закрытии счета, об изменении реквизитов счета по сообщению POB
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация Извещения об ошибках в сообщении банка об открытии             */
/* или о закрытии счета, об изменении реквизитов счета по сообщению POB     */
/*                                                                          */
/*  Имя файла: mnsgdpob.mac                                                 */
/*  Создан:    22.12.04                                  Фомченкова Л.Н.    */
/****************************************************************************/

import "wlmnstls.mac", WldInter;

file mesOC(wlmes) key 0;

var ErrList = TArray;
var OutNumMes : string = "";

private macro InitGlobals()
  OutNumMes = "";
  ErrList.Size = 0;
end;

macro FillNumMes(val)
  OutNumMes = val;
end;

macro FillErrCode(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).ErrCode != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.ErrCode = val;
end;

macro FillErrName(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).ErrName != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.ErrName = val;
end;

macro FillAttrCode(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).AttrCode != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.AttrCode = val;
end;

macro FillAttrValue(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).AttrValue != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.AttrValue = val;
end;

var Fld1  = ТПолеМНС( "НомСооб",     "FillNumMes" ),
    Fld2  = ТПолеМНС( "КодОшибки",   "FillErrCode" ),    
    Fld3  = ТПолеМНС( "НаимОшибки",  "FillErrName" ),
    Fld4  = ТПолеМНС( "КодРекв",     "FillAttrCode" ),
    Fld5  = ТПолеМНС( "ЗначРекв",    "FillAttrValue" );    

private macro Construct()
  var field_name, field_value, fldd, i;
  /* Последовательно считываем поля и заполняем лексемы */
  while(СчитатьПоле(field_name, field_value))  
      i = IndexMNS(field_name);
      if (i != -1)
         fldd = ПоляМНС.Value(i);    
         fldd.ОбработатьПолеФормы( field_value ); 
      end;
  end;
  return TRUE;
end; /* Construct */

/* Релиз POB */
macro GenDoc( addrMes )
  var ErrDescription="Не найдено исходное сообщение";
  var ErrCode = 0, Code = "", State = WLD_STATUS_RESPRM_DEFECT, isFoundMes = false, NumMes = "";
  var OutMesID = 0;// ID исходящего сообщения, если его не нашли - 0
  SetBuff( wlmes, addrMes );
  InitGlobals();
  
  // Чтение полей сообщения wlmes
  Construct();

  var InsideAbonentID : integer = 0, 
      InsideAbonentCodeKind : integer = 0, 
      InsideAbonentCode : string = "",
      mesFileName : string = "",
      OutMesTrn : string = "";

  // Поиск исходящего сообщения
  OutMesID = GetKvitSBC( wlmes.MesID, OutNumMes, @InsideAbonentID, 
                         @InsideAbonentCodeKind, @InsideAbonentCode, 
                         @mesFileName, @OutMesTrn );

  isFoundMes = (OutMesID != 0);

  if( isFoundMes or
      GetInsideAbonentForSBF( wlmes.Department, @InsideAbonentID, 
                             @InsideAbonentCodeKind, @InsideAbonentCode )
    )
    wlmes.InsideAbonentID = InsideAbonentID;
    wlmes.InsideAbonentCodeKind = InsideAbonentCodeKind;
    wlmes.InsideAbonentCode = InsideAbonentCode;
    UpdateMes(wlmes);
  end;

  if( isFoundMes )

    var ThirdSymbol : string = substr(mesFileName, 3, 1);
    if(ThirdSymbol == "K")
      ErrCode = FNS_RP_APPROVED_WITH_ERROR;
    elif(ThirdSymbol == "N")
      ErrCode = FNS_RP_RECEIVED_MES_ERROR_PFR;
    elif(ThirdSymbol == "T")
      ErrCode = FNS_RP_RECEIVED_MES_ERROR_FSS;
    end;

    if( ErrCode )
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);
    end;
    State = WLD_STATUS_RESPRM_RECEIVE;
  end;

  var ResultID = 0;
  if( not ОшибкаЛогическогоКонтроляСообщения( OutMesID, ErrCode, ErrDescription, State, true, ResultID ) )
    std.msg( string("|Ошибка при переносе в отбракованные сообщения ", OutMesTrn) ); 
    return false;
  else
    for( var ErrInfo, ErrList )
      fillWlerror(ResultID, ErrInfo, wlerror);
      if ( not ВставитьОшибкуОбработкиСообщения(wlerror) )
        std.msg( string("|Ошибка при создании записи об ошибке обработки сообщения ") ); 
        return false;
      end;
    end;          
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;