/*
  $Name:         ufNtfCar_GenProc.mac
  $Module:       МБР
  $Description:  Процедура генерации уведомлений о зачислении/невозможности зачисления средств
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Процедура генерации уведомлений о зачислении/невозможности зачисления средств
//
// Программист  : Чукина Т.А.
//
// Создан       : 12.04.2018
//
//-----------------------------------------------------------------------------

import "ufNtfCar_lib.mac", "ufNtfCar_Create.mac", "ufNtfCar_Rep.mac";

// Проверка кода ответа
private macro GetAnswerCodeWithCheck(Code : string) : string
  var OutCode : string = "";

  index_wlcode("/ОТ" + Code + "/", TYPE_CODE_UFBS_ED244, @OutCode);

  return OutCode;
end;

private macro GetAnswerCode(NeedExecNotify : integer) : string
  var RegVal : string = "";

  if(NeedExecNotify == WLPM_EXECNOTIFY_SUCCESS)
    RegVal = UfNtfCar_GetAnswerCodeSuccess();
  elif(NeedExecNotify == WLPM_EXECNOTIFY_FAIL)
    RegVal = UfNtfCar_GetAnswerCodeFail();
  else
    RunError(ERR_WRONG_NEEDEXECNOTIFY, ERR_WRONG_NEEDEXECNOTIFY);
  end;

  return GetAnswerCodeWithCheck(RegVal);
end;

private macro GenNotifyAndUpdatePayment
( PaymentID : integer,
  AnswerCode : string,
  ReqID : @integer,
  ErrMsg : @string
)
  macro TrnAction
    var Payment : RsbPayment = RsbPayment(PaymentID);

    if( UfCreateNotifyPmCarry(PaymentID, AnswerCode, @ReqID, @ErrMsg) == 0 )

      Payment.NeedExecNotify = 0;
      var stat : integer = Payment.Update();

      if(stat)
        ErrMsg = "Ошибка при сохранении изменений в платеже\n" + GetErrMsgEx(stat);
        AbortTrn();
      end;
    else
      AbortTrn();
    end;
  end;

  var TrnResult : bool = ProcessTrn( null, "TrnAction" );

  if(not TrnResult)
    if(not ErrMsg)
      ErrMsg = "Ошибка при внесении изменений в БД";
    end;
  end;
end;

private macro GenNotifications
( PaymentIds : TArray, 
  NeedExecNotify : integer, 
  Report : TUfNtfCarryReport
)

  Report.PrintTableTitle(NeedExecNotify);

  var AnswerCode : string = GetAnswerCode(NeedExecNotify);
  var ErrWrongReg : string = UfNtfCar_GetErrWrongReg(NeedExecNotify);
  var IsHeaderPrinted : bool = false;

  for(var PaymentID : integer, PaymentIds)
    var ReqID : integer = 0, ErrMsg : string = "";

    // Генерим уведомление и сбрасываем wlpm.NeedExecNotify в платеже
    GenNotifyAndUpdatePayment(PaymentID, AnswerCode, @ReqID, @ErrMsg);

    // Выводим информацию в отчет
    var IsErrWrongReg : bool = (not AnswerCode or index(ErrMsg, ErrWrongReg));

    if(not IsHeaderPrinted and not IsErrWrongReg)
      Report.PrintTableHeader();
      IsHeaderPrinted = true;
    end;

    if(IsErrWrongReg)
      Report.PrintErrWrongReg(NeedExecNotify);
      break; // пропустить все последующие платежи с этим NeedExecNotify
    end;

    Report.PrintTableRow(PaymentID, ReqID, ErrMsg);
  end;

  if(IsHeaderPrinted)
    Report.PrintTableBottom();
  end;
end;

macro GenPmCarryNotifications(ExecPayments : TArray, NonExecPayments : TArray, DepList : string)
  var Report : TUfNtfCarryReport = TUfNtfCarryReport();
  Report.PrintRepHeader(ExecPayments, NonExecPayments, DepList);

  if(ExecPayments.size)
    GenNotifications(ExecPayments, WLPM_EXECNOTIFY_SUCCESS, Report);
  end;

  if(NonExecPayments.size)
    GenNotifications(NonExecPayments, WLPM_EXECNOTIFY_FAIL, Report);
  end;
end;

private macro GetDepCondition(DepList : string) : string
  var cond : string = "";

  if(DepList)
    cond = " and dep.t_Name in (";

    var dpdepNames : TArray = split(DepList, ",");
    var HasPrevElem : bool = false;
    for(var depName : string, dpdepNames)
      if(HasPrevElem)
        cond = cond + ", ";
      end;

      cond = cond + "'" + trim(depName) + "'";

      HasPrevElem = true;
    end;

    cond = cond + ") ";
  end;

  return cond;
end;

private macro GetCorschemCondition(CorschemList : string) : string
  var cond : string = "";

  if(CorschemList)
    cond = " and wlpm.t_Corschem in (" + CorschemList + ") ";
  end;

  return cond;
end;

macro GenPmCarryNotifications_FromSheduler
( DepList : string,
  FIID : integer,
  CorschemList : string
)
  if( (FIID == null) or (FIID == ALLFININSTR) )
    FIID = NATCUR;
  end;

  var ExecPayments : TArray = TArray(),
      NonExecPayments : TArray = TArray();

  var query : string =
    "select pm.t_PaymentID, wlpm.t_NeedExecNotify "
    "  from dwlpm_dbt wlpm, dpmpaym_dbt pm, ddp_dep_dbt dep "
    " where wlpm.t_NeedExecNotify in (" + WLPM_EXECNOTIFY_SUCCESS + ", " +
                                          WLPM_EXECNOTIFY_FAIL + ") " +
    "   and pm.t_PaymentID = wlpm.t_PaymentID "
    "   and pm.t_StartDepartment = dep.t_Code " + 
    GetDepCondition(DepList) +
    "   and pm.t_BaseFIID = :FIID " +
    GetCorschemCondition(CorschemList);

  var params : TArray = makeArray( SQLParam("FIID", FIID) );

  var rs = execSQLselect(query, params);

  while(rs and rs.moveNext())
    if(rs.value("t_NeedExecNotify") == WLPM_EXECNOTIFY_SUCCESS)
      ExecPayments[ExecPayments.size] = rs.value("t_PaymentID");

    elif(rs.value("t_NeedExecNotify") == WLPM_EXECNOTIFY_FAIL)
      NonExecPayments[NonExecPayments.size] = rs.value("t_PaymentID");
    end;
  end;

  GenPmCarryNotifications(ExecPayments, NonExecPayments, DepList);
end;
