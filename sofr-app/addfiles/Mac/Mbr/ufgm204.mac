/*
$Name:           ufgm204.mac
$Module:         Межбанковские расчеты
$Description:    Генерация сообщения ED204 транспорта УФЭБС
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED204 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm204.mac                                                  */
/*  Создан:    26.09.05                                  Фомченкова Л.Н.    */
/****************************************************************************/
import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac";

var ver_st = 1;

private macro GetCodeField( wlreq )
  var Code = "";
  var pos = Index(wlreq.queries, "Code");
  if( pos > 0 )
    Code = SubStr( wlreq.queries, pos + 5, 1 );
  end;
  return Code;
end;

/* Заполнение полей сообщения - запроса об отзыве ЭПД (пакета ЭПД) */
macro GenMes( addrMes, addrReq )

  var InitMesId:integer = 0, IsRequestPacket:bool = false;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem: object;
  var ixml:object = ActiveX("Microsoft.XMLDOM");  
  var strXml;
  var EDReceiver:string;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlReq, addrReq );

  mes = xml.createElement("ED204");

  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  EDReceiver = GetEDReceiver(wlreq.RecipientID, wlreq.RecipientcodeKind, wlreq.RecipientCode, "", 1);
  if (EDReceiver != "")
      mes.setAttribute("EDReceiver", EDReceiver);
  end;

  if( wlreq.RelatedRef == "" )
    RunError( "Идентификаторы отзываемого ЭПС/пакета ЭПС должны быть заполнены. Задайте в запросе ссылку на отзываемое ЭС" ); 
  end;

  if( SubStr( wlReq.RelatedRef, 7, 10 ) != SubStr( wlmes.Trn, 7, 10 ) ) 
    msgboxEx( "Составитель запроса не совпадает с составителем отзываемого ЭПС. В исполнении запроса может быть отказано", 0 );
  end;

  // Если задана форма сообщения - ищем конкретный ЭПД, иначе ищем пакет ЭПД
  if( wlreq.InitFormIDMes == 0 )
    IsRequestPacket = true;
  end;

  if( IsRequestPacket )
    // Ищем пакет ЭПД по журналу сеансов
    if( not WldFindSessByUID( TRANSP_UFBS, wlreq.InitDateMes, wlreq.RelatedRef ) )
      RunError( String("Не найден исходящий пакет ЭПД|с номером '", wlreq.RelatedRef, "' за дату ", wlreq.InitDateMes) );
    end;
  end;
  
  // Запрос об отзыве ЭПД. (0 - отзыв отложенного ЭПД, 1 - отзыв пакета ЭПД)
  var Code = GetCodeField( wlreq );
  mes.setAttribute( "Code", Code );
  
  elem = xml.createElement("EDRefID");

  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlreq.RelatedRef); 

  elem.setAttribute("EDNo",       ed_nda.EDNo );
  elem.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; // Успешное завершение 

  OnError( er ) // обработка ошибок 
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV2( addrMes, addrReq )
  ver_st = 2;
  return GenMes( addrMes, addrReq );
end;
