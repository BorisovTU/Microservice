/*
  $Name:         ufgm573.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED573 транспорта УФЭБС
*/

import "ufgenmes.mac", BankInter, CTInter, "oralib.mac", MesInter, WldInter;

private const WlReqQueriesPanelFldName = "Messages";
private const WlReqInitFormMesPanelFldName = "InitForm";

private const retPanelFieldNameParmNum = 2;

private macro FindQueries(wlreq)
  if(wlreq.Queries == "")
    return false;
  end;
  
  var indx = index(wlreq.Queries, "СП");
  
  if(indx == 0)
    return false;
  end;
  
  if(not StrIsNumber(SubStr(wlreq.Queries, indx + 2, 2)))
    return false;
  end;
  
  return true;
end;

macro CheckObj( addrReq, refilledAddrReq, retPanelFieldName )
  
  record wlReq (wlreq); 
  SetBuff( wlReq, addrReq );
    
  if(FindQueries(wlreq))
    var code_str = "";
    var code_index : integer = index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED573, code_str);
    if(code_index == 0)
      setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
      RunError("Для формы ED573 в поле <Сообщение> можно указать только коды /СП10/, /СП20/ или /СП30/. Задайте один из допустимых кодов");
    end;
  else
    setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
    RunError("Для формы ED573 в поле <Сообщение> необходимо ввести один из фиксированных кодов из справочника");
  end;
  
  return true;
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;  

macro GenMes( addrMes, addrReq )
  
  record wlmes(wlmes);
  record wlReq(wlreq);
    
  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var indx = 0;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object; 
  
  mes = xml.createElement("ED573");
  
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );

  if(indx = index(wlreq.Queries,"СП"))
    mes.setAttribute("DictionRequest",   SubStr(wlreq.Queries, indx + 2, 1));
  end;
  
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );

  return true; /*Успешное завершение*/
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;    