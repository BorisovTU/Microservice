/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация платежей по документу выписки                                 */
/*                                                                          */
/*  Имя файла: wlcrpmhd.mac                                                 */
/*  Создан:    29.05.00                                       Бабин А.П.    */
/****************************************************************************/

import "swgendoc.mac", rsd, oralib, likepy, "pmlib.mac";

macro GenPaym( addrConf, addrPmpaym, addrDebet, addrCredit, addrRm )
  var error=0, CreditFlag=0, RmPropFlag=0, Currency, OutsideAbonentID;
  var rs:object;
  var select:string;
  var params:TArray;

  /* Исходное подтверждение, по которому формируется платеж */
  SetBuff( wlconf, addrConf );
  
  SetBuff( wlpmpaym, addrPmpaym );
  if( ValType(addrDebet)!=V_UNDEF )
     SetBuff( wlpmprop, addrDebet );
     CreditFlag = 1;
  end;

  if( ValType(addrCredit)!=V_UNDEF )
    SetBuff( wlpmprop, addrCredit );
    CreditFlag = 0;
  end;

  if( ValType(addrRm)!=V_UNDEF )
    SetBuff( wlpmrmprop, addrRm );
    RmPropFlag = 1;
  end;

  /* Поиск ответного сообщения по выписке */
  select = "select mes.t_OutsideAbonentID from dwlmeslnk_dbt lnk, dwlmes_dbt mes where "+
                             "lnk.t_ObjID = :wlconf_HeadID and "+ 
                             "lnk.t_ObjKind = :OBJTYPE_HEAD and "+ 
                             "mes.t_MesID = lnk.t_MesID";

  params = makeArray( SQLParam("wlconf_HeadID", wlconf.HeadID),
                      SQLParam("OBJTYPE_HEAD", OBJTYPE_HEAD));
  rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    OutsideAbonentID = rs.value(0);
  else
    std.msg("Не найдено сообщение");
    return false;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Amount    = wlconf.Sum;
  wlpmpaym.PayAmount = wlconf.Sum;
  wlpmpaym.ValueDate = wlconf.DateValue;
  wlpmpaym.FIID      = wlconf.FIID;
  wlpmpaym.PayFIID   = wlconf.FIID;  

  /* Дебетовые свойства платежа */
  wlpmprop.PayFIID  = wlpmpaym.PayFIID;
  if ( (wlconf.BankID==0) AND (wlconf.BankName=="") AND
       (wlconf.CorrBankID==0) AND (wlconf.CorrBankName=="") )
     wlpmprop.CodeKind = PTCK_CONTR;
     wlpmprop.BankCode = ПолучитьКодСубъекта( OutsideAbonentID, PTCK_CONTR, error );
     if ( CreditFlag )
        wlpmpaym.PayerBankID = OutsideAbonentID;
        wlpmpaym.ReceiverBankID = {OurBank};
     else
        wlpmpaym.PayerBankID = {OurBank};
        wlpmpaym.ReceiverBankID = OutsideAbonentID;
     end;
  elif ( (wlconf.CorrBankID==0) AND (wlconf.CorrBankName=="") )
     wlpmprop.CorrCodeKind = PTCK_CONTR;
     wlpmprop.CorrCode = ПолучитьКодСубъекта( OutsideAbonentID, PTCK_CONTR, error );     
  else
     error = 0;
  end;
  if ( error )
     return false;
  end;

  if ( (wlconf.BankID!=0) OR (wlconf.BankName!="") )
    wlpmprop.CodeKind = wlconf.CodeKind;
    wlpmprop.BankCode = wlconf.CodeValue;
    if ( CreditFlag )
       wlpmpaym.PayerBankID = wlconf.BankID;
       wlpmpaym.ReceiverBankID = {OurBank};
       wlpmrmprop.PayerBankName = wlconf.BankName;
    else
       wlpmpaym.PayerBankID = {OurBank};
       wlpmpaym.ReceiverBankID = wlconf.BankID;
       wlpmrmprop.ReceiverBankName = wlconf.BankName;
    end;
  end;

  if ( (wlconf.CorrBankID!=0) OR (wlconf.CorrBankName!="") )
    wlpmprop.CorrCodeKind = wlconf.CorrCodeKind;
    wlpmprop.CorrCode = wlconf.CorrCodeValue;

    if ( CreditFlag )
       wlpmrmprop.PayerCorrBankName = wlconf.CorrBankName;
    else
       wlpmrmprop.ReceiverCorrBankName = wlconf.CorrBankName;
    end;
  end;

  /* Реквизиты платежа, характерные для SWIFT */
  wlpmrmprop.Number               = wlconf.Number;
  wlpmrmprop.Date                 = wlconf.DateValue;
  wlpmrmprop.ComissCharges        = PM_CHRG_OUR;
  wlpmrmprop.ProcessKind          = " 1";
  wlpmrmprop.ShifrOper            = "01";
  wlpmrmprop.Priority             = PM_DefaultMaxPriority();

  return TRUE;
end;
  