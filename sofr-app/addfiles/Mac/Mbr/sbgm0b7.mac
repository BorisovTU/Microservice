/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SBRF3 0B7, 0BF                                    */
/*                                                                          */
/*  Имя файла: sbgm0B7.mac                                                  */
/*  Создан:  07.08.00                                        Бабин А.П.     */
/****************************************************************************/

import "sbgenmes.mac", "swgenmes.mac";

/* Генерация информационного сообщения */
macro GenMesExec( MacroProc, Parm )
  var field_value, error, CodeKind, PartyID, ВидКодаСтр, BankName, InMesID;

  /* Тип документа */
  f_wlmesrls.RlsFormID = wlmes.RlsFormID;
  if(not GetEQ(f_wlmesrls))
    std.msg("Ошибка поиска релиза формы: "+string(wlmes.RlsFormID));
    return false;
  end;
  
  f_wlmesfrm.FormID = f_wlmesrls.FormID;
  if( not GetEQ(f_wlmesfrm) )
    std.msg("Ошибка поиска формы: "+string(f_wlmesrls.FormID));
    return false;
  end;
  /*здесь проверим может сообщение транзитное */
  if( ЯвляетсяЛиТелеграммаТранзитной(wlinfo.InfoID, f_wlmesfrm.FormID, InMesID)) 
     return SB_ЗаписатьТранзитноеСообщение( InMesID);
  end;

  if( f_wlmesfrm.Name == sign_0B7 )    /* Сообщение участнику */
    field_value = type_007;
  elif( f_wlmesfrm.Name == sign_0BF )  /* Сообщение свободного формата */
    field_value = type_070;    
  end;
  ExecMacro( MacroProc, "DT", field_value, Parm );
  
  CodeKind = PTCK_CLIRING;

  /* Участник-отправитель, он же банк-создатель телеграммы */
  PartyID = wlinfo.OriginatorID;  
  field_value = ПолучитьКодСубъекта( PartyID, CodeKind, error );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Для банка-создателя не найден код с видом кода: " + CodeKind );
  end;
  ExecMacro( MacroProc, "PA", CB_StrSubst0( String(field_value:10) ), Parm );

  /* Участник-получатель */
  PartyID = wlinfo.RecipientID;  
  field_value = ПолучитьКодСубъекта( PartyID, CodeKind, error );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Для банка-получателя не найден код с видом кода: " + CodeKind );
  end;
  ExecMacro( MacroProc, "RC", CB_StrSubst0( String(field_value:10) ), Parm );

  /* Банк-плательщик, заполняется по банку-создателю */
  field_value = ПолучитьКодСубъекта( wlinfo.OriginatorID, PTCK_BIC, error, 1 );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Для банка-плательщика не найден код с видом кода: " + PTCK_BIC );
  end;
  /* Ищем субъекта */
  PartyID = ПолучитьКодСубъекта( field_value, PTCK_BIC, Error );
  if( Error != 0 )
    RunError( "|Не найден субъект с кодом: " + field_value );
  end;
  /* Если банк-плательщик и участник-отправитель это один и тот же субъект */
  if( PartyID == wlinfo.OriginatorID )
    BankName = wlinfo.OriginatorName;
  else
    if( ПолучитьСубъекта(PartyID, wlparty) != 0 )
      RunError( "|Не найден субъект с идентификатором: " + PartyID );
    end;
    BankName = wlparty.Name;
  end;
  /* Корсчет определяем по справочнику отделений банков */
  f_wldprt.PartyID = PartyID;
  if( not GetEQ(f_wldprt))
    RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
  end;
  ExecMacro( MacroProc, "SB", BankName, Parm );
  ExecMacro( MacroProc, "SN", field_value, Parm );
  ExecMacro( MacroProc,  "SK", f_wldprt.CorAcc, Parm );

  ВидКодаСтр = ПолучитьВидКодаSBRF3( PTCK_BIC );
  if( ВидКодаСтр != "" )    
    ExecMacro( MacroProc,  "SS", ВидКодаСтр, Parm );
  else /* Не найден такой вид кода */      
    RunError( "|Невозможно определить платежную систему банка-создателя|для вида кода " + string(PTCK_BIC) );
  end;

  /* Банк-получатель, заполняется по участнику получателю */
  field_value = ПолучитьКодСубъекта( wlinfo.RecipientID, PTCK_BIC, error, 1 );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Для банка-получателя не найден код с видом кода: " + PTCK_BIC );
  end;
  /* Ищем субъекта */
  PartyID = ПолучитьКодСубъекта( field_value, PTCK_BIC, Error );
  if( Error != 0 )
    RunError( "|Не найден субъект с кодом: " + field_value );
  end;
  /* Если банк-получатель и участник-получатель это один и тот же субъект */
  if( PartyID == wlinfo.RecipientID )
    BankName = wlinfo.RecipientName;
  else
    if( ПолучитьСубъекта(PartyID, wlparty) != 0 )
      RunError( "|Не найден субъект с идентификатором: " + PartyID );
    end;
    BankName = wlparty.Name;
  end;
  /* Корсчет определяем по справочнику отделений банков */
  f_wldprt.PartyID = PartyID;
  if( not GetEQ(f_wldprt))
    RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
  end;
  ExecMacro( MacroProc,  "BN", BankName, Parm );
  ExecMacro( MacroProc,  "BC", field_value, Parm );
  ExecMacro( MacroProc,  "BK", f_wldprt.CorAcc, Parm );

  ВидКодаСтр = ПолучитьВидКодаSBRF3( PTCK_BIC );
  if( ВидКодаСтр != "" )    
    ExecMacro( MacroProc,  "RS", ВидКодаСтр, Parm );
  else /* Не найден такой вид кода */      
    RunError( "|Невозможно определить платежную систему банка-получателя|для вида кода " + string(PTCK_BIC) );
  end;

  /* Тип сообщения */
  ExecMacro( MacroProc, "MT", wlInfo.TypeOper, Parm );

  /* Сообщение */
  if( f_wlmesfrm.Name == sign_0B7 )    /* Сообщение участнику */
      field_value = ПрочитатьТекстИнфСообщения( wlinfo );
      /*текст телеграммы должен быть меньше, чем 210+210+160=580 символов*/
      if( strlen(field_value) > 580 )
        RunError("|Текст сообщения не должен превышать 580 символов");
      end;
      ExecMacro( MacroProc, "MP", substr( field_value,   1, 210 ), Parm );
      ExecMacro( MacroProc, "PP", substr( field_value,   211, 210 ), Parm );
      ExecMacro( MacroProc, "PN", substr( field_value,   421, 160 ), Parm );
  elif( f_wlmesfrm.Name == sign_0BF )  /* Сообщение свободного формата */
    field_value = ПрочитатьТекстИнфСообщения( wlinfo );
    ExecMacro( MacroProc, "ME", field_value, Parm );  
  end;

  /* Дата последней обработки */
  ExecMacro( MacroProc,  "LD", ДатаДДММГГ( {curdate} ), Parm );  

  if(wlinfo.RelatedRef != "" )
    ExecMacro( MacroProc,  "ST", wlinfo.RelatedRef, Parm );  
  end;

  SetParm( 1, Parm );

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

/* Релиз 0B7 */
macro GenMes( addrMes, addrInfo )
  var format = true;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlInfo, addrInfo );

  PrintLog(2,"Генерация сообщения по SBRF3, релиз 0B7");    

  return GenMesExec( "SB_ЗаписатьПолеЛог", format );

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;

/* Релиз 0B7J */
macro GenMesPIB( addrMes, addrInfo )
  var BCI = "";

  SetBuff( wlmes,  addrMes );
  SetBuff( wlInfo, addrInfo );

  PrintLog(2,"Генерация сообщения по SBRF3, релиз 0B7J");    

  if( not GenMesExec( "SB_ЗаполнитьПоле", BCI ) )
    return false;
  end;

  SB_ЗаписатьПолеЛог( SB_Tag_PIB, BCI, true );

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;