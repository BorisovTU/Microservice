/*
  $Name:         ufgo244.mac
  $Module:       МБР
  $Description:  Обработка ответа на запрос ED244
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Обработка ответа на запрос ED244                                         */
/*                                                                          */
/*  Имя файла: ufgo244.mac                                                  */
/*  Создан:    27.04.12                                      Чукина Т.А.    */
/****************************************************************************/

import "wlglobal.mac", MesInter, oralib, likepy, CTInter, "wluftool.mac", WldInter, 
       PaymInter, OprInter, "wlgenmes.mac", "uf243244.mac", "repgo244.mac", "pmlib.mac",
       "TFieldsPI.mac", "ufNtfCar_lib.mac";

private class TMesInfo
  var EDAuthor      : string = "", 
      EDAuthorOEPD  : string = "", // OriginalEPD\EDAuthor
      EDDefineAnswerCode : string = "",
      EDDefineRequestCode: string = "",
      AccDocDate    : string = "",
      AccDocNo      : string = "",
      PayerAcc      : string = "",
      PayeeAcc      : string = "",
      PayerINN      : string = "",
      PayeeINN      : string = "",
      Sum           : string = "",
      PayerLongName : string = "",
      PayeeLongName : string = "",
      Purpose       : string = "",
      Address       : string = "",
      DrawerStatus  : string = "",
      PayerKPP      : string = "",
      PayeeKPP      : string = "",
      CBC           : string = "",
      OKATO         : string = "",
      PaytReason    : string = "",
      TaxiPeriod    : string = "",
      DocNo         : string = "",
      DocDate       : string = "",
      TaxPaytKind   : string = "",

      // Для версии 2.5.9
      NamedFields    : TArray = TArray(), // поименованные поля EDDefineAnswerInfo
      NumberedFields : TArray = TArray(); // пронумерованные поля EDDefineAnswerInfo

  var ExistsInitialED : bool = false;
  var RefInitialED : EDNoDateAuthor = EDNoDateAuthor();

end;

private macro readMesFlds(MesInfo : TMesInfo)
  var fieldname = "", fieldvalue = "", blockname = "";
  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if ( (fieldname == "EDAuthor") and (blockname == "") )
      MesInfo.EDAuthor = fieldvalue;
    end;
    if ( (fieldname == "EDAuthor") and (blockname == "OriginalEPD") )
      MesInfo.EDAuthorOEPD = fieldvalue;
    end;  
    if (fieldname == "EDDefineAnswerCode")
      MesInfo.EDDefineAnswerCode = fieldvalue;
    end;
    if (fieldname == "AccDocDate")
      MesInfo.AccDocDate = fieldvalue;
    end;
    if (fieldname == "AccDocNo")
      MesInfo.AccDocNo = fieldvalue;
    end;
    if (fieldname == "PayerAcc")
      MesInfo.PayerAcc = fieldvalue;
    end;
    if (fieldname == "PayeeAcc")
      MesInfo.PayeeAcc = fieldvalue;
    end;
    if (fieldname == "PayerINN")
      MesInfo.PayerINN = fieldvalue;
    end;
    if (fieldname == "PayeeINN")
      MesInfo.PayeeINN = fieldvalue;
    end;
    if (fieldname == "Sum")
      MesInfo.Sum = fieldvalue;
    end;
    if (fieldname == "PayerLongName")
      MesInfo.PayerLongName = fieldvalue;
    end;
    if (fieldname == "PayeeLongName")
      MesInfo.PayeeLongName = fieldvalue;
    end;
    if (fieldname == "Purpose")
      MesInfo.Purpose = fieldvalue;
    end;
    if (fieldname == "Address")
      MesInfo.Address = fieldvalue;
    end;
    if (fieldname == "DrawerStatus")
      MesInfo.DrawerStatus = fieldvalue;
    end;
    if (fieldname == "PayerKPP")
      MesInfo.PayerKPP = fieldvalue;
    end;
    if (fieldname == "PayeeKPP")
      MesInfo.PayeeKPP = fieldvalue;
    end;
    if (fieldname == "CBC")
      MesInfo.CBC = fieldvalue;
    end;
    if (fieldname == "OKATO")
      MesInfo.OKATO = fieldvalue;
    end;
    if (fieldname == "PaytReason")
      MesInfo.PaytReason = fieldvalue;
    end;
    if (fieldname == "TaxiPeriod")
      MesInfo.TaxiPeriod = fieldvalue;
    end;
    if (fieldname == "DocNo")
      MesInfo.DocNo = fieldvalue;
    end;
    if (fieldname == "DocDate")
      MesInfo.DocDate = fieldvalue;
    end;
    if (fieldname == "TaxPaytKind")
      MesInfo.TaxPaytKind = fieldvalue;
    end;                      
  end; // while ( СчитатьПоле )  
end;

private macro UpdatePaymentFromMesInfo(Payment : RsbPayment, MesInfo : TMesInfo)
  if(MesInfo.AccDocDate)
    Payment.ClientDate = ToDate(MesInfo.AccDocDate);
  end;
  if(MesInfo.AccDocNo)
    Payment.Number = MesInfo.AccDocNo;
  end;
  if(MesInfo.PayerAcc)
    Payment.SetPayerAccount (Payment.PayerFIID, Payment.Chapter, MesInfo.PayerAcc);
  end;
  if(MesInfo.PayeeAcc)
    Payment.SetReceiverAccount (Payment.ReceiverFIID, Payment.Chapter, MesInfo.PayeeAcc);
  end;
  var KPP = "";
  if(MesInfo.PayerINN)
    KPP = RemoveINN(Payment.PayerINN);
    Payment.PayerINN = MesInfo.PayerINN;
    if(KPP)
      Payment.PayerINN = Payment.PayerINN + "/" + KPP;
    end;
  end;
  if(MesInfo.PayeeINN)
    KPP = RemoveINN(Payment.ReceiverINN);
    Payment.ReceiverINN = MesInfo.PayeeINN;
    if(KPP)
      Payment.ReceiverINN = Payment.ReceiverINN + "/" + KPP;
    end;
  end;
  if(MesInfo.Sum)
    Payment.BaseAmount = moneyL( MesInfo.Sum )/100;
  end;
  if(MesInfo.PayerLongName)
    Payment.PayerName = MesInfo.PayerLongName;
  end;
  if(MesInfo.PayeeLongName)
    Payment.ReceiverName = MesInfo.PayeeLongName;
  end;
  if(MesInfo.Purpose)
    Payment.Ground = MesInfo.Purpose;
  end;
  if(MesInfo.Address)
    Payment.PayerName = Payment.PayerName + ", " + MesInfo.Address;
  end;
  if(MesInfo.DrawerStatus)
    Payment.TaxAuthorState = MesInfo.DrawerStatus;
  end;
  if(MesInfo.PayerKPP)
    Payment.PayerINN = RemoveKPP(Payment.PayerINN) + "/" + MesInfo.PayerKPP;
  end;
  if(MesInfo.PayeeKPP)
    Payment.ReceiverINN = RemoveKPP(Payment.ReceiverINN) + "/" + MesInfo.PayeeKPP;
  end;
  if(MesInfo.CBC)
    Payment.BttTICode = MesInfo.CBC;
  end;
  if(MesInfo.OKATO)
    Payment.OKATOCode = MesInfo.OKATO;
  end;
  if(MesInfo.PaytReason)
    Payment.TaxPmGround = MesInfo.PaytReason;
  end;
  if(MesInfo.TaxiPeriod)
    Payment.TaxPmPeriod = MesInfo.TaxiPeriod;
  end;
  if(MesInfo.DocNo)
    Payment.TaxPmNumber = MesInfo.DocNo;
  end;
  if(MesInfo.DocDate)
    Payment.TaxPmDate = MesInfo.DocDate;
  end;
  if(MesInfo.TaxPaytKind)
    Payment.TaxPmType = MesInfo.TaxPaytKind;
  end;
end;

// направить платеж на возврат
private macro SendPmToReturn(Payment : RsbPayment, ReturnReason : string, msg : string, StepSymbolToRemove : string)
  
  // Записать в примечание платежа "Причина отказа (возврата)" значение ReturnReason
  if( Payment.Notes.ReadNote(PM_NOTEKIND_DENIALGROUND, {curdate}) )
    Payment.Notes.DelNote(PM_NOTEKIND_DENIALGROUND);
  end;
  Payment.Notes.AddNote(PM_NOTEKIND_DENIALGROUND, ReturnReason);

  if( StepSymbolToRemove )
    if( not PM_InsertAndExecuteBranch(Payment.PaymentID, StepSymbolToRemove, "в", OPRBR_REMOVE, "W") )
      RunError("Ошибка при вставке блока \"Возврат невыясненного платежа\" ");
    end;
  else
    // Установить категорию платежа "Подлежит возврату" в "Да"
    Payment.Categories.ConnectAttr(15, 1, NULL, NULL, {curdate});

    // Запустить операцию обработки по платежу
    if( not PM_ExecuteOperation (Payment.PaymentID, Payment.DocKind) )
      RunError("Ошибка при выполнении операции по платежу");
    end;
  end;

  // Выдать сообщение msg
  std.msg(msg);

end;
/* РЕЛИЗ ЗАКРЫТ
macro GenObj( addrMes, mode )

  if(mode == WLD_GENOBJ_ACTION_ROLLBACK) // Откат обработки не реализуется
    return TRUE;
  end;

  SetBuff( wlmes, addrMes );
  PrintLog(2,"Обработка входящего ответа ED244");

  var MesInfo : TMesInfo = TMesInfo();
  readMesFlds(MesInfo);

  // Если значение в поле EDAuthor не равно OriginalEPD/EDAuthor
  if(MesInfo.EDAuthor != MesInfo.EDAuthorOEPD)
    std.msg( "Ответ ED244 от составителя " + MesInfo.EDAuthor + 
             " ссылается на исходный ЭПД другого составителя: " + MesInfo.EDAuthorOEPD );
    return FALSE;
  end;

  var ed_nda : EDNoDateAuthor = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.RelatedRef);

  // Найти все исходные входящие сообщения wlmes_paym для сообщения ответа wlmes_in
  var select = "Select t_MesID " +
               "  from dwlmes_dbt " +
               " where t_Trn = :Trn " +
               "   and t_Direct = 'X' ";
  var params = makeArray( SQLParam("Trn", wlmes.RelatedRef) );
  var rs = execSQLselect(select, params);
  if( not rs or not rs.moveNext() )
    std.msg( "Не найден исходный ЭПД с EDNo: " + ed_nda.EDNo + ", " +
             "EDDate: " + string(ed_nda.EDDate:f) + ", " +
             "EDAuthor: " + ed_nda.EDAuthor );
    return FALSE;
  end;
  
  // По найденным сообщениям ищутся платежи, которые по ним сгенерированы
  var AddCondStr = "wlpm.t_WlPmID IN (";
  var IsPaymFound = false;
  select = "Select wlpm.t_WlPmID WlPmID " +
           "  from dwlpm_dbt wlpm, dwlmeslnk_dbt lnk, dwlmes_dbt mes "
           " where mes.t_Trn     = :Trn " +
           "   and mes.t_Direct  = 'X' " +
           "   and lnk.t_MesID   = mes.t_MesID " +
           "   and lnk.t_ObjKind = :ObjType " +
           "   and wlpm.t_WlPmID = lnk.t_ObjID ";
  params = NULL;
  params = makeArray( SQLParam("Trn", wlmes.RelatedRef),
                      SQLParam("ObjType", OBJTYPE_PAYMENT) );
  rs = NULL;
  rs = execSQLselect(select, params);
  while(rs and rs.moveNext())
    if(not IsPaymFound)
      IsPaymFound = true;
      AddCondStr = AddCondStr + string( rs.value("WlPmID") );
    else
      AddCondStr = AddCondStr + "," + string( rs.value("WlPmID") );
    end;
  end;
  if(IsPaymFound)
    AddCondStr = AddCondStr + ")";
  else
    std.msg( "По исходному ЭПД с EDNo: " + ed_nda.EDNo + ", " +
             "EDDate: " + string(ed_nda.EDDate:f) + ", " +
             "EDAuthor: " + ed_nda.EDAuthor + " еще не создан платеж");
    return FALSE;
  end;

  // Пользователь выбирает в скроллинге платеж 
  var PaymentID : integer = SelectExternPaymentFromScrol("X", AddCondStr);
  if(PaymentID <= 0)
    return FALSE;
  end;

  var Payment : RsbPayment = RsbPayment(PaymentID);
  var StepSymbol : string = "";

  // Если выбранный платеж отложенный
  if(Payment.PropStatus == PM_PROP_LOADED)
    if( (MesInfo.EDDefineAnswerCode != "01") and (MesInfo.EDDefineAnswerCode != "03") )
      UpdatePaymentFromMesInfo(Payment, MesInfo);
      Payment.Update();
    end;
    if(MesInfo.EDDefineAnswerCode == "01")
      SendPmToReturn(Payment, "Затребовано к возврату банком отправителя", "Исходный платеж направлен на возврат отправителю");
    end;
  // Если платеж отвергнут 
  elif(Payment.PaymStatus == PM_REJECTED)
    if( (MesInfo.EDDefineAnswerCode != "01") and (MesInfo.EDDefineAnswerCode != "03") )
      UpdatePaymentFromMesInfo(Payment, MesInfo);
      if( PM_InsertAndExecuteBranch(Payment.PaymentID, "", "N", OPRBR_INSERT, "W") )
        std.msg("Уточненные реквизиты ЭПД успешно записаны в платеж");
      else
        std.msg("Ошибка при редактировании отвергнутого платежа");
        return FALSE;
      end;
    end;
    if(MesInfo.EDDefineAnswerCode == "01")
      SendPmToReturn(Payment, "Затребовано к возврату банком отправителя", "Исходный платеж из списка отвергнутых платежей направлен на возврат отправителю");
    end;
  // Если платеж на ручной обработке или в невыясненных 
  elif( StepSymbol = FindPmReadyStepFromList(Payment, "З", "U") )
    if( (MesInfo.EDDefineAnswerCode != "01") and (MesInfo.EDDefineAnswerCode != "03") )
      UpdatePaymentFromMesInfo(Payment, MesInfo);
      if( PM_InsertAndExecuteBranch(Payment.PaymentID, StepSymbol, "ъ", OPRBR_INSERT, "W") )
        std.msg("Уточненные реквизиты ЭПД успешно записаны в платеж");
      else
        std.msg("Ошибка при редактировании платежа");
        return FALSE;
      end;
    end;
    if(MesInfo.EDDefineAnswerCode == "01")
      SendPmToReturn(Payment, "Затребовано к возврату банком отправителя", "Исходный платеж направлен на возврат отправителю", 
                     StepSymbol
                    );
    end;
  // Если обработка платежа завершена 
  elif(Payment.PaymStatus == PM_FINISHED)
    if(MesInfo.EDDefineAnswerCode != "03")
      std.msg("Обработка платежа уже завершена, внесение изменений в документ невозможно. Откатите обработку платежа");
      return FALSE;
    else
      return TRUE;
    end;
  else
    std.msg("Состояние документа не позволяет внести в него изменения. Откатите обработку платежа");
    return FALSE;
  end;

  return TRUE;
  
  OnError(er) // обработка ошибок времени выполнения
    ExeptionMessage(er);
    return FALSE;
end;
*/
private macro saveFldNoValToMesInfo(FieldNo, FieldValue, MesInfo)

  if( StrIsNumber(FieldNo) )
    MesInfo.NumberedFields[ int(FieldNo) ] = FieldValue;
  else
    var IsSaved = false;
    for(var MesFld : TMesField, MesInfo.NamedFields)
      if(MesFld.FieldName == FieldNo)
        MesFld.FieldValue = FieldValue;
        IsSaved = true;
        break;
      end;
    end;

    if(not IsSaved)
      MesInfo.NamedFields[ MesInfo.NamedFields.size ] = TMesField(FieldNo, FieldValue);
    end;
  end;

end;

private macro saveFldToMesInfo(fieldname, fieldvalue, blockname, MesInfo)
  if ( (fieldname == "EDAuthor") and (blockname == "") )
    MesInfo.EDAuthor = fieldvalue;
  elif ( (fieldname == "EDAuthor") and (blockname == "OriginalEPD") )
    MesInfo.EDAuthorOEPD = fieldvalue;
  elif (fieldname == "EDDefineAnswerCode")
    MesInfo.EDDefineAnswerCode = fieldvalue;
  elif (fieldname == "EDDefineRequestCode")
    MesInfo.EDDefineRequestCode = fieldvalue;
  elif (blockname == "InitialED")
    MesInfo.ExistsInitialED = true;

    if(fieldname == "EDNo")
      MesInfo.RefInitialED.EDNo = fieldvalue;
    end;
    if(fieldname == "EDDate")
      MesInfo.RefInitialED.EDDate = ToDateYYYY_MM_DD(fieldvalue);
    end;
    if(fieldname == "EDAuthor")
      MesInfo.RefInitialED.EDAuthor = fieldvalue;
    end;
  elif( index(blockname, "EDDefineAnswerInfo") and 
        not index(blockname, "EDReestrInfo") and
        ( substr(fieldname, 1, 1) != "_" )
      )
    if(fieldname == "FieldNo")
      var FieldNo = fieldvalue;

      if( СчитатьПоле(fieldname, fieldvalue, blockname) and
          (fieldname == endField) and index(blockname, "FieldNo") and
          СчитатьПоле(fieldname, fieldvalue, blockname) and
          (fieldname == beginField) and index(blockname, "FieldValue") and
          СчитатьПоле(fieldname, fieldvalue, blockname) and
          (fieldname == "FieldValue")
        )
        saveFldNoValToMesInfo(FieldNo, fieldvalue, MesInfo);
      else
        saveFldToMesInfo(fieldname, fieldvalue, blockname, MesInfo);
      end;
    else
      MesInfo.NamedFields[ MesInfo.NamedFields.size ] = TMesField(fieldname, fieldvalue);
    end;
  end;
end;

private macro readMesFlds259( MesInfo : TMesInfo )
  var fieldname = "", fieldvalue = "", blockname = "";

  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
    saveFldToMesInfo(fieldname, fieldvalue, blockname, MesInfo);
  end;
end;

private class TPrnInfo
  var  TmpFileName, 
       OldName,
       FailedFields : TArray = TArray();
end;

private macro StartReport(PrnInfo : TPrnInfo, TmpFile, Payment : RsbPayment)
  // Перенаправляем вывод в файл
  PrnInfo.TmpFileName = GetTxtFileName(TmpPrnFileName_GenObj244);
  PrnInfo.OldName = SetOutPut( PrnInfo.TmpFileName );

  if(not open( TmpFile, PrnInfo.TmpFileName ) )
    RunError( String("Файл не открыт: ", PrnInfo.TmpFileName) );
  end;

  // PrintHeader
  [
    Уточняемые реквизиты исходного ЭПС 

    Референс:         #########################
    Номер документа:  ######
    Дата документа:   ##########

    Изменение реквизитов платежа
    ┌───────────────────┬───────────────────┬─────────────────────────────┬─────────────────────────────┐
    │Номер/наимен.      │ Реквизит платежа  │       Прежнее значение      │       Новое значение        │
    │     поля          │                   │                             │                             │
  ](wlmes.RelatedRef, Payment.Number, Payment.Date);
end;

private macro FinishReport(PrnInfo : TPrnInfo, TmpFile)
  // PrintFooter
  [ └───────────────────┴───────────────────┴─────────────────────────────┴─────────────────────────────┘

    Не удалось идентифицировать реквизиты
    ┌──────────────────────────────────────────────────────────────────────┬──────────────────────────────┐
    │ Номер/наименование                                                   │   Значение поля              │
    │        поля                                                          │                              │
  ];

  for(var MesField, PrnInfo.FailedFields)
  [ ├──────────────────────────────────────────────────────────────────────┼──────────────────────────────┤
    │######################################################################│##############################│
  ] (MesField.FieldName, MesField.FieldValue:w);
  end;

  [ └──────────────────────────────────────────────────────────────────────┴──────────────────────────────┘
  ];

  close(TmpFile);
  SetOutPut( PrnInfo.OldName, true ); // Перенаправляем вывод обратно
end;

private macro ResetPIAttrs(MesInfo: TMesInfo, FieldsPI: TFieldsPI)
  var NeedSetPayerPI : bool = false,
      NeedSetPayeePi : bool = false;

  // пронумерованные поля
  for(var i : integer, 0, MesInfo.NumberedFields.size - 1, 1)
    if(MesInfo.NumberedFields[i] != null)
      ProcessPIFld(i, null, MesInfo.NumberedFields[i], FieldsPI, @NeedSetPayerPI, @NeedSetPayeePI);
    end;
  end;

  // поименованные поля
  for(var MesField : TMesField, MesInfo.NamedFields)
    if(MesInfo.NumberedFields[ GetFNoByFName(MesField.FieldName) ] == null) // если реквизит не менялся по какому-либо полю из блока "EDFieldList"
      ProcessPIFld(null, MesField.FieldName, MesField.FieldValue, FieldsPI, @NeedSetPayerPI, @NeedSetPayeePI);
    end;
  end;

  if(NeedSetPayerPI)
    FieldsPI.ResetPI(PRT_Debet);
  end;

  if(NeedSetPayeePI)
    FieldsPI.ResetPI(PRT_Credit);
  end;
end;

private macro ResetRoutePartAttrs(MesInfo: TMesInfo, FieldsRoutePart : TFieldsRoutePart)
  var IsRouteChanged : bool = false;

  // сейчас данные об участниках маршрута - только в поименованных полях
  for(var MesField : TMesField, MesInfo.NamedFields)
    ProcessRoutePartFld(MesField.FieldName, MesField.FieldValue, FieldsRoutePart, @IsRouteChanged);
  end;

  if(IsRouteChanged)
    FieldsRoutePart.UpdateRouteParticipants();
  end;

end;

private macro UpdatePaymAttr( Payment      : RsbPayment, 
                              FNo          : integer, 
                              FieldName    : string, 
                              FieldValue   : string, 
                              FieldsPI     : TFieldsPI,
                              FieldsRoutePart : TFieldsRoutePart,
                              FailedFields : TArray 
                            ) : bool

  var FieldNoName = IfThenElse(FNo == null, FieldName, string(FNo)); // Номер/наименование поля
  var PaymAttrName : string = "", OldValue : variant, NewValue : variant;

  if( UpdatePaymFromField(Payment, FNo, FieldName, FieldValue, FieldsPI, FieldsRoutePart, @PaymAttrName, @OldValue, @NewValue) )

    if(NewValue != OldValue)
  [ ├───────────────────┼───────────────────┼─────────────────────────────┼─────────────────────────────┤
    │###################│###################│#############################│#############################│
  ](FieldNoName:w, PaymAttrName:w, OldValue:w, NewValue:w);
    end;

    return TRUE;
  else
    FailedFields[ FailedFields.size ] = TMesField(FieldNoName, FieldValue);
    return FALSE;
  end;
end;

private macro UpdatePaymentFromMesInfo259(Payment : RsbPayment, MesInfo : TMesInfo)
  var PrnInfo : TPrnInfo = TPrnInfo();
  FILE TmpFile() txt;
  StartReport(PrnInfo, TmpFile, Payment);

  var FieldsPI : TFieldsPI = TFieldsPI(Payment);
  ResetPIAttrs(MesInfo, FieldsPI);

  var FieldsRoutePart : TFieldsRoutePart = TFieldsRoutePart(Payment);
  ResetRoutePartAttrs(MesInfo, FieldsRoutePart);

  var IsAttrDefined : bool = false; // удалось определить хотя бы один реквизит платежа

  // пронумерованные поля
  for(var i : integer, 0, MesInfo.NumberedFields.size - 1, 1)
    if(MesInfo.NumberedFields[i] != null)
      if( UpdatePaymAttr(Payment, i, null, MesInfo.NumberedFields[i], FieldsPI, FieldsRoutePart, PrnInfo.FailedFields) )
        IsAttrDefined = true;
      end;
    end;
  end;

  // поименованные поля
  for(var MesField : TMesField, MesInfo.NamedFields)
    if( (MesField.FieldName != "EDDefineAnswerText") and // не вносится в платеж
        (MesInfo.NumberedFields[ GetFNoByFName(MesField.FieldName) ] == null) // если реквизит не менялся по какому-либо полю из блока "EDFieldList"
      )
      if( UpdatePaymAttr(Payment, null, MesField.FieldName, MesField.FieldValue, FieldsPI, FieldsRoutePart, PrnInfo.FailedFields) )
        IsAttrDefined = true;
      end;
    end;
  end;

  if(not IsAttrDefined) // не удалось определить ни один реквизит платежа
    msgbox("Не удалось сопоставить реквизиты, уточненные в ответе, реквизитам исходного платежа");
    return FALSE;
  end;

  if(PrnInfo.FailedFields.size > 0)
    msgbox("Часть реквизитов, уточненных в ответе, не удалось сопоставить реквизитам платежа");
  end;

  FinishReport(PrnInfo, TmpFile);

  // Вывести многозакладочную панель  платежа на экран (закладка R-макет по умолчанию) в режиме просмотра
  if( PM_ProcessPanel(Payment, null, null, null, null, null, null, null, null, 366, "Отчет о сопоставлении реквизитов  Alt-F7", "repgo244.mac", "PrintRepCmpPaymAttrs") == 0 )
    return true; // Если выход из панели был по F2
  else
    return false; // Если выход из панели был по Esc
  end;
end;

private macro LinkAnswerToPayment(MesID : integer, Payment : RsbPayment, PmDirect : string)
  var AnswerID : integer = GetObjIDByMesLink(wlmes.MesID, OBJTYPE_REQ, "X");
  if(not AnswerID)
    RunError("Не найден учетный объект по сообщению");
  end;

  var WlPmID : integer = IfThenElse(PmDirect == "X", Payment.InWlPmID, Payment.OutWlPmID);
  if(WlPmID <= 0)
    RunError("Не найден заголовок платежа в МБР");
  end;

  if( not IsLinkedWlreq(AnswerID, WlPmID, OBJTYPE_PAYMENT, PmDirect) )
    if( WldCreateReqLink(AnswerID, WlPmID, OBJTYPE_PAYMENT, PmDirect) != 0 )
      RunError("Ошибка при создании связки ответа с платежом");
    end;
  end;
end;

private class TCloseAnsParams
( p_AnswerCode : string,
  p_RequestCode : string,
  p_AnsMesID : integer,
  p_Payment : RsbPayment,
  p_PmDirect : string
)
  var AnswerCode : string = p_AnswerCode,
      RequestCode : string = p_RequestCode,
      AnsMesID : integer = p_AnsMesID,
      Payment : RsbPayment = p_Payment,
      PmDirect : string = p_PmDirect;
end;

private macro PlaceAnswerToClosed(Prm : TCloseAnsParams) : bool
  var msg = "Автоматическое изменение реквизитов и/или состояния платежа на основании данных ответа не может быть выполнено. Переместить ответ в обработанные?";

  if( MsgBoxEx(msg, MB_YES+MB_NO, IND_NO) == IND_YES )
    if( ( (Prm.AnswerCode == UfNtfCar_GetAnswerCodeSuccess()) or
          (Prm.AnswerCode == UfNtfCar_GetAnswerCodeFail())
        )
        and
        (Prm.RequestCode == "00")
      )
      LinkAnswerToPayment(Prm.AnsMesID, Prm.Payment, Prm.PmDirect);
    end;

    return true;
  end;

  return false;
end;

private macro GetDocKindFromDB(PaymentID : integer) : integer
  var q = "select t_DocKind " +
          "  from dpmpaym_dbt " +
          " where t_PaymentID = :PaymentID ";
  var rs = execSQLselect(q, makeArray( SQLParam("PaymentID", PaymentID) ) );
  if(rs and rs.moveNext())
    return rs.value("t_DocKind");
  end;

  return 0;
end;

macro GenObj259( addrMes, mode : integer, CalledFrom : integer )

  if(CalledFrom == null)
    CalledFrom = ED244_PROCOBJ_CALLFROM_GENOBJ;
  end;

  if(mode == WLD_GENOBJ_ACTION_ROLLBACK) // Откат обработки не реализуется
    return TRUE;
  end;

  SetBuff( wlmes, addrMes );
  PrintLog(2,"Обработка входящего ответа ED244");

  var MesInfo : TMesInfo = TMesInfo();
  readMesFlds259(MesInfo);


  if( (IsOprMultiExec) and (not(MesInfo.EDDefineAnswerCode == UfNtfCar_GetAnswerCodeSuccess()) and 
      (MesInfo.EDDefineRequestCode == "00"))
    )
    std.msg("Пакетная обработка ответов возможна только для уведомлений об успешном зачислении на счет получателя. Обработайте ответ вручную в списке принятых ответов");
    return FALSE;
  end;

  if ( MesInfo.ExistsInitialED )
    var outMesID = FindReqOutMesID( MesInfo.RefInitialED.GetTrn(), wlmes.TpSchemID );

    if( outMesID > 0 )
      if( WldChangeStatus(outMesID, OBJTYPE_MES, WLD_STATUS_MES_CLOSED) != 0 )
        std.msg("Ошибка при смене статуса сообщения с исходным запросом");
        return FALSE;
      end;

      var InitialReqID : integer = GetObjIDByMesLink(outMesID, OBJTYPE_REQ, "");
      if(InitialReqID > 0)
        if( WldChangeStatus(InitialReqID, OBJTYPE_REQ, WLD_STATUS_REQ_CLOSED) != 0 )
          std.msg("Ошибка при помещении исходного запроса в обработанные");
          return FALSE;
        end;
      end;
    end;
  end;

  if( InList( MesInfo.EDDefineAnswerCode, 
              "01", "02", "03", "04", "05", "06", "07", "08", "11", "12" )
    )
    // Если значение в поле EDAuthor не равно OriginalEPD/EDAuthor
    if(MesInfo.EDAuthor != MesInfo.EDAuthorOEPD)
      std.msg( "Ответ ED244 от составителя " + MesInfo.EDAuthor + 
               " ссылается на исходный ЭПС другого составителя: " + MesInfo.EDAuthorOEPD );
      return FALSE;
    end;
  end;

  var ed_nda : EDNoDateAuthor = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.RelatedRef);

  var IsFound = false;
  var AddCondStr = "";

  // Найти все исходные входящие сообщения wlmes_paym для сообщения ответа wlmes_in
  var select = "Select t_MesID " +
               "  from dwlmes_dbt " +
               " where t_Trn = :Trn ";
  if( InList( MesInfo.EDDefineAnswerCode, "01", "02", "03", "04", "05", "06", "07", "08", "11", "12", "18" ) )
    select = select + " and t_Direct = 'X' ";
  elif( InList( MesInfo.EDDefineAnswerCode, "00", "09", "10", "13", "14", "15", "19" ) )
    select = select + " and t_Direct = chr(0) ";
  end;

  var params = makeArray( SQLParam("Trn", wlmes.RelatedRef) );
  var rs = execSQLselect(select, params);
  while( rs and rs.moveNext() )
    if(not IsFound)
      IsFound = true;
      AddCondStr = "";
    else
      AddCondStr = AddCondStr + ", ";
    end;

    AddCondStr = AddCondStr + string( rs.value("t_MesID") );
  end;

  if(not IsFound)
    std.msg( "Не найден исходный ЭПС с EDNo: " + ed_nda.EDNo + ", " +
             "EDDate: " + string(ed_nda.EDDate:f) + ", " +
             "EDAuthor: " + ed_nda.EDAuthor );
    return FALSE;
  end;
  
  var Direct = "";

  // По найденным сообщениям ищутся платежи, которые по ним сгенерированы
  var PaymentID : integer = 0;

  IsFound = false; params = NULL; rs = NULL;
  select = "Select wlpm.t_WlPmID, wlpm.t_PaymentID, wlpm.t_Direct " +
           "  from dwlpm_dbt wlpm, dwlmeslnk_dbt lnk "
           " where lnk.t_MesID   in ( " + AddCondStr + " )" +
           "   and lnk.t_ObjKind = :ObjType " +
           "   and wlpm.t_WlPmID = lnk.t_ObjID ";

  params = makeArray( SQLParam("ObjType", OBJTYPE_PAYMENT) );

  rs = execSQLselect(select, params);
  while(rs and rs.moveNext())
    if(not IsFound)
      Direct = rs.value("t_Direct");
      PaymentID = rs.value("t_PaymentID");

      IsFound = true;
      AddCondStr = "wlpm.t_WlPmID IN (";
    else
      PaymentID = 0; // Нашли больше одного платежа, поэтому не знаем, какой будет
      AddCondStr = AddCondStr + ", ";
    end;

    AddCondStr = AddCondStr + string( rs.value("t_WlPmID") );
  end;

  if(IsFound)
    AddCondStr = AddCondStr + ")";
  else
    std.msg( "По исходному ЭПС с EDNo: " + ed_nda.EDNo + ", " +
             "EDDate: " + string(ed_nda.EDDate:f) + ", " +
             "EDAuthor: " + ed_nda.EDAuthor + " отсутствует платеж");
    return FALSE;
  end;

  if(not IsOprMultiExec)
    // Пользователь выбирает в скроллинге платеж 
    PaymentID = SelectExternPaymentFromScrol(Direct, AddCondStr);
  else // процедура была вызвана в пакетном режиме
    if(PaymentID == 0)
      std.msg
      (
        "Найдено более одного платежа с реквизитами EDNo: " + ed_nda.EDNo +
        ", EDDate: " + DDpMMpYYYY(ed_nda.EDDate) + ", EDAuthor: " + ed_nda.EDAuthor +
        ". Необходимо обработать ответ вручную в списке принятых ответов"
      );
    end;
  end;
  if(PaymentID <= 0)
    return FALSE;
  end;

  var Payment : RsbPayment = null,
      PsOrder : RsbPsPayOrder = null;
  if( GetDocKindFromDB(PaymentID) == PS_PAYORDER )
    PsOrder = RsbPsPayOrder(PaymentID);
    Payment = PsOrder.Payment;
  else
    Payment = RsbPayment(PaymentID);
  end;

  var StepSymbol : string = "";
  var CloseAnsPrm : TCloseAnsParams = TCloseAnsParams
    ( MesInfo.EDDefineAnswerCode,
      MesInfo.EDDefineRequestCode, 
      wlmes.MesID, 
      Payment,
      Direct
    );

  if( (MesInfo.EDDefineAnswerCode == UfNtfCar_GetAnswerCodeSuccess()) and
      (MesInfo.EDDefineRequestCode == "00")
    )
    LinkAnswerToPayment(wlmes.MesID, Payment, Direct);
  elif
    ( ( (Payment.PropStatus == PM_PROP_LOADED) or // Если выбранный платеж отложенный
        (Payment.PaymStatus == PM_REJECTED) or    // Если платеж отвергнут 
        ( StepSymbol = FindPmReadyStepFromList(Payment, "З", "U") ) // Если платеж на ручной обработке или в невыясненных 
      ) 
      and InList(MesInfo.EDDefineAnswerCode, "01", "02", "04", "05", "06", "07", "08", "11") 
    )
    var msg = "";

    if(MesInfo.EDDefineAnswerCode == "01")
      if(Payment.PaymStatus == PM_REJECTED)
        msg = "Исходный платеж из списка отвергнутых платежей направлен на возврат отправителю";
      else
        msg = "Исходный платеж направлен на возврат отправителю";
      end;

      SendPmToReturn(Payment, "Затребовано к возврату банком отправителя", msg, StepSymbol);
    else
      if(not UpdatePaymentFromMesInfo259(Payment, MesInfo, StepSymbol))
        return PlaceAnswerToClosed(CloseAnsPrm);
      end;

      if(Payment.PropStatus == PM_PROP_LOADED)
        var ObjForUpdate = IfThenElse( PsOrder == null, Payment, PsOrder );
        if( ObjForUpdate.Update() != 0 )
          std.msg("Ошибка при редактировании платежа");
          return FALSE;
        end;
      elif(Payment.PaymStatus == PM_REJECTED)
        if( PM_InsertAndExecuteBranch(Payment.PaymentID, "", "N", OPRBR_INSERT) )
          std.msg("Уточненные реквизиты ЭПД успешно записаны в платеж");
        else
          std.msg("Ошибка при редактировании отвергнутого платежа");
          return FALSE;
        end;
      elif(StepSymbol)
        if( PM_InsertAndExecuteBranch(Payment.PaymentID, StepSymbol, "ъ", OPRBR_INSERT) )
          std.msg("Уточненные реквизиты ЭПД успешно записаны в платеж");
        else
          std.msg("Ошибка при редактировании платежа");
          return FALSE;
        end;
      end;
    end;
  else
    return PlaceAnswerToClosed(CloseAnsPrm);
  end;

  return TRUE;
  
  OnError(er) // обработка ошибок времени выполнения
    std.msg( RsbGetErrorEx(er) );
    return FALSE;
end;

