/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED380 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm380.mac                                                  */
/*  Создан:    28.09.12  Chukina T.                                         */
/****************************************************************************/

import "ufgenmes.mac", "wluftool.mac";

private macro ParseQueries( Queries: string, // /N/БИК
                            LimitTransKind: @string, 
                            LimitDirection: @string, 
                            PURBIC: @string
                          )

  var ind1 = 0, ind2 = 0, indEndStr = 0;
  LimitTransKind = LimitDirection = PURBIC = "";

  ind1 = index(Queries, "/");
  if(not ind1)
    RunError("Не определен вид лимита");
  end;

  ind2 = index(Queries, "/", ind1 + 1);
  if(ind2)
    LimitTransKind = substr(Queries, ind1+1, ind2-ind1-1);
    indEndStr = index(Queries, "\n", ind2+1);
    if(indEndStr)
      PURBIC = Trim( substr(Queries, ind2+1, indEndStr-ind2-1) );
    else
      PURBIC = Trim( substr(Queries, ind2+1) );
    end;
  else
    LimitTransKind = substr(Queries, ind1+1);
  end;

  if(not LimitTransKind)
    RunError("Не определен вид лимита");
  end;

  if( (LimitTransKind == "2") or (LimitTransKind == "3") )
    LimitDirection = "1";
  end;
end;

macro GenMes( addrMes, addrReq )

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED380");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);
  
  var LimitTransKind = "", LimitDirection = "", PURBIC = "";
  ParseQueries(wlReq.Queries, @LimitTransKind, @LimitDirection, @PURBIC);
  mes.setAttribute("LimitTransKind",  LimitTransKind);
  if(LimitDirection)
    mes.setAttribute("LimitDirection",  LimitDirection);
  end;
  if(PURBIC)
    mes.setAttribute("PURBIC",  PURBIC);
  end;

  /*if(wlmes.RelatedRef)
    var elem = xml.createElement("EDRefID");
    FillEDNoDateAuthorByRef_XMLField(elem, wlmes.RelatedRef);
    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;*/

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;