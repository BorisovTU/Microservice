/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Печать сообщений SWIFT стандарт RUR5                                     */
/*                                                                          */
/*  Имя файла: swrurprn.mac                                                 */
/*  Создан:    21.09.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgnrdoc.mac", "adress.mac";

FILE bicdir( ptbicdir ) key 0;
FILE f_party( party ) key 0;
const PRINT_SHIFT = "                     ";

/* Макрос расшифровки поля 32 */
macro PrintField32( поле:string )
  var datev,name_currency,short_currency,amt;

  datev = YYMMDD2Date(поле);
  short_currency=SubStr(поле,7,3);
  ПолучитьФинИнПоISO( short_currency, wlfininstr );
  name_currency = wlfininstr.Name;
  amt=SubStr(поле,10);
  std.out( 1, PRINT_SHIFT + string(datev:f) );
  std.out( 1, PRINT_SHIFT + name_currency );
  std.out( 1, PRINT_SHIFT + PRINT_SHIFT + PRINT_SHIFT + amt );
end;

/* Макрос печати поля 32B */
macro PrintField32B( поле )
  var name_currency,short_currency,amt;
  short_currency=SubStr( поле,1,3);
  ПолучитьФинИнПоISO( short_currency, wlfininstr );
  name_currency = wlfininstr.Name;
  amt=SubStr( поле,4);
  [32B:CURRENCY CODE, AMOUNT];
  [    #](поле);
  std.out( 1, PRINT_SHIFT + PRINT_SHIFT + PRINT_SHIFT + name_currency );
  std.out( 1, PRINT_SHIFT + PRINT_SHIFT + PRINT_SHIFT + amt );
end;

/* Распечатать многострочное поле */
macro PrintMultiFields( поле, LenStr, Numbers );
  var num, count;
  array dest;

  РазбитьНаСтроки( поле, dest, LenStr, Numbers );
  num = Asize( dest );
  count=0;
  while( count<num )
    if ( dest(count)!="" ) [    #]( dest(count) ); end;
    count=count+1;
 end;
end;

/* Макрос для печати полей 52, 53, 54, 56, 57, 58 */
macro PrintIns( имя, поле, standart )
   var count,num, PLUS, ID, error;
   array dest;

   if ( valtype(Standart)==V_UNDEF ) standart = ST_UNDEF; end;

   if ( SubStr( имя,3,1 )== "A" ) PLUS="- BIC";
   elif ( SubStr( имя,3,1 )== "B" ) PLUS="- BRANCH";
   elif ( SubStr( имя,3,1 )== "D" ) PLUS="- NAME";
   else PLUS="";
   end;

   if ( SubStr( имя,1,2 )== "52" )
      [###:ORDERING INSTITUTION #]( имя, PLUS );
   elif ( SubStr( имя,1,2 )== "53" )
      [###:SENDER'S CORRESPONDENT #]( имя, PLUS );
   elif ( SubStr( имя,1,2 )== "54" )
      [###:RESEIVER'S CORRESPONDENT #]( имя, PLUS );
   elif ( SubStr( имя,1,2 )== "56" )
      [###:INTERMEDIARY #]( имя, PLUS );
   elif ( SubStr( имя,1,2 )== "57" )
      [###:ACCOUNT WITH INSTITUTION #]( имя, PLUS );
   elif ( SubStr( имя,1,2 )== "58" )
      [###:BENEFICIARY INSTITUTION #]( имя, PLUS );
   end;

   if ( SubStr( имя,3,1 )=="D" )
     StrRestoreStandart( поле, поле, standart );
   end;
   PrintMultiFields( поле, 37, 5 );

   if( SubStr( имя,3,1 )== "A" )
     РазбитьНаСтроки( поле,dest,37,5);
     if ( SubStr(dest(0),1,1)!="/" ) count=0;
     else count=1; end;

     ID = ПолучитьКодСубъекта( dest( count ), PTCK_SWIFT, error );
     if( error )
       std.out( 1, PRINT_SHIFT + "BIC NOT FOUND !!!");
     else
       if( ID )
         bicdir.PartyID = ID;
         f_party.PartyID = ID;
       else
         bicdir.PartyID = {OurBank};
         f_party.PartyID = {OurBank};
       end;
       if( getEQ( bicdir ))
         std.out( 1, PRINT_SHIFT + bicdir.InstitutionName );
         std.out( 1, PRINT_SHIFT + bicdir.City );
       else
         std.out( 1, PRINT_SHIFT + "BANK NOT FOUND !!!");
       end;
     end;
   end;
end;

/* Макрос для печати полей 60, 62, 64, 65 */
macro PrintField60_62_64_65( имя, поле )   

   if( SubStr(имя,1,2) == "60" )
     [###:OPENING BALANCE](имя);
   elif ( SubStr(имя,1,2) == "64" )
     [###:CLOSING AVAILABLE BALANCE](имя);
   elif ( SubStr(имя,1,2) == "65" )
     [###:FORVARD AVAILABLE BALANCE](имя);
   else
     [###:CLOSING BALANCE](имя);
   end;
   [    #](поле);
   if( SubStr(поле,1,1)=="D" )
     std.out( 1, PRINT_SHIFT + "DEBIT");
   else
     std.out( 1, PRINT_SHIFT + "CREDIT");
   end;
   PrintField32( SubStr(поле,2) );
end;

/* Печать произвольного поля */
macro PrintOtherField( имя, поле )
  FILE wltpfld(wltpfld) key 1;

  wltpfld.TpID = TRANSP_SWIFT;
  wltpfld.Name = имя;

  if( GetEQ( wltpfld ) )
    [###:#](имя, StrUpr(wltpfld.Description ));
    PrintMultiFields( поле, wltpfld.LenEdit, wltpfld.NumLines );
  else
    [###:#](имя, поле);
  end;
end;

macro PrintField61( поле, standart )
   var num, count;
   array dest;

   if ( valtype(Standart)==V_UNDEF ) standart = ST_UNDEF; end;

   РазбитьНаСтроки( поле, dest, 65, 2 );
   num = Asize( dest );
   if ( num==2 )
      StrRestoreStandart( dest(1), dest(1), standart, 0 );
   end;
   count=0;
   while( count<num )
     if ( dest(count)!="" ) [    #]( dest(count) ); end;
     count=count+1;
   end;
end;

/* Макрос для печати поля */
macro РаспечататьПоле( имя, поле, standart )

  if ( valtype(Standart)==V_UNDEF ) standart = ST_UNDEF; end;

  if( поле!="" )
    /* ----------------- Печать поля 11 (A,R,S) ----------------------- */
    if( (имя == "11A") OR (имя == "11R") OR (имя == "11S") )
      [###:MT AND DATE OF ORIGINAL MESSAGE](имя);
      PrintMultiFields( поле, 10, 3 );
    /* ----------------- Печать поля 12 ------------------------------ */
    elif( имя == "12" )
      [###:SUB-MESSAGE TYPE](имя);
      [    #](поле);
    /* ----------------- Печать поля 13 ------------------------------ */
    elif( имя == "13" )
      [###:DATA/TIME INDICATOR](имя);
      [    #](поле);
    /* ----------------- Печать поля 20 ------------------------------ */
    elif( имя == "20" )
      [###:TRANSACTION REFERENCE NUMBER](имя);
      [    #](поле);
    /* ----------------- Печать поля 21 ------------------------------ */
    elif( имя == "21" )
      [###:RELATED REFERENCE](имя);
      [    #](поле);
    /* ----------------- Печать поля 25 ------------------------------ */
    elif( имя == "25" )
      [###:ACCOUNT IDENTIFICATION](имя);
      [    #](поле);
    /* ----------------- Печать поля 28 (любое) ---------------------- */
    elif( SubStr(имя,1,2) == "28" )
      [###:STATMENT NUMBER/SEQUENCE NUMBER](имя);
      [    #](поле);
    /* ----------------- Печать поля 30 ------------------------------ */
    elif( имя == "30" )
      [###:VALUE DATE](имя);
      [    ##########](YYMMDD2Date(поле):f);
    /* ----------------- Печать поля 32A ----------------------------- */
    elif( имя == "32A" )
      [###:VALUE DATE, CURRENCY CODE, AMOUNT](имя);
      [    #](поле);
      PrintField32( поле );
    /* ----------------- Печать поля 32B ----------------------------- */
    elif( имя == "32B" )
      PrintField32B( поле );
    /* ----------------- Печать поля 32 ----------------------------- */
    elif( имя == "32C" )
      [###:VALUE DATE, CURRENCY CODE, AMOUNT](имя);
      [    #](поле);
      PrintField32( поле );
    /* ----------------- Печать поля 32A ----------------------------- */
    elif( имя == "32D" )
      [###:VALUE DATE, CURRENCY CODE, AMOUNT](имя);
      [    #](поле);
      PrintField32( поле );
    /* ----------------- Печать поля 34F ----------------------------- */
    elif( SubStr(имя,1,3) == "34F" )
      [####:NUMBERS AND SUMM OF ENTRIES](имя);
      [     #](поле);
    /* ----------------- Печать поля 50 ------------------------------ */
    elif( (имя == "50") OR (имя == "50K") )
      [###:ORDERING CUSTOMER](имя);
      StrRestoreStandart( поле, поле, standart );
      PrintMultiFields( поле, 35, 4 );
    /* ---------- Печать полей 52, 53, 54, 56, 57, 58 ---------------- */
    elif( (SubStr(имя,1,2) == "52") OR (SubStr(имя,1,2) == "53") OR
          (SubStr(имя,1,2) == "54") OR (SubStr(имя,1,2) == "56") OR
          (SubStr(имя,1,2) == "57") OR (SubStr(имя,1,2) == "58") )
      PrintIns( имя, поле, standart );
    /* ----------------- Печать поля 59 ------------------------------ */
    elif( имя == "59" )
      [###:BENEFICIARY CUSTOMER](имя);
      StrRestoreStandart( поле, поле, standart );
      PrintMultiFields( поле, 35, 5 );
    /* ----------------- Печать поля 60 (любое) ---------------------- */
    elif ( SubStr(имя,1,2) == "60" )
      PrintField60_62_64_65( имя, поле );
    /* ----------------- Печать поля 61 ------------------------------ */
    elif( имя == "61" )
      [###:STATEMENT LINE](имя);
      PrintField61( поле, standart );
    /* ----------------- Печать полей 62, 64, 65 --------------------- */
    elif ( SubStr(имя,1,2) == "62" )
      PrintField60_62_64_65( имя, поле );
    elif ( SubStr(имя,1,2) == "64" )
      PrintField60_62_64_65( имя, поле );
    elif ( SubStr(имя,1,2) == "65" )
      PrintField60_62_64_65( имя, поле );
    /* ----------------- Печать поля 70 ------------------------------ */
    elif( имя == "70" )
      [###:DETAILS OF PAYMENT](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 4 );
     /* ----------------- Печать поля 71A ----------------------------- */
    elif ( имя == "71A" )
      [###:DETAILS OF CHARGES](имя);
      [    #](поле);
    /* ----------------- Печать поля 71B ------------------------------ */
    elif( имя == "71B" )
      [###:DETAILS OF CHARGES](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 6 );
    /* ----------------- Печать поля 72 ------------------------------ */
    elif( имя == "72" )
      [###:SENDER TO RECEIVER INFORMATION](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 6 );
    /* ----------------- Печать поля 75 ------------------------------ */
    elif( имя == "75" )
      [###:QUERIES](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 6 );
    /* ----------------- Печать поля 76 ------------------------------ */
    elif( имя == "76" )
      [###:ANSWERS](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 6 );
     /* ----------------- Печать поля 77E ----------------------------- */
    elif ( имя == "77E" )
      [###:PROPRIETARY MESSAGE](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 78, 125 );
    /* ----------------- Печать поля 77A ------------------------------ */
    elif( имя == "77A" )
      [###:NARRATIVE](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 35, 20 );
    /* ----------------- Печать поля 79 ------------------------------ */
    elif( имя == "79" )
      [###:NARRATIVE DESCRIPTION OF THE MESSAGE TO WHICH TRE QUERY RELATES](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 50, 35 );
    /* ----------------- Печать поля 86 ------------------------------ */
    elif( имя == "86" )
      [###:INFORMATION TO ACCOUNT OWNER](имя);
      StrRestoreStandart( поле, поле, standart, 0 );
      PrintMultiFields( поле, 65, 6 );
    /* ----------------- Печать поля 90 ------------------------------ */
    elif( SubStr(имя,1,2) == "90" )
      [###:NUMBERS AND SUMM OF ENTRIES](имя);
      [    #](поле);
    /* ----------------- Печать поля MFIELDS -------------------------- */
    elif( имя == "MFIELDS" )
      [COPY OF LEAST THE MANDATORY FIELDS OF THE ORIGINAL MESSAGE];
      PrintMultiFields( поле, 35, 55 );
    /* ----------------- Все остальные поля ---------------------------- */
    else
      PrintOtherField( имя, поле );
    end;
  end;
  return TRUE;
end; /* macro РаспечататьПоле */

class TForm(addrMes)
   var name = TArray;
   var value = TArray;
   var stat, SenderID, ReceiverID, DateMessage, NameForm, NumberForm, Standart, NameStandart;


   macro ReadFields
       var field_name, field_value;
       /* Последовательно считываем и печатаем поля сообщения */
       while( СчитатьПоле( field_name, field_value ) )
         name( name.size ) = field_name;
         value( value.size ) = field_value;         
       end;
   end;

   /* Макрос печати заголовка формы */
   macro PrintHeader()
     record RecordAdress ( adress );
     var CodeBuf, error, InstitutionName, City, Size;

     if ( stat == FALSE ) return FALSE; end;
   
     std.RSBankHeader;
     [Message Type: ####              #]( NumberForm, StrUpr(NameForm) );
   
     /* Отправитель сообщения */
     CodeBuf = ПолучитьКодСубъекта( SenderID, PTCK_SWIFT, error );

     if( SenderID )
       bicdir.PartyID = SenderID;
       f_party.PartyID = SenderID;
     else 
       bicdir.PartyID = {OurBank};
       f_party.PartyID = {OurBank};
     end;
    
     if( getEQ( bicdir ))
       InstitutionName = bicdir.InstitutionName;
       City = bicdir.City;
     elif( getEQ( f_party ))
       ClearRecord(RecordAdress);
       НайтиЮридическийАдресСубъекта(f_party.PartyID,RecordAdress);
       InstitutionName = f_party.Name;
       City = RecordAdress.Place;
     else
       InstitutionName = " ";
       City = " ";
     end;
     [From        :       ########### ###################################
                                      ###################################](CodeBuf,InstitutionName,City);
     /* Получатель сообщения */
     CodeBuf = ПолучитьКодСубъекта( ReceiverID, PTCK_SWIFT, error );

     if( ReceiverID )
       bicdir.PartyID = ReceiverID;
       f_party.PartyID = ReceiverID;
     else 
       bicdir.PartyID = {OurBank};
       f_party.PartyID = {OurBank};
     end;
          
     if( getEQ( bicdir ))
       InstitutionName = bicdir.InstitutionName;
       City = bicdir.City;
     elif( getEQ( f_party ))
       ClearRecord(RecordAdress);
       НайтиЮридическийАдресСубъекта(f_party.PartyID,RecordAdress);
       InstitutionName = f_party.Name;
       City = RecordAdress.Place;
     else
       InstitutionName = " ";
       City = " ";
     end;
     [To          :       ########### ###################################
                                      ###################################](CodeBuf,InstitutionName,City);
     /* Дата сообщения */
     [Date of message: ##########](DateMessage:f);
     [______________________________________________________________________________];
     return TRUE;
   end;

   macro PrintFields( Translate )
      var count = 0, st;
      if ( (valtype(Translate)==V_UNDEF) OR (Translate==0) )
         st = ST_UNDEF;
      else 
         st = Standart;
      end;
      while( count < name.size )
        if( not РаспечататьПоле( name(count), value(count), st ) )
            MsgBox( "Ошибка при печати поля сообщения" + string(name(count)) );
            return FALSE;
        end;
        count = count + 1;
      end;
      return TRUE;
   end;

   /* Конец отчета */
   macro PrintFloor      
      std.RSBankFooter;
      println("");
      return TRUE;
   end;
   
   macro Initial( addrMes )
       SetBuff( wlmes, addrMes );
      
       FILE rls(wlmesrls) key 0 ;       
       FILE form(wlmesfrm) key 0 ;
      
       rls.RlsFormID = wlmes.RlsFormID;
       if(not GetEQ(rls))
         MsgBox("Ошибка поиска релиза формы сообщения N "+string(wlmes.RlsFormID));
         return FALSE;
       end;

       if ( substr(rls.Name,strlen(rls.Name)-strlen(ЗначениеRUR5)+1,strlen(ЗначениеRUR5))==ЗначениеRUR5 )
           Standart = ST_RUR5;
           NameStandart = ЗначениеRUR5;
       elif ( substr(rls.Name,strlen(rls.Name)-strlen(ЗначениеRUR4)+1,strlen(ЗначениеRUR4))==ЗначениеRUR4 )
           Standart = ST_RUR4;
           NameStandart = ЗначениеRUR4;
       elif ( substr(rls.Name,strlen(rls.Name)-strlen(ЗначениеRUR6)+1,strlen(ЗначениеRUR6))==ЗначениеRUR6 )
           Standart = ST_RUR6;
           NameStandart = ЗначениеRUR6;
       else
           Standart = ST_UNDEF;
           NameStandart = "";
       end;
      
       form.FormID = rls.FormID;
       if(not GetEQ(form))
         MsgBox("Ошибка поиска формы сообщения N "+string(rls.FormID));
         return FALSE;
       end;
      
       if( wlmes.Direct == "X" )   /* Входящее сообщение */
         SenderID = wlmes.OutsideAbonentID;
         ReceiverID = {OurBank};
       else                        /* Исходящее сообщение */
         SenderID = {OurBank};
         ReceiverID = wlmes.OutsideAbonentID;
       end;
      
       /* Печатаем заголовок */
       NumberForm = form.Name;
       NameForm = form.Description;
       DateMessage = wlmes.OutsideAbonentDate;
      
       ReadFields();
       return TRUE;      
   end;

   macro GetNameStandart()
      return NameStandart;
   end;

   /* Инициализация */
   name.size = 0;
   value.size = 0;
   stat = Initial( addrMes );
end;

const WITHOUT_TRANSLATE = 0,
      WITH_TRANSLATE    = 1;

/* Макрос печати форм */
macro PrintForm( addrMes, MassCopy )
  var Forma = TForm( addrMes );
  var MsgTmpFileName, OldName;
  file MsgTmpFile() txt;

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  if ( not Forma.PrintHeader() ) 
     return FALSE;
  end;

  if ( not Forma.PrintFields(WITHOUT_TRANSLATE) ) 
     return FALSE;
  end;

  [- - - - - - - - - - - - - - - - - СТАНДАРТ #### - - - - - - - - - - - - - - - ](Forma.GetNameStandart()); 

  if ( not Forma.PrintFields(WITH_TRANSLATE) ) 
     return FALSE;
  end;

  if ( not Forma.PrintFloor() ) 
     return FALSE;
  end;  

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;

  return TRUE;  
end;