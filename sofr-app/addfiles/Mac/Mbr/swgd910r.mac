/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям SWIFT MT910 стандарт RUR5          */
/*                                                                          */
/*  Имя файла: swgd910r.mac                                                 */
/*  Создан:     8.09.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgnrdoc.mac", "pmlib.mac";

record Pmpaym     ( pmpaym );
record Pmpropdeb  ( pmprop );
record Pmpropcred ( pmprop );
record Pmrmprop   ( pmrmprop );

FILE   OutMes(wlmes);

/* Подтверждение кредита */
class StatementMessage
 var
  standart         :integer,     /* Стандарт транслитерации */ 
  TRF              :string,      /* Ссылочный номер операции */
  RelatedRef       :string,      /* Ссылка */
  Account          :string,      /* Корсчет */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  OrdCustomer      :Customer,    /* Приказодатель */
  OrdBank          :Bank,        /* Банк Приказодателя */
  Intermediary     :Bank,        /* Посредник */
  RcvInfo          :TRcvInfo100, /* Информация Получателю поле 72 */

  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank;          /* Получатель */

  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;

end; /* class StatementMessage */

var MT910 : StatementMessage;

macro Fill20( TRF )
  MT910.TRF = TRF;
  MT910.standart = ДоопределитьСтандарт( TRF );
  return TRUE;
end;

macro Fill21( TRF )
  MT910.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT910.Account = Account;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT910.ValueDate = Date;
  MT910.Currency  = Cur;
  MT910.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill50F(Account, CodeKind, Code, Name)
  var INN = "";
  if(CodeKind == PTCK_INN)
    INN = Code;
  end;
  MT910.OrdCustomer = Customer( Name, Account, Code, INN, CodeKind); 
  return TRUE;
end;

macro Fill50K( Address, Account, System, Code, INN )
  StrRestoreStandart( Address, Address, ST_RUR6 );
  MT910.OrdCustomer = Customer( Address, Account, Code, INN );
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT910.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT910.standart );
  MT910.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT910.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT910.standart );
  MT910.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  StrRestoreStandart( Narrative, Narrative, MT910.standart );
  MT910.RcvInfo = TRcvInfo100( Str, Narrative );

  StrRestoreStandart( MT910.RcvInfo.Str, MT910.RcvInfo.Str, MT910.standart );  
  StrRestoreStandart( MT910.RcvInfo.NZP, MT910.RcvInfo.NZP, MT910.standart );
  return TRUE;
end;

/* Заполнение списка полей формы  MT910*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,     FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,               FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld25  = ТПолеФормы( AccountIdentificationField,          FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A,  FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld50F = ТПолеФормы( OrderingCustomerField_F,             FIELD_OPTIONAL,  "Read50F", "Fill50F", "" ),
    Fld50K = ТПолеФормы( OrderingCustomerField_K,             FIELD_OPTIONAL,  "ReadD",   "Fill50K", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,          FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,          FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),    
    Fld56A = ТПолеФормы( IntermediaryField_A,                 FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                 FIELD_OPTIONAL,  "ReadD",   "Fill56D", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,    FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMT910( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT910.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT910.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT910 */

macro GenDoc( addrMes )
  MT910 = NULL;
  MT910 = StatementMessage();

  var OutPaymentObj; // исходящее требование
  var InPaymentObj;  // входящий платёж по подтверждению об оплате требования
  var IsConfAccDem : bool;

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по МТ910");

  ClearRecord(wlconf);

  if( not ConstructMT910( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if( not ПолучитьФинИнПоISO( MT910.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT910.Currency );
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number       = MT910.TRF;
  wlconf.RelatedRef   = MT910.RelatedRef;
  wlconf.Account      = MT910.Account;  
  wlconf.FIID         = wlfininstr.FIID;
  wlconf.DateValue    = MT910.ValueDate;
  wlconf.Sum          = MT910.Sum;
  wlconf.DKFlag       = WL_CREDIT;
  if( MT910.RcvInfo.NZP != "" )
    wlconf.Description = MT910.RcvInfo.NZP;
  else
    wlconf.Description = substr( MT910.RcvInfo.Str, 1,215 );
  end;

  wlconf.TransferDate = wlconf.DateValue;//{curdate};
  
  if( not ОпределитьСхемуРасчетовПоКорсчету( wlconf.Corschem, wlmes.OutsideAbonentID, wlconf.Account, wlconf.FIID ) )
    return FALSE;
  end;    

  if ( MT910.OrdBank.IsSet() )
     wlconf.BankID    = MT910.OrdBank.PartyID;
     wlconf.CodeKind  = MT910.OrdBank.CodeKind;
     wlconf.CodeName  = MT910.OrdBank.CodeName;
     wlconf.CodeValue = MT910.OrdBank.CodeBank;
     wlconf.BankName  = MT910.OrdBank.Name;
  elif( MT910.Sender.IsSet() )
     wlconf.BankID    = MT910.Sender.PartyID;
     wlconf.CodeKind  = MT910.Sender.CodeKind;
     wlconf.CodeName  = MT910.Sender.CodeName;
     wlconf.CodeValue = MT910.Sender.CodeBank;
     wlconf.BankName  = MT910.Sender.Name;
  end;

  if ( MT910.Intermediary.IsSet() )
     wlconf.CorrBankID    = MT910.Intermediary.PartyID;
     wlconf.CorrCodeKind  = MT910.Intermediary.CodeKind;
     wlconf.CorrCodeName  = MT910.Intermediary.CodeName;
     wlconf.CorrCodeValue = MT910.Intermediary.CodeBank;
     wlconf.CorrBankName  = MT910.Intermediary.Name;
  end;

  /* Если ссылка на исходное сообщение не задана или не найдено исходное сообщение по этой ссылке или исходное сообщение 
     является исходящим акцептным требованием, то по сообщению также генерируется платеж */
  IsConfAccDem = IsConfirmAcceptDemand( wlconf.RelatedRef, wlconf.DateValue, wlconf.Sum, wlconf.FIID, wlconf.Corschem );
  if( (MT910.RelatedRef == "") or (not НайтиСообщениеПоНомеру(MT910.RelatedRef)) or (IsConfAccDem == true) )

    Pmpaym.Amount        = MT910.Sum;
    Pmpaym.PayAmount     = Pmpaym.Amount;
    Pmpaym.BaseAmount    = Pmpaym.Amount;
    Pmpaym.OrderAmount   = Pmpaym.Amount;
    Pmpaym.ValueDate     = {CurDate};
    Pmpaym.FIID          = wlfininstr.FIID;
    Pmpaym.PayFIID       = Pmpaym.FIID;
    Pmpaym.BaseFIID      = Pmpaym.FIID;
    Pmpaym.OrderFIID     = Pmpaym.FIID;

    if( MT910.OrdBank.IsSet() )
       Pmpaym.PayerBankID = MT910.OrdBank.PartyID;
    else
       Pmpaym.PayerBankID = wlmes.OutsideAbonentID;
    end;

    if( MT910.OrdCustomer.IsSet() )
      Pmpaym.Payer         = MT910.OrdCustomer.PartyID;
      Pmpaym.PayerCodeKind = MT910.OrdCustomer.CodeKind;
      Pmpaym.PayerCode     = MT910.OrdCustomer.Code;
      Pmpaym.PayerAccount  = MT910.OrdCustomer.Account;
    else
      Pmpaym.Payer         = MT910.OrdBank.PartyID;
      Pmpaym.PayerCodeKind = MT910.OrdBank.CodeKind;
      Pmpaym.PayerCode     = MT910.OrdBank.CodeBank;
      Pmpaym.PayerAccount  = MT910.OrdBank.Account;
    end;
    
    Pmpropdeb.TransferDate  = Pmpaym.ValueDate;
    Pmpropdeb.PayFIID       = Pmpaym.FIID;
    
    if( MT910.OrdBank.IsSet() )
      Pmpropdeb.CodeKind      = MT910.OrdBank.CodeKind;
      Pmpropdeb.BankCode      = MT910.OrdBank.CodeBank;
    else
      Pmpropdeb.CodeKind      = MT910.Sender.CodeKind;
      Pmpropdeb.BankCode      = MT910.Sender.CodeBank;
    end;
    
    Pmpropdeb.CorrCodeKind  = MT910.InterMediary .CodeKind;
    Pmpropdeb.CorrCode      = MT910.InterMediary.CodeBank;
    Pmpropdeb.CorrAcc       = MT910.InterMediary.Account;
    Pmpropdeb.CorrID        = MT910.InterMediary.PartyID;
    Pmpropcred.TransferDate = Pmpaym.ValueDate;
    Pmpropcred.PayFIID      = Pmpaym.FIID;

    Pmrmprop.Number         = MT910.TRF;
    Pmrmprop.Reference      = MT910.RelatedRef;
    Pmrmprop.PayDate        = Pmpaym.ValueDate;
    Pmrmprop.Date           = Pmpaym.ValueDate;
    Pmrmprop.ClientDate     = Pmpaym.ValueDate;
    
    if( MT910.OrdCustomer.IsSet() )
      Pmrmprop.PayerName    = MT910.OrdCustomer.Name;
      Pmrmprop.PayerINN     = MT910.OrdCustomer.INN;
    else
      Pmrmprop.PayerName    = MT910.OrdBank.Name;
    end;

    if( MT910.OrdBank.IsSet() )    
      Pmrmprop.PayerBankName      = MT910.OrdBank.Name;
      Pmrmprop.PayerCorrAccNostro = MT910.OrdBank.Account;
    else
      Pmrmprop.PayerBankName      = MT910.Sender.Name;
      Pmrmprop.PayerCorrAccNostro = MT910.Sender.Account;
    end;
    
    Pmrmprop.PayerCorrBankName  = MT910.Intermediary.Name;
    Pmrmprop.ReceiverCorrAccNostro = MT910.Account;
    Pmrmprop.Priority       = PM_DefaultMaxPriority();
    Pmrmprop.PartyInfo      = MT910.RcvInfo.REC;
    Pmrmprop.PaymentKind    = "Э";
    Pmrmprop.ProcessKind    = " 1";
    Pmrmprop.ShifrOper      = "01";
    Pmrmprop.ComissCharges  = PM_CHRG_OUR;
    Pmrmprop.Ground         = wlconf.Description;

    if( IsConfAccDem == true )
      Pmpaym.Receiver         = wlpmpaym.Receiver;
      Pmpaym.ReceiverCodeKind = wlpmpaym.ReceiverCodeKind;
      Pmpaym.ReceiverCode     = wlpmpaym.ReceiverCode;
      Pmpaym.ReceiverAccount  = wlpmpaym.ReceiverAccount;
      Pmpaym.ReceiverBankID   = wlpmpaym.ReceiverBankID;
      Pmpropcred.CodeKind     = wlpmpropcred.CodeKind;
      Pmpropcred.BankCode     = wlpmpropcred.BankCode;
      Pmrmprop.ReceiverBankName = wlpmrmprop.ReceiverBankName;
      Pmrmprop.ReceiverName   = wlpmrmprop.ReceiverName;
    else
      Pmpaym.Receiver         = -1;
      Pmpaym.ReceiverCodeKind =  0;
      Pmpaym.ReceiverCode     = "";
      Pmpaym.ReceiverAccount  = "";
      Pmpaym.ReceiverBankID   = {OurBank};
      Pmpropcred.CodeKind     = PTCK_SWIFT;
      Pmpropcred.BankCode     = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT );
      Pmrmprop.ReceiverBankName = {Name_Bank};
      Pmrmprop.ReceiverName   = "";
    end;

    Pmpropcred.Corschem = Pmpropdeb.Corschem = -1;
    Pmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, Pmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(Pmpropdeb.Corschem != -1)
      Pmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    if( not ВставитьПлатеж( Pmpaym, Pmpropdeb, Pmpropcred, Pmrmprop ) )
      std.msg("Ошибка при сохранении платежа");
      return FALSE;
    end;

    var PaymentObj = RsbPayment( Pmpaym.PaymentID );

    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT910.Sender.GetRoute(), true );
    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT910.OrdBank.GetRoute() );
    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT910.Intermediary.GetRoute() );

    if( PaymentObj.Update() )
      std.msg("Ошибка при сохранении платежа");
      return FALSE;
    end;

    if( IsConfAccDem == true )
      OutPaymentObj = RsbPayment(wlpmpaym.PaymentID); // требование
      InPaymentObj  = RsbPayment(Pmpaym.PaymentID);   // входящий платёж по подтверждению об оплате требования
      // Если исходное сообщение является исходящим акцептным требованием, то связать созданный платеж с этим требованием 
      // по виду связи PMLINK_KIND_KVITING
      if( OutPaymentObj.LinkPayment(InPaymentObj, PMLINK_KIND_KVITING) )
        std.msg("Ошибка при создании связи платежа");
        return FALSE;
      end;
      OutPaymentObj.Update();
    end;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вводе подтверждения");
    return FALSE;
  end;  

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
