/*
  $Name:         ufgd114.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация распоряжений на оплату по сообщениям УФЭБС ED114
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация распоряжений на оплату по сообщениям УФЭБС ED114               */
/*                                                                          */
/* Имя файла: ufgd114.mac                                                   */
/* Создан:    16.07.12                                       Chukina T.     */
/****************************************************************************/

import BankInter, PaymInter, OprInter, wluftool;

macro GenDoc (addrMes) 

  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация распоряжения на оплату по сообщению ED114" );

  var Order : RsbInCollectOrder = RsbInCollectOrder( 0 );
  Order.DocKind = Order.PrimDocKind = DLDOC_IN_PMCOL;
  Order.ShifrOper = "06";
  Order.PrimDocOrigin = PD_OR_AUTO;

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var tmpstr = "";

  var field_name, field_value;
  if( not СчитатьПоле( field_name, field_value ) )
    return FALSE;
  end;

  if ( not xml.loadXML(field_value) )
    println( string( "Неверный формат сообщения по форме ED114" ) );
    return FALSE;
  end; 

  var child:object, node:object = xml.DocumentElement;  
  if( xml.NodeType == DOCUMENT_NODE )
    var i = 0;
    while( i < xml.childNodes.length )
      child = xml.childNodes.item(i);
      if( child and (child.nodeType==CHILD_NODE) )
        node = child;
        break;
      end;
      i = i+1;
    end;
  end;
  if( IsChildNode(node, "DepartmentalInfo") )
    Order.TaxAuthorState = ReadAttribute(node, "DrawerStatus", "DepartmentalInfo", false);
    Order.BttTICode      = ReadAttribute(node, "CBC",          "DepartmentalInfo", false);
    Order.OKATOCode      = ReadAttribute(node, "OKATO",        "DepartmentalInfo", false);
    Order.TaxPmGround    = ReadAttribute(node, "PaytReason",   "DepartmentalInfo", false);
    Order.TaxPmPeriod    = ReadAttribute(node, "TaxPeriod",    "DepartmentalInfo", false);
    Order.TaxPmNumber    = ReadAttribute(node, "DocNo",        "DepartmentalInfo", false);
    Order.TaxPmDate      = ReadAttribute(node, "DocDate",      "DepartmentalInfo", false);
    Order.TaxPmType      = ReadAttribute(node, "TaxPaytKind",  "DepartmentalInfo", false);
  end;


  FillPayment_113_114(xml, wlmes, Order);

  if( Order.Update() != 0 )
    std.msg("Ошибка при вводе предъявленного инкассового поручения");
    return FALSE;
  end;

  /*Связь рублевого платежа РКО с входящим сообщением */
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = Order.PaymentID;
  meslnk.rec.ObjKind = OBJTYPE_PMCLAIM;
  var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
  if( (MesLnkObj.Insert( meslnk ) != 0) or (MesLnkObj.Save() != 0) )
    std.msg("Ошибка при создании связи предъявленного инкассового поручения с входящим сообщением");
    return FALSE;
  end;
  
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;  

end;