/*
  $Name:         ufgd508.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация квитанции по сообщению УФЭБС ED508
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация квитанции по сообщению УФЭБС ED508                             */
/*                                                                          */
/* Имя файла: ufgd508.mac                                                   */
/* Создан:    21.07.16                                           Чукина Т.А.*/
/****************************************************************************/
import "ufgendoc.mac", oralib, likepy;

private macro IsFinishCtrlInfo(blockname : string, fieldname : string) : bool
  if( (fieldname == endField) and (blockname == "SWIFTDocCtrlInfo") )
    return true;
  end;

  return false;
end;

private macro FinishReadingCtrlInfo
( CurrentField : TMesField, // текущее считанное поле блока SWIFTDocCtrlInfo
  SWIFTCtrlCode : @string,
  Annotation : @string,
  ErrList : RsbWlError
)

  var blockname : string = CurrentField.BlockName,
      fieldname : string = CurrentField.FieldName,
      fieldvalue : string = CurrentField.FieldValue,
      wlerrBuf : TRecHandler = TRecHandler("wlerror.dbt");

  while( not IsFinishCtrlInfo(blockname, fieldname) )
    if(fieldname == "SWIFTCtrlCode")
      SWIFTCtrlCode = fieldvalue;

    elif(fieldname == "Annotation")
      Annotation = fieldvalue;

    elif(fieldname == "SWIFTErrCode")
      if(wlerrBuf.rec.Code)
        // уже была считана предыдущая ошибка - надо ее вставить 
        // перед обработкой следующей
        if( ErrList.Insert( wlerrBuf ) != 0 )
          RunError( "Ошибка при создании записи об ошибке обработки сообщения" ); 
        end;

        // и подготовить буфер для новой записи
        wlerrBuf.Clear();
      end;

      wlerrBuf.rec.Code = fieldvalue;

    elif(fieldname == "SWIFTErrAnnotation")
      wlerrBuf.rec.Description = fieldvalue;

    end;

    if( not СчитатьПоле(fieldname, fieldvalue, blockname) )
      RunError("Неверная структура сообщения по форме ED508");
    end;
  end;

  // Вставляем последнюю прочитанную ошибку
  if(wlerrBuf.rec.Code)
    if( ErrList.Insert( wlerrBuf ) != 0 )
      RunError( "Ошибка при создании записи об ошибке обработки сообщения" ); 
    end;
  end;

end;

private macro KvitMesWithNegativeResult
( InitialMesID : integer, 
  TerminalSessionNum : string,
  ResultID : @integer
)
  var MesID : integer = 0, // Значения по умолчанию - для случая "сообщение не найдено"
      ErrCode : integer = 0,
      ErrDescription : string = "Не найдено исходное сообщение",
      State : integer = WLD_STATUS_RESPRM_DEFECT;

  if(InitialMesID > 0)
    // Значения для случая "сообщение найдено"
    MesID = InitialMesID;
    ErrCode = 2;
    ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_UFBS, ErrCode);
    State = WLD_STATUS_RESPRM_CLOSE;
  end;

  if( not ОшибкаЛогическогоКонтроляСообщения
      ( MesID, 
        ErrCode, 
        ErrDescription, 
        State, 
        true, 
        ResultID )
    )
    RunError( string("Ошибка при переносе в отбракованные сообщения ", TerminalSessionNum) ); 
  end;

end;

private macro AddInfoToErrList
( SWIFTCtrlCode : string,
  Annotation : string,
  TerminalSessionNum : string,
  ErrList : RsbWlError
)
  var wlerrBuf : TRecHandler = TRecHandler("wlerror.dbt");

  if(SWIFTCtrlCode or Annotation)
    wlerrBuf.Clear();
    wlerrBuf.rec.Code = SWIFTCtrlCode;
    wlerrBuf.rec.Description = Annotation;
    if( ErrList.Insert( wlerrBuf ) != 0 )
      RunError( "Ошибка при создании записи об ошибке обработки сообщения" ); 
    end;
  end;

  wlerrBuf.Clear();
  wlerrBuf.rec.Code = "TSNum";
  wlerrBuf.rec.Description = TerminalSessionNum;
  if( ErrList.Insert( wlerrBuf ) != 0 )
    RunError( "Ошибка при создании записи об ошибке обработки сообщения" ); 
  end;
end;

private macro RejectInitialMes
( InitialMesID : integer, 
  TerminalSessionNum : string,
  CurrentField : TMesField // текущее считанное поле блока SWIFTDocCtrlInfo
)

  var SWIFTCtrlCode : string = "",
      Annotation : string = "",
      ErrList : RsbWlError = RsbWlError(0);

  // Дочитываем оставшиеся данные в текущем блоке SWIFTDocCtrlInfo
  // и сохраняем их в SWIFTCtrlCode, Annotation, ErrList
  FinishReadingCtrlInfo(CurrentField, @SWIFTCtrlCode, @Annotation, ErrList);

  // Отбраковываем исходное сообщение
  var ResultID : integer = 0;
  KvitMesWithNegativeResult(InitialMesID, TerminalSessionNum, @ResultID);

  // Дозаполняем список ошибок квитанции
  AddInfoToErrList(SWIFTCtrlCode, Annotation, TerminalSessionNum, ErrList);

  // Сохраняем список ошибок квитанции в БД
  if( ErrList.TransferSelect( ResultID ) != 0 )
    RunError( "Ошибка при сохранении списка ошибок обработки сообщения" ); 
  end;
end;

private macro GetInitialMesByTrn(Trn : string) : integer
  return GetOutMesIdInOperDprt(TRANSP_UFBS, Trn);
end;

private macro GetInitialMesBySessAndRelatedRef
( SessionID : integer, 
  TerminalSessionNum : string

) : integer

  var MesID : integer = 0;

  var rs = execSQLselectPrm
    ( "select t_MesID "
      "  from dwlmes_dbt "
      " where t_SessionID = :SessionID "
      "   and t_RelatedRef = :TerminalSessionNum ",
      SQLParam("SessionID", SessionID),
      SQLParam("TerminalSessionNum", TerminalSessionNum)
    );

  if(rs and rs.moveNext())
    MesID = rs.value(0);
  end;

  return MesID;
end;

private macro ConfirmDelivery(InitialMesID : integer, DeliveryDate : date, State : integer)
  if(InitialMesID <= 0)
    InitialMesID = 0;
  end;

  var DeliveryCode : integer = 1;
  var Description : string = IfThenElse
    ( InitialMesID,
      Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_UFBS, DeliveryCode),
      "Не найдено исходное сообщение"
    );

  if( not ВставитьПодтверждениеОДоставке
      ( InitialMesID, 
        null, // NoChangeState
        DeliveryDate, 
        Description, 
        true, // CreateKvt,
        IfThenElse(InitialMesID, DeliveryCode, null),
        State,
        null, // DeliveryTime
        null, // CreateDate
        null, // CreateTime
        IfThenElse(InitialMesID, null, WLD_STATUS_RESPRM_DEFECT) // KvtState
      )
    )
    RunError( string("Не удалось поместить в доставленные сообщение ID = ", InitialMesID) ); 
  end;

  if( (State == WLD_STATUS_MES_CLOSED) and (InitialMesID > 0) )
    var InfoID : integer = GetObjIDByMesLink(InitialMesID, OBJTYPE_INFO, "");

    if( WldChangeStatus(InfoID, OBJTYPE_INFO, WLD_STATUS_INFO_CLOSED) != 0 )
      RunError("Ошибка при смене статуса исходного информационного сообщения");
    end;
  end;
end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация квитанции по сообщению ED508" );

  var HasSWIFTDocCtrlInfo : bool = false;
  if( WlMes_GetFieldValue(wlmes.MesID, beginField, "SWIFTDocCtrlInfo") )
    HasSWIFTDocCtrlInfo = true;
  end;

  var blockname : string = "",
      fieldname : string = "",
      fieldvalue : string = "",
      DeliveryDate : date = date(0, 0, 0),
      InitialMesID : integer = 0,
      SessionID : integer = 0,
      errmes : string = "",
      IsFinish : bool = false;

  while( not IsFinish and СчитатьПоле(fieldname, fieldvalue, blockname) )

    if( (fieldname == "EDDate") and (blockname == "EDRefID") )
      DeliveryDate = ToDate(fieldvalue);
      errmes = "Не найдено исходящее сообщение " + wlmes.RelatedRef + " за дату " + 
               DeliveryDate;

      if(not HasSWIFTDocCtrlInfo)
        InitialMesID = GetInitialMesByTrn(wlmes.RelatedRef);

        if(InitialMesID > 0)
          ConfirmDelivery(InitialMesID, DeliveryDate, WLD_STATUS_MES_CLOSED);
          IsFinish = true;
        else
          RunError(errmes, errmes);
        end;
      else
        SessionID = WldFindSessByUID( TRANSP_SWIFT, date(0,0,0), wlmes.RelatedRef );

        if(SessionID <= 0)
          RunError(errmes, errmes);
        end;
      end;
    end;

    if(fieldname == "TerminalSessionNum")
      var TerminalSessionNum : string = fieldvalue;
      InitialMesID = GetInitialMesBySessAndRelatedRef(SessionID, TerminalSessionNum);

      if(InitialMesID <= 0)
        errmes = "Не найдено исходное сообщение с TerminalSessionNum = " + 
                 TerminalSessionNum;
        CreateWarning(CWM_NOPREFIX, WLD_WARN_MACRO, errmes);
      end;

      if( СчитатьПоле(fieldname, fieldvalue, blockname) )
        // Если после TerminalSessionNum нет других полей до конца блока SWIFTDocCtrlInfo
        if( IsFinishCtrlInfo(blockname, fieldname) )
          ConfirmDelivery(InitialMesID, DeliveryDate);
        else
          var CurrentField : TMesField = TMesField(fieldname, fieldvalue, blockname);
          RejectInitialMes(InitialMesID, TerminalSessionNum, CurrentField);
        end;
      else
        RunError("Неверная структура сообщения по форме ED508");
      end;
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
                       
