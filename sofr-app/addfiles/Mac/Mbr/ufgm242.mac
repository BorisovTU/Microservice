/*
  $Name:         ufgm242.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED242 транспорта УФЭБС
*/
/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED242 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm242.mac                                                  */
/*  Создан:    05.03.09                                                     */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac";

private const 
      code10 = "/ПП10/", 
      code21 = "/ПП21/", 
      code22 = "/ПП22/",
      code23 = "/ПП23/",
      code24 = "/ПП24/",
      code25 = "/ПП25/",
      code26 = "/ПП26/",
      code30 = "/ПП30/",
      code40 = "/ПП40/",
      code41 = "/ПП41/";
      
private const 
      code1 = "/RRICode1/",
      code2 = "/RRICode2/",
      code3 = "/RRICode3/",
      code4 = "/RRICode4/";

macro GenMes( addrMes, addrReq )

  var field_value, TypeCode, SessionID = 0, MesForm, IsPacket, InitMesID = 0, rs, SessUID, RepeatReceptInqCode;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object,elem:object,;
  var ixml:object = ActiveX("Microsoft.XMLDOM");  
  var strXml;

  mes = xml.createElement("ED242");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  
  var data;
  var rs2 = execSQLSelect( "Select DBMS_LOB.SUBSTR(t_fmtblobdata_xxxx, 1000, 1) from dwlreq_dbt where t_ReqID = :ReqID ", MakeArray(SQLParam("ReqID", wlreq.reqID)));
  
  if( rs2 and rs2.moveNext())
    data = rs2.value(0);
  end;
  if( Index(wlreq.Queries, code1 ))
    RepeatReceptInqCode = "1";
  elif( Index(wlreq.Queries, code2 ))
    RepeatReceptInqCode = "2";
    var SubPos = Index(data, "/SessID");
    if( (SubStr(data, SubPos, 1) == "/") and (SubStr(data, SubPos+8, 1) == "/"))
      SessionID = int(SubStr(data, SubPos+7, 1));
    end;
  elif( Index(wlreq.Queries, code3 )) 
    RepeatReceptInqCode = "3";
  elif( Index(wlreq.Queries, code4 ))
    RepeatReceptInqCode = "4";
  end;

  mes.setAttribute("RepeatReceptInqCode",  RepeatReceptInqCode);

  if((RepeatReceptInqCode == "2") or (RepeatReceptInqCode == "3"))
    if( wlreq.InitFormMes )
      mes.setAttribute("EDTypeNo", wlreq.InitFormMes );
    elif(RepeatReceptInqCode == "3")
      RunError("Ошибка: Поле 'Форма исходного сообщения' должно быть заполнено");     
    end;
  end;
    
  //Элемент отсутствует, если не задан номер рейса(SessionID)
  if( RepeatReceptInqCode == "2")
    if( (SessionID >= 1) and (SessionID <= 3) )
      elem = xml.createElement("Session");
      var elem2 = xml.createElement("SessionID");
      elem2.appendChild(xml.createTextNode(SessionID));
      elem.appendChild(elem2);
      mes.appendChild(elem);
    else
      RunError("Выберите значение из справочника в поле 'Дополнительная информация'");
    end;
  end;
  
  if( RepeatReceptInqCode == "1" )
    if( wlreq.RelatedRef == "" )
      RunError("Ошибка: Поле 'Ссылка' должно быть заполнено");     
    end;
    if( strlen(wlreq.RelatedRef) < 11 )
      RunError("Ошибка: Поле 'Ссылка' для входящего сообщения| должно быть 'EDAuthor'+'EDNo'");     
    end;

    elem = xml.createElement("EDRefID");    

    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn(wlmes.RelatedRef); 

    elem.setAttribute("EDNo",       ed_nda.EDNo );
    elem.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
    elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);  
  elif( RepeatReceptInqCode == "4" )
    if( wlreq.RelatedRef == "" )
      RunError("Ошибка: Поле 'Ссылка' должно быть заполнено");     
    end;

    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn(wlmes.RelatedRef); // EDNo сообщения по ссылке

    elem = xml.createElement("EDRefID");
    elem.setAttribute("EDNo", ed_nda.EDNo );
    elem.setAttribute("EDDate", YYYYMMDD(ed_nda.EDDate));
    elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;
   
  var IndexCutStart = 0, IndexCutEnd = 0, ARMNo, Temp;
  if  ( Index(wlReq.Queries, "ARMNo:") )
    
    IndexCutStart = Index(wlReq.Queries, "ARMNo:");
    
    Temp = SubStr( wlReq.Queries, IndexCutStart + 6); 
    
    if( IndexCutEnd = Index(Temp, "\n") )
      Temp = SubStr( Temp, 1, IndexCutEnd);
    end;
    ARMNo = Trim( Temp );
    
    if( (StrLen( ARMNo ) == 2) AND (StrIsNumber( ARMNo ) == 0) )
      mes.setAttribute("ARMNo", ARMNo );
    else
      MsgBoxEx( "Неверно задан номер АРМ получателя (отправителя). Поле ARMNo сообщения заполнено не будет", 0);
    end;
  end;
   
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
