/*
  $Name:         mnsgmsbc.mac
  $Module:       МБР
  $Description:  Генерация сообщения SBC
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SBC                                               */
/*                                                                          */
/*  Имя файла: mnsgmsbc.mac                                                 */
/*  Создан:  02.12.04                                    Фомченкова Л.Н.    */
/****************************************************************************/

import "wlgenmes.mac", "wlmnstls.mac", OprInter, oralib, likepy, PSInter, "fnsMesAccCommon.mac";

record Налогоплательщик(party);
record reqopena(reqopena);
record reqclosa(reqclosa);
record GenAcc(account);


FILE dp_dep( dp_dep ) key 0;
FILE party( party ) key 0;
FILE typeac( typeac ) key 0;
FILE persn( persn ) key 0;

private const TA_SACNT  = 1;
private const TA_SACNTC = 4;
private const NATCUR = 0;

macro GenMes( addrMes, buff, Открытие, addrOldMes, vers )
  Var RespID, Error, ИдПол, Адрес, Год, ИНННП, ОГРННП, КодНОНП, ИДГНИКлиента, КодВидаСчета, ОГРНКО,
      Серия = "" , Номер = "", ДатаРегистрации = date(), Счет, Валюта,
      ФДЛБанка, ИННКО = "", КППКО = "", НаимКО, КодСостДог, ИНН, 
      ФИОПрБ, НомФ, КППНП = " ", ФИОИП = "", СвидГР, СвидНУ, Нотариус, Адвокат,OldfnsMessageID,ВидРегОргана = 0,  БИК;
  record OldMes(wlmes);

 /* 
  #139530  
  if(not IsMacroInOperation())
     RunError(string("|Данное сообщение МНС генерируется только на шаге операции"));
  end;
*/

  SetBuff( wlmes,  addrMes );
  SetBuff( OldMes,  addrOldMes );

  var IsRecreate:bool = OldMes.MesID!=0; //Перегенерация?

  PrintLog(2,"Генерация сообщения по SBC");
  ПолучитьСчетСВалютой(buff, Открытие, Счет, Валюта );
  if((not Открытие))
     ПолучитьСчет(Валюта, Счет, GenAcc );
  else
     GenAcc.Client = buff.ClientID;
     GenAcc.Open_Date = buff.Date;
     GenAcc.Type_Account = buff.Type_Account;
     GenAcc.Department = buff.AccountDepartment;
  end;

  var AccMsgKind : integer = IfThenElse(Открытие, ACCMSG_KIND_OPEN, ACCMSG_KIND_CLOSE);
  var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg(GenAcc.Department, AccMsgKind, vers);

  dp_dep.Code = GenAcc.Department;
  GetEQ( dp_dep );
  party.PartyID = dp_dep.PartyID;
  GetEQ( party );

  DateSplit({curdate}, NULL, NULL, Год);
  /*ИдФайл*/
  ЗаписатьПолеЛог( "ИдФайл", CommonSBC.ИдФайл );
  /*ТипИнФ*/
  ЗаписатьПолеЛог( "ТипИнф", CommonSBC.ТипИнф );

  /*ВерсПрог*/
  ЗаписатьПолеЛог( "ВерсПрог", CommonSBC.ВерсПрог);
  /*ТелОтпр*/
  ЗаписатьПолеЛог( "ТелОтпр", CommonSBC.ТелОтпр );
  /*ДолжнОтпр*/
  ЗаписатьПолеЛог( "ДолжнОтпр", CommonSBC.ДолжнОтпр );
  /*ФИООтпр*/
  ЗаписатьПолеЛог( "ФамОтпр", CommonSBC.ФамОтпр );
  /*КолДок*/
  ЗаписатьПолеЛог( "КолДок", CommonSBC.КолДок );
  /*ВерсФорм*/
  ЗаписатьПолеЛог( "ВерсФорм", CommonSBC.ВерсФорм );
  /*НомСооб*/
  if( vers == 501 )
    Error = not GetMessageNumber(party.PartyID, Счет, "", Валюта, -1, Серия, Номер, IsRecreate, IfThenElse( Открытие == 1, true, false ) );  
  elif( vers == 502 )
    Error = not GetMessageNumber502(party.PartyID, Счет, "", 1, Валюта,  GenAcc.Client, Серия, Номер, IfThenElse( IsRecreate == true, OldMes.MesID, 0) );  
  end; 
  var ErrText:string;
  if(Error)
    ErrText = GetErrMsg();
    if( not ErrText )
      ErrText = "Ошибка при генерации номера сообщения";
    end;
    RunError( ErrText );
  end;

  ЗаписатьПолеЛог( "НомСооб", string(Серия,",",Номер) );

  /*ИдДок*/
  ЗаписатьПолеЛог( "ИдДок", CommonSBC.ИдДок);  
  /*ДатаСооб*/
  ЗаписатьПолеЛог("ДатаСооб", CommonSBC.ДатаСооб);
  ЗаписатьПолеЛог( "КодНОБ", CommonSBC.КодНОБ );
  ЗаписатьПолеЛог( "ДолжнПрБ", CommonSBC.ДолжнПрБ );
  ЗаписатьПолеЛог( "ТелБанка", CommonSBC.ТелБанка );

  if(Открытие)
    ЗаписатьПоле_ДатаЗаклДог( buff.date, buff.unioncontrid, Счет, Валюта, buff );
  else
    ЗаписатьПоле_ДатаЗакрСч(GenAcc.Close_Date);
  end;

  if( GetKindActionFNS() !=  KIND_ACTION_FNS_NULL )
      
    /*НаимКО*/
    НаимКО = ПолучитьНаимКо(dp_dep.Code);
    ЗаписатьПолеЛог("НаимКО",НаимКО);
    /*АдрКО*/
    Адрес = ПолучитьАдрес(party.PartyID);
    
    if(Адрес != "")
       ЗаписатьПолеЛог( "АдрКО", Адрес );
    end;
    /*РегНомКО*/
    ЗаписатьПолеЛог("РегНомКО", string(int(SubStr(Серия,1,4))));
    /*НомФ*/
    if( not НашБанкГоловной(party.PartyID) )
      НомФ = string(int(SubStr(Серия,5,4)));
      ЗаписатьПолеЛог( "НомФ", НомФ );
    else
      ЗаписатьПолеЛог( "НомФ", " " );
    end;
    /*БИК*/
    БИК = ПолучитьКодСубъекта( party.PartyID, PTCK_BIC, Error, 1, 1 );
    ЗаписатьПолеЛог( "БИК", БИК );

    /*ИННКО*/
    ПроверитьИНН();
    ИННКО = RemoveKPP( GetPartyINN(party.PartyID, 1) );
    ЗаписатьПолеЛог( "ИННКО", ИННКО );
    /*КППКО*/
    КППКО = RemoveINN( GetPartyINN(party.PartyID, 1) );
    ЗаписатьПолеЛог( "КППКО", КППКО );
    
    /*ОГРНКО*/
    ОГРНКО = ПолучитьКодСубъекта( party.PartyID, PTCK_OGRN, Error, 0, 1 );
    ЗаписатьПолеЛог( "ОГРНКО", ОГРНКО );

    
    /*НаимНП*/
    /*ФИОИП*/
      ПолучитьСубъекта(GenAcc.Client, Налогоплательщик);
    ИНН = ПолучитьКодСубъекта(Налогоплательщик.PartyID, PTCK_INN, Error);
    ИНННП = RemoveKPP(ИНН);
    КППНП = RemoveINN(ИНН);
    
    if(strlen( ИНННП ) != 12)  
      ЗаписатьПолеЛог("НаимНП",Налогоплательщик.Name);
    else
      ClearRecord(persn);
      persn.PersonID = Налогоплательщик.PartyID;
      if( getEQ(persn) )
        ФИОИП =  string( persn.Name1 + "," + persn.Name2 + "," + persn.Name3);
        ЗаписатьПолеЛог("ФИОИП", ФИОИП);
      end;
    end;
    
    /*СвидГР*/
    Серия = "";
    Номер = "";
    СвидГР = "";
    CheckObjAttrPresence( Нотариус, OBJTYPE_PARTY, UniID( Налогоплательщик, OBJTYPE_PARTY ), 
                          PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Нотариус ); 
    CheckObjAttrPresence( Адвокат,  OBJTYPE_PARTY, UniID( Налогоплательщик, OBJTYPE_PARTY ), 
                          PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Адвокат );
                          
    ИДГНИКлиента = ПолучитьИДГНИ(Налогоплательщик.PartyID, Серия, Номер, ДатаРегистрации, СвидетельствоГосРегистрации );
    if( (Серия != "") and (Номер != "") and (Налогоплательщик.NotResident != "X") and 
        (not Нотариус) and (not Адвокат) // не является нотариусом или адвокатом
      )
      СвидГР =  trim(reqopena.ClientSertif);
    end;
    if(СвидГР != "")
       ЗаписатьПолеЛог( "СвидГР", СвидГР);
    else
       ЗаписатьПолеЛог( "СвидГР", " ");
    end;

    /*СвидНУ*/
    Серия = "";
    Номер = "";
    СвидНУ = "";
    ИДГНИКлиента = ПолучитьИДГНИ(Налогоплательщик.PartyID, Серия, Номер, ДатаРегистрации, СвидетельствоОПостановкеНаУчет );
    if( (Серия != "") and (Номер != "") )
      СвидНУ =  trim(reqopena.ClientSertifReg);
    end;

    if(СвидНУ != "")
       ЗаписатьПолеЛог( "СвидНУ", СвидНУ);
    else
       ЗаписатьПолеЛог( "СвидНУ", " ");
    end;

    /*ИНННП*/
        ЗаписатьПолеЛог( "ИНННП", ИНННП );
    /*КППНП*/
    if(StrLen(ИНННП) != 12) // клиент - юридическое лицо
      ЗаписатьПолеЛог( "КППНП", IfThenElse( КППНП != "", КППНП, " " ) );
    end;
    if(StrLen(ИНННП) == 12)                // клиент - физическое лицо
      ЗаписатьПолеЛог( "КППНП", " " );
    end;

    /*ОГРН*/
    if( (Налогоплательщик.NotResident != "X") and (not Нотариус) and (not Адвокат) ) // не является нерезидентом, нотариусом или адвокатом
      ОГРННП = pyOR( ПолучитьКодСубъекта( Налогоплательщик.PartyID, PTCK_OGRN, Error, 0, 1 ), " " );
      ЗаписатьПолеЛог( "ОГРННП", ОГРННП );
    else
      ЗаписатьПолеЛог( "ОГРННП", " ");
    end;

    /*КодСостДог*/
    if(vers == 501)
      if(Открытие)
         КодСостДог = 1;
      else
         КодСостДог = 0;
      end;
    else
      if(Открытие)
         КодСостДог = 0;
      else
         КодСостДог = buff.SubKind;
      end;
    end;
    ЗаписатьПолеЛог( "КодСостДог", string(КодСостДог));
    
    if(not Открытие)
      var SfCntr : TRecHandler = TRecHandler("sfcontr.dbt"); // договор обслуживания для счета
      var IsSfContr : bool = ( ПолучитьДоговорОбслуживания(Валюта, Счет, SfCntr) == 0 );
       //ДатаРастДог
       ЗаписатьПоле_ДатаРастДог( buff, IfThenElse(IsSfContr, SfCntr, null), vers );    
       //НомерДог
       if(SfCntr.rec.Number != "")
         ЗаписатьПолеЛог( "НомерДог", SfCntr.rec.Number );
       else
         ЗаписатьПолеЛог( "НомерДог", " " );
       end;
    else 
       if( vers == 502 )
         /*ДатаРастДог*/
         ЗаписатьПолеЛог( "ДатаРастДог", " ");
       end;
       /*НомерДог*/
         ЗаписатьПолеЛог( "НомерДог", buff.SFContrNum );
       end;

    /*КодСостСч*/
    ЗаписатьПолеЛог( "КодСостСч", string(Открытие));
    /*ВалСч*/
    if( Валюта == NATCUR )
       КодВидаСчета = "0";
       typeac.iNumType = TA_SACNT;
    else
       КодВидаСчета = "1";
       typeac.iNumType = TA_SACNTC;
    end;
    ЗаписатьПолеЛог("ВалСч", КодВидаСчета);
    /*ВидСч*/
    typeac.Type_Account = GenAcc.Type_Account;
    if(getEQ( typeac ))
      ЗаписатьПолеЛог("ВидСч",typeac.Name_Type);
    end;

    //ДатаОткрСч
    var OpenDate:date = date(0,0,0);
    var NoteChToStlm = "";
    var CloseDate:date = date(0,0,0);
    if(Открытие)
      if(IsRecreate)
        NoteChToStlm = ReadNoteForAccount(Счет, Валюта, 42); //42 = ACC_NOTE_KIND_DATE_CHANGE_TO_SETTLEMENT
        if(NoteChToStlm != "") 
          OpenDate = date(StrSubst(NoteChToStlm, " ", "0"));
        else
          if( GenAcc.Open_Date != date(0,0,0) )
            OpenDate = GenAcc.Open_Date;
          else
            OpenDate = {curdate};
          end;
        end;
      else  
        OpenDate = GenAcc.Open_Date;
      end;

      ЗаписатьПолеЛог( "ДатаОткрСч", DDpMMpYYYY(OpenDate) );
    else
      ЗаписатьПолеЛог( "ДатаОткрСч", DDpMMpYYYY(GenAcc.Open_Date) );
    end;

    /*НомСч*/
    ЗаписатьПолеЛог( "НомСч", Счет );

    /*ФИОПрБ*/
    ФИОПрБ = "";
    Error = GetRegValForOPENAC("ИМНС_ФИО", V_STRING, ФИОПрБ);
      ФИОПрБ = РазделитьЗапятымиФИО( ФИОПрБ );
    ЗаписатьПолеЛог( "ФИОПрБ", ФИОПрБ );

    if(CommonSBC._ВидРегОргана)
      ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
    end;

    if(CommonSBC._СпОбм)
      ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
    end;

  else
    OldfnsMessageID = GetOldfnsMessageID();
    ЗаписатьПолеДляАннул( "НаимКО"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "АдрКО"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "РегНомКО"   , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомФ"       , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "БИК"        , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ИННКО"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КППКО"      , OldfnsMessageID );    
    ЗаписатьПолеДляАннул( "ОГРНКО"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НаимНП"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ФИОИП"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "СвидГР"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "СвидНУ"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ИНННП"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КППНП"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ОГРННП"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КодСостДог" , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ДатаРастДог", OldfnsMessageID);
    ЗаписатьПолеДляАннул( "НомерДог"   , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КодСостСч"  , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ВалСч"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ВидСч"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ДатаОткрСч" , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомСч"      , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ФИОПрБ"     , OldfnsMessageID );
    ЗаписатьПолеДляАннул( "_СпОбм"     , OldfnsMessageID );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
/*
macro CheckObjC( addrReq )
   SetBuff(reqclosa, addrReq);
   return MNS_CheckObj( reqclosa, ACCMSG_KIND_CLOSE );
end;

macro CheckObjO( addrReq )
   SetBuff(reqopena, addrReq);
   return MNS_CheckObj( reqopena, ACCMSG_KIND_OPEN );
end;
*/

macro GenMesO( addrMes, addrReq, addrOldMes )
   SetBuff(reqopena, addrReq);
   return GenMes(addrMes, reqopena, 1, addrOldMes, 501);
end;


macro GenMesC( addrMes, addrReq, addrOldMes )
   SetBuff(reqclosa, addrReq);
   return GenMes(addrMes, reqclosa, 0, addrOldMes, 501);
end;

macro GenMesO502( addrMes, addrReq, addrOldMes )
   SetBuff(reqopena, addrReq);
   return GenMes(addrMes, reqopena, 1, addrOldMes, 502);
end;


macro GenMesC502( addrMes, addrReq, addrOldMes )
   SetBuff(reqclosa, addrReq);
   return GenMes(addrMes, reqclosa, 0, addrOldMes, 502);
end;
