/*
  $Name:         wlexpost.mac
  $Module:       Межбанковские расчеты
  $Description:  Постобработка файла экспорта
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                     Постобработка файла экспорта                         */
/*                                                                          */
/*  Имя файла: wlexpost.mac                                                 */
/*  Создан:  5.03.2003  Andrew B. Panov                                     */
/****************************************************************************/
import BankInter, MesInter, oralib, likepy, PSInter, ServiceAction, WldInter;
import fileop;         /* Объект для работы с файлами на сервере и терминале   */
import rsexts;
file OutFile("") txt write;
import "wlmnstls.mac", "pm_const.mac", "wlofkggtools.mac";
/* Все нормально, перенос файла экспорта в      */
/* директорию экспорта происходит автоматически */
const WLD_POSTEXPORT_OK           =  0;

/* Все нормально, но перенос файла экспорта в       */
/* директорию экспорта производится пользователем в */
/* данном макросе                                   */
const WLD_POSTEXPORT_OK_STOP      = -1;

/* Все нормально, перенос файла экспорта в                               */
/* директорию экспорта происходит автоматически.                         */
/* Содержимое временного файла изменилось (все изменения проводить       */
/* в файле с именем, переданным в макрос в качестве параметра FileName). */
/* При необходимости изменения имени файла экспорта                      */
/* требуется создать копию временного файла с нужным именем              */
const WLD_POSTEXPORT_CHANGE       = -2;

/* Все нормально, но перенос файла экспорта в                            */
/* директорию экспорта производится пользователем в                      */
/* данном макросе.                                                       */
/* Содержимое временного файла изменилось (все изменения проводить       */
/* в файле с именем, переданным в макрос в качестве параметра FileName). */
/* При необходимости изменения имени файла экспорта                      */
/* требуется создать копию временного файла с нужным именем.             */
const WLD_POSTEXPORT_CHANGE_STOP  = -3;

/*Ошибка*/
const WLD_POSTEXPORT_ERROR        =  1;

const TRANSP_SPFSCBR = 51; //А.Киселев 29.03.2019 пользовательский транспорт СПФСЦБРФ

/* Внимание! Использовать WLD_POSTEXPORT_OK_STOP, WLD_POSTEXPORT_CHANGE_STOP  */
/* ТОЛЬКО в том случае, если требуется перенос файла экспорта в нестандартную */
/* директорию.                                                                */

/****************************************************************************/
/*Для примера - архивация файла экспорта                                    */
/****************************************************************************/
macro RarExport1(FileName, NewFileName)
  var cmd, dir, fileN, ext, error, arcName;
  
  dir =/*"D:\\RSHB_SOFR\\PUBLIC\\OUT\\OUT_BANK_MESSAGE" +*/ SplitFile(FileName, fileN, ext);
  
  /*Имя архивного файла.*/
  arcName = dir + fileN + ".rar";    

  /*Установим новое полное(dir+fname+ext) имя файла экспорта*/
  NewFileName = arcName;
  if (SetParm(1, NewFileName) == false)
    return WLD_POSTEXPORT_ERROR;
  end;

  /*Командная строка для архиватора*/
  cmd = "a -ep " + arcName + " " + FileName; 

  /*Архивируем*/
  error = Run("rar.exe", cmd);
  if (error == 0)
    return WLD_POSTEXPORT_OK;
  else 
    return WLD_POSTEXPORT_ERROR;
  end;
end;

/*PNV i-support 487319 - преименовать файл для TELEX*/
private macro GetNewFileName( ObjId, ObjKind, filename_ ) :string
var Select, DataSet,  Query :string = "",
    filename = filename_,
    partyname = "",
    MesCode = "",
    Dealcode = "",
    FinCode = "",
    DealDate = "";

  if( ( ObjKind == 148 ) or ( ObjKind == 145 ) ) //сделки ФИСИКО
  Query = " SELECT distinct case when DIR.T_NOTRESIDENT = CHR(0) then Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^а-яА-Я ]' ), ' ', '-'), 0, 15) else Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^a-zA-Z ]' ), ' ', '-'), 0, 15) end partyname, " +
           "         REGEXP_REPLACE( deal.t_code, '\\D') dealcode, " +
           "    (SELECT t_ccy " +
           "          FROM DFININSTR_DBT " +
           "         WHERE t_fiid = (SELECT t_fiid " +
           "                           FROM DDVNFI_DBT " +
           "                          WHERE t_dealid = deal.T_ID AND t_type = 0)) cur " +
           "  FROM ddvndeal_dbt deal, dparty_dbt DIR " +
           " WHERE deal.T_ID = ? and deal.T_CONTRACTOR =  DIR.t_partyid ";

  elif( ( ObjKind == 103 ) )  //сделки МБК
   Query = " SELECT distinct case when DIR.T_NOTRESIDENT = CHR(0) then Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^а-яА-Я ]' ), ' ', '-'), 0, 15) else Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^a-zA-Z ]' ), ' ', '-'), 0, 15) end partyname, " +
           "                     REGEXP_REPLACE( deal.t_dealcode, '\\D') dealcode, " +
           "                (SELECT t_ccy  " +
           "                      FROM DFININSTR_DBT  " +
           "                     WHERE t_fiid = (SELECT t_pfi  " +
           "                                       FROM ddl_leg_dbt  " +
           "                                      WHERE t_dealid = deal.T_dealID AND t_legkind = 0)) cur, DEAL.T_BOFFICEKIND, DIR.T_Name " +
           "                                      FROM ddl_tick_dbt deal, dparty_dbt DIR " +
           "             WHERE T_DEALID = ? AND deal.T_partyid =  DIR.t_partyid and DEAL.T_BOFFICEKIND = 102 ";

  elif( ( ObjKind == 108 ) ) //сделки НЕТТИНГА
   Query = "   SELECT distinct case when DIR.T_NOTRESIDENT = CHR(0) then Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^а-яА-Я ]' ), ' ', '-'), 0, 15) else Substr(Replace(REGEXP_REPLACE (DIR.T_Name, '[^a-zA-Z ]' ), ' ', '-'), 0, 15) end partyname, " +
           "                       REGEXP_REPLACE( deal.t_dealnumber, '\\D') dealcode, " +
           "                  (SELECT t_ccy  " +
           "                        FROM DFININSTR_DBT  " +
           "                       WHERE t_fiid = (SELECT t_fiid  " +
           "                                         FROM DDVNFI_DBT  " +
           "                                        WHERE t_dealid = deal.T_nettingID AND t_type = 0)) cur " +
           "                                         FROM ddl_nett_dbt deal, dparty_dbt DIR " +
           "               WHERE deal.T_contractor =  DIR.t_partyid and T_NETTINGID = ? " ;
     return filename;
  end;

  Select = RSDCommand( query );
  Select.AddParam("ObjId", RSDBP_IN, ObjId ); 
  Select.Execute();
  DataSet = TRsbDataSet(Select);

  if (DataSet.MoveNext())
      partyname = DataSet.partyname;
      partyname = StrSubst(partyname, "ООО-", "");
      partyname = StrSubst(partyname, "ПАО-", "");
      partyname = StrSubst(partyname, "АО-", "");
      partyname = StrSubst(partyname, "АКБ-", "");
      partyname = StrSubst(partyname, "КБ-", "");
      StrChangeRusXSet( partyname, partyname);
      Dealcode = DataSet.dealcode;
      FinCode = DataSet.cur;
  end;
  if( ( ObjKind == 103 ) )  //сделки МБК
        Query = "  SELECT Rsl.t_name, B.T_BANKDATE FROM dwlmes_dbt B, DWLMESRLS_DBT Rsl  " +
                "   WHERE B.T_MESID =  " +
                "  (select A.T_MESID  from  dwlmeslnk_dbt A, dwldlmm_dbt wl where  A.T_OBJID = wl.T_DLMMID and A.T_OBJKIND = 512 and WL.T_DEALID = ?  ) "+
                "    and RSL.T_RLSFORMID = B.T_RLSFORMID";
        Select = RSDCommand( query );
        Select.AddParam("ObjId", RSDBP_IN, ObjId ); 
  else
        Query = " SELECT Rsl.t_name, B.T_BANKDATE FROM dwlmes_dbt B, DWLMESRLS_DBT Rsl  "+
                "  WHERE B.T_MESID =  (select T_MESID  from  dwlmeslnk_dbt A where  A.T_OBJID = ? and A.T_OBJKIND = ? ) "+
                " and RSL.T_RLSFORMID = B.T_RLSFORMID ";
        Select = RSDCommand( query );
        Select.AddParam("ObjId", RSDBP_IN, ObjId ); 
        Select.AddParam("ObjKind", RSDBP_IN, ObjKind ); 
        Select.Execute();
  end;

  DataSet = TRsbDataSet(Select);
  if (DataSet.MoveNext())
     MesCode = "mt" + DataSet.name;
     DealDate = Trim(StrSubst(String(Date(DataSet.BANKDATE):f), ".", ""));
  end;
  if (partyname != "")
      filename = partyname + "-" + MesCode + "-" + DealDate + "-" + FinCode + "-" + Dealcode;
  end; 
  return filename;

onError(err)

 return filename;
end;
/*PNV*/

//А.Киселев 01.04.2019 каталог экспорта.
private macro GetFullPathExportTpFMT( TpId :integer, Dep :integer ) :string
var Params :TArray,
    DataSet :RsdRecordset,
    Query :string = "",
    StrBuf :string = "";

  Query = " SELECT T_EXPORTDIR, T_ARCHIVEDIR FROM dwltpfrmt_dbt " +
          " WHERE T_TPID = :TpId " +
          "  AND T_DEPARTMENT = :Dep " +
          "  AND T_STATE = 1 ";
 
  Params = Null;
  Params = makeArray( SQLParam("TpId", TpId),
                      SQLParam("Dep", Dep) );
  DataSet = execSQLSelect( Query, Params );
  if( (DataSet.MoveNext) and ( ValType(DataSet.value( 0, Null, V_STRING )) != V_UNDEF ) and
    ( ValType(DataSet.value( 0, Null, V_STRING )) != 26 ) )
   StrBuf = DataSet.value( 0, Null, V_STRING );
   if( Index( StrBuf, "..\\" ) == 1 ) //относительный путь
//    StrBuf = "D:\\RSHB_SOFR" + SubStr(StrBuf, 3); ///!!!!!!!!!!костыль исправляем нужна функция ROOTDIR реестр пустой
    StrBuf = GetCurDir(false) + "\\" + StrBuf;
   end;

   return StrBuf;
  else
   return "";
  end;
  Params = Null;

onError(err)
 Params = Null;
 return "";
end;;
//А.Киселев 01.04.2019 каталог экспорта.


//А.Киселев 01.04.2019 каталог экспорта.
private macro GetFullPathExportARCHTpFMT( TpId :integer, Dep :integer ) :string
var Params :TArray,
    DataSet :RsdRecordset,
    Query :string = "";

  Query = " SELECT T_EXPORTDIR, T_ARCHIVEDIR FROM dwltpfrmt_dbt " +
          " WHERE T_TPID = :TpId " +
          "  AND T_DEPARTMENT = :Dep " +
          "  AND T_STATE = 1 ";
  Params = Null;
  Params = makeArray( SQLParam("TpId", TpId),
                      SQLParam("Dep", Dep) );

  DataSet = execSQLSelect( Query, Params );
  if( DataSet.MoveNext )
   return DataSet.value( 1, Null, V_STRING );
  else
   return "";
  end;
  Params = Null;

onError(err)
 Params = Null;
 return "";
end;;
//А.Киселев 01.04.2019 каталог экспорта.


//А.Киселев 01.04.2019 Ищем ИД транспорта ответа по сделке/платежа и т.д.
private macro GetTpIdByObjParm( ObjId, ObjKind ) :integer
var Params :TArray,
    DataSet :RsdRecordset,
    Query :string = "";

  if( ( ObjKind == 148 ) or ( ObjKind == 145 ) ) //сделки ФИСИКО
   Query = " SELECT T_CONFTPID FROM ddvndeal_dbt " +
           " WHERE T_ID = :ObjId ";
  elif( ( ObjKind == 103 ) )  //сделки МБК
   Query = " SELECT T_CONFTPID FROM ddl_tick_dbt " +
           " WHERE T_DEALID = :ObjId ";
  elif( ( ObjKind == 108 ) ) //сделки НЕТТИНГА
   Query = " SELECT T_CONFIRMTRANSPORT FROM ddl_nett_dbt " +
           " WHERE T_NETTINGID = :ObjId ";
  end;
 
  if( Query != "" )
   Params = Null;
   Params = makeArray( SQLParam("ObjId", ObjId) );
   DataSet = execSQLSelect( Query, Params );
   if( DataSet.MoveNext )
    return DataSet.value( 0, Null, V_INTEGER );
   else
    return -1;
   end;
   Params = Null;
  else
   return -1;
  end;

onError(err)

 Params = Null;
 return -1;

end;
//А.Киселев 01.04.2019 Ищем ИД транспорта ответа по сделке/платежа и т.д.



//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.
/*
private macro GetObjParm( SessId :integer, ObjId :@integer, ObjKind :@integer ) :integer
var Params :TArray,
    DataSet :RsdRecordset,
    Query :string = "";


  Query = "   SELECT A.T_OBJID, A.T_OBJKIND FROM dwlmeslnk_dbt A " +
          "   WHERE A.T_MESID = ( SELECT B.T_MESID FROM dwlmes_dbt B " +
          "                       WHERE B.T_SESSIONID = :SessId ) ";
/*
          "                     WHERE B.T_SESSIONID = ( SELECT C.T_SESSIONID FROM dwlsess_dbt C " +
          "                                             WHERE C.T_SESSIONID = :SessId ) ) ";
*/

  Params = Null;
  Params = makeArray( SQLParam("SessId", SessId) );
  DataSet = execSQLSelect( Query, Params );
  if( DataSet.MoveNext )
   ObjId = DataSet.value( 0, Null, V_INTEGER );
   ObjKind = DataSet.value( 1, Null, V_INTEGER );
  else
   ObjId = -1;
   ObjKind = -1;
   return 1;
  end;
  Params = Null;
  DataSet = Null;


  if( ObjKind == 512 )//если объект-подтверждение из МБК          

   Query = "     SELECT T_DEALID, 103 FROM dwldlmm_dbt " +
           "     WHERE T_DLMMID = :ObjId ";

   Params = makeArray( SQLParam("ObjId", ObjId) );
   DataSet = execSQLSelect( Query, Params );
   if( DataSet.MoveNext )
    ObjId = DataSet.value( 0, Null, V_INTEGER );
    ObjKind = DataSet.value( 1, Null, V_INTEGER );
   else
    ObjId = -1;
    ObjKind = -1;
    return 1;
   end;
   Params = Null;

  end;
 
  return 0;

onError(err)
  ObjId = -1;
  ObjKind = -1;
  return 1;
end;
*/
//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.


//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.
private macro GetObjParm( SessId :integer, ObjId :@integer, ObjKind :@integer ) :integer
var cmd,
    Query :string = "";


  Query = " DECLARE " +
          "  v_ObjId      dwlmeslnk_dbt.T_OBJID%TYPE; " +
          "  v_ObjKind    dwlmeslnk_dbt.T_OBJKIND%TYPE; " +
          "  v_SessId     ddvndeal_dbt.T_ID%TYPE := :p_SessId; " +
          "  p_ObjId      dwlmeslnk_dbt.T_OBJID%TYPE; " +
          "  p_ObjKind    dwlmeslnk_dbt.T_OBJKIND%TYPE; " +

          " BEGIN " +
          "  BEGIN " +

          "   SELECT A.T_OBJID, A.T_OBJKIND INTO v_ObjId, v_ObjKind FROM dwlmeslnk_dbt A " +
          "   WHERE A.T_MESID = ( SELECT B.T_MESID FROM dwlmes_dbt B " +
          "                       WHERE B.T_SESSIONID = v_SessId ); " +
//если объект-подтверждение из МБК          
          "   IF v_ObjKind = 512 THEN " +

          "     SELECT T_DEALID, 103 INTO v_ObjId, v_ObjKind FROM dwldlmm_dbt " +
          "     WHERE T_DLMMID = v_ObjId; " +

          "   END IF; " +


          "   :p_ObjId := v_ObjId; " +
          "   :p_ObjKind := v_ObjKind; " +


          "  EXCEPTION " +
          "   WHEN OTHERS THEN " +
          "   BEGIN " +
          "    :p_ObjId := -1; " +
          "    :p_ObjKind := -1; " +
          "   END; " +
          "  END; " +

          " END; ";

  cmd = RSDCommand(Query);
  cmd.AddParam("p_SessId", RSDBP_IN, SessId );
  cmd.AddParam("p_ObjId", RSDBP_OUT, V_INTEGER, 0 );
  cmd.AddParam("p_ObjKind", RSDBP_OUT, V_INTEGER, 0 );
  cmd.execute();

  if( (ValType(cmd.value("p_ObjId")) == V_UNDEF) or (ValType(cmd.value("p_ObjId")) == 26))
   ObjId = -1;
   return 1;
  else
   ObjId = cmd.value("p_ObjId");
  end;

  if( (ValType(cmd.value("p_ObjKind")) == V_UNDEF) or (ValType(cmd.value("p_ObjKind")) == 26))
   ObjKind = -1;
   return 1;
  else
   ObjKind = cmd.value("p_ObjKind");
  end;
 
  return 0;

onError(err)
  ObjId = -1;
  ObjKind = -1;
  return 1;
end;
//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.




// Заполнение данных для печати отчета экспорта для ГИС ГМП
private macro FillExpInfoGis( SessionID, mes )
  var node = mes.DocumentElement;
  var PackageID = "";
  var GisGmpCode = "";
  var GisGmpDescr = "";
        
  if( FindChildNode( node, "ResultData", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/" ) )
    PackageID = ReadValue( node, "ResultData", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/ResultData" );
  end;
  
  if( FindChildNode( node, "ResultCode", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/" ) )
    GisGmpCode = ReadValue( node, "ResultData", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/ResultCode" );
  elif( FindChildNode( node, "faultcode", "Body/Fault" ) )
    GisGmpCode = ReadValue( node, "faultcode", "Body/Fault/faultcode" );
  end;
  
  if( FindChildNode( node, "ResultCode", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/" ) )
    GisGmpDescr = ReadValue( node, "ResultData", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/ResultData" );
    if( strlen(GisGmpDescr) == 0 )
      GisGmpDescr = ReadValue( node, "ResultData", "Body/GISGMPTransferMsg/MessageData/AppData/ResponseMessage/Ticket/RequestProcessResult/ResultDescription" );
    end; 
  elif( FindChildNode( node, "faultstring", "Body/Fault" ) )
    GisGmpDescr = ReadValue( node, "faultstring", "Body/Fault/faultstring" );
  end;

  execSQL( " Begin WLD_EXPREP.AddGisExpRepInfo(?,?,?,?); end; ", MakeArray( SQLParam("", SessionID),
                                                                            SQLParam("", PackageID),
                                                                            SQLParam("", GisGmpCode),
                                                                            SQLParam("", GisGmpDescr)));
end;

private macro IsCategories63( MesID, ReqID:@integer )

  var Party : RsbParty;

  var select = "SELECT rqa.t_clientid, rqa.t_requestid "
               "  FROM dwlmeslnk_dbt lnk, dreqopena_dbt rqa "
               " WHERE t_mesid = :MesID AND lnk.t_objid = rqa.t_requestid ";
  var params = makeArray( SQLParam("MesID", MesID) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs.moveNext() )
    Party = RsbParty( rs.value(0) );
    reqID = rs.value(1);
    if( Party.Category.IsAttrPresense(63 /*Хозяйственное общество, имеющее стратегическое значение*/, 1, null, null, true, {curdate}) or
        Party.Category.IsAttrPresense(63 /*Хозяйственное общество, имеющее стратегическое значение*/, 2, null, null, true, {curdate}) or
        Party.Category.IsAttrPresense(63 /*Хозяйственное общество, имеющее стратегическое значение*/, 3, null, null, true, {curdate}) or
        Party.Category.IsAttrPresense(63 /*Хозяйственное общество, имеющее стратегическое значение*/, 4, null, null, true, {curdate}) )
      return true;
    end;
  end;

  return false;

end;

/****************************************************************************/
/*Для примера - переименование файла экспорта                                    */
/****************************************************************************/
/*переименование файла экспорта для сообщений ГНИ*/
macro RenameFileMNS(FileName, NewFileName, SessID)
  RECORD wlreq(reqopena);

  var cmd, dir, fileN, ext, error, _Name,Name, RegOrg, _dir = "", FrmName = "";
  var rs:object;
  var select:string;
  var params:TArray;
  select = "select frm.t_Name, mes.t_MesID " + 
           "  from dwlmesrls_dbt rls, dwlmes_dbt mes, dwlmesfrm_dbt frm " +
           " where mes.t_SessionID =:SessID " +
           " and mes.t_RlsFormID = rls.t_RlsFormID and rls.t_FormID = frm.t_FormID";
  params = makeArray( SQLParam("SessID", SessID));
  rs = execSQLselect( select, params, FALSE );

  if(not rs.moveNext())
    return WLD_POSTEXPORT_ERROR;
  else
    FrmName = rs.value("t_Name");
  end;

  dir = SplitFile(FileName, fileN, ext);
  Name = GetFileNameMNS( FrmName, SessID, rs.Value("t_MesID") );
  if(Name == -1)
    return WLD_POSTEXPORT_ERROR;
  end;

  if( InList(FrmName, "BUV", "BZ1") )
    FrmName = ""; // для BUV, BZ1 все будет в Name после GetFileNameMNS
  end;

  var reqid = 0;
  RegOrg = GetFldValue( rs.Value("t_MesID"), "_ВидРегОргана" );
  if( RegOrg != "" )
    if( int(RegOrg) ==  REG_ORGAN_KIND_PFR )
      GetRegValForOPENAC("КАТАЛОГ_ПФР", V_STRING, _dir);
    elif( int(RegOrg) ==  REG_ORGAN_KIND_FSS )
      GetRegValForOPENAC("КАТАЛОГ_ФСС", V_STRING, _dir);
    end;
    if(_dir != "")
      if (not ExistFile(_dir))
         if(not MakeDir(_dir))
           msgbox("Ошибка создания каталога:|" + _dir);
           return WLD_POSTEXPORT_ERROR; 
         end;
      end;

      if( substr(trim(_dir), strlen(trim(_dir))) == "\\")
        dir = trim(_dir);
      else
        dir = trim(_dir) + "\\" ;
      end;
    end;
  elif( (SubStr( FrmName+Name, 1, 4) == "SBC0") AND (SubStr( Name, strlen(Name)-1 ) == "00") AND IsCategories63( rs.Value("t_MesID"), @reqid ) )
    wlreq.RequestID = reqid;
    var sObjID = UniID(wlreq, OBJTYPE_REQOPENA);
    if( not LoadImageObj(FileName, OBJTYPE_REQOPENA, sObjID, 1) )
      RunError("Ошибка прикрепления файла " + FileName + " к сообщению об открытии счёта " + rs.Value("t_MesID"));
    end;
  end;

  /*Имя файла.*/
  _Name = dir + FrmName + Name + ext;

  if (SetParm(1, _Name) == false)
    return WLD_POSTEXPORT_ERROR;
  end;
  return WLD_POSTEXPORT_OK;
end;

macro GetSysDate():string
   var day, mon, year, str;

   DateSplit(date(), day, mon, year );
   if ( day < 10 )
     day = "0"+string(day);
   else
     day = string(day);
   end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = SubStr(string(year), 3) + mon + day ;
  return str;
end;

/*переименование файла экспорта для сообщений ГИС ГМП*/
macro RenameFileGG(FileName, NewFileName, SessID, AnswerName:@string)
  var Code:string = "", Name:string = "", dir, fileN, ext;
  var rs:object;
  var select:string;
  var params:TArray;
  var RegVal:bool = false, RegErr = 0;
  var WebServAddr:string = "";

  dir = SplitFile(FileName, fileN, ext);

  Name = dir + SubStr( fileN, 1, 4 );

  select = "select code.t_Code " + 
           " from dobjcode_dbt code, ddp_dep_dbt dep, dwltpfrmt_dbt frmt, dwlsess_dbt sess " +
           " where sess.t_SessionID =:SessID            " +
           " and frmt.t_TpFrmtID    = sess.t_TpFrmtID   " +
           " and dep.t_Code         = frmt.t_Department " +
           " and code.t_ObjectID    = dep.t_PartyID     " +
           " and code.t_CodeKind    = 13                " +
           " and code.t_ObjectType  = 3                 ";

  params = makeArray( SQLParam("SessID", SessID));
  rs = execSQLselect( select, params, FALSE );

  if(not rs.moveNext())
    return WLD_POSTEXPORT_ERROR;
  else
    Code = rs.value(0);
  end;

  if( Index(Code, "/") > 0 ) 
    Code = SubStr( Code, Index(Code, "/") + 1 );
    if( Code != "" )
      AnswerName = strLpad( Code, 4, "0" );
    end;
  else 
    AnswerName = "0000";
  end;

  AnswerName = AnswerName + GetSysDate();
  Name = Name + AnswerName + SubStr( fileN, 11 ) + ".xml";

  if (SetParm(1, Name) == false)
    return WLD_POSTEXPORT_ERROR;
  end;

  return WLD_POSTEXPORT_OK;
end;

private macro SendGIS_GMP( objXMLDOC, xmlxml )
  
  objXMLDOC.send(xmlxml);

  if( (objXMLDOC.Status == 500) and (strlen(objXMLDOC.responseXML.xml) > 0) )
    return "";
  end;

  if ( (objXMLDOC.Status >= 400) And (objXMLDOC.Status <= 599) )
    return "Отправка сообщения прошла с ошибкой: " + objXMLDOC.Status + " " + objXMLDOC.statusText; 
  end;
  
  return "";

onError(err)
  return "Ошибка при отправке сообщения - " + err.message + "\nОшибка ActiveX: " + err.AxMes; 
end;

macro GetAnswer( FileName:string, TpFrmtID:integer, AnswerName:string, Sess )
  var RegErr = 0;
  var url:string = "";
  var answerFN = "";
  var stat = 0; 
 
  var select = "select wltpfrmt.t_ImportDir, wltpfrmt.t_ExportFileNameRefID " + 
               " from dwltpfrmt_dbt wltpfrmt " +
               " where wltpfrmt.t_TpID = 14  " +
               "   and wltpfrmt.t_State <> 3 "; //WLD_STATUS_TPFRMT_NOTACTIV

  var rs = execSQLselect( select, NULL, FALSE );

  if(not rs.moveNext())
    return 0;
  else
    var param = makeArray( SQLParam( "" , V_STRING , RSDBP_OUT ),
                           SQLParam( "" , rs.value(1)  ),
                           SQLParam( "" , OBJTYPE_SESS ),
                           SQLParam( "" , 0            ),
                           SQLParam( "" , date(0,0,0)  ),
                           SQLParam( "" , date(0,0,0)  ),
                           SQLParam( "" , 0            ),
                           SQLParam( "" , 0            )
                         );
    if( execStoredFunc( "RSI_RSB_REFER.WldGenerateReference", V_INTEGER, param ) == 0 )
      var fName = param.value(0).value;
      AnswerName = SubStr( fName, 1, 4 ) + AnswerName + SubStr( fName, 11 );
    end;

    answerFN = rs.value(0) + "\\" + AnswerName;
    WldCreateDirection( rs.value(0) );
  end;

  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\ADDRESSWEBSERVER", V_STRING, url, RegErr);
  // Адрес сервиса 
  var endpoint = url + "?WSDL";
  // Вызываемый метод
  var soapAction = "http://roskazna.ru/SmevUnifoService/UnifoTransferMsg";

  // Создаем объект XML и загружаем его сообщением из первого файла
  var mes = ActiveX("MSXML2.DOMDocument");
  mes.async = false;
  mes.load(FileName);
  
  // Создаем объект вызова сервиса
  var objXMLDOC = ActiveX("Msxml2.ServerXMLHTTP.6.0");

  var RegVal = "";
  RegErr = 0;
  GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\SETPROXY", V_STRING, RegVal, RegErr);
  if( RegVal != "" )
    if( RegVal != "0//" )
      var pos1 = index(RegVal, "/"), pos2 = 0;
      if( pos1 > 0 )
        pos2 = index(RegVal, "/", pos1+1);
        var count = 1;
        if( pos2 > 0 )
          count = int(substr(RegVal, 1, pos1));
        end;
        objXMLDOC.setProxy(count, substr(RegVal, pos1+1, pos2-pos1-1), substr(RegVal, pos2+1)); // явное указание прокси
      end;
    else
      objXMLDOC.setProxy(0); // указание прокси
    end;
  end;

  // Делаем вызов,... 
  // ... заполняем заголовок HTTP
  objXMLDOC.open("POST", endpoint, false );
  objXMLDOC.setRequestHeader("Content-Type", "text/xml; charset=utf-8");
  objXMLDOC.setRequestHeader("SOAPAction", soapAction);

  var xmlxml = mes.xml;

  var Msg = SendGIS_GMP( objXMLDOC, xmlxml );

  if ( Msg != "" )
    CreateErrMsg(WLD_WARN_MACRO, Msg);
    return WLD_POSTEXPORT_ERROR; 
  else
    // Ответ сохраняем в указанном файле
    stat = Open( OutFile, answerFN, "utf8" );
    if((stat == true))

      OutFile.str = objXMLDOC.responseXML.xml;
      
      FillExpInfoGis( Sess.SessionID, objXMLDOC.responseXML );
      
      insert(OutFile);
  
      close(OutFile);
    end;

    setUserMenuNoActive();
//    system( 4020, CodeFor("W"), " -f:" + string(TpFrmtID) );
    InitError();
    setUserMenuActive();
  end;

  return WLD_POSTEXPORT_OK;

end;
/****************************************************************************/
/*Для примера - архивация файла экспорта и перенос в указанную директорию   */
/****************************************************************************/
macro RarExport2(FileName, NewFileName)
  var cmd, dir, fileN, ext, error, arcName;
  
  dir = SplitFile(FileName, fileN, ext);
  /*Установим новое полное(dir+fname+ext) имя файла экспорта*/
  NewFileName = "D:\\RSHB_SOFR\\PUBLIC\\OUT\\OUT_BANK_COMPLETE\\"+fileN+".rar"; 
  if (SetParm(1, NewFileName) == false)
    return WLD_POSTEXPORT_ERROR;
  end;

  /*Командная строка для архиватора*/
  cmd = "a -ep " + NewFileName + " " + FileName;

  /*Архивируем*/
  error = Run("rar.exe", cmd);
  if (error == 0)
    return WLD_POSTEXPORT_OK_STOP;
  else 
    return WLD_POSTEXPORT_ERROR;
  end;
end;
/***************************************************************************/

/* Вызываемый макрос постобработки */
macro MainExec(FileName, Sess)
  /************************ Не трогать !!!!!!!! ******************************/
  RECORD s(wlsess);
  var error;
  SetBuff(s, Sess);
  var RegVal:bool = false;
  var RegErr = 0;

  var DirName :string = "",
      FileNm :string = "",
      FileExt :string = "",
      state :integer = 0,
      ObjId :integer = 0,
      ObjKind :integer = 0,
      TpIdConf :integer = 0;

  /*Здесь будет находится новое имя файла экспорта*/
  /*При возврате WLD_POSTEXPORT_OK, WLD_POSTEXPORT_CHANGE это имя*/
  /*определяет файл в директории временных файлов (будет удален), иначе - имя*/
  /*определяет местонахождение файла экспорта, который будет связан с сеансом*/
  s.FileName = FileName;
  /*****************************************************************************/
// Поискать сделку заменить s.FileName
  if( (s.TpId == TRANSP_SWIFT) or (s.TpId == TRANSP_TELEX) or (s.TpId == TRANSP_SPFSCBR) )

    state = GetObjParm( s.SessionID, @ObjId, @ObjKind );
    if( state == 0 )
     TpIdConf = GetTpIdByObjParm( ObjId, ObjKind );

     /*PNV i-support 501239 если в операции, по которой сформировано сообщение, не заполнен транспорт, берем из сеанса*/
     if ( (TpIdConf == 0) or (TpIdConf == -1) ) /*PNV i-support 504030*/
         TpIdConf = s.tpid;
     end;
     /*PNV*/

     if( TpIdConf > 0 )
      DirName = SplitFile( FileName, FileNm, FileExt); 
      FileName = GetFullPathExportTpFMT( TpIdConf, {OperDprt} );

      /*PNV i-support 487319 - преименовать файл для TELEX*/
      if( TpIdConf ==  TRANSP_TELEX)
         FileNm = GetNewFileName( ObjId, ObjKind, FileNm );
      end;
      /*PNV*/
 
      s.FileName = FileName + "\\" + FileNm + FileExt;
     else
      return WLD_POSTEXPORT_ERROR;
     end;
    else
     return WLD_POSTEXPORT_ERROR;
    end;

  end;



  if (s.TpID == TRANSP_MNS)
     error = RenameFileMNS(FileName, s.FileName, s.SessionID);
     return error;
  end;

  if (s.TpID == 14) //TRANSP_OFK
    GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\CALLWEBSERVER", V_BOOL, RegVal, RegErr);
    var AnswerName = "";
    error = RenameFileGG(FileName, s.FileName, s.SessionID, @AnswerName);
    if( RegVal )
      error = GetAnswer( FileName, s.TpFrmtID, AnswerName, s );
    end; 
    return error;
  end;

  /* Для примера */
  /*
   var error;
   error = RarExport1(FileName, s.FileName);
   return error;
  */

  return WLD_POSTEXPORT_OK;
end;
