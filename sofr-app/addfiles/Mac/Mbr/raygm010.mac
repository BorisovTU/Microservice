/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по "Луч" MF010                                       */
/*                                                                          */
/*  Имя файла: raygm010.mac                                                 */
/*  Создан:  30.03.09                                                       */
/****************************************************************************/
import "raygenmes.mac";

macro GenMes( addrMes, addrbuf )
  file llval(llvalues) key 0;
  RECORD DpBuf( dpmsginf );
  var    msginf, msgdoc, msgParty;  
  var    error, PartyCode, field_value, ProcessingCode;

  SetBuff( wlmes,  addrMes );
  SetBuff( DpBuf,  addrbuf );
  
  msginf = RsbDepoInfoMessage(DpBuf.MessageID);

  if ( valtype(msginf)==V_UNDEF )
     RunError( "|Не определен объект класса" );
  end;

  PrintLog(2,"Генерация сообщения по МF010");

  /*Блок ORDER_HEADER*/
  Write_ORDER_HEADER_Block( msginf );

  ProcessingCode = msginf.ProcessingCode;
  /* Блок MF010 */
  ЗаписатьПолеЛогВБлок( MF010_Block, BeginOfBlock, MF010_Block );

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_DEAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*dep_acc_c*/
    ЗаписатьПолеЛогСПроверкой( dep_acc_c_Field, msgParty.Account );
    /*sec_c*/
    ЗаписатьПолеЛогСПроверкой( sec_c_Field, msgParty.Partition );
    /*deponent_c*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( deponent_c_Field, PartyCode );
    else
      RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
    end;
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (DEAG)" );
  end; 


  if((ProcessingCode == "11") or (ProcessingCode == "13") or (ProcessingCode == "83/11") or (ProcessingCode == "83/13"))
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
    if ( valtype(msgParty)!=V_UNDEF )
      /*depon_acc_c*/
      ЗаписатьПолеЛогСПроверкой( depon_acc_c_Field, msgParty.Account );
      /*depon_sec_c*/
      ЗаписатьПолеЛогСПроверкой( depon_sec_c_Field, msgParty.Partition );
      /*depon_code*/      
      PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
      if( not error )
        ЗаписатьПолеЛогСПроверкой( depon_code_Field, PartyCode );
      end;
    end; 
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*corr_acc_c*/
    ЗаписатьПолеЛогСПроверкой( corr_acc_c_Field, msgParty.Account );
    /*corr_sec_c*/
    ЗаписатьПолеЛогСПроверкой( corr_sec_c_Field, msgParty.Partition );
    /*corr_code*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( corr_code_Field, PartyCode );
    else
      RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
    end;
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (REAG)" );
  end; 

  if((ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "83/11") or (ProcessingCode == "83/12"))
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
    if ( valtype(msgParty)!=V_UNDEF )
      /*corr_depon_acc_c*/
      ЗаписатьПолеЛогСПроверкой( corr_depon_acc_c_Field, msgParty.Account );
      /*corr_depon_sec_c*/
      ЗаписатьПолеЛогСПроверкой( corr_depon_sec_c_Field, msgParty.Partition );
      /*cor_depon_code*/
      PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
      if( not error )
        ЗаписатьПолеЛогСПроверкой( corr_depon_code_Field, PartyCode );
      else
        RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
      end;
    else
      RunError( "|Отсутствует информация об идентификаторе стороны (BUYR)" );
    end; 
  elif((ProcessingCode == "16") or (ProcessingCode == "16/1"))
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
    if ( valtype(msgParty)!=V_UNDEF )
      /*corr_depon_acc_c*/
      ЗаписатьПолеЛогСПроверкой( corr_depon_acc_c_Field, msgParty.Account );
    end;
  end;

  msginf.RewindMessageDocument();
  msgdoc = msginf.GetMessageDocument();
  while( valtype(msgdoc)!=V_UNDEF )
    if(msgdoc.DocType == DPMSGDOC_GROUND)
      llval.List = OBJTYPE_KINDDOC;
      llval.Element = msgdoc.DocumentKind;
      if ( not getEQ(llval) )
         RunError("|Не найдена запись в справочнике видов документов");
      end;
      if(not ((ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "13") or 
              (ProcessingCode == "83/11") or (ProcessingCode == "83/12") or (ProcessingCode == "83/13")))
        /*based_on*/
        ЗаписатьПолеЛог( based_on_Field, llval.Name );
        /*based_numb*/
        ЗаписатьПолеЛог( based_numb_Field, msgdoc.DocumentNumber );
      end;
      /*based_date*/
      ЗаписатьПолеЛог( based_date_Field, YYYYMMDD(msgdoc.DocumentDate) );
    end;
    msgdoc = msginf.GetMessageDocument();
  end;

  /*deponent_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SENDER,0);
  if ( valtype(msgParty)!=V_UNDEF )
    if( msgParty.PartyCode != "" )
      ЗаписатьПолеЛог( deponent_bic_Field, msgParty.PartyCode );      
    end;
  end; 

  /*corr_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_RECEIVER,0);
  if ( valtype(msgParty)!=V_UNDEF )
    if( msgParty.PartyCode != "" )
      ЗаписатьПолеЛог( corr_bic_Field, msgParty.PartyCode );      
    end;
  end; 

  /*depon_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
  if ( valtype(msgParty)!=V_UNDEF )
    ЗаписатьПолеЛог( depon_bic_Field,msgParty.PartyCode );
  end;
  /*corr_depon_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
  if ( valtype(msgParty)!=V_UNDEF )
    ЗаписатьПолеЛог( corr_depon_bic_Field,msgParty.PartyCode );
  end;

  /*Блок seсurities*/
  if( not УстановитьКонтекстБлока( seсurities_Block ) )
    RunError("|Ошибка при установке контекста блока "+seсurities_Block);
  end;
  ЗаписатьПолеЛог( BeginOfBlock, seсurities_Block ) ;
   /*Блок seсurity*/
  if( not УстановитьКонтекстБлока( seсurity_Block ) )
    RunError("|Ошибка при установке контекста блока "+seсurity_Block);
  end;
  ЗаписатьПолеЛог( BeginOfBlock, seсurity_Block ) ;

  /*seсurity_c*/   
  ЗаписатьПолеЛог( security_c_Field, msginf.SecurityCode);
  /*sequrity_q*/      
  ЗаписатьПолеЛог( security_q_Field, msginf.QuantitytoBeSettled);
  /*sequrity_isin*/   
  field_value = FillISIN(msginf);
  if(field_value != "")
    ЗаписатьПолеЛог( security_isin_Field, field_value);  
  end;

  ЗаписатьПолеЛог( EndOfBlock, seсurity_Block ) ;
  if( not УстановитьКонтекстБлока( ".." ) )
     RunError("|Ошибка при установке контекста блока ..");
  end; 
  /*Конец блока seсurity*/

  ЗаписатьПолеЛог( EndOfBlock, seсurities_Block ) ;
  if( not УстановитьКонтекстБлока( ".." ) )
     RunError("|Ошибка при установке контекста блока ..");
  end; 
  /*Конец блока seсurities*/


  ЗаписатьПолеЛог( EndOfBlock, MF010_Block );
  /* Конец блока MF010 */
  
  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

macro CheckMes( addrMes )
  var field_name, field_value, ProcessingCode = "";
  SetBuff( wlmes, addrMes );
  

  while( СчитатьПоле( field_name, field_value ) )
    if(field_name == order_t_id_Field)
      ProcessingCode = field_value;
    end;
    /*контроль на русские буквы*/
    if( (field_name == deposit_c_Field )       or
        (field_name == contrag_c_Field )       or
        (field_name == dep_acc_c_Field)        or
        (field_name == sec_c_Field)            or
        (field_name == deponent_c_Field)       or
        (field_name == depon_acc_c_Field)      or
        (field_name == depon_sec_c_Field)      or
        (field_name == depon_code_Field)       or
        (field_name == corr_acc_c_Field)       or
        (field_name == corr_sec_c_Field)       or
        (field_name == corr_code_Field)        or
        (field_name == corr_depon_acc_c_Field) or
        (field_name == corr_depon_sec_c_Field) or
        (field_name == corr_depon_code_Field))
      if(not CheckForRus( field_name, field_value, true))
        return false;
      end;
    end;

    if(ProcessingCode != "")
      /*контроль по коду операции*/
      if(not ((ProcessingCode == "11") or
              (ProcessingCode == "13") or
              (ProcessingCode == "83/11") or
              (ProcessingCode == "83/13")) )
        if( ((field_name == depon_acc_c_Field) or
             (field_name == depon_sec_c_Field) or
             (field_name == depon_code_Field))  )
          std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
          return false;
        end;
      end;
    end;
 
    if( ((field_name == depon_acc_c_Field) or
         (field_name == depon_sec_c_Field) or
         (field_name == depon_code_Field)) )    
      if(not((ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "83/11") or (ProcessingCode == "83/12")))
        if( not ((field_name == depon_acc_c_Field) and ((ProcessingCode == "16") or (ProcessingCode == "16/1"))))
          std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
          return false;      
        end;
      end;
    end;

    if((field_name == based_on_Field) or
       (field_name == based_numb_Field) )
      if( (ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "13") or 
          (ProcessingCode == "83/11") or (ProcessingCode == "83/12") or (ProcessingCode == "83/13"))
        std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
        return false;      
      end;
    end;
  end;

  field_name = "";
  /*контроль на заполненность полей*/
  if((ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "83/11") or (ProcessingCode == "83/12"))
    if  (not ПолучитьПоле(wlmes.MesID, corr_depon_acc_c_Field, field_value))
      field_name = corr_depon_acc_c_Field;
    elif(not ПолучитьПоле(wlmes.MesID, corr_depon_sec_c_Field, field_value)) 
      field_name = corr_depon_sec_c_Field;
    elif(not ПолучитьПоле(wlmes.MesID, corr_depon_code_Field ,  field_value))
      field_name = corr_depon_code_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  if( not((ProcessingCode == "11") or (ProcessingCode == "12") or (ProcessingCode == "13") or 
          (ProcessingCode == "83/11") or (ProcessingCode == "83/12") or (ProcessingCode == "83/13")))
    if  (not ПолучитьПоле(wlmes.MesID, based_on_Field, field_value))
      field_name = based_on_Field;
    elif(not ПолучитьПоле(wlmes.MesID, based_numb_Field, field_value)) 
      field_name = based_numb_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  return true; 

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;