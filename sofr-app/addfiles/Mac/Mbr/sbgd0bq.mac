/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по сообщениям SBRF3 сигнатура 0BQ                     */
/*                                                                          */
/*  Имя файла: sbgd0BQ.mac                                                  */
/*  Создан:    07.02.03                                       Бабин А.П.    */
/****************************************************************************/

import "sbgendoc.mac";

/* Формирование запроса на получение выписки по сообщениям СБРФ */
macro GenDocExec( IsPIB )
  var field_name, field_value, chapter = 1, Currency, error, sum, SBRFStr = "";

  /* Формируем строку сообщения СБРФ */
  if( not SB_MakeSBRF3Message( IsPIB, SBRFStr ) )
    return false;
  end;

  ClearRecord(wlreq);
  wlreq.TRN = wlmes.TRN;

  while( SB_СчитатьПолеИзСообщения( field_name, field_value, SBRFStr) == 0 )
     if ( field_name=="CU" )
       if( not ПолучитьФинИнПоISO( field_value, wlfininstr ) )
         std.msg( "Не определена валюта сообщения по коду ISO: " + field_value );
         return FALSE;
       end;
       wlreq.FIID = wlfininstr.FIID;
     elif ( field_name=="VT" )
       if ( substr(field_value,1,3)!="019" )
          wlreq.SubKind = 1;
       end;
     elif ( field_name=="VB" )
       wlReq.PmDateValue = ДДММГГДата(field_value);
       wlReq.Corschem    = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlReq.FIID, 1 );
       if( wlreq.Corschem == -1 )
         Currency = ПолучитьКодФинИн(wlreq.FIID, error);
         if ( error )
            Currency = "???";
         end;
         std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
         return FALSE;
       end;
      if( not ВставитьЗапросНаПолучениеВыписки( wlreq ) )
        std.msg("Ошибка при сохранении запроса");
        return FALSE;
      end;      
     end;
  end;

  return true;
end;

/* Релиз 0BQ */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса на получение выписки по форме 0BQ");

  return GenDocExec( false );
end;

/* Релиз 0BQJ */
macro GenDocPIB( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса на получение выписки по форме 0BQJ");

  return GenDocExec( true );
end;
