/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Константы и вспомогательные функции для генерации учетных объектов по   */
/*  сообщениям СМБР                                                         */
/*                                                                          */
/*  Имя файла: sbgendoc.mac                                                 */
/*  Создан:    21.02.00                                        Алешин А.В.  */
/****************************************************************************/

import "wldoc.mac", "sbblock.mac", "wlgendoc.mac", "wlfindpm.mac", FIInter, cryptdlm;

const CONTEXTID_CHECK_SIGN_SBRF3 = "ТранспортМБР|4|Обработка_сообщения";

/* Проверка ЭЦП входящего сообщения */
macro SBCheckKey( SBRFStr )
  var StrForSign = "", CryptoAPI = RsCryptoAPI(), rsms; 

  /* Выполняем проверку ЭЦП если существует настройка */
  if( CryptoAPI.IsCryptoActionNeeded( CONTEXTID_CHECK_SIGN_SBRF3, 370 ) )
    /* Формирование блока данных */ 
    if( not FormingDataInit( wlmes, SBRFStr, StrForSign ) )
      std.msg( "Ошибка при формировании блока данных" );
      return FALSE;
    end;

    /* Проверяем ЭЦП сообщения */
    rsms = RsbMessage( wlmes.MesID );
    rsms.PIB = StrForSign;
    if( not CryptoAPI.ExecCryptoAction( CONTEXTID_CHECK_SIGN_SBRF3, rsms ))
      std.msg( String("Ошибка при проверке ЭЦП сообщения|", + GetErrMsg()) );
      return false;
    end;
  end;

  return TRUE;
end;

/* Сформировать строку СБРФ для генерации платежа */
macro SB_MakeSBRF3Message( IsPIB, Str )
  var SBRFStr = "", field_name, field_value;

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    std.msg( String("Не найдена форма сообщения, RlsFormID = ", wlmes.RlsFormID) );
    return false;
  end;

  /* Формируем строку сообщения в формате СБРФ для ключевания */
  SBRFStr = f_wlmesfrm.Name + wlmes.TRN;
  
  /* Считываем поля сообщения */
  if( IsPIB == true )
    if( not СчитатьПоле( field_name, field_value ) OR (field_name != SB_Tag_PIB) OR (field_value == "") )
      std.msg( String("В сообщении по форме:", f_wlmesfrm.Name, " референс: ", wlmes.TRN, "|не заполнено поле '", SB_Tag_PIB, "'") );
      return false;
    else
      SBRFStr = SBRFStr + field_value;
    end;  
  else
    while( СчитатьПоле( field_name, field_value ) )
       SBRFStr = SBRFStr + MakeFieldSBRF3( field_name, field_value, false, false );
    end;
  end;

  SetParm( 1, SBRFStr );

  return true;
end;


/* ------------- Банк ------------- */
class SbBank( pBIC, pCodeKind, pName )

 var PartyID       :integer,  /* Ссылка на банк */
     CodeKind      :integer,  /* Вид кода субъекта */
     CodeBank      :string,   /* Код банка */
     Account       :string,   /* Счет */
     Name          :string,   /* Наименование */
     CliringCode   :string,   /* Код клиринга */
     error;

  /* инициализация */
  if(valtype(pBIC)==V_STRING)
    CodeBank = pBIC;
  else
    CodeBank = "";
  end;
  if(valtype(pCodeKind)==V_INTEGER)
    CodeKind = pCodeKind;
  else
    CodeKind = PTCK_BIC;  /* По умолчанию проставляем БИК ЦБ */
  end;
  if(valtype(pName)==V_STRING)
    Name = pName;
  else
    Name = "";
  end;

  Account     = "";
  CliringCode = "";
  PartyID     = -1;

  macro SetBank()
   if( CodeBank != "" )
     PartyID = ПолучитьКодСубъекта( CodeBank, CodeKind, error );
     if( error )
       PartyID = -1;
     end;
   elif( CliringCode != "" )
     PartyID = ПолучитьКодСубъекта( CliringCode, PTCK_CLIRING, error );
     if( error == 0 )
       Codekind = PTCK_CLIRING;
     else
       PartyID = -1;
     end;
   end;
   return PartyID; /* Возвращаем идентификатор субъекта */
  end;

  macro IsSet()
    return NOT((CodeBank=="")AND(Name=="")AND(CliringCode==""));
  end;

  macro InsertRoute( Direct, IsInstructionAbonent )
    if( IsSet() )
      if ( direct==RTDIR_IN )
         return ВставитьВходящийУзелМаршрута( PartyID, CodeKind, CodeBank, "", "", Name, IsInstructionAbonent );
      else
         return ВставитьИсходящийУзелМаршрута( PartyID, CodeKind, CodeBank, "", "", Name, IsInstructionAbonent );
      end;
    else
      return TRUE;
    end;
  end;

  macro GetErrMsg( role:string )
    var str = "", nn = "", nc = "", ncc = "", nk  = "";
    if( PartyID == -1 )
      if( name != "" ) nn = String("|наименование банка: ", Name ); end;
      if( CodeBank != "" ) nc = String("|код банка: ", CodeBank ); end;
      if( CliringCode != "" ) ncc = String("|код клиринга: ", CliringCode ); end;
      nk = String("|вид кода банка: ", CodeKind );
      str = String("Не определен ", role, nc, nk, ncc, nn );
    end;
    return str;
  end;

end;

class SbCustomer(pName, pAccount, pINN, pKPP )
  /* инициализация */
 var
  Account :string, /* Счет */
  Name    :string, /* Name and Address */
  INN     :string,
  KPP     :string,
  Dprt    :string; /* Филиал */


  /* инициализация */
  if(valtype(pAccount)==V_STRING)
    Account = pAccount;
  else
    Account = "";
  end;
  if(valtype(pName)==V_STRING)
    Name = pName;
  else
    Name = "";
  end;
  if(valtype(pINN)==V_STRING)
    INN = pINN;
  else
    INN = "";
  end;
  if(valtype(pKPP)==V_STRING)
    KPP = pKPP;
  else
    KPP = "";
  end;
  Dprt = "";
end;

/* Определяет по виду платежной системы SBRF3 вид кода RS-Bank              */
macro ПолучитьВидКодаПоSBRF3( KindCodeStr )
  var KindCode;

  if(   KindCodeStr == KindCodeSBRF3_CONTR )
    KindCode = PTCK_CONTR;
  elif( KindCodeStr == KindCodeSBRF3_CLIENT )
    KindCode = PTCK_CLIENT;
  elif( KindCodeStr == KindCodeSBRF3_BANKLICENSE )
    KindCode = PTCK_BANKLICENSE;
  elif( KindCodeStr == KindCodeSBRF3_BIC )
    KindCode = PTCK_BIC;
  elif( KindCodeStr == KindCodeSBRF3_CLIRING )
    KindCode = PTCK_CLIRING;
  elif( KindCodeStr == KindCodeSBRF3_SWIFT )
    KindCode = PTCK_SWIFT;
  elif( KindCodeStr == KindCodeSBRF3_REUTER )
    KindCode = PTCK_REUTER;
  elif( KindCodeStr == KindCodeSBRF3_MICEX )
    KindCode = PTCK_MICEX;
  elif( KindCodeStr == KindCodeSBRF3_DEPOREGN )
    KindCode = PTCK_DEPOREGN;
  elif( KindCodeStr == KindCodeSBRF3_CHIPS )
    KindCode = PTCK_CHIPS;
  elif( KindCodeStr == KindCodeSBRF3_BICPLUS )
    KindCode = PTCK_BICPLUS;
  elif( KindCodeStr == KindCodeSBRF3_NATCOD )
    KindCode = PTCK_NATCOD;
  elif( KindCodeStr == KindCodeSBRF3_BANKREGNUM )
    KindCode = PTCK_BANKREGNUM;
  elif( KindCodeStr == KindCodeSBRF3_SMFR )
    KindCode = PTCK_SMFR;
  elif( KindCodeStr == KindCodeSBRF3_SBRF )
    KindCode = PTCK_SBRF;
  elif( KindCodeStr == KindCodeSBRF3_INN )
    KindCode = PTCK_INN;
  elif( KindCodeStr == KindCodeSBRF3_ORCBLICENSE )
    KindCode = PTCK_ORCBLICENSE;
  else
    KindCode = 0;
  end;

  return KindCode;
end;
