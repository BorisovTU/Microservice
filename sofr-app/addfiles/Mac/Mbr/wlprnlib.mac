/*
  $Name: wlprnlib.mac
  $Module: Межбанковские расчеты
  $Description: Библиотека классов и функций для универсальной печати сообщений
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//                 Подсистема "Межбанковские расчеты"
//
//  Описание    : Библиотека классов и функций для универсальной печати сообщений
//
//  Программист : Евграфова Л.Н.
//
//  Создан      : 08.02.2018
//
// --------------------------------------------------------------------

import "lib_str.mac", "likepy.mac", "wltools.mac";

const DefValueLen = 50;           //ширина значения поля по умолчанию
const DefNameLen = 50;            //ширина названия поля по умолчанию
const DefTabStep = 4;             //величина отступа для блока по умолчанию
const DefPrintNameBlock = true;   //Признак печати названия блока
const DefListValueLen = 100;      //ширина справочного значения поля по умолчанию


/*класс печати одного поля*/
class TPrnField
  var 
      CodeField: string = "",             // код поля
      NameField: string = "",             // название поля
      ValueField: string = "",            // значение поля
      NameLen: integer = DefNameLen,      // ширина названия поля
      ValueLen : integer = DefValueLen,   // ширина значения поля
      TabLen : integer = 0,               // Величина отступа
      ListField: bool = false,            // признак справочного поля
      ListValue: string = "",             // значение из справочника
      NumLine: integer = 1,               // количество строк для вывода значения при табличной печати
      ValueTblField:TArray = TArray,      // значение поля для табличной печати
      TblLen = DefValueLen;               // Ширина поля для таблицы
  /* 
  Получить все необходимые данные для печати
  */
  macro FillData(p_FieldCode : string, 
                 p_FieldValue : string, 
                 p_TpID:integer, 
                 p_RlsFieldID: integer)  

     CodeField = p_FieldCode;
     ValueField = p_FieldValue;
     var query, params, rs; 
     if(p_RlsFieldID != 0)

       //Получить название поля из релиза, длину поля и номер справочника
       query = " select wlmesfld.t_Description , wlmesfld.t_DirectoryID, wltpfld.t_LenEdit "
                    "   from dwlmesfld_dbt wlmesfld, dwltpfld_dbt wltpfld  "
                    "  where wlmesfld.t_FieldID = :RlsFieldID "
                    "    AND wlmesfld.t_TpFieldID = wltpfld.t_TpFieldID ";

       params = makeArray( SQLParam("RlsFieldID", p_RlsFieldID));
       rs = execSQLselect( query, params );
                             
       if( rs and rs.moveNext() )
         NameField = rs.Value(0);
         Namelen = StrLen(NameField);
         ValueLen = rs.Value(2);
         if(rs.Value(1) != 0)
            ListValue = GetWlcodeDescription(rs.Value(1), ValueField);
            if (ListValue != "")
               ListField = true;
            end;
         end;
       else
         // Если не найдено, название поля равно коду поля
         NameField = CodeField;
         Namelen = StrLen(NameField);
       end;    
     else
       //Если поля нет в релизе получить название поля из справочника поле и длину поля
       query = " select wltpfld.t_Description, wltpfld.t_LenEdit "
                    "   from dwltpfld_dbt wltpfld  "
                    "  where wltpfld.t_TpID = :TpID "
                    "    AND wltpfld.t_Name = :Name ";

       params = makeArray( SQLParam("TpID", p_TpID), SQLParam("Name", p_FieldCode));
       rs = execSQLselect( query, params );
                             
       if( rs and rs.moveNext() )
         NameField = rs.Value(0);
         ValueLen = rs.Value(1);
       else
         // Если не найдено, название поля равно коду поля
         NameField = CodeField;
       end;
       NameLen = StrLen(NameField);
     end;
     TblLen = IfThenElse(ListField, DefListValueLen, ValueLen);
     //Для табличной печати подготовить значение как массив строк
     ValueTblField = StrTransferByWord(IfThenElse(ListField, ListValue, ValueField), TblLen);
     NumLine = ValueTblField.Size;
  end;

  //Печать нетабличного поля
  macro PrintField()
    ValueField = StrSubst( ValueField, "\n", " ");
    println(StrAlign(" ",TabLen) +  StrAlign(NameField, NameLen) + ": ", ( ValueField + IfThenElse(ListField," " + ListValue,"")):w);
  end;                                                                                                   

  //Печать табличного поля
  macro PrintTblField(i:integer) 
    if( i < ValueTblField.Size)
       print(ValueTblField[i], "|");
    else
       print(StrAlign(" ", TblLen), "|");
    end;
  end;

end;

/*класс печати табличного поля*/
class TPrnTblField
  var 
      CodeField: string = "",             // код поля
      NameField: string = "",             // название поля
      NumLine: integer = 1,               // количество строк для вывода значения при табличной печати
      ValueTblField:TArray = TArray,      // название поля для табличной печати
      TblLen = DefValueLen;               // Ширина поля для таблицы
  /* 
  Получить все необходимые данные для печати
  */
  macro FillData(p_FieldCode : string, 
                 p_FieldName : string, 
                 p_TblLen:integer)  

     CodeField = p_FieldCode;
     NameField = p_FieldName;
     TblLen = p_TblLen;
     //Для табличной печати подготовить значение как массив строк
     ValueTblField = StrTransferByWord(NameField, TblLen);
     NumLine = ValueTblField.Size;
  end;

  //Печать значения поля
  macro PrintField(Fld, i:integer, IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("|");
    end;
    if(Fld.CodeField == CodeField)
       Fld.PrintTblField(i);
    else
       print(StrAlign(" ", TblLen), "|");
    end;
    if(IsLast)
       println();
    end;
  end;                                                                                                   

  //Печать названия поля
  macro PrintNameField(i:integer, IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("|");
    end;
    if( i < ValueTblField.Size)
       print(ValueTblField[i], "|");
    else
       print(StrAlign(" ", TblLen), "|");
    end;

    if(IsLast)
       println();
    end;
  end;                                                                                                   

  //Печать верхней рамки
  macro PrintHead(IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("┌");
    end;
    print(StrAlign("─",TblLen, STR_ALIGN_LEFT, "-"), ifThenElse(IsLast, "┐","┬"));
    if(IsLast)
       println();
    end;

  end;                                                                                                   

  //Печать линии раздела
  macro PrintLine(IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("├");
    end;
    print(StrAlign("─",TblLen, STR_ALIGN_LEFT, "-"), ifThenElse(IsLast, "┤","┼"));
    if(IsLast)
       println();
    end;
  end;                                                                                                   

  //Печать нижней рамки
  macro PrintFooter(IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("└");
    end;
    print(StrAlign("─",TblLen,STR_ALIGN_LEFT, "-"), ifThenElse(IsLast, "┘","┴"));
    if(IsLast)
       println();
    end;
  end;                                                                                                   
 
  //Печать пустого поля
  macro PrintEmptyField(IsFirst:Bool, IsLast:Bool) 
    if(IsFirst)
       print("|");
    end;

    print(StrAlign(" ", TblLen), "|");

    if(IsLast)
       println();
    end;
  end;                                                                                                   

end;


/*класс печати блока поля*/

class TPrnBlock
  var 
      CodeBlock: string = "",             // код блока
      NameBlock: string = "",             // название блока
      NameLen: integer = DefNameLen,      // ширина названия блока
      TabLen : integer = 0,               // Величина отступа
      TblPrint: bool = 0,                 // Признак табличной печати
      FieldList: TArray = TArray,         // Список дочерних полей
      BlockList: TArray = TArray,         // Список дочерних блоков
      NumLine: integer = 1,               // Количество строк для табличной печати
      Parent: TPrnBlock = NULL;           // Родительский блок

  /* 
  Получить все необходимые данные для печати
  */
  macro FillData(p_BlockCode : string, 
                 p_TblPrint: bool, 
                 p_MesID:integer, 
                 p_RlsFieldID: integer)  
     CodeBlock  = p_BlockCode;
     TblPrint = p_TblPrint;
     var query, params, rs; 
     if(p_RlsFieldID != 0) // ид первого поля в блоке
       //если блок в релизе, то получить название из него
       query = " select wlmesfld1.t_Description"
                    "   from dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfld1 "
                    "  where wlmesfld.t_FieldID = :RlsFieldID "
                    "    AND wlmesfld.t_Master = wlmesfld1.t_FieldID ";

       params = makeArray( SQLParam("RlsFieldID", p_RlsFieldID));
       rs = execSQLselect( query, params );
                             
       if( rs and rs.moveNext() )
         NameBlock = rs.Value(0);
       else
         NameBlock = CodeBlock;
       end;    
       Namelen = StrLen(NameBlock);
     else
       //получить название блока как название релиза сообщения
       query = " select wlmesrls.t_Description "
                    "   from dwlmesrls_dbt wlmesrls, dwlmes_dbt wlmes  "
                    "  where wlmes.t_MesID = :MesD "
                    "    AND wlmes.t_RlsFormID = wlmesrls.t_RlsFormID ";

       params = makeArray( SQLParam("MesID", p_MesID));
       rs = execSQLselect( query, params );
                             
       if( rs and rs.moveNext() )
         NameBlock = rs.Value(0);
       else
         NameBlock = CodeBlock;
       end;
       Namelen = StrLen(NameBlock);
     end;
  end;

  //Добавить блок
  macro AddBlock(Blk:TPrnBlock)
     Blk.Tablen = Tablen + DefTabStep;
     Blk.Parent = WeakRef(This);
     BlockList[BlockList.Size] = Blk;
  end;


  //Добавить поле
  macro AddField(Fld:TPrnField)
     Fld.TabLen = Tablen;
     FieldList[FieldList.Size] = Fld;
     NumLine = max(NumLine, Fld.NumLine);
  end;
  
  //Найти табличный вложенный блок
  macro FindTblBlock(): integer
     var i = 0;
     while( i < BlockList.Size )
       if(BlockList.Value(i).TblPrint == true)
         return i;
       end;
       i = i + 1;
     end;
     return -1;
  end;

  //Печать блока
  //Сначала печатаются все поля блока
  //Потом печатаются все табличные блоки (табличность для каждого блока задается извне, табличный блок не может иметь вложенных блоков, они не напечатаются)
  //Потом печатаются вложенные нетабличные блоки рекурсивно
  macro PrintBlock()  
     // Название блока
     println();
     if(DefPrintNameBlock)
        println(StrAlign(" ", TabLen) +  StrUpr(NameBlock));
     end;

     //вычислить ширину названия как максимальную среди всех полей для выравнивания
     var MaxLen = arrMax(map(FieldList, "GetLenNameField"));
     //установить для всех полей максимальную ширину названия
     var i = 0;
     while( i < FieldList.Size )
        FieldList.Value(i).NameLen = MaxLen;
        i = i + 1;
     end;
     // Печать полей блока
     foreach(FieldList, "PrnField");

     // Печать табличных блоков
     var t = FindTblBlock();
     while(t != -1)
         var CodeBlk = BlockList[t].CodeBlock;
         var TblBlockList = TArray;
         i = 0;
         for (i, 0, BlockList.Size - 1)
            var blk:TPrnBlock = BlockList.Value(i);
            if (BlockList.Value(i).CodeBlock == CodeBlk)
                TblBlockList[TblBlockList.Size] = blk;
                BlockList[i] = NULL;  //потом удалим обработанные табличные блоки
            end;      
         end;
         // Печать таблицы с табличными блоками
         ExecMacro ("PrintTblBlocks", TblBlockList);
         // Удалить напечатанные блоки
         BlockList = filter(BlockList, "DelNullBlock");
         t = FindTblBlock();
     end;

     // Печать оставшихся нетабличных блоков
     foreach(BlockList, "PrnBlock");
  end;
end;

macro PrnField(Fld:TPrnField)
  Fld.PrintField();
end;

macro GetLenNameField(Fld:TPrnField):integer
  return Fld.NameLen;
end;

macro PrnBlock(Blk:TPrnBlock)
  Blk.PrintBlock();
end;

//Найти в списке табличных полей поле по коду
macro FindField(FldList:TArray, FieldCode): integer
     var i = 0;
     while( i < FldList.Size )
       if(FldList.Value(i).CodeField == FieldCode)
         return i;
       end;
       i = i + 1;
     end;
     return -1;
end;

//Удалить из списка пустые блоки
macro DelNullBlock(Blk:TPrnBlock): bool
     if(Blk == NULL)
         return false;
     end;
     return true;
end;

//Печать группы табличных блоков
//В процедуру уже приходят блоки с одинаковым кодом
macro PrintTblBlocks(BlockList:TArray)
   var TblFieldList = TArray;
   var i:TPrnBlock, j:TPrnField, k = 0;
   var MaxNumLine = 1, NumLine = 0; 

  // Название таблицы как название блока
  println();
  if(DefPrintNameBlock)
      println(StrAlign(" ", BlockList.Value(0).TabLen) +  StrUpr(BlockList.Value(0).NameBlock));
  end;

  //Формируем заголовок
   for(i, BlockList)
     for (j, i.FieldList)
         if(FindField(TblFieldList, j.CodeField) == -1)
             var t = TPrnTblField;
             t.FillData(j.CodeField, j.NameField, j.TblLen);
             MaxNumLine = max(MaxNumLine, t.NumLine);
             TblFieldList[TblFieldList.Size] = t;
         end;
     end;
   end;

   //Печатаем 
   k = 0;
   println;
   //Верхняя рамка
   for(k, 0, TblFieldList.Size - 1)
     TblFieldList.Value(k).PrintHead((k == 0), (k == TblFieldList.Size-1)); 
   end;

   NumLine = 0;
   //Сам заголовок
   for (NumLine, 0, MaxNumLine-1)
     k = 0;
     for(k, 0, TblFieldList.Size - 1)
         TblFieldList.Value(k).PrintNameField(NumLine,(k == 0), (k == TblFieldList.Size - 1) ); 
     end;
   end;

   //Печатаем строки с данными
   for(i, BlockList)
     NumLine = 0;
     //Линия раздела
     k = 0;
     for(k, 0, TblFieldList.Size - 1)
        TblFieldList.Value(k).PrintLine((k == 0), (k == TblFieldList.Size-1)); 
     end;

     //Сами данные
     var n = 0;
     for(n, 0, i.NumLine -1)
       k = 0;
       for(k, 0, TblFieldList.Size - 1)
          var f = FindField(i.FieldList, TblFieldList.Value(k).CodeField);
          if(f != -1)
             TblFieldList.Value(k).PrintField(i.FieldList[f], n,(k == 0), (k == TblFieldList.Size-1));
          else
             TblFieldList.Value(k).PrintEmptyField((k == 0), (k == TblFieldList.Size-1)); 
          end;
       end;
     end;
   end;

   //Печатаем нижнюю рамку
   k = 0;
   for(k, 0, TblFieldList.Size - 1)
     TblFieldList.Value(k).PrintFooter((k == 0), (k == TblFieldList.Size-1)); 
   end;
   println;

end;

/*

var f:TPrnField, f1:TPrnField;
f.FillData("SystemCode", "1", 9, 10666);
//f.PrintField();
f1.FillData("Priority", "111111", 9, 0);
//f1.PrintTblField(0);


var b:TPrnBlock;
b.FillData("AccDoc", false, 0, 10670);
b.AddField(f);
var c:TPrnBlock;
c.FillData("AccDoc", false, 0, 10670);
b.AddBlock(c);
c.AddField(f1);

var d:TPrnBlock;
d.FillData("AccDoc", false, 0, 10670);
c.AddBlock(d);
d.AddField(f1);


b.PrintBlock();
*/  



