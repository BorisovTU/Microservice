/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED379                                     */
/*                                                                          */
/*  Имя файла: ufgd379.mac                                                  */
/*  Создан:    02.11.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdreqliq.mac";

class (TGenDocLiqReq) TGenDocED379()

  InitTGenDocLiqReq(MESKIND_ANSWER, "ED379");

  class LimitInfo
    var PURBIC = "",
        LimitSum = "";
  end;

  var LimitTransKind = "",
      PURBIC         = "",
      LimitInfoArray = TArray();

  macro ReadFields() : bool
    var fieldname = "", fieldvalue = "", blockname = "";
    while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if  (blockname == "")
        if  (fieldname == "EDAuthor")
          OriginatorUIS = fieldvalue;
        elif(fieldname == "EDReceiver")
          RecipientUIS = fieldvalue;
        elif(fieldname == "LimitTransKind")
          LimitTransKind = fieldvalue;
        elif(fieldname == "PURBIC")
          PURBIC = fieldvalue;
        end;
      elif(blockname == "LimitInfo")
        if  (fieldname == beginField)
          LimitInfoArray[ LimitInfoArray.size ] = LimitInfo();
        elif(fieldname == "PURBIC")
          LimitInfoArray[ LimitInfoArray.size-1 ].PURBIC = fieldvalue;
        elif(fieldname == "LimitSum")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitSum = fieldvalue;
        end;
      elif(blockname == "EDRefID")
        if  (fieldname == "EDDate")
          EDDate_InitMes = fieldvalue;
        end;
      end; 
    end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

    return TRUE;
  end;

  macro FillQueries() : bool
    // "/LimitTransKind/<CRLF>/PURBIC/LimitSum<CRLF>"

    wlreq.Queries  = "/" + LimitTransKind + "/";

    if(LimitInfoArray.size < 1)
      std.msg("Не заполнен обязательный блок LimitInfo в сообщении по форме " + FormName);
      return FALSE;
    elif( (LimitTransKind < 1) OR (LimitTransKind > 3) )
      std.msg("Неверное значение LimitTransKind");
      return FALSE;
    end;

    for(var item, LimitInfoArray)
      wlreq.Queries = wlreq.Queries + "\n" + "/" + item.PURBIC + "/" + item.LimitSum;
    end;

    return TRUE;
  end;

  macro OnAfterGenWlreq() : bool    

    var Department : integer = GetDprtByBIC(PURBIC);

    if(Department <= 0)
      std.msg("Не найден филиал с кодом БИК " + PURBIC);
      return FALSE;
    end;

    // обновляются настройки ликвидности для филиала
    
    var DpLiqSet : RsbDpLiqSet = RsbDpLiqSet(Department);

    var Sum : moneyL = $0;
    if( (LimitTransKind == "1") or (LimitTransKind == "3") )
      Sum = StrToMoneyUFBS( LimitInfoArray[0].LimitSum );
    end;

    if  (LimitTransKind == "1")
      DpLiqSet.CurTotalLimit = Sum;
    elif(LimitTransKind == "2")
      var PartyID   : integer = -1,
          DpBilLim  : RsbDpBilLim = DpLiqSet.DpBilLim,
          billimRec : TRecHandler = TRecHandler("dpbillim.dbt");

      for(var item, LimitInfoArray)
        PartyID = ПолучитьКодСубъекта(item.PURBIC, PTCK_BIC);        

        if(PartyID > 0)
          sum = StrToMoneyUFBS(item.LimitSum);
          billimRec.Clear();

          if( DpBilLim.FindByParty(PartyID, billimRec) == 0 )
            billimRec.rec.CurLimit = sum;

            if(DpBilLim.Update(billimRec) != 0)
              std.msg("Ошибка при обновлении настроек двустороннего лимита " + 
                      "для филиала " + PURBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          else
            billimRec.rec.PartyID = PartyID;
            billimRec.rec.CurLimit = sum;

            if(DpBilLim.Insert(billimRec) != 0)
              std.msg("Ошибка при вводе настроек двустороннего лимита " + 
                      "для филиала " + PURBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          end;
        end; // Если субъект найден
      end; // for каждый блок "LimitInfo" 
    elif(LimitTransKind == "3")
      DpLiqSet.CurMultilateralLimit = Sum;
    end;
    
    if(DpLiqSet.Update() != 0)
      std.msg("Ошибка при сохранении настроек ликвидности");
      return FALSE;
    end;

    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       
  
  var gdObj : TGenDocED379 = TGenDocED379();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
