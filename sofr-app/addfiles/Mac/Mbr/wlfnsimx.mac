/*
  $Name: wlfnsimx.mac
  $Module: MBR
  $Description: Импорт сообщений ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Импорт сообщений ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 03.06.2014
//
//-----------------------------------------------------------------------------

import "wlimport.mac", "xmlmestools.mac", "wlmnstls.mac", "wl365pch.mac";

/* Эта ошибка будет выводиться после отчета о загрузке */
var _ОшибкаФорматаИмениФайла : string = "";

FILE f_bnkdprt(bankdprt);
private macro GetBIC_RCC( PartyID )
  f_bnkdprt.PartyID = PartyID;
  if( not GetEQ(f_bnkdprt))
    RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
  end;
  return f_bnkdprt.BIC_RCC;
end;

private macro InitGlobals()
  _ОшибкаФорматаИмениФайла = "";

  // Инициализируем список ошибок
  ImportErrors = TArray();
end;

private macro ParseFileName
( ПолноеИмяФайла : string, 
  RespCodeKind : @string,
  RespCode : @string, 
  TRN : @string,
  FormName : @string,
  RegOrgKind : @integer,
  DateInFileName : @string

) : bool

  var ИмяФайла : string = "", Расширение : string = "";
  SplitFile( ПолноеИмяФайла, ИмяФайла, Расширение );

  FormName = ОпределитьИмяФормыФНС( ИмяФайла, @RegOrgKind );
  if(FormName == "SBK")
    FormName = "SBF";
  end;

  if(InList(FormName, "RPO", "ROO"))    
    TRN = StrSubst( SubStr( ИмяФайла, 4 ), "_", "" );
  elif( InList( FormName, "PNO", "ZSO", "ZSN", "ZSV", "TRB",
                           "APN", "APO", "APZ"
              ) 
      )
    var temp = SubStr( ИмяФайла, 1, Index( ИмяФайла, "_" )-1 );
    temp = SubStr( temp, 1, StrLen(temp) - 7 );
    TRN = temp + StrSubst( SubStr( ИмяФайла, index( ИмяФайла, "_" ) + 1 ), "_", "" );
  else
    TRN = ИмяФайла;
  end;

  if( FormName == "KWT")
    temp = SubStr( ИмяФайла, Index( ИмяФайла, "_" )+1 );
    temp = StrSubst( temp, "_", "" );
    TRN = "KWTFCB_" + temp;
  end;

  RespCode = PTCK_MNS;
  if( FormName == "KWT" )
    RespCode = {MFO_RCC};
    RespCodeKind = PTCK_BIC;
  elif(FormName == "SBF")
    RespCode = SubStr(ИмяФайла, 14, 4);
  elif( InList( FormName, "RPO", "ROO", "PNO", "ZSO", "ZSN", "ZSV", "TRB" ) )
    RespCode = GetBIC_RCC( {ourbank} );
    RespCodeKind = PTCK_BIC;
  else
    RespCode = SubStr(ИмяФайла, 13, 4);
  end;

  const DoCheckPath : string = "PS\\СООБЩЕНИЯ ФНС\\ПРОВЕРКА ФОРМАТА\\ИМЯ ФАЙЛА\\ВЫПОЛНЯТЬ_ПРОВЕРКУ";
  const CheckStrucPath : string = "PS\\СООБЩЕНИЯ ФНС\\ПРОВЕРКА ФОРМАТА\\ИМЯ ФАЙЛА\\СТРУКТУРА";

  if(FormName)

    if( Bnk_GetRegistryValue(DoCheckPath, V_BOOL, true) and
        NeedCheck365P(FormName) and
        Bnk_GetRegistryValue(CheckStrucPath, V_BOOL, true)
      )
      ПроверкаФорматаИмениФайла365П( ИмяФайла, true );

      if( ImportErrors.size > 0 )
        _ОшибкаФорматаИмениФайла = "Неверная структура имени файла сообщения";
      end;
    end;

  else
  
    ImportErrors[ImportErrors.size] = 
      "03;в символах 1-3 имени файла неверно обозначен тип сообщения. Сообщение с типом " + 
      SubStr( ИмяФайла, 1, 3 ) + " не может быть принято банком";
  end;

  if( FormName != "KWT" ) // Для KWT дату берем позже из КВТНОПРИНТ\ДатаВремяПроверки 
    DateInFileName = SubStr(ИмяФайла, 17, ДлинаДаты);
  end;

  return true;
end;

class SelectTpParameters( _FormName, IndividualNodeObj, IndividualEntrepreneurNodeObj, EntityNodeObj )
  var FormName = "",
      Individual = false,
      IndividualEntrepreneur = false,
      Entity = false;
  macro Construct( _FormName, IndividualNodeObj, IndividualEntrepreneurNodeObj, EntityNodeObj )
    FormName = _FormName;
    if( (IndividualNodeObj != null) and (IndividualEntrepreneurNodeObj == null) and (EntityNodeObj == null) )
      Individual = true;
    end;
    if( (IndividualNodeObj == null) and (IndividualEntrepreneurNodeObj != null) and (EntityNodeObj == null) )
      IndividualEntrepreneur = true;
    end;
    if( (IndividualNodeObj == null) and (IndividualEntrepreneurNodeObj == null) and (EntityNodeObj != null) )
      Entity = true;
    end;
  end;

  Construct( _FormName, IndividualNodeObj, IndividualEntrepreneurNodeObj, EntityNodeObj );
end;

macro SelectTpParm( RlsName, params:SelectTpParameters, ClientTypes )
  if( valtype( ClientTypes ) == V_UNDEF )
    ClientTypes = "";
  end;

  if( InList( params.FormName, ФормаZSO, ФормаZSN, ФормаZSV ) )
    if( ClientTypes == "" )
      return true;
    elif( params.Individual )
      if( Index( ClientTypes, "ФЛ" ) )
        return true;
      else
        return false;
      end;
    elif( params.IndividualEntrepreneur )
      if( Index( ClientTypes, "ФП" ) )
        return true;
      else
        return false;
      end;
    elif( params.Entity )
      if( Index( ClientTypes, "ЮЛ" ) )
        return true;
      else
        return false;
      end;
    else
      return true;
    end;
  else
    return true;
  end;
end;

private macro FillWlmes
( Транспорт : integer, 
  ИмяФормы : string,
  node : object, 
  RespCodeKind : integer,
  RespCode : string, 
  TRN : string,
  DateInFileName : string

) : bool

  var error : integer = 0;

  var ВидСообщения : string = "",
      Форма : integer = ОпределитьФорму( Транспорт, ИмяФормы, ВидСообщения );

  var RespID = ПолучитьКодСубъекта( RespCode, RespCodeKind, error );    
  if ( error )
    ErrImport( string("Не найден субъект с кодом ", RespCode) );
    return false;
  end;

  var IndividualNodeObj = GetChildNodeConsideringHierarchy( node, "ПФЛ" );
  var IndividualEntrepreneurNodeObj = GetChildNodeConsideringHierarchy( node, "ПлИП" );
  var EntityNodeObj = GetChildNodeConsideringHierarchy( node, "ПлЮЛ" );

  var selTpParams = SelectTpParameters( ИмяФормы, IndividualNodeObj, IndividualEntrepreneurNodeObj, EntityNodeObj );

  var Релиз : integer = 0;
  var TpShemID : integer = ОпределитьТранспортнуюСхему(RespID, -1, -1, Транспорт, Форма, Релиз, "SelectTpParm", selTpParams, wlsess.TpFrmtID);
  if ( TpShemID == -1 ) 
    Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Не удалось определить транспортную схему для респондента " + RespID + ", транспорта " + Транспорт + ", формы " + Форма );
    return false;      
  end;

  if( not CheckOnDoubling( TRN, Релиз ) )
    return false;
  end;

  ClearRecord( wlmes );
  wlmes.TpSchemID              = TpShemID;
  wlmes.RlsFormID              = Релиз;
  wlmes.Kind                   = ВидСообщения;
  wlmes.TRN                    = TRN;

  
  var ДатаОбр = ReadAttribute(node, "ДатаОбр", "Документ", false);
  var ДатаВремяПроверки = ReadAttribute(node, "ДатаВремяПроверки", "КВТНОПРИНТ", false);

  if( ДатаОбр != "" )
    wlmes.OutsideAbonentDate = StrToDate( ДатаОбр );
  elif( ДатаВремяПроверки != "" )
    wlmes.OutsideAbonentDate = StrToDate( ДатаВремяПроверки );
  elif( IsGoodDateYYYYMMDD(DateInFileName) )
    wlmes.OutsideAbonentDate = ToDateMNS( DateInFileName );
  else
    wlmes.OutsideAbonentDate = date(0, 0, 0);
  end;

  wlmes.Date                   = wlmes.OutsideAbonentDate;
  wlmes.OutsideAbonentCodeKind = RespCodeKind;
  wlmes.OutsideAbonentCode     = RespCode;
  wlmes.OutsideAbonentID       = RespID;
  wlmes.AgentID                = wlmes.OutsideAbonentID;
  wlmes.AgentCodeKind          = wlmes.OutsideAbonentCodeKind;
  wlmes.AgentCode              = wlmes.OutsideAbonentCode;
  wlmes.Importance             = 0;

  if ( not СоздатьЗапись( wlmes ) )
    ErrImport( string("Невозможно создать запись по форме ", ИмяФормы) );
    return false;
  end;

  AddRepElem( wlmes.Kind, ИмяФормы, "RUR", money(0) );
  return true;
end;

class (TFldImportHandler) TFldImportHandlerFnsXml
  InitTFldImportHandler();

  var BIC_fldval : string = "",
      DeptNum_fldval : string = "",
      PayerAcc_fldlist : TArray = TArray(),
      ПризнЗапр: string = "";
  var ЕстьПолеБИКБПл : bool = false;

  macro ProcessAttr( FieldName : string, FieldValue : string )
    if( (FieldName == "БИК") or (FieldName == "БИКБПл") )
      BIC_fldval = FieldValue;

      if(FieldName == "БИКБПл")
        ЕстьПолеБИКБПл = true;
      end;
    elif( (FieldName == "НомФил") or (FieldName == "НомФ") )
      DeptNum_fldval = FieldValue;
    elif( FieldName == "НомСчПл" )
      PayerAcc_fldlist[PayerAcc_fldlist.size] = FieldValue;
    elif( (FieldName == "ПризнЗапр"))
      ПризнЗапр = FieldValue;
    end;
  end;

end;

private macro IsFieldInRls(RlsFormID : integer, FieldName : string) : bool
  var rs : RsdRecordset = execSQLselectPrm
  (
    "select 1 "
    "  from dwlmesfld_dbt msf, dwltpfld_dbt tpf "
    " where msf.t_RlsFormID = :RlsFormID "
    "   and tpf.t_TpFieldID = msf.t_TpFieldID "
    "   and tpf.t_Name      = :FieldName ",
    SQLParam("RlsFormID", RlsFormID),
    SQLParam("FieldName", FieldName)
  );

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

macro ImportMessage(TpID, ImportFileName, addrSess)

  SetBuff( wlsess, addrSess );

  InitGlobals();

  var RespCodeKind : integer = PTCK_MNS, RespCode : string = "", TRN : string = "", 
      RegOrgKind : integer = 0, FormName : string = "", DateInFileName : string = "";
  if( not ParseFileName( ImportFileName, @RespCodeKind, @RespCode, @TRN, @FormName, 
                         @RegOrgKind, @DateInFileName)
    )
    return false;
  end;
  
  var fName, fExt;
  SplitFile( ImportFileName, fName, fExt );
  
  var xml:object = ActiveX( "MSXML.DOMDocument" );

  if( (ImportErrors.size == 0) and (not xml.load(ImportFileName) ) )    
    _ОшибкаФорматаИмениФайла = "Формат файла " + fName + fExt + " не соответствует xsd-схеме";    
    ImportErrors[ImportErrors.size] = "10;" + _ОшибкаФорматаИмениФайла;
  end;

  if( ImportErrors.size > 0 )
    if( FormName != ФормаERROR )
      FormName = ФормаERROR;
      
      AddRepElem( wlmes.Kind, FormName, "RUR", money(0) );
      
      if( not ImportERROR_XML( fName + fExt, TpID, FormName ) )
        return false;
      end;
    end;

    ЗаписатьПолеЛог( "ИмяФайла", fName + fExt );
    ЗаписатьПолеЛог( "ДатаВремяПроверки", YYYY_MM_DD(date()) + "T"  + string(time():f) );
    
    for( var err, ImportErrors )
      ЗаписатьПолеЛогВБлок( "Ошибки", "_Ошибка", err );
    end;
  else 
    // Создать запись wlmes
    if( not FillWlmes(TpID, FormName, xml.DocumentElement, RespCodeKind, RespCode, TRN, DateInFileName) )
      return false;
    end;

    // Записать поля сообщения
    var FldHandler : TFldImportHandlerFnsXml = TFldImportHandlerFnsXml();
    ImportXmlNodeToMes(xml, wlmes.RlsFormID, FldHandler);
    if( RegOrgKind and IsFieldInRls(wlmes.RlsFormID, "_ВидРегОргана") )
      ЗаписатьПолеЛогВБлок( "..", "_ВидРегОргана", string(RegOrgKind) );
    end;

    if (FldHandler.ПризнЗапр == "2")
      FldHandler.DeptNum_fldval = "0";
    end;

    FnsImp_UpdateInsideAbonent( FormName, wlmes, FldHandler.BIC_fldval, 
                                FldHandler.DeptNum_fldval, false, 
                                FldHandler.ЕстьПолеБИКБПл, FldHandler.PayerAcc_fldlist );
  end;

  PrintImportReport();

  if( _ОшибкаФорматаИмениФайла != "" )
    std.msg( _ОшибкаФорматаИмениФайла );
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

