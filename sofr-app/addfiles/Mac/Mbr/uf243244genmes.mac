/*
  $Name:         uf243244genmes.mac
  $Module:       Межбанковские расчеты
  $Description:  Функции для генерации сообщений ED243, ED244
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для генерации сообщений ED243, ED244
//
// Программист  : Чукина Т.А.
//
// Создан       : 11.03.2014
//
//-----------------------------------------------------------------------------
import MesInter, "wltools.mac", "uf243244.mac";

//-----------------------------------------------------------------------------
// Получение данных из учетного объекта
//-----------------------------------------------------------------------------

private macro GetTypeCodeNum(ReqKind : integer) : integer
  var type_code_num : integer = 0;

  if(ReqKind == MESKIND_REQUEST)
    type_code_num = TYPE_CODE_UFBS_ED243;
  elif(ReqKind == MESKIND_ANSWER)
    type_code_num = TYPE_CODE_UFBS_ED244;
  else
    RunError("Неверный вид системного объекта \"Запрос/ответ МБР\"");
  end;

  return type_code_num;
end;

// Разобрать поле wlreq.Queries
private macro ParseQueries259
( Queries: string, 
  ReqKind : integer, 
  EDDefineReqAnsCode: @string
)

  var req_code : string = "";
  var type_code_num : integer = GetTypeCodeNum(ReqKind);
  var code_index : integer = index_wlcode(Queries, type_code_num, @req_code);
  if(code_index)
    EDDefineReqAnsCode = substr(req_code, 4, 2);
  else
    RunError("Не найден код в сообщении");
  end;
end;

private macro GetExplanatoryFields(ReqKind : integer, AddFlds : RsbAddFld, ExplanatoryFields : TArray)

  class (TRsbAddFldIterator) TExplFieldSearch(p_AddFldObj : RsbAddFld, p_Flds : TArray)
    InitTRsbAddFldIterator(p_AddFldObj);
    var ExplanatoryFields : TArray = p_Flds;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if(FldRec.rec.Type == "П")
        var FieldValue : string = Trim(FldRec.rec.Value);

        if(FieldValue)
          ExplanatoryFields[ ExplanatoryFields.size ] = TMesField(FldRec.rec.Number, FieldValue);
        end;
      end;
    end;

  end;

// begin
  var ExplFieldSearch : TExplFieldSearch = TExplFieldSearch(AddFlds, ExplanatoryFields);

  ExplFieldSearch.Go();

end;

private macro GetEDFieldList(AddFlds : RsbAddFld, EDFieldList : TArray)

  class (TRsbAddFldIterator) TRevisFieldSearch(p_AddFldObj : RsbAddFld, p_EDFieldList : TArray)
    InitTRsbAddFldIterator(p_AddFldObj);
    var EDFieldList : TArray = p_EDFieldList;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( (FldRec.rec.Type == "У") and (FldRec.rec.Number or FldRec.rec.Name) )
        var FieldNo : string = IfThenElse(FldRec.rec.Number, FldRec.rec.Number, FldRec.rec.Name);
        FieldNo = IfThenElse( SUBSTR(FieldNo,1,1) != "#", FieldNo, FldRec.rec.Name);
        EDFieldList[ EDFieldList.size ] = TMesField(FieldNo, FldRec.rec.Value);
      end;
    end;

  end;

  TRevisFieldSearch(AddFlds, EDFieldList).Go();
end;

private macro GetEDReestrInfo(AddFlds : RsbAddFld, EDReestrInfo : TArray)

  class (TRsbAddFldIterator) TReestrTrnsSearch(p_AddFldObj : RsbAddFld, p_Trns : TArray)
    InitTRsbAddFldIterator(p_AddFldObj);
    var ReestrTransactions : TArray = p_Trns;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( (FldRec.rec.Type == "Р") and 
          (FldRec.rec.Number == "TransactionID") and 
          (FldRec.rec.Value != "")
        )
        ReestrTransactions[ ReestrTransactions.size ] = TRecHandler("wladdfld.dbt");
        Copy(ReestrTransactions[ ReestrTransactions.size - 1 ], FldRec);
      end;
    end;

  end;

  class (TRsbAddFldIterator) TChildFldsSearch( p_AddFldObj : RsbAddFld, 
                                               p_ParentRec : TRecHandler,
                                               p_NextTrnSort : integer, 
                                               p_FieldList : TArray
                                             )
    InitTRsbAddFldIterator(p_AddFldObj);
    var ParentRec : TRecHandler = p_ParentRec,
        NextTrnSort : integer = p_NextTrnSort,
        FieldList : TArray = p_FieldList;

    private macro IsParentUndef(ReestrRec : TRecHandler) : bool
      // эта ситуация может возникнуть, если в скроллинге доп. полей 
      // пользователь менял тип записи "Р" и "не Р" туда-сюда
      if( (ReestrRec.rec.Number != "TransactionID") and (ReestrRec.rec.ParentID == 0) )
        return true;
      end;

      return false;
    end;

    private macro IsChildRec(ReestrRec : TRecHandler) : bool

      // по проекту
      if(ReestrRec.rec.ParentID == ParentRec.rec.FieldID)
        return TRUE;

      // по ТЗ
      elif( IsParentUndef(ReestrRec)
            and
            (ReestrRec.rec.PaymentID == ParentRec.rec.PaymentID)
          )
        if(ParentRec.rec.PaymentID)
          return TRUE;
        elif( (ReestrRec.rec.Sort > ParentRec.rec.Sort) and
              ((ReestrRec.rec.Sort < NextTrnSort) or (NextTrnSort == 0))
            )
          return TRUE;
        end;
      end;

      return FALSE;
    end;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( (FldRec.rec.Type == "Р") and 
          IsChildRec(FldRec) and
          (FldRec.rec.Number or FldRec.rec.Name)
        )
        var FieldNo : string = IfThenElse(FldRec.rec.Number, FldRec.rec.Number, FldRec.rec.Name);
        FieldNo = IfThenElse( SUBSTR(FieldNo,1,1) != "#", FieldNo, FldRec.rec.Name);
        FieldList[ FieldList.size ] = TMesField(FieldNo, FldRec.rec.Value);
      end;
    end;

  end;

//begin
  var ReestrTransactions : TArray = TArray();
  TReestrTrnsSearch(AddFlds, ReestrTransactions).Go();

  var ReestrItem : TReestrItem, TransactionIDRec : TRecHandler, NextTrnSort : integer,
      MaxIndex : integer = ReestrTransactions.size - 1;
  for(var i : integer, 0, MaxIndex, 1)
    ReestrItem = TReestrItem();
    TransactionIDRec = ReestrTransactions[i];
    if(i < MaxIndex)
      NextTrnSort = ReestrTransactions[i + 1].rec.Sort;
    else
      NextTrnSort = 0;
    end;

    ReestrItem.TransactionID = TransactionIDRec.rec.Value;

    TChildFldsSearch(AddFlds, TransactionIDRec, NextTrnSort, ReestrItem.FieldList).Go();

    EDReestrInfo[ EDReestrInfo.size ] = ReestrItem;
  end;
end;

private macro GetAddFields
  ( wlreq, 
    ExplanatoryFields : TArray,
    EDFieldList : TArray,
    EDReestrInfo : TArray
  )

  var AddFlds : RsbAddFld = RsbAddFld(wlreq.ReqID);
  GetExplanatoryFields(wlreq.Kind, AddFlds, ExplanatoryFields);
  GetEDFieldList(AddFlds, EDFieldList);
  GetEDReestrInfo(AddFlds, EDReestrInfo);

end;

macro ReadDataFromWlreq243244
  ( wlreq, 
    EDDefineReqAnsCode : @string,
    EDDefineReqAnsText : @string,
    AddFldsInfo : TAddFldsInfo
  )

  ParseQueries259(wlreq.Queries, wlreq.Kind, @EDDefineReqAnsCode);

  GetAddFields( wlreq,
                AddFldsInfo.ExplanatoryFields,
                AddFldsInfo.EDFieldList,
                AddFldsInfo.EDReestrInfo
              );

  EDDefineReqAnsText = ПолучитьОписаниеЗапросаОтвета(wlreq);

end;

//-----------------------------------------------------------------------------
// Запись данных в поля сообщения
//-----------------------------------------------------------------------------

private macro GetMesFieldByName(MesFields : TArray, FieldName : string) : TMesField
  for(var MesField : TMesField, MesFields)
    if(MesField.FieldName == FieldName)
      return MesField;
    end;
  end;

  return null;
end;

private macro WriteExplanatoryFields(ExplanatoryFields : TArray, IsTextNodeFun : ProcRef, FormName : string)
  var RlsFlds : TArray = null;
  if( FormName == "ED243" )
    RlsFlds = makeArray
                    ( "AccDocDate", 
                      "AccDocNo",
                      "PayerAcc",
                      "PayeeAcc",
                      "Sum",
                      "PayerName",
                      "PayeeName"
                    );
  elif( FormName == "ED244" )
    RlsFlds = makeArray
                    ( "AccDocDate", 
                      "AccDocNo",
                      "PayerAcc",
                      "PayeeAcc",
                      "PayerINN",
                      "PayeeINN",
                      "Sum",
                      "EnterDate",
                      "PayeeCorrAcc",
                      "PayeeBIC",
                      "PayerLongName",
                      "PayeeLongName",
                      "Purpose",      
                      "Address"
                    );
  else
    RunError("Макропроцедура WriteExplanatoryFields поддерживает только формы ED243, ED244");
  end;

  var IsTextNode : bool;

  // Записывать поля в сообщение нужно в том порядке, в каком они в релизе. Иначе
  // алгоритм системного контроля ошибается при сопоставлении полей сообщения с 
  // полями релиза (см. запрос 259295).
  // Поэтому в цикле идем по списку полей релиза и смотрим, есть ли в учетном объекте
  // данные для заполнения текущего поля релиза. Если есть, то пишем в сообщение
  for(var RlsFldName : string, RlsFlds)
    var MesField : TMesField = GetMesFieldByName(ExplanatoryFields, RlsFldName);

    if( MesField != null )
      IsTextNode = ExecMacro2(IsTextNodeFun, MesField.FieldName);
      WriteFieldLogXML( MesField.FieldName, MesField.FieldValue, IsTextNode );
    end;
  end;

end;

private macro WriteFieldListItem(FieldNo, FieldValue, BlockName)

  StartBlockLogXML( BlockName );
    
  WriteFieldLogXML("FieldNo", FieldNo, true);

  if(FieldValue)
    WriteFieldLogXML("FieldValue", FieldValue, true);
  end;
    
  FinishBlockLogXML( BlockName );

end;

private macro WriteEDFieldList(EDFieldList : TArray, BlockName : string)
  if(not BlockName)
    BlockName = "EDFieldList";
  end;

  for(var MesField : TMesField, EDFieldList)
    WriteFieldListItem( MesField.FieldName, MesField.FieldValue, BlockName );
  end;
end;

private macro WriteEDReestrInfo(EDReestrInfo : TArray)
  var BlockName : string = "EDReestrInfo";

  for(var ReestrItem : TReestrItem, EDReestrInfo)
    StartBlockLogXML( BlockName );

    ЗаписатьПолеЛог( "TransactionID", ReestrItem.TransactionID );

    WriteEDFieldList(ReestrItem.FieldList, "EDReestrFieldList");

    FinishBlockLogXML( BlockName );
  end;
end;

macro WriteEDDefineReqAnsInfo
( FormName : string, 
  EDDefineReqAnsText : @string, 
  AddFldsInfo : TAddFldsInfo
)

  if ( AddFldsInfo.hasData() or EDDefineReqAnsText )
    var InfoBlockName : string, TextFieldName : string, IsTextNodeFun : ProcRef;
    GetFldDiff243244(FormName, @InfoBlockName, @TextFieldName, @IsTextNodeFun);

    StartBlockLogXML( InfoBlockName );

    WriteExplanatoryFields(AddFldsInfo.ExplanatoryFields, IsTextNodeFun, FormName);
    WriteFieldLogXML(TextFieldName, EDDefineReqAnsText, true);
    WriteEDFieldList(AddFldsInfo.EDFieldList);
    WriteEDReestrInfo(AddFldsInfo.EDReestrInfo);

    FinishBlockLogXML( InfoBlockName );
  end;
end;
