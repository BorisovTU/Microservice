/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT001S                                   */
/*                                                                          */
/*  Имя файла: swsm001s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

record Party(party);

macro GenMes(addrMes, addrPm)
  var field_value, DKFlag;
  var RsPaym;
  SetBuff(wlmes,    addrMes);
  SetBuff(wlconf  , addrPm);

  PrintLog(2, "Генерация сообщения по МТ001S");
  RsPaym = RsbPayment( wlconf.DocumentID );
  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  if (wlmes.RelatedRef != "")
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, wlmes.RelatedRef);  /*???*/
  else
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, СсылкаОтсутствует);
  end;

  /* DC */
  DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
  ЗаписатьПолеЛог(ДебетКредитФлагСМФР, DKFlag);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог(TransactionReferenceNumberField, wlmes.TRN);

  /* 21 - Ссылка на документ или сделку */
  if (wlmes.RelatedRef != "")
    ЗаписатьПолеЛог(RelatedReferenceField, wlmes.RelatedRef);
  else
    ЗаписатьПолеЛог(RelatedReferenceField, СсылкаОтсутствует);
  end;

  if(DKFlag == "D")
  /* 25D - дебетуемый счет */
     if(RsPaym.PayerAccount == "" )
        ЗаписатьПолеЛог(DebitAccountIdentificationField, RsPaym.ReceiverBankCorrAcc);
     else
        ЗаписатьПолеЛог(DebitAccountIdentificationField, RsPaym.PayerAccount);
     end;
     /* 25C - кредитуемый счет */
     ЗаписатьПолеЛог(CreditAccountIdentificationField, RsPaym.ReceiverAccount);
  else
     /* 25D - дебетуемый счет */
     ЗаписатьПолеЛог(DebitAccountIdentificationField, RsPaym.PayerAccount);
  /* 25C - кредитуемый счет */
     if(RsPaym.ReceiverAccount == "" )
        ЗаписатьПолеЛог(CreditAccountIdentificationField, RsPaym.PayerBankCorrAcc);
     else
        ЗаписатьПолеЛог(CreditAccountIdentificationField, RsPaym.ReceiverAccount);
     end;
  end;

  /* 32A - дата валютирования, код валюты, сумма */
    field_value = ДатаПоУмолчанию +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  ЗаписатьПолеЛог(ValueDateCurrencyCodeAmountField_A, field_value);

  /* 72 - информация для банка-получателя */
  if( wlconf.Description  != "" )
     ЗаписатьПолеЛог( SenderToReceiverInformationField,  wlconf.Description );
  else
     ЗаписатьПолеЛог( SenderToReceiverInformationField,  " " );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
