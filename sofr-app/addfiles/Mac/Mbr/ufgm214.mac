/*
$Name:           ufgm214.mac
$Module:         Межбанковские расчеты
$Description:    Генерация сообщения ED214 транспорта УФЭБС
*/                                                                            

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения ED214 транспорта УФЭБС
//
// Программист  : Чукина Т.А.
//
// Создан       : 22.10.2014
//
//-----------------------------------------------------------------------------

import WldInter, "wluftool.mac", "ufgenmes.mac", "rsberror.mac";

private macro GetEDReceiverForED214(wlReq) : string
  return GetEDReceiverForWlreq(wlReq, wlReq.RelatedRef);
end;

private macro FindInMesByTrn(Trn : string, MesID : @integer, RlsFormID : @integer) : bool
  MesID = 0;
  RlsFormID = 0;

  var rs = execSQLselect("Select t_MesID, t_RlsFormID  " +
                         "  from dwlmes_dbt            " +
                         " where t_Trn = :Trn          " +
                         "   and t_Direct = 'X'        ",
                         makeArray( SQLParam("Trn", Trn) )
                        );
  if(rs and rs.moveNext)
    MesID = rs.value("t_MesID");
    RlsFormID = rs.value("t_RlsFormID");
    return true;
  end;

  return false;
end;

private macro GetSumED103(MesID : integer) : string
  var Sum : string = "";

  var fieldname : string = XMLField;
  var fieldvalue : string = "";

  if( ПолучитьПоле(MesID, fieldname, fieldvalue) )
    var xml : object = StrToXml(fieldvalue);
    Sum = ReadAttribute(xml, "Sum", "ED103");
  else
    RunError("Не найдено поле " + fieldname + " в сообщении ID = " + MesID);
  end;

  return Sum;
end;

private macro GetAcptCode(Queries : string) : string
  var AcptCode : string = "";

  var pos : integer = index(Queries, "/ACPT");
  if(pos)
    if(SubStr(Queries, pos + 6, 1) == "/")
      AcptCode = SubStr(Queries, pos + 5, 1);
    end;
  end;

  return AcptCode;
end;

macro CheckObj(addrReq)

  record wlReq (wlreq);
  SetBuff( wlReq, addrReq );
  var ErrMsg : string = "";

  var AcptCodeStr : string = GetAcptCode( wlReq.Queries );
  var AcptCode : integer = int(AcptCodeStr);
  if( not InList(AcptCode, 3, 4, 8) )
    ErrMsg = "Не указан или неверно указан  код акцепта. Выберите допустимый код акцепта из справочника по F3 в поле \"Сообщение\"";
    RunError(ErrMsg, ErrMsg);
  end;

  if( InList(AcptCode, 3, 4) )
    var Narrative : string = "", Description : string = "";
    if( not ПрочитатьТекстЗапроса_Ответа(wlReq, Narrative, Description) or not Narrative )
      RsbThrow("Не указана причина отказа в акцепте всей или части суммы требования. При указанном коде акцепта 3 или 4 следует задать причину отказа (характер нарушения договора) в поле \"Описание\"");
    end;
  end;

  var AcptSum : string = getFldValFromQueries( wlReq.Queries, "AcptSum:" ),
      SumPT   : string = getFldValFromQueries( wlReq.Queries, "SumPT:" );

  if(AcptCode == 4)
    if(not AcptSum)
      RsbThrow("При указанном коде акцепта необходимо также задать сумму, на которую акцептовано требование, в  поле ответа \"Сообщение\" в формате AcptSum:<сумма акцепта в копейках>");
    end;
  end;

  if(SumPT and AcptSum)
    if( not StrIsNumber(SumPT) or not StrIsNumber(AcptSum) or 
        (money(AcptSum) >= money(SumPT)) 
      )
      RsbThrow("Неверно указана сумма к акцепту или сумма, предъявленная к оплате. Сумма к акцепту должна быть меньше предъявленной к оплате");
    end;
  end;

  // Значение в поле "Ссылка" учетной части ответа указывает 
  // на сообщение вида "запрос" по форме ED213
  var InMesID : integer = 0, InMesRlsFormID : integer = 0;
  if( not FindInMesByTrn(wlReq.RelatedRef, @InMesID, @InMesRlsFormID)
      or
      (GetUFBSMesFormName(InMesRlsFormID) != "ED213")
    )
    RsbThrow("Не указана или неверно указана ссылка на исходный запрос. Укажите в поле \"Ссылка\" референс исходного запроса по форме ED213");
  end;

  var SumED103 : string = GetSumED103(InMesID);
  if(SumPT != SumED103)
    RsbThrow("Неверно указана сумма, предъявленная к оплате. Она должна совпадать с суммой требования из исходного запроса");
  end;

  // должно обеспечиваться равенство получателя сообщения (EDReceiver) 
  // отправителю исходного сообщения (InitialED.EDAuthor)
  var EDReceiver : string = GetEDReceiverForED214(wlReq),
      InitialED : EDNoDateAuthor = EDNoDateAuthor();
  InitialED.ConstructByTrn(wlReq.RelatedRef);
  if( EDReceiver != InitialED.EDAuthor )
    RsbThrow("Получатель сообщения EDReceiver должен совпадать с автором исходного сообщения. Уточните получателя или ссылку на исходное сообщение");
  end;

  return TRUE;

  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var AcptCode : string = GetAcptCode(wlReq.Queries);

  // Заполнение атрибутов поля "xml" сообщения
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object = xml.createElement("ED214");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);
  mes.setAttribute("EDReceiver", GetEDReceiverForED214(wlReq));

  mes.setAttribute("AcptCode", AcptCode);
  mes.setAttribute("SumPT", getFldValFromQueries(wlReq.Queries, "SumPT:"));

  if( AcptCode == "4" )
    mes.setAttribute("AcptSum", getFldValFromQueries(wlReq.Queries, "AcptSum:"));
  end;

  var elem : object = null;

  var Narrative : string = "", Description : string = "";
  if( ПрочитатьТекстЗапроса_Ответа(wlReq, Narrative, Description) and Narrative )
    var Violation : string = substr(Narrative, 1, 298);
    elem = xml.createElement("Violation");
    elem.appendChild( xml.createTextNode(TransTextField(Violation)) );
    mes.appendChild(elem);
  end;
  
  elem = xml.createElement("InitialED");  
  FillEDNoDateAuthorByRef_XMLField(elem, wlReq.RelatedRef);
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
