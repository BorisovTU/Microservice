/*
  $Name:         ufgd111.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация платежей по сообщениям УФЭБС ED111
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям УФЭБС ED111                             */
/*                                                                          */
/* Имя файла: ufgd111.mac                                                   */
/* Создан:    12.12.12                                       Chukina T.     */
/****************************************************************************/

import "ufgendoc.mac", PTInter, likepy, oralib, "pmlib.mac", "WLTOOLS.MAC";

// проверка на то, субъект с кодом БИК является филиалом ЦАБС в режиме online
private macro IsDeptCabsOnline(BankID : integer):bool
  var Query: string = " SELECT dp.t_Code "+
                      "   FROM ddp_dep_dbt dp "+
                      "  WHERE dp.t_PartyID = :BankID "+
                      "    AND dp.t_AccessMode = 1 " /*DEPARTMENT_ACCESS_ONLINE*/,
      params: TArray = makeArray( SQLParam( "BankID", BankID ) ),
      rs : RsdRecordset = execSQLselect( Query, params, true );

  return rs.MoveNext();
end;

macro GenDocByVers (addrMes, ver_st : integer) 

  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация платежа по сообщению ED111" );

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var fieldname = "", fieldvalue = "";

  if( not СчитатьПоле( fieldname, fieldvalue ) or (fieldname != XMLField) )
    std.msg("Не найдено поле '"+XMLField+"' в сообщении ED111");
    return FALSE;
  end;

  if( not xml.loadXML(fieldvalue) )
     println( string( "Неверный формат сообщения по форме ED111" ) );
     return FALSE;
  end;

  var PayerBankCode = ReadAttribute(xml, "BIC","MemoOrderPayer"),
      PayerBankID = ПолучитьКодСубъекта (PayerBankCode, PTCK_BIC),
      ReceiverBankCode = ReadAttribute(xml, "BIC","MemoOrderPayee"),
      ReceiverBankID = ПолучитьКодСубъекта (ReceiverBankCode, PTCK_BIC),
      PayerAccount = ReadAttribute(xml, "Acc","MemoOrderPayer"),
      ReceiverAccount = ReadAttribute(xml, "Acc","MemoOrderPayee"),
      ПризнакДебетаКредита = PRT_Credit,
      ПризнакТранзита = 1,
      CorrAccount = "",
      ДопНазначение = "";

  // Определение сторон платежа  
  if( IsDeptCabsOnline(PayerBankID) )
  // Если субъект с кодом вида 3 равным "MemoOrderPayer\BIC" 
  // является филиалом ЦАБС в режиме online
    ПризнакДебетаКредита = PRT_Debet;
    ПризнакТранзита = 0;
  elif( IsDeptCabsOnline(ReceiverBankID) )
  // Если субъект с кодом вида 3 равным "MemoOrderPayee\BIC" 
  // является филиалом ЦАБС в режиме online
    ПризнакДебетаКредита = PRT_Credit;
    ПризнакТранзита = 0;
  else
    var CorsNumber = -1/*ALLCORSCHEMNUMBER*/, CorsFIID = NATCUR;
    if( ОпределитьСхемуРасчетовПоКорсчету( CorsNumber, ReceiverBankID, PayerAccount, CorsFIID, false ) )
      ПризнакДебетаКредита = PRT_Debet;
      ПризнакТранзита = 0;
      ДопНазначение = " //плательщик: БИК "+PayerBankCode+", счет "+PayerAccount+"//";
      CorrAccount   = PayerAccount;
      PayerBankCode = GetBICofCorschemDepartment(CorsNumber, CorsFIID);
      PayerAccount  = "";
    elif( ОпределитьСхемуРасчетовПоКорсчету( CorsNumber, PayerBankID, ReceiverAccount, CorsFIID, false ) )
      ПризнакДебетаКредита = PRT_Credit;
      ПризнакТранзита = 0;
      ДопНазначение = " //получатель: БИК "+ReceiverBankCode+", счет "+ReceiverAccount+"//";
      CorrAccount   = ReceiverAccount;
      ReceiverBankCode = GetBICofCorschemDepartment(CorsNumber, CorsFIID);
      ReceiverAccount  = "";
    end;
  end;

  // Вызвать функцию подбора входящей корсхемы 
  var BankID : integer = IfThenElse(ПризнакДебетаКредита == PRT_Debet, ReceiverBankID, PayerBankID),
      DKFlag : String  = IfThenElse(ПризнакДебетаКредита == PRT_Debet, "Д", "К");
  var Corschem : integer = ПолучитьКорсхемуПоУмолчанию
                           ( BankID, 0, 1, DKFlag, -1, -1, 0, 
                             -1, CorrAccount, NULL, false, wlmes.TpSchemID, ""
                           );
  if( Corschem == -1/*ALLCORSCHEMNUMBER*/ )
    std.msg( "Не определена схема расчетов для респондента в рублях" );
    return FALSE;
  end;  

  // Заполнение полей платежа
  ClearRecord( wlpmpaym );
  ClearRecord( wlpmpropdeb );
  ClearRecord( wlpmpropcred );
  ClearRecord( wlpmrmprop );

  wlpmpaym.DocKind = wlpmpaym.PrimDocKind = WL_INDOC;
  wlpmrmprop.Number = ReadAttribute(xml, "AccDocNo","InitialAccDoc");
  wlpmrmprop.Reference = wlmes.Trn;
  wlpmrmprop.Date = ToDate( ReadAttribute(xml, "AccDocDate","InitialAccDoc") );

  if( СчитатьПоле( fieldname, fieldvalue ) and 
      (fieldname == NodePacketDate) AND (fieldvalue != "") )
    wlpmpaym.ValueDate = ToDate(fieldvalue);
  else
    wlpmpaym.ValueDate = wlmes.OutsideAbonentDate;
  end;

  if(ver_st >= 2016)
    wlpmrmprop.PaymentKind = PM_GetDefaultPaymentKind(WL_INDOC);
  else
    var PaytKind = int(ReadOptinalAttribute(xml,"PaytKind"));
    if( (PaytKind >= 0) and (PaytKind <= 5) )
      wlpmrmprop.PaymentKind = PaytKindToPaymentKind(PaytKind, "ED111");
    else
      std.msg( "|Недопустимое значение атрибута 'PaytKind'" );
      return FALSE;
    end;
  end;

  wlpmpaym.BaseFIID = NATCUR;
  wlpmpaym.BaseAmount = moneyL( ReadAttribute(xml, "Sum") )/100;
  wlpmrmprop.PayerName = ReadNodeText(xml, "MemoOrderPayer/AccName", false);
  wlpmpaym.PayerAccount = PayerAccount;

  InitPMPROP( wlpmpropdeb );
  wlpmpropdeb.DebetCredit = 0;
  InitPMPROP( wlpmpropcred );
  wlpmpropcred.DebetCredit = 1;

  wlpmpropdeb.CodeKind = PTCK_BIC;
  wlpmpropdeb.BankCode = PayerBankCode;
  wlpmpropdeb.IsSender = IfThenElse(ПризнакДебетаКредита == PRT_Debet, "", "X");
  if( (ПризнакДебетаКредита == PRT_Debet) and (ПризнакТранзита == 0) )
    wlpmpropdeb.Group  = PAYMENTS_GROUP_INTERNAL;
  else
    wlpmpropdeb.Group  = PAYMENTS_GROUP_EXTERNAL;
  end;
  if(ПризнакДебетаКредита == PRT_Credit)
    wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmrmprop.PayerCorrAccNostro = CorrAccount;

  wlpmpropcred.CodeKind = PTCK_BIC;
  wlpmpropcred.BankCode = ReceiverBankCode;
  wlpmpropcred.IsSender = IfThenElse(ПризнакДебетаКредита == PRT_Credit, "", "X");
  if( (ПризнакДебетаКредита == PRT_Credit) and (ПризнакТранзита == 0) )
    wlpmpropcred.Group  = PAYMENTS_GROUP_INTERNAL;
  else
    wlpmpropcred.Group  = PAYMENTS_GROUP_EXTERNAL;
  end;
  if(ПризнакДебетаКредита == PRT_Debet)
    wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmrmprop.ReceiverCorrAccNostro = CorrAccount;

  wlpmpaym.ReceiverAccount = ReceiverAccount;
  wlpmrmprop.ReceiverName = ReadNodeText(xml, "MemoOrderPayee/AccName", false);
  wlpmrmprop.ShifrOper = "09";

  var RefDocInfo : string = ReadNodeText(xml, "RefDocInfo", false) + ДопНазначение;
  wlpmrmprop.Ground = substr(RefDocInfo, 1, 600);
  if( strlen(RefDocInfo) > 600 )
    wlpmrmprop.PartyInfo = substr(RefDocInfo, 601);
  end;

  wlpmrmprop.Priority = PM_DefaultMaxPriority();

  // записать входящую корсхему во входящее свойство платежа
  if( (wlpmpropdeb.Group == PAYMENTS_GROUP_EXTERNAL) and (wlpmpropdeb.IsSender == "X") )
    wlpmpropdeb.Corschem    = Corschem;
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  elif( (wlpmpropcred.Group == PAYMENTS_GROUP_EXTERNAL) and (wlpmpropcred.IsSender == "X") )
    wlpmpropcred.Corschem    = Corschem;
    wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;  
  
  wlpmrmprop.SettlementTime = GetSettlementTime( xml, wlmes, wlpmrmprop.SettlementTime );

  // Вставка платежа
  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop, ПризнакДебетаКредита ) )
    std.msg( "Ошибка при вставке платежа" );
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDoc (addrMes)
  return GenDocByVers (addrMes, 0);
end;

macro GenDoc2016 (addrMes)
  return GenDocByVers (addrMes, 2016);
end;
