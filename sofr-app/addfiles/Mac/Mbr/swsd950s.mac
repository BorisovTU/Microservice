/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по SWIFT-SB MT910                                */
/*                                                                          */
/*  Имя файла: swsgd910.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac", "wlchmes.mac";

/* Выписка со счета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  Account          :string,        /* Корсчет */
  StatementNumber  :string,        /* Номер выписки */
  SequenceNumber   :integer,       /* Номер страницы выписки */
  OpeningBalance   :Balance,       /* Открывающий баланс */
  DocExtrLine      :StatementLine, /* Строка выписки */
  ClosingBalance   :Balance,       /* Закрывающий баланс */
  AvailableBalance :Balance,       /* Доступные средства */
  Sender           :Bank,          /* Отправитель */

  /* Вспомогательная информация */
  DebNum           :integer,       /* Число дебетовых в выписке */
  DebTurn          :moneyl,        /* Оборот дебетовых */
  CredNum          :integer,       /* Число кредитовых в выписке */
  CredTurn         :moneyl;        /* Оборот кредитовых */

  TRF              = "";
  Account          = "";
  StatementNumber  = "";
  SequenceNumber   = 0;

  /* Начальная инициализация */
  DebNum = 0;
  CredNum = 0;
  DebTurn = moneyl(0);
  CredTurn = moneyl(0);
end; /* class StatementMessage */

var MT950 : StatementMessage;

macro FillSN(TRN)
  return TRUE;
end;

macro FillRN( RelatedRef )
  return TRUE;
end;

macro FillVT( type:string )
  return TRUE;
end;

macro FillV4( _date:date )
  return TRUE;
end;

macro FillV5( _date:date, _time:time )
  return TRUE;
end;

macro FillV8D( amount:Money )
  MT950.DebTurn = amount;
  return TRUE;
end;

macro FillV8C( amount:Money )
  MT950.CredTurn = amount;
  return TRUE;
end;

macro Fill20( TRF )
  MT950.TRF = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT950.Account = trim(Account);
  return TRUE;
end;

macro Fill28( Number, Page )
  MT950.StatementNumber = Number;
  MT950.SequenceNumber = Page;
  return TRUE;
end;

macro Fill60( DKFlag, DateBal, Cur, Sum )
  MT950.OpeningBalance = Balance( DKFlag, DateBal, Cur, Sum );
  return TRUE;
end;

/* Вставляем заголовок выписки - чтобы было к чему привязывать документы выписки */
macro InsertHead()
  wlhead.Account      = MT950.Account;
  wlhead.FIID         = MT950.OpeningBalance.FIID;
  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account, wlhead.FIID ) )
    return FALSE;
  end;    
  if( not ВставитьЗаголовокВыписки( wlhead ) )
    std.msg("Ошибка при вставке заголовка выписки");
    return FALSE;
  end;
  return TRUE;
end;

/* Вставка документа выписки */
macro InsertStatementLine()
  if( MT950.DocExtrLine.IsSet() )
    ClearRecord(wlconf);
    wlconf.DateValue  = MT950.DocExtrLine.DateVal;
    wlconf.Sum        = MT950.DocExtrLine.Amount;
    wlconf.Number     = MT950.DocExtrLine.AccRef;
    wlconf.RelatedRef = MT950.DocExtrLine.Ref;
    if ( wlconf.Number=="" ) 
       wlconf.Number = wlconf.RelatedRef;
    end;
    wlconf.TransferDate = MT950.DocExtrLine.DateVal; //#118037    
    wlconf.Description = MT950.DocExtrLine.Details;
    wlconf.MessageType = SubStr( MT950.DocExtrLine.TypeOper, 2, 3 );
    if( MT950.DocExtrLine.DKFlag == DEBET_ACCOUNT )
      wlconf.DKFlag = WL_DEBET;
      MT950.DebNum  = MT950.DebNum + 1;
    else
      wlconf.DKFlag = WL_CREDIT;
      MT950.CredNum  = MT950.CredNum + 1;
    end;
    if( not ВставитьДокументВыписки( wlconf ) )
      std.msg("Ошибка при сохранении документа выписки");
      return FALSE;
    end;

    MT950.DocExtrLine = StatementLine();
    return TRUE;
  end;
  return TRUE;
end;

macro Fill61( DocExtr:StatementLine )
  MT950.DocExtrLine = DocExtr;
  return TRUE;
end;

macro Fill62( DkFlag, DateBal, Cur, Sum )
  MT950.ClosingBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill64( DkFlag, DateBal, Cur, Sum )
  MT950.AvailableBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;      

/* Заполнение списка полей формы  MT950*/
var FldSN  = ТПолеФормы( УникальныйНомерСообщенияСМФР,       FIELD_MANDATORY, "ReadTRF",      "FillSN",  "" ),
    FldRN  = ТПолеФормы( НомерСвязанногоСообщенияСМФР,       FIELD_OPTIONAL,  "ReadTRF",      "FillRN",  "" ),

    FldVT  = ТПолеФормы( ТипВыписки,                         FIELD_MANDATORY, "ReadSymbol",   "FillVT",  "" ),
    FldV4  = ТПолеФормы( ДатаПредыдущейВыписки,              FIELD_MANDATORY, "Read30",       "FillV4",  "" ),
    FldV5  = ТПолеФормы( ДатаВремяВыписки,                   FIELD_MANDATORY, "ReadDateTime", "FillV5",  "" ),
    FldV8D = ТПолеФормы( DebetTurn,                          FIELD_MANDATORY, "ReadAmount",   "FillV8D", "" ),
    FldV8C = ТПолеФормы( CreditTurn,                         FIELD_MANDATORY, "ReadAmount",   "FillV8C", "" ),

    Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF",      "Fill20",  "" ),
    Fld25  = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",       "Fill25",  "" ),
    Fld28C = ТПолеФормы( StatementSequnceNumberCField,       FIELD_MANDATORY, "Read28",       "Fill28",  "" ),
    Fld60F = ТПолеФормы( OpeningBalanceField_F,              FIELD_OPTIONAL,  "ReadBal",      "Fill60",  "InsertHead" ),
    Fld61  = ТПолеФормы( StatementLineField,                 FIELD_OPTIONAL,  "Read61",       "Fill61",  "InsertStatementLine" ),
    Fld62F = ТПолеФормы( ClosingBalanceField_F,              FIELD_OPTIONAL,  "ReadBal",      "Fill62",  "" ),
    Fld62M = ТПолеФормы( ClosingBalanceField_M,              FIELD_OPTIONAL,  "ReadBal",      "Fill62",  "" ),
    Fld64  = ТПолеФормы( ClosingAvailableBalanceField,       FIELD_OPTIONAL,  "ReadBal",      "Fill64",  "" );

macro ConstructMT950( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            wasRead = 1;
            if( fld.Name == StatementLineField )
              count = count - 1;
            end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT950.Sender   = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMT950 */

macro GenDoc( addrMes )
  var error, Currency;
  MT950 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация выписки по МТ950");

  ClearRecord(wlhead);

  if( not ConstructMT950( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlhead.Number       = MT950.StatementNumber;
  wlhead.Page         = MT950.SequenceNumber;
  wlhead.DateIn       = MT950.OpeningBalance.DateBal;
  wlhead.DateOut      = MT950.ClosingBalance.DateBal;
  wlhead.RestIn       = MT950.OpeningBalance.Amount;
  wlhead.RestOut      = MT950.ClosingBalance.Amount;
  wlhead.DebetNumDoc  = MT950.DebNum;
  wlhead.DebetTurn    = MT950.DebTurn;
  wlhead.CreditNumDoc = MT950.CredNum;
  wlhead.CreditTurn   = MT950.CredTurn;

  if( not ОбновитьЗаголовокВыписки( wlhead ) )
    std.msg("Ошибка при обновлении заголовка выписки");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
