/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                                                                          */
/*  Поиск исходящего платежа                                                */
/*                                                                          */
/*  Создан:     03.10.00                                                    */
/*  Программист Бурцев А.А.                                                 */
/*                                                                          */
/*  29.01.04 AAV Переписал поиск на SQL                                     */
/****************************************************************************/

import WldInter, OprInter, "wlglobal.MAC", oralib, likepy;

const DIRECT_PAYM_OUT = "",   /* Исходящий платеж */
      DIRECT_PAYM_IN  = "X";  /* Входящий платеж */

const OBJTYPE_WLD_MES = 502;

//Возвращает идентификатор внешнего заголовка платежа wlpm.WlPmID.
//Исходные данные:
//  Ссылка
//  Транспорт 
//  Филиал
macro WldFindPmByTrnTpDep(Trn, Transport, Dep, PaymentID:@integer):integer
  var rset:object = NULL;
  var query:string;

  query = " SELECT wlpm.t_WlPmID, wlpm.t_PaymentID"
           " FROM "
           "   dwlmes_dbt wlmes,"
           "   dwltpshem_dbt wltpshem,"
           "   dwlmeslnk_dbt wlmeslnk," 
           "   dwlpm_dbt wlpm"
           " WHERE"
           "   wlmes.t_Trn = ? AND " //Trn
           "   wlmes.t_Department = ? AND " //Dep
           "   wltpshem.t_TpShemID = wlmes.t_TpSchemID AND"
           "   wltpshem.t_TpID = ? AND" //Transport           
           "   wlmeslnk.t_MesID = wlmes.t_MesID AND" 
           "   wlmeslnk.t_ObjKind = ? AND"
           "   wlpm.t_WlPmID = wlmeslnk.t_ObjID"
           " ORDER BY wlmeslnk.t_ObjID DESC"
          ;

  var params:TArray = makeArray( SQLParam( "", Trn ),
                                 SQLParam( "", Dep ),
                                 SQLParam( "", Transport ),
                                 SQLParam( "", OBJTYPE_PAYMENT)
                               ); 

  rset = execSQLselect( query, params, TRUE );                               

  if( rset and rset.moveNext() )
    if(ValType(PaymentID) != V_UNDEF)
      PaymentID = rset.value(1);
    end;
    return rset.value(0);
  else
    return 0;
  end; 

end;


/* Поиск платежа по транспорту, референсу, дате, и направлению*/
macro WldFindPmByTRN_Tp( TpID, TRN, RespDate, Direct, InitMesID )   
  var DateCond, DirectCond, SysDateCond;
  var rs:object;
  var select:string;
  var params:TArray = TArray();
  
  if( ValType(Direct)==V_STRING )
    DirectCond = " AND mes.t_Direct = ";
    if( Direct == DIRECT_PAYM_OUT )
      DirectCond = DirectCond + "chr(0)";  /* Исходящее сообщение */
    else
      DirectCond = DirectCond + "'X'";     /* Входящее сообщение */
    end;
  else
    DirectCond = "";
  end;

  if( ValType(RespDate)==V_DATE )
    DateCond = " AND mes.t_OutsideAbonentDate = :RespDate";
  else
    DateCond = "";
  end;

  select = "select wlpm.t_PaymentID, wlpm.t_WlPmID, wlpm.t_Direct, mes.t_MesID "+
                           " from dwlpm_dbt wlpm, dwlmes_dbt mes, dwlmeslnk_dbt meslnk, dwltpshem_dbt shem where "+ 
                           " mes.t_TRN = :TRN"+
                           " and mes.t_TpSchemID = shem.T_TpShemID"+
                           " and shem.T_TpID = :TPID"+
                           " AND mes.t_Department =:OperD"+ 
                           DirectCond +
                           DateCond + 
                           " AND mes.t_MesID = meslnk.t_MesID"+
                           " AND meslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                           " AND wlpm.t_WlPmID = meslnk.t_ObjID";
  
  /* Параметры */
  params[params.size] = SQLParam("TRN", TRN);
  params[params.size] = SQLParam("TPID", TPID);
  params[params.size] = SQLParam("OperD",{OperDprt});       
  if( ValType(RespDate)==V_DATE )
    params[params.size] = SQLParam("RespDate",RespDate);
  end;
  params[params.size] = SQLParam("OBJTYPE_PAYMENT",OBJTYPE_PAYMENT);

  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
    wlpm.PaymentID = int(rs.value(0));
    wlpm.WlPmID = int(rs.value(1));
    wlpm.Direct = rs.value(2);    
    /* Ищем платеж и свойства */
    if( FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) == 0 )
       SetParm(4, rs.value(3) );
       return true;
    end;
  end;

  return false; 
end;

/* Определение валюты и схемы расчетов начального платежа по референсу */
macro WldFindPmByTRN( fillPaym:bool, TRN, RespID, RespDate, Direct, SysDate, HisDate )
  var DateCond, DirectCond, SysDateCond, HisDateCond;
  var rs:object;
  var select:string;
  var params:TArray = TArray();
  
  if( valtype(Direct)==V_UNDEF )      
    Direct = DIRECT_PAYM_OUT;
  end;

  /* Направление сообщения */
  if( Direct == DIRECT_PAYM_OUT )
    DirectCond = "chr(0)";  /* Исходящее сообщение */
  else
    DirectCond = "'X'";     /* Входящее сообщение */
  end;

  /* Дата сообщения */
  if( ValType(RespDate)==V_DATE )
     DateCond = " AND mes.t_OutsideAbonentDate = :RDate";
  else
     DateCond = "";
  end;

  /* Системная дата */
  if( ValType(SysDate)==V_DATE )
     SysDateCond = " AND mes.t_SysDate = :SDate";
  else
     SysDateCond = "";
  end;

  if( ValType(HisDate)==V_DATE )
     HisDateCond = " AND :HDate = ( SELECT min(wh.T_SYSDATE) FROM dwlhistor_dbt wh WHERE "
                   "  wh.t_objkind = :ObjMes AND wh.t_objid = mes.t_MesID)";
  else
     HisDateCond = "";
  end;

  select = "select wlpm.t_PaymentID, wlpm.t_WlPmID, wlpm.t_Direct from dwlpm_dbt wlpm, dwlmes_dbt mes, dwlmeslnk_dbt meslnk where "+ 
                           " mes.t_TRN = :TRN"+
                           " AND mes.t_Department = :OperD"+ 
                           " AND mes.t_Direct = " + DirectCond +
                           " AND mes.t_OutsideAbonentID = :RespID"+
                             DateCond+ 
                             SysDateCond+ 
                             HisDateCond+
                           " AND mes.t_MesID = meslnk.t_MesID"+
                           " AND meslnk.t_ObjKind = "+ OBJTYPE_PAYMENT +
                           " AND wlpm.t_WlPmID = meslnk.t_ObjID";
  
  /* Параметры */
  params[params.size] = SQLParam("TRN", TRN);
  params[params.size] = SQLParam("OperD", {OperDprt});
  params[params.size] = SQLParam("RespID", RespID);
  if( ValType(RespDate)==V_DATE )
    params[params.size] = SQLParam("RDate", RespDate);
  end;
  if( ValType(SysDate)==V_DATE )
    params[params.size] = SQLParam("SDate",SysDate);
  end;
  if( ValType(HisDate)==V_DATE )
    params[params.size] = SQLParam("HDate",HisDate);
    params[params.size] = SQLParam("ObjMes",OBJTYPE_WLD_MES);
  end;

  /* Обнуляем ссылку на платеж */
  wlpm.PaymentID = 0;
  wlpm.WlPmID    = 0;

  rs = execSQLselect( select, params, FALSE );

  if( rs and rs.MoveNext() )
    wlpm.PaymentID = int(rs.value(0));
    wlpm.WlPmID    = int(rs.value(1));
    wlpm.Direct    = rs.value(2);    
    /* Ищем платеж и свойства */
    if( fillPaym == true )
      if( FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) == 0 )
         return true;
      end;
    else
      return true;
    end;
  end;

  return false; 
end;

/* Sum - необязательный параметр */
macro WldFindPmByTRN_SUM_REF( IsFillPaym, TRN, ValueDate, FIID, Corschem, Sum )
  var CorsCond;
  var rs:object;
  var select:string;
  var params:TArray = TArray();

  if ( (valType(Corschem)==V_UNDEF) OR (Corschem<0) )
     CorsCond = "";
  else
     CorsCond = " AND wlpm.t_corschem = :Corschem";
  end;
  
  if ( valType(Sum)==V_UNDEF )
     select = "select wlpm.t_PaymentID, wlpm.T_wlpmID, wlpm.t_Direct from dwlpm_dbt wlpm, dwlmeslnk_dbt meslnk, dwlmes_dbt mes where "+ 
                              " wlpm.T_PAYFIID =:FIID"+
                              CorsCond+
                              " AND meslnk.t_ObjID = wlpm.t_WlPmID"+
                              " AND meslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                              " AND mes.T_MESID = meslnk.T_MESID"+
                              " AND mes.t_TRN =:TRN"+
                              " AND mes.t_Department = :OperD"+ 
                              " AND wlpm.T_VALUEDATE = :ValueDate";
  else
     select = "select wlpm.t_PaymentID, wlpm.T_wlpmID, wlpm.t_Direct from dwlpm_dbt wlpm, dwlmeslnk_dbt meslnk, dwlmes_dbt mes, dpmpaym_dbt pm where "+ 
                              " wlpm.T_PAYFIID =:FIID"+
                              CorsCond+
                              " AND meslnk.t_ObjID = wlpm.t_WlPmID"+
                              " AND meslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                              " AND mes.T_MESID = meslnk.T_MESID"+
                              " AND mes.t_TRN = :TRN"+
                              " AND mes.t_Department = :OperD"+ 
                              " AND wlpm.T_VALUEDATE = :ValueDate"+
                              " AND pm.T_PaymentID = wlpm.T_PaymentID"+
                              " AND pm.t_payAmount = :Sum";
  end;
  
  /* Параметры */
  params[params.size] = SQLParam("FIID", FIID);
  if ( (valType(Corschem)!=V_UNDEF) and (Corschem>=0) )
      params[params.size] = SQLParam("Corschem", Corschem);
  end;
  params[params.size] = SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT);
  params[params.size] = SQLParam("TRN", TRN);
  params[params.size] = SQLParam("OperD",{OperDprt});
  params[params.size] = SQLParam("ValueDate",ValueDate);
  if ( valType(Sum) )
     params[params.size] = SQLParam("Sum",Sum);
  end;
  
  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
    wlpm.PaymentID = int(rs.value(0));
    wlpm.WlPmID = int(rs.value(1));
    wlpm.Direct = rs.value(2);    
    /* Ищем платеж и свойства */
    if( IsFillPaym )
      if( FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) == 0 )
         return true;
      end;
    else
      return true;
    end;
  end;

  return false; 
end;

/* По входящему подтверждению находит платеж (на который пришло подтверждение) и вставляет запись
   о необходимости квитовки */
macro НайтиИСквитоватьПлатежи( TRN:string, ValueDate:date, Sum:money, FIID:integer, Corschem:integer )
  var rs:object;
  var select:string;
  var params:TArray = TArray();

  if ( WldFindPmByTRN_SUM_REF(false, TRN, ValueDate, FIID, Corschem, Sum) )
     if ( not СквитоватьПлатеж(wlpm.WlPmID) )
        std.msg( string( "WlPmID = ", wlpm.WlPmID) );
        RunError( "|Ошибка при квитовке платежа" );
     end;
  else
    select = "select wlpm.t_PaymentID, wlpm.T_wlpmID, wlpm.t_Direct "+
             "from dwlpm_dbt wlpm, dwlmeslnk_dbt meslnk, dwlmes_dbt mes "+
             "where     wlpm.T_PAYFIID   = :FIID"+
                  " AND wlpm.t_corschem  = :Corschem"+
                  " AND meslnk.t_ObjID   = wlpm.t_WlPmID"+
                  " AND meslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                  " AND mes.T_MESID      = meslnk.T_MESID"+
                  " AND mes.t_TRN        = :TRN"+
                  " AND wlpm.T_VALUEDATE = :ValueDate";

    params = makeArray( SQLParam("FIID", FIID),
                        SQLParam("Corschem", Corschem),
                        SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT),
                        SQLParam("TRN", TRN),
                        SQLParam("ValueDate", ValueDate));
    rs = execSQLselect( select, params, FALSE );

    while( rs.MoveNext() )
      wlpm.PaymentID = int(rs.value(0));
      wlpm.WlPmID = int(rs.value(1));
      wlpm.Direct = rs.value(2);
      if( not СквитоватьПлатеж(wlpm.WlPmID) )
         std.msg( string( "WlPmID = ", wlpm.WlPmID) );
         RunError( "|Ошибка при квитовке платежа" );
      end;
    end;
  end;

  /* Если не нашли, то не считаем ошибкой */
  return true;
end;

// По входящему подтверждению находит платеж (на который пришло подтверждение) и проверяет явл. ли он 
// исходящим акцептным требованием
macro IsConfirmAcceptDemand( TRN:string, ValueDate:date, Sum:money, FIID:integer, Corschem:integer )
  var rs:object;
  var select:string;
  var params:TArray = TArray();

  if( WldFindPmByTRN_SUM_REF(true, TRN, ValueDate, FIID, Corschem, Sum) ) // ищем платеж
    // является ли платёж исходящим требованием?
    if( (wlpmpropdeb.Group == PAYMENTS_GROUP_EXTERNAL) AND (wlpmpropdeb.IsSender != "X") AND 
        (wlpmpropcred.Group != PAYMENTS_GROUP_EXTERNAL) )
      // проверяем является ли требование акцептным
      if( wlpmpaym.DocKind == DLDOC_BANKCLAIM )
        select = "select 1 "+
                   "from dpmdemand_dbt dem "+
                  "where dem.t_PaymentID  = :PaymentID " +
                    "and dem.t_AcceptTerm = 0 ";
      else
        select = "select 1 "+
                   "from dpspaydem_dbt dem "+
                  "where dem.t_PaymentID  = :PaymentID "+
                    "and dem.t_AcceptTerm = 0 ";
      end;
      
      params = makeArray( SQLParam("PaymentID", wlpmpaym.PaymentID) );
      rs = execSQLselect( select, params, FALSE );
      
      if( rs.MoveNext() )
        return true;
      end;
    end;
  end;

  return false;
end;

macro НайтиСообщениеПоНомеру( TRN:string ):bool
  var rs:object;
  var select:string;
  var params:TArray = TArray();

  select = "select 1 "+
           "from dwlmes_dbt mes "+
           "where mes.t_TRN = :TRN ";
  
  params = makeArray( SQLParam("TRN", TRN) );
  rs = execSQLselect( select, params, FALSE );
  
  if( rs.MoveNext() )
    return true;
  end;

  return false;
end;
