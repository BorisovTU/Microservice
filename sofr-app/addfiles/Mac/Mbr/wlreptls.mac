/*
 $Name:        wlreptls.mac
 $Module:      MBR
 $Description: Общая часть отчетов
*/

import PaymInter, "globals.mac", "wldoc.mac";

FILE f_wlpmrmprop( pmrmprop ) key 0;

const
    Ошибка    = 0,
    Первый    = 1,
    Последний = 2,
    Выписка   = 1,
    Уведомление = 2,
    Запрос      = 1,
    Ответ       = 2; 

/* Настройка по умолчанию */
const ПечататьСообщениеОбУспешномВыполнении = true;


macro FillReportData( firstdoc, ТипОбъекта, НомерДокумента, Kind )

  if( ТипОбъекта == OBJTYPE_HEAD ) /* Выписки, уведомления */
    SetBuff( wlhead, firstdoc );
    SetParm( 2, wlhead.Number );  
    SetParm( 3, wlhead.Kind );  
  elif( ТипОбъекта == OBJTYPE_CONF ) /*Подтверждения*/
    SetBuff( wlconf, firstdoc );
    SetParm( 2, wlconf.Number );
  elif( ТипОбъекта == OBJTYPE_REQ )  /*Запросы/Ответы*/
    SetBuff( wlreq, firstdoc );
    SetParm( 2, wlreq.TRN );
    SetParm( 3, wlreq.Kind );  
  elif( ТипОбъекта == OBJTYPE_INFO ) /*Информационные сообщения*/
    SetBuff( wlinfo, firstdoc );
    SetParm( 2, wlinfo.TRN );
  elif( ТипОбъекта == OBJTYPE_MES )
    SetBuff( wlmes, firstdoc );
    SetParm( 2, wlmes.TRN );
  /*elif( ТипОбъекта == OBJTYPE_CONVDEAL )*/
  elif( ТипОбъекта == OBJTYPE_PAYMENT )
    SetBuff( wlpm, firstdoc );
    f_wlpmrmprop.PaymentID = wlpm.PaymentID;
    if( GetEQ(f_wlpmrmprop) )
      SetParm( 2, f_wlpmrmprop.Number );
    else
      SetParm( 2, "" );
    end;
  end;

end;

MACRO SelectActionReport (
                            Вызов,
                            firstdoc,
                            ТипОбъекта,
                            НомерДокумента,
                            Статус,
                            Сообщение,
                            ЗаголовокОтчета,
                            СообщениеОбУспешномВыполнении,
                            ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
                         )
  if( Вызов == Первый )
    [   #########################################################################################](ЗаголовокОтчета:l);
    [   Дата обработки: ##########  ########                                                     ](date:f, time);
    [   Операционист:   #####                                                                    ]({oper});
    [                                                                                            ];
    [+-----------------------------+------+------------------------------------------------------------+];
    [|         Номер               |Номер |                 Сообщение                                  |];
    [|       документа             |ошибки|                                                            |];
    [+-----------------------------+------+------------------------------------------------------------+];
  elif( Вызов == Ошибка )
    if( Статус == 0 )
      Статус = "";
      if ( ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете )
         Сообщение = СообщениеОбУспешномВыполнении;
      end;
    end;

    [|#############################|######|############################################################|](НомерДокумента,Статус,StrSubst(Сообщение, "|", " " ):w);
  elif (Вызов==Последний)
    [+-----------------------------+------+------------------------------------------------------------+];
  end;
end;