/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*               Экспорт сообщений МЦИ в формате R-макета                   */
/*                                                                          */
/*  Имя файла: wlmciout.mac                                                 */
/*  Создан:  08.08.00                                            AVM        */
/****************************************************************************/

import "wlexport.mac";

macro ЗаписатьСообщение()
  var field, buff;

  while( СчитатьПоле( field, buff ) )
    if( not ЗаписатьСтроку( buff ) )
      ErrExport("Ошибка записи поля сообщения: " + string(field));
      return false;
    end;
  end;

  return true;
end;

/***************************************************************************/
/* Макрос экспорта                                                         */
/***************************************************************************/
macro MCIOutProc
  var continue0 = TRUE, Документов = 0, err;

  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if( not err )
      ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;

  /* Последовательно считываем сообщения и формируем файл экспорта */
  while( continue0 )
    if( not ЗаписатьСообщение() )
      return false;
    end;
    if( not СчитатьЗапись( wlmes, err, false ) )
       if ( not err )
          continue0 = FALSE;
       else
          ErrExport("Ошибка чтения сообщения");
          return false;
       end;
    end;
    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;

  return true;
end;

