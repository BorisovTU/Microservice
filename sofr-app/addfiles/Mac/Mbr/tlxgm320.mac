/*
$Name:        tlxgm320.mac
$Module:      МБ Кредиты
$Description: Генерация сообщения по TELEX MT320 
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по TELEX MT320                                       */
/*                                                                          */
/*  Имя файла: txgm320.mac                                                  */
/*  Создан:  27.05.2003                                      Панов А.Б.     */
/****************************************************************************/

import "wlgenmes.mac", "swtools.mac", "wltools.mac", OprInter;

var СделкаПривлечения = TRUE; /* Тип сделки: размещение/привлечение */

FILE ПодтвержденияМБК( wldlmm ) key 1;

/* Виды базисов RS-Bank */
const RS_BASIS_30360  = 1,    /* 360 дней в году, 30 в месяце */
      RS_BASIS_ACTACT = 4,    /* в году и в месяце по календарю */
      RS_BASIS_ACT360 = 2,    /* 360 дней в году, в месяце по календ. */
      RS_BASIS_ACT365 = 8,    /* в году и в месяце по календарю без учета високосности */
      RS_BASIS_31360  = 1001; /* 360 дней в году, 31 в месяце */

/* Определение типа операции */
macro ОпределитьТипОперации( dlmmbuf, Sending )
  var Found = FALSE;
  record LastDlMM( wldlmm );
 
  /* Определяем, были ли уже отосланы подтверждения c данным типом, ищем последнее из них */
  ПодтвержденияМБК.DealID = dlmmbuf.DealID;
  ПодтвержденияМБК.Direct = dlmmbuf.Direct;     /* Ищем исходящие */ 
  if( not GetEQ( ПодтвержденияМБК ) ) /* Вообще ничего не отсылалось */
    return TYPE_OPERATION_NEWT; /* Тип - новое подтверждение */
  end;

  /* Что-то уже отсылали, проверяем тип */
  if( ПодтвержденияМБК.Type ==  dlmmbuf.Type )
    Found = TRUE;
    Copy( LastDlMM, ПодтвержденияМБК );
  end;
  /* Перебираем остальные */
  while( (Next(ПодтвержденияМБК)  == TRUE)           AND 
         (ПодтвержденияМБК.DealID == dlmmbuf.DealID) AND 
         (ПодтвержденияМБК.Direct == dlmmbuf.Direct) AND
         (ПодтвержденияМБК.Type   == dlmmbuf.Type) ) 
    Found = TRUE;
    Copy( LastDlMM, ПодтвержденияМБК );
  end;

  setparm( 1, TRUE ); /* Были любые отсылки - нужно заполнять поле 21 */

  if( not Found ) /* С данным типом еще ничего не отсылали */
    return TYPE_OPERATION_NEWT; /* Тип - новое подтверждение */
  end;

  /* Если изменились существенные параметры сделки, считаем что это поправки */
  if( (LastDlMM.DealCodePS != dlmmbuf.DealCodePS) OR
      (LastDlMM.PartyID    != dlmmbuf.PartyID)    OR
      (LastDlMM.PaymAgent  != dlmmbuf.PaymAgent)  OR
      (LastDlMM.DealDate   != dlmmbuf.DealDate)   OR
      (LastDlMM.PFI        != dlmmbuf.PFI)        OR
      (LastDlMM.CFI        != dlmmbuf.CFI)        OR
      (LastDlMM.Start      != dlmmbuf.Start)      OR
      (LastDlMM.Maturity   != dlmmbuf.Maturity)   OR
      (LastDlMM.Expiry     != dlmmbuf.Expiry)     OR
      (LastDlMM.Principal  != dlmmbuf.Principal)  OR
      (LastDlMM.Price      != dlmmbuf.Price)      OR
      (LastDlMM.Basis      != dlmmbuf.Basis)      OR
      (LastDlMM.Duration   != dlmmbuf.Duration)   OR
      (LastDlMM.Pitch      != dlmmbuf.Pitch)      OR
      (LastDlMM.Cost       != dlmmbuf.Cost) )
    return TYPE_OPERATION_AMND;
  end;

  return TYPE_OPERATION_DUPL; /* Иначе считаем, что дубликат */
end;

/* Записать поле */
macro ЗаписатьБанкЛог( BlockName, PaymAgent, CodeKind, CodeValue, BankName, CorrAccount )
  var field_value = "", Tag, tempv, ID, BankCode, error;

  /* Только по наименованию пока банк не заполняем */
  if( (PaymAgent != 0) OR ((CodeKind != 0) AND (CodeValue != "")) )
    if( (CodeKind != 0) AND (CodeValue != "") )
      ID = ПолучитьКодСубъекта( CodeValue, CodeKind, error );
      if( error ) RunError( "|Не найден банк c кодом: " + CodeValue + " и видом кода: " + CodeKind ); end;
    else
      ID = PaymAgent;
    end;

    /* Ищем для субъекта SWIFT-код. Если не нашли - отваливаем,   */
    /* так как настоятельно рекомендуется использовать именно его */
    BankCode = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error );
    if( error ) RunError( "|Не найден SWIFT-код для банка с кодом: "+ CodeValue + " и видом кода: " + CodeKind ); end;
    CodeKind = PTCK_SWIFT;
    
    if( not УстановитьКонтекстБлока( BlockName ) )
      RunError("|Ошибка при установке контекста блока " + BlockName);
    end;

    if( (BankCode != "") AND (CodeKind == PTCK_SWIFT) )
      Tag = SWIFT_Tag_A;
    else
      Tag = SWIFT_Tag_D;
    end;

    if( Tag == SWIFT_Tag_A )
      if( CorrAccount != "" )
        field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
      end;
      field_value = field_value + BankCode;
      ЗаписатьПолеЛог( SubStr( BlockName, 1, 2 ) + Tag, field_value );
    else
      if(CorrAccount != "")
        field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
      end;
      if( BankName != "" )      
        StrChangeRusXSet( BankName, tempv );
        StrMultyToFormat( tempv, tempv, 35, 4 );
        field_value = field_value + tempv;
      end;
      ЗаписатьПолеЛог( SubStr( BlockName, 1, 2 ) + Tag, field_value );
    end;

    if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока '..'");
    end;
  end;

end;

/* Непосредственно генерация МТ320 */
macro GenMes( addrMes, addrWlDlMm )
  var field_value, error, gr, Sending = FALSE, Letter = "", 
      PriceStr, ID, IntPart, FactPart, BankCode;

  SetBuff( wlmes,  addrMes );
  SetBuff( wldlmm, addrWlDlMm );

  PrintLog(2,"Генерация сообщения по МТ320");

  if( isSale( getOperationGroup( wldlmm.DealType ) ) )
    СделкаПривлечения = FALSE;  /* Это сделка размещения */
    Letter = NEGATIVE_SYMB;
  end;

  /* Ключевание. Пока ничего не происходит */
/*  ЗаписатьПолеЛог("15", ""); */

  /* Блок Sequence A */
  УстановитьКонтекстБлокаЛог( "Sequence A" );

  /* 15A New Sequence */
  ЗаписатьПолеЛог( "15A", "" );

  /* 20 Sender's Reference */
  ЗаписатьПолеЛог( "20", wlmes.TRN );

  /* Определяем тип операции */
  field_value = ОпределитьТипОперации( wldlmm, Sending );

  /* 21 Related Reference */
  if( (Sending == TRUE) AND (wlmes.RelatedRef != "") )
    ЗаписатьПолеЛог( "21", wlmes.RelatedRef );
  end;

  /* 22A - Type of Operation */
  ЗаписатьПолеЛог( "22A", field_value );

  /* 22B - Type of Event - этап сделки*/
  if( wldlmm.Type == DLMM_TYPE_CONF )   /* Ввод */
    ЗаписатьПолеЛог( "22B", TYPE_EVENT_CONF );
  elif( wldlmm.Type == DLMM_TYPE_MATU ) /* Ликвидация (завершение) */
    ЗаписатьПолеЛог( "22B", TYPE_EVENT_MATU );
  else                                  /* Пролонгация */
    ЗаписатьПолеЛог( "22B", TYPE_EVENT_ROLL );
  end;

  /* 22C Common Reference */
  ЗаписатьПолеЛог( "22C", wldlmm.DealCodePS );

  /* 21N Contract Number Party A */
  ЗаписатьПолеЛог( "21N", wldlmm.DealCode );

  /* 82a Party A */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  if( error ) RunError( "|Не найден SWIFT-код банка нашего банка" ); end;
  УстановитьКонтекстБлокаЛог( "82a" ); 
  ЗаписатьПолеЛог( "82A", BankCode );

  /* 87a Party B */
  BankCode = ПолучитьКодСубъекта( wldlmm.PaymAgent, PTCK_SWIFT, error );
  if( error ) RunError( "|Не найден SWIFT-код банка-контрагента по сделке" ); end;
  УстановитьКонтекстБлокаЛог( ".." );  /* Возвращаемся в контекст блока 'Sequence A' */
  УстановитьКонтекстБлокаЛог( "87a" );
  ЗаписатьПолеЛог( "87A", BankCode );

  /* Блок Sequence B*/
  УстановитьКонтекстБлокаЛог( "" );
  УстановитьКонтекстБлокаЛог( "Sequence B" );

  /* 15B New Sequence */
  ЗаписатьПолеЛог( "15B", "" );

  /* 17R Party's A Role */
  if( СделкаПривлечения )
    field_value = PARTYS_ROLE_BORROWER; 
  else
    field_value = PARTYS_ROLE_LENDER;   
  end;
  ЗаписатьПолеЛог( "17R", field_value );

  /* 30T Trade Date */
  ЗаписатьПолеЛог( "30T", GetSWIFTDateFull(wldlmm.DealDate) );

  /* 30V Value Date */
  ЗаписатьПолеЛог( "30V", GetSWIFTDateFull(wldlmm.Start) );

  /* 30P Maturity Date */
  ЗаписатьПолеЛог( "30P", GetSWIFTDateFull(wldlmm.Maturity) );

  /* 32B Currency and Principal Amount */
  field_value = ПолучитьКодВалютыЛог( wldlmm.PFI ) +
                GetSWIFTAmount( wldlmm.Principal );
  ЗаписатьПолеЛог( "32B", field_value );

  /* 32H - Amount ti be Settled */
  if( wldlmm.Type == DLMM_TYPE_MATU )
    field_value = Letter;
    field_value = field_value + ПолучитьКодВалютыЛог( wldlmm.PFI ) +
                  GetSWIFTAmount( wldlmm.Principal + wldlmm.Cost );
    ЗаписатьПолеЛог( "32H", field_value );
  end;

  /* 34E Currency and Interest Amount */
  field_value = Letter;
  field_value = field_value + ПолучитьКодВалютыЛог( wldlmm.PFI ) +
                GetSWIFTAmount( wldlmm.Cost );
  ЗаписатьПолеЛог( "34E", field_value );

  /* 37G Interest Rate - записываем значение с учетом того, что ставка хранится в двух полях */
  PriceStr = SubStr( String(wldlmm.Price), 1, Index( String(wldlmm.Price), "." ) - 1 );
  if( wldlmm.Point < StrLen(PriceStr) )
    IntPart = SubStr( PriceStr, 1, StrLen(PriceStr) - wldlmm.Point );
  else
    IntPart = "0";
  end;
  if( (wldlmm.Point + 1) > StrLen(PriceStr) )
    FactPart = MkStr( "0", wldlmm.Point - StrLen(PriceStr)) + PriceStr;
  else
    FactPart = SubStr( PriceStr, StrLen(PriceStr)-wldlmm.Point + 1 );
  end;
  ЗаписатьПолеЛог( "37G", GetSWIFTRate( String(IntPart, ",", FactPart) ) );

  /* 14D Day Count Fraction */
  if( wldlmm.Basis == RS_BASIS_30360 )   /* 360 дней в году, 30 в месяце */
    field_value = SWIFT_BASIS_360_360;
  elif(wldlmm.Basis == RS_BASIS_ACTACT)  /* в году и в месяце по календарю */
    field_value = SWIFT_BASIS_ACT_365;
  elif(wldlmm.Basis == RS_BASIS_ACT360)  /* 360 дней в году, в месяце по календ. */
    field_value = SWIFT_BASIS_ACT_360;
  elif(wldlmm.Basis == RS_BASIS_ACT360)  /* в году и в месяце по календарю без учета високосности */
    field_value = SWIFT_BASIS_ACT_360;
  else                                   /* 360 дней в году, 31 в месяце */
    field_value = SWIFT_BASIS_360_360;
  end;
  ЗаписатьПолеЛог( "14D", field_value );

  /* Блок Sequence C - инструкции по (поставке) оплате сумы сделки стороной A */
  УстановитьКонтекстБлокаЛог( "" );
  УстановитьКонтекстБлокаЛог( "Sequence C" );

  /* 15C New Sequence */
  ЗаписатьПолеЛог( "15C", "" );
  
  if( СделкаПривлечения )
    if( (wldlmm.PrincRetCorrCodeKind != 0) AND ( wldlmm.PrincRetCorrCode != "") )
      /* 53a Delivery Agent - через кого контрагент получит деньги */
      ЗаписатьБанкЛог( "53a", 0, wldlmm.PrincRetCorrCodeKind, wldlmm.PrincRetCorrCode, wldlmm.PrincRetCorrName, wldlmm.PrincRetCorrAccount );

      /* 56a Intermediary */ 
      ЗаписатьБанкЛог( "56a", 0, wldlmm.PrincRetBankCodeKind, wldlmm.PrincRetBankCode, wldlmm.PrincRetBankName, wldlmm.PrincRetAccount );
    end;

    /* 57a - Receiving Agent - куда наш контрагент получит свои деньги */
    ЗаписатьБанкЛог( "57a", wldlmm.PaymAgent, wldlmm.PrincRetCodeKind, wldlmm.PrincRetCode, "", "" );
  else
    if( (wldlmm.PrincCorrCodeKind != 0) AND (wldlmm.PrincCorrCode != "") )
      /* 53a Delivery Agent - через кого контрагент получит деньги */
      ЗаписатьБанкЛог( "53a", 0, wldlmm.PrincCorrCodeKind, wldlmm.PrincCorrCode, wldlmm.PrincCorrName, wldlmm.PrincCorrAccount );

      /* 56a Intermediary */ 
      ЗаписатьБанкЛог( "56a", 0, wldlmm.PrincBankCodeKind, wldlmm.PrincBankCode, wldlmm.PrincBankName, wldlmm.PrincAccount );
    end;

    /* 57a - Receiving Agent - куда наш контрагент получит свои деньги */
    ЗаписатьБанкЛог( "57a", wldlmm.PaymAgent, wldlmm.PrincCodeKind, wldlmm.PrincCode, "", "" );
  end;
  
  /* Блок Sequence D - инструкции по (поставке) оплате сумы сделки стороной B */
  УстановитьКонтекстБлокаЛог( "" );
  УстановитьКонтекстБлокаЛог( "Sequence D" );

  /* 15D New Sequence */
  ЗаписатьПолеЛог( "15D", "" );
  
  if( СделкаПривлечения )
    if( (wldlmm.PrincCorrCodeKind != 0) AND (wldlmm.PrincCorrCode != "") )
      /* 53a Delivery Agent - через кого контрагент получит деньги */
      ЗаписатьБанкЛог( "53a", 0, wldlmm.PrincCorrCodeKind, wldlmm.PrincCorrCode, wldlmm.PrincCorrName, wldlmm.PrincCorrAccount );

      /* 56a Intermediary */ 
      ЗаписатьБанкЛог( "56a", 0, wldlmm.PrincBankCodeKind, wldlmm.PrincBankCode, wldlmm.PrincBankName, wldlmm.PrincAccount );
    end;

    /* 57a - Receiving Agent - куда мы хотим получить деньги */
    ЗаписатьБанкЛог( "57a", {OurBank}, wldlmm.PrincCodeKind, wldlmm.PrincCode, "", "" );
  else
    if( (wldlmm.PrincRetCorrCodeKind != 0) AND ( wldlmm.PrincRetCorrCode != "") )
      /* 53a Delivery Agent - через кого контрагент получит деньги */
      ЗаписатьБанкЛог( "53a", 0, wldlmm.PrincRetCorrCodeKind, wldlmm.PrincRetCorrCode, wldlmm.PrincRetCorrName, wldlmm.PrincRetCorrAccount );

      /* 56a Intermediary */ 
      ЗаписатьБанкЛог( "56a", 0, wldlmm.PrincRetBankCodeKind, wldlmm.PrincRetBankCode, wldlmm.PrincRetBankName, wldlmm.PrincRetAccount );
    end;

    /* 57a - Receiving Agent - куда мы хотим получить деньги */
    ЗаписатьБанкЛог( "57a", {OurBank}, wldlmm.PrincRetCodeKind, wldlmm.PrincRetCode, "", "" );
  end;

  /* Платежные инструкции по оплате процентов */
  /* Заполняем только если не совпадают с инструкцией по оплате ОД сделки */
  if( ((wldlmm.PercentCodeKind     !=  0) AND (wldlmm.PercentCodeKind     != wldlmm.PrincRetCodeKind     )) OR
      ((wldlmm.PercentCode         != "") AND (wldlmm.PercentCode         != wldlmm.PrincRetCode         )) OR
      ((wldlmm.PercentBankCodeKind !=  0) AND (wldlmm.PercentBankCodeKind != wldlmm.PrincRetBankCodeKind )) OR
      ((wldlmm.PercentBankCode     != "") AND (wldlmm.PercentBankCode     != wldlmm.PrincRetBankCode     )) OR
      ((wldlmm.PercentCorrCodeKind !=  0) AND (wldlmm.PercentCorrCodeKind != wldlmm.PrincRetCorrCodeKind )) OR
      ((wldlmm.PercentCorrCode     != "") AND (wldlmm.PercentCorrCode     != wldlmm.PrincRetCorrCode     )) )

    if( СделкаПривлечения )
      /* Блок Sequence E - платежные инструкции по оплате процентов стороной A */
      УстановитьКонтекстБлокаЛог( "" );
      УстановитьКонтекстБлокаЛог( "Sequence E" );

      /* 15E New Sequence */
      ЗаписатьПолеЛог( "15E", "" );
    else
      /* Блок Sequence F - инструкции по оплате процентов стороной B */      
      УстановитьКонтекстБлокаЛог( "" );
      УстановитьКонтекстБлокаЛог( "Sequence F" );

      /* 15F New Sequence */
      ЗаписатьПолеЛог( "15F", "" );
    end;
        
    if( (wldlmm.PercentCorrCodeKind != 0) AND (wldlmm.PercentCorrCode != "") )
      /* 53a Delivery Agent - через кого контрагент получит проценты */
      ЗаписатьБанкЛог( "53a", 0, wldlmm.PercentCorrCodeKind, wldlmm.PercentCorrCode, wldlmm.PercentCorrName, wldlmm.PercentCorrAccount );

      /* 56a Intermediary */ 
      ЗаписатьБанкЛог( "56a", 0, wldlmm.PercentBankCodeKind, wldlmm.PercentBankCode, wldlmm.PercentBankName, wldlmm.PercentAccount );
    end;

    /* 57a - Receiving Agent - куда банк хочет получить свои проценты */
    if( СделкаПривлечения )
      ID = wldlmm.PaymAgent;
    else
      ID = {OurBank};
    end;
    ЗаписатьБанкЛог( "57a", ID, wldlmm.PercentCodeKind, wldlmm.PercentCode, "", "" );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
