/*
  $Name:         fnsgmsbc1x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SBC1 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SBC1 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 28.05.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac", FIInter;

private const vers : integer = 510; // версия формата 5.10

// НомерДог
private macro ЗаписатьПоле_НомерДог(reqchnga)
  var ContractNumber : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ContractNumber = ПолучитьПолеДляАннул("НомерДог");
  else
    var SfCntr : TRecHandler = TRecHandler("sfcontr.dbt"); // договор обслуживания для счета
    if( ПолучитьДоговорОбслуживания(reqchnga.OldAccountFIID, reqchnga.OldAccount, SfCntr) == 0 )
      ContractNumber = SfCntr.rec.Number;
    end;

    if(not ContractNumber)
      ContractNumber = reqchnga.NewContractNumber;
    end;
  end;

  ЗаписатьПолеЛог( "НомерДог", ContractNumber );
end;

// СвСчет
private macro WriteAccInfo(reqchnga, OldAcc)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  var ДатаОткрСч : string = GetAccOpenDateFldVal( reqchnga.OldAccount, 
                                                  reqchnga.OldAccountFIID, 
                                                  OldAcc.Open_Date,
                                                  reqchnga,
                                                  ACCMSG_KIND_CHNG );

  WriteFieldWithCheckAnnul( "НомСч", reqchnga.NewAccount );
  ЗаписатьПолеЛог( "ДатаОткрСч", ДатаОткрСч );
  ЗаписатьПоле_НомерДог(reqchnga);
  ЗаписатьПоле_ДатаЗаклДог( StrToDate(ДатаОткрСч), reqchnga.UnionContrID, 
                            reqchnga.OldAccount, reqchnga.OldAccountFIID, reqchnga );

  FinishBlockLogXML("СвСчет");
end;

// СвНПСтар
private macro WriteTaxPayerOldInfo(EntityCode : string, reqchnga, NewAcc)

  if( (reqchnga.ClientChange == "X") and InList(EntityCode, "1", "2", "5") )
    var ДатаИзмНП : date = reqchnga.ClientChangeDate;

    StartBlockLogXML("СвНПСтар");

    WriteFieldWithCheckAnnul( "ПрИзмНП", string(reqchnga.ClientChangeGround) );
    WriteFieldWithCheckAnnul( "ДатаИзмНП", DDpMMpYYYY(ДатаИзмНП) );

    var IsOK : bool = false, ErrMsg : string = "";
    if(EntityCode == "1")
      IsOK = WriteRusOrgBeforeChng( NewAcc.Client, "НПРО", ДатаИзмНП, @ErrMsg, reqchnga );
    elif( InList(EntityCode, "2", "5") )
      IsOK = WriteIndEmployerBeforeChng( NewAcc.Client, "НПУТ", ДатаИзмНП, EntityCode, @ErrMsg, reqchnga );
    end;

    if(not IsOK)
      if(not ErrMsg)
        ErrMsg = "Ошибка при записи сведений о налогоплательщике до внесения в них изменений или сведений о реорганизованной организации";
      end;

      RsbThrow(ErrMsg);
    end;

    FinishBlockLogXML("СвНПСтар");
  end;

end;

macro CheckObj( addrReq )
  record Req(reqchnga);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckClientKPP,
                                              GenMesCheckClientSizeKPP,
                                              GenMesCheckClientSymbolKPP,
                                              GenMesCheckClientSizeOGRN,
                                              GenMesCheckContractNumber,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality,
                                              GenMesCheckNotMatchPropetis
                                             );
                                             
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_CHNG );
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record buff(reqchnga);
  SetBuff(buff, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SBC1");

  record GenAcc(account); ClearRecord(GenAcc);
  record OldAcc(account); ClearRecord(OldAcc);
  if(buff.Origin == REQ_ACC_ORIGIN_LOANS)
    GenAcc.Client = buff.ClientID;
    OldAcc.Department = buff.Department;
  else
    ПолучитьСчет(buff.NewAccountFIID, buff.NewAccount, GenAcc );
    ПолучитьСчет(buff.OldAccountFIID, buff.OldAccount, OldAcc );
  end;

  var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg( OldAcc.Department, 
                                                         ACCMSG_KIND_CHNG, 
                                                         vers );

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonSBC);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs( CommonSBC, buff.OldAccount, buff.NewAccount, buff.OldAccountFIID, 
                      GenAcc.Client, OldMes.MesID );

  var ErrMsg : string = "";

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonSBC, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  var EntityCode : string = ""; // КодЛица

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo(GenAcc.Client, ACCMSG_KIND_CHNG, null, buff.ClientType311, @EntityCode, buff, NULL, NULL);

  // Файл \ Документ \ СвСчет
  WriteAccInfo(buff, OldAcc);

  // Файл \ Документ \ СвСчСтар
  if(buff.AccountChange == "X")
    StartBlockLogXML("СвСчСтар");
    WriteFieldWithCheckAnnul( "НомСчСтар", buff.OldAccount );
    ЗаписатьПолеЛог         ( "ДатаИзмСч", GetAccChngDate(buff, GenAcc.Open_Date) );
    FinishBlockLogXML("СвСчСтар");
  end;

  // Файл \ Документ \ СвБанкСтар
  WriteOldBankInfo(buff);

  // Файл \ Документ \ СвНПСтар
  WriteTaxPayerOldInfo(EntityCode, buff, GenAcc);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonSBC._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
  end;

  if(CommonSBC._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
