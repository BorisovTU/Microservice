/*
 $Name:        ofkgdflt.mac
 $Module:      MBR
 $Description: Генерация учетного объекта по сообщению формы FAULT
*/

import wlglobal, "wlofkggtools.mac";

private macro GetDate( MesDate:string ):date

  if( StrLen( MesDate ) == 10 ) 
    return date( int( SubStr(MesDate,1,2) ), int( SubStr(MesDate,4,2) ), int( SubStr(MesDate,7,4) ) );
  end;

  return date( 0,0,0 );
end;

private macro GetDateYYYYMMDD( MesDate:string ):date

  if( index(MesDate,"T") > 0 )
    MesDate = SubStr( MesDate, 1, index(MesDate,"T")-1 );
  end;

  if( StrLen( MesDate ) == 10 ) 
    return date( int( SubStr(MesDate,9,2) ), int( SubStr(MesDate,6,2) ), int( SubStr(MesDate,1,4) ) );
  end;

  return date( 0,0,0 );
end;

private macro GetTime( MesDate:string ):time

  return time( int( SubStr(MesDate,12,2) ), int( SubStr(MesDate,15,2) ), int( SubStr(MesDate,18,2) ) );
end;

private macro FindAllSessOutMes( OutMesArr:TArray )
  var select = " SELECT mes.t_mesid, mes.t_sessionid "
               "   FROM dwlmes_dbt mes, dwlsess_dbt sess "
               "  WHERE mes.t_sessionid = sess.t_sessionid "
               "    AND sess.t_sessionid = :SessID ";
  var rs = execSQLselect( select, makeArray( SQLParam("", OutMesArr[OutMesArr.size]) ), TRUE );
  while( rs.MoveNext() )
    OutMesArr[OutMesArr.size] = rs.value(0);
  end;
end;

/* Релиз FAULT */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация квитанции FAULT");
  var field_name, field_value;
  
  // параметры функций генерации уч.объектов
  var mESid             = 0,
      ErrCode           = 0,
      State             = 0,
      ErrDescription    = "",
      CreateDate        = date(),
      CreateTime        = time(),
      FinalPaymentID    = TArray(),
      sing              = 1, //!!!
      RequestMessageID  = "",
      Faultcode         = "",
      Faultstring       = "",
      ResultCode        = 0,
      ResultID          = 0;

  var RegVal = false, error = 0;
  GetRegistryValue( "Межбанковские расчеты//ГИС ГМП//GenObjIncorrSign", V_BOOL, RegVal, error );

  while( СчитатьПоле(field_name, field_value) )    
    if( field_name == "_sing" )
      sing = field_value;
    end;
    if( field_name == "ResultCode" )
      ResultCode = int(field_name);
    end;
    if( field_name == "RequestMessageID" )
      RequestMessageID = field_value;
    end;
    if( field_name == "FinalPaymentID" )
      FinalPaymentID[FinalPaymentID.size] = field_value;
    end;
    if( field_name == "Faultcode" )
      Faultcode = field_value;
    end;
    if( field_name == "Faultstring" )
      Faultstring = field_value;
    end;
  end;

  if( ( RegVal == false ) and ( sing == 0 ) )
    std.msg( string("Ошибка проверки ЭП сообщения-ответа web-сервера СМЭВ. Генерация учетного объекта по сообщению запрещена" ) ); 
    return false;
  end;

  var OutMesArr = TArray();
  
  if( RequestMessageID != "" )
    FindOutMesGG(OutMesArr, wlmes.TpSchemID, RequestMessageID, true );
  else
    for( var PaymID, FinalPaymentID ) // для каждого элемента
      if( FindOutMesGG(OutMesArr, wlmes.TpSchemID, PaymID, false) == true ) // найдём одно исходное сообщение (false)
        FindAllSessOutMes( OutMesArr ); // сюда попадём в случае нахождения строго одного сообщения
      end;
    end;
  end;

  if( OutMesArr.size == 0 ) // добавим элемент, что бы сгенерить один УО
    OutMesArr[0] = 0;
  end;

  for( var OutMesID, OutMesArr )
    if( OutMesID > 0 )
      MesID = OutMesID;
      ErrCode = 1;
      GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_GISGMP, 1, NULL, NULL, ErrDescription );
      State = 100;
    else
      MesID = 0;
      ErrCode = 0;
      ErrDescription = "Не найдено исходное сообщение";
      State = 50;
    end;
    CreateDate = wlmes.Date;
    CreateTime = Time();
    if( not ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID, 1, CreateDate, CreateTime ) )
      std.msg( string( "|Не удалось поместить в отбракованные сообщение ID =  ", MesID ) );
      return false;
    end;

    var ErrList = RsbWlError(0);
    var wlerr_buf = TRecHandler("wlerror.dbt");

    wlerr_buf.rec.ResultID = ResultID;
    wlerr_buf.rec.Code = Faultcode;
    wlerr_buf.rec.Description  = Faultstring;
    ErrList.Insert( wlerr_buf );
    ErrList.TransferSelect( ResultID );
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
