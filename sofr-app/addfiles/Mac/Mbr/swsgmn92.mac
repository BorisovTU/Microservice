/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MTn92                                    */
/*                                                                          */
/*  Имя файла: swsgmn92.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

macro GenMes(addrMes, addrReq)
  var field_value, ini_form, mes_id:integer, mes_date:date, mes_direct:string;

  SetBuff(wlmes, addrMes);
  SetBuff(wlReq, addrReq);

  SB_CheckFIID( wlReq.FIID );

  PrintLog(2,"Генерация сообщения по МТn92");

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  ЗаписатьНомерСвязанногоСообщения_SB( wlreq.RelatedRef, mes_id, mes_date, mes_direct, false );

  /* Запрос на отзыв можно отсылать только по исходящему сообщению */
  if( mes_direct == "X" )
    RunError( String("|Запрос на отзыв можно отсылать только по исходящему сообщению. Сообщение с референсом: ", wlreq.RelatedRef, "является ответным") );
  end;

  /* RT - тип и подтип исходного сообщения */
  ini_form = ПолучитьНазваниеФормыСвязаногоСообщенияSB( mes_id );
  if( ini_form == "" )
    RunError( String("|Не определен тип и подтип исходного сообщения с референсом: ", wlreq.RelatedRef) );
  end;
  ЗаписатьПолеЛог(ТипИПодтипСообщения, ini_form );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  ЗаписатьПолеЛог( RelatedReferenceField, wlReq.RelatedRef );    

  /* 11S МТ and Date of Original Message */
  field_value = substr(ini_form, 1, 4) + GetSWIFTDate(mes_date);   
  ЗаписатьПолеЛог(MTandDateOriginalMessage_SField, field_value);

  if( wlReq.Queries != "")
   StrMultyToFormat(wlReq.Queries, field_value, 50, 35);
   ЗаписатьПолеЛог(NarrativeDescriptionField, field_value);
  end;

  /* MFIELDS - копия обязательных полей исходного сообщения */
  СчитатьЗаполненныеПоля( mes_id, field_value);
  ЗаписатьПолеЛог(CopyMandatoryFields, field_value);

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;