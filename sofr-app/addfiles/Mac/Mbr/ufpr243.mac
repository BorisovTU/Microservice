/*
 $Name: ufpr243.mac
 $Module: MBR
 $Description: Печатная форма сообщения ED243 транспорта УФЭБС
*/
/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Печатная форма сообщения ED243 транспорта УФЭБС                          */
/*                                                                          */
/*  Имя файла: ufpr243.mac                                                  */
/*  Создан:    24.04.12                                                     */
/****************************************************************************/

import BankInter, "wluftool.mac", "uf243244prn.mac";

macro PrintDoc( addrMes, MassCopy )
  
  SetBuff( wlmes, addrMes );
  
  // Создаём xml объект
  //------------------------------------------
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  var Строка;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  
  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
    println( string( "Неверный формат сообщения по форме ED243" ) );
    return false;
  end; 
  
 
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;
  
  // EDNoDateAuthor
  // ED243 Запрос участника
  //------------------------------------------
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  [
    ED243 Запрос участника

    Номер ЭС:    #########
    Дата ЭС:     ########## ]( ed_nda.EDNo, ed_nda.EDDate );

  var Code = ed_nda.EDAuthor;
  var select = "select party.t_Name "+ 
                   "from dparty_dbt party, dpartcode_dbt partcode "+
                          "where party.t_PartyID = partcode.t_PartyID " +
                          "and partcode.t_Codekind =:CodeKind "+
                          "and partcode.t_Code =:Code ";
  var params = makeArray( SQLParam("CodeKind", PTCK_UIS),   
                      SQLParam("Code", Code) );

  var rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    [ Составитель: ########## ################################################ ]( ed_nda.EDAuthor, rs.value(0) );
  end;
   
  Code = "";
  var PartyID = -1, PartyUIS = "", PartyName = "";
  if(wlmes.Direct == "X")
    select = "select partcode.t_Code, party.t_Name "+
              "from dparty_dbt party, dpartcode_dbt partcode, ddp_dep_dbt dp_dep "+
                        "where dp_dep.t_Code =: Department "+
                        "and dp_dep.t_PartyID = party.t_PartyID "+
                        "and partcode.t_PartyID = party.t_PartyID "+
                        "and partcode.t_CodeKind =: CodeKind ";

    params = makeArray( SQLParam("Department", wlmes.Department),
                        SQLParam("CodeKind", PTCK_UIS) );
    rs = execSQLselect( select, params, FALSE );

    if( rs and rs.MoveNext() )
      PartyUIS = rs.value(0);
      PartyName = rs.value(1);
    end;  
  else
    select = "select party.t_PartyID, party.t_Name " +
             "  from dparty_dbt party, dwlreq_dbt wlreq, dwlmeslnk_dbt lnk " +
             " where party.t_PartyID = wlreq.t_RecipientID " +
             "   and wlreq.t_ReqID = lnk.t_ObjID " +
             "   and lnk.t_ObjKind = :ObjKind " +
             "   and lnk.t_MesID = :MesID " +
             "   and lnk.t_Direct = chr(0) ";
    params = makeArray( SQLParam("ObjKind", OBJTYPE_REQ),
                        SQLParam("MesID",   wlmes.MesID) );
    rs = execSQLselect( select, params );
    if(rs and rs.moveNext)
      PartyID = rs.value("t_PartyID");
      PartyName = rs.value("t_Name");
    end;
    PartyUIS = ПолучитьКодСубъекта(PartyID, PTCK_UIS);
  end;
  Code = PartyUIS;
  [ Получатель:  ########## ################################################ ]
  ( Code, PartyName ); 
  
  // Код запроса
  //------------------------------------------
  var RequestCode = ReadAttribute(xml,"EDDefineRequestCode");
  Code = "/УТ"+ RequestCode + "/";
  select = "select wlcodes.t_Description "+
              "from dwlcodes_dbt wlcodes "+
                 "where wlcodes.t_AlgNum =: AlgNum "+
                 "and wlcodes.t_Code =: Code ";
  
  params = makeArray( SQLParam("AlgNum", 49),
                     SQLParam("Code", Code) );
  
  rs = execSQLselect( select, params, FALSE );
  Array aCode;
  if( rs.MoveNext() )
    StrSplit( rs.value(0), aCode, 56, 56, 3 );
    [
      Код запроса: ## ########################################################
                      ########################################################
                      ########################################################]( RequestCode, aCode(0), aCode(1), aCode(2) );
  end;
  
  // Реквизиты ЭПС, поясняющие запрос
  //------------------------------------------
  [

    Реквизиты ЭПС, поясняющие запрос:];
  Array atmpstr;
  var tmpstr = "";
  tmpstr = ReadOptinalAttribute(xml, "AccDocDate", "EDDefineRequestInfo");
  if( tmpstr != "" )
  [    Дата документа:                   ########## ]( tmpstr );
    tmpstr = "";
  end;
  tmpstr = ReadOptinalAttribute(xml, "AccDocNo", "EDDefineRequestInfo");
  if( tmpstr != "" )
  [    Номер документа:                  ### ]( tmpstr );
    tmpstr = "";
  end;
  tmpstr = ReadOptinalAttribute(xml, "Acc", "EDDefineRequestInfo");
  if( tmpstr != "" )
  [    Номер счета:                      #################### ]( tmpstr );
    tmpstr = "";
  end;
  tmpstr = ReadNodeText(xml, "EDDefineRequestInfo/Name", false);       
  if( tmpstr != "" )
  StrSplit( tmpstr, atmpstr, 34, 34, 2 );
  [    Наименование плательщика:         ##################################  ]( atmpstr(0) );
    if(atmpstr(1))
  [                                      ##################################  ]( atmpstr(1) );
    end;
    tmpstr = ""; 
  end;
  tmpstr = ReadOptinalAttribute(xml, "Sum", "EDDefineRequestInfo");
  if( tmpstr != "" )
  [    Сумма:                            ################## ]( tmpstr );
    tmpstr = "";
  end;                                                                 
  
  // Реквизиты ЭПС, поясняющие запрос. Поля
  //------------------------------------------
  var comma = "", MaskPath = "EDDefineRequestInfo/FieldQueryMask",
      Queries = "", i = 0;
  if(ReadOptinalAttribute(xml, "Field60", MaskPath))
    Queries = Queries + " 60";
    comma = ",";
  end;
  if(ReadOptinalAttribute(xml, "Field61", MaskPath))
    Queries = Queries + comma + " 61";
    comma = ",";
  end;
  for (i, 101, 110, 1)
    if(ReadOptinalAttribute(xml, string("Field",i), MaskPath))
      Queries = Queries + comma + i;
      comma = ",";
    end;
  end;
  Array aQueries;
  if(Queries)
    StrSplit( Queries, aQueries, 48, 48, 2 );
  [    Поля                              ######################################
                                         ######################################]( aQueries(0), aQueries(1) );
  end;

  // Идентификаторы исходного ЭПС
  //------------------------------------------
  [
    Идентификаторы исходного ЭПС ];
  tmpstr = ReadAttribute(xml,"EDNo", "OriginalEPD");
  [    Номер ЭС:     ######### ]( tmpstr ); 
  tmpstr = ReadAttribute(xml,"EDDate", "OriginalEPD");
  [    Дата ЭС:      ########## ]( tmpstr );

  tmpstr = ReadAttribute(xml,"EDAuthor", "OriginalEPD"); 
  Code = tmpstr;
  select = "select party.t_Name "+
              "from dparty_dbt party, dpartcode_dbt partcode "+
                "where party.t_PartyID = partcode.t_PartyID "+
                "and partcode.t_CodeKind =: CodeKind "+
                "and partcode.t_Code =: Code"; 
  
  params = makeArray( SQLParam("CodeKind", PTCK_UIS),
                     SQLParam("Code", Code) );
  
  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
  [    Составитель:  ########## ############################################ ]( tmpstr, rs.value(0) );
  end;

  // Перенаправляем вывод обратно
  //------------------------------------------

  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 
  return true;                                                               
end;

macro PrintDoc259( addrMes, MassCopy )
  
  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
 
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;
  
  // EDNoDateAuthor
  // ED243 Запрос участника
  //------------------------------------------
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  [
    ED243 Запрос участника

    Номер ЭС:    #########
    Дата ЭС:     ########## ]( ed_nda.EDNo, DDpMMpYYYY(ed_nda.EDDate) );

  if( ed_nda.EDAuthor )
    [ Составитель: ########## ################################################ ]
    ( ed_nda.EDAuthor, GetPartyNameByUIS(ed_nda.EDAuthor) );
  end;
   
  var Code = "", PartyName = "";

  GetReqMesReceiverUFBS(wlmes, @Code, @PartyName);
  [ Получатель:  ########## ################################################ ]
  ( Code, PartyName ); 
  
  // Печать данных из полей
  PrintFields243244("ED243", wlmes);

  // Перенаправляем вывод обратно
  //------------------------------------------
  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 

  return true;                                                               
end;
