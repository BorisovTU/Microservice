/*
  $Name:         fnsaccmsg_lib.mac
  $Module:       МБР
  $Description:  Функции для сообщений по счетам
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для сообщений по счетам
//
// Программист  : Чукина Т.А.
//
// Создан       : 09.07.2014
//
//-----------------------------------------------------------------------------

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "fnsgenmesx.mac", FIInter;

macro ПолучитьПолеДляАннул(FieldName : string) : string
  // Если выполняется генерация сообщения из процедуры аннулирования,
  // то некоторые поля не генерируются, а берутся из одноименного поля старого сообщения
  return GetFldValue( GetOldfnsMessageID(), FieldName );
end;

macro WriteFieldWithCheckAnnul(FieldName : string, FieldValue : string)
  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул(FieldName, GetOldfnsMessageID());
  else
    ЗаписатьПолеЛог(FieldName, FieldValue);
  end;
end;

// -----------------------------------------------------------------------------------
// Проверки для сообщений по счетам
// -----------------------------------------------------------------------------------
class TCorrectMesFNS
  var КодНОБ : string = "",
      ИННКО : string = "",
      РегНом : string = "",
      НомФ : string = "";

  private macro CheckFld(FieldName : string, FieldValue : string) : bool
    var OldFieldValue : string = GetFldValue( GetOldfnsMessageID(), FieldName );

    if(OldFieldValue != FieldValue)
      if(not AskCorrectAction(FieldName))
        return false; 
      end;
    end;

    return true;
  end;

  macro Check1() : bool
    if( (GetKindActionFNS() == KIND_ACTION_FNS_CORRECT) and
        (GetKindRegOrgan() == REG_ORGAN_KIND_FNS)
      )

      if( not CheckFld("КодНОБ", КодНОБ) )
        return false; 
      end;

      if( not CheckFld("ИННКО", ИННКО) )
        return false; 
      end;

      if( not CheckFld("РегНом", РегНом) )
        return false; 
      end;

      if( not CheckFld("НомФ", НомФ) )
        return false; 
      end;

    end;

    return true;
  end;

  macro Check2(НомСооб : string) : bool
    if( (GetKindActionFNS() == KIND_ACTION_FNS_CORRECT) and
        (GetKindRegOrgan() == REG_ORGAN_KIND_FNS)
      )

      if( not CheckFld("НомСооб", НомСооб) )
        return false; 
      end;
    end;

    return true;
  end;

end;


// -----------------------------------------------------------------------------------
// Функции для генерации сообщений по счетам
// -----------------------------------------------------------------------------------

// общие данные для генерации сообщений по счетам
CLASS TCommonDataAccMsg
( p_AccDprt : integer, 
  p_AccMsgKind : integer, 
  p_vers : integer,
  p_IsIndivid : bool // сообщение генерится по счету физ.лица, не являющегося ИП
)

  // филиал счета заявления
  var AccDprt : integer = p_AccDprt;

  var ИдФайл : string = "",
      ТипИнф : string = "",
      ВерсПрог : string = "",
      ТелОтпр : string = "",
      ДолжнОтпр : string = "",
      ФамОтпр : string = "",
      КолДок : string = "",
      ВерсФорм : string = "",
      ИдДок : string = "",
      КНД : string = "",
      КодНОБ : string = "",
      ДолжнПрБ : string = "",
      ДатаСооб : string = "",
      ТелБанка : string = "",
      _ВидРегОргана : string = "",
      _СпОбм : string = "";

  // идентификатор субьекта - отправителя сообщения (соответствует связанному субъекту 
  // филиала счета заявления )
  var MesSender : integer = 0;

  // ТипИнф
  private macro ПолучитьТипИнф(AccMsgKind : integer) : string
    var ТипИнф : string = "";

    if(AccMsgKind == ACCMSG_KIND_CHNGBNK)
      ТипИнф = "ЕДИНСООБЩБ";
    elif(AccMsgKind == ACCMSG_KIND_CHNG)
      ТипИнф = "СООБЩИЗМЕН";
    else
      ТипИнф = "СООБЩБАНКА";
    end;

    return ТипИнф;
  end;

  // ВерсФорм
  private macro ПолучитьВерсФорм(vers : integer) : string
    var ВерсФорм : string = "";

    if( vers == 501 )
      ВерсФорм = "5.01";
    elif( vers == 502 )
      ВерсФорм = "5.02";
    elif( vers == 510 )
      ВерсФорм = "5.11";
    else
      // поле обязательное, поэтому ошибка, если не смогли заполнить
      RunError("Ошибка программирования: неверное значение параметра vers");
    end;

    return ВерсФорм;
  end;

  // ИдДок
  private macro GetIDDoc(vers : integer) : string
    var IDDoc : string = "";
    if( vers < 510 )
      IDDoc = FILL_AT_EXPORT;
    else
      IDDoc = GetGuidFnsX();
    end;

    return IDDoc;
  end;

  // КНД
  private macro ПолучитьКНД(AccMsgKind : integer, IsIndivid : bool) : string
    var КНД : string = "";

    if( (GetKindActionFNS() == KIND_ACTION_FNS_NULL) and 
        (AccMsgKind != ACCMSG_KIND_OPEN) 
      )
      КНД = ПолучитьПолеДляАннул( "КНД" );
    elif(AccMsgKind == ACCMSG_KIND_CHNGBNK)
      КНД = "1114314";
    elif(AccMsgKind == ACCMSG_KIND_CHNG)
      if(IsIndivid)
        КНД = "1114316";
      else
        КНД = "1114304";
      end;
    else
      if(IsIndivid)
        КНД = "1114315";
      else
        КНД = "1114301";
      end;
    end;

    // поле соответсвует шаблону: <xs:pattern value="[0-9]{7}"/>
    if( not IsNDigits(КНД, 7) )
      RunError("Неверно сформировано значение КНД");
    end;

    return КНД;
  end;

  // Код налогового органа банка
  private macro GetCodeNOB(BankID : integer) : string
    var КодНОБ : string = "";

    if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
      КодНОБ = ПолучитьПолеДляАннул( "КодНОБ" );
    else
      КодНОБ = GetCodeNOBWithChecks(BankID);
    end;

    return КодНОБ;
  end;

  // ДолжнПрБ
  private macro ПолучитьДолжнПрБ(vers : integer, IsIndivid : bool) : string
    var ДолжнПрБ : string = "";

    if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
      ДолжнПрБ = ПолучитьПолеДляАннул( "ДолжнПрБ" );

    else
      var RegName : string = IfThenElse(IsIndivid, "ИМНС_ДОЛЖ_ФЛ", "ИМНС_ДОЛЖ");
      var Error : integer = GetDprtStringRegValForOPENAC(RegName, AccDprt, @ДолжнПрБ);

      if( (Error or not ДолжнПрБ) )
        if(vers < 510)
          ДолжнПрБ = " "; // предписанное поле
        else // обязательное поле
          var ErrMsg : string = "Не задана должность сотрудника, подписывающего сообщения в ФНС";
          if(IsIndivid)
            ErrMsg = ErrMsg + " по счетам физ.лиц";
          end;

          RsbThrow(ErrMsg);
        end;
      end;

    end;

    return ДолжнПрБ;
  end;

  macro Fill(AccMsgKind : integer, vers : integer, IsIndivid : bool)
    // связанный субъект филиала счета заявления
    MesSender = GetDprtPartyID(AccDprt);
    if(MesSender <= 0)
      RunError("Не найден связанный субъект филиала счета заявления");
    end;

    ИдФайл = FILL_AT_EXPORT;
    ТипИнф = ПолучитьТипИнф(AccMsgKind);
    ВерсПрог = "RS-Bank V.6";
    ТелОтпр = FILL_AT_EXPORT;
    ДолжнОтпр = FILL_AT_EXPORT;
    ФамОтпр = FILL_AT_EXPORT;
    КолДок = "1";
    ВерсФорм = ПолучитьВерсФорм(vers);
    ИдДок = GetIDDoc(vers);
    КНД = ПолучитьКНД(AccMsgKind, IsIndivid);
    КодНОБ = GetCodeNOB(MesSender);
    ДолжнПрБ = ПолучитьДолжнПрБ(vers, IsIndivid);

    // ТелБанка
    if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
      ТелБанка = ПолучитьПолеДляАннул( "ТелБанка" );
    else

      ТелБанка = ПолучитьТелефон(MesSender);

      if(not ТелБанка)
        if(vers < 510)
          ТелБанка = " "; // предписанное поле
        else
          RunError("Не задан телефон в адресе банка"); // обязательное поле
        end;
      end;

    end;

    ДатаСооб = FILL_AT_EXPORT;

    // _ВидРегОргана, _СпОбм
    if( (GetKindActionFNS() == KIND_ACTION_FNS_NULL) and (vers >= 510) )
      _ВидРегОргана = ПолучитьПолеДляАннул( "_ВидРегОргана" );
      _СпОбм = ПолучитьПолеДляАннул( "_СпОбм" );
    else
      var RegOrgKind : integer = GetKindRegOrgan();
      if( RegOrgKind )
        _ВидРегОргана = string(RegOrgKind);
      else
        _ВидРегОргана = "";
      end;

      _СпОбм = GetllValueElement( 2300, "Электронно" );
    end;

    return TRUE;
  end;

  // constructor
  if( (p_AccMsgKind != NULL) AND (p_vers != NULL) )
    Fill(p_AccMsgKind, p_vers, p_IsIndivid);
  end;
END;

// атрибуты элемента "Файл"
macro WriteFileAttrs(CommonAccMsg : TCommonDataAccMsg)
  ЗаписатьПолеЛог("ИдФайл", CommonAccMsg.ИдФайл);
  ЗаписатьПолеЛог("ТипИнф", CommonAccMsg.ТипИнф);
  ЗаписатьПолеЛог("ВерсПрог", CommonAccMsg.ВерсПрог);
  ЗаписатьПолеЛог("ТелОтпр", CommonAccMsg.ТелОтпр);
  ЗаписатьПолеЛог("ДолжнОтпр", CommonAccMsg.ДолжнОтпр);
  ЗаписатьПолеЛог("ФамОтпр", CommonAccMsg.ФамОтпр);
  ЗаписатьПолеЛог("КолДок", CommonAccMsg.КолДок);
  ЗаписатьПолеЛог("ВерсФорм", CommonAccMsg.ВерсФорм);
end;

private macro GetPartyAttr_PartyType(PartyID : integer) : integer

  macro HasPartyAttr(sPartyID : string, AttrID : integer) : bool
    var AttrFound : bool = false;
    if( CheckObjAttrPresence(AttrFound, OBJTYPE_PARTY, sPartyID, PARTY_ATTR_GROUP_PTTYPE, AttrID)
        and AttrFound )
      return true;
    end;

    return false;
  end;

  // Для категории "Тип субъекта" может быть задано несколько значений
  // Сначала проверяем основное значение, затем неосновные
  var AttrID : integer = GetPartyMainAttr_PartyType(PartyID);

  // Если основная категория не задана, значит, ни одной не задано. Дальше нет смысла
  if(AttrID == 0)
    return AttrID;
  end;

  // Ищем те значения, по которым можно заполнить поля (КодЛица)
  if( not InList( AttrID, PARTY_ATTR_PTTYPE_NOTARIUS, 
                          PARTY_ATTR_PTTYPE_ADVOCAT,
                          PARTY_ATTR_PTTYPE_MANAGPARTNER )
    )
    var RecParty : TRecHandler = TRecHandler("party.dbt");
    RecParty.rec.PartyID = PartyID;
    var sPartyID : string = UniID(RecParty, OBJTYPE_PARTY);

    if( HasPartyAttr(sPartyID, PARTY_ATTR_PTTYPE_NOTARIUS) )
      AttrID = PARTY_ATTR_PTTYPE_NOTARIUS;
    elif( HasPartyAttr(sPartyID, PARTY_ATTR_PTTYPE_ADVOCAT) )
      AttrID = PARTY_ATTR_PTTYPE_ADVOCAT;
    elif( HasPartyAttr(sPartyID, PARTY_ATTR_PTTYPE_MANAGPARTNER) )
      AttrID = PARTY_ATTR_PTTYPE_MANAGPARTNER;

    // не влияет на КодЛица, но влияет на выбор блока НЕТИНН
    elif( HasPartyAttr(sPartyID, PARTY_ATTR_PTTYPE_KFH) )
      AttrID = PARTY_ATTR_PTTYPE_KFH;
    end;
  end;

  return AttrID;
end;

macro GetTaxPayerPartyData
( PartyID : integer, 
  LegalForm : @integer, 
  IsIndividEmployer : @bool, 
  AttrPartyType : @integer, 
  NotResident : @string
) : bool

  var q : string = "select t_LegalForm, NVL(t_NotResident, chr(0)) as t_NotResident "
                   "  from dparty_dbt "
                   " where t_PartyID = :PartyID ";
  var params : TArray = makeArray( SQLParam("PartyID", PartyID) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    LegalForm = rs.value("t_LegalForm");

    if(LegalForm == PTLEGF_PERSN)
      IsIndividEmployer = IsIndividualEmployer(PartyID);
    else
      IsIndividEmployer = false;
    end;
    
    AttrPartyType = GetPartyAttr_PartyType(PartyID);  

    NotResident = rs.value("t_NotResident");

    return true;
  end;

  return false;
end;

// НомСооб, ТипСооб
private macro ЗаписатьНомСообИТипСооб
( PartyID : integer, 
  Account : string, 
  Account_New : string, 
  FIID : integer, 
  ClientID : integer, 
  OldMesID : integer,
  ChangeBankProps : bool,
  IsSFC : bool
)

  var Серия : string = "", Номер : string = "";

  if( not GetMessageNumber510( PartyID, Account, Account_New, 1, FIID, ClientID, 
                               Серия, Номер, OldMesID, ChangeBankProps, IsSFC )
    )
    var ErrText : string = GetErrMsg();
    if( not ErrText )
      ErrText = "Ошибка при генерации номера сообщения";
    end;
    RunError( ErrText );
  end; 

  ЗаписатьПолеЛог("НомСооб", Серия);
  if( not TCorrectMesFNS().Check2(Серия) )
    RunError("Корректировка значения поля 'НомСооб' не допускается");
  end;

  ЗаписатьПолеЛог("ТипСооб", Номер);

end;

// ФамПрБ
macro ЗаписатьФамПрБ
( AccDprt : integer, // филиал счета заявления
  IsIndivid : bool // сообщение генерится по счету физ.лица, не являющегося ИП
)

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул("ФамПрБ", GetOldfnsMessageID());
    return;
  end;

  var ФамПрБ : string = "";

  var RegVal : string = "",
      RegName : string = IfThenElse(IsIndivid, "ИМНС_ФИО_ФЛ", "ИМНС_ФИО");
  var Error : integer = GetDprtStringRegValForOPENAC(RegName, AccDprt, @RegVal);
  if(Error or not RegVal)
    var ErrMsg : string = "Не заданы ФИО сотрудника, подписывающего сообщения в ФНС";
    if(IsIndivid)
      ErrMsg = ErrMsg + " по счетам физ.лиц";
    end;

    RunError(ErrMsg);
  end;

  var arrFIO : TArray = StrCut(RegVal, ",. ");

  // Фамилия
  ФамПрБ = arrFIO[0];

  // Имя (первая буква)
  if(arrFIO.size > 1)
    ФамПрБ = ФамПрБ + " " + substr( arrFIO[1], 1, 1 );
  end;

  // Отчество (первая буква)
  if(arrFIO.size > 2)
    ФамПрБ = ФамПрБ + " " + substr( arrFIO[2], 1, 1 );
  end;

  ЗаписатьПолеЛог( "ФамПрБ", ФамПрБ );
end;

// атрибуты элемента "Документ"
macro WriteDocumentAttrs
( CommonAccMsg : TCommonDataAccMsg, 
  Счет : string, 
  НовыйСчет : string, 
  Валюта : integer,
  ClientID : integer,
  OldMesID : integer,
  IsIndivid : bool // сообщение генерится по счету физ.лица, не являющегося ИП
)
  ЗаписатьПолеЛог("ИдДок", CommonAccMsg.ИдДок);
  ЗаписатьПолеЛог("КНД", CommonAccMsg.КНД);

  ЗаписатьПолеЛог("КодНОБ", CommonAccMsg.КодНОБ);

  ЗаписатьНомСообИТипСооб( CommonAccMsg.MesSender, Счет, НовыйСчет, Валюта, ClientID, 
                           OldMesID, false, IsIndivid );
  ЗаписатьПолеЛог("ДолжнПрБ", CommonAccMsg.ДолжнПрБ);
  ЗаписатьФамПрБ(CommonAccMsg.AccDprt, IsIndivid);
  ЗаписатьПолеЛог("ТелБанка", CommonAccMsg.ТелБанка);
  ЗаписатьПолеЛог("ДатаСооб", CommonAccMsg.ДатаСооб);
end;

// Формирование поля "КодЛица"
macro GetEntityCodeFld
( LegalForm : integer,
  NotResident : string,
  IsIndividEmployer : bool,
  AttrPartyType : integer,
  ClientType311 : string

) : string

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    return ПолучитьПолеДляАннул( "КодЛица" );
  elif( ClientType311 )
    return ClientType311;
  elif( (LegalForm == PTLEGF_INST) and (NotResident != "X") )
    return "1"; // российская организация
  elif( (LegalForm == PTLEGF_PERSN) and IsIndividEmployer and
        not InList(AttrPartyType, PARTY_ATTR_PTTYPE_NOTARIUS, 
                                  PARTY_ATTR_PTTYPE_ADVOCAT,
                                  PARTY_ATTR_PTTYPE_MANAGPARTNER)
      )
    return "2"; // индивидуальный предприниматель
  elif( (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_NOTARIUS) )
    return "3"; // нотариус, занимающийся частной практикой
  elif( (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_ADVOCAT) )
    return "4"; // адвокат, учредивший адвокатский кабинет
  elif( (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_MANAGPARTNER) )
    return "5"; // управляющий товарищ инвестиционного товарищества
  elif( (LegalForm == PTLEGF_INST) and (NotResident == "X") )
    return "6"; // иностранная организация
  elif( (LegalForm == PTLEGF_PERSN) and not IsIndividEmployer and not AttrPartyType )
    return "7"; // физическое лицо, не являющееся индивидуальным предпринимателем
  end;

  // ничего не смогли вернуть
  RunError("Не удалось определить код лица");
end;

// ВалСч
macro GetAccCurFldVal(Валюта : integer) : string
  var КодВалСчета : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    КодВалСчета = ПолучитьПолеДляАннул( "ВалСч" );
  else
    if( Валюта == NATCUR )
      КодВалСчета = "0";
    else
      КодВалСчета = "1";
    end;
  end;

  return КодВалСчета;
end;

private macro GetShortNameKO( Department ) : string
  var НаимКО = "";

  var HeadPartyID : integer = GetHeadDprtPartyID();
  if( HeadPartyID > 0 )
    НаимКО = Bnk_GetPartyShortName( HeadPartyID );

    var PartyID : integer = GetDprtPartyID(Department);
    if( PartyID > 0 )
      if( PartyID != HeadPartyID )
        var PartyName = Bnk_GetPartyShortName(PartyID);
        if( PartyName )
          НаимКО = НаимКО + IfThenElse( index(PartyName,"филиал"), ", ", ", филиал ") + PartyName;
        end;
      end;
    end;
  end;
  return НаимКО;
end;

// Сведения о банке (филиале банка) (СвБанкТип)
// true - удалось заполнить поля, false - не удалось
macro WriteBankInfo
( CommonSBC : TCommonDataAccMsg, 
  BlockName : string, 
  ErrMsg : @string

) : bool

  ErrMsg = "";
  var PartyID = CommonSBC.MesSender;
  // Get field values
  var Bank : TBankInfoAccMsg = TBankInfoAccMsg();
  if( not Bank.Fill(PartyID, @ErrMsg) )
    return FALSE;
  end;

  Bank.НаимКО = GetShortNameKO( CommonSBC.AccDprt );

  // Write fields
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("РегНом", Bank.РегНом);
  ЗаписатьПолеЛог("НомФ", Bank.НомФ);
  ЗаписатьПолеЛог("БИК", Bank.БИК);
  ЗаписатьПолеЛог("НаимКО", Bank.НаимКО);
  ЗаписатьПолеЛог("ИННКО", Bank.ИННКО);
  ЗаписатьПолеЛог("КППКО", Bank.КППКО);
  ЗаписатьПолеЛог("ОГРНКО", Bank.ОГРНКО);
  if(Bank.ПризнТер == "1")
    ЗаписатьПолеЛог("ПризнТер", Bank.ПризнТер);
  end;
  WriteAddrRF(Bank.Addr, "АдрМНКО");

  FinishBlockLogXML(BlockName);

  return TRUE;
end;

// Сведения о банке (филиале банка), передавшем счет (депозит) (СвБанкСтар)
// true - удалось заполнить поля, false - не удалось
macro WriteOldBankInfo(reqchnga)
  if(reqchnga.BankChange == "X")
    StartBlockLogXML("СвБанкСтар");
    WriteFieldWithCheckAnnul( "ДатаИзмБанк", DDpMMpYYYY(reqchnga.BankChangeDate) );

    // Файл \ Документ \ СвБанкСтар \ СвИзмБанк
    var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg( reqchnga.Department );
    CommonSBC.MesSender = reqchnga.BankIDChange;
    var ErrMsg : string = "";
    if( not WriteBankInfo(CommonSBC, "СвИзмБанк", @ErrMsg) )
      if(not ErrMsg)
        ErrMsg = "Ошибка при записи сведений о банке (филиале банка), передавшем счет (депозит)";
      end;

      RunError(ErrMsg);
    end;

    FinishBlockLogXML("СвБанкСтар");
  end;
end;

// ВидСч (для сообщений по счетам бэк-офисов)
macro GetAccKindFld_BO(buff, AccMsgKind : integer) : string
  var AccKind : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    AccKind = ПолучитьПолеДляАннул( "ВидСч" );
  else
    var ObjType : integer = 0, NoteKind : integer = 0;
    if(AccMsgKind == ACCMSG_KIND_OPEN)
      ObjType = OBJTYPE_REQOPENA;
      NoteKind = REQOPENA_NOTE_KIND_ACCKIND;
    elif(AccMsgKind == ACCMSG_KIND_CHNG)
      ObjType = OBJTYPE_REQCHANGE;
      NoteKind = REQCHNGA_NOTE_KIND_ACCKIND;
    else
      ObjType = OBJTYPE_REQCLOSA;
      NoteKind = REQCLOSA_NOTE_KIND_ACCKIND;
    end;

    AccKind = ReadNoteForObject(ObjType, UniID(buff, ObjType), NoteKind);

    if(not AccKind)
      RunError("Не задано примечание \"Вид счета\"");
    end;
  end;

  return AccKind;
end;

// ДатаОткрСч (для сообщений по счетам бэк-офисов)
macro GetAccOpenDateFldVal_BO(buff, AccMsgKind : integer) : string
  var OpenDateStr : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    OpenDateStr = ПолучитьПолеДляАннул( "ДатаОткрСч" );
  else
    var OpenDate : date = date(0,0,0);

    if(AccMsgKind == ACCMSG_KIND_OPEN)
      OpenDate = buff.AccOpenDate;
    else
      var ObjType : integer = 0, NoteKind : integer = 0;
      if(AccMsgKind == ACCMSG_KIND_CHNG)
        ObjType = OBJTYPE_REQCHANGE;
        NoteKind = REQCHNGA_NOTE_KIND_ACC_OPENDATE;
      else
        ObjType = OBJTYPE_REQCLOSA;
        NoteKind = REQCLOSA_NOTE_KIND_ACC_OPENDATE;
      end;

      OpenDate = ReadNoteForObject(ObjType, UniID(buff, ObjType), NoteKind);
      if(OpenDate == date(0, 0, 0))
        RunError("Не задано примечание \"Дата открытия счета\"");
      end;
    end;

    OpenDateStr = DDpMMpYYYY(OpenDate);
  end;

  return OpenDateStr;
end;

// ДатаИзмСч
macro GetAccChngDate(reqchnga, NewAccOpenDate : date) : string
  var AccChngDateStr : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    AccChngDateStr = ПолучитьПолеДляАннул( "ДатаИзмСч" );

  elif( reqchnga.Origin != REQ_ACC_ORIGIN_PS ) // происхождение из бэк-офиса
    var AccChngDate : date = ReadNoteForObject
      ( OBJTYPE_REQCHANGE, 
        UniID(reqchnga, OBJTYPE_REQCHANGE), 
        REQCHNGA_NOTE_KIND_ACC_CHNGDATE
      );

    if(AccChngDate == date(0, 0, 0))
      RunError("Не задано примечание \"Дата изменения номера счета\"");
    end;

    AccChngDateStr = DDpMMpYYYY(AccChngDate);

  else
    AccChngDateStr = DDpMMpYYYY(NewAccOpenDate);
  end;

  return AccChngDateStr;
end;

// ----------------------------------------------------------------------
// Получение наименования вида счета для счетов из Главной книги
// ----------------------------------------------------------------------
private macro GetAccKindNameByAccKind(AccKind : string) : string
  if(AccKind == "01")
    return "Расчетный";
  elif(AccKind == "02")
    return "Текущий";
  elif(AccKind == "03")
    return "Специальный";
  elif(AccKind == "04")
    return "Счет по вкладу";
  elif(AccKind == "05")
    return "Депозитный";
  elif(AccKind == "06")
    return "Бюджетный";
  elif(AccKind == "07")
    return "Корреспондентский";
  elif(AccKind == "08")
    return "Корреспондент. субсчет";
  elif(AccKind == "09")
    return "Доверит. управления";
  elif(AccKind == "10")
    return "Накопительный";
  elif(AccKind == "11")
    return "Внебюджетный";
  elif(AccKind == "99")
    return "Иной";
  elif(AccKind == "00")
    return "Неустановленный";
  end;

  RunError("Ошибка программирования: некорректное значение параметра AccKind");
end;

private macro GetAccKindByTypeAccount(Type_Account : string/*, AccKinds*/) : string
  var AccKind : string = "", 
      i : integer = 0;

  while( GetParm(i, AccKind) )
    if( (AccKind == "01") and (StrBrk(Type_Account, "ЧX") > 0) 
        or
        (AccKind == "02") and (StrBrk(Type_Account, "W") > 0)
        or
        (AccKind == "03") and (StrBrk(Type_Account, "ЯQ") > 0)
        or
        (AccKind == "04") and (StrBrk(Type_Account, "J") > 0)
        or
        (AccKind == "05") and (StrBrk(Type_Account, "JG") > 0)
        or
        (AccKind == "06") and (StrBrk(Type_Account, "Б") > 0)
        or
        (AccKind == "07") and (StrBrk(Type_Account, "К") > 0)
        or
        (AccKind == "08") and (StrBrk(Type_Account, "Р") > 0)
        or
        (AccKind == "09") and (StrBrk(Type_Account, "E") > 0)
        or
        (AccKind == "10") and (StrBrk(Type_Account, "L") > 0)
        or
        (AccKind == "11") and (StrBrk(Type_Account, "Й") > 0)
      )
      return AccKind;
    end;

    i = i + 1;
  end;

  // Если нет ни одного совпадения, вернуть вид счета "99"
  return "99";
end;

// !!! Данная функция дергается из сишника
// !!! При изменеии надо учитывать
macro GetAccKindNameGK(AccClient : integer, Type_Account : string) : string
  var AccKind : string = "";

    // Если для счета на задано ни одного системного типа, вернуть значение "00"
    if(not Type_Account)
      AccKind = "00";
    else
      var IsPersn : bool = IsPerson(AccClient);
      var IsEmployer : bool = false, PartyType : integer = 0;
      if(IsPersn)
        IsEmployer = IsIndividualEmployer(AccClient);
        PartyType = GetPartyMainAttr_PartyType(AccClient);
      end;

      if(not IsPersn) // Если владелец счета юр. лицо
        AccKind = GetAccKindByTypeAccount(Type_Account, "01", "07", "08", "03", "06", "11", "09", "05", "10");
      elif(IsPersn and IsEmployer or PartyType)
        AccKind = GetAccKindByTypeAccount(Type_Account, "01", "02", "03", "04", "09");
      elif(IsPersn and not IsEmployer and not PartyType)
        AccKind = GetAccKindByTypeAccount(Type_Account, "02", "04");
      else
        AccKind = "99";
      end;
    end;

  return GetAccKindNameByAccKind(AccKind);
end;

macro GetAccountTypeSymbolByAccKindName(AccKindName : string) : string
  var AccountType : string = "";

  if(AccKindName == "Расчетный")
    AccountType = "ЧX";
  elif(AccKindName == "Текущий")
    AccountType = "W";
  elif(AccKindName == "Специальный")
    AccountType = "ЯQ";
  elif(AccKindName == "Счет по вкладу")
    AccountType = "J";
  elif(AccKindName == "Депозитный")
    AccountType = "JG";
  elif(AccKindName == "Бюджетный")
    AccountType = "Б";
  elif(AccKindName == "Корреспондентский")
    AccountType = "К";
  elif( InList(AccKindName, "Корреспондент. субсчет", "Доверит. управления") )
    AccountType = "Р";
  elif(AccKindName == "Накопительный")
    AccountType = "L";
  end;

  return AccountType;
end;
