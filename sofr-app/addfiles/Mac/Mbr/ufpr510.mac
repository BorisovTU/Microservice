/*
 $Name: ufpr510.mac
 $Module: MBR
 $Description: Печатная форма сообщения ED510 транспорта УФЭБС
*/


import BankInter, "wluftool.mac", "bnk_common.mac";

private class TPayServicesCodeList
  var ServicesCode = TArray(),
      ReceiverSWIFTBIC = TArray(),
      ServicesDate = TArray(),
      ServicesQuantity = TArray(),
      ServicesRate = TArray(),
      Sum = TArray();
end;

private macro PrintServicesTable( ServicesCode, Services : TPayServicesCodeList )
  var ServiceNum = 0;
  var CodeName = "";
  if(ServicesCode == "1")
    CodeName = "Финансовые сообщения в виде ЭС";
  elif(ServicesCode == "2")
    CodeName = "Сообщения собственного формата"
  else
    return;
  end;
  
[ 

  Список предоставленных услуг по виду # ########################################]
(ServicesCode, CodeName);    

[ ┌────────────┬───────────┬────────────────────┬────────────────────┬─────────────┐
  │    Дата    │Количество │       Тариф        │       Сумма        │  BIC SWIFT  │
];

  while( ValType(Services.ServicesDate[ServiceNum]) != V_UNDEF )
    if( ServicesCode != Services.ServicesCode[ServiceNum] )
      ServiceNum = ServiceNum + 1;
      continue;
    end;
    
    if( ValType(Services.ReceiverSWIFTBIC[ServiceNum]) == V_UNDEF )
      Services.ReceiverSWIFTBIC[ServiceNum] = "";
    end;
                                                                              
[ ├────────────┼───────────┼────────────────────┼────────────────────┼─────────────┤];
                                                                              
[ │ ########## │ ######### │ ################## │ ################## │ ########### │]
    (Services.ServicesDate[ServiceNum],
     Services.ServicesQuantity[ServiceNum],
     Services.ServicesRate[ServiceNum],
     Services.Sum[ServiceNum],
     Services.ReceiverSWIFTBIC[ServiceNum]);
 
    ServiceNum = ServiceNum + 1;
  end;


[ └────────────┴───────────┴────────────────────┴────────────────────┴─────────────┘];

end;

macro PrintDoc( addrMes, MassCopy )
  SetBuff( wlmes, addrMes );
  
  var FieldName, FieldValue, BlockName;
  var EDNo, EDDate, EDAuthor, EDReceiver, RegisterNo, TotalSum, PayServicesCodeList = TPayServicesCodeList();
  var Trn, PartyNameAuthor, PartyNameReceiver, ServicesName;
  
  while( СчитатьПоле(FieldName, FieldValue, BlockName) )
    if( BlockName == "" )
      if( FieldName == "EDNo" )
        EDNo = FieldValue;
      elif( FieldName == "EDDate" )
        EDDate = FieldValue;
      elif( FieldName == "EDAuthor" )
        EDAuthor = FieldValue;
      elif( FieldName == "EDReceiver" )
        EDReceiver = FieldValue;
      elif( FieldName == "RegisterNo" )
        RegisterNo = FieldValue;
      elif( FieldName == "TotalSum" )
        TotalSum = FieldValue;
      end;
    elif( BlockName == "PayServicesCodeList" )
      if( FieldName == "ServicesCode" )
        PayServicesCodeList.ServicesCode[PayServicesCodeList.ServicesCode.size] = FieldValue;
      elif( FieldName == "ReceiverSWIFTBIC" )
        PayServicesCodeList.ReceiverSWIFTBIC[PayServicesCodeList.ReceiverSWIFTBIC.size] = FieldValue;
      elif( FieldName == "ServicesDate" )
        PayServicesCodeList.ServicesDate[PayServicesCodeList.ServicesDate.size] = FieldValue;
      elif( FieldName == "ServicesQuantity" )
        PayServicesCodeList.ServicesQuantity[PayServicesCodeList.ServicesQuantity.size] = FieldValue;
      elif( FieldName == "ServicesRate" )
        PayServicesCodeList.ServicesRate[PayServicesCodeList.ServicesRate.size] = FieldValue;
      elif( FieldName == "Sum" )
        PayServicesCodeList.Sum[PayServicesCodeList.Sum.size] = FieldValue;
      end;   
    end;      
  end;
  
  Trn = wlmes.Trn;
  PartyNameAuthor = Bnk_GetPartyName(GetCodeOwnerID(PTCK_UIS, EDAuthor));
  PartyNameReceiver = Bnk_GetPartyName(GetCodeOwnerID(PTCK_UIS, EDReceiver));
  
[ ED510 Информация об услугах, предоставленных Банком России пользователю СПФС
  Номер ЭС:      ########
  Дата ЭС:       ##########
  Референс ЭС:   ########################################
  Составитель:   ########## ########################################################################################################
  Получатель     ########## ########################################################################################################
  Номер реестра: ########
  
]( EDNo,
   EDDate,
   Trn,
   EDAuthor, PartyNameAuthor,
   EDReceiver, PartyNameReceiver,
   RegisterNo
  );
  var Code1 = false, Code2 = false, i = 0;
  while(i < PayServicesCodeList.ServicesCode.size)
    if(PayServicesCodeList.ServicesCode[i] == "1")
      Code1 = true;
    end;
    if(PayServicesCodeList.ServicesCode[i] == "2")
      Code2 = true;
    end;
    i = i + 1;
  end;
  
  if(Code1)
    PrintServicesTable("1", PayServicesCodeList);
  end;
  if(Code2)
    PrintServicesTable("2", PayServicesCodeList);
  end;
  
  
  return true;                                        
end;
