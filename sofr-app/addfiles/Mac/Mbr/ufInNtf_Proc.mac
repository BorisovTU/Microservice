/*
  $Name:         ufInNtf_Proc.mac
  $Module:       МБР
  $Description:  Процедура обработки уведомлений о зачислении
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Процедура обработки уведомлений о зачислении
//
// Программист  : Евграфова Л.Н.
//
// Создан       : 13.06.2018
//
//-----------------------------------------------------------------------------

import "ufNtfCar_lib.mac", "ufInNtf_Rep.mac", "wldoc.mac", OprInter;

private macro GetDepCondition(DepList : string) : string
  var cond : string = "";

  if(DepList)
    cond = " and dep.t_Name in (";

    var dpdepNames : TArray = split(DepList, ",");
    var HasPrevElem : bool = false;
    for(var depName : string, dpdepNames)
      if(HasPrevElem)
        cond = cond + ", ";
      end;

      cond = cond + "'" + trim(depName) + "'";

      HasPrevElem = true;
    end;

    cond = cond + ") ";
  end;

  return cond;
end;

private macro GetDateCondition(DateFlag : string) : string
  var cond : string = "";

  if(Dateflag == "0")
    cond = " and wlsess.t_BankDate = RSBSESSIONDATA.curdate ";
  end;

  if(Dateflag == "1")
    cond = " and wlsess.t_BankDate = RSI_RsbOperDay.GetDepartmentOperDate( dep.t_code ) ";
  end;

  return cond;
end;


macro ProcessInNotifications
( DepList : string,
  DateFlag : string
)
  var Report : TUfInNtfReport = TUfInNtfReport();
  var query : string =
      "SELECT wlreq.t_ReqID " +
      " FROM dwlreq_dbt wlreq," +
      " dwlmeslnk_dbt wlmeslnk," +
      " dwlmes_dbt wlmes," +
      " dwlmesrls_dbt wlmesrls," +
      " dwlmesfrm_dbt wlmesfrm," +
      " dwladdfld_dbt wladdfld," +
      " ddp_dep_dbt dep," +
      " dwlsess_dbt wlsess " +
      " WHERE     Wlreq.t_Kind = " + MESKIND_ANSWER +
      " AND Wlreq.t_State = " + WLD_STATUS_REQ_RECEIV +
      " AND Wlreq.t_Direct = 'X'" +
      " AND Wlreq.t_ReqID = wlmeslnk.t_ObjID" +
      " AND Wlmeslnk.t_ObjKind = " + OBJTYPE_REQ +
      " AND Wlmeslnk.t_MesID = wlmes.t_MesID"
      " AND Wlmes.t_RlsFormID = wlmesrls.t_RlsFormID"
      " AND Wlmesrls.t_FormID = wlmesfrm.t_FormID"
      " AND Wlmesfrm.t_Name = 'ED244'" +
      " AND Wladdfld.t_ObjectID = wlreq.t_ReqID " +
      " AND Wladdfld.t_Number = 'RequestCode'" +
      " AND Wladdfld.t_Value = '00' " +
      " AND Wlreq.t_Queries LIKE " +
      "        '%/ОТ' || '" + UfNtfCar_GetAnswerCodeSuccess + "'"
      "        || '/%'" + 
      " AND Wlreq.t_RecipientID = dep.t_PartyID" + 
      GetDepCondition(DepList) +
      " AND Wlmes.t_SessionId = wlsess.t_SessionID " +
      GetDateCondition(DateFlag); 

  var rs = execSQLselect(query);
  var stat:integer;
  var old = SetDialogFlag(0);
  var mode_multi : bool = Opr_SetMultiExec( true ); // в групповом режиме
  Report.PrintRepHeader();
  while(rs and rs.moveNext())
     stat = ProcessInObject(rs.Value(0), OBJTYPE_REQ);
     Report.PrintTableRow(rs.Value(0), stat);
  end;
  Report.PrintTableBottom();
  SetDialogFlag(old);
  Opr_SetMultiExec(mode_multi);
end;
