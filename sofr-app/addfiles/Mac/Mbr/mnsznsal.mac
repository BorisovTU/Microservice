 /*
 $Name: mnsznsal.mac
 $Module: МБР
 $Description: Общие функции для импорта/генерации/печати сообщений ZNS, ZNO
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mnsznsal.mac
  Created     : 27.01.2011
  Programmer  : Popova O.
  Description : Общие функции для импорта/генерации/печати сообщений ZNS, ZNO

└───────────────────────────────────────────────────────────────────────────*/


import likepy, WldInter, FIInter, "wlmnstls.mac";
    
// Поля сообщений ZNS, ZNO
var ИдФайл           = "",
    ТипИнф           = "",
    ВерсПрог         = "",
    ТелОтпр          = "",
    ДолжнОтпр        = "",
    ФамОтпр          = "",
    КолДок           = "",
    ВерсФорм         = "",
    ИдДок            = "",
    НомерЗапроса     = "",
    ДатаЗапроса      = "",
    КодНО            = "",
    НаимНО           = "",
    АдрНО            = "",
    ИННКО            = "",
    КППКО            = "",
    БИК              = "",
    НаимКО           = "",
    ОбоснЗапроса     = "",
    КласЧинРук       = "",
    ФИОРук           = "",
    ДолжРук          = "",
    ИНННП            = "",
    КППНП            = "",
    НаимНП           = "",
    ФИОИП            = "",
    ВидСправки       = "",
    Счет             = TArray(), // Массив полей "Счет"
    ТипЗапроса       = "",
    НомФ             = "",
    ОсновЗапроса     = "",
    ВидЗапроса       = "",
    ДатаНач          = "",
    ДатаКон          = "";

private macro ClearFieldsZNS
    ИдФайл           = "";
    ТипИнф           = "";
    ВерсПрог         = "";
    ТелОтпр          = "";
    ДолжнОтпр        = "";
    ФамОтпр          = "";
    КолДок           = "";
    ВерсФорм         = "";
    ИдДок            = "";
    НомерЗапроса     = "";
    ДатаЗапроса      = "";
    КодНО            = "";
    НаимНО           = "";
    АдрНО            = "";
    ИННКО            = "";
    КППКО            = "";
    БИК              = "";
    НаимКО           = "";
    ОбоснЗапроса     = "";
    КласЧинРук       = "";
    ФИОРук           = "";
    ДолжРук          = "";
    ИНННП            = "";
    КППНП            = "";
    НаимНП           = "";
    ФИОИП            = "";
    ВидСправки       = "";
    Счет             = TArray(); // Массив полей "Счет"
    ТипЗапроса       = "";
    НомФ             = "";
    ОсновЗапроса     = "";
    ВидЗапроса       = "";
    ДатаНач          = "";
    ДатаКон          = "";
end;

// дополнительные реквизиты из панелей отчетов
var ПоСостояниюНаДату = date( 0, 0, 0 ),
    НачалоПериода     = date( 0, 0, 0 ),
    КонецПериода      = date( 0, 0, 0 ),
    RepBankID         = {OurBank},  
    RepPriority       = 3,
    RepClaimID        = 0,
    WlRegDecID        = 0;

// Клиент, по которому создается сообщение
private class ZNSParty( ID:integer )
  var PartyID   :integer;
  var LegalForm :integer;
  var FullName  :string;
  var FullINN   :string;

  macro ZNSParty( ID:integer )
    private var PartyObj = RsbParty( ID );
    PartyID    = ID;
    LegalForm  = PartyObj.LegalForm;
    if( LegalForm == PTLEGF_INST )
      FullName =  PartyObj.FullName;
    else
      FullName = string( PartyObj.LastName, ",", PartyObj.FirstName, ",", PartyObj.Patronymic );
    end;
    FullINN    = PartyObj.Code(PTCK_INN);
  end;
  ZNSParty(ID);
end;

private var КлиентПоСчету:variant;

// выводим отчет из РКО
private macro IsPSReport():bool
  return ИдФайл == "";
end;

class MnsMessageFormZNS()

   private var Error;     // Действие выполнено с ошибкой
   private var ErrorMes;  // Сообщение об ошибке

   macro MnsMessageFormZNS()
     Error = 0;
     ErrorMes = "";

     // сброс глобализмов
     ClearFieldsZNS();
     КлиентПоСчету = NULL;

     var field_name, field_value;
     if( (ВидСправки == "") and (ВидЗапроса == "") )
       while( СчитатьПоле( field_name, field_value ) )
         if( field_name == "ИдФайл" )
           ИдФайл = field_value;
         elif( field_name == "ТипИнф" )
           ТипИнф = field_value;
         elif( field_name == "ВерсПрог" )
           ВерсПрог = field_value;
         elif( field_name == "ТелОтпр" )
           ТелОтпр = field_value;
         elif( field_name == "ДолжнОтпр" )
           ДолжнОтпр = field_value;
         elif( field_name == "ФамОтпр" )
           ФамОтпр = field_value;
         elif( field_name == "КолДок" )
           КолДок = field_value;
         elif( field_name == "ВерсФорм" )
           ВерсФорм = field_value;
         elif( field_name == "ИдДок" )
           ИдДок = field_value;
         elif( field_name == "НомЗапр" )
           НомерЗапроса = field_value;
         elif( field_name == "ДатаЗапр" )
           ДатаЗапроса = field_value;
         elif( field_name == "КодНО" )
           КодНО = field_value;
         elif( field_name == "НаимНО" )
           НаимНО = field_value;
         elif( field_name == "АдрНО" )
           АдрНО = field_value;
         elif( field_name == "ИННКО" )
           ИННКО = field_value;
         elif( field_name == "КППКО" )
           КППКО = field_value;
         elif( field_name == "БИК" )
           БИК = field_value;
         elif( field_name == "НаимКО" )
           НаимКО = field_value;
         elif( field_name == "ОбоснЗапр" )
           ОбоснЗапроса = field_value;
         elif( field_name == "КласЧинРук" )
           КласЧинРук = field_value;
         elif( field_name == "ФИОРук" )
           ФИОРук = field_value;
         elif( field_name == "ДолжРук" )
           ДолжРук = field_value;
         elif( field_name == "ТипЗапр" )
           ТипЗапроса = field_value;
         elif( field_name == "ИНННП" )
           ИНННП = field_value;
         elif( field_name == "КППНП" )
           КППНП = field_value;
         elif( field_name == "НаимНП" )
           НаимНП = field_value;
         elif( field_name == "ФИОИП" )
           ФИОИП = field_value;
         elif( field_name == "НомСч" )
           Счет[Счет.Size] = field_value;
         elif( field_name == "_ВидСправ" )
           ВидСправки = field_value;
         elif( field_name == "НомФ" )
           НомФ = field_value;
         elif( field_name == "ОсновЗапр" )
           ОсновЗапроса = field_value;
         elif( field_name == "ВидЗапр" )
           ВидЗапроса = field_value;
         elif( field_name == "ДатаНач" )
           ДатаНач = field_value;
         elif( field_name == "ДатаКон" )
           ДатаКон = field_value;
         end;
       end;
     end;
   end;

   private macro CheckBeforePrint()

     var PartyID = ПолучитьКодСубъекта( string( ИНННП + IfThenElse( КППНП != "", "/", "" ) + КППНП ), PTCK_INN, Error );
     if( ( Error > 0 ) and ( КППНП != "" ) )
       Error = 0;  // Не нашли субъекта по полному коду - поищем только по ИНН
       PartyID = GetLegalPersonIDByINN( ИНННП );
       if( PartyID > 0 )
         КлиентПоСчету = ZNSParty( PartyID );
         if( КлиентПоСчету.LegalForm == PTLEGF_INST )
           ErrorMes = "КПП клиента не совпадает с указанным в запросе ФНС";// это не ошибка, только предупреждение
         end;
       end;
     end;
     if( ( PartyID <= 0 ) and ( ВидСправки != "NS" ) and (ВидЗапроса != "1") )
       Error = 1;  // это ошибка для всех форм, кроме NS
       ErrorMes = "Не найден клиент с ИНН " + ИНННП;
     elif( PartyID > 0 )
       if( КлиентПоСчету == NULL )
         КлиентПоСчету = ZNSParty( PartyID );
       end;
       if( ( ( КлиентПоСчету.LegalForm == PTLEGF_INST  ) and ( НаимНП != КлиентПоСчету.FullName ) ) or
           ( ( КлиентПоСчету.LegalForm == PTLEGF_PERSN ) and ( ФИОИП  != КлиентПоСчету.FullName ) ) )
         ErrorMes = IfThenElse( ErrorMes == "", "Наименование клиента не совпадает с указанным в запросе ФНС", ErrorMes );
       end;
     end;
     return IfThenElse( ( Error != 0 ) or ( ErrorMes != "" ), 1, 0 ); 
   end;

   macro PrintDoc()

     Array Text, Buttons;
     var ClientID = 0;  getparm( 1, ClientID ); 
     if( ClientID > 0 )
       КлиентПоСчету = ZNSParty( ClientID );
     end;
     if( ( ClientID == 0 ) and CheckBeforePrint() ) // проверку субъекта делаем только для сообщений ZNS
       if( GetDialogFlag() and ( Error == 0 ) ) // на все предупреждения выдадим запрос
         Text(0) = ErrorMes + ".|Продолжить печать?";
         Buttons( 0 ) = "Да";
         Buttons( 1 ) = "Нет";
         Error = IfThenElse( ConfWin( Text, Buttons ) == 1, 1, 0 );
       else
         msgbox( ErrorMes );
       end;
       if( Error )
         return 0; // ошибка уже обработана
       end;
     end;
     if( Error == 0 )
       if( ValType( КлиентПоСчету ) == V_GENOBJ )
         ClientID = КлиентПоСчету.PartyID;
       elif( ( (ВидСправки == "NS") or (ВидЗапроса == "1") ) and not IsPSReport() )
         ClientID = -1; // в этом случае выводим справку об отсутствии счетов
       elif( ClientID > 0 )  
         КлиентПоСчету = ZNSParty( ClientID );
       end;
       if( (ВидСправки == "NS") or (ВидЗапроса == "1") )
         Error = ExecMacroFile( "znsnsprn.mac", "PrintReportInfoAboutExistenceAccounts", ClientID, ДатаЗапроса, Счет );
       elif( (ВидСправки == "OS") or (ВидЗапроса == "2") )
         Error = ExecMacroFile( "znsosprn.mac", "PrintReportInfoAboutRestAccounts", ClientID, ПоСостояниюНаДату, Счет );
       elif( (ВидСправки == "VS") or (ВидЗапроса == "3") )
         Error = ExecMacroFile( "znsvsprn.mac", "PrintReportStatementOprAccounts", ClientID, ДатаЗапроса, Счет, WlRegDecID );
       end;
     end;             
     return Error;
   end;

   macro GetSubKind()
     if( ВидСправки == "NS" )
       return 1;
     elif( ВидСправки == "OS" )
       return 2;
     else
       return 3;
     end;
   end;

   macro GetNarrative()
     private var _ВидСправки;
     if( (ВидСправки == "NS") or (ВидЗапроса == "1") )
       _ВидСправки = "о наличии счетов в банке у ";
     elif( (ВидСправки == "OS") or (ВидЗапроса == "2") )
       _ВидСправки = "об остатках денежных средств на счетах ";
     elif( (ВидСправки == "VS") or (ВидЗапроса == "3") )
       _ВидСправки = "о предоставлении выписок по операциям на счетах ";
     else 
       ErrorMes = "Неверно задан вид справки";
       Error = 1;
     end;
     return   "Запрос № " + НомерЗапроса + " от " + ДатаЗапроса + " из налогового органа " + КодНО + " " + НаимНО + " " + 
              _ВидСправки + "клиента " + ИНННП + IfThenElse( КППНП != "", string( "\\" + КППНП ), "" ) + " " +
              IfThenElse( НаимНП != "", НаимНП , ФИОИП );
   end;
   MnsMessageFormZNS();

end;

//-----------------------------------------------------------------------------------------------------------
// Функции, необходимые для печати документов
//-----------------------------------------------------------------------------------------------------------

macro ЗаголовокСправки_ММ_3_06_178()

  [
                                                                                                 Форма по КНД 1114306

                                                             ########################################################
                                                             ────────────────────────────────────────────────────────
                                                                       (наименование налогового органа)

                                                             ########################################################
                                                             ────────────────────────────────────────────────────────
                                                                                  (адрес)

  ]( НаимНО:w, АдрНО:w );
end;

macro ДанныеБанкаИ_Клиента()
  
  var INN = "", BIC = {MFO_Bank}, NameBank = {Name_Bank};
  array BankINN, BankKPP, ClientINN, ClientKPP, BankBIC;
  
  INN = ПолучитьКодСубъекта( RepBankID, PTCK_INN );
  StrSplit( substr( INN, 1, index( INN, "/" ) - 1 ), BankINN, 1, 1, 10 );
  StrSplit( substr( INN,    index( INN, "/" ) + 1 ), BankKPP, 1, 1,  9 );
  StrSplit( BIC, BankBIC, 1, 1, 9 );

  if( IsPSReport() )
    NameBank = НаимКО;
    BIC = ПолучитьКодСубъекта( RepBankID, PTCK_BIC );
  end;
  StrSplit( ИНННП, ClientINN, 1, 1, 12 );
  StrSplit( КППНП, ClientKPP, 1, 1,  9 );

  [    

         Банк  #
              ─────────────────────────────────────────────────────────────────────────────────────
                      (полное наименование (сокращенное наименование) в соответствии с 
                        Книгой государственной регистрации кредитных организаций)
            ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐                ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
    ИНН     │#│#│#│#│#│#│#│#│#│#│            КПП │#│#│#│#│#│#│#│#│#│
            └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘                └─┴─┴─┴─┴─┴─┴─┴─┴─┘
         ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
    БИК  │#│#│#│#│#│#│#│#│#│
         └─┴─┴─┴─┴─┴─┴─┴─┴─┘

          В соответствии с запросом налогового органа от #          N  #
                                                        ────────────  ──────────────────────

          в отношении
      #
     ───────────────────────────────────────────────────────────────────────────────────────────────────
     (наименование организации, Ф.И.О. индивидуального предпринимателя (нотариуса, занимающегося частной
                            практикой, адвоката, учредившего адвокатский кабинет)

            ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐            ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
    ИНН/КИО │#│#│#│#│#│#│#│#│#│#│#│#│        КПП │#│#│#│#│#│#│#│#│#│
            └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘            └─┴─┴─┴─┴─┴─┴─┴─┴─┘
 ]( {Name_Bank}, 
    BankINN(0),BankINN(1),BankINN(2),BankINN(3),BankINN(4),BankINN(5),BankINN(6),BankINN(7),BankINN(8),BankINN(9),   
    BankKPP(0),BankKPP(1),BankKPP(2),BankKPP(3),BankKPP(4),BankKPP(5),BankKPP(6),BankKPP(7),BankKPP(8),
    BankBIC(0),BankBIC(1),BankBIC(2),BankBIC(3),BankBIC(4),BankBIC(5),BankBIC(6),BankBIC(7),BankBIC(8),
    ДатаЗапроса, НомерЗапроса, 
    IfThenElse( НаимНП != "", НаимНП, ФИОИП ), 
    ClientINN(0),ClientINN(1),ClientINN(2),ClientINN(3),ClientINN(4),ClientINN(5),ClientINN(6),ClientINN(7),ClientINN(8),
    ClientINN(9),ClientINN(10),ClientINN(11),
    ClientKPP(0),ClientKPP(1),ClientKPP(2),ClientKPP(3),ClientKPP(4),ClientKPP(5),ClientKPP(6),ClientKPP(7),ClientKPP(8)
 );
end;

private macro СортировкаПоКодуВалюты( Acc1:TRecHandler, Acc2:TRecHandler )
  // сначала все рублевые, потом валютные по возрастанию кода валюты
  var Code1 = IfThenElse( Acc1.rec.Code_Currency == 0, "000", GetFICode( Acc1.rec.Code_Currency ) );
  var Code2 = IfThenElse( Acc2.rec.Code_Currency == 0, "000", GetFICode( Acc2.rec.Code_Currency ) );
    if( Code1 < Code2 ) return -1;
  elif( Code1 > Code2 ) return  1;
  elif( Acc1.rec.Account > Acc2.rec.Account ) return  1;
  elif( Acc1.rec.Account < Acc2.rec.Account ) return -1;
  end;
  return 0;
end;

// Возвращает массив из счетов (accounts) клиента, уже отсортированный
macro ВыбратьСчетаКлиента( ClientID:integer ):TArray

  var Acc :TArray = TArray();
  var facc:TBFile;
  var RegTypeAcc:string = ""; GetRegValForOPENAC( "ТИПЫ_СЧЕТОВ", V_STRING, RegTypeAcc );

  var OpenDate  = date( 0, 0, 0 ),
      CloseDate = date( 0, 0, 0 );
  if( (ВидСправки == "NS") or (ВидЗапроса == "1") )
    OpenDate = IfThenElse( IsPSReport(), ПоСостояниюНаДату, date( ДатаЗапроса ) ); 
  elif( (ВидСправки == "VS") or (ВидЗапроса == "3") )
    OpenDate  = IfThenElse( IsPSReport(), КонецПериода, {curdate} ); 
    CloseDate = IfThenElse( IsPSReport(), НачалоПериода, date( ДатаЗапроса ) );
  else
    OpenDate  = date( ДатаЗапроса );
    CloseDate = date( ДатаЗапроса );
  end;
  var select  = "  select t_Account, t_Chapter, t_Code_Currency, t_Type_Account      "+
                "    from daccount_dbt                                               "+
                "   where t_Client       = :ClientID                                 "+
                "     and t_Department   = :OperDep                                  "+
                "     and t_Chapter      = 1                                         "+
                "     and t_Open_Date   <= :OpenDate                                 ";
  if( (ВидСправки == "OS") or (ВидЗапроса == "2") ) select = select +
                "     and ( t_Close_Date  =  :ZeroDate                               "+
                "        or t_Close_Date  >  :CloseDate )                            ";
  elif( (ВидСправки == "VS") or (ВидЗапроса == "3") ) select = select +
                "     and ( t_Close_Date  =  :ZeroDate                               "+
                "        or t_Close_Date  >= :CloseDate )                            ";
  end;
  select = select + " and t_Type_Account not like '%П%'                              ";

  var params = makeArray( SQLParam( "ClientID", ClientID     ),
                          SQLParam( "OperDep" , {OperDprt}   ),
                          SQLParam( "OpenDate", OpenDate     ) );
  if( (ВидСправки != "NS") and (ВидЗапроса != "1") )
    params[params.size] = SQLParam( "ZeroDate"  , date(0,0,0) );
    params[params.size] = SQLParam( "CloseDate" , CloseDate   );
  end;
  var rs = execSQLselect( select, params, FALSE );

  while( ( rs.moveNext() ) )

    if( ( StrBrk( rs.value(3), "НМU" ) == 0 ) and
        ( StrBrk( RegTypeAcc, rs.value(3) ) > 0 ) )
      facc = TBFile( "account.dbt" ,  "R", 0, "account.dbt" ,  "bank.def" );
      facc.KeyNum = 0;
      facc.rec.Account         = rs.value(0);
      facc.rec.Chapter         = rs.value(1);
      facc.rec.Code_Currency   = rs.value(2);
      if( facc.GetEQ() )
        Acc( Acc.Size() ) = TRecHandler("account");
        Copy( Acc( Acc.Size() - 1 ), facc );
      end;
    end;
  end;
  Qsort( Acc, @СортировкаПоКодуВалюты ); 
  return Acc;
end;

macro GetNameTypeAcc( TypeAcc : string, FIID : integer )

  if( ( (ВидСправки != "OS") and (ВидЗапроса != "2") ) and
      (Index(TypeAcc, "X") > 0) and (FIID > 0) )
    return "текущий счет";
  elif( (Index(TypeAcc, "Ч") > 0) or 
        ( (Index(TypeAcc, "X") > 0) and ( (ВидСправки == "OS") or (ВидЗапроса == "2") ) ) )
    return "расчетный счет";
  elif( Index(TypeAcc, "Б") > 0 )
    return "бюджетный счет";
  elif( Index(TypeAcc, "К") > 0 )
    return "корреспондентский счет";
  elif( Index(TypeAcc, "Я") > 0 )
    return "специальный банковский счет";
  elif( Index(TypeAcc, "Q") > 0 )
    return "специальный брокерский счет";
  elif( ( Index(TypeAcc, "J") > 0 ) or
        ( Index(TypeAcc, "G") > 0 ) )
    return "вклад(депозит)";
  else
    return "";
  end;

end;

macro ДатаПодписьПредставителяБанка()

  var error = 0;                                   
  var RegIFNSPost:string; if(GetRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", V_STRING, RegIFNSPost ))  RegIFNSPost = ""; end;
  var RegIFNS_FIO:string; if(GetRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО" , V_STRING, RegIFNS_FIO ))  RegIFNS_FIO = ""; end;   

[


   Представитель  ######################################## ########################################                ##########
      банка       ──────────────────────────────────────── ────────────────────────────────────────  ────────────  ──────────
                                (должность)                               (Ф.И.О)                      (подпись)    (дата)


                                                                                                         М.П.
]( RegIFNSPost, RegIFNS_FIO, {curdate});

end;


//-----------------------------------------------------------------------------------
// Заполнение реквизитов для печати справок по УО "Справка в ФНС"
//-----------------------------------------------------------------------------------
macro GetNOAdress( ID:integer):string
  var rset:object;
  var select:string;
  var params:TArray = TArray();

  select = " select t_adress from dadress_dbt where t_partyID = :ID and t_type in( :legal, :real, :post ) order by t_type";

  params[params.size] = SQLParam( "ID"    , ID             );
  params[params.size] = SQLParam( "legal" , PTADDR_LEGAL   );
  params[params.size] = SQLParam( "real"  , PTADDR_REAL    );
  params[params.size] = SQLParam( "post"  , PTADDR_POST    );
  rset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return "";
END;

macro GetAccRecHandler( wlacclnk:TRecHandler ):TRecHandler

  var f_acc:TBFile;
  var r_acc:TRecHandler = TRecHandler( "account", "bank.def" );
  f_acc = TBFile( "account", "bank.def", 0 );
  f_acc.rec.Account         = wlacclnk.rec.Account;
  f_acc.rec.Code_Currency   = wlacclnk.rec.FIID;
  f_acc.rec.Chapter         = wlacclnk.rec.Chapter;
  if( f_acc.GetEQ() )
    Copy( r_acc, f_acc );
  end;
  return r_acc;
end;

macro GetLnkAccRest( Account:string, Chapter:integer, FIID:integer ):money

  var WlAccList:RsbWlAccLnk = RsbWlAccLnk( WlRegDecID, OBJTYPE_FNSINFO );
  var r_acl:TRecHandler = TRecHandler( "wlacclnk", "bank.def" );
  if( ( WlAccList.size > 0 ) and ( WlAccList.Find( r_acl, Account, FIID, Chapter ) == 0 ) )
    return r_acl.rec.RestIn;
  end;
  return $0;
end;
