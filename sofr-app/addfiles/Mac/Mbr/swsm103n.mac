/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT103N                                   */
/*                                                                          */
/*  Имя файла: swsm103s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

record Party(party);
const Relise = "103N";

macro GenMes(addrMes, addrPm, addrOldMes)

  var field_value, Tag, IsTransit, BaseAmount, CurrentCode, error, fld33B, fld32A, 
      continue0, field33BPresent = 0, NeedAcc = true;
  var ОстатокПоля70;
  var Recreate = false;
  var RsPaym; 

  RECORD combuf( oprsfcom );    
  Record oldwlMes (wlmes);
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( oldwlmes,  addrOldMes );
     Recreate = true;
  end;
  PrintLog(2, "Генерация сообщения по МТ103");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  SB_CheckFIID( wlpmpaym.BaseFIID );
  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог(TransactionReferenceNumberField, wlmes.TRN);

  /* 23B       */
  ЗаписатьПолеЛог( BankOperationCodeField_B, BANKOPERCODE_CRED ) ;

  /* 23Е   берем теперь из dwlpmrmprop_dbt.InstructionCode  */
  field_value = "";
  StrChangeRusXSet( RsPaym.InstructionCode, field_value );
  ЗаписатьПолеЛогВБлок( "23E", InstructionCodeField, string(field_value), NULL,Recreate, OldwlMes);

  /* Восстанавливаем нулевой контекст блока */
  if (not УстановитьКонтекстБлока())
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  
  // Документ порожден транзитным платежом или сам транзитный
  var PaymentIsTransit:bool = (((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)) OR PM_PaymentIsChildTransit( RsPaym.PaymentID ));
  
  /* 32A - дата валютирования, код валюты, сумма */
    field_value = ДатаПоУмолчанию +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  fld32A = field_value;
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );
/*  ЗаписатьПолеЛог(ValueDateCurrencyCodeAmountField_A, field_value);*/

  /* 33B - Инструктируемая сумма, валюта */
  // Транзитный документ
  if( PaymentIsTransit )
    // из входящего сообщения
    ПолучитьПоле( GetMessageByPayment( RsPaym.PaymentID ), CurrencyOriginalOrderedAmountField, fld33B );
    if( fld33B )
      WriteFieldEx(CurrencyOriginalOrderedAmountField, fld33B, Recreate, OldwlMes);
    end;
  else // Исходящий документ
    if( (RsPaym.OrderAmount != 0) and ((RsPaym.OrderAmount != RsPaym.FutureBaseAmount) or 
                                       (RsPaym.OrderFIID != RsPaym.BaseFIID)) 
      )
      field_value = ПолучитьКодВалютыЛог(RsPaym.OrderFIID) + GetSWIFTAmount(RsPaym.OrderAmount);
      fld33B = field_value;
      WriteFieldEx(CurrencyOriginalOrderedAmountField, field_value, Recreate, OldwlMes);
    end;
  end;
  
  /* 36  - Кросс-курс */
  if( PaymentIsTransit )
    field_value = "";
    // из входящего сообщения
    ПолучитьПоле( GetMessageByPayment( RsPaym.PaymentID ), ExchangeRateField, field_value );

    if( field_value )
      WriteFieldEx( ExchangeRateField, field_value, Recreate, OldwlMes );
    end;
  end;

  /* 50a - приказодатель */
  field_value = ГенерацияПоля50 ( RsPaym, Tag, NULL);
    if (field_value=="")
      std.out( 2, "PaymentID: " + wlpmpaym.PaymentID );
      RunError( "|Не найден плательщик" );
    end;
  ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField + Tag, field_value, NULL, Recreate, OldwlMes );
  /* Восстанавливаем нулевой контекст */
  if (not УстановитьКонтекстБлока())
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionFieldA_SB(RsPaym, Tag );
  if (field_value == "")
    std.out(2, "PaymentID: " + wlpmpaym.PaymentID);
    RunError("|Не найден банк приказодателя");
  end;
  ЗаписатьПолеВБлокКонтекст0(OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes);

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value, Recreate, OldwlMes );  

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );      

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCustomerFieldA_SB( RsPaym );/*59A*/
  if (field_value == "")
    field_value = FillBeneficiaryCustomerField_SB( RsPaym , NULL, NeedAcc );/*59*/
    if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден получатель" );
    end;
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL, Recreate, OldwlMes);
  else
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_A, field_value, NULL,Recreate, OldwlMes);
  end;
  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 70  - информация для бенефициара */
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70 );
  WriteFieldEx  (DetailsOfPaymentField, field_value, Recreate, OldwlMes);

  /* 71A - расходы */
  field_value = GetCommisCode (RsPaym.ComissCharges);
  WriteFieldEx  (DetailsOfChargesField_A, field_value, Recreate, OldwlMes); 
  
  /* 71F */
  ЗаписатьПоле71F( RsPaym, PaymentIsTransit, field_value/*ComissCharges*/, fld33B, fld32A );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT103_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField_SB(RsPaym, ALLOWCODES_MT103_STANDART, ОстатокПоля70 );
  if( field_value != "" )
    WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes); 
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

