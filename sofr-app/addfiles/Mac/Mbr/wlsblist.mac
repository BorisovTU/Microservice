/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                      Импорт  сообщений  СМБР                             */
/*                                                                          */
/*  Имя файла: wlsblist.mac                                                 */
/*  Создан:  06.02.03                                        BARS           */
/****************************************************************************/

import "wlsbrftl.mac", "wlimport.mac";
FILE FileTpFld(wltpfld);

 /* Общие глобализмы */
var СчитаннаяСтрока = "", НакопленнаяСтрока = "";      /* считанная и необработанная строка */

const ДлинаСтроки = 99999;

const STLIST_TYPE = 0,
      SBRF3_TYPE = 1;

const VCODE_USUAL = "019",
      VCODE_REESTR = "020";

/***************************************************************************/
/*  Функция записи многострочного поля в форматированную часть             */
/*  Строки записываем с символом перевода строки                           */
/*  Возвращает: TRUE или FALSE                                             */
/***************************************************************************/
macro ЗаписатьМногострочноеПоле( кодПоля, ЗначениеПоля )
  var oldkey, IsFind, IsFindField = true;  

  if( кодПоля == SB_Tag_ReferenceField )
    wlmes.RelatedRef = SubStr( ЗначениеПоля, SB_date_len+SB_creator_len, SB_number_len );
    if( not ОбновитьЗапись( wlmes ) )
      return FALSE;
    end;
/* Теперь закачиваем как текст SCR 45597*/
/*  
  elif( кодПоля == SB_Tag_ExtraInfoField ) 
    return TRUE;
*/
  end;  
 
  if( not ЗаписатьПоле( кодПоля, ToOEM(ЗначениеПоля, true), IsFindField ) )
    if( not IsFindField )
      println( string("В релизе формы не найдено поле транспорта ", кодПоля, ". Информация поля ", кодПоля, " '", ToOEM(ЗначениеПоля, true), "' игнорируется.") );
    else
      return FALSE;
    end;
  end;
  
  return TRUE;
end;

/* Обработать поле сообщения */
macro ОбработатьПоле( кодПоля, ЗначениеПоля )
  if ( trim(ЗначениеПоля)=="" ) 
     return true;
  end;
  if( ЗаписатьМногострочноеПоле( кодПоля, ЗначениеПоля ) )
    return TRUE;
  else
    return FALSE;
  end;
end;

macro ОбработатьПолеExt( кодПоля, ЗначениеПоля )
   var Path = кодПоля;
   if ( not УстановитьКонтекстБлока() )
      return false;
   end;

   while( index(Path,"\\") )
     if ( not УстановитьКонтекстБлока(substr(Path, 1, index(Path,"\\")-1)) )
        return false;
     end;
     Path = substr(Path, index(Path,"\\")+1);
   end;

   return ОбработатьПоле( Path, ЗначениеПоля );
end;

macro ОбработатьСтроку( stat, IsNewLine, shift, size, field, macroCheck )
   var длина, value;

   if ( not stat )
      return;
   end;

   if ( IsNewLine )
      длина = ДлинаСтроки;
      СчитаннаяСтрока = СчитатьСтроку( длина );
      if( (длина == КонецФайла) OR (длина == 0) )
          ErrImport( "Неожиданный конец файла" );
          setparm( 0, false );
      end;
   end;   

   if ( (valtype(macroCheck)!=V_UNDEF) AND (macroCheck!="") )
      if ( not ExecMacro2(macroCheck, СчитаннаяСтрока) )
         return;
      end;
   end;

   if ( field!="" )
      if ( size )
         value = НакопленнаяСтрока+substr(СчитаннаяСтрока, shift+1, size);
      else
         value = НакопленнаяСтрока+substr(СчитаннаяСтрока, shift+1);
      end;
      if ( not ОбработатьПолеExt(field, value) )
        ErrImport( "Не могу обработать поле " + field );
        setparm( 0, false );
      end;
      НакопленнаяСтрока = "";
   else
      if ( size )
         value = НакопленнаяСтрока+substr(СчитаннаяСтрока, shift+1, size);
      else
         value = НакопленнаяСтрока+substr(СчитаннаяСтрока, shift+1);
      end;
      НакопленнаяСтрока = НакопленнаяСтрока + value;
   end;

end;

macro ПропуститьСтроку(stat)
   var длина;

   if ( not stat )
      return;
   end;

   длина = ДлинаСтроки;
   СчитаннаяСтрока = СчитатьСтроку( длина );
   /* Строка может быть и пустой */
   if( (длина == КонецФайла) )
       ErrImport( "Неожиданный конец файла" );
       setparm( 0, false );
   end;
end;

macro IsDocExt( Строка )
   if ( int(substr(Строка,1,6)) )
      return true;
   else
      return false;
   end;
end;

macro ПрочитатьПоляВыписки()
   var stat = true, continue0, VCode;

   ОбработатьСтроку( stat, true,  20, 10, "PA" );
   ОбработатьСтроку( stat, true,  20, 10, "RC" );
   ОбработатьСтроку( stat, true,  20, 1,  "VT" );
   ОбработатьСтроку( stat, true,  20, 3,  "VCODE" );
   VCode = substr( СчитаннаяСтрока, 21, 3 );
   ОбработатьСтроку( stat, true,  20, 2,  "VNUM" );
   ОбработатьСтроку( stat, true,  20, 3,  "CU" );
   ОбработатьСтроку( stat, true,  20, 10, "DATEIN" );
   ОбработатьСтроку( stat, false, 31, 10, "DATEOUT" );
   ОбработатьСтроку( stat, true,  20, 10, "PVB" );
   ОбработатьСтроку( stat, true,  20, 10, "VDATE" );
   ОбработатьСтроку( stat, false, 31, 5,  "VTIME" );

   ПропуститьСтроку(stat);
   ПропуститьСтроку(stat);
   if ( substr(СчитаннаяСтрока,8,1)=="1" )
      if ( VCode==VCODE_USUAL )
         ОбработатьСтроку( stat, true,  20, 20,  "A\\ACC" );
         ОбработатьСтроку( stat, true,  75, 2,   "" );
         ОбработатьСтроку( stat, false, 79, 18,  "A\\INBL" );
      end;
      ПропуститьСтроку(stat);
      continue0 = true;
      while( continue0 AND stat )
         ОбработатьСтроку( stat, true, 0, 0,  "A\\DOC\\DOC", "IsDocExt" );
         continue0 = IsDocExt(СчитаннаяСтрока);
      end;
      ОбработатьСтроку( stat, false, 79, 18,  "A\\DAM" );
      ОбработатьСтроку( stat, false, 116, 6,  "A\\DNUM" );
      ОбработатьСтроку( stat, true,  79, 18,  "A\\KAM" );
      ОбработатьСтроку( stat, false, 116, 6,  "A\\KNUM" );
      if ( VCode==VCODE_USUAL )
         ОбработатьСтроку( stat, true,  75, 2,   "" );
         ОбработатьСтроку( stat, false, 79, 18,  "A\\OUTBL" );
      end;

      ПропуститьСтроку(stat);
      ПропуститьСтроку(stat);
   end;
   
   if ( substr(СчитаннаяСтрока,8,1)=="2" )
      if ( VCode==VCODE_USUAL )
         ОбработатьСтроку( stat, true,  20, 20,  "B\\ACC" );
         ОбработатьСтроку( stat, true,  75, 2,   "" );
         ОбработатьСтроку( stat, false, 79, 18,  "B\\INBL" );
      end;
      ПропуститьСтроку(stat);
      continue0 = true;
      while( continue0 AND stat )
         ОбработатьСтроку( stat, true, 0, 0,  "B\\DOC\\DOC", "IsDocExt" );
         continue0 = IsDocExt(СчитаннаяСтрока);
      end;
      ОбработатьСтроку( stat, false, 79, 18,  "B\\DAM" );
      ОбработатьСтроку( stat, false, 116, 6,  "B\\DNUM" );
      ОбработатьСтроку( stat, true,  79, 18,  "B\\KAM" );
      ОбработатьСтроку( stat, false, 116, 6,  "B\\KNUM" );
      if ( VCode==VCODE_USUAL )
         ОбработатьСтроку( stat, true,  75, 2,   "" );
         ОбработатьСтроку( stat, false, 79, 18,  "B\\OUTBL" );
      end;

      ПропуститьСтроку(stat);
      ПропуститьСтроку(stat);
   end;

   if ( substr(СчитаннаяСтрока,8,1)=="3" )
      ПропуститьСтроку(stat);
      continue0 = true;
      while( continue0 AND stat )
         ОбработатьСтроку( stat, true, 0, 0,  "C\\DOC\\DOC", "IsDocExt" );
         continue0 = IsDocExt(СчитаннаяСтрока);
      end;
      ОбработатьСтроку( stat, false, 47, 18,  "C\\DAM" );
      ОбработатьСтроку( stat, false, 84, 6,   "C\\DNUM" );
      ОбработатьСтроку( stat, true,  47, 18,  "C\\KAM" );
      ОбработатьСтроку( stat, false, 84, 6,   "C\\KNUM" );

      ПропуститьСтроку(stat);
      ПропуститьСтроку(stat);
   end;

   ПропуститьСтроку(stat);
   continue0 = true;
   while( continue0 AND stat )
      ОбработатьСтроку( stat, true, 0, 0,  "D\\DOC\\DOC", "IsDocExt" );
      continue0 = IsDocExt(СчитаннаяСтрока);
   end;
   ОбработатьСтроку( stat, false, 43, 18,  "D\\AM" );
   ОбработатьСтроку( stat, false, 80, 6,   "D\\NUM" );

   ПропуститьСтроку(stat);
   ПропуститьСтроку(stat);
   ПропуститьСтроку(stat);
   continue0 = true;
   while( continue0 AND stat )
      ОбработатьСтроку( stat, true, 0, 0,  "E\\DOC\\DOC", "IsDocExt" );
      continue0 = IsDocExt(СчитаннаяСтрока);
   end;
   ОбработатьСтроку( stat, false, 43, 18,  "E\\AM" );
   ОбработатьСтроку( stat, false, 80, 6,   "E\\NUM" );

   ПропуститьСтроку(stat);
   ПропуститьСтроку(stat);
   ПропуститьСтроку(stat);
   continue0 = true;
   while( continue0 AND stat )
      ОбработатьСтроку( stat, true, 0, 0,  "F\\DOC\\DOC", "IsDocExt" );
      continue0 = IsDocExt(СчитаннаяСтрока);
   end;
   ОбработатьСтроку( stat, false, 43, 6,  "F\\NUM" );

   if ( stat )
      return 3;
   end;

   return -1;
end;