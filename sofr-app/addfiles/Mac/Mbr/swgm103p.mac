/*
$Name: swgm103p.mac
$Module: Межбанковские расчеты
$Description: Генерация сообщения по SWIFT MT103+
*/

import "swgenmes.mac";

record Party(party);
const  Relise = "103+";


macro GenMes( addrMes, addrPm, AddrOldMes )
  var field_value, BNC_Name, field72, INN, Tag, ComAmount, ComFIID, IsTransit, BaseAmount, CurrentCode, error, continue0;
  var ОстатокПоля70, field33BPresent = 0;;
  var Recreate = false;
  var RsPaym;
  RECORD combuf( oprsfcom );    
  RECORD oldwlMes (wlmes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( Oldwlmes,  addrOldMes );
     Recreate = true;
  end;

  PrintLog(2,"Генерация сообщения по МТ103+");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  IsTransit = (((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)) OR PM_PaymentIsChildTransit( RsPaym.PaymentID ));  
  var InMesID = 0;
  if(IsTransit)
    InMesID = getInMesIDByPayment(RsPaym);
  end;

  var CommissCode = GetCommisCode (RsPaym.ComissCharges);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 23B       */
  ЗаписатьПолеЛог( BankOperationCodeField_B, BANKOPERCODE_CRED ) ;

  /* 23Е   берем теперь из dwlpmrmprop_dbt.InstructionCode  */
  StrChangeRusXSet( RsPaym.InstructionCode, field_value );
  ЗаписатьПолеЛогВБлок( "23E", InstructionCodeField, string(field_value), NULL, Recreate, OldwlMes);
  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  
  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );

  var fld32A = field_value;

  /* 33B - Инструктируемая сумма, валюта */
  var fld33B = WriteField33B( RsPaym, InMesID, CommissCode, Recreate, OldwlMes );

  /* 36  - Кросс-курс */
  WriteField36( IsTransit, InMesID, fld32A, fld33B, Recreate, OldwlMes );

  /* 50A - приказодатель */
  field_value = FillOrderingCustomerField103( RsPaym, Tag );
    if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден плательщик" );
  else
    ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField+Tag, field_value, NULL, Recreate, OldwlMes );
  end;

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 52A - банк приказодателя */
  Tag = SWIFT_Tag_A;  
  field_value = FillOrderingInstitutionField(RsPaym, Tag, 0, "103+" );
  WriteFieldEx  (OrderingInstitutionField_A, field_value, Recreate, OldwlMes );

  /* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_A;
  field_value = FillSenderCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value, Recreate, OldwlMes );

  /* 54a - Корреспондент получателя */
  Tag = SWIFT_Tag_A;
  field_value = FillReceiverCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField_A, "", field_value, Recreate, OldwlMes );

  /* 55a - Третий рамбурсирующий посрдник */
  Tag = SWIFT_Tag_A;
  field_value = FillThirdReibursBankField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( ThirdReimbursementInstitutionField_A, "", field_value, Recreate, OldwlMes );    

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 56A - банк посредник */
  Tag = SWIFT_Tag_A;
  field_value = FillIntermediaryField(RsPaym, Tag, null, "MT103+" );
  WriteFieldEx  (IntermediaryField_A, field_value, Recreate, OldwlMes );

  /* 57A - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField(RsPaym, Tag, null, "MT103+" );
  if(Tag != SWIFT_Tag_A)
    RunError("|Не удалось записать поле 57 по опции А:|отсутствует SWIFT-код");
  end;
  WriteFieldEx  (AccountWithInstitutionField_A, field_value, Recreate, OldwlMes );

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCopyExternalFld(RsPaym);
  if (field_value != "")
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes );
  end;
  if (field_value == "")
    field_value = FillBeneficiaryCustomerField_A( RsPaym );
    if(field_value != "")
      ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_A, field_value, NULL,Recreate, OldwlMes );
    end;
  end;
  if (field_value == "")
    field_value = FillBeneficiaryCustomerField_F(RsPaym);
    if(field_value != "")
      ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_F, field_value, NULL,Recreate, OldwlMes );
    end;
  end;
  if (field_value == "")
    field_value = FillBeneficiaryCustomerField( RsPaym );
    if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден получатель" );
    end;
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes );
  end;

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 70  - информация для бенефициара */
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70 );
  WriteFieldEx  (DetailsOfPaymentField, field_value, Recreate, OldwlMes );  

  /* 71A - расходы */
  field_value = CommissCode;
    WriteFieldEx  (DetailsOfChargesField_A, field_value, Recreate, OldwlMes );  
  ЗаписатьКомиссииПлатежа( RsPaym, field_value, TRUE );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT103_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_STANDART, ОстатокПоля70 );
  if( field_value != "" )
    WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );  
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

