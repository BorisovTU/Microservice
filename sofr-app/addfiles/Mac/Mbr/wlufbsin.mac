/*
  $Name: wlufbsin.mac
  $Module: Межбанковские расчеты
  $Description: Импорт  сообщений  УФЭБС
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                      Импорт  сообщений  УФЭБС                            */
/*                                                                          */
/*  Имя файла: wlufbsin.mac                                                 */
/*  Создан:  06.10.04                                         BARS          */
/****************************************************************************/

import "wlimport.mac", "wluftool.mac", "wlufsign.mac", "xmlmestools.mac", "uf501.mac", bnk_dirfile, bnk_dttmfrmt;

const CONTEXTID_CHECK_SIGN_UFBS = "ТранспортМБР|9|Импорт_файла";

var ver_st = 1;
var PacketDate = "";
var NumberRace : integer = null;
var TypeRace : string = null;
var SettlementTime : time = null;

/*Сложить содержимое повторяющихся блоков в один массив*/
/*??? Пока не допускается повторяющихся полей, содержащих повторяющиеся поля*/
macro CutInsideContent(pat:string, node:object, arr:object) :bool
  
   var chNode:object;
   var chNodeList:object;
   var i = 0;
       
   /* Получаем список подчиненных узлов */
   chNodeList = node.childNodes();    
   if( chNodeList.length() )      
      while( (chNode = chNodeList.item(i)) != NULL )
         if( (StrUpr(GetNodeName(chNode.NodeName)) == StrUpr(pat)))
            /* Сохраняем узел документа выписки */
            arr(arr.Size()) = StrSubst(StrSubst(StrSubst(String(chNode.xml),StrFor(13),""),StrFor(10),""),StrFor(9),"");
            /* Удаляем узел из списка */
            if( node.removeChild( chNode ) == NULL )
               RunError( "Ошибка при удалении узла: " + pat );     
               return false;
            end;
   else
            i = i + 1;
         end;
   end;
end;

   return true;
end;

private class Map(_keys:TArray, _values:TArray)
    var keys:TArray = _keys;
    var values:TArray = _values;
    
    macro value(key)
      
      for(var idx, 0, keys.Size )
        if((keys[idx] == key) or ((ValType(key) == V_STRING) and (StrUpr(keys[idx]) == StrUpr(key))))
          return values[idx];
        end;       
      end;
      
      return null;
    end;
end;

private class Element
  var context:string = "";
  var field:string = "";
  var value:string = "";
  var elements:TArray = TArray;  
end;


/* Разобрать контент повторяющихся полей рекурсивно*/
/*Входящие парметры: */
/*  список названий тегов - соответствующих им полей*/
/*  исходный узел */
/*Результат: список рекурсивно вложенных элементов */
macro CutInsideContentRecursively(fields:Map, node:object):TArray

   var chNode:object;
   var chNodeList:object;
   var fieldName = "";
   var nodeName = "";
   var i = 0;
   
   var elements:TArray = TArray;
   
   chNodeList = node.childNodes(); 
   
   if( chNodeList.length() )      
      while( (chNode = chNodeList.item(i)) != NULL )
      
        nodeName = GetNodeName(chNode.NodeName);
        fieldName = fields.value(nodeName);
      
          if( fieldName and ( fieldName != "" ) ) 
            
            var idx = elements.Size;
            elements[idx] = Element;
            
            elements[idx].context = nodeName;
            elements[idx].field = fieldName;
            elements[idx].elements = CutInsideContentRecursively(fields, chNode );            
            elements[idx].value = StrSubst(StrSubst(StrSubst(String(chNode.xml),StrFor(13),""),StrFor(10),""),StrFor(9),"");            
            
            if( node.removeChild( chNode ) == NULL )
               RunError( "Ошибка при удалении узла: " + nodeName );     
               return false;
            end;
          else
            i = i + 1;
          end;
    end;
  end;
  
  return elements;
  
end;

private macro WriteFieldsRecursively(fields:TArray)

  for(var element, fields)                  
      if( not УстановитьКонтекстБлока(element.context) )
        RunError( "Ошибка при установке контекста блока: " + element.context );
      end;
      
      if( not ЗаписатьПоле( element.field, element.value ) )
        RunError( "Ошибка записи поля: " + element.field );     
      end;
      
      if(element.elements.Size > 0)
        WriteFieldsRecursively(element.elements);
      end;
      
      if( not УстановитьКонтекстБлока("..") )
        RunError( "Ошибка при установке родительского контекста блока" );
      end;
  end;
  
  return true;
end;

macro CutNodeText(node:object, arr:object, NodeName:string)
   var chNode:object;
   var chNodeList:object;
   var i = 0, ind = 0;
   var NodeText:string;
   /* Получаем список подчиненных узлов */
   chNodeList = node.childNodes();    
   if( chNodeList.length() )      
     while( (chNode = chNodeList.item(i)) != NULL )
       if( (StrUpr(GetNodeName(chNode.NodeName)) == StrUpr(NodeName)))
         NodeText = chNode.Text;
         while(strlen(NodeText)) 
           ind = 0;
           ind = index(NodeText,SYMB_ENDL);
           if(ind and (ind <= 2000))
             arr(arr.Size()) = SubStr(NodeText,1,ind);
             NodeText = SubStr(NodeText,ind+1);
           elif( strlen(NodeText) > 2000 )
             arr(arr.Size()) = SubStr(NodeText,1,2000);
             NodeText = SubStr(NodeText,2001);
           else
             arr(arr.Size()) = NodeText;
             NodeText = "";
           end;
         end;
          
          /* Удаляем узел из списка */
         if( node.removeChild( chNode ) == NULL )
           RunError( "Ошибка при удалении узла: " + NodeName);     
           return false;
         end;
       else
          i = i + 1;
       end;
     end;
   end;

   return true;
end;

private var thisRespID:integer, thisRespIDError:integer;

private macro GetRespID( error:@integer ):integer
  if( valtype( thisRespID ) == V_UNDEF )
    thisRespID = GetCodeParty( {MFO_RCC}, PTCK_BIC, thisRespIDError );
  end;
  error = thisRespIDError;
  return thisRespID;
end;

private macro IsCompoundForm(FormName : string) : bool
  return ( (FormName == NodeED107) or
           (FormName == NodeED243) or
           (FormName == NodeED244) or
           (FormName == NodeED245) or
           (FormName == NodeED280) or
           (FormName == NodeED374) or
           (FormName == NodeED332) or
           (FormName == NodeED379) or
           (FormName == NodeED381) or
           (FormName == NodeED375) or
           (FormName == NodeED463) or
           (FormName == NodeED465) or
           (FormName == NodeED508) or
           (FormName == NodeED510) or
           (FormName == NodeED530) or
           (FormName == NodeED541) or
           (FormName == NodeED574) or
           (FormName == NodeED802) or
           (FormName == NodeED805) or
           (FormName == NodeED807) or
           (FormName == NodeED808) or
           (FormName == NodeED811) or
           (FormName == NodeED814) 
         );
end;

private macro ProcessSessionID(node : object, NumberRace : @integer, TypeRace : @string)

  macro IsPacketNode(node : object) : bool
    if( SubStr( StrUpr( node.BaseName ), 1, 6) == "PACKET" )
      return true;
    end;

    return false;
  end;

  macro IsEDXXXNode(NodeName : string) : bool
    if( (substr(NodeName, 1, 2) == "ED") and
        StrIsNumber( substr(NodeName, 3, 3) )
      )
      return true;
    end;

    return false;
  end;

  macro NeedProcessSessionID(node : object, NumberRace : integer) : bool

    if( (NumberRace == null) 
        and
        ( IsPacketNode(node) or IsEDXXXNode(GetNodeName(node.NodeName)) )
      )
      return true;
    end;

    return false;
  end;

  macro NeedProcessSessionType(node : object, TypeRace : integer) : bool

    if( (TypeRace == null) 
        and
        ( IsPacketNode(node) or IsEDXXXNode(GetNodeName(node.NodeName)) )
      )
      return true;
    end;

    return false;
  end;

  macro NeedProcessSettlementTime(node : object, SettlementTime : time) : bool
    if( IsPacketNode(node) or IsEDXXXNode(GetNodeName(node.NodeName)) )
      return true;
    end;

    return false;
  end;


// begin

  var strNumberRace : string = "";
  if( NeedProcessSessionID(node, NumberRace) )
    if( IsPacketNode(node) )
      strNumberRace = ReadOptinalAttribute(node, "SessionID");
      if( strNumberRace == "" )        
        strNumberRace = ReadNodeText(node, "Session/SessionID", false);
      end;
    elif( IsEDXXXNode(GetNodeName(node.NodeName)) )
      strNumberRace = ReadOptinalAttribute(node, "SessionID");
      if( strNumberRace == "" )
        strNumberRace = ReadNodeText(node, "Session/SessionID", false);
        if( strNumberRace == "" )
          strNumberRace = ReadNodeText(node, "ProcessingDetails/Session/SessionID", false);
        end;
      end;
    end;
  end;

  var strSettlementTime : string = "";
  if( NeedProcessSettlementTime(node, SettlementTime) )
    if( IsPacketNode(node) )
      if( strSettlementTime == "" )        
        strSettlementTime = ReadNodeText(node, "Session/SettlementTime", false);
      end;
    elif( IsEDXXXNode(GetNodeName(node.NodeName)) )
      if( strSettlementTime == "" )
        strSettlementTime = ReadNodeText(node, "Session/SettlementTime", false);
        if( strSettlementTime == "" )
          strSettlementTime = ReadNodeText(node, "ProcessingDetails/Session/SettlementTime", false);
        end;
      end;
    end;
  end;

  var strTypeRace = "";
  if( NeedProcessSessionType(node, TypeRace) )
    if( IsPacketNode(node) )
      strTypeRace = ReadOptinalAttribute(node, "SessionType", "Session");
    elif( IsEDXXXNode(GetNodeName(node.NodeName)) )
      strTypeRace = ReadOptinalAttribute(node, "SessionType", "Session");
      if( strTypeRace == "")
        strTypeRace = ReadOptinalAttribute(node, "SessionType", "ProcessingDetails/Session");
      end;
    end;
  end;


  if(strNumberRace and StrIsNumber(strNumberRace))
    NumberRace = int(strNumberRace);
  end;

  var NewSettlementTime : time = null;
  if( strSettlementTime )
    NewSettlementTime = hhmmssToTime( strSettlementTime );
    if( (SettlementTime == null) or (SettlementTime < NewSettlementTime) )
      SettlementTime = NewSettlementTime;
    end;
  end;

  if( strTypeRace )
    TypeRace = strTypeRace;
  end;

  if( strSettlementTime or strNumberRace or strTypeRace )
    if( not SetWldSessOnUFBSImport( NumberRace, SettlementTime, TypeRace ) )
      RunError("Ошибка при сохранении атрибутов сеанса");
    end;
  end;

end;

class (TFldImportHandler) TFldImportHandlerUFBS
  InitTFldImportHandler();

  macro ProcessAttrNm( FieldName : @string )
    if( FieldName == "ForeignPaytsSystemMode" )
      FieldName = "ForeignPaytsSystemMo";
    end;
    if( FieldName == "CorrectionDescription" )  
      FieldName = "CorrectionDescriptio";
    end;
    if( FieldName == "NetAvailableLiquidity" )  
      FieldName = "NetAvailableLiquidit";
    end;
    if( FieldName == "IndeterminateAmountFlag" )  
      FieldName = "IndeterminateAmountF";      
    end;
  end;

  macro ProcessNodeNm( NodeName : @string )
    if( NodeName == "CorrectionDescription" )  
      NodeName = "CorrectionDescriptio";
    end;
  end;

end;

private macro GetCorAccFromMesNode(MesNode) : string
  var CorAcc : string = "";

  var FormName : string = MesNode.BaseName();
  if( InList(FormName, "ED113", "ED114") )
    CorAcc = ReadOptinalAttribute(MesNode, "CorrespAcc", "Payer/Bank");
  else
    CorAcc = ReadOptinalAttribute(MesNode, "CorrespAcc", "Payee/Bank");
  end;

  return CorAcc;
end;

private macro GetCorschemDepartmentByCorAcc(CorAcc : string) : integer
  var Dprt : integer = 0;

  var query : string = 
    "select t_Department "
    "  from dcorschem_dbt "
    " where t_FIID = 0 /*NATCUR*/ "
    "   and t_CorAccount = :CorAcc ";
  var params : TArray = makeArray( SQLParam("CorAcc", CorAcc) );

  var rs = execSQLselect(query, params);
  if(rs and rs.moveNext())
    Dprt = rs.value("t_Department");
  end;

  return Dprt;
  end;

private macro GetCorschemDepartment(MesNode) : integer
  var Dprt : integer = 0;

  var CorAcc : string = GetCorAccFromMesNode(MesNode);
  if(CorAcc)
    Dprt = GetCorschemDepartmentByCorAcc(CorAcc);
  end;

  return Dprt;
end;

private macro GetEDReceiverDepartment(MesNode) : integer
  var Dprt : integer = 0;

  var EDReceiver : string = ReadOptinalAttribute(MesNode, "EDReceiver");
  if(EDReceiver)
    var PartyID : integer = GetPartyIdByUIS(EDReceiver);

    if(PartyID > 0)
      Dprt = GetDepIdByPartyId(PartyID);
    end;
  end;

  return Dprt;
end;

private macro GetStartDepartment
( WlmesKind, 
  MesNode // узел с самим сообщением (без пакетов и конвертов)
)
  var StartDepartment : integer = 0;

  if( WlmesKind == MESKIND_PAYMENT)
    StartDepartment = GetCorschemDepartment(MesNode);
  end;

  if( not StartDepartment )
    StartDepartment = GetEDReceiverDepartment(MesNode);
  end;

  return StartDepartment;
end;

private macro GetInsideAbonent( wlmes, node )

  var OrgBIC = ReadOptinalAttribute( node, "OrgBIC");
  
  if( strlen(OrgBIC) != 0 )
    var PartyID = GetCodeOwnerID(PTCK_BIC, OrgBIC);
    if(PartyID > 0)
      wlmes.InsideAbonentID = PartyID;
      wlmes.InsideAbonentCode = OrgBIC;
      wlmes.InsideAbonentCodeKind = PTCK_BIC;
    end;
  end;
  
end;

private macro GetInsideAbonentForED8XX( wlmes, node )

  var AbCode = ReadOptinalAttribute( node, "EDReceiver");
  
  if( strlen(AbCode) != 0 )
    var PartyID = GetCodeOwnerID(PTCK_UIS, AbCode);
    if(PartyID > 0)
      wlmes.InsideAbonentID = PartyID;
      wlmes.InsideAbonentCode = AbCode;
      wlmes.InsideAbonentCodeKind = PTCK_UIS;
    end;
  end;
  if (wlmes.InsideAbonentID <= 0)
      wlmes.InsideAbonentID = {OurBank};
      wlmes.InsideAbonentCodeKind = PTCK_UIS;
      wlmes.InsideAbonentCode = GetPartyCode(wlmes.InsideAbonentID, wlmes.InsideAbonentCodeKind);
      if (wlmes.InsideAbonentCode == "")
          wlmes.InsideAbonentCodeKind = PTCK_BIC;
          wlmes.InsideAbonentCode = GetPartyCode(wlmes.InsideAbonentID, wlmes.InsideAbonentCodeKind);
      end;
  end;
end;

/* Импорт одного сообщения */
macro ОбработатьСообщение( node, ВидСообщения, НомерФормы, IsPack, IsMesSigned, p_RelatedRef )  
  var ДатаСоздания   = ToDate(ReadAttribute(node, "EDDate"));
  var TpShemID:integer, РелизФормы:integer, Author:string = ReadAttribute(node, "EDAuthor");

  var НомерДокумента = YYMMDD(ДатаСоздания) + Author + ReadAttribute(node, "EDNo");  
  var rs:object, select:string, chNode:object, chNodeList:object; 
  var nodename:string = "", fieldname:string, xml:string;
  var params:TArray, arrayNode = TArray();
  var arraySize:integer = 0, i:integer = 0, error;

  if( not IsMesSigned)
    if(IsChildNode(node, "SigValue"))
      IsMesSigned = true;
    end;
  end;

  std.out( 1, "  Прочитано сообщение: форма "+ node.BaseName() +" от "+ Author + " датой " + ДатаСоздания + " номер " + НомерДокумента );

  ProcessSessionID(node, @NumberRace, @TypeRace);

  var RespID = GetRespID( @error );
  if( error )
    ErrImport( string("В справочнике отсутствует код РКЦ ") + string({MFO_RCC}));
    return FALSE;
  end;

  TpShemID = ОпределитьТранспортнуюСхемуПоКоду( {MFO_RCC}, ВидКодаТранспорта, "", -1, "", -1, Транспорт, НомерФормы, РелизФормы, NULL, NULL, wlsess.TpFrmtID );
  if( TpShemID == -1 )
     RunError( string( "Не определена транспортная схема для респондента: ", {MFO_RCC} ) );          
  end;

  /* Если уже было такое сообщение от корреспондента - не закачиваем */
  select = "select mes.t_RlsFormID from dwlmes_dbt mes "
           "where  mes.t_Direct='X' and "
                  "mes.t_Department =:OperD and " 
                  "mes.t_TRN =:NumberDoc"
            //      "mes.t_OutsideAbonentID = :OutsideAbonentID and "
            //      "mes.t_OutsideAbonentDate = :DataCreate"
            ;  
  params = makeArray( SQLParam("OperD",            {OperDprt}),
                      SQLParam("NumberDoc",        НомерДокумента)//,
                    //  SQLParam("OutsideAbonentID", RespID),  
                    //  SQLParam("DataCreate",       ДатаСоздания)
                    );  

  rs = execSQLselect( select, params, TRUE );

  while( rs.MoveNext() )
    if( rs.value(0) == РелизФормы )
      std.out( 1, String( "    Cообщение c номером EDNo = ", ReadAttribute(node, "EDNo"), " за дату = ", ДатаСоздания, " ранее уже было принято - игнорируется") );
      return;
    end;
  end;

  /* Заполняем ответное сообщение */
  ClearRecord( wlmes );
  wlmes.TpSchemID           = TpShemID;
  wlmes.RlsFormID           = РелизФормы;
  wlmes.Kind                = ВидСообщения;
  wlmes.TRN                 = НомерДокумента;
  wlmes.OutsideAbonentDate  = ДатаСоздания;
  wlmes.OutsideAbonentID    = RespID;
  wlmes.AgentID             = RespID;
  FillMesCode( TRANSP_MCI, wlmes );  


  var rel_ref = NULL;
  
  rel_ref = EDNoDateAuthor();

  //Заполняем wlmes.RelatedRef из EDRefID или InitialED или OriginalEPD.
  //Если есть оба узла (EDRefID и InitialED), то приоритет у EDRefID.
  if(IsChildNode(node, "EDRefID"))
    //Если есть узел EDRefID, то все атрибуты должны быть
    rel_ref.EDNo     =        ReadAttribute(node, "EDNo",      "EDRefID");
    rel_ref.EDDate   = ToDate(ReadAttribute(node, "EDDate",    "EDRefID"));
    rel_ref.EDAuthor =        ReadAttribute(node, "EDAuthor",  "EDRefID");
    
    wlmes.RelatedRef = rel_ref.GetTrn();
  elif(IsChildNode(node, "OriginalEPD"))
    //Если есть узел OriginalEPD, то EDAuthor может отсутствовать
    rel_ref.EDNo     =        ReadAttribute(node, "EDNo",      "OriginalEPD");
    rel_ref.EDDate   = ToDate(ReadAttribute(node, "EDDate",    "OriginalEPD"));
    rel_ref.EDAuthor =        ReadOptinalAttribute(node, "EDAuthor",  "OriginalEPD");
    
    wlmes.RelatedRef = rel_ref.GetTrn();
  elif(IsChildNode(node, "InitialED"))
    //Если есть узел InitialED, то все атрибуты должны быть
    rel_ref.EDNo     =        ReadAttribute(node, "EDNo",      "InitialED");
    rel_ref.EDDate   = ToDate(ReadAttribute(node, "EDDate",    "InitialED"));
    rel_ref.EDAuthor =        ReadAttribute(node, "EDAuthor",  "InitialED");
    
    wlmes.RelatedRef = rel_ref.GetTrn();
  elif(p_RelatedRef)
    wlmes.RelatedRef = p_RelatedRef;  
  end;
  
  if( IsMesSigned )
    wlmes.Signed = SET_CHAR;
  end;

  wlmes.StartDepartment = GetStartDepartment(wlmes.Kind, node);

  GetInsideAbonent(wlmes, node);

  // Обработка ED802, ED805, ED807, ED808, 811, 814                                                     
  if( InList( node.BaseName(), NodeED802, NodeED805, NodeED807, NodeED808, NodeED811, NodeED814 ) )
    GetInsideAbonentForED8XX(wlmes, node);     
  end;

  if( not СоздатьЗапись( wlmes ) )
    RunError( "Невозможно создать запись по форме: " + node.BaseName() );
  end;

  /* Сохраняем xml-файл ED802, ED805, ED807, ED808 как прикрепленный объект. Сохраняем ED802, ED807, ED808 во временный файл, т.к. он может прийти в конверте */
  if( InList( node.BaseName(), NodeED802, NodeED805, NodeED807, NodeED808 ) )
    var FileDirectory = Bnk_GetTxtFileDir();    
    /* Записываем только ED802, ED807, ED808 во временный XML-файл */
    var tmpFileName = FileDirectory + "\\" + wlmes.TRN + ".xml";
    var strDoc = TStreamDoc( tmpFileName, "c", "rsansi" );
    var tmpXml = "<?xml version=\"1.0\" encoding=\"Windows-1251\" ?>\n" + node.xml;
    strDoc.WriteLine( tmpXml );
    strDoc = null;
                            
    /* Прикрепляем XML-файл с ED802, ED807, ED808 к созданному сообщению */
    var sWlMesID : string = makeObjectID( OBJTYPE_MES, null, wlmes );
    if( not LoadImageObj( tmpFileName, OBJTYPE_MES, sWlMesID, WLD_MES_IMGTYPE_XML_FILE ) )
      RunError( "Ошибка прикрепления файла " + tmpFileName + " к сообщению " + wlmes.MesID );
    end;

    /* Удаляем временный XML-файл с ED802, ED807, ED808 */
    RemoveFile( tmpFileName );
  end;

  /* Обрабатываем выписку */
  if( node.BaseName() == NodeED211 )
     nodename = NodeTransInfo;
     fieldname = UFBSDocExtField; 
  elif( node.BaseName() == NodeED207)
     nodename = NodeEDInfo;
     fieldname = UFBSDocExtField;
  elif( node.BaseName() == NodeED217)
     nodename = NodeCreditInfo;
     fieldname = UFBSDocInfoField;
  elif( node.BaseName() == NodeED221)
     nodename = NodeEDCopy;
     fieldname = UFBSDocExtField;
  elif( node.BaseName() == NodeED241)
     nodename  = NodeEDRequisiteList;
     fieldname = UFBSDocExtField;
  elif( node.BaseName() == NodeED215)
     nodename = NodeEDCopy;
     fieldname = UFBSDocExtField;
  elif( node.BaseName() == NodeED108)
     nodename = NodeCreditTransferTransactionInfo;
     fieldname = UFBSCreditTransferTransField;
  elif( GetNodeName(node.NodeName) == NodeED501 )
    ImportED501(node, wlmes.Trn);
    AddRepElem(wlmes.Kind, GetNodeName(node.NodeName()), "", $0 );
    return;
  elif( IsCompoundForm( GetNodeName(node.NodeName()) ) )
     ImportXmlNodeToMes(node, wlmes.RlsFormID, TFldImportHandlerUFBS());
     AddRepElem(wlmes.Kind, GetNodeName(node.NodeName()), "RUR", (moneyL(ReadOptinalAttribute(node, "Sum"))/100) );
     return;
  end;
      
  if(node.BaseName() == NodeED217)
    var fields = Map(makeArray(NodeCreditInfo, NodeAccDocInfo), makeArray(UFBSDocInfoField, UFBSAccDocInfoField));    
    arrayNode = CutInsideContentRecursively(fields, node);    
  elif(nodename and (not CutInsideContent(nodename, node, arrayNode)))
     RunError( "Ошибка при обработке повторяющегося поля" + nodename);
  end;
  
  if( node.BaseName() == NodeED219 )
     CutNodeText(node, arrayNode,NodeReportContent);
     nodename = NodeReportCon;
     fieldname = UFBSRepConStrField;
  end;

  if( node.BaseName() == NodeED330 )
     CutNodeText(node, arrayNode,NodeCorrectionDescription);
     nodename = NodeDescrCorr;
     fieldname = UFBSDescrStrField;
  end;

  if(nodename == NodeEDRequisiteList)
    nodename  = NodeEDRqstList;
  elif(nodename == NodeCreditTransferTransactionInfo)
    nodename  = NodeCreditTransferTrans;
  end;

  /*Надо, возможно, нарезать еще полей EDCopy сокращенного формата - EDCopyBrf*/
  if( (ver_st == 2) and (nodename == NodeEDCopy) and (not CutInsideContent(nodename + "Brf", node, arrayNode)))
     RunError( "Ошибка при обработке повторяющегося поля" + nodename + "Brf");
  end;

  /* Сохраняем оставщееся сообщение целиком в одном поле */
  if( not ЗаписатьПоле(XMLField, StrSubst(StrSubst(StrSubst(String(node.xml),StrFor(13),""),StrFor(10),""),StrFor(9),"")))
    RunError( "Ошибка записи поля: " + XMLField );     
  end;
  
  /* Записываем информацию о дате получения пакета */
  if( ((node.BaseName() == "ED101") or (node.BaseName() == "ED102") or (node.BaseName() == "ED103") or 
       (node.BaseName() == "ED104") or (node.BaseName() == "ED105") or (node.BaseName() == "ED110") or
       (node.BaseName() == "ED111") or (node.BaseName() == "ED108")
      ) and ( ValType(PacketDate) == V_STRING ) and ( PacketDate != "" ) 
    )
     if( not ЗаписатьПоле(NodePacketDate, PacketDate) )
       RunError( "Ошибка записи поля: " + NodePacketDate );     
     end;
  end;

  // Записываем информацию о документах выписки
  
  if((node.BaseName() == NodeED217) and (arrayNode.Size() > 0))
    if( not WriteFieldsRecursively(arrayNode) )
      RunError( "Ошибка записи полей ED217" );
    end;

    arrayNode.Size() = 0;
  elif( (arraySize = arrayNode.Size()) > 0)
    i = 0;
    
    if( not УстановитьКонтекстБлока(nodename) )
      RunError( "Ошибка при установке контекста блока: " + nodename );
    end;

    while( i < arraySize )
      if( not ЗаписатьПоле( fieldname, arrayNode(i) ) )
        RunError( "Ошибка записи поля: " + fieldname );     
      end;
      i = i + 1;
    end;
    
    if( not УстановитьКонтекстБлока("") )
      RunError( "Ошибка при установке нулевого контекста блока" );
    end;

    arrayNode.Size() = 0;
  end;

  AddRepElem(wlmes.Kind, GetNodeName(node.NodeName()), "RUR", moneyL(ReadOptinalAttribute(node, "Sum"))/100 );
end;

/* Обработать все сообщения */
macro ОбработатьСообщения(node:object, IsPack, IsMesSigned )
  var i;
  var child:object;
  var НомерФормы = -1, ВидСообщения;

  if( node.nodeType==CHILD_NODE )
    if( IsDocument(node.NodeName()) )
      ProcessSessionID(node, @NumberRace, @TypeRace);

      if( GetNodeName(node.NodeName()) == "ED273" )
        var RelatedRef = GetRefByEDNoDateAuthor(node);
        for( i, 0, node.childNodes.length-1 )
          child = node.childNodes.item(i);
          if( child and (child.nodeType==CHILD_NODE) )
            НомерФормы = ОпределитьФорму( Транспорт, GetNodeName(child.NodeName()), ВидСообщения );
            if( НомерФормы > 0 )
              ОбработатьСообщение( child, ВидСообщения, НомерФормы, IsPack, IsMesSigned, RelatedRef ); 
            end;
          end;
        end;
      else
        if( GetNodeName(node.NodeName())!="ED105" )
          НомерФормы = ОпределитьФорму( Транспорт, GetNodeName(node.NodeName()), ВидСообщения );
        else
          if( (ReadAttribute(node,"BIC","Payee/Bank")=={MFO_Bank}) and (ReadOptinalAttribute(node,"EDNo","InitialED")!="") )
            НомерФормы = ОпределитьФорму( Транспорт, "ED105C", ВидСообщения );
          else
            НомерФормы = ОпределитьФорму( Транспорт, GetNodeName(node.NodeName()), ВидСообщения );
          end;
        end;

        if( НомерФормы > 0 )        
          ОбработатьСообщение( node, ВидСообщения, НомерФормы, IsPack, IsMesSigned ); 
        end;
      end;

      return;
    end;
  end;

  i=0;
  while( i < node.childNodes.length )
    child = node.childNodes.item(i);
    if( child and (child.nodeType==CHILD_NODE) )
       ОбработатьСообщения( child, IsPack, IsMesSigned );
    end;
    i=i+1;
  end;

end;

/* Импорт сообщений УФЭБС */
macro UFBSInProc( TpID, ImportFileName, addrSess )
  var i:integer;
  var xml:object = ActiveX( "MSXML.DOMDocument" ), child:object, node:object;
  var IsPack:bool = false;
  var MessagesSigned:bool = true;  /* Пока считаем, что сообщения проверены в программе УАРМ КБР */
  // по умолчанию MessagesSigned=true, но в настройке может быть false
  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\TREATSIGNED", V_BOOL, MessagesSigned );

  var CryptoAPI = RsCryptoAPI();

  SetBuff( wlsess, addrSess );  
  NumberRace = null;

  if( not xml.load(ImportFileName) )
    ErrImport( string( "Неверный формат файла импорта|", ImportFileName,  "|Невозможно загрузить xml-документ" ) );
    return false;
  end;

  node = xml.DocumentElement;

  i = 0;
  if( xml.NodeType == DOCUMENT_NODE )
    while( i < xml.childNodes.length )
      child = xml.childNodes.item(i);
      if( child and (child.nodeType==CHILD_NODE) )
        node = child;
        break;
      end;
      i = i+1;
    end;
  end;

  

  /* Если содержимое оформлено конвертом, то ЭС содержится внутри env:Body */
  if( StrUpr( node.BaseName ) == "ENVELOPE" )
    node = node.selectSingleNode("//env:Body").firstChild;
  end;

  /* Если ЭС оформлен как конверт ЭЦП, то его содержимое находится в sen:Object в виде base64.
     Выполняем преобразование base64 для узла Object */
  if( StrUpr( node.BaseName ) == "SIGENVELOPE" )
    if( not ПроверкаЭЦП( CONTEXTID_CHECK_SIGN_UFBS, node ))
      return false;
    else
      MessagesSigned = true;
    end;
  end;

  /* Обрабатывается пакет ЭПД (тег PacketEPD) или пакет ЭСИД (тег PacketESID) */
  if( SubStr( StrUpr( node.BaseName ), 1, 6) == "PACKET" )
    IsPack = true;
  end;

  Транспорт = TpID;
  ВидКодаТранспорта = PTCK_BIC;

  if(IsPack) 

     var EDDate   = ToDate(ReadAttribute(node, "EDDate"));
     var EDAuthor = ReadAttribute(node, "EDAuthor");
     var EDNo = ReadAttribute(node, "EDNo");

     if( not УстановитьИдентификаторСеанса( YYMMDD( EDDate ) + EDAuthor + EDNo ) )
        return FALSE;
     end;
  else 
     if( not УстановитьИдентификаторСеанса("") )
        return FALSE;
     end;
  end;

  if(IsPack)
     PacketDate = ReadOptinalAttribute(node,"EDDate");

     if( not MessagesSigned)
       if(IsChildNode(node, "SigValue"))
         MessagesSigned = true;
       end;
     end;

     ProcessSessionID(node, @NumberRace, @TypeRace);
  end;
  
  if( GetNodeName(node.NodeName) == NodeED503 )
    ExecMacroFile("uf503.mac", "ImportED503", ImportFileName, node);
    return true;
  else
    /* Импорт всех сообщений из файла */
    ОбработатьСообщения( node, IsPack, MessagesSigned );
  end;

  PrintImportReport();

  return true;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro UFBSInProcV2( TpID, ImportFileName, addrSess )
   ver_st = 2;

   return UFBSInProc( TpID, ImportFileName, addrSess );
end;
