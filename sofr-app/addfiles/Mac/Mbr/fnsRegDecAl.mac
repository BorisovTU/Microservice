/*
  $Name:         fnsRegDecAl.mac
  $Module:       МБР
  $Description:  Функции для решений регистрационных органов RPO, ROO, APN, APO, APZ
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для решений регистрационных органов RPO, ROO, APN, APO, APZ
//
// Программист  : Чукина Т.А.
//
// Создан       : 18.06.2014
//
//-----------------------------------------------------------------------------
import "wlmnstls.mac", "bnk_ptlib.mac", "wltools.mac", "bnk_common.mac", "addr_ref.mac",
       FIInter;

class TRegDecision
  var DecisionNumber : string = "", // НомРеш, НомРешПр, НомРешОт - заполнять в наследниках
      БИК : string = "",
      КодНО : string = "",
      НаимНО : string = "",
      DecisionDate : string = "", // ДатаРеш, ДатаРешПр, ДатаРешОт - заполнять в наследниках
      ClientINN : string = "", // ИННЮЛ, ИННИП, ИНННП
      ClientKPP : string = "", // КПП, КППНП
      ДолжнОтпр : string = "",
      ФамОтпр : string = "",
      ТелОтпр : string = "";


  private macro SaveFieldsRlsSpecific
  ( fieldname : string, 
    fieldvalue : string, 
    blockname : string
  )
    // Функция предназначена для переопределения в конкретных релизах
  end;

  macro ReadFields
    var fieldname : string = "", fieldvalue : string = "", blockname : string = "";

    while( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if( InList(fieldname, "БИК", "БИКБПл") )
        БИК = fieldvalue;
      elif(fieldname == "КодНО")
        КодНО = fieldvalue;
      elif(fieldname == "НаимНО")
        НаимНО = fieldvalue;
      elif(fieldname == "ДолжнОтпр")
        ДолжнОтпр = fieldvalue;
      elif(fieldname == "ФамОтпр")
        ФамОтпр = fieldvalue;
      elif(fieldname == "ТелОтпр")
        ТелОтпр = fieldvalue;
      end;

      SaveFieldsRlsSpecific(fieldname, fieldvalue, blockname);
    end;

  end;

  private macro FillRgdBuf(wlmes : StrucRef, wlregdec : StrucRef)
    ClearRecord(wlregdec);
  
    // Заполнение учетных буферов
    wlregdec.OriginatorCodeKind       = PTCK_MNS;
    wlregdec.OriginatorCode           = КодНО;
    wlregdec.OriginatorName           = НаимНО; 
    wlregdec.OriginatorID             = ПолучитьКодСубъекта( wlregdec.OriginatorCode, wlregdec.OriginatorCodeKind );
    wlregdec.RecipientID              = wlmes.InsideAbonentID;
    wlregdec.RecipientCodeKind        = wlmes.InsideAbonentCodeKind;
    wlregdec.RecipientCode            = wlmes.InsideAbonentCode;
    wlregdec.RecipientName            = Bnk_GetPartyName(wlregdec.RecipientID);
    wlregdec.Number                   = DecisionNumber; 
    wlregdec.Date                     = ToDateYYYY_MM_DD(DecisionDate);
    wlregdec.DeliveryDate             = GetSessSysDate(wlmes);
    wlregdec.ClientINN                = ClientINN; 
    wlregdec.ClientKPP                = ClientKPP; 
    wlregdec.RespPersonPost           = ДолжнОтпр; 
    wlregdec.RespPersonFIO            = ФамОтпр; 
    wlregdec.RespPersonTel            = ТелОтпр; 
    wlregdec.RegPartyKind             = REGPT_TAXINSTITUTE;
  end;

  // Вставить связанные записи, после того как определился DecisionID
  private macro InsertRelatedRecords(DecID : integer) : bool
    // Метод предназначен для переопределения в наследниках
  end;

  macro GenRgd(wlmes : StrucRef) : bool

    FillRgdBuf(wlmes, wlregdec);

    var DecID : integer = 0;
    if( not ВставитьРешениеРегистрационногоОргана( wlregdec, DecID ) )
      std.msg("Ошибка при вставке решения регистрационного органа");
      return FALSE;
    end;

    if( DecID > 0 )
      if(not InsertRelatedRecords(DecID))
        return FALSE;
      end;
    end;
  
    return TRUE;
  end;

  // Описание ошибки "направлен налоговым органом не в тот банк"
  macro CreateErrListWrongBank(wlmes : StrucRef) : RsbWlError
    // для переопределения в наследниках
  end;
end;

private macro GenPB_RegDecToWrongBank
( wlmes : StrucRef, 
  RegDecision : TRegDecision

) : bool

  // Параметры создаваемого УО
  var ErrElement = FNS_RP_CANNOT_EXECUTED_BANK;
  var ErrDescription : string = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrElement);

  var ErrList : RsbWlError = RegDecision.CreateErrListWrongBank(wlmes);

  // Вставка УО
  var ResultID = 0;
  if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrElement, ErrDescription, ErrList, "", -1, 0, "", "", -1, 0, "", "", NULL, NULL, ResultID ) )
    std.msg( "Ошибка при вставке подтверждения банка" );
    return FALSE;
  end;

  // Связь подтверждения с входящим сообщением
  var meslnk : TRecHandler = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = ResultID;
  meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
  var MesLnkObj : RsbWlMesLnk = RsbWlMesLnk( meslnk.rec.ObjKind, 
                                             meslnk.rec.ObjID, 
                                             meslnk.rec.Direct );
  if( (MesLnkObj.Insert( meslnk ) != 0) or
      (MesLnkObj.Save() != 0)
    )
    std.msg( "Ошибка при создании связи подтверждения с сообщением" );
    return FALSE;
  end;

  return TRUE;
end;

macro GenRegDecision(wlmes : StrucRef, RegDecision : TRegDecision) : bool
  var IsGenerated : bool = false;

  RegDecision.ReadFields();

  if( wlmes.InsideAbonentID <= 0)
    IsGenerated = GenPB_RegDecToWrongBank(wlmes, RegDecision);
  else
    IsGenerated = RegDecision.GenRgd(wlmes);
  end;

  return IsGenerated;
end;
