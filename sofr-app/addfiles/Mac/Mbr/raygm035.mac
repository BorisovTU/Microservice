/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по "Луч" MF035                                       */
/*                                                                          */
/*  Имя файла: raygm035.mac                                                 */
/*  Создан:  30.03.09                                                       */
/****************************************************************************/
import "raygenmes.mac";

macro GenMes( addrMes, addrbuf )
  FILE Party( party ) key 0;
  file llval(llvalues) key 0;
  RECORD DpBuf( dpmsginf );
  var    msginf, msgdoc, msgParty;  
  var    error,PartyCode, field_value, ProcessingCode;

  SetBuff( wlmes,  addrMes );
  SetBuff( DpBuf,  addrbuf );
  
  msginf = RsbDepoInfoMessage(DpBuf.MessageID);

  if ( valtype(msginf)==V_UNDEF )
     RunError( "|Не определен объект класса" );
  end;

  PrintLog(2,"Генерация сообщения по МF010");

  /*Блок ORDER_HEADER*/
  Write_ORDER_HEADER_Block( msginf );

  ProcessingCode = msginf.ProcessingCode;

  /* Блок MF035 */
  ЗаписатьПолеЛогВБлок( MF035_Block, BeginOfBlock, MF035_Block );

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*deponent_c*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( deponent_c_Field, PartyCode );
    else
      RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
    end;
    /*dep_acc_c*/
    ЗаписатьПолеЛогСПроверкой( dep_acc_c_Field, msgParty.Account );
    /*sec_c*/
    ЗаписатьПолеЛогСПроверкой( sec_c_Field, msgParty.Partition );    
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (REAG)" );
  end; 
  
  
  /*client_cod*/   
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
  if ( valtype(msgParty)!=V_UNDEF )
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( client_cod_Field, PartyCode );
    end;
  end; 

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_PSET);
  if ( valtype(msgParty)!=V_UNDEF )
    /*reestr_cod*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( reestr_cod_Field, PartyCode );
    end;
    /*reestr_bic*/
    ЗаписатьПолеЛог( reestr_bic_Field, msginf.PartyCode );
  end; 

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_DEAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*sender_cod*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( sender_cod_Field, PartyCode );
    end;
    /*full_name*/
    party.PartyID = msgParty.PartyID;
    if ( getEQ(party) )
      ЗаписатьПолеЛог( full_name_Field, party.ShortName);
    end;
    /*person_bic*/
    ЗаписатьПолеЛог( person_bic_Field, msginf.PartyCode );
    /*sr_acc_c*/
    ЗаписатьПолеЛог( sr_acc_c_Field, msginf.Account );
  end; 
  

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
  if ( valtype(msgParty)!=V_UNDEF )
    /*pr_client_code*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( pr_client_code_Field, PartyCode );
    end;
    /*pr_client_name*/
    party.PartyID = msgParty.PartyID;
    if ( getEQ(party) )
      ЗаписатьПолеЛог( pr_client_name_Field, party.ShortName);
    end;
    /*pr_client_bic*/
    ЗаписатьПолеЛог( pr_client_bic_Field, msginf.PartyCode );
    /*pr_client_acc*/
    ЗаписатьПолеЛог( pr_client_acc_Field, msginf.Account );
  end; 
  
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_DEAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*oper_mark*/
    if( msgParty.HasInformation == "X")
      ЗаписатьПолеЛог( oper_mark_Field, "Y" );
    else
      ЗаписатьПолеЛог( oper_mark_Field, "N" );
    end;
    /*oper_num*/
    ЗаписатьПолеЛог( oper_num_Field, msgParty.ProcessingReference );
    /*oper_date*/
    ЗаписатьПолеЛог( oper_date_Field, YYYYMMDD(msgParty.ProcessingDate) );
  end; 

  /*need_cvit*/
  if((msginf.NeedCounterDraft == "X") and (ProcessingCode != "35") and (ProcessingCode != "35/1"))
    ЗаписатьПолеЛог( need_cvit_Field,"Y");
  end;
  /*reciv_payment*/  
  if(msginf.ExpenceBuyer)
    field_value = "Y";
  else
    field_value = "N";
  end;
  ЗаписатьПолеЛог( reciv_payment_Field, field_value);
  /*seсurity_c*/         
  ЗаписатьПолеЛог( security_c_Field, msginf.SecurityCode);
  /*sequrity_q*/         
  ЗаписатьПолеЛог( security_q_Field, msginf.QuantitytoBeSettled);

  msgParty = GetMessageParty(msginf,DPMSGPAT_DEPONENT,0);
  if ( valtype(msgParty)!=V_UNDEF )
    /*sec_price*/
    ЗаписатьПолеЛог( sec_price_Field, msgParty.DealAmount);
    /*curr_code*/  
    ClearRecord(f_wlfininstr);
    f_wlfininstr.FIID = msgParty.DealAmountCurrency;
    if( getEQ( f_wlfininstr ))
      ЗаписатьПолеЛог( curr_code_Field,  f_wlfininstr.Ccy);
    end;
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
  if ( valtype(msgParty)!=V_UNDEF )
    /*deponent_bic*/      
    ЗаписатьПолеЛог( deponent_bic_Field, msgParty.PartyCode);
  end;
  
  /*sequrity_isin*/
  ЗаписатьПолеЛог( security_isin_Field, FillISIN(msginf));  



  ЗаписатьПолеЛог( EndOfBlock, MF035_Block );
  /* Конец блока MF035 */

end;

macro CheckMes( addrMes )
  var field_name, field_value, ProcessingCode = "";
  SetBuff( wlmes, addrMes );

  while( СчитатьПоле( field_name, field_value ) )
    if(field_name == order_t_id_Field)
      ProcessingCode = field_value;
    end;    
    if( (field_name == deposit_c_Field ) or
        (field_name == contrag_c_Field ) or
        (field_name == reestr_cod_Field) or
        (field_name == deponent_c_Field) or
        (field_name == dep_acc_c_Field)  or
        (field_name == sec_c_Field) )
      /*контроль на русские буквы*/
      if(not CheckForRus( field_name, field_value, true))
        return false;
      end;
    elif(field_name == oper_mark_Field)
      if( (field_value != "Y") and (field_value != "N"))
        std.msg(string("В поле '",field_name,"' недопустимые символы"));
        return false;      
      end;
    elif(field_name == need_cvit_Field)
      if((ProcessingCode == "35") or (ProcessingCode == "35/1"))
        std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
        return false;            
      elif(field_value != "Y")
        std.msg(string("В поле '",field_name,"' недопустимые символы"));
        return false;      
      end;
    end;
    /*контроль по коду операции*/
  end;



  return true; 

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;