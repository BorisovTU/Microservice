/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                      Импорт  сообщений  МЦИ                              */
/*                                                                          */
/*  Имя файла: wlmciin.mac                                                  */
/*  Создан:  08.08.00                                         AVM           */
/****************************************************************************/

import "wlimport.mac", "wlmctool.mac", "cb_sql.mac", oralib, likepy;

/* Общие глобализмы */
private var СчитаннаяСтрока:string = "";    /* считанная и необработанная строка */
private var Счетчик_КС:integer     = 0;          /* Счетчик блоков документов (через контрольные суммы) */

private var ПричинаОтказа;
private var ЗаголовокВыписки;
private var ДатаОбработки;
private var NDocExt = 0; /* Количество документов выписки */

private var impTpShemID:integer;
private var impRespID:integer;
private var impFormName:string;
private var impKindMes:integer;
private var impRslFormID:integer;

private const ДлинаСтроки = 12345;  /* Максимальная длина строки (полноформатный документ) */
private const MaketR = "R", МакетЭСИД = "E000", MaketDelim = "#";
private const РазмерПричиныОтказа = 100;
private const FormDocUni   = "R (UNI)",
              FormDoc1Cut  = "R1скр",
              FormDoc2Cut  = "R2скр",
              FormDoc1Full = "R1плн",
              FormDoc2Full = "R2плн",
              FormConf     = "C",
              FormConfD    = "RD",
              FormRej      = "REJ",
              FormRest     = "REST",
              FormExt      = "E0003";

private const impMCI_OK     =  0, /* все ОК                                         */
              impMCIRest    =  1, /* сообщение об остатках на к/с                   */
              impMCI_Delim  =  2, /* найден разделитель сообщений                   */
              impMCI_EOF    =  3, /* найден конец файла                             */
              impMCI_MesErr = -1; /* ошибка при обработке сообщения (ругается сама) */

/* Определяет дату обработки сообщений */
macro ОпределитьДатуОбработки( строка )

   ДатаОбработки = date( int(substr(строка, 27,2)), int(substr(строка, 29,2)), int(substr(строка, 31,4)) ) ;
   return true;

   OnError(er) /* обработка ошибок */
      std.out( 1, string("Неверный идентификатор реестра") );
      return false; 
end;

/*****************************************************************************
  Процедура проверки того, что строка является записью о документе
  Возвращает значение типа V_INTEGER:
  = impMCI_OK    - если строка является записью о документе;
  = impMCI_Delim - если строка является шапкой или контрольной суммой документа;
  = impMCI_EOF   - если строка является результирующей в конце файла.

  Устанавливает глобальную переменную Счетчик_КС
  'Счетчик_КС' = 1  Ответные документы.
  'Счетчик_КС' = 2  Списания из УБР.
  'Счетчик_КС' = 3  Выписки.
  'Счетчик_КС' = 4  Неоплаченные документы.
  'Счетчик_КС' = 5  Неоплаченные документы (окончательно).
  'Счетчик_КС' = 6  Отозванные документы.
  'Счетчик_КС' = 7  Не прошедшие контроль документы.
******************************************************************************/
macro Проверка_начала_конца_файла( Строка )
   var Maket;

   maket = substr( Строка, 1, 4 );

   if ( (maket!=МакетЭСИД) AND (Счетчик_КС==0) )
       /* Считали идентификатор реестра */
       if ( not ОпределитьДатуОбработки(Строка) )
           return impMCI_MesErr;
       end;
       return impMCI_Delim;
   end;

   if( (maket == МакетЭСИД) AND (int(substr(Строка, 5, 1))>Счетчик_КС) )
      Счетчик_КС = int(substr(Строка, 5, 1));
      impTpShemID = -1;
      return impMCI_Delim;
   elif( substr(Строка, 1, 1) == MaketDelim )
      return impMCI_Delim;
   end;

   return impMCI_OK;
end;

macro ПроверитьНалоговуюИнформацию(СчитаннаяСтрока)
var FieldMes="";
var  StatusSost,    
     KBK    ,
     OKATO  ,
     OsnNalPay            ,
     NalPeriod            ,
     NumNalDoc            ,
     DateNalDoc           ,
     TypeNalPay           ;

 if (substr(СчитаннаяСтрока,760,2)!="  ")
   // Заполним буфера налоговых данных
   StatusSost = trim(substr(СчитаннаяСтрока,760,2));
   KBK        = trim(substr(СчитаннаяСтрока,762,19));
   OKATO      = trim(substr(СчитаннаяСтрока,781,11));
   OsnNalPay  = trim(substr(СчитаннаяСтрока,792,2));
   NalPeriod  = trim(substr(СчитаннаяСтрока,794,10));
   NumNalDoc  = trim(substr(СчитаннаяСтрока,804,15));
   DateNalDoc = trim(substr(СчитаннаяСтрока,819,10));
   TypeNalPay = trim(substr(СчитаннаяСтрока,829,2));
  // Определим какие поля не заполнены и заполним их 0000
  if ((strlen(KBK)==0))        
        StrSet (СчитаннаяСтрока, 762 , "0000000000000000000");
        FieldMes = FieldMes + " код КБК,";
   
  end;
  if ((strlen(OKATO)==0))      
        FieldMes = FieldMes + " код ОКАТО,";
        StrSet (СчитаннаяСтрока, 781 , "00000000000");

  end;
  if ((strlen(OsnNalPay)==0))  
        StrSet (СчитаннаяСтрока, 792 , "00");
        FieldMes = FieldMes + " Основание платежа,";
  end;
  if ((strlen(NalPeriod)==0))  
        FieldMes = FieldMes + " Налоговый период,";
        StrSet (СчитаннаяСтрока, 794 , "0000000000");
  end;
  if ((strlen(NumNalDoc)==0)) 
        FieldMes = FieldMes + " Номер налогового платежа,";
        StrSet (СчитаннаяСтрока, 804 , "000000000000000");
  end;
  if ((strlen(DateNalDoc)==0)) 
        FieldMes = FieldMes + " Дата налогового платежа,";
        StrSet (СчитаннаяСтрока, 819 , "0000000000");
  end;
  if ((strlen(TypeNalPay)==0)) 
        FieldMes = FieldMes + " Тип налогового платежа,";
        StrSet (СчитаннаяСтрока, 829 , "00");
  end;
 // Удалим последнюю запятую
 if (StrLen (FieldMes) >0) // Отсекаем сит. правильного заполненого док-а
  FieldMes = substr(FieldMes,1,strlen(FieldMes)-1);
  [Принят ошибочно оформленный документ № ### от ######## на сумму ##################,] 
  ( substr(СчитаннаяСтрока,75,3), substr(СчитаннаяСтрока,78,8), Money(substr(СчитаннаяСтрока,137,18))/100 );
  [номер счета плательщика ####################, номер счета получателя ####################.]
  (substr(СчитаннаяСтрока,35,20), substr(СчитаннаяСтрока,97,20) );
  print  ("Поля документа: ",FieldMes," заполнены нулями.");
  [ ];
 end;
 end;                                           
 return СчитаннаяСтрока;
end;

/* Пропустить сообщение с неизвестной формой */
macro ReadUnknownMsg()
  std.out( 2, СчитаннаяСтрока );
  return impMCI_OK;
end;

/* Считать идентификаторы сообщения */
macro СчитатьИдентификатор( Форма, НомерДокумента, ДатаСоздания, Отказ, Причина, Выписка)
  var УИД, НДокумента, Дата, FormID, НомерФормыСообщения, error;

  if( Счетчик_КС == 1 )   /* Ответный документ */
    if ( substr(СчитаннаяСтрока, 866, 1)=="1" )
       НомерФормыСообщения = FormDoc1Full;
    else
       НомерФормыСообщения = FormDoc1Cut;
    end;
    УИД = substr( СчитаннаяСтрока, 16, 10 );
    НДокумента = УИД + substr( СчитаннаяСтрока, 2, 6 ); /* Электронный номер документа */
    /*Дата = СчитатьДату(СчитаннаяСтрока, 78);*/
    Дата = ДатаОбработки;
  elif( Счетчик_КС == 2 )   /* Списания из УБР. */
    if ( substr(СчитаннаяСтрока, 866, 1)=="1" )
       НомерФормыСообщения = FormDoc2Full;
    else
       НомерФормыСообщения = FormDoc2Cut;
    end;
    УИД = substr( СчитаннаяСтрока, 16, 10 );
    НДокумента = УИД + substr( СчитаннаяСтрока, 2, 6 ); /* Электронный номер документа */
    /*Дата = СчитатьДату(СчитаннаяСтрока, 78);*/
    Дата = ДатаОбработки;
  elif(Счетчик_КС == 3)   /* Выпсики          */
    НомерФормыСообщения = FormExt;
    НДокумента = substr( СчитаннаяСтрока, 1, 6 );
    /*Дата = СчитатьДату(СчитаннаяСтрока, 7);*/
    Дата = ДатаОбработки;
    SetParm(5, true);     
  elif(Счетчик_КС == 4)   /* Отказ - нет денег               */
    НомерФормыСообщения = FormRej;
    НДокумента = substr( СчитаннаяСтрока, 1, 6 );
    /*Дата = СчитатьДату(СчитаннаяСтрока, 7);*/
    Дата = ДатаОбработки;
    if ( strlen(СчитаннаяСтрока)>=26 )
      if ( substr( СчитаннаяСтрока, 25, 2 )=="00" )
         SetParm(4,"Аннулирован");
      elif ( substr( СчитаннаяСтрока, 25, 2 )=="01" )
         SetParm(4, "Отложен, недостаток средств на счете плательщика" );
      elif ( substr( СчитаннаяСтрока, 25, 2 )=="02" )
         SetParm(4, "Отложен, возникновение пассивного остатка на активном счете" );
      else
         SetParm(4, "Отложен, неизвестная причина отказа" );       
      end;
    else
       SetParm(4, "Недостаточно средств - отложен" );
    end;
  elif(Счетчик_КС == 5)   /* Отказ - нет денег               */
    НомерФормыСообщения = FormRej;
    НДокумента = substr( СчитаннаяСтрока, 1, 6 );
    /*Дата = СчитатьДату(СчитаннаяСтрока, 7);    */
    Дата = ДатаОбработки;
    SetParm(4,"Недостаточно средств - аннулирован");
  elif(Счетчик_КС == 6)   /* Подтверждение отзыва            */
    НомерФормыСообщения = FormConfD;
    НДокумента = substr( СчитаннаяСтрока, 1, 6 );
    /*Дата = СчитатьДату(СчитаннаяСтрока, 7);*/
    Дата = ДатаОбработки;
    SetParm(4,"Документ отозван из МЦИ");
  elif(Счетчик_КС == 7)   /* Отказ - не прошли контроль      */
    НомерФормыСообщения = FormRej;
    if ( strlen(СчитаннаяСтрока)<=РазмерПричиныОтказа )
        ПричинаОтказа = СчитаннаяСтрока;
        return impMCI_Delim;
    else
       НДокумента = substr( СчитаннаяСтрока, 2, 6 );
       /*Дата = СчитатьДату(СчитаннаяСтрока, 78);*/
       Дата = ДатаОбработки;
       SetParm(4, ПричинаОтказа);
    end;
  else
    return impMCI_Delim;
  end;

  SetParm( 0, НомерФормыСообщения );
  SetParm( 1, НДокумента );

  SetParm( 2, Дата );

  if( ( Счетчик_КС == 1 ) OR ( Счетчик_КС == 2 ) OR ( Счетчик_КС == 3 ) )   /* Ответный документ, подтверждение, выписка */
    SetParm( 3, FALSE );
  else
    SetParm( 3, TRUE ); /* Всяческие отказы */
  end;

  if( (impFormName != НомерФормыСообщения) OR (impTpShemID == -1) OR (impKindMes == 0) OR (impRslFormID == -1) )
    /* Определяем ID формы сообщения */
    FormID = ОпределитьФорму( Транспорт, НомерФормыСообщения, impKindMes );
    if( FormID == -1 )
      ErrImport( string( "Не определен номер формы - ", НомерФормыСообщения,", сообщение игнорируется" ) );
      impFormName = "";
      ReadUnknownMsg();
      return impMCI_MesErr;
    end;
    /* Определяем транспортную схему */
    impTpShemID = ОпределитьТранспортнуюСхему( impRespID, -1, -1, Транспорт, FormID, impRslFormID, NULL, NULL, wlsess.TpFrmtID );
    if( impTpShemID == -1 )
      ErrImport( string( "Не определена транспортная схема для респондента: ", {MFO_RCC}, "форма сообщения: ", НомерФормыСообщения ) );
      impFormName = "";
      return impMCI_MesErr;
    end;

    impFormName = НомерФормыСообщения;
  end;

  /* Осуществляем проверку налоговой информации только для платежных форм */
  if( impKindMes == MESKIND_PAYMENT )
    СчитаннаяСтрока = ПроверитьНалоговуюИнформацию(СчитаннаяСтрока);
  end;

  return impMCI_OK;
end;

/* Прочитать текстовый блок неизвестной формы */
macro СчитатьБлокЦелевойИнформации( Форма, НомерДокумента, ДатаСоздания, Отказ, Причина, Выписка )
  var error, Строка, continue0, длина, result, stat;
  var Сумма = $0;
  var rs:object;
  var select:string;
  var params:TArray;

  std.out( 1, "Прочитано сообщение: макет " + Форма + " эл. номер " + НомерДокумента );
  std.out( 2, "Строка сообщения: " + СчитаннаяСтрока );

  /* Если уже было такое сообщение от корреспондента - не закачиваем */
  /* Пропускаем подтверждения на платежи, находящиеся в картотеке у корреспондента, */
  /* поскольку ранее было получено подтверждение отказа*/
  select = "select mes.t_RlsFormID from dwlmes_dbt mes where "+
                            "mes.t_Direct = 'X' and "+
                            "mes.t_Department =:OperD and "+ 
                            "mes.t_TRN = :DocNumber and "+
                            "mes.t_OutsideAbonentID = :RespID and "+
                            "mes.t_OutsideAbonentDate = :DataCreate";  
  params = makeArray( SQLParam("OperD",      {OperDprt}),
                      SQLParam("DocNumber",  НомерДокумента),
                      SQLParam("RespID",     impRespID),
                      SQLParam("DataCreate", ДатаСоздания));
  rs = execSQLselect( select, params, FALSE );
  while( rs.MoveNext() )
    if( rs.value(0) == impRslFormID )
      std.out( 1, "Cообщение уже было принято - игнорируется" );
      return impMCI_OK;
    end;
  end;

  /* Заполняем буфер импортируемого сообщения */
  ClearRecord( wlmes );
  wlmes.TpSchemID          = impTpShemID;
  wlmes.RlsFormID          = impRslFormID;
  wlmes.Kind               = impKindMes;
  wlmes.OutsideAbonentID   = impRespID;
  wlmes.AgentID            = impRespID;
  wlmes.TRN                = НомерДокумента;
  wlmes.OutsideAbonentDate = ДатаСоздания;
  FillMesCode( TRANSP_MCI, wlmes );

  if( not СоздатьЗапись( wlmes ) )
    ErrImport( "Невозможно создать запись по форме: " + Форма );
    return impMCI_MesErr;
  end;

  if( impKindMes == MESKIND_PAYMENT )
    Сумма = substr( СчитаннаяСтрока, 137, 18 );
    Сумма = moneyL(Сумма)/100;
  end;

  AddRepElem(wlmes.Kind, Форма, "RUR", Сумма);
  
  if ( Отказ )
    if( not ЗаписатьПоле( FormFldE57, СчитаннаяСтрока ) )
      ErrImport( "Ошибка записи поля: " + FormFldE57 );
      return impMCI_MesErr;
    end;

    if( not ЗаписатьПоле( FormFldDsc, Причина ) )
      ErrImport( "Ошибка записи поля: " + FormFldDsc );
      return impMCI_MesErr;
    end;
  elif( not Выписка )
    if( not ЗаписатьПоле( FormFldDoc, СчитаннаяСтрока ) )
      ErrImport( "Ошибка записи поля: " + FormFldDoc );
      return impMCI_MesErr;
    end;
  else
    if( not ЗаписатьПоле( FormFldHeadExt, ЗаголовокВыписки ) )
      ErrImport( "Ошибка записи поля: " + FormFldHeadExt );
      return impMCI_MesErr;
    end;

    if( not УстановитьКонтекстБлока( "A" ) )
      RunError("|Ошибка при установке контекста блока 'A'");
      return impMCI_MesErr;
    end;

    continue0 = true;
    while(continue0)/*Считываем остальные выписки*/
      if( not ЗаписатьПоле( FormFldDocExt, СчитаннаяСтрока ) )
        ErrImport( "Ошибка записи поля: " + FormFldDocExt );
        return impMCI_MesErr;
      end;
      длина = ДлинаСтроки;
      Строка = СчитатьСтроку( длина );
      if( (длина == КонецФайла) OR (Trim(Строка) == "") )
        return impMCI_EOF; /* Достигнут конец файла */
      else
        СчитаннаяСтрока = Строка;
      end;      
      result = Проверка_начала_конца_файла( Строка );
      if( ( result != impMCI_OK ) )
      return result;      
      end;
      NDocExt = NDocExt +1;
    end;
  end;

  return impMCI_OK;
end;

/***************************************************************************/
/*  Функция считывания сообщения в формате R-макета                        */
/*  Возвращает:                                                            */
/*  impMCI_OK     =  0, все ОК                                             */
/*  impMCIRest    =  1, сообщение об остатках на к/с                       */
/*  impMCI_Delim  =  2, найден разделитель сообщений                       */
/*  impMCI_EOF    =  3, найден конец файла                                 */
/*  impMCI_MesErr = -1; ошибка при обработке сообщения (ругается сама)     */
/***************************************************************************/
macro ОбработатьСообщение()

  /* Вспомогательные переменные */
  var Строка, длина, result, Форма, ДатаСоздания, Создатель, НомерДокумента,  Отказ, Причина, Выписка, Заголовок;

  /* Считываем загловок */
  длина = ДлинаСтроки;
  Строка = СчитатьСтроку( длина );
  if( (длина == КонецФайла) OR (Trim(Строка) == "") )
    return impMCI_EOF; /* Достигнут конец файла */
  else
    СчитаннаяСтрока = Строка;
  end;

  result = Проверка_начала_конца_файла( Строка );
  if( ( result != impMCI_OK ) )
    if( (result == impMCI_Delim) AND (Счетчик_КС == 3))
      ЗаголовокВыписки = Строка;
    end;
    return result;
  end;

  Причина = "";
  Выписка = false;
  result = СчитатьИдентификатор( Форма, НомерДокумента, ДатаСоздания, Отказ, Причина, Выписка);
  if ( result != impMCI_OK )
    return result;
  end;
  return СчитатьБлокЦелевойИнформации( Форма, НомерДокумента, ДатаСоздания, Отказ, Причина, Выписка );
end;

/* Процедура импорта сообщений МЦИ */
macro MCIInProc( TpID, ImportFileName, addrSess )
  var error, stat, continue0, NDoc = 0;

  SetBuff( wlsess, addrSess );
  
  Транспорт = TpID;
  ВидКодаТранспорта = PTCK_BIC;

  impTpShemID  = -1;
  impRespID    = -1;
  impFormName  = "";
  impKindMes   = 0;
  impRslFormID = 0;
  
  ПерейтиВНачалоФайла();

  /* Определим отправителя */
  impRespID = ПолучитьКодСубъекта( {MFO_RCC}, ВидКодаТранспорта, error );
  if( error )
    ErrImport( string("В справочнике отсутствует код расчетного центра вида ")+string(ВидКодаТранспорта));
    return FALSE;
  end;

  /* Считываем сообщения */
  continue0 = TRUE;
  while( continue0 )
     stat = ОбработатьСообщение(); NDoc = NDocExt+NDoc + 1;     
     Message("Обработано строк - "+string(NDoc));
     if( stat == impMCIRest )   /* Была обработана информация об остатках на корсчете - последняя в файле полезная запись */
       continue0 = FALSE;
     elif( stat == impMCI_EOF ) /* найден конец файла */
       continue0 = FALSE;
     elif( stat == impMCI_MesErr )
       return FALSE;
     elif( stat == 0 )
     end;

     /* Проверяем, действительно ли мы загружаем файл нужного формата */
     /* для МЦИ - это наличие символов Е000 */
     if( (NDoc == 2) AND (substr( СчитаннаяСтрока, 1, 4 ) !=  МакетЭСИД ))
       println( string( "Неверный формат файла импорта" ) );
       return FALSE;
     end;
     NDocExt = 0;
  end;

  PrintImportReport();
  return true;
end;


