/*
  $Name:         ufNtfCar_Rep.mac
  $Module:       МБР
  $Description:  Протокол процедуры генерации уведомлений о зачислении/невозможности зачисления
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Протокол процедуры генерации уведомлений о зачислении/невозможности зачисления
//
// Программист  : Чукина Т.А.
//
// Создан       : 12.04.2018
//
//-----------------------------------------------------------------------------

import "SwitchOutput.mac", "bnk_common.mac", "lib_str.mac", "bnk_dttmfrmt.mac",
       globals, "bnk_dirfile.mac", "ufNtfCar_lib.mac", "wltools.mac";

private macro GetRepFileName : string
  var Directory : string = "..\\txtfile\\";

  macro GetFileName() : string
    var DDDDDD : string = DDMMYY(date()),
        ЧЧMMСС : string = TimeToStr(time(), "HHMISS"),
        NNNN : string = GetLastSymbols( strLpad(string({oper}), 4, "0"), 4 );

    var FName : string = "ED244_" + DDDDDD + "_" + ЧЧMMСС + "_" + NNNN + ".txt";

    return Directory + FName;
  end;

//begin
  var FName : string = GetFileName();

  // если название совпадет с точностью до секунды, пробуем еще раз
  while( FileExists(FName) )
    FName = GetFileName();
  end;

  return FName;
end;

private macro GetDepListFromFirstPaym(PaymList : TArray) : string
  var dpdepName : string = "";

  if(PaymList.size)
    var PaymentID : integer = PaymList[0];
    var rs = execSQLselectPrm
      ( "Select dep.t_Name "
        "  from dpmpaym_dbt pm, ddp_dep_dbt dep "
        " where pm.t_PaymentID = :PaymentID "
        "   and dep.t_Code = pm.t_StartDepartment ",
        SQLParam("PaymentID", PaymentID)
      );

    if(rs and rs.moveNext())
      dpdepName = rs.value("t_Name");
    end;
  end;

  return dpdepName;
end;

private macro GetDprtPartyName(dpdepName : string) : string
  var PtName : string = "";

  var rs = execSQLselectPrm
    ( "Select pt.t_ShortName "
      "  from ddp_dep_dbt dep, dparty_dbt pt "
      " where dep.t_Name = :dpdepName "
      "   and pt.t_PartyID = dep.t_PartyID ",
      SQLParam("dpdepName", dpdepName)
    );

  if(rs and rs.moveNext())
    PtName = rs.value("t_ShortName");
  end;

  return PtName;
end;

private macro GetPaymNumber(PaymentID : integer) : string
  var PaymNumber : string = "";

  var rs = execSQLselectPrm
    ( "Select t_Number "
      "  from dpmrmprop_dbt "
      " where t_PaymentID = :PaymentID ",
      SQLParam("PaymentID", PaymentID)
    );

  if(rs and rs.moveNext())
    PaymNumber = rs.value("t_Number");
  end;

  return PaymNumber;
end;

private macro GetReqFields(ReqID : integer, ReqSysDate : @string, ReqTrn : @string)
  var rs = execSQLselectPrm
    ( "Select t_SysDate, t_Trn "
      "  from dwlreq_dbt "
      " where t_ReqID = :ReqID ",
      SQLParam("ReqID", ReqID)
    );

  if(rs and rs.moveNext())
    ReqSysDate = DDpMMpYYYY( rs.value("t_SysDate") );
    ReqTrn = rs.value("t_Trn");
  else
    ReqSysDate = "";
    ReqTrn = "";
  end;
end;

class (TSwitchOutput) TUfNtfCarryReport
  InitTSwitchOutput( GetRepFileName() );

  macro PrintRepHeader(ExecPayments : TArray, NonExecPayments : TArray, DepList : string)
    var DepName : string = "";
    if(not DepList)
      DepList = GetDepListFromFirstPaym( IfThenElse(ExecPayments.size, ExecPayments, 
                                                                       NonExecPayments) );
      DepName = GetDprtPartyName(DepList);
    end;


    [ 
      Протокол процедуры генерации уведомлений о зачислении/невозможности зачисления средств
    
      Дата отчета:  #
      Операционист: #
      Филиал:       #
    ]( DDpMMpYYYY(date()):l, 
       string( {oper} ) + " " + {Name_Oper},
       string(DepList) + " " + DepName );
  end;

  macro PrintTableTitle(NeedExecNotify : integer)

    if(NeedExecNotify == WLPM_EXECNOTIFY_SUCCESS)
    [
      Результат формирования уведомлений о зачислении средств];

    elif(NeedExecNotify == WLPM_EXECNOTIFY_FAIL)
    [
      Результат формирования уведомлений о невозможности зачисления средств];
    end;

  end;

  macro PrintTableHeader
    [ ┌────────────────────────────────────────────┬────────────────────────────────────┬────────────────────────────┐
      │                   Платеж                   │            Уведомление             │                            │
      ├───────┬──────────┬─────────────────────────┼──────────┬─────────────────────────┤          Примечание        │
      │ Номер │   Дата   │         Референс        │   Дата   │         Референс        │                            │
    ];
  end;

  macro PrintErrWrongReg(NeedExecNotify : integer)
    var ErrWrongReg : string = UfNtfCar_GetErrWrongReg(NeedExecNotify);

    [   #
    ](ErrWrongReg);
  end;

  macro PrintTableRow(PaymentID : integer, ReqID : integer, ErrMsg : string)
    RECORD wlmes(wlmes);
    FindWldMesByPaym(PaymentID, "X"/*WLD_MES_IN*/, 0/*TpID*/, wlmes);

    var ReqSysDate : string = "", ReqTrn : string = "";
    GetReqFields(ReqID, @ReqSysDate, @ReqTrn);

    [ ├───────┼──────────┼─────────────────────────┼──────────┼─────────────────────────┼────────────────────────────┤
      │#######│##########│#########################│##########│#########################│############################│
    ]( GetPaymNumber(PaymentID), DDpMMpYYYY(wlmes.SysDate), wlmes.Trn, ReqSysDate, ReqTrn, ErrMsg:w);
  end;

  macro PrintTableBottom

    [ └───────┴──────────┴─────────────────────────┴──────────┴─────────────────────────┴────────────────────────────┘
    ];
  end;
end;
