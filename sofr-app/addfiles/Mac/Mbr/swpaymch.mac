/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Проверка сообщений платежей SWIFT                                        */
/*                                                                          */
/*  Имя файла: swpaymch.mac                                                 */
/*  Создан:    31.12.02                                      Бабин А.П .    */
/****************************************************************************/

import "swgendoc.mac", "wlchmes.mac";

/* Платежная инструкция "Клиентский перевод" */
class CustomerTransfer
 var
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  OrdCustomer      :Customer,    /* Приказодатель */
  IsOrdFill        :bool,
  IsBnfFill        :bool,
  BnfCustomer      :Customer;    /* Бенефициар */

  ValueDate  = date(0,0,0);
  Currency   = "";
  Sum        = $0;
  IsOrdFill  = false;
  IsBnfFill  = false;
end; /* class CustomerTransfer */

var MTCheck = CustomerTransfer;

macro Fill32A( Date, Cur, Sum )
  MTCheck.ValueDate = Date;
  MTCheck.Currency  = Cur;
  MTCheck.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill50( Address, Account, System, Code, INN )
  MTCheck.OrdCustomer = Customer( Address, Account, "", INN );  
  MTCheck.IsOrdFill  = true;
  return TRUE;
end;

macro Fill59( Address, Account, System, Code, INN )
  MTCheck.BnfCustomer = Customer( Address, Account, "", INN );
  MTCheck.IsBnfFill  = true;
  return TRUE;
end;

/* Заполнение списка полей */
var Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_OPTIONAL,  "Read32A", "Fill32A", "" ),
    Fld32C = ТПолеФормы( ValueDateCurrencyCodeAmountField_C, FIELD_OPTIONAL,  "Read32A", "Fill32A", "" ),
    Fld32D = ТПолеФормы( ValueDateCurrencyCodeAmountField_D, FIELD_OPTIONAL,  "Read32A", "Fill32A", "" ),    
    Fld50  = ТПолеФормы( OrderingCustomerField,              FIELD_OPTIONAL,  "ReadD",   "Fill50",  "" ),    
    Fld59  = ТПолеФормы( BeneficiaryCustomerField,           FIELD_OPTIONAL,  "ReadD",   "Fill59",  "" );

macro ConstructMTCheck( RespID )
   var field_name, field_value, sizeList, i, fld, result = true;

   sizeList = ПоляФормы.size;

   while( result AND СчитатьПоле(field_name, field_value) )
       i = 0;
       while( i<sizeList )
          fld = ПоляФормы.Value(i);
          if( field_name==fld.Name )
             if( not fld.ОбработатьПолеФормы( field_value ) )
                 result = false;
             end;
             i = sizeList;
          else
             i = i+1;
          end;          
       end;
   end;

   return result;
end;

macro CheckMes( addrMes )
  SetBuff( wlmes, addrMes );

  if( not ConstructMTCheck( wlmes.OutsideAbonentID ) )
    std.msg( "Ошибка при разборе собщения" );
    return FALSE;
  end;

  if ( not FillBuffPayment(wlmes) )
     std.msg( "Не найдена связь сообщения с платежем" );
     return false;
  end;

  if ( not CheckMessagesPayment(wlpm) )
     std.msg( "Платежу соответствует несколько одинаковых сообщений|Откатите выгрузку в МБР данного платежа и попробуйте снова." );
     return false;
  end;

  if( not ПолучитьФинИнПоISO( MTCheck.Currency, wlfininstr ) )
     std.msg( "Не определена валюта сообщения по коду ISO :" +  MTCheck.Currency );
     return FALSE;
  end;  

  if ( ( MTCheck.IsOrdFill AND (MTCheck.OrdCustomer.Account!="") AND (wlpmpaym.PayerAccount!=MTCheck.OrdCustomer.Account) )  OR
       ( MTCheck.IsBnfFill AND (wlpmpaym.ReceiverAccount!=MTCheck.BnfCustomer.Account) ) OR 
       (wlpmpaym.PayAmount!=MTCheck.Sum) OR
       (wlpmpaym.ValueDate!=MTCheck.ValueDate) OR
       (wlpmpaym.PayFIID!=wlfininstr.FIID) )
      std.msg( string( "Сообщение не соответствует платежу.|Откатите выгрузку в МБР данного платежа и попробуйте снова.") );
      return false;
  end; 

  return true; 
end;