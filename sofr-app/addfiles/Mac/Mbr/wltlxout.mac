/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                     Экспорт сообщений TELEX                              */
/*                                                                          */
/*  Имя файла: wltlxout.mac                                                 */
/*  Создан:  09.06.03                                           Панов А.В.  */
/****************************************************************************/

import "wlexport.mac", "swtools.mac";

/***************************************************************************/
/* Сформировать код начала сообщения                                       */
/***************************************************************************/
macro ЗаписатьКодНачалаСообщения()
  ЗаписатьСтроку("");
  ЗаписатьСтроку("YYYY");
  ЗаписатьСтроку("");
  return TRUE;
end;

/***************************************************************************/
/* Сформировать заголовок                                                  */
/***************************************************************************/
macro ЗаписатьЗаголовокСообщения()
  var BankCode, error;

  BankCode = ПолучитьКодСубъекта({OurBank}, PTCK_TLX, error);
  if( error ) ErrExport( "Не найден TELEX-код Отправителя сообщения!" ); return FALSE; end;

  if( not ЗаписатьСтроку( "FROM" + ":" + BankCode) )
    ErrExport( "Ошибка при записи Отправителя сообщения!" );
    return FALSE; 
  end;

  if ( (wlmes.OutsideAbonentCodeKind==PTCK_TLX) AND (wlmes.OutsideAbonentCode!="") )
     BankCode = wlmes.OutsideAbonentCode;
     error = 0;
  else
     BankCode = ПолучитьОткрытыйКодСубъекта( wlmes.OutsideAbonentID, PTCK_TLX, error );
  end;
  if( error ) ErrExport( "Не найден TELEX-код Получателя сообщения!" ); return FALSE; end;

  if( not ЗаписатьСтроку( "TO" + ":" + BankCode ) )
    ErrExport( "Ошибка при записи Получателя сообщения!" );
    return FALSE; 
  end;

  if( not ЗаписатьСтроку( "DATE" + ":" + GetSWIFTDate( {curdate} ) ) )
    ErrExport( "Ошибка при записи Даты сообщения!" );
    return FALSE; 
  end;

  if( not DefineFormExport( wlmes.RlsFormID ) )
    return FALSE;
  end;

  ЗаписатьСтроку("");
  if( not ЗаписатьСтроку("MT" + ФормаЭкспорт.Name + ":" + StrUpr(ФормаЭкспорт.Description) ) )
    ErrExport( "Ошибка при записи Номера формы сообщения!" );
    return FALSE; 
  end;
  ЗаписатьСтроку("");

  return TRUE;
end;

/* Последовательный перебор и запись полей сообщения  */
macro ЗаписатьПоляСообщения()
  var field, buff;
  while( СчитатьПоле( field, buff ) )
    if( not ЗаписатьСтроку( string(":", field, ":", buff) ) )
      ErrExport("Ошибка записи поля сообщения" + string(field));
      return false;
    end;
  end;
  return true;
end;

/***************************************************************************/
/* Сформировать текст сообщения                                            */
/***************************************************************************/
macro ЗаписатьТекстСообщения()

  if( not ЗаписатьПоляСообщения() )
     return FALSE;
  end;

  return TRUE;
end;

macro ЗаписатьКодКонцаСообщения() 

  if( not ЗаписатьСтроку( "\n" + "ZZZZ"/*КодКонцаСообщения*/ ) )
    ErrExport( "Ошибка при записи кода конца сообщения" );
    return FALSE;
  end;

  return TRUE;
end;
  
/***************************************************************************/
/*  Функция формирования сообщения TELEX                                   */
/*  Возвращает: TRUE или FALSE                                             */
/***************************************************************************/
macro ЗаписатьСообщение()

  if (not ЗаписатьКодНачалаСообщения()) return false; end;
  if (not ЗаписатьЗаголовокСообщения()) return false; end;
  if (not ЗаписатьТекстСообщения()    ) return false; end;
  if (not ЗаписатьКодКонцаСообщения() ) return false; end;

  return TRUE;
end;

/* Макрос экспорта  */
macro TELEXOutProc
  var continue0 = 1, Документов = 0, err;

  if( not СчитатьЗапись( wlmes, err ) )
    if ( not err )
       std.msg("Не найдено ни одного сообщения для отправки");
    else
       std.msg("Ошибка чтения сообщения");
    end;
    return false;
  end;

  while( continue0 )
    if( not ЗаписатьСообщение() )
      return false;
    end;
    if( СчитатьЗапись( wlmes, err ) )
//      ЗаписатьРазделительСообщений();
    else
      if (not err)
         continue0 = 0;
      else
         ErrExport("Ошибка чтения сообщения");
         return false;
      end;
    end;

    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;

  return true;
end;
