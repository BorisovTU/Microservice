/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                        Экспорт сообщений "Луч"                           */
/*                                                                          */
/*  Имя файла: wlrayout.mac                                                 */
/*  Создан:    30.03.09                                                     */
/****************************************************************************/

import "wlexport.mac", "raygenmes.mac", oralib, likepy;

macro RayOutProc( fileName, addrSess )
  var xml:object = ActiveX("Microsoft.XMLDOM");
  var Batch:object,item:object, Document:object, CurNode:object, FirstNode:object, ParNode:object;
  var field, field_value, err;

  SetBuff( wlSess, addrSess );

  xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='Windows-1251'") );  

  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if ( not err )
       ErrExport("Не найдено ни одного сообщения для отправки");
    else
       ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end; 

  /* Формируем файл экспорта */

  Batch = xml.createElement("Batch");
  item  = xml.createElement("Documents_amount");
  item.appendChild(xml.createTextNode("1") );
  Batch.appendChild(item);
  Document = xml.createElement("Document");
  Document.setAttribute("DOC_ID",1);  
   
  CurNode = FirstNode = Batch.appendChild(Document);
  while( СчитатьПоле( field, field_value ) )
    if( field == "begin" )
      item = xml.createElement(field_value);
      CurNode = CurNode.appendChild(item);
    elif( field == "end" )
      if( field_value != CurNode.nodename)
        ErrExport(string("Нарушена структура сообщения",wlmes.Trn));
        return false;
      else
        CurNode = CurNode.parentNode;
      end;
    else
      item  = xml.createElement(field);
      item.appendChild(xml.createTextNode(TransField(field_value)) );
      CurNode.appendChild(item);
    end;
  end;    
  
  if(CurNode.nodename != FirstNode.nodename)
    ErrExport(string("Нарушена структура сообщения",wlmes.Trn));
    return false;
  end;

  xml.appendChild(Batch);

  /*Если сообщений больше одного ругаемся*/
  if( СчитатьЗапись( wlmes, err ) )
    ErrExport( "Сообщения 'Луч' должны выгружаться в файл по одному.|Неверно настроен формат транспорта" );
    return false;
  end;    
  
  xml.Save(fileName); 

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ErrExport(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;