/*
  $Name:         ufNtfCar_Create.mac
  $Module:       МБР
  $Description:  Формирование уведомления о зачислении
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Формирование уведомления о зачислении
//
// Программист  : Чукина Т.А.
//
// Создан       : 10.04.2018
//
//-----------------------------------------------------------------------------

import WldInter, MesInter, PaymInter, "wltools.mac", "wluftool.mac", "ufNtfCar_lib.mac",
       "uf243244.mac", "bnk_common.mac", CTInter;

private macro GetDateFromRef(Ref : string) : date
  var ednda : EDNoDateAuthor = EDNoDateAuthor();
  ednda.ConstructByTrn(Ref);
  return ednda.EDDate;
end;

private macro GetRegValAnswerText(NeedExecNotify : integer) : string
  var RegBranch : string = "", DefaultVal : string = "";

  if(NeedExecNotify == WLPM_EXECNOTIFY_SUCCESS)
    RegBranch = "ЗАЧИСЛЕНИЕ";
    DefaultVal = "Подтверждение зачисления денежных средств на счет получателя средств";
  elif(NeedExecNotify == WLPM_EXECNOTIFY_FAIL)
    RegBranch = "ЗАЧИСЛЕНИЕ НЕВОЗМОЖНО";
    DefaultVal = "Невозможно зачислить денежные средства по указанному в распоряжении банковскому счету получателя средств";
  else
    RunError(ERR_WRONG_NEEDEXECNOTIFY, ERR_WRONG_NEEDEXECNOTIFY);
  end;

  var RegPath : string = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\УВЕДОМЛЕНИЯ\\" + 
    RegBranch +"\\ТЕКСТ ОТВЕТА";

  return Bnk_GetRegistryValue(RegPath, V_STRING, DefaultVal);
end;

// коды поясняющих полей в строке заменить значениями этих полей
// Код поля - это текст выделенный символами "//"
private macro SubstExplFldCodesToValues(s : string, Payment : RsbPayment) : string
  var PayerBank : TRecHandler = TRecHandler("pmroute"), 
      PrevInstrAgent : TRecHandler = TRecHandler("pmroute"), 
      SenderBank : TRecHandler = TRecHandler("pmroute"), 
      ExecutorBank : TRecHandler = TRecHandler("pmroute"), 
      IntermediaryBank : TRecHandler = TRecHandler("pmroute"), 
      ReceiverBank : TRecHandler = TRecHandler("pmroute");
  Payment.GetOperationParticipantsFromRoute
      ( PrevInstrAgent, SenderBank, ExecutorBank, 
        IntermediaryBank, PayerBank, ReceiverBank );

  var SubstPairs : TArray = TArray(); // пары (заменяемый текст, замещающий текст)

  var Len : integer = StrLen(s);
  var Offset : integer = 1;

  while(Offset < Len)
    var OpenBracketPos : integer = 0, CloseBracketPos : integer = 0,
        FldCode : string = "";

    OpenBracketPos = index(s, "//", Offset);
    if(OpenBracketPos > 0)
      CloseBracketPos = index(s, "//", OpenBracketPos + 2);
    end;

    if(CloseBracketPos > 0)
      FldCode = SubStr(s, OpenBracketPos + 2, CloseBracketPos - OpenBracketPos - 2);
    end;

    if(FldCode)
      var FldValue : string = "";
      var IsMatchFound : bool = GetFieldValFromPaym
        ( null, FldCode, @FldValue, Payment.GetPM_PAYM(), Payment.GetPMRMPROP(), 
          Payment.GetCREDIT(), Payment.GetDEBET(), null,
          PayerBank, PrevInstrAgent, SenderBank, 
          ExecutorBank, IntermediaryBank, ReceiverBank );

      if(not IsMatchFound)
        var ErrMsg : string = UfNtfCar_GetErrWrongReg(Payment.NeedExecNotify);
        RunError(ErrMsg, ErrMsg);
      end;

      SubstPairs[SubstPairs.size] = TMesField(FldCode, FldValue);
    end;

    if(CloseBracketPos > 0)
      Offset = CloseBracketPos + 2;
    else
      Offset = Len;
    end;
  end;

  for(var Fld : TMesField, SubstPairs)
    s = StrSubst(s, "//" + Fld.FieldName + "//", Fld.FieldValue);
  end;

  return s;
end;

private macro GetNarrative(Payment : RsbPayment) : string
  var AnswerText : string = "";
  var RegText : string = GetRegValAnswerText(Payment.NeedExecNotify);
  if(RegText)
    AnswerText = SubstExplFldCodesToValues(RegText, Payment);// заменить в строке коды поясняющих полей на значения этих полей
  end;

  return AnswerText;
end;

private macro InsertAddFld
( Flds : RsbAddFld,
  FieldNumber : string,
  FieldValue : string,
  PaymentID : integer,
  Sort : integer
)
  var FldRec : TRecHandler = TRecHandler("wladdfld.dbt");
  FldRec.rec.Type = "П";

  FldRec.rec.Number = FieldNumber;
  FldRec.rec.Name = Bnk_GetLLValuesName(OBJTYPE_ADDFLD_EXPLANATORYFIELD, FldRec.rec.Number);  
  FldRec.rec.Value = FieldValue;
  FldRec.rec.ParentID = 0;
  FldRec.rec.PaymentID = PaymentID;
  FldRec.rec.Sort = Sort;

  var stat : integer = Flds.Insert(FldRec);
  if(stat)
    var ErrMsg : string = "Ошибка при вставке записи в список дополнительных полей ответа\n" + GetErrMsgEx(stat);
    RunError(ErrMsg, ErrMsg);
  end;
end;

private macro GetEnterDate(Payment : RsbPayment) : string
  var EnterDate : date = Payment.ReceiverChargeOffDate;

  const ZeroDate : date = date(0, 0, 0);
  if(EnterDate == ZeroDate)
    var query : string =
      "Select rm.t_ReceiverChargeOffDate "
      "  from dpmlink_dbt pmlink, dpmrmprop_dbt rm "
      " where pmlink.t_InitialPayment = :InitialPaymentID "
      "   and pmlink.t_LinkKind in ( :PMLINK_KIND_CLOSACC, "
      "                              :PMLINK_KIND_PARTPAYMENT, "
      "                              :PMLINK_KIND_RETREDIR ) "
      "   and rm.t_PaymentID = pmlink.t_PurposePayment "
      "   and rm.t_ReceiverChargeOffDate > :ZeroDate ";

    var rs = execSQLselectPrm
    ( query,
      SQLParam("InitialPaymentID", Payment.PaymentID),
      SQLParam("PMLINK_KIND_CLOSACC", PMLINK_KIND_CLOSACC),
      SQLParam("PMLINK_KIND_PARTPAYMENT", PMLINK_KIND_PARTPAYMENT),
      SQLParam("PMLINK_KIND_RETREDIR", PMLINK_KIND_RETREDIR),
      SQLParam("ZeroDate", ZeroDate)
    );

    if(rs and rs.moveNext())
      EnterDate = rs.value("t_ReceiverChargeOffDate");
    end;
  end;

  if(EnterDate == ZeroDate)
    var ErrMsg : string = "Не найдена дата зачисления средств на счет получателя. Попробуйте отправить уведомление позже или введите его вручную";
    RunError(ErrMsg, ErrMsg);
  end;

  return YYYY_MM_DD(EnterDate);
end;

private macro GetAccDocDate(Payment : RsbPayment) : string
  return YYYY_MM_DD(Payment.Date);
end;

private macro GetAccDocNo(Payment : RsbPayment) : string
  return GetLastSymbols(Payment.Number, 6);
end;

private macro FillAddFlds(Req : RsbWlRequest, Payment : RsbPayment)
  var Flds : RsbAddFld = Req.AddFields;

  var PaymentID : integer = Payment.PaymentID;
  var Sort : integer = 0;
  InsertAddFld(Flds, "RequestCode", "00", PaymentID, Sort = Sort + 1);

  if(Payment.NeedExecNotify == WLPM_EXECNOTIFY_SUCCESS)
    InsertAddFld(Flds, "EnterDate", GetEnterDate(Payment), PaymentID, Sort = Sort + 1);
  else
    InsertAddFld(Flds, "AccDocDate", GetAccDocDate(Payment), PaymentID, Sort = Sort + 1);
    InsertAddFld(Flds, "AccDocNo", GetAccDocNo(Payment), PaymentID, Sort = Sort + 1);
    InsertAddFld(Flds, "PayerAcc", Payment.PayerAccount, PaymentID, Sort = Sort + 1);
    InsertAddFld(Flds, "PayerName", Payment.PayerName, PaymentID, Sort = Sort + 1);
    InsertAddFld(Flds, "PayeeAcc", Payment.ReceiverAccount, PaymentID, Sort = Sort + 1);
    InsertAddFld(Flds, "PayeeName", Payment.ReceiverName, PaymentID, Sort = Sort + 1);
  end;

  InsertAddFld(Flds, "Sum", MoneyToStrUFBS(Payment.BaseAmount), PaymentID, Sort = Sort + 1);
end;

private macro DefineRls(Req : RsbWlRequest, TpID: integer)
  var TpSchemID: integer = 0, RlsFormID: integer = 0;

  if(not WldDefineRlsForm
         ( OBJTYPE_REQ, 
           Req.RecipientID, 
           WLD_MES_KIND_ANSWER, 
           TpID, TpSchemID, RlsFormID, Req.SubKind )
    )
    RunError("Ошибка при подборе релиза");
  end;

  var stat : integer = Req.SetTpSchemID(TpSchemID);

  if(not stat)
    stat = Req.SetRlsFormID(RlsFormID);
  end;

  if(stat)
    var ErrMsg : string = "Ошибка при установке параметров релиза\n" + GetErrMsgEx(stat);
    RunError(ErrMsg, ErrMsg);
  end;
end;

private macro LinkAnswerToPayment(Req : RsbWlRequest, Payment : RsbPayment)
  var ReqLinks : RsbWlReqLnk = Req.ReqLinks;

  var lnkRec : TRecHandler = TRecHandler("wlreqlnk");
  lnkRec.rec.ObjID = Payment.InWlPmID;
  lnkRec.rec.ObjKind = OBJTYPE_PAYMENT;
  lnkRec.rec.ObjDirect = "X"; // WLD_MES_IN

  var stat : integer = ReqLinks.Insert(lnkRec);
  if(stat)
    var ErrMsg : string = "Ошибка при создании привязки ответа к платежу\n" + GetErrMsgEx(stat);
    RunError(ErrMsg, ErrMsg);
  end;
end;

macro UfCreateNotifyPmCarry
( PaymentID : integer, 
  Code : string, 
  ReqID : @integer,
  ErrMsg : @string

) : integer

  RECORD wlmes(wlmes);
  if(not FindWldMesByPaym(PaymentID, "X" /*WLD_MES_IN*/, TRANSP_UFBS, wlmes))
    RunError("Не найдено входящее сообщение по транспорту УФЭБС для платежа ИД=" + PaymentID);
  end;

  var Payment : RsbPayment = RsbPayment(PaymentID);

  var Req : RsbWlRequest = RsbWlRequest();
  Req.OriginatorID = Payment.ReceiverBankID;
  Req.OriginatorCodeKind = PTCK_BIC;
  Req.OriginatorCode = GetPartyCode(Req.OriginatorID, Req.OriginatorCodeKind);
  Req.OriginatorName = Payment.ReceiverBankName;  
  Req.RecipientCodeKind = PTCK_UIS;
  Req.RecipientCode = substr( wlmes.TRN, 7, 10 );  
  Req.RelatedRef = wlmes.Trn;
  Req.Direct = ""; // WLD_MES_OUT
  Req.Kind = MESKIND_ANSWER;
  Req.SubKind = WLD_SUBKIND_ANS_NOTIFY;
  Req.InitDateMes = GetDateFromRef(Req.RelatedRef);
  Req.InitFormIDMes = GetFormIDByRlsID(wlmes.RlsFormID);
  Req.Corschem = Payment.InCorschem;
  Req.FIID = Payment.InCorschemFIID;
  Req.Queries = Code;
  Req.Narrative = GetNarrative(Payment);
  
  var rs = execSQLselect( 
                        " SELECT OBJCODE.T_OBJECTID, " +
                        "        COUNT (OBJCODE.T_OBJECTID) OVER (PARTITION BY (OBJCODE.T_CODE)) " +
                        "   FROM DOBJCODE_DBT OBJCODE " +
                        "  WHERE     OBJCODE.T_CODE = :CODE " +
                        "        AND OBJCODE.T_CODEKIND = :CODEKIND " +
                        "        AND OBJCODE.T_OBJECTTYPE = :OBJTYPE_PARTY " +
                        "        AND OBJCODE.T_BANKDATE <= :BANKDATE " +
                        "        AND (   OBJCODE.T_BANKCLOSEDATE = :ZERODATE " +
                        "             OR OBJCODE.T_BANKCLOSEDATE >= " +
                        "                :BANKCLOSEDATE ) ",
                        makeArray( 
                          SQLParam( "CODE", Req.RecipientCode ),
                          SQLParam( "CODEKIND", Req.RecipientCodeKind ),
                          SQLParam( "OBJTYPE_PARTY", OBJTYPE_PARTY ),
                          SQLParam( "BANKDATE", Req.InitDateMes ),
                          SQLParam( "ZERODATE", date(0,0,0) ),
                          SQLParam( "BANKCLOSEDATE", Req.InitDateMes )
                          ),
                        false
                      );
  
  if( rs and rs.moveNext() and ( rs.value(1) == 1 ) )
    Req.RecipientID = rs.value(0);
    Req.RecipientName = Bnk_GetPartyName( rs.value(0) );
  else
    Req.RecipientID = -1;
  end; 

  FillAddFlds(Req, Payment);
  DefineRls(Req, getTpIDofTpSchem(wlmes.TpSchemID));
  LinkAnswerToPayment(Req, Payment);

  var stat : integer = Req.SaveChanges();
  if(not stat)
    ReqID = Req.ReqID;
  else
    ErrMsg = "Ошибка при сохранении ответа в БД\n" + GetErrMsgEx(stat);
  end;

  return stat;

OnError(er)
  ErrMsg = RsbGetErrorEx(er);
  return 1362; // ALERR_LOGICERRINMACRO
end;
