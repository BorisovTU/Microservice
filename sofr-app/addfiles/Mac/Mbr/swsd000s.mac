/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по SWIFT-SB MT000S                                     */
/*                                                                          */
/*  Имя файла: swsd000s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Ответ */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */  
  Answers          :string,        /* Текст ответа  */
  Narrative        :string,
  InitFormName     :string,        /* Форма инициирующего сообщения */
  InitDate         :date,          /* Дата инициирующего сообщения */
  Description      :string,   

  Sender           :Bank;          /* Отправитель */

  TRF              = "";
  RelatedRef       = "";
  Answers          = "";
  InitFormName     = "";
  InitDate         = date(0,0,0);

  Narrative = "";
  Description = "";

end; /* class StatementMessage */

var MT000S : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro FillRT(SubType)
  return true;
end;

macro Fill20( TRF )
  MT000S.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MT000S.RelatedRef = TRF;
  return TRUE;
end;

macro Fill77A( Str )
  MT000S.Narrative = Str;
  return TRUE;
end;

macro FillCopy( Str )
  return TRUE;
end;

macro НайтиКодОтветаНаСообщение000S(answercode: string) :string

  var result = "Причина помещения в отбракованные: " + answercode;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_description from dwlcodes_dbt where t_algnum = 39 and t_code=:code";
  
  params = makeArray( SQLParam("code", answercode));
  rs = execSQLselect( select, params, FALSE );
  if ( rs and rs.MoveNext() )
     result = rs.value(0);
  end;
        
  return result;
end;

/* Заполнение списка полей формы  MT000S*/
var FldSN      = ТПолеФормы( УникальныйНомерСообщенияСМФР,    FIELD_MANDATORY, "ReadTRF",     "FillSN",   "" ),
    FldRN      = ТПолеФормы( НомерСвязанногоСообщенияСМФР,    FIELD_OPTIONAL,  "ReadTRF",     "FillRN",   "" ),
    FldRT      = ТПолеФормы( ТипИПодтипСообщения,             FIELD_OPTIONAL,  "ReadRT",      "FillRT",   "" ),
    Fld20      = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF",     "Fill20",   "" ),
    Fld21      = ТПолеФормы( RelatedReferenceField,           FIELD_MANDATORY, "ReadTRF",     "Fill21",   "" ),
    Fld77A     = ТПолеФормы( NarrativeField,                  FIELD_MANDATORY, "Read77ASB",   "Fill77A",  "" ),
    FldMFIELDS = ТПолеФормы( CopyMandatoryFields,             FIELD_OPTIONAL,  "ReadMFIELDS", "FillCopy", "" );

macro ConstructMT000S( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;
  var buf;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MT000S.Sender   = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMT000S */

macro GenDoc( addrMes )
  var rs:object;
  var select:string;
  var params:TArray;
  var строка:string;
  MT000S = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация учетного объекта по МТ000S");
  /*Проверяем ключ входящего сообщения*/ 
  if( not CheckMesKey( wlmes ))
    return false;
  end;
  ClearRecord(wlreq);

  if( not ConstructMT000S( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  select =   "select dw.t_mesID, dw.t_state from dwlmes_dbt dw where dw.t_direct = chr(0) " 
           + "and dw.t_TRN = :RelatedRef and dw.t_Department = :dep ";     
  params = makeArray( SQLParam("RelatedRef", MT000S.RelatedRef),
                      SQLParam("dep", {OperDprt} ));
  rs = execSQLselect( select, params, TRUE );

  if( not rs.MoveNext() )
    std.msg("Не найдено исходное сообщение");
    return FALSE;
  end;  

  if ( rs.value(1)<30 )
     std.msg( string("|Исходящее сообщение ", wlmes.RelatedRef, " не отправлено") );
     return false;
  end;
  
  строка = StrSubst(MT000S.Narrative,"\n","");
  строка = StrSubst(строка," ","");
  if((Substr(строка,1,1)=="/") And (Substr(строка,4,1)=="/"))
    строка = НайтиКодОтветаНаСообщение000S(строка);
  else
    строка = MT000S.Narrative;
  end;

  if ( not ОшибкаЛогическогоКонтроляСообщения(rs.value(0), строка) )
     std.msg( string( "Не удалось поместить в отбракованные сообщение ID = ", rs.value(0) ));
     return false;
  end;

  /*Так было раньше*/
  /* Заполнение учетных буферов 
  wlreq.TRN          = MT000S.TRF;
  wlreq.RelatedRef   = MT000S.RelatedRef;
  wlreq.Queries      = MT000S.Answers;
  wlreq.InitFormMes  = MT000S.InitFormName;
  wlreq.InitDateMes  = MT000S.InitDate;
  if ( not НайтиКорсхемуПоКонтрагенту(wlmes.OutsideAbonentID, wlreq.Corschem, wlreq.FIID) )
      std.msg( "Не определена схема расчетов" );
      return FALSE;
  end;  
  
  if( not ВставитьОтвет( wlreq, MT000S.Narrative, MT000S.Description ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end; */

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
