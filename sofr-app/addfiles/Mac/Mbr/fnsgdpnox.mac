/*
$Name:           fnsgdpnox.mac
$Module:         МБР
$Description:    Генерация платежа по сообщению PNO_300
*/

import wldoc, wlmnstls, OprInter, FIInter, PTInter, PaymInter, wlgendoc, "bnk_common.mac";

private macro GetAccountFIIDFromDatabase( Account:string ):integer

  var select:string = "SELECT t_Code_Currency " +
                     " FROM daccount_dbt " +
                     " WHERE t_Account = :Account " +
                     "   AND t_Type_Account not like '%П%' " +
                     "   AND t_Type_Account not like '%Н%' " +
                     "   AND t_Type_Account not like '%М%' " +
                     "   AND t_Type_Account not like '%U%' ";
                      
  var params:TArray = makeArray( SQLParam( "Account", Account ) );
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return -1;
end;
/*
macro ВнутреннийПлатеж( BankID )
    const DEP_TYPE_FILIAL = 1; /* Когда проэкспортят, тогда заменить на нее */
    var rs:object;
    var select:string;
    var params:TArray;

    select = "select 1 from ddp_dep_dbt dep where "+
               "dep.t_PartyID =:BankID and dep.t_NodeType = :Dep and dep.t_status <> 3 and " +
               "dep.t_AccessMode < 3";
    params = makeArray( SQLParam("BankID", BankID),
                        SQLParam("Dep", DEP_TYPE_FILIAL));
    rs = execSQLselect( select, params, FALSE );

    if( rs and rs.moveNext() )
       return true;
    end;
    return false;
end;
*/

private macro GetWlSessDate( SessID:integer ):date

  var select = "select t_BankDate from dwlsess_dbt where "+
               "t_SessionID =:SessID ";
  var params = makeArray( SQLParam("SessID", SessID) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )
    return rs.Value(0);
  end;
  return date(0,0,0);
end;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация платежа по PNO");

  /* Заполнение учетных буферов */
  ClearRecord( wlpmpaym );
  wlpmpaym.FIID = wlpmpaym.PayFIID = wlpmpaym.BaseFIID = wlpmpaym.OrderFIID = ALLFININSTR;
  ClearRecord( wlpmpropdeb );
  ClearRecord( wlpmpropcred );
  ClearRecord( wlpmrmprop );
  ClearRecord( wlpmdemand );

  var field_name, field_value, error = 0;
  var INNNP = "", KPPNP = "";
  var NumAccPayer = "", NumAccBankPayer = "";
  var NumAccReceiver = "", NumAccBankReceiver = "";
  var INNPol = "", KPPPol = "";
  var ВидПор : string = "";
  while( СчитатьПоле( field_name, field_value ) )
    if( field_name == "НомПоруч" )
      wlpmrmprop.Number = field_value;
    end;
    if( field_name == "ДатаПоруч" )
      wlpmrmprop.Date = ToDateYYYY_MM_DD(field_value);
    end;
    if( field_name == "ВидПлат" )
      wlpmrmprop.PaymentKind = field_value;
    end;
    if( field_name == "СумПлат" )
      wlpmpaym.BaseAmount = moneyL( strSubst(field_value, "-", "") )/100;
    end;
 /*   if( field_name == "НомПоруч" )
      wlpmrmprop.Number = field_value;
    end;  */
    if( field_name == "ИНННП" )
      INNNP = field_value;
    end;
    if( field_name == "КППНП" )
      KPPNP = field_value;
    end;
    if( field_name == "Плательщ" )
      wlpmrmprop.PayerName = field_value;
    end;
    if( field_name == "НомСчПл" )
      NumAccPayer = field_value;
    end;
    if( field_name == "НомСчБПл" )
      wlpmrmprop.PayerCorrAccNostro = field_value;
    end;
    if( field_name == "БанкПл" )
      wlpmrmprop.PayerBankName = field_value;
    end;
    if( field_name == "БанкПол" )
      wlpmrmprop.ReceiverBankName = field_value;
    end;
    if( field_name == "БИКБПол" )
      wlpmpropcred.BankCode = field_value;
    end;
    if( field_name == "НомСчПол" )
      NumAccReceiver = field_value;
    end; 
    if( field_name == "НомСчБПол" )
      NumAccBankReceiver = field_value;
    end;
    if( field_name == "ИННПол" )
      INNPol = field_value;
    end;
    if( field_name == "КПППол" )
      KPPPol = field_value;
    end;
    if( field_name == "Получ" )
      wlpmrmprop.ReceiverName = field_value;
    end;
    if( field_name == "ВидОп" )
      wlpmrmprop.Shifroper = field_value;
    end;
    if( field_name == "НазнПл" )
      wlpmrmprop.Ground = field_value;
    end;
    if( field_name == "ВидПор" )
      ВидПор = field_value;
    end;
    if( field_name == "ОчерПл" )
      wlpmrmprop.Priority = int(field_value);
    end;
    if( field_name == "КодПл" )
      wlpmrmprop.UIN = field_value;
    end;
    if( field_name == "Статус" )
      wlpmrmprop.TaxAuthorState = field_value;
    end;
    if( field_name == "КБК" )
      wlpmrmprop.BTTTICode = field_value;
    end;
    if( field_name == "ОКТМО" )
      wlpmrmprop.OKATOCode = field_value;
    end;
    if( field_name == "КодОсн" )
      wlpmrmprop.TaxPmGround = field_value;
    end;
    if( field_name == "СрокУплТр" )
      wlpmrmprop.TaxPmPeriod = field_value;
    end;
    if( field_name == "НомТреб" )
      wlpmrmprop.TaxPmNumber = field_value;
    end;
    if( field_name == "ДатаТреб" )
      wlpmrmprop.TaxPmDate = field_value;
    end;
    if( field_name == "ТипПлат" )
      wlpmrmprop.TaxPmType = field_value;
    end;
  end;
  wlpmpaym.DocKind = WL_INDOC;
  wlpmpaym.PrimDocKind = WL_INDOC;
  wlpmpaym.ValueDate = {curdate};
  wlpmpaym.BaseFIID = NATCUR;

  wlpmrmprop.PayerINN = INNNP;
  if( KPPNP )
    wlpmrmprop.PayerINN = wlpmrmprop.PayerINN + "/" + KPPNP;
  end;

  wlpmpaym.Payer = 0; //ПолучитьКодСубъекта( wlpmrmprop.PayerINN, PTCK_INN );
  
  if( NumAccPayer != "" )
    wlpmpaym.PayerAccount = NumAccPayer;
  end;

  if( wlpmpaym.PayerAccount )
    wlpmpaym.FIID = GetAccountFIIDFromDatabase( wlpmpaym.PayerAccount );
    if( wlpmpaym.FIID == ALLFININSTR )
      wlpmpaym.FIID = Bnk_GetAccountFIID( wlpmpaym.PayerAccount );
    end;
  end;

  if( wlpmpaym.FIID == wlpmpaym.BaseFIID )
    wlpmpaym.Amount = wlpmpaym.BaseAmount;  

  elif(wlpmpaym.FIID != ALLFININSTR)
    if( not ConvSumCross( wlpmpaym.Amount, wlpmpaym.BaseAmount, wlpmpaym.ValueDate, 
                          wlpmpaym.BaseFIID, wlpmpaym.FIID ) )
      RunError("Ошибка конвертации суммы");
    end;
  end;

  wlpmpaym.PayAmount = wlpmpaym.BaseAmount;
  wlpmpaym.PayFIID = wlpmpaym.BaseFIID;
  wlpmpropcred.PayFIID = wlpmpaym.FIID;
  wlpmpropdeb.PayFIID = wlpmpaym.PayFIID;
  
  if( wlmes.InsideAbonentID > 0 )
    wlpmpaym.PayerBankID = wlmes.InsideAbonentID;
    wlpmpropdeb.CodeKind = wlmes.InsideAbonentCodeKind;
    wlpmpropdeb.BankCode = wlmes.InsideAbonentCode;
  else
    wlpmpaym.PayerBankID = wlmes.AgentID;
    wlpmpropdeb.CodeKind = wlmes.AgentCodeKind;
    wlpmpropdeb.BankCode = wlmes.AgentCode;
  end;
  
  wlpmpropcred.CodeKind = PTCK_BIC;
  wlpmpaym.ReceiverBankID = ПолучитьКодСубъекта( wlpmpropcred.BankCode, wlpmpropcred.CodeKind );
  wlpmrmprop.ReceiverCorrAccNostro = NumAccBankReceiver;

  if( NumAccReceiver != "" )
    wlpmpaym.ReceiverAccount = NumAccReceiver;
  end;

  wlpmrmprop.ReceiverINN = INNPol + "/" + KPPPol;
  
  wlpmpaym.Receiver = ПолучитьКодСубъекта( wlpmrmprop.ReceiverINN, PTCK_INN, error );
  record Acc(account);
  if( error or ( wlpmpaym.Receiver <= 0) )    
    error = ПолучитьСчет( wlpmpaym.PayFIID, wlpmpaym.ReceiverAccount, Acc, true );
    if (not error)
      wlpmpaym.Receiver = Acc.Client;
    end;
  end;

  wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  wlpmpropdeb.Corschem = -1;
 /* if (ВнутреннийПлатеж(wlpmpaym.PayerBankID))
    wlpmpropdeb.Group = PAYMENTS_GROUP_INTERNAL;
  else
    wlpmpropdeb.Group = PAYMENTS_GROUP_EXTERNAL;
  end; */
  wlpmpropcred.IsSender = "X" /*SET_CHAR*/;
  wlpmpropcred.Corschem = -1;
  wlpmdemand.AcceptTerm = PM_DEMAND_TERM_WITHOUTACCEPT;
  wlpmdemand.Accept = PM_DEMAND_ACCEPT_NONE;
  if( ВидПор == "КГН" )
    wlpmdemand.IsESID = ESID_KGN;
  else
    wlpmdemand.IsESID = IS_ESID_PAYMENT;
  end;

  wlpmpaym.PayerBankEnterDate = {curdate};
  wlpmrmprop.PayDate = GetWlSessDate( wlmes.SessionID );

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  // Вставка платежа в базу данных
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop, PRT_Debet, wlpmdemand ) )
    std.msg( "Ошибка при вставке платежа" );
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
