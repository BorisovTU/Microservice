/*
  $Name: wlcaches.mac
  $Module: Межбанковские расчеты
  $Description: Кеши для МБР
*/

// Кеши для МБР

import unicache, oralib, likepy, RsbDataSet;


// Кеш ID форм сообщения


class (TUniCache) TFormsByName

  InitTUniCache( V_STRING );

  private class (TUniCacheKey) TFormsByNameKey ( p_TpID:integer, p_Name:string )
    var m_TpID:integer = p_TpID,
        m_Name:string  = p_Name;
    macro AsSimpleValue():variant
      return string( m_TpID:o:6, m_Name );
    end;
  end;

  macro Key( p_TpID:integer, p_Name:string ):TFormsByNameKey
    return TFormsByNameKey( p_TpID, p_Name );
  end;

  private macro Find( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool
    p_ErrText = "";
    p_ErrCode = 0;

    var form:TRecHandler = TRecHandler( "wlmesfrm" );
    ClearRecord( form );
    var select:string = "select mesfrm.t_FormID, mesfrm.t_KindMes"+
                        " from dwlmesfrm_dbt mesfrm where"+ 
                        " mesfrm.t_TpID = :TpID"+
                        " and mesfrm.t_Name = :Name";
    var params:TArray = makeArray( SQLParam("TpID", p_Key.m_TpID),
                                   SQLParam("Name", p_Key.m_Name) );
    var rs:RsdRecordset = execSQLselect( select, params, FALSE );

    if( rs.MoveNext() )
      form.rec.FormID  = int( rs.value( 0 ) );
      form.rec.KindMes = int( rs.value( 1 ) );
    else
      form.rec.FormID = -1;
    end;

    p_Value = form;

    return ( form.rec.FormID != -1 );
  end;

end;


// Кэш полей блоков релиза сообщения

class TRlsFld
  var FieldID : integer = 0,
      Name    : string = "",
      Master  : integer = 0,
      BlockFlag : string = "",
      BlockName : string = "",
      MandatoryFlag : string = "";
end;

class (TUniCache) TRlsFldsByMaster

  InitTUniCache( V_INTEGER );

  private var RlsFormID : integer = 0;

  macro SetRlsFormID( NewRlsFormID : integer )
    if(RlsFormID != NewRlsFormID)
      RemoveAll();
      RlsFormID = NewRlsFormID;
    end;
  end;

  private macro Find( p_Key:variant, p_Value:@variant, p_ErrCode:@integer, p_ErrText:@string ):bool
    p_ErrText = "";
    p_ErrCode = 0;

    var RlsFlds : TArray = TArray();

    var Master : integer = p_Key;

    var cmd : RsdCommand = RsdCommand();

    var query : string = 
    "select fld.t_FieldID, "
    "       Nvl(tpf.t_Name, chr(1)) t_Name, "
    "       fld.t_Master, "
    "       fld.t_BlockFlag, "
    "       fld.t_BlockName, "
    "       fld.t_MandatoryFlag "
    "  from dwlmesfld_dbt fld, dwltpfld_dbt tpf "
    " where fld.t_RlsFormID = :RlsFormID "
    "   and fld.t_Master    = :Master "
    "   and tpf.t_TpFieldID(+) = fld.t_TpFieldID "
    " order by fld.t_GroupIndex ";

    cmd.AddParam("RlsFormID", RSDBP_IN, RlsFormID);
    cmd.AddParam("Master",    RSDBP_IN, Master);

    cmd.CmdText = query;

    var rs = TRsbDataSet(cmd);

    var i : integer = 0;
    while( rs.MoveNext() )
      var fld : TRlsFld = TRlsFld();
      fld.FieldID = rs.FieldID;
      fld.Name = rs.Name;
      fld.Master = rs.Master;
      fld.BlockFlag = rs.BlockFlag;
      fld.BlockName = rs.BlockName;
      fld.MandatoryFlag = rs.MandatoryFlag;

      RlsFlds[i] = fld;
      i = i + 1;
    end;

    p_Value = RlsFlds;

    return ( i != 0 );
  end;

end;
