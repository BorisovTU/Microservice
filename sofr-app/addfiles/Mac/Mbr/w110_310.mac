/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Вернуть из картотеки у корреспондента                                   */
/*  Имя файла: w110_310.mac                                                 */
/*  Создан:  17.10.00                                         Бабин А.П.    */
/****************************************************************************/

import  InsCarryDoc, OprInter, BankInter, RMInter, "rmconst.mac", "wldoc.mac" ;

RECORD Corschem( corschem );
var PaymentObj:RsbPayment;

var DEBET = "Д", KREDIT = "К" ;

macro ExecuteStep( doc, first )
  var err, DKFlag = "", FlagTrans = 0, stat;
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();

  if ( PaymentObj.IsCredit() )
     DKFlag = KREDIT;
  else 
     DKFlag = DEBET;
  end;

  FlagTrans = PaymentObj.IsTransit();  

  if ( (PaymentObj.Receiver AND ВидСубъекта(PaymentObj.Receiver, PTK_BANK)) OR
       ((PaymentObj.DocKind!=PS_PAYORDER) AND
        (PaymentObj.DocKind!=PS_CPORDER) AND 
        (PaymentObj.DocKind!=WL_INDOC) AND
        (PaymentObj.DocKind!=WL_WIPM) ) )
    if ( not CarryPlanDocuments(PaymentObj.PaymentID) )
       MsgBox("Ошибка при помещении планируемой проводки в проведенные");
       return 1;
    end;
  end;

  if( PaymentObj.ReceiverFIID == 0 )
    /* Заполняем поля документа */
    paymtr.Chapter = 1 ;
    paymtr.FIIDPayer = PaymentObj.ReceiverFIID ;
    paymtr.Sum = PaymentObj.ReceiverAmount ;
    paymtr.ResultCarry = 1 ;

    paymtr.Kind_Oper  = " 1" ;
    paymtr.Shifr_Oper = "09" ;

    /* Реальные счета и коды банков */
    if( FlagTrans == 1 )
      if( DKFlag == KREDIT )
        paymtr.AccountPayer      = PaymentObj.FuturePayerAccount;
        paymtr.AccountReceiver   = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_ACCOUNT ) ;
      else
        paymtr.AccountPayer      = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_ACCOUNT ) ;
        paymtr.AccountReceiver   = PaymentObj.FutureReceiverAccount;
      end ;
    else
      if( DKFlag == KREDIT )
        paymtr.AccountPayer        = PaymentObj.FuturePayerAccount;
        paymtr.AccountReceiver     = PaymentObj.PayerAccount ;
      else
        paymtr.AccountPayer        = PaymentObj.ReceiverAccount ;
        paymtr.AccountReceiver     = PaymentObj.FutureReceiverAccount;
      end ;
    end ;

    paymtr.Date_Carry = {curdate};
    paymtr.Department = PaymentObj.Department ;

    paymtr.Ground = substr( "Возврат из картотеки у корреспондента документа/"+wlpmrmprop.Ground, 1, 210 ) ;
    paymtr.Numb_Document = PaymentObj.Number ;
  else
    msgBox( "Валютные платежи не ставятся и не возвращаются из картотеки у кор-та" );
    return 1;
  end ;

  if ( not IsPlacedInUnclosed(PaymentObj.PaymentID) )
     /* Проводка на счет картотеки */
     if( not paymtr.Carry() )
       MsgBox("Ошибка при актуализации платежа");
       return 1;
     end;
  else
     if( DKFlag == KREDIT )
        if ( not ChangePaymentObjDocuments( PaymentObj.PaymentID, 1, paymtr.AccountPayer, paymtr.AccountReceiver ) )
           return 1 ;
        end;
     else
        if ( not ChangePaymentObjDocuments( PaymentObj.PaymentID, 0, paymtr.AccountReceiver, paymtr.AccountPayer ) )
           return 1 ;
        end;
     end;
  end;

  PaymentObj.PaymStatus = PM_FINISHED;

  PaymentObj.PropStatus = PM_PROP_CLOSED;

  PaymentObj.CardFileDateOut = {curdate};

  return 0 ;
end ;