/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация объекта по сообщениям SWIFT MT544-546                              */
/*                                                                          */
/*  Имя файла: swgd544.mac                                                  */
/*  Создан:    02.02.05                                      Бабин А.П.     */
/****************************************************************************/

import DPInter, CTInter, "swgendoc.mac", "cb_sql.mac", oralib, likepy;

var msginf : RsbDepoInfoMessage; 
var msgLink, msgParty;
var CurrentBlock = "";
var Blocks = Stack;
var ReceiverReferenceID = 0;
var FormType = "";

macro NewLink()
    if ( (valtype(msgLink)==V_UNDEF) OR
         ((msgLink.ReceiverReference!=date(0,0,0)) and
         (msgLink.SenderReference!=date(0,0,0))) )
       msgLink = RsbDepoLinkedMessage;
    end;
    return true;
end;

macro AddLink()
    if ( (msgLink.ReceiverReference!=date(0,0,0)) and
         (msgLink.SenderReference!=date(0,0,0)) )
       return msginf.AddLinkedMessage(msgLink);
    end;
    return true;
end;

macro NewParty100()
    msgParty = RsbDepoMessageParty;
    msgParty.PartyType = DPMSGPAT_SECURITIES;
    return true;
end;

macro NewParty120()
    msgParty = RsbDepoMessageParty;
    msgParty.PartyType = DPMSGPAT_OTHER;
    return true;
end;

macro AddParty()
    return msginf.AddMessageParty(msgParty);
end;

macro Fill16R( newBlock )
   if ( CurrentBlock!="" )
      Blocks.Add(CurrentBlock);
   end;
   CurrentBlock = newBlock;
   return true;
end;

macro Fill16S( nameBlock )
   CurrentBlock = Blocks.Get();
   return true;
end;

macro Fill20C( Qualifer, Reference )
   if ( CurrentBlock==GENL_Block )
      msginf.SenderReference = Reference;
   elif ( CurrentBlock==LINK_Block )
      if ( Qualifer=="RELA" )
         msgLink.ReceiverReference = Reference;
      elif ( Qualifer=="PREV" )
         msgLink.SenderReference = Reference;
      end;
   elif ( CurrentBlock==SETPRTY_Block )
      if ( Qualifer=="PROC" )
         msgParty.ProcessingReference = Reference;
      end;
   end;
   return true;
end;

macro Fill23G( Code, value )
   var error;
   msginf.messageFunction = GetllValueElement(OBJTYPE_MSGFUN, Code, error);
   if ( error )
      return false;
   end;
   return true;
end;

macro Fill98AC( Qualifer, RDate, RTime )
   if ( CurrentBlock==GENL_Block )
      msginf.PreparationDate = RDate;
      msginf.PreparationTime = RTime;
   elif ( CurrentBlock==TRADDET_Block )
      if ( Qualifer=="TRAD" )
         msginf.TradeDate = RDate;
         msginf.TradeTime = RTime;
      elif ( Qualifer=="SETT" )
         msginf.SettlementDate = RDate;
         msginf.SettlementTime = RTime;
      elif ( Qualifer=="ESET" )
         msginf.EffectiveSettlementDate = RDate;
         msginf.EffectiveSettlementTime = RTime;
      end;
   elif ( CurrentBlock==SETPRTY_Block )
      if ( Qualifer=="PROC" )
         msgParty.ProcessingDate = RDate;
         msgParty.ProcessingTime = RTime;
      end;
   end;
   return true;
end;

macro Fill98B( Qualifer, Scheme, DateCode )
   /*АХЗ*/
   return true;
end;

macro Fill22FH( Qualifier, Scheme, Indicator )
   var error;
   if ( CurrentBlock==SETDET_Block )
       if ( Qualifier=="SETR" )          
          msginf.SettlementTransactionType = GetllValueElement(OBJTYPE_MSGTRANS, Indicator, error);
          if ( error )
             return false;
          end;          
       end;
   end;
   return true;
end;

macro Fill13A( Qualifier, Number )
   return true;
end;

macro Fill94B( Qualifier, Scheme, PlaceCode, Narrative )
   var error, PartyID;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( CurrentBlock==TRADDET_Block )
      msginf.TradePlaceName = "Внебиржевой рынок";
      msginf.TradePlaceType = GetllValueElement(OBJTYPE_MSGTRDPL, PlaceCode, error);
      if ( error )
         return false;
      end;
      if ( msginf.TradePlaceType==OBJTYPE_MSGTRDPL_EXCH )
         msginf.TradePlaceCodeKind=PTCK_MIC;
         msginf.TradePlaceCode=Narrative;
         msginf.TradePlaceName = "Не найден";
         PartyID = ПолучитьКодСубъекта( msginf.TradePlaceCode, msginf.TradePlaceCodeKind, error );      
         if ( not error )

            msginf.TradePlaceID = PartyID;
            select = "select t_Name from dparty_dbt where t_PartyID=:PartyID";
            params = makeArray( SQLParam("PartyID", PartyID));
            rs = execSQLselect( select, params, FALSE );
            if ( rs.MoveNext() )
               msginf.TradePlaceName = rs.value(0);
            end;
         end;
      end;
   end;
   return true;
end;

macro Fill94F( Qualifier, PlaceCode, BIC )
   return true;
end;

macro DeleteBreak( str )
  var ind = index(str,SYMB_ENDL);
  while( ind )
     str = substr(str,1,ind-1)+substr(str,ind+1);
     ind = index(str,SYMB_ENDL);
  end;
  SetParm(0,str);
end;

macro ПолучитьТипКода(shrtname: string)

   var rs:object = execSQLselect("SELECT t_codekind FROM dobjkcode_dbt WHERE t_shortname= \'"+shrtname +"\' and t_objecttype ="+OBJTYPE_FININSTR + ";");
   if( rs AND rs.moveNext() )
      return rs.value(0);
   else
     StrChangeCharset( shrtname, RusXSet, shrtname, RusSet );
     rs = execSQLselect("SELECT t_codekind FROM dobjkcode_dbt WHERE UPPER(t_shortname) = \'"+shrtname +"\' and t_objecttype = " + OBJTYPE_FININSTR + ";");
     if( rs AND rs.moveNext() )
       return rs.value(0);
     end;
   end;
  
   return FICK_USERCODE;
end;

macro Fill35B( ISIN, Description )   
   var error, CodeKindName;
   var rs:object;
   if( ISIN != "" )
      msginf.SecurityID = ПолучитьКодФинИн( trim(ISIN), error, FICK_ISIN );
   else
     if ( index(Description,"CORP/") )
        Description = substr( Description, index(Description,"CORP/")+5 );

        if ( index(Description,"/") )
           CodeKindName = substr( Description, 1, index(Description,"/")-1 );
           Description = substr( Description, index(Description,"/")+1 );
        else
        return false;
      end;

        
        if ( index(Description,"\n") )
           msginf.SecurityCode = substr( Description, 1, index(Description,"\n")-1 );
   else
           msginf.SecurityCode = Description;
        end;

       msginf.SecurityCodeKind = ПолучитьТипКода(CodeKindName);

       msginf.SecurityID = ПолучитьКодФинИн( msginf.SecurityCode, error, msginf.SecurityCodeKind );
     end;
   end;

   if (error)
      return false;
   end;

   return true;
end;

macro Fill36B( Qualifier, Code, Sum )
   if ( CurrentBlock==FIAC_Block )
      if ( Qualifier=="ESTT" )
          msginf.SettledQuantity = moneyl(Sum);
      end;
   end;
   return true;
end;

macro Fill97A( Qualifier, Account )
   var error;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( CurrentBlock==FIAC_Block )
      if ( index(Account,"/KRZD/") )
         msginf.Account = substr( Account, 1, index(Account,"/KRZD/")-1 );
         msginf.Partition = substr( Account, index(Account,"/KRZD/")+strlen("/KRZD/") );
      else
         msginf.Account = Account;
      end;      
      
      /*Необходимо получить допустимый вид регистрационного документа*/
      /*поиск производится: по PartyID, субъекту экономики (T_OBJECTTYPE = 3 = OBJTYPE_PARTY),*/
      /*идентификатору вида регистрирующего органа (T_REGPARTYKIND = 50) - "Регистрирующий субъект для анкет субъектов"*/
      /*и идентификатору вида регистрационного документа  (T_REGDOCKIND = 31) - "Код субъекта в кодировке контрагента"*/
      select = "select T_CODEKIND from dobjalreg_dbt where t_PartyID=:SeId and T_OBJECTTYPE = 3 and T_REGPARTYKIND = 50 and T_REGDOCKIND = 31";
      params = makeArray( SQLParam("SeId", msginf.SenderID));
      rs = execSQLselect( select, params, FALSE );  
      if ( rs.MoveNext() )
         msginf.DepositorCodeKind = rs.value(0);
      else
      msginf.DepositorCodeKind = 1;
      end;

      msginf.DepositorID = {OurBank};
      /* Заполняем по-простому (письмо Кобякова за 09.02.2005) */
      msginf.DepositorCode = ПолучитьКодСубъекта( {OurBank}, msginf.DepositorCodeKind, error );
      select = "select t_Name from dparty_dbt where t_PartyID=:SeId";
      params = makeArray( SQLParam("SeId", {OurBank}));
      rs = execSQLselect( select, params, FALSE );  
      if ( rs.MoveNext() )
         msginf.DepositorName = rs.value(0);
      end;

   elif ( CurrentBlock==SETPRTY_Block )
      if ( index(Account,"/KRZD/") )
         msgParty.Account = substr( Account, 1, index(Account,"/KRZD/")-1 );
         msgParty.Partition = substr( Account, index(Account,"/KRZD/")+strlen("/KRZD/") );
      else
         msgParty.Account = Account;
      end;
   end;
   return true;
end;

macro Fill97B( Qualifier, Scheme, CodeType, Account )
   return Fill97A(Qualifier,Account);
end;

macro Fill95C(Qualifier, CountryCode)
   return true;
end;

macro FillPartyKind( Qualifier )
   var error;
   msgParty.PartyKind = GetllValueElement(OBJTYPE_MSGPARTYTP, Qualifier, error);
   if ( error )
      return false;
   end;

   if ( CurrentBlock==SETPRTY_Block )
      if ( (msgParty.PartyKind==OBJTYPE_MSGPATYTP_EXCH) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MEOR) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MERE) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_TRRE) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_INVE) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_ACCW) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_BENM) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_PAYE) ) 
         return false;
      end;
   elif( CurrentBlock == CSHPRTY_Block )
      if(  (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_ACCW) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_BENM) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_PAYE) )
         return false;
      end;
   else
      if ( (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_EXCH) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_MEOR) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_MERE) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_TRRE) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_INVE) )
         return false;
      end;
   end;
   return true;
end;

macro Fill95P(Qualifier, BIC)
   var error;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( (CurrentBlock==SETPRTY_Block) OR (CurrentBlock==OTHRPRTY_Block) )
      if ( not FillPartyKind(Qualifier) )
         return false;
      end;
      if (substr(BIC,9,3) == "XXX")
         BIC = substr(BIC,1,8);
      end;
      msgParty.PartyCode = BIC;
      msgParty.PartyCodeKind = PTCK_SWIFT;
      msgParty.PartyID = ПолучитьКодСубъекта( msgParty.PartyCode, msgParty.PartyCodeKind, error );
      if ( error )
         return false;
      end;
      select = "select t_Name from dparty_dbt where t_PartyID=:PartyID";
      params = makeArray( SQLParam("PartyID",msgParty.PartyID ));
      rs = execSQLselect( select, params, FALSE );
      if ( rs.MoveNext() )
         msgParty.PartyName = rs.value(0);
      end;
   end;
   return true;
end;

macro Fill95R(Qualifier, Scheme, PropCode)
   return true;
end;

macro Fill95Q(Qualifier, Name)
   var error;
  var rs:object;
  var select:string;
  var params:TArray;
   var pid;

   if ( (CurrentBlock==SETPRTY_Block) OR (CurrentBlock==OTHRPRTY_Block) )
      if ( not FillPartyKind(Qualifier) )
         return false;
      end;
      msgParty.PartyID = -1;      
      msgParty.PartyCodeKind = PTCK_CONTR;
      DeleteBreak(Name);
      if ( Index(Name, "XX/CORP/") )
         Name = substr( Name, Index(Name, "XX/CORP/")+strlen("XX/CORP/") );
         if ( not Index(Name, "/") )
            return false;
         end;
         
         msgParty.DepoPartyCodeKind = ПолучитьТипКода( substr( Name, 1, index(Name,"/")-1 ) );
         msgParty.DepoPartyCode = substr( Name, Index(Name, "/")+1 );
         
         pid = ПолучитьКодСубъекта( msgParty.DepoPartyCode, msgParty.DepoPartyCodeKind, error );
         if ((pid != "") and (not error) )
            msgParty.PartyID = pid;
            msgParty.PartyCodeKind = PTCK_SWIFT;
            msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
            
            select = "select t_Name from dparty_dbt where t_PartyID=:tpid";
            params = makeArray( SQLParam("tpid", pid));
            rs = execSQLselect( select, params, FALSE );
            if ( rs and rs.MoveNext() )
               msgParty.PartyName=rs.value(0);
            else
               msgParty.PartyName = "Не найден";
            end;
            
            if ( error )
               msgParty.PartyCodeKind = PTCK_CONTR;
               msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
                             
               if ( error )
                  msgParty.PartyCode = "";
                  msgParty.PartyName = "Не найден";
               end;
            end;
         end;
      else
         msgParty.PartyName = Name;
         select = "select t_PartyID from dparty_dbt where t_Name=:Name or t_ShortName=:Name";
         params = makeArray( SQLParam("Name",GetSQLString(Name)));
         rs = execSQLselect( select, params, FALSE );
         if ( rs.MoveNext() )
            msgParty.PartyID=rs.value(0);
            msgParty.PartyCodeKind = PTCK_SWIFT;
            msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
            if ( error )
               msgParty.PartyCodeKind = PTCK_CONTR;
               msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
               if ( error )
                  msgParty.PartyCode = "";
               end;
            end;
         end;
      end;      
   end;
   return true;
end;

macro Fill95S(Qualifier, Scheme, TypeID, CountryCode, AlternateID)
   return true;
end;

macro Fill70a( Qualifier, Narrative )
   var count, msgDoc, docMstr, code, strval, continue0;
   array dest;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( (CurrentBlock==FIA_Block) and (Qualifier=="FIAN") )      
      if ( index(Narrative,"/PLDG/Y") )
         msginf.HasCharge = true;
      end;
      return true;
   elif ( (CurrentBlock==TRADDET_Block) and (Qualifier=="SPRO") )
      DeleteBreak(Narrative);
      if ( index(Narrative,"/MACH/Y") )
         msginf.NeedCounterDraft = true;
      end;   
   elif ( CurrentBlock==SETPRTY_Block )
      if ( (msgParty.PartyKind==OBJTYPE_MSGPATYTP_PSET) and (Qualifier=="PART") )
          DeleteBreak(Narrative);
          if ( index(Narrative,"/PROC/Y" ) )
             msgParty.HasInformation = true;
          end;
      end;
   elif ( CurrentBlock==OTHRPRTY_Block )
      if ( (Qualifier=="PART") and (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MERE) )         
         DeleteBreak(Narrative);
         if ( index(Narrative,"/SEND/") )
            count = index(Narrative,"/SEND/")+strlen("/SEND/");
            if ( not ReadDateYYYYMMDD(Narrative, count, msginf.LateDeliveryDate) )
                return false;
            end;
         end;
      elif ( (Qualifier=="DECL") and (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MERE) )
         if ( index(Narrative,"/OSNV/\n") )
            РазбитьНаСтроки( Narrative, dest, 35 );
            count = 1;  /* Первую строку пропускаем */
            docMstr = "";
            while( count<=Asize( dest ) )
               if ( count<Asize( dest ) )
                  code = substr(dest(count), 1, 2);
                  if ( code!="//" )
                     code = substr(dest(count), 1, 6);
                  end;
               else
                  code = "END";
               end;

               if ( (docMstr!="") and (code!="//")  and (code!="DOCM") )
                   select = "select t_Name, t_Element from dllvalues_dbt where t_List=:OBJTYPE_KINDDOC";
                   params = makeArray( SQLParam("OBJTYPE_KINDDOC", OBJTYPE_KINDDOC));
                   rs = execSQLselect( select, params, FALSE );
                   continue0 = true;
                   while( continue0 and rs.MoveNext() )
                      StrChangeRusXSet( rs.value(0), strval );
                      if ( strval==docMstr )
                         msgdoc.DocumentKind = rs.value(1);
                         continue0 = false;
                      end;
                   end;                   
                   docMstr = "";
               end;

               if ( (code=="/DOCN/") OR (code=="END") )
                   if ( count>1 )
                      msginf.AddMessageDocument(msgDoc);
                   end;
                   if ( code=="/DOCN/" )
                      msgDoc = RsbDepoMessageDocument;
                      msgDoc.DocumentNumber = substr(dest(count), 7);
                   end;
               elif ( code=="/DOCD/" )
                   if ( not ReadDateYYYYMMDD(dest(count), 7, msgdoc.SignedDate) )
                     return false;
                   end;
               elif ( code=="/DOCM/" )
                   docMstr = substr(dest(count), 7);
               elif ( code=="//" )
                   docMstr = docMstr + substr(dest(count), 2);
               end;
               count = count + 1;
            end;
         end;
      end;
   end;
   return true;
end;

macro Fill19A(Qualifier, Sign, Cur, Sum)
   if ( CurrentBlock==AMT_Block )
      if ( Qualifier=="ESTT" )
         if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
           std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
           return FALSE;
         end;
         msginf.SettlementAmountCurrency = wlfininstr.FIID;
         msginf.SettlementAmount = moneyl(Sum);
      elif ( Qualifier=="DEAL" )
         if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
           std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
           return FALSE;
         end;
         msginf.DealAmountCurrency = wlfininstr.FIID;
         msginf.DealAmount = moneyl(Sum);
      end;
   end;
   return true;
end;

/* Заполнение списка полей формы  MT544-546*/
var FldGENLR    = ТПолеФормы( "16R:GENL",     FIELD_MANDATORY, "Read16",   "Fill16R",  ""                     ),
    Fld20C      = ТПолеФормы( "20C",          FIELD_MANDATORY, "Read20C",  "Fill20C",  ""                     ),
    Fld23G      = ТПолеФормы( "23G",          FIELD_MANDATORY, "ReadCode", "Fill23G",  ""                     ),
    Fld98A      = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98C      = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),        
    Fld22F      = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",           THISFLD  ),
    Fld22H      = ТПолеФормы( "22H",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",           Fld22F   ),
    FldLINKR    = ТПолеФормы( "16R:LINK",     FIELD_MANDATORY, "Read16",   "Fill16R",  "NewLink"              ),
    Fld22F2     = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", ""                     ),
    Fld13A      = ТПолеФормы( "13A",          FIELD_OPTIONAL,  "Read13A",  "Fill13A",  ""                     ),
    Fld20C2     = ТПолеФормы( "20C",          FIELD_MANDATORY, "Read20C",  "Fill20C",  ""                     ),
    FldLINKS    = ТПолеФормы( "16S:LINK",     FIELD_MANDATORY, "Read16",   "Fill16S",  "AddLink",    FldLINKR ),
    FldGENLS    = ТПолеФормы( "16S:GENL",     FIELD_MANDATORY, "Read16",   "Fill16S",  ""                     ),
    FldTRADDETR = ТПолеФормы( "16R:TRADDET",  FIELD_MANDATORY, "Read16",   "Fill16R",  ""                     ),
    Fld94B      = ТПолеФормы( "94B",          FIELD_OPTIONAL,  "Read94B",  "Fill94B",  ""                     ),
    Fld98A2     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", "",           THISFLD  ),
    Fld98B2     = ТПолеФормы( "98B",          FIELD_OPTIONAL,  "Read98B",  "Fill98B",  "",           Fld98A2  ),
    Fld98C2     = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", "",           Fld98A2  ),
    Fld90A      = ТПолеФормы( "90A",          FIELD_OPTIONAL,  "Read90A",  "Fill90A",  ""                     ),
    Fld90B      = ТПолеФормы( "90B",          FIELD_OPTIONAL,  "Read90B",  "Fill90B",  ""                     ),
    Fld99A      = ТПолеФормы( "99A",          FIELD_OPTIONAL,  "Read99A",  "Fill99A",  ""                     ),
    Fld35B      = ТПолеФормы( "35B",          FIELD_MANDATORY, "Read35B",  "Fill35B",  ""                     ),
    FldFIAR     = ТПолеФормы( "16R:FIA",      FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld22F3     = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",            THISFLD ),
    Fld12A      = ТПолеФормы( "12A",          FIELD_OPTIONAL,  "Read12A",  "Fill12A",  "",            THISFLD ),
    Fld12B      = ТПолеФормы( "12B",          FIELD_OPTIONAL,  "Read12B",  "Fill12B",  "",            Fld12A  ),
    Fld12C      = ТПолеФормы( "12C",          FIELD_OPTIONAL,  "Read12C",  "Fill12C",  "",            Fld12A  ),
    Fld11A      = ТПолеФормы( "11A",          FIELD_OPTIONAL,  "Read11A",  "Fill11A",  ""                     ),
    Fld98A3     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", "",            THISFLD ),
    Fld92A      = ТПолеФормы( "92A",          FIELD_OPTIONAL,  "Read92A",  "Fill92A",  "",            THISFLD ),
    Fld13A2     = ТПолеФормы( "13A",          FIELD_OPTIONAL,  "Read13A",  "Fill13A",  "",            THISFLD ),
    Fld13B      = ТПолеФормы( "13B",          FIELD_OPTIONAL,  "Read13B",  "Fill13B",  "",            Fld13A2 ),
    Fld13K      = ТПолеФормы( "13K",          FIELD_OPTIONAL,  "Read13K",  "Fill13K",  "",            Fld13A2 ),
    Fld17B      = ТПолеФормы( "17B",          FIELD_OPTIONAL,  "Read17B",  "Fill17B",  "",            THISFLD ),
    Fld90A2     = ТПолеФормы( "90A",          FIELD_OPTIONAL,  "Read90A",  "Fill90A",  "",            THISFLD ),
    Fld90B2     = ТПолеФормы( "90B",          FIELD_OPTIONAL,  "Read90B",  "Fill90B",  "",            Fld90A2 ),
    Fld36B      = ТПолеФормы( "36B",          FIELD_OPTIONAL,  "Read36B",  "Fill36B",  "",            THISFLD ),
    Fld70E      = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  ""                     ),
    FldFIAS     = ТПолеФормы( "16S:FIA",      FIELD_OPTIONAL,  "Read16",   "Fill16S",  ""                     ),
    Fld22F4     = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",            THISFLD ),
    Fld70E2     = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  "",            THISFLD ),
    FldTRADDETS = ТПолеФормы( "16S:TRADDET",  FIELD_MANDATORY, "Read16",   "Fill16S",  ""                     ),
    FldFIACR    = ТПолеФормы( "16R:FIAC",     FIELD_MANDATORY, "Read16",   "Fill16R",  ""                     ),
    Fld36B2     = ТПолеФормы( "36B",          FIELD_MANDATORY, "Read36B",  "Fill36B",  "",            THISFLD ),
    Fld19A      = ТПолеФормы( "19A",          FIELD_OPTIONAL,  "Read19A",  "Fill19A",  "",            THISFLD ),
    Fld70D      = ТПолеФормы( "70D",          FIELD_OPTIONAL,  "Read70D",  "Fill70a",  ""                     ),
    Fld13B2     = ТПолеФормы( "13B",          FIELD_OPTIONAL,  "Read13B",  "Fill13B",  "",            THISFLD ),
    Fld97A      = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  "",            THISFLD ),
    Fld97B      = ТПолеФормы( "97B",          FIELD_OPTIONAL,  "Read97B",  "Fill97B",  "",            Fld97A  ),
    Fld94B2     = ТПолеФормы( "94B",          FIELD_OPTIONAL,  "Read94B",  "Fill94B",  ""                     ),
    Fld94F2     = ТПолеФормы( "94F",          FIELD_OPTIONAL,  "Read94F",  "Fill94F",  ""                     ),
    FldFIACS    = ТПолеФормы( "16S:FIAC",     FIELD_MANDATORY, "Read16",   "Fill16S",  "",            FldFIACR),
    FldREPOR    = ТПолеФормы( "16R:REPO",     FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld98A4     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98B4     = ТПолеФормы( "98B",          FIELD_OPTIONAL,  "Read98B",  "Fill98B",  ""                     ),
    Fld98C4     = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),
    Fld22F5     = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",            THISFLD ),
    Fld20C3     = ТПолеФормы( "20C",          FIELD_OPTIONAL,  "Read20C",  "Fill20C",  "",            THISFLD ),
    Fld92A2     = ТПолеФормы( "92A",          FIELD_OPTIONAL,  "Read92A",  "Fill92A",  "",            THISFLD ),
    Fld92C2     = ТПолеФормы( "92C",          FIELD_OPTIONAL,  "Read92C",  "Fill92C",  "",            Fld92A2 ),
    Fld99B2     = ТПолеФормы( "99B",          FIELD_OPTIONAL,  "Read99B",  "Fill99B",  "",            THISFLD ),
    Fld19A2     = ТПолеФормы( "19A",          FIELD_OPTIONAL,  "Read19A",  "Fill19A",  "",            THISFLD ),
    Fld70C      = ТПолеФормы( "70C",          FIELD_OPTIONAL,  "Read70C",  "Fill70a",  ""                     ),
    FldREPOS    = ТПолеФормы( "16S:REPO",     FIELD_OPTIONAL,  "Read16",   "Fill16S",  ""                     ),
    FldSETDETR  = ТПолеФормы( "16R:SETDET",   FIELD_MANDATORY, "Read16",   "Fill16R",  ""                     ),
    Fld22F6     = ТПолеФормы( "22F",          FIELD_MANDATORY, "Read22FH", "Fill22FH", "",            THISFLD ),
    FldSETPRTYR = ТПолеФормы( "16R:SETPRTY",  FIELD_MANDATORY, "Read16",   "Fill16R",  "NewParty100"          ),
    Fld95C      = ТПолеФормы( "95C",          FIELD_OPTIONAL,  "Read95C",  "Fill95C",  "",            THISFLD ),    
    Fld95S      = ТПолеФормы( "95S",          FIELD_OPTIONAL,  "Read95S",  "Fill95S",  "",            Fld95C  ),    
    Fld95P      = ТПолеФормы( "95P",          FIELD_OPTIONAL,  "Read95P",  "Fill95P",  "",            Fld95C  ),    
    Fld95R      = ТПолеФормы( "95R",          FIELD_OPTIONAL,  "Read95R",  "Fill95R",  "",            Fld95C  ),    
    Fld95Q      = ТПолеФормы( "95Q",          FIELD_OPTIONAL,  "Read95Q",  "Fill95Q",  "",            Fld95C  ),
    Fld97A2     = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  ""                     ),
    Fld97B2     = ТПолеФормы( "97B",          FIELD_OPTIONAL,  "Read97B",  "Fill97B",  ""                     ),
    Fld98A5     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98C5     = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),    
    Fld20C4     = ТПолеФормы( "20C",          FIELD_OPTIONAL,  "Read20C",  "Fill20C",  ""                     ),
    Fld70E4     = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  "",            THISFLD ),
    Fld70C4     = ТПолеФормы( "70C",          FIELD_OPTIONAL,  "Read70C",  "Fill70a",  "",            Fld70E4 ),
    Fld70D4     = ТПолеФормы( "70D",          FIELD_OPTIONAL,  "Read70D",  "Fill70a",  "",            Fld70E4 ),
    FldSETPRTYS = ТПолеФормы( "16S:SETPRTY",  FIELD_MANDATORY, "Read16",   "Fill16S",  "AddParty",    FldSETPRTYR),
    FldCSHPRTYR = ТПолеФормы( "16R:CSHPRTY",  FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld95S2     = ТПолеФормы( "95S",          FIELD_OPTIONAL,  "Read95S",  "Fill95S",  "",            THISFLD ),    
    Fld95P2     = ТПолеФормы( "95P",          FIELD_OPTIONAL,  "Read95P",  "Fill95P",  "",            Fld95S2 ),    
    Fld95R2     = ТПолеФормы( "95R",          FIELD_OPTIONAL,  "Read95R",  "Fill95R",  "",            Fld95S2 ),    
    Fld95Q2     = ТПолеФормы( "95Q",          FIELD_OPTIONAL,  "Read95Q",  "Fill95Q",  "",            Fld95S2 ),
    Fld97A3     = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  "",            THISFLD ),
    Fld70E5     = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  "",            THISFLD ),
    Fld70C5     = ТПолеФормы( "70C",          FIELD_OPTIONAL,  "Read70C",  "Fill70a",  "",            Fld70E5 ),
    Fld70D5     = ТПолеФормы( "70D",          FIELD_OPTIONAL,  "Read70D",  "Fill70a",  "",            Fld70E5 ),
    FldCSHPRTYS = ТПолеФормы( "16S:CSHPRTY",  FIELD_OPTIONAL,  "Read16",   "Fill16S",  "",            FldCSHPRTYR),
    FldAMTR     = ТПолеФормы( "16R:AMT",      FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld17B2     = ТПолеФормы( "17B",          FIELD_OPTIONAL,  "Read17B",  "Fill17B",  "",            THISFLD ),
    Fld19A3     = ТПолеФормы( "19A",          FIELD_OPTIONAL,  "Read19A",  "Fill19A",  "",            THISFLD ),
    Fld98A6     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98C6     = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),        
    Fld92B      = ТПолеФормы( "92B",          FIELD_OPTIONAL,  "Read92B",  "Fill92B",  ""                     ),
    FldAMTS     = ТПолеФормы( "16S:AMT",      FIELD_OPTIONAL,  "Read16",   "Fill16S",  "",            FldAMTR ),    
    FldSETDETS  = ТПолеФормы( "16S:SETDET",   FIELD_MANDATORY, "Read16",   "Fill16S",  ""                     ),
    FldOTHRPRTYR= ТПолеФормы( "16R:OTHRPRTY", FIELD_OPTIONAL,  "Read16",   "Fill16R",  "NewParty120"          ),
    Fld95C3     = ТПолеФормы( "95C",          FIELD_OPTIONAL,  "Read95C",  "Fill95C",  "",            THISFLD ),    
    Fld95S3     = ТПолеФормы( "95S",          FIELD_OPTIONAL,  "Read95S",  "Fill95S",  "",            Fld95C3 ),    
    Fld95P3     = ТПолеФормы( "95P",          FIELD_OPTIONAL,  "Read95P",  "Fill95P",  "",            Fld95C3 ),    
    Fld95R3     = ТПолеФормы( "95R",          FIELD_OPTIONAL,  "Read95R",  "Fill95R",  "",            Fld95C3 ),    
    Fld95Q3     = ТПолеФормы( "95Q",          FIELD_OPTIONAL,  "Read95Q",  "Fill95Q",  "",            Fld95C3 ),
    Fld97A4     = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  "",            THISFLD ),
    Fld70E6     = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  "",            THISFLD ),
    Fld70C6     = ТПолеФормы( "70C",          FIELD_OPTIONAL,  "Read70C",  "Fill70a",  "",            Fld70E6 ),
    Fld70D6     = ТПолеФормы( "70D",          FIELD_OPTIONAL,  "Read70D",  "Fill70a",  "",            Fld70E6 ),
    Fld20C5     = ТПолеФормы( "20C",          FIELD_OPTIONAL,  "Read20C",  "Fill20C",  ""                     ),
    FldOTHRPRTYS= ТПолеФормы( "16S:OTHRPRTY", FIELD_OPTIONAL,  "Read16",   "Fill16S",  "AddParty",    FldOTHRPRTYR );

macro ConstructMT544( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode, repeatFlag = 0;
  var rs:object;
  var select:string;
  var params:TArray;

  msginf.MessageStandart  = 1;   /* SWIFT    */
  msginf.MessageDirection = 1;   /* входящее */
  msginf.MessageType      = FormType; /* MT544    */

  msginf.SenderCodeKind = PTCK_SWIFT;
  msginf.SenderID = RespID;
  msginf.SenderCode = ПолучитьКодСубъекта( msginf.SenderID, msginf.SenderCodeKind, error );
  if ( error )
     std.msg( "Не найден SWIFT-код отправителя сообщения" );
     return false;
  end;
  select = "select t_Name from dparty_dbt where t_PartyID=:SenderID";
  params = makeArray( SQLParam("SenderID", msginf.SenderID));
  rs = execSQLselect( select, params, FALSE );

  if ( rs.MoveNext() )
     msginf.SenderName = rs.value(0);
  end;

  msginf.ReceiverCodeKind = PTCK_SWIFT;
  msginf.ReceiverID = {OurBank};
  msginf.ReceiverCode = ПолучитьКодСубъекта( {OurBank}, msginf.ReceiverCodeKind, error );
  if ( error )
     std.msg( "Не найден SWIFT-код получателя сообщения" );
     return false;
  end;
  select = "select t_Name from dparty_dbt where t_PartyID=:SeId";
  params = makeArray( SQLParam("SeId", {OurBank}));
  rs = execSQLselect( select, params, FALSE ); 
  if ( rs.MoveNext() )
     msginf.ReceiverName = rs.value(0);
  end;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )    
    fld = ПоляФормы.Value(count);        
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if ( (field_name==StartOfBlock) OR (field_name==EndOfBlock) )
        field_name = field_name + ":" + field_value;
      end;

      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно and (not RepeatFlag) )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          wasRead = 0;
        end;
      else
        repeatFlag = 0;
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            wasRead = 1;
            if ( fld.repeatWith>=0 )
                repeatFlag = 1;
                count = fld.repeatWith-1;
             end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;      
  end;

  if( result == FALSE )
    return result;
  end;
    
  /*if ( GetReferenceIDByType(OBJTYPE_DEPOINFMSG, REFOBJ_OPERATIONDEPO, ReceiverReferenceID) )
     std.msg( "Не найден описатель референса" );
     return false;
  end;

  if ( GenerateReference(ReceiverReferenceID,msginf.ReceiverReference) )
     std.msg( "Ошибка при генерации референса по описателю " + string(ReceiverReferenceID) );
     return false;
  end;*/
  msginf.State            = DPMSGINF_STATE_PREP; /*DPMSGINF_STATE_REG; // Зарегистрировано */

  return TRUE;
end; /* ConstructMT544 */

private macro GenDocEx( addrMes )
  SetBuff( wlmes, addrMes );  

  PrintLog(2,"Генерация уч. объекта по МТ" + FormType);

  if( not ConstructMT544( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if ( not msginf.Update() )
     /*RestoreReference(ReceiverReferenceID, msginf.ReceiverReference);*/
     std.msg("Ошибка при сохранении информации");
     return FALSE;
  end;

  if ( not ПривязатьОбъектДепоКСообщению(msginf.MessageID) )
     /*RestoreReference(ReceiverReferenceID, msginf.ReceiverReference);*/
     std.msg("Ошибка при создании связи с сообщением");
     return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenDoc( addrMes )
  var stat = false;
  msginf = RsbDepoInfoMessage(0);
    stat = GenDocEx( addrMes );
  msginf = null;
  return stat;
end;

macro GenDoc544( addrMes )

  FormType = 544;  

  return GenDoc(addrMes);
end;

macro GenDoc546( addrMes )

  FormType = 546;  

  return GenDoc(addrMes);
end;
