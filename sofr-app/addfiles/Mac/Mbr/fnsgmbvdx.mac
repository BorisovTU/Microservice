/*
$Name:               fnsgmbvdx.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения BVD XML
*/

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "mnsbxtool.mac";

private macro WriteField
( FieldName : string,
  FieldValue : string,
  IsWrongValue : bool
)
  if(IsWrongValue)
    FieldValue = "";
  end;

  if(FieldValue)
    ЗаписатьПолеЛог(FieldName, FieldValue);
  end;
end;

private macro ФормированиеДанныхКонтрагента(rs_wlconf: RsdRecordset)
  
  // Файл \ Счет \ Операции \ РеквПлат
  StartBlockLogXML( "РеквПлат" );

  /*НаимПП */
  ЗаписатьПолеЛог("НаимПП", substr( rs_wlconf.value("t_ClientName"), 1, 160 ));
  
  /*ИННПП*/
  var FieldValue : string = rs_wlconf.value("t_ClientINN"), len : integer = strlen(FieldValue);
  WriteField( "ИННПП", FieldValue, (len < 5) or (len > 12) );
  
  /*КПППП*/
  FieldValue = rs_wlconf.value("t_ClientKPP"); len = strlen(FieldValue);;
  WriteField("КПППП", FieldValue, len != 9 );

  /*НомСчПП*/
  WriteField("НомСчПП", rs_wlconf.value("t_ClientAccount"));

  FinishBlockLogXML( "РеквПлат" );

end;

private macro GetClientType( ClientID )
  file PARTY("party.dbt") key 0;
  PARTY.PartyID = ClientID;
  GetEQ(PARTY);
  return PARTY.LegalForm;
end;

private macro HH_MM_SS( tm ) : string
   var ss, mm, hh, str;
   TimeSplit(tm, hh, mm, ss );
   str = string(hh:2, ":", mm:2, ":", ss:2) ;
   str = StrSubst ( str, " ", "0" );
   return str;
end;

private macro ОбработатьСчет( recAcc:TRecHandler, PartNum, AccNum, wlregdec )

  record Fin( fininstr ); ClearRecord( Fin );
  record Acc( account  ); ClearRecord( Acc );
  record party( party);
  var tmpstr = "", FullINN = "";
    // Файл \ ВЫПБНДОПОЛ
    StartBlockLogXML("ВЫПБНДОПОЛ");
    /*ПорНом*/
    ЗаписатьПолеЛог( "ПорНом", AccNum );
    /*ПорНомДФ*/
    ЗаписатьПолеЛог( "ПорНомДФ", PartNum );
    /*НомСчВклЭС*/
    ЗаписатьПолеЛог( "НомСч", recAcc.rec.Account );

    var IsDeposit = InList( wlregdec.LnkTypeInfo, 6/*WLD_SUBKIND_FNSINFO_NS2*/, 
                                                  7/*WLD_SUBKIND_FNSINFO_OS2*/,
                                                  8/*WLD_SUBKIND_FNSINFO_VS2*/ );

    var AccDprtName : string = ""; 
    CB_GetDepartmentCodeAndName(recAcc.rec.Department, null, null, AccDprtName);

    /*Блок "Файл \ ВЫПБНДОПОЛ \ Операции"*/
    var query = string (
  "SELECT wlconf.t_DateValue,\n",
  "       wlconf.t_ShifrOper,\n",
  "       wlconf.t_DocNumber,\n",
  "       wlconf.t_DocDate,\n",
  "       wlconf.t_BankAcc  t_CorrAcc,\n",
  "       wlconf.t_BankName t_BankName,\n",
  "       wlconf.t_CodeValue t_BankCode,\n",
  "       wlconf.t_ReceiverINN  t_ClientINN,\n",
  "       wlconf.t_ReceiverKPP  t_ClientKPP,\n",  
  "       wlconf.t_Sum,\n",
  "       wlconf.t_DKFlag,\n",
  "       wlconf.t_Description,\n",  
  "       wlconf.t_ReceiverAccount t_ClientAccount,\n",
  "       wlconf.t_ReceiverName    t_ClientName \n",  
  "  FROM dwlconf_dbt wlconf\n"
  "           WHERE     wlconf.t_HeadID = ?\n",
  "                 AND wlconf.t_HeadKind = " + WLCONF_HEADKIND_WLREGDEC + "\n",
  "                 AND wlconf.t_Account = ?\n",
  "                 AND wlconf.t_FIID = ?\n",
  "                 AND wlconf.t_NumberPart = ?\n"                   
  "        ORDER BY wlconf.t_ConfID;");

    var rs_wlconf:RsdRecordset = execSQLselect(query, 
        MakeArray(SQLParam("", wlregdec.DecisionID),
          SQLParam("", recAcc.rec.Account),
          SQLParam("", recAcc.rec.FIID),
          SQLParam("", int( PartNum ))));
    var ИдБлок = 1;
    if (rs_wlconf)
      while ( rs_wlconf.moveNext() )
        // Файл \ ВЫПБНДОПОЛ \ Операции
        StartBlockLogXML("Операции");
        ЗаписатьПолеЛог( "ИдБлок", string(ИдБлок) );
        ИдБлок = ИдБлок + 1;
        ЗаписатьПолеЛог( "ДатаОпер", YYYYMMDDXML( date(rs_wlconf.value( "t_DateValue" )) ) );
        /*НазнПл*/
        ЗаписатьПолеЛог( "НазнПл", substr( StrSubst ( rs_wlconf.value( "t_Description" ), SYMB_ENDL, SYMB_BLANK), 1, 1000 ) );

        // Файл \ ВЫПБНДОПОЛ \ Операции \ РеквДок
        StartBlockLogXML("РеквДок");
        ЗаписатьПолеЛог( "ВидДок", rs_wlconf.value( "t_ShifrOper" ) );
        ЗаписатьПолеЛог( "НомДок", subStr( rs_wlconf.value( "t_DocNumber" ), 1, 20 ) );

        /*ДатаДок*/
        ЗаписатьПолеЛог( "ДатаДок", YYYYMMDDXML( date(rs_wlconf.value("t_DocDate")) ) );
        FinishBlockLogXML( "РеквДок" );


        // Файл \ ВЫПБНДОПОЛ \ Операции \ РеквБанка
        StartBlockLogXML("РеквБанка");

        /*НомКорСч*/
        ЗаписатьПолеЛог( "НомКорСч", rs_wlconf.value("t_CorrAcc") );

        /*НаимБП*/
        ЗаписатьПолеЛог( "НаимБП", substr( rs_wlconf.value("t_BankName"), 1, 160 ) );

        /*БИКБП*/
        tmpstr = rs_wlconf.value("t_BankCode");
        ЗаписатьПолеЛог( "БИКБП", tmpstr );

        FinishBlockLogXML( "РеквБанка" );


        ФормированиеДанныхКонтрагента(rs_wlconf);

        // Файл \ ВЫПБНДОПОЛ \ Операции \ СуммаОпер
        StartBlockLogXML( "СуммаОпер" );
        /*Дебет*/
        if( rs_wlconf.value( "t_DKFlag" ) == WL_DEBET )
          ЗаписатьПолеЛог( "Дебет", string( rs_wlconf.value( "t_Sum" ) ) );
        else
          ЗаписатьПолеЛог( "Дебет", "0.00" );
        end;

        /*Кредит*/
        if( rs_wlconf.value("t_DKFlag") == WL_CREDIT )
          ЗаписатьПолеЛог( "Кредит", string( rs_wlconf.value( "t_Sum" ) ) );
        else
          ЗаписатьПолеЛог( "Кредит", "0.00" );
        end;
        FinishBlockLogXML( "СуммаОпер" );

        FinishBlockLogXML("Операции");
      end;
    end;

    FinishBlockLogXML("ВЫПБНДОПОЛ");
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record wlregdec(wlregdec);
  SetBuff(wlregdec, addrRegdec);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BVD");

  var rs;
  // чтение глобализмов
  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();
  var AccountNumberFNS: integer = GetAccountNumberFNS();
  var FileNumberFNS: integer = GetFileNumberFNS();

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьФайл("ВЫПБНДОПОЛ");

  var FileNumber = "";
  var AccountNumber = "";

  if (OldMes.MesID != 0)
    if( AccountNumber = GetFldValue(OldMes.MesID, "ПорНом") )
      AccountNumberFNS = int(AccountNumber);
    end;
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    if( AccountNumber = GetFldValue(OldfnsMessageID, "ПорНом") )
      AccountNumberFNS = int(AccountNumber);
    end;
  else
    AccountNumber = StrSubst(string(AccountNumberFNS:6), " ", "0");
  end;

  if (OldMes.MesID != 0) //Перегенерация?
    if( not (FileNumber = GetFldValue(OldMes.MesID, "ПорНомДФ")) )
      FileNumber = "000001";
    end;
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    if( not (FileNumber = GetFldValue(OldfnsMessageID, "ПорНомДФ")) )
      FileNumber = "000001";
    end;
  elif (FileNumberFNS > 0)
    FileNumber = StrSubst(string(FileNumberFNS:6), " ", "0");
  else
    FileNumber = "000001";
  end;

  var acclnk = RsbWlAccLnk( wlregdec.DecisionID, OBJTYPE_FNSINFO );
  var recAcc:TRecHandler = TRecHandler( "wlacclnk.dbt" );
  if( acclnk )
    if( not acclnk.First( recAcc ) )
      if (recAcc.rec.LnkObjectID == AccountNumberFNS)
        ОбработатьСчет( recAcc, FileNumber, AccountNumber, wlregdec );
      else
        while( not acclnk.Next( recAcc ) )
          if (recAcc.rec.LnkObjectID == AccountNumberFNS)
            ОбработатьСчет( recAcc, FileNumber, AccountNumber, wlregdec );
            break;
          end;
        end;
      end;
    end;
  end;

  FinishBlockLogXML("Файл");

  /*_НомерОтправ*/
  FnsWriteSendNum(OldMes);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
