/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT100                                       */
/*                                                                          */
/*  Имя файла: swgm100.mac                                                  */
/*  Создан:  21.01.00                                        Алешин А.В.    */
/****************************************************************************/

import "swgenmes.mac";

record Party(party);

macro GenMes( addrMes, addrPm )
  var field_value, BNC_Name, field72, INN, Tag, ОстатокПоля70;
  var RsPaym;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog(2,"Генерация сообщения по МТ100");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.ValueDate ) +
                ПолучитьКодВалютыЛог( RsPaym.BaseFIID ) +
                GetSWIFTAmount( RsPaym.FutureBaseAmount );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );

  /* 50 - приказодатель */
  field_value = FillOrderingCustomerField( RsPaym );
  if ( field_value=="" )
    std.out( 2, "PaymentID: " + RsPaym.PaymentID );
    RunError( "|Не найден плательщик" );
  end;
  ЗаписатьПолеЛог( OrderingCustomerField, field_value );

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( OrderingInstitutionField+Tag, "52a", field_value );

  /* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_A;
  field_value = FillSenderCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value );

  /* 54a - корреспондент получателя */
  Tag = SWIFT_Tag_A;
  field_value = FillReceiverCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField+Tag, "54a", field_value );

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value );

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value );  

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCustomerField( RsPaym );
  if ( field_value=="" )
    std.out( 2, "PaymentID: " + RsPaym.PaymentID );
    RunError( "|Не найден получатель" );
  end;
  ЗаписатьПолеЛог( BeneficiaryCustomerField, field_value );

  /* 70  - информация для бенефициара */
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT100_STANDART, ОстатокПоля70 );
  ЗаписатьПолеЛог( DetailsOfPaymentField, field_value );

  /* 71A  - расходы */
  field_value = GetCommisCode (RsPaym.ComissCharges);;
  ЗаписатьПолеЛог( DetailsOfChargesField_A, field_value );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT100_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT100_STANDART, ОстатокПоля70 );  
  if( field_value != "" )
    ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;