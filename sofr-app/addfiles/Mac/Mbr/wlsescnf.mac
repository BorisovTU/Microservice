/*

    Отчет "Документы сеанса - платежи"
    

    Подсистема - Межбанковские расчеты
    Создан 24.10.2000
    Программист Антон А. Бурцев (AB)
*/

import wldoc, FIInter, wlsestls;

macro Line()
[+-+-------------------------+--------------------+-----------------+];
end;

macro WriteHeader()
[|X|       Референс          |       Счет         |      Сумма      |];
Line();

end;


var TotalAmount, TotalUAmount, CurFIID, CurDK, Direct;

macro InitAllParm()

   ReportObjects = MESKIND_CONF; /* Тип объектов отчета - подтверждения */
   ReportObjectsTitle = "Подтверждения сеанса";

end;

macro InitReport(SessionID);

   var stat = CommonInitReport("ПЕРЕЧЕНЬ ПОДТВЕРЖДЕНИЙ СЕАНСА", SessionID);

   if ( stat != 0 )
      return stat;
   end;

   TotalAmount = $0;
   TotalUAmount = $0;
   CurFIID = -1;
   CurDK = -1;
   Direct = wlsess.Direct;

   return 0;

end;



/* Отрисовка разделителя по валютам */
macro HandleFIIDChange(NewFIID)

   if ( CurFIID != -1 ) /* Если не первый вызов */

Line();

[Общая сумма : ##################
##############################################################################################################]
( TotalAmount:a:r , SumToStr(CurFIID, TotalAmount):l );

   end;

   if ( NewFIID != -1 ) /* Если не последний вызов */
      println();
      println(StrUpr(ПолучитьИмяФинИн(NewFIID)));
      Line();
   end;

   CurFIID = NewFIID;
   TotalAmount = $0;

end;

/* Отрисовка разделителя по Дебету/Кредиту */
macro HandleDKChange(NewDK)

   HandleFIIDChange(-1);

   if ( NewDK != -1 ) /* Если не последний вызов */

      println();
      if   ( NewDK == WL_CREDIT )
         println("                           ОБОРОТЫ ПО КРЕДИТУ");
      elif ( NewDK == WL_DEBET )
         println("                           ОБОРОТЫ ПО ДЕБЕТУ");
      end;

      Line();

      WriteHeader();

   end;

   CurDK = NewDK;

end;


/* Вывод строки отчета */
macro PrintDoc(ObjKind, ObjID)

   file conf(wlconf) key 0;

   conf.ConfID = ObjID;

   if ( not GetEQ(conf) )
      return;
   end;

   /* Верхний уровень группировки - Дебет/Кредит */
   if ( conf.DKFlag != CurDK )
      HandleDKChange(conf.DKFlag);
   end;

   /* Следующий уровень группировки - по валютам */
   if ( CurFIID != conf.FIID )
      HandleFIIDChange(conf.FIID);
   end;

[|#|#########################|####################|#################|]
(
   conf.Cancel,
   conf.Number:l,
   conf.Account,
   conf.Sum
);

   TotalAmount = TotalAmount + conf.Sum;

end;

macro DoneReport()

   HandleFIIDChange(-1);
   HandleDKChange(-1);

end;