/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Сравнение транспортных схем                                              */
/*                                                                          */
/*  Имя файла: wlcmtpsh.mac                                                 */
/*  Создан:    24.12.03                                      Панов А.Б.     */
/****************************************************************************/
import RSD, BankInter, FIInter, PTInter, oralib, likepy;

const SET_CHAR   = "X",
      UNSET_CHAR = "";

const WLD_STATUS_TYPE_DIRECT = 6,
      WLD_STATUS_TYPE_TPSHEM = 16;

var CmpParm, CmpResp, TpShemID, CmpTpShemID;

macro GetTpName(TpID)
  file f(wltransp) key 0;

  f.TpID = TpID;
  if (GetEQ(f))
    return f.Name;
  else
    return "не найден";
  end;
end;

macro GetFrmName(FormID)
  file f(wlmesfrm) key 0;

  f.FormID = FormID;
  if (GetEQ(f))
    return f.Name;
  else
    return "<не найдена>";
  end;
end;

macro GetRlsName(RlsID)
  file f(wlmesrls) key 0;

  f.RlsFormID = RlsID;
  if (GetEQ(f))
    return f.Name;
  else
    return "<не найден>";
  end;
end;

macro GetPTName(PartyID)
  file f(party) key 0;

  if (PartyID < 1)
    return "<не задан>";
  end;

  f.PartyID = PartyID;
  if (GetEQ(f))
    return f.ShortName;
  else
    return "<не найден>";
  end;
end;

macro GetStateName(TypeState, State)
  file f(wlstatus) key 0;
  
  f.TypeState = TypeState;
  f.State     = State;
  GetEQ(f);

  return f.Name;
end;

macro GetDirectionName(Direction)
  file f(namealg) key 0;
  
  f.iTypeAlg   = 2601;
  f.iNumberAlg = Direction;
  GetEQ(f);

  return f.szNameAlg;
end;

macro GetRegionKindName(rk)
  file f(objattr) key 0;

  f.ObjectType = 3;
  f.GroupID    = 15;
  f.AttrID     = rk;
  
  if (GetEQ(f))
    return f.Name;
  else
    return "<не найдена>";
  end;
end;

macro GetPartyName(PartyID)
  var Code = "", err;

  if (PartyID == -1)
    return "<Все респонденты>";
  end;

  Code = ПолучитьКодСубъекта(PartyID, PTCK_CONTR, err);

  if (err == 0)
    return Code;
  else
    return "<не найден>";
  end;
end;

macro GetPMKind(pk)
  file f("pmpopknd") key 0;

  f.PaymentKind = pk;
  if (GetEQ(f))
    return f.Name;
  else
    return "<не найден>";
  end;
end;

macro CharToStr(char_val)
  var str_val;
  if (char_val == SET_CHAR)
    str_val = " Да ";
  else
    str_val = " Нет ";
  end;
  return str_val;
end;

macro ConvTypeTime(dttm)
  var dt, tm;
  DtTmSplit(dttm, dt, tm);
  return tm;
end;


macro PrintHeader
  println("==============================================================================");
  println("Отчет о сравнении транспортных схем № ", TpShemID, " и № ", CmpTpShemID);
  println();
end;

macro PrintFooter
  println("------------------------------------------------------------------------------");
end;


macro PrintDifferencesTpShem
  file fTpShem(wltpshem) key 0;
  file fCmpTpShem(wltpshem) key 0;

  var strS  = TArray,
      strCS = TArray,
      i, count;

  fTpShem.TpShemID = TpShemID;
  GetEQ(fTpShem);
  fCmpTpShem.TpShemID = CmpTpShemID;
  GetEQ(fCmpTpShem);

  count = 0;

  if (fTpShem.State != fCmpTpShem.State)
    strS(count)  = string("Состояние: ", GetStateName(WLD_STATUS_TYPE_TPSHEM, fTpShem.State));
    strCS(count) = string("Состояние: ", GetStateName(WLD_STATUS_TYPE_TPSHEM, fCmpTpShem.State));
    count = count + 1;
  end;

  if (fTpShem.TpID != fCmpTpShem.TpID)
    strS(count)  = string("Вид транспорта: ", fTpShem.TpID,    " (", GetTpName(fTpShem.TpID),    ")");
    strCS(count) = string("Вид транспорта: ", fCmpTpShem.TpID, " (", GetTpName(fCmpTpShem.TpID), ")");
    count = count + 1;
  end;

  if (fTpShem.Name != fCmpTpShem.Name)
    strS(count)  = string("Название транспортной схемы: ", fTpShem.Name);
    strCS(count) = string("Название транспортной схемы: ", fCmpTpShem.Name);
    count = count + 1;
  end;

  if (fTpShem.Description != fCmpTpShem.Description)
    strS(count)  = string("Описание транспортной схемы: ", fTpShem.Description);
    strCS(count) = string("Описание транспортной схемы: ", fCmpTpShem.Description);
    count = count + 1;
  end;

  if (fTpShem.OutsideAbonentID != fCmpTpShem.OutsideAbonentID)
    strS(count)  = string("Внешний абонент: ", GetPTName(fTpShem.OutsideAbonentID));
    strCS(count) = string("Внешний абонент: ", GetPTName(fCmpTpShem.OutsideAbonentID));
    count = count + 1;
  end;

  if (fTpShem.InNoControl != fCmpTpShem.InNoControl)
    strS(count)  = string("Игнорировать контроль исходящих платежей:", CharToStr(fTpShem.InNoControl));
    strCS(count) = string("Игнорировать контроль исходящих платежей:", CharToStr(fCmpTpShem.InNoControl));
    count = count + 1;
  end;

  if (fTpShem.WlInfoAutoControl != fCmpTpShem.WlInfoAutoControl)
    strS(count)  = string("Автоконтроль входящих информационных сообщений:", CharToStr(fTpShem.WlInfoAutoControl));
    strCS(count) = string("Автоконтроль входящих информационных сообщений:", CharToStr(fCmpTpShem.WlInfoAutoControl));
    count = count + 1;
  end;

  if (fTpShem.InDeliveryProcessing != fCmpTpShem.InDeliveryProcessing)
    strS(count)  = string("Обрабатывать подтверждения о доставке сообщений:", CharToStr(fTpShem.InDeliveryProcessing));
    strCS(count) = string("Обрабатывать подтверждения о доставке сообщений:", CharToStr(fCmpTpShem.InDeliveryProcessing));
    count = count + 1;
  end;

  println("+++++++ Общая часть схемы № ", TpShemID, " +++++++");
  println();
  i = 0;
  while(i < count)
    [#](strS(i));
    i = i + 1;
  end; 

  println();
  println("+++++++ Общая часть схемы № ", CmpTpShemID, " +++++++");
  println();
  i = 0;
  while(i < count)
    [#](strCS(i));
    i = i + 1;
  end; 
end;

macro PrintParm(sID, csID)
  var rs:object;
  var select:string;
  var params:TArray;
  
  select = "select parm.* from dwltpparm_dbt parm where parm.t_TpShemID =:sID "+
           " and not exists(select * from dwltpparm_dbt cparm where cparm.t_TpShemID =:csID "+
                          " and parm.t_FormID = cparm.t_FormID"+
                          " and parm.t_RlsFormID = cparm.t_RlsFormID"+
                          " and parm.t_Direction = cparm.t_Direction"+
                          " and parm.t_Description = cparm.t_Description"+
                          " and parm.t_State = cparm.t_State"+
                          " and parm.t_Sort = cparm.t_Sort"+
                          " and parm.t_RegionKind = cparm.t_RegionKind"+
                          " and parm.t_ShifrOper = cparm.t_ShifrOper"+
                          " and parm.t_PayerAccountMask = cparm.t_PayerAccountMask"+
                          " and parm.t_ReceiverAccountMask = cparm.t_ReceiverAccountMask"+
                          " and parm.t_PayerBankCodeKind = cparm.t_PayerBankCodeKind"+
                          " and parm.t_PayerBankCodeMask = cparm.t_PayerBankCodeMask"+
                          " and parm.t_ReceiverBankCodeKind = cparm.t_ReceiverBankCodeKind"+
                          " and parm.t_ReceiverBankCodeMask = cparm.t_ReceiverBankCodeMask"+
                          " and parm.t_MaxAmount = cparm.t_MaxAmount"+
                          " and parm.t_FIID = cparm.t_FIID)";
  params = makeArray( SQLParam("sID", sID),
                      SQLParam("csID", csID));
  rs = execSQLselect( select, params, FALSE );

  println();
  println("+++++++ Параметры схемы № ", sID, " +++++++");
  while(rs.moveNext)
    [Состояние: #](GetStateName(WLD_STATUS_TYPE_DIRECT, rs.value("t_State")));
    [Сортировка: ####](rs.value("t_Sort"));
    [Форма: #](GetFrmName(rs.value("t_FormID")));
    [Релиз формы: #](GetRlsName(rs.value("t_RlsFormID")));
    [Направление: #](GetDirectionName(rs.value("t_Direction")));
    if(rs.value("t_Description")!="")[Описание: #](rs.value("t_Description"));end;
    if(rs.value("t_RegionKind")!=0)[Региональность: #](GetRegionKindName(rs.value("t_RegionKind")));end;
    if(rs.value("t_PayerBankCodeKind")!=0)[Плательщик вид кода: ##](rs.value("t_PayerBankCodeKind"));end;
    if(rs.value("t_PayerBankCodeMask")!="")[Плательщик маска кода: #](rs.value("t_PayerBankCodeMask"));end;
    if(rs.value("t_PayerAccountMask")!="")[Плательщик маска счета: #](rs.value("t_PayerAccountMask"));end;
    if(rs.value("t_ReceiverBankCodeKind")!=0)[Получатель вид кода: ##](rs.value("t_ReceiverBankCodeKind"));end;
    if(rs.value("t_ReceiverBankCodeMask")!="")[Получатель маска кода: #](rs.value("t_ReceiverBankCodeMask"));end;
    if(rs.value("t_ReceiverAccountMask")!="")[Получатель маска счета: #](rs.value("t_ReceiverAccountMask"));end;
    if(rs.value("t_MaxAmount")!=0)
      println("Максимальная сумма: ", rs.value("t_MaxAmount"), 
              " в валюте ", ПолучитьКодФинИн(rs.value("t_FIID")));
    end;
    if(rs.value("t_ShifrOper")!="")[Шифр операции: ##](rs.value("t_ShifrOper"));end;
    [+++++];
  end;
end;
                        
macro PrintDifferencesParm 
  PrintParm(TpShemID, CmpTpShemID);
  PrintParm(CmpTpShemID, TpShemID);
end;

macro PrintResp(sID, csID)
  var bt, et;
  var rs:object;
  var select:string;
  var params:TArray;
  select = "select r.* from dwltpshpt_dbt r where r.t_TpShemID =:sID "+
           " and not exists(select * from dwltpshpt_dbt cr where cr.t_TpShemID =:csID "+ 
                          " and r.t_PartyID = cr.t_PartyID"+
                          " and r.t_State = cr.t_State"+
                          " and r.t_Sort = cr.t_Sort"+
                          " and r.t_InsideAbonentID = cr.t_InsideAbonentID"+
                          " and r.t_RegionKind = cr.t_RegionKind"+
                          " and r.t_PaymentKind = cr.t_PaymentKind"+
                          " and r.t_BeginPeriod = cr.t_BeginPeriod"+
                          " and r.t_EndPeriod = cr.t_EndPeriod"+
                          " and r.t_MaxAmount = cr.t_MaxAmount"+
                          " and r.t_FIID = cr.t_FIID)";
  params = makeArray( SQLParam("sID", sID),
                      SQLParam("csID", csID));
  rs = execSQLselect( select, params, FALSE );

  println();
  println("+++++++ Респонденты схемы № ", sID, " +++++++");
  while(rs.moveNext)
    [Состояние: #](GetStateName(WLD_STATUS_TYPE_DIRECT, rs.value("t_State")));
    [Сортировка: ####](rs.value("t_Sort"));
    if(rs.value("t_PartyID")!=0)[Респондент: #](GetPartyName(rs.value("t_PartyID")));end;
    if(rs.value("t_InsideAbonentID")!=0)[Внутренний абонент: #](GetPartyName(rs.value("t_InsideAbonentID")));end;
    if(rs.value("t_PaymentKind")!="")
      [Вид платежа: ########## #](rs.value("t_PaymentKind"), GetPMKind(rs.value("t_PaymentKind")));
    end;
    if(rs.value("t_RegionKind")!=0)[Региональность: #](GetRegionKindName(rs.value("t_RegionKind")));end;
    bt = ConvTypeTime(rs.value("t_BeginPeriod"));
    et = ConvTypeTime(rs.value("t_EndPeriod"));
    if((bt!=Time(0,0,0)) or (et!=Time(0,0,0))) println("Время действия: c ", bt, " по ", et); end;
    if(rs.value("t_MaxAmount")!=0)
      println("Максимальная сумма: ", rs.value("t_MaxAmount"), 
              " в валюте ", ПолучитьКодФинИн(rs.value("t_FIID")));
    end;
    [+++++];
  end;
end;

macro PrintDifferencesResp 
  PrintResp(TpShemID, CmpTpShemID);
  PrintResp(CmpTpShemID, TpShemID);
end;

macro CmpTpShem(_TpShemID, _CmpTpShemID, _CmpParm, _CmpResp)
  var rs;

  CmpParm     = _CmpParm;
  CmpResp     = _CmpResp;
  TpShemID    = _TpShemID;
  CmpTpShemID = _CmpTpShemID;

  PrintHeader;
  PrintDifferencesTpShem;
  if (CmpParm == SET_CHAR) PrintDifferencesParm end;
  if (CmpResp == SET_CHAR) PrintDifferencesResp end;
  PrintFooter;
    
  return true;
end;
