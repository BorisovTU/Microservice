/*
  $Name:         mnsgmpb.mac
  $Module:       МБР
  $Description:  Генерация сообщения по PB
*/

import "wlgenmes.mac", "wlmnstls.mac", WldInter;

var FrmtKind:Integer  = 0;

private macro formatDate( dt )
   var day, mon, year, str;
   DateSplit(dt, day, mon, year );
   str = string( year:4, "-", mon:2, "-", day:2);
   str = StrSubSt( str, " ", "0" );
   return str;
end;

private macro formatTime( tm )
   var ss, mm, hh, str;
   TimeSplit(tm, hh, mm, ss );
   str = string(hh:2, ":", mm:2, ":", ss:2) ;
   str = StrSubst ( str, " ", "0" );
   return str;
end;

macro GenMes( addrMes, buff, addrOldMes )

  record OldMes( wlmes );  ClearRecord( OldMes );

  SetBuff( wlmes,     addrMes     );
  SetBuff( OldMes,    addrOldMes  );
  SetBuff( wlresprm,  buff        );

  PrintLog(2,"Генерация сообщения по PB"); 

  УстановитьКонтекстБлокаЛог("СтрокаИзвещения");
  ЗаписатьПолеЛог( "_СтрокаИзвещения", wlresprm.FileName + МНС_КонецБлока );

  var ErrList = RsbWlError( wlresprm.ResultID );
  var wlerror = TRecHandler("wlerror.dbt" );

  if( wlresprm.ErrCode == 0 )
    ЗаписатьПолеЛог( "_СтрокаИзвещения", "10" + МНС_КонецФрагмента );
  elif( ( ErrList.First( wlerror ) == 0 ) and ( wlerror.rec.Code != "15" ) )
    ЗаписатьПолеЛог( "_СтрокаИзвещения", wlerror.rec.Code + ";" + wlerror.rec.Description + МНС_КонецФрагмента );
  else
    ЗаписатьПолеЛог( "_СтрокаИзвещения", wlerror.rec.Code + ";" + wlerror.rec.Description );/* первая запись с кодом 15 */
    while( ErrList.Next( wlerror ) == 0 )
      ЗаписатьПолеЛог( "_СтрокаИзвещения", wlerror.rec.Code + ";" + wlerror.rec.Description );
    end;
    ЗаписатьПолеЛог( "_СтрокаИзвещения", МНС_КонецФрагмента );
  end;
  
  ЗаписатьПолеЛог( "_СтрокаИзвещения", formatDate( wlresprm.CreateDate ) + МНС_КонецФрагмента );
  ЗаписатьПолеЛог( "_СтрокаИзвещения", formatTime( wlresprm.CreateTime ) + МНС_КонецФрагмента );

  return true;
  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;

private macro CheckWlCodes( wlerror:object ):bool

  var algnum = 0;
  
  if( FrmtKind == WLD_FORMAT_TXT) 
     algnum = 54;
  else
     algnum = 76;
  end;     

  if( not existsSQLselect( "select 1 from dwlcodes_dbt where t_algnum = :algnum and t_code=:code", makeArray( SQLParam( "algnum", algnum ),
                                                                                                              SQLParam( "code", wlerror.rec.Code ) 
                                                                                                             ) ) )
    RunError( "Недопустимый код ошибки " + wlerror.rec.Code );
    return false;
  end;
  return true;
end;

private macro CheckDescrLen( wlerror:object ):bool
  var descrArray = split( wlerror.rec.Description, ";" );
  var Descr = wlerror.rec.Description;
  if( wlerror.rec.Code == "15" )
    Descr = descrArray[descrArray.size-1];
  end;
  if( strlen( Descr ) > 512 )
    RsbThrow( "Количество символов в описании результата проверки|не может быть больше 512" );
    return false;
  end;
  return true;
end;

private macro CheckConditionForErrList( ErrList:object, cond_func:string ):bool

  var wlerror = TRecHandler("wlerror.dbt" );
  if( ErrList.First( wlerror ) == 0 )
    if( ExecMacro2( cond_func, wlerror ) == false )
      return false;
    end;
    while( ErrList.Next( wlerror ) == 0 )
      if( ExecMacro2( cond_func, wlerror ) == false )
        return false;
      end;
    end;
  end;
  return true;
end;



macro CheckObj_( buff )
  SetBuff( wlresprm,  buff );

  var ErrList = RsbWlError( wlresprm.ResultID );

  if( ( wlresprm.ErrCode != 0 ) and ( ErrList.Size == 0 ) )
    RsbThrow( string( "Не найдена информация о результатах проверки, ", wlresprm.ResultID ) );
  end;

  return CheckConditionForErrList( ErrList, "CheckDescrLen" ) and
         CheckConditionForErrList( ErrList, "CheckWlCodes"  );

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;

macro CheckObj( buff )

  FrmtKind = WLD_FORMAT_TXT;
  return CheckObj_(buff);

end;

macro CheckObjXml( buff )

  FrmtKind = WLD_FORMAT_XML;
  return CheckObj_(buff);

end;

