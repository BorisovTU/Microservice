/*
  $Name:        fnsgmbuvx.mac
  $Module:      МБР
  $Description: Генерация сообщения BUV по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения BUV по транспорту ФНС в формате XML
//
// Программист  : Timonin D.V.
//
// Создан       : 25.08.2014
//
//-----------------------------------------------------------------------------
import "fns365P_lib.mac", FIInter;

// Сформировать поля: код филиала, БИК филиала, Наименование филиала
private macro constructDepCodeBICNameFldValue( ndlnkRec : TRecHandler, Code : @string, BIC : @string, Name : @string ) : bool
  var Error = 0, ind = 0;
  var stat:bool = FALSE;

  var DepCode = "", DepBIC = "", DepName = "";
  var strSQL = "select t_PartyID " +
               "  from ddp_dep_dbt " +
               " where t_Code = " + ndlnkRec.rec.Department;
  var rs : RsdRecordset = execSQLselect(strSQL);
  if(rs and rs.moveNext())
        
    // код филиала
    DepCode = ПолучитьКодСубъекта (rs.value("t_PartyID"), PTCK_BANKREGNUM, Error, 0);
    // взять символы после слеша
    ind = index(DepCode, "/");
    if( (ind > 0) and (ind < strlen(DepCode)) )
      DepCode = subStr(DepCode, ind + 1);
    else
      DepCode = "";
    end;
    // дополнить слева нулями до 4-х символов
    while( strlen(DepCode) < 4 )
      DepCode = "0" + DepCode;
    end;

    // БИК филиала
    DepBIC = GetPartyCode(rs.value("t_PartyID"), PTCK_BIC);

    // Наименование филиала
    DepName = ndlnkRec.rec.Name;

    Code = DepCode;
    BIC = DepBIC;
    Name = DepName;

    stat = TRUE;
  end;

  return stat;  
end;

macro GenMes( addrMes, addrNFns, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record notfns(wlnotfns);
  SetBuff(notfns, addrNFns);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BUV");

  record GenAcc(account);
  var ErrMsg : string = "", FILL_AT_EXPORT : string = " ";

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьПолеЛог("ИдЭС", GetGuidFnsX());
  ЗаписатьПолеЛог("ТипИнф", "УВЕБНОБМЕН");
  ЗаписатьПолеЛог("ВерсПрог", "RS-Bank V.6");
  ЗаписатьПолеЛог("ТелОтпр", FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ДолжнОтпр", FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ФамОтпр", FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ВерсФорм", "3.00");

  // Файл \ УВЕБНОБМЕН
  StartBlockLogXML("УВЕБНОБМЕН");

  // атрибуты элемента "УВЕБНОБМЕН"
  ЗаписатьПолеЛог("ДатаУвед", YYYY_MM_DD(date()));
  ЗаписатьПолеЛог("КодСоб", notfns.Action);
  ЗаписатьПолеЛог("ДатаЗаклДог", YYYY_MM_DD(notfns.ContractDate));
  if( (notfns.Action == "12") and (notfns.AddAgreeDate != date(0,0,0)) )
    ЗаписатьПолеЛог("ДатаДопСогл", YYYY_MM_DD(notfns.ContractDate));
  end;
  ЗаписатьПолеЛог("ДатаСоб", YYYY_MM_DD(notfns.Date));
   
  if( InList(notfns.Action, "11", "12") and (notfns.ContractCond == "") )
    ErrMsg = "Не удалось заполнить поле ""КодУсл"" . " +
          "Обязательно для заполнения при значении поля ""КодСоб"" " + string(notfns.Action) + ".";
    RunError(ErrMsg, ErrMsg);
  end;

  ЗаписатьПолеЛог("КодУсл", notfns.ContractCond);

  // Файл \ УВЕБНОБМЕН \ СвБанк
  СведенияОБанке("СвБанк", wlmes.InsideAbonentID);

  if((notfns.Action == "12") and ((strlen(notfns.NewBankCode) > 0) or (strlen(notfns.NewBankName) > 0)))
    СведенияОБанке("СвБанкНов", wlmes.InsideAbonentID, notfns.NewBankCode, notfns.NewBankName );
  end;

  // Файл \ УведомлениеБ \ Филиалы
  var Code = "", BIC = "", Name = "";
  var ndlnkObj : RsbWlNDLnk = RsbWlNDLnk(notfns.NoticeID);
  var ndlnkRec : TRecHandler = TRecHandler("wlndlnk.dbt");
  var IsDprt : bool = ( ndlnkObj.First(ndlnkRec) == 0 );

  while(IsDprt)
    StartBlockLogXML("Филиалы");
    // Записать поля: код филиала, БИК филиала, Наименование филиала
    if( constructDepCodeBICNameFldValue( ndlnkRec, @Code, @BIC, @Name ) )
      ЗаписатьПолеЛог( "НомФил", Code );
      ЗаписатьПолеЛог( "БИК", BIC );
      ЗаписатьПолеЛог( "НаимФил", Name );
    end;
    FinishBlockLogXML("Филиалы");

    IsDprt = ( ndlnkObj.Next(ndlnkRec) == 0 );
  end;

  FinishBlockLogXML("УВЕБНОБМЕН");

  FinishBlockLogXML("Файл");

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
