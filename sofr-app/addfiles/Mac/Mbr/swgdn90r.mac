/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT MTn90 стандарт RUR5               */
/*                                                                          */
/*  Имя файла: swgdn90r.mac                                                 */
/*  Создан:    22.09.00                                       Бабин А.П.    */
/****************************************************************************/

import "swgnrdoc.mac";

/* Платежная инструкция  */
class StatementMessage
 var
  standart         :integer,     /* Стандарт транслитерации */
  TRF              :string,      /* Ссылочный номер операции */
  Refer            :string,      /* Ссылка */
  Account          :string,      /* Корсчет */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  Credit           :bool,        /* Признак дебета/кредита */
  OrdBank          :Bank,        /* Банк Приказодателя */
  Sender           :Bank,        /* Отправитель */
  Charges          :string,      /* Детали расходов */
  RcvInfo          :TRcvInfo100; /* Информация Получателю поле 72 */

  TRF              = "";
  Refer            = "";
  Account          = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Charges          = "";

end; /* class StatementMessage */

var MTn90 : StatementMessage;

macro Fill20( TRF )
  MTn90.TRF = TRF;
  MTn90.standart = ДоопределитьСтандарт( TRF );
  return TRUE;
end;

macro Fill21( Refer )
  MTn90.Refer = Refer;
  return TRUE;
end;

macro Fill25( Account )
  MTn90.Account = Account;
  return TRUE;
end;

macro Fill32C( Date, Cur, Sum )
  MTn90.ValueDate = Date;
  MTn90.Currency  = Cur;
  MTn90.Sum       = moneyl(Sum);
  MTn90.Credit    = TRUE;
  return TRUE;
end;

macro Fill32D( Date, Cur, Sum )
  MTn90.ValueDate = Date;
  MTn90.Currency  = Cur;
  MTn90.Sum       = moneyl(Sum);
  MTn90.Credit    = FALSE;
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )  
  MTn90.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MTn90.standart );
  MTn90.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill71B( Charges )
  MTn90.Charges = Charges;
  StrRestoreStandart( MTn90.Charges, MTn90.Charges, MTn90.standart );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  StrRestoreStandart( Str, Str, MTn90.standart );
  StrRestoreStandart( Narrative, Narrative, MTn90.standart );
  MTn90.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MTn90*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld25  = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld32C = ТПолеФормы( ValueDateCurrencyCodeAmountField_C, FIELD_OPTIONAL, "Read32A", "Fill32C", "" ),
    Fld32D = ТПолеФормы( ValueDateCurrencyCodeAmountField_D, FIELD_OPTIONAL, "Read32A", "Fill32D", "" ),    
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),        
    Fld71B = ТПолеФормы( DetailsOfChargesField_B,            FIELD_MANDATORY, "Read71B", "Fill71B", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMTn90( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MTn90.Sender   = Bank( BankCode );       

  return TRUE;
end; /* ConstructMTn90 */

macro GenDoc( addrMes )
  var error;
  MTn90 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТn90");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMTn90( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Amount      = MTn90.Sum;
  wlpmpaym.PayAmount   = MTn90.Sum;
  wlpmpaym.BaseAmount  = MTn90.Sum;
  wlpmpaym.OrderAmount = MTn90.Sum;
  wlpmpaym.ValueDate = MTn90.ValueDate;
  if( not ПолучитьФинИнПоISO( MTn90.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MTn90.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;
  
  if( MTn90.Credit == TRUE )
    /* Наш корсчет кредитуется, это кредитовка (поручение) */
    wlpmpaym.PayerBankID     = wlmes.OutsideAbonentID;
    wlpmpaym.ReceiverBankID  = {OurBank};
    wlpmpaym.PayerAccount    = MTn90.Account;
    /* Счет получаетеля - должен быть счет доходов */
  else
    /* С нас списывают проценты, дебетовка, это требование */
    wlpmpaym.PayerBankID     = {OurBank};
    wlpmpaym.ReceiverBankID  = wlmes.OutsideAbonentID;
    wlpmpaym.ReceiverAccount = MTn90.Account;
    /* Счет плательщика - должен быть счет расходов */
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Ground = MTn90.Charges;
  wlpmrmprop.PartyInfo = MTn90.RcvInfo.REC;
  /*wlpmrmprop.MessageType = Transfer;#59030*/
  if ( MTn90.RcvInfo.RPP_Number=="")
     wlpmrmprop.Number             = MTn90.TRF;
     wlpmrmprop.PayDate            = MTn90.ValueDate;
     wlpmrmprop.Date               = MTn90.ValueDate;
     wlpmrmprop.ClientDate         = MTn90.ValueDate;
     wlpmrmprop.ShifrOper          = "01";
  else
     wlpmrmprop.Number             = MTn90.RcvInfo.RPP_Number;
     wlpmrmprop.PayDate            = MTn90.RcvInfo.RPP_Date;
     wlpmrmprop.Date               = MTn90.RcvInfo.RPP_Date;
     wlpmrmprop.ClientDate         = MTn90.RcvInfo.RPP_DateCarry;
     wlpmrmprop.ShifrOper          = MTn90.RcvInfo.RPP_ShifrOper;
  end;

  if( MTn90.Credit == TRUE )
     wlpmrmprop.PayerBankName         = MTn90.Sender.Name;
     wlpmrmprop.ReceiverBankName      = {Name_Bank};
  else
     wlpmrmprop.PayerBankName         = {Name_Bank};
     wlpmrmprop.ReceiverBankName      = MTn90.Sender.Name;
  end;  
  
  wlpmrmprop.Priority              = MTn90.RcvInfo.RPP_Priority;
  wlpmrmprop.PartyInfo             = MTn90.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = MTn90.RcvInfo.RPP_PaymentKind;
  if( wlpmrmprop.PaymentKind == "С" )
    wlpmrmprop.Instancy = 1;
  end;

  wlpmrmprop.ProcessKind           = " 1";
  
  if ( MTn90.Refer!=СсылкаОтсутствует )
     wlpmrmprop.Reference = MTn90.Refer;
  end;

  /* Заполняем свойства платежа */
  if( MTn90.Credit == TRUE )
     /* Дебетовые свойства платежа, поручение */
     if ( wlpmpaym.ValueDate==date(0,0,0) )
        wlpmpropdeb.TransferDate = {curdate};
     else
        wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
     end;
     wlpmpropdeb.PayFIID    = wlpmpaym.PayFIID;
     wlpmpropdeb.CodeKind   = MTn90.Sender.CodeKind;
     wlpmpropdeb.BankCode   = MTn90.Sender.CodeBank;
     /* В поле 25 явно прописан наш ностро-корсчет */
     wlpmpropdeb.OurCorrAcc = MTn90.Account;
     wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
     wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
     wlpmpropdeb.Corschem     = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
     if(wlpmpropdeb.Corschem != -1)
       wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
     end;

     ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, 0, wlpmrmprop );
     
     if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, 0, wlpmrmprop ) )
       std.msg("Ошибка при вставке платежа");
       return FALSE;
     end;
  else
     /* Заполняем кредитовые свойства платежа, требование */
     if ( wlpmpaym.ValueDate==date(0,0,0) )
        wlpmpropcred.TransferDate = {curdate};
     else
        wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
     end;
     wlpmpropcred.PayFIID      = wlpmpaym.PayFIID;
     wlpmpropcred.CodeKind     = MTn90.Sender.CodeKind;
     wlpmpropcred.BankCode     = MTn90.Sender.CodeBank;
     /* В поле 25 явно прописан наш ностро-корсчет */
     wlpmpropcred.OurCorrAcc   = MTn90.Account;
     wlpmpropcred.InOurBalance = ""; /* Счет указан в балансе корреспондента */
     wlpmpropcred.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
     wlpmpropcred.Corschem     = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
     if(wlpmpropcred.Corschem != -1)
       wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
     end;
     
     ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, 0, wlpmpropcred, wlpmrmprop );
     
     if( not ВставитьПлатеж( wlpmpaym, 0, wlpmpropcred, wlpmrmprop, PRT_Debet ) )
       std.msg("Ошибка при вставке платежа");
       return FALSE;
     end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;