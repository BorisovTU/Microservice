/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Отчеты по платежам                                                      */
/*  Имя файла: wlreppm.mac                                                 */
/*  Создан:  28.02.01                                         Бабин А.П.    */
/****************************************************************************/

import FIInter, PTInter, "wldoc.mac", pmprops;

file cors( corschem ) key 1;

var DK, InOut, Code, CurFIID=-10, Sum=0, Num=0;

macro SumToStr(FIID, sum)

   f_wlfininstr.FIID = FIID;
   if ( GetEQ( f_wlfininstr ) )
      return CurToStrAlt( sum, null, null, int(f_wlfininstr.ISO_Number) );
   elif ( FIID>=0 )
      return string(sum) + " (Неизвестная валюта)";
   else return "";
   end;

end;

macro DateToStr( date )
   var day, mon, year, str;
   DateSplit(date, day, mon, year );
   if ( day < 10 )
     day = "0"+string(day);
   else
     day = string(day);
   end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = day + "." + mon + "." + string(year);
  return str;
end;

macro ChangeFI(FIID, FlagEnd )

   if ( ((CurFIID!=FIID) AND (CurFIID>=0)) OR (valtype(FlagEnd)!=V_UNDEF) )
[+----------------------------+---------------+-+----------+----------------------------+-----------+----------------------------+-------------------------+
Итого      ######################   ######################################################################################################################
Документов ######

](Sum:a, SumToStr(CurFIID, Sum):l, Num:l);
   end;

   if ( (CurFIID!=FIID) AND (FIID>=0) )
[
           ############### #
+----------------------------+---------------+-+----------+----------------------------+-----------+----------------------------+-------------------------+
](ПолучитьКодФинИн(FIID), StrUpr(ПолучитьИмяФинИн(FIID)));

   end;

   if( CurFIID!=FIID )
      CurFIID = FIID;
      Sum = $0;
      Num =  0;
   end;

end;

macro PrintHeader( Mode, DKFlag, CorrID, TpID, DateB, DateE, DepBIC, DepName, DepID )
  var NameRep, error;

  file party(party);

  Code = ПолучитьКодСубъекта( CorrID, PTCK_CONTR, error );

  if ( error )
     msgBox("Не найден код корреспондента");
     return false;
  end;

  party.PartyID = CorrID;
  GetEQ(party);

  f_wltransp.TpID = TpID;
  GetEQ(f_wltransp);

  if ( mode==0 )
     NameRep = "ОТЧЕТ ПО НЕОТПРАВЛЕННЫМ ПЛАТЕЖАМ";
     InOut = 0;
  elif ( mode==1 )
     NameRep = "ОТЧЕТ ПО НЕСКВИТОВАННЫМ ИСХОДЯЩИМ ПЛАТЕЖАМ";
     InOut = 0;
  elif ( mode==2 )
     NameRep = "ОТЧЕТ ПО СКВИТОВАННЫМ ИСХОДЯЩИМ ПЛАТЕЖАМ";
     InOut = 0;
  elif ( mode==3 )
     NameRep = "ОТЧЕТ ПО ПРИНЯТЫМ ПЛАТЕЖАМ";
     InOut = 1;
  elif ( mode==4 )  /*С 3.09.01 Не используется */
     NameRep = "ОТЧЕТ ПО ПЛАТЕЖАМ ОТПРАВЛЕННЫМ В АРМ";
     InOut = 1;
  end;

  DK = DKFlag;
  if ( DKFlag=="К" )
     NameRep = NameRep + " - " + "КРЕДИТ";
  else
     NameRep = NameRep + " - " + "ДЕБЕТ";
  end;

  if ( CurFIID!=-10 )
     //ChangeFI(-1, 1);
  else 
     ChangeFI(-1);
  end;

[      #######################################################################################

            Филиал          ############### #
            Корреспондент   ############### #
](NameRep:c, DepBIC, DepName, Code, party.Name);

  if (f_wltransp.Name)
[           Транспорт       #
](f_wltransp.Name);
  end;

[           с               #
            по              #
]
( DateToStr(DateB), DateToStr(DateE) ) ;

[
+----------------------------+---------------+-+----------+----------------------------+----------------------------------------+-------------------------+
|     Корреспондентский      |     Номер     |В|   Дата   |                            |            Корреспондент               |                         |
|           счет             |    платежа    |И| валютир. |  N счета клиента           +-----------+----------------------------+          Сумма          |
|                            |               |Д|          |                            |    БИК    |           Счет             |                         |
+----------------------------+---------------+-+----------+----------------------------+-----------+----------------------------+-------------------------+];

  return true;
end;

macro PrintRecord( addrPmPaym, addrRm, /*addrDebet, addrCredit*/addrPmProp )
    var NamePaym, stat, AccountClnt, AccountClntBank, BICCode;

    SetBuff( wlpmpaym, addrPmPaym );
    SetBuff( wlpmrmprop, addrRm );
    /*SetBuff( wlpmpropdeb, addrDebet );
    SetBuff( wlpmpropcred, addrCredit );*/
    SetBuff( wlpmprop, addrPmProp );    

    if( (( DK == "Д" ) /*AND ( InOut == 0 ) )
       OR (( DK == "К" ) AND ( InOut == 1 )*/ ))
      AccountClnt     = wlpmpaym.ReceiverAccount ;
      AccountClntBank = wlpmpaym.PayerAccount ;
      GetPartyCodeEx(wlpmpaym.PayerBankID, 3, @BICCode );
    else
      AccountClnt     = wlpmpaym.PayerAccount ;
      AccountClntBank = wlpmpaym.ReceiverAccount ;
      GetPartyCodeEx(wlpmpaym.ReceiverBankID, 3, @BICCode );
    end ;

    NamePaym = wlpmrmprop.Number;    

    /*if ( wlpmpropdeb.PaymentID==wlpmpaym.PaymentID )
       cors.Number = wlpmpropdeb.Corschem;
       cors.FI_KIND = 1;
       cors.FIID = wlpmpropdeb.PayFIID;
    else
       cors.Number = wlpmpropcred.Corschem;
       cors.FI_KIND = 1;
       cors.FIID = wlpmpropcred.PayFIID;
    end;*/
    cors.Number = wlpmprop.Corschem;
    cors.FI_KIND = 1;
    cors.FIID = wlpmprop.PayFIID;       

    GetEQ( cors );

    ChangeFI(wlpmpaym.BaseFIID/*PayFIID*/);

    [|############################|###############|#|##########|############################|###########|############################|#########################|]
    ( cors.Account,
                                    NamePaym,
                                                   DK, DateToStr(wlpmpaym.ValueDate),
                                                                   AccountClnt,                 BICCode,
                                                                                                         AccountClntBank,             wlpmpaym.BaseAmount:a ) ;
    Sum = Sum+wlpmpaym.BaseAmount;
    Num = Num+1;
    return true;
end;

macro PrintFloor( )
    ChangeFI(-1, 1);
    return true;
end;