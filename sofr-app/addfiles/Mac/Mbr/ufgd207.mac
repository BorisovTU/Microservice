/*
 $Name: ufgd207.mac
 $Module: МБР
 $Description: Генерация выписок по сообщениям УФЭБС ED207
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям УФЭБС ED207                              */
/*                                                                          */
/* Имя файла: ufgd207.mac                                                   */
/* Создан:    28.10.04                                      Фомченкова Л.Н. */
/****************************************************************************/
import "ufgendoc.mac";

var ver_st = 1;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Node:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Сумма, Corschem, Currency, Error, Cancel, field_name, CreateHead = true;
  var StatusCode, i = 0, Page, EDDate, j = 0;
  var PaymentID = 0;
  var relRef = NULL;
  var InitialEDRef = null;
  var wlMesTRNs = "";


  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация выписки по ED207" );

  ClearRecord( wlhead );
  
  var only_update_payments;

  while( СчитатьПоле( field_name, Строка ) )
     
  if( field_name == XMLField )

    if ( not xml.loadXML(Строка) )
       println( string( "Неверный формат сообщения по форме ED207" ) );
       return false;
    end;

    EDDate = ToDate(ReadAttribute(xml,"EDDate"));
    
    StatusCode = ReadAttribute(xml,"StatusCode");
    Cancel = UF_getCancelForConf( StatusCode, ver_st );
    
    only_update_payments = false;

    if((StatusCode == "11") or (StatusCode == "20") )
      CreateHead = false;
    elif ( (Cancel == KVIT_ERROR) ) 
      std.msg( "Сообщения ED207 со статусом " + StatusCode + " обрабатываются без формирования подтверждений" );
      InitialEDRef = EDNoDateAuthor();
      if( not ПроверитьЭлелементInitialEDНаНаличие( xml, InitialEDRef.EDNo, InitialEDRef.EDDate, InitialEDRef.EDAuthor ) )
        InitialEDRef = null;
      end;
      only_update_payments = true;
    else
      /* Заполнение учетных буферов */
      wlhead.Number       = int(ReadAttribute(xml,"EDDayNo"));
      Page = ReadOptinalAttribute(xml,"MessageNo");
      if (Page)
          wlhead.Page     = int(Page);
      end;
      wlhead.DateIn       = EDDate;
      wlhead.DateOut      = EDDate;
      wlhead.FIID         = 0;
      wlhead.Corschem     = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlhead.FIID, 1 );

      ПроверитьЭлелементPartInfoНаНаличие(xml, wlhead.PartAggregateID, wlhead.Page );

      InitialEDRef = EDNoDateAuthor();
      if( not ПроверитьЭлелементInitialEDНаНаличие( xml, InitialEDRef.EDNo, InitialEDRef.EDDate, InitialEDRef.EDAuthor ) )
        InitialEDRef = null;
      end;

      if( wlhead.Corschem == -1 )
        Currency = ПолучитьКодФинИн(wlhead.FIID, error);
        if ( error )
           Currency = "???";
        end;
        std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
        return FALSE;
      end;

      if( not ВставитьЗаголовокВыписки( wlhead ) )
        std.msg("Ошибка при вставке заголовка выписки");
        return FALSE;
      end;
    end;
  elif( field_name == UFBSDocExtField )
        /*Вставляем документы выписки*/
        if( not node.loadXML(Строка) )
            println( string( "Неверный формат сообщения по форме ED207" ) );
            return false;  
        end;
        
        relRef = EDNoDateAuthor();
        relRef.EDNo   = ReadAttribute(Node,"EDNo", "EDRefID");
        relRef.EDDate = ToDate(ReadAttribute(Node,"EDDate", "EDRefID"));
        relRef.EDAuthor = ReadAttribute(Node, "EDAuthor", "EDRefID");

        if( not only_update_payments )
        
          ClearRecord(wlconf);

          wlconf.Sum        = moneyL(ReadAttribute(Node,"Sum"))/100;
          wlconf.FIID       = 0;                            
          wlconf.Cancel     = Cancel;
          
          wlconf.BankID = ToRespID(relRef.EDAuthor);
          wlconf.Number = relRef.EDNo;
          wlconf.TransferDate = wlconf.DateValue = EDDate;
          
          wlconf.RelatedRef = relRef.GetTrn();
          wlconf.Description = ПолучитьКодОтветаНаСообщение(StatusCode, TYPE_CODE_UFBS_ED203);
          wlconf.DKFlag = 0;

          if(StatusCode == "11")
            ОбработатьКакСообщениеОДоставке(wlconf.DateValue,wlconf.RelatedRef, false);
          elif(StatusCode == "20")
            ОбработатьКакED201(wlconf.DateValue,wlconf.RelatedRef, 0);
          else

            if(StatusCode == "08")
               ОбработатьКакСообщениеОДоставке(wlconf.DateValue,wlconf.RelatedRef, false);
            end;

            /* Вставляем документ выписки */
            if( not ВставитьДокументВыписки( wlconf ) )
              std.msg("Ошибка при сохранении документа выписки");
              return FALSE;
            end;
           
          end;
        end;
        
        if( wlMesTRNs == "" )
          wlMesTRNs = "'" + relRef.GetTrn() + "'";
        else
          wlMesTRNs = wlMesTRNs + "," + "'" + relRef.GetTrn() + "'";
        end;
        
     else
     
        std.msg( string( "Неизвестный код поля: ", field_name) );
        return FALSE;
     
     end;

  end;
      
  /* Обновим выписку */ 
  if( ( ( not only_update_payments ) and CreateHead and (not ОбновитьЗаголовокВыписки( wlhead ) ) ) )
    std.msg("Ошибка при обновлении заголовка выписки");
    return FALSE;
  end;
  
  if( wlMesTRNs != "" )
    var select = "select t_PaymentID "
                   "from dwlmes_dbt mes, "
                        "dwlmeslnk_dbt meslnk, "
                        "dwlpm_dbt wlpm "
                   "where mes.t_Trn in ( " + wlMesTRNs + " /* !!Список референсов */  ) " 
                     "and meslnk.t_MesID = mes.t_MesID "
                     "and meslnk.t_ObjKind = :OBJTYPE_PAYMENT "
                     "and wlpm.t_WlPmID = meslnk.t_ObjID "
                     "and wlpm.t_Direct = chr(0)/*WLD_MES_OUT*/ ";
  
    // Обновляем поле dwlpm_dbt.t_StatusCode
    if( StatusCode != "" )
      execSQL( "update dwlpm_dbt "
               "   set t_StatusCode = :StatusCode "
               " where t_PaymentID in ( " + select + " )",
               makeArray( SQLParam("StatusCode", StatusCode ),
                          SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT ))
             );
    end;
  end;
  
  var InitialEDReference = "";
  
  if( InitialEDRef )
    InitialEDReference = InitialEDRef.GetTrn();
  end;  
    
  if( InitialEDReference != "" )
    var InitialMesID = 0;
    var InitEDSelect = " select mes.t_MesID as t_MesID"
                       " from dwlmes_dbt mes, "
                       "      dwlmeslnk_dbt meslnk, "
                       "      dwlreq_dbt wlreq "
                   "where mes.t_Trn = :RelatedRef "
                     "and meslnk.t_MesID = mes.t_MesID "
                     "and meslnk.t_ObjKind = 505/*OBJTYPE_WLD_REQ*/ "
                     "and meslnk.t_ObjID = wlreq.t_ReqID "
                     "and wlreq.t_Direct = chr(0)/*WLD_MES_OUT*/ "
                     "and wlreq.t_SubKind = :WLD_SUBKIND_REQ_CHPMPREC "
                     ;
    var rs : RsdRecordset = execSQLselectPrm( InitEDSelect, SQLParam( "RelatedRef", InitialEDReference ),
                                                            SQLParam( "WLD_SUBKIND_REQ_CHPMPREC", WLD_SUBKIND_REQ_CHPMPREC ) );

    if( rs and rs.moveNext() )
      InitialMesID = rs.value( "t_MesID" );
    end;

    if( InitialMesID != 0 )
      var Element = 0;
      var Note = "";
      GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_UFBS, StatusCode, Element, Note );
      ОшибкаЛогическогоКонтроляСообщения( InitialMesID, Element, IfThenElse( Note == "", StatusCode, Note ) );
    end;    
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
