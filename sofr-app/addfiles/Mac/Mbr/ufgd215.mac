/*
  $Name:         ufgd215.mac
  $Module:       Межбанковские расчеты
  $Description:  описание
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям УФЭБС ED215                              */
/*                                                                          */
/* Имя файла: ufgd215.mac                                                   */
/* Создан:    06.07.06                                     Гайворонский Е.Г.*/
/****************************************************************************/
import "ufgendoc.mac";

var ver_st = 1;
file wlreq2 ("wlreq.dbt") key 3;

macro НайтиКодРежима(answercode: string) :string

  var result = "Неизвестный код режима: " + answercode;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_description from dwlcodes_dbt where t_algnum = 42 and t_code=:code";
  
  params = makeArray( SQLParam("code", answercode));
  rs = execSQLselect( select, params, FALSE );
  if ( rs and rs.MoveNext() )
     result = rs.value(0);
  end;
        
  return result;
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Node:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Строка2, Receiver, Error, InitAuthor, Part = 0, field_name; 
  var Cancel = KVIT_NORMAL, StatusCode = "", ErrDescription = "", CtrlCode = "", ReadFromNode = "";
  var EDDate = "", j = 0;
  var relRef = NULL;
  var wlMesTRNs = "";

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED215" );

  ClearRecord( wlreq );

  while( СчитатьПоле( field_name, Строка ) )
    if( field_name == XMLField )
      if ( not xml.loadXML(Строка) )
         println( string( "Неверный формат сообщения по форме ED215" ) );
         return false;
      end;
    
      EDDate = ReadAttribute(xml,"EDDate");
      StatusCode = ReadOptinalAttribute(xml,"StatusStateCode");
      if(StatusCode != "")
        wlreq.Queries = StatusCode;
        Строка2 = НайтиКодРежима(wlreq.Queries);
        Cancel = UF_getCancelForConf(StatusCode, ver_st);
      end;
    
      /* Заполнение учетных буферов */
      wlreq.TRN                = wlmes.Trn; 

      var uis = ReadAttribute(xml,"EDAuthor");
      FillPartyByUIS_PTCK_UIS(uis, @wlreq.OriginatorID, 
                                          @wlreq.OriginatorCodeKind, 
                                          @wlreq.OriginatorCode, 
                                          @wlreq.OriginatorName );

      Receiver = ReadOptinalAttribute(xml,"EDReceiver");
      FillPartyByUIS_PTCK_UIS(Receiver, @wlreq.RecipientID, 
                                          @wlreq.RecipientCodeKind, 
                                          @wlreq.RecipientCode, 
                                          @wlreq.RecipientName );
    
      wlreq.RelatedRef = wlmes.RelatedRef;
    
      if(ReadOptinalAttribute(xml,"EDNo","InitialED" ) != "")
         wlreq.InitDateMes = ToDate(ReadAttribute(xml,"EDDate","InitialED" ));
         InitAuthor        = ToRespID(ReadAttribute(xml,"EDAuthor","InitialED" ));
         wlreq.InitFormIDMes = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);  
      end;
     
      if( ver_st == 1 )
         if( not ВставитьОтвет( wlreq, Строка2 ) )
           std.msg("Ошибка при вводе ответа");
           return FALSE;
         end;
      else
         if( (not ПроверитьЭлелементPartInfoНаНаличие(xml, wlreq.PartAggregateID, Part)) Or (Part == 1))
            if( not ВставитьОтвет( wlreq, Строка2 ) )
               std.msg("Ошибка при вводе ответа");
               return FALSE;
            end;
         else
            ClearRecord( wlreq2 );
            wlreq2.PartAggregateID = ReadAttribute(xml,"PartAggregateID","PartInfo");
            wlreq2.Direct = "X";
            if( GetEQ( wlreq2 ))
            /*Строка2*/
               if( not ОбновитьОтвет( wlreq2, "" ) ) 
                  std.msg("Ошибка при обновлении ответа");
                  return FALSE;
               end;
            else
               std.msg("Не найдена головная часть ответа");
               return FALSE;
            end;  
         end;
      end;
    elif( field_name == UFBSDocExtField )
      if( not node.loadXML(Строка) )
         println( string( "Неверный формат сообщения по форме ED215" ) );
         return false;
      end;
      
        ReadFromNode = Node.ChildNodes.Item(0).ChildNodes.Item(0).NodeName;
        
        relRef = EDNoDateAuthor();
    
        relRef.EDNo   = ReadAttribute(Node,"EDNo",          ReadFromNode);
        relRef.EDDate = ToDate(ReadAttribute(Node,"EDDate", ReadFromNode));
        relRef.EDAuthor = ReadAttribute(Node, "EDAuthor",   ReadFromNode);
        
        if( wlMesTRNs == "" )
          wlMesTRNs = "'" + relRef.GetTrn() + "'";
        else
          wlMesTRNs = wlMesTRNs + ", " + "'" + relRef.GetTrn() + "'";
        end; 
        
        if (Cancel == KVIT_ERROR)
          std.msg( "Сообщения ED215 со статусом " + StatusCode + " обрабатываются без формирования подтверждений" );
        else

          ClearRecord(wlconf);
     
          wlconf.RelatedRef   = relRef.GetTrn();
          wlconf.TransferDate = ToDate(EDDate);
          wlconf.Description = ПолучитьКодОтветаНаСообщение(StatusCode, TYPE_CODE_UFBS_ED203);

          if(not СформироватьПодтверждение(wlconf, Cancel, node, ReadFromNode))
            return false;
          end;
        end;
    else 
       std.msg( string( "Неизвестный код поля: ", field_name) );
       return FALSE;
    end; 
    
    
  end; // while( СчитатьПоле( field_name, Строка ) )

  if( wlMesTRNs != "" )
    var select = "select t_PaymentID "
                   "from dwlmes_dbt mes, "
                        "dwlmeslnk_dbt meslnk, "
                        "dwlpm_dbt wlpm "
                   "where mes.t_Trn in ( " + wlMesTRNs + " ) " /* !! Список референсов */
                     "and meslnk.t_MesID = mes.t_MesID "
                     "and meslnk.t_ObjKind = 501/*OBJTYPE_PAYMENT*/ "
                     "and wlpm.t_WlPmID = meslnk.t_ObjID "
                     "and wlpm.t_Direct = chr(0)/*WLD_MES_OUT*/ ";
  
    // Обновляем поле dwlpm_dbt.t_StatusCode
    if( StatusCode != "" )
      execSQL( "update dwlpm_dbt "
               "   set t_StatusCode = :StatusCode "
               " where t_PaymentID in ( " + select + " )",
               makeArray( SQLParam("StatusCode", StatusCode ))
             );
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
