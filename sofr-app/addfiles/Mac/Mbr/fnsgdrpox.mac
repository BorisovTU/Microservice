/*
$Name:             fnsgdrpox.mac
$Module:           МБ Расчеты
$Description:      Генерация учетного объекта по сообщению формы RPO (XML)
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация учетного объекта по сообщению формы RPO
//
// Программист  : Чукина Т.А.
//
// Создан       : 19.06.2014
//
//-----------------------------------------------------------------------------

import "fnsRegDecAl_RP.mac";

private class (TRegDecision_RP) TRPO
  InitTRegDecision_RP();

  var СумВзыск : string = "",
      Обстоят  : string = "";

  // Для переопределения в классах конкретных видов решений
  macro GetDocTypeName : string
    return "решение о приостановлении операций";
  end;

  private macro SaveFieldsRlsSpecific
  ( fieldname : string, 
    fieldvalue : string, 
    blockname : string
  )
    // вызов метода родительского класса TRegDecision_RP
    SaveFieldsRlsSpecific(fieldname, fieldvalue, blockname);

    // сохранение полей релиза RPO
    if(fieldname == "НомРешПр")
      DecisionNumber = fieldvalue;
    elif(fieldname == "ДатаРешПр")
      DecisionDate = fieldvalue;
    elif(fieldname == "СумВзыск")
      СумВзыск = fieldvalue;
    elif(fieldname == "Обстоят")
      Обстоят = fieldvalue;
    end;
  end;

  private macro FillRgdBuf(wlmes : StrucRef, wlregdec : StrucRef)
    // Вызов метода родительского класса TRegDecision_RP
    FillRgdBuf(wlmes, wlregdec);

    // Специфика RPO
    wlregdec.Type = WLD_TYPE_REGDEC_RPO;
    if(СумВзыск)
      wlregdec.Amount = money(СумВзыск);
    end;
    wlregdec.Ground = GetWlcodeDescription(TYPE_CODE_FNS_RPO, КодОснов);
    wlregdec.Description = Обстоят; 
  end;
end;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2, "Генерация учетного объекта по сообщению RPO");

  var RPO : TRPO = TRPO();

  return GenRegDecision(wlmes, RPO);

  OnError(er)
    ExeptionMessage(er);
    return FALSE;
end;
