/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация объекта по сообщениям SWIFT MT544, MT546                       */
/*                                                                          */
/*  Имя файла: swgd544.mac                                                  */
/*                                                                          */
/****************************************************************************/

import DPInter, CTInter, "swgendoc.mac", "cb_sql.mac", oralib, likepy, "swgnrdoc.mac";

private const FICK_NADC = 14;

var msginf : RsbDepoInfoMessage; 
var msgLink, msgParty;
var CurrentBlock = "";
var Blocks = Stack;
var ReceiverReferenceID = 0;
var NumState = 0;

private macro ClearLink()
  msgLink.LinkageType = 20; // BEFO
  msgLink.LinkedMessageType = 0;
  msgLink.SenderReference = "";
  msgLink.ReceiverReference = "";
end;

macro NewLink()
    if ( valtype(msgLink)==V_UNDEF )
        msgLink = RsbDepoLinkedMessage;
    end;
    return true;
end;

macro AddLink()
  var stat = true;
    // Сохраняем ссылку только если она парная, т.е. заполнена номерами по нашему журналу и журналу отправителя
    if ( (msgLink.ReceiverReference!="") and
         (msgLink.SenderReference!="") )
       stat = msginf.AddLinkedMessage(msgLink);
       ClearLink();
    end;
    return stat;
end;

macro NewParty100()
    msgParty = RsbDepoMessageParty;
    msgParty.PartyType = DPMSGPAT_SECURITIES;
    return true;
end;

macro NewParty120()
    msgParty = RsbDepoMessageParty;
    msgParty.PartyType = DPMSGPAT_OTHER;
    return true;
end;

macro AddParty()
    return msginf.AddMessageParty(msgParty);
end;

macro NewStat()
  var stat = true;
    // Если в _предыдущем_ блоке LINKS считали ссылку и она не парная - сохранить ее
    if ( (msgLink.ReceiverReference!="") or
         (msgLink.SenderReference!="") )
       stat = msginf.AddLinkedMessage(msgLink);
       ClearLink();
    end;

  NumState = NumState + 1;
  return stat;
end;

macro Fill16R( newBlock )
   if ( CurrentBlock!="" )
      Blocks.Add(CurrentBlock);
   end;
   CurrentBlock = newBlock;
   return true;
end;

macro Fill16S( nameBlock )
   CurrentBlock = Blocks.Get();
   return true;
end;

macro Fill20C( Qualifer, Reference )
   if ( CurrentBlock==GENL_Block )
      msginf.SenderReference = Reference;
   elif ( CurrentBlock==LINK_Block )
      if ( Qualifer=="RELA" )
         msgLink.ReceiverReference = Reference;
      elif ( Qualifer=="PREV" )
         msgLink.SenderReference = Reference;
      end;
   end;
   return true;
end;

macro Fill23G( Code, value )
   var error;

   msginf.messageFunction = GetllValueElement(OBJTYPE_MSGFUN, Code, error);
   if ( error )
      return false;
   end;

   if(value != "")
     msginf.messageSubFunction = GetllValueElement(OBJTYPE_MSGSFUN, value, error);  
     if ( error )
        return false;
     end;
   end;

   return true;
end;

macro Fill25D(Code, value)
  var StateKind = 0, State = 0, error = 0;
  StateKind = GetllValueElement(OBJTYPE_MSGSTATKIND, Code, error); 
  if ( error )
    return false;
  end;
  State = GetllValueElement(OBJTYPE_MSGSTAT, substr(value, index(value,SYMB_SLASH) + 1), error);
  if ( error )
    return false;
  end;

  if( NumState == 1 )
    msginf.StateKind1 = StateKind;
    msginf.State1 = State;
  elif( NumState == 2 )
    msginf.StateKind2 = StateKind;
    msginf.State2 = State;
  elif( NumState == 3 )
    msginf.StateKind3 = StateKind;
    msginf.State3 = State;
  end; 
  return true;
end;

macro Fill24B(Code, value)
  var Reas, error;

  Reas = GetllValueElement(OBJTYPE_MSGREASON, value, error);
  if ( error )
    return false;
  end;

  if( NumState == 1 )
    msginf.Reason1 = Reas;
    if(Reas == OBJTYPE_MSGREASON_CANI)
      if(msginf.Reason != "")
        msginf.Reason = "Отмена поручения осуществляется по поручению депонента, " + msginf.Reason;
      else
        msginf.Reason = "Отмена поручения осуществляется по поручению депонента";
      end;
    elif((msginf.StateKind1 == OBJTYPE_MSGSTATKIND_MTCH) AND (msginf.State1 == OBJTYPE_MSGSTAT_NMAT) AND (Reas == OBJTYPE_MSGREASON_NARR))
      if(msginf.Reason != "")
        msginf.Reason = "Не совпали параметры квитуемых поручений, " + msginf.Reason;
      else
        msginf.Reason = "Не совпали параметры квитуемых поручений";
      end;
    elif((msginf.StateKind1 == OBJTYPE_MSGSTATKIND_MTCH) AND (msginf.State1 == OBJTYPE_MSGSTAT_NMAT) AND (Reas == OBJTYPE_MSGREASON_CMIS))
      if(msginf.Reason != "")
        msginf.Reason = "Отсутствует встречное поручение депо, " + msginf.Reason;
      else
        msginf.Reason = "Отсутствует встречное поручение депо";
      end;
    end;
  elif( NumState == 2 )
    msginf.Reason2 = Reas;
    if((msginf.StateKind2 == OBJTYPE_MSGSTATKIND_IPRC) AND (msginf.State2 == OBJTYPE_MSGSTAT_CAND) AND (Reas == OBJTYPE_MSGREASON_NARR))
      if(msginf.Reason != "")
        msginf.Reason = "Поручение депо отменено, " + msginf.Reason;
      else
        msginf.Reason = "Поручение депо отменено";
      end;
    end;
  elif( NumState == 3 )
    msginf.Reason3 = Reas;
    if((msginf.StateKind3 == OBJTYPE_MSGSTATKIND_SETT) AND (msginf.State3 == OBJTYPE_MSGSTAT_PENF) AND (Reas == OBJTYPE_MSGREASON_NARR))
      if(msginf.Reason != "")
        msginf.Reason = "Поручение депо отменено, " + msginf.Reason;
      else
        msginf.Reason = "Поручение депо отменено";
      end;
    end;
  end;

  return true;
end;

macro Fill98AC( Qualifer, RDate, RTime )
   if ( CurrentBlock==GENL_Block )
      msginf.PreparationDate = RDate;
      msginf.PreparationTime = RTime;
   elif ( CurrentBlock==SETTRAN_Block )
      msginf.EffectiveSettlementDate = RDate;
      msginf.EffectiveSettlementTime = RTime;
   end;
   return true;
end;

macro Fill98B( Qualifer, Scheme, DateCode )
   /*АХЗ*/
   return true;
end;

macro Fill22FH( Qualifier, Scheme, Indicator )   
   if ( Qualifier=="SETR" )          
     if( Indicator == "TRAD")
       msginf.IsClearing = true;
     end;
   elif( Qualifier=="REDE" )
     if( Indicator == "DELI")
       msginf.IsWritingOff = true;
     end;
   end;
   return true;
end;

macro Fill13A( Qualifier, Number )
   if(Qualifier == "LINK")
     MsgLink.LinkedMessageType = Number;
   end;
   return true;
end;

macro Fill13B( Qualifier, Number )
   return true;
end;

macro DeleteBreak( str )
  var ind = index(str,SYMB_ENDL);
  while( ind )
     str = substr(str,1,ind-1)+substr(str,ind+1);
     ind = index(str,SYMB_ENDL);
  end;
  SetParm(0,str);
end;

macro ПолучитьТипКода(shrtname: string)

   var rs:object = execSQLselect("SELECT t_codekind FROM dobjkcode_dbt WHERE t_shortname= \'"+shrtname +"\';");
   if( rs AND rs.moveNext() )
      return rs.value(0);
   end;
  
   return FICK_USERCODE;
end;

macro Fill35B( ISIN, Description )   
   var error;
   

   /*Если удается найти код анкеты по ISIN, ищем по ISIN, иначе ищем по коду у контрагента */
   if( ISIN != "" )
      msginf.SecurityID = ПолучитьКодФинИн( trim(ISIN), error, FICK_ISIN );
   else
     if ( index(Description,"CORP/NADC/") )
        Description = substr( Description, index(Description,"CORP/NADC/")+10 );
        
        msginf.SecurityCodeKind = FICK_NADC;  
        
        if ( index(Description,"\n") )
           msginf.SecurityCode = substr( Description, 1, index(Description,"\n")-1 );
        else
           msginf.SecurityCode = Description;
        end;

        msginf.SecurityID = ПолучитьКодФинИн( msginf.SecurityCode, error, msginf.SecurityCodeKind );
     else
       return false;
     end;
   end;

   if(error) 
      return false;
   end;

   return true;
end;

macro Fill36B( Qualifier, Code, Sum )
      if ( Qualifier=="SETT" )
          msginf.SettledQuantity = moneyl(Sum);
      end;
   return true;
end;

macro Fill97A( Qualifier, Account )
  if ( CurrentBlock==SETTRAN_Block )
     if ( index(Account,"/KRZD/") )
        msginf.Account = substr( Account, 1, index(Account,"/KRZD/")-1 );
        msginf.Partition = substr( Account, index(Account,"/KRZD/")+strlen("/KRZD/") );
     else
        msginf.Account = Account;
     end;      
  else
    if ( index(Account,"/KRZD/") )
       msgParty.Account = substr( Account, 1, index(Account,"/KRZD/")-1 );
       msgParty.Partition = substr( Account, index(Account,"/KRZD/")+strlen("/KRZD/") );
    else
       msgParty.Account = Account;
    end;
  end;
  return true;
end;

macro Fill97B( Qualifier, Scheme, CodeType, Account )
   return Fill97A(Qualifier,Account);
end;

macro Fill95C(Qualifier, CountryCode)
   return true;
end;

macro Fill95C1(Qualifier, CountryCode)
   NewParty120();
   if( not Fill95C(Qualifier, CountryCode))
     return false;
   end;
   return AddParty();
end;

macro FillPartyKind( Qualifier )
   var error;
   msgParty.PartyKind = GetllValueElement(OBJTYPE_MSGPARTYTP, Qualifier, error);
   if ( error )
      return false;
   end;

   if ( CurrentBlock==SETPRTY_Block )      
      if ( (msgParty.PartyKind==OBJTYPE_MSGPATYTP_EXCH) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MEOR) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_MERE) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_TRRE) OR
           (msgParty.PartyKind==OBJTYPE_MSGPATYTP_INVE) )
         return false;
      end;
   elif( CurrentBlock == CSHPRTY_Block )
      if(  (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_ACCW) OR
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_BENM) OR
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_PAYE) )
         return false;
      end;
   else
      if ( (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_EXCH) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_MEOR) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_MERE) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_TRRE) AND
           (msgParty.PartyKind!=OBJTYPE_MSGPATYTP_INVE) )
         return false;
      end;
   end;
   return true;
end;

macro Fill95P(Qualifier, BIC)
   var error;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( (CurrentBlock==SETPRTY_Block) OR (CurrentBlock==OTHRPRTY_Block) OR (CurrentBlock == CSHPRTY_Block) )
      if ( not FillPartyKind(Qualifier) )
         return false;
      end;
      BIC = StrSubst(BIC,"XXX","");
      msgParty.PartyCode = BIC;
      msgParty.PartyCodeKind = PTCK_SWIFT;
      msgParty.PartyID = ПолучитьКодСубъекта( msgParty.PartyCode, msgParty.PartyCodeKind, error );
      if ( error )
         return false;
      end;
      select = "select t_Name from dparty_dbt where t_PartyID=:PartyID";
      params = makeArray( SQLParam("PartyID",msgParty.PartyID ));
      rs = execSQLselect( select, params, FALSE );
      if ( rs.MoveNext() )
         msgParty.PartyName = rs.value(0);
      end;
   end;
   return true;
end;

macro Fill95P1(Qualifier, BIC)
   NewParty120();
   if( not Fill95P(Qualifier, BIC))
     return false;
   end;
   return AddParty();
end;

macro Fill95R(Qualifier, Scheme, PropCode)
   return true;
end;

macro Fill95R1(Qualifier, Scheme, PropCode)
   NewParty120();
   if( not Fill95R(Qualifier, Scheme, PropCode))
     return false;
   end;
   return AddParty();
end;

macro Fill95Q(Qualifier, Name)
   var error;
  var rs:object;
  var select:string;
  var params:TArray;
   var pid;

   if ( (CurrentBlock==SETPRTY_Block) OR (CurrentBlock==OTHRPRTY_Block) )
      if ( not FillPartyKind(Qualifier) )
         return false;
      end;
      msgParty.PartyID = -1;      
      msgParty.PartyCodeKind = PTCK_CONTR;
      DeleteBreak(Name);
      if ( Index(Name, "XX/CORP/") )
         Name = substr( Name, Index(Name, "XX/CORP/")+strlen("XX/CORP/") );
         if ( not Index(Name, "/") )
            return false;
         end;
         
         msgParty.DepoPartyCodeKind = ПолучитьТипКода( substr( Name, 1, index(Name,"/")-1 ) );
         msgParty.DepoPartyCode = substr( Name, Index(Name, "/")+1 );
         
         pid = ПолучитьКодСубъекта( msgParty.DepoPartyCode, msgParty.DepoPartyCodeKind, error );
         if ((pid != "") and (not error) )
            msgParty.PartyID = pid;
            msgParty.PartyCodeKind = PTCK_SWIFT;
            msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
            
            select = "select t_Name from dparty_dbt where t_PartyID=:tpid";
            params = makeArray( SQLParam("tpid", pid));
            rs = execSQLselect( select, params, FALSE );
            if ( rs and rs.MoveNext() )
               msgParty.PartyName=rs.value(0);
            else
               msgParty.PartyName = "Не найден";
            end;
            
            if ( error )
               msgParty.PartyCodeKind = PTCK_CONTR;
               msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
                             
               if ( error )
                  msgParty.PartyCode = "";
                  msgParty.PartyName = "Не найден";
               end;
            end;
         end;
      else
         StrRestoreStandart(Name,msgParty.PartyName, ST_RUR5);
         select = "select t_PartyID from dparty_dbt where t_Name=:Name or t_ShortName=:Name";
         params = makeArray( SQLParam("Name",GetSQLString(Name)));
         rs = execSQLselect( select, params, FALSE );
         if ( rs.MoveNext() )
            msgParty.PartyID=rs.value(0);
            msgParty.PartyCodeKind = PTCK_SWIFT;
            msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
            if ( error )
               msgParty.PartyCodeKind = PTCK_CONTR;
               msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
               if ( error )
                  msgParty.PartyCode = "";
               end;
            end;
         end;
      end;
   elif( CurrentBlock == CSHPRTY_Block )        
      if ( not FillPartyKind(Qualifier) )
         return false;
      end;
      msgParty.PartyCode = Name;
      msgParty.PartyCodeKind = PTCK_BIC;
      msgParty.PartyID = ПолучитьКодСубъекта( msgParty.PartyCode, msgParty.PartyCodeKind, error );
      if ( error )
         return false;
      end;
      select = "select t_Name from dparty_dbt where t_PartyID=:PartyID";
      params = makeArray( SQLParam("PartyID",msgParty.PartyID ));
      rs = execSQLselect( select, params, FALSE );
      if ( rs.MoveNext() )
         msgParty.PartyName = rs.value(0);
      end;
   end;
   return true;
end;

macro Fill95Q1(Qualifier, Name)
   NewParty120();
   if( not Fill95Q(Qualifier, Name))
     return false;
   end;
   return AddParty();
end;

macro Fill95S(Qualifier, Scheme, TypeID, CountryCode, AlternateID)
   return true;
end;

macro Fill70a( Qualifier, Narrative )
   var count, msgDoc, docMstr, code, strval, continue0, RegDate, Name, Comment, str, reason = "", error = 0;
   array dest;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( (CurrentBlock==SETTRAN_Block) and (Qualifier=="SPRO") )      
      if(index(Narrative,"/REGD/"))
        ReadDateYYYYMMDD( Narrative, index(Narrative,"/REGD/") + 6, msginf.RedisterDate );
        ReadTimeHHMMSS( Narrative, index(Narrative,"/REGD/") + 6 + 8, msginf.RedisterTime );
      end;
      if(index(Narrative,"/STMD/"))
        ReadDateYYYYMMDD( Narrative, index(Narrative,"/STMD/") + 6, msginf.SettlementDate );
        ReadTimeHHMMSS( Narrative, index(Narrative,"/STMD/") + 6 + 8, msginf.SettlementTime );        
      end;
      if(index(Narrative,"/ENDD/"))
        ReadDateYYYYMMDD( Narrative, index(Narrative,"/ENDD/") + 6, msginf.LateDeliveryDate );
        ReadTimeHHMMSS( Narrative, index(Narrative,"/ENDD/") + 6 + 8, msginf.LateDeliveryTime );        
      end;

     РазбитьНаСтроки( Narrative, dest, 35 );
     count = 1;  /* Первую строку пропускаем */
     Name = "";
     while( count<=Asize( dest ) )
      if(index(dest(count),"/INIT/"))
        msgParty = RsbDepoMessageParty;
        msgParty.PartyType = 40;
        msgParty.DepoPartyCodeKind = PTCK_NDC.
        msgParty.PartyCodeKind = PTCK_SWIFT;

        if(index(dest(count),"/INIT/XX/CORP/NADC/"))
          if(index(str,SYMB_SLASH))
            msgParty.DepoPartyCode = substr(dest(count),index(dest(count),"/INIT/XX/CORP/NADC/") + 19);
            msgParty.PartyID = ПолучитьКодСубъекта( msgParty.DepoPartyCode, msgParty.DepoPartyCodeKind, error );
            msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
          else
            msgParty.DepoPartyCode = str;
          end;
        else
          msgParty.PartyCode = substr(Narrative,index(dest(count),"/INIT/") + 6);
          msgParty.PartyID = ПолучитьКодСубъекта( msgParty.PartyCode, msgParty.PartyCodeKind, error );
          msgParty.DepoPartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.DepoPartyCodeKind, error );
        end;
        select = "select t_Name from dparty_dbt where t_PartyID=:id";
        params = makeArray( SQLParam("id", msgParty.PartyID));
        rs = execSQLselect( select, params, FALSE );
        if ( rs and rs.MoveNext() )
           msgParty.PartyName=rs.value(0);
        end;
        msginf.AddMessageParty(msgParty);
      end;
      if(index(dest(count),"/TRNC/"))
        str = substr(dest(count),index(dest(count),"/TRNC/")+6);
        if(index(str,SYMB_SLASH))
          code = substr(str,1,index(str,SYMB_SLASH)-1);
          str = substr(str,index(str,SYMB_SLASH)+1);
          if( index(str,index(str,SYMB_SLASH)) != 5 )
            code = code + "/" + substr(str,1,index(str,SYMB_SLASH)-1);
          end;
          msginf.ProcessingCode = code;
        else
          msginf.ProcessingCode = str;
        end;
      end;
      count = count + 1;
     end;
      
      return true;
   elif (Qualifier=="REAS") 
     if((msginf.StateKind1 = 10 ) and ( msginf.State1 = 10 ))
       if(index(Narrative,"/REGD/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/REGD/") + 6, msginf.CancelRedisterDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/REGD/") + 6 + 8, msginf.CancelRedisterTime );
       end;
       if(index(Narrative,"/ORDD/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/ORDD/") + 6, msginf.CancelCreateDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/ORDD/") + 6 + 8, msginf.CancelCreateTime );
       end;
       if(index(Narrative,"/DAEX/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/DAEX/") + 6, msginf.CancelAcceptDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/DAEX/") + 6 + 8, msginf.CancelAcceptTime );
       end;
     else
       if(index(Narrative,"/REGD/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/REGD/") + 6, msginf.RedisterDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/REGD/") + 6 + 8, msginf.RedisterTime );
       end;
       if(index(Narrative,"/ORDD/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/ORDD/") + 6, msginf.CreateDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/ORDD/") + 6 + 8, msginf.CreateTime );
       end;
       if(index(Narrative,"/DAEX/"))
         ReadDateYYYYMMDD( Narrative, index(Narrative,"/DAEX/") + 6, msginf.AcceptDate  );
         ReadTimeHHMMSS( Narrative, index(Narrative,"/DAEX/") + 6 + 8, msginf.AcceptTime );
       end;
     end;

     if(index(Narrative,"/STMD/"))
       ReadDateYYYYMMDD( Narrative, index(Narrative,"/STMD/") + 6, msginf.SettlementDate );
       ReadTimeHHMMSS( Narrative, index(Narrative,"/STMD/") + 6 + 8, msginf.SettlementTime );        
     end;
     if(index(Narrative,"/ENDD/"))
       ReadDateYYYYMMDD( Narrative, index(Narrative,"/ENDD/") + 6, msginf.LateDeliveryDate );
       ReadTimeHHMMSS( Narrative, index(Narrative,"/ENDD/") + 6 + 8, msginf.LateDeliveryTime );        
     end;

     if(index(Narrative,"/TRND/"))
       ReadDateYYYYMMDD( Narrative, index(Narrative,"/TRND/") + 6, msginf.ProcessingDate  );
       ReadTimeHHMMSS( Narrative, index(Narrative,"/TRND/") + 6 + 8, msginf.ProcessingTime );        
     end;

     if(index(Narrative,"/REJD/"))
       ReadDateYYYYMMDD( Narrative, index(Narrative,"/REJD/") + 6, msginf.RejectDate  );
       ReadTimeHHMMSS( Narrative, index(Narrative,"/REJD/") + 6 + 8, msginf.RejectTime );        
     end;

     РазбитьНаСтроки( Narrative, dest, 35 );
     count = 1;  /* Первую строку пропускаем */
     Name = "";
     while( count<=Asize( dest ) )
       if(count == 1)
         if(index(dest(count),"XX/CORP/NADC/"))
           count = count + 1; 
           if(substr(dest(count),1,6) == "/NAME/")
             reason = substr(dest(count),7);
           end;
         else

         end;
       else
         if(substr(dest(count),1,2)=="//")
           reason = reason + substr(dest(count),3);
         else

          if(index(dest(count),"/INIT/"))
            msgParty = RsbDepoMessageParty;
            msgParty.PartyType = 40;
            msgParty.DepoPartyCodeKind = PTCK_NDC.
            msgParty.PartyCodeKind = PTCK_SWIFT;

            if(index(dest(count),"/INIT/XX/CORP/NADC/"))
              if(index(str,SYMB_SLASH))
                msgParty.DepoPartyCode = substr(dest(count),index(dest(count),"/INIT/XX/CORP/NADC/") + 19);
                msgParty.PartyID = ПолучитьКодСубъекта( msgParty.DepoPartyCode, msgParty.DepoPartyCodeKind, error );
                msgParty.PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.PartyCodeKind, error );
              else
                msgParty.DepoPartyCode = str;
              end;
            else
              msgParty.PartyCode = substr(Narrative,index(dest(count),"/INIT/") + 6);
              msgParty.PartyID = ПолучитьКодСубъекта( msgParty.PartyCode, msgParty.PartyCodeKind, error );
              msgParty.DepoPartyCode = ПолучитьКодСубъекта( msgParty.PartyID, msgParty.DepoPartyCodeKind, error );
            end;
            select = "select t_Name from dparty_dbt where t_PartyID=:id";
            params = makeArray( SQLParam("id", msgParty.PartyID));
            rs = execSQLselect( select, params, FALSE );
            if ( rs and rs.MoveNext() )
               msgParty.PartyName=rs.value(0);
            end;
            msginf.AddMessageParty(msgParty);
          end;
                 
          if(index(dest(count),"/TRNC/"))
            str = substr(dest(count),index(dest(count),"/TRNC/")+6);
            if(index(str,SYMB_SLASH))
              code = substr(str,1,index(str,SYMB_SLASH)-1);
              str = substr(str,index(str,SYMB_SLASH)+1);
              if( index(str,index(str,SYMB_SLASH)) != 5 )
                code = code + "/" + substr(str,1,index(str,SYMB_SLASH)-1);
              end;
              msginf.ProcessingCode = code;
            else
              msginf.ProcessingCode = str;
            end;
          end;

          if(index(dest(count),"/TRNN/"))
            str = substr(dest(count),index(dest(count),"/TRNN/")+6);
            if(index(str,SYMB_SLASH))
              msginf.ProcessingCode = substr(dest(count),1,index(dest(count),SYMB_SLASH)-1);;
            else
              msginf.ProcessingCode = str;
            end;
          end;

         end;      

       end;
      count = count + 1;
     end;
     return true;
   end;
   return true;
end;

macro Fill19A(Qualifier, Sign, Cur, Sum)
   if ( CurrentBlock==AMT_Block )
      if ( Qualifier=="ESTT" )
         if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
           std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
           return FALSE;
         end;
         msginf.SettlementAmountCurrency = wlfininstr.FIID;
         msginf.SettlementAmount = moneyl(Sum);
      elif ( Qualifier=="DEAL" )
         if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
           std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
           return FALSE;
         end;
         msginf.DealAmountCurrency = wlfininstr.FIID;
         msginf.DealAmount = moneyl(Sum);
      end;
   end;
   return true;
end;

/* Заполнение списка полей формы  MT548*/
var FldGENLR    = ТПолеФормы( "16R:GENL",     FIELD_MANDATORY, "Read16",   "Fill16R",  ""                     ),
    Fld20C      = ТПолеФормы( "20C",          FIELD_MANDATORY, "Read20C",  "Fill20C",  ""                     ),
    Fld23G      = ТПолеФормы( "23G",          FIELD_MANDATORY, "ReadCode", "Fill23G",  ""                     ),
    Fld98A      = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98C      = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),        
    FldLINKR    = ТПолеФормы( "16R:LINK",     FIELD_MANDATORY, "Read16",   "Fill16R",  "NewLink"              ),
    Fld13A      = ТПолеФормы( "13A",          FIELD_OPTIONAL,  "Read13A",  "Fill13A",  ""                     ),
    Fld13B      = ТПолеФормы( "13B",          FIELD_OPTIONAL,  "Read13B",  "Fill13B",  ""                     ),        
    Fld20C2     = ТПолеФормы( "20C",          FIELD_MANDATORY, "Read20C",  "Fill20C",  ""                     ),
    FldLINKS    = ТПолеФормы( "16S:LINK",     FIELD_MANDATORY, "Read16",   "Fill16S",  "AddLink",    FldLINKR ),
    FldSTATR    = ТПолеФормы( "16R:STAT",     FIELD_MANDATORY, "Read16",   "Fill16R",  "NewStat"              ),
    Fld25D      = ТПолеФормы( "25D",          FIELD_MANDATORY, "Read20C",  "Fill25D",  ""                     ),
    FldREASR    = ТПолеФормы( "16R:REAS",     FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld24B      = ТПолеФормы( "24B",          FIELD_OPTIONAL,  "Read20C",  "Fill24B",  ""                     ),
    Fld70D      = ТПолеФормы( "70D",          FIELD_OPTIONAL,  "Read70D",  "Fill70a",  ""                     ),
    FldREASS    = ТПолеФормы( "16S:REAS",     FIELD_OPTIONAL,  "Read16",   "Fill16S",  ""                     ),
    FldSTATS    = ТПолеФормы( "16S:STAT",     FIELD_MANDATORY, "Read16",   "Fill16S",  "",           FldSTATR ),
    FldGENLS    = ТПолеФормы( "16S:GENL",     FIELD_MANDATORY, "Read16",   "Fill16S",  ""                     ),
    FldSETTRANR = ТПолеФормы( "16R:SETTRAN",  FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld35B      = ТПолеФормы( "35B",          FIELD_OPTIONAL,  "Read35B",  "Fill35B",  ""                     ),
    Fld36B      = ТПолеФормы( "36B",          FIELD_OPTIONAL,  "Read36B",  "Fill36B",  ""                     ),
    Fld97A      = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  ""                     ),
    Fld97B      = ТПолеФормы( "97B",          FIELD_OPTIONAL,  "Read97B",  "Fill97B",  ""                     ),
    Fld22F      = ТПолеФормы( "22F",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",            THISFLD ),
    Fld22H      = ТПолеФормы( "22H",          FIELD_OPTIONAL,  "Read22FH", "Fill22FH", "",            Fld22F  ),
    Fld98A2     = ТПолеФормы( "98A",          FIELD_OPTIONAL,  "Read98A",  "Fill98AC", ""                     ),
    Fld98B2     = ТПолеФормы( "98B",          FIELD_OPTIONAL,  "Read98B",  "Fill98B",  ""                     ),
    Fld98C2     = ТПолеФормы( "98C",          FIELD_OPTIONAL,  "Read98C",  "Fill98AC", ""                     ),
    Fld70E      = ТПолеФормы( "70E",          FIELD_OPTIONAL,  "Read70E",  "Fill70a",  ""                     ),
    FldSETPRTYR = ТПолеФормы( "16R:SETPRTY",  FIELD_OPTIONAL,  "Read16",   "Fill16R",  "NewParty100"          ),
    Fld95P      = ТПолеФормы( "95P",          FIELD_OPTIONAL,  "Read95P",  "Fill95P",  ""                     ),    
    Fld95R      = ТПолеФормы( "95R",          FIELD_OPTIONAL,  "Read95R",  "Fill95R",  ""                     ),    
    Fld95Q      = ТПолеФормы( "95Q",          FIELD_OPTIONAL,  "Read95Q",  "Fill95Q",  ""                     ),
    Fld97A2     = ТПолеФормы( "97A",          FIELD_OPTIONAL,  "Read97A",  "Fill97A",  ""                     ),
    Fld97B2     = ТПолеФормы( "97B",          FIELD_OPTIONAL,  "Read97B",  "Fill97B",  ""                     ),
    FldSETPRTYS = ТПолеФормы( "16S:SETPRTY",  FIELD_OPTIONAL,  "Read16",   "Fill16S",  "AddParty", FldSETPRTYR),
    FldSETTRANS = ТПолеФормы( "16S:SETTRAN",  FIELD_OPTIONAL,  "Read16",   "Fill16S",  ""                     ),
    FldADDINFOR = ТПолеФормы( "16R:ADDINFO",  FIELD_OPTIONAL,  "Read16",   "Fill16R",  ""                     ),
    Fld95C2     = ТПолеФормы( "95C",          FIELD_OPTIONAL,  "Read95C",  "Fill95C1", "",            THISFLD ),    
    Fld95P2     = ТПолеФормы( "95P",          FIELD_OPTIONAL,  "Read95P",  "Fill95P1", "",            Fld95C2 ),    
    Fld95R2     = ТПолеФормы( "95R",          FIELD_OPTIONAL,  "Read95R",  "Fill95R1", "",            Fld95C2 ),    
    Fld95Q2     = ТПолеФормы( "95Q",          FIELD_OPTIONAL,  "Read95Q",  "Fill95Q1", "",            Fld95C2 ),
    FldOTHRPRTYS= ТПолеФормы( "16S:ADDINFO",  FIELD_OPTIONAL,  "Read16",   "Fill16S",  ""                     );

macro ConstructMT548( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode, repeatFlag = 0;
  var rs:object;
  var select:string;
  var params:TArray;

  msginf.MessageStandart  = 1;   /* SWIFT    */
  msginf.MessageDirection = 1;   /* входящее */
  msginf.MessageType      = 548; /* MT548    */

  msginf.SenderCodeKind = PTCK_SWIFT;
  msginf.SenderID = RespID;
  msginf.SenderCode = ПолучитьКодСубъекта( msginf.SenderID, msginf.SenderCodeKind, error );
  if ( error )
     std.msg( "Не найден SWIFT-код отправителя сообщения" );
     return false;
  end;
  select = "select t_ShortName from dparty_dbt where t_PartyID=:SenderID";
  params = makeArray( SQLParam("SenderID", msginf.SenderID));
  rs = execSQLselect( select, params, FALSE );

  if ( rs.MoveNext() )
     msginf.SenderName = rs.value(0);
  end;

  msginf.ReceiverCodeKind = PTCK_SWIFT;
  msginf.ReceiverID = {OurBank};
  msginf.ReceiverCode = ПолучитьКодСубъекта( {OurBank}, msginf.ReceiverCodeKind, error );
  if ( error )
     std.msg( "Не найден SWIFT-код получателя сообщения" );
     return false;
  end;
  select = "select t_ShortName from dparty_dbt where t_PartyID=:SeId";
  params = makeArray( SQLParam("SeId", {OurBank}));
  rs = execSQLselect( select, params, FALSE ); 
  if ( rs.MoveNext() )
     msginf.ReceiverName = rs.value(0);
  end; 

  msginf.TradePlaceType      = 10; //биржа
  msginf.TradePlaceCodeKind  = 24; //константы нет :(
  msginf.TradePlaceCode      = "MICEX";
  select = "select t_ObjectID from dobjcode_dbt where t_code = :code and t_codekind = :codekind and t_ObjectType = " + OBJTYPE_PARTY;
  params = makeArray( SQLParam("code", msginf.TradePlaceCode),
                      SQLParam("codekind", msginf.TradePlaceCodeKind));
  rs = execSQLselect( select, params, FALSE ); 
  if ( rs.MoveNext() )
    msgInf.TradePlaceID      = rs.value(0);
    select = "select t_Name from dparty_dbt where t_PartyID=:TradeId";
    params = makeArray( SQLParam("TradeId", msgInf.TradePlaceID));
    rs = execSQLselect( select, params, FALSE ); 
    if ( rs.MoveNext() )
      msginf.TradePlaceName    = rs.value(0);
    end;  
  end; 


  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )    
    fld = ПоляФормы.Value(count);        
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if ( (field_name==StartOfBlock) OR (field_name==EndOfBlock) )
        field_name = field_name + ":" + field_value;
      end;

      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно and (not RepeatFlag) )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          wasRead = 0;
        end;
      else
        repeatFlag = 0;
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            wasRead = 1;
            if ( fld.repeatWith>=0 )
                repeatFlag = 1;
                count = fld.repeatWith-1;
             end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;      
  end;

  if( result == FALSE )
    return result;
  end;

  msginf.State            = DPMSGINF_STATE_PREP; 

  return TRUE;
end; /* ConstructMT544 */

private macro GenDocEx( addrMes )
  SetBuff( wlmes, addrMes );  

  PrintLog(2,"Генерация уч. объекта по МТ548" );

  if( not ConstructMT548( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if ( not msginf.Update() )
     /*RestoreReference(ReceiverReferenceID, msginf.ReceiverReference);*/
     std.msg("Ошибка при сохранении информации");
     return FALSE;
  end;

  if ( not ПривязатьОбъектДепоКСообщению(msginf.MessageID) )
     /*RestoreReference(ReceiverReferenceID, msginf.ReceiverReference);*/
     std.msg("Ошибка при создании связи с сообщением");
     return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenDoc( addrMes )
  var stat = false;
  msginf = RsbDepoInfoMessage(0, 4/*DEPOMSG_INFKIND_NOTIFIC*/);
    stat = GenDocEx( addrMes );
  msginf = null;
  return stat;
end;
