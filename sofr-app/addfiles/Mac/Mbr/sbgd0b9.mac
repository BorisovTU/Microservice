/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SBRF3 сигнатура 0B1                     */
/*                                                                          */
/*  Имя файла: sbgd0b9.mac                                                  */
/*  Создан:    07.02.03                                       Бабин А.П.    */
/****************************************************************************/

import "sbgendoc.mac";

FILE SB_Corschem( corschem ) key 4;

/* чтение даты из строки */
macro СчитатьДату(Строка)
  var День, Месяц, Год;

  День  = int( substr( Строка, 1, 2 ) );
  Месяц = int( substr( Строка, 4, 2 ) );
  Год   = int( substr( Строка, 7, 4 ) );

  return date( День, Месяц, Год );
end;

macro InsertDocExt(InitHead, chapter, field_value)

   if ( chapter>2  ) 
      return true;
   end;

   ClearRecord(wlconf);
   wlconf.Number     = trim(substr(field_value,16,6));
   wlconf.DateValue  = СчитатьДату( substr(field_value,140,10) );
   wlconf.Sum        = money( doublel(substr(field_value,80,18)) );   

   wlconf.RelatedRef =   substr(field_value,99,6) 
                       + substr(field_value,106,10) 
                       + substr(field_value,117,6);
  
   wlconf.Description = substr(field_value,154);
   wlconf.MessageType =  substr(field_value,8,2);
   if ( substr(field_value,76,1)=="0" )
     wlconf.DKFlag = WL_CREDIT;
   else
     wlconf.DKFlag = WL_DEBET;
   end;
   if ( wlconf.Number=="" )
     /* Такие документы делаем закрытыми */
     wlconf.State=70; 
   end;

   wlconf.TransferDate = wlconf.DateValue; //#118037
   
   if( not ВставитьДокументВыписки( wlconf ) )
     std.msg("Ошибка при сохранении документа выписки");
     return FALSE;
   end;

   return true;
end;

var oldChapter, PageNum;
macro ВставитьВыписку( InitHead, chapter )
   if ( oldChapter!=chapter )
        oldChapter = chapter;
        PageNum = PageNum+1;
        copy( wlhead, InitHead );
        wlhead.Page = PageNum;
        if( not ВставитьЗаголовокВыписки( wlhead ) )
          std.msg("Ошибка при сохранении заголовка выписки");
          return FALSE;
        end;
   end;
   return true;
end;

macro PlacePaymentInUnclosed(InitHead, field_value)
   var RelatedRef;

   RelatedRef =   substr(field_value,67,6) 
                + substr(field_value,74,10) 
                + substr(field_value,85,6);

   if ( WldFindPmByTRN_SUM_REF(false, RelatedRef, СчитатьДату( trim(substr(field_value,33,10)) ), 
                                 InitHead.FIID, -1, money( doublel(substr(field_value,48,18)) )) )
      if ( oldChapter==0 )
        /* Вставляем заголовок выписки (если еще выписок небыло) */
        if ( not ВставитьВыписку( InitHead, 3 ) )
           return false;
        end;
        /* И делаем ее сквитованной, так как документов у нее не будет */
        wlhead.State = 60;
        if( not ОбновитьЗаголовокВыписки( wlhead ) )
          std.msg("Ошибка при сохранении заголовка выписки");
          return FALSE;
        end;
      end;
      if ( not ПоместитьВНезавершенные(wlpm.WlPmID) )
         PrintLog( 0, string( "Ошибка при помещении в незавершенные WlPmID = ", wlpm.WlPmID) );
         PrintLog( 0, "Строка: " + field_value );
      end;
   else
      PrintLog( 0, "Ошибка при поиске платежа для помещения в незавершенные " );
      PrintLog( 0, "Строка: " + field_value );
   end;

   return true;
end;

macro GenDoc( addrMes )
   var field_name, field_value, chapter, Currency, error, sum, block;
   Record InitHead(wlhead);

   oldChapter = 0;
   PageNum = 0;

   SetBuff( wlmes, addrMes );
   ClearRecord(InitHead);

   while( СчитатьПоле(field_name, field_value, block) )

      if ( block=="" )
         chapter = 0;
      elif ( substr(block,1,1)=="A" )
         chapter = 1;
      elif ( substr(block,1,1)=="B" )
         chapter = 2;
      elif ( substr(block,1,1)=="C" )
         chapter = 3;
      elif ( substr(block,1,1)=="D" )
         chapter = 4;
      elif ( substr(block,1,1)=="E" )
         chapter = 5;
      else
         chapter = 6;
      end;

      if ( field_name=="VCODE" )
         if ( substr(field_value,1,3)!="019" )
            InitHead.SubKind = 1;
         end;
      elif ( field_name=="VNUM" )
         InitHead.Number = Trim(field_value);
      elif ( field_name=="CU" )
        if( not ПолучитьФинИнПоISO( field_value, wlfininstr ) )
          std.msg( "Не определена валюта сообщения по коду ISO: " + field_value );
          return FALSE;
        end;
        InitHead.FIID = wlfininstr.FIID;
      elif ( field_name=="DATEIN" )
        InitHead.DateIn = СчитатьДату(field_value);
        InitHead.DateOut = InitHead.DateIn;
        InitHead.Corschem     = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, InitHead.FIID, 1 );
        if( InitHead.Corschem == -1 )
          Currency = ПолучитьКодФинИн(InitHead.FIID, error);
          if ( error )
             Currency = "???";
          end;
          std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
          return FALSE;
        end;       
      elif ( field_name=="DATEOUT" )
        InitHead.DateOut = СчитатьДату(field_value);
      elif ( field_name=="ACC" )
        if ( chapter<3 )           
           InitHead.Account = field_value;

           ClearRecord(SB_Corschem);
           SB_Corschem.CorrID = wlmes.OutsideAbonentID;
           SB_Corschem.CorAccount = InitHead.Account;
           if ( GetEQ(SB_Corschem) )
               InitHead.Corschem = SB_Corschem.Number;
           else
              std.msg( "Не определена схема расчетов для респондента по счету " + wlhead.Account );
              return FALSE;
           end;

           if ( not ВставитьВыписку( InitHead, chapter )  )
              return false;
           end;
        end;
      elif ( field_name=="INBL" )
        if ( chapter<3 )
          if ( not ВставитьВыписку( InitHead, chapter )  )
            return false;
          end;
          sum = money( doublel(substr(field_value,3)) );
          if ( substr(field_value,1,1)!="0" )
             sum = -sum;
          end;
          wlhead.RestIn = sum;
        end;
      elif ( field_name=="DOC" )
        if ( (chapter==1) OR (chapter==2) )
           if ( not ВставитьВыписку( InitHead, chapter )  )
              return false;
           end;
           if ( not InsertDocExt(InitHead, chapter, field_value) )
              return false;
           end;
        elif ( chapter==3 )
           /* Помещаем соответсвующий платеж в незавершенные */
           if ( not PlacePaymentInUnclosed(InitHead, field_value) )
              return false;
           end;
        end;
      elif ( field_name=="DAM" )
        if ( chapter<3 )
           if ( not ВставитьВыписку( InitHead, chapter )  )
             return false;
           end;
           wlhead.CreditTurn = money( doublel(field_value) );
        end;
      elif ( field_name=="DNUM" )
        if ( chapter<3 )
           wlhead.CreditNumDoc = int(field_value);
        end;
      elif ( field_name=="KAM" )
        if ( chapter<3 )
           wlhead.DebetTurn = money( doublel(field_value) );
        end;
      elif ( field_name=="KNUM" )
        if ( chapter<3 )
           wlhead.DebetNumDoc = int(field_value);
           if( not ОбновитьЗаголовокВыписки( wlhead ) )
             std.msg("Ошибка при обновлении заголовка выписки");
             return FALSE;
           end;
        end;
      elif ( field_name=="OUTBL" )
        if ( (chapter-1)<3 )
          sum = money( doublel(substr(field_value,3)) );
          if ( substr(field_value,1,1)!="0" )
             sum = -sum;
          end;
          wlhead.RestOut = sum;
          if( not ОбновитьЗаголовокВыписки( wlhead ) )
             std.msg("Ошибка при обновлении заголовка выписки");
             return FALSE;
          end;
        end;
      elif ( field_name=="NUM" )
      end;
   end;

   return true;
end;
