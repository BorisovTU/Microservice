 /*
 $Name: ufgm243.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения ED243 транспорта УФЭБС
 */

/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED243 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm243.mac                                                  */
/*  Создан:    15.06.11                                                     */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac", 
       "wltools.mac", "rsberror.mac", "lib_str.mac", "uf243244.mac", 
       "uf243244genmes.mac";

private const VER_ST_2_5_9 : integer = 259;

private const code01 = "/УТ01/", 
              code02 = "/УТ02/", 
              code03 = "/УТ03/", 
              code04 = "/УТ04/", 
              code05 = "/УТ05/", 
              code06 = "/УТ06/", 
              code07 = "/УТ07/", 
              code08 = "/УТ08/",
              code09 = "/УТ09/",
              code10 = "/УТ10/",
              code11 = "/УТ11/",
              code12 = "/УТ12/",
              code13 = "/УТ13/",
              code14 = "/УТ14/",
              code15 = "/УТ15/",
              code16 = "/УТ16/",
              code99 = "/УТ99/";
 
private macro ParseQueries(Queries: string, EDDefineRequestCode: @string, EDDefineRequestInfo: TArray)
  var req_code : string = "";
  var code_index : integer = index_wlcode(Queries, TYPE_CODE_UFBS_ED243, @req_code);
  if(code_index)
    EDDefineRequestCode = substr(req_code, 4, 2);
  else
    RunError("Не найден код в сообщении");
  end;

  if (EDDefineRequestCode == "08")
    var nums08str = substr(wlReq.Queries, Index(wlReq.Queries, code08)+6);
    var comma_pos = 0, numstr = "", num = 0;    
    var i = 0;
    while (nums08str)
      comma_pos = index(nums08str, ",");
      if (comma_pos > 0)
        numstr = substr(nums08str, 1, comma_pos - 1);
        nums08str = substr(nums08str, comma_pos + 1);
      else
        numstr = substr(nums08str, 1);
        nums08str = "";
      end;
      num = int(numstr);
      if( (num!=0) and ( (num==60) or (num==61) or (num>=101) and (num<=110) ) )
        EDDefineRequestInfo(i) = TMesField("Field" + string(num), "1");
        i = i + 1;
      end;
    end;
    // Если номера не указаны, то считается, что указаны номера: 
    // 60, 61, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110
    if(EDDefineRequestInfo.size() == 0)
      EDDefineRequestInfo(0) = TMesField("Field60", "1");
      EDDefineRequestInfo(1) = TMesField("Field61", "1");
      for(i, 101, 110)
        EDDefineRequestInfo(EDDefineRequestInfo.size()) = TMesField("Field" + string(i), "1");
      end;
    end;
  else    
    var RequestInfoFNames : TArray = makeArray("AccDocDate",
                                               "AccDocNo",
                                               "Acc",
                                               "Sum",
                                               "Name"
                                              );
    var fld : TMesField = NULL;
    while( Index( Queries, "\n" ) )
      var curr_line = substr( Queries, 1, Index( Queries, "\n" )-1 );
      fld = getFieldFromQueriesLine(curr_line, RequestInfoFNames);
      if(fld)
        EDDefineRequestInfo[ EDDefineRequestInfo.size ] = fld;
      end;
      Queries = substr( Queries, Index( Queries, "\n" )+1 );
    end;
    // последняя строка из Queries
    fld = getFieldFromQueriesLine(Queries, RequestInfoFNames);
    if(fld)
      EDDefineRequestInfo[ EDDefineRequestInfo.size ] = fld;
    end;
  end;

end;

macro GenMes( addrMes, addrReq )
  var i = 0;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  // Разобрать поле wlreq.Queries
  var EDDefineRequestCode = "";    
  var EDDefineRequestInfo : TArray = TArray();
  ParseQueries(wlReq.Queries, @EDDefineRequestCode, EDDefineRequestInfo);  
  
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED243");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );

  mes.setAttribute("EDDefineRequestCode",  EDDefineRequestCode);

  // OriginalEPD -------------------------------------------
  if (wlreq.RelatedRef == "")
    RunError("Ошибка: Поле 'Ссылка' должно быть заполнено");     
  end;
  
  var elem:object;
  elem = xml.createElement("OriginalEPD");    

  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.RelatedRef); 

  elem.setAttribute("EDNo",       ed_nda.EDNo );
  elem.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  
                                           
  mes.appendChild(elem);  
  // OriginalEPD -------------------------------------------

  // EDDefineRequestInfo ----------------------------------------------
  if ( (EDDefineRequestInfo.size > 0) and ( (EDDefineRequestCode == "03") or
                                            (EDDefineRequestCode == "06") or
                                            (EDDefineRequestCode == "08") )
     )    

    var elemInfo:object;
    elemInfo = xml.createElement("EDDefineRequestInfo");

    if ( (EDDefineRequestCode == "03") or (EDDefineRequestCode == "06") )      
      for( var FldItem, EDDefineRequestInfo )
        if(FldItem.FieldName == "Name")
          var subElem:object;
          subElem = xml.createElement(FldItem.FieldName);
          subElem.appendChild( xml.createTextNode(FldItem.FieldValue) );
          elemInfo.appendChild(subElem);
        else
          elemInfo.setAttribute(FldItem.FieldName, FldItem.FieldValue);
        end;
      end;      
    end;

    if (EDDefineRequestCode == "08")
      var elemMask:object;
      elemMask = xml.createElement("FieldQueryMask");
      for(i, 0, EDDefineRequestInfo.size() - 1, 1)
        elemMask.setAttribute( EDDefineRequestInfo(i).FieldName, "1");
      end;
      elemInfo.appendChild(elemMask);
    end;

    mes.appendChild(elemInfo);
  end;
  // EDDefineRequestInfo ----------------------------------------------
   
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

private macro GetAccNameStrings( req_code : string, ver_st : integer, 
                                 AccStr : @string, NameStr : @string )

  AccStr = NameStr = "";

  if( InList(req_code, code03, code06) )
    var AccAttr  = "Acc",  AccFld  = "", 
        NameAttr = "Name", NameFld = "";

    if(req_code == code03)
      AccFld = wlpmpaym.ReceiverAccount;
      NameFld = wlpmrmprop.ReceiverName;

      if(ver_st == VER_ST_2_5_9)
        AccAttr = "PayeeAcc"; 
        NameAttr = "PayeeName";
      end;
    else
      AccFld = wlpmpaym.PayerAccount;
      NameFld = wlpmrmprop.PayerName;

      if(ver_st == VER_ST_2_5_9)
        AccAttr = "PayerAcc"; 
        NameAttr = "PayerName";
      end;
    end;

    AccStr  = "\n" + AccAttr + ": " + AccFld;
    NameStr = "\n" + NameAttr + ": " + TransTextField(substr(NameFld, 1, 160));

  elif( InList(req_code, code10, code11, code13) )
    AccStr  = "\n" + "PayerAcc: " + wlpmpaym.PayerAccount +
              "\n" + "PayeeAcc: " + wlpmpaym.ReceiverAccount;

    if( InList(req_code, code10, code11) )
      NameStr = "\n" + "PayerName: " + TransTextField(substr(wlpmrmprop.PayerName, 1, 160)) +
                "\n" + "PayeeName: " + TransTextField(substr(wlpmrmprop.ReceiverName, 1, 160));
    end;
  end;
end;

macro CheckObj(addrReq, addrRefillReq, ver_st)

  SetBuff( wlReq, addrReq );

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  var code_index : integer = index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED243, @req_code);
  if(code_index == 0)
    RunError("Не найден код в сообщении");
  end;

  // Найти платеж wlpmpaym, wlpmrmprop, на который отсылается запрос
  if( not WldFindPmByTRN_Tp( TRANSP_UFBS, wlReq.RelatedRef, NULL, IfThenElse(ver_st == VER_ST_2_5_9, null, DIRECT_PAYM_IN) ) )
    RsbThrow( IfThenElse( ver_st == VER_ST_2_5_9,
                          "Исходное сообщение не является ЭПД. Укажите в поле \"Ссылка\" референс сообщения вида \"платеж\".",
                          "Не найден входящий платеж|с референсом '" + wlReq.RelatedRef + "'"
                        ) );
  end;  

  // Дозаполнить учетный объект
  RECORD RefillReq(wlreq);
  if(ValType(addrRefillReq) == V_MEMADDR)
    SetBuff( RefillReq, addrRefillReq );
    Copy(RefillReq, wlReq, 1);      
  end;

  var TextAfterCode = SubStr( wlReq.Queries, code_index + StrLen(req_code) );
  TextAfterCode = Trim(TextAfterCode);
  
  if( (TextAfterCode == "") and (ValType(addrRefillReq) == V_MEMADDR) )
    
    if( InList(req_code, code03, code06) or
        (ver_st == VER_ST_2_5_9) and InList(req_code, code10, code11, code13)
      )
      var AccStr : string = "", NameStr : string = "";
      GetAccNameStrings(req_code, ver_st, @AccStr, @NameStr);

      TextAfterCode = "\n" + "AccDocDate: " + YYYYMMDD(wlpmrmprop.ClientDate) + 
                      "\n" + "AccDocNo: " + GetLastSymbols(wlpmrmprop.Number, 3) +
                      AccStr +
                      "\n" + "Sum: " + MoneyToStrUFBS(wlpmpaym.BaseAmount) +
                      NameStr;
      
    elif( (ver_st == VER_ST_2_5_9) and (req_code == code08) )
      TextAfterCode = "\n" + "EDFieldList: 60, 61, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110";
    end;

    if(TextAfterCode)
      RefillReq.Queries = SubStr( wlReq.Queries, 1, code_index - 1 + StrLen(req_code) ) + TextAfterCode;
    end;
  end;

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

private macro GetAddFldNames(req_code : string) : TArray
  var Names : TArray = TArray();

  if(req_code == code03)
    Names = makeArray("AccDocDate", "AccDocNo", "PayeeAcc", "Sum", "PayeeName");
  elif(req_code == code06)
    Names = makeArray("AccDocDate", "AccDocNo", "PayerAcc", "Sum", "PayerName");
  elif(req_code == code10)
    Names = makeArray( "AccDocDate", "AccDocNo", "PayerAcc", "PayeeAcc", "Sum", 
                       "PayerName", "PayeeName" );
  elif(req_code == code11)
    Names = makeArray( "AccDocDate", "AccDocNo", "PayerAcc", "PayeeAcc", "Sum", 
                       "PayerName", "PayeeName" );
  elif(req_code == code13)
    Names = makeArray( "AccDocDate", "AccDocNo", "PayerAcc", "PayeeAcc", "Sum" );
  end;

  return Names;
end;

// Вставить поясняющие поля
private macro InsertExplanatoryFlds( FieldNames : TArray,
                                     pm, rm, cr, db, pspaydem,
                                     PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                                     AddFlds : RsbAddFld
                                   )

  var addfldRec : TRecHandler = TRecHandler("wladdfld.dbt");
  var Sort : integer = 0;

  for(var FName : string, FieldNames)

    if( InsertAddFld_ByBuffers( "П", FName, "ED243", null, null, 
                                pm, rm, cr, db, pspaydem,
                                PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                                0, @Sort, AddFlds, addfldRec )
      )

      RunError("Ошибка при добавлении поля, поясняющего запрос");
    end;

  end;

end;

private macro FillPspaydem(pmpaym, pspaydem)
  FILE f_pspaydem(pspaydem);

  f_pspaydem.OrderID = pmpaym.PaymentID;
  if( (pmpaym.DocKind == PS_PAYORDER) and GetEQ(f_pspaydem) )
    copy(pspaydem, f_pspaydem);
  else
    ClearRecord(pspaydem);
  end;
end;

macro CheckObj259(addrReq, addrRefillReq)
  SetBuff( wlReq, addrReq );

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  var code_index : integer = index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED243, @req_code);
  if(code_index == 0)
    RunError("Не найден код в сообщении");
  end;

  // Найти платеж wlpmpaym, wlpmrmprop, на который отсылается запрос
  if( not WldFindPmByTRN_Tp( TRANSP_UFBS, wlReq.RelatedRef, NULL, null ) )
    RsbThrow("Исходное сообщение не является ЭПД. Укажите в поле \"Ссылка\" референс сообщения вида \"платеж\".");
  end;  

  // Дозаполнить учетный объект
  var AddFlds : RsbAddFld = RsbAddFld(wlReq.ReqID);
  RECORD pspaydem(pspaydem);
  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);
  
  if( AddFlds.size == 0 )

    var FieldNames : TArray = GetAddFldNames(req_code);

    if( FieldNames.size > 0 )
      FillPspaydem(wlpmpaym, pspaydem);

      GetOperationParticipantsFromRoute
        ( wlpmpaym.PaymentID,
          PrevInstrAgent, SenderBank, ExecutorBank, 
          IntermediaryBank, PayerBank, ReceiverBank
        );

      InsertExplanatoryFlds
        ( FieldNames, wlpmpaym, wlpmrmprop, wlpmpropcred, wlpmpropdeb, pspaydem, 
          PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
          AddFlds );
    end;

  end;

  return true;

  OnError(er) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;


macro GenMes259( addrMes, addrReq )
  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  var EDDefineRequestCode : string = "",
      EDDefineRequestText : string = "",
      AddFldsInfo : TAddFldsInfo = TAddFldsInfo();

  ReadDataFromWlreq243244(wlReq, @EDDefineRequestCode, @EDDefineRequestText, AddFldsInfo);
  
  // EDNo, EDDate, EDAuthor
  FillEDNoDateAuthorByRef_CompoundRls(wlmes.TRN);

  ЗаписатьПолеЛог( "EDReceiver", GetEDReceiverForWlreq(wlreq) );
  ЗаписатьПолеЛог( "EDDefineRequestCode", EDDefineRequestCode );

  // OriginalEPD -------------------------------------------
  if (wlreq.RelatedRef == "")
    RunError("Ошибка: Поле 'Ссылка' должно быть заполнено");     
  end;
  StartBlockLogXML( "OriginalEPD" );
  FillEDNoDateAuthorByRef_CompoundRls(wlmes.RelatedRef);
  FinishBlockLogXML( "OriginalEPD" );
  // OriginalEPD -------------------------------------------

  // EDDefineRequestInfo ----------------------------------------------
  WriteEDDefineReqAnsInfo("ED243", EDDefineRequestText, AddFldsInfo);
   
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
