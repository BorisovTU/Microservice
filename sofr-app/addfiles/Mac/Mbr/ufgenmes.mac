 /*
 $Name: ufgenmes.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщений транспорта УФЭБС (общие функции)
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщений транспорта УФЭБС (общие функции)                     */
/*                                                                          */
/*  Имя файла: ufgenmes.mac                                                 */
/*  Создан:    29.09.04                                       Бабин А.П.    */
/****************************************************************************/

import "globals.mac", "oralib.mac", "wluftool.mac", bnk_ptlib, "wlgenmes.mac",
       Календарь;

macro GetEDReceiver( RecipientID, RecipientCodeKind, RecipientCode, RelatedRef, NoErr)
   var EDReceiver : string = "";
   var Error = 0;
   if(ValType(NoErr) == V_UNDEF)
     NoErr = false;
   end;

   if (RecipientCodeKind == PTCK_UIS)
     EDReceiver = RecipientCode;
   else
     if (RecipientID > 0)
       EDReceiver = ПолучитьКодСубъекта( RecipientID, PTCK_UIS, Error );
     end;
     if ((EDReceiver == "") and (ValType(RelatedRef) != V_UNDEF))
       EDReceiver = SubStr(RelatedRef, 7, 10);
     end;
   end;
   if ((EDReceiver == "") and (not(NoErr)) )
     RunError( "|Не удалось определить уникальный идентификатор получателя ЭC (EDReceiver)" );
   end;

   //проверок пока нет, так как нет предупреждений
   return EDReceiver;
end;

macro GetEDReceiverForWlreq(wlReq, RelatedRef : string) : string
  return GetEDReceiver(wlReq.RecipientID, wlReq.RecipientcodeKind, wlReq.RecipientCode, RelatedRef);
end;

macro GetEDAuthor( AbonentID )
   var Error, MFO_9;
   if( (AbonentID == 0) or (AbonentID == {OurBank}) )
      return string(substr(Свой_МФО, 3, 7),"000");
   else
      MFO_9 = ПолучитьКодСубъекта( AbonentID, PTCK_BIC, Error ) ;
      if (Error)
         RunError( "|Не найден БИК банка-составителя сообщения" );
      end;
      return string(substr(MFO_9, 3, 7),"000");
   end;
end;

private var _lastTpShemID:integer = -1,
            _lastEDAuthor:string  = "";
macro GetEDAuthorByMes( wlmesrec ):string
  if( wlmesrec.TpSchemID == _lastTpShemID )
    return _lastEDAuthor;
  end;
  var rs:RsdRecordset = execSQLselect( "SELECT DP.T_PARTYID FROM DWLTPSHEM_DBT TS, DDP_DEP_DBT DP WHERE TS.T_TPSHEMID = :TPSHEMID AND DP.T_CODE = TS.T_DEPARTMENT",
                                       makeArray( SQLParam( "TPSHEMID", wlmesrec.TpSchemID ) ) );
  if( rs.moveNext() )
    _lastEDAuthor = GetEDAuthor( rs.value( 0 ) );
    _lastTpShemID = wlmesrec.TpSchemID;
  else
    RunError( "|Не найден банк-составитель сообщения" );
  end;
  return _lastEDAuthor;
end;

/* Получить признак системы обаботки (01 - непрерывная, 02 - дискретная, 05 - RTGS). */
macro GetEDSystemCode( TpShemID ):string
   ClearRecord(f_wltpshem);
   f_wltpshem.TpShemID = TpShemID;
   if ( GetEQ(f_wltpshem) and (f_wltpshem.SystemCode!="") )
     return f_wltpshem.SystemCode;
   else
     return "01";
   end;
end;

macro GetReqSettlementDate(OutDPP : date, MesEDDate : date, PaymentKind : string) : date
  var AllowFutureDate : bool = Bnk_GetRegistryValue
    ( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ПЛАТЕЖИ БУДУЩЕЙ ДАТОЙ\\ПЛАТЕЖ_БУДУЩЕЙ_ДАТОЙ",
      V_BOOL,
      false
    );
  if( (not AllowFutureDate) or
      (OutDPP <= MesEDDate) or
      (PaymentKind == "С")
    )
    // Поле не заполняется
    RETURN date(0,0,0);
  end;

  var ErrMsg : string = "";

  var MaxPeriod : integer = Bnk_GetRegistryValue
    ( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ПЛАТЕЖИ БУДУЩЕЙ ДАТОЙ\\МАКСИМАЛЬНЫЙ_ПЕРИОД",
      V_INTEGER,
      1
    );
  var MaxDate : date = GetNextWorkDate(MesEDDate, MaxPeriod);
  if(OutDPP > MaxDate)
    ErrMsg = "Указанная в сообщении требуемая дата исполнения|(ReqSettlementDate) может быть|больше текущего опердня филиала не более чем на количество рабочих дней в настройке|МЕЖБАНКОВСКИЕ РАСЧЕТЫ\УФЭБС\ПЛАТЕЖИ БУДУЩЕЙ ДАТОЙ\ МАКСИМАЛЬНЫЙ_ПЕРИОД.|Укажите в платеже другую исходящую ДПП или повторите выгрузку в МБР в другой день.";
    RunError(ErrMsg, ErrMsg);
  end;

  RETURN OutDPP;
end;

private macro GetPacketType(RlsFormID : integer) : integer
  var PacketType : integer = 0;

  var rs : RsdRecordset = execSQLselectPrm
    ( "select frm.t_PacketType "
      "  from dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
      " where rls.t_RlsFormID = :RlsFormID "
      "   and frm.t_FormID = rls.t_FormID ",
      SQLParam("RlsFormID", RlsFormID)
    );
  if(rs and rs.moveNext())
    PacketType = rs.value(0);
  end;

  return PacketType;
end;

private macro CheckED113ED114( MesID )

  var select = " Select distinct frm.t_Name "
                "   From dwlmesfrm_dbt frm, dwlmesrls_dbt rls, dwlmes_dbt mes "
                "  Where mes.t_MesID = :MesID "
                "    and mes.t_RlsFormID = rls.t_RlsFormID "
                "    and frm.t_FormID = rls.t_FormID ";
  
  var rs; 
  
  rs = execSQLselect( select, MakeArray( SQLParam("MesID", MesID )));
                  
  while( rs and rs.MoveNext() )
    if( inList( rs.value("t_Name"), "ED113", "ED114" ) )
      return true;
    end;
  end; 
  
  return false;
end;

/* Используются ли пакеты ЭПД/ЭСИД/EID.
   определяем по настройке формата транспорта: максимальное число сообщений при экспорте в файл */
macro GetModePacketEPD_ESID( wlmes, IsPacket : @bool, PacketType : @integer, MaxExpMes : @integer ):bool
  var Name:string, FormatFile:integer, GroupSessByPacketType : bool;

  if( (ОпределитьФорматТранспортаДляТранспортнойСхемы( wlmes.TpSchemID, Name, FormatFile, MaxExpMes, GroupSessByPacketType )) == 0 )
    std.msg("Не найден формат транспорта УФЭБС для транспортной схемы " + wlmes.TpSchemID);
    return false;
  end;

  if( FormatFile != WLD_FORMAT_XML )
    std.msg("У формата транспорта УФЭБС установлен неверный формат файла обмена (не xml)");
    return false;
  end;

  IsPacket = false;
  PacketType = 0;
  
  var RegUsePacketCash;
    PacketType = GetPacketType(wlmes.RlsFormID);

  GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\Использовать_PacketCash", V_BOOL, RegUsePacketCash);
  
  if(( ( (MaxExpMes != 1) or  ( (PacketType == 4) and (RegUsePacketCash) ) and GroupSessByPacketType ) or CheckED113ED114(wlmes.MesID)))
    

    if(PacketType)
      IsPacket = true;
    end;
  end;
 
  return true;
end;

macro GetMesSystemCode( MesID )
  var RegSysCode;
  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\SystemCode", V_STRING, RegSysCode);
  var rs = execSQLSelect( " Select tps.t_SystemCode "
                          "   From dwltpshem_dbt tps, dwlmes_dbt mes "
                          "  Where mes.t_MesID = :MesID "
                          "    and tps.t_TpShemID = mes.t_TpSchemID ",
                          MakeArray( SQLParam("MesID", MesID)));
                          
  if( rs and rs.MoveNext() )
    if( strlen(rs.value("t_SystemCode")) > 0 )
      return rs.value("t_SystemCode");
    else
      return RegSysCode;
    end;
  end;
end;

macro GetSystemCode( wlmes, Err : @String, IsPacket, PacketName, SessID, IsRepeat )
  var SystemCode = "";
  var rs;
    
  if( IsPacket == false )
    rs = execSQLSelect( " Select t_SystemCode "
                        "   From dwltpshem_dbt "
                        "  Where t_TpShemID = :ShemID ",
                        MakeArray( SQLParam( "ShemID", wlmes.TpSchemID) ));
    if( rs and rs.MoveNext() )
      SystemCode = rs.value(0);
    end;
  else
    rs = execSQLSelect( " Select tp.t_GroupSessBySystemCode "
                        "   From dwltpfrmt_dbt tp, dwlsess_dbt sess "
                        "  Where tp.t_TpFrmtID = sess.t_TpFrmtID "
                        "    and sess.t_SessionID = :SessionID ",
                        MakeArray( SQLParam( "SessionID", SessID )
                                 ));
    if( rs and rs.MoveNext() )
      if( rs.value(0) == "X" )
        SystemCode = GetMesSystemCode(wlmes.MesID);
      elif( PacketName == "PacketEPD" )
        if(IsRepeat)
          SystemCode = GetMesSystemCode(wlmes.MesID);
        else
          SystemCode = "";
          Err = "Не допускается включение в один пакет ЭПС сообщений с разным признаком системы обработки (SystemCode)";
        end;
      end;        
    end;   
  end;
  
  if( strlen(SystemCode) == 0 )
    GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\SystemCode", V_STRING, SystemCode);
  end;
  
  if(InList(SystemCode, "01", "02", "05"))
    return SystemCode;
  else
    Err = "Некорректный вид системы обработки";
    return "";
  end;
  
end;

macro GetSystemCodeForGenMes( type:string, wlmes, Err : @String, SystemCode : @String )
  var IsPacket;
  GetModePacketEPD_ESID( wlmes, @IsPacket );
  
  var rs = execSQLSelect( " Select mesrls.t_Name "
                          "   From dwlmesrls_dbt mesrls "
                          "  Where mesrls.t_RlsFormID = :RlsFormID ",
                          MakeArray( SQLParam("RlsFormID", wlmes.RlsFormID)));
  
  if( IsPacket )    

    var toFill : bool = Index(Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\SystemCode_В_ПАКЕТЕ", V_STRING, ""), type ) != 0;

    if(toFill or (type == "ED204") or (type == "ED242") or ( (rs and rs.movenext()) and  
        (CompareStrWithMasks( "ED???v.3", rs.value("t_Name") ) == 0)))
      SystemCode = GetSystemCode( wlmes, @Err, false );
      return true;
    end;      

    return false;
  end;
  
  SystemCode = GetSystemCode( wlmes, @Err, false );
  
  return true;
  
end;

macro TrimNull( value )
   var result = trim(value);
   var count = 1;

   while( (count<=strlen(result)) and (substr(result,count,1)=="0") )
      count = count+1;
   end;
   result = substr(result,count);
   return result;
end;

macro YYYYMMDD( dateInit )
   var day, mon, year, str;

   DateSplit(date(dateInit), day, mon, year );
   if ( day < 10 )
     day = "0"+string(day);
   else
     day = string(day);
   end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = string(year) + "-" + mon + "-" + day ;
  return str;
end;

macro ДатаДДММГГГГ( dateInit )
  var day, mon, year;
  DateSplit(dateInit, day, mon, year );
  return StrSubst(string(day:2, ".", mon:2, ".", year:4), " ", "0");
end;

macro TransTextField( strInit )
   var str="", count, len = strlen(strInit), symb;
   count = 1;
   while( count<=len )
      symb = substr(strInit, count, 1);
     /* if ( symb=="<" )
         symb = "&lt;";
      elif ( symb==">" )
         symb = "&gt;";
      elif ( symb=="&" )
         symb = "&amp;";
      elif ( symb=="\"" )
         symb = "&quot;"; */
      if ( ((symb >= "\x01") and (symb <= "\x08")) or
           ((symb >= "\x0B") and (symb <= "\x0C")) or 
           ((symb >= "\x0E") and (symb <= "\x1F")) or
           ((symb >= "\xB0") and (symb <= "\xDF")) or
           ((symb >= "\xF8") and (symb <= "\xFB")) or
           ((symb >= "\xFD") and (symb <= "\xFF")) 
         )
         symb = ".";
      elif( (symb == "\x09") or (symb == "\x0A") or (symb == "\x0D") )
         symb = " ";
      elif( symb == "\x7F" )
         symb = "?";
      elif( (symb == "Є") or (symb == "є"))
         symb = "e";
      elif( symb == "Ї")
         symb = "I";
      elif( symb == "ї")
         symb = "i";
      elif( symb == "Ў")
         symb = "Y";
      elif( symb == "ў")
         symb = "y";
      end;
      str = str + symb;
      count = count + 1;
   end;
   /*return ToANSI(str);*/
   return str;
   /* Ничего самим делать не надо.*/
   /*return strInit;*/
end;

macro FillEDNoDateAuthorByRef_XMLField(elem : object, Ref : string)
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(Ref);

  elem.setAttribute( "EDNo",       ed_nda.EDNo );
  elem.setAttribute( "EDDate",     YYYYMMDD(ed_nda.EDDate) );
  elem.setAttribute( "EDAuthor",   ed_nda.EDAuthor );
end;

macro FillEDNoDateAuthorByRef_CompoundRls(Ref : string)
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(Ref);

  ЗаписатьПолеЛог( "EDNo",     ed_nda.EDNo );
  ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(ed_nda.EDDate) );
  ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
end;

private macro GetPaytKindRegVal(form: string, mi: integer, ma: integer, def:string): string
  var err, val;
  GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\PAYTKIND\\"+form, V_INTEGER, val, err);
  if (err) 
    return def; 
  end;
  if ((val < mi) or (val > ma)) 
    return def; 
  end;
  return string(val);
end;

macro GetPaytKind(form: string, PaymentKind: string, TpSchemID: integer): string
  // Особые случаи
  if( InList(form, "ED101", "ED103", "ED104", "ED105")  and
      ( PaymentKind == "С" ) /* Срочно */               and
      ( GetEDSystemCode(TpSchemID) == "05" ) /* БЭСП */
    )
    return "4";
  elif( InList(form, "ED110", "ED113", "ED114", "ED111") and
        InList(PaymentKind, "П", "Т") /* Почтой, Телеграфом */
      )
    if(PaymentKind == "П") // Почтой
      return "2";
    else                   // Телеграфом
      return "3";
    end;
  elif( InList(form, "ED111", "ED107") and
        ( PaymentKind == "С" ) /* Срочно */
      )
    return "4";
  end;

  // Общий случай
  return GetPaytKindRegVal(form, 1, 9, "");
end;

macro ЗаписатьПоляИзСтрокиПеречисления(source : string, BlockName : string, FieldName : string, ParentBlockName : string)
  var arrstr : TArray = StrCut(source);

  for(var s, arrstr)
    if(ParentBlockName)
      УстановитьКонтекстБлокаЛог( ParentBlockName );
      ЗаписатьПолеЛог( beginField, ParentBlockName );
    end;
    if(BlockName)
      УстановитьКонтекстБлокаЛог( BlockName );
      ЗаписатьПолеЛог( beginField, BlockName );
    end;

    ЗаписатьПолеЛог(FieldName, s);

    if(BlockName)
      ЗаписатьПолеЛог( endField, BlockName ) ;
      УстановитьКонтекстБлокаЛог( ".." );
    end;
    if(ParentBlockName)
      ЗаписатьПолеЛог( endField, ParentBlockName ) ;
      УстановитьКонтекстБлокаЛог( ".." );
    end;
  end;
end;

