/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям МЦИ макета R (документы)                */
/*                                                                          */
/* Имя файла: wlmcgdr.mac                                                   */
/* Создан:    15.08.00                                             AVM      */
/****************************************************************************/
import "wlmcgd.mac", FIInter, "wltreas.mac";

const E0001 = 1,
      E0002 = 2;

macro GenDoc( addrMes, group )
  var Сумма, Day, Mon, Year, Error, Corschem,
      ВИД_ПЛАТЕЖА, Currency, PayerName, ReceiverName, accstr, accstr2, Credit_Flag, displacement;
  var IsTwist = false, IsTransit=false;

  if ( valtype(group)==V_UNDEF )
     group = E0001;
  end;
  /* Смещение в позиции из-за увеличения pmrmprop.BttTICode c 19 до 20 */
  if( {curdate} >= date(01,01,2005) ) 
   displacement = 1;
  else
   displacement = 0;
  end;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация платежа по R-макету (новый формат с 02.06.03)" );

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  ClearRecord(wlpmpropdeb);  
  ClearRecord(wlpmpropcred);  

  if( not СчитатьПоле( FormFldDoc, Строка ) )
    return FALSE;
  end;

  if ( group==E0001 )
     Credit_Flag = 1;
  else
     Credit_Flag = 0;
  end;   

  if ( substr(Строка, 868+displacement,1)=="4" )
     IsTwist = true;
  end;

  /* Заполнение учетных буферов */
  Сумма = substr( Строка, 137, 18 );
  wlpmpaym.Amount            = moneyL( Сумма )/100;
  wlpmpaym.PayAmount         = moneyL( Сумма )/100;
  wlpmpaym.BaseAmount        = wlpmpaym.PayAmount;

  Day = int( substr( Строка, 8, 2 ) );
  Mon = int( substr( Строка, 10, 2 ) );
  Year= int( substr( Строка, 12, 4 ) );
  wlpmpaym.ValueDate         = date( Day, Mon, Year );

  /* В R-макете ходят только рубли */
  wlpmpaym.FIID              = 0;
  wlpmpaym.PayFIID           = wlpmpaym.FIID;
  wlpmpaym.BaseFIID          = wlpmpaym.FIID;

  if ( (substr(Строка, 88, 9)!=Свой_МФО) and
       (substr(Строка, 26, 9)!=Свой_МФО) )
     IsTransit = true;
  end;

  if(Credit_Flag OR IsTransit )
    InitPMPROP(wlpmpropdeb);
    wlpmpropdeb.DebetCredit = 0; /* BANK_DEBET from BankInter */
    if ( wlpmpaym.ValueDate==date(0,0,0) )
       wlpmpropdeb.TransferDate = {curdate};
    else
       wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
    end;

    wlpmpropdeb.PayFIID   = wlpmpaym.PayFIID;
    wlpmpropdeb.CodeKind  = PTCK_BIC;
    if( not IsTwist )
      wlpmpropdeb.BankCode  = substr( Строка, 26, 9 );
    else
      wlpmpropdeb.BankCode  = substr( Строка, 88, 9 );
    end;
  end;

  if( (not Credit_Flag) OR IsTransit )
    InitPMPROP(wlpmpropcred);
    wlpmpropcred.DebetCredit = 1; /* BANK_CREDIT from BankInter */
    if ( wlpmpaym.ValueDate==date(0,0,0) )
       wlpmpropcred.TransferDate = {curdate};
    else
       wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
    end;

    wlpmpropcred.PayFIID   = wlpmpaym.PayFIID;
    wlpmpropcred.CodeKind  = PTCK_BIC;

    if( IsTwist )
      wlpmpropcred.BankCode  = substr( Строка, 26, 9 );
    else
      wlpmpropcred.BankCode  = substr( Строка, 88, 9 );
    end;
  end;
  
  if ( not IsTwist )
     wlpmpaym.PayerAccount      = Trim( substr( Строка, 35, 20 ) );
     wlpmpaym.ReceiverAccount   = Trim( substr( Строка, 97, 20 ) );  
  else
     wlpmpaym.ReceiverAccount   = Trim( substr( Строка, 35, 20 ) );
     wlpmpaym.PayerAccount      = Trim( substr( Строка, 97, 20 ) );  
  end;
  /*Инициируем плательщика - см.SCR№89542*/
  InitPAYER( wlpmpaym, group );
  /*Инициируем получателя  - см.SCR№92258*/
  InitReceiver( wlpmpaym, group );

  if ( not IsTwist )
     accstr = substr( Строка, 55, 20 );
     accstr2 = substr( Строка, 117, 20 );
  else
     accstr = substr( Строка, 117, 20 );
     accstr2 = substr( Строка, 55, 20 );
  end;
  if( accstr != mkstr(" ",20) )
    accstr = Trim( accstr );
  else 
    accstr = "";
  end;

  if( accstr2 != mkstr(" ",20) )
    accstr2 = Trim( accstr2 );
  else 
    accstr2 = "";
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number = Trim( substr( Строка, 75, 3 ) );

  Day = int( substr( Строка, 78, 2 ) );
  Mon = int( substr( Строка, 80, 2 ) );
  Year= int( substr( Строка, 82, 4 ) );
  wlpmrmprop.Date                  = date( Day, Mon, Year );

  // Дата поступления в банк плательщика
  Day = int(substr( Строка, 728, 2 ));
  Mon = int(substr( Строка, 730, 2 ));
  Year= int(substr( Строка, 732, 4 ));
  wlpmpaym.PayerBankEnterDate      = date( Day, Mon, Year );

  // Дата помещения в картотеку
  Day = int(substr( Строка, 736, 2 ));
  Mon = int(substr( Строка, 738, 2 ));
  Year= int(substr( Строка, 740, 4 ));
  wlpmpaym.I2PlaceDate             = date( Day, Mon, Year );

  ВИД_ПЛАТЕЖА = substr( Строка, 865+displacement, 1 );

  if( ВИД_ПЛАТЕЖА == "1" )
    if ( not IsTwist )
       PayerName                        = Trim( substr( Строка, 177, 160 ) );
       ReceiverName                     = Trim( substr( Строка, 358, 160 ) );
    else
       PayerName                        = Trim( substr( Строка, 358, 160 ) );
       ReceiverName                     = Trim( substr( Строка, 177, 160 ) );
    end;

    if ( not IsTwist )
       wlpmrmprop.PayerINN              = ConstructINN( Trim( substr( Строка, 156, 12 ) ), Trim( substr( Строка, 168, 9 ) ) );
       wlpmrmprop.ReceiverINN           = ConstructINN( Trim( substr( Строка, 337, 12 ) ), Trim( substr( Строка, 349, 9 ) ) );
    else
       wlpmrmprop.PayerINN              = ConstructINN( Trim( substr( Строка, 337, 12 ) ), Trim( substr( Строка, 349, 9 ) ) );
       wlpmrmprop.ReceiverINN           = ConstructINN( Trim( substr( Строка, 156, 12 ) ), Trim( substr( Строка, 168, 9 ) ) );
    end;

    wlpmrmprop.PayerName             = RemoveKPPFromName( PayerName );
    wlpmrmprop.ReceiverName          = RemoveKPPFromName( ReceiverName );       

    wlpmrmprop.Ground                = Trim( substr( Строка, 518, 210 ) );

    wlpmrmprop.TaxAuthorState  = Trim(substr( Строка, 760, 2 ));
    wlpmrmprop.BttTICode       = Trim(substr( Строка, 762, 19+displacement ));
    wlpmrmprop.OKATOCode       = Trim(substr( Строка, 781+displacement, 11 ));
    wlpmrmprop.TaxPmGround     = Trim(substr( Строка, 792+displacement, 2 ));
    wlpmrmprop.TaxPmPeriod     = Trim(substr( Строка, 794+displacement, 10 ));
    wlpmrmprop.TaxPmNumber     = Trim(substr( Строка, 804+displacement, 15 ));
    wlpmrmprop.TaxPmDate       = Trim(substr( Строка, 819+displacement, 10 ));
    wlpmrmprop.TaxPmType       = Trim(substr( Строка, 829+displacement, 2 ));    

    Day = int(substr( Строка, 744, 2 ));
    Mon = int(substr( Строка, 746, 2 ));
    Year= int(substr( Строка, 748, 4 ));
    wlpmrmprop.PayerChargeOffDate   = date( Day, Mon, Year );

    // Дата отметки банка плучателя
    Day = int(substr( Строка, 752, 2 ));
    Mon = int(substr( Строка, 754, 2 ));
    Year= int(substr( Строка, 756, 4 ));
    wlpmpaym.ReceiverBankMarkDate = date( Day, Mon, Year );

    // Номер частичного платежа
    wlpmpaym.PartPaymNumber    = Trim( substr( Строка, 831+displacement, 3 ) );

    // Шифр платежного документа
    wlpmpaym.PartPaymShifrMain = substr( Строка, 834+displacement, 2 );

    // Номер платежного документа
    wlpmpaym.PartPaymNumMain   = Trim( substr( Строка, 836+displacement, 3 ) );

    // Дата платежного документа
    Day = int(substr( Строка, 839+displacement, 2 ));
    Mon = int(substr( Строка, 841+displacement, 2 ));
    Year= int(substr( Строка, 843+displacement, 4 ));
    wlpmpaym.PartPaymDateMain  = date( Day, Mon, Year );

    // Сумма остатка платежа
    Сумма = substr( Строка, 847+displacement, 18 );
    wlpmpaym.PartPaymRestAmountMain  = moneyL( doubleL( Сумма ) );

  else
    wlpmrmprop.IsShortFormat = "X";
    if ( not IsTwist )
       wlpmrmprop.PayerINN              = Trim( substr( Строка, 156, 12 ) );
       wlpmrmprop.ReceiverINN           = Trim( substr( Строка, 337, 12 ) );    
    else
       wlpmrmprop.PayerINN              = Trim( substr( Строка, 337, 12 ) );
       wlpmrmprop.ReceiverINN           = Trim( substr( Строка, 156, 12 ) );    
    end;

    //Для сокращенного формата названия плательщика/получателя не должны быть заполнены
    //SCR 53947
    wlpmrmprop.PayerName = " ";
    wlpmrmprop.ReceiverName = " ";
  end;  

  wlpmrmprop.Priority              = int( substr( Строка, 155, 1 ) );

  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = substr( Строка, 86, 2 );

  if(Credit_Flag and (not IsTransit) )
    wlpmpaym.PayerBankID = ПолучитьКодСубъекта( wlpmpropdeb.BankCode, wlpmpropdeb.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк плательщика: " + wlpmpropdeb.BankCode );
      return FALSE;
    end;

    wlpmpaym.ReceiverBankID = {OurBank};

    if( ПолучитьСубъекта( wlpmpaym.PayerBankID, wlparty ) != 0 )
      std.msg( "Не найден банк плательщика" );
      return FALSE;
    end;
    wlpmrmprop.PayerBankName         = wlparty.Name;
    wlpmrmprop.PayerCorrAccNostro    = accstr;

    wlpmrmprop.ReceiverBankName      = {Name_Bank};
    if ( not IsTwist )
       wlpmrmprop.ReceiverCorrAccNostro = "";
    else
       wlpmrmprop.ReceiverCorrAccNostro = Trim(substr( Строка, 55, 20 ));
    end;
    wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropdeb.Corschem != -1)
      wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, 0, wlpmrmprop );
    
    if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, 0, wlpmrmprop ) )
      std.msg( "Ошибка при вставке платежа" );
      return FALSE;
    end;

  elif( (not Credit_Flag) and (not IsTransit) )
  
    wlpmpaym.ReceiverBankID = ПолучитьКодСубъекта( wlpmpropcred.BankCode, wlpmpropcred.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк получателя: " + wlpmpropcred.BankCode );
      return FALSE;
    end;

    /* Если в ответном требовании получателем выступает "наш банк" - то внешний платеж не сформируется */
    if(wlpmpaym.ReceiverBankID == {OurBank})
      std.msg( "Ошибка логичекого контроля.|В ответном требовании (группа E0002) получателем (БИК " + wlpmpropcred.BankCode + ") является \"наш банк\"" );
      return FALSE;
    end;

    wlpmpaym.PayerBankID = {OurBank};

    if( ПолучитьСубъекта( wlpmpaym.ReceiverBankID, wlparty ) != 0 )
      std.msg( "Не найден банк получателя" );
      return FALSE;
    end;
    wlpmrmprop.ReceiverBankName      = wlparty.Name;
    wlpmrmprop.ReceiverCorrAccNostro = accstr;

    wlpmrmprop.PayerBankName         = {Name_Bank};
    wlpmrmprop.PayerCorrAccNostro    = "";
    wlpmpropcred.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropcred.Corschem != -1)
      wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, 0, wlpmpropcred, wlpmrmprop );

    if( not ВставитьПлатеж( wlpmpaym, 0, wlpmpropcred, wlpmrmprop, PRT_Debet ) )
      std.msg( "Ошибка при вставке платежа" );
      return FALSE;
    end;
  else
    wlpmpaym.PayerBankID = ПолучитьКодСубъекта( wlpmpropdeb.BankCode, wlpmpropdeb.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк плательщика: " + wlpmpropdeb.BankCode );
      return FALSE;
    end;

    wlpmpaym.ReceiverBankID = ПолучитьКодСубъекта( wlpmpropcred.BankCode, wlpmpropcred.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк получателя: " + wlpmpropcred.BankCode );
      return FALSE;
    end;

    if( ПолучитьСубъекта( wlpmpaym.ReceiverBankID, wlparty ) != 0 )
      std.msg( "Не найден банк получателя" );
      return FALSE;
    end;
    wlpmrmprop.ReceiverBankName      = wlparty.Name;
    wlpmrmprop.ReceiverCorrAccNostro = accstr;

    if( ПолучитьСубъекта( wlpmpaym.PayerBankID, wlparty ) != 0 )
      std.msg( "Не найден банк плательщика" );
      return FALSE;
    end;
    wlpmrmprop.PayerBankName         = wlparty.Name;
    wlpmrmprop.PayerCorrAccNostro    = accstr2;
    wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropdeb.Corschem != -1)
      wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );

    if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
      std.msg( "Ошибка при вставке платежа" );
      return FALSE;
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenDocE0001( addrMes )
    return GenDoc( addrMes, E0001 );
end;

macro GenDocE0002( addrMes )
    return GenDoc( addrMes, E0002 );
end;
