/*
$Name:         ufgd463.mac
$Module:       Межбанковские расчеты
$Description:  Генерация учетного объекта ED463
*/

// ED463 Извещение о результатах промежуточного контроля

import "ufgendoc.mac";

private macro ProcMes( Error, MesID, CtrlCode, ErrDescription, Annotation, ErrorDiagnostic )

  RECORD wlreq(wlreq);
  ClearRecord(wlreq);
  var ResultID = 0;
  var ErrCode;
  if( CtrlCode == "2945" )
    ErrCode = 1;
  else
    ErrCode = 2;
  end; 
  
  ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, 100, true, @ResultID ); 
  
  var wlerr_buf = TRecHandler("wlerror.dbt");
  
  wlerr_buf.rec.ResultID = ResultID;
  wlerr_buf.rec.Code = CtrlCode;
  wlerr_buf.rec.Description = ErrDescription;
  
  Error.Insert(wlerr_buf);
  
  if( strlen(Annotation) != 0 )
    wlerr_buf = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.ResultID = ResultID;
    wlerr_buf.rec.Description = Annotation;
    
    Error.Insert(wlerr_buf);
  end;
  
  var rs = execSQLSelect( " Select lnk.t_ObjID, lnk.t_ObjKind "
                          "   From dwlmeslnk_dbt lnk "
                          "  Where lnk.t_MesID = :MesID ",
                          MakeArray( SQLParam("MesID", MesID) )
                        );
  if( rs and rs.MoveNext() )
    if( rs.value("t_ObjKind") == OBJTYPE_REQ )
      var Note = string(CtrlCode + "; " + Annotation);
      if( strlen(ErrorDiagnostic) > 0 )
        Note = Note + "\n\n" + ErrorDiagnostic;
      end;
      
      wlreq.ReqID = rs.value("t_ObjID");
      var sReqID : string = makeObjectID(OBJTYPE_REQ, null, wlreq);
      AddNoteForObject(OBJTYPE_REQ, sReqID, 6 /* Результат обработки у адресата */, Note );
    end;
  end;
  
  Error.TransferSelect(ResultID);
  
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED463" );
  
  var FieldName, FieldValue, BlockName;
  var Annotation, CtrlCode, RefEDNo, RefEDDate, RefEDAuthor, ErrorDiagnostic;
  
  while( СчитатьПоле(FieldName, FieldValue, BlockName) )
    if( BlockName == "" )
      if( FieldName == "CtrlCode" )
        CtrlCode = FieldValue;
      end;
    elif( BlockName == "Annotation" )
      if( FieldName == "Annotation" )
        Annotation = FieldValue;
      end;
    elif( BlockName == "EDRefID" )
      if( FieldName == "EDNo" )
        RefEDNo = FieldValue;
      elif( FieldName == "EDDate" )
        RefEDDate = FieldValue;
      elif( FieldName == "EDAuthor" )
        RefEDAuthor = FieldValue;
      end;
    elif( BlockName == "ErrorDiagnostic" )
      if( FieldName == "ErrorDiagnostic" )
        ErrorDiagnostic = FieldValue;
      end;
    end;
  end;

  if( strlen(wlmes.RelatedRef) == 0 )
    RunError("Не найдено исходящее сообщение");
  end;
  
  var CountMes = 0;
  var Error = RsbWlError();
  var ErrDescription = "";
  
  var descr = execSQLSelect( " Select val.t_Name "
                             "   From dllvalues_dbt val "
                             "  Where val.t_List = 2062 "
                             "    and t_Code = :Code ",
                             MakeArray( SQLParam("Code", CtrlCode)));
  if( descr and descr.MoveNext())
    ErrDescription = descr.value("t_Name");
  end;
  
  var rs = execSQLSelect( " Select out.t_MesID "
                          "   From dwlmes_dbt out, dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
                          "  Where out.t_Trn = :RelatedRef "
                          "    and out.t_Direct = chr(0) "
                          "    and out.t_RlsFormID = rls.t_RlsFormID "
                          "    and rls.t_FormID = frm.t_FormID "
                          "    and frm.t_TpID = :TpID ",
                          MakeArray( SQLParam("RelatedRef", wlmes.RelatedRef),
                                     SQLParam("TpID", 9)));
                                     
  if( rs and rs.MoveNext() )
    CountMes = CountMes + 1;
    ProcMes( Error, rs.value("t_MesID"), CtrlCode, ErrDescription, Annotation, ErrorDiagnostic);
  else
    rs = execSQLSelect( " Select out.t_MesID "
                        "   From dwlmes_dbt out, dwlmesrls_dbt rls, dwlmesfrm_dbt frm, dwlsess_dbt wlsess "
                        "  Where out.t_SessionID = wlsess.t_SessionID "
                        "    and out.t_Direct = chr(0) "
                        "    and out.t_RlsFormID = rls.t_RlsFormID "
                        "    and rls.t_FormID = frm.t_FormID "
                        "    and frm.t_TpID = :TpID "
                        "    and wlsess.t_SessUID = :RelatedRef ",
                        MakeArray( SQLParam("TpID", 9),
                                   SQLParam("RelatedRef", wlmes.RelatedRef)));
  end;
  
  while( rs and rs.MoveNext() )
    CountMes = CountMes + 1;
    ProcMes( Error, rs.value("t_MesID"), CtrlCode, ErrDescription, Annotation, ErrorDiagnostic);
  end;
  
  if( CountMes == 0 )
    RunError("Не найдено исходящее сообщение № " + RefEDNo + " от " + RefEDDate );
  end;
  
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
  

end;

macro GenDocV2( addrMes )
   return GenDoc( addrMes );
end;
