/*
$Name:               mnsbxtool.mac
$Module:            МБ Расчеты
$Description:      Функции для сообщений BNS, BOS XML
*/

import "fns365P_lib.mac", "fnsgenmesx.mac";

private macro GetIsHeadOffice(PartyID : integer) : bool
  var BankRegNum : string = ПолучитьКодСубъекта(PartyID, PTCK_BANKREGNUM);
  var SymbolsAfterSlash : string = GetSymbolsAfterSlash(BankRegNum);

  if( SymbolsAfterSlash and (int(SymbolsAfterSlash) != 0) )
    return FALSE;
  end;

  return TRUE;
end;

macro ЗаписатьПоле_ТипСправ(wlregdec, FieldName : string) : string

  var ТипСправ : string = "";

  if(not FieldName)
    FieldName = "ТипСправ";
  end;

  var isReq : bool = false;
  var isDecision : bool = false;

  if( wlregdec.LnkDocType == OBJTYPE_REQ )
    isReq = true;  
  elif( wlregdec.LnkDocType == OBJTYPE_DECISION )
    isDecision = true;
  end;

  var IsHeadOffice : bool = GetIsHeadOffice(wlregdec.OriginatorID);

  if( isReq and (wlregdec.LnkReqType == FNS_REQTYPE_BANK_WITH_DEPS) and IsHeadOffice )
    ТипСправ = "3";
  elif( ( ( isReq and ( wlregdec.LnkReqType == FNS_REQTYPE_BANK_WITHOUT_DEPS ) ) or isDecision ) and IsHeadOffice )
    ТипСправ = "4";
  elif( isReq and InList( wlregdec.LnkReqType, FNS_REQTYPE_BANK_WITH_DEPS, FNS_REQTYPE_BANK_WITHOUT_DEPS ) and not IsHeadOffice )
    ТипСправ = "5";
  elif( ( ( isReq and ( wlregdec.LnkReqType == FNS_REQTYPE_ONE_DEP ) ) or isDecision ) and not IsHeadOffice )
    ТипСправ = "6";
  end;

  if( ( ТипСправ == "" ) and isReq )
    var ErrMsg : string = "Не удалось заполнить поле " + FieldName + ". " +
                          "Для справки, формируемой в ответ на запрос, поле обязательно для заполнения. ";
    RunError( ErrMsg, ErrMsg );
  else
    ЗаписатьПолеЛог(FieldName, ТипСправ);
  end;

  return ТипСправ;
end;

private macro ПолучитьИдСубъектовДляПеречняФилиалов(OriginatorID : integer)
  var query : string =
    "select t_PartyID "
    "  from ddp_dep_dbt "
    " where t_NodeType = :DEPARTMENT_TYPE_FILIAL "
    "   and t_PartyID <> :OriginatorID "
    "   and t_Status <> 3 /*DEPARTMENT_STATUS_CLOSED*/ ";
  var params : TArray = makeArray
    ( SQLParam("DEPARTMENT_TYPE_FILIAL", 1),
      SQLParam("OriginatorID", OriginatorID) );

  return execSQLselect(query, params);
end;

// блоки "Самостоят"
macro ПереченьФилиалов(ТипСправ : string, OriginatorID : integer)
  if(ТипСправ == "3")
    var rs = ПолучитьИдСубъектовДляПеречняФилиалов(OriginatorID);

    while(rs and rs.moveNext())
      СведенияОБанке("Самостоят", rs.value("t_PartyID"));
    end;
  end;
end;

macro СведенияОНалоговомОргане(BlockName, wlregdec)
  var Error = "";
  if (not WriteTaxInstitution(wlregdec.recipientID, wlregdec.RecipientName, wlregdec.RecipientCode, "СвНО", @Error))
    RunError(Error);
  end;
end;

macro ПлательщикЮЛ(BlockName, wlregdec)
  WritePayerInst(BlockName, wlregdec.ClientID, wlregdec.ClientINN, wlregdec.ClientKPP, wlregdec.ClientName);
end;

macro ПлательщикИП(BlockName, wlregdec)
  WritePayerPersnEmployer(BlockName, wlregdec.ClientID, wlregdec.ClientINN, wlregdec.ClientName);
end;

macro ПлательщикФЛ(BlockName, wlregdec)
  WritePayerPersnShortData(BlockName, wlregdec.ClientID, wlregdec.ClientINN, wlregdec.ClientName);
end;

macro ЗаписатьПлательщикаВСправкуФНС(wlregdec)
  if( wlregdec.ClientType == RO_CLIENTTYPE_INST )
    ПлательщикЮЛ("ПлЮЛ", wlregdec);
  elif( wlregdec.ClientType == RO_CLIENTTYPE_PERSN_EMPLOYER )
    ПлательщикИП("ПлИП", wlregdec);
  elif( wlregdec.ClientType == RO_CLIENTTYPE_PERSN )
    ПлательщикФЛ("ПлФЛ", wlregdec);
  end;
end;

macro ЗаписатьПредБанка(BlockName, DepPartyID, IsIFNS)
  var tmpstr = "";
  var rs_dp_dep = getRsDpDepByPartyID(DepPartyID);
  rs_dp_dep.moveNext();
  StartBlockLogXML(BlockName);
  GetDprtStringRegValForOPENAC( IfThenElse(IsIFNS, "СПРАВКИ_ИФНС_ДОЛЖ", "365-П\\СООБ365П_ДОЛЖ"), rs_dp_dep.value("t_Code"), @tmpstr );
  ЗаписатьПолеЛог("Должность", tmpstr);

  GetDprtStringRegValForOPENAC( IfThenElse(IsIFNS, "СПРАВКИ_ИФНС_ФИО", "365-П\\СООБ365П_ФИО"), rs_dp_dep.value("t_Code"), @tmpstr );
  var FIO = split(tmpstr, " ");
  WriteFIO("ФИО", FIO[0], FIO[1], FIO[2]);
  FinishBlockLogXML(BlockName);
end;

macro ЗаписатьНаимПодтв(wlregdec)
  var НаимПодтв : string = "";

  var rs : RsdRecordset = execSQLselectPrm
    ( "select wlsess.t_FileName "
      "  from dwlmeslnk_dbt wlmeslnk1, dwlmes_dbt wlmes1, dwlresprm_dbt wlresprm, "
      "       dwlmeslnk_dbt wlmeslnk, dwlmes_dbt wlmes, dwlsess_dbt wlsess "
      " where wlmeslnk1.t_ObjID = :LnkDocID "
      "   and wlmeslnk1.t_ObjKind = :LnkDocType "
      "   and wlmes1.t_MesID = wlmeslnk1.t_MesID "
      "   and wlresprm.t_InitialMesID = wlmes1.t_MesID "
      "   and wlresprm.t_ErrCode in (0, 8) "
      "   and wlmeslnk.t_ObjID = wlresprm.t_ResultID "
      "   and wlmeslnk.t_ObjKind = :OBJTYPE_WLD_MESDEFECT "
      "   and wlmes.t_MesID = wlmeslnk.t_MesID "
      "   and wlsess.t_SessionID = wlmes.t_SessionID ",
      SQLParam("LnkDocID", wlregdec.LnkDocID),
      SQLParam("LnkDocType", wlregdec.LnkDocType),
      SQLParam("OBJTYPE_WLD_MESDEFECT", OBJTYPE_MESDEFECT)
    );

  if(rs and rs.moveNext())
    НаимПодтв = rs.value(0);

    if(НаимПодтв)
      var FName : string = "", FExt : string = "";
      SplitFile(НаимПодтв, FName, FExt);
      НаимПодтв = FName;
      if(FExt)
        НаимПодтв = НаимПодтв + FExt;
      end;
    end;

  else
    var sObjID : string = makeObjectID(OBJTYPE_FNSINFO, NULL, wlregdec);
    НаимПодтв = ReadNoteForObject(OBJTYPE_FNSINFO, sObjID, NOTEKIND_FNSINFO_FILENAME_PB);
  end;

  if(НаимПодтв)
    var xmlIndx = Index( НаимПодтв, ".xml" );
    if( ( xmlIndx > 0 ) and ( StrLen( НаимПодтв ) == xmlIndx + 3 ) )
      НаимПодтв = SubStr( НаимПодтв, 1, xmlIndx - 1 );
    end;
    ЗаписатьПолеЛог("НаимПодтв", НаимПодтв);
  end;
end;

private macro IsSentPB( regdec, fnNote:@string ) : bool

  execStoredFunc( "WLD_FNSANS.clearViewParam", V_UNDEF );
  execStoredFunc( "WLD_FNSANS.addObjectID", V_UNDEF, makeArray( SQLParam( "p_ObjectID", regdec.LnkDocID )) );
  execStoredFunc( "WLD_FNSANS.addObjectType", V_UNDEF, makeArray( SQLParam( "p_ObjectType", regdec.LnkDocType )) );

  
  var isRetOk = false;
  var note : string = "";
  
  var query : string =
    "select 1 "
    "  from dwlresprm_dbt rsp, dwlfnsans_dbt ans "
    " where rsp.t_State >= :WLD_STATUS_RESPRM_SEND "
    "   and rsp.t_Type = :WLD_TYPE_RESPRM_DELIVERY "
    "   and rsp.t_ResultID = ans.t_AnswerID "
    "   and ans.t_AnswerType = :OBJTYPE_WLD_MESDEFECT ";

  var params = makeArray
    ( SQLParam("WLD_STATUS_RESPRM_SEND", WLD_STATUS_RESPRM_SEND),
      SQLParam("WLD_TYPE_RESPRM_DELIVERY", WLD_TYPE_RESPRM_DELIVERY),
      SQLParam("OBJTYPE_WLD_MESDEFECT", OBJTYPE_MESDEFECT)
    );

  var rs = execSQLselect(query, params);
  if(rs and rs.moveNext())
    isRetOk = true;
  end;

  if( not isRetOk )
    if( regdec.LnkDocID > 0 )
      // Или в примечании объекта wlregdec 
      note = readNoteForObject(OBJTYPE_FNSINFO, 
                               makeObjectID(OBJTYPE_FNSINFO, NULL, regdec), 
                               NOTEKIND_FNSINFO_FILENAME_PB);  
      if( strlen( note ) > 0 )
        fnNote = note;
        isRetOk = true;
      else
        fnNote = "";
        isRetOk = false;
      end;
    else
      isRetOk = true;
    end;
  end;

  return isRetOk;
end;

private macro GetMessageDepsPartyID( regdec ): TArray

  var arr: TArray = TArray();
  
  var query : string =
    " select t_Department " + 
    "  from dwlacclnk_dbt " +
    " where t_ObjectID   = :ObjID " +
    "   and t_ObjectType = :ObjType ";
  if( regdec.LnkReqType == FNS_REQTYPE_BANK_WITH_DEPS )
    query = query + " and t_Department = ( select t_Code from ddp_dep_dbt where t_PartyID = :OriginatorID ) ";
  end;
    query = query + " group by t_Department ";

  var params : TArray = makeArray
    ( SQLParam( "ObjID", regdec.DecisionID ),
      SQLParam( "ObjType", OBJTYPE_FNSINFO ) );
  if( regdec.LnkReqType == FNS_REQTYPE_BANK_WITH_DEPS )
    params[params.size] = SQLParam( "OriginatorID", regdec.OriginatorID );
  end;

  var rs = execSQLselect( query, params );

  while( rs and rs.moveNext() )
    var rsd = execSQLSelect("select t_PartyID from ddp_dep_dbt where t_Code = " + int( rs.value(0) ) );
    if( rsd and rsd.moveNext() )
      arr[arr.size] = rsd.value(0);
    end;
  end;

  if( arr.size() == 0 )
    arr[arr.size] = regdec.OriginatorID;
  end;

  return arr;

end;


// Проверки справок ФНС
macro CheckObj_FNSINFO_Xml( regdec )

  var Code = "", Error = 0, ErrMsg : string = "";
  var MesDepartments: TArray;

  var PartyName : string = "";
  var FullINN   : string = "";
  var ИНН       : string = "";
  var КПП       : string = "";

  var OriginatorDprt : integer = -1;

  MesDepartments = GetMessageDepsPartyID( regdec );

  for( var department, MesDepartments )
    PartyName = FullINN = ИНН = КПП = "";
    OriginatorDprt = -1;

    // Наименование филиала
    PartyName = Bnk_GetPartyShortName( department );
    if( not PartyName )
      ErrMsg = string( "Для ", ПолучитьКодСубъекта( department, PTCK_BIC ), " не задано сокращенное наименование " );
      RunError(ErrMsg, ErrMsg);
    end;  

    /*ИНН филиала*/
    FullINN = ПолучитьКодСубъекта( department, PTCK_INN, Error, 0 );
    ИНН = RemoveKPP( FullINN );
    if( Error or (ИНН == "") )
      ErrMsg = string("Для ", PartyName, " не задан ИНН");
      RunError(ErrMsg, ErrMsg);
    end;

    /*Количество символов в ИНН филиала*/
    if(strlen(ИНН) != 10)
      ErrMsg = string( "Для ", PartyName, " задано неверное значение ИНН" );
      RunError(ErrMsg, ErrMsg);
    end;

    /*КПП филиала*/
    КПП = RemoveINN( FullINN );
    if( КПП == "" )
      ErrMsg = string("Для  ", PartyName, " не задан КПП");
      RunError(ErrMsg, ErrMsg);
    end;

    /*Количество символов в КПП филиала*/
    if(strlen(КПП) != 9)
      ErrMsg = string("Для ", PartyName, " задано неверное значение КПП");
      RunError(ErrMsg, ErrMsg);
    end;

    // филиал, связанный с субъектом department 
    OriginatorDprt = GetDepIdByPartyId( department );

    /*Регистрационный номер банка*/
    Code = ПолучитьКодСубъекта( department, PTCK_BANKREGNUM, Error );

    // Регистрационный номер филиала
    if( not IsHeadDprt(OriginatorDprt) )
      if( not GetSymbolsAfterSlash(Code) )
        ErrMsg = string("Для ", PartyName, " не задан порядковый номер филиала");
        RunError(ErrMsg, ErrMsg);
      end;
    end;  

    /*БИК филиала*/
    ПолучитьКодСубъекта( department, PTCK_BIC, Error, 1, 1);
    if(Error)
      ErrMsg = string("Для ", PartyName, " не задан БИК");
      RunError(ErrMsg, ErrMsg);
    end;
    
  end;  
  
  //--------------------------------------------------------------------------
  // Client
  record ClientParty(party);
  if (regdec.ClientID > 0)
    if( ПолучитьСубъекта(regdec.ClientID, ClientParty) )
      RunError(string("Не найден налогоплательщик ", regdec.ClientID));
    end;

    Code = ПолучитьКодСубъекта( regdec.ClientID, PTCK_CONTR, Error);
    if(Error)
      Code = "";
    end;
  else
    Code = "";
  end;
        
  /*Наименование клиента*/
  var ClientName : string = regdec.ClientName;
  if( not  ClientName )
    RunError(string("Для клиента ", Code, " не задано наименование"));
  end;

  if(regdec.ClientINN == "")
    ErrMsg = string("Для ", ClientName, " не задан ИНН");
    RunError(ErrMsg, ErrMsg);
  end;

  /*Количество символов в ИНН клиента*/
  if(regdec.ClientID > 0)
    if( ((ClientParty.LegalForm == PTLEGF_INST) and 
         (ClientParty.NotResident != "X") and 
         (strlen(regdec.ClientINN) != 10)
        ) or 
        ((ClientParty.LegalForm == PTLEGF_INST) and 
         (ClientParty.NotResident == "X") and 
         (strlen(regdec.ClientINN) != 5 ) and (strlen(regdec.ClientINN) != 10)
        ) or 
        ((ClientParty.LegalForm == PTLEGF_PERSN) and (strlen(regdec.ClientINN) != 12)) )
      ErrMsg = string("Для ", ClientName, " задано неверное значение ИНН");
      RunError(ErrMsg, ErrMsg);
    end;
  end;
  
  if( strlen(regdec.ClientINN) == 10 ) 
    /*КПП клиента*/
    if (regdec.ClientKPP == "")
      ErrMsg = string("Для ", ClientName, " не задан КПП");
      RunError(ErrMsg, ErrMsg);
    end;

    /*Количество символов в КПП клиента*/
    if ( strlen(regdec.ClientKPP) != 9 )
      ErrMsg = string("Для ", ClientName, " задано неверное значение КПП");
      RunError(ErrMsg, ErrMsg);
    end;
  end;

  /*Сотрудник, подписывающий сообщение*/
  var tmpstr = "";
  Error = GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", OriginatorDprt, @tmpstr );
  if( Error or ( Trim(tmpstr) == "") )
    RunError(string("Не задана должность сотрудника, подписывающего сообщения в ФНС"));
  end;
  Error = GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО", OriginatorDprt, @tmpstr );
  tmpstr = Trim(tmpstr);
  if( Error or (tmpstr == "") or (index(tmpstr, " ") == 0) )  
    RunError(string("Не заданы ФИО сотрудника, подписывающего сообщения в ФНС"));
  end;
  
  /*Имя файла исходного сообщения*/ 
  var note : string = readNoteForObject(OBJTYPE_FNSINFO, 
                                        makeObjectID(OBJTYPE_FNSINFO, NULL, regdec), 
                                        NOTEKIND_FNSINFO_INITIALFILENAME);
  if(note)
    /*Количество символов в имени файла*/
    if(strlen(note) != 31)
      RunError("Неверно задано имя файла запроса ФНС или решения рег. органа");
    end;
  else
    if( (regdec.LnkDocType == OBJTYPE_DECISION) and (regdec.LnkDocDate == date(0,0,0))
        or
        (regdec.LnkDocType == OBJTYPE_REQ) and (not regdec.LnkDocExtID)
      )
      RunError("Невозможно определить имя файла исходного запроса. Задайте имя файла в примечании или идентификатор запроса в справке/выписке");
    end;
  end;

  /*Символы в  номере */
  if ( (strlen(regdec.Number) > 6) or (not StrIsNumber(regdec.Number)) )
    RunError("Неверно задан номер справки (выписки)");
  end;

  /*Символы в номере связанного объекта*/
  if ( (strlen(regdec.LnkDocNumber) > 6) or (not StrIsNumber(regdec.LnkDocNumber)) )
    RunError("Неверно задан номер связанного объекта");
  end;

  // Задан тип исходного запроса
  if( (regdec.LnkDocType == OBJTYPE_REQ) and not regdec.LnkReqType )
    RunError("Не задан тип исходного запроса, невозможно определить тип справки (выписки)");
  end;
  
  // Отправлено подтверждение P1 для связанного объекта
  var pbFnNote : string = "";
  if( not IsSentPB( regdec, @pbFnNote ) )
    ErrMsg = "Не сформировано или не отправлено подтверждение (PB1) о принятии банком исходного запроса/решения, поступившего в электронной форме. Невозможно заполнить реквизит сообщения \"НаимПодтв\"";
    RunError(ErrMsg, ErrMsg);
  end;

  if( pbFnNote and ( not (SubStr(pbFnNote, 1, 4) == "PB1_") ) )
    ErrMsg = "Имя файла подтверждения (PB1) в примечании \"Имя файла подтверждения PB1\" указано неверно";
    RunError(ErrMsg, ErrMsg);
  end;
  
  return TRUE;

  OnError(er) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    //ExeptionMessage(er);
    return FALSE;

end;

macro GetAccRestForFnsInfoOS(LnkDocType : integer, AccLnkRestIn : money, AccLnkRestOut : money) : money

  var ShowRest : integer = 0;
  var RegPath : string = RegOpenAcc + "ПОКАЗЫВАТЬ_ОСТАТОК\\" + ifThenElse(LnkDocType == OBJTYPE_REQ, "ОТВЕТ_ЗАПРОС", "ОТВЕТ_РЕШЕНИЕ" );
  var err : integer = 0;
  GetRegistryValue(RegPath, V_INTEGER, ShowRest, err);
  if (err)
    ShowRest = 0;
  end;

  return IfThenElse(ShowRest == 1, AccLnkRestIn, AccLnkRestOut);
end;
