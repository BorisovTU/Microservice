/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по "Луч" MF036                                       */
/*                                                                          */
/*  Имя файла: raygm036.mac                                                 */
/*  Создан:  30.03.09                                                       */
/****************************************************************************/

import "raygenmes.mac";

macro GenMes( addrMes, addrbuf )
  FILE Party( party ) key 0;
  file llval(llvalues) key 0;
  RECORD DpBuf( dpmsginf );
  var    msginf, msgdoc, msgParty;  
  var    error,PartyCode, ProcessingCode,field_value;

  SetBuff( wlmes,  addrMes );
  SetBuff( DpBuf,  addrbuf );
  
  msginf = RsbDepoInfoMessage(DpBuf.MessageID);

  if ( valtype(msginf)==V_UNDEF )
     RunError( "|Не определен объект класса" );
  end;

  PrintLog(2,"Генерация сообщения по МF010");

  /*Блок ORDER_HEADER*/
  Write_ORDER_HEADER_Block( msginf );

  ProcessingCode = msginf.ProcessingCode;

  /* Блок MF036 */
  ЗаписатьПолеЛогВБлок( MF036_Block, BeginOfBlock, MF036_Block );


  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_DEAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*deponent_c*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( deponent_c_Field, PartyCode );
    else
      if((ProcessingCode =="36") or (ProcessingCode== "38"))
        RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
      end;
    end;
    /*dep_acc_c*/
    ЗаписатьПолеЛогСПроверкой( dep_acc_c_Field, msgParty.Account );
    /*sec_c*/
    ЗаписатьПолеЛогСПроверкой( sec_c_Field, msgParty.Partition );    
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (DEAG)" );
  end; 

  /*client_cod*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
  if ( valtype(msgParty)!=V_UNDEF )
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( client_cod_Field, PartyCode );
    end;
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_PSET);
  if ( valtype(msgParty)!=V_UNDEF )
    /*reestr_cod*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛогСПроверкой( reestr_cod_Field, PartyCode );
    else
      if((ProcessingCode =="38") or (ProcessingCode== "38/1"))
        RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
      end;
    end;
    /*reestr_bic*/
    if( not ((ProcessingCode == "38") or (ProcessingCode == "38/1")))
      ЗаписатьПолеЛог( reestr_bic_Field, msgParty.PartyCode );
    end;
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
  if ( valtype(msgParty)!=V_UNDEF )
    /*pr_jur_or_ind*/
    if( msgParty.LegalForm != "" )
      ЗаписатьПолеЛог( pr_jur_or_ind_Field, msgParty.LegalForm);
    end;
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*receiver_code*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( receiver_code_Field, PartyCode );
    end;
    /*recv_acc_c*/
    ЗаписатьПолеЛог( recv_acc_c_Field, msgParty.Account);
    /*full_name*/
    party.PartyID = msgParty.PartyID;
    if ( getEQ(party) )
      ЗаписатьПолеЛог( full_name_Field, party.ShortName);
    end;
    /*person_bic*/
    ЗаписатьПолеЛог( reestr_bic_Field, msgParty.PartyCode );
  end;


  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
  if ( valtype(msgParty)!=V_UNDEF )
    /*pr_client_code*/
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );
    if( not error )
      ЗаписатьПолеЛог( pr_client_code_Field, PartyCode );
    end;
    /*pr_client_name*/
    party.PartyID = msgParty.PartyID;
    if ( getEQ(party) )
      ЗаписатьПолеЛог( pr_client_name_Field, party.ShortName);
    end;
    /*pr_client_bic*/
    ЗаписатьПолеЛог( pr_client_bic_Field, msgParty.PartyCode );
    /*pr_client_acc*/
    ЗаписатьПолеЛог( pr_client_acc_Field, msgParty.Account);
  end;

  /*promise_ch*/
  if(msginf.HasCharge == "X")
    ЗаписатьПолеЛог( promise_ch_Field, "Y");
  else
    ЗаписатьПолеЛог( promise_ch_Field, "N");
  end;

  /*oper_date*/
  if(msgInf.SettlementDate != date(0,0,0))
    ЗаписатьПолеЛог( oper_date_Field, YYYYMMDD(msgInf.SettlementDate) );
  end;

  /*need_cvit*/
  if( not((ProcessingCode =="36") or (ProcessingCode== "38/1")))
    if(msginf.NeedCounterDraft == "X")
      ЗаписатьПолеЛог( need_cvit_Field, "Y");
    else
      ЗаписатьПолеЛог( need_cvit_Field, "N");
    end;
  end;

  /*seсurity_c*/         
  ЗаписатьПолеЛог( security_c_Field, msginf.SecurityCode);
  /*sequrity_q*/         
  ЗаписатьПолеЛог( security_q_Field, msginf.QuantitytoBeSettled);

  msgParty = GetMessageParty(msginf,DPMSGPAT_DEPONENT,0);
  if ( valtype(msgParty)!=V_UNDEF )
    /*sec_price*/  
    ЗаписатьПолеЛог(sec_price_Field, msgParty.DealAmount);
    /*curr_code*/  
    ClearRecord(f_wlfininstr);
    f_wlfininstr.FIID = msgParty.DealAmountCurrency;
    if( getEQ( f_wlfininstr ))
      ЗаписатьПолеЛог( curr_code_Field,  f_wlfininstr.Ccy);
    end;
  end;

  /*deponent_bic*/    
  msgParty = GetMessageParty(msginf, DPMSGPAT_SECURITIES, OBJTYPE_MSGPATYTP_BUYR );
  if ( valtype(msgParty)!=V_UNDEF )
    ЗаписатьПолеЛог( deponent_bic_Field, msgParty.PartyCode);
  end;

  /*sequrity_isin*/   
  ЗаписатьПолеЛог( security_isin_Field, FillISIN(msginf));  

  /*Блок documents*/
  if( not УстановитьКонтекстБлока( documents_Block ) )
    RunError("|Ошибка при установке контекста блока "+documents_Block);
  end;
  ЗаписатьПолеЛог( BeginOfBlock, documents_Block ) ;

  msginf.RewindMessageDocument();
  msgdoc = msginf.GetMessageDocument();
  while( valtype(msgdoc)!=V_UNDEF )
    if(msgdoc.DocType == DPMSGDOC_GROUND)
       /*Блок document*/
      if( not УстановитьКонтекстБлока( document_Block ) )
        RunError("|Ошибка при установке контекста блока "+document_Block);
      end;
      ЗаписатьПолеЛог( BeginOfBlock, document_Block ) ;

      llval.List = OBJTYPE_KINDDOC;
      llval.Element = msgdoc.DocumentKind;
      if ( not getEQ(llval) )
         RunError("|Не найдена запись в справочнике видов документов");
      end;
      /*doc_name*/
      ЗаписатьПолеЛог( doc_name_Field, llval.Name );
      /*doc_num*/
      ЗаписатьПолеЛог( doc_num_Field, msgdoc.DocumentNumber );
      /*doc_date*/
      ЗаписатьПолеЛог( doc_date_Field, YYYYMMDD(msgdoc.DocumentDate) );

      ЗаписатьПолеЛог( EndOfBlock, document_Block ) ;
      if( not УстановитьКонтекстБлока( ".." ) )
         RunError("|Ошибка при установке контекста блока ..");
      end; 
      /*Конец блока document*/  
    end;
    msgdoc = msginf.GetMessageDocument();

  end;

  ЗаписатьПолеЛог( EndOfBlock, documents_Block ) ;
  if( not УстановитьКонтекстБлока( ".." ) )
     RunError("|Ошибка при установке контекста блока ..");
  end; 
  /*Конец блока documents*/



  ЗаписатьПолеЛог( EndOfBlock, MF036_Block );
  /* Конец блока MF036*/

end;

macro CheckMes( addrMes )
  var field_name, field_value, ProcessingCode = "";
  SetBuff( wlmes, addrMes );

  while( СчитатьПоле( field_name, field_value ) )
    if(field_name == order_t_id_Field)
      ProcessingCode = field_value;
    end;
    /*контроль на русские буквы*/
    if( (field_name == deposit_c_Field ) or
        (field_name == contrag_c_Field ) or
        (field_name == reestr_cod_Field) or
        (field_name == deponent_c_Field) or
        (field_name == dep_acc_c_Field)  or
        (field_name == sec_c_Field) )
      if(not CheckForRus( field_name, field_value, true))
        return false;
      end;
    elif(field_name == promise_ch_Field)
      if( (field_value != "Y") and (field_value != "N") )
        std.msg(string("В поле '",field_name,"' недопустимые символы"));
        return false;      
      end;
    /*контроль по коду операции*/
    elif(field_name == need_cvit_Field)
      if((ProcessingCode == "36") or (ProcessingCode == "38/1"))
        std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
        return false;            
      elif(( field_value != "Y" ) and ( field_value != "N" ))
        std.msg(string("В поле '",field_name,"' недопустимые символы"));
        return false;      
      end;
    end;
  end;

  
  if((ProcessingCode == "36") or (ProcessingCode == "38"))
    if  (not ПолучитьПоле(wlmes.MesID, deponent_c_Field, field_value))
      field_name = deponent_c_Field;
    elif(not ПолучитьПоле(wlmes.MesID, sec_c_Field, field_value))
      field_name = sec_c_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end;    
  end;

  if((ProcessingCode == "38") or (ProcessingCode == "38/1"))
    if  (not ПолучитьПоле(wlmes.MesID, reestr_cod_Field, field_value))
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",reestr_cod_Field,"' должно быть заполнено"));
      return false;      
    end;    
  end;

  if((ProcessingCode == "36") or (ProcessingCode == "38/1"))
    if  (not ПолучитьПоле(wlmes.MesID, need_cvit_Field, field_value))
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",need_cvit_Field,"' должно быть заполнено"));
      return false;      
    end;    
  end;

  return true; 

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;