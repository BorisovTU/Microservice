/*
  $Name: ufpr213.mac
  $Module: Межбанковские расчеты
  $Description: Печатная форма сообщения ED213 транспорта УФЭБС
*/

/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Печатная форма сообщения ED213 транспорта УФЭБС                          */
/*                                                                          */
/*  Имя файла: ufpr213.mac                                                  */
/*  Создан:    15.10.14                                      Чукина Т.А.    */
/****************************************************************************/

import MesInter, "wlglobal.mac", "wluftool.mac", "prpmdmi_lib.mac", "bnk_ptlib.mac";

private class (TPayDemPrnInfo) TPayDemPrnInfoByMessage(xml : object)

  private macro ReadDate(node : object, AttrName : string, path : string) : date
    var AttrDate : date = date(0, 0, 0);

    var AttrStr : string = ReadOptinalAttribute(node, AttrName, path);
    if(AttrStr)
      AttrDate = ToDate(AttrStr);
    end;

    return AttrDate;
  end;

  private macro GetBankInHierarchy(BIC : string, CorrAcc : string, TableName : string) : integer
    var BankID : integer = -1;

    var ColName : string = "", ParentColName = "";
    if(TableName == "ddp_dep_dbt")
      ColName = "t_Code";
      ParentColName = "t_ParentCode";
    elif(TableName == "dparty_dbt")
      ColName = "t_PartyID";
      ParentColName = "t_Superior";
    else
      RunError("Ошибка программирования: неверный параметр TableName = " + TableName);
    end;

    var query : string = 
      " select t.t_PartyID " +
      "   from " + TableName + " t ";
    if(CorrAcc)
      query = query + ", dbankdprt_dbt bankdprt "
      "  where bankdprt.t_PartyID = t.t_PartyID "
      "    and bankdprt.t_CorAcc = :CorAcc ";
    end;
    query = query + 
      " start with t.t_PartyID in " +
      "    ( select ptc.t_PartyID " +
      "        from dpartcode_dbt ptc " +
      "       where ptc.t_Code = :CodeBIC " +
      "         and ptc.t_CodeKind = :PTCK_BIC " +
      "    ) " +
      "  connect by prior t." + ColName + " = t." + ParentColName +
      "         and not exists ( select 1 " +
      "                            from dpartcode_dbt ptc1 " +
      "                           where ptc1.t_PartyID = t.t_PartyID " +
      "                             and ptc1.t_CodeKind = :PTCK_BIC1 " +
      "                        ) ";
    var params : TArray = TArray();
    if(CorrAcc)
      params[ params.size ] = SQLParam("CorAcc", CorrAcc);
    end;
    params[ params.size ] = SQLParam("CodeBIC", BIC);
    params[ params.size ] = SQLParam("PTCK_BIC", PTCK_BIC);
    params[ params.size ] = SQLParam("PTCK_BIC1", PTCK_BIC);

    var rs : RsdRecordset = execSQLselect(query, params);
    if(rs and rs.moveNext())
      BankID = rs.value("t_PartyID");
    end;

    return BankID;
  end;

  private macro GetBankName(BIC : string, CorrAcc : string) : string
    var BankName : string = "";

    var BankID : integer = GetBankInHierarchy(BIC, CorrAcc, "ddp_dep_dbt");
    if(BankID <= 0)
      BankID = GetBankInHierarchy(BIC, CorrAcc, "dparty_dbt");
    end;

    if(BankID > 0)
      BankName = Bnk_GetPartyName(BankID);
    end;

    return BankName;
  end;

  private macro GetPartyProperties(xml : object, type : string) : TPartyProperties
    var Props : TPartyProperties = TPartyProperties(type);

    var Tag : string = IfThenElse(type == "Payer", "Payer", "Payee");
    Props.Name = ReadNodeText(xml, Tag + "/Name", false);
    Props.INN = ConstructINN( ReadOptinalAttribute(xml,"INN", Tag), ReadOptinalAttribute(xml,"KPP", Tag) );
    Props.Account = ReadOptinalAttribute(xml,"PersonalAcc", Tag);
    Props.BankBIC  = ReadAttribute( xml, "BIC", Tag + "/Bank", false );
    Props.BankCorrAccount  = ReadOptinalAttribute(xml,"CorrespAcc", Tag + "/Bank");
    Props.BankName = GetBankName(Props.BankBIC, Props.BankCorrAccount);

    return Props;
  end;

  // Конструктор
  InitTPayDemPrnInfo();

  Date_Into     = ReadDate(xml, "ReceiptDate");
  PayDate       = ReadDate(xml, "MaturityDate");
  chargeOffDate = ReadDate(xml, "ChargeOffDate");
  DocDate       = ReadDate(xml, "AccDocDate", "AccDoc");
  DocNumber     = ReadAttribute(xml, "AccDocNo", "AccDoc", false);
  PaymentKind   = PaytKindToPaymentKind( ReadOptinalAttribute(xml, "PaytKind"), "ED103" );
  AcceptTerm    = ReadAttribute(xml, "PaytCondition", false);
  AcceptPeriod  = ReadOptinalAttribute(xml, "AcptTerm");
  Amount        = moneyL( ReadAttribute(xml,"Sum", false) )/100;
  AmountStr     = RubToStrAlt(Amount);

  PayerProps    = GetPartyProperties(xml, "Payer");
  ReceiverProps = GetPartyProperties(xml, "Receiver");

  ShifrOper     = ReadAttribute(xml,"TransKind", false);
  Priority      = int( ReadAttribute(xml,"Priority", false) );
  UIN           = ReadOptinalAttribute(xml,"PaymentID");
  ground        = ReadNodeText(xml,"Purpose", false);
  I2_Date       = ReadDate(xml, "FileDate");

end;

macro PrintDoc( addrMes, MassCopy )

  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  // Чтение данных
  var xml : object = ReadXMLField(wlmes);

  var ed_nda : EDNoDateAuthor = GetEDNoDateAuthorFromXml(xml);
  var AuthorName : string = "";
  FillPartyByUIS_PTCK_UIS(ed_nda.EDAuthor, null, null, null, @AuthorName);

  var EDReceiver : string = ReadAttribute(xml, "EDReceiver", false),
      ReceiverName : string = "";
  FillPartyByUIS_PTCK_UIS(EDReceiver, null, null, null, @ReceiverName);

  // Сама печать данных
  [
    ED213 Запрос акцепта

    Номер ЭС:    #
    Дата ЭС:     #
    Составитель: ########## #
    Получатель:  ########## #

  ]( ed_nda.EDNo, 
     ed_nda.EDDate:f, 
     ed_nda.EDAuthor, AuthorName,
     EDReceiver, ReceiverName
   );

  var nodeED103 : object = GetChildNode(xml, "ED103");
  var PayDemPrnInfo = TPayDemPrnInfoByMessage( nodeED103 );
  if(not PrintPayDem1256(PayDemPrnInfo) )
    return FALSE;
  end;

  // Перенаправляем вывод обратно
  SetOutPut( OldName, true );

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 

  return TRUE;

  OnError(er)
    ExeptionMessage(er);
    return FALSE;
end;
