/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям SWIFT MT940                              */
/*                                                                          */
/*  Имя файла: swgd940.mac                                                  */
/*  Создан:    20.03.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgendoc.mac";

/* Выписка со счета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */
  Account          :string,        /* Корсчет */
  StatementNumber  :string,        /* Номер выписки */
  SequenceNumber   :integer,       /* Номер страницы выписки */
  OpeningBalance   :Balance,       /* Открывающий баланс */
  DocExtrLine      :TArray,//StatementLine, /* Строка выписки */
  InfExtrLine      :TArray,//string,        /* Информация владельцу счета */
  Information      :string,        /* Информация владельцу счета */
  ClosingBalance   :Balance,       /* Закрывающий баланс */
  AvailableBalance :Balance,       /* Доступные средства */
  ForwardAvailableBalance :Balance,/* Доступные средства на дату */
  FlagSetForwardBalance :integer,  /* Флаг доступных средств на дату */
  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank,          /* Получатель */

  /* Вспомогательная информация */
  DebNum           :integer,       /* Число дебетовых в выписке */
  DebTurn          :moneyl,        /* Оборот дебетовых */
  CredNum          :integer,       /* Число кредитовых в выписке */
  CredTurn         :moneyl,        /* Оборот кредитовых */
  Cln              :bool,          /*выписка клиентская*/
  IsCorschem       :bool;          /*Определилась корсхема или нет*/


  /* Начальная инициализация */
  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  StatementNumber  = "";
  SequenceNumber   = 0;
  DebNum = 0;
  CredNum = 0;
  DebTurn = moneyl(0);
  CredTurn = moneyl(0);
  FlagSetForwardBalance = 0;
  DocExtrLine = TArray();
  InfExtrLine = TArray();
  Information = "";
  Cln = False;  
  IsCorschem = False; 
end; /* class StatementMessage */

var MT940 : StatementMessage;

macro Fill20( TRF )
  MT940.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MT940.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT940.Account = Account;
  return TRUE;
end;

macro Fill28( Number, Page )
  MT940.StatementNumber = Number;
  MT940.SequenceNumber = Page;
  return TRUE;
end;

macro Fill60( DKFlag, DateBal, Cur, Sum )
  MT940.OpeningBalance = Balance( DKFlag, DateBal, Cur, Sum );
  return TRUE;
end;

/* Вставляем заголовок выписки - чтобы было к чему привязывать документы выписки */
macro InsertHead()
  wlhead.Account      = MT940.Account;
  wlhead.FIID         = MT940.OpeningBalance.FIID; 
  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account, wlhead.FIID, false ) )
    if (not(MT940.Cln))
        std.msg( String("Не определена схема расчетов для респондента по корсчету: ", MT940.Account) );
        return FALSE;
    end;
  else
    MT940.IsCorschem = TRUE;
    if( not ВставитьЗаголовокВыписки( wlhead ) )
      std.msg("Ошибка при вставке заголовка выписки");
      return FALSE;
    end;
  end;
  return TRUE;
end;

/* Вставка документа выписки, вынесено из цикла чтения, т.к. нужны данные, которые доступны будут после */
macro InsertStatementLine(DocExtrLine, Information)
  if( DocExtrLine.IsSet() )
    ClearRecord(wlconf);
    wlconf.DateValue  = DocExtrLine.DateVal;
    wlconf.Sum        = DocExtrLine.Amount;
    wlconf.Number     = DocExtrLine.AccRef;
    wlconf.RelatedRef = DocExtrLine.Ref;
    if ( wlconf.Number=="" ) 
       wlconf.Number = wlconf.RelatedRef;
    end;
    if ( DocExtrLine.DateInp == date(0,0,0) )
      wlconf.TransferDate = MT940.ClosingBalance.DateBal;//Wlhead.DateOut
    else
      wlconf.TransferDate = DocExtrLine.DateInp;
    end;
   
    wlconf.Description = substr( DocExtrLine.Details + "\n" + Information, 1, 215 );
    wlconf.MessageType = SubStr( DocExtrLine.TypeOper, 2, 3 );
    if( DocExtrLine.DKFlag == DEBET_ACCOUNT )
      wlconf.DKFlag = WL_DEBET;
      MT940.DebNum  = MT940.DebNum + 1;
      MT940.DebTurn = MT940.DebTurn + DocExtrLine.Amount;
    else
      wlconf.DKFlag = WL_CREDIT;
      MT940.CredNum  = MT940.CredNum + 1;
      MT940.CredTurn = MT940.CredTurn + DocExtrLine.Amount;
    end;
    if( not ВставитьДокументВыписки( wlconf ) )
      std.msg("Ошибка при сохранении документа выписки");
      return FALSE;
    end;
    return TRUE;
  end;
  return TRUE;
end;

macro Fill61( DocExtr:StatementLine )
  MT940.DocExtrLine[MT940.DocExtrLine.size] = DocExtr;
  MT940.InfExtrLine[MT940.InfExtrLine.size] = "";
  return TRUE;
end;

macro Fill62( DkFlag, DateBal, Cur, Sum )
  MT940.ClosingBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill64( DkFlag, DateBal, Cur, Sum )
  MT940.AvailableBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill65( DkFlag, DateBal, Cur, Sum )
  MT940.ForwardAvailableBalance = Balance( DkFlag, DateBal, Cur, Sum );
  MT940.FlagSetForwardBalance = 1;
  return TRUE;
end;

macro InserAvailableBalance
  if (not(MT940.IsCorschem))
      return TRUE;
  end;
  if ( MT940.FlagSetForwardBalance )
     ClearRecord(wlrest);
     wlrest.DateValue  = MT940.ForwardAvailableBalance.DateBal;
     wlrest.Sum        = MT940.ForwardAvailableBalance.Amount;
     if( MT940.ForwardAvailableBalance.DKFlag == DEBET_ACCOUNT )
        wlrest.DKFlag = WL_DEBET;
     else
        wlrest.DKFlag = WL_CREDIT;
     end;
     if( not ВставитьДоступныеСредства( wlrest ) )
        std.msg("Ошибка при сохранении доступных средств");
        return FALSE;
     end;
     MT940.FlagSetForwardBalance = 0;
  end;
  return TRUE;
end;

macro Fill86( value )
  MT940.InfExtrLine[MT940.InfExtrLine.size - 1] = value;
  return TRUE;
end;

macro Fill86_H( value )
  MT940.Information = value;
  return TRUE;
end;

/* Заполнение списка полей формы  MT940*/
var Fld20   = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21   = ТПолеФормы( RelatedReferenceField,              FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "" ),
    Fld25   = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld28C  = ТПолеФормы( StatementSequnceNumberCField,       FIELD_MANDATORY, "Read28",  "Fill28",  "" ),
    Fld60F  = ТПолеФормы( OpeningBalanceField_F,              FIELD_OPTIONAL,  "ReadBal", "Fill60",  "" ),
    Fld60M  = ТПолеФормы( OpeningBalanceField_M,              FIELD_OPTIONAL,  "ReadBal", "Fill60",  "InsertHead" ),
    Fld61   = ТПолеФормы( StatementLineField,                 FIELD_OPTIONAL,  "Read61",  "Fill61",  "" ),
    Fld86   = ТПолеФормы( InformationToAccountOwnerField,     FIELD_OPTIONAL,  "Read86",  "Fill86",  "" ),
    Fld62F  = ТПолеФормы( ClosingBalanceField_F,              FIELD_OPTIONAL,  "ReadBal", "Fill62",  "" ),
    Fld62M  = ТПолеФормы( ClosingBalanceField_M,              FIELD_OPTIONAL,  "ReadBal", "Fill62",  "" ),
    Fld64   = ТПолеФормы( ClosingAvailableBalanceField,       FIELD_OPTIONAL,  "ReadBal", "Fill64",  "" ),
    Fld65   = ТПолеФормы( ForwardAvailableBalanceField,       FIELD_OPTIONAL,  "ReadBal", "Fill65",  "InserAvailableBalance" ),
    Fld86_H = ТПолеФормы( InformationToAccountOwnerField,     FIELD_OPTIONAL,  "Read86",  "Fill86_H",  "" );

macro ConstructMT940( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  MT940.Cln = МожетЛиВыпискаБытьКлиентской(wlmes.RlsFormID);
  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )          
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          if ( (NeedRead) AND (fld.Name==InformationToAccountOwnerField) )
              count = count - 2;
          end;
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;
            if( fld.Name == InformationToAccountOwnerField )
              count = count - 2;
            elif( fld.Name ==  ForwardAvailableBalanceField )
              count = count - 1;
            end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if (MT940.IsCorschem)
    for(count, 0, MT940.DocExtrLine.size - 1, 1)
      result = InsertStatementLine(MT940.DocExtrLine[count], MT940.InfExtrLine[count]);
      if (not result)
        break;
      end;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT940.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT940.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT940 */

macro GenDoc( addrMes )
  MT940 = NULL;
  MT940 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация выписки по МТ940");

  ClearRecord(wlhead);

  if( not ConstructMT940( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if(MT940.IsCorschem)
      /* Заполнение учетных буферов */
      wlhead.Number       = MT940.StatementNumber;
      wlhead.Page         = MT940.SequenceNumber;
      wlhead.DateIn       = MT940.OpeningBalance.DateBal;
      wlhead.DateOut      = MT940.ClosingBalance.DateBal;
      wlhead.RestIn       = MT940.OpeningBalance.Amount;
      wlhead.RestOut      = MT940.ClosingBalance.Amount;
      wlhead.DebetNumDoc  = MT940.DebNum;
      wlhead.DebetTurn    = MT940.DebTurn;
      wlhead.CreditNumDoc = MT940.CredNum;
      wlhead.CreditTurn   = MT940.CredTurn;
      wlhead.Description  = substr( MT940.Information, 1,215 );

      if( not ОбновитьЗаголовокВыписки( wlhead ) )
        std.msg("Ошибка при обновлении заголовка выписки");
        return FALSE;
      end;
  else
      ClearRecord(wlinfo);
      /* Заполнение учетных буферов */
      wlinfo.TRN          = MT940.TRF;
      wlinfo.RelatedRef   = MT940.RelatedRef;

      /* Создатель - наш абонент */
      wlinfo.OriginatorID       = MT940.Sender.PartyID;
      wlinfo.OriginatorCodeKind = MT940.Sender.CodeKind;
      wlinfo.OriginatorCode     = MT940.Sender.CodeBank;
      wlinfo.OriginatorName     = MT940.Sender.Name;

      /* Получатель - наш банек */
      wlinfo.RecipientID       = {OurBank};
      wlinfo.RecipientCodeKind = MT940.Receiver.CodeKind;
      wlinfo.RecipientCode     = MT940.Receiver.CodeBank;
      wlinfo.RecipientName     = MT940.Receiver.Name;
      MT940.Information = string("Выписка по клиентскому счёту N ", MT940.Account);
      if( not ВставитьИнфСообщение( wlinfo, MT940.Information ) )
        std.msg("Ошибка при вводе информационного сообщения");
        return FALSE;
      end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;


