/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT-SB сигнатура 0SA                  */
/*                                                                          */
/*  Имя файла: swsd0SA.mac                                                  */
/*  Создан:    19.05.06                                    Гайворонский Е.Г.*/
/****************************************************************************/

import "sbgendoc.mac", rsd, oralib, likepy;

macro НайтиКодОтветаНаСообщение(answercode: string) :string

  var result = "Причина помещения в отбракованные: " + answercode;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_description from dwlcodes_dbt where t_algnum = 40 and t_code=:code";
  
  params = makeArray( SQLParam("code", answercode));
  rs = execSQLselect( select, params, FALSE );
  if ( rs and rs.MoveNext() )
     result = rs.value(0);
  end;
        
  return result;
end;

/* Релиз 0SA */
macro GenDoc( addrMes )
  var answercode, numberline, ErrCode, ErrDescription = "";
  var rs:object;
  var select:string;
  var params:TArray;

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по форме 0SA");

  answercode = SubStr(wlmes.RespProcessingDescr,1,3);
  numberline = SubStr(wlmes.RespProcessingDescr,4);

  select = "select t_mesID, t_state from dwlmes_dbt where t_direct=chr(0) and T_TRN=:RelatedRef and T_Department = :OperD";
  params = makeArray( SQLParam("RelatedRef", wlmes.RelatedRef),
                      SQLParam("OperD",{OperDprt} ));
  rs = execSQLselect( select, params, FALSE );
  if ( not rs.MoveNext() )
     std.msg( string("|Не найдено исходящее сообщение ", wlmes.RelatedRef) );
     return false;
  end;

  if ( rs.value(1)<30 )
     std.msg( string("|Исходящее сообщение ", wlmes.RelatedRef, " не отправлено") );
     return false;
  end;
  
  if ( answercode =="000" )
     if ( rs.value(1)>30 )
        return true;
     end;

     if ( not ВставитьПодтверждениеОДоставке(rs.value(0)) )
        std.msg( string("|Ошибка при переносе в доставленные сообшения ", wlmes.RelatedRef) ); 
        return false;
     end;
  else
     if(not GetElementAndNoteLLVALUES(OBJTYPE_WLRESCODE_SMFR, answercode, ErrCode, ErrDescription))
       ErrDescription = answercode;
     end;
     if ( not ОшибкаЛогическогоКонтроляСообщения(rs.value(0), НайтиКодОтветаНаСообщение(answercode) ) )
        std.msg( string( "Не удалось поместить в отбракованные сообщение ID = ", rs.value(0)) );
        return false;
     end;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;