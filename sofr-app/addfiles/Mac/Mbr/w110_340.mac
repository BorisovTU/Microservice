/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Куда поместить документ из списка отвергнутых                           */
/*  Имя файла: w110_340.mac                                                 */
/*  Создан:  8.10.02                                         Бердюкова.И.И. */
/****************************************************************************/

import  InsCarryDoc, OprInter, BankInter, RMInter, "wldoc.mac", "rmconst.mac" ;

RECORD Corschem( corschem );
var PaymentObj:RsbPayment;

const DEBET  = "Д",
      KREDIT = "К";

const Menu1 = "Возврат в АРМ";
const Menu2 = "В картотеку корсчета";
const Menu3 = "Возврат средств клиенту";
const Menu4 = "Отменить";
const FromRejected = "Переместить:";

macro ExecuteCaseStep( Kind_Operation, Number_Step, first, KindDoc )
  Var menu_item, DKFlag, SumKvit, Cancel, stat;
  var oldDlg;
  Array MenuStr;

  if ( PaymentObj.IsCredit() )
     DKFlag = KREDIT;
  else 
     DKFlag = DEBET;
  end;

  var FlagTrans = PaymentObj.IsTransit();  

  if( PaymentObj.ReceiverFIID == 0 ) /* Рублевые платежи */
     menu_item = Opr_MakeChoice(Menu1, Menu2, Menu3, Menu4, FromRejected);
      
     if ( menu_item == 0 ) /*Выполнить шаг "Возврат в АРМ"*/
       return string(""/*НомерШагаВозвратВАРМИзМБР*/);
     elif ( menu_item == 1 ) /*Выполнить шаг "Помещение в картотеку корсчета"*/
       if ( GetCorAcc(PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_CARDFILE)=="" )
         /* Если не задан счет работы с картотеками, */
         /* то такой платеж остается в списке отвергнутых  */
         MsgBox("Невозможно перемещение платежа в картотеку корсчета.|В схеме расчетов " + PaymentObj.OutCorschem + " не задан счет картотеки корсчета.");
         return 1;
       else
         return string(НомерШагаПомещениеКартКорсчетаМБР);
       end;
       
     elif ( menu_item == 2 ) /*Выполнить шаг "Возврат средств клиенту"*/
       /*Возврат не может быть осуществлён, если платёж является */
       /*частичной оплатой Картотеки №2*/
       if( PaymentObj.SubPurpose >0 )
         MsgBox("Невозможно сделать возврат частичной оплаты картотеки №2");
         return 1;
       end;  
       return string(НомерШагаВозвратСредствКлиенту);
     else 
       return 1; /*Отмена*/
     end;
  else /* Валютные платежи в картотеку корсчета не помещаются => и не предлагаем*/     
     menu_item = Opr_MakeChoice(Menu1, Menu3, Menu4, FromRejected);
     if( menu_item == 0 ) /*Выполнить шаг "Возврат в АРМ"*/
       return string(""/*НомерШагаВозвратВАРМИзМБР*/);
     elif ( menu_item == 1 ) /*Выполнить шаг "Возврат средств клиенту"*/
       return string(НомерШагаВозвратСредствКлиенту);
     else
       return 1;  /*Отмена*/
     end;
  end;
  return 0;
end;