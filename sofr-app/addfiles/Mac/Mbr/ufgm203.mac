/*
$Name:           ufgm203.mac
$Module:         Межбанковские расчеты
$Description:    Генерация сообщения ED203 транспорта УФЭБС
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED202 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm202.mac                                                  */
/*  Создан:    23.06.06                                  Гайворонский Е.Г.  */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac";

var ver_st = 1;

private macro GetAccField( wlreq )
  var Acc = "";
  if( wlreq.Corschem == -1 )
    return Acc;
  end;
  
  var select = " Select cor.t_CorAccount as CorAccount "
               "   From dcorschem_dbt cor "
               "  Where cor.t_Number = :Corschem "
               "    and cor.t_FIID = :FIID ";
  var rs = execSQLSelect( select, MakeArray( SQLParam( "Corschem", wlreq.Corschem ),
                                             SQLParam( "FIID", wlreq.FIID )
                                           ));
  if( rs and rs.MoveNext() )
    Acc = rs.value("CorAccount");
  end;
  
  return Acc;
  
end;

macro ВычислитьКод( Строка )
  var rs:object;
  var select:string;

  if( ver_st == 1 )
   if((StrLen(Строка) == 2)  And ( Index(Строка, "00") or 
                                   Index(Строка, "01") or 
                                   Index(Строка, "02") or 
                                   Index(Строка, "03") or 
                                   Index(Строка, "04") or 
                                   Index(Строка, "06") or 
                                   Index(Строка, "08") or 
                                   Index(Строка, "09") or 
                                   Index(Строка, "11") or 
                                   Index(Строка, "20")
     ))
      return true;
   else
      return false;
   end;
  elif( ver_st == 2 )
    select = "select t_code from dwlcodes_dbt where t_algnum = 42";
    rs = execSQLselect( select, null, false );
    while( rs and rs.MoveNext() )
      if( Index(Строка, rs.value(0)) )
        return true;
      end;
    end;
    return false;
  end;
end;

/* Заполнение полей сообщения-запроса на получение выписки ED210 */
macro GenMes( addrMes, addrReq )

  SetBuff( wlmes,  addrMes );
  SetBuff( wlReq,  addrReq );

  var strXml;
  var InitMesId:integer = 0;
  var IsRequestPacket:bool = false;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem: object;
  var iXml:object = ActiveX("Microsoft.XMLDOM");  
  var EDReceiver:string;
  
  mes = xml.createElement("ED203");
  
  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  
  
  EDReceiver = GetEDReceiver(wlreq.RecipientID, wlreq.RecipientcodeKind, wlreq.RecipientCode, "", 1);
  if (EDReceiver != "")
      mes.setAttribute("EDReceiver", EDReceiver);
  end;

  var Acc = GetAccField( wlreq );
  if( strlen(Acc) > 0 )
    mes.setAttribute("Acc", Acc);
  end;

  if(ВычислитьКод( wlreq.Queries ))  
     mes.setAttribute("StatusCode",  wlreq.Queries );
  else
     RunError( String("Задан непонятный запрос по группе ЭПД: '", wlreq.Queries, "'" ));
  end;

  mes.setAttribute("GroupInquiryCode", "1" );
  
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV2( addrMes, addrReq )
  ver_st = 2;
  return GenMes( addrMes, addrReq );
end;
