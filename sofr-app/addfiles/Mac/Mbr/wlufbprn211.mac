/************************************
*  Печать выписки УФЭБС ED211       *                                     
*  Имя файла:wlufbprn211.mac        *                                       
*************************************/

import  "wlmcgd.mac", "wluftool.mac";

macro РаспечататьПоле( xml)
var DCFlag, PersonalAccount="";

  DCFlag=ReadAttribute(xml,"DC");

  if(DCFlag==1)
    PersonalAccount=ReadAttribute(xml,"PayeePersonalAcc");
  elif(DCFlag==2)
    PersonalAccount=ReadAttribute(xml,"PayerPersonalAcc");
  end;

 [| ###### | ##########  | ##########    | #####       | #################### | ################## |
 ](
   ReadAttribute(xml,"EDNo", "EDRefID"):l, ReadAttribute(xml,"EDDate", "EDRefID"):l, ReadAttribute(xml,"EDAuthor", "EDRefID"):l,
   ReadAttribute(xml,"AccDocNo"):l, PersonalAccount:l, ( moneyL( ReadAttribute(xml,"Sum") )/100 ):l
  );

  return TRUE;  

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;


end;

/* Функция печати заголовка формы */
macro PrintHeader( xml )
var AbstractKindNum,AbstractKind="";
  std.RSBankHeader;
  [Номер ЭСИД в течение опердня:         ##########](ReadAttribute(xml,"EDNo"));  
  [Дата составления ЭСИД:                ##########](ReadAttribute(xml,"EDDate"));
  [Идентификатор составителя ЭСИД:       ##########](ReadAttribute(xml,"EDAuthor"));
  [№ счета:                              ####################](Trim( ReadAttribute(xml,"Acc") ));
  [Входящий остаток:                     ##################]  ((moneyL( ReadOptinalAttribute(xml,"EnterBal") )/100 ):l);
  [Исходящий остаток:                    ##################]  ((moneyL( ReadAttribute(xml,"OutBal") )/100 ):l);
  [Сумма лимита внутридневного кредита:  ##################]  ((moneyL( ReadOptinalAttribute(xml,"CreditLimitSum") )/100 ):l);
  [Сумма забронированных средств:        ##################]  ((moneyL( ReadOptinalAttribute(xml,"ReservedSum") )/100 ):l);
  [Дата, на которую формируется выписка: ##################]  (ReadAttribute(xml,"AbstractDate"));
  AbstractKindNum=ReadAttribute(xml,"AbstractKind");
  if(AbstractKindNum==0)
    AbstractKind="Окончательная выписка";
  elif(AbstractKindNum==1)
    AbstractKind="Промежуточная выписка";
  elif(AbstractKindNum==2)
    AbstractKind="Текущий остаток на счёте";
  elif(AbstractKindNum==3)
    AbstractKind="Свёрнутая выписка";
  else
    AbstractKind="Неизвестный тип";
  end;
  [Тип выписки:                          #######################](AbstractKind);


  println("");

  [+------------------------------------------------------------------------------------------------+
   | Номер  | Дата        | Идентификатор | № расчётн.  | Корреспондирующий    | Сумма ЭПД          |
   |  ЭПД   | составления | составителя   | документа   | счет                 |                    |
   |--------+-------------+---------------+-------------+----------------------+--------------------|
  ];
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
  
end;

macro PrintFoot(СуммаДокументов, Документы)
  var Sum_str; 

  Sum_str = RubToStrAlt(СуммаДокументов/100); /*Сумма прописью*/

  [|------------------------------------------------------------------------------------------------|
    Итого:  #################  #
    Документов: #
  ]( (СуммаДокументов/100 ):l, Sum_str:l, string(Документы):l );
end;

/* Функция печати форм */
macro PrintForm( addrMes, MassCopy )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка;
  var MsgTmpFileName, OldName;
  var Sum = $0, NumOfDoc = 0;
  FILE MsgTmpFile() txt;
  SetBuff( wlmes, addrMes );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения" ) );
     return false;
  end;
  
   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  /* Печатаем заголовок выписки */
  if( not PrintHeader( xml ) )
    return FALSE;
  end;

  // Последовательно считываем поля
  while( СчитатьПоле( XMLField, Строка) )
    if ( not xml.loadXML(Строка) )
       println( string( "Неверный формат сообщения" ) );
       return false;
    end;
   
    if(not РаспечататьПоле(xml))
      return false;
    end;

    Sum = Sum +  moneyL( ReadAttribute(xml,"Sum") ); //Сумма в копейках
    NumOfDoc = NumOfDoc + 1;    
    
  end;
  
  PrintFoot(Sum, NumOfDoc);
  
  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
