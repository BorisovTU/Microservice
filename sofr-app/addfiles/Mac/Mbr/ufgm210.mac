/*
  $Name:         ufgm210.mac
  $Module:       Межбанковские расчеты
  $Description:  описание
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED210 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm210.mac                                                  */
/*  Создан:    24.10.05                                  Фомченкова Л.Н.    */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac", "WLTOOLS.MAC";

var ver_st = 1;

/* Заполнение полей сообщения-запроса на получение выписки ED210 */
macro GenMes( addrMes, addrReq )

  SetBuff( wlmes,  addrMes );
  SetBuff( wlReq, addrReq );

  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem: object;
  
  mes = xml.createElement("ED210");

  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  /* Запрос выписки из счета. 
     1 - промежуточная выписка, 
     2 - текущий остаток на счете, 
     3 - свернутая выписка, 
     4 - отчет для выверки документов дня участников, 
     5 - извещение о задолженности по внутридневному кредиту 
     6 - отчет для выверки документов для ПУР в системе БЭСП*/
  if( (wlreq.SubKind < 1) OR (wlreq.SubKind > 6) )
    RunError( "|Тип запроса выписки должен быть в диапазоне значений от 1 до 6" );
  end;
  mes.setAttribute("AbstractRequest", wlreq.SubKind);
  /* Дата, на которую формируется выписка */
  if( wlreq.PmDateValue == date(0,0,0) )
    RunError( "|Дата, на которую формируется выписка не может быть нулевой" );
  end;
  mes.setAttribute("AbstractDate", YYYYMMDD(wlreq.PmDateValue));
  
  var Acc;
  if( (wlreq.FIID >= 0) and (wlreq.Corschem >= 0) )
    Acc = GetCorAcc( wlreq.FIID, wlreq.Corschem, CORS_ACC_CORACCOUNT );
    mes.setAttribute("Acc", TransTextField(Acc));
  end;

  var SessionID : string = getFldValFromQueries(wlreq.Queries, "SessionID:");
  if( ((wlreq.SubKind == 1) OR (wlreq.SubKind == 3)) AND SessionID )
    SessionID = substr(SessionID, 1, 1); // 1 символ
    if( StrIsNumber(SessionID) )
      mes.setAttribute("SessionID", SessionID);
    else
      RunError("Номер рейса, за который запрашивается выписка, должен являться числом в диапазоне от 0 до 9");
    end;
  end;

  var BIC;
  if( not Acc )
    BIC = GetPartyCode( wlreq.OriginatorID, PTCK_BIC );
    if( BIC )
      mes.setAttribute("BIC", TransTextField(BIC));
    else
      RunError("Для субъекта " + wlreq.OriginatorID + " не найден код вида 3" );
    end;
  elif( Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED210_ЗАПОЛНЯТЬ_BIC", V_BOOL, false) )
    BIC = GetBICofCorschemDepartment(wlreq.Corschem, wlreq.FIID);
    if( BIC )
      mes.setAttribute("BIC", TransTextField(BIC));
    else
      RunError("Для филиала корсхемы с номером " + wlreq.Corschem + " и валютой " + wlreq.FIID + " не найден код вида 3" );
    end;
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV2( addrMes, addrReq )
  ver_st = 2;
  return GenMes( addrMes, addrReq );
end;
