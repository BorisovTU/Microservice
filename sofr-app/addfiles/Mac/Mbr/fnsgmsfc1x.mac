/*
  $Name:         fnsgmsfc1x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SFC1 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SFC1 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 30.07.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac";

private const vers : integer = 510, // версия формата 5.10
              IsIndivid : bool = true,
              AccMsgKind : integer = ACCMSG_KIND_CHNG;

macro CheckObj( addrReq )
  record Req(reqchnga);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckContractNumber,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality,
                                              GenMesCheckNotMatchPropetis,
                                              GenMesCheckDULSizeCode
                                             );
  
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_CHNG );
end;

// Файл\Документ\СвСчет
private macro GetContractDate(SfContrDate : date) : string
  var SfContrDateStr : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    SfContrDateStr = ПолучитьПолеДляАннул( "ДатаЗаклДог" );
  else
    if( SfContrDate != date(0, 0, 0) )
      SfContrDateStr = DDpMMpYYYY(SfContrDate);
    else
      RunError("Не заполнена дата заключения договора");
    end;
  end;

  return SfContrDateStr;
end;

private macro WriteAccInfo(reqchnga)
  StartBlockLogXML("СвСчет");

  WriteFieldWithCheckAnnul( "НомСч", reqchnga.NewAccount );
  ЗаписатьПолеЛог         ( "ДатаОткрСч", GetAccOpenDateFldVal_BO(reqchnga, AccMsgKind) );
  WriteFieldWithCheckAnnul( "НомерДог", reqchnga.NewContractNumber );
  ЗаписатьПолеЛог         ( "ДатаЗаклДог", GetContractDate(reqchnga.SfContrDate) );

  FinishBlockLogXML("СвСчет");
end;

// Файл\Документ\СвСчСтар
private macro WriteOldAccInfo(reqchnga)
  StartBlockLogXML("СвСчСтар");

  WriteFieldWithCheckAnnul( "НомСчСтар", reqchnga.OldAccount );
  ЗаписатьПолеЛог         ( "ДатаИзмСч", GetAccChngDate(reqchnga) );

  FinishBlockLogXML("СвСчСтар");
end;

private macro IsDiffClient(Req) : bool
  if( (Req.ClientName != Req.ClientNameOld) or
      (Req.ClientINN != Req.ClientINNOld) or
      (Req.ClientOGRN != Req.ClientOGRNOld) or
      (Req.ClientBirthDate != Req.ClientBirthDateOld) or
      (Req.ClientBirthPlace != Req.ClientBirthPlaceOld) or
      (Req.ClientCardType != Req.ClientCardTypeOld) or
      (Req.ClientCardNum != Req.ClientCardNumOld) or
      (Req.ClientCardDate != Req.ClientCardDateOld)
    )
    return TRUE;
  end;

  return FALSE;
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record Req(reqchnga);
  SetBuff(Req, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SFC1");

  var ErrMsg : string = "";

  var CommonAccMsg : TCommonDataAccMsg = TCommonDataAccMsg
                                         ( Req.Department, 
                                           AccMsgKind, 
                                           vers,
                                           IsIndivid );

  var ClientID : integer = Req.ClientID;

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonAccMsg);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonAccMsg, "", "", ALLFININSTR, ClientID, OldMes.MesID, IsIndivid);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonAccMsg, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo_Ind(ClientID, Req.ClientType311, AccMsgKind, Req);

  // Файл \ Документ \ СвСчет
  WriteAccInfo(Req);

  // Файл\Документ\СвСчСтар
  if(Req.OldAccount != Req.NewAccount)
    WriteOldAccInfo(Req);
  end;

  // Файл \ Документ \ СвБанкСтар
  WriteOldBankInfo(Req);

  // Файл \ Документ \ СвНПСтар
  if( Req.ClientChange and IsDiffClient(Req) )
    WriteTaxPayerInfo_Ind(ClientID, Req.ClientType311, AccMsgKind, Req, null, null, true);
  end;

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonAccMsg._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonAccMsg._ВидРегОргана);
  end;

  if(CommonAccMsg._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonAccMsg._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
