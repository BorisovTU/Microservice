/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям УФЭБС ED105                        */
/*                                                                          */
/* Имя файла: ufgd105c.mac                                                  */
/* Создан:    27.10.04                                             BARS     */
/****************************************************************************/
import "ufgendoc.mac";

macro GenDoc( addrMes )
   var xml:object = ActiveX( "MSXML.DOMDocument" );
   var Строка;

   SetBuff( wlmes, addrMes );

   PrintLog( 2, "Генерация подтверждений по ED105" );

   ClearRecord(wlconf);

   if( not СчитатьПоле( XMLField, Строка ) )
     return FALSE;
   end;

   if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED105" ) );
     return false;
   end;

   wlconf.DKFlag = WL_DEBET;

   wlconf.Number     = ReadAttribute(xml,"EDNo");  
   wlconf.RelatedRef = wlmes.RelatedRef;

   wlconf.DateValue = ToDate(ReadAttribute(xml,"EDDate", "InitialED"));

   wlconf.TransferDate = ToDate(ReadAttribute(xml,"EDDate"));

   wlconf.Sum = moneyL( doubleL( ReadAttribute(xml,"Sum") ) );

   wlconf.FIID = 0;
   wlConf.CorSchem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlconf.FIID, 1, "Д" );
   if ( wlConf.CorSchem==-1 )
      std.msg("Не найдена корсхема по респонденту");
      return FALSE;
   end;

   if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вставке подтверждения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

macro GenDocV2( addrMes )
   return GenDoc( addrMes );
end;
