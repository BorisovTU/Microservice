/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*   Печать отчета квитовки платежей без подтверждений                      */
/*                                                                          */
/*  Имя файла: wlfkvtpm.mac                                                 */
/*  Создан:    16.07.01                                     Бабин А.П.      */
/****************************************************************************/

import "wldoc.mac";

const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )
  var stat, Номер;

  setbuff (wlpm, firstdoc);

  if( Вызов == Первый )
    [                                                                               ];
    [   Отчет о групповой квитовке начальных платежей без подтверждений             ];
    [   Дата обработки: ##########  ########                                        ](date:f, time);
    [   Операционист:   #####                                                       ]({oper});
    [                                                                               ];
    [+-----------------------------+------+------------------------------------------------------------+];
    [|        Номер                |Номер |                 Сообщение                                  |];
    [|       платежа               |ошибки|                                                            |];
    [+-----------------------------+------+------------------------------------------------------------+];
  elif( Вызов == Ошибка )
    if( Статус == 0 )
      Сообщение = "Квитовка платежа выполнена успешно";
    end;
    if( FindPayment(
                    wlpm.PaymentID,  /* Индентификатор */
                    0,
                    0,      /* Subpurpose                   */
                    0, /* Вид первичного документа */
                    0,
                    true,   /* Поиск по реальной базе       */
                    null,
                    null,
                    null,
                    wlpmrmprop
                    ) != 0 )
      Статус = 1;
      Сообщение = "Не найден платежный документ";
    else      
        Номер = wlpmrmprop.Number;
    end;
    [|#############################|######|############################################################|](Номер,Статус,StrSubst(Сообщение, "|", " " ):w);
         elif (Вызов==Последний)
    [+-----------------------------+------+------------------------------------------------------------+];
  end;
end;
