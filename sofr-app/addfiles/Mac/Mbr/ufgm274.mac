/*
$Name:           ufgm274.mac
$Module:         МБР
$Description:    Генерация сообщения ED274 транспорта УФЭБС
*/                                                                            

import WldInter, "ufgenmes.mac", "wluftool.mac", "wltools.mac", "rsberror.mac",
       "bnk_common.mac";

private macro FindInMesByTrn(Trn : string, RelatedRef : @string) : bool
  RelatedRef = "";

  var rs = execSQLselect("Select t_MesID, t_RelatedRef " +
                         "  from dwlmes_dbt            " +
                         " where t_Trn = :Trn          " +
                         "   and t_Direct = 'X'        ",
                         makeArray( SQLParam("Trn", Trn) )
                        );
  if(rs and rs.moveNext)
    RelatedRef = rs.value("t_RelatedRef");
    return true;
  end;

  return false;
end;

private macro GetInitialEDReference(InRelatedRef:string, Queries:string) : string
  var ref_str = "";

  if(InRelatedRef)
    ref_str = InRelatedRef;
  else    
    ref_str = getFldValFromQueries(Queries, "EDRefID:");
  end;

  return ref_str;
end;

private macro GetEDReceiverForED274(wlReq) : string
  return GetEDReceiverForWlreq(wlReq, wlReq.RelatedRef);
end;

private macro ConvertDateStrToUfbsFrmt(DateStr : string) : string
  var DateUfbs : string = "";

  var dt : date = null;
  if( StrIsNumber( SubStr(DateStr, 1, 4) ) )
    // считаем, что дата в формате YYYY-MM-DD
    dt = ToDate(DateStr);
  elif( StrIsNumber( SubStr(DateStr, 1, 2) ) )
    // считаем, что дата в формате DD.MM.YYYY
    dt = ToDate_DDpMMpYYYY(DateStr);
  end;

  if(dt != null)
    DateUfbs = YYYYMMDD(dt);
  end;

  return DateUfbs;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var InRelatedRef : string = "";
  var tempstr:string = "";

  // Найти входящее сообщение wlmes_in, на которое ссылается ответ
  /*if( wlReq.RelatedRef )
    FindInMesByTrn(wlReq.RelatedRef, @InRelatedRef);    
  end;*/

  // Заполнение атрибутов поля "xml" сообщения
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED274");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);

  mes.setAttribute("EDReceiver", GetEDReceiverForED274(wlReq) );

  var ind = index(wlReq.Queries, "InfoCode:");
  mes.setAttribute("InfoCode",   SubStr(wlReq.Queries, ind+9, 1));

  tempstr = getFldValFromQueries( wlReq.Queries, "SumPT:" );
  if(tempstr)
    mes.setAttribute("SumPT", tempstr);
  end;

  tempstr = getFldValFromQueries( wlReq.Queries, "AcptSum:"  );
  if( tempstr )
    mes.setAttribute("AcptSum", tempstr );
  end;

  tempstr = getFldValFromQueries( wlReq.Queries, "AcptDate:" );
  if( tempstr )
    mes.setAttribute("AcptDate", ConvertDateStrToUfbsFrmt(tempstr) );
  end; 

  var elem : object = null;      
  
  var Annotation = ПолучитьОписаниеЗапросаОтвета(wlReq);
  if( not Annotation )
    Annotation = GetWlcodeDescription( TYPE_CODE_UFBS_ED274, 
                                       substr(wlReq.Queries, ind, 10)
                                     );
  end;
  elem = xml.createElement("Annotation");
  elem.appendChild( xml.createTextNode(TransTextField(Annotation)) );
  mes.appendChild(elem);

  var ref_str = ""; // строка референса

  // Не заполняется
  //ref_str = GetInitialEDReference(InRelatedRef, wlReq.Queries);
  //elem = xml.createElement("InitialED");  
  //FillEDNoDateAuthorByRef_XMLField(elem, ref_str);
  //elem.appendChild(xml.createTextNode(""));
  //mes.appendChild(elem);

  if(wlReq.RelatedRef)
    ref_str = wlReq.RelatedRef;    
  else
    ref_str = getFldValFromQueries(wlReq.Queries, "EDRefID:");
  end;                                           
  elem = xml.createElement("EDRefID");  
  FillEDNoDateAuthorByRef_XMLField(elem, ref_str);
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

private macro CheckSum(SumStr : string) : bool

  if(SumStr)
    if( StrLen(SumStr) > 18 )
      return FALSE;
    end;

    if( not StrIsNumber(SumStr) )
      return FALSE;
    end;
  end;

  return TRUE; // ошибки не обнаружены
end;

private macro CheckDate(DateStr : string) : bool
  if(DateStr)
    if( not ConvertDateStrToUfbsFrmt(DateStr) )
      return FALSE;
    end;
  end;

  return TRUE;
end;

macro CheckObj(addrReq)

  record wlReq (wlreq);
  SetBuff( wlReq, addrReq );

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED274, @req_code);
  if(not req_code)
    RunError("Не найден код в сообщении");
  end;

  var InRelatedRef : string = "";

  if(not wlReq.RelatedRef )
    if(not index(wlReq.Queries, "EDRefID:"))
      RunError("Не найдена ссылка на исходный ЭПД");
    end;
  end;  

  // должно обеспечиваться равенство получателя сообщения (EDReceiver) 
  // отправителю исходного сообщения (InitialED.EDAuthor)
  var EDReceiver : string = GetEDReceiverForED274(wlReq);
  var ref_str = GetInitialEDReference(wlreq.RelatedRef, wlReq.Queries);
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(ref_str); 
  if( EDReceiver != ed_nda.EDAuthor )
    RsbThrow("Получатель уведомления (EDReceiver) не совпадает с " +
             "отправителем исходного сообщения (EDRefID.EDAuthor)");
  end;

  if( not CheckSum( getFldValFromQueries(wlReq.Queries, "SumPT:") ) )
    RunError("Не корректно заполнено поле \"SumPT\"");
  end;

  if( not CheckSum( getFldValFromQueries(wlReq.Queries, "AcptSum:") ) )
    RunError("Не корректно заполнено поле \"AcptSum\"");
  end;

  if( not CheckDate( getFldValFromQueries( wlReq.Queries, "AcptDate:" ) ) )
    RunError("Не корректно заполнено поле \"AcptDate\"");
  end;

  return true;

  OnError(er) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
