/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения универсального R-макета                              */
/* Действительно по v.5.10.79                                               */
/*  Имя файла: wlmcgmr.mac                                                  */
/*  Создан:  10.08.00                                               AVM     */
/****************************************************************************/

import "wlmcgm.mac", "wltreas.mac";

macro GenMes( addrMes, addrPm )
  var DKFlag = -1, ВИД_ПЛАТЕЖА = 0;
  var FlagTrans, НомерДокументаСтр, НомерДокумента, День, Месяц, Год,
      ShifrOperation, Вид_обработки, MFO_9, СсылкаНаБанк, Сумма, Основание,
      Плательщик, Получатель, Error,
      PayerINN, ReceiverINN, PayerKPP, ReceiverKPP, StringError = "";
  var RsPaym;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog( 2, "Генерация сообщения R для МЦИ " );

  GetProtectiveCodeMode();
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  PayerINN    = RemoveKPP(RsPaym.PayerINN);
  ReceiverINN = RemoveKPP(RsPaym.ReceiverINN);
  PayerKPP    = RemoveINN(RsPaym.PayerINN);
  ReceiverKPP = RemoveINN(RsPaym.ReceiverINN);

  if ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  elif ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  else
     DKFlag = -1;
  end;            

  FlagTrans = IsTransitPayment(RsPaym);  

  if( DKFlag == -1 ) /* документ не внешний */
    RunError( "|Платеж с ID: " + string( RsPaym.PaymentID ) + "не внешний" );
  end ;

  if( DKFlag == WL_DEBET )
    if( GetTrue( False, "Документ " + RsPaym.Number + "  - дебетовый. Выгружать его?" ) == False )
      RunError( "|Документ дебетовый" );
    end;
  end;

  /* Формируем строку с записью о выгружаемом документе */
  СтрокаДокумента = "R" ;                         /* 1 */

  /* Взять последние пять цифр из номера документа  */
  if( Общий_Регион( DKFlag ) )
     НомерДокументаСтр = Последние_Символы( RsPaym.Number, 5 ) ;
  else
  /* Согласно приложению к Указанию 1160-У для межрегиональных электронных платежей 
     в качестве номера документа должны браться 3 цифры, 
     а потом дополняться слева нулями до 5 знаков. */
     НомерДокументаСтр = Последние_Символы( RsPaym.Number, 3 ) ;
  end;

  НомерДокумента = int( НомерДокументаСтр ) ;
  if( НомерДокумента  ==  0 )
    /* Номер документа или уже нулевой или не числовой */

    /* Если необходимо, чтобы при нечисловом номере документа номер
    выгруженного становился нулевым автоматически -
    уберите следующие строки */

    if( GetTrue( True, "Номер документа " + RsPaym.Number + " не числовой. Выгрузить документ с N 0 ?" ) == False )
      RunError( "|Нечисловой номер документа" );
    end;
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НомерДокумента, 5 ) ;   /* 2-6 */

  /* Признак дебет/кредит сейчас - только 1 */
  if( DKFlag == WL_CREDIT )
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "1", 1 ) ; /* 7 */ /*м/б можно сделать 4 - кредит*/
  else
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "1", 1 ) ; /* 7 */
  end;

  /* Срок платежа - с проверкой даты на корректность */
  datesplit( RsPaym.PayDate, День, Месяц, Год ) ;
  if( (День == 0) AND (Месяц == 0) AND (Год == 0) )
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ) ;
  else
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 ) ;  /* 8-9   */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 ) ;  /* 10-11 */  /* 8-15 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 ) ;  /* 12-15 */
  end ;

  if ( not Платеж_ФОР(RsPaym) )
     /* Код очередности платежа - допустимы лишь 01, 02, 03, 04, 05, 06 */
     if( ( RsPaym.Priority < 1) OR ( RsPaym.Priority > 6 ) )
       RunError( "|Неверная очередность платежа" );
     end;

     СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.Priority, 2 );
  else
     СтрокаДокумента = СтрокаДокумента + "0 " /* 16-17 */
  end;

  /* Дата ввода документа в систему расчетов */
  /* Проверка на соответствие текущей дате операционного дня */
  datesplit( {curdate}, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 18-19 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 20-21 */  /* 18-25 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 22-25 */

  /* По инструкции 1160-y от 11 июля 2002 документы совершаемые внутри  */
  /* Московского региона с шифром операции 02, 06, 08, 16               */
  /* должны выгружаться в МЦИ в виде                                    */
  /* НЕ полноформатного документа независимо от суммы.                  */  
  ShifrOperation = int( substr( RsPaym.ShifrOper, 1, 2) ) ;

  if( (  ( ShifrOperation == 2 )
      OR ( ShifrOperation == 6 )
      OR ( ShifrOperation == 8 )
      OR ( ShifrOperation == 16 ) ) AND (Общий_Регион(DKFlag)==TRUE) AND
      ( (Свой_Регион=="45") OR (Свой_Регион=="46") ) )
    ВИД_ПЛАТЕЖА = 0;
  elif ( ShifrOperation == 1 )
    /* Все платежные требования, независимо от суммы - полноформатные */
    ВИД_ПЛАТЕЖА = 1;
  else
    /* В остальных случаях делаем также полноформатные */
    ВИД_ПЛАТЕЖА = 1;
  end;

  /* Вид операции - допустимые значения 01, 02, 03, 04, 05, 06, 07, 08, 09, 10, 11, 12, 13, 16 */
  if( ( ShifrOperation < 1 )
      OR (     ( ShifrOperation > 13 )
            AND ( ShifrOperation != 16 )
          )
    )
    RunError( "|Неверный вид операции" );
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.ShifrOper, 2, Числовое_Поле );   /* 26-27 */

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ВИД_ПЛАТЕЖА, 1 );  /* 28 */  

  /* Для межрегиональных платежей Вид_обработки=10 */
  /* Для внутрирегиональных - 01 */
  if( ВИД_ПЛАТЕЖА == 0 )
      Вид_обработки = "00" ;
  elif( Общий_Регион( DKFlag ) == FALSE )
      Вид_обработки = "10" ;
  else
      if ( IsAbonentPUG(DKFlag) AND IsAbonentMoskowRegion(DKFlag) )
         Вид_обработки = "15" ;
      else
         Вид_обработки = "01" ;
      end;
  end; 
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Вид_обработки, 2 ) ;  /* 29 - 30 */

  if ( not ПроверитьВидОбработкиИВидОперации(string(ВИД_ПЛАТЕЖА), Вид_обработки, substr( RsPaym.ShifrOper, 1, 2), StringError) )
     RunError( "|" + StringError);
  end;

  /* Номер авизо, только 000 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "000", 3 );  /* 31-33 */

  /* Дата документа, должна быть датой и не больше текущего опердня */
  if( (RsPaym.Date > {curdate} ) )
    RunError( "|Неверная дата документа" );
  end;
  datesplit( RsPaym.Date, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 34-35 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 36-37 */  /* 34-41 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 38-41 */

  /* Дата приема документа от клиента */
  /* Если не заполнена - записываем текущую дату опердня */
  if( RsPaym.ClientDate == date( 0, 0, 0 ) )
    datesplit( {curdate}, День, Месяц, Год );
  else
    datesplit( RsPaym.ClientDate, День, Месяц, Год );
  end ;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 42-43 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 44-45 */  /* 42-49 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 46-49 */

  /* Признак года, только 0 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "0", 1 );  /* 50 */

  /*
        БИК и Коррсчет КО плательщика
        (проверка на наличие в справочнике БИК и Коррсчета)
  */
  if( DKFlag == WL_CREDIT )
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО,      9, Числовое_Поле );  /* 51-59 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_Корсчет, 20, Числовое_Поле );  /* 60-79 */
  else
    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.PayerBankCode, RsPaym.PayerBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, MFO_9, 9, Числовое_Поле ); /* 51-59 */
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.PayerCorrAccNostro, 20, Числовое_Поле ); /* 60-79 */
      else
        RunError( "|Не найден БИК банка плательщика" );
      end ;
    else
      RunError( "|Не найдена ссылка на банк плательщика" );
    end ;
  end;

  /* Лицевой счет плательщика */
  if( ( ( RsPaym.PayerAccount == "") AND ( Вид_обработки != 1 ) ) )
    RunError( "|Неверен номер счета плательщика" );
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.PayerAccount, 20, Числовое_Поле );  /* 80-99 */

  /* Сумма документа */
  if( RsPaym.ReceiverAmount == 0 )
    RunError( "|Нулевая сумма документа" );
  end;
  Сумма = double( RsPaym.ReceiverAmount )*100;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Сумма, 18 );                  /* 100-117 */

  if( DKFlag == WL_CREDIT )
    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.ReceiverBankCode, RsPaym.ReceiverBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
        if( ((not IsAbonentPUG(DKFlag)) OR (not IsAbonentMoskowRegion(DKFlag))) AND 
            (substr( MFO_9, strlen( MFO_9 )-2, 3 ) == "002") AND
            (MFO_9 != "044501002") AND (MFO_9 != "044536002") AND (MFO_9 != "044537002") )
          RunError( "|Получатель (БИК = " + RsPaym.ReceiverBankCode + ") является учреждением ЦБ РФ" );
        end;
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, MFO_9, 9, Числовое_Поле );  /* 118-126 */
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverCorrAccNostro, 20, Числовое_Поле );  /* 127-146 */
      else
        RunError( "|Не найден БИК получателя" );
      end;
    else
      RunError( "|Не найдена ссылка на банк получателя" );
    end;
  else
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО,      9, Числовое_Поле ); /* 118-126 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_Корсчет, 20, Числовое_Поле ); /* 127-146 */
  end;

  /* Счет получателя */
  if( ( RsPaym.ReceiverAccount == "" ) AND ( Вид_обработки != 1 ) )
    RunError( "|Неверен номер счета получателя" );
  end;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverAccount, 20, Числовое_Поле );   /* 147-166 */

  /* ИНН КО Плательщика, разрешено 000000000000 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, PayerINN, 12, Числовое_Поле );     /* 167-178 */

  /* ИНН КО Получателя, разрешено 000000000000 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ReceiverINN, 12, Числовое_Поле );  /* 179-190 */

  /* Порядковый номер электронного документа */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, wlmes.TRN, 6 );   /* 191-196 */

  /* Уникальный номер составителя */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НашКодСоставителя, 10 );

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "0", 1 );

  /* Это для МЦИ */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "00000", 5 );

  if( ВИД_ПЛАТЕЖА  ==  1 )
  /* Если ВИД_ПЛАТЕЖА равен 0, то текстовки не заполняются */
  /* C 08.04.1998 дополняются ПРОБЕЛАМИ СПРАВА */
  /* Основание платежа */
    Основание = string( RsPaym.Ground:210 ) ;
    СтрокаДокумента = СтрокаДокумента + Основание ;  /* 197-406 */

    /* Наименование плательщика */
    if ( PayerKPP != "" )
       Плательщик = "КПП " + PayerKPP + " ";
    else
       Плательщик = "";
    end;

    Плательщик = string( ( Плательщик + RsPaym.PayerName):160 ) ;
    СтрокаДокумента = СтрокаДокумента + Плательщик;    /* 407-566 */
    /* Наименование получателя */
    Получатель = string( RsPaym.ReceiverName:160 ) ;
    СтрокаДокумента = СтрокаДокумента + Получатель; /* 567-726 */

    /* В положении 1160-У  от 11.06.2002 отсутствует */
    /*if( UseProtectiveCode )
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ЗащитныйКод(), 196 ); /* 727-923 */
    end;*/
  end;

  ЗаписатьПолеЛог( FormFldDoc, СтрокаДокумента, TRUE );

  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
