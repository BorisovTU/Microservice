 /*
 $Name:        mnsgdkwt.mac
 $Module:      МБР
 $Description: Генерация учетного объекта по сообщению формы KWT
 */


/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация учетного объекта по сообщению формы KWT                        */
/*                                                                          */
/*  Имя файла: mnsgdkwt.mac                                                 */
/*  Создан:    08.11.11                                  Чукина Т.А.        */
/****************************************************************************/

import "wlmnstls.mac", WldInter, oralib, wlgendoc, MesInter;

private macro findInitialMes(ExportFileShortName: string, MesID: @integer, MesState: @integer)  
  var rs:object;
  var select:string, from:string, where:string;
  var params:TArray = TArray();
  
  select = "select mes.t_MesID, mes.t_State, mes.t_InsideAbonentID, " +
           "       mes.t_InsideAbonentCodeKind, mes.t_InsideAbonentCode ";
  from   = "  from dwlmes_dbt mes, dwlsess_dbt sess ";
  where  = " where mes.t_sessionid = sess.t_sessionid " +
           "   and upper(substr(sess.t_filename, instr(sess.t_filename, '\\', -1, 1) + 1)) = " + 
           "       upper(:FileName || '.txt' )";
     
  params = makeArray( SQLParam("FileName", ExportFileShortName) );

  rs = execSQLSelect (select + from + where, params, true);

  if (rs.MoveNext())      
    wlmes.InsideAbonentID = rs.value("t_InsideAbonentID");
    wlmes.InsideAbonentCodeKind = rs.value("t_InsideAbonentCodeKind");
    wlmes.InsideAbonentCode = rs.value("t_InsideAbonentCode");
    UpdateMes(wlmes);

    MesID = rs.value("t_MesID");
    MesState = rs.value("t_State");
  end;
    
end;

private macro existsOtherParts(MesID: integer)
  var SendNum = ""; // номер отправки
  ПолучитьПоле(MesID, "_НомерОтправ", SendNum);
  var select = "select val.t_Value " +
               "  from dwlmes_dbt mes, dwlmeslnk_dbt lnk, dwlmeslnk_dbt lnk1, " + 
               "       dwlmesval_dbt val, dwltpfld_dbt fld " +
               " where mes.t_MesID <> " + MesID +
               "    and mes.t_State not in (" + WLD_STATUS_MES_DEFECT + ", " + WLD_STATUS_MES_CLOSED + ") " +
               "    and lnk.t_MesID = " + MesID +
               "    and lnk.t_ObjKind = lnk1.t_ObjKind " + 
               "    and lnk.t_ObjID = lnk1.t_ObjID " +
               "    and mes.t_MesID = lnk1.t_MesID " +
               "    and val.t_MesID = mes.t_MesID " +
               "    and val.t_TpFieldID = fld.t_TpFieldID " +
               "    and fld.t_TpID = " + TRANSP_MNS +
               "    and fld.t_Name = '_НомерОтправ' " +
              "group by val.t_Value " +
               " having TO_NUMBER(val.t_Value) < TO_NUMBER('" + SendNum + "') ";
  InitError;
  return existsSQLselect(select);
end;

private macro fillWlerror(ResultID, field_value)
  var PrevErrorID = IfThenElse(wlerror.ParentID, wlerror.ParentID, wlerror.ErrorID);

  ClearRecord(wlerror);
  wlerror.ResultID = ResultID;
        
  var cut_end_field_value = strSubst(field_value, МНС_КонецФрагмента, ""),
      i = int( substr(field_value, 1, 2) ),
      ind = index(cut_end_field_value, ";");
        
  if( (i >= 20) and (i <= 26) and (ind == 3) )
    wlerror.Code = subStr(cut_end_field_value, 1, ind - 1);

    // Если есть текст после символа ";" в значении поля
    if( ind < strlen(cut_end_field_value) )
      wlerror.Description = subStr(cut_end_field_value, ind + 1);
    else
      wlerror.Description = ПолучитьКодОтветаНаСообщение(wlerror.Code, 53);
    end;
  else
    wlerror.ParentID = PrevErrorID;
    wlerror.Description = cut_end_field_value;
  end;

end;

private macro getWlresprmResultIDifExistsElse0(InitialMesID : integer) : integer

  var ResultID = 0;

  var select = " select resprm.t_ResultID " +
               "   from dwlresprm_dbt resprm " +
               "  where resprm.t_InitialMesID = " + InitialMesID +
               "    and resprm.t_Type = 0 " +
               "    and resprm.t_ErrCode = 7 " +
               "    and not exists (select 1 " +
               "                      from dwlmeslnk_dbt lnk " +
               "                     where lnk.t_ObjID = resprm.t_ResultID " +
               "                       and lnk.t_ObjKind = " + OBJTYPE_MESDEFECT +
               "                   ) ";

  var rs : RsdRecordset = execSQLselect(select);
  if (rs and rs.moveNext)
    ResultID = rs.value(0);
  end;

  return ResultID;
end;

// Получить остальные сообщения (MesID) учетного объекта, с которым связано сообщение MesID
// (само сообщение MesID в результат не включается)
private macro getSameObjectMessagesRs(MesID : integer) : RsdRecordset
  var SendNum = ""; // номер отправки
  ПолучитьПоле(MesID, "_НомерОтправ", SendNum);
  var select = " select mes1.t_MesID " +
               "   from dwlmes_dbt mes1, dwlmeslnk_dbt lnk1, dwlmeslnk_dbt lnk, " +
               "        dwlmesval_dbt val, dwltpfld_dbt fld " +
               "  where mes1.t_MesID  = lnk1.t_MesID " +
               "    and lnk1.t_ObjID  = lnk.t_ObjID " +
               "    and lnk1.t_ObjKind = lnk.t_ObjKind " +
               "    and lnk.t_MesID = " + MesID +
               "    and mes1.t_MesID <> " + MesID +
               "    and mes1.t_State > " + WLD_STATUS_MES_DEFECT +
               "    and val.t_Value = '" + SendNum + "' " +
               "    and val.t_MesID = mes1.t_MesID " +
               "    and val.t_TpFieldID = fld.t_TpFieldID " +
               "    and fld.t_TpID = " + TRANSP_MNS +
               "    and fld.t_Name = '_НомерОтправ' ";
  var rs : RsdRecordset = execSQLselect(select);
  InitError;
  return rs;
end;

macro getMsgType(MesID)
  var rs : RsdRecordset = execSQLselect("select t_type from dwlregdec_dbt where t_decisionid = " + string(MesID));
  if (rs.moveNext() and (rs.value(0) == WLD_TYPE_REGDEC_IVS))
    return "выписку";
  end;
  return "справку";
end;

/* Релиз KWT */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация подтверждения о доставке KWT");

  var field_name, field_value, isFoundMes = false;
  
  // параметры функций генерации уч.объектов
  var MesID             = 0, // ID исходящего сообщения
      MesState          = 0, // статус исходящего сообщения
      ErrCode           = 0,
      ErrDescription    = "Не найдено исходное сообщение",
      State             = WLD_STATUS_RESPRM_DEFECT,
      DeliveryDate      = date(),
      Description       = "",
      CreateKvt         = true,
      NoChangeState     = true;
  
  var RejectParts = false;
  GetRegValForOPENAC( "ОТВЕРГАТЬ_ЧАСТИ_ВЫПИСКИ", V_BOOL, RejectParts );

  // Найти исходящее сообщение по имени файла
  // (имя файла сеанса должно совпадать со значением первого считанного поля) 
  if (СчитатьПоле(field_name, field_value))
    findInitialMes( strSubst(field_value, "###", ""), @MesID, @MesState );  
    isFoundMes = (MesID > 0);
    if (isFoundMes and existsOtherParts(MesID))
      std.msg( string( "Квитанция поступила на ",
          getMsgType(MesID),
          ", у которой есть несквитованные сообщения, отправленные ранее. ",
          "Для этих сообщений выполните процедуру квитовки, после чего повторите генерацию объекта" ) );
      return false;
    end;
  else
    std.msg( string( "Ошибка формата сообщения ", wlmes.MesID ) ); 
    return false;
  end;

  // Информация о результатах проверки
  if (СчитатьПоле(field_name, field_value))

    // чтение полей
    var FieldVals : TArray = TArray();      
    var continue0 = true;
    while (continue0)
      FieldVals(FieldVals.Size) = field_value;
      continue0 = СчитатьПоле(field_name, field_value);
      if (field_name != "_СтрокаИзвещения")
        continue0 = false;
      end;        
    end;

    if (subStr(FieldVals[0], 1, 2) != "20")
      
      var ResultID = 0;
      
      if ( isFoundMes and (MesState == 25) )

        // если значение настройки "PS\ REQOPENACC\ OPERATION\ Отвергать_Части_Выписки" = NO
        if (not RejectParts)
          // Найти запись wlresprm
          ResultID = getWlresprmResultIDifExistsElse0(MesID);
          // Если запись найдена
          if (ResultID > 0)
            var RecWlmeslnk = TRecHandler("wlmeslnk.dbt");
            RecWlmeslnk.rec.MesID   = wlmes.MesID;
            RecWlmeslnk.rec.Direct  = "X";
            RecWlmeslnk.rec.ObjID   = ResultID;
            RecWlmeslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
            var MesLnkObj = RsbWlMesLnk( RecWlmeslnk.rec.ObjKind, RecWlmeslnk.rec.ObjID, "X" );
            MesLnkObj.Insert(RecWlmeslnk);
            MesLnkObj.Save();
          end;
        end;
      end;

      // если запись квитовки wlresprm не была найдена или не искалась
      if (ResultID <= 0)
        // отбраковка исходящего сообщения
        if( isFoundMes )        
          ErrCode = FNS_RP_NOT_APPROVED_FNS;
          ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);
          if( ErrDescription == "" )
            std.msg( string("|Неверный код ошибки в сообщении ", wlmes.MesID) ); 
            return false;
          end;
          State   = WLD_STATUS_RESPRM_RECEIVE;
        else
          State = WLD_STATUS_RESPRM_DEFECT;
        end;
        if( not ОшибкаЛогическогоКонтроляСообщения( MesID, 
                                                    ErrCode, 
                                                    ErrDescription, 
                                                    State,
                                                    true,
                                                    ResultID ) )
          std.msg( string("|Ошибка при переносе в отбракованные сообщения ", MesID) ); 
          return false;
        end;
      end;

      // сохранение ошибок обработки в dwlerror_dbt
      ClearRecord(wlerror);
      for (var i, 0, FieldVals.Size - 3) // исключая 2 последних поля
        // обрабатываем текущее поле
        fillWlerror(ResultID, FieldVals(i));
        if ( not ВставитьОшибкуОбработкиСообщения(wlerror) )
          std.msg( string("|Ошибка при создании записи об ошибке обработки сообщения ", MesID) ); 
          return false;
        end;
      end;

      // Если исходящее сообщение mes было найдено
      if( isFoundMes )
        // Если в сообщении есть поле $(НомЧасти) и 
        // значение настройки "PS\ REQOPENACC\ OPERATION\ Отвергать_Части_Выписки" = NO
        if( ПолучитьПоле(MesID, "НомЧасти", field_value) and not RejectParts )
          // Получить остальные сообщения учетного объекта
          var rs_mes : RsdRecordset = getSameObjectMessagesRs(MesID);
          // каждое найденное сообщение отбраковать
          if(rs_mes)
            ErrCode = FNS_RP_NOT_APPROVED_PART_EXTRACT;
            ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);
            State   = WLD_STATUS_RESPRM_RECEIVE;
            var CreateMesLnk = 0;
            while(rs_mes.moveNext)
              if( not ОшибкаЛогическогоКонтроляСообщения( rs_mes.value("t_MesID"), 
                                                          ErrCode, 
                                                          ErrDescription, 
                                                          State,
                                                          true,
                                                          NULL,
                                                          CreateMesLnk ) )
                std.msg( string("|Ошибка при переносе в отбракованные сообщения ", rs_mes.value("t_MesID")) ); 
                return false;
              end;
            end; // while(rs_mes.moveNext)
          end; // if(rs_mes)
        end;
        InitError;
      end;
    else // значение поля начинается с "20"
      if( isFoundMes )
        GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_MNS, 0, NULL, ErrDescription );
        if( MesState == 25 )
          NoChangeState  = true;
        else
          NoChangeState  = false;
        end;

        DeliveryDate = ToDateYYYY_MM_DD( FieldVals(FieldVals.Size - 2) );
        if ( not ВставитьПодтверждениеОДоставке( MesID, NoChangeState, DeliveryDate, ErrDescription, CreateKvt ) )
          std.msg( string( "|Ошибка при переносе в закрытые сообщения ", MesID ) );
          return false;
        end;
      else
        State = WLD_STATUS_RESPRM_DEFECT;
        if( not ОшибкаЛогическогоКонтроляСообщения( MesID, 
                                                    ErrCode, 
                                                    ErrDescription, 
                                                    State,
                                                    true,
                                                    ResultID ) )
          std.msg( string("|Ошибка при переносе в отбракованные сообщения ", MesID) ); 
          return false;
        end;
      end;

    end;
  else // В сообщении нет информации о результатах проверки
    std.msg( string( "Ошибка формата сообщения ", wlmes.MesID ) ); 
    return false;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;