 /*
 $Name: znsvsprn.mac
 $Module: МБР
 $Description: Печать выписки об операциях по счетам клиента
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : znsosprn.mac
  Created     : 31.01.2011
  Programmer  : Popova O.
  Description : Печать выписки об операциях по счетам клиента

└───────────────────────────────────────────────────────────────────────────*/
import CurrInter, "fnsprzall.mac";

// нумерация полей в массиве реквизитов проводки
private const OprDate   =  0, Shifr     =  1, Number    =  2, DocDate   =  3, CorrAcc   =  4, BankName  =  5,
              BIC       =  6, ClientID  =  7, ClName    =  8, NumAcc    =  9, AmountD   = 10, AmountC   = 11,
              Ground    = 12, INN       = 13, KPP       = 14;

private macro ВыпискаПоСчету( index: integer, this: FNSPrint, Account: TRecHandler, StateDate:date )

  var FICode = GetFICode( Account.rec.Code_Currency );
  var ПроводкиПоСчету:Object;
  array Acc;
  StrSplit( Account.rec.Account, Acc, 1, 1, 20 );            

  var D1 = StateDate, D2 = {curdate};
  if( ( this.НачалоПериода != date( 0, 0, 0 ) ) or ( this.КонецПериода != date( 0, 0, 0 ) ) ) 
    D1 = this.НачалоПериода;
    D2 = this.КонецПериода;
  end;

  var InRest  = IfThenElse( Account.rec.Code_Currency == 0/*NATCUR*/, RestA   ( Account.rec.Account,                            D1 - 1, NULL  , 1/*CHAPT1*/ ), 
                                                                      RestAC  ( Account.rec.Account, Account.rec.Code_Currency, D1 - 1, NULL  , 1/*CHAPT1*/ ) );
  var OutRest = IfThenElse( Account.rec.Code_Currency == 0/*NATCUR*/, RestA   ( Account.rec.Account,                            D2    , NULL  , 1/*CHAPT1*/ ), 
                                                                      RestAC  ( Account.rec.Account, Account.rec.Code_Currency, D2    , NULL  , 1/*CHAPT1*/ ) );
  var SDebet  = IfThenElse( Account.rec.Code_Currency == 0/*NATCUR*/, DebetA  ( Account.rec.Account,                            D1    , D2    , 1/*CHAPT1*/ ), 
                                                                      DebetAC ( Account.rec.Account, Account.rec.Code_Currency, D1    , D2    , 1/*CHAPT1*/ ) );
  var SCredit = IfThenElse( Account.rec.Code_Currency == 0/*NATCUR*/, KreditA ( Account.rec.Account,                            D1    , D2    , 1/*CHAPT1*/ ), 
                                                                      KreditAC( Account.rec.Account, Account.rec.Code_Currency, D1    , D2    , 1/*CHAPT1*/ ) );

  [

                                             ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐
    а) #                                  б) │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│    в) #
       __________                            └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘       _____________________________________________
         № п/п                                № счета, специального банковского счета        вид счета, специального банковского счета


    г) ################################    д) ################################
       ────────────────────────────────       ────────────────────────────────
               владелец счета                           бенефициар

                                                                                          
    е) ##########                          ж) ##########                                   
       ────────────────────────────────       ────────────────────────────────
      дата открытия счета, специального      дата закрытия счета, специального             
        банковского счета (дд.мм.гг)           банковского счета (дд.мм.гг)
                                                                                                            ┌─┬─┬─┐
    з) ##########                                                                                        и) │#│#│#│ - цифровой код валюты счета
       ────────────────────────────────                                                                     └─┴─┴─┘  (специального банковского счета)
      дата изменения реквизитов счета (специального банковского счета) (дд.мм.гг); № измененного счета
            (специального банковского счета) (до изменения / после изменения)                                     

    к) ##########                                                         л) ##########
       ────────────────────────────────                                      ────────────────────────────────
      дата начала периода, за который представлена выписка (дд.мм.гг)     дата окончания периода, за который представлена выписка (дд.мм.гг)
  
    Таблица 1
    ┌─────────────────────────────────────┬──────────────────────────────────────┬──────────────────────────────────────┬────────────────────────────────────┐
    │   остаток по счету (специальному    │ сумма по дебету счета (специального  │ сумма по кредиту счета (специального │   остаток по счету (специальному   │
    │ банковскому счету) на начало периода│     банковского счета) за период     │     банковского счета) за период     │ банковскому счету) на конец периода│
    │           (руб., коп./вал.)         │           (руб., коп./вал.)          │           (руб., коп./вал.)          │           (руб., коп./вал.)        │
    ├─────────────────────────────────────┼──────────────────────────────────────┼──────────────────────────────────────┼────────────────────────────────────┤
    │                   1                 │                     2                │                    3                 │                   4                │
    ├─────────────────────────────────────┼──────────────────────────────────────┼──────────────────────────────────────┼────────────────────────────────────┤
    │#####################################│######################################│######################################│####################################│ 
    └─────────────────────────────────────┴──────────────────────────────────────┴──────────────────────────────────────┴────────────────────────────────────┘

    Таблица 2
    ┌────┬──────────┬────────────────────────┬──────────────────────────────────────────┬──────────────────────────────────────────────────────┬─────────────────────────────────┬───────────┐
    │    │  Дата и  │ Реквизиты документа,   │                                          │                                                      │  Сумма операции                 │           │
    │    │   время  │     на основании       │                                          │                                                      │      по счету                   │           │
    │    │ соверше- │    которого была       │  Реквизиты банка плательщика/получателя  │     Реквизиты плательщика/получателя денежных        │   (специальному                 │           │
    │    │   ния    │  совершена операция    │            денежных средств              │                    средств                           │    банковскому                  │           │
    │    │ операции │по счету (специальному  │                                          │                                                      │       счету)                    │назначение │
    │ №  │(дд.мм.гг;│  банковскому счету)    │                                          │                                                      │ (руб., коп./вал.)               │  платежа  │
    │п/п │ чч.мм.сс)├──────┬──────┬──────────┼────────────────────┬───────────┬─────────┼─────────┬────────────┬──────────┬────────────────────┼────────────────┬────────────────┤           │
    │    │          │ вид  │номер │   дата   │                    │наименова- │         │наимено- │            │          │    № счета         │       по       │       по       │           │
    │    │          │(шифр)│      │          │     № корреспон-   │    ние    │   БИК   │ вание/  │  ИНН/КИО   │   КПП    │   (специального    │     дебету     │     кредиту    │           │
    │    │          │      │      │          │   дентского счета  │           │         │ Ф.И.О.  │            │          │ банковского счета) │                │                │           │
    ├────┼──────────┼──────┼──────┼──────────┼────────────────────┼───────────┼─────────┼─────────┼────────────┼──────────┼────────────────────┼────────────────┼────────────────┼───────────┤
    │ 1  │    2     │  3   │  4   │    5     │         6          │     7     │    8    │    9    │    10      │    11    │        12          │       13       │       14       │    15     │
    ├────┼──────────┼──────┼──────┼──────────┼────────────────────┼───────────┼─────────┼─────────┼────────────┼──────────┼────────────────────┼────────────────┼────────────────┼───────────┤
  ]                                                                                               
   ( Index,
     Acc(0), Acc(1), Acc(2), Acc(3), Acc(4), Acc(5), Acc(6), Acc(7), Acc(8), Acc(9),
     Acc(10), Acc(11), Acc(12), Acc(13), Acc(14), Acc(15), Acc(16), Acc(17), Acc(18), Acc(19),
     this.GetNameTypeAcc( Account.rec.Type_Account, Account.rec.Code_Currency ),
     this.GetAccName( Account.rec.Client ):c,        // AccOwner
     this.GetAccName( Account.rec.BeneficiaryID ):c, // AccBenef
     Account.rec.Open_date:c,
     Account.rec.Close_date:c,
     Changes[0]:c,
     SubStr(FICode ,  1, 1), SubStr(FICode ,  2, 1), SubStr(FICode ,  3, 1), 
     D1, D2,
     InRest:r, 
     IfThenElse( (SDebet  == $0), "-", SDebet  ):r, 
     IfThenElse( (SCredit == $0), "-", SCredit ):r, 
     OutRest:r );

   if( printCarryes( Account.rec.Account, Account.rec.Code_Currency, D1, D2, REGPT_TAXINSTITUTE ) == 0 )
  [ │  - │    -     │  -   │  -   │     -    │         -          │     -     │    -    │    -    │      -     │     -    │          -         │       -        │        -       │     -     │]
   end;

  [ └────┴──────────┴──────┴──────┴──────────┴────────────────────┴───────────┴─────────┴─────────┴────────────┴──────────┴────────────────────┴────────────────┴────────────────┴───────────┘];

end;

private macro PrintReportStatementOprAccounts( this: FNSPrint, ReqDate:date )
  var i = 0, j = -1;                                         
  var PrnAccounts:TArray = TArray();// сюда переносим счета из Accounts, которые есть в ReqAccounts
  Array Text, Buttons, Acc;
  var ErrMsg = "";
  var rs;
  rs = execSQLSelect("select t_Code from ddp_dep_dbt where t_PartyID = " + int(this.RepBankID));
  rs.moveNext();
  var Department = rs.value(0);

  if( this.WlRegDecID <= 0 )
    var Accounts:TArray = this.ВыбратьСчетаКлиента( ReqDate );
    while( i < Accounts.Size )
      while( j < this.Счет.Size )
        j = j + 1;
        if( ( this.Счет.Size == 0 ) or
            ( ( j < this.Счет.Size ) and  
              ( this.Счет[j] == Accounts(i).rec.Account ) and ( GetFICode( Accounts[i].rec.Code_Currency ) == SubStr( this.Счет[j], 6, 3 ) ) ) )
          PrnAccounts[PrnAccounts.Size] = Accounts[i];
          if( j < this.Счет.Size )
            this.Счет[j] = "";
          end;
        end;
      end;
      j = -1;
      i = i + 1;
    end;

    // какие-то из счетов не были найдены
    if( PrnAccounts.Size <= 0 )
      MsgBox( "Не найдено ни одного открытого счета клиента" );
      return 1;
    elif( PrnAccounts.Size < this.Счет.Size )
      j = 0;
      while( j < this.Счет.Size )         
        if( ErrMsg == "" )
          ErrMsg = "Не найден(ы) счет(а) ";
        end;
        if( this.Счет[j] != "" )
          Text(0) = string("Не найден счет № ", this.Счет[j], ".|Продолжить печать?");
          Buttons( 0 ) = "Да";
          Buttons( 1 ) = "Нет";
          if( GetDialogFlag() )
            if( ConfWin( Text, Buttons ) == 1 )
              return 0; // при отказе от печати не должно быть сообщения об ошибке выполнения макроса
            end;
          else
            // запомним все ненайденные счета через ","
            ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + this.Счет[j];
          end;
        end;
        j = j + 1;
      end;
      if( not GetDialogFlag() and
          ( StrBrk( ErrMsg, "0123456789" ) ) )
        SetMNSWarning( ErrMsg );
      end;
    end;
    Accounts = null;
  else
    PrnAccounts = TArray(this.Счет);
  end;
  i = 0; 

    if( PrnAccounts.Size <= 0 )
      MsgBox( "Не найдено ни одного открытого счета клиента" );
      return 1;
    end;
    ЗаголовокСправки_ММ_3_06_178("1114307"); 
    [
                                           Выписка
                       по операциям на счете (специальном банковском счете)
    ];
    ДанныеБанкаИ_Клиента();
  
    
    // YES
    var Errors = 0;
    for(i, 0, PrnAccounts.size - 1)
      var Description = GetError(PrnAccounts(i).rec.Account, WlRegDecID);
      if(Description) 
        Errors = Errors + 1;
      end;
    end;
    i = 0;
    if(GetYes(PrnAccounts, WlRegDecID) or (Errors < PrnAccounts.size))
    [                                                                                                                                                                          ┌───┐
        по операциям на счетах (специальных банковских счетах) указанного лица, а также по операциям с денежными средствами, права на которые принадлежат (денежные              │ # │
        средства подлежат передаче при возникновении оснований, предусмотренных договором с банком) указанному лицу, на счетах иных лиц,                                         └───┘]
        (GetYes(PrnAccounts, WlRegDecID));
      
      while( i < PrnAccounts.Size )
        if( strlen(GetError(PrnAccounts(i).rec.Account, WlRegDecID)) == 0 )
    this.ДанныеФилиала(1, 1, PrnAccounts(i).rec.Department, ReqDate);
    ВыпискаПоСчету( i + 1, this, PrnAccounts(i), ReqDate );
        end;
        i = i + 1;
      end;
    end;

    // OP
    var Op = GetOp(PrnAccounts, this.WlRegDecID);
    if(Op)
    [                                                                                                                                                                          ┌───┐
        об отсутствии следующих счетов (специальных банковских счетов) указанного лица, а также счетов иных лиц для совершения операций с денежными средствами, права            │ X │ 
        на которые принадлежали (денежные средства подлежали передаче при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших            └───┘ 
      ################# в:
        ─────────────────
      ]( string( "\"", substr(YYYYMMDD(ReqDate),7,2), "\" ",
                       Месяца(substr(YYYYMMDD(ReqDate),5,2)):c, " ",
                       substr(YYYYMMDD(ReqDate),1,4), " г." ));
      i = 0;
      var SeqNumber : Integer = 1;
      while(i < PrnAccounts.size )
        if( isAccNotFound(PrnAccounts(i).rec.Account, this.WlRegDecID))
          StrSplit( PrnAccounts(i).rec.Account, Acc, 1, 1, 20 );   
          [
            ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐
      ##  № │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│
            └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘
            владелец счета ####################################################################################################
                           ────────────────────────────────────────────────────────────────────────────────────────────────────
          ]( SeqNumber,
       Acc(0), Acc(1), Acc(2), Acc(3), Acc(4), Acc(5), Acc(6), Acc(7), Acc(8), Acc(9),
       Acc(10), Acc(11), Acc(12), Acc(13), Acc(14), Acc(15), Acc(16), Acc(17), Acc(18), Acc(19),
             this.GetAccName( PrnAccounts(i).rec.Client )        // AccOwner
     );
          SeqNumber = SeqNumber + 1;
        end;
        i = i + 1;
      end;
    end;
    // NO
    Errors = 0;
    for(i, 0, PrnAccounts.size - 1)
      Description = GetError(PrnAccounts(i).rec.Account, WlRegDecID);
      if( (Description) and (SubStr(strlwr(Description), 1, strlen("Счет не найден")) != strlwr("Счет не найден"))) 
        Errors = Errors + 1;
      end;
    end;
    if( Errors or (PrnAccounts.size == 0) )
    [                                                                                                                                                                          ┌───┐
        об отсутствии счетов (специальных банковских счетов) указанного лица, а также счетов иных лиц для совершения операций с денежными средствами, права на которые           │ X │
      принадлежали (денежные средства подлежали передаче при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших                       └───┘];
     this.ДанныеФилиала(1, 1, PrnAccounts(0).rec.Department, ReqDate);
    end;
    // PS
    if(this.Ps)
    [
                                                                                                                                                                               ┌───┐
        выписка по операциям на счете (специальном банковском счете) в части информации следующих филиалов банка будет представлена ими самостоятельно:                          │ X │
                                                                                                                                                                                 └───┘];
    this.ДанныеФилиала(1, 1, PrnAccounts(0).rec.Department, ReqDate);
    end;

    ДатаПодписьПредставителяБанка();
    [];
  return 0;
end;
