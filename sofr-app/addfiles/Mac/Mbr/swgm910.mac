/*
 $Name:   swgm910.mac
 $Module: MBR
 $Description: Генерация сообщения по SWIFT MT910
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT910                                       */
/*                                                                          */
/*  Имя файла: swgm910.mac                                                  */
/*  Создан:  20.06.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

macro GenMes( addrMes, addrConf )
  var field_value;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );
  var PaymentObj = RsbPayment(wlconf.DocumentID);
  var Tag;

  PrintLog(2,"Генерация сообщения по МТ910");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  field_value = FillRelatedReferenceForDebitCreditConfirmation(wlconf);
  ЗаписатьПолеЛог( RelatedReferenceField, field_value );

  /* 25 - счет подтверждения */
  ЗаписатьПолеЛог( AccountIdentificationField, wlconf.Account );

  /* 13D - Date/Time Indication */
  field_value = GetSWIFTDate( date() ) + GetSWIFTTime(Time()) + GetSessTimeZone();
  ЗаписатьПолеЛог( DateTimeIndicationField, field_value );  

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( wlconf.DateValue ) +
                ПолучитьКодВалютыЛог( wlconf.FIID ) +
                GetSWIFTAmount( wlconf.Sum );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );  

  /* 50A - приказодатель */
  if( not УстановитьКонтекстБлока( "50a" ) )
    RunError("|Ошибка при установке контекста блока '50A' ");
  end;

  field_value = FillOrderingCustomerField103( PaymentObj, Tag );
  if (field_value != "")
    ЗаписатьПолеЛог( OrderingCustomerField + Tag, field_value );    
  end;
 
  if( not УстановитьКонтекстБлока( ".." ) )
    RunError("|Ошибка при установке контекста блока");
  end;

  /* 52a - банк приказодателя */
  if( (wlconf.CodeValue!="") OR (wlconf.BankName!="") )
    ClearRecord(Route);
    Route.PartyID  = wlconf.BankID;
    Route.CodeKind = wlconf.CodeKind;
    Route.CodeName = wlconf.CodeName;
    Route.CodeValue = wlconf.CodeValue;
    Route.Name = wlconf.BankName;
    ЗаписатьПолеМаршрутаКонтекст0Лог( OrderingInstitutionField, "52a", Route );
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  /* 56a - банк приказодателя */
  if( (wlconf.CorrCodeValue!="") OR (wlconf.CorrBankName!="") )
    ClearRecord(Route);
    Route.PartyID  = wlconf.CorrBankID;
    Route.CodeKind = wlconf.CorrCodeKind;
    Route.CodeName = wlconf.CorrCodeName;
    Route.CodeValue = wlconf.CorrCodeValue;
    Route.Name = wlconf.CorrBankName;
    ЗаписатьПолеМаршрутаКонтекст0Лог( IntermediaryField, "56a", Route );
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  /* 72 */
  StrChangeRusXSet( wlconf.Description, field_value );
  StrMultyToFormat( field_value, field_value, 65, 6 );
  ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;