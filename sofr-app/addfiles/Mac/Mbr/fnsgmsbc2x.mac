/*
  $Name:         fnsgmsbc2x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SBC2 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SBC2 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 27.05.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac", FIInter;

private const vers : integer = 510; // версия формата 5.10

// Записать сведения о договоре: НомерДог, КодСостДог, ДатаРастДог
private macro GetContractInfo( GenAcc, reqclosa, 
                               IsSfContr : @bool, SfCntr : TRecHandler,
                               IsSfUnionContr : @bool, SfUnionContrCloseDate : @date,
                               Note_ContractNumber : @string, Note_ContractCloseDate : @date
                             )
  // договор обслуживания
  SfCntr.Clear();
  if( ПолучитьДоговорОбслуживания(GenAcc.Code_Currency, GenAcc.Account, SfCntr) == 0 )
    IsSfContr = true;
  else
    IsSfContr = false;
  end;

  // сводный договор
  if(IsSfContr and SfCntr.rec.UnionContrID)
    IsSfUnionContr = GetSfUnionContrCloseDate( SfCntr.rec.UnionContrID, 
                                              @SfUnionContrCloseDate );
  else
    IsSfUnionContr = false;
    SfUnionContrCloseDate = date(0, 0, 0);
  end;

  // данные из примечаний для счетов бэк-офисов
  if(reqclosa.Origin == REQ_ACC_ORIGIN_LOANS)
    var ObjType : integer = OBJTYPE_REQCLOSA,
        sObjID : string = UniID(reqclosa, OBJTYPE_REQCLOSA);

    Note_ContractNumber = ReadNoteForObject( ObjType, sObjID, REQCLOSA_NOTE_KIND_CONTRACT_NUMBER );
    Note_ContractCloseDate = ReadNoteForObject( ObjType, sObjID, REQCLOSA_NOTE_KIND_CONTRACT_CLOSEDATE );
  end;
end;

private macro WriteContractInfo(GenAcc, reqclosa)

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    var OldfnsMessageID : integer = GetOldfnsMessageID();
    ЗаписатьПолеДляАннул("НомерДог", OldfnsMessageID);
    ЗаписатьПолеДляАннул("КодСостДог", OldfnsMessageID);
    ЗаписатьПолеДляАннул("ДатаРастДог", OldfnsMessageID);
  else
    var IsSfContr : bool = false;
    var SfCntr : TRecHandler = TRecHandler("sfcontr.dbt"); // договор обслуживания для счета
    var IsSfUnionContr : bool = false, SfUnionContrCloseDate : date = date(0, 0, 0);
    var Note_ContractNumber : string = "", Note_ContractCloseDate : date = date(0, 0, 0);
    GetContractInfo( GenAcc, reqclosa, @IsSfContr, SfCntr, @IsSfUnionContr, @SfUnionContrCloseDate,
                     @Note_ContractNumber, @Note_ContractCloseDate );

    // НомерДог
    if(reqclosa.Origin == REQ_ACC_ORIGIN_LOANS)
      if(Note_ContractNumber)
        ЗаписатьПолеЛог( "НомерДог", Note_ContractNumber );
      end;
    elif(IsSfContr and SfCntr.rec.Number)
      ЗаписатьПолеЛог( "НомерДог", SfCntr.rec.Number );
    end;

    // КодСостДог
    var КодСостДог : integer;
    if( IsSfUnionContr and (SfUnionContrCloseDate == date(0, 0, 0)) )
      КодСостДог = 0;
    elif( (reqclosa.Origin == REQ_ACC_ORIGIN_LOANS) and 
          (Note_ContractCloseDate == date(0, 0, 0)) )
      КодСостДог = 0;
    else
      КодСостДог = reqclosa.SubKind;
    end;
    ЗаписатьПолеЛог( "КодСостДог", string(КодСостДог) );

    // ДатаРастДог
    if(reqclosa.Origin == REQ_ACC_ORIGIN_LOANS)
      if( Note_ContractCloseDate != date(0, 0, 0) )
        ЗаписатьПолеЛог( "ДатаРастДог", DDpMMpYYYY(Note_ContractCloseDate) );
      end;
    else
      ЗаписатьПоле_ДатаРастДог( reqclosa, IfThenElse(IsSfContr, SfCntr, null), vers, 
                                IsSfUnionContr, SfUnionContrCloseDate );
    end;
  end;

end;

private macro WriteAccCloseDate(reqclosa, AccCloseDate : date)

  if(reqclosa.Origin == REQ_ACC_ORIGIN_LOANS)

    if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
      ЗаписатьПолеДляАннул( "ДатаЗакрСч", GetOldfnsMessageID() );
    else
      var ObjType : integer = OBJTYPE_REQCLOSA, 
          NoteKind : integer = REQCLOSA_NOTE_KIND_CONTRACT_CLOSEDATE;
      AccCloseDate = ReadNoteForObject(ObjType, UniID(reqclosa, ObjType), NoteKind);

      ЗаписатьПолеЛог( "ДатаЗакрСч", DDpMMpYYYY(AccCloseDate) );
    end;

  else
    ЗаписатьПоле_ДатаЗакрСч(AccCloseDate);
  end;

end;

private macro WriteAccInfoClose(GenAcc, reqclosa)
  StartBlockLogXML("Закрыт");

  WriteFieldWithCheckAnnul( "КодСостСч", "0" );
  WriteAccCloseDate(reqclosa, GenAcc.Close_Date);
  WriteContractInfo(GenAcc, reqclosa);

  FinishBlockLogXML("Закрыт");
end;

// СвСчет
private macro WriteAccInfo(Счет : string, Валюта : integer, GenAcc, reqclosa)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  WriteAccInfoAttrs(Счет, Валюта, GenAcc, reqclosa, ACCMSG_KIND_CLOSE);

  // Файл\Документ\СвСчет\Закрыт
  WriteAccInfoClose(GenAcc, reqclosa);

  FinishBlockLogXML("СвСчет");
end;

macro CheckObj( addrReq )
  record Req(reqclosa);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckClientSizeKPP,
                                              GenMesCheckClientSymbolKPP,
                                              GenMesCheckClientSizeOGRN,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality
                                             );
  
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_CLOSE );
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record reqclosa(reqclosa);
  SetBuff(reqclosa, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SBC2");

  record GenAcc(account);
  var Счет : string, Валюта : integer, ErrMsg : string = "";

  ПолучитьСчетСВалютой(reqclosa, 0 /*Закрытие*/, Счет, Валюта);
  if(reqclosa.Origin == REQ_ACC_ORIGIN_LOANS)
    ПолучитьСчет_БО( reqclosa.Code_Currency, reqclosa.Account, reqclosa, OBJTYPE_REQCLOSA, GenAcc );
  else
    ПолучитьСчет(Валюта, Счет, GenAcc);
  end;

  var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg( GenAcc.Department, 
                                                         ACCMSG_KIND_CLOSE, 
                                                         vers );

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonSBC);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonSBC, Счет, "", Валюта, GenAcc.Client, OldMes.MesID);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonSBC, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo(GenAcc.Client, ACCMSG_KIND_CLOSE, GenAcc.Open_Date, reqclosa.ClientType311, "", NULL, NULL, reqclosa);

  // Файл \ Документ \ СвСчет
  WriteAccInfo(Счет, Валюта, GenAcc, reqclosa);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonSBC._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
  end;

  if(CommonSBC._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
