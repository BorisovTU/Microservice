/*
  $Name:         uf243244.mac
  $Module:       Межбанковские расчеты
  $Description:  Функции для запросов/ответов ED243/ED244
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Функции для запросов/ответов ED243/ED244                                 */
/*                                                                          */
/*  Имя файла: uf243244.mac                                                 */
/*  Создан:    03.07.13                                      Чукина Т.А.    */
/****************************************************************************/

import "wlglobal.mac", "lib_str.mac", oralib, likepy, "wlgendoc.mac", "wlgenmes.mac", 
       "wluftool.mac", "ufgenmes.mac", "pmlib.mac", pm_const,
       "TFieldsPI.mac", "TFieldsOperPartRoute.mac";

// Контекст вызова для процедуры обработки учетного объекта по сообщению ED244
const ED244_PROCOBJ_CALLFROM_SCROL : integer = 0,
      ED244_PROCOBJ_CALLFROM_GENOBJ : integer = 1;

//------------------------------------------------------------------------------
// Таблица Соответствие номеров полей реквизитам платежа
//------------------------------------------------------------------------------

macro IsEqFName243244(FieldName : string, CheckName : string) : bool
  var EqualTable : TArray = makeArray
    (
      makeArray("AccDocNo", "PayerDocNo"),
      makeArray("AccDocDate", "PayerDocDate"),
      makeArray("Sum", "TransactionSum"),
      makeArray("PayerLongName", "PayerName", "TradePayerName"),
      makeArray("PayerAcc", "TransacPayerAcc"),
      makeArray("PayeeBIC"),
      makeArray("PayeeCorrAcc"),
      makeArray("PayeeLongName", "PayeeName", "TradePayeeName"),
      makeArray("PayeeAcc", "TransacPayeeAcc"),
      makeArray("Purpose", "TransacPurpose", "74"),
      makeArray("PayerINN", "TransacPayerINN"),
      makeArray("PayeeINN", "TransacPayeeINN"),
      makeArray("F101R"),
      makeArray("F106R"),
      makeArray("F107R"),
      makeArray("F108R"),
      makeArray("F109R"),
      makeArray("F110R"),
      makeArray("EnterDate"),
      makeArray("Address"),
      makeArray("TransactionID"),
      makeArray("TransactionDate"),
      makeArray("PayerPersonalID"),
      makeArray("PayerPersonName"),
      makeArray("PayerAddress"),
      makeArray("PayeePersonalID"),
      makeArray("PayeePersonName"),
      makeArray("PayeeAddress"),
      makeArray("OperationID"),
      makeArray("DocIndex"),
      makeArray("F111R"),
      makeArray("F112R"),
      makeArray("81.1"),
      makeArray("81.2"),
      makeArray("81.3"),
      makeArray("81.4"),
      makeArray("82.1"),
      makeArray("82.2"),
      makeArray("82.3"),
      makeArray("82.4"),
      makeArray("83.3"),
      makeArray("84.3"),
      makeArray("85.1"),
      makeArray("85.2"),
      makeArray("85.3"),
      makeArray("85.4"),
      makeArray("86.1"),
      makeArray("86.2"),
      makeArray("86.3"),
      makeArray("86.4")
    );

  for(var EqualNames : TArray, EqualTable)
    if( (find(EqualNames, FieldName) >= 0) and
        (find(EqualNames, CheckName) >= 0)
      )
      return TRUE;
    end;
  end;

  return FALSE;
end;

macro GetFNoByFName(FieldName : string) : integer

  if  ( IsEqFName243244(FieldName, "AccDocNo") )
    return 3;
  elif( IsEqFName243244(FieldName, "AccDocDate") )
    return 4;
  elif( IsEqFName243244(FieldName, "Sum") )
    return 7;
  elif( IsEqFName243244(FieldName, "PayerLongName") )
    return 8;
  elif( IsEqFName243244(FieldName, "PayerAcc") )
    return 9;
  elif( IsEqFName243244(FieldName, "PayeeBIC") )
    return 14;
  elif( IsEqFName243244(FieldName, "PayeeCorrAcc") )
    return 15;
  elif( IsEqFName243244(FieldName, "PayeeLongName") )
    return 16;
  elif( IsEqFName243244(FieldName, "PayeeAcc") )
    return 17;
  elif( IsEqFName243244(FieldName, "Purpose") )
    return 24;
  elif( IsEqFName243244(FieldName, "PayerINN") )
    return 60;
  elif( IsEqFName243244(FieldName, "PayeeINN") )
    return 61;
  elif( IsEqFName243244(FieldName, "F101R") )
    return 101;
  elif( IsEqFName243244(FieldName, "F106R") )
    return 106;
  elif( IsEqFName243244(FieldName, "F107R") )
    return 107;
  elif( IsEqFName243244(FieldName, "F108R") )
    return 108;
  elif( IsEqFName243244(FieldName, "F109R") )
    return 109;
  elif( IsEqFName243244(FieldName, "F110R") )
    return 110;
  end;

  return 0;
end;

macro FitsFieldNoOrName(FieldNo, // м.б. string, int, null
                        FieldName : string, CheckFName : string) : bool

  if( FieldNo == null )
    if( IsEqFName243244(FieldName, CheckFName) )
      return TRUE;
    end;
  elif( IsEqFName243244(FieldNo, CheckFName) or (FieldNo == GetFNoByFName(CheckFName)) )
    return TRUE;
  end;

  return FALSE;
end;

private macro GetAddress
( PartyID : integer, 
  address // : BtFileRef | TbFile | StrucRef | TRecHandler
)

  if( НайтиЮридическийАдресСубъекта(PartyID, address) or 
      НайтиАдресСубъекта(PartyID, PTADDR_REAL, address) or
      НайтиПочтовыйАдресСубъекта(PartyID, PTADDR_POST, address)
    )
    return true;
  end;

  return false;
end;

macro GetFieldValFromPaym( FieldNo: string, FieldName: string, FieldValue: @string,
                           p_pm, p_rm, p_cr, p_db, p_pspaydem,
                           p_PayerBank, p_PrevInstrAgent, p_SenderBank, p_ExecutorBank, p_IntermediaryBank, p_ReceiverBank
                         ): bool
  macro GetRls(cr, db) : integer
    if( (cr.Group == PAYMENTS_GROUP_EXTERNAL) and cr.RlsFormID )
      return cr.RlsFormID;
    else
      return db.RlsFormID;
    end;
  end;

  macro GetSchem(cr, db) : integer
    if( (cr.Group == PAYMENTS_GROUP_EXTERNAL) and cr.TpSchemID )
      return cr.TpSchemID;
    else
      return db.TpSchemID;
    end;
  end;

  // На случай, если переданы TRecHandler-ы
  record pm(pmpaym); copy(pm, p_pm);
  record rm(pmrmprop); copy(rm, p_rm);
  record cr(pmprop); copy(cr, p_cr);
  record db(pmprop); copy(db, p_db);
  record pspaydem(pspaydem);
  if(p_pspaydem != null)
    copy(pspaydem, p_pspaydem);
  else
    ClearRecord(pspaydem); // запись pspaydem у платежа может отсутствовать
  end;

  // отдельные участники операции могут отсутствовать
  record PayerBank(pmroute);
  if(p_PayerBank != null)
    copy(PayerBank, p_PayerBank);
  end;
  record PrevInstrAgent(pmroute);
  if(p_PrevInstrAgent != null)
    copy(PrevInstrAgent, p_PrevInstrAgent);
  end;
  record SenderBank(pmroute);
  if(p_SenderBank != null)
    copy(SenderBank, p_SenderBank);
  end;
  record ExecutorBank(pmroute);
  if(p_ExecutorBank != null)
    copy(ExecutorBank, p_ExecutorBank);
  end;
  record IntermediaryBank(pmroute);
  if(p_IntermediaryBank != null)
    copy(IntermediaryBank, p_IntermediaryBank);
  end;
  record ReceiverBank(pmroute);
  if(p_ReceiverBank != null)
    copy(ReceiverBank, p_ReceiverBank);
  end;

  var sPaymentID : string = makeObjectID(OBJTYPE_PAYMENT, NULL, pm);

  FieldValue = "";

  if( FitsFieldNoOrName(FieldNo, FieldName, "AccDocNo") )
    FieldValue = GetLastSymbols(rm.Number, PM_DOCNO_NONZERO_LEN);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "AccDocDate") )
    FieldValue = YYYYMMDD(rm.Date);
  elif(FieldNo == "5")
    FieldValue = GetPaytKind( GetUFBSMesFormName(GetRls(cr, db)), rm.PaymentKind, GetSchem(cr, db) );
  elif( FitsFieldNoOrName(FieldNo, FieldName, "Sum") )
    FieldValue = MoneyToStrUFBS(pm.BaseAmount);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerLongName") )
    if( pm.Payer != -1 )
      FieldValue = Bnk_GetPartyName(pm.Payer);
    else
      FieldValue = rm.PayerName;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerAcc") )
    FieldValue = pm.PayerAccount;
  elif(FieldNo == "11")
    FieldValue = db.BankCode;
  elif(FieldNo == "12")
    FieldValue = rm.PayerCorrAccNostro;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeBIC") )
    FieldValue = cr.BankCode;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeCorrAcc") )
    FieldValue = rm.ReceiverCorrAccNostro;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeLongName") )
    if( pm.Receiver != -1 )
      FieldValue = Bnk_GetPartyName(pm.Receiver);
    else
      FieldValue = rm.ReceiverName;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeAcc") )
    FieldValue = pm.ReceiverAccount;
  elif(FieldNo == "18")
    FieldValue = rm.ShifrOper;
  elif(FieldNo == "21")
    FieldValue = rm.Priority;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "Purpose") )
    FieldValue = rm.Ground;
  elif( InList(FieldNo, "35", "36", "72") )
    if(pspaydem.OrderID)
      if(FieldNo == "35")
        FieldValue = string( GetPaytCondition(pspaydem.AcceptTerm) );
      elif(FieldNo == "36")
        FieldValue = pspaydem.AcceptPeriod;
      elif(FieldNo == "72")
        FieldValue = YYYYMMDD(pspaydem.AcceptDate);
      end;
    else
      return FALSE;
    end;
  elif(FieldNo == "37")
    FieldValue = YYYYMMDD(rm.DocDispatchDate);
  elif(FieldNo == "38")
    FieldValue = pm.PartPaymNumber;
  elif(FieldNo == "39")
    FieldValue = pm.PartPaymShifrMain;
  elif(FieldNo == "40")
    FieldValue = pm.PartPaymNumMain;
  elif(FieldNo == "41")
    FieldValue = YYYYMMDD(pm.PartPaymDateMain);
  elif(FieldNo == "42")
    FieldValue = MoneyToStrUFBS( pm.PartPaymRestAmountMain );
  elif(FieldNo == "48")
    FieldValue = YYYYMMDD(pm.ReceiverBankMarkDate);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerINN") )
    if( pm.Payer != -1 )
      FieldValue = ПолучитьКодСубъекта( pm.Payer, PTCK_INN );
    else
      FieldValue = rm.PayerINN;
    end;
    FieldValue = RemoveKPP(FieldValue);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeINN") )
    if( pm.Receiver != -1 )
      FieldValue = ПолучитьКодСубъекта( pm.Receiver, PTCK_INN );
    else
      FieldValue = rm.ReceiverINN;
    end;
    FieldValue = RemoveKPP(FieldValue);
  elif(FieldNo == "62")
    FieldValue = YYYYMMDD(pm.PayerBankEnterDate);
  elif(FieldNo == "63")
    FieldValue = YYYYMMDD(pm.I2PlaceDate);
  elif(FieldNo == "70")
    FieldValue = GetContentOperationString(pm.ContentOperation, pm);
  elif(FieldNo == "71")
    FieldValue = YYYYMMDD(rm.PayerChargeOffDate);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F101R") )
    FieldValue = rm.TaxAuthorState;
  elif( FieldNo == "102" )
    if( pm.Payer != -1 )
      FieldValue = ПолучитьКодСубъекта( pm.Payer, PTCK_INN );
    else
      FieldValue = rm.PayerINN;
    end;
    FieldValue = RemoveINN(FieldValue);
  elif( FieldNo == "103" )
    if( pm.Receiver != -1 )
      FieldValue = ПолучитьКодСубъекта( pm.Receiver, PTCK_INN );
    else
      FieldValue = rm.ReceiverINN;
    end;
    FieldValue = RemoveINN(FieldValue);
  elif(FieldNo == "104")
    FieldValue = rm.BTTTICode;
  elif(FieldNo == "105")
    FieldValue = rm.OKATOCode;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F106R") )
    FieldValue = rm.TaxPmGround;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F107R") )
    FieldValue = rm.TaxPmPeriod;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F108R") )
    FieldValue = rm.TaxPmNumber;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F109R") )
    FieldValue = rm.TaxPmDate;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F110R") )
    FieldValue = rm.TaxPmType;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "EnterDate") )
    FieldValue = YYYYMMDD(rm.ReceiverChargeOffDate);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "Address") )
    if( GetAddress(pm.Payer, wladdress) == true )
      FieldValue = wladdress.Adress;
    else
      msgboxex("Не найден адрес плательщика", 0, 0, "Предупреждение");
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "TransactionID") )
    FieldValue = rm.Reference;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "TransactionDate") )
    var TransactionDate = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_ACCEPTUATEDATE);
    if( (TransactionDate != null) and (TransactionDate != date(0, 0, 0)) )
      FieldValue = YYYYMMDD(TransactionDate);
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerPersonalID") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_PAYERID);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerPersonName") )
    if( pm.Payer != -1 )
      FieldValue = Bnk_GetPartyName(pm.Payer);
    else
      FieldValue = RemoveAddressFromPaymSideName(rm.PayerName);
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayerAddress") )
    if( GetAddress(pm.Payer, wladdress) == true )
      FieldValue = wladdress.Adress;
    else
      FieldValue = GetAddressFromPaymSideName(rm.PayerName);
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeePersonalID") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_PAYEEID);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeePersonName") )
    if( pm.Receiver != -1 )
      FieldValue = Bnk_GetPartyName(pm.Receiver);
    else
      FieldValue = RemoveAddressFromPaymSideName(rm.ReceiverName);
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "PayeeAddress") )
    if( GetAddress(pm.Receiver, wladdress) == true )
      FieldValue = wladdress.Adress;
    else
      FieldValue = GetAddressFromPaymSideName(rm.ReceiverName);
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "OperationID") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_UNI_OPERATIONID);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "DocIndex") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_DOCINDEX);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F111R") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_OFK_ACCOUNT);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "F112R") )
    FieldValue = ReadNoteForObject(OBJTYPE_PAYMENT, sPaymentID, PM_NOTEKIND_PAYM_BUDGETRECEIVER_ACC);
  elif( FitsFieldNoOrName(FieldNo, FieldName, "81.1") )
    FieldValue = PayerBank.Name;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "81.2") )
    if(p_PayerBank != null)
      if(PayerBank.CodeKind != PTCK_BIC)
        FieldValue = GetPartyCode(PayerBank.PartyID, PTCK_BIC);
      else
        FieldValue = PayerBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "81.3") )
    if(p_PayerBank != null)
      if(PayerBank.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(PayerBank.PartyID, PTCK_SWIFT);
      else
        FieldValue = PayerBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "81.4") )
    FieldValue = PayerBank.OutAccount;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "82.1") )
    FieldValue = PrevInstrAgent.Name;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "82.2") )
    if(p_PrevInstrAgent != null)
      if(PrevInstrAgent.CodeKind != PTCK_BIC)
        FieldValue = GetPartyCode(PrevInstrAgent.PartyID, PTCK_BIC);
      else
        FieldValue = PrevInstrAgent.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "82.3") )
    if(p_PrevInstrAgent != null)
      if(PrevInstrAgent.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(PrevInstrAgent.PartyID, PTCK_SWIFT);
      else
        FieldValue = PrevInstrAgent.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "82.4") )
    FieldValue = PrevInstrAgent.OutAccount;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "83.3") )
    if(p_SenderBank != null)
      if(SenderBank.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(SenderBank.PartyID, PTCK_SWIFT);
      else
        FieldValue = SenderBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "84.3") )
    if(p_ExecutorBank != null)
      if(ExecutorBank.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(ExecutorBank.PartyID, PTCK_SWIFT);
      else
        FieldValue = ExecutorBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "85.1") )
    FieldValue = IntermediaryBank.Name;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "85.2") )
    if(p_IntermediaryBank != null)
      if(IntermediaryBank.CodeKind != PTCK_BIC)
        FieldValue = GetPartyCode(IntermediaryBank.PartyID, PTCK_BIC);
      else
        FieldValue = IntermediaryBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "85.3") )
    if(p_IntermediaryBank != null)
      if(IntermediaryBank.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(IntermediaryBank.PartyID, PTCK_SWIFT);
      else
        FieldValue = IntermediaryBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "85.4") )
    FieldValue = IntermediaryBank.InAccount;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "86.1") )
    FieldValue = ReceiverBank.Name;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "86.2") )
    if(p_ReceiverBank != null)
      if(ReceiverBank.CodeKind != PTCK_BIC)
        FieldValue = GetPartyCode(ReceiverBank.PartyID, PTCK_BIC);
      else
        FieldValue = ReceiverBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "86.3") )
    if(p_ReceiverBank != null)
      if(ReceiverBank.CodeKind != PTCK_SWIFT)
        FieldValue = GetPartyCode(ReceiverBank.PartyID, PTCK_SWIFT);
      else
        FieldValue = ReceiverBank.CodeValue;
      end;
    end;
  elif( FitsFieldNoOrName(FieldNo, FieldName, "86.4") )
    FieldValue = ReceiverBank.InAccount;
  else
    return FALSE;
  end;

  if(not FieldValue)
    return FALSE;
  end;

  return TRUE;
end;

// Обёртка для вызова из сишника
macro GetFieldValFromPaym_C( FieldNo: string, FieldValue: string,
                             addr_pm, addr_rm, addr_cr, addr_db, addr_pspaydem,
                             addr_PayerBank, addr_PrevInstrAgent, addr_SenderBank, addr_ExecutorBank, addr_IntermediaryBank, addr_ReceiverBank
                           ): bool

  record pm(pmpaym);          SetBuff( pm, addr_pm );
  record rm(pmrmprop);        SetBuff( rm, addr_rm );
  record cr(pmprop);          SetBuff( cr, addr_cr );
  record db(pmprop);          SetBuff( db, addr_db );
  record pspaydem(pspaydem);  SetBuff( pspaydem, addr_pspaydem );

  record PayerBank(pmroute);          SetBuff( PayerBank, addr_PayerBank );
  record PrevInstrAgent(pmroute);     SetBuff( PrevInstrAgent, addr_PrevInstrAgent );
  record SenderBank(pmroute);         SetBuff( SenderBank, addr_SenderBank );
  record ExecutorBank(pmroute);       SetBuff( ExecutorBank, addr_ExecutorBank );
  record IntermediaryBank(pmroute);  SetBuff( IntermediaryBank, addr_IntermediaryBank );
  record ReceiverBank(pmroute);       SetBuff( ReceiverBank, addr_ReceiverBank );

  var IsValueDefined = GetFieldValFromPaym( FieldNo, null, @FieldValue,
                                            pm, rm, cr, db, pspaydem, 
                                            PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank );

  if(not IsValueDefined)
    FieldValue = "";
  end;

  SetParm( 1, FieldValue );

  return IsValueDefined;
end;

macro ProcessPIFld( FNo            : integer, 
                    FieldName      : string, 
                    FieldValue     : string, 
                    FieldsPI       : TFieldsPI,
                    NeedSetPayerPI : @bool, 
                    NeedSetPayeePI : @bool
                  )

  if( FitsFieldNoOrName(FNo, FieldName, "PayerLongName") )
    // PaymAttrName = "Наименование плательщика";
    if( (FieldsPI.Payer.NewValues.ClientName != FieldValue) and
        (RemoveAddressFromPaymSideName(FieldsPI.Payer.NewValues.ClientName) != FieldValue)
      )
      FieldsPI.Payer.NewValues.ClientName = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerAcc") )
    // PaymAttrName = "Счет плательщика";
    if(FieldsPI.Payer.NewValues.Account != FieldValue)
      FieldsPI.Payer.NewValues.Account = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif(FNo == 11)
    // PaymAttrName = "БИК банка плательщика";
    if(FieldsPI.Payer.NewValues.BankCode != FieldValue)
      FieldsPI.Payer.NewValues.BankCode = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif(FNo == 12)
    // PaymAttrName = "Счет банка плательщика";
    if(FieldsPI.Payer.NewValues.CorrAccNostro != FieldValue)
      FieldsPI.Payer.NewValues.CorrAccNostro = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeBIC") )
    // PaymAttrName = "БИК банка получателя";
    if(FieldsPI.Payee.NewValues.BankCode != FieldValue)
      FieldsPI.Payee.NewValues.BankCode = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeCorrAcc") )
    // PaymAttrName = "Счет банка получателя";
    if(FieldsPI.Payee.NewValues.CorrAccNostro != FieldValue)
      FieldsPI.Payee.NewValues.CorrAccNostro = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeLongName") )
    // PaymAttrName = "Наименование получателя";
    if( (FieldsPI.Payee.NewValues.ClientName != FieldValue) and
        (RemoveAddressFromPaymSideName(FieldsPI.Payee.NewValues.ClientName) != FieldValue)
      )
      FieldsPI.Payee.NewValues.ClientName = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeAcc") )
    // PaymAttrName = "Счет получателя";
    if(FieldsPI.Payee.NewValues.Account != FieldValue)
      FieldsPI.Payee.NewValues.Account = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerINN") )
    // PaymAttrName = "ИНН плательщика";
    if(FieldsPI.Payer.NewValues.ClientINN != FieldValue)
      FieldsPI.Payer.NewValues.ClientINN = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeINN") )
    // PaymAttrName = "ИНН получателя";
    if(FieldsPI.Payee.NewValues.ClientINN != FieldValue)
      FieldsPI.Payee.NewValues.ClientINN = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FNo == 102 )
    // PaymAttrName = "КПП плательщика";
    if(FieldsPI.Payer.NewValues.ClientKPP != FieldValue)
      FieldsPI.Payer.NewValues.ClientKPP = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FNo == 103 )
    // PaymAttrName = "КПП получателя";
    if(FieldsPI.Payee.NewValues.ClientKPP != FieldValue)
      FieldsPI.Payee.NewValues.ClientKPP = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "Address") )
    // PaymAttrName = "Адрес плательщика";
    if(FieldsPI.Payer.NewValues.ClientAddress != FieldValue)
      FieldsPI.Payer.NewValues.ClientAddress = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerPersonName") )
    // PaymAttrName = "ФИО плательщика";
    if( (FieldsPI.Payer.NewValues.ClientName != FieldValue) and
        (RemoveAddressFromPaymSideName(FieldsPI.Payer.NewValues.ClientName) != FieldValue)
      )
      FieldsPI.Payer.NewValues.ClientName = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerAddress") )
    // PaymAttrName = "Адрес плательщика";
    if(FieldsPI.Payer.NewValues.ClientAddress != FieldValue)
      FieldsPI.Payer.NewValues.ClientAddress = FieldValue;
      NeedSetPayerPI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeePersonName") )
    // PaymAttrName = "ФИО получателя";
    if( (FieldsPI.Payee.NewValues.ClientName != FieldValue) and
        (RemoveAddressFromPaymSideName(FieldsPI.Payee.NewValues.ClientName) != FieldValue)
      )
      FieldsPI.Payee.NewValues.ClientName = FieldValue;
      NeedSetPayeePI = true;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeAddress") )
    // PaymAttrName = "Адрес получателя";
    if(FieldsPI.Payee.NewValues.ClientAddress != FieldValue)
      FieldsPI.Payee.NewValues.ClientAddress = FieldValue;
      NeedSetPayeePI = true;
    end;
  end;
end;

macro ProcessRoutePartFld
                  (
                    FieldName      : string, 
                    FieldValue     : string, 
                    FieldsRoutePart : TFieldsRoutePart,
                    IsRouteChanged : @bool
                  )

  if( FitsFieldNoOrName(null, FieldName, "81.1") )
    if(FieldsRoutePart.NewValues.PayerBank.rec.Name != FieldValue)
      FieldsRoutePart.NewValues.PayerBank.rec.Name = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "81.2") )
    if( (FieldsRoutePart.NewValues.PayerBank.rec.CodeKind == PTCK_BIC) and
        (FieldsRoutePart.NewValues.PayerBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.PayerBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "81.3") )
    if( (FieldsRoutePart.NewValues.PayerBank.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.PayerBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.PayerBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "81.4") )
    if(FieldsRoutePart.NewValues.PayerBank.rec.OutAccount != FieldValue)
      FieldsRoutePart.NewValues.PayerBank.rec.OutAccount = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "82.1") )
    if(FieldsRoutePart.NewValues.PrevInstrAgent.rec.Name != FieldValue)
      FieldsRoutePart.NewValues.PrevInstrAgent.rec.Name = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "82.2") )
    if( (FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeKind == PTCK_BIC) and
        (FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "82.3") )
    if( (FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "82.4") )
    if(FieldsRoutePart.NewValues.PrevInstrAgent.rec.OutAccount != FieldValue)
      FieldsRoutePart.NewValues.PrevInstrAgent.rec.OutAccount = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "83.3") )
    if( (FieldsRoutePart.NewValues.SenderBank.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.SenderBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.SenderBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "84.3") )
    if( (FieldsRoutePart.NewValues.ExecutorBank.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.ExecutorBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.ExecutorBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "85.1") )
    if(FieldsRoutePart.NewValues.IntermediaryBank.rec.Name != FieldValue)
      FieldsRoutePart.NewValues.IntermediaryBank.rec.Name = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "85.2") )
    if( (FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeKind == PTCK_BIC) and
        (FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "85.3") )
    if( (FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "85.4") )
    if(FieldsRoutePart.NewValues.IntermediaryBank.rec.InAccount != FieldValue)
      FieldsRoutePart.NewValues.IntermediaryBank.rec.InAccount = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "86.1") )
    if(FieldsRoutePart.NewValues.ReceiverBank.rec.Name != FieldValue)
      FieldsRoutePart.NewValues.ReceiverBank.rec.Name = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "86.2") )
    if( (FieldsRoutePart.NewValues.ReceiverBank.rec.CodeKind == PTCK_BIC) and
        (FieldsRoutePart.NewValues.ReceiverBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.ReceiverBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "86.3") )
    if( (FieldsRoutePart.NewValues.ReceiverBank.rec.CodeKind == PTCK_SWIFT) and
        (FieldsRoutePart.NewValues.ReceiverBank.rec.CodeValue != FieldValue)
      )
      FieldsRoutePart.NewValues.ReceiverBank.rec.CodeValue = FieldValue;
      IsRouteChanged = true;
    end;
  elif( FitsFieldNoOrName(null, FieldName, "86.4") )
    if(FieldsRoutePart.NewValues.ReceiverBank.rec.InAccount != FieldValue)
      FieldsRoutePart.NewValues.ReceiverBank.rec.InAccount = FieldValue;
      IsRouteChanged = true;
    end;
  end;
end;

// если Payment.DocKind == PS_PAYORDER, то перед вызовом этой функции должен быть
// создан объект RsbPsPayOrder
macro UpdatePaymFromField( Payment      : RsbPayment, 
                           FNo          : integer, 
                           FieldName    : string, 
                           FieldValue   : string, 
                           FieldsPI     : TFieldsPI,
                           FieldsRoutePart : TFieldsRoutePart,
                           PaymAttrName : @string, 
                           OldValue     : @variant, 
                           NewValue     : @variant
                         ) : bool

  if( FitsFieldNoOrName(FNo, FieldName, "AccDocNo") )
    PaymAttrName = "Номер документа";
    OldValue = Payment.Number;
    NewValue = Payment.Number = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "AccDocDate") )
    PaymAttrName = "Дата документа";
    OldValue = Payment.Date;
    NewValue = Payment.Date = ToDate(FieldValue);
  elif(FNo == 5)
    PaymAttrName = "Вид платежа";
    OldValue = Payment.PaymentKind;
    NewValue = Payment.PaymentKind = PaytKindToPaymentKind(int(FieldValue));
  elif( FitsFieldNoOrName(FNo, FieldName, "Sum") )
    PaymAttrName = "Сумма";
    OldValue = Payment.BaseAmount;
    NewValue = moneyL( FieldValue )/100;
    if(NewValue != OldValue)
      Payment.PayerAmount = $0;
      Payment.ReceiverAmount = $0;
      Payment.BaseAmount = NewValue;
      Payment.Actuate();
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerLongName") )
    PaymAttrName = "Наименование плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientName;
    NewValue = FieldsPI.Payer.NewValues.ClientName;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerAcc") )
    PaymAttrName = "Счет плательщика";
    OldValue = FieldsPI.Payer.OldValues.Account;
    NewValue = FieldsPI.Payer.NewValues.Account;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif(FNo == 11)
    PaymAttrName = "БИК банка плательщика";
    OldValue = FieldsPI.Payer.OldValues.BankCode;
    NewValue = FieldsPI.Payer.NewValues.BankCode;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif(FNo == 12)
    PaymAttrName = "Счет банка плательщика";
    OldValue = FieldsPI.Payer.OldValues.CorrAccNostro;
    NewValue = FieldsPI.Payer.NewValues.CorrAccNostro;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeBIC") )
    PaymAttrName = "БИК банка получателя";
    OldValue = FieldsPI.Payee.OldValues.BankCode;
    NewValue = FieldsPI.Payee.NewValues.BankCode;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeCorrAcc") )
    PaymAttrName = "Счет банка получателя";
    OldValue = FieldsPI.Payee.OldValues.CorrAccNostro;
    NewValue = FieldsPI.Payee.NewValues.CorrAccNostro;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeLongName") )
    PaymAttrName = "Наименование получателя";
    OldValue = FieldsPI.Payee.OldValues.ClientName;
    NewValue = FieldsPI.Payee.NewValues.ClientName;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeAcc") )
    PaymAttrName = "Счет получателя";
    OldValue = FieldsPI.Payee.OldValues.Account;
    NewValue = FieldsPI.Payee.NewValues.Account;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif(FNo == 18)
    PaymAttrName = "Вид операции";
    OldValue = Payment.ShifrOper;
    NewValue = Payment.ShifrOper = FieldValue;
  elif(FNo == 21)
    PaymAttrName = "Очередность платежа";
    OldValue = Payment.Priority;
    NewValue = Payment.Priority = int(FieldValue);
  elif( FitsFieldNoOrName(FNo, FieldName, "Purpose") )
    PaymAttrName = "Назначение платежа";
    OldValue = Payment.Ground;
    NewValue = Payment.Ground = FieldValue;
  elif( InList(FNo, 35, 36, 72) )
    if(Payment.DocKind == PS_PAYORDER)
      var PsOrder : RsbPsPayOrder = RsbPsPayOrder(Payment.PaymentID);
      if(FNo == 35)
        PaymAttrName = "Условие оплаты";
        OldValue = PsOrder.AcceptTerm;
        NewValue = PsOrder.AcceptTerm = GetAcceptTermByPaytCond(FieldValue);
      elif(FNo == 36)
        PaymAttrName = "Срок для акцепта";
        OldValue = PsOrder.AcceptPeriod;
        NewValue = PsOrder.AcceptPeriod = int(FieldValue);
      elif(FNo == 72)
        PaymAttrName = "Окончание срока акцепта";
        OldValue = PsOrder.AcceptDate;
        NewValue = PsOrder.AcceptDate = ToDate(FieldValue);
      end;
    else
      return FALSE;
    end;
  elif(FNo == 37)
    PaymAttrName = "Дата отсылки документов плательщику";
    OldValue = Payment.DocDispatchDate;
    NewValue = Payment.DocDispatchDate = ToDate(FieldValue);
  elif(FNo == 38)
    PaymAttrName = "Номер частичного платежа";
    OldValue = Payment.PartPaymNumber;
    NewValue = Payment.PartPaymNumber = int(FieldValue);
  elif(FNo == 39)
    PaymAttrName = "Шифр платежного документа";
    OldValue = Payment.PartPaymShifrMain;
    NewValue = Payment.PartPaymShifrMain = FieldValue;
  elif(FNo == 40)
    PaymAttrName = "Номер исходного документа";
    OldValue = Payment.PartPaymNumMain;
    NewValue = Payment.PartPaymNumMain = FieldValue;
  elif(FNo == 41)
    PaymAttrName = "Дата исходного документа";
    OldValue = Payment.PartPaymDateMain;
    NewValue = Payment.PartPaymDateMain = ToDate(FieldValue);
  elif(FNo == 42)
    PaymAttrName = "Сумма остатка платежа";
    OldValue = Payment.PartPaymRestAmountMain;
    NewValue = Payment.PartPaymRestAmountMain = moneyL( FieldValue )/100;
  elif(FNo == 48)
    PaymAttrName = "Дата представления документов в банк получателя";
    OldValue = Payment.ReceiverBankMarkDate;
    NewValue = Payment.ReceiverBankMarkDate = ToDate(FieldValue);
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerINN") )
    PaymAttrName = "ИНН плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientINN;
    NewValue = FieldsPI.Payer.NewValues.ClientINN;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeINN") )
    PaymAttrName = "ИНН получателя";
    OldValue = FieldsPI.Payee.OldValues.ClientINN;
    NewValue = FieldsPI.Payee.NewValues.ClientINN;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif(FNo == 62)
    PaymAttrName = "Дата поступления в банк плательщика";
    OldValue = Payment.PayerBankEnterDate;
    NewValue = Payment.PayerBankEnterDate = ToDate(FieldValue);
  elif(FNo == 63)
    PaymAttrName = "Дата помещения в картотеку";
    OldValue = Payment.I2PlaceDate;
    NewValue = Payment.I2PlaceDate = ToDate(FieldValue);
  elif(FNo == 70)
    PaymAttrName = "Содержание операции";
    OldValue = Payment.ContentOperation;
    NewValue = Payment.ContentOperation = FieldValue;
  elif(FNo == 71)
    PaymAttrName = "Дата списания со счета плательщика";
    OldValue = Payment.PayerChargeOffDate;
    NewValue = Payment.PayerChargeOffDate = ToDate(FieldValue);
  elif( FitsFieldNoOrName(FNo, FieldName, "F101R") )
    PaymAttrName = "Статус составителя";
    OldValue = Payment.TaxAuthorState;
    NewValue = Payment.TaxAuthorState = FieldValue;
  elif( FNo == 102 )
    PaymAttrName = "КПП плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientKPP;
    NewValue = FieldsPI.Payer.NewValues.ClientKPP;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FNo == 103 )
    PaymAttrName = "КПП получателя";
    OldValue = FieldsPI.Payee.OldValues.ClientKPP;
    NewValue = FieldsPI.Payee.NewValues.ClientKPP;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif(FNo == 104)
    PaymAttrName = "КБК";
    OldValue = Payment.BttTICode;
    NewValue = Payment.BttTICode = FieldValue;
  elif(FNo == 105)
    PaymAttrName = "ОКАТО";
    OldValue = Payment.OKATOCode;
    NewValue = Payment.OKATOCode = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "F106R") )
    PaymAttrName = "Основание налогового платежа";
    OldValue = Payment.TaxPmGround;
    NewValue = Payment.TaxPmGround = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "F107R") )
    PaymAttrName = "Налоговый период";
    OldValue = Payment.TaxPmPeriod;
    NewValue = Payment.TaxPmPeriod = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "F108R") )
    PaymAttrName = "Номер налогового документа";
    OldValue = Payment.TaxPmNumber;
    NewValue = Payment.TaxPmNumber = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "F109R") )
    PaymAttrName = "Дата налогового документа";
    OldValue = Payment.TaxPmDate;
    NewValue = Payment.TaxPmDate = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "F110R") )
    PaymAttrName = "Тип налогового документа";
    OldValue = Payment.TaxPmType;
    NewValue = Payment.TaxPmType = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "EnterDate") )
    PaymAttrName = "Дата зачисления средств на счет получателя";
    OldValue = Payment.ReceiverChargeOffDate;
    NewValue = Payment.ReceiverChargeOffDate = ToDate(FieldValue);
  elif( FitsFieldNoOrName(FNo, FieldName, "Address") )
    PaymAttrName = "Адрес плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientAddress;
    NewValue = FieldsPI.Payer.NewValues.ClientAddress;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "TransactionID") )
    PaymAttrName = "Номер записи в реестре";
    OldValue = Payment.Reference;
    NewValue = Payment.Reference = FieldValue;
  elif( FitsFieldNoOrName(FNo, FieldName, "TransactionDate") )
    PaymAttrName = "Дата перевода";
    // Записать в примечание платежа "Дата приема к исполнению" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_ACCEPTUATEDATE, ToDate(FieldValue)) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerPersonalID") )
    PaymAttrName = "Идентификатор плательщика";
    // Записать в примечание платежа "Идентификатор плательщика" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_PAYERID, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerPersonName") )
    PaymAttrName = "ФИО плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientName;
    NewValue = FieldsPI.Payer.NewValues.ClientName;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayerAddress") )
    PaymAttrName = "Адрес плательщика";
    OldValue = FieldsPI.Payer.OldValues.ClientAddress;
    NewValue = FieldsPI.Payer.NewValues.ClientAddress;
    if( (NewValue != OldValue) and not FieldsPI.Payer.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeePersonalID") )
    PaymAttrName = "Идентификатор получателя";
    // Записать в примечание платежа "Идентификатор получателя" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_PAYEEID, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeePersonName") )
    PaymAttrName = "ФИО получателя";
    OldValue = FieldsPI.Payee.OldValues.ClientName;
    NewValue = FieldsPI.Payee.NewValues.ClientName;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "PayeeAddress") )
    PaymAttrName = "Адрес получателя";
    OldValue = FieldsPI.Payee.OldValues.ClientAddress;
    NewValue = FieldsPI.Payee.NewValues.ClientAddress;
    if( (NewValue != OldValue) and not FieldsPI.Payee.IsSetPI )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "OperationID") )
    PaymAttrName = "Уникальный номер операции";
    // Записать в примечание платежа "ED108: Уникальный номер операции" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_UNI_OPERATIONID, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "DocIndex") )
    PaymAttrName = "Индекс документа";
    // Записать в примечание платежа "ED108: Индекс документа" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_DOCINDEX, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "F111R") )
    PaymAttrName = "Номер счета в ОФК";
    // Записать в примечание платежа "Номер счета в ОФК" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_OFK_ACCOUNT, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "F112R") )
    PaymAttrName = "Номер счета в финансовом органе";
    // Записать в примечание платежа "Номер счета бюджетополучателя в финансовом органе" значение FieldValue
    if( Payment.Notes.AddNote(PM_NOTEKIND_PAYM_BUDGETRECEIVER_ACC, FieldValue) != 0 )
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "81.1") )
    PaymAttrName = "Наименование банка-плательщика";
    OldValue = FieldsRoutePart.OldValues.PayerBank.rec.Name;
    NewValue = FieldsRoutePart.NewValues.PayerBank.rec.Name;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "81.2") )
    PaymAttrName = "БИК банка-плательщика";
    if(FieldsRoutePart.OldValues.PayerBank.rec.CodeKind == PTCK_BIC)
      OldValue = FieldsRoutePart.OldValues.PayerBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.PayerBank.rec.CodeKind == PTCK_BIC)
      NewValue = FieldsRoutePart.NewValues.PayerBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "81.3") )
    PaymAttrName = "BIC SWIFT банка-плательщика";
    if(FieldsRoutePart.OldValues.PayerBank.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.PayerBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.PayerBank.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.PayerBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "81.4") )
    PaymAttrName = "Номер счета банка-плательщика";
    OldValue = FieldsRoutePart.OldValues.PayerBank.rec.OutAccount;
    NewValue = FieldsRoutePart.NewValues.PayerBank.rec.OutAccount;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "82.1") )
    PaymAttrName = "Наименование предыдущего инструктирующего банка";
    OldValue = FieldsRoutePart.OldValues.PrevInstrAgent.rec.Name;
    NewValue = FieldsRoutePart.NewValues.PrevInstrAgent.rec.Name;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "82.2") )
    PaymAttrName = "БИК предыдущего инструктирующего банка";
    if(FieldsRoutePart.OldValues.PrevInstrAgent.rec.CodeKind == PTCK_BIC)
      OldValue = FieldsRoutePart.OldValues.PrevInstrAgent.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeKind == PTCK_BIC)
      NewValue = FieldsRoutePart.NewValues.PrevInstrAgent.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "82.3") )
    PaymAttrName = "BIC SWIFT предыдущего инструктирующего банка";
    if(FieldsRoutePart.OldValues.PrevInstrAgent.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.PrevInstrAgent.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.PrevInstrAgent.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.PrevInstrAgent.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "82.4") )
    PaymAttrName = "Номер счета предыдущего инструктирующего банка";
    OldValue = FieldsRoutePart.OldValues.PrevInstrAgent.rec.OutAccount;
    NewValue = FieldsRoutePart.NewValues.PrevInstrAgent.rec.OutAccount;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "83.3") )
    PaymAttrName = "BIC SWIFT банка-отправителя";
    if(FieldsRoutePart.OldValues.SenderBank.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.SenderBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.SenderBank.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.SenderBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "84.3") )
    PaymAttrName = "BIC SWIFT банка-исполнителя";
    if(FieldsRoutePart.OldValues.ExecutorBank.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.ExecutorBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.ExecutorBank.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.ExecutorBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "85.1") )
    PaymAttrName = "Наименование агента банка-получателя";
    OldValue = FieldsRoutePart.OldValues.IntermediaryBank.rec.Name;
    NewValue = FieldsRoutePart.NewValues.IntermediaryBank.rec.Name;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "85.2") )
    PaymAttrName = "БИК агента банка-получателя";
    if(FieldsRoutePart.OldValues.IntermediaryBank.rec.CodeKind == PTCK_BIC)
      OldValue = FieldsRoutePart.OldValues.IntermediaryBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeKind == PTCK_BIC)
      NewValue = FieldsRoutePart.NewValues.IntermediaryBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "85.3") )
    PaymAttrName = "BIC SWIFT агента банка-получателя";
    if(FieldsRoutePart.OldValues.IntermediaryBank.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.IntermediaryBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.IntermediaryBank.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.IntermediaryBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "85.4") )
    PaymAttrName = "Номер счета агента банка-получателя";
    OldValue = FieldsRoutePart.OldValues.IntermediaryBank.rec.InAccount;
    NewValue = FieldsRoutePart.NewValues.IntermediaryBank.rec.InAccount;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "86.1") )
    PaymAttrName = "Наименование банка-получателя";
    OldValue = FieldsRoutePart.OldValues.ReceiverBank.rec.Name;
    NewValue = FieldsRoutePart.NewValues.ReceiverBank.rec.Name;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "86.2") )
    PaymAttrName = "БИК банка-получателя";
    if(FieldsRoutePart.OldValues.ReceiverBank.rec.CodeKind == PTCK_BIC)
      OldValue = FieldsRoutePart.OldValues.ReceiverBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.ReceiverBank.rec.CodeKind == PTCK_BIC)
      NewValue = FieldsRoutePart.NewValues.ReceiverBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "86.3") )
    PaymAttrName = "BIC SWIFT банка-получателя";
    if(FieldsRoutePart.OldValues.ReceiverBank.rec.CodeKind == PTCK_SWIFT)
      OldValue = FieldsRoutePart.OldValues.ReceiverBank.rec.Name;
    end;
    if(FieldsRoutePart.NewValues.ReceiverBank.rec.CodeKind == PTCK_SWIFT)
      NewValue = FieldsRoutePart.NewValues.ReceiverBank.rec.Name;
    end;

    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  elif( FitsFieldNoOrName(FNo, FieldName, "86.4") )
    PaymAttrName = "Номер счета банка-получателя";
    OldValue = FieldsRoutePart.OldValues.ReceiverBank.rec.InAccount;
    NewValue = FieldsRoutePart.NewValues.ReceiverBank.rec.InAccount;
    if( (NewValue != OldValue) and not FieldsRoutePart.IsUpdated )
      // значение изменилось, но установить в платеже мы его не смогли
      return FALSE;
    end;
  else
    return FALSE;
  end;

  return TRUE;
end;
// конец Таблица Соответствие номеров полей реквизитам платежа -----------------


macro DDpMMpYYYYFromUFBS( DateStr : string) : string
  return DDpMMpYYYY( ToDate(DateStr) );

  OnError(er) // если дата не в формате YYYY-MM-DD, можем отвалиться при преобразовании
    return DateStr; // тогда просто возвращаем исходную строку без преобразований
end;

macro GetPartyNameByUIS(UIS : string) : string
  var BIC = "04"+ SubStr( UIS, 1, 7 );
  var select = "select party.t_Name "+ 
                   "from dparty_dbt party, dpartcode_dbt partcode "+
                          "where party.t_PartyID = partcode.t_PartyID " +
                          "and partcode.t_Codekind =:CodeKind "+
                          "and partcode.t_Code =:Code ";
  var params = makeArray( SQLParam("CodeKind", PTCK_BIC),
                          SQLParam("Code", BIC) );

  var rs = execSQLselect( select, params, FALSE );
  if( rs and rs.MoveNext() )
    return rs.value(0);
  end;

  return "";
end;

macro GetReqMesReceiverUFBS(wlmes, ReceiverUIS : @string, ReceiverName : @string)
  var PartyID = -1, PartyUIS = "", PartyName = "", 
      select = "", params : TArray, rs : RsdRecordset;

  if(wlmes.Direct == "X")
    select = "select partcode.t_Code, party.t_Name "+
              "from dparty_dbt party, dpartcode_dbt partcode, ddp_dep_dbt dp_dep "+
                        "where dp_dep.t_Code =: Department "+
                        "and dp_dep.t_PartyID = party.t_PartyID "+
                        "and partcode.t_PartyID = party.t_PartyID "+
                        "and partcode.t_CodeKind =: CodeKind ";

    params = makeArray( SQLParam("Department", wlmes.Department),
                        SQLParam("CodeKind", PTCK_UIS) );
    rs = execSQLselect( select, params, FALSE );

    if( rs and rs.MoveNext() )
      PartyUIS = rs.value(0);
      PartyName = rs.value(1);
    end;  
  else
    select = "select party.t_PartyID, party.t_Name " +
             "  from dparty_dbt party, dwlreq_dbt wlreq, dwlmeslnk_dbt lnk " +
             " where party.t_PartyID = wlreq.t_RecipientID " +
             "   and wlreq.t_ReqID = lnk.t_ObjID " +
             "   and lnk.t_ObjKind = :ObjKind " +
             "   and lnk.t_MesID = :MesID " +
             "   and lnk.t_Direct = chr(0) ";
    params = makeArray( SQLParam("ObjKind", OBJTYPE_REQ),
                        SQLParam("MesID",   wlmes.MesID) );
    rs = execSQLselect( select, params );
    if(rs and rs.moveNext)
      PartyID = rs.value("t_PartyID");
      PartyName = rs.value("t_Name");
    end;
    PartyUIS = ПолучитьКодСубъекта(PartyID, PTCK_UIS);
  end;

  if(PartyUIS)
    ReceiverUIS = PartyUIS;
  end;
  ReceiverName = PartyName;
end;

macro IsTextNodeED243(fld_name : string): bool
  return InList(fld_name, "PayerName", "PayeeName");
  // Поле EDDefineRequestText не включается, т.к. оно обрабатывается особым образом
end;

macro IsTextNodeED244(fld_name : string): bool
  return InList(fld_name, "PayerLongName", "PayeeLongName", "Purpose", "Address");
  // Поле EDDefineAnswerText не включается, т.к. оно обрабатывается особым образом
end;

macro GetFldDiff243244
(
  FormName : string, 
  InfoBlockName : @string, 
  TextFieldName : @string, 
  IsTextNodeFun : @ProcRef
)

    if(FormName == "ED243")
      InfoBlockName = "EDDefineRequestInfo";
      TextFieldName = "EDDefineRequestText";
      IsTextNodeFun = @IsTextNodeED243;
    elif(FormName == "ED244")
      InfoBlockName = "EDDefineAnswerInfo";
      TextFieldName = "EDDefineAnswerText";
      IsTextNodeFun = @IsTextNodeED244;
    else
      RunError("Макропроцедура GetFldDiff243244 поддерживает только формы ED243, ED244");
    end;
end;

class TReestrItem
  var TransactionID : string = "",
      FieldList     : TArray = TArray();
end;

CLASS TAddFldsInfo

  // array of TMesField (Name, Value)
  var ExplanatoryFields : TArray = TArray();

  // array of TMesField (FieldNo, FieldValue)
  var EDFieldList : TArray = TArray();

  // array of TReestrItem (TransactionID + FieldList)
  //   ( FieldList : TArray of TMesField (FieldNo, FieldValue) )
  var EDReestrInfo : TArray = TArray();


  MACRO hasData() : bool

    if( (ExplanatoryFields.size > 0) or 
        (EDFieldList.size > 0) or 
        (EDReestrInfo.size > 0) 
      )
      return true;
    end;

    return false;
  END;

END;

//------------------------------------------------------------------------------
// Заполнение списка дополнительных полей запроса/ответа МБР
//------------------------------------------------------------------------------

private macro GetAddFldName(Number : string, Type : string, FormName : string) : string
  var Name : string = "";

  if(Type == "П")
    if(FormName)
      var ClassShortName : string = "ДППП_" + FormName;
      Name = GetLLClsValName(ClassShortName, Number);
    end;
  else
    Name = Bnk_GetLLValuesName(OBJTYPE_ADDFLD_REVISEDFIELD, Number);
  end;

  return Name;
end;

private macro GetAddFldValue
( Number : string, pm, rm, cr, db, pspaydem, 
  PayerBank, PrevInstrAgent, SenderBank, 
  ExecutorBank, IntermediaryBank, ReceiverBank
) : string

  var Value : string = "";

  if( (pm == null) or (rm == null) or
      (cr == null) or (db == null) 
      // запись pspaydem у платежа может отсутствовать
    )
    Value = "";
  else
    GetFieldValFromPaym( Number, null, @Value, 
                         pm, rm, cr, db, pspaydem,
                         PayerBank, PrevInstrAgent, SenderBank, 
                         ExecutorBank, IntermediaryBank, ReceiverBank );
  end;

  return Value;
end;

private macro GetAddFldPaymentID(pm) : integer
  var PaymentID : integer = 0;

  if(pm != null)
    if( (ValType(pm) == V_STRUC) or (ValType(pm) == V_FILE) )
      PaymentID = pm.PaymentID;
    elif( (ValType(pm) == V_GENOBJ) and
          (IsEqClass("TRecHandler", pm) or IsEqClass("TbFile", pm))
        )
      PaymentID = pm.rec.PaymentID;
    end;
  end;

  return PaymentID;
end;

macro InsertAddFld_ByBuffers
( Type : string,
  Number : string,
  FormName : string, // можно не передавать, если Name != null или Type != "П"
  Name : string, // если null, определится автоматически по Number, Type и FormName
  Value : string, // если null, определится автоматически по Number и буферам pm, rm и т.д.
  pm, rm, cr, db, pspaydem, // если null, то wladdfld.PaymentID = 0, wladdfld.Value = "" (если Value == null)
  PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
  ParentID : integer,
  LastSort : @integer, // IN OUT
  AddFldObj : RsbAddFld,
  FldRec : TRecHandler // OUT
) : integer

  if(FldRec == null)
    FldRec = TRecHandler("wladdfld.dbt");
  end;

  FldRec.Clear();
  FldRec.rec.Type = Type;

  if(Name == null)
    Name = GetAddFldName(Number, FldRec.rec.Type, FormName);
  end;

  if( (Name == null) OR (Name == "") )
    FldRec.rec.Name = Number;
  else
    FldRec.rec.Name = Name;
    FldRec.rec.Number = Number;
  end;

  if(Value == null)
    Value = GetAddFldValue( FldRec.rec.Number, pm, rm, cr, db, pspaydem,
                            PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank );
    if(Value == null)
      Value = GetAddFldValue(FldRec.rec.Name, pm, rm, cr, db, pspaydem,
                             PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank );
    end;
  end;

  FldRec.rec.Value = Value;

  FldRec.rec.PaymentID = GetAddFldPaymentID(pm);

  FldRec.rec.Sort = LastSort + 1;
  LastSort = FldRec.rec.Sort;

  FldRec.rec.ParentID = ParentID;

  return AddFldObj.Insert(FldRec);
end;

macro InsertAddFld
( Type : string,
  Number : string,
  FormName : string, // можно не передавать, если Name != null или Type != "П"
  Name : string, // если null, определится автоматически по Number, Type и FormName
  Value : string, // если null, определится автоматически по Number и буферам pm, rm и т.д.
  PaymentObjOrID, // если null или 0, то wladdfld.PaymentID = 0, wladdfld.Value = "" (если Value == null)
  ParentID : integer,
  LastSort : @integer, // IN OUT
  AddFldObj : RsbAddFld,
  FldRec : TRecHandler // OUT
) : integer

  var Payment : RsbPayment = null;
  if( (ValType(PaymentObjOrID) == V_INTEGER) and (PaymentObjOrID > 0) )
    Payment = RsbPayment(PaymentObjOrID);
  elif( (ValType(PaymentObjOrID) == V_GENOBJ) and IsEqClass("RsbPayment", PaymentObjOrID) )
    Payment = PaymentObjOrID;
  end;

  var pm : TRecHandler = null, rm : TRecHandler = null, 
      cr : TRecHandler = null, db : TRecHandler = null;
  var PayerBank : TRecHandler = TRecHandler("pmroute"), 
      PrevInstrAgent : TRecHandler = TRecHandler("pmroute"), 
      SenderBank : TRecHandler = TRecHandler("pmroute"), 
      ExecutorBank : TRecHandler = TRecHandler("pmroute"), 
      IntermediaryBank : TRecHandler = TRecHandler("pmroute"), 
      ReceiverBank : TRecHandler = TRecHandler("pmroute");
  if(Payment != null)
    pm = TRecHandler("pmpaym.dbt"); copy( pm, Payment.GetPM_PAYM() );
    rm = TRecHandler("pmrmprop.dbt"); copy( rm, Payment.GetPMRMPROP() );
    cr = TRecHandler("pmprop.dbt"); copy( cr, Payment.GetCREDIT() );
    db = TRecHandler("pmprop.dbt"); copy( db, Payment.GetDEBET() );
    Payment.GetOperationParticipantsFromRoute
      ( PrevInstrAgent, SenderBank, ExecutorBank, 
        IntermediaryBank, PayerBank, ReceiverBank );
  end;

  var pspaydem : TbFile = TbFile("pspaydem.dbt", "R", 0);
  pspaydem.Clear();
  if( (Payment != null) and (Payment.DocKind == PS_PAYORDER) )
    pspaydem.rec.OrderID = Payment.PaymentID;
    if( not pspaydem.GetEQ() )
      pspaydem.Clear();
    end;
  end;

  return InsertAddFld_ByBuffers
         ( Type,
           Number,
           FormName,
           Name,
           Value,
           pm, rm, cr, db, pspaydem,
           PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
           ParentID,
           @LastSort,
           AddFldObj,
           FldRec
         );
end;


//------------------------------------------------------------------------------
// Функции для автоматического заполнения ED244
// - при проверке и дозаполнении УО ED244 в CheckObj
// - при обработке принятого ED243 в GenObj
//------------------------------------------------------------------------------

class TAddFldCopy(p_Number : string, p_Name : string)
  var Number : string = p_Number,
      Name : string = p_Name;
end;

macro GetRevisedFieldsCopy(ReqID : integer) : TArray

  class (TRsbAddFldIterator) RevFldSearch(p_AddFldObj : RsbAddFld, p_RevFlds : TArray)
    InitTRsbAddFldIterator(p_AddFldObj);
    var RevFlds : TArray = p_RevFlds;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( FldRec.rec.Type == "У" )
        RevFlds[ RevFlds.size ] = TAddFldCopy(FldRec.rec.Number, FldRec.rec.Name);
      end;
    end;
  end;

//begin
  var RevFlds : TArray = TArray();

  var AddFlds : RsbAddFld = RsbAddFld(ReqID);
  RevFldSearch( AddFlds, RevFlds ).Go();

  return RevFlds;
end;

macro InsertAnsRevisedFieldsFromCopy
(
  RevFlds : TArray,
  Payment : RsbPayment,
  AnsAddFlds : RsbAddFld,
  LastSort : @integer
) : integer

  var stat : integer = 0;

  var FldRec : TRecHandler = TRecHandler("wladdfld.dbt");
  for(var RevFld : TAddFldCopy, RevFlds)

    stat = InsertAddFld( "У", RevFld.Number, "ED244", RevFld.Name, null, Payment, 0, 
                         @LastSort, AnsAddFlds, FldRec );

    if(stat)
      return stat;
    end;
  end;

  return stat;
end;

macro GetReestrInfoCopy(ReqID : integer) : TArray

  class (TRsbAddFldIterator) TransFldsSearch(p_AddFldObj : RsbAddFld, p_TransFldIDs : TArray)
    InitTRsbAddFldIterator(p_AddFldObj);
    var TransFldIDs : TArray = p_TransFldIDs;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( (FldRec.rec.Type == "Р") and (FldRec.rec.Number == "TransactionID") )
        TransFldIDs[ TransFldIDs.size ] = FldRec.rec.FieldID;
      end;
    end;
  end;

  class (TRsbAddFldIterator) ReestrSearch( p_AddFldObj : RsbAddFld, 
                                           p_TransFieldID : integer,
                                           p_ReestrInfo : TArray
                                         )
    InitTRsbAddFldIterator(p_AddFldObj);
    var TransFieldID : integer = p_TransFieldID,
        ReestrInfo : TArray = p_ReestrInfo,
        FieldList : TArray = TArray();

    private macro OnProcessRecord(FldRec : TRecHandler)
      if( FldRec.rec.FieldID == TransFieldID )
        var ReestrItem : TReestrItem = TReestrItem();
        ReestrItem.TransactionID = FldRec.rec.Value;
        ReestrItem.FieldList = FieldList;
        ReestrInfo[ ReestrInfo.size ] = ReestrItem;
      elif( (FldRec.rec.Type == "Р") and (FldRec.rec.ParentID == TransFieldID) )
        FieldList[ FieldList.size ] = TAddFldCopy(FldRec.rec.Number, FldRec.rec.Name);
      end;
    end;
  end;

//begin
  var AddFlds : RsbAddFld = RsbAddFld(ReqID);
  var TransFldIDs : TArray = TArray();
  TransFldsSearch(AddFlds, TransFldIDs).Go();

  var ReestrInfo : TArray = TArray();
  for(var FieldID : integer, TransFldIDs)
    ReestrSearch(AddFlds, FieldID, ReestrInfo).Go();
  end;
  return ReestrInfo;
end;

private macro GetIncludedPayment( Payment, // : RsbPayment или RECORD pmpaym 
                                  TransactionID : string ) : RsbPayment
  var IncludedPayment : RsbPayment = null;

  var IncludedPaymentID : integer = 0;
  if( (Payment.DocKind == DLDOC_MULTYPM) or (Payment.Purpose == PM_PURP_MULTI) )
    var select : string = 
           "select rm.t_PaymentID " +
           "  from dpmlink_dbt pmlink, dpmrmprop_dbt rm " +
           " where pmlink.t_InitialPayment  = :InitialPaymID " +
           "   and pmlink.t_LinkKind        = :LinkKind " +
           "   and pmlink.t_InitialPayment != pmlink.t_PurposePayment " +
           "   and rm.t_PaymentID = pmlink.t_PurposePayment " +
           "   and rm.t_Reference = :TransactionID ";

    var params = makeArray( SQLParam( "InitialPaymID", Payment.PaymentID ),
                            SQLParam( "LinkKind",      PMLINK_KIND_MULTYPM ),
                            SQLParam( "TransactionID", TransactionID ) );

    var rs : RsdRecordset = execSQLselect( select, params );
    if(rs and rs.moveNext())
      IncludedPaymentID = rs.value(0);
    end;
  end;

  if(IncludedPaymentID > 0)
    IncludedPayment = RsbPayment(IncludedPaymentID);
  end;

  return IncludedPayment;
end;

macro InsertAnsReestrInfoFromCopy
(
  ReestrInfo : TArray,
  Payment, // : RsbPayment или RECORD pmpaym
  AnsAddFlds : RsbAddFld,
  LastSort : @integer
) : integer

  var stat : integer = 0;

  var FldRec : TRecHandler = TRecHandler("wladdfld.dbt");
  var ParentNumber : string = "TransactionID";
  var ParentName : string = Bnk_GetLLValuesName(OBJTYPE_ADDFLD_REVISEDFIELD, ParentNumber);
  var ParentID : integer = 0;
  var IncludedPayment : RsbPayment = null;

  for(var ReestrItem : TReestrItem, ReestrInfo)

    IncludedPayment = GetIncludedPayment(Payment, ReestrItem.TransactionID);

    // EDReestrInfo\TransactionID
    stat = InsertAddFld( "Р", ParentNumber, null, ParentName, ReestrItem.TransactionID, 
                         IncludedPayment, 0/*ParentID*/, @LastSort, AnsAddFlds, FldRec
                       );

    if(not stat)
      ParentID = FldRec.rec.FieldID;
    else
      return stat;
    end;

    // EDReestrInfo\EDReestrFieldList
    for(var ListItem : TAddFldCopy, ReestrItem.FieldList)

      stat = InsertAddFld( "Р", ListItem.Number, null, ListItem.Name, null /*Value*/, 
                           IncludedPayment, ParentID, @LastSort, AnsAddFlds, FldRec
                         );

      if(stat)
        return stat;
      end;
    end;
  end;

  return stat;
end;


//------------------------------------------------------------------------------
// Функции для генерации УО по входящим ED243, ED244
//------------------------------------------------------------------------------

// Вызывается в цикле чтения полей сообщения
macro ReadEDDefineReqAnsInfoFld
(
  FormName : string, 
  blockname : string, 
  fieldname : string,
  fieldvalue : string,
  AddFldsInfo : TAddFldsInfo,
  EDDefineReqAnsText : @string
)

  var InfoBlockName : string, TextFieldName : string, IsTextNodeFun : ProcRef;
  GetFldDiff243244(FormName, @InfoBlockName, @TextFieldName, @IsTextNodeFun);
  var IsTextNode : bool = ExecMacro2(IsTextNodeFun, fieldname);
  var ExplanatoryFields : TArray = AddFldsInfo.ExplanatoryFields,
      EDFieldList : TArray = AddFldsInfo.EDFieldList,
      EDReestrInfo : TArray = AddFldsInfo.EDReestrInfo;

    if ( (blockname == InfoBlockName) and ( substr(fieldname, 1, 1) != "_" )
         or IsTextNode
       )
      ExplanatoryFields[ ExplanatoryFields.size ] = TMesField(fieldname, fieldvalue);
    end;
                      
    if ( index(blockname, "EDFieldList") )
      if ( (fieldname == "FieldNo") and (fieldvalue != "") )
        EDFieldList[ EDFieldList.size ] = TMesField(fieldvalue, "");
      end;

      if ( (fieldname == "FieldValue") and (fieldvalue != "") )
        EDFieldList[ EDFieldList.size - 1 ].FieldValue = fieldvalue;
      end;
    end;

    if ( index(blockname, "EDReestrInfo") )

      var ReestrNum : integer;

      if ( (fieldname == "TransactionID") and (fieldvalue != "") )
        ReestrNum = EDReestrInfo.size;// создаем новый элемент
        EDReestrInfo[ ReestrNum ] = TReestrItem();
        EDReestrInfo[ ReestrNum ].TransactionID = fieldvalue;
      end;

      var FieldList : TArray;
      if( InList(fieldname, "FieldNo", "FieldValue") and (fieldvalue != "") )
        ReestrNum = EDReestrInfo.size - 1;// дописываем в последний
        FieldList = EDReestrInfo[ ReestrNum ].FieldList;

        if ( fieldname == "FieldNo" )
          FieldList[ FieldList.size ] = TMesField(fieldvalue, "");// создаем новый элемент
        elif ( fieldname == "FieldValue" )
          FieldList[ FieldList.size - 1 ].FieldValue = fieldvalue;// дописываем в последний
        end;
      end;

    end;

    if( (fieldname == TextFieldName) and fieldvalue )
      EDDefineReqAnsText = fieldvalue;
    end;

end;

private macro InsertMesFieldsToAddFldObj
( FormName : string,
  AddFldType : string, 
  MesFields : TArray, 
  AddFldObj : RsbAddFld,
  LastSort : @integer
) : integer

  var stat : integer = 0;
  var FldRec : TRecHandler = TRecHandler("wladdfld.dbt");

  for(var MesFld : TMesField, MesFields)

    stat = InsertAddFld( AddFldType, MesFld.FieldName, FormName, null, MesFld.FieldValue, 
                         0/*PaymentID*/, 0/*ParentID*/, @LastSort, AddFldObj, FldRec
                       );

    if(stat)
      return stat;
    end;
  end;

  return stat;
end;

private macro InsertEDReestrInfoToAddFldObj
( EDReestrInfo : TArray, 
  AddFlds : RsbAddFld, 
  LastSort : @integer 
) : integer

  var stat : integer = 0;

  var FldRec : TRecHandler = TRecHandler("wladdfld.dbt");
  var ParentNumber : string = "TransactionID";
  var ParentName : string = Bnk_GetLLValuesName(OBJTYPE_ADDFLD_REVISEDFIELD, ParentNumber);
  var ParentID : integer = 0;

  for(var ReestrItem : TReestrItem, EDReestrInfo)

    // EDReestrInfo\TransactionID
    stat = InsertAddFld( "Р", ParentNumber, null, ParentName, ReestrItem.TransactionID, 
                         0/*PaymentID*/, 0/*ParentID*/, @LastSort, AddFlds, FldRec
                       );

    if(not stat)
      ParentID = FldRec.rec.FieldID;
    else
      return stat;
    end;

    // EDReestrInfo\EDReestrFieldList
    for(var MesFld : TMesField, ReestrItem.FieldList)

      stat = InsertAddFld( "Р", MesFld.FieldName, null, null, MesFld.FieldValue, 
                           0/*PaymentID*/, ParentID, @LastSort, AddFlds, FldRec
                         );

      if(stat)
        return stat;
      end;
    end;

  end;

  return stat;
end;

macro InsertAddFldsInfoToObj
( FormName : string,
  AddFldsInfo : TAddFldsInfo,
  AddFlds : RsbAddFld
)

  var Sort : integer = 0;
  if( InsertMesFieldsToAddFldObj(FormName, "П", AddFldsInfo.ExplanatoryFields, AddFlds, @Sort) != 0 )
    RunError("Ошибка при сохранении поясняющих полей");
  end;
  if( InsertMesFieldsToAddFldObj(FormName, "У", AddFldsInfo.EDFieldList, AddFlds, @Sort) != 0 )
    RunError("Ошибка при сохранении уточняемых полей");
  end;
  if( InsertEDReestrInfoToAddFldObj( AddFldsInfo.EDReestrInfo, AddFlds, @Sort ) != 0 )
    RunError("Ошибка при сохранении записей реестра");
  end;

end;

//------------------------------------------------------------------------------
