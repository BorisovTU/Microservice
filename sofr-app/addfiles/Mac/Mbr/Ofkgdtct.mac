/*
 $Name:        Ofkgdtct.mac
 $Module:      MBR
 $Description: Генерация учетного объекта по сообщению формы TICKET
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация учетного объекта по сообщению формы TICKET                     */
/*                                                                          */
/*  Имя файла: Ofkgdtct.mac                                                 */
/*  Создан:    24.04.13                                  Лазаренко М.       */
/****************************************************************************/

import WldInter, oralib, wlgendoc, MesInter, wlglobal, "wlofkggtools.mac";

private const WLD_STATUS_SESS_PROCESSED = 40;

private macro GetDate( MesDate:string ):date

  if( StrLen( MesDate ) == 10 ) 
    return date( int( SubStr(MesDate,1,2) ), int( SubStr(MesDate,4,2) ), int( SubStr(MesDate,7,4) ) );
  end;

  return date( 0,0,0 );
end;

private macro GetDateYYYYMMDD( MesDate:string ):date

  if( index(MesDate,"T") > 0 )
    MesDate = SubStr( MesDate, 1, index(MesDate,"T")-1 );
  end;

  if( StrLen( MesDate ) == 10 ) 
    return date( int( SubStr(MesDate,9,2) ), int( SubStr(MesDate,6,2) ), int( SubStr(MesDate,1,4) ) );
  end;

  return date( 0,0,0 );
end;

private macro GetTime( MesDate:string ):time

  return time( int( SubStr(MesDate,12,2) ), int( SubStr(MesDate,15,2) ), int( SubStr(MesDate,18,2) ) );
end;

private macro FindOutMes( TpSchemID:integer, ID:string ):integer

  var select = " select mes.t_MesID from dwlmes_dbt mes, dwltpshem_dbt shem, dwlmesval_dbt val " +
               " where  mes.t_Direct    = chr(0)        " +
               "   and  mes.t_TpSchemID = shem.t_TpShemID " +
               "   and  shem.t_TpID     = ( select t_TpID from dwltpshem_dbt where t_TpShemID = :TpSchemID ) " +
               "   and  val.t_MesID     = mes.t_MesID       " +
               "   and  val.t_TpFieldID = ( select t_tpFieldID from dwltpfld_dbt where t_Name = 'ID' ) " +
               "   and  val.t_Value     = :ID ";
  
  var params = makeArray( SQLParam( "TpSchemID"  , TpSchemID   ),
                          SQLParam( "ID"         , ID          ) );
  var rs = execSQLselect( select, params, TRUE );

  if( rs.MoveNext() )
    return rs.Value(0);
  end;
  
  return 0;
end;

/* Релиз TICKET */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация подтверждения о доставке TICKET");
  var field_name, field_value;
  
  // параметры функций генерации уч.объектов
  var outMesID          = 0, // ID исходного сообщения
      MesID             = 0, // ID исходящего сообщения
      ErrCode           = 0,
      State             = 0,
      ErrDescription    = "",
      DeliveryDate      = date(),
      DeliveryTime      = time(),
      CreateDate        = date(),
      CreateTime        = time(),
      Description       = "",
      ErrorDescription  = "",
      CreateKvt         = true,
      KvtState          = 0,
      sing              = 1, //!!!
      ID                = "",
      ErrorCode         = "",
      MesDate           = date(),
      ResultID          = 0;
  
  var RegVal = false;
  GetRegistryValue( "Межбанковские расчеты\\ГИС ГМП\\GenObjIncorrSign", V_BOOL, RegVal, error );

  while( СчитатьПоле(field_name, field_value) )    
    if( field_name == "_sing" )
      sing = field_value;
    end;
    if( field_name == "ID" )
      ID = field_value;
    end;
    if( field_name == "ErrorCode" )
      ErrorCode = field_value;
    end;
    if( field_name == "ErrorDescription" )
      ErrorDescription = field_value;
    end;
    if( field_name == "Date" )
      MesDate = field_value;
    end;
  end;

  if( ( RegVal == false ) and ( sing == 0 ) )
    std.msg( string("Ошибка проверки ЭП сообщения-ответа web-сервера СМЭВ. Генерация учетного объекта по сообщению запрещена" ) ); 
    return false;
  end;

  outMesID = FindOutMes( wlmes.TpSchemID, ID );

  if( StrLen( ErrorCode ) > 0 )
    if( outMesID > 0 )
      MesID = outMesID;
      ErrCode = 1;
      GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_GISGMP, ErrCode, NULL, NULL, ErrDescription );
      State = 100;
    else
      MesID = 0;
      ErrCode = 0;
      ErrDescription = "Не найдено исходное сообщение";
      State = 50;
    end;
    CreateDate = GetDate( MesDate );
    CreateTime = GetTime( MesDate );
    if( not ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID, 1, CreateDate, CreateTime ) )
      std.msg( string( "|Не удалось поместить в отбракованные сообщение ID =  ", MesID ) );
      return false;
    end;

    var ErrList = RsbWlError(0);
    var wlerr_buf = TRecHandler("wlerror.dbt");

    wlerr_buf.rec.ResultID = ResultID;
    wlerr_buf.rec.Code = ErrorCode;
    wlerr_buf.rec.Description  = ErrorDescription;
    ErrList.Insert( wlerr_buf );
    ErrList.TransferSelect( ResultID );
  else
    if( outMesID > 0 )
      MesID = outMesID;
      KvtState = 100;
    else
      MesID = 0;
      KvtState = 50;
    end;
    State = 60;
    DeliveryDate = GetDate( MesDate );
    DeliveryTime = GetTime( MesDate );
    GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_GISGMP, 0, NULL, NULL, Description );
    CreateKvt = true;
    CreateDate = GetDate( MesDate );
    CreateTime = GetTime( MesDate );
    if( not ВставитьПодтверждениеОДоставке( MesID, false, DeliveryDate, Description, CreateKvt, 0, State, DeliveryTime, CreateDate, CreateTime, KvtState ) )
        std.msg( string( "|Ошибка при переносе в закрытые сообщения ", MesID ) );
        return false;
      end;
  end;
    
  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

private macro IsFormPackage( MesID ):bool
  var rs = execSQLselect( " SELECT t_MesID "
                          "   FROM dwlmes_dbt "
                          "  WHERE     t_MesID = :MesID "
                          "        AND t_rlsformid = (SELECT t_RlsFormID "
                          "                             FROM dwlmesrls_dbt "
                          "                            WHERE UPPER(t_name) = 'PACKAGE') ", 
                          makeArray(SQLParam("MesID", MesID)), FALSE );
  if( rs and rs.moveNext() )
    return true;
  end;
  return false;
end;

private macro GetSessIDFromMes(MesID):integer
  var rs = execSQLselect( " select t_SessionID from dwlmes_dbt where t_mesid = ? ", makeArray( SQLParam("", MesID) ), FALSE );
  if( rs and rs.MoveNext() )
    return rs.value(0);
  end;
  return 0;
end;

/* Релиз TICKET1.16 */
macro GenDoc1_16( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация подтверждения о доставке TICKET1.16");
  var field_name, field_value;
  
  // параметры функций генерации уч.объектов
  var outMesID          = TArray(), // ID исходного сообщения
      ErrCode           = 0,
      State             = 0,
      ErrDescription    = "",
      DeliveryDate      = date(),
      DeliveryTime      = time(),
      CreateDate        = date(),
      CreateTime        = time(),
      Description       = "",
      ResultDescription = "",
      ResultData        = "",
      CreateKvt         = true,
      KvtState          = 0,
      sing              = 1, //!!!
      rqID              = "",
      ResultCode        = "",
      MesDate           = date(),
      ResultID          = 0;

  var RegVal = false;
  GetRegistryValue( "Межбанковские расчеты\\ГИС ГМП\\GenObjIncorrSign", V_BOOL, RegVal, error );

  while( СчитатьПоле(field_name, field_value) )    
    if( field_name == "_sing" )
      sing = field_value;
    end;
    if( field_name == "rqId" )
      rqID = field_value;
    end;
    if( field_name == "ResultCode" )
      ResultCode = field_value;
    end;
    if( field_name == "ResultData" )
      ResultData = field_value;
    end;
    if( field_name == "ResultDescription" )
      ResultDescription = field_value;
    end;
    if( field_name == "Date" )
      MesDate = field_value;
    end;
  end;

  if( ( RegVal == false ) and ( sing == 0 ) )
    std.msg( string("Ошибка проверки ЭП сообщения-ответа web-сервера СМЭВ. Генерация учетного объекта по сообщению запрещена" ) ); 
    return false;
  end;

  var findOutMesPack = false;
  if( not InList(ResultCode, 50, 60) )
    findOutMesPack = true;
  end;
  FindOutMesGG( outMesID, wlmes.TpSchemID, rqID, findOutMesPack );

  if(outMesID.size == 0)
    outMesID[0] = 0;
  end;

  for( var MesID, outMesID )
    if( (StrLen( ResultCode ) > 0) AND (ResultCode != "0") )
      if( MesID > 0 )
        if( ResultCode == "50" )
          ErrCode = 2;
        elif( ResultCode == "60" )
          ErrCode = 3;
        else
          ErrCode = 1;
        end;
        GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_GISGMP, ErrCode, NULL, NULL, ErrDescription );
        State = 100;
      else
        MesID = 0;
        ErrCode = 0;
        ErrDescription = "Не найдено исходное сообщение";
        State = 50;
      end;
      CreateDate = GetDateYYYYMMDD( MesDate );
      CreateTime = GetTime( MesDate );
      if( not ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID, 1, CreateDate, CreateTime ) )
        std.msg( string( "|Не удалось поместить в отбракованные сообщение ID =  ", MesID ) );
        return false;
      end;

      var ErrList = RsbWlError(0);
      var wlerr_buf = TRecHandler("wlerror.dbt");

      wlerr_buf.rec.ResultID = ResultID;
      wlerr_buf.rec.Code = IfThenElse( ResultDescription, ResultCode, "" );
      wlerr_buf.rec.Description  = ResultDescription;
      ErrList.Insert( wlerr_buf );
      ErrList.TransferSelect( ResultID );
    else
      if( MesID > 0 )
        KvtState = 100;
      else
        MesID = 0;
        KvtState = 50;
      end;
      State = 60;
      DeliveryDate = GetDateYYYYMMDD( MesDate );
      DeliveryTime = GetTime( MesDate );
      GetElementAndNoteLLVALUES( OBJTYPE_WLRESCODE_GISGMP, 0, NULL, NULL, Description );
      CreateKvt = true;
      CreateDate = GetDateYYYYMMDD( MesDate );
      CreateTime = GetTime( MesDate );
      if( not ВставитьПодтверждениеОДоставке( MesID, false, DeliveryDate, Description, CreateKvt, 0, State, DeliveryTime, CreateDate, CreateTime, KvtState ) )
          std.msg( string( "|Ошибка при переносе в закрытые сообщения ", MesID ) );
          return false;
      end;
      if( (MesID > 0) and not IsFormPackage(MesID) )
        execSQL( " update dwlsess_dbt set t_OutSideAbonentInfo = :ResultData, t_State = :State "
                 "  where t_SessionID = :SessionID ",            //WLD_STATUS_SESS_PROCESSED = 40, // Принят в обработку
                 makeArray( SQLParam( "ResultData", ResultData ), SQLParam( "State", WLD_STATUS_SESS_PROCESSED ), SQLParam( "SessionID", GetSessIDFromMes(MesID) ) ) );
      end;
    end;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
