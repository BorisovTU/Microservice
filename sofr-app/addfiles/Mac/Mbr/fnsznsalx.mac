/*
$Name:               fnsznsalx.mac
$Module:            МБ Расчеты
$Description:      Общие функции для импорта/генерации/печати сообщений ZSN, ZSO, ZVS
*/

import likepy, WldInter, FIInter, "wlmnstls.mac", "bnk_common.mac";
    
// Поля сообщений ZNS, ZNO

// Клиент, по которому создается сообщение
private class ZNSParty( ID:integer )
  var PartyID   :integer;
  var LegalForm :integer;
  var FullName  :string;
  var FullINN   :string;

  macro ZNSParty( ID:integer )
    private var PartyObj = RsbParty( ID );
    PartyID    = ID;
    LegalForm  = PartyObj.LegalForm;
    if( LegalForm == PTLEGF_INST )
      FullName =  PartyObj.FullName;
    else
      FullName = string( PartyObj.LastName, " ", PartyObj.FirstName, " ", PartyObj.Patronymic );
    end;
    FullINN    = PartyObj.Code(PTCK_INN);
  end;
  ZNSParty(ID);
end;

private var КлиентПоСчету:variant;

private class AccInfo()
  var Name: string,
      DateFrom: date,
      DateTo: date;
end;

class FnsMessageFormZSN(_WlRegDecID)

   private var Error;     // Действие выполнено с ошибкой
   private var ErrorMes;  // Сообщение об ошибке
   private var WlRegDecID = _WlRegDecID;
   var 
       Счет            = TArray(), // Массив полей "Счет"
       КЭСП            = TArray(),
       ИдЭС             = "",
       ТипЗапр          = "",
       ВидЗапр          = "",
       НомЗапр          = "",
       ДатаЗапр         = "",
       ОсновЗапр        = "",
       КодНО            = "",
       НаимНО           = "",
       
       КодОснов         = "",

       ИННЮЛ            = "",
       ИННФЛ            = "",
       ИННИП            = "",
       КПП              = "",
       НаимЮЛ           = "",
       БИК              = "",
       НомФил           = "",
       НаимБанк         = "",
       ДатаНач          = "",
       ДатаКон          = "",
       НомСч            = "",
       ИдКЭСП           = "",

       ФИОИП            = "",
       Фамилия          = "",
       Имя              = "",
       Отчество         = "",
       КодДУЛ           = "",
       СерНомДок        = "",
       ДатаДок          = "",
       ДатаРожд         = "",
       МестоРожд        = "",

       ДатаПоСост       = "",

       ИННБанк          = "",
       КППБанк          = "",
       РегНом           = "",
       СтНКРФ           = "",
       НаДату           = "",
       ЗаПериод         = "",
       ТелОтпр          = "",
       ПризнЗапр        = "",
       ПлФЛ_ФИО         = "",
       Руководитель_ФИО = "",
       
       // для блока АдрПлат
       Индекс           = "",
       КодРегион        = "",
       Район            = "",
       Город            = "",
       НаселПункт       = "",
       Улица            = "",
       Дом              = "",
       Корпус           = "",
       Кварт            = "",
       АдресКлиента     = "",
       
       ПоУказанным      = "",
       ПоВсем           = "";

   macro IsPSReport():bool
     return ИдЭС == "";
   end;

   macro MnsMessageFormZSN()
     Error = 0;
     ErrorMes = "";
     КлиентПоСчету = NULL;
     var field_name, field_value, acc;
     while( СчитатьПоле( field_name, field_value ) )
       var i = GenPropID(this, field_name);
       if (i != -1)
         this(i) = field_value;
       end;
       if (field_name == "_end")
         if (field_value == "ПоУказанным")
           acc = AccInfo();
           acc.Name = НомСч;
           acc.DateFrom = ToDateYYYY_MM_DD(ДатаНач);
           acc.DateTo = ToDateYYYY_MM_DD(ДатаКон);
           Счет[Счет.size] = acc;
         /*elif (field_value == "КЭСП")
           acc = AccInfo();
           acc.Name = ИдКЭСП;
           acc.DateFrom = ToDateYYYY_MM_DD(ДатаНач);
           acc.DateTo = ToDateYYYY_MM_DD(ДатаКон);
           КЭСП[КЭСП.size] = acc;*/
         elif (field_value == "ПФЛ")
           ПлФЛ_ФИО = Фамилия;
           if (StrLen(Имя) > 0)
             ПлФЛ_ФИО = ПлФЛ_ФИО + ", " + Имя;
             if (StrLen(Отчество) > 0)
               ПлФЛ_ФИО = ПлФЛ_ФИО + ", " + Отчество;
             end;
           end;
         elif (field_value == "ПлИП")
           ФИОИП = Фамилия + " " + Имя + " " + Отчество;
         elif (field_value == "Руководитель")
           Руководитель_ФИО = Фамилия + " " + Имя + " " + Отчество;
         elif( field_value == "АдрПлат" )
           АдресКлиента = Индекс+","+
                          КодРегион+","+
                          Район+","+
                          Город+","+
                          НаселПункт+","+
                          Улица+","+
                          Дом+","+
                          Корпус+","+
                          Кварт;
         end;
       end;
     end;
   end;
   
   private macro GetINN : string
     var ИНН : string = "";

     if( ИННЮЛ )
       ИНН = ИННЮЛ;
     elif( ИННФЛ )
       ИНН = ИННФЛ;
     elif( ИННИП )
       ИНН = ИННИП;
     end;

     return ИНН;
   end;

   private macro CheckBeforePrint()
   
     var PartyID;
     var ИНН : string = GetINN();
     
     PartyID = ПолучитьКодСубъекта( string( ИНН + IfThenElse( КПП != "", "/", "" ) + КПП ), PTCK_INN, Error );
   
     if( ( Error > 0 ) and ( КПП != "" ) )
       Error = 0;  // Не нашли субъекта по полному коду - поищем только по ИНН
       PartyID = GetLegalPersonIDByINN( ИНН );
        if( PartyID > 0 )
         КлиентПоСчету = ZNSParty( PartyID );
         if( КлиентПоСчету.LegalForm == PTLEGF_INST )
           ErrorMes = "КПП клиента не совпадает с указанным в запросе ФНС";// это не ошибка, только предупреждение
         end;
       end;
     end;
     if( PartyID <= 0 )
       if( (ВидЗапр == "1") or (ВидЗапр == "6") )
         Error = 0;
         ErrorMes = "";
       else
         Error = 1;  // это ошибка для всех форм, кроме NS
         ErrorMes = "Не найден клиент с ИНН " + ИНН;
       end;
     elif( PartyID > 0 )
       if( КлиентПоСчету == NULL )
         КлиентПоСчету = ZNSParty( PartyID );
       end;
       if( ( ( КлиентПоСчету.LegalForm == PTLEGF_INST  ) and ( НаимЮЛ != КлиентПоСчету.FullName ) ) or
           ( ( КлиентПоСчету.LegalForm == PTLEGF_PERSN ) and ( ФИОИП  != КлиентПоСчету.FullName ) ) )
         ErrorMes = IfThenElse( ErrorMes == "", "Наименование клиента не совпадает с указанным в запросе ФНС", ErrorMes );
       end;
     end;
     return IfThenElse( ( Error != 0 ) or ( ErrorMes != "" ), 1, 0 );
   end;

   macro PrintDoc()

     Array Text, Buttons;
     var ClientID = 0;  getparm( 1, ClientID ); 
     if( ClientID > 0 )
       КлиентПоСчету = ZNSParty( ClientID );
     end;
     if( ( ClientID == 0 ) and CheckBeforePrint() ) // проверку субъекта делаем только для сообщений ZNS
       if( GetDialogFlag() and ( Error == 0 ) ) // на все предупреждения выдадим запрос
         Text(0) = ErrorMes + ".|Продолжить печать?";
         Buttons( 0 ) = "Да";
         Buttons( 1 ) = "Нет";
         Error = IfThenElse( ConfWin( Text, Buttons ) == 1, 1, 0 );
       else
         msgbox( ErrorMes );
       end;
       if( Error )
         return 0; // ошибка уже обработана
       end;
     end;
     if( Error == 0 )
       if( ValType( КлиентПоСчету ) == V_GENOBJ )
         ClientID = КлиентПоСчету.PartyID;
       elif( ( (ВидЗапр == "1") or (ВидЗапр == "6") ) and not IsPSReport() )
         ClientID = -1; // в этом случае выводим справку об отсутствии счетов
       elif( ClientID > 0 )  
         КлиентПоСчету = ZNSParty( ClientID );
       end;
       var Accs = TArray();
       for (var one, Счет)
         Accs[Accs.size] = one.Name;
       end;
       if(ВидЗапр == "1") // Запрос о представлении справки о наличии счетов (специальных банковских счетов)
         Error = ExecMacroFile( "znsnsprnmes.mac", "PrintReportInfoAboutExistenceAccounts", this );
       elif(ВидЗапр == "6") // Запрос о представлении справки о наличии вкладов (депозитов)
         Error = ExecMacroFile( "znsnsprnmes.mac", "PrintReportInfoAboutExistenceDeposits", this );

       elif(ВидЗапр == "2") // Запрос о представлении справки об остатках на счетах
         Error = ExecMacroFile( "znsosprnmes.mac", "PrintReportInfoAboutRestAccounts", this );
       elif(ВидЗапр == "4") // Запрос о представлении справки об остатках ЭДС
         Error = ExecMacroFile( "znsosprnmes.mac", "PrintReportInfoAboutRestEMF", this );
       elif(ВидЗапр == "7") // Запрос о представлении справки об остатках на вкладах (депозитах)
         Error = ExecMacroFile( "znsosprnmes.mac", "PrintReportInfoAboutRestDeposits", this );

       elif(ВидЗапр == "3") // Запрос о представлении выписок по счетам
         Error = ExecMacroFile( "znsvsprnmes.mac", "PrintReportStatementOprAccounts", this );
       elif(ВидЗапр == "5") // Запрос о представлении справок о переводах ЭДС
         Error = ExecMacroFile( "znsvsprnmes.mac", "PrintReportStatementOprEMF", this );
       elif(ВидЗапр == "8") // Запрос о представлении выписок по вкладам (депозитам)
         Error = ExecMacroFile( "znsvsprnmes.mac", "PrintReportStatementOprDeposits", this );
       end;
     end;             
     return Error;
   end;

   macro GetNarrative()
     return   "Запрос № " + НомЗапр + " от " + ДатаЗапр + " из налогового органа " + КодНО + " " + НаимНО + " " + 
              "по счетам клиента " + GetINN() + IfThenElse( КПП != "", string( "/" + КПП ), "" ) + " " +
              IfThenElse( НаимЮЛ != "", НаимЮЛ , ФИОИП ) + 
              " по данным банка (филиала) " + БИК + " (" + НомФил + ") " + НаимБанк + " " +
              IfThenElse( ДатаПоСост, "На дату " + ДатаПоСост, "За период с " + ДатаНач + " по " + ДатаКон ) +
              ". Вид запроса: " + Bnk_GetLLValuesName(OBJTYPE_FNSKINDINFO, int(ВидЗапр)) +
              ". Тип запроса: " + Bnk_GetLLValuesName(OBJTYPE_FNS_REQTYPE, int(ТипЗапр));
   end;

   MnsMessageFormZSN();

end;


macro InsertReqLnkAccList(ReqID, Message)
    var i;
    var   ObjectType;
    GetParm(2, ObjectType);
    if (ObjectType == NULL)
      ObjectType = OBJTYPE_REQ;
    end;
    /* Заполнение связанных счетов */
    for(i, Message.Счет )
      ClearRecord( wlacclnk );
      wlacclnk.ObjectID      = ReqID;
      wlacclnk.ObjectType    = ObjectType;
      wlacclnk.Chapter       = 1;
      wlacclnk.Type          = IfThenElse((Message.ВидЗапр == "7") or (Message.ВидЗапр == "8"), WLACCLNK_TYPE_DEPOSIT, WLACCLNK_TYPE_ACCOUNT);
      wlacclnk.Account       = i.Name;
      wlacclnk.FIID          = Bnk_GetAccountFIID( i.Name );
      if( not ВставитьСвязанныйСчет( wlacclnk ) )
        RunError("Ошибка при вставке связанного счета");
      end;
    end;
end;


private macro CreateFnsRequest
(
  RecipientID : integer,
  ParentReqID : integer,
  wlmes,
  Message : FnsMessageFormZSN,
  ReqID : @integer

)

  RECORD wlreq(wlreq);
  ClearRecord(wlreq);

  /* Заполнение учетных буферов */
  wlreq.Kind                     = MESKIND_REQUEST;
  wlreq.SubKind                  = Message.ВидЗапр;
  wlreq.Trn                      = Message.НомЗапр;
  wlreq.PmDateValue              = ToDateYYYY_MM_DD( Message.ДатаЗапр, 1, 2 );
  wlreq.RelatedRef               = wlmes.Trn;
  wlreq.OriginatorID             = ПолучитьКодСубъекта( Message.КодНО, PTCK_MNS );
  wlreq.OriginatorCodeKind       = PTCK_MNS;
  wlreq.OriginatorCode           = Message.КодНО;
  wlreq.OriginatorName           = Message.НаимНО; 

  wlreq.RecipientID              = RecipientID;
  wlreq.RecipientCodeKind        = wlmes.InsideAbonentCodekind;
  if(RecipientID == wlmes.InsideAbonentID)
    wlreq.RecipientCode          = wlmes.InsideAbonentcode;
  else
    wlreq.RecipientCode          = ПолучитьКодСубъекта(RecipientID, wlreq.RecipientCodeKind, null, 1);
  end;
  wlreq.RecipientName            = Bnk_GetPartyName(wlreq.RecipientID);

  wlreq.Corschem                 = -1;
  wlreq.FIID                     = -1;
  wlreq.Queries                  = IfThenElse( Message.ТипЗапр == "1", "по всем счетам", "" );

  if( not ВставитьЗапрос( wlreq, Message.GetNarrative(), "", NULL, ReqID ) )
    RunError("Ошибка при вводе запроса");
  end;

  if( ReqID > 0 )

    wlreq.ReqID = ReqID;
    var sReqID : string = makeObjectID(OBJTYPE_REQ, null, wlreq);

    if( addNoteForObject(OBJTYPE_REQ, sReqID, NOTEKIND_WLDREQ_CLIENTADRESS, Message.АдресКлиента) )
      RunError("Ошибка при вставке примечания \"Адрес клиента\"");
    end;

    var NoteStr : string = "Код ДУЛ " + Message.КодДУЛ + ", " + 
                           "серия и номер " + Message.СерНомДок + ", " + 
                           "дата " + Message.ДатаДок;
    if( addNoteForObject(OBJTYPE_REQ, sReqID, NOTEKIND_WLDREQ_CLIENTDUL, NoteStr) )
      RunError("Ошибка при вставке примечания \"Документ, удостоверяющий личность клиента\"");
    end;

    var NoteDate : date = ToDateYYYY_MM_DD(Message.ДатаРожд);
    if( addNoteForObject(OBJTYPE_REQ, sReqID, NOTEKIND_WLDREQ_CLIENTDATEBIRTH, NoteDate) )
      RunError("Ошибка при вставке примечания \"Дата рождения клиента\"");
    end;

    if( addNoteForObject(OBJTYPE_REQ, sReqID, NOTEKIND_WLDREQ_CLIENTBIRTHPLACE, Message.МестоРожд) )
      RunError("Ошибка при вставке примечания \"Место рождения клиента\"");
    end;

    /* Заполнение связанных счетов */
    InsertReqLnkAccList(ReqID, Message);

    // Связь с родительским запросом
    if(ParentReqID > 0)
      if( WldCreateReqLink(ParentReqID, ReqID, OBJTYPE_REQ, "X") != 0 )
        RunError("Ошибка при вставке связи с родительским запросом");
      end;
    end;

  end;

end;

private macro GetOnlineDepartmentsPartyIds() : TArray
  var PartyIds : TArray = TArray();

  var rs = execSQLselectPrm
  (
    "select t_PartyID "
    "  from ddp_dep_dbt "
    " where t_NodeType = 1 /*DEPARTMENT_TYPE_FILIAL*/ "
    "   and t_AccessMode = 1 /*DEPARTMENT_ACCESS_ONLINE*/ "
    "   and t_Status = 2 /*DEPARTMENT_STATUS_ACTIVE*/ "
    "   and ( t_CloseNodeDate >= :curdate "
    "         or "
    "         t_CloseNodeDate = to_date('01010001','ddmmyyyy') ) ",
    SQLParam("curdate", {curdate})
  );

  while(rs and rs.moveNext())
    PartyIds[ PartyIds.size ] = rs.value("t_PartyID");
  end;

  return PartyIds;
end;

macro GenDocStd(addrMes)

  SetBuff( wlmes, addrMes );
  var error = 0, ReqID = -1;
  var Accounts:TArray = TArray();

  var Message = FnsMessageFormZSN();

  PrintLog(2,"Генерация запроса по ZSN");

  /* Если при загрузке не определился внутренний абонент, то создаем подтверждение банка с ошибкой */
  if( wlmes.InsideAbonentID <= 0 )
  
    var ErrList = RsbWlError(0);

    var wlerr_buf = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.Code         = "41";
    if (Message.ВидЗапр == "3")
      wlerr_buf.rec.Description  = "Электронный документ запрос № " + Message.НомЗапр + " от " + Message.ДатаЗапр + " ошибочно направлен налоговым органом не в тот банк (филиал банка). В банке "+
                                 ПолучитьБИКГоловногоФилиала( {OurBank} ) + " нет филиала с БИК " + Message.БИК + " и номером " + Message.НомФил;
    else
      wlerr_buf.rec.Description  = "Электронный документ запрос № " + Message.НомЗапр + " от " + Message.ДатаЗапр + " ошибочно направлен налоговым органом не в тот банк (филиал банка).";
    end;
    ErrList.Insert( wlerr_buf );
    /* Параметры создаваемого УО */
    var ResultID = 0;
    var ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;
    var ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

    if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList, "", -1, 0, "", "", -1, 0, "", "", NULL, NULL, ResultID ) )
      std.msg( "Ошибка при вставке подтверждения банка" );
      return FALSE;
    end;
    /*Связь подтверждения с входящим сообщением */
    var meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = ResultID;
    meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
    var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
    MesLnkObj.Insert( meslnk );
    MesLnkObj.Save();
    return TRUE;
  end;
  
  // wlmes.InsideAbonentID > 0 - Сформировать запрос(ы) 
  var MainReqID : integer = 0;
  CreateFnsRequest(wlmes.InsideAbonentID, 0, wlmes, Message, @MainReqID);

  if(Message.ТипЗапр == "1")
    var PartyIds : TArray = GetOnlineDepartmentsPartyIds(PartyIds);

    for(var PartyID : integer, PartyIds)
      if(PartyID != wlmes.InsideAbonentID)
        CreateFnsRequest(PartyID, MainReqID, wlmes, Message, @ReqID);
      end;
    end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;