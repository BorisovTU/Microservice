/*
  $Name: wlprnmes.mac
  $Module: Межбанковские расчеты
  $Description: Универсальная печать сообщений
*/

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//                 Подсистема "Межбанковские расчеты"
//
//  Описание    : Печать сообщений
//
//  Программист : Евграфова Л.Н.
//
//  Создан      : 08.02.2018
//
// --------------------------------------------------------------------

/*
Для релизов блочного типа использовать макрофункцию PrintBlockForm
Для релизов с xml-полями использовать макрофункцию PrintXmlForm (пока только обрабатывает поле xml)
*/


import "wlprnlib.mac", wlglobal, "xmlmestools.mac";

const WLD_TRNP_UFBS = 9;


var TblNodeList:TArray;

/* Макрос печати форм с блочной структурой*/
macro PrintBlockForm( addrMes, MassCopy )
  var FieldName, FieldValue, FieldID, BlockID;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;
  var CurrentBlock:TPrnBlock = TPrnBlock, NewBlock:TPrnBlock, MesBlock:TPrnBlock = TPrnBlock;
  var Field:TPrnField = TPrnField;
  var query, params, rs, rs1, rs2, rs3, params1; 

  SetBuff( wlmes, addrMes );

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;
  query = " select wlmesrls.t_Name "
          "   from dwlmesrls_dbt wlmesrls, dwlmes_dbt wlmes  "
          "  where wlmes.t_MesID = :MesD "
          "    AND wlmes.t_RlsFormID = wlmesrls.t_RlsFormID ";
  params = makeArray( SQLParam("MesID", wlmes.MesID));
  rs = execSQLselect( query, params );
                            
  if( not rs or not rs.moveNext() )
    std.msg( String("Не найден релиз сообщения: ", wlmes.MesID));
    return FALSE;
  end;

  //Родительским блоком считаем само сообщение
  MesBlock.FillData(rs.Value(0), false, wlmes.MesID, 0);

  //Получить список полей
  query = " select wltpfld.T_Name, wlmesval.t_Value, wlmesval.t_FieldID, wlmesfld.t_Master "
          "   from dwltpfld_dbt wltpfld, dwlmesval_dbt wlmesval, dwlmesfld_dbt wlmesfld "
          "  where wlmesval.t_MesID = :MesD "
          "    AND wlmesval.t_TpFieldID = wltpfld.t_TpFieldID "
          "    AND wlmesval.t_FieldID = wlmesfld.t_FieldID "
          "   order by wlmesval.t_Index" ;
  rs = execSQLselect( query, params );

  CurrentBlock = MesBlock;
                      
  while(rs.MoveNext())
     FieldName = rs.Value(0);
     FieldValue = rs.Value(1);
     FieldID = rs.Value(2);
     BlockID = rs.Value(3);
     Var TblPrint  = false;
     params1 = makeArray( SQLParam("FieldID", BlockID));
     if (FieldName == beginField)  //Начало блока

         //Проверить является ли блок табличным: для этого у него число повторений должно быть равно 0 и нет вложенных блоков (исключая блоки содержащие только одно значимое поле)
          query = "SELECT count(wlmesfld1.t_FieldId) "
                  "FROM dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfld1 "
                  "WHERE     wlmesfld.t_FieldID = :FieldID "
                  "AND wlmesfld1.t_Master = wlmesfld.t_FieldID "
                  "AND wlmesfld1.t_BlockFlag = CHR(88) "
                  "AND EXISTS "
                  "(SELECT wlmesfld2.t_FieldID "
                  "   FROM dwlmesfld_dbt wlmesfld2, dwltpfld_dbt wltpfld "
                  "  WHERE wlmesfld2.t_Master = wlmesfld1.t_FieldID "
                  "        AND wlmesfld2.t_TpFieldID = wltpfld.t_TpFieldID(+) "
                  "        AND (wlmesfld2.t_BlockFlag = CHR (88)"
                  "             OR NVL(wltpFld.t_Name, CHR(1)) NOT IN" 
                  "                   ('_begin', '_end', wlmesfld1.t_BlockName, CHR(1))))";
          rs1 = execSQLselect( query, params1 );
          query = "SELECT wlmesfld.t_BlockNumQnt, wlmesfld.t_BlockName "
                  "FROM dwlmesfld_dbt wlmesfld "
                  "WHERE     wlmesfld.t_FieldID = :FieldID ";
          rs2 = execSQLselect( query, params1 );
          if (rs1 and rs2 and rs1.MoveNext() and rs2.MoveNext())
             TblPrint = (rs1.Value(0) == 0) and (rs2.Value(0) == 0);
          end;
         //Надо ли формировать новый блок 
         query = "SELECT count(wlmesfld1.t_FieldID) "
                 "FROM dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfld1, dwltpfld_dbt wltpfld "
                 "WHERE     wlmesfld1.t_Master = wlmesfld.t_FieldID "
                 "AND wlmesfld.t_fieldID = :FieldID "
                 "AND wlmesfld1.t_TpFieldID = wltpfld.t_TpFieldID(+) "
                 "AND (wlmesfld1.t_BlockFlag = CHR (88) "
                 "OR NVL (wltpFld.t_Name, CHR (1)) NOT IN "
                 " ('_begin', '_end', wlmesfld.t_BlockName, CHR (1)))";
         rs3 = execSQLselect( query, params1 );
         if (rs3 and rs3.MoveNext() and rs3.Value(0) != 0)
           NewBlock = TPrnBlock;
           NewBlock.FillData(FieldValue, TblPrint, 0, FieldID);
           CurrentBlock.AddBlock(NewBlock);
           CurrentBlock = NewBlock;
         end;

     elif (FieldName == endField) //Конец блока
          //Надо ли выходить из блока
         query = "SELECT count(wlmesfld1.t_FieldID) "
                 "FROM dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfld1, dwltpfld_dbt wltpfld "
                 "WHERE     wlmesfld1.t_Master = wlmesfld.t_FieldID "
                 "AND wlmesfld.t_fieldID = :FieldID "
                 "AND wlmesfld1.t_TpFieldID = wltpfld.t_TpFieldID(+) "
                 "AND (wlmesfld1.t_BlockFlag = CHR (88) "
                 "OR NVL (wltpFld.t_Name, CHR (1)) NOT IN "
                 " ('_begin', '_end', wlmesfld.t_BlockName, CHR (1)))";
         rs3 = execSQLselect( query, params1 );
         if (rs3 and rs3.MoveNext() and rs3.Value(0) != 0)
           CurrentBlock = CurrentBlock.Parent;
         end;
     else //поле блока
         Field = TPrnField;
         Field.FillData(FieldName, FieldValue, 0, FieldID);
         CurrentBlock.AddField(Field);
     end;
  end;
  
  MesBlock.PrintBlock();

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

//проверить могут ли дочерние елементы быть табличными
//Для этого они не должны иметь дочерних элементов, кроме текстовых
//Элемента с конкретным названием должно быть больше одного
macro IsTblElement(xml, NodeName): bool
  var child:object, node:object;
  // Проверить, есть ли элемент уже в списке табличных элементов
  if (find(TblNodeList, NodeName) != -1)
    return true;
  end;

  var i = 0, j = 0, Count = 0;
  for( i, 0, xml.childNodes.length-1 )
     child = xml.childNodes.item(i);
     if ((child.nodeType == CHILD_NODE) and (child.BaseName == NodeName))
         if (child.childNodes.length > 0)
           for( j, 0, child.childNodes.length-1 )
              node = child.childNodes.item(i);
              if(node and node.nodeType != TEXT_NODE)
                 //Есть вложенные элементы
                return false;
              end;
           end;
         else
            Count = Count + 1;
         end;
     end;
  end;
  if (Count > 1)
    TblNodelist[TblNodeList.size] = NodeName;
    return true;
  else
    return false;
  end;
end;


/*Запись в структуру для печати из xml*/
macro PrintXmlField(xml:object, Block:TPrnBlock)
  var FieldName, FieldValue, FieldID, BlockID;
  var NewBlock:TPrnBlock;
  var Field:TPrnField = TPrnField;
  var query, params, rs, rs1, rs2, rs3, params1; 
  var child:object, node:object;
  var i = 0, TblPrint = false;
  TblNodeList = TArray;
  //Считать все атрибуты узла
  while( i < xml.attributes.length )
    child = xml.attributes.item(i);
    if( child and (child.nodeType==ATTR_NODE) and (child.BaseName != "") )
      Field = TPrnField;
      Field.FillData(child.BaseName, child.nodeValue, WLD_TRNP_UFBS, 0);
      Block.AddField(Field);
    end;
    i = i + 1;
  end;
  i = 0;

 
  //Есть дочерние элементы
  if(xml.childNodes.Length > 0)
    while( i < xml.ChildNodes.length )
      child = xml.ChildNodes.item(i);
      if( child and (child.nodeType == CHILD_NODE) )
         TblPrint = IsTblElement(xml, child.BaseName);
         NewBlock = TPrnBlock;
         NewBlock.FillData(child.BaseName, TblPrint, 0, 0);
         Block.AddBlock(NewBlock);
         PrintXmlField(child, NewBlock);
      elif( child and (child.nodeType == TEXT_NODE) )
         Field = TPrnField;
         Field.FillData(xml.BaseName, child.text, WLD_TRNP_UFBS, 0);
         Block.AddField(Field);
      end;
      i = i + 1;
    end;
  end;
end;

  
/* Макрос печати форм с полем xml*/
macro PrintXmlForm( addrMes, MassCopy )
  var FieldName, FieldValue;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;
  var MesBlock:TPrnBlock = TPrnBlock;
  var rs, query, params; 
  var xml:object = ActiveX( "MSXML.DOMDocument" );

  if(СчитатьПоле(FieldName, FieldValue))
    if (FieldName = "xml")
      if( not xml.loadXML(FieldValue) )
        std.msg( String("Не верный формат поля: ", FieldName));
        return FALSE;
      end;
    end;
  end;

  SetBuff( wlmes, addrMes );

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  query = " select wlmesrls.t_Name "
          "   from dwlmesrls_dbt wlmesrls, dwlmes_dbt wlmes  "
          "  where wlmes.t_MesID = :MesD "
          "    AND wlmes.t_RlsFormID = wlmesrls.t_RlsFormID ";
  params = makeArray( SQLParam("MesID", wlmes.MesID));
  rs = execSQLselect( query, params );
                            
  if( not rs or not rs.moveNext() )
    std.msg( String("Не найден релиз сообщения: ", wlmes.MesID));
    return FALSE;
  end;

  //Родительским блоком считаем само сообщение
  MesBlock.FillData(rs.Value(0), false, wlmes.MesID, 0);

  //Считать данные из xml
  PrintXmlField(xml.DocumentElement, MesBlock);

  //Печать
  MesBlock.PrintBlock();

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;


