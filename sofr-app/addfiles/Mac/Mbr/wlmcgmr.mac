/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения универсального R-макета                              */
/* Действительно по v.5.10.79                                               */
/*  Имя файла: wlmcgmr.mac                                                  */
/*  Создан:  10.08.00                                               AVM     */
/****************************************************************************/

import "wlmcgm.mac", "wltreas.mac", OprInter;

/* Выравнивает вправо и дополняет нулями */
macro Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, str, dataStr,  len, type )
    if ( ВИД_ПЛАТЕЖА )
        return Форматный_вывод_в_строку( str, dataStr, len, type );
    else
        return str = str + mkstr( " ", len );
    end;
end;

/* Выравнивает влево и дополняет пробелами */
macro вывод_в_строкуExt( ВИД_ПЛАТЕЖА, str, dataStr,  len )
    var v;
    if ( ВИД_ПЛАТЕЖА )
        v = string(dataStr);
        v = substr(v,1,len);
        if ( strlen(v)<len )
           v = v + mkstr( " ", len-strlen(v) );
        end;
        return str = str + v;
    else
        return str = str + mkstr( " ", len );
    end;
end;

macro вывод_в_строку( str, dataStr,  len )
   return вывод_в_строкуExt( 1, str, dataStr,  len );
end;

macro GenMesUNI( addrMes, addrPm, ВИД_ПЛАТЕЖА )
  var DKFlag = -1;
  var FlagTrans, НомерДокументаСтр, День, Месяц, Год,
      ShifrOperation, Вид_обработки, MFO_9, СсылкаНаБанк, Сумма, Основание,
      Плательщик, Получатель, Error,
      PayerINN, ReceiverINN, PayerKPP, ReceiverKPP, StringError = "",
      PlaceDate, PaymentIDStr, PayerChargeOffDate, displacement;

  /* Разрешенные шифры пл. документа для вида операции 16*/
  var PartPmShifrMain = "01 02 03 04 05 06 07 08 09 10 11 12 13 14 15 16";
  var RsPaym;
  /* Смещение в позиции из-за увеличения pmrmprop.BttTICode c 19 до 20 */
  if( {curdate} >= date(01,01,2005) ) 
   displacement = 1;
  else
   displacement = 0;
  end;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog( 2, "Генерация сообщения R для МЦИ " );

  GetProtectiveCodeMode();

  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  PayerINN    = RemoveKPP(RsPaym.PayerINN);
  ReceiverINN = RemoveKPP(RsPaym.ReceiverINN);
  PayerKPP    = RemoveINN(RsPaym.PayerINN);
  ReceiverKPP = RemoveINN(RsPaym.ReceiverINN);

  if ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  elif ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  else
     DKFlag = -1;
  end;            

  FlagTrans = IsTransitPayment(RsPaym);  

  if( DKFlag == -1 ) /* документ не внешний */
    RunError( "|Платеж с ID: " + string( RsPaym.PaymentID ) + "не внешний" );
  end ;

  if( DKFlag == WL_DEBET )
    if( GetTrue( False, "Документ " + RsPaym.Number + "  - дебетовый. Выгружать его?" ) == False )
      RunError( "|Документ дебетовый" );
    end;
  end;

  /* По инструкции 1160-y от 11 июля 2002 документы совершаемые внутри  */
  /* Московского региона с шифром операции 02, 06, 08, 16               */
  /* должны выгружаться в МЦИ в виде                                    */
  /* НЕ полноформатного документа независимо от суммы.                  */  

  /* Документы с шифром 06, 16 с 01.01.2004г (по 1274-У) передаются в виде ПФ */
  ShifrOperation = int( substr( RsPaym.ShifrOper, 1, 2) ) ;

  if ( valtype(ВИД_ПЛАТЕЖА)==V_UNDEF )
     ВИД_ПЛАТЕЖА = 0;
     if( (  ( ShifrOperation == 2 )         
         OR ( ShifrOperation == 8 ) ) AND (Общий_Регион(DKFlag)==TRUE) AND
         ( (Свой_Регион=="45") OR (Свой_Регион=="46") ) )
       ВИД_ПЛАТЕЖА = 0;
     elif ( (ShifrOperation == 1) 
         OR (ShifrOperation == 6)
         OR (ShifrOperation == 16) ) 
       /* Все платежные требования, независимо от суммы - полноформатные */
       ВИД_ПЛАТЕЖА = 1;
     else
       /* В остальных случаях делаем также полноформатные */
       ВИД_ПЛАТЕЖА = 1;
     end;
  end;

  /* Для межрегиональных платежей Вид_обработки=10 */
  /* Для внутрирегиональных - 01 */
  if( ВИД_ПЛАТЕЖА == 0 )
      Вид_обработки = "00" ;
  elif( Общий_Регион( DKFlag ) == FALSE )      
      Вид_обработки = "10" ;
  else
      if ( IsAbonentPUG(DKFlag) AND IsAbonentMoskowRegion(DKFlag) )
         Вид_обработки = "15" ;
      else
         Вид_обработки = "01" ;
      end;
  end; 

  /* Формируем строку с записью о выгружаемом документе */
  СтрокаДокумента = "R" ;                         /* 1 */

  /* Порядковый номер электронного документа */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, wlmes.TRN, 6 );   /* 2-7 */

  datesplit( {curdate}, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 8-9 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 10-11 */  /* 8-15 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 12-15 */

  /* Уникальный номер составителя */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НашКодСоставителя, 10 ); /* 16-25 */

  if( DKFlag == WL_CREDIT )
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО,      9, Числовое_Поле );  /* 26-34 */
  else
    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.PayerBankCode, RsPaym.PayerBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, MFO_9, 9, Числовое_Поле ); /* 26-34 */
      else
        RunError( "|Не найден БИК банка плательщика" );
      end ;
    else
      RunError( "|Не найдена ссылка на банк плательщика" );
    end ;
  end;

  /* Лицевой счет плательщика */
  if( ( ( RsPaym.PayerAccount == "") AND ( Вид_обработки != 1 ) ) )
    RunError( "Неверен номер счета плательщика" );
  end;

  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.PayerAccount, 20, Числовое_Поле );  /* 35-54 */

  if( DKFlag == WL_CREDIT )    
    СтрокаДокумента = вывод_в_строку( СтрокаДокумента, Свой_Корсчет, 20, Числовое_Поле );  /* 55-74 */
  else
    СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.PayerCorrAccNostro, 20, Числовое_Поле ); /* 55-74 */    
  end;

  НомерДокументаСтр = Последние_Символы( RsPaym.Number, 3 ) ;

  if( ПроверитьНомерДокумента(НомерДокументаСтр)  ==  false )
    /* Номер документа или уже нулевой или не числовой */
    НомерДокументаСтр = "0";

    /* Если необходимо, чтобы при нечисловом номере документа номер
    выгруженного становился нулевым автоматически -
    уберите следующие строки */

    if ( not IsOprMultiExec() )
      if( GetTrue( True, "Номер документа " + RsPaym.Number + " не числовой. Выгрузить документ с N 0 ?" ) == False )
        RunError( "|Нечисловой номер документа" );
      end;
    else
      RunError( "|Нечисловой номер документа" );
    end;
  end;

  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, НомерДокументаСтр, 3 ) ;   /* 75-77 */

  /* Дата документа, должна быть датой и не больше текущего опердня */
  if( (RsPaym.Date > {curdate} ) )
    RunError( "|Неверная дата документа" );
  end;
  datesplit( RsPaym.Date, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 78-79 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 80-81 */  /* 78-85 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 82-85 */

  /* Вид операции - допустимые значения 01, 02, 03, 04, 05, 06, 07, 08, 09, 10, 11, 12, 13, 16 */
  ShifrOperation = int( substr( RsPaym.ShifrOper, 1, 2) ) ;
  if ( not ПроверитьВидОбработкиИВидОперации( ВИД_ПЛАТЕЖА, Вид_обработки, substr(RsPaym.ShifrOper,1,2), StringError) )
     RunError( "|" + StringError);
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.ShifrOper, 2, Числовое_Поле );   /* 86-87 */

  if( DKFlag == WL_CREDIT )
    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.ReceiverBankCode, RsPaym.ReceiverBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
        if( ((not IsAbonentPUG(DKFlag)) OR (not IsAbonentMoskowRegion(DKFlag))) AND 
            (substr( MFO_9, strlen( MFO_9 )-2, 3 ) == "002") AND
            (MFO_9 != "044501002") AND (MFO_9 != "044536002") AND (MFO_9 != "044537002") )
          RunError( "|Получатель (БИК = " + RsPaym.ReceiverBankCode + ") является учреждением ЦБ РФ" );
        end;
        СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, MFO_9, 9, Числовое_Поле );  /* 88-96 */
      else
        RunError( "|Не найден БИК получателя" );
      end;
    else
      RunError( "|Не найдена ссылка на банк получателя" );
    end;
  else
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО,      9, Числовое_Поле ); /* 88-96 */
  end;

  /* Счет получателя */
  if( ( RsPaym.ReceiverAccount == "" ) AND ( Вид_обработки != 1 ) )
    RunError( "|Неверен номер счета получателя" );
  end;
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverAccount, 20, Числовое_Поле );   /* 97-116 */

  if ( Вид_обработки =="15" )
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 20 ); /* 117-136 */
  else
    if( DKFlag == WL_CREDIT )
      if ( (substr(MFO_9,7,3)=="000") OR (substr(MFO_9,7,3)=="001") OR (substr(MFO_9,7,3)=="002") )
         СтрокаДокумента = СтрокаДокумента + mkstr( " ", 20 ); /* 117-136 */
      else
         СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverCorrAccNostro, 20, Числовое_Поле );  /* 117-136 */
      end;
    else
      if ( (substr(Свой_МФО,7,3)=="000") OR (substr(Свой_МФО,7,3)=="001") OR (substr(Свой_МФО,7,3)=="002") )
         СтрокаДокумента = СтрокаДокумента + mkstr( " ", 20 ); /* 117-136 */
      else
         СтрокаДокумента = вывод_в_строку( СтрокаДокумента, Свой_Корсчет, 20, Числовое_Поле ); /* 117-136 */
      end;
    end;
  end;

  /* Сумма документа */
  if( RsPaym.ReceiverAmount == 0 )
    RunError( "|Нулевая сумма документа" );
  end;
  Сумма = double( RsPaym.ReceiverAmount )*100;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Сумма, 18 );                  /* 137-154 */

  if ( not Платеж_ФОР(RsPaym) )
     /* Код очередности платежа - допустимы лишь 01, 02, 03, 04, 05, 06 */
     if( ( RsPaym.Priority < 1) OR ( RsPaym.Priority > 6 ) )
       RunError( "|Неверная очередность платежа" );
     end;

     СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.Priority, 1 ); /* 155 */
  else
     СтрокаДокумента = СтрокаДокумента + " " /* 155 */
  end;
  
  /* ИНН КО Плательщика, разрешено 000000000000 */
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, PayerINN, 12 );     /* 156-167 */

  if ( (RsPaym.TaxAuthorState!="") AND (PayerKPP=="") )
     RunError( "|Отсутсвует КПП плательщика" );
  end;

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, PayerKPP, 9 ); /* 168-176 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.PayerName, 160 ) ;  /* 177-336 */

  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, ReceiverINN, 12 );     /* 337-348 */

  if ( (RsPaym.TaxAuthorState!="") AND (ReceiverKPP=="") )
     RunError( "|Отсутсвует КПП получателя" );
  end;

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, ReceiverKPP, 9 ); /* 349-357 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.ReceiverName, 160 ) ;  /* 358-517 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.Ground, 210 ); /* 518-727 */

  /* Дата поступления в банк плательщика */
  if ( (ShifrOperation == 16) OR (RsPaym.PayerBankEnterDate==date(0,0,0)) OR (IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 728 -735 */
  else
     datesplit( RsPaym.PayerBankEnterDate, День, Месяц, Год );
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, День,  2 );   /* 728-729 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Месяц, 2 );   /* 730-731 */  /* 728-735 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Год,   4 );   /* 732-735 */
  end;

  /* Дата помещения в картотеку */
  if ( (ShifrOperation != 16) AND (RsPaym.I2PlaceDate!=date(0,0,0)) AND (not IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )  
     datesplit( RsPaym.I2PlaceDate, День, Месяц, Год );
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, День,  2 );   /* 736-737 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Месяц, 2 );   /* 738-739 */  /* 736-743 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Год,   4 );   /* 740-743 */
  else
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 736-743 */
  end;

  if ( (ShifrOperation == 16) OR (IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 744 -751 */
  else
     PayerChargeOffDate = PM_GetPayerChargeOffDate(RsPaym.PaymentID);
     if( PayerChargeOffDate == date( 0, 0, 0 ) )
       datesplit( {curdate}, День, Месяц, Год );
     else
       datesplit( PayerChargeOffDate, День, Месяц, Год );
     end ;
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, День,  2 );   /* 744-745 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Месяц, 2 );   /* 746-747 */  /* 744-751 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, Год,   4 );   /* 748-751 */
  end;

  if( ( ShifrOperation == 6 ) AND ( RsPaym.ReceiverBankMarkDate !=date(0,0,0)) )
    datesplit( RsPaym.ReceiverBankMarkDate, День, Месяц, Год );
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 752-753 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 754-755 */  /* 752-759 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 756-759 */
  else
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 752-759 */
  end;

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxAuthorState, 2 );  /* 760 -761 */

  if ( (RsPaym.TaxAuthorState!="") 
     AND (   (RsPaym.BttTICode=="")   OR (RsPaym.OKATOCode  =="")   OR (RsPaym.TaxPmGround=="")
          OR (RsPaym.TaxPmPeriod=="") OR (RsPaym.TaxPmNumber=="")   OR (RsPaym.TaxPmDate=="")
          OR (RsPaym.TaxPmType=="")
         )
     )
     RunError( "|Не все поля налогового платежа заполнены" );
  end;

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.BttTICode, 19+displacement ); /* 762-780 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.OKATOCode, 11 ); /* 781-791 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxPmGround, 2 ); /* 792-793 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxPmPeriod, 10 ); /* 794-803 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxPmNumber, 15 ); /* 804-818 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxPmDate, 10 );  /* 819-828 */

  СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.TaxPmType, 2 );  /* 829-830 */

  if( ShifrOperation == 16 )
    СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.PartPaymNumber, 3 );  /* 831-833 */

    //Шифр пл. документа. /* 834-835 */
    if ( ( RsPaym.PartPaymShifrMain == "") OR 
         (not index(PartPmShifrMain, RsPaym.PartPaymShifrMain)) )
      RunError( "|Шифр платежного документа "+ string(RsPaym.PartPaymShifrMain) + " не соответствует виду операции 16" );
    else
      СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.PartPaymShifrMain, 2 );  /* 834-835 */
    end;

    /* Номер платежного документа */
    if( RsPaym.PartPaymNumMain == "" )
      RunError( "|Отсутствует номер платежного документа" );
    end;
    СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, RsPaym.PartPaymNumMain, 3 );  /* 836-838 */

    /* Дата платежного документа */
    if( RsPaym.PartPaymDateMain !=date(0,0,0))
      datesplit( RsPaym.PartPaymDateMain, День, Месяц, Год );
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 839-840 */
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 841-842 */  /* 839-846 */
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 843-846 */
    else
      СтрокаДокумента = mkstr( " ", 8 ); /* 839-846 */
    end;

    /* Сумма остатка платежа */
    Сумма = double( RsPaym.PartPaymRestAmountMain );
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Сумма, 18 );        /* 847-864 */

  else
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 34 ); /* 831-864 */
  end;


  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ВИД_ПЛАТЕЖА, 1 );  /* 865 */

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "0", 1 );  /* 866 */

  /* Признак года, только пробел */
  СтрокаДокумента = СтрокаДокумента + " " ;  /* 867 */

  /* Признак дебет/кредит сейчас - только 1 */
  if( DKFlag == WL_CREDIT )
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "1", 1 ) ; /* 868 */
  else
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "1", 1 ) ; /* 868 */
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Вид_обработки, 2 ) ;  /* 869 - 870 */

  /* Номер авизо, только пробелы */
  СтрокаДокумента = вывод_в_строкуExt( not ВИД_ПЛАТЕЖА, СтрокаДокумента, "   ", 3 );  /* 871-873 */

  /* Вид авизо, только пробелы */
  СтрокаДокумента = вывод_в_строкуExt( not ВИД_ПЛАТЕЖА, СтрокаДокумента, "  ", 2 );  /* 874-875 */

  /* Признак составителя ЭПД, только 0 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, "0", 1 );  /* 876-876 */

  /* Резервное поле, только пробелы */
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, "   ", 3 );  /* 877-879 */

  /* Срок платежа, пока пробелы */
  СтрокаДокумента = СтрокаДокумента + mkstr(" ", 8);  /* 880-887 */

  /* Содержание операции */
  if( (ShifrOperation == 16) AND (RsPaym.PartPaymNumber!=0) )
     СтрокаДокумента = вывод_в_строкуExt( ВИД_ПЛАТЕЖА, СтрокаДокумента, "Частичная оплата", 16 ); /* 888-903 */
  else
     СтрокаДокумента = СтрокаДокумента + mkstr(" ", 16);  /* 888-903 */
  end;

  ЗаписатьПолеЛог( FormFldDoc, СтрокаДокумента, TRUE );

  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenMesFULL( addrMes, addrPm )
   return GenMesUNI( addrMes, addrPm, 1 );
end;

macro GenMesCUT( addrMes, addrPm )
   return GenMesUNI( addrMes, addrPm, 0 );
end;