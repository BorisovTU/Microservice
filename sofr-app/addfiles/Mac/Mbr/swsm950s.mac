/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT950S                                   */
/*                                                                          */
/*  Имя файла: swsm950s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac", oralib, likepy;

const WLD_SUBKIND_HEAD_FINAL = 1;

macro ПолучитьДатуПредыдущейВыписки(Account)
  var LastDate, LastID;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select hd.t_HeadID "+
           "from dwlhead_dbt hd "+
           "where hd.t_Account=:Account and "+
           "hd.t_Kind=1"+
           "Order By hd.t_HeadID";
  params = makeArray( SQLParam("Account", Account));
  rs = execSQLselect( select, params, FALSE );
  if (not rs.MoveNext())
    LastID = rs.value(0);
  else
    return "";
  end;

  select = "select hd.t_BankDate from dwlhead_dbt hd where hd.t_HeadID=:LastID";
  params = makeArray( SQLParam("LastID", LastID));
  rs = execSQLselect( select, params, FALSE );

  if (not rs.MoveNext())
    LastDate = GetSWIFTDate(rs.value(0));
    msgbox(LastDate);
  else
    return "";
  end;

  return LastDate;
end;

macro GenMes(addrMes, addrHead)
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT;
  var StatementLine, TpID, NameForm, stat, StLineNum;

  SetBuff(wlmes,  addrMes );
  SetBuff(wlhead, addrHead);

  SB_CheckFIID( wlhead.FIID );

  PrintLog(2,"Генерация сообщения по МТ950S");

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  if (wlmes.RelatedRef != "")
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, wlmes.RelatedRef);
  end;

  /* VT - Тип выписки/реестра */
  if (wlhead.SubKind == WLD_SUBKIND_HEAD_FINAL)
    field_value = "1";
  else
    field_value = "0";
  end;
  ЗаписатьПолеЛог(ТипВыписки, field_value);

  /* V4 - Дата Предыдущей Выписки */
  field_value = ПолучитьДатуПредыдущейВыписки(wlhead.Account);
  ЗаписатьПолеЛог(ДатаПредыдущейВыписки, field_value);

  /* V5 - Дата и Время Формирования Выписки */
  field_value = GetSWIFTDate(wlhead.BankDate)+GetSWIFTTime(wlhead.SysTime);
  ЗаписатьПолеЛог(ДатаВремяВыписки, field_value);

  /* V8D - Суммарный Оборот По Дебету */
  field_value = wlhead.DebetTurn;
  ЗаписатьПолеЛог(DebetTurn, string(field_value));

  /* V8C - Суммарный Оборот По Кредиту */
  field_value = wlhead.CreditTurn;
  ЗаписатьПолеЛог(CreditTurn, string(field_value));

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог(TransactionReferenceNumberField, wlmes.TRN);

  /* 25 - счет выписки */
  ЗаписатьПолеЛог(AccountIdentificationField, wlhead.Account);

  /* 28C - Номер выписки */
  ЗаписатьПолеЛог(StatementSequnceNumberCField, string(substr(wlhead.Number,strlen(wlhead.Number)-4), "/", 1));

  /* Открывающий баланс */
  /* Дата начала периода выписки */
  dateSWIFT = GetSWIFTDate(wlhead.DateIn);
  /* Код дебета/кредита */
  if (wlhead.RestIn < 0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;
  /* Входящий остаток */
  amountSWIFT = GetSWIFTAmount(dKnum * wlhead.RestIn);
  /* Код валюты */
  CUR = ПолучитьКодВалютыЛог(wlhead.FIID);
  ЗаписатьПолеЛог(OpeningBalanceField_F, String(dKflag, dateSWIFT, CUR, amountSWIFT));

  /******************** Заполняем документы выписки ******************/
  УстановитьФильтрДокументовВыписки(wlhead.HeadID);
  StLineNum = 0;
  while(ПолучитьДокументВыписки(wlConf))
     StLineNum = StLineNum + 1;
     if (wlConf.DKFlag==WL_DEBET)
       dKflag = DEBET_ACCOUNT;
     else
       dKflag = CREDIT_ACCOUNT;
     end;
     StatementLine = "";
     StatementLine = StatementLine + GetSWIFTDate(wlConf.DateValue);
     StatementLine = StatementLine + substr(string(dateSWIFT), 3);
     StatementLine = StatementLine + dKflag;
     StatementLine = StatementLine + GetSWIFTAmount(wlConf.Sum);
     StatementLine = StatementLine + "NTRF";
     StatementLine = StatementLine + substr(wlConf.RelatedRef, 1, 16);
     StatementLine = StatementLine + "//" + substr(wlConf.Number, 1, 16);
     /* Supplementary Details */
     StatementLine = StatementLine + "/";
     NameForm = ПолучитьНазваниеФормыСвязаногоСообщенияSB(wlconf.RelatedRef);
     if (NameForm == "")
       NameForm = "000I";
     elif (strlen(NameForm) == 3)
       NameForm = NameForm + КодПодтипСообщения_Общий;
     end;
     StatementLine = StatementLine + NameForm;
     StatementLine = StatementLine + "/" + wlconf.Account;
     StatementLine = StatementLine + string(StLineNum:6:o, StLineNum);
     ЗаписатьПолеЛогВБлок("61", StatementLineField, StatementLine);
  end;
  /*******************************************************************/
 
  /* Закрывающий баланс */
  /* Дата окончания периода выписки */
  dateSWIFT = GetSWIFTDate(wlhead.dateOut);
  /* Код дебета/кредита */
  if (wlhead.RestOut < 0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;
  /* Исходящий остаток */
  amountSWIFT = GetSWIFTAmount(dKnum * wlhead.RestOut);
  
  ЗаписатьПолеЛог(ClosingBalanceField_F, String(dKflag, dateSWIFT, CUR, amountSWIFT));

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;