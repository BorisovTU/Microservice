/*
  $Name:         ufgm813.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED813 транспорта УФЭБС
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения ED813 транспорта УФЭБС
//
// Программист  : Kolyakin V.V.
//
// Создан       : 25.07.2018
//
//-----------------------------------------------------------------------------


import BankInter, CTInter, oralib, MesInter, WldInter, ufgenmes, bnk_ptlib, bnk_common;

private macro GetWlAddFldValueForED813( ObjectID : integer, Number : string ):string
  var Value : string = "";

  var query : string = " SELECT t_Value " +
                       " FROM dwladdfld_dbt " +
                       " WHERE t_ObjectID = :ObjectID "
                       "   AND t_Number = :Num ";

  var rs : RsdRecordset = execSQLselectPrm( query, SQLParam( "ObjectID", ObjectID ),
                                                   SQLParam( "Num", Number ) );

  if( rs and rs.moveNext() )
    Value = rs.value( "t_Value" );
  end;

  return Value;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);
  
  SetBuff( wlReq, addrReq );
  SetBuff( wlmes, addrMes );

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  ЗаписатьПолеЛог( "EDNo",     ed_nda.EDNo, TRUE );
  ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(ed_nda.EDDate), TRUE );
  ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor, TRUE );
  
  var WlAddFldValue : string = "";

  /*Блок EDRefID*/
  WlAddFldValue = GetWlAddFldValueForED813( wlReq.ReqID, "#EDRefID" );
  УстановитьКонтекстБлокаЛог( "EDRefID" );
    ЗаписатьПолеЛог( beginField, "EDRefID" );
    ed_nda = NULL;
    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn( WlAddFldValue );
    ЗаписатьПолеЛог( "EDNo",     ed_nda.EDNo, TRUE );
    ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(ed_nda.EDDate), TRUE );
    ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor, TRUE );
    ЗаписатьПолеЛог( endField, "EDRefID" );
  УстановитьКонтекстБлокаЛог( ".." );

  /*Блок BeforeAnother*/
  WlAddFldValue = GetWlAddFldValueForED813( wlReq.ReqID, "#BeforeAnother" );
  if( WlAddFldValue != "" )
    УстановитьКонтекстБлокаЛог( "BeforeAnother" );
      ЗаписатьПолеЛог( beginField, "BeforeAnother" );
      ed_nda = NULL;
      ed_nda = EDNoDateAuthor();
      ed_nda.ConstructByTrn( WlAddFldValue );
      ЗаписатьПолеЛог( "EDNo",     ed_nda.EDNo, TRUE );
      ЗаписатьПолеЛог( "EDDate",   YYYYMMDD(ed_nda.EDDate), TRUE );
      ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor, TRUE );
      ЗаписатьПолеЛог( endField, "BeforeAnother" );
    УстановитьКонтекстБлокаЛог( ".." );
  end;

  return true; /*Успешное завершение*/
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;