/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT941                                       */
/*                                                                          */
/*  Имя файла: swgm941.mac                                                  */
/*  Создан:  08.06.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

macro GenMes( addrMes, addrHead )
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT;
  var StatementLine, TpID, NameForm, stat, KindPageExt;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlhead, addrHead );

  PrintLog(2,"Генерация сообщения по МТ941");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  ЗаписатьПолеЛог( RelatedReferenceField, wlhead.Number );

  /* 25 - счет выписки */
  ЗаписатьПолеЛог( AccountIdentificationField, wlhead.Account );

  /* 28 - Номер выписки */
  ЗаписатьПолеЛог( StatementSequnceNumberField, string(substr(wlhead.Number,strlen(wlhead.Number)-4), "/", wlhead.Page) );

  /* Открывающий баланс */
  /* Дата начала периода выписки */
  dateSWIFT = GetSWIFTDate( wlhead.DateIn );
  /* Код дебета/кредита */
  if(wlhead.RestIn<0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;
  /* 60f - Входящий остаток */
  amountSWIFT = GetSWIFTAmount( dKnum * wlhead.RestIn );
  /* Код валюты */
  CUR = ПолучитьКодВалютыЛог( wlhead.AccountFIID );
  ЗаписатьПолеЛог( OpeningBalanceField_F, String( dKflag, dateSWIFT, CUR, amountSWIFT ) ) ;  

  /* 90d - Количество и сумма по операциям дебета */
  ЗаписатьПолеЛог( NumberSumDebetEntriesField, String( wlhead.DebetNumDoc, CUR, GetSWIFTAmount(wlhead.DebetTurn+wlhead.DebetFloorTurn)) );  

  /* 90c - Количество и сумма по операциям  кредита */
  ЗаписатьПолеЛог( NumberSumCreditEntriesField, String( wlhead.CreditNumDoc, CUR, GetSWIFTAmount(wlhead.CreditTurn+wlhead.CreditFloorTurn)) );
 
  /* Закрывающий баланс */
  /* Дата окончания периода выписки */
  dateSWIFT = GetSWIFTDate( wlhead.dateOut );
  /* Код дебета/кредита */
  if(wlhead.RestOut<0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;

  /* 62f - Исходящий остаток */
  amountSWIFT = GetSWIFTAmount( dKnum * wlhead.RestOut );
  ЗаписатьПолеЛог( ClosingBalanceField_F, String( dKflag, dateSWIFT, CUR, amountSWIFT ) );  

  /* 86 */
  StrChangeRusXSet( wlhead.Description, field_value );
  StrMultyToFormat( field_value, field_value, 65, 6 );
  ЗаписатьПолеЛог( InformationToAccountOwnerField, field_value );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;