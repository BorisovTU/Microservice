/*
  $Name: ufpr805.mac
  $Module: Межбанковские расчеты
  $Description: Печатная форма сообщения ED805 транспорта УФЭБС
*/


import oralib, MesInter, wlglobal, wluftool, WLTOOLS, WldInter, rsexts, ufgd805;

private macro PrintTableInfo( ED805Mes : TED805MessageFields )

  [
    ED805 Перечень ЭПС, помещенных в очереди

       Номер ЭC: #########
        Дата ЭC: ##########
    Составитель: ########## ##################################################
  ]( ED805Mes.EDNo, 
     DDpMMpYYYY(ED805Mes.EDDate), 
     ED805Mes.EDAuthor, 
     Bnk_GetPartyName( ПолучитьКодСубъекта( ED805Mes.EDAuthor, PTCK_UIS, null ) )
   );
   
  if( (ED805Mes.EDReceiver != null) and (strlen(ED805Mes.EDReceiver) > 0) )
   [  Получатель: ########## ##################################################
   ](ED805Mes.EDReceiver, 
     Bnk_GetPartyName( ПолучитьКодСубъекта( ED805Mes.EDReceiver, PTCK_UIS, null ) ));
  end;
  
  [ Код причины формирования: #### ##################################################
  ](ED805Mes.CreationReason,
    GetWlcodeDescription( TYPE_CODE_UFBS_ED802_CREATIONREASON, ED805Mes.CreationReason));
    
  if( ED805Mes.InitialED != null )
    [ 
      Информация об исходном ЭСИС:
             Номер ЭС: #########
              Дата ЭС: ##########
          Составитель: ########## ##################################################
    ](ED805Mes.InitialED.EDNo, 
      DDpMMpYYYY(ED805Mes.InitialED.EDDate), 
      ED805Mes.InitialED.EDAuthor, 
      Bnk_GetPartyName( ПолучитьКодСубъекта( ED805Mes.InitialED.EDAuthor, PTCK_UIS, null ) ));
  end;
  
  [ 
    ПЕРЕЧЕНЬ РАСПОРЯЖЕНИЙ В ОЧЕРЕДЯХ ПРЕДОСТАВЛЕН: 
        По счету (ACCOUNT): ####################
           Участника (BIC): #########
  
  ](ED805Mes.Account,
    ED805Mes.BIC);

end;

private macro PrintQueueTable( Queue )

  var i = 0;
  
  [
    ################################################## (QueueType = ####)
  ](GetWlcodeDescription( TYPE_CODE_UFBS_ED8NN_QUEUETYPE, string( "/" + Queue.QueueType + "/")):w,
    Queue.QueueType);

  [ ┌─────────┬──────────────────────┬─────────────┬──────────────────────┬─────────────────────────────────────────────────────────┬───────────┬────────────┬──────────────┐
    │Приоритет│        Сумма         │БИК участника│    Счет участника    │              Причина помещения в очередь                │ Номер ЭС  │  Дата ЭС   │Составитель ЭС│
    │ платежа │                      │ получателя  │      получателя      │                                                         │           │            │              │
  ];

  while( i < Queue.QueuedPayment.size )                                                                                                                                      
    [ ├─────────┼──────────────────────┼─────────────┼──────────────────────┼─────────────────────────────────────────────────────────┼───────────┼────────────┼──────────────┤
      │   ##    │ #################### │  #########  │ #################### │ ################################################## #### │ ######### │ ########## │  ##########  │]
    (Queue.QueuedPayment[i].PaymentPrecedence,
     Queue.QueuedPayment[i].Amount,
     Queue.QueuedPayment[i].PayeeBIC,
     Queue.QueuedPayment[i].PayeeAccount,
     GetWlcodeDescription(TYPE_CODE_UFBS_ED805_QUEUINGREASONCODE, Queue.QueuedPayment[i].QueuingReasonCode):w,
     Queue.QueuedPayment[i].QueuingReasonCode,
     Queue.QueuedPayment[i].EDNo,
     DDpMMpYYYY(Queue.QueuedPayment[i].EDDate),
     Queue.QueuedPayment[i].EDAuthor);
    i = i + 1;
  end;
  
  [ └─────────┴──────────────────────┴─────────────┴──────────────────────┴─────────────────────────────────────────────────────────┴───────────┴────────────┴──────────────┘
  ];

end;

private macro PrintNoQueue()
  [ ───────────────────────────────────────────────────────────────────────────────────────
    ЭПС в очередях отсутствуют
    ───────────────────────────────────────────────────────────────────────────────────────
  ];
end;

private macro PrintNoXml()
  [ ──────────────────────────────────────────────────────────────────────────────────────────
    Перечень ЭПС в очередях не может быть выведен в отчет, т.к. отсутствует xml-файл сообщения
    ──────────────────────────────────────────────────────────────────────────────────────────
  ];
end;

macro PrintForm( addrMes, MassCopy )
  var stat = false;
  var FileDirectory = Bnk_GetTxtFileDir();
  var XmlObj : object = ActiveX("Microsoft.XMLDOM");   

  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var ImageID = 0;
  var sWlMesID : string = makeObjectID( OBJTYPE_MES, null, wlmes );

  ImageID = Bnk_GetImageID( OBJTYPE_MES, sWlMesID, WLD_MES_IMGTYPE_XML_FILE );

  if( ImageID )
    /* Записываем прикрепленный ED805 во временный XML-файл */
    var tmpXMLFileName = FileDirectory + "\\" + wlmes.TRN + ".xml";
    stat = WEB_ShowImageByID( ImageID, tmpXMLFileName );
    if( not stat )
      XmlObj = null;
    else
      if ( not XmlObj.load( tmpXMLFileName ) )
        std.msg( "Неверный формат сообщения" );
        return FALSE;
      end;
    end;
    /* Удаляем временный XML-файл с ED805 */
    RemoveFile( tmpXMLFileName );
  else
    XmlObj = null;
  end;

  var ED805Mes = TED805MessageFields();
  ReadED805MessageFields( @ED805Mes, XmlObj );
  PrintTableInfo(ED805Mes);
  
  if( XmlObj != null )
    
    var i = 0;
    while( i < ED805Mes.Queue.size )
      PrintQueueTable(ED805Mes.Queue[i]);
      i = i + 1;
    end;
    
    if( i == 0 )
      PrintNoQueue();
    end;
  else
    PrintNoXml();
  end;

  // Перенаправляем вывод обратно
  //------------------------------------------
  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 

  return true;                                                               

  OnError(er)
    ExeptionMessage(er);
    return false;
end;
