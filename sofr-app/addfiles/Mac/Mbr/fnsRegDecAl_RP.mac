/*
  $Name:         fnsRegDecAl_RP.mac
  $Module:       МБР
  $Description:  Функции для решений регистрационных органов RPO, ROO
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для решений регистрационных органов RPO, ROO
//
// Программист  : Чукина Т.А.
//
// Создан       : 12.01.2018
//
//-----------------------------------------------------------------------------
import "fnsRegDecAl.mac", "AccOperations.mac";

private macro GetPartyIdByCodeIncludeHistory
( 
  Code : string, 
  CodeKind : integer,
  CompareBeforeSlashOnly : bool

) : integer

  var PartyID : integer = 0;

  var query : string =
    "select t_ObjectID "
    "  from dobjcode_dbt "
    " where t_ObjectType = " + OBJTYPE_PARTY +
    "   and t_CodeKind = :CodeKind "
    "   and ( t_Code = :Code1 " + IfThenElse(CompareBeforeSlashOnly, "or t_Code like (:Code2 || '/%')", "") + " ) " +
    " order by t_State ";

  var params : TArray = makeArray
  (
    SQLParam("CodeKind", CodeKind),
    SQLParam("Code1", Code)
  );
  if(CompareBeforeSlashOnly)
    params[ params.size ] = SQLParam("Code2", Code);
  end;

  var rs = execSQLselect(query, params);

  if(rs and rs.moveNext())
    PartyID = rs.value("t_ObjectID");
  end;

  return PartyID;
end;

private macro GetIdFromPartcodeByInnOnly(INN : string) : integer
  var PartyID : integer = 0;

  var rs = execSQLselectPrm
  (
    "select t_PartyID "
    "  from dpartcode_dbt "
    " where t_CodeKind = :PTCK_INN "
    "   and ( t_Code = :Inn1 or t_Code like (:Inn2 || '/%') )",

    SQLParam("PTCK_INN", PTCK_INN),
    SQLParam("Inn1", INN),
    SQLParam("Inn2", INN)
  );

  if(rs and rs.moveNext())
    PartyID = rs.value("t_PartyID");
  end;

  return PartyID;
end;

private macro GetPartyIdByInnOnly(INN : string, IncludeHistory : bool) : integer
  var PartyID : integer = 0;

  if(IncludeHistory)
    PartyID = GetPartyIdByCodeIncludeHistory(INN, PTCK_INN, true);
  else
    PartyID = GetIdFromPartcodeByInnOnly(INN);
  end;

  return PartyID;
end;

macro GetClientForRegDec(ClientINN : string/*, ClientKPP : string*/) : integer
  var ClientID : integer = 0;

  var CodeKind : integer = PTCK_INN;

  var InnLen : integer = strlen(ClientINN),
      NeedSearchClosedINN : bool = Fns_NeedFindClientByClosedINN(),
      IncludeHistory : bool = false;

  if( InList(InnLen, 5, 10) and NeedSearchClosedINN )
    IncludeHistory = true;
  end;

  ClientID = GetPartyIdByInnOnly(ClientINN, IncludeHistory);

  return ClientID;
end;

class (TRegDecision) TRegDecision_RP
  InitTRegDecision();

  var КодОснов : string = "",
      НомФ : string = "",
      НаимЮЛ : string = "",
      ПлИП_Фамилия : string = "",
      ПлИП_Имя : string = "",
      ПлИП_Отчество : string = "",
      ClientType: Integer = 0, //Тип клиента
      АдрПлат : TAddrRF = null;

  var AccList : TArray = TArray(); // блоки "Счет"

  class TKESP
    var ИдКЭСП : string = "",
        ВалКЭСП : string = "";
  end;

  var KESPList : TArray = TArray(); // Блоки "КЭСП"

  // Для переопределения в классах конкретных видов решений
  private macro GetDocTypeName : string
    return "";
  end;

  private macro СохранитьПоля_ПлИП_ФИО(fieldname : string, fieldvalue : string)
    if(fieldname == "Фамилия")
      ПлИП_Фамилия = fieldvalue;
    elif(fieldname == "Имя")
      ПлИП_Имя = fieldvalue;
    elif(fieldname == "Отчество")
      ПлИП_Отчество = fieldvalue;
    end;
  end;

  private macro СохранитьПоля_АдрПлат(fieldname : string, fieldvalue : string)
    if(АдрПлат == null)
      АдрПлат = TAddrRF();
    end;

    if(fieldname == "Индекс")
      АдрПлат.Индекс = fieldvalue;
    elif(fieldname == "КодРегион")
      АдрПлат.КодРегион = fieldvalue;
    elif(fieldname == "Район")
      АдрПлат.Район = fieldvalue;
    elif(fieldname == "Город")
      АдрПлат.Город = fieldvalue;
    elif(fieldname == "НаселПункт")
      АдрПлат.НаселПункт = fieldvalue;
    elif(fieldname == "Улица")
      АдрПлат.Улица = fieldvalue;
    elif(fieldname == "Дом")
      АдрПлат.Дом = fieldvalue;
    elif(fieldname == "Корпус")
      АдрПлат.Корпус = fieldvalue;
    elif(fieldname == "Кварт")
      АдрПлат.Кварт = fieldvalue;
    end;
  end;

  private macro СохранитьПоля_КЭСП(fieldname : string, fieldvalue : string)
    if(fieldname == "ИдКЭСП")
      if( (KESPList.size == 0) or 
          (KESPList[ KESPList.size - 1 ].ИдКЭСП != "")
        )
        KESPList[ KESPList.size ] = TKESP();
      end;

      KESPList[ KESPList.size - 1 ].ИдКЭСП = fieldvalue;
    end;

    if(fieldname == "ВалКЭСП")
      if( (KESPList.size == 0) or 
          (KESPList[ KESPList.size - 1 ].ВалКЭСП != "")
        )
        KESPList[ KESPList.size ] = TKESP();
      end;

      KESPList[ KESPList.size - 1 ].ВалКЭСП = fieldvalue;
    end;
  end;

  private macro SaveFieldsRlsSpecific
  ( fieldname : string, 
    fieldvalue : string, 
    blockname : string
  )
      if(fieldname == "КодОснов")
        КодОснов = fieldvalue;
      elif(fieldname == "НомФ")
        НомФ = fieldvalue;
      elif(fieldname == "НаимЮЛ")
        НаимЮЛ = fieldvalue;
      elif( index(blockname, "ПлИП\\ФИО") )
        СохранитьПоля_ПлИП_ФИО(fieldname, fieldvalue);
      elif( (fieldname == "ИННЮЛ") or (fieldname == "ИННИП") )
        if (fieldname == "ИННЮЛ")
          ClientType = RO_CLIENTTYPE_INST;
        else
          ClientType = RO_CLIENTTYPE_PERSN_EMPLOYER;
        end;
        ClientINN = fieldvalue;
      elif(fieldname == "КПП")
        ClientKPP = fieldvalue;
      elif( index(blockname, "АдрПлат") )
        СохранитьПоля_АдрПлат(fieldname, fieldvalue);
      elif( index(blockname, "\\Счет") and (fieldname == "НомСч") )
        AccList[ AccList.size ] = fieldvalue;
      elif( index(blockname, "\\КЭСП") )
        СохранитьПоля_КЭСП(fieldname, fieldvalue);
      end;
  end;

  private macro GetClientName : string
    var ClientName : string = "";

    if(НаимЮЛ)
      ClientName = НаимЮЛ;
    else
      ClientName = ПлИП_Фамилия + " " + ПлИП_Имя;

      if(ПлИП_Отчество)
        ClientName = ClientName + " " + ПлИП_Отчество;
      end;
    end;

    return ClientName;
  end;

  private macro FillRgdBuf(wlmes : StrucRef, wlregdec : StrucRef)
    // Вызов метода базового класса TRegDecision
    FillRgdBuf(wlmes, wlregdec);

    // Специфика RPO, ROO
    wlregdec.ClientName               = GetClientName();
    wlregdec.ClientID                 = GetClientForRegDec(ClientINN/*, ClientKPP*/);
    wlregdec.ClientType               = ClientType;

  end;

  private macro InsertLnkAcc
  ( DecID : integer,
    Account : string,
    FIID : integer,
    Department: integer,
    Type : string
  )
    var   ObjectType;
    GetParm(6, ObjectType);
    if (ObjectType == NULL)
      ObjectType = OBJTYPE_DECISION
    end;
    ClearRecord( wlacclnk );
    wlacclnk.ObjectID      = DecID;
    wlacclnk.ObjectType    = ObjectType;
    wlacclnk.Account       = Account;
    wlacclnk.FIID          = FIID;
    wlacclnk.Chapter       = 1;
    wlacclnk.Type          = Type;
    wlacclnk.Department = Department;

    return ВставитьСвязанныйСчет( wlacclnk );
  end;

  private macro InsertNoteAddr(DecisionID : integer) : bool
    record adr(adress);

    ClearRecord(adr);
    adr.PostIndex = АдрПлат.Индекс;
    adr.CodeRegion = "регион";
    adr.Region = АдрПлат.КодРегион;
    adr.CodeProvince = "район";
    adr.Province = АдрПлат.Район;
    adr.CodeDistrict = "город";
    adr.District = АдрПлат.Город;
    adr.CodePlace = "населенный пункт";
    adr.Place = АдрПлат.НаселПункт;
    adr.CodeStreet = "улица";
    adr.Street = АдрПлат.Улица;
    adr.House = АдрПлат.Дом;
    adr.NumCorps = АдрПлат.Корпус;
    adr.Flat = АдрПлат.Кварт;

    GetAddress( null, null, adr, true );
    var note : string = adr.adress;

    record rgd(wlregdec); rgd.DecisionID = DecisionID;
    var stat : integer = AddNoteForObject( OBJTYPE_DECISION, 
                                           UniID(rgd, OBJTYPE_DECISION), 
                                           NOTEKIND_WLDECISION_PAYERADRESS, 
                                           note );
    if(stat)
      return FALSE;
    end;

    return TRUE;
  end;

  // Вставить счета
  macro InsertLnkAccList(DecID : integer) : bool
    var Type : string;
    Type = WLACCLNK_TYPE_ACCOUNT;
    var   ObjectType;
    GetParm(2, ObjectType);

    for(var НомСч : string, AccList)
      if( not InsertLnkAcc(DecID, НомСч, ПолучитьКодФинИн( substr(НомСч, 6, 3) ), wlmes.Department, Type, ObjectType) )
        std.msg("Ошибка при вставке связанного счета");
        return FALSE;
      end;
    end;

    Type = WLACCLNK_TYPE_KESP;
    for(var BlockKESP : TKESP, KESPList)
      if( not InsertLnkAcc( DecID, 
                            BlockKESP.ИдКЭСП, 
                            GetFIIDByCodeInAccount(BlockKESP.ВалКЭСП),
                            wlmes.Department, 
                            Type, ObjectType )
        )
        std.msg("Ошибка при вставке связанного счета");
        return FALSE;
      end;
    end;
    return TRUE;
  end;

  // Вставить связанные записи, после того как определился DecisionID
  private macro InsertRelatedRecords(DecID : integer) : bool
    
    if(not InsertLnkAccList(DecID))
        return FALSE;
    end;

    if(АдрПлат != null)
      if( not InsertNoteAddr(DecID) )
        std.msg("Ошибка при вставке примечания \"Адрес плательщика\"");
        return FALSE;
      end;
    end;

    return TRUE;
  end;

  macro CreateErrListWrongBank(wlmes : StrucRef) : RsbWlError
    var ErrList : RsbWlError = RsbWlError(0);

    var wlerr_buf : TRecHandler = TRecHandler("wlerror.dbt");

    // wlerror.Code
    wlerr_buf.rec.Code = "41";

    // wlerror.Description
    var MesReceiver : integer = GetDprtPartyID(wlmes.Department);
    wlerr_buf.rec.Description  = 
      "Электронный документ " + this.GetDocTypeName() + 
      " № " + this.DecisionNumber + " от " + this.DecisionDate +
      " ошибочно направлен налоговым органом не в тот банк (филиал банка). В банке "+
      ПолучитьКодСубъекта(MesReceiver, PTCK_BIC) + " " + Bnk_GetPartyName(MesReceiver) +
      " нет филиала с БИК " + this.БИК + " и номером " + this.НомФ;

    if( ErrList.Insert(wlerr_buf) != 0 )
      RunError("Ошибка при заполнении списка ошибок в подтверждении банка");
    end;

    return ErrList;
  end;

end;
