/*
 $Name: wlCarry.mac
 $Module: MBR
 $Description: Макрос шага "Формирование проводки по корсчету"
 */
//-----------------------------------------------------------------------------
// Блок      : 29057 - "Проведение исходящего платежа"
// Шаг       : 10    - "Формирование проводки по корсчету"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import globals, oralib, likepy, PaymInter, WldInter, InsCarryDoc, OprInter, PayInter, "SetPaymTrnDef.mac";

var PaymentObj:RsbPayment;

macro ExecuteStep( doc, first, KindDoc )
  var stat:integer = 0;
  var paymtr:RsbPaymTransaction;
  var strDate:string = "";
  /* ТЗ 1282*/
  if((PaymentObj.Dockind == DLDOC_MULTYPM) or ((PaymentObj.Dockind == DLDOC_BANKCLAIM) and (PaymentObj.DemandAcceptTerm == 0)))
    return 0;
  end;
  var KvitDate:date = execStoredFunc( "WLD_KVIT.GetKvitDate", 
                                       V_DATE, 
                                       makeArray( SQLParam( "TransferDate", PaymentObj.OutTransferDate ),
                                                  SQLParam( "Department",   PaymentObj.Department      ) ) );

  strDate = string( KvitDate );
  if( Index(strDate,"1.01.0001") ) 
    MsgBox( string( "Не найден операционный день ДПП платежа ", PaymentObj.OutTransferDate:f ) );
    return 1;
  end;
                                                  
  if ( not CarryPlanDocuments(PaymentObj.PaymentID) )
     MsgBox("Ошибка при помещении планируемой проводки в проведенные");
     return 1;
  end;

  if((PaymentObj.FuturePayerAccount != PaymentObj.FutureReceiverAccount) or
     (PaymentObj.FuturePayerFIID  != PaymentObj.FutureReceiverFIID))
    PaymentObj.ValueDate = KvitDate;
    paymtr = PaymentObj.MakeTransaction();

    if( paymtr == NULL )
      MsgBox("Ошибка при создании проводки по платежу");
      return 1;
    end;

    paymtr.Chapter         = PaymentObj.Chapter;
    paymtr.Date_Carry      = PaymentObj.ValueDate;
    paymtr.Number_Pack     = PaymentObj.NumberPack;
    paymtr.Numb_Document   = PaymentObj.Number;
    paymtr.ResultCarry     = 1;
    paymtr.Kind_Oper       = " 1";

    if (PaymentObj.ShifrOper != "")
      paymtr.Shifr_Oper      = PaymentObj.ShifrOper;
    else
      paymtr.Shifr_Oper      = "09";
    end;

    paymtr.Ground          = PaymentObj.Ground;
    paymtr.Department      = PaymentObj.Department;
    paymtr.AccountPayer    = PaymentObj.FuturePayerAccount;
    paymtr.FIIDPayer       = PaymentObj.FuturePayerFIID;
    paymtr.SumPayer        = PaymentObj.FuturePayerAmount;
    paymtr.AccountReceiver = PaymentObj.FutureReceiverAccount;
    paymtr.FIIDReceiver    = PaymentObj.FutureReceiverFIID;
    paymtr.SumReceiver     = PaymentObj.FutureReceiverAmount;
    paymtr.ClaimID         = PaymentObj.ClaimID;

    if( PaymentObj.CoverAmount != $0 ) 
      paymtr.SumEquivalentCarry = PaymentObj.CoverAmount;
    end;

    if( not paymtr.Carry )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;  
  end;
  // Освободить все резервы по платежу
  PaymentObj.FreeReserve( PaymentObj.PayerAccount, PaymentObj.Chapter, PaymentObj.PayerFIID );
  PaymentObj.StatusInfo = "Исполнен";

  return stat;
end;

/**
 * Массовые действия. До транзакции
 */
macro PrepMassExecuteStep() 
  
  return execStoredFunc( "WLD_CARRYSTEP.MassWldCarryStepPrepare", V_INTEGER );

end;

PRIVATE CLASS (TRsdRecordReader) TMassWlCarryData

  var OrderID:integer, 
      ID_Operation:integer,
      ID_Step:integer,
      FuturePayerAccount:string,
      FutureReceiverAccount:string,
      FIID_FUTUREPAYACC:integer,
      FIID_FUTURERECACC:integer,
      FuturePayerAmount:money,
      FutureReceiverAmount:money,
      Chapter:integer,
      ValueDate:date,
      NumberPack:integer,
      Number:string,
      Ground:string,
      ShifrOper:string,
      Department:integer,
      ClaimID:integer,
      CoverAmount:money,
      KvitDate:date,
      OFRSymbol:string,
      PIOFRSymbol:string      
END;

/**
 * Массовые действия. Транзакция
 */
macro MassExecuteStep()

  var select, rs:RsdRecordset;
  var Carry:RsbAccTransaction;
  var ReceiverAccount:string;
  var ErrorMessage = "", ErrorStatus = 0;
  var batchTrn:RsbBatchPaymTrn;
  var Status_After = ACCTRN_STATUS_DOCUMENT;
  var carryData : TMassWlCarryData = TMassWlCarryData();
  var accTrnData = RsbAccTransactionData(); 

  select = " select t.t_OrderID as t_OrderID, t.t_ID_Operation as t_ID_Operation, t.t_id_Step as t_id_Step,"+           
                  " pm.t_FuturePayerAccount    as t_FuturePayerAccount,"+
                  " pm.t_FutureReceiverAccount as t_FutureReceiverAccount,"+
                  " pm.T_FIID_FUTUREPAYACC     as T_FIID_FUTUREPAYACC,"+
                  " pm.T_FIID_FUTURERECACC     as T_FIID_FUTURERECACC,"+
                  " pm.t_FuturePayerAmount     as t_FuturePayerAmount,"+
                  " pm.t_FutureReceiverAmount  as t_FutureReceiverAmount,"+
                  " pm.t_Chapter     as t_Chapter,"+
                  " pm.t_ValueDate   as t_ValueDate,"+
                  " pm.t_NumberPack  as t_NumberPack,"
                  " rm.t_Number      as t_Number,"+
                  " rm.t_Ground      as t_Ground,"+
                  " rm.t_ShifrOper   as t_ShifrOper,"+
                  " pm.t_Department  as t_Department,"+
                  " pm.t_ClaimID     as t_ClaimID, "+
                  " nvl( nvpi.t_CoverAmount, 0 ) as t_CoverAmount,"+
                  " WLD_KVIT.GetKvitDate( pr.t_TransferDate, pm.t_Department ) as t_KvitDate, " +
                  " PM_COMMON.GetPaymNoteTextStr( pm.t_PaymentID, 67/*PM_COMMON.NOTEKIND_PM_OFRSYMBOL*/, pm.t_ValueDate ) as t_OFRSymbol, " +
                  " nvl( pidb.t_OFRSymbol, nvl( picr.t_OFRSymbol, CHR(0) ) ) as t_PIOFRSymbol "
             " from doprtemp_tmp t,"+
                  " dpmpaym_dbt pm,"+
                  " dpmprop_dbt pr,"+
                  " dpmrmprop_dbt rm,"+
                  " dpmnvpi_dbt nvpi,"+
                  "dpmaddpi_dbt pidb, " +
                  "dpmaddpi_dbt picr "
            " where t.t_ErrorStatus = 0"+
              " and t.t_SkipDocument = 0"+
              " and pm.t_PaymentID = t.t_OrderID"+
              " and rm.t_PaymentID = t.t_OrderID"+
              " and pr.t_PaymentID = t.t_OrderID"+
              " and pr.t_IsSender = CHR(0)"+
              " and pm.t_PaymentID = t.t_OrderID"+
              " and pm.t_DocKind <> 311"+
              " and nvpi.t_PaymentID(+) = pm.t_PaymentID " +
              " and pidb.t_PaymentID(+) = pm.t_PaymentID "
              " and pidb.t_DebetCredit(+) = 0 "
              " and pidb.t_Department(+) = pm.t_Department "
              " and picr.t_PaymentID(+) = pm.t_PaymentID "
              " and picr.t_DebetCredit(+) = 1 "
              " and picr.t_Department(+) = pm.t_Department;";

  rs = execSQLselect( select, NULL, false );  


  while(rs.moveNext())
    
    carryData.read(rs);

    if(not batchTrn.AddTransactionActuate( carryData.OrderID, 0, ACCTRN_STATUS_DOCUMENT, carryData.ID_Operation, carryData.ID_Step))        
      SetErrorOprTemp( carryData.OrderID, 1, "Ошибка при помещении планируемых проводок в проведенные" );
    elif((carryData.FuturePayerAccount != carryData.FutureReceiverAccount) or
         (carryData.FIID_FUTUREPAYACC  != carryData.FIID_FUTURERECACC))

      accTrnData.Chapter         = carryData.Chapter;
      accTrnData.Date_Carry      = carryData.KvitDate;
      accTrnData.Number_Pack     = carryData.NumberPack;
      accTrnData.Numb_Document   = carryData.Number;
      accTrnData.ResultCarry     = 1;
      accTrnData.Kind_Oper       = " 1";
      accTrnData.Shifr_Oper      = carryData.ShifrOper;
      accTrnData.Ground          = carryData.Ground;
      accTrnData.Department      = carryData.Department;
    
      accTrnData.FIIDPayer       = carryData.FIID_FuturePayAcc;
      accTrnData.FIIDReceiver    = carryData.FIID_FutureRecAcc;
      accTrnData.SumPayer        = carryData.FuturePayerAmount;
      accTrnData.SumReceiver     = carryData.FutureReceiverAmount;
      accTrnData.AccountPayer    = carryData.FuturePayerAccount;
      accTrnData.AccountReceiver = carryData.FutureReceiverAccount;
            
      accTrnData.ClaimID         = carryData.ClaimID;      
   
      if( carryData.CoverAmount != 0 )
        accTrnData.SumEquivalentCarry = carryData.CoverAmount;
      end;

      if( ErrorMessage == "" )
        ErrorStatus = SetPaymTransactionDef(accTrnData, CarryData.OrderID, 0, CarryData );
      end;

      if(ErrorStatus)
        ErrorMessage = GetErrMsg();

        if(not ErrorMessage)
          ErrorMessage = "Ошибка при установке атрибутов проводки по платежу";
        end;
      end;
      
      if( batchTrn.AddTransaction( CarryData.OrderID, accTrnData, Status_After, PM_CARRY_REASON_EXECUTION, 0, CarryData.ID_Operation, CarryData.ID_Step ) < 0 )
        ErrorMessage = "Ошибка заключительной проводки по корсчету";
        ErrorStatus = 1;
      end;          

    end;
    
  end;

  if( not batchTrn.Execute( false ) )
    batchTrn = NULL;      
  end;

  var errPaymentID:TArray;
  var errCode:TArray;
  var errMsg:TArray;

  if(not batchTrn.GetErrors(errPaymentID, errCode, errMsg) )
    batchTrn = NULL;
  end;   
  
  if( not batchTrn )
    SetErrorsOprTemp(errPaymentID, errCode, errMsg);  
  end;
    
  ErrorStatus = execStoredFunc( "WLD_CARRYSTEP.MassWldCarryStepExecute", V_INTEGER );

  return ErrorStatus;

ONERROR(x)
  MemoryError( 1, x.Message );  
  return 1;
end;

/**
 * Массовые действия. После транзакции
 */
macro PostMassExecuteStep()
  return 0;
end;
