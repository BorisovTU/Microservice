/*
  $Name:         uf501.mac
  $Module:       МБР
  $Description:  Функции для сообщений ED501
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для сообщений ED501
//
// Программист  : Чукина Т.А.
//
// Создан       : 22.12.2014
//
//-----------------------------------------------------------------------------

import oralib, likepy, "xmlmestools.mac", MesInter, rsexts, "bnk_common.mac", 
       "wluftool.mac", "wltools.mac", "wlexport.mac", "ufgenmes.mac";

const START_FILENAME_STR : string = "FileName=";

private const WrongED501MessageDirection = 1;
private const ReceiverIsNotSPFS501Participant = 1;
private const ReceiverIsSPFSParticipant501ButLater = 2;
private const ReceiverIsNotSPFSParticipant501SinceDate = 3;

private macro GetEDReceiver501(RecipientID, RecipientCodeKind, RecipientCode)
  return GetEDReceiver(RecipientID, RecipientCodeKind, RecipientCode);
onerror()
  ErrExport("Не определен УИС получателя сообщения ED501. Для получателя сообщения " +
      ПолучитьКодСубъекта( RecipientID, PTCK_CONTR ) + Bnk_GetPartyName(RecipientID) +
      " задайте код вида 47 UIS, после чего повторите экспорт");
  return false;
end;

private macro GetStrInfoID(MesID : integer) : string
  var InfoID : integer = GetObjIDByMesLink(MesID, OBJTYPE_INFO, "");
  if( InfoID <= 0 )
    RunError("Не найден учетный объект сообщения по форме ED501");
  end;

  return GetWlinfoStrID(InfoID);
end;

private macro GetAttObjRs(ObjectType : integer, ObjectID : string, ImgType : integer) : RsdRecordset
  var rs : RsdRecordset = execSQLselectPrm
  ( "select /*+ index (imgd dimgdata_dbt_idx1) */ imgd.t_ImageID "
    "  from dimgdata_dbt imgd "
    " where imgd.t_ObjectType = :ObjectType "
    "   and imgd.t_ObjectID   = :ObjectID "
    "   and imgd.t_ImageType  = :ImgType "
    "   and imgd.t_CloseDate  = to_date( '01010001', 'ddmmyyyy' ) "
    " order by imgd.t_ImageID ",
    SQLParam( "ObjectType", ObjectType ),
    SQLParam( "ObjectID", ObjectID ),
    SQLParam( "ImgType", ImgType )
  );

  return rs;
end;

private macro GetAttObjFileName(ImageID : integer) : string
  var rs : RsdRecordset = execSQLselectPrm
  ( "select imgd.t_FileName "
    "  from dimgdata_dbt imgd "
    " where imgd.t_ImageID = :ImageID ",
    SQLParam("ImageID", ImageID)
  );

  if(rs and rs.moveNext())
    return rs.value(0);
  end;

  RunError("Не найдены данные о прикрепленном файле");
end;

private macro ED501MesSendingCheck( OutsideAbonentID, Department )

  var DepartmentSubjectCode = ПолучитьКодСубъекта( GetDprtPartyID( Department ), PTCK_UIS, null, 1 );
  var query : string = 
    "select * " +
    "  from dwlusspfs_dbt " +
    " where t_ObjectType    = :ObjectType " +
    "   and t_ObjectID      = :ObjectID " +
    "   and t_ED501Exchange = 'X' " +
    "   and ( t_UISSenderCode = :DepSbjCode " +
    "    or   t_UISSenderCode = '9999999999' ) ";
  var params : TArray = makeArray( SQLParam("ObjectType", OBJTYPE_PARTY),
                                   SQLParam("ObjectID",   OutsideAbonentID),
                                   SQLParam("DepSbjCode", DepartmentSubjectCode ) );
  var rs : RsdRecordset = execSQLselect( query, params );

  if( rs and rs.moveNext() )
    return 0;
  end;

  return WrongED501MessageDirection;    
end;

private macro IsReceiverSPFS501Participant( OutsideAbonentID: integer, ED501Date: @date, ESSize501: @integer )
  var query : string = 
    "select t_BeginDate501, t_EndDate501, t_ESSize501 " +
    "  from dptspfs_dbt " +
    " where t_PartyID = :PartyID ";
  var params : TArray = makeArray( SQLParam("PartyID", OutsideAbonentID ) );
  var rs : RsdRecordset = execSQLselect( query, params );

  if( rs and rs.moveNext() )
    ESSize501 = rs.Value( "t_ESSize501" );
    if( rs.value( "t_BeginDate501" ) > {BranchCurDate} )
      ED501Date = rs.value( "t_BeginDate501" );
      return ReceiverIsSPFSParticipant501ButLater;
    end;
    if( (rs.value( "t_EndDate501" ) < {BranchCurDate}) and ( rs.value( "t_EndDate501" ) != date(0,0,0) ) )
      ED501Date = rs.value( "t_EndDate501" );
      return ReceiverIsNotSPFSParticipant501SinceDate;
    end;
    return 0;
  end;

  return ReceiverIsNotSPFS501Participant;
end;

macro PrepareForExportED501(nodeED501 : object, wlmes, retESSize501: @integer)
  var sInfoID : string = GetStrInfoID(wlmes.MesID);

  // ProprietoryDocument
  var DocumentImageID : integer = 0;
  var rs : RsdRecordset = GetAttObjRs(OBJTYPE_INFO, sInfoID, WLD_INFO_IMGTYPE_DOCUMENT);
  if(rs and rs.moveNext())
    DocumentImageID = rs.value("t_ImageID");
    var Doc : string = "";
    
    if( EncodeAttObjToBase64(DocumentImageID, Doc) )
      var ProprietoryDocumentNode : object = GetChildNode(nodeED501, "ProprietoryDocument");
      ProprietoryDocumentNode.Text = Doc;
    else
      RunError("Ошибка при кодировании данных из прикрепленного объекта");
    end;

    // Если прикрепленных объектов больше одного
    if(rs.moveNext())
      msgboxex("К информационному сообщению прикреплено более одного файла, имеющего тип \"Документ\". В сообщение записан файл, прикрепленный первым. Если требуется передать другой файл, откатите выгрузку, оставьте в списке прикрепленных объектов только один (нужный) файл с типом \"Документ\"", 0);
    end;
  else
    RsbThrow("Отсутствует документ для отправки. Добавьте файл, который требуется передать, в информационное сообщение как прикрепленный объект типа \"Документ\"");
  end;

  // Проверки на возможность направлять сообщения и то, является ли получатель действующим участником обмена в СПФС
  var stat = 0;

  stat = ED501MesSendingCheck( wlmes.OutsideAbonentID, wlmes.Department );
  if( stat == WrongED501MessageDirection )
    RsbThrow("Не допускается направление сообщений ED501 получателю с УИС " + GetEDReceiver501( wlmes.OutsideAbonentID, wlmes.OutsideAbonentCodeKind, wlmes.OutsideAbonentCode ) + 
              ". | Если с данным получателем заключен договор об обмене обновите данные справочника СПФС, направив запрос ED573 с видом 3, после чего повторите экспорт");   
    return false;
  end;
  
  var ED501Date = date(0,0,0);
  var ESSize501 = 0;
  stat = IsReceiverSPFS501Participant( wlmes.OutsideAbonentID, @ED501Date, @ESSize501 );

  if( stat == ReceiverIsNotSPFS501Participant )
    RsbThrow("Получатель с УИС " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_UIS, null, 1 ) + " не является участником обмена сообщениями ED501. | Отправка сообщения запрещена.");    
    return false;
  end;

  if( stat == ReceiverIsSPFSParticipant501ButLater )
    RsbThrow("Получатель с УИС " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_UIS, null, 1 ) + " является участником обмена сообщениями ED501 c " + 
               ED501Date + ". | Отправка сообщения до даты начала обмена данным получателем запрещена.");
    return false;
  end;

  if( stat == ReceiverIsNotSPFSParticipant501SinceDate )
    RsbThrow("Получатель с УИС " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_UIS, null, 1 ) + " не является участником обмена сообщениями ED501 c " + 
               ED501Date + ". | Отправка сообщения после даты окончания обмена с данным получателем запрещена.");
    return false;
  end;

  // ProprietoryAttachment
  var ProprietoryAttachmentNode : object = GetChildNode(nodeED501, "ProprietoryAttachment");
  rs = GetAttObjRs(OBJTYPE_INFO, sInfoID, WLD_INFO_IMGTYPE_DOC_ATTACH);
  var Att : string = "";
  if(rs and rs.moveNext())
    if( not EncodeAttObjToBase64(rs.value("t_ImageID"), Att) )
      RunError("Ошибка при кодировании данных из прикрепленного объекта");
    end;
    
    ProprietoryAttachmentNode.Text = Att;

    // Если прикрепленных объектов больше одного
    if(rs.moveNext())
      msgboxex("К информационному сообщению прикреплено более одного файла, имеющего тип \"Приложение к документу\". В сообщение записан файл, прикрепленный первым. Если требуется передать другой файл, откатите выгрузку, оставьте в списке прикрепленных объектов только один (нужный) файл с типом \"Приложение к документу\"", 0);
    end;
  else
    Att = START_FILENAME_STR + GetAttObjFileName(DocumentImageID) + "\n" + ProprietoryAttachmentNode.Text;
    if( EncodeBase64(Att, Att) )
      ProprietoryAttachmentNode.Text = Att;
    else
      RunError("Ошибка при кодировании текста по алгоритму base64");
    end;
  end;
  
  retESSize501 = ESSize501;

end;
                                                                                     
macro CheckFileSizeED501(FileName : string, ESSize501)
  var FileList : TDirList = TDirList(FileName, "F");
  if(FileList.Count < 1)
    RunError("Файл " + FileName + " не найден");
  end;

  var FileSize = FileList.Size(0) / 1024; // в килобайтах
  var MaxSize : double = 0;

  if( ESSize501 and (ESSize501 > 0) )
    MaxSize = ESSize501 / 1024; // в килобайтах
  else
    MaxSize = Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED501\\MAXSIZEMESS", V_DOUBLE, 15);
  end;

  if(FileSize > MaxSize)
    RunError("Превышен максимально допустимый размер сообщения");
  end;
end;

macro GetFileDirectoryED501() : string
  return Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED501\\FILEDIRECTORY", V_STRING, "..\\workfile");
end;

macro GetFileNameED501(WlmesTrn : string, Suffix : string) : string
  var FileDirectory : string = GetFileDirectoryED501();
  var FileType : string = Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED501\\FILETYPE", V_STRING, "txt");

  return FileDirectory + "\\" + WlmesTrn + "_" + Suffix + "." + FileType;
end;

macro ImportED501(nodeED501 : object, WlmesTrn : string)
  // Получить текст из xml, декодировать и сохранить

  // ProprietoryDocument
  var ProprietoryDocumentNode : object = GetChildNode(nodeED501, "ProprietoryDocument");
  var Doc : string = StrSubst(StrSubst(ProprietoryDocumentNode.Text, "\n", ""), "\r", "");
  var FName : string = GetFileNameED501(WlmesTrn, "Doc");
  if( not DecodeAndSaveFileFromBase64(Doc, FName) )
    RunError("Ошибка при сохранении ProprietoryDocument");
  end;
  ProprietoryDocumentNode.Text = "";

  // ProprietoryAttachment
  var ProprietoryAttachmentNode : object = GetChildNode(nodeED501, "ProprietoryAttachment");

  if (ProprietoryAttachmentNode != NULL)
    var Att : string = StrSubst(StrSubst(ProprietoryAttachmentNode.Text, "\n", ""), "\r", "");
    if( not DecodeBase64(Att, Att) )
      RunError("Ошибка при декодировании текста по алгоритму base64");
    end;

    if( StrLwr( SubStr(Att, 1, StrLen(START_FILENAME_STR)) ) == StrLwr(START_FILENAME_STR) )
      ProprietoryAttachmentNode.Text = Att;

    else
      Att = ProprietoryAttachmentNode.Text;
      FName = GetFileNameED501(WlmesTrn, "Att");

      if( not DecodeAndSaveFileFromBase64(Att, FName) )
        RunError("Ошибка при сохранении ProprietoryAttachment");
      end;

      ProprietoryAttachmentNode.Text = "";
    end;
  end;

  // записать ED501 в БД
  if( not ЗаписатьПоле(XMLField, String(nodeED501.xml)) ) // удалять символы перевода строки нельзя, т.к. в ProprietoryAttachment перевод строки может обозначать конец имени файла
    RunError( "Ошибка записи поля: " + XMLField );     
  end;
end;
