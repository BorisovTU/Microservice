/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по SWIFT-SB MT900S                               */
/*                                                                          */
/*  Имя файла: swsd900s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Подтверждение дебета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */
  Account          :string,        /* Корсчет */
  ValueDate        :date,          /* Дата валютирования */
  Currency         :string,        /* Код валюты */
  Sum              :moneyl,        /* Сумма перевода */
  OrdBank          :Bank,          /* Банк Приказодателя */
  Information      :string,        /* Информация  */
  Sender           :Bank;          /* Отправитель */

  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Information      = "";

end; /* class StatementMessage */

var MT900 : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro FillRT(SubType)
  return true;
end;

macro Fill20( TRF )
  MT900.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MT900.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25D( Account )
  MT900.Account = Account;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT900.ValueDate = Date;
  MT900.Currency  = Cur;
  MT900.Sum       = moneyl(Sum);
  return TRUE;
end;

macro FillCopy( Str )
  return TRUE;
end;

/* Заполнение списка полей формы  MT900*/
var FldSN      = ТПолеФормы( УникальныйНомерСообщенияСМФР,        FIELD_MANDATORY, "ReadTRF",     "FillSN",   "" ),
    FldRN      = ТПолеФормы( НомерСвязанногоСообщенияСМФР,        FIELD_OPTIONAL,  "ReadTRF",     "FillRN",   "" ),
    FldRT      = ТПолеФормы( ТипИПодтипСообщения,                 FIELD_OPTIONAL,  "ReadRT",      "FillRT",   "" ),
    Fld20      = ТПолеФормы( TransactionReferenceNumberField,     FIELD_MANDATORY, "ReadTRF",     "Fill20",   "" ),
    Fld21      = ТПолеФормы( RelatedReferenceField,               FIELD_MANDATORY, "ReadTRF",     "Fill21",   "" ),
    Fld25D     = ТПолеФормы( DebitAccountIdentificationField,     FIELD_MANDATORY, "Read25",      "Fill25D",  "" ),
    Fld32A     = ТПолеФормы( ValueDateCurrencyCodeAmountField_A,  FIELD_MANDATORY, "Read32A",     "Fill32A",  "" ),
    FldMFIELDS = ТПолеФормы( CopyMandatoryFields,                 FIELD_MANDATORY, "ReadMFIELDS", "FillCopy", "" );

macro ConstructMT900( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MT900.Sender   = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMT900 */

macro GenDoc( addrMes )
  MT900 = StatementMessage();
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по МТ900S");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlconf);

  if( not ConstructMT900( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if( not ПолучитьФинИнПоISO( MT900.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT900.Currency );
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number       = MT900.TRF;
  wlconf.RelatedRef   = MT900.RelatedRef;
  wlconf.Account      = MT900.Account;  
  wlconf.FIID         = wlfininstr.FIID;
  wlconf.DateValue    = MT900.ValueDate;
  wlconf.Sum          = MT900.Sum;
  wlconf.DKFlag       = WL_DEBET;
  wlconf.Description  = substr( MT900.Information, 1,215 );

 /* if ( wlconf.DateValue==date(0,0,0) )
     wlconf.TransferDate = {curdate};
  else
     wlconf.TransferDate = wlconf.DateValue;
  end;
  */
  wlconf.TransferDate = MT900.ValueDate; //#118037  

  if( not ОпределитьСхемуРасчетовПоКорсчету( wlconf.Corschem, wlmes.OutsideAbonentID, wlconf.Account, wlconf.FIID ) )
    return FALSE;
  end;    

  if ( MT900.OrdBank.IsSet() )
     wlconf.BankID    = MT900.OrdBank.PartyID;
     wlconf.CodeKind  = MT900.OrdBank.CodeKind;
     wlconf.CodeName  = MT900.OrdBank.CodeName;
     wlconf.CodeValue = MT900.OrdBank.CodeBank;
     wlconf.BankName  = MT900.OrdBank.Name;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вводе подтверждения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
