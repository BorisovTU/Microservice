/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Реестр по отвергнутым исходящим платежам                                 */
/*                                                                          */
/*  Создан:     03.05.05                                                    */
/*  Программист Желнов Ю.Н.                                                 */
/****************************************************************************/



import oralib;
import prpm;

class    CommonInfo( a, b, c )
 var
  corShet          :string,       // Коррсчет
  val              :integer,        // Код валюты
  count            :integer,       // Кол-во док-ов
  summa            :money  ;       // Общая сумма
  corShet = a;
  val     = b;
  summa   = c;
  count   = 1;
end;

var RecSave:TArray = TArray();
var CountDoc = 0;

/*  Функция поиска записи в массиве обработанных платежей.
    Если находит то увеличивает кол-во и прибавляет сумму платежа
    Иначе добавляет новую запись       
*/
macro SearchAndAdd (corshet,val,summa)
var Iter = 0;
  while  (Iter < RecSave.size)
   if (  
        (corshet = RecSave[Iter].corshet) and 
        (val = RecSave[Iter].val) 
      ) 
   // Нашли такую запись прибавим значения 
      RecSave[Iter].summa = RecSave[Iter].summa + summa;
      RecSave[Iter].count = RecSave[Iter].count + 1;

   return true;
   end;
   Iter = Iter + 1; 
  end;
  // Ничего не нашли значит добавим как новую
  RecSave[RecSave.size] = CommonInfo(string(corshet),Val,summa);


end;

macro PrintHeader ()
[
+---------------+---+----------+--------------------------+-------------------------+-----------------+-------------------------+-------------------------+
|     Док.№     |Вал|   Дата   |        Корсчет           |           Счет          |      Банк       |          Счет           |          Сумма          |
|               |   | валютир. |                          |        плательщика      |    получателя   |       получателя        |                         |
+---------------+---+----------+--------------------------+-------------------------+-----------------+-------------------------+-------------------------+
];
end;


macro PrintFooter ()
var cc=0; 
[ ];

[Итого документов    #####] (CountDoc);
[
+--------------------------+---+----------+-------------------------+
|        Корсчет           |Вал|Количество|          Сумма          |
|                          |   |документов|                         |
+--------------------------+---+----------+-------------------------+
];
while (cc < RecSave.size)
 [|##########################|###|   #####  |#########################|]
 (RecSave[cc].corshet,RecSave[cc].Val,RecSave[cc].count, RecSave[cc].Summa);
 cc = cc + 1;
end;
[+--------------------------+---+----------+-------------------------+]

end;

macro PrintDocument (ncopy, DocKind)
var params:TArray = TArray();
var params1:TArray = TArray();
var res,ss,subss,res1,subss1,res2;
var a9,a10,a11;
var s1,s2,s3;

/// Запрос под первую строку
ss = string(" SELECT ",
        " pmrmprop.T_NUMBER , val.T_FI_CODE ,paym.T_ValueDate, cor.t_Account, ",
        " paym.T_PayerAccount , code.T_ShortName , pmprop.T_BankCode, ",
        " paym.T_ReceiverAccount , paym.T_PayAmount",  
        " FROM dpmpaym_dbt paym,dpmrmprop_dbt pmrmprop, dfininstr_dbt val, ",
        " dcorschem_dbt cor , dpmprop_dbt pmprop , dobjkcode_DBT CODE " ,
        " where  pmrmprop.T_PAYMENTID = pmprop.T_PAYMENTID AND ",
        " paym.T_PAYMENTID = pmrmprop.T_PAYMENTID and ",
        " paym.t_PayFIID = val.t_FIID      and ",
        " cor.T_NUMBER = pmprop.T_CORSCHEM AND ",
        " COR.T_FIID = PMPROP.T_PAYFIID    AND ",
        " COR.T_FI_KIND = VAL.T_FI_KIND    AND ",
        " code.T_CodeKind = pmprop.T_CodeKind and ",
        "           pmprop.t_DebetCredit = 1      ",
        "   and Paym.T_PaymentID = :payID",
        "   ORDER BY val.T_FI_CODE");
/// Запрос под вторую строку
subss = string (" select lnk.t_ConfID,lnk.t_BankDate ",
                " FROM dwlkvtlnk_dbt lnk, dwlpm_dbt wlpm, ", 
                " (select max(wlpm.t_WlPmNum) mx from dwlpm_dbt wlpm ",
                "       where wlpm.t_paymentID=:payID ) sub ",
                " where  lnk.t_WlPmID=wlpm.t_WlPmID and wlpm.t_paymentID=:payID and ",
                " wlpm.t_WlPmNum=sub.mx "
                );
///
subss1 = string ("SELECT conf.t_Number , conf.t_DateValue , conf.t_Description ",
                " FROM dwlkvtlnk_dbt lnk, dwlconf_dbt conf ",
               " WHERE conf.t_ConfID = lnk.t_ConfID and lnk.t_ConfID = : ConfID ");
 if (GetDialogFlag()) 
        PrintHeader ();
 end;
 params[params.size] = SQLParam( "payID",pr_pmpaym.rec.PaymentID);
 res = execSQLSelect (ss,params,false);
 res.MoveNext();
 [|###############|###|##########|##########################|#########################|### #############|#########################|#########################|]
  (res.Value(0):l,res.Value(1):c,string(res.Value(2)):c,res.Value(3):l
     ,res.Value(4):l,res.Value(5):l,res.Value(6):l,res.Value(7):l,res.Value(8):r);
 res1 = execSQLSelect (subss,params,false);
 res1.MoveNext();
    /// Проанализируем результат подзапроса
      if (res1.Value(0)==-1)
            s1 = "";
            s2 = "Дата:";
            s3 = "Причина отказа:"; 
            a9 = " ";   
            a10 = res1.Value(1);
            a11 = "Сквитован отказом без подтверждения";
      end;
      if (res1.Value(0)==-2)
            s1 = "";
            s2 = "Дата:";
            s3 = "Причина отказа:"; 
            a9 = " ";   
            a10 = res1.Value(1);
            a11 = "Отозван без подтверждения";  
      end;      
      if (res1.Value(0)>0) 
            s1 = "Отказ";
            s2 = "дата:";
            s3 = "Причина отказа:"; 
            params1[params.size] = SQLParam( "ConfID",res1.Value(0) );
            res2 = execSQLselect (subss1,params1,false);
            res2.MoveNext();
            a9 = res2.Value(0);      
            a10 = res2.Value(1);
            a11 = res2.Value(2) ;    

      end;
     [|                               ######### ###############  ##### ##########  ############### #############################################################|]
   (s1,a9,s2,string(a10),s3,a11);
  [+---------------+---+----------+--------------------------+-------------------------+-----------------+-------------------------+-------------------------+];
  // Сохраним необходимые данные для вывода подвала отчета
  SearchAndAdd (res.Value(3),res.Value(1):c,res.Value(8):r);
  CountDoc = CountDoc + 1;
  if (GetDialogFlag()) 
        PrintFooter(); 
  end;
  return true;
end;

end;
