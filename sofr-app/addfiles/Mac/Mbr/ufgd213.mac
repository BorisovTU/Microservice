/*
$Name:           ufgd213.mac
$Module:         Межбанковские расчеты
$Description:    Генерация запросов по сообщению ED213
*/                                                                            

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация запросов по сообщению ED213
//
// Программист  : Чукина Т.А.
//
// Создан       : 23.10.2014
//
//-----------------------------------------------------------------------------

import "ufgendoc.mac", "ufgdwlreq.mac";

private class (TGenDocWlreqUFEBS) TGenDocED213()

  InitTGenDocWlreqUFEBS(MESKIND_REQUEST, "ED213");

  var AccDocNo : string = "",
      AccDocDate : string = "",
      Sum : string = "",
      PayerPersonalAcc : string = "",
      Purpose : string = "",
      AcptTerm : string = "";

  macro ReadFields() : bool
    var xml : object = ReadXMLField(wlmes);

    OriginatorUIS  = ReadAttribute(xml,"EDAuthor");
    RecipientUIS   = ReadAttribute(xml,"EDReceiver");

    var nodeED103 : object = GetChildNode(xml, "ED103");

    AccDocNo = ReadAttribute(nodeED103, "AccDocNo", "AccDoc");
    AccDocDate = ReadAttribute(nodeED103, "AccDocDate", "AccDoc");
    Sum = ReadAttribute(nodeED103, "Sum");
    PayerPersonalAcc = ReadOptinalAttribute(nodeED103, "PersonalAcc", "Payer");
    Purpose = ReadNodeText(nodeED103, "Purpose");
    AcptTerm = ReadOptinalAttribute(nodeED103, "AcptTerm");

    return TRUE;
  end;

  macro FillQueries() : bool
    wlreq.Queries = "Запрос акцепта требования № " + AccDocNo + " от " + AccDocDate +
                    " на сумму " + Sum + " к счету плательщика " + PayerPersonalAcc +
                    " на основании \"" + Purpose + "\".";
    if(AcptTerm)
      var DaysWord : string = NumToStr( int(AcptTerm), "день", "дня", "дней", true, 0 );
      if( index(DaysWord, "день") )
        DaysWord = "день";
      elif( index(DaysWord, "дня") )
        DaysWord = "дня";
      else
        DaysWord = "дней";
      end;

      wlreq.Queries = wlreq.Queries + " Срок для акцепта " + AcptTerm + " " + DaysWord;
    end;

    return TRUE;
  end;

  macro FillOthers() : bool
    wlreq.SubKind = WLD_SUBKIND_REQ_ACCEPT;
    return TRUE;
  end;
end;

macro GenDoc( addrMes )                       

  var gdObj : TGenDocED213 = TGenDocED213();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
