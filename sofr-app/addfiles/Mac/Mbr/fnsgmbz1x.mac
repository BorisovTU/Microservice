/*
  $Name:           fnsgmbz1x.mac
  $Module:         МБР
  $Description:    Процедура генерации сообщения "Подтверждение"
*/

import "wlgenmes.mac", WldInter, "xmlmestools.mac", "mnsbxtool.mac";

private const FILL_AT_EXPORT : string = " ";

private macro GetFileName( MesID, Reference ) : string

  var Filename = "";
  var select = " select NVL( SUBSTR( sess.t_FileName, " +
               "                     INSTR (sess.t_FileName, '\\', -1) + 1, " +
               "                     INSTR (sess.t_FileName, '.', -1) - INSTR (sess.t_FileName, '\\', -1) -1 ), " + 
               "             CHR(1) ) " +
               "   from dwlmes_dbt mes, dwlsess_dbt sess " +
               "  where mes.t_SessionID = sess.t_SessionID " +
               "    and mes.t_Trn       = :RELATEDREF " +
               "    and mes.t_Kind      = :WLD_MES_KIND_MNS " +
               "    and mes.t_MesID     = :MesID ";

  var params = makeArray( SQLParam("RELATEDREF", Reference),
                          SQLParam("WLD_MES_KIND_MNS", WLD_MES_KIND_MNS),
                          SQLParam("MesID", MesID) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )
    FileName = rs.value(0);
  end;

  return FileName;
end;

private macro FindFileName( Queries ) : string
  var pos = index( Queries, "ИмяФайла:" );
  if( pos > 0 )
    pos = pos + 9;
    var eos = index( Queries, "\n", pos );
    eos = IfThenElse( eos, eos - pos, NULL );

    return SubStr( Queries, pos, eos );
  end;
  return "";
end;

macro GenMes( addrMes, buff, addrOldMes )

  record OldMes( wlmes );
  ClearRecord( OldMes );

  SetBuff( wlmes,  addrMes    );
  SetBuff( wlreq,  buff       );
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения \"Запрос на повторное получение квитанции налогового органа\"");

  // Файл
  StartBlockLogXML("Файл");

  ЗаписатьФайл("ЗПРБНКВИТН");

  // Файл \ ЗПРБНКВИТН
  StartBlockLogXML("ЗПРБНКВИТН");

    if( wlreq.RelatedRef != "" )
      var MesID = GetCachedVar( "WLMESID" );
      var FileName = GetFileName( MesID, wlreq.RelatedRef );
    else
      FileName = FindFileName( wlreq.Queries );
    end;

    if( FileName == "" )
      RunError( "Не задано имя файла, для которого запрашивается повторное получение квитанции" );
    end;

    ЗаписатьПолеЛог( "ИмяФайла", FileName );
    ЗаписатьПолеЛог( "ДатаНапр", YYYY_MM_DD( Date() ) );

    // Файл \ ЗПРБНКВИТН \ СвБанк
    СведенияОБанке( "СвБанк", wlmes.InsideAbonentID );

    // Файл \ ЗПРБНКВИТН \ ПредБанка
    ЗаписатьПредБанка( "ПредБанка", wlmes.InsideAbonentID, false );

  FinishBlockLogXML("ЗПРБНКВИТН");

  FinishBlockLogXML("Файл");

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
