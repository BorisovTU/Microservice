/*
$Name:               fnsgmbnpx.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения по BNP XML
*/

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "mnsbxtool.mac", "pm_const.mac";

private const V_SPECVAL = 26;

private macro ПолучитьКодПричНеисп(errCode : string) : string
  var КодПричНеисп : string = "";

  if(errCode == "10")
    КодПричНеисп = "1";
  elif(errCode == "11")
    КодПричНеисп = "2";
  elif(errCode == "12")
    КодПричНеисп = "3";
  elif(errCode == "13")
    КодПричНеисп = "4";
  else
    RunError("Не удалось заполнить обязательное поле \"КодПричНеисп\"");
  end;

  return КодПричНеисп;
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record wlresprm(wlresprm);
  SetBuff(wlresprm, addrRegdec);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BNP");

  var rs;

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьФайл("СБЩБННЕИСП");

  // Файл \ СБЩБННЕИСП
  StartBlockLogXML("СБЩБННЕИСП");

  rs = execSQLSelect(string("select pm.t_PayerAccount, ",
                                    "decode(pm.t_PartPaymNumMain, CHR(1), rm.t_Number, pm.t_PartPaymNumMain) t_Number, ",
                                    "rm.t_PayerINN, ",
                                    "decode(pm.t_PartPaymNumMain, CHR(1), rm.t_Date, pm.t_PartPaymDateMain) t_Date, ",
                                    "cast(decode(pm.t_PartPaymNumMain, CHR(1), NULL, pm.t_BaseAmount) as number(32,12)) t_PartAmount, ",
                                    "cast(decode(pm.t_PartPaymNumMain, CHR(1), pm.t_BaseAmount, (select pm2.t_BaseAmount from dpmpaym_dbt pm2, dpmlink_dbt lnk ",
                                         " where pm2.t_PaymentID = lnk.t_InitialPayment and lnk.t_PurposePayment = pm.t_PaymentID and lnk.t_LinkKind = ? and lnk.t_InitialPayment <> lnk.t_PurposePayment)) as number(32,12)) t_Amount, ",
                                    "pm.t_PayerBankID, ",
                                    "rm.t_PayerName"
                             " from dpmpaym_dbt pm, dpmrmprop_dbt rm where pm.t_PaymentID = ? and rm.t_PaymentID = pm.t_PaymentID"), makeArray(SQLParam("", PMLINK_KIND_KVITING), SQLParam("", wlresprm.PaymentID)));

  rs.moveNext();

  ЗаписатьПолеЛог("НомСообщ", string(wlresprm.Number));
  ЗаписатьПолеЛог("НомПоруч", rs.value("t_Number"));
  ЗаписатьПолеЛог("ДатаПоруч", YYYYMMDDXML(date(rs.value("t_Date"))));
  ЗаписатьПолеЛог("КодНОБ", GetCodeNOBWithChecks(rs.value("t_PayerBankID")));
  ЗаписатьПолеЛог("СумПлат", string(int(rs.value("t_Amount") * 100)));
  ЗаписатьПолеЛог("ИНННП", RemoveKPP(rs.value("t_PayerINN")));
  ЗаписатьПолеЛог("КППНП", RemoveINN(rs.value("t_PayerINN")));
  ЗаписатьПолеЛог("Плательщ", rs.value("t_PayerName"));

  var PartPaymAmount : money = wlresprm.PartPaymAmount;
  if( (PartPaymAmount == $0) and (ValType(rs.value("t_PartAmount")) != V_SPECVAL) )
    PartPaymAmount = rs.value("t_PartAmount");
  end;
  if(PartPaymAmount)
    ЗаписатьПолеЛог("СумЧаст", string( int(PartPaymAmount * 100) ));
  end;

  ЗаписатьПолеЛог("КодПричНеисп", ПолучитьКодПричНеисп(wlresprm.errCode));

  var wlerr = RsbWlError(wlresprm.ResultID);
  var wlerr_buf = TRecHandler("wlerror.dbt");
  if (not wlerr.First(wlerr_buf))
    ЗаписатьПолеЛог("ДопСвед", wlerr_buf.rec.Description);
  end;
  ЗаписатьПолеЛог("ДатаНапр", YYYYMMDDXML(date()));

  ЗаписатьПолеЛог("НомСч", rs.value("t_PayerAccount"));

  СведенияОБанке("СвБанк", wlmes.InsideAbonentID);

  ЗаписатьПредБанка("ПредБанка", wlmes.InsideAbonentID, false);

  FinishBlockLogXML("СБЩБННЕИСП");

  FinishBlockLogXML("Файл");

  // _НомерОтправ
  FnsWriteSendNum(OldMes);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
