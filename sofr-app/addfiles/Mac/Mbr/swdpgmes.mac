/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Вспомогательные функции для генерации сообщений SWIFT депозитария        */
/*                                                                          */
/*  Имя файла: swdpgmes.mac                                                 */
/*  Создан:  19.04.05                                    Фомченкова Л.Н.    */
/****************************************************************************/

import DPInter, "swgenmes.mac", CTInter;

macro ПолучитьВнутреннийКод( FIKind, PartyID, CodeKind )
      
   if(FIKind == OBJTYPE_AVOIRISS) 
      FIKind = OBJTYPE_FININSTR; /*такое уж содержание таблицы dobjkcode_dbt*/
   end;

   var rs:object = execSQLselect("SELECT t_shortname FROM dobjkcode_dbt WHERE t_objecttype= "+FIKind+" and t_codekind="+CodeKind+";");
   if( rs AND rs.moveNext() )
      return rs.value(0);
   end;
   return "";
end;

macro GetMessageFunction(msginf:object)   
   var error, res;
   res = GetllValueElement(OBJTYPE_MSGFUN, msginf.messageFunction, error);
   if ( error )
      RunError( "|Неверное значение функции сообщения" );
   end;
   return res;
end;

macro AlignNumberRight( val, len )
    var str=string(val);
    while( strlen(str)<len )
       str = "0"+str;
    end;
    return str;
end;

macro GetLikageType( val )
   var error, res;
   res = GetllValueElement(OBJTYPE_MSGLNKTP, val, error);
   if ( error )
     RunError( "|Не известный тип связки сообщений" );
   end;
   return res;
end;

macro GetTradePlaceType(msginf)
   var error, res;
   res = GetllValueElement(OBJTYPE_MSGTRDPL, msginf.TradePlaceType, error);
   if ( error )
      return "";
   end;
   return res;
end;

macro CreateIdentificationOfSecurities(msginf, isNDC)
   var res = "";
   var BondKind, Code="", Code_="", Name, error, standart;          
 
   RECORD fin(fininstr);
   RECORD Avr(avoiriss);

   ClearRecord (fin);
   ClearRecord (Avr);

   if((valtype(isNDC)==V_UNDEF) OR (isNDC==ST_UNDEF))
     isNDC = false;
   end;
   
   if ( ПолучитьФинИн(msginf.SecurityID, fin, Avr) )
      RunError( "|Не найдена ценная бумага" );
   end;

   if(isNDC == true)
     Code  = "NADC";
     Code_ = ПолучитьКодФинИн(msginf.SecurityID, error, 14/*FICK_NADC*/);
     if(error)
       RunError("|Не найден код ценной бумаги в НДС ");
     end;
     Name  = fin.Name;
     standart =    ST_RUR5;
   else  
     Code  = ПолучитьВнутреннийКод(OBJTYPE_AVOIRISS,msginf.ReceiverID,msginf.SecurityCodeKind);     
     Code_ = msginf.SecurityCode;
     Name  = fin.Name;
     standart = ST_UNDEF;
   end;

   StrChangeRusXSet( Code, Code, standart, true);   
   StrChangeRusXSet( Code_, Code_, standart, true);
   StrChangeRusXSet( Name, Name, standart, true);

   if( (Code!="") and (Code!=""))
   res = "/XX/CORP/" + Code + "/" + Code_ + "\n";
     if(Avr.LSIN != "")
       res = res + "/RU/" + Avr.LSIN + "\n";
     end;
     if(Name != "")
       res = res + "/NAME/" + Name;
     end;
   end;


   StrMultyToFormat( res, res, 35, 4 );

   if ( Avr.ISIN!="" )
      if( (isNDC == true) AND (GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(Avr, OBJTYPE_AVOIRISS), BOUND_KIND, BondKind) ) )

         if( (BondKind == 10) OR //субъектов федерации
             (BondKind == 13) OR //ОГВЗ          
             (BondKind == 18) OR //еврооблигации 
             (BondKind == 2 ) OR //муниципальные
             (BondKind == 16)    //корпоративные

           )
           res = "ISIN "+Avr.ISIN+"\n"+res;
         end;
      else
      res = "ISIN"+Avr.ISIN+"\n"+res;
   end;
   end;


   return res;

end;

macro GetSettlementTRNType(msginf)
   var error, res;
   res = GetllValueElement(OBJTYPE_MSGTRANS, msginf.SettlementTransactionType, error);
   if ( error )
     RunError( "|Неверное значение типа расчетной операции" );
   end;
   return res;
end;

macro GetMessageParty(msginf, type, kind)
   var msgParty;
   msginf.RewindMessageParty();

   msgParty = msginf.GetMessageParty();
   while( valtype(msgParty)!=V_UNDEF )
       if ( (msgParty.PartyType==type) and (msgParty.PartyKind==kind) )
          return msgParty;
       end;
       msgParty = msginf.GetMessageParty();
   end;
   return msgParty;
end;


macro ЗаписатьИдентификатораСтороны(msginf,kind,msgParty,isNDC)
   var IsSave=true, PartyCode, error, prefix, fldValue, type;
   if((valtype(isNDC)==V_UNDEF) or (isNDC==ST_UNDEF))
      isNDC = false;
   end;

   prefix = GetllValueElement(OBJTYPE_MSGPARTYTP, kind, error);
   if ( error )
      RunError("|Неизвестный идентификатор стороны");
   end;
   prefix = ":"+prefix+"/";
   if ( (kind==OBJTYPE_MSGPATYTP_EXCH) OR
        (kind==OBJTYPE_MSGPATYTP_MEOR) OR
        (kind==OBJTYPE_MSGPATYTP_MERE) OR
        (kind==OBJTYPE_MSGPATYTP_TRRE) OR
        (kind==OBJTYPE_MSGPATYTP_INVE) )
      type = DPMSGPAT_OTHER;
   elif( (kind==OBJTYPE_MSGPATYTP_ACCW) OR
         (kind==OBJTYPE_MSGPATYTP_BENM) OR
         (kind==OBJTYPE_MSGPATYTP_PAYE)
       )
      type = DPMSGPAT_CURRENCIES;
   else
      type = DPMSGPAT_SECURITIES;
   end;   

   if( not УстановитьКонтекстБлока( "95a" ) )
     RunError("|Ошибка при установке контекста блока 95a");
   end;

   if ( (valtype(msgParty)==V_UNDEF) OR (msgParty.PartyType!=kind) )
      msgParty = GetMessageParty(msginf, type, kind);
   end;
   if ( valtype(msgParty)!=V_UNDEF )
      if (( msgParty.PartyCodeKind==PTCK_SWIFT ) and (msgParty.PartyCode != ""))
         ЗаписатьПолеЛог( PartyPField, prefix+"/"+msgParty.PartyCode ) ;
      else
         PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_SWIFT, error );
         if ( not error )
             ЗаписатьПолеЛог( PartyPField, prefix+"/"+PartyCode );
         else
             if(isNDC == true)
               if( type == DPMSGPAT_CURRENCIES)
                 PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_BIC, error );
                 if ( not error )
                   ЗаписатьПолеЛог( PartyQField, prefix+"/"+PartyCode );
                 else
                   if ( msgParty.PartyName!="" )
                      StrChangeRusXSet( msgParty.PartyName, fldValue, ST_RUR5, true );
                      StrMultyToFormat( fldValue, fldValue, 35, 4 );
                      ЗаписатьПолеЛог( PartyQField, prefix+"/"+fldValue );
                   else
                      IsSave = false;
                   end;
                 end;               
               else
                 PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_NDC, error );
                 if ( not error )
                   ЗаписатьПолеЛог( PartyQField, prefix+"/"+"XX/CORP/NADC/"+PartyCode );
                 else
                   if ( msgParty.PartyName!="" )
                      StrChangeRusXSet( msgParty.PartyName, fldValue, ST_RUR5, true );
                      StrMultyToFormat( fldValue, fldValue, 35, 4 );
                      ЗаписатьПолеЛог( PartyQField, prefix+"/"+fldValue );
                   else
                      IsSave = false;
                   end;
                 end;
               end;
             else
             if ( msgParty.DepoPartyCode!="" )                  
                ЗаписатьПолеЛог( PartyQField, prefix+"/"+"XX/CORP/"+
                                 ПолучитьВнутреннийКод(OBJTYPE_PARTY,msginf.ReceiverID,msgParty.DepoPartyCodeKind)+
                                 "/"+msgParty.DepoPartyCode );
             else
                if ( msgParty.PartyName!="" )
                   StrChangeRusXSet( msgParty.PartyName, fldValue );
                   StrMultyToFormat( fldValue, fldValue, 35, 4 );
                   ЗаписатьПолеЛог( PartyQField, prefix+"/"+fldValue );
                else
                   IsSave = false;
                end;
             end;
         end;
      end;
      end;
   else
      IsSave = false;
   end;

   if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока ..");
   end;

   setparm(2,msgParty);

   return IsSave;
end;

macro ЗаписатьСчетИРазделИдентификатораСтороны(msginf,kind,msgParty)
   var IsSave=true, type, pref;

   if( not УстановитьКонтекстБлока( "97a" ) )
     RunError("|Ошибка при установке контекста блока 97a");
   end;

   if( (kind==OBJTYPE_MSGPATYTP_ACCW) OR
       (kind==OBJTYPE_MSGPATYTP_BENM) OR
       (kind==OBJTYPE_MSGPATYTP_PAYE)
     )
      type = DPMSGPAT_CURRENCIES;
      pref = ":CASH//";
   else
      type = DPMSGPAT_SECURITIES;
      pref = ":SAFE//";
   end;   

   if ( (valtype(msgParty)==V_UNDEF) OR (msgParty.PartyType!=kind) )
      msgParty = GetMessageParty(msginf, type, kind);
   end;
   if ( valtype(msgParty)!=V_UNDEF )
      if ( (msgParty.Account!="") and (msgParty.Partition!="") )
         ЗаписатьПолеЛог( AccountAField, pref+msgParty.Account+"/KRZD/"+msgParty.Partition );
      elif ( msgParty.Account!="" )
         ЗаписатьПолеЛог( AccountAField, pref+msgParty.Account );
      else
         IsSave = false;
      end;
   else
      IsSave = false;
   end;

   if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока ..");
   end;

   setparm(2,msgParty);

   return IsSave;
end;


file llval(llvalues) key 0;


macro ЗаписатьПереченьДокументов( msginf, isNDC )
   var IsSave = false, fieldValue="", fieldValue2, msgdoc, i, standart = ST_UNDEF;   

   if((valtype(isNDC)==V_UNDEF) OR (isNDC==ST_UNDEF))
     isNDC = false;
   elif(isNDC == true)
     standart = ST_RUR5;
   end;

   msginf.RewindMessageDocument();
   msgdoc = msginf.GetMessageDocument();
   while( valtype(msgdoc)!=V_UNDEF )
      llval.List = OBJTYPE_KINDDOC;
      llval.Element = msgdoc.DocumentKind;
      if ( not getEQ(llval) )
          RunError("|Не найдена запись в справочнике видов документов");
      end;
      if ( fieldValue!="" )
         fieldValue = fieldValue + "\n";
      end;

      if(isNDC)
        StrChangeRusXSet( msgdoc.DocumentNumber, fieldValue2, standart, true );
      else
        StrChangeRusXSet( msgdoc.DocumentNumber, fieldValue2, standart );
      end;

      fieldValue = fieldValue + "/DOCN/"+fieldValue2;      
      if ( msgdoc.SignedDate!=date(0,0,0) )
         fieldValue = fieldValue + "\n/DOCD/" + GetSWIFTDateFull(msgdoc.SignedDate);
      end;
      if ( msgdoc.DocumentNumber!="" )
         if(isNDC)
           StrChangeRusXSet( llval.Name, fieldValue2, standart, true );
         else
           StrChangeRusXSet( llval.Name, fieldValue2, standart );
         end;

         fieldValue2 = "/DOCM/" + fieldValue2;
         StrMultyToFormat( fieldValue2, fieldValue2, 33, 9 );
         i = 0;
         while( i<strlen(fieldValue2) )
            if( substr(fieldValue2,i,1)=="\n" )
                fieldValue2 = substr( fieldValue2, 1,i ) + "//" + substr( fieldValue2, i+1 );
                i = i+2;
            end;
            i = i+1;
         end;
         fieldValue = fieldValue + "\n" + fieldValue2;
      end;
      msgdoc = msginf.GetMessageDocument();
   end;

   if ( fieldValue!="" )
      if( not УстановитьКонтекстБлока( "70a" ) )
        RunError("|Ошибка при установке контекста блока 70a");
      end;   
      
      StrMultyToFormat( fieldValue, fieldValue, 35, 9 );
      if(isNDC)
        ЗаписатьПолеЛог( FIANarrativeEField, ":DECL//XX/CORP/NADC/OSNV/\n"+fieldValue );
                                                         
      else  
      ЗаписатьПолеЛог( FIANarrativeEField, ":DECL//XX/CORP/"+
                       ПолучитьВнутреннийКод(OBJTYPE_AVOIRISS,msginf.ReceiverID,msginf.SecurityCodeKind)+
                       "/OSNV/\n"+fieldValue );
      
      end;
      IsSave = true;

      if( not УстановитьКонтекстБлока( ".." ) )
         RunError("|Ошибка при установке контекста блока ..");
      end;
   end;

   return IsSave;
end;

/* возможно ли заполнить AMT*/
macro FillSumForAmountField( msginf )
  ClearRecord(f_wlfininstr);
  f_wlfininstr.FIID = msginf.SettlementAmountCurrency;  
  if( getEQ( f_wlfininstr ) And msginf.SettlementAmount )
    return true;
  end;
end;
/* Записать в сообщение значения суммы сделки и суммы проведенных расчетов */
macro FillSumOfAmountAField( msginf, НадоРугаться, isNDC )

  ClearRecord(f_wlfininstr);
  f_wlfininstr.FIID = msginf.SettlementAmountCurrency;
  
  if((valtype(isNDC)==V_UNDEF) or (isNDC == ST_UNDEF))
    isNDC = false;
  end;

  if( getEQ( f_wlfininstr ) And msginf.SettlementAmount )
     if(isNDC == true)
       ЗаписатьПолеЛог( SumOfAmountAField, ":SETT//"+ f_wlfininstr.Ccy + msginf.SettlementAmount ) ;
     else
     ЗаписатьПолеЛог( SumOfAmountAField, ":SETT//"+ f_wlfininstr.Ccy + GetSWIFTAmount(msginf.SettlementAmount) ) ;
     end;
  elif( (not getEQ( f_wlfininstr )) And (not msginf.SettlementAmount) )

     if(НадоРугаться) RunError("| Поле 19a (сумма проведенных расчетов) должно быть заполнено"); end;
  else
     RunError("|Несоответствие валюты и суммы проведенных расчетов при записи поля 19a");
  end;

end;

/* Получить сумму в формате НДЦ */
macro GetNDCAmount( sum )
  var Amount, pos;
  Amount = string( sum );
  pos = index( Amount, "." );
  strset( Amount, pos, ",");
  if( int( substr( Amount, pos+1 ) ) == 0 )
    Amount = substr( Amount, 1, pos+1 );
  end;

  return Amount;
end;