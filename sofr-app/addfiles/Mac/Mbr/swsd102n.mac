/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по SWIFT-SB MT102N                                    */
/*                                                                          */
/*  Имя файла: swsd102n.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac", "wlchmes.mac", "pmlib.mac";

/* Последовательности сообщения МТ102 */
const BLOCK_A = "A",
      BLOCK_B = "B",
      BLOCK_C = "C";

var PaymentList = TArray;

file PaymMulty(pmpaym) key 6;

/* Детали транзакции - собственно сами платежи */
class TransactionDetails
 var
  Reference         :string,      /* Ссылочный номер операции */
  Currency          :string,      /* Код валюты */
  InstrCurrency     :string,      /* Код валюты актива */
  Amount            :moneyl,      /* Сумма перевода */
  InstrAmount       :moneyl,      /* Сумма актива */
  OrdCustomer       :Customer,    /* Приказодатель */
  OrdBank           :Bank,        /* Банк Приказодателя */
  AccBank           :Bank,        /* Банк-держатель счета */
  Trans             :bool,        /* Признак транзитного */
  BnfCustomer       :Customer,    /* Бенефициар */
  BnfInfo           :TBnfInfo,    /* Информация Бенефициару */
  ComissCharges     :integer,     /* Расходы корреспондентов */
  ExchangeRate      :doubleL,     /* Курс пересчета */
  SendersCharges    = TArray,     /* Расходы отправителя */
  ReceiversCharges  :TCharges;    /* Расходы получателя */

  ComissCharges = PM_CHRG_OUR;

  Reference     = "";
  Amount        = $0;
  InstrAmount   = $0;
  InstrCurrency = "";
  SendersCharges.Size = 0;  
  ReceiversCharges = TCharges(0,0);
  ExchangeRate = 0;

  macro IsSet()
    return NOT((Reference == "") AND (Amount == $0));
  end;

  macro GetSendersChargesSize
    return SendersCharges.Size;
  end;
end;

/* Платежная инструкция "Сводный клиентский перевод" */
class MultipleCustomerCreditTransfer
 var
  ContextBlock      :string,             /* Контекст блока (последовательности) */
  TRF               :string,             /* Ссылочный номер операции */
  BankOperationCode :string,             /* Банковский операционный код */
  ValueDate         :date,               /* Дата валютирования */
  Currency          :string,             /* Код валюты */
  Sum               :moneyl,             /* Сумма перевода */
  OrdCustomer       :Customer,           /* Приказодатель */
  OrdBank           :Bank,               /* Банк Приказодателя */
  Sender            :Bank,               /* Отправитель */
  SndCorresp        :Bank,               /* Агент Отправителя */
  RcvCorresp        :Bank,               /* Агент Получателя */
  ComissCharges     :integer,            /* Расходы корреспондентов */
  ExchangeRate      :doubleL,            /* Курс пересчета */
  Payment           :TransactionDetails, /* Детали транзакции */
  SendersCharges    :TCharges,           /* Расходы отправителя */
  ReceiversCharges  :TCharges,           /* Расходы получателя */
  RcvInfo           :TRcvInfo100;        /* Информация Получателю */

  ExchangeRate      = 0;
  ComissCharges     = PM_CHRG_OUR;
end; /* class MultipleCustomerCreditTransfer */

var MT102 : MultipleCustomerCreditTransfer;

macro FillSN(TRN)
  return true;
end;

macro Fill19(Sum)
  return TRUE;
end;

macro Fill20(TRF)
  MT102.TRF = TRF;
  return TRUE;
end;

macro Fill21(TRF)
  MT102.Payment.Reference = TRF;
  return TRUE;
end;

macro Fill23(Code)
  MT102.BankOperationCode = Code;
  return TRUE;
end;

macro Fill26T(Code)
  return TRUE;
end;

macro Fill32A(Date, Cur, Sum)
  MT102.ValueDate = Date;
  MT102.Currency  = Cur;
  MT102.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill32B(Cur, Sum)
  MT102.Payment.Currency = Cur;
  MT102.Payment.Amount   = moneyl(Sum);
  return TRUE;
end;

macro Fill33B(Cur, Sum)
  MT102.Payment.InstrCurrency = Cur;
  MT102.Payment.InstrAmount   = moneyl(Sum);
  return TRUE;  
end;

macro Fill36(Rate)
  if (MT102.ContextBlock == BLOCK_A)
    MT102.ExchangeRate = Rate;
  else
    MT102.Payment.ExchangeRate = Rate;
  end;
  return TRUE;
end;

macro Fill50A(BIC, Account, System, Code)
  if (MT102.ContextBlock == BLOCK_A)
    MT102.OrdCustomer = Customer("", Account, BIC);
  else
    MT102.Payment.OrdCustomer = Customer("", Account, BIC);
  end;
  return TRUE;
end;

macro Fill50K(Address, Account, System, Code, INN)
  if (MT102.ContextBlock == BLOCK_A)
    MT102.OrdCustomer = Customer(Address, Account, "", INN);
  else
    MT102.Payment.OrdCustomer = Customer(Address, Account, "", INN);
  end;
  return TRUE;
end;

macro Fill52A(BIC, Account, System, Code)
  if( MT102.ContextBlock == BLOCK_A )
    MT102.OrdBank = Bank(BIC, "", "", Account, System, Code);
  else
    MT102.Payment.OrdBank = Bank(BIC, "", "", Account, System, Code );
  end;
  return TRUE;
end;

macro Fill52B(Branch, Account, System, Code)
  if( MT102.ContextBlock == BLOCK_A )
    MT102.OrdBank = Bank("", "", Branch, Account, System, Code);
  else
    MT102.Payment.OrdBank = Bank("", "", "", Account, "", "");
  end;
  return TRUE;
end;
            
macro Fill52C(Account, System, Code)
  if( MT102.ContextBlock == BLOCK_A )
    MT102.OrdBank = Bank("", "", "", Account, System, Code);
  else
    MT102.Payment.OrdBank = Bank("", "", "", Account, System, Code);
  end;
  return TRUE;
end;

macro Fill53A(BIC, Account, System, Code)
  MT102.SndCorresp = Bank(BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill53C(Account, System, Code)
  MT102.SndCorresp = Bank("", "", "", Account, System, Code);
  return TRUE;
end;

macro Fill54A(BIC, Account, System, Code)
  MT102.RcvCorresp = Bank(BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill57A(BIC, Account, System, Code)
  MT102.Payment.AccBank = Bank(BIC, "", "", Account, System, Code);
  return TRUE;
end;

macro Fill57C(Account, System, Code)
  MT102.Payment.AccBank = Bank("", "", "", Account, System, Code);
  return TRUE;
end;

macro Fill59(Address, Account, System, Code, INN)
  MT102.Payment.BnfCustomer = Customer(Address, Account, "", INN);
  return TRUE;
end;

macro Fill59A(BIC, Account, System, Code)
  MT102.Payment.BnfCustomer = Customer("", Account, BIC);
  return TRUE;
end;

macro Fill70(Str)
  MT102.Payment.BnfInfo = TBnfInfo(Str);
  return TRUE;
end;

macro Fill71A(ComissCharges )
  if (MT102.ContextBlock == BLOCK_A)
    MT102.ComissCharges = ComissCharges;
  else
    MT102.Payment.ComissCharges = ComissCharges;
  end;
  return TRUE;
end;

macro Fill71F(Cur, Sum)
  if (MT102.ContextBlock == BLOCK_B)
    MT102.Payment.SendersCharges[MT102.Payment.GetSendersChargesSize] = TCharges(Cur, Sum);
  else
    MT102.SendersCharges = TCharges(Cur, Sum);
  end;
  return TRUE;
end;

macro Fill71G(Cur, Sum)
  if (MT102.ContextBlock == BLOCK_B)
    MT102.Payment.ReceiversCharges = TCharges(Cur, Sum, {OurBank});
  else
    MT102.ReceiversCharges = TCharges(Cur, Sum);
  end;
  return TRUE;
end;

macro Fill13C(Code, GMT:TGMT)
  return TRUE;
end;

macro Fill72(Str, Narrative)
  MT102.RcvInfo = TRcvInfo100(Str, Narrative);
  return TRUE;
end;

macro Fill77B(Str)
  return TRUE;
end;

macro SetBlockA() MT102.ContextBlock = BLOCK_A; return TRUE; end;
macro SetBlockB() MT102.ContextBlock = BLOCK_B; return TRUE; end;
macro SetBlockC() MT102.ContextBlock = BLOCK_C; return TRUE; end;

macro AddPaymentToList
  var Obj:TransactionDetails;

  if (MT102.Payment.IsSet())
    Obj = MT102.Payment;
    PaymentList(PaymentList.Size) = Obj;
    MT102.Payment = TransactionDetails();
  end;
  return TRUE;
end;

/* Заполнение списка полей формы  MT102*/
var FldSN   = ТПолеФормы(УникальныйНомерСообщенияСМФР,         FIELD_MANDATORY, "ReadTRF", "FillSN",  "SetBlockA"),
    Fld20   = ТПолеФормы(TransactionReferenceNumberField,      FIELD_MANDATORY, "ReadTRF", "Fill20",  ""),
    Fld23   = ТПолеФормы(BankOperationCodeField,               FIELD_MANDATORY, "ReadTRF", "Fill23",  ""),    
    Fld50AA = ТПолеФормы(OrderingCustomerField_A,              FIELD_OPTIONAL,  "ReadA",   "Fill50A", ""),
    Fld50KA = ТПолеФормы(OrderingCustomerField_K,              FIELD_OPTIONAL,  "ReadD",   "Fill50K", ""),
    Fld52AA = ТПолеФормы(OrderingInstitutionField_A,           FIELD_OPTIONAL,  "ReadA",   "Fill52A", ""),
    Fld52BA = ТПолеФормы(OrderingInstitutionField_B,           FIELD_OPTIONAL,  "ReadB",   "Fill52B", ""),
    Fld52CA = ТПолеФормы(OrderingInstitutionField_C,           FIELD_OPTIONAL,  "ReadC",   "Fill52C", ""),
    Fld26T  = ТПолеФормы(TransactionTypeCodeField,             FIELD_OPTIONAL,  "ReadType","Fill26T", ""),    
    Fld77B  = ТПолеФормы(RegulatoryReportingField,             FIELD_OPTIONAL,  "Read77B", "Fill77B", ""),
    Fld71A  = ТПолеФормы(DetailsOfChargesField_A,              FIELD_OPTIONAL,  "Read71A", "Fill71A", ""),
    Fld36   = ТПолеФормы(ExchangeRateField,                    FIELD_OPTIONAL,  "ReadRate","Fill36",  ""),    

    Fld21   = ТПолеФормы(RelatedReferenceField,                FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "SetBlockB"),
    Fld32B  = ТПолеФормы(ValueDateCurrencyCodeAmountField_B,   FIELD_OPTIONAL,  "Read32B", "Fill32B", ""),
    Fld57A  = ТПолеФормы(AccountWithInstitutionField_A,        FIELD_OPTIONAL,  "ReadA",   "Fill57A", ""),
    Fld57C  = ТПолеФормы(AccountWithInstitutionField_C,        FIELD_OPTIONAL,  "ReadC",   "Fill57C", ""),
    Fld59   = ТПолеФормы(BeneficiaryCustomerField,             FIELD_OPTIONAL,  "ReadD",   "Fill59",  ""),
    Fld59A  = ТПолеФормы(BeneficiaryCustomerField_A,           FIELD_OPTIONAL,  "ReadA",   "Fill59A", ""),
    Fld70   = ТПолеФормы(DetailsOfPaymentField,                FIELD_OPTIONAL,  "Read70",  "Fill70",  ""),
    Fld26TB = ТПолеФормы(TransactionTypeCodeField,             FIELD_OPTIONAL,  "ReadType","Fill26T", ""),    
    Fld77BB = ТПолеФормы(RegulatoryReportingField,             FIELD_OPTIONAL,  "Read77B", "Fill77B", ""),
    Fld33B  = ТПолеФормы(CurrencyOriginalOrderedAmountField,   FIELD_OPTIONAL,  "Read32B", "Fill33B", ""),    
    Fld71AB = ТПолеФормы(DetailsOfChargesField_A,              FIELD_OPTIONAL,  "Read71A", "Fill71A", ""),
    Fld71FB = ТПолеФормы(SendersChargesField,                  FIELD_OPTIONAL,  "Read32B", "Fill71F", ""),
    Fld71GB = ТПолеФормы(ReceiversChargesField,                FIELD_OPTIONAL,  "Read32B", "Fill71G", ""),
    Fld36B  = ТПолеФормы(ExchangeRateField,                    FIELD_OPTIONAL,  "ReadRate","Fill36",  "AddPaymentToList"),

    Fld32A  = ТПолеФормы(ValueDateCurrencyCodeAmountField_A,   FIELD_MANDATORY, "Read32A", "Fill32A", "SetBlockC"),
    Fld19   = ТПолеФормы(SumOfAmountField,                     FIELD_OPTIONAL,  "ReadSum", "Fill19",  ""),
    Fld71GC = ТПолеФормы(ReceiversChargesField,                FIELD_OPTIONAL,  "Read32B", "Fill71G", ""),
    Fld53A  = ТПолеФормы(Sender_sCorrespondentField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill53A", ""),
    Fld53C  = ТПолеФормы(Sender_sCorrespondentField_C,         FIELD_OPTIONAL,  "ReadC",   "Fill53C", ""),
    Fld54A  = ТПолеФормы(Receiver_sCorrespondentField_A,       FIELD_OPTIONAL,  "ReadA",   "Fill54A", ""),
    Fld72   = ТПолеФормы(SenderToReceiverInformationField,     FIELD_OPTIONAL,  "Read72",  "Fill72",  "");

macro ConstructMT102(RespID)
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode, CountBlockB = 0;

  /* Последовательно считываем поля и заполняем лексемы */
  while(loop)
    fld = ПоляФормы.Value(count);    

    if (wasRead AND (not СчитатьПоле(field_name, field_value)))
      loop = 0;
    else
      /*MsgBox( "field_name :", field_name, " fld.Name: ", fld.Name);*/
      if (fld.Name == RelatedReferenceField)
        CountBlockB = count;
      end;

      if ((field_name != fld.Name))
        if (fld.ПолеОбязательно)
          std.msg("Не указано обязательное поле " + fld.Name);
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          if ((fld.Name == ExchangeRateField) AND (MT102.ContextBlock == BLOCK_B))
            if (MT102.Payment.IsSet())
              count = CountBlockB - 1;
            end;
            if (fld.ВыполнитьФункцияБлока)
            else
              std.msg("Ошибка при выполнении функции блока " + fld.Name);
              result = FALSE;
              loop = 0;
            end;
          end;
          wasRead = 0;
        end;
      else
        if (fld.ОбработатьПолеФормы(field_value))
          if (fld.ВыполнитьФункцияБлока)
            if ((fld.Name == ExchangeRateField) AND (MT102.ContextBlock == BLOCK_B))
              count = CountBlockB - 1;
            elif ((fld.Name == SendersChargesField) OR (fld.Name == TimeIndicationField))
              count = count - 1;
            end;
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  if (not MT102.Sender.IsSet)
    BankCode = ПолучитьКодСубъекта(RespID, PTCK_SMFR, error);
    MT102.Sender = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);
  end;

  return TRUE;
end; /* ConstructMT102 */

macro GenDoc(addrMes)
  var error, arsize, i = 0, Obj:TransactionDetails, FIID, count;
  MT102 = MultipleCustomerCreditTransfer();
  PaymentList = TArray;

  SetBuff(wlmes, addrMes);

  PrintLog(2,"Генерация платежей по МТ102N");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  if (not ConstructMT102(wlmes.OutsideAbonentID))
    return FALSE;
  end;

  arsize = PaymentList.Size;
  if (arsize == 0)
    std.msg("В сообщении не найдено платежей");
    return FALSE;
  end;

  /* Данные, общие для всех платежей */
  if (not ПолучитьФинИнПоISO(MT102.Currency, wlfininstr))
    std.msg("Не определена валюта платежа по коду ISO: " +  MT102.Currency);
    return FALSE;
  else
    FIID = wlfininstr.FIID;
  end;

  /* В ЦАБС платежи не могут исполняться в неоткрытом операционном дне */
  if( MT102.ValueDate == date(0,0,0) )
    MT102.ValueDate = {curdate}; 
  end;

  /* Проходим по списку и вставляем платежи */
  while(i < arsize)
    ClearRecord(wlpmpaym);
    ClearRecord(wlpmrmprop);  
    InitPMPROP(wlpmpropdeb);
    InitPMPROP(wlpmpropcred);
  
    Obj = PaymentList(i);

    /* Заполнение учетных буферов */
    Obj.Trans = Obj.AccBank.IsSet(); /* документ транзитный - если указан банк бенефициара */

    /* Реквизиты плательщика */
    if (MT102.OrdCustomer.IsSet())
      wlpmpaym.PayerAccount  = MT102.OrdCustomer.Account;
      wlpmpaym.PayerCode     = MT102.OrdCustomer.Code;
      wlpmpaym.PayerCodeKind = MT102.OrdCustomer.CodeKind;
    else
      wlpmpaym.PayerAccount  = Obj.OrdCustomer.Account;
      wlpmpaym.PayerCode     = Obj.OrdCustomer.Code;
      wlpmpaym.PayerCodeKind = Obj.OrdCustomer.CodeKind;
    end;

    /* Реквизиты получателя */
    wlpmpaym.Payer = -1;
    wlpmpaym.ReceiverAccount  = Obj.BnfCustomer.Account;
    wlpmpaym.ReceiverCode     = Obj.BnfCustomer.Code;
    wlpmpaym.ReceiverCodeKind = Obj.BnfCustomer.CodeKind;

    wlpmpaym.PayAmount = Obj.Amount;
    wlpmpaym.PayFIID   = FIID;
    wlpmpaym.ValueDate = MT102.ValueDate;

    /* Дебет: сумма и валюта */
    wlpmpaym.FIID   = FIID;
    wlpmpaym.Amount = Obj.Amount;

    wlpmpaym.BaseFIID   = FIID;
    wlpmpaym.BaseAmount = Obj.Amount;

    /* Для платежей с конверсией */
    if (Obj.InstrCurrency != "")
      if (not ПолучитьФинИнПоISO(Obj.InstrCurrency, wlfininstr))
        std.msg("Не определена валюта инструкции по коду ISO: " +  Obj.InstrCurrency);
        return FALSE;
      end;
      wlpmpaym.OrderFIID   = wlfininstr.FIID;
      wlpmpaym.OrderAmount = Obj.InstrAmount;
    else
      wlpmpaym.OrderFIID   = FIID;
      wlpmpaym.OrderAmount = Obj.Amount;
    end;
    
    /* Банк плательщика */
    if( MT102.OrdBank.IsSet() )
      wlpmpaym.PayerBankID = MT102.OrdBank.PartyID;
    elif( Obj.OrdBank.IsSet() )
      wlpmpaym.PayerBankID = Obj.OrdBank.PartyID;
    else
      wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;
    end;

    /* Дебетовые свойства платежа */
    if( wlpmpaym.ValueDate==date(0,0,0) )
      wlpmpropdeb.TransferDate = {curdate};
    else
      wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
    end;
    wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;

    if( MT102.OrdBank.IsSet() )
      wlpmpropdeb.CodeKind     = MT102.OrdBank.CodeKind;
      wlpmpropdeb.BankCode     = MT102.OrdBank.CodeBank;
      wlpmpropdeb.CorrCodeKind = MT102.Sender.CodeKind;
      wlpmpropdeb.CorrCode     = MT102.Sender.CodeBank;
      wlpmpropdeb.CorrAcc      = MT102.Sender.Account;
      wlpmpropdeb.CorrID       = MT102.Sender.PartyID;
    elif( Obj.OrdBank.IsSet() )
      wlpmpropdeb.CodeKind     = Obj.OrdBank.CodeKind;
      wlpmpropdeb.BankCode     = Obj.OrdBank.CodeBank;
      wlpmpropdeb.CorrCodeKind = MT102.Sender.CodeKind;
      wlpmpropdeb.CorrCode     = MT102.Sender.CodeBank;
      wlpmpropdeb.CorrAcc      = MT102.Sender.Account;
      wlpmpropdeb.CorrID       = MT102.Sender.PartyID;
    else
      wlpmpropdeb.CodeKind = MT102.Sender.CodeKind;
      wlpmpropdeb.BankCode = MT102.Sender.CodeBank;

      if ( MT102.SndCorresp.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT102.SndCorresp.CodeKind;
        wlpmpropdeb.CorrCode     = MT102.SndCorresp.CodeBank;
        wlpmpropdeb.CorrID       = MT102.SndCorresp.PartyID;
      end;
    end;    

    if ( MT102.SndCorresp.IsSet() OR MT102.RcvCorresp.IsSet() )
       if( MT102.OrdBank.IsSet() OR Obj.OrdBank.IsSet() )
           wlpmpropdeb.InstructionAbonent = INS_ABONENT_CORR;
       else
           wlpmpropdeb.InstructionAbonent = INS_ABONENT_BANK;
       end;
    end;

    if ( MT102.RcvCorresp.IsSet() )
       wlpmpropdeb.OurCorrCodeKind = MT102.RcvCorresp.CodeKind;
       wlpmpropdeb.OurCorrCode     = MT102.RcvCorresp.CodeBank;
       wlpmpropdeb.OurCorrID       = MT102.RcvCorresp.PartyID;
    elif ( (MT102.SndCorresp.IsSet()) AND 
           (not MT102.RcvCorresp.IsSet()) AND
           (MT102.SndCorresp.PartyID!=wlpmpropdeb.CorrID) )
       wlpmpropdeb.OurCorrCodeKind = MT102.SndCorresp.CodeKind;
       wlpmpropdeb.OurCorrCode     = MT102.SndCorresp.CodeBank;
       wlpmpropdeb.OurCorrID       = MT102.SndCorresp.PartyID;
    end;

    /* Реквизиты платежа, характерные для R-макета */
    wlpmrmprop.Number                = Obj.Reference;
    wlpmrmprop.PayDate               = MT102.ValueDate;
    wlpmrmprop.Date                  = MT102.ValueDate;
    wlpmrmprop.ClientDate            = MT102.ValueDate;
    if( MT102.OrdCustomer.IsSet() )      
      wlpmrmprop.PayerName           = MT102.OrdCustomer.Name;
      wlpmrmprop.PayerINN            = MT102.OrdCustomer.INN;
    else
      wlpmrmprop.PayerName           = Obj.OrdCustomer.Name;
      wlpmrmprop.PayerINN            = Obj.OrdCustomer.INN;
    end;
    if ( MT102.OrdBank.IsSet() )
       wlpmrmprop.PayerBankName      = MT102.OrdBank.Name;
       wlpmrmprop.PayerCorrAccNostro = MT102.OrdBank.Account;       
       wlpmrmprop.PayerCorrBankName     = MT102.Sender.Name;     
    elif ( Obj.OrdBank.IsSet() )
       wlpmrmprop.PayerBankName      = Obj.OrdBank.Name;
       wlpmrmprop.PayerCorrAccNostro = Obj.OrdBank.Account;
       wlpmrmprop.PayerCorrBankName     = MT102.Sender.Name;      
    else
       wlpmrmprop.PayerBankName      = MT102.Sender.Name;
       wlpmrmprop.PayerCorrAccNostro = MT102.Sender.Account;
    end;
    wlpmrmprop.ReceiverName          = Obj.BnfCustomer.Name;
    wlpmrmprop.ReceiverINN           = Obj.BnfCustomer.INN;
    wlpmrmprop.Priority              = PM_DefaultMaxPriority();
    wlpmrmprop.PartyInfo             = MT102.RcvInfo.REC;
    wlpmrmprop.Ground                = Obj.BnfInfo.Text;
    wlpmrmprop.PaymentKind           = "Э";
    wlpmrmprop.ProcessKind           = " 1";
    wlpmrmprop.ShifrOper             = "01";

    wlpmrmprop.ComissCharges        = Obj.ComissCharges;

    /* Записываем полное содержимое поля */
    wlpmrmprop.AdditionalInfo       = MT102.RcvInfo.Str;
    
    /* Кредитовые свойства платежа */
    if( wlpmpaym.ValueDate==date(0,0,0) )
      wlpmpropcred.TransferDate = {curdate};
    else
      wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
    end;
    wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;
    wlpmpropcred.CodeKind = Obj.AccBank.CodeKind;
    wlpmpropcred.BankCode = Obj.AccBank.CodeBank;

    if( Obj.Trans )
      wlpmpaym.ReceiverBankID = Obj.AccBank.PartyID;
      wlpmrmprop.ReceiverBankName = Obj.AccBank.Name;
      wlpmrmprop.ReceiverCorrAccNostro = Obj.AccBank.Account;
    else
      if( wlpmpropcred.BankCode != "" )
        // получаем ID банка по коду
        wlpmpaym.ReceiverBankID = ПолучитьКодСубъекта( wlpmpropcred.BankCode, wlpmpropcred.CodeKind, error );
      else
        // определяем банк по счету получателя
        wlpmpaym.ReceiverBankID = GetOwnerID( wlpmpaym.PayFIID, wlpmpaym.ReceiverAccount );
      end;
      
      if( wlpmpaym.ReceiverBankID == 0 )
        wlpmpaym.ReceiverBankID = {OurBank};
        wlpmrmprop.ReceiverBankName = {Name_Bank};
      else
        ПолучитьНаименованиеКлиента( wlpmpaym.ReceiverBankID, wlpmrmprop.ReceiverBankName ); 
      end;
    end;
    wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropdeb.Corschem != -1)
      wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
    
    if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
      std.msg("Ошибка при сохранении платежа");
      return FALSE;
    end;

    var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

    /* Узел Приказодателя - A,D */
    if( MT102.OrdBank.IsSet() )
      PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT102.OrdBank.GetRoute() );
    else
      PaymentObj.Routes.InsertRouteNode( RTDIR_IN, Obj.OrdBank.GetRoute() );
    end;
    /* Узел Отправителя - A */
    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT102.Sender.GetRoute(), true );
    /* Узел Корреспондента Отправителя - A,B,D */
    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT102.SndCorresp.GetRoute() );
    /* Узел Корреспондента Получателя - A,B,D */
    PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT102.RcvCorresp.GetRoute() );
    /* Узел Банка Бенефициара - A,B,D */
    PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, Obj.AccBank.GetRoute() );

    if( PaymentObj.Update() )
      std.msg("Ошибка при сохранении платежа");
      return FALSE;
    end;

    count = 0;
    while(count<Obj.GetSendersChargesSize)
      if (not Obj.SendersCharges[count].InsertCharges)
         std.msg("Ошибка при сохранении расходов отправителя");
         return FALSE;
      end;
      count = count + 1;
    end;

    if ( Obj.ReceiversCharges.Amount!=$0 )
       if ( not Obj.ReceiversCharges.InsertCharges )
         std.msg("Ошибка при сохранении расходов получателя");
         return FALSE;
       end;
    end;
    
    i = i + 1;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

/* Ключевой фильтр на сводные платежи */
macro OneStepMultyPaym( FirstDocMulty, PaymentID )
  if( FirstDocMulty )
     return ( (GetEQ( PaymMulty ))  and
              (PaymMulty.SubSplittedPayment == PaymentID) and
              (PaymMulty.CreatedInSS >= 0)
            );
  else
     return ( (Next( PaymMulty )) and
              (PaymMulty.SubSplittedPayment == PaymentID) and
              (PaymMulty.CreatedInSS >= 0)
            );
  end;
end;

class TDataPaym( _PayerAccount, _ReceiverAccount, _PayAmount, _ValueDate, _PayFIID )
    var PayerAccount, ReceiverAccount, PayAmount, ValueDate, PayFIID;

    PayerAccount = _PayerAccount;
    ReceiverAccount = _ReceiverAccount;
    PayAmount = _PayAmount;
    ValueDate = _ValueDate;
    PayFIID = _PayFIID;

end;

macro CheckMes( addrMes )
  var IsFirst = true, i, j, IsFind, OrdCustomer:Customer;
  var Obj:TransactionDetails;
  var payments = TArray;
  SetBuff( wlmes, addrMes );

  if( not ConstructMT102( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if ( not FillBuffPayment(wlmes) )
     std.msg( "Не найдена связь сообщения с платежем" );
     return false;
  end;

  if ( not CheckMessagesPayment(wlpm) )
     std.msg( "Платежу соответствует несколько одинаковых сообщений|Откатите выгрузку в МБР данного платежа и попробуйте снова." );
     return false;
  end;

  PaymMulty.SubSplittedPayment = wlpmpaym.PaymentID;
  PaymMulty.CreatedInSS = 1;   
  while( OneStepMultyPaym( IsFirst, wlpmpaym.PaymentID ) )
     payments(payments.size) = TDataPaym( PaymMulty.PayerAccount, PaymMulty.ReceiverAccount, PaymMulty.PayAmount, PaymMulty.ValueDate, PaymMulty.PayFIID );
     IsFirst = false;
  end;

  if ( payments.size!=PaymentList.Size )
     std.msg( string( "Сообщение не соответствует платежу.|Откатите выгрузку в МБР данного платежа и попробуйте снова.") );
     return false;
  end;

  i = 0; 
  while( i<payments.size )
     
     IsFind = false;
     j = 0;
     while( j<PaymentList.Size )
        Obj = PaymentList(j);
        if( MT102.OrdCustomer.IsSet() )
           OrdCustomer = MT102.OrdCustomer;
        else
           OrdCustomer = Obj.OrdCustomer;
        end;

        if( not ПолучитьФинИнПоISO( Obj.Currency, wlfininstr ) )
          std.msg( "Не определена валюта сообщения по коду ISO :" +  Obj.Currency );
          return FALSE;
        end;  

        if ( (payments(i).PayerAccount==OrdCustomer.Account) AND
             (payments(i).ReceiverAccount==Obj.BnfCustomer.Account) AND
             (payments(i).PayAmount==Obj.Amount) AND
             (payments(i).ValueDate==MT102.ValueDate ) AND
             (payments(i).PayFIID==wlfininstr.FIID)
           )
           IsFind = true;
           j = PaymentList.Size;
        else
           j = j + 1;
        end;
     end;

     if ( not IsFind )
        std.msg( string( "Сообщение не соответствует платежу.|Откатите выгрузку в МБР данного платежа и попробуйте снова.") );
        return false;
     end;
     i = i + 1;
  end;

  return true; 
end;