/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по сообщению ED243                                    */
/*                                                                          */
/*  Имя файла: ufgd243.mac                                                  */
/*  Создан:    17.06.11                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "bnk_common.mac", "uf243244.mac";

private macro НайтиКодРежима(strcode: string) :string

  var result = GetWlcodeDescription(TYPE_CODE_UFBS_ED243, strcode);
  if(not result)
    result = "Неизвестный код режима: " + strcode;
  end;
        
  return result;
end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация запроса по ED243");

  ClearRecord(wlreq);
  
  var error = 0, Строка, i = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  
  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
    println( string( "Неверный формат сообщения по форме ED243" ) );
    return false;
  end; 
  
  /* Заполнение учетных буферов */
  wlreq.Trn                      = wlmes.Trn;
  wlreq.RelatedRef               = wlmes.RelatedRef;
  wlreq.Direct                   = "X";  
  wlreq.Kind                     = MESKIND_REQUEST;
  
  wlreq.OriginatorID             = ToRespID(ReadAttribute(xml,"EDAuthor"));
  wlreq.OriginatorCodeKind       = PTCK_BIC;
  wlreq.OriginatorCode           = ПолучитьКодСубъекта(wlreq.OriginatorID, wlreq.OriginatorCodeKind, Error);

  FILE party(party) key 0;
  party.PartyID = wlreq.OriginatorID;
  if(GetEQ(party))
    wlreq.OriginatorName         = party.Name;
  end;
  
  wlreq.RecipientCode            = {MFO_Bank};
  wlreq.RecipientCodeKind        = PTCK_BIC;
  wlreq.RecipientID              = {OurBank};
  wlreq.RecipientName            = {Name_Bank};
  
  wlreq.InitDateMes              = ToDate(ReadAttribute(xml,"EDDate", "OriginalEPD"));
  wlreq.InitFormIDMes            = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);

  // wlreq.Queries -------------------------------------------------
  var EDDefineRequestCode = ReadAttribute(xml,"EDDefineRequestCode");
  wlreq.Queries                  = "/УТ"+EDDefineRequestCode+"/";
  
  var comma = "", MaskPath = "EDDefineRequestInfo/FieldQueryMask";
  if(ReadOptinalAttribute(xml, "Field60", MaskPath))
    wlreq.Queries              = wlreq.Queries + " 60";
    comma = ",";
  end;
  if(ReadOptinalAttribute(xml, "Field61", MaskPath))
    wlreq.Queries              = wlreq.Queries + comma + " 61";
    comma = ",";
  end;
  for (i, 101, 110, 1)
    if(ReadOptinalAttribute(xml, string("Field",i), MaskPath))
      wlreq.Queries            = wlreq.Queries + comma + i;
      comma = ",";
    end;
  end;
  
  var tmpstr = "";
  tmpstr = ReadOptinalAttribute(xml, "AccDocDate", "EDDefineRequestInfo");
  if (tmpstr != "")
    wlreq.Queries                = wlreq.Queries + string("\n", "AccDocDate: ", tmpstr);
  end;
  tmpstr = ReadOptinalAttribute(xml, "AccDocNo", "EDDefineRequestInfo");
  if (tmpstr != "")
    wlreq.Queries                = wlreq.Queries + string("\n", "AccDocNo: ", tmpstr);
  end;
  tmpstr = ReadOptinalAttribute(xml, "Acc", "EDDefineRequestInfo");
  if (tmpstr != "")
    wlreq.Queries                = wlreq.Queries + string("\n", "Acc: ", tmpstr);
  end;
  tmpstr = ReadOptinalAttribute(xml, "Sum", "EDDefineRequestInfo");
  if (tmpstr != "")
    wlreq.Queries                = wlreq.Queries + string("\n", "Sum: ", tmpstr);
  end;
  tmpstr = ReadNodeText(xml, "EDDefineRequestInfo/Name", false);
  if (tmpstr != "")
    wlreq.Queries                = wlreq.Queries + string("\n", "Name: ", tmpstr);
  end;
  // wlreq.Queries -------------------------------------------------
        
  var Description                = НайтиКодРежима("/УТ"+EDDefineRequestCode+"/");


  if( not ВставитьЗапрос( wlreq, Description ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDoc259( addrMes )
  SetBuff( wlmes, addrMes );

  var FormName : string = "ED243";
  
  PrintLog(2,"Генерация запроса по " + FormName);

  ClearRecord(wlreq);
  
  var error = 0;
  
  /* Заполнение учетных буферов */
  wlreq.Trn                      = wlmes.Trn;
  wlreq.RelatedRef               = wlmes.RelatedRef;
  wlreq.Direct                   = "X";  
  wlreq.Kind                     = MESKIND_REQUEST;

  wlreq.InitFormIDMes            = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);

  var AddFldsInfo : TAddFldsInfo = TAddFldsInfo(),
      Narrative : string = "",
      fieldname : string = "", fieldvalue : string = "", blockname : string = "";

  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if ( (fieldname == "EDAuthor") and (blockname == "") )
      FillPartyByUIS_PTCK_UIS(fieldvalue, @wlreq.OriginatorID, 
                                          @wlreq.OriginatorCodeKind, 
                                          @wlreq.OriginatorCode, 
                                          @wlreq.OriginatorName );
    end;

    if ( (fieldname == "EDReceiver") and (blockname == "") )
      FillPartyByUIS_PTCK_UIS(fieldvalue, @wlreq.RecipientID, 
                                          @wlreq.RecipientCodeKind, 
                                          @wlreq.RecipientCode, 
                                          @wlreq.RecipientName );
    end;

    if ( (fieldname == "EDDate") and (blockname == "OriginalEPD") )
      wlreq.InitDateMes          = ToDate(fieldvalue);
    end;

    if ( (fieldname == "EDDefineRequestCode") and (blockname == "") )
      var EDDefineRequestCode = fieldvalue;
      wlreq.Queries = "/УТ"+EDDefineRequestCode+"/";
      wlreq.Queries = wlreq.Queries + НайтиКодРежима(wlreq.Queries);
    end;

    // EDDefineRequestInfo
    ReadEDDefineReqAnsInfoFld( FormName, blockname, fieldname, fieldvalue, 
                               AddFldsInfo, @Narrative );
  end;
  
  // Объект RsbAddFld должен существовать и быть заполнен к моменту вставки запроса/ответа
  var AddFlds : RsbAddFld = RsbAddFld(wlreq.ReqID);
  InsertAddFldsInfoToObj( FormName, AddFldsInfo, AddFlds );
        
  if( not ВставитьЗапрос( wlreq, Narrative ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
