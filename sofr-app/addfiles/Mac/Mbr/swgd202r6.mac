/*
$Name: swgd202r6.mac
$Module: Межбанковские расчеты
$Description: Генерация платежей по сообщениям SWIFT MT202 стандарт RUR6
*/

/****************************************************************************/
/*                   R-Style SoftLab                                        */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT MT202 стандарт RUR6               */
/*                                                                          */
/****************************************************************************/

import "swgnrdoc.mac";

/* Платежная инструкция "Общий банковский перевод" */
class GeneralFinInstTransfer
 var
  standart         :integer,     /* Стандарт транслитерации */
  TRF              :string,      /* Ссылка на операцию */
  RelatedRef       :string,      /* Ссылка на сообщение или сделку */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :money,      /* Сумма перевода */
  Credit           :bool,        /* Признак дебета/кредита */
  Trans            :bool,        /* Признак транзитного */
  OrdBank          :Bank,        /* Банк Приказодатель */
  Instructor       :Bank,        /* Инструктирующий банк */
  Sender           :Bank,        /* Отправитель */
  SndCorresp       :Bank,        /* Корреспондент Отправителя */
  RcvCorresp       :Bank,        /* Корреспондент Получателя */
  Intermediary     :Bank,        /* Посредник */
  AccBank          :Bank,        /* Банк-держатель счета */
  BnfBank          :Bank,        /* Банк Бенефициар */
  DirectAcc        :string,      /* Счет для идентификации корсхемы */
  DirectDKFlag     :string,             /* признак дебета-кредита счета для идентификации корсхемы */  
  RcvInfo          :TRcvInfo100, /* Информация Получателю */
  ReceiverINN      :string;

  TRF              = "";
  RelatedRef       = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Trans            = false;
  ReceiverINN      = "";

  DirectAcc = "";

end; /* class GeneralFinInstTransfer */

var MT202 : GeneralFinInstTransfer;

macro Fill20( TRF )
  MT202.TRF = TRF;
  MT202.standart = ДоопределитьСтандарт( TRF );
  return TRUE;
end;

macro Fill21( RelatedRef )
  MT202.RelatedRef = RelatedRef;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT202.ValueDate = Date;
  MT202.Currency  = Cur;
  MT202.Sum       = money(Sum);
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT202.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT202.standart );
  MT202.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill53A( BIC, Account, System, Code, DKFlag )
  MT202.DirectDKFlag = DKFlag;
  MT202.DirectAcc = Account;
  MT202.SndCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill53B( Branch, Account, System, Code, DKFlag )
  MT202.DirectDKFlag = DKFlag;
  MT202.DirectAcc = Account;
  MT202.SndCorresp = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill53D( Address, Account, System, Code, INN, DKFlag )
  MT202.DirectDKFlag = DKFlag;
  MT202.DirectAcc = Account;
  MT202.SndCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill54A( BIC, Account, System, Code )
  MT202.RcvCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill54B( Branch, Account, System, Code )
  MT202.RcvCorresp = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill54D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT202.standart );
  MT202.RcvCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT202.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT202.standart );
  MT202.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57a( BIC, Name, Account, CliringSystem, CliringCode, INN )
  MT202.AccBank = Bank( BIC, Name, "", Account, CliringSystem, CliringCode, INN );
  return TRUE;
end;

macro Fill58A( BIC, Account, System, Code )
  MT202.BnfBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill58D( Address, Account, System, Code, INN )
  StrRestoreStandart( Address, Address, MT202.standart );
  MT202.BnfBank = Bank( "", Address, "", Account, System, Code );
  MT202.ReceiverINN = INN;
  return TRUE;
end;

macro Fill72( Str, Narrative )
  StrRestoreStandart( Narrative, Narrative, MT202.standart, null, true/*передавать код ВО*/ );
  MT202.RcvInfo = TRcvInfo100( Str, Narrative );

  StrRestoreStandart( MT202.RcvInfo.REC, MT202.RcvInfo.REC, MT202.standart );
  StrRestoreStandart( MT202.RcvInfo.INS, MT202.RcvInfo.INS, MT202.standart );
  StrRestoreStandart( MT202.RcvInfo.ACC, MT202.RcvInfo.ACC, MT202.standart );
  StrRestoreStandart( MT202.RcvInfo.INT, MT202.RcvInfo.INT, MT202.standart );
  StrRestoreStandart( MT202.RcvInfo.NZP, MT202.RcvInfo.NZP, MT202.standart, null, true/*передавать код ВО*/ );
  StrRestoreStandart( MT202.RcvInfo.BNF, MT202.RcvInfo.BNF, MT202.standart );
  
  return TRUE;
end;

/* Заполнение списка полей формы  MT202*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),
    Fld53A = ТПолеФормы( Sender_sCorrespondentField_A,       FIELD_OPTIONAL,  "ReadA",   "Fill53A", "" ),
    Fld53B = ТПолеФормы( Sender_sCorrespondentField_B,       FIELD_OPTIONAL,  "ReadB",   "Fill53B", "" ),
    Fld53D = ТПолеФормы( Sender_sCorrespondentField_D,       FIELD_OPTIONAL,  "ReadD",   "Fill53D", "" ),
    Fld54A = ТПолеФормы( Receiver_sCorrespondentField_A,     FIELD_OPTIONAL,  "ReadA",   "Fill54A", "" ),
    Fld54B = ТПолеФормы( Receiver_sCorrespondentField_B,     FIELD_OPTIONAL,  "ReadB",   "Fill54B", "" ),
    Fld54D = ТПолеФормы( Receiver_sCorrespondentField_D,     FIELD_OPTIONAL,  "ReadD",   "Fill54D", "" ),
    Fld56A = ТПолеФормы( IntermediaryField_A,                FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                FIELD_OPTIONAL,  "ReadD",   "Fill56D", "" ),
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadBlock57aTag_A_RUR", "Fill57a", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadBlock57aTag_B_RUR", "Fill57a", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadBlock57aTag_D_RUR", "Fill57a", "" ),
    Fld58A = ТПолеФормы( BeneficiaryInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill58A", "" ),
    Fld58D = ТПолеФормы( BeneficiaryInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill58D", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMT202( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT202.Sender   = Bank( BankCode, "", "", MT202.SndCorresp.Account );

  MT202.Credit = TRUE; /* входящие банковские переводы - кредитовые */
  MT202.Trans  = MT202.AccBank.IsSet(); /* документ транзитный - если указан держатель счета */

  return TRUE;
end; /* ConstructMT202 */

macro GenDoc( addrMes )
  var error;
  MT202 = NULL;
  MT202 = GeneralFinInstTransfer();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ202");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMT202( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.Amount = MT202.Sum;
  wlpmpaym.PayAmount = MT202.Sum;
  wlpmpaym.BaseAmount = MT202.Sum;
  wlpmpaym.ValueDate = MT202.ValueDate;
  if( not ПолучитьФинИнПоISO( MT202.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO :" +  MT202.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID = wlfininstr.FIID;
  wlpmpaym.PayFIID = wlfininstr.FIID;
  wlpmpaym.BaseFIID = wlfininstr.FIID;

  if ( MT202.OrdBank.IsSet() )
     wlpmpaym.PayerBankID = MT202.OrdBank.PartyID;
  else
     std.msg( "Не заполнено обязательное поле 52" );
     return FALSE;
  end;
  
  wlpmpaym.PayerAccount = MT202.SndCorresp.Account;

  wlpmpaym.Receiver         = MT202.BnfBank.PartyID; 
  wlpmpaym.ReceiverAccount  = MT202.BnfBank.Account; 
  wlpmpaym.ReceiverCodeKind = MT202.BnfBank.CodeKind; 
  wlpmpaym.ReceiverCode     = MT202.BnfBank.CodeBank; 

  /* Дебетовые свойства платежа */
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;


  wlpmpropdeb.CodeKind = MT202.OrdBank.CodeKind;
  wlpmpropdeb.BankCode = MT202.OrdBank.CodeBank;
  if ( MT202.Instructor.IsSet() )
     wlpmpropdeb.CorrCodeKind = MT202.Instructor.CodeKind;
     wlpmpropdeb.CorrCode     = MT202.Instructor.CodeBank;
     wlpmpropdeb.CorrID       = MT202.Instructor.PartyID;
  else
     wlpmpropdeb.CorrCodeKind = MT202.Sender.CodeKind;
     wlpmpropdeb.CorrCode     = MT202.Sender.CodeBank;
     wlpmpropdeb.CorrAcc      = MT202.Sender.Account;
     wlpmpropdeb.CorrID       = MT202.Sender.PartyID;
  end;

  if ( MT202.SndCorresp.IsSet() OR MT202.RcvCorresp.IsSet() )
     if ( (not MT202.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_CORR;
     end;
  end;

  if ( MT202.RcvCorresp.IsSet() )
     wlpmpropdeb.OurCorrCodeKind = MT202.RcvCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202.RcvCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202.RcvCorresp.PartyID;
  elif ( (MT202.SndCorresp.IsSet()) AND 
         (not MT202.RcvCorresp.IsSet()) AND
         (MT202.SndCorresp.PartyID!=wlpmpropdeb.CorrID) )
     wlpmpropdeb.OurCorrCodeKind = MT202.SndCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202.SndCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202.SndCorresp.PartyID;
  end;

  /* Если в 53 указан только счет, считаем это явным заданием корсчета */
  if( MT202.DirectAcc != "" )
    wlpmpropdeb.OurCorrAcc   = MT202.DirectAcc;
    if(MT202.DirectDKFlag == "C")
      wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
    elif(MT202.DirectDKFlag == "D")
      wlpmpropdeb.InOurBalance = "X"; /* Счет указан в нашем балансе */
    end;
    if( wlpmpropdeb.OurCorrID <= 0 )
      wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
    end;
  end;


  /* Реквизиты платежа, характерные для R-макета */
  if ( MT202.RcvInfo.RPP_Number=="")
     wlpmrmprop.Number             = MT202.TRF;
     wlpmrmprop.PayDate            = MT202.ValueDate;
     wlpmrmprop.Date               = MT202.ValueDate;
     wlpmrmprop.ClientDate         = MT202.ValueDate;
     wlpmrmprop.ShifrOper          = "01";
  else
     wlpmrmprop.Number             = MT202.RcvInfo.RPP_Number;
     wlpmrmprop.PayDate            = MT202.RcvInfo.RPP_Date;
     wlpmrmprop.Date               = MT202.RcvInfo.RPP_Date;
     wlpmrmprop.ClientDate         = MT202.RcvInfo.RPP_DateCarry;
     wlpmrmprop.ShifrOper          = MT202.RcvInfo.RPP_ShifrOper;
  end;
  if ( MT202.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName         = MT202.OrdBank.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT202.OrdBank.Account;
     if ( MT202.Instructor.IsSet() )
        wlpmrmprop.PayerCorrBankName     = MT202.Instructor.Name;     
     else
        wlpmrmprop.PayerCorrBankName     = MT202.Sender.Name;     
     end;
  end;  
  wlpmrmprop.Priority              = MT202.RcvInfo.RPP_Priority;
  wlpmrmprop.PartyInfo             = MT202.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = MT202.RcvInfo.RPP_PaymentKind;
  if( wlpmrmprop.PaymentKind == "С" )
    wlpmrmprop.Instancy = 1;
  end;
  wlpmrmprop.ProcessKind           = " 1";
  if ( (valtype(MT202.RcvInfo.NZP)!=V_UNDEF) AND (MT202.RcvInfo.NZP!="") )
      wlpmrmprop.Ground = MT202.RcvInfo.NZP;
  end;

  wlpmrmprop.ComissCharges = PM_CHRG_OUR;
  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo       = MT202.RcvInfo.Str;

  wlpmrmprop.ReceiverName          = MT202.BnfBank.Name;
  wlpmrmprop.ReceiverINN           = MT202.ReceiverINN;

  if ( MT202.RelatedRef!=СсылкаОтсутствует )
     wlpmrmprop.Reference = MT202.RelatedRef;
  end;

  /* Кредитовые свойства платежа */
  wlpmpropcred.TransferDate = {curdate};
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;

  SetReceiverBankForInPaymentBy57a( wlmes, MT202.AccBank, wlpmpaym, wlpmpropcred, wlpmrmprop );

  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, wlpmpropdeb.OurCorrAcc , 
                                                      NULL, false, wlmes.TpSchemID, wlpmpropdeb.InOurBalance, wlpmpropdeb.OurCorrID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  wlpmpropdeb.TransferDate = GetInTransferDate(wlpmpropdeb.Corschem, wlpmpropdeb.PayFIID, wlpmpaym.ValueDate);

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.OrdBank.GetRoute() );
  /* Узел Инструктирующего банка - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.Instructor.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.Sender.GetRoute(), true );
  /* Узел Корреспондента Отправителя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.SndCorresp.GetRoute() );
  /* Узел Корреспондента Получателя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.RcvCorresp.GetRoute() );
  /* Узел Посредника - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202.Intermediary.GetRoute() );
  /* Узел Банка Бенефициара - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202.AccBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;