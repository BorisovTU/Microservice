/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                          Импорт  сообщений                               */
/*                                                                          */
/*  Имя файла: wltlxin.mac                                                  */
/*  Создан:  19.06.03                                         Панов А.Б.    */
/****************************************************************************/

import "swtools.mac", "wlimport.mac";

const ПолеОтправителя = "FROM:",
      ПолеПолучателя = "TO:",
      ПолеДаты = "DATE:",
      ПолеНомераФормы = "MT",
      КодНачалаСообщенияTELEX = "YYYY", 
      КодКонцаСообщенияTELEX = "ZZZZ",
      Len_NumForm = 3,
      Len_Date = 6;   

var Отправитель, Получатель, ДатаОтправки, НомерФормы;

 /* Общие глобализмы */
var СтрокаСчитана  = FALSE;    /* флаг "из файла считана строка, но не обработана" */
var СчитаннаяСтрока = "";      /* считанная и необработанная строка */
var СтрокиПоля = TArray;              /* Массив строк многострочного поля */

const ДлинаСтроки = 75;
const ДлинаСтрокиПоля = 70;

/* Устанавливает контекст для поля */
macro УстановитьКонтекстДляПоляРелиза( РелизФормы, TpFieldID, error )
   var res;
   res = RlsContext.SetContext( TpFieldID, "", "", error );
   SetParm( 2, error );
   return res;
end;

/***************************************************************************/
/*  Функция записи многострочного поля в форматированную часть             */
/*  Строки записываем с символом перевода строки                           */
/*  Возвращает: TRUE или FALSE                                                           */
/***************************************************************************/
macro ЗаписатьМногострочноеПоле( кодПоля, числоСтрок )
 var СтрокаПоля = 0;
 var поле = "", Стандарт, РелизФормы;
 record mesrls ( wlmesrls  );   

 while( СтрокаПоля < числоСтрок )
    if(   СтрокаПоля == 0 ) поле = поле + String(       СтрокиПоля(0) );
    else                    поле = поле + String( "\n", СтрокиПоля(СтрокаПоля) );
    end;
 СтрокаПоля = СтрокаПоля + 1;
 end;

 if ( кодПоля == TransactionReferenceNumberField )
    wlmes.TRN = поле;

    TpShem.TpShemID = wlmes.TpSchemID;
    if ( GetEQ(TpShem) )
       mesrls.RlsFormID = wlmes.RlsFormID;
       if ( GetEQ(mesrls) )
          wlmes.TpSchemID = ОпределитьТранспортнуюСхему(wlmes.OutsideAbonentID, -1, -1, TpShem.TpID, mesrls.FormID, РелизФормы, NULL, NULL, wlsess.TpFrmtID );
          if ( wlmes.TpSchemID == -1 ) return FALSE; end;
          wlmes.RlsFormID = РелизФормы;
       end;
    end;

    ОбновитьЗапись( wlmes );
 end;
 if ( кодПоля == RelatedReferenceField )
    wlmes.RelatedRef = поле;
    ОбновитьЗапись( wlmes );
 end;
 if( not ЗаписатьПоле( кодПоля, поле ) ) return FALSE; end;

 return TRUE;
end;

macro СчитатьЗаголовок()
  var строка, длина;

  /* Читаем отправителя */
  длина = 0;
  while (длина == 0)
    длина = 75;
    строка = СчитатьСтроку(длина);
  end;
  if ((длина == КонецФайла) OR 
      (substr(строка, 1, strlen(ПолеОтправителя)) != ПолеОтправителя) OR
      (substr(строка, strlen(ПолеОтправителя)+1) == "") OR
      (substr(строка, strlen(ПолеОтправителя)+1, 1) == "?"))
    ErrImport("Не найден код отправителя");
    return false;
  else
    Отправитель = substr(строка, strlen(ПолеОтправителя)+1);
  end;

  /* Читаем получателя */
  длина = 75;
  строка = СчитатьСтроку(длина);
  if ((длина == 0) OR
      (длина == КонецФайла) OR 
      (substr(строка, 1, strlen(ПолеПолучателя)) != ПолеПолучателя) OR
      (substr(строка, strlen(ПолеПолучателя)+1) == "") OR
      (substr(строка, strlen(ПолеПолучателя)+1, 1) == "?"))
    ErrImport("Не найден код получателя");
    return false;
  else
    Получатель = substr(строка, strlen(ПолеПолучателя)+1);
  end;

  /* Читаем дату */
  длина = 75;
  строка = СчитатьСтроку(длина);
  if ((длина == 0) OR
      (длина == КонецФайла) OR 
      (substr(строка, 1, strlen(ПолеДаты)) != ПолеДаты) OR
      (substr(строка, strlen(ПолеДаты)+1) == "") OR
      (substr(строка, strlen(ПолеДаты)+1, 1) == "?"))
    ErrImport("Не найдена дата сообщения");
    return false;
  else
    ДатаОтправки = substr(строка, strlen(ПолеДаты)+1, Len_Date);
  end;

  /* Читаем форму */
  длина = 0;
  while (длина == 0)
    длина = 75;
    строка = СчитатьСтроку(длина);
  end;
  if ((длина == КонецФайла) OR 
      (substr(строка, 1, strlen(ПолеНомераФормы)) != ПолеНомераФормы) OR
      (substr(строка, strlen(ПолеНомераФормы)+1) == ""))
    ErrImport("Не найден номер формы сообщения");
    return false;
  else
    НомерФормы = substr(строка, strlen(ПолеНомераФормы)+1, Len_NumForm);
  end;

  return true;
end;

macro ОбработатьПоле( кодПоля, числоСтрок )
   if ( ЗаписатьМногострочноеПоле( кодПоля, числоСтрок ) )
      ЗаписаноПоле( кодПоля );
      return TRUE;
   else
      return FALSE;
   end;
end;

/***************************************************************************/
/*  Функция считывания поля сообщением                                     */
/*  Передаваемые параметры:                                                */
/*        ВидСообщения - вид сообщения                                     */
/*  Возвращаемые значения:                                                 */
/*            КодПоля         - код поля                                   */
/*            ЧислоСтрок      - число считанных строк                      */
/*                                                                         */
/*  Возвращает:                                                            */
/*            0  - все ОК                                                  */
/*            1  - поле не найдено                                         */
/*            2  - неизвестный код поля                                    */
/*            3  - строка поля не считана                                  */
/*            4  - неверный формат поля                                    */
/*            5  - документ выписки                                        */
/*            6  - найден конец сообщения                                  */
/*            7  - прочитано последнее поле в сообщении                    */
/*            8  - Уже прочитано и обработано последнее поле               */
/***************************************************************************/
macro СчитатьПолеИзФайла(РелизФормы, КодПоля, ЧислоСтрок)
 var   continue0;      /* флаг "продолжать цикл */
 var   длина;         /* длина считанной строки */
 var   строка,        /* считанная строка */
       предстрока,    /* предыдущая считанная строка */
       строка1;
 var   позиция;       /* номер символа в строке */
 var   count;         /* счетчик числа считанных строк */
 var   EndOfMess = FALSE; /* найден конец сообщения */
 var   retval = 0, error, TpFieldID;

 /* Находим первую строку поля */
 continue0 = 1;
 while (continue0)
    if (СтрокаСчитана)
       строка = String( СчитаннаяСтрока );
       СтрокаСчитана = FALSE;
    else
       длина = ДлинаСтроки;
       строка = СчитатьСтроку(длина);
       if (длина == КонецФайла) return 3; end;
       if (строка == "")
         return 1;
       end;
    end;

    printlog(2,строка);
    if (SubString(строка, 1, 1) == КодНачалаПоля) continue0 = 0;
    else return 4; /* Неверный формат поля */
    end;
 end;
 count = 1;

 /* определяем код поля */
 /* Отбрасываем код начала поля */
 строка = SubString( строка, 2 );
 /* Получаем код поля (все до следующего ':') */
 КодПоля = SubString( строка, 1, index(строка, КодРазделительНомера)-1 );
 /* Возвращаем код поля */
 SetParm( 1, КодПоля );

 /* определяем максимальное число строк поля */
 ЧислоСтрок = ЧислоСтрокПоля(КодПоля, TpFieldID);

 if (ЧислоСтрок == 0) return 2; end;

 if (not УстановитьКонтекстДляПоляРелиза(РелизФормы, TpFieldID, error))
    return 2;
 end;

 /* отбрасываем маркеры поля */
 позиция = Index( строка, КодРазделительНомера ) + 1;
 if (позиция <= 1)   return 4; end;

 строка = SubStr(строка, позиция);
 предстрока = строка;
 СтрокиПоля(0) = предстрока;

 /* считываем последовательно оставшиеся строки поля */
 if (ЧислоСтрок > 1)
    continue0 = 1;
    while(continue0)
      длина = ДлинаСтроки;
      строка = СчитатьСтроку(длина);
      if (длина == КонецФайла)
        EndOfMess = TRUE;
        continue0 = 0;
        retval = 3;
      elif (строка == "")
        continue0 = 0;
        retval = 7;
      else
        if(Substr(строка,1,1) == КодРазделительНомера )
          /* найдено начало следующего поля */
          СчитаннаяСтрока = строка;
          СтрокаСчитана = TRUE;
          continue0 = 0;
        else
          printlog(2,строка);
          СтрокиПоля(count)  =  строка;
          /* записываем предыдущую строку */
          СтрокиПоля(count-1) = предстрока;
          предстрока = строка;
          count = count + 1;
          if( count >= ЧислоСтрок )
            continue0 = 0;
          end;
        end;
      end;
    end; /* while */
 end;

 SetParm( 2, count );
 return retval;

end;

macro СчитатьТекстСообщения(SWIFTвидСообщения, Отправитель, ДатаОтправки, Получатель)
  var строка, длина, continue0, stat, НомерПеременной, ЗначениеПеременной ;
  var НомерФормы = 0, continue2, result;
  var кодПоля, числоСтрок, РелизФормы, TpShemID, error, ВидСообщения, Стандарт, STP;
  var RespID;
  var Валюта = "", Сумма = $0;

  длина = 70;
  строка = СчитатьСтроку(длина);

  НомерФормы = ОпределитьФорму(Транспорт, SWIFTвидСообщения, ВидСообщения);
  if (НомерФормы == -1)
     SetParm(1, НомерФормы);
     ErrImport(string("Не определен номер формы - ",SWIFTвидСообщения,", сообщение игнорируется"));
  end;

  RespID = ПолучитьКодСубъекта(Отправитель, ВидКодаТранспорта, error);
  if (error)
     ErrImport("Не найден код субъекта");
     return -1;
  end;

  TpShemID = ОпределитьТранспортнуюСхему(RespID, -1, -1, Транспорт, НомерФормы, РелизФормы, "ВыбратьПараметрОбмена", NULL, wlsess.TpFrmtID);
  if ( TpShemID == -1 ) return -1; end;

  ClearRecord( wlmes );
  wlmes.TpSchemID = TpShemID;
  wlmes.RlsFormID = РелизФормы;
  wlmes.OutsideAbonentID = RespID;
  wlmes.AgentID = wlmes.OutsideAbonentID;
  FillMesCode( TRANSP_SWIFT, wlmes );
  wlmes.Kind = ВидСообщения;
  wlmes.Importance = 0;
  if ( not СоздатьЗапись( wlmes ) )
     ErrImport( "Невозможно создать запись по форме MT "+SWIFTвидСообщения );
     return -1;
  end;
  ИнициализацияМассиваПолей();

  /* Последовательно читаем поля сообщения */
  СтрокаСчитана = FALSE;
  continue0 = 1;
  while(continue0)
     stat = СчитатьПолеИзФайла(РелизФормы, кодПоля, числоСтрок);
     if( stat == 1 )
        result = 0;
     elif( stat == 2 )
        ErrUnknownField(кодПоля);
        result = -1;
     elif( stat == 3 )
        ErrImport("Неожиданный конец файла");
        result = -1;
     elif( stat == 4 )
        ErrImport("Неверный формат поля");
        result = -1;
     elif( stat == 5 )
        result = 0;
     elif( stat == 6 )
        result = 1;
     elif( stat == 7 )
        result = 0;
     elif( stat == 8 )
        result = 0;
     end;
     if( stat )
        continue0 = 0;
     end;
     if( (stat == 0) OR (stat == 7) )
        if( not ОбработатьПоле( кодПоля, числоСтрок ) )
           ErrImport("Не обработали поле: " + кодПоля);
           continue0 = 0;
           result = -1;
        end;

        // Для платежей заполняем в отчете сумму и валюту
        if( (ВидСообщения == MESKIND_PAYMENT) AND (кодПоля == ValueDateCurrencyCodeAmountField_A) )
          Валюта = SubStr(string(СтрокиПоля(0)),7,3);
          Сумма  = doubleL(SubStr(string(СтрокиПоля(0)),10));
        end;

     end;
     СтрокиПоля.Size = 0;
  end;

  AddRepElem(wlmes.Kind, SWIFTвидСообщения, Валюта, Сумма);

  return result;
end;

macro СчитатьКодНачалаСообщения()
  var длина, строка;
  длина = 0;
  while (длина == 0)
    длина = ДлинаСтроки;
    строка = СчитатьСтроку(длина);
  end;
  if (длина == КонецФайла)
    return 3;
  end;
  if ((длина != КонецФайла) AND (строка != КодНачалаСообщенияTELEX))
    ErrImport("Не найден код начала сообщения");
    return 1;
  end;
  return 0;
end;

macro СчитатьКодКонцаСообщения()
  var длина, строка;
  длина = 0;
  while (длина == 0)
    длина = ДлинаСтроки;
    строка = СчитатьСтроку(длина);
  end;
  if (длина == КонецФайла)
    return 3;
  end;
  if ((длина != КонецФайла) AND (строка != КодКонцаСообщенияTELEX))
    ErrImport("Не найден код конца сообщения");
    return 1;
  end;
  return 0;
end;

/***************************************************************************/
/*  Функция считывания сообщения                                           */
/*  Возвращает:                                                            */
/*             0 - все ОК                                                  */
/*             1 - сообщение не найдено                                    */
/*             3 - найден конец файла                                      */
/*            -1 - ошибка при обработке сообщения (ругается сама)          */
/***************************************************************************/
macro ОбработатьСообщение();

  /* Вспомогательные переменные */
  var result, result1;

  result = СчитатьКодНачалаСообщения();
  msgbox(result);
  if (result == 3) return 3; end;
  if (result == 1) return -1; end;
  if (not СчитатьЗаголовок(Отправитель, Получатель, ДатаОтправки, НомерФормы)) return -1; end;
  result = СчитатьТекстСообщения(НомерФормы, Отправитель, ДатаОтправки, Получатель);
  if (result == 0)
    result1 = СчитатьКодКонцаСообщения();
    msgbox(result1);
    if (result1 == 3) return 3; end;
    if (result1 == 1) return -1; end;
  end;
  if (НомерФормы == -1) return 0; end; /* пропускаем неизвестные формы */
  if (result == 1)
    result = 2;
  elif (result) return result; end;

  if ( wlmes.RlsFormID<=0 )
     ErrImport( "Не найдена форма сообщения" );
     return FALSE;
  end;
  if (not ПроверкаПолейСообщения(wlmes.RlsFormID, 0)) return -1; end;

   return result;
end;

macro TELEXInProc(TpID, importFileName, addrSess)
   var num = 0, i;
   var stat;
   var continue0, Документов = 0;
   SetBuff( wlsess, addrSess );
   Транспорт = TpID;
   ВидКодаТранспорта = PTCK_TLX;

   ПерейтиВНачалоФайла();
   continue0 = true;
   while (continue0)
      stat = ОбработатьСообщение();
      if( stat == 1 )
        continue0 = FALSE;
      elif( stat == 3 ) /* найден конец файла */
        continue0 = FALSE;
      elif( stat == -1 )
        return FALSE;
      end;
      Документов = Документов + 1;
      message("Идет прием сообщений. Обработано: ", Документов );
   end;

   PrintImportReport();

   return true;
end;
