// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//                 Подсистема "Межбанковские расчеты"
//
//  Описание    : Генерация выплаты для аккредитива по сообщению SWIFT MT754
//
//  Программист : Чукина Т.А.
//
//  Создан      : 30.07.2013
//
// --------------------------------------------------------------------

import "swgendoc.mac", LCInter, "swparser.mac", MesInter;

private class TAdviceOfPayment

  var Lcpay : TRecHandler = TRecHandler("lcpay"),

      Rei   : TRecHandler = TRecHandler("lcparty"), // Рамбурсирующий банк
      Ben   : TRecHandler = TRecHandler("lcparty"), // Банк бенефициара
      BBen  : TRecHandler = TRecHandler("lcparty"), // бенефициар
      CurrParty : TRecHandler,

      IsReiFilled  : bool = false, 
      IsBenFilled  : bool = false, 
      IsBBenFilled : bool = false,

      LcdocNumber : string = "",
      ExeInfo     : string = "",
      Narrative   : string = "";

  // конструктор
  Lcpay.Clear(); Lcpay.rec.FIID = Lcpay.rec.AddFIID = ALLFININSTR;
  Rei.Clear();  Rei.rec.Kind  = LCPARTY_KIND_REIMBURSING_BANK;
  Ben.Clear();  Ben.rec.Kind  = LCPARTY_KIND_BENEFICIARY;
  BBen.Clear(); BBen.rec.Kind = LCPARTY_KIND_BENEFICIARY_BANK;

  private macro Fill32( Date, Cur, Sum )
    if( ValType(Date) == V_DATE )
      Lcpay.rec.PayDate = Date;
    end;

    if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
      std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
      return FALSE;
    end;
    Lcpay.rec.FIID    = wlfininstr.FIID;

    Lcpay.rec.Amount  = money(Sum);

    return TRUE;
  end;

  macro Fill32A( Date, Cur, Sum )
    return Fill32( Date, Cur, Sum );
  end;

  macro Fill32B( Cur, Sum )
    return Fill32( null, Cur, Sum );
  end;

  macro Fill33B( Cur, Sum )
    if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
      std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
      return FALSE;
    end;
    Lcpay.rec.AddFIID    = wlfininstr.FIID;

    Lcpay.rec.AddAmount  = money(Sum);

    return TRUE;  
  end;

  macro Fill71B( Charges )
    Lcpay.rec.Deduction = Charges;
    return TRUE;
  end;

  macro Fill73B( Charges )
    Lcpay.rec.Charge = Charges;
    return TRUE;
  end;

  private macro Fill34( Date, Cur, Sum )
    if( ValType(Date) == V_DATE )
      Lcpay.rec.ClaimDate = Date;
    end;

    Lcpay.rec.ClaimAmount  = money(Sum);

    return TRUE;
  end;

  macro Fill34A( Date, Cur, Sum )
    return Fill34( Date, Cur, Sum );
  end;

  macro Fill34B( Cur, Sum )
    return Fill34( null, Cur, Sum );
  end;

  private macro ParseFinOrgIdentField(Tag, CodeOrName, Account)
    if(Account)
      CurrParty.rec.Account = Account;
    end;

    if(Tag == SWIFT_Tag_A)
      CurrParty.rec.CodeKind = PTCK_SWIFT;
      CurrParty.rec.Code = CodeOrName;

      var error = 0,
          PartyID = ПолучитьКодСубъекта(CurrParty.rec.Code, CurrParty.rec.CodeKind, error);
      if(not error and (PartyID > 0))
        CurrParty.rec.PartyID = PartyID;
        CurrParty.rec.Name = Bnk_GetPartyName(CurrParty.rec.PartyID);
      end;
    else
      CurrParty.rec.Name = CodeOrName;
    end;

    return TRUE;
  end;

  macro FillA( BIC, Account, System, Code )
    return ParseFinOrgIdentField(SWIFT_Tag_A, BIC, Account);
  end;

  macro FillB( NameAddress, Account, System, Code )
    return ParseFinOrgIdentField(SWIFT_Tag_B, NameAddress, Account);
  end;

  macro FillD( NameAddress, Account, System, Code )
    return ParseFinOrgIdentField(SWIFT_Tag_D, NameAddress, Account);
  end;

  private macro ReadAndFillParty(fieldname : string, fieldvalue : string) : bool
    var FNum = substr(fieldname, 1, 2), 
        Tag  = substr(fieldname, 3, 1);

    if( FNum == Sender_sCorrespondentField )
      CurrParty = Rei;
      IsReiFilled = true;
      Lcpay.rec.ToReimBankCond = "X";
    elif( FNum == AccountWithInstitutionField )
      CurrParty = BBen;
      IsBBenFilled = true;
    elif( FNum == BeneficiaryInstitutionField )
      CurrParty = Ben;
      IsBenFilled = true;
    end;

    var OK = false;

    if(Tag == SWIFT_Tag_A)
      OK = ReadA( fieldvalue, 1, R2M(this, "FillA") );
    elif(Tag == SWIFT_Tag_B)
      OK = ReadB( fieldvalue, 1, R2M(this, "FillB") );
    elif(Tag == SWIFT_Tag_D)
      OK = ReadD_( fieldvalue, 1, R2M(this, "FillD") );
    end;

    return OK;
  end;

  // Чтение полей, заполнение учетных буферов
  private macro ReadFieldsAndFillBuffers() : bool
    var fieldname = "", fieldvalue = "", OK : bool;

    while ( СчитатьПоле(fieldname, fieldvalue) )
      OK = true;

      if(fieldname == TransactionReferenceNumberField)
        Lcpay.rec.Trn = fieldvalue;
      elif(fieldname == RelatedReferenceField)
        LcdocNumber = fieldvalue;
      elif(fieldname == ValueDateCurrencyCodeAmountField_A)
        OK = Read32A( fieldvalue, 1, R2M(this, "Fill32A") );
      elif(fieldname == ValueDateCurrencyCodeAmountField_B)
        OK = Read32B( fieldvalue, 1, R2M(this, "Fill32B") );
      elif(fieldname == CurrencyOriginalOrderedAmountField)
        OK = Read32B( fieldvalue, 1, R2M(this, "Fill33B") );
      elif(fieldname == DetailsOfChargesField_B)
        OK = Read71B( fieldvalue, 1, R2M(this, "Fill71B") );
      elif(fieldname == ChargesAddedField)
        OK = Read71B( fieldvalue, 1, R2M(this, "Fill73B") );
      elif(fieldname == "34A")
        OK = Read32A( fieldvalue, 1, R2M(this, "Fill34A") );
      elif(fieldname == "34B")
        OK = Read32B( fieldvalue, 1, R2M(this, "Fill34B") );
      elif( InList(substr(fieldname, 1, 2), Sender_sCorrespondentField, // 53a
                                            AccountWithInstitutionField, // 57a
                                            BeneficiaryInstitutionField // 58a
                  )
          )
        OK = ReadAndFillParty(fieldname, fieldvalue);
      elif(fieldname == SenderToReceiverInformationField) // 72
        ExeInfo = fieldvalue;
      elif(fieldname == NarrativeField) // 77A
        Narrative = fieldvalue;
      end;

      if( not OK )
        std.msg("Ошибка при обработке поля формы " + fieldname);
        return FALSE;
      end;
    end;

    return TRUE;
  end;

  private macro InsertParties(Pays : RsbLCPay) : bool
    var Parties : RsbLCParty = Pays.Parties;

    if(IsReiFilled)
      if( Parties.Insert(Rei) != 0 )
        std.msg("Ошибка при вставке рамбурсирующего банка по выплате");
        return FALSE;
      end;
    end;

    if(IsBenFilled)
      if( Parties.Insert(Ben) != 0 )
        std.msg("Ошибка при вставке бенефициара по выплате");
        return FALSE;
      end;
    end;

    if(IsBBenFilled)
      if( Parties.Insert(BBen) != 0 )
        std.msg("Ошибка при вставке банка бенефициара по выплате");
        return FALSE;
      end;
    end;

    var Exe : TRecHandler = TRecHandler("lcparty");
    Exe.Clear();  
    Exe.rec.Kind  = LCPARTY_KIND_EXEC_BANK;
    Exe.rec.CodeKind = wlmes.OutsideAbonentCodeKind;
    Exe.rec.Code = wlmes.OutsideAbonentCode;
    Exe.rec.PartyID = wlmes.OutsideAbonentID;
    Exe.rec.Name = Bnk_GetPartyName(wlmes.OutsideAbonentID);
    Exe.rec.PartyInfo = ExeInfo;
    if(Narrative)
      Exe.rec.PartyInfo = Exe.rec.PartyInfo + SYMB_ENDL + Narrative;
    end;
    if( Parties.Insert(Exe) != 0 )
      std.msg("Ошибка при вставке исполняющего банка по выплате");
      return FALSE;
    end;

    return TRUE;
  end;

  macro GenDoc() : bool
    if( not ReadFieldsAndFillBuffers() )
      return FALSE;
    end;

    var LcdocID : integer = 0;

    // Ищем аккредитив, к которому относится выплата
    var rs = execSQLselect( "Select t_LcdocID from dlcdoc_dbt where t_Number = :Num ",
                            makeArray( SQLParam("Num", LcdocNumber) ) );

    if(rs and rs.moveNext())
      LcdocID = rs.value(0);
    end;

    if(not LcdocID) // Если аккредитив не найден
      std.msg("Не найден аккредитив с номером " + LcdocNumber + ", указанным в поле 21");
      return FALSE;
    end;

    // Вставляем выплату в аккредитив
    var LcObj : RsbLetterOfCredit = RsbLetterOfCredit(LcdocID),
        Pays : RsbLCPay = LcObj.Pays;

    if( LcObj.State == WLD_STATUS_LC_CLOSE )
      std.msg("Аккредитив с номером " + LcdocNumber + " закрыт " + LcObj.CloseDate);
      return FALSE;
    end;

    if( Pays.Insert(Lcpay) != 0 )
      std.msg("Ошибка при вставке выплаты");
      return FALSE;
    end;

    // Делаем нашу выплату текущей в списке выплат
    // - это важно для корректного получения объектов Parties и WlMesLnk
    var LcpayID = Lcpay.rec.LcpayID;
    Lcpay.Clear();
    if( Pays.First(Lcpay) != 0 )
      std.msg("Ошибка при поиске выплаты после вставки");
      return FALSE;
    end;

    while(Lcpay.rec.LcpayID != LcpayID)
      if( Pays.Next(Lcpay) != 0 )
        std.msg("Ошибка при поиске выплаты после вставки");
        return FALSE;
      end;
    end;

    // Вставляем участников по выплате
    if( not InsertParties(Pays) )
      return FALSE;
    end;

    // Вставляем связь выплаты с сообщением
    var meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = Lcpay.rec.LcpayID;
    meslnk.rec.ObjKind = OBJTYPE_LCPAY;
    var MesLnkObj = Pays.WlMesLnk;
    if( (MesLnkObj.Insert(meslnk) != 0) /*or (MesLnkObj.Save() != 0)*/ )
      std.msg("Ошибка при создании связи выплаты с входящим сообщением");
      return FALSE;
    end;

    // Сохраняем изменения в аккредитиве
    if( LcObj.SaveChanges() != 0 )
      std.msg("Ошибка при сохранении аккредитива");
      return FALSE;
    end;

    /*// Связь выплаты с сообщением
    var meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = Payment.PaymentID;
    meslnk.rec.ObjKind = OBJTYPE_LCPAY;
    var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );
    if( (MesLnkObj.Insert(meslnk) != 0) or (MesLnkObj.Save() != 0) )
      std.msg("Ошибка при создании связи выплаты с входящим сообщением");
      return FALSE;
    end;
    */
    return TRUE;
  end;
end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация выплаты для аккредитива по МТ754");
  var MT754 = TAdviceOfPayment();
  return MT754.GenDoc();

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
