/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация уведомления по сообщениям SWIFT MT210                          */
/*                                                                          */
/*  Имя файла: swgd210.mac                                                  */
/*  Создан:    09.08.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgendoc.mac";

/* Уведомление */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  Account          :string,        /* Корсчет */
  DateValue        :date,          /* Дата валютирования */
  FI_Code          :string,        /* Код валюты */
  IsExistDocNoticeFlag:bool,

  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank;          /* Получатель */  

  TRF              = "";
  DateValue        = date(0,0,0);

  Account = "";
  FI_Code = "???";
  IsExistDocNoticeFlag = false;
end; /* class StatementMessage */

var MT210 : StatementMessage;

macro Fill20( TRF )
  MT210.TRF = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT210.Account = Account;
  return TRUE;
end;

macro Fill30( Date )
  MT210.DateValue = Date;
  return TRUE;
end;


/* Вставляем заголовок уведомления - чтобы было к чему привязывать документы */
macro InsertHead()

  wlhead.Account = MT210.Account;
  wlhead.FIID    = wlconf.FIID;
  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account, wlhead.FIID ) )
     if( wlhead.Account == "")
          wlhead.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlhead.FIID, 1);
     else
        return FALSE;
     end;    
  end;    
  if( not ВставитьЗаголовокУведомления( wlhead ) )
    std.msg("Ошибка при вставке заголовка уведомления");
    return FALSE;
  end;
  return TRUE;
end;

macro InsertDocNotice()
  if( not ВставитьДокументУведомления( wlConf ) )
    std.msg("Ошибка при сохранении документа уведомления");
    return FALSE;
  end;
  return TRUE;
end;

macro Fill21( TRF )
  if ( MT210.IsExistDocNoticeFlag )
     if ( not InsertDocNotice() )
        return false;
     end;
  end;
  ClearRecord(wlconf);
  wlconf.RelatedRef = TRF;
  MT210.IsExistDocNoticeFlag = true;
  return TRUE;
end;

macro Fill32B( Cur, Sum )
  if( not ПолучитьФинИнПоISO( Cur, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  Cur );
    return FALSE;
  end;
  wlconf.DateValue    = MT210.DateValue;
  wlconf.Sum          = money(Sum);
  wlconf.FIID         = wlfininstr.FIID;
  wlconf.Account      = MT210.Account;  
  MT210.FI_Code       = wlfininstr.FI_Code;
  return TRUE;
end;

macro Fill50( Address, Account, System, Code )
  var OrdCustomer;
  OrdCustomer = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;         

macro Fill50C( BEI )
  var OrdCustomer;
  OrdCustomer = Bank( BEI, "", "", "", "", "" );  
  return TRUE; 
end;

macro Fill52A( BIC, Account, System, Code )
  var OrdBank;
  OrdBank = Bank( BIC, "", "", Account, System, Code );
  wlconf.BankID = OrdBank.PartyID;
  wlconf.CodeKind = OrdBank.CodeKind;
  wlconf.CodeName = OrdBank.CodeName;
  wlconf.CodeValue = OrdBank.CodeBank;
  wlconf.BankName = OrdBank.Name;
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  var OrdBank;
  OrdBank = Bank( "", Address, "", Account, System, Code );
  wlconf.BankID = OrdBank.PartyID;
  wlconf.CodeKind = OrdBank.CodeKind;
  wlconf.CodeName = OrdBank.CodeName;
  wlconf.CodeValue = OrdBank.CodeBank;
  wlconf.BankName = OrdBank.Name;
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  var Intermediary;
  Intermediary = Bank( BIC, "", "", Account, System, Code );
  wlconf.CorrBankID = Intermediary.PartyID;
  wlconf.CorrCodeKind = Intermediary.CodeKind;
  wlconf.CorrCodeName = Intermediary.CodeName;
  wlconf.CorrCodeValue = Intermediary.CodeBank;
  wlconf.CorrBankName = Intermediary.Name;
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  var Intermediary;
  Intermediary = Bank( "", Address, "", Account, System, Code );
  wlconf.CorrBankID = Intermediary.PartyID;
  wlconf.CorrCodeKind = Intermediary.CodeKind;
  wlconf.CorrCodeName = Intermediary.CodeName;
  wlconf.CorrCodeValue = Intermediary.CodeBank;
  wlconf.CorrBankName = Intermediary.Name;
  return TRUE;
end;

/* Заполнение списка полей формы  MT210*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld25  = ТПолеФормы( AccountIdentificationField,         FIELD_OPTIONAL,  "Read25",  "Fill25",  "" ),
    Fld30  = ТПолеФормы( ValueDateField,                     FIELD_MANDATORY, "Read30",  "Fill30",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld32B = ТПолеФормы( ValueDateCurrencyCodeAmountField_B, FIELD_MANDATORY, "Read32B", "Fill32B", "InsertHead" ),
    Fld50  = ТПолеФормы( OrderingCustomerField,              FIELD_OPTIONAL,  "ReadD_",   "Fill50",  "" ),
    Fld50C = ТПолеФормы( OrderingCustomerField_C,            FIELD_OPTIONAL,  "ReadBEI", "Fill50C", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD_",   "Fill52D", "" ),    
    Fld56A = ТПолеФормы( IntermediaryField_A,                FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                FIELD_OPTIONAL,  "ReadD_",   "Fill56D", "" );

macro ConstructMT210( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode, repeatFlag = 0;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          if ( (not repeatFlag) AND (fld.Name==IntermediaryField_D) )
             repeatFlag = 1;
             count = count - 8;
          end;
          wasRead = 0;
        end;
      else
        repeatFlag = 0;
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            wasRead = 1;
            if( fld.Name == IntermediaryField_D )
              count = count - 8;              
            end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  if ( MT210.IsExistDocNoticeFlag )
     if ( not InsertDocNotice() )
        return false;
     end;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT210.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT210.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT210 */

macro GenDoc( addrMes )
  MT210 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация уведомления по МТ210");

  ClearRecord(wlhead);

  if( not ConstructMT210( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlhead.Number   = MT210.TRF;
  wlhead.DateIn   = MT210.DateValue;
  wlhead.DateOut  = MT210.DateValue;  

  if( not ОбновитьЗаголовокУведомления( wlhead ) )
    std.msg("Ошибка при обновлении заголовка уведомления");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
