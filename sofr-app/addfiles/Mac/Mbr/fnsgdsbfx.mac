/*
  $Name:         fnsgdsbfx.mac
  $Module:       МБР
  $Description:  Генерация учетного объекта по сообщению формы SBF
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация учетного объекта по сообщению формы SBF
//
// Программист  : Чукина Т.А.
//
// Создан       : 03.05.2014
//
//-----------------------------------------------------------------------------
import MesInter, "wlmnstls.mac", "xmlmestools.mac";

private class TMesData
  var NumMes : string = "", // НомСооб
      PrcCode : string = "", // КодОбр
      InfType : string = "", //ТипИнф
      DeliveryDate : date = date(0, 0, 0), // ДатаОбр
      RegOrgKind : integer = 0, // _ВидРегОргана
      ErrList : TArray = TArray; // Ошибки (тип элементов TErrInfo)

  macro Read()
    var fieldname : string = "", fieldvalue : string = "", blockname : string = "";

    while( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if(fieldname == "НомСооб")
        NumMes = fieldvalue;
      elif(fieldname == "КодОбр")
        PrcCode = fieldvalue;
      elif(fieldname == "ДатаОбр")
        DeliveryDate = StrToDate(fieldvalue);
      elif(fieldname == "_ВидРегОргана")
        RegOrgKind = int(fieldvalue);
      elif(fieldname == "ТипИнф")
        InfType = fieldvalue;
      elif( (fieldname == beginField) and index(blockname, "Ошибки") )
        ErrList[ ErrList.size ] = TErrInfo();
      elif(fieldname == "КодОшибки")
        ErrList[ ErrList.size - 1 ].ErrCode = fieldvalue;
      elif(fieldname == "НаимОшибки")
        ErrList[ ErrList.size - 1 ].ErrName = fieldvalue;
      elif(fieldname == "КодРекв")
        ErrList[ ErrList.size - 1 ].AttrCode = fieldvalue;
      elif(fieldname == "ЗначРекв")
        ErrList[ ErrList.size - 1 ].AttrValue = fieldvalue;
      end;
    end;
  end;

end;

macro GenDoc( addrMes )
  SetBuff(wlmes, addrMes);

  PrintLog(2, "Генерация квитанции о принятии (непринятии) сообщения банка");

  var Mes : TMesData = TMesData();
  Mes.Read();

  var InsideAbonentID : integer = 0, 
      InsideAbonentCodeKind : integer = 0, 
      InsideAbonentCode : string = "",
      OutMesTrn : string = "",
      OutMesState : integer = 0;

  var OutMesID : integer = GetKvitSBC( wlmes.MesID, Mes.NumMes, @InsideAbonentID, 
                                      @InsideAbonentCodeKind, @InsideAbonentCode,
                                       null, @OutMesTrn, @OutMesState );

  var isFoundMes : bool = (OutMesID != 0);

  if( isFoundMes or
      GetInsideAbonentForSBF( wlmes.Department, @InsideAbonentID, 
                             @InsideAbonentCodeKind, @InsideAbonentCode )
    )
    wlmes.InsideAbonentID = InsideAbonentID;
    wlmes.InsideAbonentCodeKind = InsideAbonentCodeKind;
    wlmes.InsideAbonentCode = InsideAbonentCode;

    if( not UpdateMes(wlmes) )
      RunError("Ошибка при обновлении данных сообщения");
    end;
  end;

  if( ((Mes.PrcCode == "0") or (Mes.PrcCode == "2")) or (Mes.InfType == "ИЗВОКОРРЕКТ") )
    var ErrCode : integer = 0,
        ErrDescription    = "Не найдено исходное сообщение",
        State             = WLD_STATUS_RESPRM_DEFECT;

    if(isFoundMes)
      // ErrCode
      if(Mes.PrcCode == "0")
        ErrCode = FNS_RP_NOT_APPROVED_FNS;
      elif(Mes.PrcCode == "2")
        ErrCode = FNS_RP_APPROVED_WITH_ERROR;
      elif(Mes.RegOrgKind == REG_ORGAN_KIND_PFR)
        ErrCode = FNS_RP_RECEIVED_MES_ERROR_PFR;
      elif(Mes.RegOrgKind == REG_ORGAN_KIND_FSS)
        ErrCode = FNS_RP_RECEIVED_MES_ERROR_FSS;
      end;

      // ErrDescription
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);
      if(not ErrDescription)
        RunError("Не найден код ошибки " + ErrCode);
      end;

      // State
      State = WLD_STATUS_RESPRM_RECEIVE;
    end;

    var ResultID : integer = 0;
    if( not ОшибкаЛогическогоКонтроляСообщения( OutMesID, ErrCode, ErrDescription, State, true, ResultID ) )
      std.msg( string("|Ошибка при переносе в отбракованные сообщения ", OutMesTrn) ); 
      return false;
    end;

    var ErrList : RsbWlError = RsbWlError(ResultID);
    var wlerr_buf = TRecHandler("wlerror.dbt");

    for( var ErrInfo, Mes.ErrList )
      fillWlerror(ResultID, ErrInfo, wlerr_buf);

      if( ErrList.Insert( wlerr_buf ) != 0 )
        std.msg( "|Ошибка при создании записи об ошибке обработки сообщения" ); 
        return false;
      end;

      if( ErrList.TransferSelect( ResultID ) != 0 )
        std.msg( "|Ошибка при сохранении списка ошибок обработки сообщения" ); 
        return false;
      end;
    end;

  else
    var Description : string = GetConfDelivDescription( Mes.RegOrgKind );

    var NoChangeState : bool = true;
    if( OutMesState == 25 )
      NoChangeState  = true;

    elif( ( ( Mes.RegOrgKind == REG_ORGAN_KIND_FNS ) and isFoundMes and
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_PFR) ) and 
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_FSS) ) 
          )
          or
          ( ( Mes.RegOrgKind == REG_ORGAN_KIND_PFR ) and isFoundMes and
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_FNS) ) and 
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_FSS) )
          )
          or
          ( ( Mes.RegOrgKind == REG_ORGAN_KIND_FSS ) and isFoundMes and
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_PFR) ) and 
            ( GetDeliveryMes(OutMesID, REG_ORGAN_KIND_FNS) )
          )
        )
      NoChangeState  = false;

    else
      NoChangeState  = true;
    end;

    if( not ВставитьПодтверждениеОДоставке( OutMesID, NoChangeState, Mes.DeliveryDate, 
                                            Description, true /*CreateCvt*/ )
      )
      std.msg( string( "|Ошибка при переносе в закрытые сообщения ", OutMesID ) ); 
      return false;
    end;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
