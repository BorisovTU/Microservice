/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mnspraccinf.mac
  Created     :            
  Description : Печать справки об остатках на счетах по сообщениям RPO

└───────────────────────────────────────────────────────────────────────────*/

import WldInter, BankInter, "wlglobal.mac", "pmprops.mac", "wlprtool.mac", "adress.mac", mnsznsal;

const SET_CHAR   = "X",
      UNSET_CHAR = "";

macro Draw( DecisionID : integer )

  var facc    :TBFile = TBFile("account.dbt");
  var fdep    :TBFile = TBFile("dp_dep.dbt");
  var fpt     :TBFile = TBFile("party.dbt");
  var fps     :TBFile = TBFile("persn.dbt");
  Array Text, Buttons;
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  var ErrMsg = "Не найден(ы) счет(а) ";
  var ErrAcc = TArray(), i = 0;   // не найденные счета
  Счет = TArray();                // обнулим массив счетов для массовой печати
  var bankparams:TArray = NULL;

  file adrFNS(adress);
  var select = "select rgd.t_Date as DocDate, rgd.t_Number as DocNum, rgd.t_DeliveryDate as RestDate, rgd.t_RecipientID as BankID, "+
                      "rgd.t_OriginatorName as NoName, rgd.t_OriginatorID as NoID, lnk.t_Account as Account, lnk.t_FIID as FIID, "+
                      "lnk.t_Chapter as Chapter, "+
                      "case when lnk.t_LnkObjectType = 455 then acclaim.t_Priority else 3 end as ClaimPriority, "+
                      "case when lnk.t_LnkObjectType = 455 then acclaim.t_ClaimID  else 0 end as ClaimID "+
                 "from dwlregdec_dbt rgd, dwlacclnk_dbt lnk, dacclaim_dbt acclaim "+
                "where rgd.t_DecisionID         = ? "+
                  "and lnk.t_ObjectID           = rgd.t_DecisionID "+
                  "and lnk.t_ObjectType         = 519 "+
                  "and acclaim.t_ClaimID(+)     = lnk.t_LnkObjectID ";

  var params = makeArray( SQLParam( "ID", DecisionID ) );
  var rs = execSQLselect( select, params, FALSE );
  var INNnp = IfThenElse( wlregdec.ClientKPP == "", wlregdec.ClientINN, wlregdec.ClientINN + "/" + wlregdec.ClientKPP );

  while( rs.moveNext() )
    facc.KeyNum = 0;
    facc.rec.Account        = rs.value( "Account"  );
    facc.rec.Chapter        = rs.value( "Chapter"  );
    facc.rec.Code_Currency  = rs.value( "FIID"     );
    if( facc.GetEQ() and ( facc.rec.Open_Close == "" ) ) /* счет должен быть в нашем филиале и открыт */
      var INN = getPartyINN( facc.rec.Client , 1);
      if( INN == INNnp )                                 /* владельцем счета является клиент, реквизиты которого заданы в сообщении */
        fps.rec.PersonID = fpt.rec.PartyID = ПолучитьКодСубъекта( INN, PTCK_INN );
        if( (( fpt.GetEQ() ) and ( fpt.rec.LegalForm == PTLEGF_INST  ) and (wlregdec.ClientName == fpt.rec.Name)) or
            (( fps.GetEQ() ) and ( fpt.rec.LegalForm == PTLEGF_PERSN ) and (wlregdec.ClientName == fps.rec.Name1 + "," + fps.rec.Name2 + "," + fps.rec.Name3)) )
          fdep.keyNum = 0;
          fdep.rec.Code = facc.rec.Department;
          if( fdep.GetEQ() ) 
            bankparams = makeArray( SQLParam( "p_PartyID"    , fdep.rec.PartyID     ),
                                    SQLParam( "p_CodeKind"   , PTCK_BIC             ),
                                    SQLParam( "p_Code"       , V_STRING , RSDBP_OUT ),
                                    SQLParam( "p_CodeOwnerID", V_INTEGER, RSDBP_OUT ) );  
            execStoredFunc( "RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, bankparams );
            if( bankparams.value(3).value == rs.value( "BankID" ) )
              Счет( Счет.Size() ) = TRecHandler("account");
              Copy( Счет( Счет.Size() - 1 ), facc );
              RepBankID  = bankparams.value(3).value;
            end;
          end;
        else  /* не найден счет - занесем его в список */
          ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + rs.value("Account");
        end;
      else  /* не найден счет - занесем его в список */
        ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + rs.value("Account");
      end;
    else  /* не найден счет - занесем его в список */
      ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + rs.value("Account");
    end;
  end;

  ErrAcc = split( SubStr( ErrMsg, StrBrk( ErrMsg, "0123456789" ) ), ", " ); 
  if( Счет.Size == 0 )
    MsgBox( "Не найдено ни одного открытого счета клиента" );
    return false;
  elif( StrBrk( ErrMsg, "0123456789" ) )
    i = 0;
    if( GetDialogFlag() )
      Text(0) = string("Не найден счет № ", ErrAcc[i], ".|Продолжить печать?");
      if( ConfWin( Text, Buttons ) == 1 )
        return false;
      end;
    end;
    SetMNSWarning( ErrMsg );
  end;

  if( НайтиЮридическийАдресСубъекта( rs.value( "NoID" ), adrFNS ) or 
      НайтиАдресСубъекта( rs.value( "NoID" ), PTADDR_REAL, adrFNS )or
      НайтиПочтовыйАдресСубъекта( rs.value( "NoID" ), adrFNS ) ) 
  end;

  fpt.keyNum = 0;
  fpt.rec.PartyID = Счет[0].rec.Client;
  if( fpt.GetEQ() ) 
    НаимНП  = fpt.rec.Name;
  end;

  НаимНО             = rs.value( "NoName" );
  АдрНО              = adrFNS.Adress;
  НомерЗапроса       = rs.value( "DocNum" );
  ДатаЗапроса        = date( rs.value( "DocDate" ) );
  RepPriority        = int( rs.value( "ClaimPriority" ) );
  RepClaimID         = int( rs.value( "ClaimID" ) );
  ПоСостояниюНаДату  = rs.value( "RestDate" );
  ВидСправки         = "OS";
  if( MnsMessageFormZNS().PrintDoc( Счет[0].rec.Client ) )
    return false;
  else
    return true;
  end;
end;

macro PrintDocument( MassCopy, DocKind )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;
  var stat:bool;

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  copy( wlregdec, pr_wlregdec );
  stat = Draw( wlregdec.DecisionID );
  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  if( stat == true )   
    while( MassCopy != 0 )
      rewind( MsgTmpFile );
      while( next(MsgTmpFile) )
        println( MsgTmpFile.str );
      end;
      MassCopy = MassCopy - 1;
    end;    
 
    execSQL( "update dwlmes_dbt set t_IsPrinted = 'X' where t_MesID in "+
                      "( select mes.t_MesID "+
                          "from dwlmes_dbt mes, dwlmeslnk_dbt lnk "+
                         "where lnk.t_MesID    = mes.t_MesID "+
                           "and lnk.t_ObjKind  = 519 "+
                           "and lnk.t_Direct   = 'X' "+
                           "and lnk.t_ObjID    = ? ) ",
            makeArray( SQLParam( "", wlregdec.DecisionID  ) ) );
  end;

  return true;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage(er);
  return false;
end;
