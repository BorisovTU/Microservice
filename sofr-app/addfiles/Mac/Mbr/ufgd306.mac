/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED306                                     */
/*                                                                          */
/*  Имя файла: ufgd306.mac                                                  */
/*  Создан:    25.10.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdreqliq.mac";

class (TGenDocLiqReq) TGenDocED306()

  InitTGenDocLiqReq(MESKIND_ANSWER, "ED306");

  var BIC = "",
      LiquidityTransKind = "",
      Sum_str = "";

  macro ReadFields() : bool
    var field_name, field_value;  
    if( not СчитатьПоле( field_name, field_value ) )
      std.msg( String("В сообщении по форме: ", FormName, 
                      " референс: ", wlmes.TRN, 
                      "|не заполнено поле '", XMLField, "'") );
      return FALSE;
    end;

    var xml:object = ActiveX( "MSXML.DOMDocument" );
    if ( not xml.loadXML(field_value) )
      std.msg( "Неверный формат сообщения по форме ED306" );
      return FALSE;
    end;

    OriginatorUIS  = ReadAttribute(xml,"EDAuthor");
    RecipientUIS   = ReadOptinalAttribute(xml,"EDReceiver");
    EDDate_InitMes = ReadOptinalAttribute(xml,"EDDate", "InitialED");

    BIC = ReadAttribute(xml, "BIC");
    LiquidityTransKind = ReadAttribute(xml, "LiquidityTransKind");
    Sum_str = ReadAttribute(xml, "Sum");

    return TRUE;
  end;

  macro FillQueries() : bool
    // "/N/БИК/Сумма"
    wlreq.Queries  = "/" + LiquidityTransKind + "/" + BIC + "/" + Sum_str;
    return TRUE;
  end;

  macro OnAfterGenWlreq() : bool    

    var Department : integer = GetDprtByBIC(BIC);

    if(Department <= 0)
      std.msg("Не найден филиал с кодом БИК " + BIC);
      return FALSE;
    end;

    // меняется сумма текущей ликвидности
    var Sum : moneyL = StrToMoneyUFBS(Sum_str);  
    var Dpliqset : RsbDpLiqSet = RsbDpLiqSet(Department);
    if( (LiquidityTransKind == "1") or (LiquidityTransKind == "2") )
      Dpliqset.CurLiquidity = Sum;
    elif(LiquidityTransKind == "4")
      Dpliqset.CurLiquidity = Dpliqset.CurLiquidity + Sum;
    elif(LiquidityTransKind == "5")
      Dpliqset.CurLiquidity = Dpliqset.CurLiquidity - Sum;
    else
      std.msg("Неверное значение LiquidityTransKind");
      return FALSE;
    end;
    if(Dpliqset.Update() != 0)
      std.msg("Ошибка при сохранении суммы текущей ликвидности");
      return FALSE;
    end;

    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       
  
  var gdObj : TGenDocED306 = TGenDocED306();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
