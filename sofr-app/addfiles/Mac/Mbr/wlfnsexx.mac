/*
  $Name:         wlfnsexx.mac
  $Module:       Межбанковские расчеты
  $Description:  Экспорт сообщений ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Экспорт сообщений ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 29.05.2014
//
//-----------------------------------------------------------------------------

import "wlexport.mac", "xmlmestools.mac", "wlmnstls.mac";

class (TFldExportHandler) TFldExportHandlerFnsXml
  InitTFldExportHandler();

  private var Name1 = "", Name2 = "", Name3 = "", Officer = "", Phone = "", 
              IDFile = "", DateMes = "", DateSendMes = "";

  private macro IsGoodIDFile(IDFile : string) : bool
    var INN : string = substr(IDFile, 1, 10),
        delim : string = substr(IDFile, 11, 2),
        tail : string = substr(IDFile, 13);

    if( IsNDigits(INN, 10) and (delim == "**") and IsNDigits(tail, 23) )
      return TRUE;
    end;

    return FALSE;
  end;

  macro FillFieldsAtExport(ExportFileName : string, FormName : string)

    IDFile = MakeIDFile(ExportFileName, FormName);
    if( not IDFile and not InList(FormName, "PB", "BUV") )
      RunError("Ошибка формирования идентификатора файла");
    elif( InList( SubStr(FormName, 1, 3), "SBC", "SFC" ) and not IsGoodIDFile(IDFile) )
      RunError("Идентификатор файла не соответствует шаблону \"[0-9]{10}[*]{2}[0-9]{23}\"");
    end;

    if( not Name1 )
      PutPersData_Default(Name1, Name2, Name3, Officer, Phone);
      Name1 = substr( Name1, 1, 60 );

      if( not Phone or not Officer or not Name1)  
        RunError("Не указаны телефон, должность, фамилия сотрудника, выгружающего сообщение");
      end;
    end;

    if(not DateMes)
      if (InList(FormName, "BNS", "BVS", "BVD", "BOS"))
        DateMes = YYYYMMDDXML(date());
      else
        DateMes = string(date():f);
      end;
    end;

    if(not DateSendMes)
      DateSendMes = YYYY_MM_DD(date());
    end;
  end;

  private macro CorrectValue(Code : string, Value : string) : string
    if(Code == "ИдФайл")
      return IDFile;
    elif(Code == "ТелОтпр")
      return Phone;
    elif(Code == "ДолжнОтпр")
      return Officer;
    elif(Code == "ФамОтпр")
      return Name1;
    elif((Code == "ДатаСооб") and (trim(Value) == ""))
      return DateMes;
    elif(Code == "ДатаНапр")
      return DateSendMes;
    end;

    return Value;
  end;

  private macro GetDateName(FieldName : string) : string
    if(FieldName == "ДатаОткрСч")
      return "Дата открытия счета";
    elif(FieldName == "ДатаЗаклДог")
      return "Дата заключения договора";
    elif(FieldName == "ДатаЗакрСч")
      return "Дата закрытия счета";
    elif(FieldName == "ДатаИзмСч")
      return "Дата изменения номера счета";
    end;

    return FieldName;
  end;

  private macro CheckValue(FieldName : string, FieldValue : string)

    if( InList(FieldName, "ДатаОткрСч", "ДатаЗаклДог", "ДатаЗакрСч", "ДатаИзмСч") )
      var dt : date = StrToDate(FieldValue);
      if(dt > Date())
        RunError( GetDateName(FieldName) + " не должна быть больше даты сообщения" );
      end;
    end;

  end;

  macro ProcessFld(FieldName : string, FieldValue : @string) : bool
    // Поля, в названии которых первый символ равен "_", в текстовый файл не выводятся (пропускаются)
    if( SubStr(FieldName, 1, 1) == "_" )
      return FALSE;
    end;

    // Поля, заполняемые на этапе экспорта
    var CorrectedValue : string = CorrectValue(FieldName, FieldValue);
    if(CorrectedValue != FieldValue)
      if( ОбновитьПоле(CorrectedValue) == 0 )
        FieldValue = CorrectedValue;
      else
        RunError("Ошибка при обновлении значения поля " + FieldName);
      end;
    end;

    // Поля, проверяемые на этапе экспорта
    CheckValue(FieldName, FieldValue);

    return TRUE;
  end;

end;

// Создаем глобальную переменную, т.к. при групповом экспорте выполняется кеширование
// макросов экспорта, при этом значения глобальных переменных макросов сохраняются.
// Это дает автоматическое кеширование значений Name1, Officer, Phone, DateMes
// (фамилия, должность, телефон текущего пользователя, системная дата)
private var FldHandler : TFldExportHandlerFnsXml = TFldExportHandlerFnsXml();

private macro ЗаписатьСообщение( XmlObj : object, FormName : string ) : bool
  var AllFldsAreRead : bool = false;

  // чтение полей, образующих структуру XML
  var node : object = null;

  if( not ExportMesToXmlNode(@node, wlmes, @AllFldsAreRead, FldHandler) )
    return FALSE;
  end;

  if( not node )
    ErrExport("Ошибка при формировании узла XML из полей сообщения");
    return FALSE;
  end;

  XmlObj.appendChild(node);


  // чтение остальных полей
  if(not AllFldsAreRead)

    var FieldName : string = "", FieldValue : string = "", BlockName : string = "";

    while( СчитатьПоле( FieldName, FieldValue, BlockName ) )
      if( (FieldName == "_СпОбм") and InList(FieldValue, "1", "4") )
        ErrExport("Выгрузка сообщений, которые должны быть отправлены почтой или курьером, не допускается");
        return FALSE;
      end;
    end;

  end;

  return TRUE;
end;

private macro CheckExportBNP(ExportFileName)
  var FName : string = "", FExt : string = "";
  SplitFile(ExportFileName, FName, FExt);

  var PostExportFileName : string = ФормаЭкспорт.Name + GetFileNameMNS( ФормаЭкспорт.Name, 0, wlmes.MesID ) + FExt;
  var TpFrmtID : integer = wlsess.TpFrmtID;

  var rs = execSQLselectPrm
  ( "select 1 "
    "  from dwlsess_dbt wlsess "
    " where wlsess.t_FileName like ( '%\' || :PostExportFileName ) "
    "   and wlsess.t_TpFrmtID = :TpFrmtID "
    "   and wlsess.t_State <> " + WLD_STATUS_SESS_DEFECT,
    SQLParam("PostExportFileName", PostExportFileName),
    SQLParam("TpFrmtID", TpFrmtID)
  );

  if(rs and rs.moveNext())
    var ErrMsg : string = "Сообщение о неисполнении " + PostExportFileName + " уже отправлено|Формат имени файла не позволяет выгружать несколько сообщений о неисполнении в день, повторите выгрузку завтра";
    RunError(ErrMsg, ErrMsg);
  end;
end;

macro ExportMessage(ExportFileName, addrSess)
  SetBuff( wlsess, addrSess );

  var Документов = 0, err;
  var FormName : string = ""; // название формы выгружаемого сообщения

  var XmlObj : object = ActiveX("Microsoft.XMLDOM");
  XmlObj.appendChild( XmlObj.createProcessingInstruction("xml", " version='1.0' encoding='windows-1251'") );

  while( СчитатьЗапись( wlmes, err ) )
    // Проверить наличие налоговой инспекции в карточке банка
    // Проверить наличие у заданной налоговой инспекции кода вида 28
    GetCodeNOBWithChecks( wlmes.InsideAbonentID, 
                          "Не задан налоговый орган, в котором зарегистрирован банк (филиал)", 
                          "Не задан код налогового органа по месту регистрации банка (филиала)" );

    if( DefineFormExport( wlmes.RlsFormID ) )
      FormName = ФормаЭкспорт.Name;
    else
      return false;
    end;

    FldHandler.FillFieldsAtExport(ExportFileName, FormName);

    if(FormName == "BNP")
      CheckExportBNP(ExportFileName);
    end;

    if( not ЗаписатьСообщение(XmlObj, FormName) )
      return false;
    end;

    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;

  XmlObj.Save(ExportFileName);

  return true;

  OnError(er) // Обработка ошибок времени выполнения
    ErrExport(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
