/*
  $Name:         ufgd814.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО "Ответ" по сообщениям УФЭБС ED814
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация УО "Ответ" по сообщениям УФЭБС ED814
//
// Программист  : Kolyakin V.V.
//
// Создан       : 26.07.2018
//
//-----------------------------------------------------------------------------

import "ufgendoc.mac", PTInter, likepy, oralib, "pmlib.mac", wltools, bnk_dttmfrmt, bnk_dirfile, bnk_ptlib, wluftool;

private class TEDRequisites
  var EDNo:       integer,   /*Номер ЭПС в течение опердня*/
      EDDate:     date,      /*Дата составления ЭПС*/
      EDAuthor:   string;    /*Уникальный идентификатор составителя ЭС*/
      
  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
  end;

  Construct();
end;

class TED814MessageFields /*Поля сообщения ED814*/
  var EDNo:                integer,    /*Номер ЭПД в течение опердня*/
      EDDate:              date,       /*Дата составления ЭПД*/
      EDAuthor:            string,     /*Уникальный идентификатор составителя ЭПД*/
      EDReceiver:          string,     /*Уникальный идентификатор получателя ЭС*/
      OperationTime:       string,     /*Дата и время проведения операции*/
      InitialED:           variant,    /*Идентификаторы исходного ЭСИС*/
      EDRefID:             variant;    /*Идентификаторы перемещенного ЭПС*/
      
  macro Construct()
    EDNo = 0;
    EDAuthor = EDReceiver = OperationTime = "";
    EDDate = date(0,0,0);
    EDRefID = null;
    InitialED = null;
  end; 
  
  Construct();
end;


// Макрос чтения полей сообщения ED814
// Входные параметры: ED814MessageFields - структура полей сообщения ED814
macro ReadED814MessageFields( ED814MessageFields:@TED814MessageFields ) : bool
  var fieldname = "", fieldvalue = "", blockname = "";
  var stat : bool = false;

  /*Считываем только то, что хранится в полях релиза*/
  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED814MessageFields.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED814MessageFields.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED814MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED814MessageFields.EDReceiver = fieldvalue;
      elif(fieldname == "OperationTime")
        ED814MessageFields.OperationTime = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == beginField)
        ED814MessageFields.InitialED = TEDRequisites();    
      elif(fieldname == "EDNo")
        ED814MessageFields.InitialED.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED814MessageFields.InitialED.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED814MessageFields.InitialED.EDAuthor = fieldvalue;
      end;
    elif(blockname == "EDRefID")
      if(fieldname == beginField)
        ED814MessageFields.EDRefID = TEDRequisites();    
      elif(fieldname == "EDNo")
        ED814MessageFields.EDRefID.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED814MessageFields.EDRefID.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED814MessageFields.EDRefID.EDAuthor = fieldvalue;
      end;
    end; 
  end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

  return TRUE;
end;

macro GenDoc( addrMes )


  var Narrative : string = "";
  SetBuff( wlmes, addrMes );
  PrintLog( 2, "Генерация информационного сообшения по ED814" );

  var tmpPartyID = 0;
  
  var ED814MessageFields = TED814MessageFields();
  ReadED814MessageFields( @ED814MessageFields );

  var RsbWldRequestObj : RsbWlRequest = RsbWlRequest(0); // Создаём новый Rsb-объект

  RsbWldRequestObj.Trn = wlmes.Trn;
  var tmpRelatedRef = YYMMDD( ED814MessageFields.InitialED.EDDate ) + ED814MessageFields.InitialED.EDAuthor + ED814MessageFields.InitialED.EDNo;
  RsbWldRequestObj.RelatedRef = tmpRelatedRef;
  RsbWldRequestObj.Direct = "X"; /*WLD_MES_IN*/
  RsbWldRequestObj.Kind = MESKIND_ANSWER; /*Создаем ответ*/

  RsbWldRequestObj.OriginatorCode = ED814MessageFields.EDAuthor;
  RsbWldRequestObj.OriginatorCodeKind = PTCK_UIS;
  tmpPartyID = 0;
  tmpPartyID = GetCodeOwnerID( RsbWldRequestObj.OriginatorCodeKind, RsbWldRequestObj.OriginatorCode );
  if( tmpPartyID > 0 )
    RsbWldRequestObj.OriginatorID   = tmpPartyID;
    RsbWldRequestObj.OriginatorName = Bnk_GetPartyName( RsbWldRequestObj.OriginatorID );
  end;
 
  RsbWldRequestObj.RecipientCode      = wlmes.InsideAbonentCode;
  RsbWldRequestObj.RecipientCodeKind  = wlmes.InsideAbonentCodeKind;
  RsbWldRequestObj.RecipientID        = wlmes.InsideAbonentID;
  RsbWldRequestObj.RecipientName      = Bnk_GetPartyName( RsbWldRequestObj.RecipientID );

  var OperationDate = "", OperationTime = "";
  DivideYYYY_MM_DDThhmmssZToDateTime( ED814MessageFields.OperationTime, @OperationDate, @OperationTime );
  var tmpQueries = "Дата проведения операции " + OperationDate + ", время проведения операции " + OperationTime + "\n";
  RsbWldRequestObj.Queries = tmpQueries;
  
  RsbWldRequestObj.ChangeState( WLD_STATUS_REQ_RECEIV );

  RsbWldRequestObj.SaveChanges();

  /*Создаем связь документа с входящим сообщением*/
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = RsbWldRequestObj.ReqID;
  meslnk.rec.ObjKind = OBJTYPE_REQ;
  var MesLnkObj = RsbWldRequestObj.InWlmesLnk();
  MesLnkObj.Insert( meslnk );
  MesLnkObj.Save();

  /*Если найден исходящий платёж по запросу, то для запроса создаём запись wladdfld*/
  var WlMesPmID = FindReqOutMesID( wlmes.RelatedRef, wlmes.TpSchemID );
  var PaymentsCount = 0;
  var PaymentID = 0;
  PaymentID = GetPaymentIDFromMesWCount( WlMesPmID, @PaymentsCount );
  if( (PaymentID != 0) and (PaymentsCount == 1) )
    var addfld = TRecHandler("wladdfld.dbt");
    addfld.rec.Number = "#EDRefID";
    addfld.rec.Value = wlmes.RelatedRef;
    addfld.rec.PaymentID = PaymentID;
    var AddFields = RsbWldRequestObj.AddFields();
    AddFields.Insert( addfld );
    AddFields.Save();
  end;

  /*Вставка подтверждения о доставке*/
  if( ED814MessageFields.InitialED != null)
    var ReqMesID = FindReqOutMesID( RsbWldRequestObj.RelatedRef, wlmes.TpSchemID );
    if( ReqMesID > 0 )
      ВставитьПодтверждениеОДоставке( ReqMesID, null, null, null, false, null, WLD_STATUS_MES_CLOSED );
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
