/*
$Name:           fnsgmpbx.mac
$Module:         МБР
$Description:    Процедура генерации сообщения "Подтверждение"
*/

import "wlgenmes.mac", WldInter, "xmlmestools.mac", "wlmnstls.mac", "fnsgenmesx.mac";

// ДатаВремяПроверки  yyyy-mm-ddThh:mm:ss
private macro MakeDateTime( Date, Time ):string
  
  var DateTime:string = "";
  var DateStr = YYYY_MM_DD(Date);
  var TimeStr = TimeToStr(Time, "hh:mi:ss");

  DateTime = DateStr + "T" + TimeStr;

  return DateTime;
end;

macro GenMes( addrMes, buff, addrOldMes )

  record OldMes( wlmes );  ClearRecord( OldMes );

  SetBuff( wlmes,     addrMes     );
  SetBuff( OldMes,    addrOldMes  );
  SetBuff( wlresprm,  buff        );
  PrintLog(2, "Генерация сообщения по PB");

  // Файл
  StartBlockLogXML("Файл");

  ЗаписатьФайл("ПОДБНПРИНТ");

  // Файл \ ПОДБНПРИНТ
  StartBlockLogXML("ПОДБНПРИНТ");

   ЗаписатьПолеЛог( "ИмяФайла", wlresprm.FileName );
   ЗаписатьПолеЛог( "ДатаВремяПроверки", MakeDateTime( wlresprm.CreateDate, wlresprm.CreateTime ) );

    // Файл \ ПОДБНПРИНТ \ Результат

    if( wlresprm.ErrCode == 0 )
      StartBlockLogXML("Результат");
      ЗаписатьПолеЛог( "КодРезПроверки", "01" );
      FinishBlockLogXML("Результат");
    else

      var select = " select t_Code, t_Description, t_FieldCode, t_FieldValue   "
                   "   from dwlerror_dbt "
                   "     where t_ResultID = :ResultID  ";
      var params = makeArray( SQLParam( "ResultID", wlresprm.ResultID ) );
      var rs = execSQLselect( select, params, FALSE );

      while( rs and rs.moveNext() )
        
        StartBlockLogXML("Результат");
        ЗаписатьПолеЛог( "КодРезПроверки", rs.Value(0) );
        if( StrLen( rs.Value(1) ) > 0 )
          ЗаписатьПолеЛог( "Пояснение", rs.Value(1) );
        end;
        if( StrLen( rs.Value(2) ) > 0 )
          ЗаписатьПолеЛог( "КодРекв", rs.Value(2) );
        end;
        if( StrLen( rs.Value(3) ) > 0 )
          ЗаписатьПолеЛог( "ЗначРекв", rs.Value(3) );
        end;
        FinishBlockLogXML("Результат");
      end;
    end;

   FinishBlockLogXML("ПОДБНПРИНТ");

  FinishBlockLogXML("Файл");

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
