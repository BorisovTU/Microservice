/*
  $Name:        uf108taxgm.mac
  $Module:      Межбанковские расчеты
  $Description: Класс налоговых реквизитов для генерации сообщения ED108
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Описание     : Класс налоговых реквизитов для генерации сообщения ED108
//
// Программист  : Чукина Т.А.
//
// Создан       : 30.06.2015
//
//-----------------------------------------------------------------------------

import oralib, likepy, PaymInter, "lib_lang.mac", "pmbudg_lib.mac", OprInter;

class TTaxInfoED108(MainPaym : RsbPayment)

  private macro GetIsTaxAuthorStateForEach : bool
    PureVirtualFunc();
  end;

  private var
    // "головной" платеж (сводный платеж или платежное поручение на общую сумму)
    RsPaym : RsbPayment = MainPaym, 
    // Статус составителя заполнен для каждого платежа/перевода в составе общего платежа
    IsTaxAuthorStateForEach : bool = GetIsTaxAuthorStateForEach(),
    // КБК
    CommonBttTiCode : string = "",
    // ОКТМО
    CommonOkatoCode : string = "",
    // Статус составителя
    CommonTaxAuthorState : string = "",
    // заполнять статус составителя в записях реестра 
    IsTaxAuthorStateInReestr : bool = false;

  private macro GetDistinctTaxAuthorStateRs : RsdRecordset
    PureVirtualFunc();
  end;

  private macro IsPayTypeFilled : RsdRecordset
    PureVirtualFunc();
  end;

  private macro Init
    var IsEqualTaxAuthorState : bool = true;

    var rs : RsdRecordset = GetDistinctTaxAuthorStateRs();

    if(rs and rs.moveNext())
      // Первое значение запоминаем
      var TaxAuthorState : string = rs.value("t_TaxAuthorState"); 

      // Если есть еще другое значение - статусы составителя не во всех сведенных совпадают
      if(rs.moveNext())
        IsEqualTaxAuthorState = false;
      end;

      if( IsPayTypeFilled() )
        if( IsEqualTaxAuthorState and ( TaxAuthorState == "20" )  )
          CommonTaxAuthorState = "15";
        elif(RsPaym.TaxAuthorState != "")
          CommonTaxAuthorState = RsPaym.TaxAuthorState;
        elif(IsEqualTaxAuthorState)
          CommonTaxAuthorState = TaxAuthorState;
        end;
      end;      
    end;

    // CommonBttTiCode и CommonOkatoCode будут заполнены по результатам проверки 
    // CheckBtttiOkatoCodes() в Check()
  end;

  private macro Check
    PureVirtualFunc();
  end;

  macro MustWriteTaxInfo : bool
    if(RsPaym.TaxAuthorState or IsTaxAuthorStateForEach)
      return true;
    end;

    return false;
  end;

  macro GetCommonTaxAuthorState() : string
    return CommonTaxAuthorState;
  end;

  // Записать налоговые реквизиты в общую часть (DepartmentalInfo)
  macro WriteCommonPart(xml : object, mes : object)
    if( MustWriteTaxInfo() )
      var elem : object = xml.createElement("DepartmentalInfo");

      if(CommonTaxAuthorState)
        elem.setAttribute("DrawerStatus", CommonTaxAuthorState);
      end;

      if(not CommonBTTTICode)
        CommonBTTTICode = "0";
      end;
      elem.setAttribute("CBC", CommonBTTTICode );

      if(CommonOKATOCode)
        elem.setAttribute("OKATO", CommonOKATOCode );
      end;

      elem.setAttribute("PaytReason",   "0" );
      elem.setAttribute("TaxPeriod",    "0" );
      elem.setAttribute("DocNo",        "0" );
      elem.setAttribute("DocDate",      "0" );
      elem.appendChild(xml.createTextNode(""));

      mes.appendChild(elem);
    end;
  end;

  // Записать налоговые реквизиты по записи реестра (CTTDepartmentalInfo)
  macro WriteReestrRecord
  ( xml : object, mes : object,
    TaxAuthorState : string,
    TaxPmGround : string,
    TaxPmPeriod : string,
    PayType : integer,
    TaxPmNumber : string,
    TaxPmDate : string,
    TaxPmType : string,
    OFKAccount : string,
    FinAccount : string
  )
    macro setAttrNotEmpty(elem : object, AttrName : string, Attr : string)
      if(Attr)
        elem.setAttribute( AttrName, Attr );
      end;
    end;

    if( MustWriteTaxInfo() )
      if( InList(PayType, 3, 4) )
        TaxPmNumber = "0";
      end;

      var elem : object = xml.createElement("CTTDepartmentalInfo");

      if( IsTaxAuthorStateInReestr and (TaxAuthorState != "") )
        elem.setAttribute( "F101R", TaxAuthorState );
      end;

      elem.setAttribute( "F106R", Str0(TaxPmGround, true) );
      elem.setAttribute( "F107R", Str0(TaxPmPeriod, true) );
      elem.setAttribute( "F108R", Str0(TaxPmNumber, true) );
      elem.setAttribute( "F109R", Str0(TaxPmDate, true) );
      setAttrNotEmpty( elem, "F110R", TaxPmType );
      setAttrNotEmpty( elem, "F111R", OFKAccount );
      setAttrNotEmpty( elem, "F112R", FinAccount );

      elem.appendChild(xml.createTextNode(""));
      mes.appendChild(elem);
    end;
  end;

  // Constructor
  Init();
  Check();
end;

private macro GetUserYesNo(msg : string) : bool
  if( not IsOprMultiExec() and
      ( ConfWin( makeArray(msg, "Продолжить выгрузку?"), // Text
                 makeArray("Да", "Нет"), // Buttons
                 1 // Default
               ) == 0 )
    )
    return TRUE;
  end;

  return FALSE;
end;

// Сводный платеж Арм позиционера
private class (TTaxInfoED108) TTaxInfoED108_MultiPayment(MainPaym : RsbPayment)
  InitTTaxInfoED108(MainPaym);

  private macro GetDistinctTaxAuthorStateRs : RsdRecordset
    var q = "SELECT DISTINCT rm.t_TaxAuthorState " +
            "  FROM dpmlink_dbt lnk, dpmrmprop_dbt rm " +
            " WHERE lnk.t_InitialPayment = :MPPaymentID " +
            "   AND lnk.t_LinkKind       = :LinkKind " +
            "   AND rm.t_PaymentID       = lnk.t_PurposePayment ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect(q, params);

    return rs;
  end;

  private macro IsPayTypeFilled : bool


    var q = "SELECT COUNT( CASE WHEN pmpaym.t_PayType <> 0 THEN 1 ELSE 0 END ) Filled" +
            "  FROM dpmlink_dbt lnk, dpmpaym_dbt pmpaym " +
            " WHERE lnk.t_InitialPayment = :MPPaymentID " +
            "   AND lnk.t_LinkKind       = :LinkKind " +
            "   AND pmpaym.t_PaymentID   = lnk.t_PurposePayment ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect(q, params);

    if( rs and rs.moveNext() )
      return (rs.value("Filled") > 0);  
    end;

    return false;
  end;

  private macro GetIsTaxAuthorStateForEach : bool
    var sqlString = 
      "SELECT 1 FROM dual WHERE EXISTS "
                  "( SELECT 1 "
                  "    FROM dpmrmprop_dbt rm, dpmlink_dbt lnk "
                  "   WHERE lnk.t_InitialPayment = :PaymentID "
                  "     AND lnk.t_LinkKind = :LinkKind "
                  "     AND rm.t_PaymentID = lnk.t_PurposePayment "
                  "     AND ( rm.t_TaxAuthorState = CHR(1) "
                  "           or "
                  "           rm.t_TaxAuthorState IS NULL ) )";

    var params : TArray = makeArray( SQLParam("PaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",  PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect( sqlString, params );

    if( rs and rs.moveNext() )
      RETURN FALSE;
    end;

    RETURN TRUE;
  end;

  // Статус составителя заполнен хотя бы для одного платежа в составе сводного
  private macro IsTaxAuthorStateForAny : bool
    var sqlString = 
      "SELECT 1 FROM dual WHERE EXISTS "
                  "( SELECT 1 "
                  "    FROM dpmrmprop_dbt rm, dpmlink_dbt lnk "
                  "   WHERE lnk.t_InitialPayment = :PaymentID "
                  "     AND lnk.t_LinkKind = :LinkKind "
                  "     AND rm.t_PaymentID = lnk.t_PurposePayment "
                  "     AND rm.t_TaxAuthorState <> CHR(1) "
                  "     AND rm.t_TaxAuthorState IS NOT NULL )";

    var params : TArray = makeArray( SQLParam("PaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",  PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect( sqlString, params );

    if( rs and rs.moveNext() )
      RETURN TRUE;
    end;

    RETURN FALSE;
  end;

  private macro CheckBtttiOkatoCodes

    CommonBTTTICode = RsPaym.BTTTICode;
    CommonOKATOCode = RsPaym.OKATOCode;

    var q = "SELECT DISTINCT NVL(decode( paym.t_PayType, " +
            "                        4, decode(rm.t_BTTTICode, chr(1), '0'), " +
            "                           rm.t_BTTTICode ), chr(1)) as t_BTTTICode, " +
            "                NVL(rm.t_OKATOCode, chr(1)) as t_OKATOCode " +
            "  FROM dpmlink_dbt lnk, dpmpaym_dbt paym, dpmrmprop_dbt rm " +
            " WHERE lnk.t_InitialPayment = :MPPaymentID " +
            "   AND lnk.t_LinkKind       = :LinkKind " +
            "   AND paym.t_PaymentID     = lnk.t_PurposePayment " +
            "   AND rm.t_PaymentID       = lnk.t_PurposePayment ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );
    var rs : RsdRecordset = execSQLselect(q, params);

    if(rs and rs.moveNext())
      // Первый набор значений, запоминаем если пусто
      
      if(CommonBTTTICode == "")
        CommonBTTTICode = rs.value("t_BTTTICode"); 
      end;

      if(CommonOKATOCode == "")
        CommonOKATOCode = rs.value("t_OKATOCode");
      end;

      // Если есть еще один набор значений - ошибка
      if(IsTaxAuthorStateForEach and rs.moveNext())
        var ErrMsg : string = "В платежное поручение на общую сумму с реестром включаются платежи с одинаковыми налоговыми реквизитами (КБК и ОКТМО). Оставьте в составе сводного только платежи с совпадающими КБК и ОКТМО";
        RunError(ErrMsg, ErrMsg);
      end;
    end;

  end;

  private macro CheckTaxPmGroundAndType
    var q = "SELECT DISTINCT rm.t_TaxPmGround " +
            "  FROM dpmlink_dbt lnk, dpmrmprop_dbt rm " +
            " WHERE lnk.t_InitialPayment = :MPPaymentID " +
            "   AND lnk.t_LinkKind       = :LinkKind " +
            "   AND rm.t_PaymentID       = lnk.t_PurposePayment ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );
    var rs : RsdRecordset = execSQLselect(q, params);

    if(rs and rs.moveNext())
      // Если есть еще один набор значений - ошибка
      if(rs.moveNext())
        var ErrMsg = "Для распоряжений о переводе средств в бюджет, администрируемых налоговыми органами, допускается только одно значение основания платежа. В платежах, включенных в состав сводного, значения этого реквизита не совпадают. ";
        if( not GetUserYesNo(ErrMsg) ) 
          RunError(ErrMsg, ErrMsg);
        end;
      end;
    end;

  end;

  private macro GetPayType : integer
    var PayType : integer = 0;

    var q = "SELECT pm.t_PayType " +
            "  FROM dpmlink_dbt lnk, dpmpaym_dbt pm " +
            " WHERE lnk.t_InitialPayment = :MPPaymentID " +
            "   AND lnk.t_LinkKind       = :LinkKind " +
            "   AND pm.t_PaymentID       = lnk.t_PurposePayment " +
            "   AND ROWNUM = 1 ";

    var params : TArray = makeArray( SQLParam("PaymentID", RsPaym.PaymentID),
                                     SQLParam("LinkKind",  PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect(q, params);
    if(rs and rs.moveNext())
      PayType = rs.value("t_PayType");
    end;

    return PayType;
  end;

  private macro CheckPayType
    var PayType : integer = GetPayType();

    if(PayType == BPT_TAX)
      CheckTaxPmGroundAndType();
    elif(PayType == BPT_CUSTOM)
      var ErrMsg = "Для распоряжений о переводе средств в бюджет, администрируемых таможенными органами, не предусмотрены платежи на общую сумму с реестром. ";
      if( not GetUserYesNo(ErrMsg) ) 
        RunError(ErrMsg, ErrMsg);
      end;
    end;
  end;

  macro Check
    var ErrMsg : string = "";

    // Если статус составителя не заполнен для каждого, то не должен быть заполнен
    // ни для одного
    if(not IsTaxAuthorStateForEach)
      if( IsTaxAuthorStateForAny() )
        ErrMsg = "В платежном поручении на общую сумму с реестром не должны одновременно присутствовать платежи с заполненным и незаполненным статусом составителя. Оставьте в составе сводного только платежи с заданным статусом составителя, либо только с незаданным.";
        RunError(ErrMsg, ErrMsg);
      end;
    end;

    if(IsTaxAuthorStateForEach or RsPaym.TaxAuthorState)
      CheckBtttiOkatoCodes();
    end;

    CheckPayType();
  end;
end;

// Платеж на общую сумму по переводам физ.лиц
private class (TTaxInfoED108) TTaxInfoED108_BdTrSummary(MainPaym : RsbPayment)
  InitTTaxInfoED108(MainPaym);

  private macro GetDistinctTaxAuthorStateRs : RsdRecordset
    var q = "SELECT DISTINCT t_TaxAuthorState " +
            "  FROM dbdtransf_dbt " +
            " WHERE t_CoverPaymentID = :MPPaymentID ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID) );

    var rs : RsdRecordset = execSQLselect(q, params);

    return rs;
  end;

  private macro IsPayTypeFilled : bool
    return true;
  end;

  private macro GetIsTaxAuthorStateForEach : bool
    var IsForEach : bool = true; // Сначала предполагаем, что для всех заполнено

    // Проверяем, нет ли такого, для кого пусто
    var rs : RsdRecordset = execSQLselectPrm
    ( "select 1 "
      "  from dual "
      " where exists "
      "       ( select 1 "
      "           from dbdtransf_dbt "
      "          where t_CoverPaymentID = :CoverPaymentID "
      "            and (t_TaxAuthorState = chr(1) or t_TaxAuthorState is null) "
      "       ) ",
      SQLParam("CoverPaymentID", RsPaym.PaymentID)
    );

    if(rs and rs.moveNext())
      IsForEach = false;
    end;

    // Результат
    return IsForEach;
  end;

  private macro CheckBtttiOkatoCodes

    CommonBTTTICode = RsPaym.BTTTICode;
    CommonOKATOCode = RsPaym.OKATOCode;

    var q = "SELECT DISTINCT NVL(t_BTTTICode, chr(1)) as t_BTTTICode, " +
            "                NVL(t_OKATOCode, chr(1)) as t_OKATOCode " +
            "  FROM dbdtransf_dbt " +
            " WHERE t_CoverPaymentID = :MPPaymentID ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID) );
    var rs : RsdRecordset = execSQLselect(q, params);

    if(rs and rs.moveNext())
      if(CommonBTTTICode == "")
        CommonBTTTICode = rs.value("t_BTTTICode"); 
      end;

      if(CommonOKATOCode == "")
        CommonOKATOCode = rs.value("t_OKATOCode");
      end;

      // Если есть еще один набор значений - ошибка
      if(rs.moveNext())
        var ErrMsg : string = "В платежное поручение на общую сумму с реестром включаются платежи с одинаковыми налоговыми реквизитами (КБК и ОКТМО). Оставьте в составе сводного только платежи с совпадающими КБК и ОКТМО";
        RunError(ErrMsg, ErrMsg);
      end;
    end;

  end;

  macro Check
    CheckBtttiOkatoCodes();
  end;
end;


macro GetTaxInfoED108(MainPaym : RsbPayment) : TTaxInfoED108
  var TaxInfo : TTaxInfoED108 = null;

  if( MainPaym.DocKind == DLDOC_MULTYPM )
    TaxInfo = TTaxInfoED108_MultiPayment(MainPaym);
  else
    TaxInfo = TTaxInfoED108_BdTrSummary(MainPaym);
  end;

  return TaxInfo;
end;
