//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Описание     : Класс для переустановки платежных инструкций
//                по данным из сообщения
//
// Программист  : Чукина Т.А.
//
// Создан       : 28.11.2013
//
//-----------------------------------------------------------------------------

import PaymInter, "wlgendoc.mac", "likepy.mac", "wlgenmes.mac";

  CLASS TPIAttrs
    var ClientName    : String = "",
        ClientAddress : String = "",
        Account       : String = "",
        BankCode      : String = "",
        CorrAccNostro : String = "",
        ClientINN     : String = "",
        ClientKPP     : String = "";
  END;

CLASS TFieldsPI(p_Payment : RsbPayment)

  CLASS TPaymentSide
    var OldValues : TPIAttrs = TPIAttrs(),
        NewValues : TPIAttrs = TPIAttrs(),
        IsSetPI   : bool = false;
  END;

  var Payer   : TPaymentSide = TPaymentSide(),
      Payee   : TPaymentSide = TPaymentSide(),
      Payment : RsbPayment   = p_Payment;

  PRIVATE MACRO CopyPaymAttrs(Values : TPIAttrs, Side : integer)
    if(Side == PRT_Debet)
      Values.ClientName    = Payment.PayerName;
      Values.ClientAddress = GetAddressFromPaymSideName(Payment.PayerName);
      Values.Account       = Payment.PayerAccount;
      Values.BankCode      = Payment.PayerBankCode;
      Values.CorrAccNostro = Payment.PayerCorrAccNostro;
      Values.ClientINN     = RemoveKPP(Payment.PayerINN);
      Values.ClientKPP     = RemoveINN(Payment.PayerINN);
    elif(Side == PRT_Credit)
      Values.ClientName    = Payment.ReceiverName;
      Values.ClientAddress = GetAddressFromPaymSideName(Payment.ReceiverName);
      Values.Account       = Payment.ReceiverAccount;
      Values.BankCode      = Payment.ReceiverBankCode;
      Values.CorrAccNostro = Payment.ReceiverCorrAccNostro;
      Values.ClientINN     = RemoveKPP(Payment.ReceiverINN);
      Values.ClientKPP     = RemoveINN(Payment.ReceiverINN);
    else
      RunError("Ошибочное значение параметра Side");
    end;
  END;

  // конструктор
  CopyPaymAttrs(Payer.OldValues, PRT_Debet);
  CopyPaymAttrs(Payer.NewValues, PRT_Debet);
  CopyPaymAttrs(Payee.OldValues, PRT_Credit);
  CopyPaymAttrs(Payee.NewValues, PRT_Credit);

  private macro GetNewPaymSideName(PaymSide : TPaymentSide) : string
    if( (PaymSide.NewValues.ClientAddress != PaymSide.NewValues.ClientAddress)
        or
        ( (GetAddressFromPaymSideName(PaymSide.NewValues.ClientName) == "") and
          (PaymSide.NewValues.ClientAddress != "")
        )
      )
      var NameOnly = RemoveAddressFromPaymSideName(PaymSide.NewValues.ClientName);
      return ConstructPaymSideName(NameOnly, PaymSide.NewValues.ClientAddress);
    end;

    return PaymSide.NewValues.ClientName;
  end;

  MACRO ResetPI(Side : integer) : bool
    var PaymSide : TPaymentSide = null;
    var stat : integer = 0;

    if(Side == PRT_Debet)
      PaymSide = Payer;
      stat = Payment.SetPayerPI( Payment.PayerGroup, 
                                 IfThenElse( PaymSide.OldValues.BankCode == PaymSide.NewValues.BankCode,
                                             Payment.PayerBankID, 
                                             PTID_UNKNOWNPARTY ),
                                 Payment.PayerBankCodeKind, 
                                 PaymSide.NewValues.BankCode, 
                                 IfThenElse( PaymSide.OldValues.BankCode == PaymSide.NewValues.BankCode,
                                             Payment.PayerBankName, 
                                             "" ),
                                 PaymSide.NewValues.CorrAccNostro, 
                                 Payment.PayerFIID, 
                                 Payment.Chapter, 
                                 PaymSide.NewValues.Account,
                                 IfThenElse( PaymSide.OldValues.Account == PaymSide.NewValues.Account,
                                             Payment.Payer, 
                                             null ),
                                 GetNewPaymSideName(PaymSide),
                                 ConstructINN(PaymSide.NewValues.ClientINN, PaymSide.NewValues.ClientKPP)
                               );
    elif(Side == PRT_Credit)
      PaymSide = Payee;
      stat = Payment.SetReceiverPI
                               ( Payment.ReceiverGroup, 
                                 IfThenElse( PaymSide.OldValues.BankCode == PaymSide.NewValues.BankCode,
                                             Payment.ReceiverBankID, 
                                             PTID_UNKNOWNPARTY ),
                                 Payment.ReceiverBankCodeKind, 
                                 PaymSide.NewValues.BankCode, 
                                 IfThenElse( PaymSide.OldValues.BankCode == PaymSide.NewValues.BankCode,
                                             Payment.ReceiverBankName, 
                                             "" ),
                                 PaymSide.NewValues.CorrAccNostro, 
                                 Payment.ReceiverFIID, 
                                 Payment.Chapter, 
                                 PaymSide.NewValues.Account,
                                 IfThenElse( PaymSide.OldValues.Account == PaymSide.NewValues.Account,
                                             Payment.Receiver, 
                                             null ),
                                 GetNewPaymSideName(PaymSide),
                                 ConstructINN(PaymSide.NewValues.ClientINN, PaymSide.NewValues.ClientKPP)
                               );
    else
      RunError("Ошибочное значение параметра Side");
    end;

    if(stat == 0)
      PaymSide.IsSetPI = true;
      CopyPaymAttrs(PaymSide.NewValues, Side);
    else
      PaymSide.IsSetPI = false;
    end;

    return PaymSide.IsSetPI;
  END;

  macro destructor()
    Payment = null;
  end;

END;
