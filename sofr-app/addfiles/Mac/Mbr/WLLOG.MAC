/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Функции стандартного вывода макросов МБР                                */
/*                                                                          */
/*  Имя файла: wllog.mac                                                    */
/*  Создан:  21.01.00                                        Васильев Д.В.  */
/****************************************************************************/

import BankInter, "globals.mac", MesInter;

var ПутьНастройкиУровняДетализации = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ПЕРЕМЕННЫЕ\\DETAILSLEVEL";

var QTMode = GetBankCaseItem() == 777; /* флаг запуска в режиме Q-тестов, чтобы предотвратить разрыв лога вывода результатов */

/* Глобальная переменная (регистрозависимая). Содержит пользовательскую ошибку,
   возникшую при выполнении макроса. Используется программой для построения отчетов,
   при групповых режимах обработки данных (безынтерфейсный режим) */
var WldMacroErrStr:String = "";

const WLD_REG_OK = 0;

class TLogStream( _echoflag )
  var Details:integer,
      EchoFlag:bool;

  macro SetEcho( _echoflag )
    var oldecho = EchoFlag;
    if( _echoflag )
      EchoFlag = true;
    else
      EchoFlag = false;
    end;
    return oldecho;
  end;

  macro Out( Lvl, Str )
    if( not QTMode and ( Lvl <= Details ) )
      println(Str);
    end;

    ToRSTrace(Str);
  end;

  macro SetDetailsLevel
    var err;
    GetRegistryValue( ПутьНастройкиУровняДетализации, V_INTEGER, Details, err );
    if( err != WLD_REG_OK )
      Details = 1;
      if( not QTMode )
        out(1, "Не определена переменная УРОВЕНЬ ДЕТАЛИЗАЦИИ ОТЧЕТОВ в настройках банка" );
      end;
    end;
  end;

  macro Init( _echoflag )
    SetEcho(_echoflag);
    SetDetailsLevel;
    if( Details )
      if( not QTMode )
        SetOutput( NULL, true ) ;
      end;
    end ;
    out(2,"Уровень детализации отчета: "+string(Details)+"\n");
  end;

  macro Err( Lvl, Str )
    if( not QTMode )
      out( Lvl, "Ошибка        : "+Str);
    end;
  end;

  macro Warn( Lvl, Str )
    if( not QTMode )
      out( Lvl, "Предупреждение: "+Str);
    end;
  end;

  macro View
    var logfile;
    FILE outfile() txt ;
    if( ( not QTMode ) and Details )
      logfile = SetOutput(NULL) ;
      if( open( outfile, logfile ) )
        ViewFile( outfile ) ;
        close( outfile ) ;
      end ;
    end;
  end;

  macro Msg( Str )
    if( not QTMode ) 
      WldMacroErrStr = String( Str );

      if( EchoFlag AND GetDialogFlag() )
        MsgBox(str);
      else                                            
        out( 1, StrSubst( str, "|", " " ) );
      end; 
    end
  end;

  macro MsgErr( Str )
    if( not QTMode ) 
      WldMacroErrStr = String( Str );

      if( EchoFlag AND GetDialogFlag() )
        MsgBox(str);
      else
        err( 1, StrSubst( str, "|", " " ) );
      end;
    end;
  end;

  macro RSBankHeader
    if( not QTMode )
      out(1,"===================================================== "
                 +{oper}+" "+string({curdate})+" "+string(time));
    end;
  end;

  macro RSBankFooter
    if( not QTMode )
      out(1,"== Конец отчета ==============================================================");
    end;
  end;

  /* construct */
  Init( _echoflag );

end; /* class LogFile */

var std = TLogStream( true );

macro PrintLog( Lvl, Str )
  if( not QTMode )
    std.out( Lvl, Str );
  end;
end;

macro PrintWarn( Lvl, Err )
  if( not QTMode )
    std.warn( Lvl, Err );
  end;
end;

macro ViewLog
  if( not QTMode )
    std.view;
  end;
end;
