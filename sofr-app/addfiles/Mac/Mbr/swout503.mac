 /*
 $Name: swout503.mac
 $Module: Межбанковские расчеты
 $Description: Экспорт сообщений SWIFT503
 */
import "swiftout.mac", "ufgenmes.mac", rsexts, "arm_kbr_tools.mac";

private const ZeroDate = date(0,0,0);

private const WrongED503MessageDirection = 1;
private const WrongED503MessageType = 2;
private const ReceiverIsNotSPFSParticipant = 1;
private const ReceiverIsSPFSParticipantButLater = 2;
private const ReceiverIsNotSPFSParticipantSinceDate = 3;

macro GetEDReceiver503(RecipientID, RecipientCodeKind, RecipientCode)
  return GetEDReceiver(RecipientID, RecipientCodeKind, RecipientCode);
onerror()
  ErrExport("Не определен УИС получателя сообщения ED503. Для получателя сообщения " +
      ПолучитьКодСубъекта( RecipientID, PTCK_CONTR ) + Bnk_GetPartyName(RecipientID) +
      " задайте код вида 47 UIS, после чего повторите экспорт");
  return false;
end;

macro GetTerminalSessionNum(Num)
  var d, m;
  DateSplit(date(), d, m);
  if (d < 10) d = "0" + string(d); end;
  if (m < 10) m = "0" + string(m); end;
  return string(d) + string(m) + Num;
end;

macro CheckExistingTSN( SessionTSN, TerminalSessionNum )
  var i = 0;

  while( i < SessionTSN.size )
    if( (strlen(TerminalSessionNum) > 0) and (TerminalSessionNum == SessionTSN[i]))
      return true;
    end;
    i = i + 1;
  end;
  SessionTSN[SessionTSN.size] = TerminalSessionNum;
  return false;
end;

private macro GetMessageAuthor( WlMessages )
  var lastMesStartDepartment;
  var allMesOneDepartment = true;
  var regVal = 0;

  if( (WlMessages) and (WlMessages.size > 0) )
    lastMesStartDepartment = WlMessages[0][1];
    for( var Message, WlMessages )
      if( lastMesStartDepartment != Message[1] )
        allMesOneDepartment = false;
      end;
    end;
  end;

  if( allMesOneDepartment )
    if( GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ЦЕНТРАЛИЗОВАННЫЙ ОБМЕН\\EDAUTHOR_ЭСИСЭИС", V_STRING, regVal) == V_INTEGER )
      if( regVal == 1 )
        return WlMessages[0][1]; // StartDepartment 
      elif( regVal == 2 )
        return WlMessages[0][2]; // Department
      end;
    end;      
  end;

  return WlMessages[0][2]; // Department
end;

private macro ED503MesSendingCheck( OutsideAbonentID, MesAuthor, retMessageTypes: @string )

  var DepartmentSubjectCode = ПолучитьКодСубъекта( GetDprtPartyID( MesAuthor ), PTCK_SWIFT, null, 1 );
  var query : string = 
    "select t_MessageType " +
    "  from dwlusspfs_dbt " +
    " where t_ObjectType    = :ObjectType " +
    "   and t_ObjectID      = :ObjectID " +
    "   and t_ED503Exchange = 'X' " +
    "   and ( t_SWIFTSenderCode = :DepSbjCode " +
    "    or   t_SWIFTSenderCode = 'ZZZZRUZZZZZ' ) ";
  var params : TArray = makeArray( SQLParam("ObjectType", OBJTYPE_PARTY),
                                   SQLParam("ObjectID",   OutsideAbonentID),
                                   SQLParam("DepSbjCode", DepartmentSubjectCode ) );
  var rs : RsdRecordset = execSQLselect( query, params );

  if( rs and rs.moveNext() )
    retMessageTypes = rs.Value( "t_MessageType" );
    return 0;
  end;

  return WrongED503MessageDirection;    
end;

private macro IsCorrectMessageTypeForED503( MesID: integer, MessageTypes: string )

  if( MessageTypes == "99999" )
    return 0;
  end;

  var mesfrmquery : string = 
    "select frm.t_Name " +
    "  from dwlmesfrm_dbt frm, dwlmesrls_dbt rls, dwlmes_dbt mes " +
    " where frm.t_FormID = rls.t_FormID "
    "   and rls.t_RlsFormID = mes.t_RlsFormID "
    "   and mes.t_MesID = :MesID ";
  var mesfrmparams : TArray = makeArray( SQLParam("MesID", MesID) );
  var mesfrmrs : RsdRecordset = execSQLselect( mesfrmquery, mesfrmparams );

  if( mesfrmrs and mesfrmrs.moveNext() )
    var tmpMessageType = MessageTypes;
    var indx = index( tmpMessageType, "MT" );
    var tmpMesFrmName = "";
    while( indx != 0 )  
      tmpMesFrmName = Substr( tmpMessageType, indx, 5 );
      tmpMessageType = SubStr( tmpMessageType, 1, indx - 1 ) + SubStr( tmpMessageType, indx + 5 );
      if( RegExMatch( tmpMesFrmName, "MT\\d{3}" ) == true )  
        if( Substr(tmpMesFrmName, 3, 3) == mesfrmrs.Value( "t_Name" ) )
          return 0;
        end;
      end;
      indx = index( tmpMessageType, "MT" );
    end;
  end;

  return WrongED503MessageType; 
end;

private macro IsReceiverSPFSParticipant( OutsideAbonentID: integer, ED503Date: @date, ESSize503: @integer )
  var query : string = 
    "select t_BeginDate503, t_EndDate503, t_ESSize503 " +
    "  from dptspfs_dbt " +
    " where t_PartyID = :PartyID ";
  var params : TArray = makeArray( SQLParam("PartyID", OutsideAbonentID ) );
  var rs : RsdRecordset = execSQLselect( query, params );

  if( rs and rs.moveNext() )
    ESSize503 = rs.Value( "t_ESSize503" );
    if( rs.value( "t_BeginDate503" ) > {BranchCurDate} )
      ED503Date = rs.value( "t_BeginDate503" );
      return ReceiverIsSPFSParticipantButLater;
    end;
    if( (rs.value( "t_EndDate503" ) < {BranchCurDate}) and ( rs.value( "t_EndDate503" ) != ZeroDate ) )
      ED503Date = rs.value( "t_EndDate503" );
      return ReceiverIsNotSPFSParticipantSinceDate;
    end;
    return 0;
  end;

  return ReceiverIsNotSPFSParticipant;
end;

macro ExportMessage( ExportFileName, addrSess )

  var continue0 = 1, Документов = 0, err, tsn, regVal;
  var SessionTSN = TArray();
  SetBuff( wlsess, addrSess );
  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if( not err )
      ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;

  var WlmesDepartment = wlmes.Department;
  var WlmesStartDepartment = wlmes.StartDepartment;
  var WlMesTRN = wlmes.TRN;

  var UseArmKBR = GetTpSchemSign(wlmes.TpSchemID);  

  if( ((ОпределитьФорматТранспорта( 2, ВидТерминала )) == 0) OR (ВидТерминала == "" ) )
    ErrExport( string("Не найден формат по умолчанию для траснпорта №2 ") );
    return false;
  end;

  var xml : object = ActiveX("Microsoft.XMLDOM");  
  xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='UTF-8'") );
  var mes : object = xml.createElement("ED503");
  xml.appendChild(mes);
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  FillEDNoDateAuthorByRef_XMLField(mes, wlsess.SessUID);
  err = GetEDReceiver503(wlmes.OutsideAbonentID, wlmes.OutsideAbonentCodeKind, wlmes.OutsideAbonentCode);
  if (err == false)
    return false;
  end;
  mes.setAttribute("ActualReceiver", err );
  mes.setAttribute("ReceiverSWIFTBIC", ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_SWIFT, err ));
  if ( err )
    ErrExport( "Не определен код вида 6 \"BIC ISO (SWIFT)\" банка-получателя. Задайте БИК SWIFT для получателя сообщения, после чего повторите экспорт" );
    return false;
  end;
  mes.setAttribute("SenderSWIFTBIC", ПолучитьКодСубъекта( GetDprtPartyID(wlmes.Department), PTCK_SWIFT, err ));
  if ( err )
    ErrExport( "Не определен код вида 6 \"BIC ISO (SWIFT)\" банка-отправителя. Задайте БИК SWIFT для отправителя сообщения, после чего повторите экспорт" );
    return false;
  end;

  var stat = 0;

  var ED503Date = ZeroDate;
  var ESSize503 = 0;
  stat = IsReceiverSPFSParticipant( wlmes.OutsideAbonentID, @ED503Date, @ESSize503 );

  if( stat == ReceiverIsNotSPFSParticipant )
    ErrExport("Получатель с BIC SWIFT " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_SWIFT, null, 1 ) + " не является участником обмена сообщениями ED503. | Отправка сообщения запрещена.");    
    return false;
  end;

  if( stat == ReceiverIsSPFSParticipantButLater )
    ErrExport("Получатель с BIC SWIFT " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_SWIFT, null, 1 ) + " является участником обмена сообщениями ED503 c " + 
               ED503Date + ". | Отправка сообщения до даты начала обмена данным получателем запрещена.");
    return false;
  end;

  if( stat == ReceiverIsNotSPFSParticipantSinceDate )
    ErrExport("Получатель с BIC SWIFT " + ПолучитьКодСубъекта( wlmes.OutsideAbonentID, PTCK_SWIFT, null, 1 ) + " не является участником обмена сообщениями ED503 c " + 
               ED503Date + ". | Отправка сообщения после даты окончания обмена с данным получателем запрещена.");
    return false;
  end;

  var elem : object = null;

  var SWIFTBase64;
  var WlMessages  = TArray(TArray());
  /* Последовательно считываем сообщения и формируем файл экспорта */
  while( continue0 )
    var refValue = "";

    if( GenerateReference(255, refValue) != 0 )
      ErrExport( "Ошибка при получении значения референса \"Формирование последовательности для TerminalSessionNum в ED503\"" );
      return false;
    end;
  
    tsn = GetTerminalSessionNum(refValue);

    if( CheckExistingTSN( SessionTSN, tsn ))
      ErrExport( "Найдено совпадение идентификаторов TerminalSessionNum для сообщений, включенных в один пакет.\r"
                 "Рекомендуется проверить корректность настройки используемого описателя референса и сформировать пакет повторно.");
      return false;
    end;
  
    // SWIFTContainer
    elem = xml.createElement("SWIFTContainer");

    elem.setAttribute("TerminalSessionNum", tsn);
    wlmes.RelatedRef = tsn;
    ОбновитьЗапись(wlmes);

    WlDefineForm(wlmes.RlsFormID);
    elem.setAttribute("FormatType", "MT" + f_wlmesrls.Name);
    if( not ЗаписатьЗаголовок() ) return FALSE; end;

    НомерСессии = SubStr(tsn, 1, 4);
    ISN = SubStr(tsn, 5, 6);

    if( not ЗаписатьСообщение( false ) )
      return false;
    end;

    if ((GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ\\ЗАПОЛНЯТЬ_БЛОК_ТРЕЙЛЕРОВ", V_BOOL, regVal) == V_BOOL) and regVal)
      var code;
      if ((GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ\\ТРЕЙЛЕР", V_STRING, code) != V_STRING) or (code == ""))
        ErrExport("Не настроен порядок формирования блока трейлеров. Проверьте заполнение настроек в ветке МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ");
        return false;
      end;
      if ((GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ\\МАКРОФАЙЛ", V_STRING, regVal) == V_STRING) and (regVal != ""))
        regVal = ExecMacroFile(regVal, "ПолучитьЗначениеТрейлера", wlmes);
      elif ((GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ\\ЗНАЧЕНИЕ", V_STRING, regVal) != V_STRING) or (regVal == ""))
        ErrExport("Не настроен порядок формирования блока трейлеров. Проверьте заполнение настроек в ветке МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\БЛОК ТРЕЙЛЕРОВ");
        return false;
      end;
      if (index(code, КодНачалаПоля) != strlen(code))
        code = code + КодНачалаПоля;
      end;
      if (not СохранитьБлок(КодНачалаБлока+НомерБлокаTrailer+КодРазделительНомера+КодНачалаБлока+code+regVal+КодКонцаБлока+КодКонцаБлока))
        ErrExport( "Ошибка при записи блока Trailer" );
        return FALSE;
      end;
    end;

    WlMessages[WlMessages.size] = TArray();
    WlMessages[WlMessages.size-1][0] = wlmes.MesID;
    WlMessages[WlMessages.size-1][1] = wlmes.StartDepartment; 
    WlMessages[WlMessages.size-1][2] = wlmes.Department;
    WlMessages[WlMessages.size-1][3] = wlmes.OutsideAbonentID;
    WlMessages[WlMessages.size-1][4] = wlmes.OutsideAbonentCodeKind;
    WlMessages[WlMessages.size-1][5] = wlmes.OutsideAbonentCode;

    if( not СчитатьЗапись( wlmes, err, false ) )
      if ( not err )
        continue0 = 0;
      else
        ErrExport("Ошибка чтения сообщения");
        return false;
      end;
    else
      if( not UseArmKBR )
        UseArmKBR = GetTpSchemSign(wlmes.TpSchemID);
      end;
    end;
    
    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );

    if( not ЗаписатьКонцовку() )
      return false;
    end;
    EncodeBase64(join(out, "\r\n"), SWIFTBase64);
    var swift = xml.createElement("SWIFTDocument");
    swift.appendChild(xml.createTextNode(SWIFTBase64));
    elem.appendChild(swift);
    mes.appendChild(elem);
  end;
  
  // Проверки сообщений
  var MesAuthor;
  var MessageTypes = "";

  if( WlMessages and ( WlMessages.size > 0 ) )
    MesAuthor = GetMessageAuthor( WlMessages );
                           /*wlmes.OutsideAbonentID*/
    stat = ED503MesSendingCheck( WlMessages[0][3], MesAuthor, @MessageTypes );
    if( stat == WrongED503MessageDirection )
                           /*  wlmes.OutsideAbonentID, wlmes.OutsideAbonentCodeKind, wlmes.OutsideAbonentCode */
      var EDReceiver503 = GetEDReceiver503( WlMessages[0][3], WlMessages[0][4], WlMessages[0][5] );
      ErrExport("Не допускается направление сообщений ED503 получателю с УИС " + EDReceiver503 + 
                ". | Если с данным получателем заключен договор об обмене обновите данные справочника СПФС, направив запрос ED573 с видом 3, после чего повторите экспорт");   
      return false;
    end;

    for( var Message, WlMessages )
      stat = IsCorrectMessageTypeForED503( Message[0], MessageTypes );
      if( stat == WrongED503MessageType )                             /*wlmes.OutsideAbonentID*/
        ErrExport("В адрес получателя с BIC SWIFT " + ПолучитьКодСубъекта( Message[3], PTCK_SWIFT, null, 1 ) + " в составе ED503 допускается отправка сообщений только следующих типов " + 
                   MessageTypes + ". | Исключите из перечня выгружаемых сообщений недопустимые типы и повторите экспорт");    
        return false;
      end;
    end;
  end;

  mes.setAttribute("SWIFTContainerQuantity", string(Документов));

  if( UseARMKBR )
    var xml_string = StrSubst(xml.xml, "<?xml version=\"1.0\"?>", "<?xml version=\"1.0\" encoding=\"WINDOWS-1251\"?>");
    xml_string = ToANSI(xml_string, true);

    var conditions = GetConditions( wlsess.SessionID, xml.childNodes.item(1).baseName, WlmesStartDepartment, WlmesDepartment );

    var ids = makeArray(string(wlsess.SessionID));
    var xmls = makeArray(xml_string);
                            
    var outIDs = TArray;
    var outXMLs = TArray;
    var checkStats = TArray;
    var checkErrTexts = TArray;
    var error : string;

    var SigningResult = 0;
    var SigningError = ""; 

    SigningResult = BNK_UFEBS_makeZK(ids, xmls, OemToUtf8( conditions ), false, outIDs, outXMLs, checkStats, checkErrTexts, SigningError);
      
    if(SigningResult != 0)

      if( SigningError == "" )
        SigningError = "Ошибка при подписании ЗК: " + string(SigningResult);
      else
        SigningError = string(SigningResult) + " " + SigningError;
      end;  

      ErrExport( SigningError );
      return false;
    elif(outXMLs.size > 0)
      SigningResult = checkStats(0);    
      SigningError = checkErrTexts(0);
    end;

    if(SigningResult != 0)    

      if( SigningError == "" )
        SigningError = "Ошибка при подписании ЗК: " + string(SigningResult);
      else
        SigningError = string(SigningResult) + " " + SigningError;
      end;

      ErrExport( SigningError );
      return false;
    else
      xml_string = outXMLs(0);
    end;
    
    if( saveXMLInSessionData( xml_string, wlsess.SessionID ) )
      ErrExport( "Ошибка при сохранении данных сеанса" );
      return false;
    end;

    var update = "";
    var parm : TArray = TArray;  
    
    parm[0] = SQLParam("State", 30);
    parm[1] = SQLParam("MessageType", xml.childNodes.item(1).baseName);
    parm[2] = SQLParam("SessionID", wlsess.SessionID);
    
    update = "UPDATE dwlsess_dbt SET t_State = :State, t_MessageType = :MessageType where t_SessionID = :SessionID ";
       
    if( not execSQL(update, parm, true) )
      ErrExport( "Ошибка при установке статуса сеанса" );
      return false;
    end;

  else
    xml.Save(ExportFileName);
  end;
    
  var rsize: integer;
  var size: integer = 0;
  if( ESSize503 and (ESSize503 > 0) )
    size = ESSize503;
  elif( GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED503\\MAXSIZEMESS", V_INTEGER, size ) == V_INTEGER )
    size = size * 1024;
  end;
  
  if( not UseArmKBR )  
    GetFileInfo(ExportFileName, NULL, NULL, rsize);
  else
    rsize = strlen( xml_string );
  end;

  if ( rsize > size )
    ErrExport("Превышен максимально допустимый размер сообщения ED503");
    return false;
  end;
  
  return TRUE; /* Успешное завершение */
OnError(er) /* Обработка ошибок времени выполнения */
  ErrExport(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
  return FALSE;
end;                          
