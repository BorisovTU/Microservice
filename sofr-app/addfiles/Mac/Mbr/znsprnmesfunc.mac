 /*
 $Name: znsprnmesfunc.mac
 $Module: МБР
 $Description: Общие функции печатных форм запросов ФНС  
 */

import "fnsznsalx.mac", "wlglobal.mac";

// конвертировать дату из сообщения в формате гггг-мм-дд в тип date
macro ParseStringToDate(sdate)

  return date( int(substr(sdate, 9, 2)), int(substr(sdate, 6, 2)), int(substr(sdate, 1, 4)) );

end;

// Заполнить дату в формате <дд> месяц гггг г. из даты из типа date
macro FillFullDateFromDateToString(resdate):string

  return  string( "\"", 
                  SubStr( string(resdate:m:f), 1, 2 ), 
                  "\"", 
                  SubStr( string(resdate:m:f), 3, strlen(string(resdate:m:f) ) - 2 )
                 ) ;
end;

// Заполнить дату в формате <дд> месяц гггг г. из даты из сообщения в формате гггг-мм-дд
macro FillFullDateFromMassegeString(sdate):string

  return  FillFullDateFromDateToString(ParseStringToDate(sdate));
  
end;

macro GetAddress( PartyID, address )
  
  if( НайтиЮридическийАдресСубъекта(PartyID, address) or 
      НайтиАдресСубъекта(PartyID, PTADDR_REAL, address) or
      НайтиПочтовыйАдресСубъекта(PartyID, PTADDR_POST, address)
    )
    return true;
  end;

  return false;
end;

macro GetLawReference(Code, AlgNum)
  var codeparm;
  if ( substr(Code, 1, 1) == "1")
    codeparm = 1;
  elif ( substr(Code, 2, 1) == "1")
    codeparm = 2;
  elif ( substr(Code, 3, 1) == "1")
    codeparm = 3;
  end;
  
  FILE wlcode(wlcodes) key 1;
  ClearRecord(wlcode);
  wlcode.AlgNum = AlgNum;
  wlcode.Code   = codeparm;
  if(getEQ(wlcode))
    return wlcode.Description;
  end;
  return "";
end;

macro GetClientName(this: FnsMessageFormZSN)
  var name = "";
  if (StrLen(this.НаимЮЛ) > 0)
    name = this.НаимЮЛ;
  elif (StrLen(this.ПлФЛ_ФИО) > 0)
    name = this.ПлФЛ_ФИО;
  elif (StrLen(this.ФИОИП) > 0 )
    name = this.ФИОИП;
  end;  
  return name;
end;

macro GetClientINN( this : FnsMessageFormZSN )
  var INN : string = "";
     if( ИННЮЛ )
       INN = ИННЮЛ;
     elif( ИННФЛ )
       INN = ИННФЛ;
     elif( ИННИП )
       INN = ИННИП;
     end;
  return INN;
end;

macro GetBankDetails(this: FnsMessageFormZSN)
  
  return "ИНН " + this.ИННБанк + 
         ", КПП " + this.КППБанк +
         ", БИК " + this.БИК;   
end;

macro GetReqKind(this: FnsMessageFormZSN)
  
  var ReqKind = "";
  if ( this.ПризнЗапр == "1" )
    ReqKind = "в банке с учетом информации филиалов банка";
  elif ( this.ПризнЗапр == "2" )
    ReqKind = "в банке без учета информации филиалов банка";
  elif ( this.ПризнЗапр == "3" )
    ReqKind = "в указанном филиале банка";
  end;
  return ReqKind;
end;

macro GetReqPeriod(this: FnsMessageFormZSN)

  var ReqPeriod = "";
  if (StrLen(this.ДатаПоСост) > 0)
    ReqPeriod = "по состоянию на " + FillFullDateFromMassegeString(this.ДатаПоСост);
  elif ( (StrLen(this.ДатаНач) > 0) and (StrLen(this.ДатаКон) > 0))
    ReqPeriod = "в период с " + FillFullDateFromMassegeString(this.ДатаНач) + " по " + FillFullDateFromMassegeString(this.ДатаКон);
  end;
  return ReqPeriod;
end;
