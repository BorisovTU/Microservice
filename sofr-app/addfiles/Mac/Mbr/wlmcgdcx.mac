/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям МЦИ макета R (подтверждения)       */
/*                                                                          */
/* Имя файла: wlmcgdc.mac                                                   */
/* Создан:    15.08.00                                             AVM      */
/****************************************************************************/
import "wlmcgd.mac", FIInter;

macro GenDoc( addrMes )
  var Corschem = 0, Сумма, Day, Mon, Year, Error, CorAcc_Payer,
      ВИД_ПЛАТЕЖА, Currency;

  record bank( party );

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация подтверждения по R-макету" );

  ClearRecord( wlconf );

  if( not СчитатьПоле( FormFldDoc, Строка ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number       = wlmes.TRN;
  wlconf.RelatedRef   = substr( Строка, 191, 6 );
  wlconf.Account      = Свой_Корсчет;

  wlconf.FIID         = 0;

  Day = int( substr( Строка, 18, 2 ) );
  Mon = int( substr( Строка, 20, 2 ) );
  Year= int( substr( Строка, 22, 4 ) );
  wlconf.DateValue    = date( Day, Mon, Year );

  if ( wlconf.DateValue==date(0,0,0) )
     wlconf.TransferDate = {curdate};
  else
     wlconf.TransferDate = wlconf.DateValue;
  end;

  Сумма = substr( Строка, 100, 18 );
  wlconf.Sum          = moneyL( doubleL( Сумма ) );
  wlconf.DKFlag       = WL_DEBET;

  ВИД_ПЛАТЕЖА = substr( Строка, 28, 1 );

  if( ВИД_ПЛАТЕЖА == "1" )
    wlconf.Description  = TrimZero( substr( Строка, 213, 210 ) );
  end;

  wlconf.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlconf.FIID, 1 );
  if( wlconf.Corschem == -1 )
    Currency = ПолучитьКодФинИн(wlconf.FIID, error);
    if ( error )
       Currency = "???";
    end;
    std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
    return FALSE;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg( "Ошибка при вставке подтверждения" );
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;

