/*
$Name: swgd202c.mac
$Module: Межбанковские расчеты
$Description: Генерация платежей по сообщениям SWIFT MT202COV
*/
import "swgendoc.mac", "pmlib.mac";

/* Платежная инструкция "Общий банковский перевод" */
class GeneralFinInstTransfer
 var
  TRF              :string,      /* Ссылка на операцию */
  RelatedRef       :string,      /* Ссылка на сообщение или сделку */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  InstrCurrency    :string,      /* Код валюты актива */
  Sum              :money,      /* Сумма перевода */
  InstrSum         :money,      /* Сумма актива */
  OrdCustomer      :Customer,    /* Приказодатель */
  Credit           :bool,        /* Признак дебета/кредита */
  Trans            :bool,        /* Признак транзитного */
  OrdBank          :Bank,        /* Банк Приказодатель */
  Instructor       :Bank,        /* Инструктирующий банк */
  Sender           :Bank,        /* Отправитель */
  SndCorresp       :Bank,        /* Корреспондент Отправителя */
  RcvCorresp       :Bank,        /* Корреспондент Получателя */
  Intermediary     :Bank,        /* Посредник */
  AccBank          :Bank,        /* Банк-держатель счета */
  BnfBank          :Bank,        /* Банк Бенефициар */
  DirectAcc        :string,      /* Счет для идентификации корсхемы */
  DirectDKFlag     :string,             /* признак дебета-кредита счета для идентификации корсхемы */  
  RcvInfo          :TRcvInfo100, /* Информация Получателю */
  BnfInfo          :TBnfInfo,    /* Информация Бенефициару */
  ReceiverINN      :string;

  TRF              = "";
  RelatedRef       = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Trans            = false;
  ReceiverINN      = "";
  InstrCurrency = "";
  InstrSum      = $0;

  DirectAcc = "";
end; /* class GeneralFinInstTransfer */

var MT202COV : GeneralFinInstTransfer;

macro Fill13C( Code, GMT:TGMT )
  return TRUE;
end;

macro Fill20( TRF )
  MT202COV.TRF = TRF;
  return TRUE;
end;

macro Fill21( RelatedRef )
  MT202COV.RelatedRef = RelatedRef;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT202COV.ValueDate = Date;
  MT202COV.Currency  = Cur;
  MT202COV.Sum       = money(Sum);
  return TRUE;
end;

macro Fill50A( BIC, Account, System, Code )
  MT202COV.OrdCustomer = Customer( "", Account, BIC );
  return TRUE;
end;

macro Fill50F(Account, CodeKind, Code, Name)
  var INN = "";
  if(CodeKind == PTCK_INN)
    INN = Code;
  end;
  MT202COV.OrdCustomer = Customer( Name, Account, Code, INN, CodeKind); 
  return TRUE;
end;

macro Fill50K( Address, Account, System, Code, INN )
  MT202COV.OrdCustomer = Customer( Address, Account, "", INN );
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT202COV.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  MT202COV.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill53A( BIC, Account, System, Code )
  MT202COV.SndCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill53B( Branch, Account, System, Code )
  if ( (valtype(Branch)==V_UNDEF) AND (valtype(System)==V_UNDEF) AND 
       (valtype(Code)==V_UNDEF) AND (valtype(Account)!=V_UNDEF) AND (Account!="") )
      MT202COV.DirectAcc = Account;
  else 
      MT202COV.SndCorresp = Bank( "", "", Branch, Account, System, Code );
  end;
  return TRUE;
end;

macro Fill53D( Address, Account, System, Code )
  MT202COV.SndCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill54A( BIC, Account, System, Code )
  MT202COV.RcvCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill54B( Branch, Account, System, Code )
  MT202COV.RcvCorresp = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill54D( Address, Account, System, Code )
  MT202COV.RcvCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT202COV.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56C( Account )
  MT202COV.Intermediary = Bank( "", "", "", Account, "", "" );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  MT202COV.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57a( BIC, Name, Account, CliringSystem, CliringCode, INN )
  MT202COV.AccBank = Bank( BIC, Name, "", Account, CliringSystem, CliringCode, INN );
  return TRUE;
end;

macro Fill58A( BIC, Account, System, Code )
  MT202COV.BnfBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill58D( Address, Account, System, Code )
  MT202COV.BnfBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill70( Str )
  MT202COV.BnfInfo = TBnfInfo( Str );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT202COV.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

macro Fill33B( Cur, Sum )
  MT202COV.InstrCurrency  = Cur;
  MT202COV.InstrSum       = money(Sum);
  return TRUE;
end;

/* Заполнение списка полей формы  MT202COV*/

var Fld20  = ТПолеФормы( TransactionReferenceNumberField,      FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld13C = ТПолеФормы( TimeIndicationField,                FIELD_OPTIONAL,  "Read13C", "Fill13C", "" ),   
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A,   FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,           FIELD_OPTIONAL, "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,           FIELD_OPTIONAL, "ReadD",   "Fill52D", "" ),
    Fld53A = ТПолеФормы( Sender_sCorrespondentField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill53A", "" ),
    Fld53B = ТПолеФормы( Sender_sCorrespondentField_B,         FIELD_OPTIONAL,  "ReadB",   "Fill53B", "" ),
    Fld53D = ТПолеФормы( Sender_sCorrespondentField_D,         FIELD_OPTIONAL,  "ReadD",   "Fill53D", "" ),
    Fld54A = ТПолеФормы( Receiver_sCorrespondentField_A,       FIELD_OPTIONAL,  "ReadA",   "Fill54A", "" ),
    Fld54B = ТПолеФормы( Receiver_sCorrespondentField_B,       FIELD_OPTIONAL,  "ReadB",   "Fill54B", "" ),
    Fld54D = ТПолеФормы( Receiver_sCorrespondentField_D,       FIELD_OPTIONAL,  "ReadD",   "Fill54D", "" ),
    Fld56A = ТПолеФормы( IntermediaryField_A,                  FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                  FIELD_OPTIONAL,  "ReadD",   "Fill56D", "" ),

    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadBlock57aTag_A",   "Fill57a", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadBlock57aTag_B",   "Fill57a", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadBlock57aTag_D",   "Fill57a", "" ),

    Fld58A = ТПолеФормы( BeneficiaryInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill58A", "" ),
    Fld58D = ТПолеФормы( BeneficiaryInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill58D", "" ),

    Fld72  = ТПолеФормы( SenderToReceiverInformationField,     FIELD_OPTIONAL,  "Read72",  "Fill72",  "" ),

    Fld50A_B = ТПолеФормы( OrderingCustomerField_A,              FIELD_OPTIONAL,  "", "", "" ),  //По Sequence B мы не должны заполнять поля, она игнорируется
    Fld50F_B = ТПолеФормы( OrderingCustomerField_F,              FIELD_OPTIONAL,  "", "", "" ),
    Fld50K_B = ТПолеФормы( OrderingCustomerField_K,              FIELD_OPTIONAL,  "", "", "" ),
    Fld52A_B = ТПолеФормы( OrderingInstitutionField_A,           FIELD_OPTIONAL,  "", "", "" ),
    Fld52D_B = ТПолеФормы( OrderingInstitutionField_D,           FIELD_OPTIONAL,  "", "", "" ),
    Fld56A_B = ТПолеФормы( IntermediaryField_A,                  FIELD_OPTIONAL,  "", "", "" ),
    Fld56C_B = ТПолеФормы( IntermediaryField_C,                  FIELD_OPTIONAL,  "", "", "" ),
    Fld56D_B = ТПолеФормы( IntermediaryField_D,                  FIELD_OPTIONAL,  "", "", "" ),
    Fld57A_B = ТПолеФормы( AccountWithInstitutionField_A,        FIELD_OPTIONAL,  "", "", "" ),
    Fld57B_B = ТПолеФормы( AccountWithInstitutionField_B,        FIELD_OPTIONAL,  "", "", "" ),
    Fld57D_B = ТПолеФормы( AccountWithInstitutionField_D,        FIELD_OPTIONAL,  "", "", "" ),  
    Fld59_B  = ТПолеФормы( BeneficiaryCustomerField,             FIELD_OPTIONAL,  "", "", "" ),
    Fld59A_B = ТПолеФормы( BeneficiaryCustomerField_A,           FIELD_OPTIONAL,  "", "", "" ),
    Fld59F_B = ТПолеФормы( BeneficiaryCustomerField_F,           FIELD_OPTIONAL,  "", "", "" ),            
    Fld70_B  = ТПолеФормы( DetailsOfPaymentField,                FIELD_MANDATORY, "", "", "" ),
    Fld72_B  = ТПолеФормы( SenderToReceiverInformationField,     FIELD_OPTIONAL,  "", "", "" ),
    Fld33B_B = ТПолеФормы( CurrencyOriginalOrderedAmountField,   FIELD_OPTIONAL,  "", "", "" );

macro ConstructMT202COV( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;
  
  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            if( fld.Name == TimeIndicationField )
              count = count - 1;
            end;
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT202COV.Sender   = Bank( BankCode, "", "", MT202COV.SndCorresp.Account );

  MT202COV.Credit = TRUE; /* входящие банковские переводы - кредитовые */
  MT202COV.Trans  = MT202COV.AccBank.IsSet() ; /* документ транзитный - если указан держатель счета */

  return TRUE;
end; /* ConstructMT202COV */

macro GenDoc( addrMes )
  var error;
  MT202COV = NULL;
  MT202COV = GeneralFinInstTransfer();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ202");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMT202COV( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer            = MT202COV.OrdCustomer.PartyID;
  wlpmpaym.PayerAccount     = MT202COV.OrdCustomer.Account;
  wlpmpaym.PayerCode        = MT202COV.OrdCustomer.Code;
  wlpmpaym.PayerCodeKind    = MT202COV.OrdCustomer.CodeKind;

  wlpmpaym.Receiver         = MT202COV.BnfBank.PartyID; 
  wlpmpaym.ReceiverAccount  = MT202COV.BnfBank.Account; 
  wlpmpaym.ReceiverCodeKind = MT202COV.BnfBank.CodeKind; 
  wlpmpaym.ReceiverCode     = MT202COV.BnfBank.CodeBank; 

  wlpmpaym.Amount      = MT202COV.Sum;
  wlpmpaym.PayAmount   = MT202COV.Sum;
  wlpmpaym.BaseAmount  = MT202COV.Sum;

  wlpmpaym.ValueDate = MT202COV.ValueDate;
  if( not ПолучитьФинИнПоISO( MT202COV.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO :" +  MT202COV.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  if(MT202COV.InstrCurrency != "")
    if( not ПолучитьФинИнПоISO( MT202COV.InstrCurrency, wlfininstr ) )
      std.msg( "Не определена валюта сообщения по коду ISO :" +  MT202COV.InstrCurrency );
      return FALSE;
    end;
    wlpmpaym.OrderAmount = MT202COV.InstrSum;
    wlpmpaym.OrderFIID = wlfininstr.FIID;
  else
    wlpmpaym.OrderAmount = MT202COV.Sum;
  end;
  
  if ( MT202COV.OrdBank.IsSet() )
     wlpmpaym.PayerBankID = MT202COV.OrdBank.PartyID;
  else
     wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;
  end;
  
  SetReceiverBankForInPaymentBy57a( wlmes, MT202COV.AccBank, wlpmpaym, wlpmpropcred, wlpmrmprop );

  /* Дебетовые свойства платежа */
  wlpmpropdeb.TransferDate = {curdate};
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  if ( MT202COV.OrdBank.IsSet() )
     wlpmpropdeb.CodeKind = MT202COV.OrdBank.CodeKind;
     wlpmpropdeb.BankCode = MT202COV.OrdBank.CodeBank;
     if ( MT202COV.Instructor.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT202COV.Instructor.CodeKind;
        wlpmpropdeb.CorrCode     = MT202COV.Instructor.CodeBank;
        wlpmpropdeb.CorrID       = MT202COV.Instructor.PartyID;
     else
        wlpmpropdeb.CorrCodeKind = MT202COV.Sender.CodeKind;
        wlpmpropdeb.CorrCode     = MT202COV.Sender.CodeBank;
        wlpmpropdeb.CorrAcc      = MT202COV.Sender.Account;
        wlpmpropdeb.CorrID       = MT202COV.Sender.PartyID;
     end;
  else
     wlpmpropdeb.CodeKind = MT202COV.Sender.CodeKind;
     wlpmpropdeb.BankCode = MT202COV.Sender.CodeBank;

     if ( MT202COV.SndCorresp.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT202COV.SndCorresp.CodeKind;
        wlpmpropdeb.CorrCode     = MT202COV.SndCorresp.CodeBank;
        wlpmpropdeb.CorrID       = MT202COV.SndCorresp.PartyID;
     end;
  end;

  if ( MT202COV.SndCorresp.IsSet() OR MT202COV.RcvCorresp.IsSet() )
     if ( (not MT202COV.OrdBank.IsSet()) AND (not MT202COV.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_BANK;
     elif ( (not MT202COV.OrdBank.IsSet()) OR (not MT202COV.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_CORR;
     end;
  end;

  if ( MT202COV.RcvCorresp.IsSet() )
     wlpmpropdeb.OurCorrCodeKind = MT202COV.RcvCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202COV.RcvCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202COV.RcvCorresp.PartyID;
  elif ( (MT202COV.SndCorresp.IsSet()) AND 
         (not MT202COV.RcvCorresp.IsSet()) AND
         (MT202COV.SndCorresp.PartyID!=wlpmpropdeb.CorrID) )
     wlpmpropdeb.OurCorrCodeKind = MT202COV.SndCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202COV.SndCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202COV.SndCorresp.PartyID;
  end;

  /* Если в 53 указан только счет, считаем это явным заданием корсчета */
  /* Если в 53 указан только счет, считаем это явным заданием корсчета */
  if( MT202COV.DirectAcc != "" )
    wlpmpropdeb.OurCorrAcc   = MT202COV.DirectAcc;
    if(MT202COV.DirectDKFlag == "C")
      wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
    elif(MT202COV.DirectDKFlag == "D")
      wlpmpropdeb.InOurBalance = "X"; /* Счет указан в нашем балансе */
    end;
    if( wlpmpropdeb.OurCorrID <= 0 )
      wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
    end;
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = MT202COV.TRF;
  wlpmrmprop.PayDate               = MT202COV.ValueDate;
  wlpmrmprop.Date                  = MT202COV.ValueDate;
  wlpmrmprop.ClientDate            = MT202COV.ValueDate;
  if ( MT202COV.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName         = MT202COV.OrdBank.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT202COV.OrdBank.Account;
     if ( MT202COV.Instructor.IsSet() )
        wlpmrmprop.PayerCorrBankName     = MT202COV.Instructor.Name;     
     else
        wlpmrmprop.PayerCorrBankName     = MT202COV.Sender.Name;     
     end;
  else
     wlpmrmprop.PayerBankName         = MT202COV.Sender.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT202COV.Sender.Account;
  end;  
  
  wlpmrmprop.PayerName             = MT202COV.OrdCustomer.Name;
  wlpmrmprop.PayerINN              = MT202COV.OrdCustomer.INN;

  wlpmrmprop.ReceiverName          = MT202COV.BnfBank.Name;
  wlpmrmprop.ReceiverINN           = MT202COV.ReceiverINN;

  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT202COV.RcvInfo.REC;
  wlpmrmprop.Ground                = MT202COV.BnfInfo.Text;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "01";

  wlpmrmprop.ComissCharges = PM_CHRG_OUR;
  
  FillDebetCreditFIID(wlpmpaym, wlpmpropdeb, wlpmpropcred);

  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo       = MT202COV.RcvInfo.Str;

  if ( MT202COV.RelatedRef!=СсылкаОтсутствует )
     wlpmrmprop.Reference = MT202COV.RelatedRef;
  end;

  /* Кредитовые свойства платежа */
  wlpmpropcred.TransferDate = {curdate};
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;

  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, wlpmpropdeb.OurCorrAcc , 
                                                      NULL, false, wlmes.TpSchemID, wlpmpropdeb.InOurBalance, wlpmpropdeb.OurCorrID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202COV.OrdBank.GetRoute() );
  /* Узел Инструктирующего банка - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202COV.Instructor.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202COV.Sender.GetRoute(), true );
  /* Узел Корреспондента Отправителя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202COV.SndCorresp.GetRoute() );
  /* Узел Корреспондента Получателя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202COV.RcvCorresp.GetRoute() );
  /* Узел Посредника - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202COV.Intermediary.GetRoute() );
  /* Узел Банка Бенефициара - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202COV.AccBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;