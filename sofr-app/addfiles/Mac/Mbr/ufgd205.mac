/*
 $Name: ufgd205.mac
 $Module: МБР
 $Description: Генерация подтверждений по сообщениям УФЭБС ED205
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям УФЭБС ED205                        */
/*                                                                          */
/* Имя файла: ufgd205.mac                                                   */
/* Создан:    27.10.04                                      Фомченкова Л.Н. */
/****************************************************************************/
import "ufgendoc.mac";

var ver_st = 1;

private macro GetPayAmount(PaymentID): moneyL

  VAR select:string = " select t_PayAmount"
                      " from dpmpaym_dbt" 
                      " where t_PaymentID = ? "
                      ;
  VAR params:TArray = makeArray(SQLParam( "", PaymentID ));
  
  VAR rset:RsdRecordset = execSQLselect( select, params, false );
  if( rset and rset.moveNext() )
    return rset.value(0);
  else     
    return $0;
  end;
  
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Сумма = $0, Currency, error, Cancel;
  var StatusCode, CtrlCode, RefDate, ErrDiag:string, IsPack = false;
  
  var InitialEDRef = null;

  record bank( party );

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация подтверждения по ED205" );

  ClearRecord( wlconf );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED205" ) );
     return false;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number     = wlmes.TRN;
  wlconf.RelatedRef = wlmes.RelatedRef; 
  wlconf.FIID       = 0;
  wlconf.DateValue = ToDate(ReadAttribute(xml,"EDDate", "EDRefID"));
  wlconf.Account      = Свой_Корсчет;

  wlconf.TransferDate = ToDate(ReadAttribute(xml,"EDDate"));

  wlconf.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlconf.FIID, 1);
  if( wlconf.Corschem == -1 )
    Currency = ПолучитьКодФинИн(wlconf.FIID, error);
    if ( error )
       Currency = "???";
    end;
    std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
    return FALSE;
  end;

  wlconf.BankID = wlmes.OutsideAbonentID;
  if( ПолучитьСубъекта( wlconf.BankID, bank ) != 0 )
    std.msg( "Не найден банк отправитель" );
    return FALSE;
  end; 
  wlconf.BankName = bank.Name;
  Сумма = moneyL(  ReadOptinalAttribute(xml,"Balance") ) /100 ;
  
  if (Сумма)
      wlconf.Sum = Сумма;
  end;
  
  StatusCode = ReadAttribute(xml,"StatusStateCode");
  Cancel = UF_getCancelForConf( StatusCode, ver_st );
  CtrlCode   = ReadOptinalAttribute(xml,"CtrlCode");
 
  /*Описание вставляем всегда, если присутствует StatusCode           */ 
  /*проверку на KVIT_CANCEL выполнит макрос квитовки                  */
  wlconf.Description = ПолучитьКодОтветаНаСообщение(StatusCode, TYPE_CODE_UFBS_ED203);
  /*По CtrlCode описание ошибки здесь не ищем,                        */
  /*потому что за обработку сообщения ответственен участок кода ED201,*/
  /*подтверждение при этом не формируется                             */  

  ErrDiag = ReadOptinalAttribute(xml,"ErrorDiagnostic");
  /*Если wlconf.Description до сих пор пустой, то смотрим, присутствует ли тег ErrorDiagnostic,*/
  /*если он есть, то запихивает его в описание "сколько влезет"*/
  if( ErrDiag And (not wlconf.Description))
     wlconf.Description = ErrDiag;
  end;

  var SettlementTime = GetSettlementTime( xml, wlmes );
  
  InitialEDRef = EDNoDateAuthor();
  if( not ПроверитьЭлелементInitialEDНаНаличие( xml, InitialEDRef.EDNo, InitialEDRef.EDDate, InitialEDRef.EDAuthor ) )
    InitialEDRef = null;
  end;
  
  var select = "select t_PaymentID "
                 "from dwlmes_dbt mes, "
                      "dwlmeslnk_dbt meslnk, "
                      "dwlpm_dbt wlpm "
                 "where mes.t_Trn = :RelatedRef1 "
                   "and meslnk.t_MesID = mes.t_MesID "
                   "and meslnk.t_ObjKind = 501/*OBJTYPE_PAYMENT*/ "
                   "and wlpm.t_WlPmID = meslnk.t_ObjID "
                   "and wlpm.t_Direct = chr(0)/*WLD_MES_OUT*/ "
               "union "
               "select t_PaymentID "
                 "from dwlsess_dbt ses, " 
                      "dwlmes_dbt mes, "
                      "dwlmeslnk_dbt meslnk, "
                      "dwlpm_dbt wlpm "
                 "where ses.t_SessUID = :RelatedRef2 "
                   "and mes.t_SessionID = ses.t_SessionID "
                   "and meslnk.t_MesID = mes.t_MesID "
                   "and meslnk.t_ObjKind = 501/*OBJTYPE_PAYMENT*/ "
                   "and wlpm.t_WlPmID = meslnk.t_ObjID "
                   "and wlpm.t_Direct = chr(0)/*WLD_MES_OUT*/ ";

  if( SettlementTime != time(0,0,0) )
    wlconf.Description = wlconf.Description + "\nSettlementTime (Время исполнения в ПБР): " + SettlementTime;

    execSQL( "update dpmrmprop_dbt "
             "   set t_SettlementTime = :SettlementTime1 "
             " where t_PaymentID in ( " + select + " )"
             "   and t_SettlementTime < :SettlementTime2 ",
             makeArray( SQLParam("SettlementTime1", time(SettlementTime)),
                        SQLParam("RelatedRef1", wlmes.RelatedRef ),
                        SQLParam("RelatedRef2", wlmes.RelatedRef ),
                        SQLParam("SettlementTime2", time(SettlementTime))
                      )
           );
           
  end;

  // Обновляем поле dwlpm_dbt.t_StatusCode
  if( StatusCode != "" )
    execSQL( "update dwlpm_dbt "
             "   set t_StatusCode = :StatusCode "
             " where t_PaymentID in ( " + select + " )",
             makeArray( SQLParam("StatusCode", StatusCode ),
                        SQLParam("RelatedRef1", wlmes.RelatedRef ),
                        SQLParam("RelatedRef2", wlmes.RelatedRef )
                      )
           );
  end;
  
  var InitialEDReference = "";
  
  if( InitialEDRef )
    InitialEDReference = InitialEDRef.GetTrn();
  end;  
    
  if( InitialEDReference != "" )  
  
    var InitialMesID = 0;
  
    var query =
                " SELECT mes.t_MesID AS t_MesID                           " +
                " FROM dwlmes_dbt     mes,                                " +
                "      dwlmeslnk_dbt  meslnk,                             " +
                "      dwlreq_dbt     wlreq,                              " +
                "      dwlmesrls_dbt  rls,                                " +
                "      dwlmesfrm_dbt  frm,                                " +
                "      dwlmes_dbt     mes2,                               " +
                "      dwlmesrls_dbt  rls2,                               " +
                "      dwlmesfrm_dbt  frm2,                               " +
                "      dwlobjknd_dbt  wlobjkind                           " +
                " WHERE     mes.t_Trn = :InitialEDReference               " +
                "      AND meslnk.t_MesID = mes.t_MesID                   " +
                "      AND meslnk.t_ObjKind = :OBJTYPE_WLD_REQ            " +
                "      AND meslnk.t_ObjID = wlreq.t_ReqID                 " +
                "      AND wlreq.t_Direct = CHR(0)                        " +
                "      AND mes2.t_MesID = :WlMesID                        " +
                "      AND mes.t_RlsFormID = rls.t_RlsFormID              " +
                "      AND rls.t_FormID = frm.t_FormID                    " +
                "      AND mes2.t_RlsFormID = rls2.t_RlsFormID            " +
                "      AND rls2.t_FormID = frm2.t_FormID                  " +
                "      AND frm.t_TpID = frm2.t_TpID                       " +
                "      AND wlobjkind.t_ObjID = frm.t_FormID               " +
                "      AND wlobjkind.t_Type = frm.t_KindMes               " +
                "      AND wlobjkind.t_ObjKind = :OBJTYPE_WLD_FORM        " +
                "      AND wlobjkind.t_Number = :WLD_SUBKIND_REQ_CHPMPREC ";
                
                
     var rs : RsdRecordset = execSQLselect( query, makeArray( SQLParam( "RelatedRef", InitialEDReference ),
                                                              SQLParam( "OBJTYPE_WLD_REQ", OBJTYPE_REQ ),
                                                              SQLParam( "WlMesID", wlmes.MesID ),
                                                              SQLParam( "OBJTYPE_WLD_FORM", OBJTYPE_FORM ),
                                                              SQLParam( "WLD_SUBKIND_REQ_CHPMPREC", WLD_SUBKIND_REQ_CHPMPREC ) ) );
                                                              
                                                              
    if( rs and rs.moveNext() )
      InitialMesID = rs.value( "t_MesID" );
    end;

    if( InitialMesID != 0 )
      ОшибкаЛогическогоКонтроляСообщения( InitialMesID, 0, ПолучитьКодОтветаНаСообщение(StatusCode, TYPE_CODE_UFBS_ED203), WLD_STATUS_RESPRM_CLOSE );
    end;

  end;


  if((StatusCode == "20") Or (StatusCode == "21") or (StatusCode == "31"))
     return ОбработатьКакED201(wlconf.DateValue, wlconf.RelatedRef, int(CtrlCode));
  elif((StatusCode == "11") or (StatusCode == "19") or (StatusCode == "30") or (StatusCode == "41") or (StatusCode == "42"))
     if(StatusCode == "30")
       IsPack = true;
     end;
     return ОбработатьКакСообщениеОДоставке(wlconf.DateValue, wlconf.RelatedRef, IsPack);
  elif (Cancel == KVIT_ERROR)
      std.msg( "Сообщения ED205 со статусом " + StatusCode + " обрабатываются без формирования подтверждений" );
  else
      wlconf.Cancel = Cancel;
      wlconf.DKFlag = 0;

      if(StatusCode == "08")
        ОбработатьКакСообщениеОДоставке(wlconf.DateValue, wlconf.RelatedRef, false);
      end;

      if( not ВставитьПодтверждение( wlconf ) )
        std.msg("Ошибка при вводе подтверждения");
        return FALSE;
      end;

  end;
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
