/*
  $Name:         ufgm542.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED542 транспорта УФЭБС и проверка учетного объекта
*/

import "ufgenmes.mac", BankInter, CTInter, "oralib.mac", MesInter, WldInter, "bnk_common.mac";

private const WlReqQueriesPanelFldName = "Messages";
private const WlReqInitFormMesPanelFldName = "InitForm";

private const retPanelFieldNameParmNum = 2;

private macro CheckInitFormMes( InitFormMes )
  if( RegExMatch( Trim( InitFormMes ), "ED\\d{3}" ) == true )
    return true;
  end; 
  return false;
end;

macro CheckObj( addrReq, refilledAddrReq, retPanelFieldName )
  
  record wlReq (wlreq);
  SetBuff( wlReq, addrReq );
  
  var indx = 0;
  
  if(indx = index(wlreq.Queries, "RRICode"))
    var number:string = SubStr(wlreq.Queries, indx+7, 1);
    if( not( (number == "1") or (number == "3") ) )
      setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
      RunError("Для формы ED542 в поле <Сообщение> можно указать только код /RRICode1/ или /RRICode3/. Задайте один из допустимых кодов");
    end;
  else
    setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
    RunError("Для формы ED542 в поле <Сообщение> необходимо ввести один из фиксированных кодов из справочника");
  end;    
  
  var ARMNo = Trim( getFldValFromQueries(wlReq.Queries, "ARMNo:") );
  
  if( ( strlen(ARMNo) != 2 ) or ( not StrIsNumber(ARMNo) ) )
    msgboxEx("Неверно задан номер АРМ получателя (отправителя). Поле ARMNo сообщения заполнено не будет", 0);    
  end;
  
  if(wlreq.RelatedRef != "")
    if(index(wlreq.Queries, "RRICode3") != 0)
      msgboxEx("Для кода сообщения /RRICode3/ ссылку на исходное сообщение задавать не следует. Указанное значение учитываться не будет", 0);
    end;
  end;
  
  if(wlreq.InitFormMes != "")
    if(index(wlreq.Queries, "RRICode1") != 0)
      msgboxEx("Для кода сообщения /RRICode1/ форму исходного сообщения задавать не следует. Указанное значение учитываться не будет", 0);
    end;
  end;
  
  if(( wlreq.InitFormMes != "" ) and ( CheckInitFormMes(wlreq.InitFormMes) == false ))
    setparm( retPanelFieldNameParmNum, WlReqInitFormMesPanelFldName );
    RunError("Неверно задан тип сообщений, по которым запрашивается информация, EDTypeNo.| Необходимо задать номер формы из списка сообщений УФЭБС, например, <ED503>");       
  end;
  
  if( /*wlreq.InitDateMes and */wlreq.InitDateMes != date(0,0,0) )
    if(index(wlreq.Queries, "RRICode3") != 0)
      msgboxEx("Для кода сообщения /RRICode3/ дату исходного сообщения задавать не следует. Указанное значение учитываться не будет", 0);
    end;
  end;
  
  return true;
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);
  
  SetBuff( wlReq, addrReq );
  SetBuff( wlmes, addrMes );
  
  var xml:object = ActiveX( "Microsoft.XMLDOM" );  
  var mes:object, elem: object; 
  
  mes = xml.createElement( "ED542" );  
  mes.setAttribute( "xmlns", "urn:cbr-ru:ed:v2.0" );
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  mes.setAttribute( "EDNo",       ed_nda.EDNo );
  mes.setAttribute( "EDDate",     YYYYMMDD(ed_nda.EDDate) );
  mes.setAttribute( "EDAuthor",   ed_nda.EDAuthor );  
  
  var RepeatReceptInqCode;
  if(index(wlreq.Queries, "RRICode1") != 0)
    RepeatReceptInqCode = 1;
  elif(index(wlreq.Queries, "RRICode3") != 0)
    RepeatReceptInqCode = 3;
  end;
  
  mes.setAttribute( "RepeatReceptInqCode", RepeatReceptInqCode );
  if( RepeatReceptInqCode == 3 )
    mes.setAttribute( "EDTypeNo", "0" + SubStr(wlreq.InitFormMes, strlen(wlreq.InitFormMes) - 2) ); 
  end;

  var ARMNo = Trim( getFldValFromQueries(wlReq.Queries, "ARMNo:") );
  if( (ARMNo != "") and (strlen(ARMNo) == 2) and (StrIsNumber(ARMNo)) )
    mes.setAttribute( "ARMNo", ARMNo );
  end; 
  
  if(RepeatReceptInqCode == 1)
    elem = xml.createElement( "EDRefID" );

    if( (wlreq.RelatedRef != "") and (strlen(wlreq.RelatedRef ) > 11))
      elem.setAttribute( "EDNo", FirstZerosDelete(SubStr(wlreq.RelatedRef,11)) );
    end;
    
   // if( wlreq.InitDateMes != date(0,0,0) )
      elem.setAttribute( "EDDate", YYYYMMDD(wlreq.InitDateMes) );
  //  end;
    
    if( (wlreq.RelatedRef != "") and (strlen(wlreq.RelatedRef ) >= 10))
      elem.setAttribute( "EDAuthor", SubStr(wlreq.RelatedRef,1, 10) );
    end;    
    
    mes.appendChild(elem);
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return true; /*Успешное завершение*/
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;