/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT MTn98                             */
/*                                                                          */
/*  Имя файла: swgdn98.mac                                                  */
/*  Создан:    22.09.00                                       Бабин А.П.    */
/****************************************************************************/

import "swgendoc.mac";

/* Платежная инструкция в своем формате */
class CustomerTransfer
 var
  TRF              :string,      /* Ссылочный номер операции */
  subMesType       :string,      /* Тип сообщения */
  Message          :TPrpMes,     /* Сообщение в своем формате */
  Sender           :Bank;        /* Отправитель */

  TRF              = "";
  subMesType       = "";

end; /* class CustomerTransfer */

var MTn98 : CustomerTransfer;

macro Fill20( TRF )
  MTn98.TRF = TRF;
  return TRUE;
end;

macro Fill12( str )
  MTn98.subMesType = str;
  return TRUE;
end;

macro Fill77E( str )
  MTn98.Message = TPrpMes( MTn98.subMesType, str );
  return TRUE;
end;

/* Заполнение списка полей формы  MT100*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),    
    Fld12  = ТПолеФормы( SubMessageType,                     FIELD_MANDATORY, "ReadType","Fill12",  "" ),
    Fld77E = ТПолеФормы( ProprietaryMessage,                 FIELD_MANDATORY, "Read77E", "Fill77E", "" ); 

macro ConstructMTn98( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MTn98.Sender   = Bank( BankCode );  

  return TRUE;
end; /* ConstructMTn98 */

macro GenDoc( addrMes )
  var error, Corschem;
  MTn98 = CustomerTransfer();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТn98");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmpropdeb);
  ClearRecord(wlpmpropcred);
  ClearRecord(wlpmrmprop);

  if( not ConstructMTn98( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */  
  if ( MTn98.Message.IsSet()==FALSE )
     /* Сообщение было разобрано с ошибками */
     return FALSE;
  end;

  /* Далее следует заполнить учетные буфера из полей класса TPrpMes */
  /* Сейчас заполняется лишь малая часть полей платежа (т.к. поле 77E не разбирается) */
  /* Для его разбора по своему собственному формату следует доработать класс TPrpMes в swgendoc.mac */

  /* Валюту и схему расчетов Вы тоже можете получать в поле 77E, но пока же мы
     пытаемся определить эти данные из корреспондента                             */
  if ( НайтиКорсхемуПоКонтрагенту(wlmes.OutsideAbonentID, Corschem, wlpmpaym.PayFIID) )
      wlpmpaym.FIID  = wlpmpaym.PayFIID;
  else
      std.msg( "Не определена схема расчетов" );
      return FALSE;
  end;  

  wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;
  
  wlpmpaym.ReceiverBankID = {OurBank};

  /* Дебетовые свойства платежа */
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  wlpmpropdeb.CodeKind = MTn98.Sender.CodeKind;
  wlpmpropdeb.BankCode = MTn98.Sender.CodeBank;
  wlpmpropdeb.Corschem = Corschem;  
  
  /* Реквизиты платежа, характерные для SWIFT */
  wlpmrmprop.Number = MTn98.TRF;

  /* Пока мы всю информацию полученную в 77E записываем в детали платежа */
  wlpmrmprop.Ground = substr( MTn98.Message.Message, 1, 210 );

  /*wlpmrmprop.MessageType        = Transfer;#59030*/
  wlpmrmprop.ProcessKind        = " 1";
  wlpmrmprop.ShifrOper          = "01";

  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, 0, wlpmrmprop );

  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, 0, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
