/*
  $Name:         fnsgmsfc0x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SFC0 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SFC0 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 09.07.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac";

private const vers : integer = 510, // версия формата 5.10
              IsIndivid : bool = true,
              AccMsgKind : integer = ACCMSG_KIND_OPEN;

macro CheckObj( addrReq )
  record Req(reqopena);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckContractNumber,
                                              GenMesCheckAccountType,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality,
                                              GenMesCheckDULCode,
                                              GenMesCheckDULSizeCode,
                                              GenMesCheckDULSeriesAndNumber,
                                              GenMesCheckDULDate,
                                              GenMesCheckClientExistDatePlacBirth
                                             );
  
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_OPEN );
end;

// Файл\Документ\СвСчет\Открыт
private macro WriteAccInfoOpen(reqopena)
  StartBlockLogXML("Открыт");

  WriteFieldWithCheckAnnul( "КодСостСч", "1" );
  WriteFieldWithCheckAnnul( "НомерДог", reqopena.SfContrNum );
  WriteFieldWithCheckAnnul( "КодСостДог", "0" );
  var ContractDate : string = IfThenElse(reqopena.SfContrDate != date(0, 0, 0), DDpMMpYYYY(reqopena.SfContrDate), "");
  WriteFieldWithCheckAnnul( "ДатаЗаклДог", ContractDate );

  FinishBlockLogXML("Открыт");
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record Req(reqopena);
  SetBuff(Req, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SFC0");

  var ErrMsg : string = "";

  var CommonAccMsg : TCommonDataAccMsg = TCommonDataAccMsg
                                         ( Req.Department, 
                                           AccMsgKind, 
                                           vers,
                                           IsIndivid );

  var ClientID : integer = Req.ClientID;

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonAccMsg);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonAccMsg, "", "", ALLFININSTR, ClientID, OldMes.MesID, IsIndivid);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonAccMsg, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo_Ind(ClientID, Req.ClientType311, AccMsgKind, NULL, Req, NULL);

  // Файл \ Документ \ СвСчет
  WriteAccInfo_Ind(Req, AccMsgKind, @WriteAccInfoOpen);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonAccMsg._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonAccMsg._ВидРегОргана);
  end;

  if(CommonAccMsg._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonAccMsg._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
