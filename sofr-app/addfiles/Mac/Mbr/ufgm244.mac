 /*
 $Name: ufgm244.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения ED244 транспорта УФЭБС
 */

/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED244 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm244.mac                                                  */
/*  Создан:    16.06.11                                                     */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac", 
       "adress.mac", "wltools.mac", "pmprops.mac", "pmlib.mac", "uf243244.mac",
       "uf243244genmes.mac";

private const VER_ST_2_5_9 : integer = 259;

private macro ParseQueries(Queries: string, EDDefineAnswerCode: @string, EDDefineAnswerInfo: TArray, ver_st)
  var req_code : string = "";
  var code_index : integer = index_wlcode(Queries, TYPE_CODE_UFBS_ED244, @req_code);
  if(code_index)
    EDDefineAnswerCode = substr(req_code, 4, 2);
  else
    RunError("Не найден код в сообщении");
  end;
  Queries = substr( Queries, code_index + strlen(req_code) );
  
  var AnswerInfoFNames : TArray = NULL;
  if(ver_st == VER_ST_2_5_9)
    AnswerInfoFNames   = makeArray("AccDocDate",
                                   "AccDocNo",
                                   "PayerAcc",
                                   "PayeeAcc",
                                   "PayerINN",
                                   "PayeeINN",
                                   "Sum",
                                   "EnterDate",
                                   "PayeeCorrAcc",
                                   "PayeeBIC",
                                   "PayerLongName",
                                   "PayeeLongName",
                                   "Purpose",
                                   "Address",
                                   "Field/",
                                   "TransactionID"
                                  );
  else
    if (EDDefineAnswerCode == "08")    
      AnswerInfoFNames = makeArray("PayerINN", "PayeeINN", "DrawerStatus", 
                                   "PayerKPP", "PayeeKPP", "CBC", "OKATO", "PaytReason", 
                                   "TaxiPeriod", "DocNo", "DocDate", "TaxPaytKind"
                                  );
    else    
      AnswerInfoFNames = makeArray("AccDocDate",
                                   "AccDocNo",
                                   "PayerAcc",
                                   "PayeeAcc",
                                   "PayerINN",
                                   "PayeeINN",
                                   "Sum",
                                   "PayerLongName",
                                   "PayeeLongName",
                                   "Purpose",
                                   "Address"
                                  );
    end;
  end;

  var QueriesLines : TArray = StrCut(Queries, "\n");
  var fld : TMesField = NULL;
  for( var FName, AnswerInfoFNames)
    for( var curr_line, QueriesLines )
      fld = getFieldFromQueriesLine(curr_line);
      if(fld and ( (fld.FieldName == FName) 
                   or 
                   (substr(fld.FieldName, 1, 6) == "Field/") and (FName == "Field/") )
        )
        EDDefineAnswerInfo[ EDDefineAnswerInfo.size ] = fld;
      end;
    end;
  end;

end;

private macro FindInWlmes_req( TRN:string, wlmes_req_MesID:@integer, wlmes_req_RelatedRef:@string ):bool
  var rs:object;
  var select:string;
  var params:TArray = TArray();

  select = "select wlmes_req.t_MesID, wlmes_req.t_RelatedRef "+
           "from dwlmes_dbt wlmes_req, dwltpshem_dbt wltpshem, "+
           "     dwlmesrls_dbt wlmesrls, dwlmesfrm_dbt wlmesfrm "+
           "where wlmes_req.t_TRN = :TRN "+
           "  and wlmes_req.t_Direct = 'X' /* WLD_MES_IN */ "+
           "  and wlmes_req.t_tpschemID = wltpshem.t_TpShemID "+
           "  and wltpshem.t_TpID = 9 " +
           "  and wlmes_req.t_RlsFormID = wlmesrls.t_RlsFormID "+
           "  and wlmesrls.t_FormID = wlmesfrm.t_FormID "+
           "  and wlmesfrm.t_Name = 'ED243' ";
  
  params = makeArray( SQLParam("TRN", TRN) );
  rs = execSQLselect( select, params );
  
  if( rs and rs.MoveNext() )
    wlmes_req_MesID = rs.value("t_MesID");
    wlmes_req_RelatedRef = rs.value("t_RelatedRef");
    return true;
  end;

  return false;
end;

private macro GetOneField(MesID : integer, FieldName : string) : string
  var select : string = "";

  select = "select wlmesval.t_value "+
           "  from dwlmesval_dbt wlmesval, dwltpfld_dbt wltpfld "+
           " where wlmesval.t_MesID = :MesID "+
           "   and wlmesval.t_TpFieldID = wltpfld.t_TpFieldID "+
           "   and wltpfld.t_TpID = 9 and wltpfld.t_Name = :FieldName ";
  var rs = execSQLselect(select, makeArray( SQLParam("MesID",     MesID),
                                            SQLParam("FieldName", FieldName) ));
  if( rs and rs.MoveNext() )
    return rs.value(0);
  end;

  return "";
end;

macro GenMes( addrMes, addrReq )
  var tmpstr = "", error;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  // Найти входящее сообщение с запросом wlmes_req, на которое ссылается ответ
  var wlmesreq_MesID = 0, wlmes_req_RelatedRef = "", IsInMesFound = false;
  var wlmes_req_xml:object = ActiveX( "MSXML.DOMDocument" );
  if( not FindInWlmes_req( wlReq.RelatedRef, @wlmesreq_MesID, @wlmes_req_RelatedRef ) )
    IsInMesFound = false;
  else
    IsInMesFound = true;
    if ( not wlmes_req_xml.loadXML( GetOneField(wlmesreq_MesID, "xml") ) )
      RunError( string( "Неверный формат сообщения по форме ED243" ) );
    end;
  end;

  // Разобрать поле wlreq.Queries
  var EDDefineAnswerCode = "";
  var EDDefineAnswerInfo : TArray = TArray();
  ParseQueries(wlReq.Queries, @EDDefineAnswerCode, EDDefineAnswerInfo);  

  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  ЗаписатьПолеЛог( "EDNo", ed_nda.EDNo );
  ЗаписатьПолеЛог( "EDDate", YYYYMMDD(ed_nda.EDDate) );
  ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
    
  if(IsInMesFound)
    ЗаписатьПолеЛог( "EDDefineRequestCode", ReadAttribute(wlmes_req_xml, "EDDefineRequestCode") );
  else
    ЗаписатьПолеЛог( "EDDefineRequestCode", "00" );
  end;
  ЗаписатьПолеЛог( "EDDefineAnswerCode", EDDefineAnswerCode );

  // OriginalEPD -------------------------------------------
  УстановитьКонтекстБлокаЛог( "OriginalEPD" );
  ЗаписатьПолеЛог( beginField, "OriginalEPD" );
  if(IsInMesFound)
    ЗаписатьПолеЛог( "EDNo",   ReadAttribute(wlmes_req_xml, "EDNo", "OriginalEPD") );
    ЗаписатьПолеЛог( "EDDate", ReadAttribute(wlmes_req_xml, "EDDate", "OriginalEPD") );
    tmpstr = ReadOptinalAttribute(wlmes_req_xml, "EDAuthor", "OriginalEPD");
    if (tmpstr)
      ЗаписатьПолеЛог( "EDAuthor", tmpstr );
    end;
  else
    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn(wlmes.RelatedRef);

    ЗаписатьПолеЛог( "EDNo",   ed_nda.EDNo );
    ЗаписатьПолеЛог( "EDDate", YYYYMMDD(ed_nda.EDDate) );
    if (ed_nda.EDAuthor)
      ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
    end;
  end;
  ЗаписатьПолеЛог( endField, "OriginalEPD" ) ;
  УстановитьКонтекстБлокаЛог( ".." );
  // OriginalEPD -------------------------------------------

  // EDDefineAnswerInfo ----------------------------------------------
  macro IsSeparateElement(fld_name : string): bool
    return ( (fld_name == "PayerLongName") or (fld_name == "PayeeLongName") or 
             (fld_name == "Purpose") or (fld_name == "Address") 
           );
  end;
  if( EDDefineAnswerInfo.size > 0 )
    УстановитьКонтекстБлокаЛог( "EDDefineAnswerInfo" );
    ЗаписатьПолеЛог( beginField, "EDDefineAnswerInfo" );

    if (EDDefineAnswerCode == "08")
      УстановитьКонтекстБлокаЛог( "AccDocAddInfo" );
      ЗаписатьПолеЛог( beginField, "AccDocAddInfo" );    
    end;

    for( var FldItem, EDDefineAnswerInfo )    
      if( IsSeparateElement(FldItem.FieldName) )
        УстановитьКонтекстБлокаЛог( FldItem.FieldName );
        ЗаписатьПолеЛог( beginField, FldItem.FieldName );
      end;
    
      ЗаписатьПолеЛог(FldItem.FieldName, FldItem.FieldValue);
    
      if( IsSeparateElement(FldItem.FieldName) )
        ЗаписатьПолеЛог( endField, FldItem.FieldName ) ;
        УстановитьКонтекстБлокаЛог( ".." );
      end;    
    end;
  
    if (EDDefineAnswerCode == "08")    
      ЗаписатьПолеЛог( endField, "AccDocAddInfo" ) ;
      УстановитьКонтекстБлокаЛог( ".." );   
    end;
  
    ЗаписатьПолеЛог( endField, "EDDefineAnswerInfo" ) ;
    УстановитьКонтекстБлокаЛог( ".." ); 
  end;
  // EDDefineAnswerInfo ----------------------------------------------
   
  // InitialED -------------------------------------------
  УстановитьКонтекстБлокаЛог( "InitialED" );
  ЗаписатьПолеЛог( beginField, "InitialED" );
  if(IsInMesFound)
    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn(wlmes.RelatedRef);

    ЗаписатьПолеЛог( "EDNo", ed_nda.EDNo );
    ЗаписатьПолеЛог( "EDDate", YYYYMMDD(ed_nda.EDDate) );
    ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
  else
    ЗаписатьПолеЛог( "EDNo",   "999999999" );
    ЗаписатьПолеЛог( "EDDate", YYYYMMDD({curdate}) );
    ЗаписатьПолеЛог( "EDAuthor", GetEDAuthor(wlReq.RecipientID) );
  end;
  ЗаписатьПолеЛог( endField, "InitialED" ) ;
  УстановитьКонтекстБлокаЛог( ".." );
  // InitialED -------------------------------------------
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

private macro CheckTransactionIDs(wlreq) : bool
  var ind1 = index(wlreq.Queries, "TransactionID:");

  if(ind1)
    var len = null;
    var ind2 = index(wlreq.Queries, "\n", ind1);
    if(ind2)
      len = ind2 - ind1;
    end;
    var MesFld : TMesField = getFieldFromQueriesLine( substr(wlreq.Queries, ind1, len) );
    var IDs : TArray = StrCut(MesFld.FieldValue);

    var i : integer, j : integer;
    for(i, 0, IDs.size - 2)
      for(j, i + 1, IDs.size - 1)
        if(IDs[j] == IDs[i])
          return FALSE;
        end;
      end;
    end;

  end;

  return TRUE;
end;

private var TextAfterCode : string = "";
macro addLineAfterCode(FieldName : string, FieldValue : string)
  TextAfterCode = TextAfterCode + "\n" + FieldName + ": " + FieldValue;
end;

// Дозаполнить учетный объект по альбому 2.5.0
private macro Refill250(EDDefineAnswerCode : string, wlmesreq_MesID : integer)
    // AccDocAddInfo --------------------------------------------
    if (EDDefineAnswerCode == "08")
      var MaskPath = "EDDefineRequestInfo/FieldQueryMask";
      
      var wlmes_req_xml:object = ActiveX( "MSXML.DOMDocument" );
      if(wlmesreq_MesID)
        if ( not wlmes_req_xml.loadXML( GetOneField(wlmesreq_MesID, "xml") ) )
          RunError( string( "Неверный формат сообщения по форме ED243" ) );
        end;
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field60", MaskPath) )
        addLineAfterCode("PayerINN", substr( wlpmrmprop.PayerINN, 1, index( wlpmrmprop.PayerINN, "/" ) - 1 ));      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field61", MaskPath) )
        addLineAfterCode("PayeeINN", substr( wlpmrmprop.ReceiverINN, 1, index( wlpmrmprop.ReceiverINN, "/" ) - 1 ));      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field101", MaskPath) )
        addLineAfterCode("DrawerStatus", wlpmrmprop.TaxAuthorState );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field102", MaskPath) )
        addLineAfterCode("PayerKPP", RemoveINN(wlpmrmprop.PayerINN) );
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field103", MaskPath) )
        addLineAfterCode("PayeeKPP", RemoveINN(wlpmrmprop.ReceiverINN) );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field104", MaskPath) )
        addLineAfterCode("CBC", wlpmrmprop.BTTTICode );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field105", MaskPath) )
        addLineAfterCode("OKATO", wlpmrmprop.OKATOCode );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field106", MaskPath) )
        addLineAfterCode("PaytReason", wlpmrmprop.TaxPmGround );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field107", MaskPath) )
        addLineAfterCode("TaxiPeriod", wlpmrmprop.TaxPmPeriod );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field108", MaskPath) )
        addLineAfterCode("DocNo", wlpmrmprop.TaxPmNumber );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field109", MaskPath) )
        addLineAfterCode("DocDate", wlpmrmprop.TaxPmDate );      
      end;
    
      if( not wlmesreq_MesID or ReadOptinalAttribute(wlmes_req_xml, "Field110", MaskPath) )
        addLineAfterCode("TaxPaytKind", wlpmrmprop.TaxPmType );      
      end;
    
    end;
    // AccDocAddInfo --------------------------------------------
end;

private macro AddLineByFldName( FieldName: string, pm, rm, cr, db, pspaydem,
                                PayerBank, PrevInstrAgent, SenderBank, 
                                ExecutorBank, IntermediaryBank, ReceiverBank )
  var FieldValue : string = "";

  if( GetFieldValFromPaym( null, FieldName, @FieldValue, 
                           IfThenElse(pm == null, wlpmpaym, pm), 
                           IfThenElse(rm == null, wlpmrmprop, rm), 
                           IfThenElse(cr == null, wlpmpropcred, cr), 
                           IfThenElse(db == null, wlpmpropdeb, db), 
                           IfThenElse(pspaydem == null, wlpspaydem, pspaydem),
                           PayerBank, PrevInstrAgent, SenderBank, 
                           ExecutorBank, IntermediaryBank, ReceiverBank 
                         )
    )
    addLineAfterCode(FieldName, FieldValue);
  end;
end;

// Если к платежу привязана разноска и в ней одна запись, то номер счета берется из разноски pmaddpi.Account
private macro GetAccountFromPmAddPI( PaymentID: integer, DebetCredit: integer, 
                                     Account: @string ): bool
  var q = "SELECT t_Account " +
          "  FROM dpmaddpi_dbt " +
          " WHERE t_PaymentID   = :PaymentID " +
          "   AND t_DebetCredit = :DebetCredit ";

  var params = makeArray( SQLParam("PaymentID",   PaymentID),
                          SQLParam("DebetCredit", DebetCredit) );

  var rs = execSQLselect(q, params);
  if(rs and rs.moveNext())
    Account = rs.value(0);
    if(not rs.moveNext()) // если запись только одна
      return TRUE;
    end;
  end;

  Account = "";
  return FALSE;
end;

private macro GetMesValRs(MesID: integer, FieldName: string, BlockName: string, ParentBlockName: string): RsdRecordset
  var select = "select wlmesval.t_value ";
  var from   = "  from dwlmesval_dbt wlmesval, dwltpfld_dbt wltpfld, "+
               "       dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfldM ";
  if(ParentBlockName)
    from = from+"     ,dwlmesfld_dbt wlmesfldMP ";
  end;
  var where  = " where wlmesval.t_MesID      = :MesID " +
               "   and wlmesval.t_TpFieldID  = wltpfld.t_TpFieldID " +
               "   and wltpfld.t_TpID        = 9 " +
               "   and wltpfld.t_Name        = :FieldName " +
               "   and wlmesfld.t_FieldID    = wlmesval.t_FieldID " +
               "   and wlmesfld.t_Master     = wlmesfldM.t_FieldID " +
               "   and wlmesfldM.t_BlockName = :BlockName ";
  if(ParentBlockName)
    where = where+"and wlmesfldM.t_Master    = wlmesfldMP.t_FieldID " +
               "   and wlmesfldMP.t_BlockName= :ParentBlockName ";
  end;
  var params : TArray = makeArray( SQLParam("MesID",     MesID),
                                   SQLParam("FieldName", FieldName),
                                   SQLParam("BlockName", BlockName) );
  if(ParentBlockName)
    params[ params.size ] = SQLParam("ParentBlockName", ParentBlockName);
  end;
  var rs = execSQLselect(select+from+where, params);
  return rs;
end;

private macro FillArrFieldNoFromED243(wlmesreq_MesID : integer, arrFieldNo : TArray)
  var rs = GetMesValRs(wlmesreq_MesID, "FieldNo", "FieldNo", "EDFieldList");
  while(rs and rs.moveNext())
    arrFieldNo[ arrFieldNo.size ] = rs.value(0);
  end;
end;

private macro AddLinesFieldByNo( arrFieldNo : TArray,
                                 PayerBank, PrevInstrAgent, SenderBank, 
                                 ExecutorBank, IntermediaryBank, ReceiverBank )
  var FieldValue = "";

  for(var FieldNo, arrFieldNo)
    if( GetFieldValFromPaym( FieldNo, null, @FieldValue, 
                             wlpmpaym, wlpmrmprop, 
                             wlpmpropcred, wlpmpropdeb, wlpspaydem,
                             PayerBank, PrevInstrAgent, SenderBank, 
                             ExecutorBank, IntermediaryBank, ReceiverBank
                           )
      )
      addLineAfterCode("Field/"+FieldNo, FieldValue);
    end;
  end;
end;

private macro GetTransactionIDStr(wlmesreq_MesID : integer)
  var IDStr = "";

  var IsFirst : bool = true;
  var rs = GetMesValRs(wlmesreq_MesID, "TransactionID", "EDReestrInfo");
  while(rs and rs.moveNext())
    if(not IsFirst)
      IDStr = IDStr + ",";
    else
      IsFirst = false;
    end;

    IDStr = IDStr + rs.value(0);
  end;

  return IDStr;
end;

private macro FillBuffers(EDDefineAnswerCode : string, pm, rm, cr, db, pspaydem)
  var rs: RsdRecordset;

  if( (EDDefineAnswerCode == "10") and
      (rs = getPurposePaymentRs(wlpmpaym.PaymentID, PMLINK_KIND_CLOSACC)) and
      rs.moveNext() and 
      (FindPayment( rs.value(0), 0, 0, 0, 0, true, pm, db, cr, rm ) == 0) 
    )
    // берем соответствующие реквизиты из связанного платежа
    // буферы pm, db, cr, rm уже заполнены функцией FindPayment, остался pspaydem
    if(pspaydem != null)
      var f_pspaydem : TbFile = TbFile("pspaydem.dbt");
      f_pspaydem.rec.OrderID = pm.PaymentID;
      if( (pm.DocKind == PS_PAYORDER) and GetEQ(f_pspaydem) )
        copy(pspaydem, f_pspaydem);
      else
        ClearRecord(pspaydem);
      end;
    end;
  else
    // заполняем значением соответствующих реквизитов исходного платежа
    copy(pm, wlpmpaym); copy(rm, wlpmrmprop); copy(cr, wlpmpropcred); copy(db, wlpmpropdeb);
    copy(pspaydem, wlpspaydem);
  end;
end;

// Дозаполнить учетный объект по альбому 2.5.9
private macro Refill259(EDDefineAnswerCode : string, wlmesreq_MesID : integer)
  RECORD pm(pmpaym); RECORD rm(pmrmprop); RECORD cr(pmprop); RECORD db(pmprop); 

  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);
  GetOperationParticipantsFromRoute
  ( wlpmpaym.PaymentID,
    PrevInstrAgent, SenderBank, ExecutorBank, 
    IntermediaryBank, PayerBank, ReceiverBank
  );

  var arrFieldNo : TArray = TArray(), FieldValue : string = "";

  if (EDDefineAnswerCode == "08")
    if(wlmesreq_MesID)
      FillArrFieldNoFromED243(wlmesreq_MesID, arrFieldNo);
    else
      arrFieldNo = makeArray("60", "61", "101", "102", "103", "104", "105", "106", "107", "108", "109", "110");
    end;

    AddLinesFieldByNo( arrFieldNo,
                       PayerBank, PrevInstrAgent, SenderBank, 
                       ExecutorBank, IntermediaryBank, ReceiverBank );

  elif(EDDefineAnswerCode == "09")
    AddLineByFldName( "EnterDate", null, null, null, null, null,
                      PayerBank, PrevInstrAgent, SenderBank, 
                      ExecutorBank, IntermediaryBank, ReceiverBank
                    );
    AddLineByFldName( "Sum", null, null, null, null, null,
                      PayerBank, PrevInstrAgent, SenderBank, 
                      ExecutorBank, IntermediaryBank, ReceiverBank
                    );
  elif(EDDefineAnswerCode == "10")
    FillBuffers(EDDefineAnswerCode, pm, rm, cr, db);

    // Если к платежу привязана разноска и в ней одна запись
    if( GetAccountFromPmAddPI(pm.PaymentID, PRT_Credit, @FieldValue) )
      // номер счета берется из разноски pmaddpi.Account
      addLineAfterCode("PayeeAcc", FieldValue);
    else
      AddLineByFldName( "PayeeAcc",    pm, rm, cr, db, null,
                        PayerBank, PrevInstrAgent, SenderBank, 
                        ExecutorBank, IntermediaryBank, ReceiverBank
                      );
    end;
    AddLineByFldName("PayeeCorrAcc",  pm, rm, cr, db, null,
                      PayerBank, PrevInstrAgent, SenderBank, 
                      ExecutorBank, IntermediaryBank, ReceiverBank
                    );
    AddLineByFldName("PayeeBIC",      pm, rm, cr, db, null,
                      PayerBank, PrevInstrAgent, SenderBank, 
                      ExecutorBank, IntermediaryBank, ReceiverBank
                    );
    AddLineByFldName("PayeeLongName", pm, rm, cr, db, null,
                      PayerBank, PrevInstrAgent, SenderBank, 
                      ExecutorBank, IntermediaryBank, ReceiverBank
                    );
  elif(EDDefineAnswerCode == "11")
    if(wlmesreq_MesID)
      FillArrFieldNoFromED243(wlmesreq_MesID, arrFieldNo);
      AddLinesFieldByNo( arrFieldNo,
                         PayerBank, PrevInstrAgent, SenderBank, 
                         ExecutorBank, IntermediaryBank, ReceiverBank );
    end;
  elif(EDDefineAnswerCode == "12")
    if(wlmesreq_MesID)
      FieldValue = GetTransactionIDStr(wlmesreq_MesID);
      if(FieldValue)
        addLineAfterCode("TransactionID", FieldValue);
      end;
    end;
  end;
end;

macro CheckObj(addrReq, addrRefillReq, ver_st )

  SetBuff( wlReq, addrReq );

  FILE f_pspaydem(pspaydem) key 0;

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  var code_index : integer = index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED244, @req_code);
  if(code_index == 0)
    RunError("Не найден код в сообщении");
  end;

  // Найти входящее сообщение с запросом wlmes_req, на которое ссылается ответ
  var wlmesreq_MesID = 0, RelatedRef = "", IsInMesFound = false;
  if( not FindInWlmes_req( wlReq.RelatedRef, @wlmesreq_MesID, @RelatedRef ) )
    // входящее сообщение не найдено
    IsInMesFound = false;
    RelatedRef = wlReq.RelatedRef;
  else
    // входящее сообщение найдено
    IsInMesFound = true;
  end;
  // Найти платеж pmpaym, pmrmprop, на который ссылается входящий запрос
  if( not WldFindPmByTRN_Tp( TRANSP_UFBS, RelatedRef, NULL, IfThenElse(ver_st == VER_ST_2_5_9, null, DIRECT_PAYM_OUT) ) )
    RsbThrow( IfThenElse( ver_st == VER_ST_2_5_9,
                          "Для формирования ответа необходимо указать исходный запрос или ЭПС, по которому уточняются реквизиты. Задайте в поле \"Ссылка\" референс сообщения вида \"запрос\" или \"платеж\".",
                          "Не найден входящий платеж|с референсом '" + RelatedRef + "'"
                        ) );
  else
    f_pspaydem.OrderID = wlpmpaym.PaymentID;
    if( GetEQ(f_pspaydem) )
      copy(wlpspaydem, f_pspaydem);
    else
      ClearRecord(wlpspaydem);
    end;
  end;

  if(ver_st == VER_ST_2_5_9)
    if(not CheckTransactionIDs(wlreq))
      RsbThrow("Номера записей реестра, по которым уточняется информация, должны быть уникальны в пределах одного ответа. Исключите повторение номеров в строке, начинающейся с символов \"TransactionID\".");
    end;
  end;

  // Дозаполнить учетный объект
  RECORD RefillReq(wlreq);
  if(ValType(addrRefillReq) == V_MEMADDR)
    SetBuff( RefillReq, addrRefillReq );
    Copy(RefillReq, wlReq, 1);      
  end;
  
  TextAfterCode = SubStr( wlReq.Queries, code_index + StrLen(req_code) );
  if( (Trim(TextAfterCode) == "") and (ValType(addrRefillReq) == V_MEMADDR) )
    
    var EDDefineAnswerCode : string = substr(req_code, 4, 2);
    var tmpstr : string = "", error : integer = 0;

    if ( InList(EDDefineAnswerCode, "01", "03", "19") )
      AddLineByFldName("AccDocDate");
      AddLineByFldName("AccDocNo");
      AddLineByFldName("PayerAcc");
    end;

    if ( InList(EDDefineAnswerCode, "01", "02", "03", "19") )
      AddLineByFldName("PayeeAcc");
    end;

    var PayerINN = "";
    if ( (EDDefineAnswerCode == "03") or (EDDefineAnswerCode == "06") )
      AddLineByFldName("PayerINN");
    end;

    if ( (EDDefineAnswerCode == "02") or (EDDefineAnswerCode == "03") )
      AddLineByFldName("PayeeINN");
    end;

    if ( InList(EDDefineAnswerCode, "01", "03", "19") )
      AddLineByFldName( "Sum" );
    end;

    // PayerLongName
    if ( (EDDefineAnswerCode == "01") or 
         (EDDefineAnswerCode == "03") or 
         (EDDefineAnswerCode == "04") )
      AddLineByFldName("PayerLongName");
    end;
    // PayerLongName

    // PayeeLongName
    if (  (EDDefineAnswerCode == "01") or (EDDefineAnswerCode == "02") 
       or (EDDefineAnswerCode == "03") or (EDDefineAnswerCode == "05") )
      AddLineByFldName("PayeeLongName");
    end;
    // PayeeLongName
  
    if ( InList(EDDefineAnswerCode, "19") )
      AddLineByFldName("PayerName");
      AddLineByFldName("PayeeName");
    end;

    // Purpose
    if (EDDefineAnswerCode == "07")
      AddLineByFldName("Purpose");
    end;
    // Purpose
  
    // Address
    if ( (EDDefineAnswerCode == "06") )
      AddLineByFldName("Address");
    end;
    // Address

    if(ver_st == VER_ST_2_5_9)
      Refill259(EDDefineAnswerCode, wlmesreq_MesID);
    else
      Refill250(EDDefineAnswerCode, wlmesreq_MesID);
    end;
  
    RefillReq.Queries = SubStr( wlReq.Queries, 1, code_index - 1 + StrLen(req_code) ) + TextAfterCode;
  end;

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

private macro CheckTransactionIDsInFldObj(AddFlds : RsbAddFld) : bool

  class (TRsbAddFldIterator) TTransactionsCheck(p_AddFldObj : RsbAddFld)

    InitTRsbAddFldIterator(p_AddFldObj);
    var TransactionIDs : TArray = TArray(),
        IsDoubleID : bool = false;

    macro CheckDouble(ID : string) : bool
      for(var SavedID : string, TransactionIDs)
        if(ID == SavedID)
          return FALSE;
        end;
      end;

      return TRUE;
    end;

    private macro OnProcessRecord(FldRec : TRecHandler)
      if(FldRec.rec.Number == "TransactionID")
        var TransactionID : string = FldRec.rec.Value;

        if( CheckDouble(TransactionID) == true )
          TransactionIDs[ TransactionIDs.size ] = TransactionID;
        else
          IsDoubleID = true;
          Finish();
        end;
      end;
    end;

  end;

  var TransactionsCheck : TTransactionsCheck = TTransactionsCheck(AddFlds);
  TransactionsCheck.Go();
  return (not TransactionsCheck.IsDoubleID);

end;

private macro GetInReqIDbyMesID(MesID : integer) : integer
  var q : string = "select t_ReqID "
                   "  from dwlreq_dbt req, dwlmeslnk_dbt lnk "
                   " where lnk.t_MesID = :MesID "
                   "   and lnk.t_Direct = 'X' /* WLD_MES_IN */ "
                   "   and lnk.t_ObjKind = :OBJTYPE_WLD_REQ "
                   "   and req.t_ReqID = lnk.t_ObjID "
                   "   and req.t_Kind = :WLD_MES_KIND_REQUEST ";

  var params : TArray = makeArray( SQLParam("MesID", MesID),
                                   SQLParam("OBJTYPE_WLD_REQ", OBJTYPE_REQ),
                                   SQLParam("WLD_MES_KIND_REQUEST", MESKIND_REQUEST) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    return rs.value("t_ReqID");
  end;

  return 0;
end;

private macro GetAddFields
( EDDefineAnswerCode : string, 
  wlmesreq_MesID : integer,
  ExplanatoryFields : @TArray, 
  RevisedFields : @TArray, 
  ReestrInfo : @TArray
)

  if(EDDefineAnswerCode == "01")
    ExplanatoryFields = makeArray( "AccDocDate", "AccDocNo", "PayerAcc", "PayerLongName",
                                   "PayeeAcc", "PayeeLongName", "Sum" );

  elif(EDDefineAnswerCode == "02")
    ExplanatoryFields = makeArray( "PayeeAcc", "PayeeINN", "PayeeLongName" );

  elif(EDDefineAnswerCode == "03")
    ExplanatoryFields = makeArray( "AccDocDate", "AccDocNo", "PayerAcc", "PayerINN", 
                                   "PayerLongName", "PayeeAcc", "PayeeINN", 
                                   "PayeeLongName", "Sum" );

  elif(EDDefineAnswerCode == "04")
    ExplanatoryFields = makeArray( "PayerLongName" );

  elif(EDDefineAnswerCode == "05")
    ExplanatoryFields = makeArray( "PayeeLongName" );

  elif(EDDefineAnswerCode == "06")
    ExplanatoryFields = makeArray( "PayerINN", "Address" );

  elif(EDDefineAnswerCode == "07")
    ExplanatoryFields = makeArray( "Purpose" );

  elif( InList(EDDefineAnswerCode, "08", "11") )
    if(wlmesreq_MesID)
      FillArrFieldNoFromED243(wlmesreq_MesID, RevisedFields);
    end;

  elif(EDDefineAnswerCode == "09")
    ExplanatoryFields = makeArray( "EnterDate", "Sum" );

  elif(EDDefineAnswerCode == "10")
    ExplanatoryFields = makeArray( "PayeeAcc", "PayeeCorrAcc", "PayeeBIC", "PayeeLongName" );

  elif(EDDefineAnswerCode == "12")
    var ReqID : integer = GetInReqIDbyMesID(wlmesreq_MesID);
    if(ReqID > 0)
      ReestrInfo = GetReestrInfoCopy(ReqID);
    end;
  elif(EDDefineAnswerCode == "19")
    ExplanatoryFields = makeArray( "AccDocDate", "AccDocNo", "PayerAcc",
                                   "PayerName", "PayeeAcc",
                                   "PayeeName", "Sum" );
  end;

end;

private macro InsertExplanatoryFlds
               ( AddFldNumbers : TArray, EDDefineAnswerCode : string,
                 pm, rm, cr, db, pspaydem,
                 PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                 AddFlds : RsbAddFld, LastSort : @integer
               ) : integer

  macro GetValue(EDDefineAnswerCode : string, Number : string, PaymentID : integer) : string
    var AddFldValue : string = null;

    if( (EDDefineAnswerCode == "10") and (Number == "PayeeAcc") and
        GetAccountFromPmAddPI(PaymentID, PRT_Credit, @AddFldValue)
      )
      return AddFldValue;
    end;

    return null;
  end;

  var stat : integer = 0;
  var AddFldValue : string = null;
  var addfldRec : TRecHandler = TRecHandler("wladdfld.dbt");

  for(var Number : string, AddFldNumbers)
    AddFldValue = GetValue(EDDefineAnswerCode, Number, pm.PaymentID);

    stat = InsertAddFld_ByBuffers( "П", Number, "ED244", null, AddFldValue, 
                                   pm, rm, cr, db, pspaydem, 
                                   PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                                   0/*ParentID*/, @LastSort, AddFlds, addfldRec );

    if(stat)
      return stat;
    end;
  end;

  return stat;
end;

private macro InsertRevisedFields
               ( AddFldNumbers : TArray,
                 pm, rm, cr, db, pspaydem,
                 PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                 AddFlds : RsbAddFld, LastSort : @integer
               ) : integer

  var stat : integer = 0;

  for(var Number : string, AddFldNumbers)

    stat = InsertAddFld_ByBuffers( "У", Number, "ED244", null, null, 
                                   pm, rm, cr, db, pspaydem, 
                                   PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                                   0/*ParentID*/, @LastSort, AddFlds );

    if(stat)
      return stat;
    end;
  end;

  return stat;
end;

macro CheckObj259(addrReq, addrRefillReq)

  SetBuff( wlReq, addrReq );
  var AddFlds : RsbAddFld = RsbAddFld(wlReq.ReqID);
  var ErrMsg : string = "";

  FILE f_pspaydem(pspaydem) key 0;

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  var code_index : integer = index_wlcode(wlReq.Queries, TYPE_CODE_UFBS_ED244, @req_code);
  if(code_index == 0)
    RunError("Не найден код в сообщении");
  end;

  // Найти входящее сообщение с запросом wlmes_req, на которое ссылается ответ
  var wlmesreq_MesID = 0, RelatedRef = "", IsInMesFound = false;
  if( not FindInWlmes_req( wlReq.RelatedRef, @wlmesreq_MesID, @RelatedRef ) )
    // входящее сообщение не найдено
    IsInMesFound = false;
    RelatedRef = wlReq.RelatedRef;
  else
    // входящее сообщение найдено
    IsInMesFound = true;
  end;
  // Найти платеж pmpaym, pmrmprop, на который ссылается входящий запрос
  if( not WldFindPmByTRN_Tp( TRANSP_UFBS, RelatedRef, NULL, null ) )
    if(IsInMesFound)
      ErrMsg = "Исходный запрос должен ссылаться на платеж";
    else
      ErrMsg = "Для формирования ответа необходимо указать исходный запрос или ЭПС, по которому уточняются реквизиты. Задайте в поле \"Ссылка\" референс сообщения вида \"запрос\" или \"платеж\".";
    end;

    RunError(ErrMsg, ErrMsg);
  else
    f_pspaydem.OrderID = wlpmpaym.PaymentID;
    if( GetEQ(f_pspaydem) )
      copy(wlpspaydem, f_pspaydem);
    else
      ClearRecord(wlpspaydem);
    end;
  end;

  if(not CheckTransactionIDsInFldObj(AddFlds))
    RsbThrow("Номера записей реестра, по которым уточняется информация, должны быть уникальны в пределах одного ответа. Исключите повторение номеров в полях с номером \"TransactionID\".");
  end;

  RECORD pm(pmpaym); RECORD rm(pmrmprop); RECORD cr(pmprop); RECORD db(pmprop); 
  RECORD pspaydem(pspaydem);
  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);
  // Дозаполнить учетный объект
  if(AddFlds.size == 0)
    var EDDefineAnswerCode : string = substr(req_code, 4, 2);
    var ExplanatoryFields : TArray = TArray(),
        RevisedFields : TArray = TArray(),
        ReestrInfo : TArray = TArray();

    GetAddFields( EDDefineAnswerCode, wlmesreq_MesID, 
                  @ExplanatoryFields, @RevisedFields, @ReestrInfo
                );
    FillBuffers(EDDefineAnswerCode, pm, rm, cr, db, pspaydem);
    GetOperationParticipantsFromRoute
        ( pm.PaymentID,
          PrevInstrAgent, SenderBank, ExecutorBank, 
          IntermediaryBank, PayerBank, ReceiverBank
        );

    var Sort : integer = 0;
    var stat : integer = 0;
    if(not stat)
      InsertExplanatoryFlds
               ( ExplanatoryFields, EDDefineAnswerCode,
                 pm, rm, cr, db, pspaydem,
                 PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                 AddFlds, @Sort
               );
    end;

    if(not stat)
      InsertRevisedFields
               ( RevisedFields,
                 pm, rm, cr, db, pspaydem,
                 PayerBank, PrevInstrAgent, SenderBank, ExecutorBank, IntermediaryBank, ReceiverBank,
                 AddFlds, @Sort
               );
    end;

    if(not stat)
      stat = InsertAnsReestrInfoFromCopy( ReestrInfo, pm, AddFlds, @Sort );
    end;

    if(stat)
      RunError("Ошибка при вставке дополнительных полей ответа");
    end;
  end;

  return true;

  OnError(er) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

private macro GetOriginalEPDFromInMesReq(wlmesreq_MesID : integer, ed_nda : EDNoDateAuthor)
  var q =  "select wltpfld.t_Name, wlmesval.t_value "+
           "  from dwlmesval_dbt wlmesval, dwltpfld_dbt wltpfld, "+
           "       dwlmesfld_dbt wlmesfld, dwlmesfld_dbt wlmesfldM " +
           " where wlmesval.t_MesID      = :MesID "+
           "   and wlmesval.t_TpFieldID  = wltpfld.t_TpFieldID "+
           "   and wlmesfld.t_FieldID    = wlmesval.t_FieldID " +
           "   and wlmesfld.t_Master     = wlmesfldM.t_FieldID " +
           "   and wlmesfldM.t_BlockName = :BlockName ";

  var rs = execSQLselect(q, makeArray( SQLParam("MesID",     wlmesreq_MesID),
                                       SQLParam("BlockName", "OriginalEPD") ));
  while(rs and rs.moveNext())
    if(rs.value("t_Name") == "EDNo")
      ed_nda.EDNo = rs.value("t_Value");
    elif(rs.value("t_Name") == "EDDate")
      ed_nda.EDDate = ToDate(rs.value("t_Value"));
    elif(rs.value("t_Name") == "EDAuthor")
      ed_nda.EDAuthor = rs.value("t_Value");
    end;
  end;
end;

private macro FindMesFieldWithName(MesFields : TArray, FieldName : string, FieldFound : @TMesField) : bool

  for(var MesField : TMesField, MesFields)
    if(MesField.FieldName == FieldName)
      FieldFound = MesField;
      return true;
    end;
  end;

  return false;
end;

private macro GetEDDefineRequestCode(InReqMesID : integer, EDDefineAnswerCode : string, ExplanatoryFields : TArray) : string
  var EDDefineRequestCode : string = "09";

  var MesField : TMesField = null;
  if(InReqMesID > 0)
    EDDefineRequestCode = GetOneField(InReqMesID, "EDDefineRequestCode");
  elif( FindMesFieldWithName(ExplanatoryFields, "RequestCode", @MesField) )
    EDDefineRequestCode = MesField.FieldValue;
  elif(EDDefineAnswerCode == "12")
    EDDefineRequestCode = "13";
  elif(EDDefineAnswerCode == "18")
    EDDefineRequestCode = "16";
  end;

  return EDDefineRequestCode;
end;

macro GenMes259( addrMes, addrReq )
  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  // Найти входящее сообщение с запросом wlmes_req, на которое ссылается ответ
  var wlmesreq_MesID = 0, wlmes_req_RelatedRef = "", IsInMesFound = false;
  if( not FindInWlmes_req( wlReq.RelatedRef, @wlmesreq_MesID, @wlmes_req_RelatedRef ) )
    IsInMesFound = false;
  else
    IsInMesFound = true;
  end;

  var EDDefineAnswerCode : string = "",
      EDDefineAnswerText : string = "",
      AddFldsInfo : TAddFldsInfo = TAddFldsInfo();

  ReadDataFromWlreq243244(wlReq, @EDDefineAnswerCode, @EDDefineAnswerText, AddFldsInfo);

  // EDNo, EDDate, EDAuthor
  FillEDNoDateAuthorByRef_CompoundRls(wlmes.TRN);

  ЗаписатьПолеЛог( "EDReceiver", GetEDReceiverForWlreq(wlreq) );
  ЗаписатьПолеЛог( "EDDefineRequestCode", GetEDDefineRequestCode(wlmesreq_MesID, EDDefineAnswerCode, AddFldsInfo.ExplanatoryFields) );
  ЗаписатьПолеЛог( "EDDefineAnswerCode", EDDefineAnswerCode );

  // OriginalEPD -------------------------------------------
  StartBlockLogXML( "OriginalEPD" );

  var ed_nda : EDNoDateAuthor = EDNoDateAuthor();
  if(IsInMesFound)
    GetOriginalEPDFromInMesReq(wlmesreq_MesID, ed_nda);
  else
    ed_nda.ConstructByTrn(wlmes.RelatedRef);
  end;

  ЗаписатьПолеЛог( "EDNo",   ed_nda.EDNo );
  ЗаписатьПолеЛог( "EDDate", YYYYMMDD(ed_nda.EDDate) );
  if (ed_nda.EDAuthor)
    ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
  end;

  FinishBlockLogXML( "OriginalEPD" );
  // OriginalEPD -------------------------------------------

  // EDDefineAnswerInfo
  WriteEDDefineReqAnsInfo("ED244", EDDefineAnswerText, AddFldsInfo);

  // InitialED -------------------------------------------
  if(IsInMesFound)
    StartBlockLogXML( "InitialED" );
    FillEDNoDateAuthorByRef_CompoundRls(wlmes.RelatedRef);
    FinishBlockLogXML( "InitialED" );
  end;
  // InitialED -------------------------------------------
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
