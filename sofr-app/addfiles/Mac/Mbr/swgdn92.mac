/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов SWIFT MTn92                                           */
/*                                                                          */
/*  Имя файла: swgdn92.mac                                                  */
/*  Создан:    22.09.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgendoc.mac";

/* Запрос */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */  
  InitFormName     :string,        /* Форма инициирующего сообщения */
  InitDate         :date,          /* Дата инициирующего сообщения */
  Description      :string,   

  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank;          /* Получатель */

  TRF              = "";
  RelatedRef       = "";
  InitFormName     = "";
  InitDate         = date(0,0,0);
  Description = "";

end; /* class StatementMessage */

var MTn92 : StatementMessage;

macro Fill20( TRF )
  MTn92.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MTn92.RelatedRef = TRF;
  return TRUE;
end;

macro Fill11( FormName, Date, SessNum, ISN )
  MTn92.InitFormName = FormName;
  MTn92.InitDate = Date;
  return TRUE;
end;

macro Fill79( Str )
  MTn92.Description = Str;
  return TRUE;
end;

macro FillCopy( Str )
  return TRUE;
end;

/* Заполнение списка полей формы  MTn92*/
var Fld20   = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21   = ТПолеФормы( RelatedReferenceField,           FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),    
    Fld11S  = ТПолеФормы( MTandDateOriginalMessage_SField, FIELD_OPTIONAL,  "Read11",  "Fill11", "" ),
    Fld79   = ТПолеФормы( NarrativeDescriptionField,       FIELD_OPTIONAL,  "Read79",  "Fill79", "" ),
    FldMFIELDS = ТПолеФормы( CopyMandatoryFields,          FIELD_OPTIONAL,  "ReadMFIELDS",  "FillCopy", "" );

macro ConstructMTn92( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MTn92.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MTn92.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMTn92 */

macro GenDoc( addrMes )
  MTn92 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса по МТn92");

  ClearRecord(wlreq);

  if( not ConstructMTn92( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlreq.TRN          = MTn92.TRF;
  wlreq.RelatedRef   = MTn92.RelatedRef;
  wlreq.InitDateMes  = MTn92.InitDate;
  wlreq.InitFormIDMes = GetFormIDByName(MTn92.InitFormName,TRANSP_SWIFT);
  
  if ( not НайтиКорсхемуПоКонтрагенту(wlmes.OutsideAbonentID, wlreq.Corschem, wlreq.FIID) )
      std.msg( "Не определена схема расчетов" );
      return FALSE;
  end;  
  
  if( not ВставитьЗапрос( wlreq, "", MTn92.Description ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
