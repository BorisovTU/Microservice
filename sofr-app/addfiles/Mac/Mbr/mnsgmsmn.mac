/*
  $Name:         mnsgmsmn.mac
  $Module:       МБР
  $Description:  Генерация сообщения по SMN 
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SMN                                               */
/*                                                                          */
/*  Имя файла: mnsgmsmn.mac                                                 */
/*  Создан:  05.12.04                                    Фомченкова Л.Н.    */
/****************************************************************************/
                                                              
import "wlgenmes.mac", "wlmnstls.mac", OprInter, PSInter, "fnsMesAccCommon.mac";

record Налогоплательщик(party);
record buff(reqchnga);
record GenAcc(account);
record OldAcc(account);


FILE persn( persn ) key 0;
FILE typeac( typeac ) key 0;
FILE dp_dep( dp_dep ) key 0;
FILE party( party ) key 0;

private const NATCUR = 0;
private const TA_SACNT  = 1;
private const TA_SACNTC = 4;


macro GenMes( addrMes, addrReq, addrOldMes, vers )
  Var RespID, Error, ИдПол, Адрес, ИНННП ="", КППНП="", ИННКППНП, ОГРННП = "", КодНОНП, ИДГНИКлиента, КодВидаСчета, КодНОИсх, ОГРНКО,
      Серия = "" , Номер = "", ДатаРегистрации = date(), СчетСтарый, ВалютаСтарая, 
      СчетНовый, ВалютаНовая, ErrorINNб, ФДЛБанка = "", ИДГНИБанка, КодНОБ, НаимКО, ИНН,
      ФИОИП, НомФ, ИННКО = " ", КППКО = " ", ДолжнПрБ, ФИОПрБ, ТелБанка, Нотариус, Адвокат, OldfnsMessageID, ВидРегОргана, БИК;

/*
  #139530  
  if(not IsMacroInOperation())
     RunError(string("|Данное сообщение МНС генерируется только на шаге операции"));
  end;
*/

  record OldMes(wlmes);  
  
  SetBuff( wlmes,    addrMes );
  SetBuff( buff, addrReq );
  SetBuff( OldMes,    addrOldMes );

  var IsRecreate:bool = OldMes.MesID!=0; //Перегенерация?

  PrintLog(2,"Генерация сообщения по SMN");
  ПолучитьСчет(buff.NewAccountFIID, buff.NewAccount, GenAcc );
  ПолучитьСчет(buff.OldAccountFIID, buff.OldAccount, OldAcc );
  dp_dep.Code = OldAcc.Department;
  GetEQ( dp_dep );
  party.PartyID = dp_dep.PartyID;
  GetEQ( party );

  /*ИдФайл*/
  ЗаписатьПолеЛог( "ИдФайл", " " );
  /*ТипИнФ*/
  ЗаписатьПолеЛог( "ТипИнф", "СООБЩИЗМЕН" );
  /*ВерсПрог*/
  ЗаписатьПолеЛог( "ВерсПрог", "RS-Bank V.6");
  /*ТелОтпр*/
  ЗаписатьПолеЛог( "ТелОтпр", " " );
  /*ДолжнОтпр*/
  ЗаписатьПолеЛог( "ДолжнОтпр", " " );
  /*ФамОтпр*/
  ЗаписатьПолеЛог( "ФамОтпр", " " );
  /*КолДок*/
  ЗаписатьПолеЛог( "КолДок", "1" );
  /*ВерсФорм*/
  if(vers == 501)
    ЗаписатьПолеЛог( "ВерсФорм", "5.01" );
  elif(vers == 502)
    ЗаписатьПолеЛог( "ВерсФорм", "5.02" );
  end;
  /*НомСооб*/
  
  if( vers == 501 )
  Error = not GetMessageNumber(party.PartyID, buff.OldAccount, buff.NewAccount, buff.OldAccountFIID, buff.NewAccountFIID, Серия, Номер);
  elif( vers == 502 )
    Error = not GetMessageNumber502(party.PartyID, buff.OldAccount, buff.NewAccount, 1, buff.OldAccountFIID, GenAcc.Client, Серия, Номер, IfThenElse( IsRecreate == true, OldMes.MesID, 0));
  end;  
  if(Error)
     RunError(string("Ошибка при генерации номера сообщения"));
  end;    
  ЗаписатьПолеЛог("НомСооб", string(Серия,",",Номер));
  
  /*ИдДок*/
  ЗаписатьПолеЛог( "ИдДок", " ");
  /*ДатаСооб*/
  ЗаписатьПолеЛог("ДатаСооб", " ");
  if( GetKindActionFNS() !=  KIND_ACTION_FNS_NULL )
    /*КодНОБ*/
    ИДГНИБанка = party.taxInstitution;//ПолучитьИДГНИ(party.PartyID);
    КодНОБ = ПолучитьКодСубъекта( ИДГНИБанка, PTCK_MNS, Error);
    ЗаписатьПолеЛог( "КодНОБ", КодНОБ );
    /*НаимКО*/
    НаимКО = ПолучитьНаимКо(dp_dep.Code);
    ЗаписатьПолеЛог("НаимКО",НаимКО);
    /*АдрКО*/
    Адрес = ПолучитьАдрес({OurBank});
    if(Адрес != "")
       ЗаписатьПолеЛог( "АдрКО", Адрес );
    end;

    /*НомСчСтар*/
    ЗаписатьПолеЛог( "НомСчСтар", buff.OldAccount );
    /*НомерДог*/
      var SfCntr : TRecHandler = TRecHandler("sfcontr.dbt");
      ПолучитьДоговорОбслуживания( buff.OldAccountFIID, buff.OldAccount, SfCntr );
      ЗаписатьПолеЛог("НомерДог", SfCntr.rec.Number);
    /*НаимНП*/
    /*ФИОИП*/
        ПолучитьСубъекта(GenAcc.Client, Налогоплательщик);
    ИНН = ПолучитьКодСубъекта(Налогоплательщик.PartyID, PTCK_INN, Error);
    ИНННП = RemoveKPP(ИНН);
    КППНП = RemoveINN(ИНН);
    
    if(strlen( ИНННП ) != 12)  
      ЗаписатьПолеЛог("НаимНП",Налогоплательщик.Name);
    else
      ClearRecord(persn);
      persn.PersonID = Налогоплательщик.PartyID;
      if( getEQ(persn) )
        ФИОИП =  string( persn.Name1 + "," + persn.Name2 + "," + persn.Name3);
        ЗаписатьПолеЛог("ФИОИП", ФИОИП);
    end;
    end;
    /*ИНННП*/
    ЗаписатьПолеЛог( "ИНННП", ИНННП );
    /*КППНП*/
    if(КППНП != "")
      ЗаписатьПолеЛог( "КППНП", КППНП );
    else
      ЗаписатьПолеЛог( "КППНП", " " );
    end;
    /*ОГРННП*/
    ОГРННП = ПолучитьКодСубъекта(Налогоплательщик.PartyID, PTCK_OGRN, Error, 0, 1);
    CheckObjAttrPresence( Нотариус, OBJTYPE_PARTY, UniID( Налогоплательщик, OBJTYPE_PARTY ), 
                          PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Нотариус ); 
    CheckObjAttrPresence( Адвокат,  OBJTYPE_PARTY, UniID( Налогоплательщик, OBJTYPE_PARTY ), 
                          PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Адвокат );
    if( (ОГРННП != "") and (Налогоплательщик.NotResident != "X") and 
        (not Нотариус) and (not Адвокат) // не является нотариусом или адвокатом
      )
      ЗаписатьПолеЛог( "ОГРННП", ОГРННП );
    else
      ЗаписатьПолеЛог( "ОГРННП", " " );
    end;
    
    if(vers == 501)
      /*ПризнИзм*/
      ЗаписатьПолеЛог( "ПризнИзм", "2" );
    elif(vers == 502)
      /*ПризнИзмНСч*/
      ЗаписатьПолеЛог( "ПризнИзмНСч", "1" );
      /*ВалСч*/
      if( buff.NewAccountFIID == NATCUR )
         КодВидаСчета = "0";
         typeac.iNumType = TA_SACNT;
      else
         КодВидаСчета = "1";
         typeac.iNumType = TA_SACNTC;
      end;
      ЗаписатьПолеЛог("ВалСч", КодВидаСчета);
      /*ВидСч*/
      typeac.Type_Account = buff.NewAccountType;
      if(getEQ( typeac ))
        ЗаписатьПолеЛог("ВидСч",typeac.Name_Type);
      end;

      /*ПризнИзмКО*/
      ЗаписатьПолеЛог( "ПризнИзмКО", "0" );

    end;
    /*РегНомКО*/
    ЗаписатьПолеЛог("РегНомКО", string(int(SubStr(Серия,1,4))));
    /*НомФ*/
    if( not НашБанкГоловной(party.PartyID) )
      НомФ = string(int(SubStr(Серия,5,4)));
      ЗаписатьПолеЛог("НомФ", НомФ );
    else
      ЗаписатьПолеЛог("НомФ", " ");
    end;
    /*БИК*/
    БИК = ПолучитьКодСубъекта( party.PartyID, PTCK_BIC, Error, 1, 1);
    ЗаписатьПолеЛог( "БИК", БИК );
    /*ИННКО*/
    ПроверитьИНН();
    ИННКО = RemoveKPP(GetPartyINN(party.PartyID, 1) );
    ЗаписатьПолеЛог( "ИННКО", ИННКО );
    /*КППКО*/
    КППКО = RemoveINN(GetPartyINN(party.PartyID, 1) );
    ЗаписатьПолеЛог( "КППКО", КППКО );
    /*ОГРНКО*/
    ОГРНКО = ПолучитьКодСубъекта(party.PartyID, PTCK_OGRN, Error, 0, 1 );
    ЗаписатьПолеЛог( "ОГРНКО", ОГРНКО );
    /*НомСчИз*/
    ЗаписатьПолеЛог( "НомИзмСч", buff.NewAccount );
    /*ДатаИзмСч*/
      ЗаписатьПолеЛог( "ДатаИзмСч", DDpMMpYYYY(GenAcc.Open_Date) ); 
    /*ДолжнПрБ*/
    GetRegValForOPENAC("ИМНС_ДОЛЖ", V_STRING, ДолжнПрБ);
    ЗаписатьПолеЛог("ДолжнПрБ",ДолжнПрБ);
    /*ФИОПрБ*/
    ФИОПрБ = "";
    Error = GetRegValForOPENAC("ИМНС_ФИО", V_STRING, ФИОПрБ);
      ФИОПрБ = РазделитьЗапятымиФИО( ФИОПрБ );
    ЗаписатьПолеЛог( "ФИОПрБ", ФИОПрБ );

    /*ТелБанка*/
    ТелБанка = ПолучитьТелефон(party.PartyID);
    if( ТелБанка != "")
      ЗаписатьПолеЛог("ТелБанка",ТелБанка);
    else
      ЗаписатьПолеЛог("ТелБанка", " ");
    end;
    ВидРегОргана = GetKindRegOrgan();
    if( ВидРегОргана )
     ЗаписатьПолеЛог( "_ВидРегОргана", string(ВидРегОргана) );
    end;
    /* Способ обмена - электронно */
    ЗаписатьПолеЛог( "_СпОбм", string(GetllValueElement( 2300, "Электронно", Error )) );
  else
    OldfnsMessageID = GetOldfnsMessageID();
    ЗаписатьПолеДляАннул( "КодНОБ",    OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НаимКО",    OldfnsMessageID );
    ЗаписатьПолеДляАннул( "АдрКО",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомСчСтар", OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомерДог",  OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НаимНП",    OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ФИОИП",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ИНННП",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КППНП",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ОГРННП",    OldfnsMessageID );
    if(vers == 501)
      ЗаписатьПолеДляАннул( "ПризнИзм", "2" );
    elif(vers == 502)
      ЗаписатьПолеДляАннул( "ПризнИзмНСч", OldfnsMessageID);
      ЗаписатьПолеДляАннул( "ВалСч",       OldfnsMessageID);
      ЗаписатьПолеДляАннул( "ВидСч",       OldfnsMessageID);
      ЗаписатьПолеДляАннул( "ПризнИзмКО",  OldfnsMessageID);
    end;
    ЗаписатьПолеДляАннул( "РегНомКО",  OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомФ",      OldfnsMessageID );
    ЗаписатьПолеДляАннул( "БИК",       OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ИННКО",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "КППКО",     OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ОГРНКО",    OldfnsMessageID );
    ЗаписатьПолеДляАннул( "НомИзмСч",  OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ДатаИзмСч", OldfnsMessageID ); 
    ЗаписатьПолеДляАннул( "ДолжнПрБ",  OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ФИОПрБ",    OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ТелБанка",  OldfnsMessageID );
    ЗаписатьПолеДляАннул( "_СпОбм",    OldfnsMessageID );
    
  end;

  return TRUE; /* Успешное завершение */
  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenMes501( addrMes, addrReq, addrOldMes )
 return GenMes( addrMes, addrReq, addrOldMes, 501); 
end;

macro GenMes502( addrMes, addrReq, addrOldMes )
 return GenMes( addrMes, addrReq, addrOldMes, 502 );
end;

/*
macro CheckObj(addrReq)
  SetBuff(buff, addrReq);
  return MNS_CheckObj( buff, ACCMSG_KIND_CHNG );
end;
*/
