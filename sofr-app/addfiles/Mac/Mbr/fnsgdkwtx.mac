/*
$Name:           fnsgdkwtx.mac
$Module:         МБР
$Description:    Процедура генерации учетного объекта по сообщению формы KWT_300
*/

import wldoc, wlmnstls, OprInter, FIInter, PTInter, PaymInter, wlgendoc, PsInter;

private macro GetllName( List, Element ) : string
  var select:string;
  var rs:object;
  var params:TArray;
  select = " select t_Name from dllvalues_dbt where t_List=:List and t_Element=:Element ";
  params = makeArray( SQLParam("List", List  ),
                      SQLParam("Element", Element) );
  rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    return rs.value(0);
  end;
  return "";
end;

// Найти исходное сообщение и квитанцию wlresprm
private macro FindOrigMessAndReceipt( MesID:integer, FileName:string, Type:integer ) : bool
  var select = " select res.t_ResultID " +
               "   from dwlresprm_dbt res " +
               "  where res.t_InitialMesID = :MesID " +
               "    and res.t_FileName = :FileName " +
               "    and res.t_Type = :Type ";

  var params = makeArray( SQLParam( "MesID", MesID ), 
           SQLParam( "FileName", FileName ),
           SQLParam( "Type", Type ) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )
    return TRUE;
  end;
  return FALSE;
end;

// Вызов функции ОшибкаЛогическогоКонтроляСообщения
private macro DefectMes( MesID:integer, ResultID:@integer, NoChangeState:bool, State_:integer ):integer

  var stat:integer = 0;
  var ErrCode, ErrDescription, State;

  if( MesID > 0 )
    MesID = MesID;
    Errcode = FNS_RP_NOT_APPROVED_FNS;
    ErrDescription = GetllName( OBJTYPE_WLRESCODE_MNS, Errcode );
    State = pyOR( State_, 0 );
  else
    MesID = 0;
    Errcode = FNS_RP_APPROVED;
    ErrDescription = "Не найдено исходное сообщение";
    State = 50;
  end; 
  stat = IfThenElse( ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID, NULL, NULL, NULL, pyOR( NoChangeState, FALSE) ), 1, 0 );
  return stat;
end;

// Получить дату из поля ДатаВремяПроверки
private macro GetDate( DateTime:string ):date

  return date( int( SubStr(DateTime,9,2) ), int( SubStr(DateTime,6,2) ), int( SubStr(DateTime,1,4) ) );
end;

// Найти запись wlresprm
private macro GetResultID( MesID:integer ):integer

  var select = " select res.t_ResultID " +
               "   from dwlresprm_dbt res " +
               "     where res.t_InitialMesID = :MesID " +
               "       and res.t_Type = 0 " +
               "       and res.t_ErrCode = 7 " +
               "       and not exists ( select 1 from dwlmeslnk_dbt lnk " +
                                       "   where lnk.t_ObjID = res.t_ResultID " +
                                       "     and lnk.t_ObjKind = :OBJTYPE_MESDEFECT )";
  var params = makeArray( SQLParam( "MesID", MesID ), SQLParam( "OBJTYPE_MESDEFECT", OBJTYPE_MESDEFECT ) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )
    return rs.Value(0);
  end;
  return 0;
end;

private macro getObjType( ObjID ): integer
  var select = " select t_LnkDocType from dwlregdec_dbt where t_DecisionID = :ObjID ";
  var rs = execSQLselect( select, makeArray(SQLParam("", ObjID)), FALSE );
  if( rs and rs.moveNext() )
    return rs.value(0);
  end;
  return 0;
end;

//Проверить сообщения учётного объекта
private macro CheckOtherMess( MesID:integer ):bool

  var select = " select mes.t_MesID, lnk.t_ObjKind, lnk.t_ObjID " +
               "   from dwlmes_dbt mes, dwlmeslnk_dbt lnk, dwlmeslnk_dbt lnk1, dwlmesval_dbt val, dwlmesval_dbt val1 " +
               "     where mes.t_MesID = lnk1.t_MesID " +
               "       and lnk1.t_ObjID   = lnk.t_ObjID " +
               "       and lnk1.t_ObjKind = lnk.t_ObjKind " +
               "       and lnk.t_MesID = :MesID " +
               "       and mes.t_MesID <> lnk.t_MesID "  +
               "       and mes.t_State not in (25, 60) " +
               "       and mes.t_MesID = val1.t_MesID " +
               "       and :MesID = val.t_MesID " +
               "       and val1.t_TpFieldID = ( select t_TpFieldID from dwltpfld_dbt where t_Name = '_НомерОтправ' ) " + 
               "       and val.t_TpFieldID  = ( select t_TpFieldID from dwltpfld_dbt where t_Name = '_НомерОтправ' ) " +
               "       and val1.t_Value < val.t_Value ";

  var params = makeArray( SQLParam( "", MesID ), SQLParam( "", MesID ) );
  var rs = execSQLselect( select, params, FALSE );
  if( rs and rs.moveNext() )
    var Object = "справку/выписку", 
        ObjType = getObjType(rs.value("t_ObjID"));
    if( ObjType == OBJTYPE_FNSINFO )
      Object = "справку";
    elif( ObjType == OBJTYPE_REQ )
      Object = "выписку";
    end;
    msgbox( "Квитанция поступила на " + Object + ", у которой есть несквитованные сообщения, отправленные ранее. Для этих сообщений выполните процедуру квитовки, после чего повторите генерацию объекта" );
    return true;
  end;
  return false;
end;

// Создать запись WlError
private macro CreateWlError( Result:string, Description:string, Code:string, Value:string, ResultID:integer )

  var ErrList = RsbWlError(0);

  var wlerr_buf = TRecHandler("wlerror.dbt");
  wlerr_buf.rec.Code         = Result;

  if( StrLen( Description ) > 0 )
    wlerr_buf.rec.Description  = Description;
  else
    GetWlcodeDescription( 76, Result );
  end;

  if( StrLen( Code ) > 0 )
    wlerr_buf.rec.FieldCode = Code;
    if( StrLen( wlerr_buf.rec.Description ) > 0 )
      wlerr_buf.rec.Description = wlerr_buf.rec.Description + ". ";
    end;
    wlerr_buf.rec.Description = wlerr_buf.rec.Description + "Код реквизита:" + Code;
  end;
  if( StrLen( Value ) > 0 )
    wlerr_buf.rec.FieldValue = Value;
    if( StrLen( wlerr_buf.rec.Description ) > 0 )
      wlerr_buf.rec.Description = wlerr_buf.rec.Description + ". ";
    end;
    wlerr_buf.rec.Description = wlerr_buf.rec.Description + "Значение реквизита:" + Value;
  end;

  if( ErrList.Insert( wlerr_buf ) )
    return false;
  end;

  if( ResultID )
    // Сохраняем список ошибок квитанции в БД
    if( ErrList.TransferSelect( ResultID ) != 0 )
      RunError( "Ошибка при сохранении списка ошибок обработки сообщения" ); 
      return false;
    end;
  end;

  return true;
end;

// Првоерить наличие поля
private macro isExistsField( MesID:integer, FieldName:string ):bool
  
  var select = " select 1 from dwlmesval_dbt val " +
               "   where val.t_MesID = :MesID    " +
               "     and val.t_TpFieldID = ( select t_TpFieldID from dwltpfld_dbt where t_Name = :FieldName )";

  var params = makeArray( SQLParam("MesID", MesID), SQLParam("FieldName", FieldName) );
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )
    return true;
  end;
  return false;
end;

// Отбраковать сообщения УО
private macro DefectOtherMess( MesID:integer ):integer

   var stat:integer = 0;
   if( isExistsField( MesID, "НомЧасти" ) )
     var select = " select mes.t_MesID " +
               "   from dwlmes_dbt mes, dwlmeslnk_dbt lnk, dwlmeslnk_dbt lnk1, dwlmesval_dbt val, dwlmesval_dbt val1 " +
               "     where mes.t_MesID = lnk1.t_MesID " +
               "       and lnk1.t_ObjID   = lnk.t_ObjID " +
               "       and lnk1.t_ObjKind = lnk.t_ObjKind " +
               "       and lnk.t_MesID = :MesID " +
               "       and mes.t_MesID <> lnk.t_MesID "  +
               "       and mes.t_State > 25 " +
               "       and val1.t_TpFieldID = ( select t_TpFieldID from dwltpfld_dbt where t_Name = '_НомерОтправ' ) " + 
               "       and val.t_TpFieldID  = ( select t_TpFieldID from dwltpfld_dbt where t_Name = '_НомерОтправ' ) " +
               "       and val1.t_Value = val.t_Value ";
 
    var params = makeArray( SQLParam( "MesID", MesID ) );
    var rs = execSQLselect( select, params, FALSE );

    while( rs and rs.moveNext() )
      stat = ОшибкаЛогическогоКонтроляСообщения( rs.Value(0), FNS_RP_NOT_APPROVED_PART_EXTRACT, GetllName( OBJTYPE_WLRESCODE_MNS, FNS_RP_NOT_APPROVED_PART_EXTRACT ), 0, 0 );
    end;
  end;
  return stat;
end;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация УО по KWT");

  var field_name, field_value, block_name, err = 0;
  var FileName:string = "";
  var MesID:integer = 0, MesState:integer = 0;
  var isFirstResult:bool = true;
  var ResultCode:string = "";
  var DateTime:string = "";
  var needCreateError:bool = false;
  var Description, Code, Value;
  var ResultID = 0;
  var isErrorInserted = false;

  var RejectFlag:bool = false;
  GetRegistryValue( "PS\\REQOPENACC\\OPERATION\\Отвергать_Части_Выписки", V_BOOL, RejectFlag, err);
  
  while( СчитатьПоле( field_name, field_value, block_name ) )
    if( field_name == "ИмяФайла" )
      FileName = field_value;

      if( Index( FileName, "BZ1" ) == 1 )
        FileName = Substr( FileName, 5, StrLen( FileName ) - 4 - 9 )
      end;
 
      var select = "select mes.t_MesID, mes.t_State, mes.t_InsideAbonentID, mes.t_InsideAbonentCodeKind, mes.t_InsideAbonentCode " +
                   "  from dwlmes_dbt mes, dwlsess_dbt sess " +
                   "    where mes.t_SessionID = sess.t_SessionID " +
                   "      and upper(substr(sess.t_FileName, instr(sess.t_FileName, '\\', -1, 1) + 1)) = upper(:FILENAME || '.xml')";
      var params = makeArray( SQLParam("FILENAME", FileName) );
      var rs = execSQLselect( select, params, FALSE );

      if( rs and rs.moveNext() )
        MesID = rs.Value(0);
        MesState = rs.Value(1);
        wlmes.InsideAbonentID = rs.Value(2);
        wlmes.InsideAbonentCodeKind = rs.Value(3);
        wlmes.InsideAbonentCode = rs.Value(4);

        if( CheckOtherMess( MesID ) )
          return false;
        end;
      end;
    end;

    if( field_name == "ДатаВремяПроверки" )
      DateTime = field_value;
    end;

    var reqBlck = "Результат";
    var pos = index(block_name, reqBlck);
    if( (pos > 0) AND (pos+strlen(reqBlck) == strlen(block_name)+1 ) ) // проверим, что это последний блок
      if( field_name == "КодРезПроверки" )
        ResultCode = field_value;
      end;
      if( field_name == "Пояснение" )
        Description = field_value;
      end;
      if( field_name == "КодРекв" )
        Code = field_value;
      end;
      if( field_name == "ЗначРекв" )
        Value = field_value;
      end;

      if( (field_name == "_end") AND (isFirstResult == true) )
        isFirstResult = false;
        var NoChangeState:bool = false, State = -1;
        if( ResultCode != "01" )

          needCreateError = true;

          if( ( MesState == 25 ) and ( RejectFlag == false ) )
            ResultID = GetResultID( MesID );
            if( ResultID > 0 )
              /*Связь подтверждения с входящим сообщением */
              var meslnk = TRecHandler("wlmeslnk.dbt");
              meslnk.rec.MesID = wlmes.MesID;
              meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
              meslnk.rec.ObjID   = ResultID;
              meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
              var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
              MesLnkObj.Insert( meslnk );
              MesLnkObj.Save();
            else
              if( FindOrigMessAndReceipt( MesID, FileName, 0 ) == TRUE )
                NoChangeState = TRUE;
                State = 100;
              else
                NoChangeState = FALSE;
                State = 0;
              end;
              DefectMes( MesID, NULL, NoChangeState, State );
            end;
          else 
            DefectMes( MesID, @ResultID );
          end;
          isErrorInserted = CreateWlError( ResultCode, Description, Code, Value, ResultID );
        else // ResultCode == "01"
          var DeliveryDate:date;

          if( (FindOrigMessAndReceipt( MesID, FileName, 1 ) == TRUE) OR (wlmes.State == WLD_STATUS_MES_DEFECT) )
            NoChangeState = true;
          else
            NoChangeState = false;
          end;

          ВставитьПодтверждениеОДоставке( MesID, NoChangeState, GetDate( DateTime ), GetllName( OBJTYPE_WLRESCODE_MNS, FNS_RP_APPROVED ), true )
        end;
      elif( needCreateError == true ) //isFirstResult == false
        if( field_name == "_end" ) // необходимо проверять блоками, иначе одна ошибка будет записываться несколько раз
          isErrorInserted = CreateWlError( ResultCode, Description, Code, Value, ResultID );
        end;
        if( ( MesID > 0 ) and ( RejectFlag == false ) )   
          DefectOtherMess( MesID );
        end;
      end;
    end;

    if( isErrorInserted and needCreateError )
      ResultCode = Description = Code = Value = "";
      isErrorInserted = false;
    end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
