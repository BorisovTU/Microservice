/*
$Name:           ufgm218.mac
$Module:         Межбанковские расчеты
$Description:    Генерация сообщения ED218 транспорта УФЭБС
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED202 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm202.mac                                                  */
/*  Создан:    23.06.06                                  Гайворонский Е.Г.  */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac";

var ver_st = 1;

private macro GetReportIDField( wlreq )
  var ReportID = "";
  var pos = Index(wlreq.queries, "ReportID");
  if( pos > 0 )
    ReportID = SubStr( wlreq.queries, pos + 9, 7 );
  end;
  return ReportID;
end;

private macro GetStatusCodeField( wlreq )
  var MakingStatusCode = "";
  var pos = Index(wlreq.queries, "MakingStatusCode");
  if( pos > 0 )
    MakingStatusCode = SubStr( wlreq.queries, pos + 17, 1 );
  end;
  return MakingStatusCode;
end;

macro ВычислитьКод( Строка )
   if((StrLen(Строка) == 1)  And ( Index(Строка, "0") or Index(Строка, "1")))
      return true;
   else
      return false;
   end;
end;

/* Заполнение полей сообщения-запроса на получение выписки ED210 */
macro GenMes( addrMes, addrReq )

  SetBuff( wlmes,  addrMes );
  SetBuff( wlreq,  addrReq );

  var strXml;
  var InitMesId:integer = 0;
  var IsRequestPacket:bool = false;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED218");
  
  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  var ReportID = GetReportIDField( wlreq );
  if( strlen(ReportID) > 0 )
    mes.setAttribute("ReportID", ReportID );
  end;
  
  var MakingStatusCode = GetStatusCodeField( wlreq );
  if( strlen(MakingStatusCode) > 0 )
    mes.setAttribute( "MakingStatusCode", MakingStatusCode );
  else
    MsgBox("Не задан код признака формирования формы. Выберите необходимый код из справочника по F3 в поле \"Сообщение\"");
    return FALSE;
  end;

  mes.setAttribute("ReportDate", YYYYMMDD(wlreq.InitDateMes) );
  
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV2( addrMes, addrReq )
  ver_st = 2;
  return GenMes( addrMes, addrReq );
end;
