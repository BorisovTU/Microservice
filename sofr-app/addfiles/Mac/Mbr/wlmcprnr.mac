/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Печать сообщений МЦИ (R-макет) по ОКУД 04011101                         */
/*  Печать платежного поручения заимствована из ГКБО                        */
/*                                                                          */
/*  Имя файла: wlmciprnr.mac                                                */
/*  Создан:    09.04.01                                      Алешин А.В.    */
/****************************************************************************/

import "wlmcgd.mac";

/* Макрос для печати поля */
macro Draw( TRN, createdate, creator,number, docdate,dptch,payerINN,
            payer, deb_acc, payer_corracc, bank_payer,  payerMFO,
            sum,receiverINN,
            receiver, kred_acc, receiver_corracc,bank_receiver, receiverMFO,
            ground, priority, paydate, kindoper, ClnDate, DateChargeOffDate, TaxAuthorSate,
            PayerKPP, ReceiverKPP, BttTICode, OKATOCode, TaxPmGound, TaxPmPeriod, TaxPmNumber, TaxPmDate, TaxPmType )

  var rub, sumstr, pospoint, Плательщик, Получатель, ClnDateStr, DateChargeOffDateStr;

  
  Плательщик = payer;
  Получатель = receiver;


  ARRAY SS,SG,SBP,SBR,SP,SR;

  if ( IsOwnerAccOwnBank(payerMFO, 0, deb_acc) )
     ClnDateStr = mkstr(" ", 12 );
     DateChargeOffDateStr = mkstr(" ", 12 );
  else
     ClnDateStr = string(ClnDate:f);
     DateChargeOffDateStr = string(DateChargeOffDate:f);
  end;

  sumstr = string(MoneyL(sum));
  rub = RubToStrAlt(sum);
  pospoint = Index(sumstr, ".");
  if(Int(SubStr(sumstr, pospoint + 1, 2)) == 0 )
    rub = SubStr(rub,1,Index(rub,"коп")-4);
  end;

  strsplit( Плательщик,         SP,    50,50,4 ); /* Плательщик      */
  strsplit( bank_payer,         SBP,   50,50,2 ); /* Банк-плательщик */
  strsplit( rub,                SS,    76,76,3 ); /* Сумма пpописью  */
  strsplit( ground,             SG,    85,85,3 ); /* Основание       */
  strsplit( bank_receiver,      SBR,   50,50,2 ); /* Банк-получатель */
  strsplit( Получатель,         SR,    50,50,4 ); /* Получатель      */

[
                                                          Код формы документа
                                                              по ОКУД 0401101

   Порядковый номер                       Дата составления
   электронного документа ########        электронного документа ##########

   Уникальный
   идентификатор составителя #

  ─────────────────────────────────────────────────────────────────────────────────────────────────

       ############              ############
  ───────────────────────   ──────────────────────
    Поступ. в банк плат.     Списано со сч. плат.
                                                                                  ┌────┐
   Платежное поручение N ###############        ############     #############    │ ## │
                                                ────────────     ─────────────    └────┘                   
                                                    Дата          Вид платежа
                                                                                                     
   Сумма   │ ####################################################################################    
   прописью│ ####################################################################################    
           │ ####################################################################################    
  ─────────┴──────────────────────────────────────────┬───────────┬────────────────────────────────  
   ИНН ##################### КПП #################### │ Сумма     │ #############################    
   ################################################## │           │                                  
   ################################################## │           │                                  
   ################################################## ├───────────┤                                  
   ################################################## │ Сч.N.     │ #############################    
                                                      │           │                                  
   Плательщик                                         │           │                                  
  ────────────────────────────────────────────────────┼───────────┼────────────────────────────────  
   ################################################## │ БИК       │ #############################    
   ################################################## ├───────────┤                                  
                                                      │ Сч.N.     │ #############################    
   Банк плательщика                                   │           │                                  
  ────────────────────────────────────────────────────┼───────────┼────────────────────────────────  
   ################################################## │ БИК       │ #############################    
   ################################################## ├───────────┤                                  
                                                      │ Сч.N.     │ #############################    
   Банк получателя                                    │           │                                  
  ────────────────────────────────────────────────────┼───────────┤                                  
   ИНН ##################### КПП #################### │ Сч.N.     │ #############################    
   ################################################## │           │                                  
   ################################################## ├───────────┼──────┬────────────┬────────────  
   ################################################## │  Вид оп.  │ #### │ Срок плат. │ ##########   
   ################################################## ├───────────┤      ├────────────┤              
                                                      │ Наз. пл.  │      │ Очер плат. │ ##########   
                                                      ├───────────┤      ├────────────┤              
   Получатель                                         │ Код       │      │ Рез. поле  │              
  ─────────────────────┬────────────┬────┬────────────┼───────────┴──────┼────────────┼────────────  
   ####################│ ###########│ ## │ ########## │ ###############  │ ########## │ ## 
  ─────────────────────┴────────────┴────┴────────────┴──────────────────┴────────────┴────────────   
   Назначение платежа:
   ###############################################################################################   
   ###############################################################################################   
   ###############################################################################################                                                                                                        
  ─────────────────────────────────────────────────────────────────────────────────────────────────  

                                                                    Отметки банка             
                                                                                                     
] (
   TRN, createdate:f:c, Creator,
   ClnDateStr:c, DateChargeOffDateStr:c,
   number:l, docdate:f:c,dptch:c,TaxAuthorSate:l,
   SS(0):l,SS(1):l,SS(2),
   payerINN:l,payerKPP:l,sum:l:f,SP(0):l,SP(1):l,SP(2),SP(3),
   deb_acc:l,
   SBP(0):l,payerMFO:l,SBP(1):l,payer_corracc:l,
   SBR(0):l,receiverMFO:l,SBR(1):l,receiver_corracc:l,ReceiverINN:l,ReceiverKPP:l,
   kred_acc:l,SR(0),SR(1),SR(2),
   kindoper:l, paydate:f:c:l,SR(3), priority:l,
   BttTICode:l, OKATOCode:l, TaxPmGound:l, TaxPmPeriod:l, TaxPmNumber:l, TaxPmDate:f:l, TaxPmType:l,
   SG(0):l,SG(1):l,SG(2):l
  );

  return TRUE;
end; 

/* Установить наименование банка */
macro SetBankName( Code, _Name, _CorAcc )
  record pt  (party);
  var error, Name, PartyID;

  PartyID = ПолучитьКодСубъекта( Code, PTCK_BIC, Error );
  if( Error )
    std.msg( "Не найден банк с кодом: " + Code + " и видом кода: " + PTCK_BIC );
    return FALSE;
  end;

  if (ПолучитьСубъекта(PartyID, pt) != 0)
    std.msg("Не найден банк c кодом: " + Code + " в справочнике субъектов");
    return 1;
  end;

  Name = pt.Name;

  SetParm( 1, Name );
  return TRUE;
end;

macro РаспечататьПоле( имя, Строка )
  file pknd(pmpopknd) key 0;

  var payer_corracc    = "", bank_payer,    payerMFO,
      receiver_corracc = "", bank_receiver, receiverMFO,
      dptch, PayerName = "", ReceiverName = "", Ground = "", displacement;
  /* Смещение в позиции из-за увеличения pmrmprop.BttTICode c 19 до 20 */
  if( {curdate} >= date(01,01,2005) ) 
   displacement = 1;
  else
   displacement = 0;
  end;
      
  if( имя != FormFldDoc )
    return TRUE;
  end;

  /* Банк плательщика */
  payerMFO  = substr( Строка, 26, 9 );
  if( not SetBankName( payerMFO, bank_payer ) )
    return FALSE
  end;
  payer_corracc  = substr( Строка, 55, 20 );
  if( payer_corracc != mkstr(" ",20) )
    payer_corracc = Trim( payer_corracc );
  end;
 
  /* Банк получателя */
  receiverMFO = substr( Строка, 88, 9 );
  if( not SetBankName( receiverMFO, bank_receiver ) )
    return FALSE
  end;
  receiver_corracc  = substr( Строка, 117, 20 );
  if( receiver_corracc != mkstr(" ",20) )
    receiver_corracc = Trim( receiver_corracc );
  end;

  pknd.PaymentKind = "Э";
  if (GetEQ(pknd))
    dptch = pknd.Name;
  else
    std.msg("Не найден вид платежа");
  end;

  /* Если полноформатный платеж */
  if( substr( Строка, 865, 1 ) == "1" )
    PayerName             = Trim( substr( Строка, 177, 160 ) );
    ReceiverName          = Trim( substr( Строка, 358, 160 ) );
    Ground                = Trim( substr( Строка, 518, 210 ) );
  end;

  Draw( substr( Строка, 2, 6 ),
        СчитатьДату( Строка, 8 ),
        substr( Строка, 16, 10 ),
        substr( Строка, 75, 3 ),
        СчитатьДату( Строка, 78 ),
        dptch,
        Trim( substr( Строка, 156, 12 ) ),
        PayerName,
        Trim( substr( Строка, 35, 20 ) ),
        payer_corracc,
        bank_payer,
        payerMFO,
        moneyL( doubleL( substr( Строка, 137, 18 ) )/100 ),
        Trim( substr( Строка, 337, 12 ) ),
        ReceiverName,
        Trim( substr( Строка, 97, 20 ) ),
        receiver_corracc,
        bank_receiver,
        receiverMFO,
        Ground,
        int( substr( Строка, 155, 1 ) ),
        date(0,0,0),
        substr( Строка, 86, 2 ),
        СчитатьДату( Строка, 728),
        СчитатьДату( Строка, 744),
        Trim(substr(Строка, 760, 2 )),
        Trim(substr(Строка, 168, 9 )),
        Trim(substr(Строка, 349, 9 )),
        Trim(substr(Строка, 762, 19+displacement )),
        Trim(substr(Строка, 781+displacement, 11 )),
        Trim(substr(Строка, 792+displacement, 2 )),
        Trim(substr(Строка, 794+displacement, 10 )),
        Trim(substr(Строка, 804+displacement, 15 )),
        Trim(substr(Строка, 819+displacement, 10 )),
        Trim(substr(Строка, 829+displacement, 2 )) );

  return TRUE;
end;


/* Макрос печати форм */
macro PrintForm( addrMes, MassCopy )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;

  SetBuff( wlmes, addrMes );

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  /* Последовательно считываем и печатаем поля сообщения */
  while( СчитатьПоле( field_name, field_value ) )
    if( not РаспечататьПоле( field_name, field_value ) )
      return FALSE;
    end;
  end;

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;