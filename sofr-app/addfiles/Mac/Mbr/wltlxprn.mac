/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Печать сообщений TELEX                                                  */
/*                                                                          */
/*  Имя файла:   wltlxprn.mac                                               */
/*  Создан:      03.11.00                                    Алешин А.В.    */
/*  Переработан: 29.05.03                                    Панов А.Б.     */
/****************************************************************************/

import "wldoc.mac", "wltools.mac";

FILE bicdir( ptbicdir ) key 0;
const PRINT_SHIFT = "                     ";
var MTDescLen;

macro FormatDate(usual_date)
  var dd, mm, yyyy, ret;

  datesplit( usual_date, dd, mm, yyyy );
  ret = string(dd:2, ".", mm:2, ".", yyyy:4);
  /* Заменяем пробелы нулями */
  return StrSubst(ret, " ", "0");
end;

/* Макрос для печати поля 79 */
macro РаспечататьПоле79(поле)
  FILE wltpfld(wltpfld) key 1;
  var num, count;
  array dest;

  wltpfld.TpID = TRANSP_TELEX;
  wltpfld.Name = "79";
  if (GetEQ(wltpfld))
    if (поле != "")
      РазбитьНаСтроки(поле, dest, wltpfld.LenEdit, wltpfld.NumLines);
      num = Asize(dest);
      count=0;
      while(count < num)
        if (dest(count) != "")
          [#](dest(count));
        end;
        count=count+1;
      end;
    else
      println();
    end;
  end;

  return TRUE;
end; /* macro РаспечататьПоле79 */



/* Распечатать многострочное поле */
macro PrintMultiFields(поле, LenStr, Numbers);
  var num, count, width, i;
  array dest;

  width = 78-MTDescLen+4;
  РазбитьНаСтроки(поле, dest, LenStr, Numbers);
  num = Asize(dest);
  [   #](dest(0));
  count=1;
  while(count < num)
    if (dest(count) != "")
      i = 0;
      while(i < width)
        print(" ");
        i = i + 1;
      end;
      [#](dest(count));
    end;
    count=count+1;
  end;
end;

/* Макрос для печати поля */
macro РаспечататьПоле(имя, поле)
  FILE wltpfld(wltpfld) key 1;
  var str, width, i;

  /*Пока поле "15" не печатаем, когда будет делаться ключевание - устаканить*/
  if (имя == "15")
    return TRUE;
  end;

  wltpfld.TpID = TRANSP_TELEX;
  wltpfld.Name = имя;
  if (GetEQ(wltpfld))
    width = 78-MTDescLen;
    str = string("  :   ", имя, "/", StrUpr(wltpfld.Description));
    print(str);
    i = strlen(str);
    while(i < width)
      print(" ");
      i = i + 1;
    end;
    print(":");
    if (поле != "")
      PrintMultiFields(поле, wltpfld.LenEdit, wltpfld.NumLines);
    else
      println();
    end;
  else
    [  :   ###/#](имя, поле);
  end;

  return TRUE;
end; /* macro РаспечататьПоле */

/* Макрос печати заголовка формы */
macro PrintHeader( NumberForm, NameForm, SenderID, ReceiverID, DateMessage )
  var CodeBuf, error, InstitutionName, City, Size;

  /* Телексный адрес получателя */
  CodeBuf = ПолучитьКодСубъекта(ReceiverID, PTCK_TLX, error);
  if (error != 0)
    CodeBuf = "??????";
  end;
  [#](CodeBuf);

  /* Отправитель сообщения */
  bicdir.PartyID = SenderID;
  if (getEQ(bicdir))
    InstitutionName = bicdir.InstitutionName;
    City = bicdir.City;
  else
    InstitutionName = "???";
    City = "???";
  end;
  println("FROM: ", InstitutionName, ", ", City);

  /* Получатель сообщения */
  bicdir.PartyID = ReceiverID;
  if (getEQ(bicdir))
    InstitutionName = bicdir.InstitutionName;
    City = bicdir.City;
  else
    InstitutionName = "???";
    City = "???";
  end;
  println("TO:   ", InstitutionName, ", ", City);

  /* Дата сообщения */
  println("DATE: ", FormatDate(DateMessage));
  
  /* Форма */
  if (NumberForm != "399") /* для подтверждения неттинга не печатаем */
    println("MT:   ", NumberForm, StrUpr(NameForm):69:r);
  end;
  println();

  MTDescLen = strlen(StrUpr(NameForm));
  return TRUE;
end;

/* Макрос печати форм */
macro PrintForm( addrMes, MassCopy )
  var field_name, field_value, SenderID, ReceiverID, InstitutionName;
  var CodeBuf, error;
  var MsgTmpFileName, OldName;

  SetBuff( wlmes, addrMes );

  FILE rls(wlmesrls) key 0 ;
  FILE form(wlmesfrm) key 0 ;
  FILE MsgTmpFile() txt;

  rls.RlsFormID = wlmes.RlsFormID;
  if(not GetEQ(rls))
    MsgBox("Ошибка поиска релиза формы сообщения N "+string(wlmes.RlsFormID));
    return FALSE;
  end;

  form.FormID = rls.FormID;
  if(not GetEQ(form))
    MsgBox("Ошибка поиска формы сообщения N "+string(rls.FormID));
    return FALSE;
  end;

  if( wlmes.Direct == "X" )   /* Входящее сообщение */
    SenderID = wlmes.OutsideAbonentID;
    ReceiverID = {OurBank};
  else                        /* Исходящее сообщение */
    SenderID = {OurBank};
    ReceiverID = wlmes.OutsideAbonentID;
  end;

  /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  /* Печатаем заголовок */
  if( not PrintHeader( form.Name, form.Description, SenderID, ReceiverID, wlmes.OutsideAbonentDate ) )
    return FALSE;
  end;

  /* Последовательно считываем и печатаем поля сообщения */
  if (form.Name != "399")
    while( СчитатьПоле( field_name, field_value ) )
      if( not РаспечататьПоле( field_name, field_value ) )
        MsgBox( "Ошибка при печати поля сообщения" + string(field_name) );
        return FALSE;
      end;
    end;
  else /* печать подтверждения неттинга (поле 79) */
    while(СчитатьПоле(field_name, field_value))
      if (field_name == "79")
        if (not РаспечататьПоле79(field_value))
          MsgBox( "Ошибка при печати поля сообщения" + string(field_name) );
          return FALSE;
        end;
      end;
    end;
  end;

  println();
  println("THANKS AND BEST REGARDS");
  bicdir.PartyID = SenderID;
  if (getEQ(bicdir))
    InstitutionName = bicdir.InstitutionName;
  else
    InstitutionName = "???";
  end;
  println(InstitutionName);
  /* Телексный адрес отправителя */
  CodeBuf = ПолучитьКодСубъекта(SenderID, PTCK_TLX, error);
  if (error != 0)
    CodeBuf = "??????";
  end;
  [#](CodeBuf);

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy != 0)
    rewind(MsgTmpFile);
    while( next( MsgTmpFile ) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;

  return TRUE;
end;
