/*
  $Name:         ufgd541.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО по сообщению ED541 транспорта УФЭБС
*/

import "ufgendoc.mac";

FILE wlreq2 ("wlreq.dbt") key 3;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  var PartNo = "",
      PartQuantity = "",
      PartAggregateID = "",
      InitDateMes : date = date(0,0,0);
  var fieldname = "", fieldvalue = "", blockname = "";
  
  PrintLog(2,"Генерация ответа по ED541");
  
  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if ( (fieldname == "EDDate") and (blockname == "InitialED") )
      InitDateMes = ToDate( fieldvalue ); 
    end;
    if ( (fieldname == "PartNo") and (blockname == "PartInfo") )
      PartNo = fieldvalue;
    end;
    if ( (fieldname == "PartAggregateID") and (blockname == "PartInfo") )
      PartAggregateID = fieldvalue;
    end;
    if ( (fieldname == "PartQuantity") and (blockname == "PartInfo") )
      PartQuantity = fieldvalue;
    end;
  end;
                                                                               
  if( ( PartNo == "" ) or ( int(PartNo) == 1 ) )
    ClearRecord(wlreq);
    wlreq.Trn                = wlmes.Trn;
    wlreq.RelatedRef         = wlmes.RelatedRef;
    wlreq.Direct             = "X"/*WLD_MES_IN*/;  
    wlreq.Kind               = MESKIND_ANSWER;
    wlreq.OriginatorCode     = wlmes.OutSideAbonentCode; 
    wlreq.OriginatorCodeKind = wlmes.OutSideAbonentCodeKind;
    wlreq.OriginatorID       = wlmes.OutSideAbonentID;
    wlreq.OriginatorName     = Bnk_GetPartyName(wlreq.OriginatorID);
    wlreq.RecipientCode      = ПолучитьКодСубъекта( {OurBank}, PTCK_UIS, null );
    wlreq.RecipientCodeKind  = PTCK_UIS;  
    wlreq.RecipientID        = {OurBank};
    wlreq.RecipientName      = Bnk_GetPartyName( wlreq.RecipientID );
    wlreq.InitDateMes        = InitDateMes;
    if( int(PartNo) == 1 )                              
      wlreq.PartAggregateID  = PartAggregateID;
      wlreq.Queries = "Информация о переданных/полученных ЭС в " + PartQuantity + " частях с общим идентификатором " + wlreq.PartAggregateID + ".";     
    end;

    if( not ВставитьОтвет( wlreq ) )
      std.msg("Ошибка при вводе ответа");
      return FALSE;
    end;
  else
    ClearRecord( wlreq2 );
    wlreq2.PartAggregateID = PartAggregateID;
    wlreq2.Direct = "X";
    if( GetEQ( wlreq2 ))
      if( not ОбновитьОтвет( wlreq2, "") )
        std.msg("Ошибка при обновлении ответа");
        return FALSE;
      end;
    else
      std.msg("Не найдена головная часть ответа");
      return FALSE;
    end;        
  end;
  
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;