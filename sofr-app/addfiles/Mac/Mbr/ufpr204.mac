/*
  $Name: ufpr204.mac
  $Module: Межбанковские расчеты
  $Description: Печать формы по сообщению ED204
*/

import "ufgendoc.mac";

macro GetRequestInfo( MesID, OriginatorCode : @String, OriginatorName : @String, RecipientCode : @String, RecipientName : @String )
  var rs = execSQLSelect( " Select wlreq.t_OriginatorCode, wlreq.t_OriginatorName, wlreq.t_RecipientCode, wlreq.t_RecipientName "
                          "   From dwlreq_dbt wlreq, dwlmeslnk_dbt meslnk "
                          "  Where meslnk.t_MesID = :MesID "
                          "    and wlreq.t_ReqID = meslnk.t_ObjID ",
                          MakeArray( SQLParam("MesID", MesID) ));
                         
  if( rs and rs.MoveNext())
    OriginatorCode = rs.value("t_OriginatorCode");
    OriginatorName = rs.value("t_OriginatorName");
    RecipientCode = rs.value("t_RecipientCode");
    RecipientName = rs.value("t_RecipientName");
  end;
end;

macro PrintForm( addrMes, MassCopy )
  var EDNo,
      EDDate,
      OriginatorCode,
      OriginatorName,
      RecipientCode,
      RecipientName,
      Code,
      Description,
      SystemCode,
      Description2,
      RefEDNo,
      RefEDDate,
      RefEDAuthor,
      RefPartyName;
    
  SetBuff( wlmes, addrMes );
  
  var field_name, field_value, строка, название;  
  
  СчитатьПоле( field_name, field_value );
  название = SubStr(field_value, 2, 5);
  if(Index(field_value, String("</",название,">")))
     строка = StrSubst(field_value, String("</",название,">"),"");
  else
     строка = StrSubst(field_value, "/>",">");
  end;
  
  while( СчитатьПоле( field_name, field_value ) )
     строка = строка + field_value;
  end;
  
  строка = строка + String("</",название,">");
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(строка) )
    println( string( "Неверный формат сообщения по форме ED108" ) );
    return FALSE;
  end; 
  
  EDNo = ReadAttribute( xml, "EDNo" );
  EDDate = ReadAttribute( xml, "EDDate" );
  GetRequestInfo( wlmes.MesID, @OriginatorCode, @OriginatorName, @RecipientCode, @RecipientName );
  Code = ReadAttribute( xml, "Code" );
  
  var rs = execSQLSelect(" Select t_Description "
                         "   From dwlcodes_dbt "
                         "  Where t_AlgNum = :AlgNum "
                         "    and t_CodeNum = :CodeNum ",
                         MakeArray(SQLParam("AlgNum", 65 /*TYPE_CODE_UFBS_ED204*/),
                                   SQLParam("CodeNum", int(Code) )));
  if( rs and rs.MoveNext() )
    Description = rs.value("t_Description");
  end;
  
  SystemCode = ReadAttribute( xml, "SystemCode", "", false);
  if( SystemCode )
    rs = execSQLSelect( " Select t_szNameAlg "
                        "   From dnamealg_dbt "
                        "  Where t_iTypeAlg = :Type "
                        "    and t_iNumberAlg = :NumberAlg ",
                        MakeArray(SQLParam("Type", 2621),
                                  SQLParam("NumberAlg", int(SystemCode))));
    if( rs and rs.MoveNext() )
      Description2 = rs.value("t_szNameAlg");
    end;
  end;

  RefEDNo = ReadAttribute( xml, "EDNo", "EDRefID");
  RefEDDate = ReadAttribute( xml, "EDDate", "EDRefID");
  RefEDAuthor = ReadAttribute( xml, "EDAuthor", "EDRefID");
  
  rs = execSQLSelect( " Select pt.t_Name "
                      "   From dparty_dbt pt, dpartcode_dbt ptcode "
                      "  Where pt.t_PartyID = ptcode.t_PartyID "
                      "    and ptcode.t_CodeKind = :CodeKind "
                      "    and ptcode.t_Code = :Code ",
                      MakeArray(SQLParam("CodeKind", PTCK_UIS),
                                SQLParam("Code", RefEDAuthor)));
  if( rs and rs.MoveNext() )
    RefPartyName = rs.value("t_Name");
  end;
[
  ED204 Запрос об отзывве/аннулировании ЭПС (пакета ЭПС)
  
  Номер ЭС:     #########
  Дата ЭС:      ##########
  Составитель:  ########## ##################################################
  Получатель:   ########## ##################################################
  
  Код операции по отзыву (Code): ####### ####################################################################################################
](EDNo,
  EDDate,
  OriginatorCode,
  OriginatorName,
  RecipientCode,
  RecipientName,
  Code,
  Description);

if( SystemCode )
[Признак системы обработки (SystemCode): ########## ####################################################################################################
](
  SystemCode,
  Description2 );
end;

[  Идентификаторы отзываемого/аннулируемого ЭПС (пакета ЭПС)
      Номер ЭС:     #########
      Дата ЭС:      ##########
      Составитель:  ########## ##################################################
](RefEDNo,
  RefEDDate,
  RefEDAuthor,
  RefPartyName);
  
  return true;
end;