/*
  $Name:         ufgm462.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения по ED462
*/

// Генерация сообщения по форме ED462 Заявка на получение или сдачу денежной наличности

import "ufgenmes.mac";

private macro GenRequest( mes, wlreq )

  StartBlockLogXML("Request");
  
  var DocNo,
      DocDate,
      OrgBic,
      NameClient,
      BicPBR,
      NamePBR,
      OperationType,
      OperationDate,
      Acc,
      Sum;
  
  var rs = execSQLSelect( " Select csh.t_Number as DocNo, "
                          "        TO_CHAR(csh.t_Date, 'YYYY-MM-DD' ) as DocDate, "
                          "        substr(req.t_RecipientName, 1, 300) as NamePBR, "
                          "        csh.t_OperType as OperationType, "
                          "        TO_CHAR(csh.t_OperDate, 'YYYY-MM-DD' ) as OperationDate, "
                          "        req.t_Sum as Sum "
                          "   From dwlreq_dbt req, dwlcshreq_dbt csh "
                          "  Where req.t_ReqID = :ReqID "
                          "    and csh.t_ReqID = req.t_ReqID ",
                          MakeArray( SQLParam( "ReqID", wlreq.ReqID)));
                                     
  var rs2 = execSQLSelect( " Select NVL(ptcode.t_Code, chr(1)) as OrgBic, pt.t_Name as NameClient, cor.t_CorAccount as Acc "
                           "   From dpartcode_dbt ptcode, dwlreq_dbt req, dcorschem_dbt cor, ddp_dep_dbt dep, dparty_dbt pt "
                           "  Where req.t_ReqID = :ReqID "
                           "    and req.t_Corschem = cor.t_Number "
                           "    and req.t_FIID = cor.t_FIID "
                           "    and dep.t_Code = cor.t_Department "
                           "    and ptcode.t_PartyID(+) = dep.t_PartyID "
                           "    and ptcode.t_CodeKind(+) = :CodeKind "
                           "    and pt.t_PartyID = dep.t_PartyID ",
                           MakeArray( SQLParam("ReqID", wlreq.ReqID),
                                      SQLParam("CodeKInd", PTCK_BIC)));
  

  if( rs and rs.MoveNext() )
    DocNo = rs.value("DocNo");
    if( strlen(rs.value("DocDate")) != 0 )
      DocDate = rs.value("DocDate");
    end;
    NamePBR = rs.value("NamePBR");
    OperationType = rs.value("OperationType");
    OperationDate = rs.value("OperationDate");
    Sum = MoneyToStrUFBS(rs.value("Sum"));
    end;
  
  if( rs2 and rs2.MoveNext() ) 
    if( strlen(rs2.value("OrgBIC")) != 0)
      OrgBIC = rs2.value("OrgBIC");
    end;
    NameClient = rs2.value("NameClient");
    Acc = rs2.value("Acc");
    end;
  
  GetPartyCodeEx(wlreq.RecipientID, PTCK_BIC, @BicPBR);
  
  if( ValType(DocNo) != V_UNDEF )
    ЗаписатьПолеЛог( "DocNo", DocNo );
  end;
  if( ValType(DocDate) != V_UNDEF )
    ЗаписатьПолеЛог( "DocDate", DocDate );
  end;
  if( ValType(OrgBIC) != V_UNDEF )
    ЗаписатьПолеЛог( "OrgBIC", OrgBIC );
  end;
  if( ValType(NameClient) != V_UNDEF )
    ЗаписатьПолеЛог( "NameClient", NameClient );
  end;
  if( ValType(BicPBR) != V_UNDEF )
    ЗаписатьПолеЛог( "BicPBR", BicPBR );
  end;
  if( ValType(NamePBR) != V_UNDEF )
    ЗаписатьПолеЛог( "NamePBR", NamePBR );
  end;
  if( ValType(OperationType) != V_UNDEF )
    ЗаписатьПолеЛог( "OperationType", OperationType );
  end;
  if( ValType(OperationDate) != V_UNDEF )
    ЗаписатьПолеЛог( "OperationDate", OperationDate );
  end;
  if( ValType(Acc) != V_UNDEF )
    ЗаписатьПолеЛог( "Acc", Acc );
  end;
  if( ValType(Sum) != V_UNDEF )
    ЗаписатьПолеЛог( "Sum", Sum );
  end;
  
  FinishBlockLogXML("Request");
                                   
end;

private macro GenCash( mes, ReqID )

  
  var rs = execSQLSelect( " Select crval.t_Kind as CashType, "
                          "        crval.t_Value as Nominal, "
                          "        crval.t_Amount as Sum, "
                          "        crval.t_Packet as ValsPackType "
                          "   From dwlcrval_dbt crval "
                          "  Where crval.t_ReqID = :ReqID ",
                          MakeArray( SQLParam("ReqID", ReqID) ));
                          
  while( rs and rs.MoveNext() )
    StartBlockLogXML("Cash");
    
    ЗаписатьПолеЛог( "CashType", rs.value("CashTYpe") );
    ЗаписатьПолеЛог( "Nominal", MoneyToStrUFBS(rs.value("Nominal")) );
    ЗаписатьПолеЛог( "Sum", MoneyToStrUFBS(rs.value("Sum")));
    ЗаписатьПолеЛог( "ValsPackType", rs.value("ValsPackType") );
    
    FinishBlockLogXML("Cash");
  end;  
  
end;

private macro GenCashInfo( mes, ReqID )
    
  var rs = execSQLSelect( " Select cash.t_Symbol as CashCode, "
                          "        cash.t_Sum as CashSum "
                          "   From dsymbcash_dbt cash "
                          "  Where cash.t_ObjectID = :ReqID "
                          "    and cash.t_ObjectType = :ObjectType ",
                          MakeArray( SQLParam( "ReqID", ReqID),
                                     SQLParam( "ObjectType", OBJTYPE_REQ)));
  while( rs and rs.MoveNext() )
    StartBlockLogXML("CashInfo");
      
    ЗаписатьПолеЛог("CashCode", substr(rs.value("CashCode"), 2) );
    ЗаписатьПолеЛог("CashSum", MoneyToStrUFBS(rs.value("CashSum")) );
    
    FinishBlockLogXML("CashInfo");
  end;
  
end;

private macro GenCustomerData( mes, ReqID )
  
  var rs = execSQLSelect( " Select csh.t_Customer "
                          "   From dwlcshreq_dbt csh "
                          "  Where csh.t_ReqID = :ReqID ",
                          MakeArray( SQLParam("ReqID", ReqID)));
  if( rs and rs.MoveNext() )
    StartBlockLogXML("CustomerData");
    ЗаписатьПолеЛог("CustomerName", rs.value("t_Customer"));
  FinishBlockLogXML("CustomerData");
  end;
  
end;

private macro GenCreatorInfo( mes, ReqID )
  
  var rs = execSQLSelect( " Select crform.t_Post, crform.t_Name "
                          "   From dwlcrform_dbt crform "
                          "  Where crform.t_ReqID = :ReqID ",
                          MakeArray( SQLParam("ReqID", ReqID)));
  while( rs and rs.MoveNext() )
    StartBlockLogXML("CreatorInfo");
    
    ЗаписатьПолеЛог("Position", rs.value("t_Post"));
    ЗаписатьПолеЛог("FIO", rs.value("t_Name"));
  
    FinishBlockLogXML("CreatorInfo");
  end;
  
end; 

macro GenMes( addrMes, addrReq )
  SetBuff( wlmes,  addrMes );
  SetBuff( wlreq,  addrReq );
  
  var strXml;
  var InitMesId:integer = 0;
  var IsRequestPacket:bool = false;
  var NoInitMes:bool = false;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem: object;
  var iXml:object = ActiveX("Microsoft.XMLDOM");  
  
  var rs:object;
  var select:string;
  var params:TArray;
  var ObjID:integer, ObjKind:integer;

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  ЗаписатьПолеЛог("EDNo",       ed_nda.EDNo );
  ЗаписатьПолеЛог("EDDate",     YYYYMMDD(ed_nda.EDDate));
  ЗаписатьПолеЛог("EDAuthor",   ed_nda.EDAuthor );  

  // Request
  GenRequest(mes, wlreq);
  
  // Cash
  GenCash(mes, wlreq.ReqID);
  
  // CashInfo
  GenCashInfo(mes, wlreq.ReqID);
  
  // CustomerData
  GenCustomerData(mes, wlreq.ReqID);
  
  // Annotation
  if( strlen(wlreq.Queries) != 0 )
    StartBlockLogXML("Annotation");
    ЗаписатьПолеЛог("Annotation", wlreq.Queries);
    FinishBlockLogXML("Annotation");
  end;
  
  //CreatorInfo
  GenCreatorInfo(mes, wlreq.ReqID);
  
  

  return true;
  
end;