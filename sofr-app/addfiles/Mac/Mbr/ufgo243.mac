/*
 $Name: ufgo243.mac
 $Module: МБР
 $Description: Генерация ответа на запрос ED243
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответа на запрос ED243                                         */
/*                                                                          */
/*  Имя файла: ufgo243.mac                                                  */
/*  Создан:    25.04.12                                      Чукина Т.А.    */
/****************************************************************************/

import "wlglobal.mac", MesInter, oralib, likepy, CTInter, "wluftool.mac", WldInter, 
       "bnk_common.mac", "uf243244.mac";

private macro IsAutoFillED244() : bool
  return Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\AutoFillED244", V_BOOL, false);
end;

private macro GetAnsCodeFromReqQueries(ReqQueries : string) : string
  var ans_code : string = "", 
      req_code : string = "";

  if( index_wlcode(ReqQueries, TYPE_CODE_UFBS_ED243, @req_code) )

    req_code = substr(req_code, 4, 2);

    if(req_code == "09")
      ans_code = "11"
    elif(req_code == "13")
      ans_code = "12";
    elif(req_code == "15")
      ans_code = "17";
    elif(req_code == "01" or (req_code == "02") or (req_code == "03") or (req_code == "10") 
     or (req_code == "11") or (req_code == "14") or (req_code == "99") )
      ans_code = "";
    else
      ans_code = req_code;
    end;

  end;

  return ans_code;
end;

private macro InsertAnsExplanatoryFields
(
  AddFldNumbers : TArray,
  Payment : RsbPayment,
  AnsAddFlds : RsbAddFld,
  LastSort : @integer
) : integer

  var stat : integer = 0;

  for(var Number : string, AddFldNumbers)

    stat = InsertAddFld( "П", Number, "ED244", null, null, Payment, 0, 
                         @LastSort, AnsAddFlds );

    if(stat)
      return stat;
    end;
  end;

  return stat;
end;

private macro FillAnswerAddFlds
  ( RequestReqID : integer, 
    Payment : RsbPayment,
    ans_code : string,
    AnsAddFlds : RsbAddFld
  )

  var stat : integer = 0;

  var FieldNameList : TArray = TArray(), // array of string
      FieldNoList : TArray = TArray(),   // array of TAddFldCopy
      ReestrInfo : TArray = TArray();    // array of TReestrItem (TransactionID + array of TAddFldCopy)

  if(ans_code == "04")
    FieldNameList = makeArray("PayerLongName");
  elif(ans_code == "05")
    FieldNameList = makeArray("PayeeLongName");
  elif(ans_code == "06")
    FieldNameList = makeArray("PayerINN", "Address");
  elif(ans_code == "07")
    FieldNameList = makeArray("Purpose");
  elif( InList(ans_code, "08", "11") )
    FieldNoList = GetRevisedFieldsCopy(RequestReqID);
  elif(ans_code == "12")
    ReestrInfo = GetReestrInfoCopy(RequestReqID);
  end;

  var Sort : integer = 0;
  if(not stat)
    stat = InsertAnsExplanatoryFields(FieldNameList, Payment, AnsAddFlds, @Sort);
  end;

  if(not stat)
    stat = InsertAnsRevisedFieldsFromCopy(FieldNoList, Payment, AnsAddFlds, @Sort);
  end;

  if(not stat)
    stat = InsertAnsReestrInfoFromCopy(ReestrInfo, Payment, AnsAddFlds, @Sort);
  end;

  if(stat)
    msgbox("Ошибка при вставке дополнительных полей ответа");
  end;

end;

private macro AutoFillED244
  ( RequestReqID : integer, 
    RequestQueries : string, 
    Payment : RsbPayment,
    ans, // : RECORD
    AnsAddFlds : RsbAddFld
  )

  if( IsAutoFillED244() )
    var ans_code : string = GetAnsCodeFromReqQueries( RequestQueries );

    if(ans_code)
      ans.Queries = "/ОТ" + ans_code + "/";
      FillAnswerAddFlds(RequestReqID, Payment, ans_code, AnsAddFlds);
    end;
  end;

end;

private macro FillAnswerFromWlreq_in
  ( InMesID : integer, 
    Payment : RsbPayment, 
    ans, // : rexord
    AddFlds : RsbAddFld
  )

  var select = "Select wlreq.* " +
               "  from dwlreq_dbt wlreq, dwlmeslnk_dbt lnk " +
               " where lnk.t_MesID   = :MesID " +
               "   and lnk.t_Direct  = 'X' " +
               "   and lnk.t_ObjKind = :ObjType " +
               "   and wlreq.t_ReqID = lnk.t_ObjID ";
  var params : TArray = makeArray( SQLParam("MesID", InMesID),
                                   SQLParam("ObjType", OBJTYPE_REQ) );
  var rs : RsdRecordset = execSQLselect(select, params);
  if(rs and rs.moveNext())
    ans.OriginatorCode = rs.value("t_RecipientCode");
    ans.OriginatorCodeKind = rs.value("t_RecipientCodeKind");
    ans.OriginatorID = rs.value("t_RecipientID");
    ans.OriginatorName = rs.value("t_RecipientName");
    ans.RecipientCode = rs.value("t_OriginatorCode");
    ans.RecipientCodeKind = rs.value("t_OriginatorCodekind");
    ans.RecipientID = rs.value("t_OriginatorID");
    ans.RecipientName = rs.value("t_OriginatorName");

    AutoFillED244(rs.value("t_ReqID"), rs.value("t_Queries"), Payment, ans, AddFlds);
  end;  
end;

private macro getFldFromXmlMes(MesID: integer, FieldName: string):string
  var buff : string = "", str1 : string = "";
  if( ПолучитьПоле (MesID, "xml", buff) )
    var mes:object = ActiveX("Microsoft.XMLDOM");
    if ( not mes.LoadXML(buff) )
      std.msg("Ошибка чтения строки xml: " + string(buff));
    end;         

    if ( mes.selectSingleNode("//@"+FieldName) )
      return mes.selectSingleNode("//@"+FieldName).nodeValue;
    end;
  else
    InitError();
    if( ПолучитьПоле (MesID, FieldName, buff) )
      return buff;
    else
      DisplayError();
    end;
  end;

  return "";
end;

macro GenObj( addrMes, mode )

  if(mode == WLD_GENOBJ_ACTION_ROLLBACK) // Откат обработки не реализуется
    return TRUE;
  end;

  SetBuff( wlmes, addrMes );
  PrintLog(2,"Генерация ответа на запрос ED243");

  // Найти все исходные сообщения wlmes_out для сообщения запроса wlmes_in
  var select = "Select t_MesID " +
               "  from dwlmes_dbt " +
               " where t_Trn = :Trn " +
               "   and t_Direct = chr(0) ";
  var params = makeArray( SQLParam("Trn", wlmes.RelatedRef) );
  var rs = execSQLselect(select, params);
  if( not rs or not rs.moveNext() )
    std.msg( "Не найдено исходное ЭПС с EDNo: " + SubStr(wlmes.RelatedRef, 17) + ", " +
             "EDDate: " + SubStr(wlmes.RelatedRef, 5, 2) + "." + SubStr(wlmes.RelatedRef, 3, 2) + "." + SubStr(wlmes.RelatedRef, 1, 2) + ", " +
             "EDAuthor: " + SubStr(wlmes.RelatedRef, 7, 10) );
    return FALSE;
  end;
  
  // По найденным сообщениям ищутся платежи, по которым они сгенерированы
  var AddCondStr = "wlpm.t_WlPmID IN (-1"; // -1 - чтобы не отобрать в скроллинг все подряд, если ничего не нашли
  select = "Select wlpm.t_WlPmID WlPmID " +
           "  from dwlpm_dbt wlpm, dwlmeslnk_dbt lnk, dwlmes_dbt mes "
           " where mes.t_Trn     = :Trn " +
           "   and mes.t_Direct  = chr(0) " +
           "   and lnk.t_MesID   = mes.t_MesID " +
           "   and lnk.t_ObjKind = :ObjType " +
           "   and wlpm.t_WlPmID = lnk.t_ObjID ";
  params = NULL;
  params = makeArray( SQLParam("Trn", wlmes.RelatedRef),
                      SQLParam("ObjType", OBJTYPE_PAYMENT) );
  rs = NULL;
  rs = execSQLselect(select, params);
  while(rs and rs.moveNext())
    AddCondStr = AddCondStr + "," + string( rs.value("WlPmID") );
  end;
  AddCondStr = AddCondStr + ")";

  // Пользователь выбирает в скроллинге платеж 
  var PaymentID : integer = SelectExternPaymentFromScrol("", AddCondStr);
  if(PaymentID <= 0)
    return FALSE;
  end;
  var Payment : RsbPayment = RsbPayment(PaymentID);

  // Создать учетный объект "исходящий ответ"
  RECORD answer (wlreq);
  ClearRecord(answer);
  answer.RelatedRef = wlmes.Trn;
  answer.Direct = ""; // WLD_MES_OUT
  answer.Kind = MESKIND_ANSWER;
  var AddFlds : RsbAddFld = RsbAddFld(answer.ReqID);
  FillAnswerFromWlreq_in(wlmes.MesID, Payment, answer, AddFlds);
  answer.InitDateMes = ToDate( getFldFromXmlMes(wlmes.MesID, "EDDate") );
  answer.InitFormIDMes = GetFormIDByRlsID(wlmes.RlsFormID);
  answer.Corschem = Payment.OutCorschem;
  answer.FIID = Payment.OutCorschemFIID;
  var TpID : integer = getTpIDofTpSchem(wlmes.TpSchemID);
  
  // Открыть панель ответа на редактирование с созданными параметрами 
  if( WlExecuteReqEditPanel(answer, "", "", "", TpID) != 0 )
    return FALSE;
  end;

  return TRUE;
  
  OnError(er) // обработка ошибок времени выполнения
    ExeptionMessage(er);
    return FALSE;
end;

