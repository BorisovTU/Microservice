 /*
 $Name: mnsgmbns.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения по BNS
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по BNS                                               */
/*                                                                          */
/*  Имя файла: mnsgmbns.mac                                                 */
/*  Создан:  27.10.11                                    Чукина Т.А.        */
/****************************************************************************/
                                                              
import "wlgenmes.mac", "wlmnstls.mac", FIInter, OprInter;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record OldMes(wlmes);
  ClearRecord(OldMes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlregdec, addrRegdec );
  SetBuff( OldMes,   addrOldMes );

  PrintLog(2,"Генерация сообщения по BNS"); 

  var Error = 0, Code = "", ind = 0, tmpstr = "";

  /*ИдФайл*/
  ЗаписатьПолеЛог( "ИдФайл", " " );
  /*ТипИнФ*/
  ЗаписатьПолеЛог( "ТипИнф", "НАЛСЧЕТОВ" );
  /*ВерсПрог*/
  ЗаписатьПолеЛог( "ВерсПрог", "RS-Bank V.6" );
  /*ТелОтпр*/
  ЗаписатьПолеЛог( "ТелОтпр", " " );
  /*ДолжнОтпр*/
  ЗаписатьПолеЛог( "ДолжнОтпр", " " );
  /*ФИООтпр*/
  ЗаписатьПолеЛог( "ФамОтпр", " " );
  /*КолДок*/
  ЗаписатьПолеЛог( "КолДок", "1" );
  /*ВерсФорм*/
  ЗаписатьПолеЛог( "ВерсФорм", "2.01" );
  /*ИдДок*/
  tmpstr = execStoredFunc( "sys_guid", V_STRING );
  ЗаписатьПолеЛог( "ИдДок", tmpstr );  
  /*НомСправ*/
  ЗаписатьПолеЛог( "НомСправ", wlregdec.Number );
  
  var FullINN = GetPartyINN(wlregdec.OriginatorID, 1);
  /*ИННКО*/
  ЗаписатьПолеЛог( "ИННКО", RemoveKPP( FullINN ) );
  /*КППКО*/
  ЗаписатьПолеЛог( "КППКО", RemoveINN( FullINN ) );
  /*БИК*/
  code = ПолучитьКодСубъекта(wlregdec.OriginatorID, PTCK_BIC, error, 1);
  ЗаписатьПолеЛог( "БИК", code );
  /*НаимКО*/
  var subject = Tbfile("party.dbt", "r");
  subject.rec.PartyID = wlregdec.OriginatorID;
  GetEQ( subject );
  ЗаписатьПолеЛог( "НаимКО", substr( subject.rec.Name, 1, 160 ) );
  /*НомФ*/    
  Code = ПолучитьКодСубъекта( wlregdec.OriginatorID, PTCK_BANKREGNUM, Error);
  ind = index(Code, "/");
  if ( (ind == 0) or (ind == strlen(Code) ) )
    ЗаписатьПолеЛог( "НомФ", "0" );
  else
    ЗаписатьПолеЛог( "НомФ", SubStr(Code, ind + 1) );
  end;
  /*НомЗапр*/
  ЗаписатьПолеЛог("НомЗапр", wlregdec.LnkDocNumber );
  /*ДатаЗапр*/
  ЗаписатьПолеЛог("ДатаЗапр", DDpMMpYYYY(wlregdec.LnkDocDate));
  /*КодНО*/
  if (wlregdec.recipientID > 0)
    Code = ПолучитьКодСубъекта( wlregdec.recipientID, PTCK_MNS, Error);
  else
    Code = wlregdec.RecipientCode;
  end;
  ЗаписатьПолеЛог("КодНО", Code);

  /*ИНННП*/
  var INNNP = wlregdec.ClientINN;
  ЗаписатьПолеЛог("ИНННП", INNNP);
  /*КППНП*/  
  if (wlregdec.ClientKPP)
    ЗаписатьПолеЛог("КППНП", wlregdec.ClientKPP);
  elif ( strlen(INNNP) != 12 )
    ЗаписатьПолеЛог("КППНП", " ");
  end;
  /*НаимНП / ФИОИП*/
  record party(party);
  if ( strlen(INNNP) != 12 )
    if (wlregdec.ClientID > 0)
      ПолучитьСубъекта(wlregdec.ClientID, party);
      ЗаписатьПолеЛог("НаимНП", substr( party.Name, 1, 160 ) );
    else
      ЗаписатьПолеЛог("НаимНП", substr( wlregdec.ClientName, 1, 160 ) );
    end;
  else
    if (wlregdec.ClientID > 0)
      var persn = Tbfile("persn.dbt", "r");
      persn.rec.PersonID = wlregdec.ClientID;
      GetEQ(persn);
      if( strlen( "ФИОИП", persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3 ) > 160 )
        ЗаписатьПолеЛог("ФИОИП", substr( persn.rec.Name1 + "," + persn.rec.Name2, 1, 160 ));
      else
        ЗаписатьПолеЛог("ФИОИП", persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3);
      end;
    else                                                          
      if( strlen( РазделитьЗапятымиФИО(Trim(wlregdec.ClientName)) ) > 160 )
        ЗаписатьПолеЛог( "ФИОИП", substr( ФамилияИмя( РазделитьЗапятымиФИО( Trim(wlregdec.ClientName) ), "," ), 1, 160 ) );
      else
        ЗаписатьПолеЛог( "ФИОИП", РазделитьЗапятымиФИО(Trim(wlregdec.ClientName)) );
      end;
    end;
  end;
  
  var rs_dp_dep = getRsDpDepByPartyID(wlregdec.OriginatorID);
  rs_dp_dep.moveNext();
  /*ДолжнПрБ*/
  GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", rs_dp_dep.value("t_Code"), @tmpstr );
  /*GetRegValForOPENAC("СПРАВКИ_ИФНС_ДОЛЖ", V_STRING, tmpstr);*/
  ЗаписатьПолеЛог("ДолжнПрБ", tmpstr);
  /*ФИОПрБ*/
  GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО", rs_dp_dep.value("t_Code"), @tmpstr );
  /*GetRegValForOPENAC("СПРАВКИ_ИФНС_ФИО", V_STRING, tmpstr);*/
  ЗаписатьПолеЛог( "ФИОПрБ", ПолучитьФИОПрБ( tmpstr ) );

  /*ДатаСправ*/
  ЗаписатьПолеЛог( "ДатаСправ", DDpMMpYYYY(wlregdec.Date) );
  /*ДатаСооб*/
  ЗаписатьПолеЛог( "ДатаСооб", DDpMMpYYYY(date) );

  /*Блок "Счет" */
  record Acc(account);
  record Fin(fininstr);
  var rs_acclnk = getRsWlacclnk(wlregdec.DecisionID, OBJTYPE_FNSINFO);
  if (rs_acclnk)
    УстановитьКонтекстБлокаЛог("Счет");
    while( rs_acclnk.MoveNext() )
      var acclnkRec : TRecHandler = TRecHandler("wlacclnk");
      CopyRSetToFBuff(acclnkRec, rs_acclnk);

      if ( not IsLnkAccountPrcError(acclnkRec, WLD_TYPE_REGDEC_INS) )// если не было ошибок при обработке счета
        ClearRecord( Acc );
        ПолучитьСчет( rs_acclnk.value("t_FIID"), rs_acclnk.value("t_Account"), Acc );
        /*НомСч*/
        ЗаписатьПолеЛог( "НомСч", Acc.Account );
        /*ВидСч*/
        ЗаписатьПолеЛог( "ВидСч", acclnkRec.rec.TypeAcc );
        /*ВалСч*/
        ClearRecord(Fin);
        ПолучитьФинИн(rs_acclnk.value("t_FIID"), Fin);
        ЗаписатьПолеЛог( "ВалСч", Fin.ISO_Number );
        /*ДатаОткрСч*/
        ЗаписатьПолеЛог( "ДатаОткрСч", DDpMMpYYYY(acclnkRec.rec.DateOpen) );

        /*ДатаЗакрСч*/
        var ДатаЗакрСч : date = acclnkRec.rec.DateClose;
        if( (ДатаЗакрСч != ZeroDate) and (ДатаЗакрСч <= wlregdec.Date) )
          ЗаписатьПолеЛог( "ДатаЗакрСч", DDpMMpYYYY(ДатаЗакрСч) );
        end;
      end;
    end;
    УстановитьКонтекстБлокаЛог("..");
  end;

  /*_НомерОтправ*/
  tmpstr = "";
  if (OldMes.MesID != 0) //Перегенерация?
    if ( not ПолучитьПоле (OldMes.MesID, "_НомерОтправ", tmpstr) )
      tmpstr = "1";  
    end;
  elif (GetKindActionFNS() == KIND_ACTION_FNS_RESEND)
    if ( not ПолучитьПоле (GetOldfnsMessageID(), "_НомерОтправ", tmpstr) )
      tmpstr = "2";  
    else
      tmpstr = string( int(tmpstr) + 1 );
    end;
  else
    tmpstr = "1";
  end;
  ЗаписатьПолеЛог( "_НомерОтправ", tmpstr );

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;

macro CheckObj(addrRegdec)
  SetBuff(wlregdec, addrRegdec);
  return CheckObj_FNSINFO( wlregdec );
end;

