/*
  $Name:         ufgd107.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация платежей по сообщениям УФЭБС ED107
*/

import "ufgendoc.mac", PTInter, likepy, oralib, "pmlib.mac";

var V = 1;


class TBankData              //Параметры банка
  var BIC:         string,   //БИК банка
      SWBIC:       string,   //BIC SWIFT банка
      BankAccount: string,   //Счет банка
      CorrespAcc:  string,   //Корсчет банка
      Name:        string,   //Наименование банка
      BlockName:   string;   //Название блока
      
  macro Construct()
    BIC = "";
    SWBIC = "";
    BankAccount = "";
    CorrespAcc = "";
    Name = "";
  end;

  Construct();
end;

class TED107MessageFields                 /*Поля сообщения ED107*/
  var EDNo:             integer,          /*Номер ЭПД в течение опердня*/
      EDDate:           date,             /*Дата составления ЭПД*/
      EDAuthor:         string,           /*Уникальный идентификатор составителя ЭПД*/
      EDReceiver:       string,           /*Уникальный идентификатор получателя ЭС*/
      PaytKind:         string,           /*Вид платежа*/
      Sum:              moneyL,           /*Сумма*/
      TransKind:        string,           /*Вид операции*/
      Priority:         integer,          /*Очередность платежа*/
      SystemCode:       string,           /*Признак системы обработки*/
      SessionID:        integer,          /*Номер рейса*/
      PaymentID:        string,           /*Уникальный идентификатор платежа*/
                                       
      AccDocNo:         integer,          /*Номер расчетного документа*/
      AccDocDate:       date,             /*Дата расчетного документа*/
                        /*??*/     
      RelatedDocNo:     string,           /*Номер документа*/
      RelatedDocDate:   date,             /*Дата документа*/

      OrderingBank:     variant/*TBankData*/,        /*Банк-плательщик*/
      PrevInstrAgent:   variant/*TBankData*/,        /*Предыдущий инструктирующий банк*/
      InstructingAgent: variant/*TBankData*/,        /*Банк-отправитель*/
      InstructedAgent:  variant/*TBankData*/,        /*Банк-исполнитель*/
      AcctWithInst:     variant/*TBankData*/,        /*Агент банка-получателя*/
      Beneficiary:      variant/*TBankData*/,        /*Банк-получатель*/

      Sndr2RecvrInfo:   string,           /*Банковская информация*/
                                   
      InitialEDNo:      integer,          /*Номер ЭС в течение опердня*/
      InitialEDDate:    date,             /*Дата составления ЭС*/
      InitialEDAuthor:  string,           /*Уникальный идентификатор составителя ЭС*/

      BusinessScenario: string,           /*Идентификатор бизнес-сценария*/
      DebitDate:        date,             /*Дата списания с корсчета*/
      CreditDate:       date,             /*Дата зачисления на корсчет*/
      Sessiontype:      string,           /*Идентификатор типа рейса*/
      SessionIDBlock:   integer,          /*Номер рейса*/
      SettlementTime:   string;           /*Время исполнения распоряжения*/

  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
    EDReceiver = "";
    PaytKind = "";
    Sum = moneyL(0);
    TransKind = "";
    Priority = 0;
    SystemCode = "";
    SessionID = 0;
    PaymentID = "";

    AccDocNo = 0;
    AccDocDate = date(0,0,0);

    RelatedDocNo = "";
    RelatedDocDate = date(0,0,0);

    OrderingBank = null;
    PrevInstrAgent = null;
    InstructingAgent = null;
    InstructedAgent = null;
    AcctWithInst = null;
    Beneficiary = null;

    Sndr2RecvrInfo = "";

    InitialEDNo = 0;
    InitialEDDate = date(0,0,0);
    InitialEDAuthor = "";

    BusinessScenario = "";
    DebitDate = date(0,0,0);
    CreditDate = date(0,0,0);
    Sessiontype = "";
    SessionIDBlock = 0;
    SettlementTime = "";
  end; 

  Construct();
end;

// Макрос чтения полей сообщения ED107
// Входные параметры: ED107MessageFields - структура полей сообщения ED107
macro ReadED107MessageFields( ED107MessageFields:@TED107MessageFields ) : bool
  var fieldname = "", fieldvalue = "", blockname = "";

  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED107MessageFields.EDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED107MessageFields.EDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED107MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED107MessageFields.EDReceiver = fieldvalue;
      elif(fieldname == "PaytKind")
        ED107MessageFields.PaytKind = fieldvalue;
      elif(fieldname == "Sum")
        ED107MessageFields.Sum = moneyL(fieldvalue);
      elif(fieldname == "TransKind")
        ED107MessageFields.TransKind = fieldvalue;
      elif(fieldname == "Priority")
        ED107MessageFields.Priority = int(fieldvalue);
      elif(fieldname == "SystemCode")
        ED107MessageFields.SystemCode = fieldvalue;
      elif(fieldname == "SessionID")
        ED107MessageFields.SessionID = int(fieldvalue);
      elif(fieldname == "PaymentID")
        ED107MessageFields.PaymentID = fieldvalue;
      end;
    elif(blockname == "AccDoc")
      if(fieldname == "AccDocNo")
        ED107MessageFields.AccDocNo = int(fieldvalue);
      elif(fieldname == "AccDocDate")
        ED107MessageFields.AccDocDate = ToDate(fieldvalue);
      end;
    elif(blockname == "RelatedDoc")
      if(fieldname == "RelatedDocNo")
        ED107MessageFields.RelatedDocNo = fieldvalue;
      elif(fieldname == "RelatedDocDate")
        ED107MessageFields.RelatedDocDate = ToDate(fieldvalue);
      end;
    elif(blockname == "OrderingBank")
      if(fieldname == "_begin")
        ED107MessageFields.OrderingBank = TBankData();
        ED107MessageFields.OrderingBank.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.OrderingBank.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.OrderingBank.SWBIC = fieldvalue;
      elif(fieldname == "BankAccount")
        ED107MessageFields.OrderingBank.BankAccount = fieldvalue;
        ED107MessageFields.OrderingBank.CorrespAcc = "";
      end;
    elif(blockname == "OrderingBank\\Name")
      if(fieldname == "Name")
        ED107MessageFields.OrderingBank.Name = fieldvalue;
      end;
    elif(blockname == "PrevInstrAgent")
      if(fieldname == "_begin")
        ED107MessageFields.PrevInstrAgent = TBankData();
        ED107MessageFields.PrevInstrAgent.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.PrevInstrAgent.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.PrevInstrAgent.SWBIC = fieldvalue;
      elif(fieldname == "BankAccount")
        ED107MessageFields.PrevInstrAgent.BankAccount = fieldvalue;
        ED107MessageFields.PrevInstrAgent.CorrespAcc = "";
      end;
    elif(blockname == "PrevInstrAgent\\Name")
      if(fieldname == "Name")
        ED107MessageFields.PrevInstrAgent.Name = fieldvalue;
      end;
    elif(blockname == "InstructingAgent")
      if(fieldname == "_begin")
        ED107MessageFields.InstructingAgent = TBankData();
        ED107MessageFields.InstructingAgent.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.InstructingAgent.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.InstructingAgent.SWBIC = fieldvalue;
      elif(fieldname == "CorrespAcc")
        ED107MessageFields.InstructingAgent.CorrespAcc = fieldvalue;
        ED107MessageFields.InstructingAgent.BankAccount = "";
      end;
    elif(blockname == "InstructedAgent")
      if(fieldname == "_begin")
        ED107MessageFields.InstructedAgent = TBankData();
        ED107MessageFields.InstructedAgent.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.InstructedAgent.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.InstructedAgent.SWBIC = fieldvalue;
      elif(fieldname == "CorrespAcc")
        ED107MessageFields.InstructedAgent.CorrespAcc = fieldvalue;
        ED107MessageFields.InstructedAgent.BankAccount = "";
      end;
    elif(blockname == "AcctWithInst")
      if(fieldname == "_begin")
        ED107MessageFields.AcctWithInst = TBankData();
        ED107MessageFields.AcctWithInst.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.AcctWithInst.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.AcctWithInst.SWBIC = fieldvalue;
      elif(fieldname == "BankAccount")
        ED107MessageFields.AcctWithInst.BankAccount = fieldvalue;
        ED107MessageFields.AcctWithInst.CorrespAcc = "";
      end;
    elif(blockname == "AcctWithInst\\Name")
      if(fieldname == "Name")
        ED107MessageFields.AcctWithInst.Name = fieldvalue;
      end;
    elif(blockname == "Beneficiary")
      if(fieldname == "_begin")
        ED107MessageFields.Beneficiary = TBankData();
        ED107MessageFields.Beneficiary.BlockName = blockname;
      elif(fieldname == "BIC")
        ED107MessageFields.Beneficiary.BIC = fieldvalue;
      elif(fieldname == "SWBIC")
        ED107MessageFields.Beneficiary.SWBIC = fieldvalue;
      elif(fieldname == "BankAccount")
        ED107MessageFields.Beneficiary.BankAccount = fieldvalue;
        ED107MessageFields.Beneficiary.CorrespAcc = "";
      end;
    elif(blockname == "Beneficiary\\Name")
      if(fieldname == "Name")
        ED107MessageFields.Beneficiary.Name = fieldvalue;
      end;
    elif(blockname == "Sndr2RecvrInfo")
      if(fieldname == "Sndr2RecvrInfo")
        ED107MessageFields.Sndr2RecvrInfo = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == "EDNo")
        ED107MessageFields.InitialEDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED107MessageFields.InitialEDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED107MessageFields.InitialEDAuthor = int(fieldvalue);
      end;      
    elif(blockname == "ProcessingDetails")
      if(fieldname == "BusinessScenario")
        ED107MessageFields.BusinessScenario = fieldvalue;
      elif(fieldname == "DebitDate")
        ED107MessageFields.DebitDate  = ToDate(fieldvalue);
      elif(fieldname == "CreditDate")
        ED107MessageFields.CreditDate  = ToDate(fieldvalue);
      end;
    elif(blockname == "ProcessingDetails\\Session")
      if(fieldname == "SessionType")
        ED107MessageFields.SessionType = fieldvalue;
      end;    
    elif(blockname == "ProcessingDetails\\Session\\SessionID")
      if(fieldname == "SessionID")
        ED107MessageFields.SessionIDBlock = int(fieldvalue);
      end;    
    elif(blockname == "ProcessingDetails\\Session\\SettlementTime")
      if(fieldname == "SettlementTime")
        ED107MessageFields.SettlementTime = fieldvalue;
      end;    
    end; 
  end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

  return TRUE;
end;

private macro GetPartCode( CodeKind:integer, CodeValue:string ):integer
  var PartyID = -1;
  var query = "select part.t_PartyID as t_PartyID " +
              "  from dpartcode_dbt part " +
              " where part.t_Code = :CodeValue " +
              "   and part.t_CodeKind = :CodeKind ";

  var params = makeArray( SQLParam( "CodeValue", CodeValue ),
                          SQLParam( "CodeKind",  CodeKind ) );

  var rs:RsdRecordset = execSQLselect( query, params );

  if (rs and rs.moveNext() )
    PartyID = rs.value( "t_PartyID" );
  end;

  return PartyID;
end;

// Макрос заполнения записи маршрута по данным банка
// Входящие параметры: route - запись маршрута (IN/OUT)
//                     Bank - параметры банка
// Возвращаемое значение - true, в случае успешного заполнения, false в противном случае
private macro FillPmRouteRecord( route, Bank:TBankData ): bool
  
  if( Bank != null )    

    if( Bank.BIC )
      route.CodeKind = 3;
      route.CodeValue = Bank.BIC;
    elif( Bank.SWBIC )
      route.CodeKind = 6;
      route.CodeValue = Bank.SWBIC;
    end;

    if( route.CodeValue != "" )
      route.PartyID = GetPartCode( route.CodeKind, route.CodeValue );
    elif( ( Bank.BankAccount != "" ) or ( Bank.CorrespAcc != "" ) )
      route.PartyID = IdentifyParticipantByAccountAndName( IfThenElse( Bank.BankAccount != "", Bank.BankAccount, Bank.CorrespAcc ), Bank.Name );
    end;
    
    if( route.PartyID <= 0 )
      return false;
    end;

    if( ( Bank.BankAccount != "" ) and (not InList( Bank.BlockName, "OrderingBank", "Beneficiary" ) ) )
      route.OutAccount = Bank.BankAccount;
    end;

    if( ( Bank.CorrespAcc != "" ) and ( Bank.BlockName == "InstructingAgent" )  )
      route.OutAccount = Bank.CorrespAcc;
    end;

    if( Bank.Name != "" )
      route.Name = Bank.Name;
    else
      var tmpBankName = "";
      var params:TArray = makeArray( SQLParam( "p_Context"  , execStoredFunc( "PM_NAMES.GetContextForPrimaryDoc", V_STRING, 
                                                                               makeArray( SQLParam( "p_DocKind", DLDOC_BANKPAYMENT ), 
                                                                                          SQLParam( "p_SubDocKind", 0 ) ) ) ), 
                                     SQLParam( "p_PartyID"  , route.PartyID ),
                                     SQLParam( "p_Date"     , {curdate} ),
                                     SQLParam( "p_BankName" , V_STRING, RSDBP_OUT )  );
      var stat = execStoredFunc( "PM_NAMES.PM_SetBankName", V_INTEGER, params );
      if( stat == 0 )
        tmpBankName = string( params.Value(3).value );
      end;
      route.Name = tmpBankName;   
    end;
    return true;
  end;
  return false;
end;

macro GenDoc (addrMes)

  var stat = true;

  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record PayerBank(pmroute);
  record PrevInstrAgent(pmroute);
  record SenderBank(pmroute);
  record ExecutorBank(pmroute);
  record IntermediaryBank(pmroute);
  record ReceiverBank(pmroute);
                                
  PrintLog( 2, "Генерация платежа по сообщению ED107" );

  var ED107MessageFields = TED107MessageFields();
  ReadED107MessageFields( @ED107MessageFields );
  
  ClearRecord( wlpmpaym );
  ClearRecord( wlpmpropdeb );
  ClearRecord( wlpmpropcred );
  ClearRecord( wlpmrmprop );

  ClearRecord( PayerBank );
  ClearRecord( PrevInstrAgent );
  ClearRecord( SenderBank );
  ClearRecord( ExecutorBank );
  ClearRecord( IntermediaryBank );
  ClearRecord( ReceiverBank );

  wlpmpaym.DocKind = wlpmpaym.PrimDocKind = WL_INDOC;
  wlpmpaym.Purpose = IfThenElse( V == 3, PM_PURP_BANKPAYORDER, PM_PURP_RCDOC);

  wlpmrmprop.Number = ED107MessageFields.AccDocNo;
  wlpmrmprop.Reference = wlmes.RelatedRef;
  wlpmrmprop.Date = ED107MessageFields.AccDocDate;
  wlpmrmprop.PaymentKind = PaytKindToPaymentKind( ED107MessageFields.PaytKind );

  wlpmpaym.BaseFIID = NATCUR;
  wlpmpaym.BaseAmount = ED107MessageFields.Sum/100;
  
  wlpmrmprop.ShifrOper = ED107MessageFields.TransKind;
  wlpmrmprop.Ground = ED107MessageFields.Sndr2RecvrInfo;

  wlpmpropdeb.IsSender = SET_CHAR;

  var CreditDate = ED107MessageFields.CreditDate;
    
  if( CreditDate == date(0, 0, 0) )  
    var rs = execSQLselect(     
                           "  SELECT CASE " +
                           "            WHEN WLSESS.T_SESSUID <> CHR (1) " +
                           "            THEN " +
                           "                TO_DATE (SUBSTR (WLSESS.T_SESSUID, 1, 6), 'YYMMDD') " +
                           "            ELSE " +
                           "                TO_DATE ('0001-01-01', 'YYYY-MM-DD') " +
                           "        END " +
                           "   FROM DWLSESS_DBT WLSESS " +
                           "  WHERE WLSESS.T_SESSIONID = :SESSIONID ",
                           makeArray
                           (
                              SQLParam("SESSIONID", wlmes.SessionID)
                           ), false );
    
    if(rs and rs.MoveNext() )
      CreditDate = rs.value(0);
    end;
  end;

  if( CreditDate == date(0, 0, 0) )  
    var rsm = execSQLselect(     
                           " SELECT CASE " +
                           "            WHEN :TRN1 <> CHR (1) THEN TO_DATE (SUBSTR ( :TRN2, 1, 6), 'YYMMDD') " +
                           "            ELSE TO_DATE ('0001-01-01', 'YYYY-MM-DD') " +
                           "        END " +
                           "   FROM DUAL ",
                           makeArray
                           (
                              SQLParam("TRN1", wlmes.TRN),
                              SQLParam("TRN2", wlmes.TRN)
                           ), false );
    
    if(rsm and rsm.MoveNext() )
      CreditDate = rsm.value(0);
    end;
  end;

  wlpmpropdeb.TransferDate = wlpmpaym.ValueDate = CreditDate;
  wlpmrmprop.Priority = ED107MessageFields.Priority;
  
  var PayerBankFilled = false;
  var SenderBankFilled = false;
  var ReceiverBankFilled = false;
  var ExecutorBankFilled = false;

  if( ED107MessageFields.OrderingBank != null )
    PayerBankFilled = FillPmRouteRecord( PayerBank, ED107MessageFields.OrderingBank );
    
    if( PayerBank.PartyID <= 0 )
      std.msg( "Не удалось идентифицировать участника операции по реквизитам блока сообщения " + ED107MessageFields.OrderingBank.BlockName );
      return false;
    end;
    
    if( (V == 3) and (ED107MessageFields.OrderingBank.BankAccount != "") )
      wlpmpaym.PayerAccount = ED107MessageFields.OrderingBank.BankAccount;
    end;    
  end;
  if( ED107MessageFields.PrevInstrAgent != null )
    FillPmRouteRecord( PrevInstrAgent, ED107MessageFields.PrevInstrAgent );
    
    if( PrevInstrAgent.PartyID <= 0 )
      std.msg( "Не удалось идентифицировать участника операции по реквизитам блока сообщения " + ED107MessageFields.PrevInstrAgent.BlockName );
      return false;
    end;
  end;
  SenderBankFilled = FillPmRouteRecord( SenderBank, ED107MessageFields.InstructingAgent );
  ExecutorBankFilled = FillPmRouteRecord( ExecutorBank, ED107MessageFields.InstructedAgent );
  if( ED107MessageFields.AcctWithInst != null )
    FillPmRouteRecord( IntermediaryBank, ED107MessageFields.AcctWithInst );
    
    if( IntermediaryBank.PartyID <= 0 )
      std.msg( "Не удалось идентифицировать участника операции по реквизитам блока сообщения " + ED107MessageFields.AcctWithInst.BlockName );
      return false;
    end;
  end;
  if( ED107MessageFields.Beneficiary != null )
    ReceiverBankFilled = FillPmRouteRecord( ReceiverBank, ED107MessageFields.Beneficiary );
    
    if( ReceiverBank.PartyID <= 0 )
      std.msg( "Не удалось идентифицировать участника операции по реквизитам блока сообщения " + ED107MessageFields.Beneficiary.BlockName );
      return false;
    end;
    
    if( (V == 3) and (ED107MessageFields.Beneficiary.BankAccount != ""))
      wlpmpaym.ReceiverAccount = ED107MessageFields.Beneficiary.BankAccount;
    end;    
  end;

  if( PayerBankFilled )
    wlpmrmprop.PayerName = PayerBank.Name;
    if(V == 3)
      wlpmpaym.Payer = PayerBank.PartyID;
      wlpmpaym.PayerCode = PayerBank.CodeValue;
      wlpmpaym.PayerCodeKind = PayerBank.CodeKind;
    end;
  elif( SenderBankFilled )
    wlpmrmprop.PayerName = SenderBank.Name;
    if(V == 3)
      wlpmpaym.Payer = SenderBank.PartyID;
      wlpmpaym.PayerCode = SenderBank.CodeValue;
      wlpmpaym.PayerCodeKind = SenderBank.CodeKind;
    end;
  end;
  
  if( ( ED107MessageFields.OrderingBank != null ) and ( ED107MessageFields.OrderingBank.BankAccount != "" ) )
    wlpmpaym.PayerAccount = ED107MessageFields.OrderingBank.BankAccount;
    wlpmpaym.PayerBankID = PayerBank.PartyID;
  end;

  if( ( ED107MessageFields.Beneficiary != null ) and ( ED107MessageFields.Beneficiary.BankAccount != "" ) )
    wlpmpaym.ReceiverAccount = ED107MessageFields.Beneficiary.BankAccount;
    wlpmpaym.ReceiverBankID = ReceiverBank.PartyID;
  end;

  if( ReceiverBankFilled )
    wlpmrmprop.ReceiverName = ReceiverBank.Name;
    if(V == 3)
      wlpmpaym.Receiver = ReceiverBank.PartyID;
      wlpmpaym.ReceiverCode = ReceiverBank.CodeValue;
      wlpmpaym.ReceiverCodeKind = ReceiverBank.CodeKind;
    end;
  elif( ExecutorBankFilled )
    wlpmrmprop.ReceiverName = ExecutorBank.Name;
    if(V == 3)
      wlpmpaym.Receiver = ExecutorBank.PartyID;
      wlpmpaym.ReceiverCode = ExecutorBank.CodeValue;
      wlpmpaym.ReceiverCodeKind = ExecutorBank.CodeKind;
    end;
  end;
  
  wlpmrmprop.UIN = ED107MessageFields.PaymentID;
             
  wlpmpropdeb.Group = PAYMENTS_GROUP_EXTERNAL; // Далее будет переопределено функцией подбора платежных инструкций
  wlpmpropcred.Group = PAYMENTS_GROUP_UNDEF; // Далее будет переопределено функцией подбора платежных инструкций
  
  if( V == 3)
    wlpmpaym.PartPaymNumMain = ED107MessageFields.RelatedDocNo;
    wlpmpaym.PartPaymDateMain = ED107MessageFields.RelatedDocDate;
    wlpmrmprop.SettlementTime = GetSettlementTime( NULL, wlmes, wlpmrmprop.SettlementTime,  ED107MessageFields.SettlementTime);  
  end;
  
  wlpmpropdeb.PayFIID = NATCUR;
  
  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, 0, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID );
  if( wlpmpropdeb.Corschem == -1 )
    std.msg( "Не определена схема расчетов для респондента в рублях" );
    return FALSE;
  else
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  // Вставка платежа
  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg( "Ошибка при вставке платежа" );
    return FALSE;
  end;
  
  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );
  
  /*Сохранение участников операции в маршрут платежа, может вызываться только для кредитового платежа*/
  if( PaymentObj.IsCredit() )
    stat = PaymentObj.SaveOperationParticipantsToRoute( IfThenElse( PayerBankFilled, PayerBank, null ),
                                                        IfThenElse( ED107MessageFields.PrevInstrAgent != null, PrevInstrAgent, null ),
                                                        IfThenElse( SenderBankFilled, SenderBank, null ),
                                                        IfThenElse( ExecutorBankFilled, ExecutorBank, null ), 
                                                        IfThenElse( ED107MessageFields.AcctWithInst != null, IntermediaryBank, null ), 
                                                        IfThenElse( ReceiverBankFilled, ReceiverBank, null ) 
                                                      );
  end;

  if( (not stat) or (PaymentObj.Update()) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;
  
  return TRUE;
  
  /* Обработка ошибок времени выполнения */
  OnError(er) 
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDocV3( addrMes )
  V = 3;
  return GenDoc( addrMes );
end;