/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Константы и вспомогательные функции для генерации учетных объектов по   */
/*  сообщениям SWIFT (в модуле можно использовать WldInter)                 */
/*                                                                          */
/*  Имя файла: swgendoc.mac                                                 */
/*  Создан:    09.02.00                                        Алешин А.В.  */
/****************************************************************************/

import "wldoc.mac", "swparser.mac", "wlnote.mac", "wlgendoc.mac", "wlfindpm.mac", oralib, likepy;
import "bnk_ptlib.mac";

private FILE acc   ("account");        /* лицевые счета */
private FILE dp_dep ("dp_dep");        /* территориальная структура */

macro GetOwnerID( FIID, Account ):integer
  
  acc.Chapter       = 1 ; /* Баланс */
  acc.Account       = Account;
  acc.Code_Currency = FIID;
  if( GetEq( acc ) )
    dp_dep.Code = acc.Department;    
  else
    return 0;
  end;
  
  if( GetEq( dp_dep ) )
    return dp_dep.PartyID;
  else
    return 0;
  end;
  
end;

macro ПолучитьНаименованиеКлиента( PartyID, Name )
   RECORD Party( party );

   if ( not ПолучитьСубъекта(PartyID, Party) )
        Name = Party.Name;
        setparm( 1, Name );
        return true;
   end;

   return false;
end;

macro ПолучитьНаименованиеВидаКода( CodeKind )
   FILE f_codekind(objkcode) key 0;

   f_codekind.CodeKind = CodeKind;
   f_codekind.ObjectType = OBJTYPE_PARTY;
   if ( GetEQ(f_codekind) )
      return f_codekind.ShortName;
   end;

   return "";
end;

private macro GetCorrIDOfCorschemByAccount( Account:string )
  
  var CorrID = -1;

  var rs = execSQLselectPrm( "select t_CorrID " 
                               "from dcorschem_dbt "
                               "where t_Account = :Account ",
                               SQLParam("Account", Account )
                            );

  if( rs.moveNext )
    CorrID = rs.value("t_CorrID");
  end;

  return CorrID;
end;

/* ------------- Банк ------------- */
class Bank( pBIC, pName, pBranch, pAccount, pCliringSystem, pCliringCode, pINN )
      
 var PartyID       :integer,  /* Ссылка на банк */
     CodeKind      :integer,  /* Вид кода субъекта */
     CodeName      :string,   /* Наименование вида кода */
     CodeBank      :string,   /* Код банка по ISO */
     Account       :string,   /* Счет */
     Name          :string,   /* Name and Address */
     Branch        :string,   /* Код филиала */
     CliringSystem :string,   /* Клиринговая система */
     CliringCode   :string,   /* Код клиринга */
     error;

  CodeName = "";
  /* инициализация */
  if(valtype(pBIC)==V_STRING)
    CodeBank = pBIC;
  else
    CodeBank = "";
  end;
  if(valtype(pAccount)==V_STRING)
    Account = pAccount;
  else
    Account = "";
  end;
  if(valtype(pName)==V_STRING)
    Name = pName;
  else
    Name = "";
  end;
  if(valtype(pBranch)==V_STRING)
    Branch = pBranch;
  else
    Branch = "";
  end;
  if(valtype(pCliringSystem)==V_STRING)
    CliringSystem = pCliringSystem;
  else
    CliringSystem = "";
  end;
  if(valtype(pCliringCode)==V_STRING)
    CliringCode = pCliringCode;
  else
    CliringCode = "";
  end;
  
  if( valtype( pINN ) == V_UNDEF )
    pINN = "";
  end;
  
  PartyID  = -1;
  CodeKind =  0;

  if( CodeBank != "" )
    PartyID = ПолучитьКодСубъекта( CodeBank, PTCK_SWIFT, error );
    if( error == 0 )
      CodeKind = PTCK_SWIFT;
    elif((strlen(CodeBank) == 11) and (substr(CodeBank,9,3) == "XXX"))
      PartyID = ПолучитьКодСубъекта( substr(CodeBank,1,8), PTCK_SWIFT, error );
      if( error == 0 )
        CodeKind = PTCK_SWIFT;
        CodeBank = substr(CodeBank,1,8);
      else
        PartyID = -1;
      end;
    else
      PartyID = -1;
    end;
  elif( CliringCode != "" )
    if( CliringSystem == CODEWORLD_FEDWIRE )
      PartyID = ПолучитьКодСубъекта( CliringCode, PTCK_ABA, error );
    elif( CliringSystem == CODEWORLD_SMFRSBRF )
      PartyID = ПолучитьКодСубъекта( CliringCode, PTCK_SMFR, error );
    elif( CliringSystem == CODEWORLD_BIC )
      PartyID = ПолучитьКодСубъекта( CliringCode, PTCK_BIC, error );
    elif( CliringSystem == CODEWORLD_CHIPS )
      PartyID = ПолучитьКодСубъекта( CliringCode, PTCK_CHIPS, error );
    else
      PartyID = ПолучитьКодСубъекта( CliringSystem + CliringCode, PTCK_NCSC, error );
    end;
    /* Субъект не найден по коду клиринга */
    if( error )    
      PartyID = -1;
    end;

    CodeBank = CliringCode;
    if( CliringSystem == CODEWORLD_FEDWIRE )
      Codekind = PTCK_ABA;
    elif( CliringSystem == CODEWORLD_SMFRSBRF )
      Codekind = PTCK_SMFR;
    elif( CliringSystem == CODEWORLD_BIC )
      Codekind = PTCK_BIC;
    elif( CliringSystem == CODEWORLD_CHIPS )
      Codekind = PTCK_CHIPS;
    else
      Codekind = PTCK_NCSC;
      CodeBank = CliringSystem+CliringCode;
    end;
  
  elif( pINN != "" )
    CodeKind = PTCK_INN;
    CodeBank = pINN;
    PartyID = ПолучитьКодСубъекта( CodeBank, CodeKind, error );
    if( error )    
      PartyID = -1;
    end;
  elif( Account != "" )
    PartyID = GetCorrIDOfCorschemByAccount( Account );
  end;

  if ( CodeKind>0 )
     CodeName = ПолучитьНаименованиеВидаКода(CodeKind);
  end;

  if ( (PartyID>=0) AND (Name=="") )          
     ПолучитьНаименованиеКлиента( PartyID, Name );
  end;

  /*MsgBox("CodeBank :", CodeBank," PartyID =", PartyID, " error", error );*/

  macro IsSet()
    return NOT((CodeBank=="")AND(Account=="")AND(Name=="")AND(Branch=="")AND(CliringSystem==""));
  end;

  macro GetRoute()
    ClearRecord(Route);
    Route.PartyID    = PartyID;
    Route.CodeKind   = CodeKind;
    Route.CodeValue  = CodeBank;
    Route.InAccount  = "";
    Route.OutAccount = "";
    Route.Name       = Name;
    if( IsSet() )
      return Route;  
    else
      return null;
    end;
  end;

  macro InsertRoute( Direct, IsInstructionAbonent )
    if( IsSet() )
      if ( Direct==RTDIR_IN )
         return ВставитьВходящийУзелМаршрута( PartyID, CodeKind, CodeBank, "", "", Name, IsInstructionAbonent );
      else
         return ВставитьИсходящийУзелМаршрута( PartyID, CodeKind, CodeBank, "", "", Name, IsInstructionAbonent );
      end;
    else
      return TRUE;
    end;
  end;

end;

/* ------------- Клиент ------------- */
class Customer(pName, pAccount,pBIC, pINN, pCodeKind)
  /* инициализация */
 var PartyID  :integer,  /* Ссылка на клиента */
     Account  :string,   /* Счет */
     Name     :string,   /* Name and Address */
     Code     :string,   /* BIC/BEI */
     CodeKind :integer,  /* Вид кода */
     INN      :string,   /* INN */
     error;

  /* инициализация */
  if(valtype(pAccount)==V_STRING)
    Account = pAccount;
  else
    Account = "";
  end;
  if(valtype(pName)==V_STRING)
    Name = pName;
  else
    Name = "";
  end;
  if(valtype(pBIC)==V_STRING)
    Code = pBIC;
  else
    Code = "";
  end;
  if(valtype(pINN)==V_STRING)
    INN = TrimRight(pINN);
  else
    INN = "";
  end;
  if(valtype(pCodeKind)==V_INTEGER)
    CodeKind = pCodeKind;
  else
    CodeKind =  PTCK_SWIFT;
  end;

  PartyID  = -1;

  if( Code != "" )
    PartyID = ПолучитьКодСубъекта( Code, CodeKind, error );
    if( error != 0 )
      PartyID = -1;
    end;
  end;

  macro IsSet()
    return NOT((Account=="")AND(Name=="")AND(Code==""));
  end;

end;

class TCharges( pCurrency, _Amount, _PartyID )
    var FIID : integer,
        Amount:moneyl,
        PartyID:integer;

   macro InsertCharges
       return ВставитьРасходыРеспондента( FIID, Amount, PartyID );
   end;

   if(valtype(pCurrency)==V_INTEGER)
     FIID = pCurrency;
   elif(valtype(pCurrency)==V_STRING)
     if( not ПолучитьФинИнПоISO( pCurrency, wlfininstr ) )
       std.err( 1, "Не определена валюта балансового остатка по коду ISO :" +  pCurrency );
     end;
     FIID = wlfininstr.FIID;
   else
     FIID = 0;
   end;

   if((valtype(_Amount)==V_MONEY) OR (valtype(_Amount)==V_MONEYL))
     Amount = _Amount;
   elif((valtype(_Amount)==V_DOUBLE) OR (valtype(_Amount)==V_DOUBLEL))
     Amount = moneyl(_Amount);
   else
     Amount = moneyl(0);
   end;

   if ( valtype(_PartyID)==V_INTEGER )
     PartyID = _PartyID;
   else
     PartyID = -1; /* Чью расходы мы не знаем */
   end;
end;

MACRO НайтиКорсхемуПоКонтрагенту( CorrID, Number, FIID )
   var rs:object;
   var select:string;
   var params:TArray;

   select = "select cors.t_number, cors.t_FIID from dcorschem_dbt cors where "+
                              "cors.t_CorrID = :CorrID";
   params = makeArray( SQLParam("CorrID", CorrID));
   rs = execSQLselect( select, params, FALSE );
   if ( not rs.moveNext() ) 
        return false;
   end;

   Number = rs.value(0);
   FIID   = rs.value(1);

   setParm( 1, Number );
   setParm( 2, FIID );

   return true;
end;

/* Класс стека */
class Stack
   var Val = TArray;
   macro Reset()
      Val.size = 0;
   end;
   macro Add(value)
      Val[Val.size] = value;
   end;
   macro Get()
      var value;
      if ( Val.size>0 )
         value = Val[Val.size-1];
         Val.size = Val.size-1;
         return value;
      else
         return value;
      end;
   end;

   Reset();
end;

macro GetInTransferDate
( CorschemNumber : integer, 
  CorschemFIID : integer,
  ValueDate : date
) : date

  if(CorschemNumber >= 0)
    var rs = execSQLselectPrm
      ( "select t_InPmDate, t_InPmDateOffset "
        "  from dcorschem_dbt "
        " where t_Number = :CorschemNumber "
        "   and t_FIID = :CorschemFIID "
        "   and t_FI_Kind = :FIKIND_CURRENCY ",
        SQLParam("CorschemNumber", CorschemNumber),
        SQLParam("CorschemFIID", CorschemFIID),
        SQLParam("FIKIND_CURRENCY", FIKIND_CURRENCY)
      );

    if(rs and rs.moveNext())
      var InPmDate : integer = rs.value("t_InPmDate");
      var InPmDateOffset : integer = rs.value("t_InPmDateOffset");

      var InitialDate : date = {curdate};
      if( (InPmDate == 0 /* value date */) and (ValueDate != date(0, 0, 0)) )
        InitialDate = ValueDate;
      end;

      return GetDateAfterWorkDays( InitialDate, InPmDateOffset );
    end;
  end;

  return {curdate};
end;


macro SetReceiverBankForInPaymentBy57a( wlmes, bank:Bank, pmpaym, pmprop_credit, pmrmprop )

  if( bank.PartyID <= 0 )
    pmpaym.ReceiverBankID = Bnk_GetDeptPartyID(wlmes.Department);
  else
    pmpaym.ReceiverBankID = bank.PartyID;
  end;

  pmprop_credit.CodeKind = bank.CodeKind;
  pmprop_credit.BankCode = bank.CodeBank;
  pmrmprop.ReceiverBankName = bank.Name;
  pmrmprop.ReceiverCorraccNostro = bank.Account;

end;
