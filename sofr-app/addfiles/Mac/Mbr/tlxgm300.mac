/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по TELEX MT300                                       */
/*                                                                          */
/*  Имя файла: txgm300.mac                                                  */
/*  Создан:  26.05.2003                                        Панов А. Б.  */
/****************************************************************************/
import "wlgenmes.mac", "swtools.mac", "wltools.mac", OprInter, FxInter, oralib, likepy;

var СделкаПривлечения = TRUE; /* Тип сделки: размещение/привлечение */

private file f_wlmeslnk(wlmeslnk) key 1;  /* связи с ключем  ObjID, ObjKind, Direct, MesID */

/* Определение типа операции */
macro ОпределитьТипОперации( DealID, Sending )
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select lnk.t_ObjID from dwlmeslnk_dbt lnk where "+
                             "lnk.t_ObjID = :DealID and "+
                             "lnk.t_ObjKind = :OBJTYPE_CONVDEAL and "+
                             "lnk.t_Direct = chr(0)";
  params = makeArray( SQLParam("DealID", DealID),
                      SQLParam("OBJTYPE_CONVDEAL", OBJTYPE_CONVDEAL));
  rs = execSQLselect( select, params, FALSE );
  if( rs.moveNext() )
    SetParm( 1, TRUE );
    return TYPE_OPERATION_AMND;
  else
    return TYPE_OPERATION_NEWT; 
  end;
end;

/* Записать поле */
macro ЗаписатьБанкЛог( BlockName, PartyID, CodeKind, CodeValue, BankName, CorrAccount )
  var field_value = "", Tag, tempv, ID, BankCode, error;

  /* Только по наименованию пока банк не заполняем */
  if( (PartyID != 0) OR ((CodeKind != 0) AND (CodeValue != "")) )
    if( (CodeKind != 0) AND (CodeValue != "") )
      ID = ПолучитьКодСубъекта( CodeValue, CodeKind, error );
      if( error ) RunError( "|Не найден банк c кодом: " + CodeValue + " и видом кода: " + CodeKind ); end;
    else
      ID = PartyID;
    end;

    /* Ищем для субъекта SWIFT-код. Если не нашли - отваливаем,   */
    /* так как настоятельно рекомендуется использовать именно его */
    BankCode = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error );
    if( error ) RunError( "|Не найден SWIFT-код для банка с кодом: "+ CodeValue + " и видом кода: " + CodeKind ); end;
    CodeKind = PTCK_SWIFT;
    
    if( not УстановитьКонтекстБлока( BlockName ) )
      RunError("|Ошибка при установке контекста блока " + BlockName);
    end;

    if( (BankCode != "") AND (CodeKind == PTCK_SWIFT) )
      Tag = SWIFT_Tag_A;
    else
      Tag = SWIFT_Tag_D;
    end;

    if( Tag == SWIFT_Tag_A )
      if( CorrAccount != "" )
        field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
      end;
      field_value = field_value + BankCode;
      ЗаписатьПолеЛог( SubStr( BlockName, 1, 2 ) + Tag, field_value );
    else
      if(CorrAccount != "")
        field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
      end;
      if( BankName != "" )      
        StrChangeRusXSet( BankName, tempv );
        StrMultyToFormat( tempv, tempv, 35, 4 );
        field_value = field_value + tempv;
      end;
      ЗаписатьПолеЛог( SubStr( BlockName, 1, 2 ) + Tag, field_value );
    end;

    if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока '..'");
    end;
  end;

end;

/* Непосредственно генерация МТ300 */
macro GenMes( addrMes, addrDeal )
  var field_value, error, Sending = FALSE, CCnvDeal, DemandPayment, 
      LiabilityPayment, IsDealSWAP, IsDealReverse, PartyID;

  SetBuff( wlmes,  addrMes );
  SetBuff( wldeal, addrDeal );

  PrintLog(2,"Генерация сообщения по TELEX МТ300");

  IsDealSWAP    = IsSWAP(GetOperationGroup(wldeal.DealType));
  IsDealReverse = IsDealReverseMode();

  if( IsDealSWAP == FALSE )
    CCnvDeal = RsbConvDeal( wldeal.DealID );
    DemandPayment    = CCnvDeal.DemandPayment;
    LiabilityPayment = CCnvDeal.LiabilityPayment;  
  else
    CCnvDeal = RsbSwapDeal( wldeal.DealID );
    if( IsDealReverse == FALSE )
      DemandPayment    = CCnvDeal.DemandPayment;
      LiabilityPayment = CCnvDeal.LiabilityPayment;  
    else
      DemandPayment    = CCnvDeal.ReverseDemandPayment;
      LiabilityPayment = CCnvDeal.ReverseLiabilityPayment;  
    end;
  end;

  /* Ключевание. Пока ничего не происходит */
/*  ЗаписатьПолеЛог("15", ""); */

  /* Блок Sequence A */
  УстановитьКонтекстБлокаЛог( "Sequence A" );

  /* 15A New Sequence */
  ЗаписатьПолеЛог( "15A", "" );

  /* 20 Sender's Reference */
  ЗаписатьПолеЛог( "20", wlmes.TRN );

  /* Определяем тип операции */
  if( IsDealSWAP == TRUE )
    field_value = TYPE_OPERATION_NEWT;
  else
    field_value = ОпределитьТипОперации( wldeal.DealID, Sending );
  end;

  /* 21 Related Reference */
  if( (IsDealSWAP == FALSE) AND (Sending == TRUE) AND (wlmes.RelatedRef != "") )
    ЗаписатьПолеЛог( "21", wlmes.RelatedRef );
  end;

  /* 22A - Type of Operation */
  ЗаписатьПолеЛог( "22A", field_value );

  /* 22C Common Reference */
  ЗаписатьПолеЛог( "22C", CCnvDeal.DealCodePS );

  /* 82a Party A */
  field_value = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  if( error ) RunError( "|Не найден SWIFT-код банка нашего банка" ); end;
  УстановитьКонтекстБлокаЛог( "82a" ); 
  ЗаписатьПолеЛог( "82A", field_value );

  /* 87a Party B */
  field_value = ПолучитьКодСубъекта( CCnvDeal.PartyID, PTCK_SWIFT, error );
  if( error ) RunError( "|Не найден SWIFT-код банка-контрагента по сделке" ); end;
  УстановитьКонтекстБлокаЛог( ".." );  /* Возвращаемся в контекст блока 'Sequence A' */
  УстановитьКонтекстБлокаЛог( "87a" );
  ЗаписатьПолеЛог( "87A", field_value );

  /* Блок Sequence B*/
  УстановитьКонтекстБлокаЛог( "" );
  УстановитьКонтекстБлокаЛог( "Sequence B" );

  /* 15B New Sequence */
  ЗаписатьПолеЛог( "15B", "" );

  /* 30T Trade Date */
  ЗаписатьПолеЛог( "30T", GetSWIFTDateFull(CCnvDeal.DealDate) );

  /* 30V Value Date */
  if( (IsDealSWAP == TRUE) AND (IsDealReverse == TRUE) )
    field_value = CCnvDeal.ReverseValueDate;
  else
    field_value = CCnvDeal.ValueDate;
  end;
  ЗаписатьПолеЛог( "30V", GetSWIFTDateFull(field_value) );

  
  /* 36 Exchange Rate - записываем значение с учетом того, что ставка хранится в двух полях */
  if( (IsDealSWAP == TRUE) AND (IsDealReverse == TRUE) )
    field_value = CCnvDeal.ReverseLeg.Rate.asDouble;
  else
    field_value = CCnvDeal.Leg.Rate.asDouble;
  end;
  ЗаписатьПолеЛог( "36", GetSWIFTRate( field_value ) );

  /* Subsequence B1 Amount Bought */
  УстановитьКонтекстБлокаЛог( "B1" );
  
  /* 32B Currency, Amount */
  field_value = ПолучитьКодВалютыЛог( DemandPayment.BaseFIID ) +
                GetSWIFTAmount( DemandPayment.BaseAmount );
  ЗаписатьПолеЛог( "32B", field_value );

  /* Заполняем, если задан банк плательщика и это не контрагент */
  if( (DemandPayment.PayerBankID!={OurBank}) AND (DemandPayment.PayerBankID != CCnvDeal.PartyID) )
    
    /* 53a Delivery Agent - кто платит деньги */
    ЗаписатьБанкЛог( "53a", DemandPayment.PayerBankID, DemandPayment.PayerBankCodeKind, DemandPayment.PayerBankCode, DemandPayment.PayerBankName, "" );

    /* 56a Intermediary - через кого мы получим купленную валюту */ 
    ЗаписатьБанкЛог( "56a", 0, DemandPayment.ReceiverBankCorrCodeKind, DemandPayment.ReceiverBankCorrCode, DemandPayment.ReceiverBankCorrName, DemandPayment.ReceiverBankCorrAcc );
  end;

  /* 57a - Receiving Agent - куда мы получим купленную валюту */
  PartyID = DemandPayment.ReceiverBankID;
  ЗаписатьБанкЛог( "57a", PartyID, DemandPayment.ReceiverBankCodeKind, DemandPayment.ReceiverBankCode, DemandPayment.ReceiverBankName, DemandPayment.ReceiverAccount );

  /* Выходим из контекста блока B1 */
  if( not УстановитьКонтекстБлока( ".." ) )
    RunError("|Ошибка при установке контекста блока '..'");
  end;

  /* Subsequence B2 Amount Sold */
  УстановитьКонтекстБлокаЛог( "B2" );

  /* 33B Currency, Amount */
  field_value = ПолучитьКодВалютыЛог( LiabilityPayment.BaseFIID ) +
                GetSWIFTAmount( LiabilityPayment.BaseAmount );
  ЗаписатьПолеЛог( "33B", field_value );

  /* Заполняем, если задан банк плательщика и это не наш банк */
  if( (LiabilityPayment.PayerBankID!={OurBank}) AND (LiabilityPayment.PayerBankID != {OurBank}) )

    /* 53a Delivery Agent - через кого мы продаем валюту */
    ЗаписатьБанкЛог( "53a", LiabilityPayment.PayerBankID, LiabilityPayment.PayerBankCodeKind, LiabilityPayment.PayerBankCode, LiabilityPayment.PayerBankCorrName, "" );

    /* 56a Intermediary */ 
    ЗаписатьБанкЛог( "56a", 0, LiabilityPayment.ReceiverBankCorrCodeKind, LiabilityPayment.ReceiverBankCorrCode, LiabilityPayment.ReceiverBankCorrName, LiabilityPayment.ReceiverBankCorrAcc );
  end;

  /* 57a - Receiving Agent - куда корреспондент получит свои деньги */
  PartyID = LiabilityPayment.ReceiverBankID;
  ЗаписатьБанкЛог( "57a", PartyID, LiabilityPayment.ReceiverBankCodeKind, LiabilityPayment.ReceiverBankCode, LiabilityPayment.ReceiverBankName, LiabilityPayment.ReceiverAccount );

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

