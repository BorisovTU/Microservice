/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Вспомогательные функции для генерации сообщений по транспорту "Луч"      */
/*                                                                          */
/*  Имя файла: raygenmes.mac                                                */
/*  Создан:    30.03.09                                                     */
/****************************************************************************/

import "raytools.mac", "wlgenmes.mac",  "wltools.mac", rsd, oralib, likepy, DPInter,FIInter;

macro GetMessageParty(msginf, type, kind)
   var msgParty;
   msginf.RewindMessageParty();

   msgParty = msginf.GetMessageParty();
   while( valtype(msgParty)!=V_UNDEF )
       if ( (msgParty.PartyType==type) and (msgParty.PartyKind==kind) )
          return msgParty;
       end;
       msgParty = msginf.GetMessageParty();
   end;
   return msgParty;
end;

macro YYYYMMDD( dateInit )
  var day, mon, year;
  DateSplit(dateInit, day, mon, year );
  return StrSubst(string(year:4, "-", mon:2,"-", day:2), " ", "0");
end;

macro HHMMSS(timeInit)
  var hour, min, sec;
  timesplit( timeInit, hour, min, sec );
  return StrSubst(string(hour:2, ":", min:2,":", sec:2), " ", "0");
end;

macro CheckForRus( fieldname, strInit, memerr)
  var count, len = strlen(strInit), symb;
  while( count<=len )      
    symb = SubStr(strInit,count,1);
    if(index(RayRusSet2,symb))
      if( memerr )
        std.msg(string("В поле ", fieldname, " не допустимо использование кириллицы"));
        return false;
      else
        RunError(string("В поле ", fieldname, " не допустимо использование кириллицы"));
        return false;
      end;
    end;
    count = count + 1;
  end;
  return true;
end;

macro ЗаписатьПолеЛогСПроверкой(fieldname, strInit)
  CheckForRus( fieldname, strInit, false);
  ЗаписатьПолеЛог( fieldname, strInit);
end;

macro Write_ORDER_HEADER_Block( msginf )
  var  msgdoc, msgParty;  
  var  error, PartyCode;

  /* Блок ORDER_HEADER */
  ЗаписатьПолеЛогВБлок( ORDER_HEADER_Block, BeginOfBlock, ORDER_HEADER_Block );
  /*deposit_c*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_CLIENT, error );    
    if( not error )
      ЗаписатьПолеЛогСПроверкой( deposit_c_Field, PartyCode );
    else
      RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
    end;
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (REAG)" );
  end; 
  
  /*contrag_c*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_OTHER,OBJTYPE_MSGPATYTP_MEOR);
  if ( valtype(msgParty)!=V_UNDEF )
    if(msgParty.DepoPartyCode!="")
      ЗаписатьПолеЛогСПроверкой( contrag_c_Field, msgParty.DepoPartyCode );
    else
      RunError("|Не заполнен код инициатора поручения");
    end;
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (MEOR)" );
  end; 
 
  /*contr_d_id*/
  ЗаписатьПолеЛог( contr_d_id_Field, msginf.SenderReference );  
  /*createdate*/
  ЗаписатьПолеЛог( сreatedate_Field, YYYYMMDD(msginf.PreparationDate) );  
  /*order_t_id*/
  if( msginf.ProcessingCode != "")
    ЗаписатьПолеЛог( order_t_id_Field, msginf.ProcessingCode );  
  else
    RunError("|Не заполнен номер операции у получателя");
  end;
 
  /*execute_dt*/
  ЗаписатьПолеЛог( execute_dt_Field, string(YYYYMMDD(msginf.SettlementDate)," ",HHMMSS(msginf.SettlementTime)));  
  /*expirat_dt*/
  ЗаписатьПолеЛог( expirat_dt_Field, string(YYYYMMDD(msginf.LateDeliverDate)," ",HHMMSS(msginf.LateDeliverTime)));  

  /*sender_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SENDER,0);
  if ( valtype(msgParty)!=V_UNDEF )
    if( msgParty.PartyCode != "" )
      ЗаписатьПолеЛог( sender_bic_Field, msgParty.PartyCode );      
    end;
  end; 

  /*deposit_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    if( msgParty.PartyCode != "" )
      ЗаписатьПолеЛог( deposit_bic_Field, msgParty.PartyCode );
    end;
  end; 

  /*contrag_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_OTHER,OBJTYPE_MSGPATYTP_MEOR);
  if ( valtype(msgParty)!=V_UNDEF )
    if( msgParty.PartyCode != "" )
      ЗаписатьПолеЛог( contrag_bic_Field, msgParty.PartyCode);
    end;
  end; 


  ЗаписатьПолеЛог( EndOfBlock, ORDER_HEADER_Block );
  /* Конец блока ORDER_HEADER */
end;


macro FillISIN( msginf )
   RECORD fin(fininstr);
   RECORD Avr(avoiriss);

   ClearRecord (fin);
   ClearRecord (Avr);

   if ( not ПолучитьФинИн(msginf.SecurityID, fin, Avr) )
     if(Avr.ISIN != "")
       return Avr.ISIN
     end;
   end;
   return "";
end;


macro TransField( strInit )
   var str="", count, len = strlen(RayRusSet1);
   count = 1;
   str = strInit;

   while( count<=len )      
      str = StrSubst(str, substr(RayRusSet1,count,1), substr(RayLatSet,count,1));
      count = count + 1;
   end;
   return str;
end;

