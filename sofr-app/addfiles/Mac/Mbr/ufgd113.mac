/*
  $Name:         ufgd113.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация распоряжений на оплату по сообщениям УФЭБС ED113
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация распоряжений на оплату по сообщениям УФЭБС ED113               */
/*                                                                          */
/* Имя файла: ufgd113.mac                                                   */
/* Создан:    13.07.12                                       Chukina T.     */
/****************************************************************************/

import BankInter, PaymInter, OprInter, wluftool;

macro GenDoc (addrMes) 

  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация распоряжения на оплату по сообщению ED113" );

  var Order : RsbInClaimOrder = RsbInClaimOrder( 0 );
  Order.DocKind = Order.PrimDocKind = DLDOC_IN_PMCLAIM;
  Order.ShifrOper = "02";
  Order.PrimDocOrigin = PD_OR_AUTO;

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var tmpstr = "";

  var field_name, field_value;
  if( not СчитатьПоле( field_name, field_value ) )
    return FALSE;
  end;

  if ( not xml.loadXML(field_value) )
    println( string( "Неверный формат сообщения по форме ED113" ) );
    return FALSE;
  end; 


  FillPayment_113_114(xml, wlmes, Order);

  Order.ReqSum = Order.BaseAmount;
  
  tmpstr = ReadOptinalAttribute(xml, "PaytCondition");
  if(tmpstr)
    Order.AcceptTerm = GetAcceptTermByPaytCond(tmpstr);
  end;
  tmpstr = ReadOptinalAttribute(xml, "AcptTerm");
  
  Order.AcceptPeriod = GetAcceptPeriodByAcptTerm(tmpstr);
  
  tmpstr = ReadOptinalAttribute(xml, "MaturityDate");
  if(tmpstr)  
    Order.AcceptDate = ToDate(tmpstr);
  end;    

  tmpstr = ReadOptinalAttribute(xml, "DocDispatchDate");
  if(tmpstr)
    Order.DocDispatchDate = ToDate(tmpstr);
  end;


  if( Order.Update() != 0 )
    std.msg("Ошибка при вводе предъявленного требования");
    return FALSE;
  end;

  /*Связь рублевого платежа РКО с входящим сообщением */
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = Order.PaymentID;
  meslnk.rec.ObjKind = OBJTYPE_PMCLAIM;
  var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
  if( (MesLnkObj.Insert( meslnk ) != 0) or (MesLnkObj.Save() != 0) )
    std.msg("Ошибка при создании связи предъявленного требования с входящим сообщением");
    return FALSE;
  end;
  
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;  

end;