/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Константы и вспомогательные функции для работы с сообщениями SWIFT-SB   */
/*                                                                          */
/*  Имя файла: swsbtls.mac                                                  */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swtools.mac", "wldoc.mac", "wltools.mac", rsd, oralib, likepy;

/* Отличные от SWIFT поля SWIFT-SB */
const УникальныйНомерСообщенияСМФР     = "SN",
      НомерСвязанногоСообщенияСМФР     = "RN",
      ТипИПодтипСообщения              = "RT",
      ДебетКредитФлагСМФР              = "DC",
      ТипВыписки                       = "VT",
      ДатаПредыдущейВыписки            = "V4",
      ДатаВремяВыписки                 = "V5",
      DebetTurn                        = "V8D",
      CreditTurn                       = "V8C",
      DebitAccountIdentificationField  = "25D",
      CreditAccountIdentificationField = "25C",
      CurrencyOrQuantityOfMetalField   = "32E";

const КодПодтипСообщения_СМФР    = "S",
      КодПодтипСообщения_КорСчет = "N",
      КодПодтипСообщения_Общий   = "I",

      КодНазначениеСообщения_Общий               = "0",
      КодНазначениеСообщения_МТ910_Платеж        = "1",
      КодНазначениеСообщения_МТ910_Подтверждение = "2",

      КодSWIFTПоУмолчанию = "SABRXXXX",
      ВремяПоУмолчанию    = "0000",
      ДатаПоУмолчанию     = "000000";

const НомерБлокаСлужебнойИнформацииСМФР   = "SB",
      КодВерсииФормата                    = "1",
      НомерДополнительногоТекстовогоБлока = "4+";

const ДлинаКодПодтипСообщения     = 1,
      ДлинаКодНазначениеСообщения = 1,
      ДлинаКодСМФР                = 4,
      ДлинаКодАбонента            = 3;


/* Определить номер формы для сообщения, заданного через MesID, либо через уникальный номер */
macro ПолучитьНазваниеФормыСвязаногоСообщенияSB( reference )
  var Name = "";
  var rs:object;
  var select:string;
  var params:TArray;
  
  select = "select rls.t_Name from dwlmesrls_dbt rls, dwlmes_dbt mes where rls.t_RlsFormID = mes.t_RlsFormID";
                           
  if( ValType( reference ) == V_INTEGER )                         
    select =  select  + " AND mes.t_MesID =:reference ";
  else /* V_STRING */
    select =  select  + " AND mes.t_TRN = :reference "+
                    " AND (mes.t_Direct = 'X' OR mes.t_Direct = chr(0))";
  end;
  params = makeArray( SQLParam("reference", reference));
  rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    Name = rs.value(0);
  end;

  return Name;
end;

/* Количество сообщений по учетному объекту */
macro КоличествоСообщенийПоУчетномуОбъектуСообщения( MesID, ObjKind )
  var Count = 0;
  var rs:object;
  var select:string;
  var params:TArray;
  
  select = "select count(meslnk1.T_MESID) from dwlmeslnk_dbt meslnk1, dwlmeslnk_dbt meslnk2"+
          " where meslnk2.t_MesID = :MesID and meslnk2.t_ObjId = meslnk1.t_ObjId"+
          " and meslnk1.t_ObjKind =:ObjKind";
  params = makeArray( SQLParam("MesID", MesID),
                      SQLParam("ObjKind", ObjKind));
  rs = execSQLselect( select, params, FALSE );                     
  if(rs.MoveNext())
      Count = rs.value(0); 
  end;
  return Count;
end;

/* Проверка Валюты для сообщений (должно быть не рубли) */
macro SB_CheckFIID( FIID )
   if(FIID == 0)
      RunError("|Генерация сообщений SWIFT-SB возможна только |для учетных объектов в инвалюте");
   end;
end;

/*
Функция получает вид кода который надо найти и признак "Искать наш код или код тербанка"
Выходные параметры: сам код, Ид владельца кода, и код клиринга если надо
Возвращает 0 если все хорошо
*/
macro ПолучитьНашКодСУчетомТС(ВидКода, ТерБанк, ИскомыйКод, ИДВладельцаКода, КодКлиринга)
   var stat = 1;
   var rs:object;
   var select:string;
   var params:TArray;

   select = "select dp.PartyID, dp.Swift, dp.Clirin from "+
   "(select dp_dep.t_Code Code, dp_dep.t_ParentCode ParentCode, dp_dep.t_PartyID PartyID, "+
   " objcode1.t_Code Swift, objcode2.t_Code Clirin "+
   " from ddp_dep_dbt dp_dep, dobjcode_dbt objcode1, dobjcode_dbt objcode2 "+
   " where objcode1.t_ObjectID(+) = dp_dep.t_PartyID and objcode1.t_CodeKind(+) = :KindCode"+
   " and objcode1.t_State(+) = 0 and objcode1.t_ObjectType(+) = 3 "+
   " and objcode2.t_ObjectID(+) = dp_dep.t_PartyID and objcode2.t_CodeKind(+) = :PTCK_CLIRING"+ 
   " and objcode2.t_State(+) = 0 and objcode2.t_ObjectType(+) = 3 ) dp "+
   " start with dp.Code = :OperD"+ 
   " connect by dp.Code = prior dp.ParentCode"
   " order by level"; 
   params = makeArray( SQLParam("KindCode", ВидКода),
                       SQLParam("PTCK_CLIRING", PTCK_CLIRING), 
                       SQLParam("OperD", {OperDprt}));
   rs = execSQLselect( select, params, FALSE );
   while(rs.MoveNext())
      if(rs.Value(1))
         if(not(ТерБанк) or (not(CompareStrWithMasks("??00000000", rs.Value(2)))))
             SetParm(2, rs.Value(1));
             SetParm(3, rs.Value(0));
             if(GetParm(4, КодКлиринга ))
                 SetParm(4, rs.Value(2));
             end;
             return 0;
         end;
      end;
   end;
   return stat;
end;

macro НайтиТерБанк(BankID)
   var TerBankID = -1, error, Code;
   Code = ПолучитьКодСубъекта(BankID, PTCK_CLIRING, error);
   if(error)
       return TerBankID;
   end;
   TerBankID = ПолучитьКодСубъекта(substr(Code, 1, 2) + "00000000", PTCK_CLIRING, error);
   if(error)
       return -1;
   end;
   return TerBankID;
end;


macro ОпределитьПолучателяСообщения(wlmes, КодКлирингаОтправителя, ТерБанк)
   var ID, OwnerCode, error = 0, StrID, RetCode = "", КодКлирингаПолучателя;
   var rs:object;
   var select:string;
   var params:TArray;

   ID = wlmes.OutsideAbonentID;
   if((ТерБанк != {OurBank}) and ((wlmes.Kind == MESKIND_REQUEST) 
      or (wlmes.kind == MESKIND_ANSWER)))
      ID = ТерБанк;
   else
      if((wlmes.Kind == MESKIND_REQUEST) or (wlmes.kind == MESKIND_ANSWER))
          select = "select req.t_RecipientID from dwlreq_dbt req, dwlmeslnk_dbt lnk"+
          " where lnk.t_MesID =:MesID and lnk.t_ObjKind =:Kind and req.t_ReqID = lnk.t_ObjID";
          params = makeArray( SQLParam("MesID", wlmes.MesID ),
                              SQLParam("Kind", wlmes.Kind));
          rs = execSQLselect( select, params, FALSE );
          if(rs.MoveNext())
              ID = rs.Value(0);
          end;
      end;
      if(wlmes.Kind == MESKIND_NOTICE)
          select = "select head.t_RecipientID from dwlhead_dbt head, dwlmeslnk_dbt lnk"+
          " where lnk.t_MesID = :MesID and lnk.t_ObjKind = :Kind and head.t_HeadID = lnk.t_ObjID";
          params = makeArray( SQLParam("MesID", wlmes.MesID ),
                              SQLParam("Kind", wlmes.Kind));
          rs = execSQLselect( select, params, FALSE );
          if(rs.MoveNext())
              ID = rs.Value(0);
          end;
      end;
      if(wlmes.Kind == MESKIND_PAYMENT)
          select = "select pm.t_ReceiverBankID from dwlpm_dbt wlpm, dwlmeslnk_dbt lnk, dpmpaym_dbt pm "+
          " where lnk.t_MesID =:MesID and lnk.t_ObjKind =:Kind and wlpm.t_WlPmID = lnk.t_ObjID and wlpm.t_PaymentID = pm.t_PaymentID";
          params = makeArray( SQLParam("MesID", wlmes.MesID ),
                    SQLParam("Kind", wlmes.Kind));
          rs = execSQLselect( select, params, FALSE );
          if(rs.MoveNext())
              ID = rs.Value(0);
          end;
      end;
   end;
   //Проверить банк на закрытость
   select = "select party.T_PartyID from dparty_dbt party, dbankdprt_dbt bank"+
            " where party.t_PartyID = bank.t_PartyID and party.T_PartyID = :ID"+
            " and (party.t_Locked = 'X' or bank.t_Lock = 'X')";
   params = makeArray( SQLParam("ID", ID));
   rs = execSQLselect( select, params, FALSE );

   if(rs.MoveNext())
       //Надо искать правоприемника
       error = GetLinkedObject (OBJROLE_PARTY_ASSIGNEE, OBJTYPE_PARTY, strsubst(string(ID:10)," ","0"), OBJTYPE_PARTY, StrID);
       if(error)
           return RetCode;
       end;
       ID = int(StrID);
   end;
   if(not IsBankType(ID, PT_KIND_SAVINGS_BANK))
       RetCode = "0000";
   else
      КодКлирингаПолучателя = ПолучитьКодСубъекта(ID, PTCK_CLIRING, error, 1);
      if(error)
          RetCode = "0000";
      else
          if(SubStr(КодКлирингаОтправителя, 1, 2) == SubStr(КодКлирингаПолучателя, 1, 2))
             RetCode = ПолучитьКодСубъекта(ID, PTCK_SMFR, error);
          else
             RetCode = "0000";
          end;
      end;
   end;
   return RetCode;
end;
