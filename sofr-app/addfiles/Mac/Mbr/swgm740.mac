// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание    : Генерация сообщения по SWIFT MT740
//
//  Программист : Чукина Т.А.
//
//  Создан      : 26.07.2013
//
// --------------------------------------------------------------------

import "swgenmes.mac", LCInter;

macro GenMes( addrMes, addrLcdoc, addrOldMes )

  var Recreate = false;
  Record oldMes(wlmes);
  Record wllcdoc(lcdoc);

  SetBuff( wlmes,   addrMes );
  SetBuff( wllcdoc, addrLcdoc );
  
  if( ValType(AddrOldMes) == V_MEMADDR )
    SetBuff( oldmes, addrOldMes );
    Recreate = true;
  end;

  PrintLog(2, "Генерация сообщения по МТ740");
  var LcObj : RsbLetterOfCredit = RsbLetterOfCredit( wllcdoc.LcdocID ),
      Lcparty : TRecHandler = TRecHandler("lcparty"),
      field_value : string = "", Tag = "";

  // 20 - номер транзакции (документа)
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wllcdoc.Number );

  // 25 - счет
  ЗаписатьПолеЛог( AccountIdentificationField, LcObj.PayerAccount_LCREIMB );

  // 40F - правила
  ЗаписатьПолеЛог( ApplicableRulesField_F, LcObj.Rules_LCREIMB );

  // 31D - дата и место предоставления документов
  ЗаписатьПолеЛог( DateAndPlaceOfExpiryField, GetSWIFTDate(wllcdoc.ValidDate)+wllcdoc.DocsPlace );

  // 58a - негоциирующий банк
  Tag = "";
  field_value = FillBeneficiaryInstitutionField_LC(LcObj, Tag);
  if(field_value)
    ЗаписатьПолеЛогВБлок( "58a", BeneficiaryInstitutionField + Tag, field_value );

    // Восстанавливаем нулевой контекст блока
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;
  end;

  // 59 - бенефициар
  field_value = "";
  if(LcObj.Name_LCREIMB)
    field_value = FillBeneficiaryCustomerField_0( LcObj.Name_LCREIMB );
  end;
  if(field_value)
    ЗаписатьПолеЛог( BeneficiaryCustomerField, field_value );
  end;

  // 32B - код валюты, сумма
  field_value = ПолучитьКодВалютыЛог(LcObj.FIID) + GetSWIFTAmount(LcObj.Amount);
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_B, field_value );

  // 39A - допустимый процент отклонения
  if(LcObj.MisCalc == LCMISCALC_KIND_DEVIATION)
    field_value = string(LcObj.ErrPlus, "/", LcObj.ErrMinus);
    ЗаписатьПолеЛог( PercentageCreditAmountToleranceField, field_value );
  end;

  // 39B - максимальная сумма
  if(LcObj.MisCalc == LCMISCALC_KIND_LIMITS)
    ЗаписатьПолеЛог( MaximumCreditAmountField, "NOT EXCEEDING" );
  end;

  // 39C - покрытие дополнительные суммы
  if(LcObj.AddAmountCond)
    field_value = SwiftStrSplit( LcObj.AddAmountCond, 35, 4 );
    ЗаписатьПолеЛог( AdditionalAmountsCoveredField, field_value );
  end;

  // 41a - Реквизиты подтверждающего банка
  Tag = "";
  field_value = FillAvailableWithByField_LC(LcObj, Tag);
  if(field_value)
    ЗаписатьПолеЛогВБлок( "41a", AvailableWithByField + Tag, field_value );

    // Восстанавливаем нулевой контекст блока
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;
  else
    RunError("Не найден подтверждающий банк");
  end;

  // 42C
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_ACCEPTANCE)
    field_value = SwiftStrSplit( LcObj.DirectBill, 35, 3 );
    ЗаписатьПолеЛог( DraftsAtField, field_value );
  end;

  // 42a - банк-трассат
  Tag = "AD";
  field_value = FillDraweeField_LC(LcObj, Tag);
  if(field_value)
    ЗаписатьПолеЛогВБлок( "42a", DraweeField + Tag, field_value );

    // Восстанавливаем нулевой контекст блока
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;
  end;

  // 42M
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_MIXED_PYMT)
    field_value = SwiftStrSplit( LcObj.MixPayDescr, 35, 4 );
    ЗаписатьПолеЛог( MixedPaymentDetailsField, field_value );
  end;

  // 42P
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_DEF_PAYMENT)
    field_value = SwiftStrSplit( LcObj.DeferPayDescr, 35, 4 );
    ЗаписатьПолеЛог( DeferredPaymentDetailsField, field_value );
  end;

  // 71A - Комиссии
  ЗаписатьПолеЛог( DetailsOfChargesField_A, LcObj.Charges );

  // 71B - Комиссии
  if(LcObj.OtherCharges)
    field_value = SwiftStrSplit( LcObj.OtherCharges, 35, 6 );
    ЗаписатьПолеЛог( DetailsOfChargesField_B, field_value );
  end;

  // 72 - информация для банка-получателя
  if( LcObj.Parties().Find(LCPARTY_KIND_REIMBURSING_BANK, Lcparty) == 0 )
    field_value = SwiftStrSplit(Lcparty.rec.PartyInfo, 35, 6);
    ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
