 /*
 $Name: ufgm108.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения ED108 транспорта УФЭБС
 */

/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED108 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm108.mac                                                  */
/*  Создан:    05.06.12                                Chukina T.           */
/****************************************************************************/

import WldInter, "ufgenmes.mac", "multypm.mac", bnk_common, "pmlib.mac", 
       "pmbudg_lib.mac", pm_const, "uf108taxgm.mac", "pmtax.mac";


const ED108v3 = 448;

private macro CheckCorPZN( Payment )
  var rs;
  var SelectStr = "";
  var ParticipantID = 0;
  
  rs = execSQLSelect( " Select cor.t_CorrID "
                      "   From dcorschem_dbt cor "
                      "  Where cor.t_Number  = :CorNumber "
                      "    and cor.t_FIID = :FIID "
                      "    and cor.t_FI_Kind = :FI_Kind ",
                      MakeArray( SQLParam("CorNumber", Payment.OutCorschem),
                                 SQLParam("FIID", Payment.OutCorschemFIID),
                                 SQLParam("FI_Kind", FIKIND_CURRENCY)));
  if( rs and rs.MoveNext )
    if( not БанкУБР(rs.value("t_CorrID")) )
      return true;
    end;
  end;
  
  if( Payment.DocKind == DLDOC_MULTYPM )
    SelectStr = " Select pm.t_ReceiverBankID "
                "   From dpmlink_dbt lnk, dpmpaym_dbt pm "
                "  Where lnk.t_InitialPayment = :PaymentID "
                "    and pm.t_PaymentID = lnk.t_PurposePayment "
                "    and rownum = 1 ";
  else
    SelectStr = " Select pm.t_ReceiverBankID "
                "   From dpmpaym_dbt pm "
                "  Where pm.t_PaymentID = :PaymentID ";
  end;
  
  rs = execSQLSelect( SelectStr, MakeArray( SQLParam("PaymentID", Payment.PaymentID)));
  
  if( rs and rs.MoveNext() )
    ParticipantID = rs.value("t_ReceiverBankID");
  end;
  
  rs = null;
  rs = execSQLSelect( " Select t_PZN "
                      "   From dbankdprt_dbt dprt "
                      "  Where dprt.t_PartyID = :PartyID ",
                      MakeArray(SQLParam("PartyID", ParticipantID)));
                      
  if( rs and rs.MoveNext() )
    if( rs.value("t_PZN") == "40" )
      return false;
    else
      return true;
    end;
  end;
  
  return true;
  
end;

private macro GetLinkedAccount( PaymentID )
  var rs = execSQLSelect( " Select pm.t_ReceiverAccount "
                          "   From dpmlink_dbt lnk, dpmpaym_dbt pm "
                          "  Where lnk.t_InitialPayment = :PaymentID "
                          "    and pm.t_PaymentID = lnk.t_PurposePayment ",
                          MakeArray( SQLParam("PaymentID", PaymentID) ));
  
  if( rs and rs.MoveNext() )
    return rs.value("t_ReceiverAccount");
  end;
  
  return ""; 
end;

private macro GetTransfAccount( PaymentID )
  var rs = execSQLSelect( " Select bd.t_ReceiverAccount "
                          "   From dbdtransf_dbt bd "
                          "  Where bd.t_CoverPaymentID = :PaymentID ",
                          MakeArray( SQLParam("PaymentID", PaymentID) ));
  
  if( rs and rs.MoveNext() )
    return rs.value("t_ReceiverAccount");
  end;
  
  return ""; 
end;

private macro CheckCurrVal( CurrVal, FirstVal, // данные для сравнения
                            ErrMsg : @string, IsErrFound : @bool, // результаты
                            AddErrMsg : string // сообщение по текущей ошибке
                          )
  if(not IsErrFound)
    if(CurrVal != FirstVal)
      ErrMsg = ErrMsg + AddErrMsg;
      IsErrFound = true;
    end;
  end;
end;

macro CheckPaymED108Requisites( Paym: RsbPayment, err_msg : @string, type : string) : integer
  var MaxPriority = Bnk_GetRegistryValue( "CB\\PAYMENTS\\MAXPRIORITY", V_INTEGER, 0 );

  if( Paym.Priority > MaxPriority )
    err_msg = err_msg + "Недопустимое значение очередности платежа, допускаются значения 1-5 или 0"; 
  end;

  var ReceiverAccount = "";
  
  if( strlen(Paym.ReceiverAccount) > 0 )
    ReceiverAccount = Paym.ReceiverAccount;
  else
    if( Paym.DocKind == DLDOC_MULTYPM )
      ReceiverAccount = GetLinkedAccount(Paym.PaymentID);
    else
      ReceiverAccount = GetTransfAccount(Paym.PaymentID);
    end;
  end;    
 
  if( БанкУБР( Paym.ReceiverBankID ) and (Paym.Date >= date(2, 10, 2017)) and ( CompareStrWithMasks( Bnk_GetRegistryValue( "COMMON\\CHECKTAXPAYMPARAMS\\УФЭБС_СЧЕТ ПОЛУЧАТЕЛЯ", V_STRING, ""), ReceiverAccount ) == 0 ) 
                                     and ( ( Paym.TaxAuthorState == "" ) or ( Paym.TaxAuthorState == null ) ) )
    err_msg = err_msg + "Для банка получателя, являющегося ПБР и счета получателя на БС 40101 статус составителя должен быть обязательно заполнен"; 
  end;

  var rlsFormID : integer = 0;
  if( IsDebetPaym( Paym ) )
    rlsFormID = Paym.GetDEBET().rec.RlsFormID;
  else
    rlsFormID = Paym.GetCREDIT().rec.RlsFormID;
  end;
  if( InList( rlsFormID, ED108v3 ) )
    var PaymentPrecedence = Paym.GetPMRMPROP().rec.Precedence;
    var PaymentKind = Paym.PaymentKind;
    if( not CheckPaymentPrecedence( PaymentPrecedence, PaymentKind ) )
      err_msg = "Некорректное значение приоритета распоряжения. Для срочного сервиса допустимы значения \"13\" или в диапазонах \"50-59\", \"60-69\". Для несрочного сервиса только значения в диапазоне \"70-79\""; 
    end;
  end;
  
  if( not CheckCorPZN(Paym) )
    err_msg = "В адрес ПУ (клиента ПУ) не может быть направлено ЭПС " + "ED108";
  end;

  if(err_msg)
    return 1;
  end;
  
  return 0;
end;

private macro CheckMesNotMulty(MultiPaym : RsbPayment, IsUBR : bool, ErrMsg : @string) : bool
  ErrMsg = "";
  var q = "SELECT 1 FROM dbdtransf_dbt  " +
          " WHERE t_CoverPaymentID  = :MPPaymentID ";

  var params : TArray = makeArray( SQLParam("MPPaymentID", MultiPaym.PaymentID) );
  var rs : RsdRecordset = execSQLselect(q, params);
  var IsErrReceiverAccount = false;
  var ReceiverAccount = "";

  if( rs and ( not rs.moveNext() ) )
    ErrMsg = ErrMsg + "Недостаточно данных для заполнения сообщения по форме ED108. У платежа должны быть связанные платежи или записи о переводах физических лиц";
  end;

  if(IsUBR)
    var q1 = " SELECT bdtr.t_ReceiverAccount " +
             "  FROM dbdtransf_dbt bdtr " +
             "  WHERE bdtr.t_CoverPaymentID = :MPPaymentID " ;

    params = makeArray( SQLParam( "MPPaymentID", MultiPaym.PaymentID ) );
    rs = execSQLselect(q, params);

    if(rs and rs.moveNext())
      ReceiverAccount = rs.value("t_ReceiverAccount");

      while(rs.moveNext())
        CheckCurrVal( rs.value("t_ReceiverAccount"), ReceiverAccount, @ErrMsg, @IsErrReceiverAccount,
                      "В платежном поручении на общую сумму с реестром, направляемом клиенту Банка России, не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя или переформируйте платеж по переводам физических лиц" );
      end;

      if( (not IsErrReceiverAccount) and MultiPaym.ReceiverAccount )
       CheckCurrVal( MultiPaym.ReceiverAccount, ReceiverAccount, @ErrMsg, @IsErrReceiverAccount,
                        "В платежном поручении на общую сумму с реестром, направляемом клиенту Банка России, не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя или переформируйте платеж по переводам физических лиц" );
      end;
    end;

  end;

  if(ErrMsg)
    return FALSE;
  end;
  return TRUE;
end;

private macro CheckMes(MultiPaym : RsbPayment, IsUBR : bool, ErrMsg : @string) : bool
  ErrMsg = "";
  var IsOK : bool = TRUE;

  var CountMultyPM = GetPaymCountIncludedInMultyPM( MultiPaym.PaymentID );
  if(CountMultyPM == 0)
    ErrMsg = ErrMsg + "Недостаточно данных для заполнения сообщения по форме ED108. Добавьте в сводный платеж хотя бы один документ. ";
    IsOK = FALSE;
  elif(CountMultyPM > 10000)
    ErrMsg = ErrMsg + "Количество записей в реестре сообщения ED108 не должно превышать 10000. Исключите лишние платежи из состава сводного. ";
    IsOK = FALSE;
  end;

  var NeedCheckReceiverAccount : bool = (IsUBR or (MultiPaym.PayType > 0));

  var q = "SELECT DISTINCT rm.t_Priority, paym.t_ReceiverBankID, rm.t_ShifrOper, " +
          "                paym.t_PayType ";
  if(NeedCheckReceiverAccount)
    q = q + "            , paym.t_ReceiverAccount ";
  end;
  q = q + "  FROM dpmlink_dbt lnk, dpmpaym_dbt paym, dpmrmprop_dbt rm " +
          " WHERE lnk.t_InitialPayment = :MPPaymentID " +
          "   AND lnk.t_LinkKind       = :LinkKind " +
          "   AND paym.t_PaymentID     = lnk.t_PurposePayment " +
          "   AND rm.t_PaymentID       = lnk.t_PurposePayment ";

  var params : TArray = makeArray( SQLParam("MPPaymentID", MultiPaym.PaymentID),
                                   SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );
  var rs : RsdRecordset = execSQLselect(q, params);

  if(rs and rs.moveNext())
    var Priority = rs.value("t_Priority"), IsErrPriority = false,
        ReceiverBankID = rs.value("t_ReceiverBankID"), IsErrReceiverBankID  = false,
        ShifrOper      = rs.value("t_ShifrOper"),      IsErrShifrOper       = false,
        PayType        = rs.value("t_PayType"),
        ReceiverAccount = "",                          IsErrReceiverAccount = false;

    if(NeedCheckReceiverAccount)
      ReceiverAccount = rs.value("t_ReceiverAccount");
    end;

    while(rs.moveNext())
      CheckCurrVal( rs.value("t_Priority"), Priority, @ErrMsg, @IsErrPriority,
                   "В платежное поручение на общую сумму с реестром включаются платежи одной очередности. Оставьте в составе сводного только платежи с одинаковой очередностью. "
                  );
      CheckCurrVal( rs.value("t_ReceiverBankID"), ReceiverBankID, @ErrMsg, @IsErrReceiverBankID,
                   "В платежное поручение на общую сумму с реестром включаются платежи в один банк получателя. Оставьте в составе сводного только платежи в один банк получателя. "
                  );
      CheckCurrVal( rs.value("t_ShifrOper"), ShifrOper, @ErrMsg, @IsErrShifrOper,
                   "Платежное поручение на общую сумму с реестром не должно содержать документы с шифром операции отличным от \"01\". Оставьте в составе сводного только платежи с шифром = \"01\". "
                  );

      if(NeedCheckReceiverAccount)
        CheckCurrVal( rs.value("t_ReceiverAccount"), ReceiverAccount, @ErrMsg, @IsErrReceiverAccount,
                      IfThenElse( IsUBR, "В платежном поручении на общую сумму с реестром, направляемом клиенту Банка России, не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя. ",
                                         "В распоряжении о переводе средств в бюджет РФ не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя или переформируйте платеж по переводам физических лиц"
                                )
                    );
      end;

    end;

    if((not IsErrReceiverAccount) and (MultiPaym.ReceiverAccount != "") )
      CheckCurrVal( MultiPaym.ReceiverAccount, ReceiverAccount, @ErrMsg, @IsErrReceiverAccount,
                      IfThenElse( IsUBR, "В платежном поручении на общую сумму с реестром, направляемом клиенту Банка России, не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя. ",
                                         "В распоряжении о переводе средств в бюджет РФ не допускается указание нескольких счетов получателей. Оставьте в составе сводного только платежи с одинаковым счетом получателя или переформируйте платеж по переводам физических лиц"
                                )
                    );
    end;

    if( IsErrPriority or IsErrReceiverBankID or IsErrShifrOper or 
        IsErrReceiverAccount )
      IsOK = FALSE;
    end;

  end;

  return IsOK;
end;

private macro GetFirstPaymData
( MPPaymentID : integer,
  Priority : @integer,
  PayerINN : @string, PayerName : @string, PayerAccount : @string, 
  ReceiverINN : @string, ReceiverName : @string, ReceiverAccount : @string, 
  PayerChargeOffDate : @date, PayType : @integer
)
  var q = "SELECT rm.t_Priority, cr.t_BankCode, rm.t_ReceiverCorrAccNostro, " +
          "       rm.t_PayerINN, rm.t_PayerName, pm.t_PayerAccount,         " +
          "       rm.t_ReceiverINN, rm.t_ReceiverName, pm.t_ReceiverAccount," +
          "       rm.t_PayerChargeOffDate, pm.t_PayType                    " +
          "  FROM dpmpaym_dbt pm, dpmrmprop_dbt rm, dpmprop_dbt cr          " +
          " WHERE pm.t_PaymentID = (select lnk.t_PurposePayment             " +
          "                           from dpmlink_dbt lnk                  " +
          "                          where lnk.t_InitialPayment = :PaymentID" +
          "                            and lnk.t_LinkKind = :LinkKind       " +
          "                            and rownum = 1                       " +
          "                        )                                        " +
          "   AND rm.t_PaymentID = pm.t_PaymentID                           " +
          "   AND cr.t_PaymentID = pm.t_PaymentID                           " +
          "   AND cr.t_DebetCredit = :DebetCredit                           ";

  var params : TArray = makeArray( SQLParam("PaymentID", MPPaymentID),
                                   SQLParam("LinkKind",  PMLINK_KIND_MULTYPM),
                                   SQLParam("DebetCredit", PRT_Credit) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    Priority = rs.value("t_Priority");
    PayerINN = rs.value("t_PayerINN");
    PayerName = rs.value("t_PayerName");
    PayerAccount = rs.value("t_PayerAccount");
    ReceiverINN = rs.value("t_ReceiverINN");
    ReceiverName = rs.value("t_ReceiverName");
    ReceiverAccount = rs.value("t_ReceiverAccount");
    PayerChargeOffDate = rs.value("t_PayerChargeOffDate");
    PayType = rs.value("t_PayType");
  end;
end;

private macro GetReceiverBankFromPayment
( MPPaymentID : integer,
  CrBankCode : @string, 
  ReceiverCorrAccNostro : @string
)
  var q = "SELECT cr.t_BankCode, rm.t_ReceiverCorrAccNostro " +
          "  FROM dpmpaym_dbt pm, dpmrmprop_dbt rm, dpmprop_dbt cr          " +
          " WHERE pm.t_PaymentID = (select lnk.t_PurposePayment             " +
          "                           from dpmlink_dbt lnk                  " +
          "                          where lnk.t_InitialPayment = :PaymentID" +
          "                            and lnk.t_LinkKind = :LinkKind       " +
          "                            and rownum = 1                       " +
          "                        )                                        " +
          "   AND rm.t_PaymentID = pm.t_PaymentID                           " +
          "   AND cr.t_PaymentID = pm.t_PaymentID                           " +
          "   AND cr.t_DebetCredit = :DebetCredit                           " +
          " ORDER BY cr.t_BankCode DESC, rm.t_ReceiverCorrAccNostro DESC";

  var params : TArray = makeArray( SQLParam("PaymentID", MPPaymentID),
                                   SQLParam("LinkKind",  PMLINK_KIND_MULTYPM),
                                   SQLParam("DebetCredit", PRT_Credit) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    CrBankCode = rs.value("t_BankCode");
    ReceiverCorrAccNostro = rs.value("t_ReceiverCorrAccNostro");    
  end;
end;


private macro GetFirstTransferData
( MPPaymentID : integer,
  PayerINN : @string, PayerName : @string, PayerAccount : @string, 
  ReceiverINN : @string, ReceiverName : @string, ReceiverAccount : @string 
)
  var q = "SELECT bdtr.t_PayerINN, bdtr.t_PayerName, bdtr.t_PayerAccount,         " +
          "       bdtr.t_ReceiverINN, bdtr.t_ReceiverName, bdtr.t_ReceiverAccount " +
          "  FROM dbdtransf_dbt bdtr          " +
          " WHERE bdtr.t_CoverPaymentID =  :PaymentID "
          "   AND rownum = 1 ";

  var params : TArray = makeArray( SQLParam("PaymentID", MPPaymentID) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    PayerINN = rs.value("t_PayerINN");
    PayerName = rs.value("t_PayerName");
    PayerAccount = rs.value("t_PayerAccount");
    ReceiverINN = rs.value("t_ReceiverINN");
    ReceiverName = rs.value("t_ReceiverName");
    ReceiverAccount = rs.value("t_ReceiverAccount");
  end;
end;


private class TPmAttrChkEq
(
  p_FirstPayerINN : string, p_FirstPayerName : string, p_FirstPayerAccount : string, 
  p_FirstReceiverINN : string, p_FirstReceiverName : string, p_FirstReceiverAccount : string
)
  private var
    FirstPayerINN : string = p_FirstPayerINN, 
    FirstPayerName : string = p_FirstPayerName, 
    FirstPayerAccount : string = p_FirstPayerAccount, 
    FirstReceiverINN : string = p_FirstReceiverINN, 
    FirstReceiverName : string = p_FirstReceiverName, 
    FirstReceiverAccount : string = p_FirstReceiverAccount;

  var IsEqualPayer : bool, 
      IsEqualReceiver : bool,
      IsEqualPayerKPP : bool,
      EqualPayerKPP : string = "", // заполненный КПП, если он совпадает во всех включенных платежах
      IsEqualReceiverKPP : bool,
      EqualReceiverKPP : string = ""; // заполненный КПП, если он совпадает во всех включенных платежах

  private macro SetOnEqFlags
    IsEqualPayer = IsEqualReceiver = IsEqualPayerKPP = IsEqualReceiverKPP = true;
  end;

  private macro GetAttrRs(SumPaymentID : integer) : RsdRecordset
    PureVirtualFunc();
  end;

  private macro CheckEqFlags(rs : RsdRecordset)
    var KPP : string = "";

    if(IsEqualPayer)
      if( ( RemoveKPP(rs.value("t_PayerINN")) != RemoveKPP(FirstPayerINN) ) or
          (rs.value("t_PayerName") != FirstPayerName) or
          (rs.value("t_PayerAccount") != FirstPayerAccount) 
        )
        IsEqualPayer = false;
      else // при совпадении реквизитов плательщика дополнительно проверяем КПП
        if(IsEqualPayerKPP)
          KPP = RemoveINN(rs.value("t_PayerINN"));
          if(KPP and EqualPayerKPP and (KPP != EqualPayerKPP))
            IsEqualPayerKPP = false;
            EqualPayerKPP = "";
          elif(KPP and not EqualPayerKPP)
            EqualPayerKPP = KPP;
          end;
        end;
      end;
    end;

    if(IsEqualReceiver)
      if( ( RemoveKPP(rs.value("t_ReceiverINN")) != RemoveKPP(FirstReceiverINN) ) or
          (rs.value("t_ReceiverName") != FirstReceiverName) or
          (rs.value("t_ReceiverAccount") != FirstReceiverAccount) 
        )
        IsEqualReceiver = false;
      else // при совпадении реквизитов получателя дополнительно проверяем КПП
        if(IsEqualReceiverKPP)
          KPP = RemoveINN(rs.value("t_ReceiverINN"));
          if(KPP and EqualReceiverKPP and (KPP != EqualReceiverKPP))
            IsEqualReceiverKPP = false;
            EqualReceiverKPP = "";
          elif(KPP and not EqualReceiverKPP)
            EqualReceiverKPP = KPP;
          end;
        end;
      end;
    end;

  end;

  private macro IsEverythingDifferent : bool
    if( not IsEqualPayer and not IsEqualReceiver )
      return true;
    end;

    return false;
  end;

  // проверить равенство атрибутов платежей в составе общего платежа
  macro CheckEqualPmAttributes
  ( 
    SumPaymentID : integer // ID общего платежа (сводного или платежа по переводам)
  )
    SetOnEqFlags(); // пока различий не нашли

    var rs : RsdRecordset = GetAttrRs(SumPaymentID);

    while(rs and rs.moveNext())
      // смотрим, есть ли различия
      CheckEqFlags(rs);

      // если все отличия уже найдены, прекращаем проверку
      if( IsEverythingDifferent() )
        break;
      end;
    end;
  end;

end;

private class (TPmAttrChkEq) TMultiPmAttrChkEq
(
  p_FirstPayerINN : string, p_FirstPayerName : string, p_FirstPayerAccount : string, 
  p_FirstReceiverINN : string, p_FirstReceiverName : string, p_FirstReceiverAccount : string,
  p_FirstPayerChargeOffDate : date
)
  InitTPmAttrChkEq( p_FirstPayerINN, p_FirstPayerName, p_FirstPayerAccount,
                    p_FirstReceiverINN, p_FirstReceiverName, p_FirstReceiverAccount );
  private var
    FirstPayerChargeOffDate : date = p_FirstPayerChargeOffDate;

  var IsEqualChargeOffDate : bool;

  private macro SetOnEqFlags
    SetOnEqFlags(); // вызов метода родительского класса

    // взводим свои дополнительные флаги
    IsEqualChargeOffDate = true;
  end;

  private macro GetAttrRs(SumPaymentID : integer) : RsdRecordset
    var q : string = 
          "SELECT rm.t_PayerINN, rm.t_PayerName, pm.t_PayerAccount,         " +
          "       rm.t_ReceiverINN, rm.t_ReceiverName, pm.t_ReceiverAccount," +
          "       rm.t_PayerChargeOffDate                                   " +
          "  FROM dpmlink_dbt lnk, dpmpaym_dbt pm, dpmrmprop_dbt rm         " +
          " WHERE lnk.t_InitialPayment = :MPPaymentID                       " +
          "   AND lnk.t_LinkKind       = :LinkKind                          " +
          "   AND pm.t_PaymentID       = lnk.t_PurposePayment               " +
          "   AND rm.t_PaymentID       = lnk.t_PurposePayment               ";

    var params : TArray = makeArray( SQLParam("MPPaymentID", SumPaymentID),
                                     SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );

    var rs : RsdRecordset = execSQLselect(q, params);

    return rs;
  end;

  private macro CheckEqFlags(rs : RsdRecordset)
    CheckEqFlags(rs); // вызов метода родительского класса

    // проверяем свои дополнительные флаги
    if(IsEqualChargeOffDate)
      if( rs.value("t_PayerChargeOffDate") != FirstPayerChargeOffDate )
        IsEqualChargeOffDate = false;
      end;
    end;
  end;

  private macro IsEverythingDifferent : bool
    if( IsEverythingDifferent() // вызов метода родительского класса
        and 
        not IsEqualChargeOffDate )
      return true;
    end;

    return false;
  end;

end;

private class (TPmAttrChkEq) TBdtrPmAttrChkEq
(
  p_FirstPayerINN : string, p_FirstPayerName : string, p_FirstPayerAccount : string, 
  p_FirstReceiverINN : string, p_FirstReceiverName : string, p_FirstReceiverAccount : string
)
  InitTPmAttrChkEq( p_FirstPayerINN, p_FirstPayerName, p_FirstPayerAccount,
                    p_FirstReceiverINN, p_FirstReceiverName, p_FirstReceiverAccount );

  private macro GetAttrRs(SumPaymentID : integer) : RsdRecordset
    var q : string = 
          "SELECT bdtr.t_PayerINN, bdtr.t_PayerName, bdtr.t_PayerAccount,         " +
          "       bdtr.t_ReceiverINN, bdtr.t_ReceiverName, bdtr.t_ReceiverAccount " +
          "  FROM dbdtransf_dbt bdtr         " +
          " WHERE bdtr.t_CoverPaymentID = :CoverPaymentID                       ";

    var params : TArray = makeArray( SQLParam("CoverPaymentID", SumPaymentID) );

    var rs : RsdRecordset = execSQLselect(q, params);

    return rs;
  end;

end;


private macro GetNotEmptyRmFld(RsPaym : RsbPayment, RmFld : string) : string
  var FldValue : string = "";

  var q : string = "", params : TArray;
  if( RsPaym.DocKind == DLDOC_MULTYPM )
    q = "SELECT rm.t_" + RmFld                           +
        "  FROM dpmlink_dbt lnk, dpmrmprop_dbt rm      " +
        " WHERE lnk.t_InitialPayment = :MPPaymentID    " +
        "   AND lnk.t_LinkKind       = :LinkKind       " +
        "   AND rm.t_PaymentID = lnk.t_PurposePayment  " +
        "   AND rm.t_" + RmFld + " <> chr(1)           " +
        "   AND rm.t_" + RmFld + " IS NOT NULL         " + 
        "   AND rownum = 1                             ";

    params = makeArray( SQLParam("MPPaymentID",  RsPaym.PaymentID),
                        SQLParam("LinkKind",     PMLINK_KIND_MULTYPM) );
  else
    q = "SELECT bdtr.t_" + RmFld                           +
        "  FROM dbdtransf_dbt bdtr      " +
        " WHERE bdtr.t_CoverPaymentID = :CoverPaymentID    " +
        "   AND bdtr.t_" + RmFld + " <> chr(1)           " +
        "   AND bdtr.t_" + RmFld + " IS NOT NULL         " + 
        "   AND rownum = 1                             ";

    params = makeArray( SQLParam("CoverPaymentID",  RsPaym.PaymentID) );
  end;

  var rs : RsdRecordset = execSQLselect(q, params);
  if(rs and rs.moveNext())
    FldValue = rs.value("t_" + RmFld);
  end;

  return FldValue;
end;

private macro GetPayerBICCorrespAcc( RsPaym : RsbPayment, 
                                     PayerBIC : @string, 
                                     PayerCorrespAcc : @string )
                                     
  var q = "SELECT dep.t_PartyID, nvl(bankdprt.t_Coracc, chr(1)) Coracc " +
          "  FROM dcorschem_dbt cors, ddp_dep_dbt dep, dbankdprt_dbt bankdprt " +
          " WHERE cors.t_Number      = :CorsNumber " +
          "   AND cors.t_FI_Kind     = :FI_Kind " +
          "   AND cors.t_FIID        = :FIID " +
          "   AND dep.t_Code         = cors.t_Department " +
          "   AND bankdprt.t_PartyID(+) = dep.t_PartyID ";

  var params : TArray = makeArray( SQLParam("CorsNumber", RsPaym.OutCorschem),
                                   SQLParam("FI_Kind",    FIKIND_CURRENCY),
                                   SQLParam("FIID",       RsPaym.OutCorschemFIID) );

  var rs : RsdRecordset = execSQLselect(q, params);

  if(rs and rs.moveNext())
    var error = 0;
    PayerBIC = ПолучитьКодСубъекта(rs.value("t_PartyID"), PTCK_BIC, error, 1);
    PayerCorrespAcc = rs.value("Coracc");
  else
    RunError("Не удалось определить реквизиты банка плательщика");
  end;
end;

private macro FillSide( elem : object, Account : string, FullINN : string, Name : string, 
                        BankBIC : string, BankCorrespAcc : string, xml : object,
                        IsZeroFill : bool )
  if(Account)
    elem.setAttribute("PersonalAcc", Account);
  end;

  var INN : string = Str0( RemoveKPP(FullINN), IsZeroFill ),
      KPP : string = Str0( RemoveINN(FullINN), IsZeroFill );
  if(INN)
    elem.setAttribute("INN", INN);
  end;
  if(KPP)
    elem.setAttribute("KPP", KPP);
  end;

  var subelem = xml.createElement("Name");
  subelem.appendChild( xml.createTextNode( TransTextField(substr(Name, 1, 160)) ) );
  elem.appendChild(subelem);

  subelem = xml.createElement("Bank");
  subelem.setAttribute("BIC", BankBIC);
  if(BankCorrespAcc)
    subelem.setAttribute("CorrespAcc", BankCorrespAcc);
  end;
  subElem.appendChild(xml.createTextNode(""));
  elem.appendChild(subelem);
end;

private macro FillSideInReestr( mes : object, IsSideInReestr : bool, ElementName : string,
                                PersonalID : string, Acc : string, FullINN : string, 
                                Name : string, Address : string, xml, IsZeroFill : bool )

  if(IsSideInReestr or PersonalID or Address) // если есть что записать в TransactionPayerInfo/TransactionPayeeInfo
    var elem = xml.createElement(ElementName);
    var subelem : object = null;
    if(PersonalID)
      elem.setAttribute("PersonalID", PersonalID);
    end;

    if(IsSideInReestr)
      if(Acc)
        elem.setAttribute("Acc", Acc);
      end;

      var INN : string = Str0( RemoveKPP(FullINN), IsZeroFill );
      if(INN)
        elem.setAttribute("INN", INN);
      end;

      if( (FullINN == "") or (strlen(FullINN) == 12) )
        var ind1 : integer = index(Name, "//"); // обрезаем адрес в наименовании
        if(ind1)
          Name = substr(Name, 1, ind1 - 1);
        end;
        subelem = xml.createElement("PersonName");
        subelem.appendChild( xml.createTextNode( TransTextField(substr(Name, 1, 70)) ) );
        elem.appendChild(subelem);
      else
        subelem = xml.createElement("TradeName");
        subelem.appendChild( xml.createTextNode( TransTextField(substr(Name, 1, 140)) ) );
        elem.appendChild(subelem);
      end;
    end;

    if(Address)
      subelem = xml.createElement("PersonAddress");
      subelem.appendChild( xml.createTextNode( TransTextField(substr(Address, 1, 70)) ) );
      elem.appendChild(subelem);
    end;

    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;
end;

// Получить счет плательщика из проводки по корсчету
private macro GetPayerAccountFromCorAccCarry(RsPaym : RsbPayment) : string
  var PayerAccount : string = "";

  var q : string = 
        " select acctrn.t_Account_Payer " +
        "  from dacctrn_dbt acctrn, dcorschem_dbt cors, dpmdocs_dbt pmdocs " +
        "  where cors.t_Number      = :CorsNumber " +
        "   and cors.t_FI_Kind     = :FI_Kind " +
        "   and cors.t_FIID        = :FIID " +
        "   and acctrn.t_Account_Receiver = cors.t_Account " +
        "   and pmdocs.t_AccTrnID = acctrn.t_AccTrnID " +
        "   and pmdocs.t_PaymentID = :PaymentID ";

  var params : TArray = makeArray
    ( SQLParam("CorsNumber", RsPaym.OutCorschem),
      SQLParam("FI_Kind",    FIKIND_CURRENCY),
      SQLParam("FIID",       RsPaym.OutCorschemFIID),
      SQLParam("PaymentID",  RsPaym.PaymentID) );

  var rs : RsdRecordset = execSQLselect(q, params);

  if(rs and rs.moveNext())
    PayerAccount = rs.Value(0);
  end;

  return PayerAccount;
end;

private macro GetPayerAccountAndNameForTransferPaym
( RsPaym : RsbPayment, 
  PayerAccount : @string, 
  PayerName : @string
)
  PayerAccount = "";
  PayerName = RsPaym.PayerName;

  var q : string = "SELECT 1 " +
        "  FROM dcorschem_dbt cors, ddp_dep_dbt dep, dpartcode_dbt ptcode " +
        " WHERE cors.t_Number      = :CorsNumber " +
        "   AND cors.t_FI_Kind     = :FI_Kind " +
        "   AND cors.t_FIID        = :FIID " +
        "   AND dep.t_Code         = cors.t_Department " +
        "   AND ptcode.t_PartyID   = dep.t_PartyID " +
        "   AND ptcode.t_CodeKind  = :BIC " +
        "   AND ptcode.t_Code     != :CODE ";

  var params : TArray = makeArray
    ( SQLParam("CorsNumber", RsPaym.OutCorschem),
      SQLParam("FI_Kind",    FIKIND_CURRENCY),
      SQLParam("FIID",       RsPaym.OutCorschemFIID),
      SQLParam("BIC",        PTCK_BIC),
      SQLParam("CODE",       RsPaym.PayerBankCode) );

  var rs : RsdRecordset = execSQLselect(q, params);

  if(rs and rs.moveNext())
    PayerAccount = RsPaym.PayerAccount;

    if(PayerAccount)
      PayerName = PayerName + " сч. " + PayerAccount;
    end;
    PayerName = PayerName + ", " + RsPaym.PayerBankName ;
  else
    PayerAccount = GetPayerAccountFromCorAccCarry(RsPaym);
  end;

end;

private macro GetAddress(FullINN : string, Name : string) : string
  var Address = "";

  if( (FullINN == "") or (strlen(FullINN) == 12) )
    Address = GetAddressFromPaymSideName(Name);
  end;

  return Address;
end;

private macro WriteTransactionBeneficiaryInfo
( xml : object, mes : object,
  OtherPayerFIO : string,
  OtherPayerName : string,
  OtherPayerINN : string,
  OtherPayerAddress : string,
  OtherTaxPersonRef : string
)
  var elem : object = xml.createElement("TransactionBeneficiaryInfo");

  macro setAttrNotEmpty(AttrName : string, Attr : string)
    if(Attr)
      elem.setAttribute( AttrName, Attr );
    end;
  end;

  macro setTextNode(NodeName : string, NodeText : string, TextLen : integer)
    if(NodeText)
      var subElem : object = xml.createElement(NodeName);

      subElem.appendChild( xml.createTextNode( TransTextField(substr(NodeText, 1, TextLen)) ) );
      elem.appendChild(subElem);
    end;
  end;

// Begin
  if( OtherPayerFIO or OtherPayerName or OtherPayerINN or
      OtherPayerAddress or OtherTaxPersonRef 
    )
    setAttrNotEmpty( "PersonalID", OtherTaxPersonRef );
    setAttrNotEmpty( "INN", OtherPayerINN );
      
    setTextNode("PersonName", OtherPayerFIO, 70);
    setTextNode("PersonAddress", OtherPayerAddress, 70);
    setTextNode("TradeName", OtherPayerName, 140);

    mes.appendChild(elem);
  end;
end;

private macro WriteReestrForMultyPm
( RsPaym : RsbPayment, 
  IsPayerInReestr : bool, 
  IsReceiverInReestr : bool, 
  TaxInfo : TTaxInfoED108,
  ReqSettlementDate : date
)

  var xml:object = ActiveX("Microsoft.XMLDOM");
  
  RECORD pmpaym(pmpaym);
  var TransactionID = 0, strPaymentID = "", note = "", note_date = date(0, 0, 0), address = "", note2 = "";
  var q : string = "SELECT paym.t_PaymentID, rm.t_Number, rm.t_Date, paym.t_BaseAmount, " +
      "       paym.t_PayerAccount, rm.t_PayerINN, rm.t_PayerName, " +
      "       paym.t_ReceiverAccount, rm.t_ReceiverINN, rm.t_ReceiverName, " +
      "       rm.t_Ground, rm.t_UIN, rm.t_TaxAuthorState, rm.t_TaxPmGround, " +
      "       rm.t_TaxPmPeriod, rm.t_TaxPmNumber, rm.t_TaxPmDate, rm.t_TaxPmType, " +
      "       paym.t_PayType " +
      "  FROM dpmlink_dbt lnk, dpmpaym_dbt paym, dpmrmprop_dbt rm " +
      " WHERE lnk.t_InitialPayment = :MPPaymentID " +
      "   AND lnk.t_LinkKind       = :LinkKind " +
      "   AND paym.t_PaymentID     = lnk.t_PurposePayment " +
      "   AND rm.t_PaymentID       = lnk.t_PurposePayment ";

  var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID),
                                 SQLParam("LinkKind",    PMLINK_KIND_MULTYPM) );
  var rs : RsdRecordset = execSQLselect(q, params);

  while(rs and rs.moveNext())
    TransactionID = TransactionID + 1;
    pmpaym.PaymentID = rs.value("t_PaymentID");
    strPaymentID = makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym);

    var mes : object = xml.createElement("CreditTransferTransactionInfo");
    mes.setAttribute( "TransactionID", TransactionID );
    var IncludedPayment : RsbPayment = RsbPayment( rs.value("t_PaymentID") );
    IncludedPayment.Reference = string(TransactionID);

    if(rs.value("t_Number") != "")
      mes.setAttribute( "PayerDocNo", GetLastSymbols(rs.value("t_Number"), 6) );
    end;
    if( date(rs.value("t_Date")) != date(0, 0, 0) )
      mes.setAttribute( "PayerDocDate", YYYYMMDD(rs.value("t_Date")) );
    end;

    note = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_UNI_OPERATIONID);
    if(note != "") 
      mes.setAttribute( "OperationID", substr(note, 1, 20) );
    end; 

    note_date = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_ACCEPTUATEDATE);
    mes.setAttribute( "TransactionDate", substr(note_date, 1, 10) );
    
    mes.setAttribute( "TransactionSum", MoneyToStrUFBS(rs.value("t_BaseAmount")) );

    if(rs.value("t_UIN") != "")
      CheckUIN(rs.value("t_ReceiverAccount"), rs.value("t_UIN"));
      mes.setAttribute( "PaymentID", rs.value("t_UIN") );
    end; 

    if(ReqSettlementDate != date(0,0,0))
      mes.setAttribute( "ReqSettlementDate", YYYYMMDD(ReqSettlementDate) );
    end;

    // TransactionPayerInfo
    note = "";
    if( InList(rs.value("t_PayType"), 3, 4) )
      note = rs.value("t_TaxPmNumber");
    end;
    if(note == "")
      note = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_PAYERID);
    end;
    FillSideInReestr( mes, IsPayerInReestr, "TransactionPayerInfo", note,
                      rs.value("t_PayerAccount"), rs.value("t_PayerINN"),
                      rs.value("t_PayerName"), 
                      GetAddress( rs.value("t_PayerINN"), rs.value("t_PayerName") ), 
                      xml, 
                      IsBudgetPaymWithCheckAcc(RsPaym.TaxAuthorState, RsPaym.ReceiverAccount, RsPaym.PayType)
                    );

    // TransactionPayeeInfo
    note = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_PAYEEID);
    FillSideInReestr( mes, IsReceiverInReestr, "TransactionPayeeInfo", note,
                      rs.value("t_ReceiverAccount"), rs.value("t_ReceiverINN"), 
                      rs.value("t_ReceiverName"), 
                      GetAddress( rs.value("t_ReceiverINN"), rs.value("t_ReceiverName") ), 
                      xml );

    var elem : object;

    // TransactionPurpose
    if( rs.value("t_Ground") != "" )
      elem = xml.createElement("TransactionPurpose");
      elem.appendChild( xml.createTextNode( TransTextField(substr(rs.value("t_Ground"), 1, 210)) ) );
      mes.appendChild(elem);
    end;

    // RemittanceInfo
    note = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_ED108_INFO);
    if(note)
      elem = xml.createElement("RemittanceInfo");
      elem.appendChild( xml.createTextNode( TransTextField(note) ) );
      mes.appendChild(elem);
    end;

    // CTTDepartmentalInfo
    note = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_OFK_ACCOUNT);
    note2 = ReadNoteForObject(OBJTYPE_PAYMENT, strPaymentID, PM_NOTEKIND_PAYM_BUDGETRECEIVER_ACC);
    TaxInfo.WriteReestrRecord
      ( xml, mes,
        rs.value("t_TaxAuthorState"),
        rs.value("t_TaxPmGround"),
        rs.value("t_TaxPmPeriod"),
        rs.value("t_PayType"),
        rs.value("t_TaxPmNumber"),
        rs.value("t_TaxPmDate"),
        rs.value("t_TaxPmType"),
        note,
        note2
      );

    ЗаписатьПолеЛог( UFBSCreditTransferTransField, mes.xml, TRUE );
  end;

end;

private macro WriteReestrForTransferPaym
( RsPaym : RsbPayment, 
  IsPayerInReestr : bool, 
  IsReceiverInReestr : bool, 
  TaxInfo : TTaxInfoED108,
  ReqSettlementDate : date
)

  var xml:object = ActiveX("Microsoft.XMLDOM");

  var q : string = "SELECT bdtr.t_Number, bdtr.t_Date, bdtr.t_Amount, bdtr.t_TaxOperation, bdtr.t_TaxPmIndex, " +
          "       bdtr.t_PayerAccount, bdtr.t_PayerINN, bdtr.t_PayerName, bdtr.t_TaxPersonRef, " +
          "       bdtr.t_ReceiverAccount, bdtr.t_ReceiverINN, bdtr.t_ReceiverName, " +
          "       bdtr.t_Ground, bdtr.t_TaxAuthorState, bdtr.t_TaxPmGround, bdtr.t_TaxReceiverRef, " +
          "       bdtr.t_TaxPmPeriod, bdtr.t_TaxPmNumber, bdtr.t_TaxPmDate, bdtr.t_TaxPmType,  " +
          "       bdtr.t_OFKAccount, bdtr.t_FinAccount, bdtr.t_UIN, bdtr.t_PayerAdress, bdtr.t_ReceiverAdress, " +
          "       bdtr.t_OtherPayerFIO, bdtr.t_OtherPayerName, bdtr.t_OtherPayerINN, bdtr.t_OtherPayerAddress, bdtr.t_OtherTaxPersonRef " +
          "  FROM dbdtransf_dbt bdtr " +
          " WHERE bdtr.t_CoverPaymentID = :MPPaymentID ";

  var params : TArray = makeArray( SQLParam("MPPaymentID", RsPaym.PaymentID) );
  var rs : RsdRecordset = execSQLselect(q, params);
  var i : integer = 0;
  while(rs and rs.moveNext())
    i = i + 1;

    var mes : object = xml.createElement("CreditTransferTransactionInfo");
    mes.setAttribute( "TransactionID", i );
    if(rs.value("t_Number") != "")
      mes.setAttribute( "PayerDocNo", rs.value("t_Number") );
    end;
    if( date(rs.value("t_Date")) != date(0, 0, 0) )
      mes.setAttribute( "PayerDocDate", YYYYMMDD(rs.value("t_Date")) );
    end;

    if( rs.value("t_TaxOperation") != "" ) 
      mes.setAttribute( "OperationID", rs.value("t_TaxOperation") );
    end; 

    if( date(rs.value("t_Date")) != date(0, 0, 0) )
      mes.setAttribute( "TransactionDate", YYYYMMDD(rs.value("t_Date")) );
    end;

    mes.setAttribute( "TransactionSum", MoneyToStrUFBS(rs.value("t_Amount")) );

    if( rs.value("t_TaxPmIndex") != "") 
      mes.setAttribute( "DocIndex", rs.value("t_TaxPmIndex") );
    end; 

    if( rs.value("t_UIN") != "") 
      CheckUIN( rs.value("t_ReceiverAccount"), rs.value("t_UIN") );
      mes.setAttribute( "PaymentID", rs.value("t_UIN") );
    end;

    if(ReqSettlementDate != date(0,0,0))
      mes.setAttribute( "ReqSettlementDate", YYYYMMDD(ReqSettlementDate) );
    end;

    // TransactionPayerInfo
    FillSideInReestr( mes, IsPayerInReestr, "TransactionPayerInfo", 
                      rs.value("t_TaxPersonRef"),
                      rs.value("t_PayerAccount"), rs.value("t_PayerINN"),
                      rs.value("t_PayerName"), rs.value("t_PayerAdress"), xml );
  
    // TransactionPayeeInfo
    FillSideInReestr( mes, IsReceiverInReestr, "TransactionPayeeInfo", 
                      rs.value("t_TaxReceiverRef"),
                      rs.value("t_ReceiverAccount"), rs.value("t_ReceiverINN"), 
                      rs.value("t_ReceiverName"), rs.value("t_ReceiverAdress"), xml );

    var elem : object;

    // TransactionPurpose
    if( rs.value("t_Ground") != "" )
      elem = xml.createElement("TransactionPurpose");
      elem.appendChild( xml.createTextNode( TransTextField(substr(rs.value("t_Ground"), 1, 210)) ) );
      mes.appendChild(elem);
    end;

    // CTTDepartmentalInfo
    TaxInfo.WriteReestrRecord
      ( xml, mes,
        rs.value("t_TaxAuthorState"),
        rs.value("t_TaxPmGround"),
        rs.value("t_TaxPmPeriod"),
        RsPaym.PayType,
        rs.value("t_TaxPmNumber"),
        string( date(rs.value("t_TaxPmDate")) ),
        rs.value("t_TaxPmType"),
        rs.value("t_OFKAccount"),
        rs.value("t_FinAccount")
      );

    // TransactionBeneficiaryInfo
    WriteTransactionBeneficiaryInfo
      ( xml, mes,
        rs.value("t_OtherPayerFIO"),
        rs.value("t_OtherPayerName"),
        rs.value("t_OtherPayerINN"),
        rs.value("t_OtherPayerAddress"),
        rs.value("t_OtherTaxPersonRef")
      );

    ЗаписатьПолеЛог( UFBSCreditTransferTransField, mes.xml, TRUE );
  end;
end;

private macro FillKppForEqualSide
( FullINN : @string, 
  IsEqualKPP : bool, 
  EqualKPP : string
)
  var FirstKPP : string = RemoveINN(FullINN);

  if(IsEqualKPP)
    if(EqualKPP and not FirstKPP)
      FullINN = RemoveKPP(FullINN) + "/" + EqualKPP;
    end;
  else
    // КПП не заполняется
    FullINN = RemoveKPP(FullINN);
  end;
end;

macro CheckPaym( addr )

  SetBuff( wlpmpaym,  addr );
  var RsPaym: RsbPayment = RsbPayment( wlpmpaym.PaymentID );

  var err_msg : string = "", err : integer = 0;

  err = CheckPaymED108Requisites( RsPaym, @err_msg, "ED108" );

  if(not err)
    err = CheckTaxPropForUfebsMes(RsPaym, "ED108", @err_msg)
  end;

  if(err)
    err_msg = substr( err_msg, 1, 499 );
    MemoryError(err, err_msg);
    return false;
  end;

  return true;

end;

private macro CheckPaymV3( addrPm, RsPaym : RsbPayment, err_msg : @string )

  if( not CheckPaym(addrPm) )
    return false;
  end;
  
  if( RsPaym.DocKind != DLDOC_MULTYPM )
    return true;
  end;
  
  var rs = execSQLSelect( " Select distinct rm.t_Precedence "
                          "   From dpmpaym_dbt pm, dpmrmprop_dbt rm, dpmlink_dbt lnk "
                          "  Where lnk.t_InitialPayment = :PaymentID "
                          "    and rm.t_PaymentID = lnk.t_PurposePayment ",
                          MakeArray( SQLParam("PaymentID", RsPaym.PaymentID) ));
  if( rs and rs.MoveNext() )
    if( (RsPaym.GetPMRMPROP().rec.Precedence != rs.value("t_Precedence")) or (rs.MoveNext()) )
      err_msg = "В платежном поручении на общую сумму с реестром не должны одновременно присутствовать платежи с различными приоритетами."
                " Оставьте в составе сводного только платежи с приоритетом, который совпадает со значением, указанном в сводном платеже.";
      return false;
    else
      return true;
    end;
  end;
  
end;

macro GenMes( addrMes, addrPm, ver_st )
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  var RsPaym = RsbPayment( wlpmpaym.PaymentID );
  PrintLog(2,"Генерация сообщения по ED108");

  var IsUBR : bool = БанкУБР(RsPaym.ReceiverBankID);

  var ErrMsg : string = "";

  if( ValType(ver_st) == V_UNDEF )
    ver_st = 0;
  end;
  
  if( ver_st == 3 )
    if( not CheckPaymV3( addrPm, RsPaym, @ErrMsg ) )
      std.msg(ErrMsg);
      return FALSE;
    end;
  end;
  

  if( RsPaym.DocKind == DLDOC_MULTYPM)
    if(not CheckMes(RsPaym, IsUBR, @ErrMsg) )
      std.msg(ErrMsg);
      return FALSE;
    end;
  else
    if(not CheckMesNotMulty(RsPaym, IsUBR, @ErrMsg) )
      std.msg(ErrMsg);
      return FALSE;
    end;
  end;

  var TaxInfo : TTaxInfoED108 = GetTaxInfoED108(RsPaym);

  var Priority = 0, CrBankCode = "", ReceiverCorrAccNostro = "", 
      PayerINN = "", PayerName = "", PayerAccount = "",
      ReceiverINN = "", ReceiverName = "", ReceiverAccount = "",
      ChargeOffDate = date(0, 0, 0),
      PayType : integer = BPT_COMMON;

  var PmAttrChkEq : TPmAttrChkEq = null;

  if( RsPaym.DocKind == DLDOC_MULTYPM )
    // получаем данные из первого платежа в составе сводного
    GetFirstPaymData(  RsPaym.PaymentID,
                      @Priority,
                      @PayerINN, @PayerName, @PayerAccount, 
                      @ReceiverINN, @ReceiverName, @ReceiverAccount, 
                      @ChargeOffDate, @PayType
                    );

    if((RsPaym.ReceiverCorrAccNostro == "") and (RsPaym.ReceiverBankCode == ""))
      GetReceiverBankFromPayment( RSPaym.PaymentID, @CrBankCode, @ReceiverCorrAccNostro );
    else
     ReceiverCorrAccNostro = RsPaym.ReceiverCorrAccNostro;
     CrBankCode = RsPaym.ReceiverBankCode;
    end;
  
    PmAttrChkEq = TMultiPmAttrChkEq
                          ( PayerINN, PayerName, PayerAccount, 
                            ReceiverINN, ReceiverName, ReceiverAccount, 
                            ChargeOffDate );

  else
    GetFirstTransferData( RsPaym.PaymentID,
                          @PayerINN, @PayerName, @PayerAccount, 
                          @ReceiverINN, @ReceiverName, @ReceiverAccount );

    PmAttrChkEq = TBdtrPmAttrChkEq
                          ( PayerINN, PayerName, PayerAccount, 
                            ReceiverINN, ReceiverName, ReceiverAccount );
  end;

  PmAttrChkEq.CheckEqualPmAttributes( RsPaym.PaymentID );

  // определяем заполнение реквизитов в общей части и в реестре
  var IsPayerInReestr : bool, IsReceiverInReestr : bool;

  if( not RsPaym.PayType and PmAttrChkEq.IsEqualPayer and
      (RsPaym.DocKind == DLDOC_MULTYPM)  
    )
    IsPayerInReestr = false;

    // в общей части - из первого платежа в составе сводного

    FillKppForEqualSide(@PayerINN, PmAttrChkEq.IsEqualPayerKPP, PmAttrChkEq.EqualPayerKPP);

  else // платеж является платежом в бюджет или реквизиты плательщика различаются
       // или платеж создан по переводам физ.лиц
    IsPayerInReestr = true;
    if(not PmAttrChkEq.IsEqualPayer)
      PayerAccount = ""; // в общей части не заполняем
    end;
    // в общей части - по сводному платежу
    PayerINN = RsPaym.PayerINN;
    PayerName = RsPaym.PayerName;
  end;

  if(IsUBR or (RsPaym.PayType > 0))
    IsReceiverInReestr = false;

    if(RsPaym.ReceiverINN)
      ReceiverINN = RsPaym.ReceiverINN;
    elif(ReceiverINN)
      // если в первом платеже в составе сводного реквизит заполнен, оставляем это значение
      ;
    else
      ReceiverINN = GetNotEmptyRmFld(RsPaym, "ReceiverINN");
    end;

    if(RsPaym.ReceiverName)
      ReceiverName = RsPaym.ReceiverName;
    elif(ReceiverName)
      // если в первом платеже в составе сводного реквизит заполнен, оставляем это значение
      ;
    else
      ReceiverName = GetNotEmptyRmFld(RsPaym, "ReceiverName");
    end;

    if(RsPaym.ReceiverAccount)
      ReceiverAccount = RsPaym.ReceiverAccount;
    else
      // оставляем значение из первого платежа в составе сводного, 
      // т.к. ReceiverAccount у всех одинаковый, раз мы прошли проверку CheckMes
      ;
    end;

    if( (RsPaym.PayType > 0) and (RsPaym.TaxAuthorState) )
      var OnlyINN : string = RemoveKPP(ReceiverINN);
      if(not OnlyINN)
        ErrMsg = "Не найден ИНН получателя или он не совпадает для всех платежей, включенных в сводный. В распоряжении о переводе средств в бюджет РФ ИНН получателя должен быть обязательно заполнен";
        RunError(ErrMsg, ErrMsg);
      end;

      if(not PmAttrChkEq.IsEqualReceiverKPP or not PmAttrChkEq.EqualReceiverKPP)
        ErrMsg = "Не найден КПП получателя или он не совпадает для всех платежей, включенных в сводный. В распоряжении о переводе средств в бюджет РФ КПП получателя должен быть обязательно заполнен";
        RunError(ErrMsg, ErrMsg);
      end;
    end;
  else
    if( PmAttrChkEq.IsEqualReceiver )
      IsReceiverInReestr = false;

      // в общей части - из первого платежа в составе сводного

      FillKppForEqualSide(@ReceiverINN, PmAttrChkEq.IsEqualReceiverKPP, PmAttrChkEq.EqualReceiverKPP);
    else
      IsReceiverInReestr = true;
      // в общей части заполняем по сводному платежу
      ReceiverINN = RsPaym.ReceiverINN;
      ReceiverName = RsPaym.ReceiverName;
      ReceiverAccount = RsPaym.ReceiverAccount;
    end;
  end;

  // Заполнение сообщения
  var xml:object = ActiveX("Microsoft.XMLDOM");

  // Поле "xml"
  var mes:object = xml.createElement("ED108");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  mes.setAttribute( "EDNo",       ed_nda.EDNo );
  mes.setAttribute( "EDDate",     YYYYMMDD(ed_nda.EDDate) );
  mes.setAttribute( "EDAuthor",   ed_nda.EDAuthor );

  var PaytKind : string = GetPaytKind("ED108", RsPaym.PaymentKind, wlmes.TpSchemID);
  if(PaytKind)
    mes.setAttribute("PaytKind", PaytKind);
  end;

  mes.setAttribute("Sum", MoneyToStrUFBS(RsPaym.BaseAmount) );
  
  var ReqSettlementDate : date = date(0,0,0);
  if( ver_st == 3 )
    mes.setAttribute( "PaymentPrecedence", RsPaym.GetPMRMPROP().rec.Precedence );

    // Запрошенная (требуемая) дата исполнения распоряжения
    
    ReqSettlementDate = GetReqSettlementDate
      ( RsPaym.OutTransferDate, 
        ed_nda.EDDate,
        RsPaym.PaymentKind );

    if(ReqSettlementDate != date(0,0,0))
      mes.setAttribute("ReqSettlementDate", YYYYMMDD(ReqSettlementDate));
    end;
  end;
  
  mes.setAttribute("TransKind", "01");
  if( RsPaym.Dockind == DLDOC_MULTYPM )
    mes.setAttribute("Priority", Priority);
  else
    mes.setAttribute("Priority", RsPaym.Priority);
  end;
  
  if( RsPaym.Dockind == DLDOC_MULTYPM )
    if( PmAttrChkEq.IsEqualChargeOffDate and (date(ChargeOffDate) != date(0, 0, 0)) and
        (ChargeOffDate >= RsPaym.Date)
      )
      if(RsPaym.PayerBankEnterDate != date(0, 0, 0))
        mes.setAttribute("ReceiptDate", YYYYMMDD(RsPaym.PayerBankEnterDate) );
      end;

      mes.setAttribute("ChargeOffDate", YYYYMMDD(ChargeOffDate) );
    end;
  else
    if( (RsPaym.PayerBankEnterDate != date(0, 0, 0)) and (RsPaym.PayerChargeOffDate != date(0, 0, 0)) )
      mes.setAttribute( "ReceiptDate", YYYYMMDD( RsPaym.PayerBankEnterDate ) );
      mes.setAttribute( "ChargeOffDate", YYYYMMDD( RsPaym.PayerChargeOffDate ) );
      if( date( RsPaym.I2PlaceDate ) != date(0, 0, 0) )
        mes.setAttribute( "FileDate", YYYYMMDD( RsPaym.I2PlaceDate ) );
      end;
    end;
  end;
    
  var Err;
  var SystemCode;
  if( GetSystemCodeForGenMes( "ED108", wlmes, @Err, @SystemCode ) )
    if( strlen(Err) != 0 )
      RunError(Err);
    else
      mes.setAttribute("SystemCode",  SystemCode );
    end;
  end;

  var UIN : string = RsPaym.GetPMRMPROP().rec.UIN;
  if(UIN)
    CheckUIN(RsPaym.ReceiverAccount, UIN);
    mes.setAttribute("PaymentID", UIN);
  end;

  var elem : object = null;

  elem = xml.createElement("AccDoc");
  elem.setAttribute("AccDocNo",   int(GetLastSymbols(RsPaym.Number, PM_DOCNO_NONZERO_LEN)) );
  elem.setAttribute("AccDocDate", YYYYMMDD(RsPaym.Date) );
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  var IsZeroFill : bool = false;

  // Блок "Payer"
  elem = xml.createElement("Payer");

  var PayerBIC = "", PayerCorrespAcc = "";
  GetPayerBICCorrespAcc(RsPaym, @PayerBIC, @PayerCorrespAcc);
  IsZeroFill = IsBudgetPaymWithCheckAcc( TaxInfo.GetCommonTaxAuthorState(), RsPaym.ReceiverAccount, 
                                         RsPaym.PayType );
  if( RsPaym.Dockind == DLDOC_MULTYPM )
    FillSide( elem, PayerAccount, PayerINN, PayerName, PayerBIC, PayerCorrespAcc, 
              xml, IsZeroFill);
  else
    GetPayerAccountAndNameForTransferPaym(RsPaym, @PayerAccount, @PayerName);

    FillSide( elem, PayerAccount, RsPaym.PayerINN, PayerName, PayerBIC, PayerCorrespAcc, 
              xml, IsZeroFill );
  end;

  mes.appendChild(elem);

  // Блок "Payee"
  elem = xml.createElement("Payee");
  if( RsPaym.Dockind == DLDOC_MULTYPM )
    FillSide( elem, ReceiverAccount, ReceiverINN, ReceiverName, CrBankCode, 
              ReceiverCorrAccNostro, xml,
              IsBudgetPaym(RsPaym.TaxAuthorState, RsPaym.PayType)
            );
  else
    FillSide( elem, RsPaym.ReceiverAccount, RsPaym.ReceiverINN, RsPaym.ReceiverName, 
              RsPaym.ReceiverBankCode, RsPaym.ReceiverCorrAccNostro, xml );
  end;
  mes.appendChild(elem);

  // Purpose
  elem = xml.createElement("Purpose");
  elem.appendChild( xml.createTextNode( TransTextField(substr(RsPaym.Ground, 1, 210)) ) );
  mes.appendChild(elem);

  // DepartmentalInfo
  TaxInfo.WriteCommonPart(xml, mes);

  // InitialED
  if(wlmes.RelatedRef)
    elem = xml.createElement("InitialED");
    FillEDNoDateAuthorByRef_XMLField(elem, wlmes.RelatedRef);
    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );

  // CreditTransferTransactionInfo
  УстановитьКонтекстБлокаЛог( UFBSCreditTransferTransField );

  if( RsPaym.Dockind == DLDOC_MULTYPM )    
    WriteReestrForMultyPm( RsPaym, IsPayerInReestr, IsReceiverInReestr, TaxInfo,
                           ReqSettlementDate );
  else //!= DLDOC_MULTYPM
    WriteReestrForTransferPaym( RsPaym, IsPayerInReestr, IsReceiverInReestr, TaxInfo,
                                ReqSettlementDate );
  end;
  УстановитьКонтекстБлокаЛог( ".." );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV3( addrMes, addrPm )
  return GenMes( addrMes, addrPm, 3 );
end;







