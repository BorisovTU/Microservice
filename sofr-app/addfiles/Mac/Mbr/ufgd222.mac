/*
$Name:         ufgd222.mac
$Module:       Межбанковские расчеты
$Description:  Генерация учетного объекта ED222
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям УФЭБС ED222                              */
/*                                                                          */
/* Имя файла: ufgd222.mac                                                   */
/* Создан:    06.07.06                                     Гайворонский Е.Г.*/
/****************************************************************************/
import "ufgendoc.mac";

private macro ExistsOutMes( wlmes )
  return existsSQLSelect( " Select 0 "
                          "   From dwlmes_dbt out, dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
                          "  Where out.t_Trn = :RelatedRef "
                          "    and out.t_Direct = chr(0) "
                          "    and out.t_RlsFormID = rls.t_RlsFormID "
                          "    and rls.t_FormID = frm.t_FormID "
                          "    and frm.t_TpID = :TpID ",
                          MakeArray( SQLParam("RelatedRef", wlmes.RelatedRef),
                                     SQLParam("TpID", 9)));
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Строка2, BIC, CashDocNo, CashDocDate, CashAcc, CashDC, Receiver, Error, InitAuthor;
  var EDDate, Sum, CorrAcc, j = 0;
  
  var StringEPS, EDNo, EDDateEPS, EDAuthor, RefDocInfo; 

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED222" );

  ClearRecord( wlinfo );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED222" ) );
     return false;
  end;

  EDDate = ReadAttribute(xml,"EDDate");

  /* Заполнение учетных буферов */
  wlinfo.TRN                = wlmes.Trn;
  
  BIC         = ReadAttribute(xml,"BIC");
  CashDocNo   = ReadAttribute(xml,"CashDocNo");
  CashDocDate = ReadAttribute(xml,"CashDocDate");
  CashAcc     = ReadAttribute(xml,"CashAcc");
  if( UF_GetDKFlag(ReadAttribute(xml, "CashDC")) == 1)
     CashDC = "дебету";
  else
     CashDC = "кредиту";
  end;

  Sum         = ReadAttribute(xml,"Sum");
  CorrAcc     = ReadAttribute(xml,"CorrAcc");

  Строка2 = "Получено подтверждение от " + BIC +
            " для кассового документа №" + CashDocNo + " за дату: " + CashDocDate +
            "\nпо счету: " + CashAcc + " и корреспондирующим с ним: " + CorrAcc +
            "\nпо " + CashDC + " на сумму: " + String(money(Sum)/100) + " руб.";
  
  var SessionType = ReadAttribute(xml,"SessionType", "Session", false);
  if( SessionType )
    Строка2 = Строка2 + "\nРаспоряжение исполнено в ";
    if( SessionType == "URGN" )
      Строка2 = Строка2 + "срочном рейсе";
    elif( SessionType == "NURG" )
      Строка2 = Строка2 + "несрочном рейсе";
    elif( SessionType == "CONS" )
      Строка2 = Строка2 + "консолидированном рейсе";
    else
      Строка2 = Строка2 + "рейсе типа " + SessionType;
    end;
  end;

  var SessionID = ReadNodeText( xml,  "Session/SessionID", false);;
  if( SessionID )
    Строка2 = Строка2 + "\nНомер консолидированного рейса: " + SessionID;
  end;

  var SettlementTime = ReadNodeText( xml,  "Session/SettlementTime", false);;
  if( SettlementTime )
    Строка2 = Строка2 + "\nВремя исполнения в ПБР: " + SettlementTime;
  end;


  EDNo       = ReadAttribute(xml, "EDNo",   "EDRefID");
  EDDateEPS  = ReadAttribute(xml, "EDDate", "EDRefID");
  EDAuthor   = ReadAttribute(xml, "EDAuthor", "EDRefID");
  RefDocInfo = ReadNodeText( xml,  "RefDocInfo", false);
  StringEPS = "\nИсходный ЭПС: номер "  + EDNo +
              " от "                    + EDDateEPS + 
              " автор "                 + EDAuthor +
              ".\nСодержание операции: " + RefDocInfo +
              ".";
              
  Строка2 = Строка2 + StringEPS;

  wlinfo.OriginatorCode = ReadAttribute(xml, "EDAuthor");
  wlinfo.OriginatorCodeKind = PTCK_UIS;
  wlinfo.OriginatorID = GetCodeOwnerID(PTCK_UIS, wlinfo.OriginatorCode);
  wlinfo.OriginatorName = Bnk_GetPartyName(wlinfo.OriginatorID);
  
  var EDReceiver = ReadAttribute(xml, "EDReceiver", "", false);
  if(  strlen(EDReceiver) != 0 )
    wlinfo.RecipientCode = EDReceiver;
    wlinfo.RecipientCodeKind = PTCK_UIS;
    wlinfo.RecipientID = GetCodeOwnerID(PTCK_UIS, wlinfo.RecipientCode );
  else
    wlinfo.RecipientID = GetDprtPartyID(wlmes.Department);
    wlinfo.RecipientCodeKind = PTCK_BIC;
    GetPartyCodeEx(wlinfo.RecipientID, PTCK_BIC, @wlinfo.RecipientCode);
  end;
  
  wlinfo.RecipientName = Bnk_GetPartyName(wlinfo.RecipientID);
  
  if( ExistsOutMes(wlmes) )
    wlinfo.RelatedRef = wlmes.RelatedRef;
  else
    wlinfo.RelatedRef = "";
  end;

  if( not ВставитьИнфСообщение( wlinfo, Строка2 ) )
    std.msg("Ошибка при вводе ответа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

macro GenDocV2( addrMes )
   return GenDoc( addrMes );
end;
