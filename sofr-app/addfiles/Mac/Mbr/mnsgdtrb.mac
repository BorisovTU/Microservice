/*
  $Name: mnsgdtrb.mac
  $Module: MBR
  $Description: Генерация учетного объекта по сообщению TRB
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация учетного объекта по сообщению TRB                              */
/*                                                                          */
/*  Имя файла: mnsgdtrb.mac                                                 */
/*  Создан:    19.07.11                                   Чукина Т.А.       */
/****************************************************************************/

import "wlmnstls.mac", "wldoc.mac";

// Поля сообщения TRB, необходимые для заполнения полей учетного объекта "Требование"
class ConstructTRB
  var 
    КодНО            = "",
    НаимНО           = "",
    БИК              = "",
    НаимКО           = "",
    НомТреб          = "",
    ДатаТреб         = "",
    СуммаПлат :money = 0.0,
    НаимЮЛ           = "",
    ФИОФЛ            = "",
    ИНННП            = "",
    КППЮЛ            = "",
    СведВзыск        = "",
    НомПоруч         = "",
    ДатаПоруч        = "",
    НомСооб          = "",
    ДатаСооб         = "",
    СрокИсп          = "",
    КБК              = "",
    ОКТМО            = "";
end;

private var TRB : ConstructTRB;

// получить значения полей из сообщения
private macro GetMesValues()
  var field_name, field_value;
  
  /* Последовательно считываем поля и заполняем лексемы */
  while(СчитатьПоле(field_name, field_value))  
    if( field_name == "КодНО" )
      TRB.КодНО = field_value;
    end;
    if( field_name == "НаимНО" )
      TRB.НаимНО = field_value;
    end;
    if( field_name == "БИК" )
      TRB.БИК = field_value;
    end;
    if( field_name == "НаимКО" )
      TRB.НаимКО = field_value;
    end;
    if( field_name == "НомТреб" )
      TRB.НомТреб = field_value;
    end;
    if( field_name == "ДатаТреб" )
      TRB.ДатаТреб = field_value;
    end;
    if( field_name == "СуммаПлат" )
      TRB.СуммаПлат = field_value;
    end;
    if( field_name == "НаимЮЛ" )
      TRB.НаимЮЛ = field_value;
    end;
    if( field_name == "ФИОФЛ" )
      TRB.ФИОФЛ = field_value;
    end;
    if( field_name == "ИНННП" )
      TRB.ИНННП = field_value;
    end;
    if( field_name == "КППЮЛ" )
      TRB.КППЮЛ = field_value;
    end;
    if( field_name == "СведВзыск" )
      TRB.СведВзыск = field_value;
    end;
    if( field_name == "НомПоруч" )
      TRB.НомПоруч = field_value;
    end;
    if( field_name == "ДатаПоруч" )
      TRB.ДатаПоруч = field_value;
    end;
    if( field_name == "НомСооб" )
      TRB.НомСооб = field_value;
    end;
    if( field_name == "ДатаСооб" )
      TRB.ДатаСооб = field_value;
    end;
    if( field_name == "СрокИсп" )
      TRB.СрокИсп = field_value;
    end;
    if( field_name == "КБК" )
      TRB.КБК = field_value;
    end;
    if( field_name == "ОКТМО" )
      TRB.ОКТМО = field_value;
    end;
  end;
  
  return TRUE;
end; /* GetMesValues */

macro GenDoc( addrMes )
  TRB = ConstructTRB();
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация учетного объекта по сообщению TRB");
  
  GetMesValues();

  var error = 0, DecID = 0;

  file f_wlsess(wlsess) key 0;
  f_wlsess.SessionID = wlmes.SessionID;
  GetEQ(f_wlsess);

  ClearRecord(wlregdec);
  
  /* Заполнение учетных буферов */
  wlregdec.Type                     = WLD_TYPE_REGDEC_TRB;
  wlregdec.OriginatorCodeKind       = PTCK_MNS;
  wlregdec.OriginatorCode           = TRB.КодНО;
  wlregdec.OriginatorName           = TRB.НаимНО; 
  wlregdec.OriginatorID             = ПолучитьКодСубъекта( wlregdec.OriginatorCode, wlregdec.OriginatorCodeKind, error );
  wlregdec.RecipientCodeKind        = wlmes.InsideAbonentCodeKind;
  wlregdec.RecipientCode            = wlmes.InsideAbonentCode;
  wlregdec.RecipientName            = TRB.НаимКО;
  wlregdec.RecipientID              = wlmes.InsideAbonentID;
  wlregdec.Number                   = TRB.НомТреб; 
  wlregdec.Date                     = date(TRB.ДатаТреб); 
  wlregdec.DeliveryDate             = f_wlsess.SysDate; 
  wlregdec.Amount                   = TRB.СуммаПлат; 
  
  wlregdec.ClientName               = IfThenElse( TRB.НаимЮЛ != "", TRB.НаимЮЛ, StrSubst(TRB.ФИОФЛ, ",", " " ) );
  if (not wlregdec.ClientName)
    std.msg("Не определено наименование клиента в сообщении");
    return false;
  end;
  wlregdec.ClientINN                = TRB.ИНННП;
  if (not wlregdec.ClientINN)
    std.msg("Не определен ИНН клиента в сообщении");
    return false;
  end;                                     
  wlregdec.ClientKPP                = TRB.КППЮЛ;
  var inn = IfThenElse( wlregdec.ClientKPP == "", wlregdec.ClientINN, wlregdec.ClientINN + "/" + wlregdec.ClientKPP ); 
  wlregdec.ClientID                 = ПолучитьКодСубъекта( inn, PTCK_INN, error ); 
  if( (error) and (TRB.КППЮЛ) )
    wlregdec.ClientID               = GetLegalPersonIDByINN( wlregdec.ClientINN ); 
  end;

  wlregdec.Ground                   = TRB.СведВзыск; 
  wlregdec.LnkDocNumber             = TRB.НомПоруч; 
  wlregdec.LnkDocDate               = TRB.ДатаПоруч; 
  wlregdec.LnkMesTrn                = TRB.НомСооб;
  if(TRB.ДатаСооб)
    wlregdec.LnkMesDate             = TRB.ДатаСооб; 
  end;
  wlregdec.ExecuteDate              = TRB.СрокИсп; 
  wlregdec.BTTTICode                = TRB.КБК; 
  wlregdec.OKATOCode                = TRB.ОКТМО; 
  
  if( not ВставитьТребованиеФНС( wlregdec, DecID ) )
    std.msg("Ошибка при вставке требования о перечислении налога");
    return false;
  end;
  
  DeleteWlRepClm( DecID );

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;