/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT942                                       */
/*                                                                          */
/*  Имя файла: swgm942.mac                                                  */
/*  Создан:  08.06.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

macro GenMes( addrMes, addrHead )
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT;
  var StatementLine, TpID, NameForm, stat, KindPageExt;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlhead, addrHead );

  PrintLog(2,"Генерация сообщения по МТ942");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  ЗаписатьПолеЛог( RelatedReferenceField, wlhead.Number );

  /* 25 - счет выписки */
  ЗаписатьПолеЛог( AccountIdentificationField, wlhead.Account );

  /* 28C - Номер выписки */
  ЗаписатьПолеЛог( StatementSequnceNumberCField, string(substr(wlhead.Number,strlen(wlhead.Number)-4), "/", wlhead.Page) );

  /* Нижняя граница сумм кредита */
  CUR = ПолучитьКодВалютыЛог( wlhead.AccountFIID );
  ЗаписатьПолеЛогВБлок( "A", FloorLimitIndicatorField, CUR+CREDIT_ACCOUNT+GetSWIFTAmount( wlhead.CreditFloor ) );

  /* Нижняя граница сумм дебета */
  ЗаписатьПолеЛогВБлок( "A", FloorLimitIndicatorField, CUR+DEBET_ACCOUNT+GetSWIFTAmount( wlhead.DebetFloor ) );

  /* 13D - Дата, время */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  ЗаписатьПолеЛог( DateTimeIndicationField, String(GetSWIFTDate( {curdate} ), GetSWIFTTime( time ), BANK_TIME_ZONE) );

  /******************** Заполняем документы выписки ******************/
  /* Дата начала периода выписки */
  dateSWIFT = GetSWIFTDate( wlhead.DateIn );  
  УстановитьФильтрДокументовВыписки(wlhead.HeadID);
  while( ПолучитьДокументВыписки(wlConf) )
     if ( wlConf.DKFlag==WL_DEBET )
         dKflag = DEBET_ACCOUNT;
     else
         dKflag = CREDIT_ACCOUNT;
     end;
     StatementLine = "";
     StatementLine = StatementLine + GetSWIFTDate( wlConf.DateValue );
     StatementLine = StatementLine + substr( string(dateSWIFT), 3 );
     StatementLine = StatementLine + dKflag;
     StatementLine = StatementLine + substr(CUR,3,1);
     StatementLine = StatementLine + GetSWIFTAmount( wlConf.Sum );
     stat = ПолучитьИмяФормы( wlConf.DocumentID, TpID, NameForm );
     if ( (stat) AND (TpID==TRANSP_SWIFT) )
        StatementLine = StatementLine + FillTypeOperField(wlConf.DocumentID, NameForm);
     else
        StatementLine = StatementLine + StatLine_TypeOperField_NonSWIFTTag + Transfer;
     end;
     StatementLine = StatementLine + substr( wlConf.RelatedRef,1,16 );
     if ( wlConf.Number!=wlConf.RelatedRef )
        StatementLine = StatementLine + "//" + substr( wlConf.Number,1,16 );     
     end;
     /*if ( wlConf.Description )
         StrChangeRusXSet( wlconf.Description, field_value );
         StatementLine = StatementLine + "/" + substr( field_value,1,34);
     end;*/
     ЗаписатьПолеЛогВБлок( "B", StatementLineField, StatementLine );
     StrChangeRusXSet( wlconf.Description, field_value );
     StrMultyToFormat( field_value, field_value, 65, 6 );
     ЗаписатьПолеЛогВБлок( "B", InformationToAccountOwnerField, field_value );
  end;
  /*******************************************************************/
     
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 90d - Количество и сумма по операциям дебета */
  ЗаписатьПолеЛог( NumberSumDebetEntriesField, String( wlhead.DebetNumDoc, CUR, GetSWIFTAmount(wlhead.DebetTurn+wlhead.DebetFloorTurn)) );  

  /* 90c - Количество и сумма по операциям  кредита */
  ЗаписатьПолеЛог( NumberSumCreditEntriesField, String( wlhead.CreditNumDoc, CUR, GetSWIFTAmount(wlhead.CreditTurn+wlhead.CreditFloorTurn)) );

  /* 86 */
  StrChangeRusXSet( wlhead.Description, field_value );
  StrMultyToFormat( field_value, field_value, 65, 6 );
  ЗаписатьПолеЛог( InformationToAccountOwnerField, field_value );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;