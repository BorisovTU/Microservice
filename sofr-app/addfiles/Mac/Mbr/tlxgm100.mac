/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по TELEX MT100                                       */
/*                                                                          */
/*  Имя файла: txgm100.mac                                                  */
/*  Создан:  08.11.00                                        Алешин А.В.    */
/****************************************************************************/

import "swgenmes.mac", "adress.mac";

record Party(party);

macro GenMes( addrMes, addrPm )
  var field_value, BNC_Name, field72, INN;
  RECORD ocb( pmroute );
  RECORD rcv( pmroute );
  RECORD snd( pmroute );
  RECORD irb( pmroute );
  RECORD acc(pmroute);
  record RecordAdress ( adress );

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog(2,"Генерация сообщения по TELEX МТ100");

  /* Ищем валютные свойства */
  if( FindPayment( wlpmpaym.PaymentID, 0, 0, 0, 0, true, wlpmpaym, 0, 0, wlpmrmprop ) != 0 )
    std.out( 2, "PaymentID: " + wlpmpaym.PaymentID );
    RunError( "|Не найден платеж" );
  end;

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( "20"/*TransactionReferenceNumberField*/, wlmes.TRN );

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( wlpmpaym.ValueDate ) +
                ПолучитьКодВалютыЛог( wlpmpaym.PayFIID ) +
                GetSWIFTAmount( wlpmpaym.Amount );
  ЗаписатьПолеЛог( "32A"/*ValueDateCurrencyCodeAmountField_A*/, field_value );

  /* 50 - приказодатель */
  if( (wlpmpaym.Payer == 0) OR (ПолучитьСубъекта(wlpmpaym.Payer, Party) != 0) )
    field_value = wlpmrmprop.PayerName;
  else
    field_value = "";
    if( wlpmpaym.Payer )
      INN = GetPartyINN( wlpmpaym.Payer );
      if( INN != "" )
        field_value = String( field_value, "ИНН ", INN, SYMB_ENDL );
      end;
    end;
    field_value = field_value + wlpmrmprop.PayerName;
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(Party.PartyID,RecordAdress);
    if( RecordAdress.Place != "" )
      field_value = String( field_value, SYMB_ENDL, RecordAdress.Place );
    end;
  end;
  StrChangeRusXSet( field_value, field_value );
  StrMultyToFormat( field_value, field_value, 35, 4 );
  ЗаписатьПолеЛог( "50"/*OrderingCustomerField*/, field_value );

  /* 59 - бенефициар */
  field_value = "";
  if( wlpmpaym.ReceiverAccount != "" )
    field_value = SYMB_SLASH + wlpmpaym.ReceiverAccount;
  end;
  if( wlpmrmprop.ReceiverName != "" )
    StrChangeRusXSet( wlpmrmprop.ReceiverName, BNC_Name );
    StrMultyToFormat( BNC_Name, BNC_Name, 35, 4 );
    if( field_value != "" )
      field_value = field_value + SYMB_ENDL + BNC_Name;
    else
      field_value = BNC_Name;
    end;
  end;
  ЗаписатьПолеЛог( "59"/*BeneficiaryCustomerField*/, field_value );

  /* 70  - информация для бенефициара */
  StrChangeRusXSet( wlpmrmprop.Ground, field_value );
  StrMultyToFormat( field_value, field_value, 35, 4 );
  ЗаписатьПолеЛог( "70"/*DetailsOfPaymentField*/, field_value );

  /* 71A  - расходы */
    field_value = GetCommisCode (wlpmrmprop.ComissCharges);
  ЗаписатьПолеЛог( "71A"/*DetailsOfChargesField_A*/, field_value );

  /* 72 - информация для банка-получателя */
  field_value = "";
  if( ПолучитьУзелПолучателяПлатежа(wlpmrmprop.PaymentID, Route) )
    field_value = String( SYMB_SLASH, Field72CodesINS, SYMB_SLASH, Route.CodeValue );
  end;
  if( ПолучитьБлижайшийУзелПлатежа( wlpmrmprop.PaymentID, RTDIR_OUT, ocb ) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
       ПолучитьУзелПолучателяПлатежа(wlpmrmprop.PaymentID, rcv) AND 
       (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
       ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
     if( field_value != "" )
       field_value = String( field_value, SYMB_ENDL );
     end;
     field_value = String( SYMB_SLASH, Field72CodesRCB, SYMB_SLASH, Route.CodeValue );
  end;
  if( wlpmrmprop.PartyInfo != "" )
    if( field_value != "" )
      field_value = String( field_value, SYMB_ENDL );
    end;
    StrChangeRusXSet( wlpmrmprop.PartyInfo, field72 );
    StrMultyToFormat( field72, field72, 35, 6 );
    field_value = field_value + field72;
  end;
  if( field_value != "" )
    ЗаписатьПолеЛог( "72"/*SenderToReceiverInformationField*/, field_value );
  end;

  /* 52a - банк приказодателя */
  if( ПолучитьУзелОтправителяПлатежа(wlpmrmprop.PaymentID, snd) AND
      ПолучитьСамыйДальнийУзелПлатежа(wlpmrmprop.PaymentID, RTDIR_IN, Route) AND 
      (Route.RouteID!=snd.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог( "52"/*OrderingInstitutionField*/, "52a", Route );
  end;

  /* 53a - корреспондент отправителя */
  if( ПолучитьУзелПолучателяПлатежа(wlpmrmprop.PaymentID, rcv) AND
      ПолучитьБлижайшийУзелПлатежа(wlpmrmprop.PaymentID, RTDIR_OUT, Route) AND
      (Route.RouteID!=rcv.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог( "53"/*Sender_sCorrespondentField*/, "53a", Route );
  end;

  /* 54a - корреспондент получателя */
  if( ПолучитьУзелПолучателяПлатежа(wlpmrmprop.PaymentID, rcv) AND 
      ПолучитьБлижайшийУзелПлатежа(wlpmrmprop.PaymentID, RTDIR_OUT, ocb) AND
      (rcv.RouteID!=ocb.RouteID) AND
      ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
      (rcv.RouteID!=Route.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог( "54"/*Receiver_sCorrespondentField*/, "54a", Route );
  end;

  /* 56a - банк посредник */
  if( ПолучитьСамыйДальнийУзелПлатежа(wlpmpaym.PaymentID, RTDIR_OUT, acc) AND
      ПолучитьУзелПолучателяПлатежа(wlpmpaym.PaymentID, rcv) AND
      ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
      (Route.RouteID!=acc.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог( "56"/*IntermediaryField*/, "56a", Route );
  end;

  /* 57a - банк бенефициара */
  if( ПолучитьУзелПолучателяПлатежа(wlpmpaym.PaymentID, rcv) AND
      ПолучитьСамыйДальнийУзелПлатежа(wlpmpaym.PaymentID, RTDIR_OUT, Route) AND
      (Route.RouteID!=rcv.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог( "57"/*AccountWithInstitutionField*/, "57a", Route );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

