/*
  $Name:         batchgd_rep.mac
  $Module:       МБР
  $Description:  Отчет о генерации учетных объектов по сообщениям
*/

import "wldoc.mac";

macro DrawHead()

  [
     Отчет о генерации учетных объектов по сообщениям 

     Операционист:       ####                        Время формирования отчета: #
     Филиал:                #
     Операционный день:  ##########
  
  ]
  ( {oper},
    string(date()) + " " + string(time()),
    {OperDprt},
    {curdate}
  );

end;

macro DrawReport_1()
  var rs:object;
  var select:string;
  var i, j:integer;
  array StringArray;


  select = "select distinct t_MesID, t_TRN, t_State, t_ErrorMessage "+
             "from dwlgpmmes_tmp "+
            "order by t_MesID ";

  rs = execSQLselect( select, null, false );

  if( not rs.moveNext() )
    return;
  end;

  [  Обработанные сообщения:
     ┌──────────────┬──────────────────────────┬──────────────┬────────────────────────────────────────┐
     │ ID сообщения │         Референс         │ Номер ошибки │                Сообщение               │];

  i = 1;
  while( (i == 1) or rs.moveNext() )

    StrSplit( StrSubst(IfThenElse(rs.value(3)!="",rs.value(3)," ") , "|", " " ), StringArray, 40 );                
     
    [  ├──────────────┼──────────────────────────┼──────────────┼────────────────────────────────────────┤    
       │##############│##########################│##############│########################################│]
    (
      rs.value(0):c,
      rs.value(1):c,
      rs.value(2):c,
      StringArray(0)
    );

    j = 1;
    while ( StrLen(StringArray(j) ) > 0)
      [  │              │                          │              │########################################│]
      (StringArray(j));
      j = j + 1;
    end;

    i = i + 1;
  end;

  [  └──────────────┴──────────────────────────┴──────────────┴────────────────────────────────────────┘];

  [   
     Всего сообщений:    # 

  ]
  ( i - 1 );

  return 0;

end;

macro DrawReport_2()
  var rs:object;
  var select:string;
  var i, j:integer;
  array StringArray;


  select = "select rep.t_MesID, rep.t_TRN, rep.t_PaymentID, rep.t_Number, rep.t_Date, fi.t_FI_Code, rep.t_Amount "+
             "from dwlgpmmes_tmp rep, "+
                 " dfininstr_dbt fi "+
            "where rep.t_PaymentID <> 0 "+
              "and fi.t_FIID = rep.t_FIID "+
            "order by rep.t_MesID ";

  rs = execSQLselect( select, null, false );

  if( not rs.moveNext() )
    return;
  end;


  [  Созданные платежи:
     ┌──────────────┬──────────────────────────┬────────────┬───────────────────┬──────────┬────────┬─────────────┐
     │ ID сообщения │         Референс         │ ID платежа │      Номер        │   Дата   │ Валюта │    Сумма    │];


  i = 1;
  while( (i == 1) or rs.moveNext() )
 
    [  ├──────────────┼──────────────────────────┼────────────┼───────────────────┼──────────┼────────┼─────────────┤
       │##############│##########################│############│###################│##########│########│#############│]
    (
      rs.value(0):c,
      rs.value(1):c,
      rs.value(2):c,
      rs.value(3):c,
      date(rs.value(4)),
      rs.value(5):c,
      rs.value(6):c
    );

    i = i + 1;
  end;

  [  └──────────────┴──────────────────────────┴────────────┴───────────────────┴──────────┴────────┴─────────────┘];

  [   
     Всего платежей:    # 
  ]( i - 1 );

  select = "select count(distinct t_MesID) "+
             "from dwlgpmmes_tmp "+
            "where t_PaymentID <> 0 ";

  rs = execSQLselect( select, null, false );

  if( rs and rs.moveNext() )
    [   
       Всего сообщений:    # 
    
    ]( int(rs.value(0)) );
  end;

  return 0;

end;

macro DrawReport()

  DrawHead();

  DrawReport_1();

  DrawReport_2();

  var rs = execSQLselect( " select 1 from dpmproc_tmp ", null, false );

  if(rs and rs.MoveNext())
    [  Операции, выполненные по платежам: ];
  end;

end;

