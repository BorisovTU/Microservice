/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                   Макрос проверки сообщений SBRF3                        */
/*                                                                          */
/*  Имя файла: sbchkey.mac                                                  */
/*  Создан:    05.07.01                                      Алешин А.В.    */
/*                                                                          */
/****************************************************************************/

import BankInter, "wlchmes.mac", "sbblock.mac", FIInter;

/* Непосредственно макрос проверки полей сообщения */ 
macro SB_CheckMes( SBRFStr )
  var field_value;  

  /* Закомментировать эти строки если не надо проверять соответствие сообщения платежу */
  if( FillBuffPayment( wlmes ) )
     
     /* Если это сообщение платежа, то делаем дополнительные проверки */
     if ( not CheckMessagesPayment(wlpm) )
       std.msg( "Платежу соответствует несколько одинаковых сообщений|Откатите выгрузку в МБР данного платежа и попробуйте снова." );
       return false;
     end;

     /* Проверка суммы */
     if( ReadFieldSBRF3( field_value, "AM", SBRFStr ) AND (money(doubleL( field_value )/100)!=wlpmpaym.PayAmount) )
       std.msg( string( "Сумма платежа не соответствует сообщению.|Повторите выгрузку в МБР данного платежа") );
       return false;
     end;

     /* Проверка финансового инструмента */
     if( ReadFieldSBRF3( field_value, "CU", SBRFStr ) )
        if( not ПолучитьФинИнПоISO( field_value, wlfininstr ) )
          std.msg( string( "Не гайден финансовый инструмент по коду ISO: ", field_value ) );
          return false;
        end;
        if ( wlfininstr.FIID!=wlpmpaym.PayFIID )
          std.msg( string( "Валюта платежа не соответствует сообщению.|Повторите выгрузку в МБР данного платежа") );
          return false;
        end;
     end;
      
     /* Проверка номера счета плательщика */
     if( ReadFieldSBRF3( field_value, "SA", SBRFStr ) AND (StrUpr(field_value)!=StrUpr(wlpmpaym.PayerAccount)) )
       std.msg( string( "Счет плательщика платежа не соответствует сообщению.|Повторите выгрузку в МБР данного платежа") );
       return false;
     end;
     
     /* Проверка номера счета получателя */
     if( ReadFieldSBRF3( field_value, "RA", SBRFStr ) AND (StrUpr(field_value)!=StrUpr(wlpmpaym.ReceiverAccount)) )
       std.msg( string( "Счет получателя платежа не соответствует сообщению.|Повторите выгрузку в МБР данного платежа") );
       return false;
     end;
  end;


  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return false;
end;

/* Контроль сообщений СБРФ */ 
macro CheckMes( addrMes )
  var SBRFStr = "", field_name, field_value;

  SetBuff( wlmes, addrMes );

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return false;
  end;

  /* Формируем строку сообщения в формате СБРФ для ключевания */
  SBRFStr = f_wlmesfrm.Name + wlmes.TRN;
  while( СчитатьПоле( field_name, field_value ) )
     SBRFStr = SBRFStr + MakeFieldSBRF3( field_name, field_value, false, false );
  end;

  return SB_CheckMes( SBRFStr );

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return false;
end;

/* Контроль сообщений СБРФ */ 
macro CheckMesPIB( addrMes )
  var SBRFStr = "", field_name = "", field_value;

  SetBuff( wlmes, addrMes );

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return false;
  end;

  /* Формируем строку сообщения в формате СБРФ для ключевания */
  SBRFStr = f_wlmesfrm.Name + wlmes.TRN;
  if( not СчитатьПоле( field_name, field_value ) OR (field_name != SB_Tag_PIB) OR (field_value == "") )
    std.msg( String("В сообщении по форме:", f_wlmesfrm.Name, " референс: ", wlmes.TRN, "|не заполнено поле '", SB_Tag_PIB, "'") );
    return false;
  end;  

  SBRFStr = SBRFStr + field_value;

  return SB_CheckMes( SBRFStr );

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return false;
end;
