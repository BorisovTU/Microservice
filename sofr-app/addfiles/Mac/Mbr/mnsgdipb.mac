 /*
 $Name:        mnsgdipb.mac
 $Module:      МБР
 $Description: Генерация подтверждений о доставке по сообщению IPB
 */


/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений о доставке по сообщению IPB                      */
/*                                                                          */
/*  Имя файла: mnsgdipb.mac                                                 */
/*  Создан:    21.12.04                                  Фомченкова Л.Н.    */
/****************************************************************************/

import "wlmnstls.mac", WldInter, oralib, likepy, PSInter;


var ErrList = Tarray;

/* Информация из полей сообщения */
class TMesInfo
 var
  IdFile            :string   = "",      
  IdDoc             :string   = "",      
  //NumAcc            :string   = "",      
  DelivDate         :date     = date(0,0,0),
  NumMes            :string   = "",
  RegOrgKind        :string   = "";

  macro Clear()
    IdFile = IdDoc = NumMes = RegOrgKind = "";
    DelivDate = date(0,0,0);
  end;
end;

var Obj:object = TMesInfo();

private macro InitGlobals()
  Obj.Clear();
  ErrList.Size = 0;
end;

macro Fill1(val)
  Obj.IdFile = val;
end;

macro Fill2(val)
  Obj.IdDoc = val;
end;

/*macro Fill3(val)
  var Obj:object;
  if( ( AccList.Size == 0 ) or ( AccList(AccList.Size - 1).NumAcc != "" ) )
    Obj = AccInfo();
    AccList(AccList.Size) = Obj;
  else
    Obj = AccList.Value(AccList.Size - 1);
  end;
  Obj.NumAcc = val;
end;*/

macro Fill4(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).ErrCode != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.ErrCode = val;
end;

macro Fill5(val)
  Obj.DelivDate = val;
end;

macro Fill6(val)
  Obj.NumMes = val;
end;

macro Fill7(val)
  Obj.RegOrgKind = val;
end;

macro FillErrName(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).ErrName != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.ErrName = val;
end;

macro FillAttrCode(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).AttrCode != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.AttrCode = val;
end;

macro FillAttrValue(val)
  var ErrObj:object;
  if( ( ErrList.Size == 0 ) or ( ErrList(ErrList.Size - 1).AttrValue != "" ) )
    ErrObj = TErrInfo();
    ErrList(ErrList.Size) = ErrObj;
  else
    ErrObj = ErrList.Value(ErrList.Size - 1);
  end;
  ErrObj.AttrValue = val;
end;

/* Заполнение списка полей формы  IPB*/
var Fld1  = ТПолеМНС( "ИдФайл",      "Fill1" ),
    Fld2  = ТПолеМНС( "ИдДок",       "Fill2" ),
    //Fld3  = ТПолеМНС( "НомСч",      "Fill3" ),    
    Fld4  = ТПолеМНС( "КодОшибки",   "Fill4" ),    
    Fld5  = ТПолеМНС( "ДатаОбр",     "Fill5" ),    
    Fld6  = ТПолеМНС( "НомСооб",        "Fill6" ),    
    Fld7  = ТПолеМНС( "_ВидРегОргана",  "Fill7" ),
    Fld8  = ТПолеМНС( "НаимОшибки",  "FillErrName"  ),
    Fld9  = ТПолеМНС( "КодРекв",     "FillAttrCode"  ),
    Fld10 = ТПолеМНС( "ЗначРекв",    "FillAttrValue" );    

macro Construct()
  var field_name, field_value, fldd, i;
  /* Последовательно считываем поля и заполняем лексемы */
  while(СчитатьПоле(field_name, field_value))  
      i = IndexMNS(field_name);
      if (i != -1)
         fldd = ПоляМНС.Value(i);    
         fldd.ОбработатьПолеФормы( field_value ); 
      end;
  end;
  return TRUE;
end; /* Construct */

macro Check(ai)
  if((not ai.IdFile) or (not ai.IdDoc) )
     return FALSE;
  end;
  return TRUE;
end; 

private macro GetUPPERShortFileName( MesID:integer )

  var select:string = "select substr(sess.t_filename, instr(sess.t_filename, '\\', -1, 1) + 1)" +
                         "from dwlmes_dbt mes, dwlsess_dbt sess " +
                        "where mes.t_MesID = :MesID " +
                          "and mes.t_sessionid = sess.t_sessionid";
  var params = makeArray( SQLParam( "MesID", wlmes.MesID ) );

  var rs = execSQLSelect( select,params,true );
  if( rs.MoveNext() )
    return StrUpr( string(rs.value(0)) );
  end;
  return "";
end;

/* Релиз IPB */
macro GenDoc( addrMes )

  var field_name, field_value, count = 0, ai, tablestr, condstr, isFoundMes = false, mesFileName = "";
  SetBuff( wlmes, addrMes );
  // параметры функций генерации уч.объектов
  var MesID             = 0,
      ErrCode           = 0,
      ErrDescription    = "Не найдено исходное сообщение",
      State             = WLD_STATUS_RESPRM_DEFECT,
      DeliveryDate      = date(0,0,0),
      Description       = "",
      CreateCvt         = true,
      NoChangeState     = true;

  PrintLog(2,"Генерация подтверждения о доставке IPB");
  InitGlobals();
  Construct();
      ai = Obj;
      if(not Check(ai))
         std.msg( string("|Не заполнены все обязательные поля в сообщении ", wlmes.TRN) );
         return false;
      end;

  var InsideAbonentID : integer = 0, 
      InsideAbonentCodeKind : integer = 0, 
      InsideAbonentCode : string = "",
      OutMesTrn : string = "",
      OutMesState : integer = 0;

  // Поиск исходящего сообщения
  MesID = GetKvitSBC( wlmes.MesID, ai.NumMes, @InsideAbonentID, 
                      @InsideAbonentCodeKind, @InsideAbonentCode, 
                      @mesFileName, @OutMesTrn, @OutMesState );

  isFoundMes = (MesID != 0);

      if( isFoundMes )
        if( OutMesState < 25 )
           std.msg( string("|Исходящее сообщение ", OutMesTrn, " не отправлено") );
           return false;
        end;
        wlmes.InsideAbonentID = InsideAbonentID;
        wlmes.InsideAbonentCodeKind = InsideAbonentCodeKind;
        wlmes.InsideAbonentCode = InsideAbonentCode;
      else
        mesFileName = GetUPPERShortFileName( wlmes.MesID );
        if( GetInsideAbonentForSBF( wlmes.Department, @InsideAbonentID, 
                                   @InsideAbonentCodeKind, @InsideAbonentCode )
          )
          wlmes.InsideAbonentID = InsideAbonentID;
          wlmes.InsideAbonentCodeKind = InsideAbonentCodeKind;
          wlmes.InsideAbonentCode = InsideAbonentCode;
        end;
      end;
      UpdateMes(wlmes);

      if( substr(mesFileName,3,1) == "E" )
        if( isFoundMes )
          ErrCode  = FNS_RP_NOT_APPROVED_FNS;
          State    = WLD_STATUS_RESPRM_RECEIVE;
        end;
        
        ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

        if( ( isFoundMes and (strlen(ErrDescription) > 0) ) or not isFoundMes )
          var ResultID : integer = 0;
          if( not ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID ) )
             std.msg( string("|Ошибка при переносе в отбракованные сообщения ", OutMesTrn) ); 
             return false;
          else
            for( var ErrInfo, ErrList )
              fillWlerror(ResultID, ErrInfo, wlerror);
              if ( not ВставитьОшибкуОбработкиСообщения(wlerror) )
                std.msg( string("|Ошибка при создании записи об ошибке обработки сообщения ") ); 
                return false;
              end;
            end;
          end;
        else
          std.msg( string("|Неверный код ошибки в сообщении ", wlmes.Trn) ); 
          return false;
        end;
      else 
        if( isFoundMes )
          if( OutMesState == 25 )
            NoChangeState  = true;
          elif( ( ( substr(mesFileName,3,1) == "F" ) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_PFR )) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_FSS )) ) or
                ( ( substr(mesFileName,3,1) == "P" ) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_FNS )) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_FSS )) ) or
                ( ( substr(mesFileName,3,1) == "R" ) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_PFR )) and (GetDeliveryMes( MesID, REG_ORGAN_KIND_FNS )) ) )
            NoChangeState  = false;
          else
            NoChangeState  = true;
          end;
          DeliveryDate = ai.DelivDate;
          ErrDescription = GetConfDelivDescription( ai.RegOrgKind );
          if ( not ВставитьПодтверждениеОДоставке( MesID, NoChangeState, DeliveryDate, ErrDescription, ErrDescription, CreateCvt ) )
              std.msg( string( "|Ошибка при переносе в закрытые сообщения ", MesID ) ); 
            return false;
          end;
        elif( not ОшибкаЛогическогоКонтроляСообщения( MesID, ErrCode, ErrDescription, State, true, ResultID ) )
          std.msg( string("|Ошибка при переносе в отбракованные сообщения ", OutMesTrn) ); 
          return false;
        end;
      end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;