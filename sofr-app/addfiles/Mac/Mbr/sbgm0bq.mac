/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация сообщения-запроса на получение выписки SBRF3                  */
/*                                                                          */
/*  Создан:     10.02.03                                                    */
/*  Программист Бабин А.П.                                                  */
/****************************************************************************/

import "sbgenmes.mac";

File Corschem( corschem ) key 0;

macro GenMesExec( MacroProc, Parm )
  var field_value, CodeKind = PTCK_CLIRING, error, ИскатьВышестоящих;
  
  ExecMacro( MacroProc, "DT", type_709, Parm );  

  ИскатьВышестоящих = 1;
  field_value = ПолучитьКодСубъекта( {OurBank}, CodeKind, error, ИскатьВышестоящих );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Не найден код банка-плательщика" );
  end;
  ExecMacro( MacroProc, "PA", CB_StrSubst0( String(field_value:10) ), Parm );

  Corschem.Number = wlreq.Corschem;
  Corschem.FIID   = wlreq.FIID;
  Corschem.FI_Kind = 1;
  if( not GetEQ(Corschem) )
    RunError( String("|Не найдена корсхема №", wlreq.Corschem) );
  end;

  field_value = ПолучитьКодСубъекта( Corschem.CorrID, CodeKind, error );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Не найден код банка-получателя" );
  end;
  ExecMacro( MacroProc, "RC", CB_StrSubst0( String(field_value:10) ), Parm );

  ExecMacro( MacroProc,"CU", ПолучитьКодВалютыЛог(wlreq.FIID), Parm );

  if ( wlreq.SubKind==0 )
     ExecMacro( MacroProc,"VT", "019", Parm );
  else
     ExecMacro( MacroProc,"VT", "020", Parm );
  end;

  ExecMacro( MacroProc, "VB", ДатаДДММГГ( wlreq.PmDateValue ), Parm );

  /* Дата последней обработки */
  ExecMacro( MacroProc,  "LD", ДатаДДММГГ( {curdate} ), Parm );  

  SetParm( 1, Parm );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;

/* Релиз 0B7 */
macro GenMes( addrMes, addrReq )
  var format = true;

  SetBuff( wlmes, addrMes );
  SetBuff( wlreq, addrReq );

  PrintLog( 2,"Генерация сообщения-запроса на получение выписки SBRF3, релиз 0BQ");    

  return GenMesExec( "SB_ЗаписатьПолеЛог", format );

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;

/* Релиз 0BQJ */
macro GenMesPIB( addrMes, addrReq )
  var BCI = "";

  SetBuff( wlmes, addrMes );
  SetBuff( wlreq, addrReq );

  PrintLog( 2,"Генерация сообщения-запроса на получение выписки SBRF3, релиз 0BQJ");    

  if( not GenMesExec( "SB_ЗаполнитьПоле", BCI ) )
    return false;
  end;

  SB_ЗаписатьПолеЛог( SB_Tag_PIB, BCI, true );

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;