/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : gnreqdec.mac
  Created     : 17.10.2011
  Programmer  : Popova O.
  Description : Протокол генерации УО "Справка ФНС"

└───────────────────────────────────────────────────────────────────────────*/
import globals, oralib, likepy, WldInter, MesInter, CTInter;

/* Вспомогательное */

record wlregdec( wlregdec );

private macro GetObjectType( type:integer ):string

  var rset:RsdRecordset = execSQLselect( "SELECT t_name FROM dobjects_dbt WHERE t_objecttype = :type", makeArray( SQLParam( "type", type ) ), TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return "";
end;
private macro GetStatusName( State:integer ):string

  var rset:RsdRecordset = execSQLselect( "SELECT t_name FROM dwlstatus_dbt WHERE t_state = :state AND t_typestate = 29 ", makeArray( SQLParam( "state", State ) ), TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return "";
end;
private macro GetOperName():string

  var rset:RsdRecordset = execSQLselect( "SELECT t_name FROM dperson_dbt WHERE t_Oper = :Oper ", makeArray( SQLParam( "Oper", {Oper} ) ), TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return "";
end;
/* Текущая таблица */
private var CurTable = 0;

/* Данные справки для отчета */
private class ( TbFile ) CFNSInfoGenRepData( _ID:integer )
  private var DocType     :string = "";
  private var StatusName  :string = "";
  private var AccList     :object = NULL;
  private var recWlAcc    :TRecHandler = TRecHandler( "wlacclnk.dbt" );
  private var FirstNote     :string = "";/*Примечание к первой строке отчета - если нет примечаний к счетам, то примечание к greqdec или к справке, иначе - примечание к первому счету*/
  private var FirstAccDescr :string = "";/*Примечание к первому счету отчета */
  InitTbFile( "greqdec.tmp", "bank.def" );
  rec.ID = _ID;
  if( GetEQ( this ) )
    AccList = RsbWlAccLnk( rec.DecisionID, OBJTYPE_FNSINFO );
    DocType = GetObjectType( rec.LnkDocType );
    StatusName = GetStatusName( rec.State );
            
    if( AccList.First( recWlAcc ) == 0 )
      FirstNote = FirstAccDescr = recWlAcc.rec.Description;
    end;
    while( ( FirstNote == "" ) and ( AccList.Next( recWlAcc ) == 0 ) )
      FirstNote = FirstNote + recWlAcc.rec.Description;
    end;
    if( FirstNote == "" )
      if( rec.Description != "" )
        FirstNote = rec.Description;
      else
        wlregdec.DecisionID = rec.DecisionID;
        FirstNote = readNoteForObject( OBJTYPE_FNSINFO, makeObjectID( OBJTYPE_FNSINFO, NULL, wlregdec ), 1 );
      end;
    else
      FirstNote = FirstAccDescr;
    end;
  end;
  /* Вывести строку отчета */
  private macro PrintStr( isFirst:bool )
    if( CurTable != WLD_TYPE_REGDEC_IVS )
[│##################│##########│##########│############│############################│##########│#######################│#################################│############│]
( IfThenElse( isFirst, DocType             , "" ):w, 
  IfThenElse( isFirst, rec.LnkDocNumber    , "" ):w, 
  IfThenElse( isFirst, rec.LnkDocDate      , "" ):w, 
  IfThenElse( isFirst, rec.ClientINN       , "" ):w,
  IfThenElse( isFirst, rec.ClientName      , "" ):w,
  IfThenElse( isFirst, rec.Date            , "" ),
  recWlAcc.rec.Account                           ,
  IfThenElse( isFirst, FirstNote           ,  recWlAcc.rec.Description ):w,
  IfThenElse( isFirst, StatusName          , "" ):w );
    else
[│###############│#########│##########│############│##########################│##########│##########│#######################│#################################│############│]
( IfThenElse( isFirst, DocType             , "" ):w, 
  IfThenElse( isFirst, rec.LnkDocNumber    , "" ):w, 
  IfThenElse( isFirst, rec.LnkDocDate      , "" ):w, 
  IfThenElse( isFirst, rec.ClientINN       , "" ):w,
  IfThenElse( isFirst, rec.ClientName      , "" ):w,
  IfThenElse( isFirst, rec.Date            , "" ),
  IfThenElse( isFirst, rec.EndDate         , "" ):w,
  recWlAcc.rec.Account                           ,
  IfThenElse( FirstNote == "", recWlAcc.rec.Description, FirstNote ):w,
  IfThenElse( isFirst, StatusName          , "" ):w );
    end;
  end;
  /* Вывести документ */
  macro PrintDoc()
    var isFirst = true;
    if( AccList.First( recWlAcc ) == 0 )
      PrintStr( true );
      while( AccList.Next( recWlAcc ) == 0 ) 
        PrintStr( false );
      end;
    else 
      PrintStr( true );
    end; 
  end;

end;

/* Вывести заголовок отчета */
private macro PrintHeaderTable( kind:integer )

  if( kind == WLD_TYPE_REGDEC_IOS )
[
Справки об остатках на счетах клиентов
┌────────────────────────────────────────┬─────────────────────────────────────────┬──────────┬───────────────────────┬─────────────────────────────────┬────────────┐
│              Ответ на                  │                Клиент                   │   Дата   │      Номер счета      │         Примечание              │   Статус   │
├──────────────────┬──────────┬──────────┼────────────┬────────────────────────────┤ справки  │                       │                                 │   справки  │
│ Вид документа    │   Номер  │   Дата   │    ИНН     │        Наименование        │          │                       │                                 │            │
├──────────────────┼──────────┼──────────┼────────────┼────────────────────────────┼──────────┼───────────────────────┼─────────────────────────────────┼────────────┤];
  elif( kind == WLD_TYPE_REGDEC_INS )
    if( CurTable != 0 )
[└──────────────────┴──────────┴──────────┴────────────┴────────────────────────────┴──────────┴───────────────────────┴─────────────────────────────────┴────────────┘];
    end;
[
Справки о наличии счетов
┌────────────────────────────────────────┬─────────────────────────────────────────┬──────────┬───────────────────────┬─────────────────────────────────┬────────────┐
│              Ответ на                  │                Клиент                   │   Дата   │      Номер счета      │         Примечание              │   Статус   │
├──────────────────┬──────────┬──────────┼────────────┬────────────────────────────┤ справки  │                       │                                 │   справки  │
│ Вид документа    │   Номер  │   Дата   │    ИНН     │        Наименование        │          │                       │                                 │            │
├──────────────────┼──────────┼──────────┼────────────┼────────────────────────────┼──────────┼───────────────────────┼─────────────────────────────────┼────────────┤
];
  elif( kind == WLD_TYPE_REGDEC_IVS )
    if( CurTable != 0 )
[└──────────────────┴──────────┴──────────┴────────────┴────────────────────────────┴──────────┴───────────────────────┴─────────────────────────────────┴────────────┘];
    end;
[
Выписки об операциях по счетам клиентов
┌────────────────────────────────────┬───────────────────────────────────────┬──────────┬──────────┬───────────────────────┬─────────────────────────────────┬────────────┐
│              Ответ на              │                Клиент                 │   Дата   │   Дата   │      Номер счета      │         Примечание              │   Статус   │
├───────────────┬─────────┬──────────┼────────────┬──────────────────────────┤  начала  │ окончания│                       │                                 │   выписки  │
│ Вид документа │  Номер  │   Дата   │    ИНН     │        Наименование      │  периода │  периода │                       │                                 │            │
├───────────────┼─────────┼──────────┼────────────┼──────────────────────────┼──────────┼──────────┼───────────────────────┼─────────────────────────────────┼────────────┤];
  end;

end;

/* Протокол процедуры генерации справок и выписок */
macro  WldPrintReportGenFNSInfo()
[
 Протокол процедуры генерации справок и выписок

 Дата отчета: ##########
 Исполнитель: #### #            

]( {curdate}, {Oper}, GetOperName() );

  var rset:RsdRecordset = execSQLselect( "SELECT t_ID, t_Type FROM dgreqdec_tmp ORDER BY t_Type, t_Date", NULL, TRUE );
  if( rset )       
    while( rset.moveNext() )         
      if( CurTable != rset.value(1) )
        PrintHeaderTable( rset.value(1) );
        CurTable = rset.value(1);
      end;                                         
      CFNSInfoGenRepData( rset.value(0) ).PrintDoc();
    end;
    if( CurTable == WLD_TYPE_REGDEC_IVS )
[└───────────────┴─────────┴──────────┴────────────┴──────────────────────────┴──────────┴──────────┴───────────────────────┴─────────────────────────────────┴────────────┘];
    elif( CurTable > 0 )
[└──────────────────┴──────────┴──────────┴────────────┴────────────────────────────┴──────────┴───────────────────────┴─────────────────────────────────┴────────────┘];
    end;
  end;
  return 0;
end;

