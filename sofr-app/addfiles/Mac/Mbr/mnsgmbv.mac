/*
$Name:               mnsgmbv.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения по BOS
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по BOS                                               */
/*                                                                          */
/*  Имя файла: mnsgmbv.mac                                                  */
/*  Создан:  27.10.11                                    Чукина Т.А.        */
/****************************************************************************/

import "wlgenmes.mac", "wlmnstls.mac", "likepy.mac", PTInter, FIInter, OprInter, WldInter;

private record party ( party );
private var persn = Tbfile("persn.dbt", "r");

private macro isExistsWlConf(HeadID, HeadKind, Account, FIID, NumberPart) : bool
  var WlconfFound = false;
  var query = " select count(1) " +
              "   from dwlconf_dbt " +
              "  where t_HeadID = " + HeadID +
              "    and t_HeadKind = " + HeadKind +
              "    and t_Account = '" + Account + "' " +
              "    and t_FIID = " + FIID +
              "    and t_NumberPart = " + NumberPart;
  var rs : RsdRecordset = execSQLselect(query);
  if(rs and rs.moveNext)
    WlconfFound = (rs.value(0) > 0);
  end;
  return WlconfFound;
end;

private macro ФормированиеДанныхКонтрагента(rs_wlconf: RsdRecordset, wlregdec)
  /*НомСчПП*/
  var AccountPP = rs_wlconf.value("t_ClientAccount"); /* значение поля "НомСчПП" */
  if (AccountPP == "") AccountPP = " "; end;
  var INNPP = rs_wlconf.value("t_ReceiverINN");
  
  /*НаимПП / ФИОПП*/
  var IsPerson : bool = (wlregdec.ClientType != RO_CLIENTTYPE_INST);
  if (IsPerson)
    ЗаписатьПолеЛог("ФИОПП", substr( rs_wlconf.value("t_ClientName"), 1, 160 ));
  else
    ЗаписатьПолеЛог("НаимПП", substr( rs_wlconf.value("t_ClientName"), 1, 160 ));
  end;

  /*ИННПП*/
  if( (INNPP == "") or (strlen(INNPP) > 12) or (not StrIsNumber(INNPP)) )
    INNPP = " ";
  end;
                
  ЗаписатьПолеЛог("ИННПП", INNPP);
  /*КПППП*/
  var KPPPP = rs_wlconf.value("t_ReceiverKPP");
  if (IsPerson or (not StrIsNumber(KPPPP)) or (strlen(KPPPP) > 9))
    KPPPP = " ";
  end;
  ЗаписатьПолеЛог("КПППП", KPPPP);

  /*НомСчПП*/
  ЗаписатьПолеЛог("НомСчПП", AccountPP);

end;

private macro ОбработатьСчет( recAcc:TRecHandler, PartNum, wlregdec )

  record Fin( fininstr ); ClearRecord( Fin );
  record Acc( account  ); ClearRecord( Acc );
  var isIncludeAcc = 0; GetRegValForOPENAC( "ВКЛЮЧАТЬ_ЗАКР_СЧЕТА_ВЫПИСКА", V_INTEGER, isIncludeAcc );
  var tmpstr = "", FullINN = "";

  if( not IsLnkAccountPrcError(recAcc, WLD_TYPE_REGDEC_IVS) // если не было ошибок при обработке счета
      and
      ( ( recAcc.rec.LnkObjectID == 0 ) or
        ( recAcc.rec.LnkObjectID == int( PartNum ) ) or
          isExistsWlConf( wlregdec.DecisionID, WLCONF_HEADKIND_WLREGDEC, recAcc.rec.Account, recAcc.rec.FIID, int( PartNum ) )
      )
    )
    УстановитьКонтекстБлокаЛог( "ВыпискаПоСчету" );
    /*НомСч*/
    ЗаписатьПолеЛог( "НомСч", recAcc.rec.Account );
    /*ВалСч*/  
    ПолучитьФинИн( recAcc.rec.FIID, Fin );
    ЗаписатьПолеЛог( "ВалСч", Fin.ISO_Number );
    /*ДатаНачала*/
    ЗаписатьПолеЛог( "ДатаНачала", DDpMMpYYYY( wlregdec.Date ) );
    /*ДатаКонца*/
    ЗаписатьПолеЛог( "ДатаКонца", DDpMMpYYYY( wlregdec.EndDate ) );
    /*ОстатНач*/
    ЗаписатьПолеЛог( "ОстатНач", string( recAcc.rec.RestIn ) );
    /*СуммаДеб*/
    ЗаписатьПолеЛог( "СуммаДеб", string(recAcc.rec.DebetTurn) );
    /*СуммаКред*/
    ЗаписатьПолеЛог( "СуммаКред", string(recAcc.rec.CreditTurn) );
    /*ОстатКон*/
    ЗаписатьПолеЛог( "ОстатКон", string( recAcc.rec.RestOut ) );

    /*Блок "ВыпискаПоСчету\ОперацииПоСчету"*/
    var query = string (
"SELECT ad.t_DateValue,\n",
"       ad.t_ShifrOper,\n",
"       ad.t_Number,\n",
"       NVL (prp.t_Date, ad.t_Date_Carry) t_Date_Carry,\n",
"       ad.t_BankAcc,\n",
"       ad.t_BankName,\n",
"       ad.t_CodeValue t_BankCode,\n",
"       ad.t_Sum,\n",
"       ad.t_DKFlag,\n",
"       ad.t_Description,\n",
"       NVL (pp.t_PaymentID, 0) t_PaymentID,\n",
"       ad.t_ReceiverAccount t_ClientAccount,\n",
"       ad.t_ReceiverName t_ClientName,\n",
"       ad.t_UsePayment,\n",
"       ad.t_ReceiverINN,\n",
"       ad.t_ReceiverKPP,\n",
"       pp.t_ReceiverBankID"
"  FROM (  SELECT wlconf.t_Sum,\n",
"                 wlconf.t_DKFlag,\n",
"                 wlconf.t_Description,\n",
"                 DECODE (wlconf.t_DKFlag,\n"
"                         1, ad.t_FIID_Receiver,\n"
"                         ad.t_FIID_Payer) t_FIID,\n"
"                 wlconf.t_DateValue,\n",
"                 wlconf.t_ShifrOper,\n",
"                 wlconf.t_Number,\n",
"                 wlconf.t_BankAcc,\n",
"                 wlconf.t_BankName,\n",
"                 wlconf.t_CodeValue,\n",
"                 wlconf.t_ReceiverName,\n",
"                 wlconf.t_ReceiverINN,\n",
"                 wlconf.t_ReceiverKPP,\n",
"                 wlconf.t_ReceiverAccount,\n",
"                 DECODE (wlconf.t_DKFlag,\n",
"                         1, ad.t_Account_Receiver,\n",
"                         ad.t_Account_Payer) t_Account,\n",
"                 ad.t_Date_Carry,\n",
"                 pd.t_PaymentID,\n",
"                 CASE\n",
"                    WHEN    pd.t_PaymentID IS NULL\n",
"                         OR EXISTS\n",
"                               (SELECT 1\n",
"                                  FROM dpmaddpi_dbt pma\n",
"                                 WHERE     pma.t_PaymentID = pd.t_PaymentID\n",
"                                       AND pma.t_DebetCredit =\n",
"                                              DECODE (wlconf.t_DKFlag, 1, 1, 0))\n",
"                    THEN\n",
"                       0\n",
"                    ELSE\n",
"                       1\n",
"                 END\n",
"                    t_UsePayment\n",
"            FROM dwlconf_dbt wlconf, dpmdocs_dbt pd, dacctrn_dbt ad\n",
"           WHERE     wlconf.t_HeadID = ?\n",
"                 AND wlconf.t_HeadKind = " + WLCONF_HEADKIND_WLREGDEC + "\n",
"                 AND wlconf.t_Account = ?\n",
"                 AND wlconf.t_FIID = ?\n",
"                 AND wlconf.t_NumberPart = ?\n",
"                 AND ad.t_AccTrnID = wlconf.t_CarryID\n",
"                 AND pd.t_AccTrnID(+) = ad.t_AccTrnID\n",
"        ORDER BY wlconf.t_ConfID) ad,\n",
"       dpmpaym_dbt pp,\n",
"       dpmrmprop_dbt prp,\n",
"       dpmprop_dbt ppd,\n",
"       dpmprop_dbt ppc,\n",
"       daccount_dbt acc\n",
" WHERE     pp.t_PaymentID(+) = ad.t_PaymentID\n",
"       AND prp.t_PaymentID(+) = ad.t_PaymentID\n",
"       AND ppd.t_PaymentID(+) = ad.t_PaymentID\n",
"       AND ppd.t_DebetCredit(+) = 0\n",
"       AND ppc.t_PaymentID(+) = ad.t_PaymentID\n",
"       AND ppc.t_DebetCredit(+) = 1\n",
"       AND acc.t_Chapter(+) = 1\n",
"       AND acc.t_Code_Currency(+) = ad.t_FIID\n",
"       AND acc.t_Account(+) = ad.t_Account");

    var rs_wlconf:RsdRecordset = execSQLselect(query, 
        MakeArray(SQLParam("", wlregdec.DecisionID),
          SQLParam("", recAcc.rec.Account),
          SQLParam("", recAcc.rec.FIID),
          SQLParam("", int( PartNum ))));

    if (rs_wlconf)
      while ( rs_wlconf.moveNext() )
        УстановитьКонтекстБлокаЛог( "ОперацииПоСчету" );
        ЗаписатьПолеЛог( "ДатаОпер", DDpMMpYYYY( rs_wlconf.value( "t_DateValue" ) ) );
        ЗаписатьПолеЛог( "ВидДок", rs_wlconf.value( "t_ShifrOper" ) );
        var DocNumLen = strLen( rs_wlconf.value( "t_Number" ) );
        if( DocNumLen <= 6 )
          ЗаписатьПолеЛог( "НомДок", rs_wlconf.value( "t_Number" ) );
        else /* последние 6 символов */
          ЗаписатьПолеЛог( "НомДок", subStr( rs_wlconf.value( "t_Number" ), DocNumLen - 5 ) );
        end;

        /*ДатаДок*/
        ЗаписатьПолеЛог( "ДатаДок", DDpMMpYYYY( rs_wlconf.value("t_Date_Carry") ) );
        
        /*НомКорСч*/
        ЗаписатьПолеЛог( "НомКорСч", rs_wlconf.value("t_BankAcc") );

        /*НаимБП*/
        ЗаписатьПолеЛог( "НаимБП", substr( rs_wlconf.value("t_BankName"), 1, 160 ) );

        /*БИКБП*/
        tmpstr = rs_wlconf.value("t_BankCode");
        ЗаписатьПолеЛог( "БИКБП", tmpstr );

        ФормированиеДанныхКонтрагента(rs_wlconf, wlregdec);

        /*Дебет*/
        if( rs_wlconf.value( "t_DKFlag" ) == WL_DEBET )
          ЗаписатьПолеЛог( "Дебет", string( rs_wlconf.value( "t_Sum" ) ) );
        else
          ЗаписатьПолеЛог( "Дебет", "0.00" );
        end;

        /*Кредит*/
        if( rs_wlconf.value("t_DKFlag") == WL_CREDIT )
          ЗаписатьПолеЛог( "Кредит", string( rs_wlconf.value( "t_Sum" ) ) );
        else
          ЗаписатьПолеЛог( "Кредит", "0.00" );
        end;

        /*НазнПл*/
        ЗаписатьПолеЛог( "НазнПл", substr( rs_wlconf.value( "t_Description" ), 1, 210 ) );

        УстановитьКонтекстБлокаЛог( ".." ); // Блок "ОперацииПоСчету"
      end;
    end;

    УстановитьКонтекстБлокаЛог( ".." ); // Блок "ВыпискаПоСчету"    
  end;
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record OldMes(wlmes);
  ClearRecord(OldMes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlregdec, addrRegdec );
  SetBuff( OldMes,   addrOldMes );

  // чтение глобализмов
  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();
  var CountPartMesFNS : integer = GetCountPartMesFNS();
  var NumPartMesFNS   : integer = GetNumPartMesFNS();

  PrintLog(2,"Генерация сообщения по BV");

  var PartNum = ""; // Номер части

  var Error = 0, Code = "", ind = 0, FullINN = "";  
  
  /*ИдФайл*/
  ЗаписатьПолеЛог( "ИдФайл", " " );
  /*ТипИнФ*/
  ЗаписатьПолеЛог( "ТипИнф", "ОПЕРАЦИИПОСЧ" );
  /*ВерсПрог*/
  ЗаписатьПолеЛог( "ВерсПрог", "RS-Bank V.6" );
  /*ТелОтпр*/
  ЗаписатьПолеЛог( "ТелОтпр", " " );
  /*ДолжнОтпр*/
  ЗаписатьПолеЛог( "ДолжнОтпр", " " );
  /*ФИООтпр*/
  ЗаписатьПолеЛог( "ФамОтпр", " " );

  /*КолДок*/
  var tmpstr = "";
  if (OldMes.MesID != 0) //Перегенерация?
    ПолучитьПоле (OldMes.MesID, "КолДок", tmpstr);
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    ПолучитьПоле (OldfnsMessageID, "КолДок", tmpstr);
  elif (CountPartMesFNS > 0)
    tmpstr = string(CountPartMesFNS);
  else
    tmpstr = "1";
  end;
  ЗаписатьПолеЛог( "КолДок", tmpstr );

  /*НомЧасти*/
  if (OldMes.MesID != 0) //Перегенерация?
    if( not ПолучитьПоле(OldMes.MesID, "НомЧасти", PartNum) )
      PartNum = "";
    end;
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    if( not ПолучитьПоле(OldfnsMessageID, "НомЧасти", PartNum) )
      PartNum = "";
    end;
  elif (NumPartMesFNS > 0)
    PartNum = string(NumPartMesFNS);
  else
    PartNum = "";
  end;
  if(PartNum != "")
    ЗаписатьПолеЛог( "НомЧасти", PartNum );
  end;

  /*ВерсФорм*/
  ЗаписатьПолеЛог( "ВерсФорм", "2.01" );
  
  // блок <Выписка по операциям на счете (счетах)> присутствует только в первой части
  if( (PartNum == "1") or (PartNum == "") )
    /*ИдДок*/
    tmpstr = execStoredFunc( "sys_guid", V_STRING );
    ЗаписатьПолеЛог( "ИдДок", tmpstr );
    /*НомВыпис*/
    ЗаписатьПолеЛог( "НомВыпис", wlregdec.Number );

    FullINN = GetPartyINN(wlregdec.OriginatorID, 1);
    /*ИННКО*/
    ЗаписатьПолеЛог( "ИННКО", RemoveKPP( FullINN ) );
    /*КППКО*/
    ЗаписатьПолеЛог( "КППКО", RemoveINN( FullINN ) );
    /*БИК*/
    code = ПолучитьКодСубъекта(wlregdec.OriginatorID, PTCK_BIC, error, 1);
    ЗаписатьПолеЛог( "БИК", code );
    /*НаимКО*/
    var subject = Tbfile("party.dbt", "r");
    subject.rec.PartyID = wlregdec.OriginatorID;
    GetEQ( subject );
    ЗаписатьПолеЛог( "НаимКО", substr( subject.rec.Name, 1, 160 ) );
    /*НомФ*/
    Code = ПолучитьКодСубъекта( wlregdec.OriginatorID, PTCK_BANKREGNUM, Error);
    ind = index(Code, "/");
    if ( (ind == 0) or (ind == strlen(Code) ) )
      ЗаписатьПолеЛог( "НомФ", "0" );
    else
      ЗаписатьПолеЛог( "НомФ", SubStr(Code, ind + 1) );
    end;
    /*НомЗапр, ДатаЗапр*/
    ЗаписатьПолеЛог( "НомЗапр", wlregdec.LnkDocNumber );
    ЗаписатьПолеЛог( "ДатаЗапр", DDpMMpYYYY(wlregdec.LnkDocDate) );
    /*КодНО*/
    if (wlregdec.recipientID > 0)
      Code = ПолучитьКодСубъекта( wlregdec.recipientID, PTCK_MNS, Error);
    else
      Code = wlregdec.RecipientCode;
    end;
    ЗаписатьПолеЛог("КодНО", Code);

    /*ИНННП*/
    var INNNP = wlregdec.ClientINN;
    ЗаписатьПолеЛог("ИНННП", INNNP);
    /*КППНП*/
    if (wlregdec.ClientKPP)
      ЗаписатьПолеЛог("КППНП", wlregdec.ClientKPP);
    elif ( strlen(INNNP) != 12 )
      ЗаписатьПолеЛог("КППНП", " ");
    end;
    /*НаимНП / ФИОИП*/
    if ( strlen(INNNP) != 12 )
      if (wlregdec.ClientID > 0)
        ПолучитьСубъекта(wlregdec.ClientID, party);
        ЗаписатьПолеЛог("НаимНП", substr( party.Name, 1, 160 ));
      else
        ЗаписатьПолеЛог("НаимНП", substr( wlregdec.ClientName, 1, 160 ));
      end;
    else
      if (wlregdec.ClientID > 0)      
        persn.rec.PersonID = wlregdec.ClientID;
        GetEQ(persn);
        if( strlen( "ФИОИП", persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3 ) > 160 )
          ЗаписатьПолеЛог("ФИОИП", substr( persn.rec.Name1 + "," + persn.rec.Name2, 1, 160 ));
        else
          ЗаписатьПолеЛог("ФИОИП", persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3);
        end;
      else
        if( РазделитьЗапятымиФИО(Trim(wlregdec.ClientName)) > 160 )
          ЗаписатьПолеЛог( "ФИОИП", substr( ФамилияИмя( РазделитьЗапятымиФИО( Trim(wlregdec.ClientName), "," ) ), 1, 160 ) );
        else
          ЗаписатьПолеЛог( "ФИОИП", РазделитьЗапятымиФИО(Trim(wlregdec.ClientName)) );
        end;
      end;
    end;

    var rs_dp_dep = getRsDpDepByPartyID(wlregdec.OriginatorID);
    rs_dp_dep.moveNext();
    /*ДолжнПрБ*/
    GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", rs_dp_dep.value("t_Code"), @tmpstr );
    ЗаписатьПолеЛог("ДолжнПрБ", tmpstr);
    /*ФИОПрБ*/
    GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО", rs_dp_dep.value("t_Code"), @tmpstr );
    ЗаписатьПолеЛог( "ФИОПрБ", ПолучитьФИОПрБ( tmpstr ) );

    /*ДатаСправ*/
    ЗаписатьПолеЛог( "ДатаСправ", DDpMMpYYYY(wlregdec.BankDate) );
    /*ДатаСооб*/
    ЗаписатьПолеЛог( "ДатаСооб", DDpMMpYYYY(date) );
  end;

  /*Блок "ВыпискаПоСчету" */
  var acclnk = RsbWlAccLnk( wlregdec.DecisionID, OBJTYPE_FNSINFO );
  var recAcc:TRecHandler = TRecHandler( "wlacclnk.dbt" );
  if( acclnk )
    if( not acclnk.First( recAcc ) )
      ОбработатьСчет( recAcc, PartNum, wlregdec );
      while( not acclnk.Next( recAcc ) )
        ОбработатьСчет( recAcc, PartNum, wlregdec );
      end;
    end;
  end;

  /*_НомерОтправ*/
  tmpstr = "";
  if (OldMes.MesID != 0) //Перегенерация?
    if ( not ПолучитьПоле (OldMes.MesID, "_НомерОтправ", tmpstr) )
      tmpstr = "1";  
    end;
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    if ( not ПолучитьПоле (OldfnsMessageID, "_НомерОтправ", tmpstr) )
      tmpstr = "2";  
    else
      tmpstr = string( int(tmpstr) + 1 );
    end;
  else
    tmpstr = "1";
  end;
  ЗаписатьПолеЛог( "_НомерОтправ", tmpstr );

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

macro CheckObj(addrRegdec)
  SetBuff(wlregdec, addrRegdec);
  return CheckObj_FNSINFO( wlregdec );
end;
