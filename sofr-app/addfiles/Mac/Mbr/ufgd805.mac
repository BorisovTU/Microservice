/*
  $Name:         ufgd805.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО "Ответ" по сообщениям УФЭБС ED805
*/

import "ufgendoc.mac", PTInter, likepy, oralib, bnk_common, bnk_dirfile,"xmlmestools.mac";

private class TInitialED
 var EDNo : integer,
     EDDate : date,
     EDAuthor : string;

  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
  end;
  Construct();
end;

private class TQueuedPayment
  var PaymentPrecedence : string,
      Amount : money,
      PayerBIC : string,
      PayerAccount : string,
      PayeeBIC : string,
      PayeeAccount : string,
      QueuingReasonCode : string,
      EDNo : integer,
      EDDate : date,
      EDAuthor : string;
      
  macro Construct()
    PaymentPrecedence = "";
    Amount = money(0);
    PayerBIC =
    PayerAccount = 
    PayeeBIC = 
    PayeeAccount = 
    QueuingReasonCode = 
    EDAuthor = "";
    
    EDNo = 0;
    EDDate = date(0,0,0); 
  end;
  
  Construct();
end;

private class TQueue
  var QueueType : string,
      QueuedPayment : TArray;
  
  macro Construct()
    QueueType = "";
    QueuedPayment = TArray();
  end;
  
  Construct();
      
end;

class TED805MessageFields
  var EDNo : integer,
      EDDate : date,
      EDAuthor : string,
      EDReceiver : string,
      CreationReason : string,
      BIC : string,
      Account : string,
      Queue : TArray,
      InitialED : variant;
   var IsXmlExists : bool;
      
  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = 
    EDReceiver = 
    CreationReason = 
    BIC = 
    Account = "";
    Queue = TArray();
    InitialED = null;
    IsXmlExists = false;
  end;
  
  Construct();
end;

macro ReadED805MessageFields( ED805MessageFields : TED805MessageFields, xmlFile )
  var fieldname = "", fieldvalue = "", blockname = "";
  var isNeedXMLParse : bool = false;
  var stat : bool = false;
  if( valtype(xmlFile) != V_UNDEF )
    isNeedXMLParse = true;
  end;
  
  /*Считываем только то, что хранится в полях релиза*/
  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED805MessageFields.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED805MessageFields.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED805MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED805MessageFields.EDReceiver = fieldvalue;
      elif(fieldname == "CreationReason")
        ED805MessageFields.CreationReason = fieldvalue;
      elif(fieldname == "BIC")
        ED805MessageFields.BIC = fieldvalue;
      elif(fieldname == "Account")
        ED805MessageFields.Account = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == beginField)
        ED805MessageFields.InitialED = TInitialED();    
      elif(fieldname == "EDNo")
        ED805MessageFields.InitialED.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED805MessageFields.InitialED.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED805MessageFields.InitialED.EDAuthor = fieldvalue;
      end;
    end; 
  end;
  
  /*Считываем данные прикрепленного XML-файла, если он есть*/
  if( isNeedXMLParse )
    var XmlAttrsElemsArray : TArray;
    stat = GetXMLElementsAndAttributesArray( xmlFile, @XmlAttrsElemsArray, NodeED805 );
    if( stat )
      ED805MessageFields.isXMLExists = true;
      for( var Xml, XmlAttrsElemsArray )
        if(Xml.BlockName == "Queue")
          if(Xml.FieldName == beginField)
            ED805MessageFields.Queue[ED805MessageFields.Queue.size] = TQueue();
          elif(Xml.FieldName == "QueueType")
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueueType = Xml.FieldValue;
          end;
        elif( Xml.BlockName == "Queue\\QueuedPayment" )
          if( Xml.FieldName == beginField )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size] = TQueuedPayment();
          elif( Xml.FieldName == "PaymentPrecedence" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].PaymentPrecedence = Xml.FieldValue;
          elif( Xml.FieldName == "Amount" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].Amount = money(Xml.FieldValue)/100;
          elif( Xml.FieldName == "PayerBIC" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].PayerBIC = Xml.FieldValue;
          elif( Xml.FieldName == "PayerAccount" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].PayerAccount = Xml.FieldValue;
          elif( Xml.FieldName == "PayeeBIC" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].PayeeBIC = Xml.FieldValue;
          elif( Xml.FieldName == "PayeeAccount" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].PayeeAccount = Xml.FieldValue;
          elif( Xml.FieldName == "QueuingReasonCode" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].QueuingReasonCode = Xml.FieldValue;
          elif( Xml.FieldName == "EDNo" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].EDNo = int(Xml.FieldValue);
          elif( Xml.FieldName == "EDDate" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].EDDate = ToDate(Xml.FieldValue);
          elif( Xml.FieldName == "EDAuthor" )
            ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment[
              ED805MessageFields.Queue[ED805MessageFields.Queue.size - 1].QueuedPayment.size - 1].EDAuthor = Xml.FieldValue;  
          end;
        end;
      end;
    end;
  end;
end;

private macro GetCorschem( Code : string, Account : string, Number : @integer, FIID : @integer )
  
  var select = " Select cor.t_Number, cor.t_FIID, cor.t_CorrID "
               "   From dcorschem_dbt cor, ddp_dep_dbt dep, dpartcode_dbt ptcode "
               "  Where cor.t_Department = dep.t_Code "
               "    and dep.t_PartyID = ptcode.t_PartyID "
               "    and ptcode.t_Code = :Code ";
               // ПРОВЕРИТЬ ПБР
  var parm = MakeArray( SQLParam("Code", Code) );
  if( Account and (strlen(Account) > 0) )
    select = select + " and cor.t_CorAccount = :Account ";
    parm[parm.size] = SQLParam("Account", Account);
  end;
  
  var rs = execSQLSelect( select, parm);
  var Msg = "";
  
  if( rs and rs.MoveNext() )
    if( БанкУБР(rs.value("t_CorrID")) )
      Number = rs.value("t_Number");
      FIID = rs.value("t_FIID");
      return true;
    end;
  end;
  
  return false;
end;

private macro GetQueuedPaymentSum( Queue )
  var i = 0;
  var Sum = money(0);
  
  while( i < Queue.QueuedPayment.size )
    Sum = Sum + Queue.QueuedPayment[i].Amount;
    i = i + 1;
  end;
  
  return Sum;
end;

macro GenDoc( addrMes )
  var FileDirectory = Bnk_GetTxtFileDir();
  var Narrative : string = "";
  var XmlObj : object = ActiveX("Microsoft.XMLDOM");
  var stat = 0;
  SetBuff( wlmes, addrMes );
  ClearRecord( wlreq );
  PrintLog( 2, "Генерация ответа по ED805" );
  
  var ImageID = 0;
  var sWlMesID : string = makeObjectID( OBJTYPE_MES, null, wlmes );

  ImageID = Bnk_GetImageID( OBJTYPE_MES, sWlMesID, WLD_MES_IMGTYPE_XML_FILE );

  if( ImageID )
    /* Записываем прикрепленный ED805 во временный XML-файл */
    var tmpXMLFileName = FileDirectory + "\\" + wlmes.TRN + ".xml";
    stat = WEB_ShowImageByID( ImageID, tmpXMLFileName );
    if( not stat )
      XmlObj = null;
    else
      if ( not XmlObj.load( tmpXMLFileName ) )
        std.msg( "Неверный формат сообщения" );
        return FALSE;
      end;
    end;
    /* Удаляем временный XML-файл с ED805 */
    RemoveFile( tmpXMLFileName );
  else
    XmlObj = null;
  end;

  var ED805Mes = TED805MessageFields();
  ReadED805MessageFields( @ED805Mes, XmlObj );
  
  var Req : RsbWlRequest = RsbWlRequest(0);
  
  Req.Trn = wlmes.Trn;
  Req.RelatedRef = wlmes.RelatedRef;
  Req.Direct = "X";
  Req.Kind = MESKIND_ANSWER;
  
  Req.OriginatorCode = ED805Mes.EDAuthor;
  Req.OriginatorCodeKind = PTCK_UIS;
  Req.OriginatorID = GetCodeOwnerID(Req.OriginatorCodeKind, Req.OriginatorCode);
  Req.OriginatorName = Bnk_GetPartyName(Req.OriginatorID);
  
  Req.RecipientCode = wlmes.InsideAbonentCode;
  Req.RecipientCodeKind = wlmes.InsideAbonentCodeKind;
  Req.RecipientCode = wlmes.InsideAbonentCode;
  Req.RecipientID = wlmes.InsideAbonentID;
  Req.RecipientName = Bnk_GetPartyName(Req.RecipientID);
  
  var CorNumber = 0, CorFIID = 0;
  
  if( GetCorschem(ED805Mes.BIC, ED805Mes.Account, @CorNumber, @CorFIID) )
    Req.Corschem = CorNumber;
    Req.FIID = CorFIID;
  end;
  
  var Query = "";
  
  Query = Query + "CreationReason (код причины формирования ЭС): " + ED805Mes.CreationReason + " " + GetWlcodeDescription( TYPE_CODE_UFBS_ED802_CREATIONREASON, ED805Mes.CreationReason);
  var i = 0;
  
  while( i < ED805Mes.Queue.size )
    Query = Query + "\nВ очереди " + ED805Mes.Queue[i].QueueType + " \"" + GetWlcodeDescription( TYPE_CODE_UFBS_ED8NN_QUEUETYPE, string( "/" + ED805Mes.Queue[i].QueueType + "/")) + "\" " +
                    ED805Mes.Queue[i].QueuedPayment.size + " распоряжений на общую сумму " + GetQueuedPaymentSum(ED805Mes.Queue[i]) + " руб.";
    i = i + 1;
  end;
  
  if( i == 0 )
    Query = Query + "\nПо счету " + ED805Mes.Account + " нет документов в очереди.";
  end;
  
  Query = substr(Query, 1, 2000);
  
  Req.Queries = Query;
  
  Req.ChangeState( WLD_STATUS_REQ_RECEIV );
  
  Req.SaveChanges();
  
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = Req.ReqID;
  meslnk.rec.ObjKind = OBJTYPE_REQ;
  var MesLnkObj = Req.InWlmesLnk();
  MesLnkObj.Insert( meslnk );
  MesLnkObj.Save();
  
  /*Вставка подтверждения о доставке*/
  if( ED805Mes.InitialED != null)
    var ReqMesID = FindReqOutMesID( Req.RelatedRef, wlmes.TpSchemID );
    if( ReqMesID > 0 )
      ВставитьПодтверждениеОДоставке( ReqMesID, null, null, null, false, null, WLD_STATUS_MES_CLOSED );
    end;
  end;

  
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;