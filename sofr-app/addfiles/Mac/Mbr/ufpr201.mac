/*
  $Name: ufpr201.mac
  $Module: Межбанковские расчеты
  $Description: Печать формы по сообщению ED201
*/

import "ufgendoc.mac";

private macro GetPartyNameForCode( Code )
  var PartyName = "";
  var rs = execSQLSelect( " Select pt.t_Name "
                          "   From dparty_dbt pt, dpartcode_dbt ptcode "
                          "  Where pt.t_PartyID = ptcode.t_PartyID "
                          "    and ptcode.t_CodeKind = :CodeKind "
                          "    and ptcode.t_Code = :Code ",
                          MakeArray(SQLParam("CodeKind", PTCK_UIS),
                                    SQLParam("Code", Code)));
  if( rs and rs.MoveNext() )
    PartyName = rs.value("t_Name");
  end;
  
  return PartyName;
end;

macro PrintForm( addrMes, MassCopy )
  var EDNo,
      EDDate,
      OriginatorCode,
      OriginatorName,
      RecipientCode,
      RecipientName,
      CtrlTime,
      CtrlCode,
      Annotation,
      ErrorDiagnostic,
      RefEDNo,
      RefEDDate,
      RefEDAuthor,
      RefPartyName,
      MsgID;
    
  SetBuff( wlmes, addrMes );
  
  var field_name, field_value, строка, название;  
  
  СчитатьПоле( field_name, field_value );
  название = SubStr(field_value, 2, 5);
  if(Index(field_value, String("</",название,">")))
     строка = StrSubst(field_value, String("</",название,">"),"");
  else
     строка = StrSubst(field_value, "/>",">");
  end;
  
  while( СчитатьПоле( field_name, field_value ) )
     строка = строка + field_value;
  end;
  
  строка = строка + String("</",название,">");
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(строка) )
    println( string( "Неверный формат сообщения по форме ED108" ) );
    return FALSE;
  end; 
  
  EDNo = ReadAttribute( xml, "EDNo" );
  EDDate = ReadAttribute( xml, "EDDate" );
  OriginatorCode = ReadAttribute( xml, "EDAuthor");
  OriginatorName = GetPartyNameForCode(OriginatorCode);
  RecipientCode = ReadAttribute(xml, "EDReceiver");
  RecipientName = GetPartyNameForCode(RecipientCode);
  
  CtrlTime = ReadAttribute( xml, "CtrlTime" );
  CtrlCode = ReadAttribute( xml, "CtrlCode" );
  Annotation = ReadAttribute( xml, "Annotation" );
  
  ErrorDiagnostic = ReadOptinalAttribute( xml, "ErrorDiagnostic" );
  
  if( GetChildNode( xml.childNodes.item(0), "EDRefID") )
    RefEDNo = ReadAttribute( xml, "EDNo", "EDRefID");
    RefEDDate = ReadAttribute( xml, "EDDate", "EDRefID");
    RefEDAuthor = ReadAttribute( xml, "EDAuthor", "EDRefID");
    RefPartyName = GetPartyNameForCode(RefEDAuthor);
  end;
  
  
  MsgID = ReadOptinalAttribute( xml, "MsgID" );
  
[
  ED201 Извещение о результатах контроля ЭС (пакета ЭС)
  
  Номер ЭС:     #########
  Дата ЭС:      ##########
  Составитель:  ########## ##################################################
  Получатель:   ########## ##################################################
  
  Время контроля (CtrlTime): ###################
  Код результата контроля ЭПС (CtrlCode): ####
  Текст пояснения (Annotation): ####################################################################################################]
  (EDNo,
  EDDate,
  OriginatorCode,
  OriginatorName,
  RecipientCode,
  RecipientName,
  CtrlTime,
  CtrlCode,
  Annotation);
  if( strlen(ErrorDiagnostic) > 0 )
[  Детальная диагностика ошибки (ErrorDiagnostic): ####################################################################################################]
  (ErrorDiagnostic);
  end;
  if( GetChildNode( xml.childNodes.item(0), "EDRefID") ) 
[ 
  Идентификаторы исходного ЭС (пакета ЭС)
      Номер ЭС:     #########
      Дата ЭС:      ##########
      Составитель:  ########## ##################################################]
  (RefEDNo,
   RefEDDate,
   RefEDAuthor,
   RefPartyName);
  end;
  if( strlen(MsgID) > 0 )    
[  Транспортный идентификатор сообщения (MsgID): ####################################################################################################
] (MsgID);
  end;
  
  return true;
end;