/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Макрос пользовательских функций автоматической квитовки                 */
/*                                                                          */
/*  Имя файла: wlaukvt.mac                                                  */
/*  Создан:  20.04.00                                        Бабин А.П.     */
/****************************************************************************/

import "wldoc.mac", FIInter;

FILE meslnk( wlmeslnk );
var tbf_meslnk = TBFile( "wlmeslnk", "r", 0 ); // для посегментной оптимизации
FILE mes( wlmes );
FILE head( wlhead );

const KvitObjectConf = 0,
      KvitObjectHead = 1, 
      KvitObjectALL  = 2;

var oldNumber;

/***************************************************************************/
/* Проверка на возможность квитовки                                        */
/***************************************************************************/
macro KvitCheck( AddrWlPm, AddrConf )
   var stat;
   RECORD Paym( pmpaym ) ;
   SetBuff( wlpm, AddrWlPm );
   SetBuff( wlconf, AddrConf );
   stat = FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, Paym, null, null, null, null);
   if ( stat )
      MsgBox( "Ошибка при поиске платежа" ) ;
      return false;
   end;
   return true;
end;

/***************************************************************************/
/* Создание шапки отчета                                                   */
/***************************************************************************/
macro Создание_шапки( KvitObject, HeadID, corSchem, FIID, DateIn, DateOut, 
                      ChDate, ChSum, ChRef, ChManyConfOnPaym,
                      ChLetPartly, ChRunMacro, ChTestBackup)
 macro BoolToChar(flag_val)
   var char_val;
   if (flag_val == TRUE)
     char_val = " Да ";
   else
     char_val = " Нет ";
   end;
   return char_val;
 end;

 const regPath_AutoKvitMacro = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\МАКРОПРОЦЕДУРЫ\\AUTOKVITMACROPROCCHECK";
 const regPath_AutoKvitMacroFile = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\МАКРОПРОЦЕДУРЫ\\AUTOKVITMACROFILECHECK";

 var corAcc = "", coresp = "", nameCur = "", error, 
     AutoKvitMacro, AutoKvitMacroFile, Extract = "", DateInStr = string(DateIn:f);

 FILE Корсхемы( corschem );

 if ( DateInStr=="" )
    DateInStr = "00-00-0000";
 end;

 if ( corSchem>=0 )
    KeyNum( Корсхемы, 1 );
    Корсхемы.Number = corSchem;
    Корсхемы.FI_Kind = 1;       /* валюта */
    Корсхемы.FIID = FIID;
    if( getEQ(Корсхемы) )
       corAcc = Корсхемы.Account;
       coresp = ПолучитьКодСубъекта( Корсхемы.CorrID, PTCK_SWIFT, error );
       if ( error ) 
          coresp = "";
       end;
    end;
 end;
 nameCur = ПолучитьКодФинИн( FIID, error );
 if ( error )
    nameCur = "";
 end;

 GetRegistryValue(regPath_AutoKvitMacro, V_STRING, AutoKvitMacro, error); 
 GetRegistryValue(regPath_AutoKvitMacroFile, V_STRING, AutoKvitMacroFile, error);

 AutoKvitMacroFile = "в файле " + AutoKvitMacroFile;
 AutoKvitMacro = " " + AutoKvitMacro + " ";
 if (ChRunMacro == FALSE)
   AutoKvitMacro = " не вызывается ";
   AutoKvitMacroFile = "";
 end;

 std.out(0, String(" "));
 std.out(0, String(" Настройки автоматической квитовки"));
 std.out(0, String(" Квитовать объекты с одинаковыми суммами            :", BoolToChar(ChSum)));
 std.out(0, String(" Квитовать объекты с одинаковыми датами             :", BoolToChar(ChDate)));
 std.out(0, String(" Квитовать объекты с одинаковыми референсами        :", BoolToChar(ChRef)));
 std.out(0, String(" Проверка дублирования                              :", BoolToChar(ChTestBackup)));
 std.out(0, String(" Квитовать несколько подтверждений с одним платежом :", BoolToChar(ChManyConfOnPaym)));
 std.out(0, String(" Частичная квитовка платежа                         :", BoolToChar(ChLetPartly)));
 std.out(0, String(" Квитовать начальные платежи                        :", BoolToChar(Корсхемы.IsKvitOutPaym)));
 std.out(0, String(" Квитовать ответные платежи                         :", BoolToChar(Корсхемы.IsKvitInPaym)));
 std.out(0, String(" Макрос дополнительных проверок                      ", AutoKvitMacro, AutoKvitMacroFile));


 std.out(0, String(" "));
 std.out(0, String(" Результат автоматической квитовки"));
 std.out(0, String(" Дата обработки : ", date:f ));
 std.out(0, String(" Операционист   : ", {oper} )); 
 if ( KvitObject==KvitObjectConf )
    std.out(0, String(" Вид объекта    : подтверждения за период с ", DateInStr, " по ", DateOut:f ));
 elif ( KvitObject==KvitObjectHead )
    if ( HeadID>0 )
       keyNum( head, 0 );
       head.HeadID = HeadID;
       if ( getEQ(head) )
          Extract = string( head.Number );
       else
          Extract = "";
       end;
       std.out(0, String(" Вид объекта    : документы выписки номер ", Extract, " за период с ", DateInStr, " по ", DateOut:f ));
    else
       std.out(0, String(" Вид объекта    : документы всех выписок за период с ", DateInStr, " по ", DateOut:f ));
    end;
 else
    std.out(0, String(" Вид объекта    : подтверждения и документы всех выписок за период с ", DateInStr, " по ", DateOut:f ) );
 end;
 if ( corSchem>=0 )
    std.out(0, String(" Корреспондент  : ", coresp ));
    std.out(0, String(" Схема расчетов : ", corSchem));
 else
    std.out(0, String(" Корреспондент  : все корреспонденты"));
    std.out(0, String(" Схема расчетов : все схемы расчетов"));
 end;
 std.out(0, String(" Код валюты     : ", nameCur ));
 if ( corSchem>=0 )
    std.out(0, String(" Корсчет        : ", corAcc ));
 end;
 std.out(0, String(" "));
 std.out(0, "-------------------------------------------------------------------------------------------------------------------------------" );
 std.out(0, "   N  |        Ссылка        |        Ссылка        |         Сумма        |         Сумма        |    ДВ    |    ДВ     | N\  " );
 std.out(0, "  п/п |  документа выписки   |        платежа       |  документа выписки   |        платежа       | док.вып. |  платежа  | стр." );

 oldNumber = -1;

 return TRUE;
end;

/* Получить номер платежа */
macro ПолучитьРеференсПлатежа( wlpm )

// VD 01.08.03 доработки под посегментную оптимизацию

    tbf_meslnk.KeyNum = 1 ;
    KeyNum( mes, 0 );
    tbf_meslnk.rec.ObjID = wlpm.WlPmID;
    tbf_meslnk.rec.ObjKind = OBJTYPE_PAYMENT;
    tbf_meslnk.rec.MesID = 0;
    tbf_meslnk.rec.Direct = "";

    tbf_wlmeslnk.addFilter(string(
      "t_ObjID=",wlpm.WlPmID,
      " and t_ObjKind=",OBJTYPE_PAYMENT));

    if ( (tbf_meslnk.getGE()) AND (tbf_meslnk.rec.ObjID==wlpm.WlPmID) AND (tbf_meslnk.rec.ObjKind==OBJTYPE_PAYMENT) )
        mes.MesID = tbf_meslnk.rec.MesID;
        if ( getEQ(mes) )
           if ( mes.RelatedRef )
              tbf_meslnk.dropFilter();
              return mes.RelatedRef;
           else
              tbf_meslnk.dropFilter();
              return mes.TRN;
           end;
        end;
    end;
    tbf_meslnk.dropFilter();
    return "";
end;

/***************************************************************************/
/* Вывод записи о квитуемых документах в файл вывода                       */
/***************************************************************************/
macro Создание_Записи( number, AddrWlPm, AddrConf, Msg )
 var showDoc = 0, Extract = "", stat, NumberStr;

 RECORD Paym( pmpaym ) ;

 KeyNum( Head, 0 ); 

 if ( ValType(AddrWlPm)!=V_UNDEF )
    SetBuff( wlpm, AddrWlPm );
    stat = FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, Paym, null, null, null, null);
    if ( stat )
       MsgBox( "Ошибка при поиске платежа" ) ;
       return FALSE ;
    end;
    showDoc = 1;
 end;

 SetBuff( wlconf, AddrConf );

 if ( wlconf.HeadID )
    head.HeadID = wlconf.HeadID;
    if ( getEQ(head) )
       Extract = string( head.Number, "/", head.Page );
    end;
 end;

 if ( oldNumber!=number )
    [--------------------------------------------------------------------------------------------------------------------------------------------];
    NumberStr = number;
 else
    NumberStr = "";
 end; 

 if( showDoc )
    [ ##### ############################# ############################# ###################### ###################### ########## ########## #####]
    (NumberStr:l, wlconf.RelatedRef:r, ПолучитьРеференсПлатежа( wlpm ), wlconf.Sum:r:a, Paym.PayAmount:r:a, wlconf.DateValue:f, Paym.ValueDate:f, Extract:r);
 else
    [ ##### ############################# ############################# ###################### ###################### ########## ########## #####]
    (NumberStr:l, wlconf.RelatedRef:r, "", wlconf.Sum:r:a, "", wlconf.DateValue:f, "", Extract:r);
 end;
 if( Msg != "" )
    [     #](Msg:l);
 end;
 oldNumber = number;
 return TRUE;
end;

/***************************************************************************/
/* Создание подвала отчета                                                 */
/*          totalConf - общее количесиво обрабатываемых документов выписки */
/*          kvitConf  - число сквитованных документов выписки              */
/***************************************************************************/
macro Выдача_статистики( totalConf, kvitConf )
 [-----------------------------------------------------------------------------------------------------------------------------];
 std.out(0, String("   "));
 std.out(0, string("   Выбрано к обработке подтверждений: ", totalConf) );
 std.out(0, string("   Сквитовано подтверждений         : ", kvitConf) );
 return TRUE;
end;