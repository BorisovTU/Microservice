/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT950                                       */
/*                                                                          */
/*  Имя файла: swgm950.mac                                                  */
/*  Создан:  01.06.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

macro GenMes( addrMes, addrHead )
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT;
  var StatementLine, TpID, NameForm, stat, KindPageExt;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlhead, addrHead );

  PrintLog(2,"Генерация сообщения по МТ950");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 25 - счет выписки */
  ЗаписатьПолеЛог( AccountIdentificationField, wlhead.Account );

  /* 28C - Номер выписки */
  ЗаписатьПолеЛог( StatementSequnceNumberCField, string(substr(wlhead.Number,strlen(wlhead.Number)-4), "/", wlhead.Page) );

  /* Открывающий баланс */
  /* Дата начала периода выписки */
  dateSWIFT = GetSWIFTDate( wlhead.DateIn );
  /* Код дебета/кредита */
  if(wlhead.RestIn<0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;
  /* Входящий остаток */
  amountSWIFT = GetSWIFTAmount( dKnum * wlhead.RestIn );
  /* Код валюты */
  CUR = ПолучитьКодВалютыЛог( wlhead.FIID );
  ПолучитьВидСтраницыВыписки( KindPageExt );
  if ( (KindPageExt==WLD_EXT_KIND_FIRST) OR (KindPageExt==WLD_EXT_KIND_SINGLE) )
     ЗаписатьПолеЛогВБлок( "60a", OpeningBalanceField_F, String( dKflag, dateSWIFT, CUR, amountSWIFT ) ) ;
  else
     ЗаписатьПолеЛогВБлок( "60a", OpeningBalanceField_M, String( dKflag, dateSWIFT, CUR, amountSWIFT ) ) ;
  end;

  /******************** Заполняем документы выписки ******************/
  УстановитьФильтрДокументовВыписки(wlhead.HeadID);
  while( ПолучитьДокументВыписки(wlConf) )
     if ( wlConf.DKFlag==WL_DEBET )
         dKflag = DEBET_ACCOUNT;
     else
         dKflag = CREDIT_ACCOUNT;
     end;
     StatementLine = "";
     StatementLine = StatementLine + GetSWIFTDate( wlConf.DateValue );
     StatementLine = StatementLine + substr( string(dateSWIFT), 3 );
     StatementLine = StatementLine + dKflag;
     StatementLine = StatementLine + substr(CUR,3,1);
     StatementLine = StatementLine + GetSWIFTAmount( wlConf.Sum );
     stat = ПолучитьИмяФормы( wlConf.DocumentID, TpID, NameForm );
     if ( (stat) AND (TpID==TRANSP_SWIFT) )
        StatementLine = StatementLine + FillTypeOperField(wlConf.DocumentID, NameForm);
     else
        StatementLine = StatementLine + StatLine_TypeOperField_NonSWIFTTag + Transfer;
     end;
     StatementLine = StatementLine + substr( wlConf.RelatedRef,1,16 );
     if ( wlConf.Number!=wlConf.RelatedRef )
        StatementLine = StatementLine + "//" + substr( wlConf.Number,1,16 );     
     end;
     /*if ( wlConf.Description )
         StrChangeRusXSet( wlconf.Description, field_value );
         StatementLine = StatementLine + "/" + substr( field_value,1,34 );
     end;*/
     ЗаписатьПолеЛогВБлок( "A", StatementLineField, StatementLine );
  end;
  /*******************************************************************/
 
  /* Закрывающий баланс */
  /* Дата окончания периода выписки */
  dateSWIFT = GetSWIFTDate( wlhead.dateOut );
  /* Код дебета/кредита */
  if(wlhead.RestOut<0)
    dKflag = DEBET_ACCOUNT;
    dKnum  = -1;
  else
    dKflag = CREDIT_ACCOUNT;
    dKnum  = 1;
  end;
  /* Исходящий остаток */
  amountSWIFT = GetSWIFTAmount( dKnum * wlhead.RestOut );
  if ( (KindPageExt==WLD_EXT_KIND_LAST) OR (KindPageExt==WLD_EXT_KIND_SINGLE) )
     ЗаписатьПолеЛогВБлок( "62a", ClosingBalanceField_F, String( dKflag, dateSWIFT, CUR, amountSWIFT ) ) ;
  else
     ЗаписатьПолеЛогВБлок( "62a", ClosingBalanceField_M, String( dKflag, dateSWIFT, CUR, amountSWIFT ) ) ;
  end;

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;