/*
  $Name:         ufgm540.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED540 транспорта УФЭБС
*/

import "ufgenmes.mac", BankInter, CTInter, "oralib.mac", MesInter, WldInter;

private const WlReqQueriesPanelFldName = "Messages";
private const WlReqInitFormMesPanelFldName = "InitForm";

private const retPanelFieldNameParmNum = 2;

private macro CheckInitFormMes( InitFormMes )
  if( (RegExMatch( Trim(InitFormMes), "ED\\d{3}" ) == true) )
    return true;
  end; 
  return false; 
end;

macro CheckObj( addrReq, refilledAddrReq, retPanelFieldName )
  record wlReq (wlreq);
  SetBuff( wlReq, addrReq );
  
  var indx = 0;
      
  if( indx = index(wlreq.Queries, "/ExTCode") )    

    var wlCode = existsSQLselect( "SELECT 1 FROM DWLCODES_DBT WLCODES " +
                                  " WHERE T_ALGNUM = 46 " +
                                  " AND T_CODE = :Code ", makeArray(SQLParam("Code", SubStr(wlreq.Queries, indx, 10))));
    

    if( not wlCode )
      setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
      RunError("Для формы ED540 в поле <Сообщение> можно указать только код /ExTCode1/ или /ExTCode2/. Задайте один из допустимых кодов");
    end;
  else
    setparm( retPanelFieldNameParmNum, WlReqQueriesPanelFldName );
    RunError("Для формы ED540 в поле <Сообщение> необходимо ввести один из фиксированных кодов из справочника");
  end;  
  
  var ARMNo = Trim( getFldValFromQueries(wlReq.Queries, "ARMNo:") );
  
  if( ( strlen(ARMNo) != 2 ) or ( not StrIsNumber(ARMNo) ) )
    msgboxEx("Неверно задан номер АРМ получателя (отправителя). Поле ARMNo сообщения заполнено не будет", 0);    
  end;
  
  if( ( wlreq.InitFormMes != "" ) and ( CheckInitFormMes(Trim(wlreq.InitFormMes)) == false ) )
    setparm( retPanelFieldNameParmNum, WlReqInitFormMesPanelFldName );
    RunError("Неверно задан тип сообщений, по которым запрашивается информация, EDTypeNo.| Необходимо задать номер формы из списка сообщений УФЭБС, например, <ED503>");       
  end;
  
  return true;
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);
    
  SetBuff( wlReq, addrReq );
  SetBuff( wlmes, addrMes );
  
  var xml:object = ActiveX( "Microsoft.XMLDOM" );  
  var mes:object; 
  
  mes = xml.createElement( "ED540" );
  mes.setAttribute( "xmlns", "urn:cbr-ru:ed:v2.0" );
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  mes.setAttribute( "EDNo",       ed_nda.EDNo );
  mes.setAttribute( "EDDate",     YYYYMMDD(ed_nda.EDDate) );
  mes.setAttribute( "EDAuthor",   ed_nda.EDAuthor );      
  
  var TypeCode;
  if(index(wlreq.Queries, "/ExTCode1/") != 0)
    TypeCode = 1;
  elif(index(wlreq.Queries, "/ExTCode2/") != 0)
    TypeCode = 2;
  end;
  
  mes.setAttribute( "ExchangeTypeCode", TypeCode );
  if( wlreq.InitFormMes != "" )
    mes.setAttribute( "EDTypeNo", "0" + SubStr(wlreq.InitFormMes, strlen(wlreq.InitFormMes) - 2) ); 
  end;
    
  if( wlreq.InitDateMes != date(0,0,0) )
    mes.setAttribute( "InquiryDate", YYYYMMDD(wlreq.InitDateMes) );
  end;
  
  var ARMNo = Trim( getFldValFromQueries(wlReq.Queries, "ARMNo:") );
  if( (ARMNo != "") and (strlen(ARMNo) == 2) and (StrIsNumber(ARMNo)) )
    mes.setAttribute( "ARMNo", Trim(ARMNo) );
  end;
  
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return true; /*Успешное завершение*/
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;