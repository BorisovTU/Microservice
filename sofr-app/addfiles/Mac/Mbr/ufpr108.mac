/*
  $Name: ufpr108.mac
  $Module: Межбанковские расчеты
  $Description: Печать формы по сообщению ED108
*/

import "ufgendoc.mac";

private class Report
 
  var TransactionID;
  
  var ReqName = TArray();
  ReqName[ReqName.size] = "Дата перевода";
  ReqName[ReqName.size] = "Сумма перевода";
  ReqName[ReqName.size] = "Номер распоряжения плательщика";
  ReqName[ReqName.size] = "Дата распоряжения плательщика";
  ReqName[ReqName.size] = "Уникальный идентификатор платежа (УИН)";
  ReqName[ReqName.size] = "Уникальный присваиваемый номер операции";
  ReqName[ReqName.size] = "Номер банковского счета плательщика";
  ReqName[ReqName.size] = "Наименование плательщика";
  ReqName[ReqName.size] = "Идентификатор плательщика";
  ReqName[ReqName.size] = "Фамилия, имя, отчество физического лица - плательщика";
  ReqName[ReqName.size] = "Адрес физического лица - плательщика";
  ReqName[ReqName.size] = "Номер банковского счета получателя ср-в";
  ReqName[ReqName.size] = "Наименование получателя средств";
  ReqName[ReqName.size] = "Идентификатор получателя средств";
  ReqName[ReqName.size] = "Фамилия, имя, отчество физического лица - получателя средств";
  ReqName[ReqName.size] = "Адрес физического лица - получателя ср-в";
  ReqName[ReqName.size] = "Назначение платежа из распоряжения плательщика";
  ReqName[ReqName.size] = "Информация, связанная с переводом";
  ReqName[ReqName.size] = "ИНН плательщика";
  ReqName[ReqName.size] = "ИНН получателя средств";
  ReqName[ReqName.size] = "101р";
  ReqName[ReqName.size] = "106р";
  ReqName[ReqName.size] = "107р";
  ReqName[ReqName.size] = "108р";
  ReqName[ReqName.size] = "109р";
  ReqName[ReqName.size] = "110р";
  ReqName[ReqName.size] = "111р";
  ReqName[ReqName.size] = "112р";
  
  var ReqVal = TArray();
  
  macro FillReqVal( node )
    node = node.ChildNodes(0);
    ReqVal = TArray();
    
    ReqVal[ReqVal.size] = ReadAttribute(node, "TransactionDate", "", false);
    ReqVal[ReqVal.size] = string(moneyL(ReadAttribute(node, "TransactionSum", "", false))/100);
    ReqVal[ReqVal.size] = ReadAttribute(node, "PayerDocNo", "", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "PayerDocDate", "", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "PaymentID", "", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "OperationID", "", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "Acc", "TransactionPayerInfo", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayerInfo/TradeName", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "PersonalID", "TransactionPayerInfo", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayerInfo/PersonName", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayerInfo/PersonAddress", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "Acc", "TransactionPayeeInfo", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayeeInfo/TradeName", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "PersonID", "TransactionPayeeInfo", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayeeInfoPersonName", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPayeeInfoPersonAddress", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "TransactionPurpose", false);
    ReqVal[ReqVal.size] = ReadNodeText(node, "RemittancelInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "INN", "TransactionPayerInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "INN", "TransactionPayeeInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F101R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F106R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F107R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F108R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F109R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F110R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F111R", "CTTDepartmentalInfo", false);
    ReqVal[ReqVal.size] = ReadAttribute(node, "F112R", "CTTDepartmentalInfo", false);
  end;
  
  var ReceiptDate,
      ChargeOffDate,
      AccDocNo,
      AccDocDate,
      PaytKind,
      DrawerStatus,
      SumText,
      PayerINN,
      PayerKPP,
      Sum,
      PayerName,
      PayerPersonalAcc,
      PayerBank,
      PayerBIC,
      PayerCorrespAcc,
      PayeeBank,
      PayeeBIC,
      PayeePersonalAcc,
      PayeeINN,
      PayeeKPP,
      PayeeCorrespAcc,
      PayeeName,
      TransKind,
      Priority,
      CBC,
      OKATO,
      PaytReason,
      TaxPeriod,
      DocNo,
      DocDate,
      TaxPaytKind,
      Purpose;
      
  private macro GetBankName( BIC )
    var rs = execSQLSelect( " Select party.t_Name as Name"
                            "   From dparty_dbt party, dpartcode_dbt partcode "
                            "  Where party.t_PartyID = partcode.t_PartyID "
                            "    and partcode.t_CodeKind = 3 "
                            "    and partcode.t_Code = :BIC ",
                            MakeArray( SQLParam( "BIC", BIC )));
    if( rs and rs.MoveNext() )
      return rs.value("Name");
    else
      return "";
    end;
  end;
      
  macro FillDescription( node )
    var field_name, field_value;
    ReceiptDate      = ReadAttribute(node, "ReceiptDate", "ED108", false);
    ChargeOffDate    = ReadAttribute(node, "ChargeOffDate", "ED108", false);
    AccDocNo         = ReadAttribute(node, "AccDocNo", "AccDoc", false);
    AccDocDate       = ReadAttribute(node, "AccDocDate", "AccDoc", false);
    var PaytKindNum  = ReadAttribute(node, "ReceiptDate", "ED108", false);
    if( PaytKindNum == "" )
      PaytKind = "не задан";
    elif( PaytKindNum == "2" )
      PaytKind = "почтой";
    elif( PaytKindNum == "3" )
      PaytKind = "телеграфом";
    elif( PaytKindNum == "4" )
      PaytKind = "срочно";
    elif( PaytKindNum == "5")
      PaytKind = "экстренный";
    end;
    DrawerStatus     = ReadAttribute(node, "DrawerStatus", "DepartmentalInfo", false);
    SumText          = moneyL(ReadAttribute(node, "Sum", "", false))/100;;
    PayerINN         = ReadAttribute(node, "INN", "Payer", false);
    PayerKPP         = ReadAttribute(node, "KPP", "Payer", false);
    Sum              = moneyL(ReadAttribute(node, "Sum", "", false))/100;
    PayerName        = ReadNodeText(node, "Payer/Name", false);
    PayerPersonalAcc = ReadAttribute(node, "PersonalAcc", "Payer", false);
    PayerBank        = GetBankName( ReadAttribute(node, "BIC", "Payer/Bank", false) );
    PayerBIC         = ReadAttribute(node, "BIC", "Payer/Bank", false);
    PayerCorrespAcc  = ReadAttribute(node, "CorrespAcc", "Payer/Bank", false);
    PayeeBank        = GetBankName( ReadAttribute(node, "BIC", "Payee/Bank", false) );
    PayeeBIC         = ReadAttribute(node, "BIC", "Payee/Bank", false);
    PayeeCorrespAcc  = ReadAttribute(node, "CorrespAcc", "Payee/Bank", false);
    PayeeINN         = ReadAttribute(node, "INN", "Payee", false);
    PayeeKPP         = ReadAttribute(node, "KPP", "Payee", false);
    PayeePersonalAcc = ReadAttribute(node, "PersonalAcc", "Payee", false);
    PayeeName        = ReadNodeText(node, "Payee/Name", false);
    TransKind        = ReadAttribute(node, "TransKind", "", false); 
    Priority         = ReadAttribute(node, "Priority", "", false);
    CBC              = ReadAttribute(node, "CBC", "DepartmentalInfo", false);
    OKATO            = ReadAttribute(node, "OKATO", "DepartmentalInfo", false);
    PaytReason       = ReadAttribute(node, "PaytReason", "DepartmentalInfo", false);
    TaxPeriod        = ReadAttribute(node, "TaxPeriod", "DepartmentalInfo", false);
    DocNo            = ReadAttribute(node, "DocNo", "DepartmentalInfo", false);
    DocDate          = ReadAttribute(node, "DocDate", "DepartmentalInfo", false);
    TaxPaytKind      = ReadAttribute(node, "TaxPaytKind", "DepartmentalInfo", false);
    Purpose          = ReadNodeText(node, "Purpose", false);
  end;
  
end;

private macro PrintDescription( r : Report )

  array ArrPayerName, ArrPayeeName, ArrPayerBank, ArrPayeeBank;
  StrSplit( r.PayerName, ArrPayerName, 39, 39, 2);
  StrSplit( r.PayeeName, ArrPayeeName, 39, 39, 5);
  StrSplit( r.PayerBank, ArrPayerBank, 39, 39, 3);
  StrSplit( r.PayeeBank, ArrPayeeBank, 39, 39, 3);
   
[   
       ##########                      ##########                ┌─────────────────┐
  ───────────────────────        ─────────────────────           │     0401060     │
   Поступ. в банк плат.           Списано со сч. плат.           └─────────────────┘
                                                             
                                        ##########   #################       ┌─────┐
  ПЛАТЕЖНОЕ ПОРУЧЕНИЕ N ##########   ─────────────── ─────────────────       │  ## │
                                           Дата         Вид платежа          └─────┘       
                                
  
     Сумма    │
     прописью │###################################################################
              │
     ----------------------------------------------------------------------------
     ИНН ############│КПП #########         │Сумма  │#####################
     ---------------------------------------│       │
     #######################################│-------│----------------------------
     #######################################│Сч. N  │####################
     Платльщик                              │       │
     ---------------------------------------│-------│
     #######################################│БИК    │#########
     #######################################│-------│
     #######################################│Сч. N  │####################
     Банк плательщика                       │       │
     ---------------------------------------│-------│----------------------------
     #######################################│БИК    │#########
     #######################################│-------│
     #######################################│Сч. N  │####################
     Банк получателя                        │       │
     ---------------------------------------│-------│
     ИНН ############│КПП #########         │Сч. N  │####################
     ---------------------------------------│       │
     #######################################│-------│----------------------------
     #######################################│Вид оп.│##         │Срок плат. │
     #######################################│-------│           │-----------│
     #######################################│Наз.пл.│           │Очер. плат.│ #  
     #######################################│-------│           │-----------│
     Получатель                             │Код    │           │Рез. поле  │
     ----------------------------------------------------------------------------
     ####################│###########│##│##########│###############│##########│##
     ----------------------------------------------------------------------------
  Назначение платежа ############################################################
  ───────────────────────────────────────────────────────────────────────────────
                              Подписи                             Отметки банка
                    
               М.П.  ───────────────────────────
                     
                     ───────────────────────────
                     
                     
]( r.ReceiptDate:c,
   r.ChargeOffDate:c,
   r.AccDocNo:c,
   r.AccDocDate:c,
   r.PaytKind,
   r.DrawerStatus,
   r.SumText:m,
   r.PayerINN,
   r.PayerKPP,
   r.Sum,
   ArrPayerName[0], ArrPayerName[1],
   r.PayerPersonalAcc,
   ArrPayerBank[0], r.PayerBIC,
   ArrPayerBank[1], ArrPayerBank[2],
   r.PayerCorrespAcc,
   ArrPayeeBank[0], r.PayeeBIC,
   ArrPayeeBank[1], ArrPayeeBank[2],
   r.PayeeCorrespAcc,
   r.PayeeINN, r.PayeeKPP,
   r.PayeePersonalAcc,
   ArrPayeeName[0],
   ArrPayeeName[1], r.TransKind,
   ArrPayeeName[2], ArrPayeeName[3],r.Priority,
   ArrPayeeName[4],
   r.CBC,
   r.OKATO,
   r.PaytReason,
   r.TaxPeriod,
   r.DocNo,
   r.DocDate,
   r.TaxPaytKind,
   r.Purpose);
   

end;

private macro PrintTableHeader()

[
  ┌────────────┬──────────────────────────────────┬──────────────────────────────────┐
  │Номер записи│                                  │                                  │
  │ в реестре  │                                  │                                  │
];
  
end;

private macro PrintTableRow( r : Report, i )
  if( strlen(r.ReqVal[i]) == 0 )
    return;
  end;
[ │            ├──────────────────────────────────┼──────────────────────────────────┤
  │            │##################################│##################################│]
  (r.ReqName[i]:w,
   r.ReqVal[i]:w
  );
end;

private macro PrintTableBottom()

[ └────────────┴──────────────────────────────────┴──────────────────────────────────┘


                         Подписи                   Отметки банка

                  ──────────────────────

              ──────────────────────────────
];
                         
end;

private macro PrintTableFirstRow( r : Report )
  var i = 0;
  while( strlen(r.ReqVal[i]) == 0 )
    i = i + 1;
  end;

[ ├────────────┼──────────────────────────────────┼──────────────────────────────────┤
  │   #####    │##################################│##################################│]
                                                                                       
  (r.TransactionID, r.ReqName[i], r.ReqVal[i]
   );
   
  return i + 1;
end;

/* Макрос печати форм */
macro PrintForm( addrMes, MassCopy )
  var field_name, field_value, строка, название;  
  
  var r = Report();

  SetBuff( wlmes, addrMes );
  
  СчитатьПоле( field_name, field_value );
  название = SubStr(field_value, 2, 5);
  if(Index(field_value, String("</",название,">")))
     строка = StrSubst(field_value, String("</",название,">"),"");
  else
     строка = StrSubst(field_value, "/>",">");
  end;
  
  while( СчитатьПоле( field_name, field_value ) )
     строка = строка + field_value;
  end;
  
  строка = строка + String("</",название,">");
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(строка) )
    println( string( "Неверный формат сообщения по форме ED108" ) );
    return FALSE;
  end; 
  
  
  r.FillDescription( xml );
  PrintDescription(r);
  var i = 0;
  var NodeList = xml.ChildNodes(0).ChildNodes();
  var Node;
  while( i < NodeList.length() )
    Node = NodeList.item(i);
    if( Node.nodeName == "CreditTransferTransactionInfo")
      xml.loadXML(Node.xml);
      PrintTableHeader();
      r.TransactionID = ReadAttribute(xml, "TransactionID");
      r.FillReqVal( xml );
      var j = PrintTableFirstRow(r);  
      while( j < r.ReqName.size )
        PrintTableRow( r, j );
        j = j + 1;
      end;
      PrintTableBottom();
    end;
    i = i + 1;
  end;
  
  return true;
end;