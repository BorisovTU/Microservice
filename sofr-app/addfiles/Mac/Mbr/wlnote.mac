/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Функциональность для работы с примечаниями                              */
/*                                                                          */
/*  Имя файла: wlnote.mac                                                   */
/*  Создан:  10.07.00                                         Бабин А.П.    */
/****************************************************************************/
import "swtools.mac", CTInter, WldInter, BankInter;

/*  Разрешенные коды для поля 72 */
const ALLOWCODES_MT100_STANDART = "ACC INS RCB BENONLY CHEQUE CORPTRAD HOLD INTRACOM PHON PHONBEN PHONIBK TELE TELEBEN TELEIBK EARLY";
const ALLOWCODES_MT100_RUR5     = "ACC INS RCB BENONLY CHEQUE CORPTRAD HOLD INTRACOM PHON PHONBEN PHONIBK TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT100_RUR4     = "ACC INS RCB BENONLY CHEQUE CORPTRAD HOLD INTRACOM PHON PHONBEN PHONIBK TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT102_STANDART = "";
const ALLOWCODES_MT103_STANDART = "ACC INS INT REC EARLY UIP";
const ALLOWCODES_MT103_RUR6     = "ACC INS INT REC RPP RPO NZP DAS UIP";
const ALLOWCODES_MT200_STANDART = "ACC BNF INS INT PHON PHONBEN PHONIBK REC TELE TELEBEN TELEIBK EARLY";
const ALLOWCODES_MT200_RUR4     = "ACC BNF INS INT PHON PHONBEN PHONIBK TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT200_RUR5     = "ACC BNF INS INT PHON PHONBEN PHONIBK TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT202_STANDART = "ACC BNF INS INT PHON PHONBEN PHONIBK REC TELE TELEBEN TELEIBK EARLY UIP";
const ALLOWCODES_MT202_RUR4     = "ACC BNF INS INT PHON PHONBEN PHONIBK REC TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT202_RUR5     = "ACC BNF INS INT PHON PHONBEN PHONIBK REC TELE TELEBEN TELEIBK RPP NZP STD EARLY";
const ALLOWCODES_MT202_RUR6     = "ACC BNF INS INT PHON PHONBEN PHONIBK REC TELE TELEBEN TELEIBK RPP NZP STD EARLY UIP";
const ALLOWCODES_MTn90_STANDART = "EARLY";
const ALLOWCODES_MTn90_RUR4     = "RPP NZP STD EARLY";
const ALLOWCODES_MTn90_RUR5     = "RPP NZP STD EARLY";

/*  Разрешенные коды для поля 70 */
const ALLOWCODES70_MT100_STANDART     = "INV RFB ROC";
const ALLOWCODES70_MT100_RUR4         = "INV RFB ROC";
const ALLOWCODES70_MT100_RUR5         = "INV RFB ROC";
const ALLOWCODES70_MT102_STANDART     = "INV RFB ROC";
const ALLOWCODES70_MT102_RUR4         = "INV RFB ROC";
const ALLOWCODES70_MT102_RUR5         = "INV RFB ROC";
const ALLOWCODES70_MT103_STANDART     = "INV RFB ROC IPI";
const ALLOWCODES70_MT103_RUR4         = "INV RFB ROC IPI";
const ALLOWCODES70_MT103_RUR5         = "INV RFB ROC IPI";
const ALLOWCODES70_MT103_RUR6         = "INV RFB ROC IPI";
const ALLOWCODES70_MTn90_STANDART     = "";
const ALLOWCODES70_MTn90_RUR4         = "";
const ALLOWCODES70_MTn90_RUR5         = "";


class TNote( _Name, _NumbNote, _IsText )
    var Name, NumbNote, IsText;

    Name = _Name;
    NumbNote = _NumbNote;
    if ( valtype(_IsText)==V_UNDEF )
       IsText = false;
    else
       IsText = _IsText;
    end;
end;

class TVeto( codeN1, codeN2 )
   var Number1, Number2;

   Number1 = codeN1;
   Number2 = codeN2;
end;

/* Класс обслуживания примечаний */
class TNotesPaymentSelve
    var TempPaymentID, TempField;
    var DefaultCode = TArray;
    var DefaultValue = TArray;
    var fields = TArray;
    var codes = TArray;
    var veto = TArray;
    var tempCodes = TArray;

    macro FindNumberField( field )
       var i = 0, cont = 1;
       while( cont AND (i<fields.size) )
           if ( fields(i)==field )
              cont = 0;
           else
              i = i+1;
           end;
       end;
       return i;
    end;

    macro FindNumberCode( field, code )
       var i = FindNumberField(field), j=0, cont = 1;
       if ( i==fields.size )
          return -1;
       end;

       while( cont AND (j<codes(i).size) )
           if ( codes(i,j).Name==code )
              cont = 0;
           else
              j = j+1;
           end;
       end;

       return j;
    end;

    macro AddCode( field, code, NumbNote, IsText )
        var i = FindNumberField(field), j;

        if ( i==fields.size )
            fields(i) = field;
            codes(i) = TArray;
            veto(i) = TArray;
            DefaultCode(i) = "";
            DefaultValue(i) = "";
        end;
        j = codes(i).size;
        codes( i,j ) = TNote( code, NumbNote, IsText );
    end;

    macro SetDefaultCodeOfField( field, code, val )
        var i = FindNumberField(field);
        if ( i<fields.size )
           DefaultCode(i) = code;
           DefaultValue(i) = val;
        end;
    end;

    macro IsDefaultCodeOfField( field, code )
        var i = FindNumberField(field);
        if ( i<fields.size )
           if ( DefaultCode(i)==code )
              return true;
           end;
        end;
        return false;
    end;

    macro AddVeto( field, code1, code2 )
        var i = FindNumberField(field), numb1, numb2;
        if ( i<fields.size )
           numb1 = FindNumberCode( field, code1 );
           numb2 = FindNumberCode( field, code2 );
           if ( (numb1>=0) AND (numb1<codes(i).size) AND
                (numb2>=0) AND (numb2<codes(i).size) )
              veto(i, veto(i).size ) = TVeto( numb1, numb2 );
           end;
        end;
    end;

    macro ReadNextNote( PaymentID, field, code, error )
        var i, j, k, continue0, num = FindNumberField( field ), noteText, PaymentIDStr;
        error = 0;

        PaymentIDStr = string( PaymentID );
        while( strlen(PaymentIDStr)<10 )
            PaymentIDStr = "0" + PaymentIDStr;
        end;

        if ( code=="" )
            i = 0;            
        else 
            i = FindNumberCode( field, code ) + 1;
        end;        

        if ( (i==0) OR (PaymentID!=TempPaymentID) OR (field!=TempField) )
           TempCodes.size = 0;
           TempPaymentID = PaymentID;
           TempField = field;
        end;

        continue0 = 1;
        while( continue0 AND (i<codes(num).size) )             
             noteText = readNoteForObject( OBJTYPE_PAYMENT, PaymentIDStr, codes(num,i).NumbNote );
             if ( (valtype(noteText)!=V_UNDEF) AND (noteText!="") )
                 if ( codes(num,i).IsText==false )
                    noteText = NOTEXT_VALUE;
                 end;
                 continue0 = 0;                 
             else
                 i = i + 1;
             end;
        end;        

        if ( not continue0 )
           j = 0;
           continue0 = 1;           
           while( continue0 AND (j < TempCodes.size) )
               k = 0;
               while( continue0 AND (k < veto(num).size) )
                  if (  ( (veto(num,k).Number1==i) AND 
                          (veto(num,k).Number2==TempCodes(j)) ) OR
                        ( (veto(num,k).Number2==i) AND 
                          (veto(num,k).Number1==TempCodes(j)) ) )
                      continue0 = 0;
                  else
                     k = k + 1;
                  end;
               end;
               if ( continue0 )
                  j = j + 1;
               end;
           end;

           if ( not continue0 )
              error = 1;
              noteText = string( "Не мoгут одновременно присутствовать коды ", codes(num,i).Name, " и ", codes(num,TempCodes(j)).Name, " в поле ", field );
           else
              TempCodes( TempCodes.size ) = i;

              code = codes(num,i).Name;
              setparm( 3, code );
           end;
                     
        else
           if ( (code=="") AND (DefaultCode(num)!="") )
              setparm( 3, DefaultCode(num) );
              noteText = DefaultValue(num);
           else
              noteText = "";
           end;
        end;

        setparm( 4, error );

        return noteText;
    end;

    macro Reset
       TempCodes.size = 0;
       TempPaymentID = 0;
       TempField = "";
    end;

    macro SaveNote( field, code, noteText )
        var i, j, k, continue0, num = FindNumberField( field ), stat;
        stat = true;

        if ( field!=TempField )
           TempCodes.size = 0;           
           TempField = field;
        end;

        if ( num==codes.size )
            stat = false;
            noteText = string( "Нет данных о кодах поля ", field );
        else
            i = FindNumberCode( field, code );
            if ( (i<0) OR (i==codes(num).size) )
               stat = false;
               noteText = string( "Нет данных о кодe ", code, " поля ", field );
            else
               j = 0;
               continue0 = 1;
               while( continue0 AND (j < TempCodes.size) )
                   k = 0;                   
                   while( continue0 AND (k < veto(num).size) )                      
                      if (  ( (veto(num,k).Number1==i) AND 
                              (veto(num,k).Number2==TempCodes(j)) ) OR
                            ( (veto(num,k).Number2==i) AND 
                              (veto(num,k).Number1==TempCodes(j)) ) )
                          continue0 = 0;
                      else
                         k = k + 1;
                      end;
                   end;
                   if ( continue0 )
                      j = j + 1;
                   end;
               end;

               if ( not continue0 )
                  stat = false;
                  noteText = string( "Не мoгут одновременно присутствовать коды ", codes(num,i).Name, " и ", codes(num,TempCodes(j)).Name, " в поле ", field );
               else                  
                  if ( codes(num, i).IsText==false )
                     noteText = NOTEXT_VALUE;
                  end;
                  if ( not ВставитьПримечание( codes(num, i).NumbNote, noteText ) )
                       stat = false;
                       noteText = string( "Ошибка при вставке примечания платежа" );
                  else
                     TempCodes( TempCodes.size ) = i;
                  end;
               end;
            end;
        end;

        setparm( 3, noteText );
        return stat;        
    end;

    macro Initial
        AddCode( BankOperationCodeField_B, BANKOPERCODE_CRTS, 1);
        AddCode( BankOperationCodeField_B, BANKOPERCODE_SPAY, 2);
        AddCode( BankOperationCodeField_B, BANKOPERCODE_SPRI, 3);
        AddCode( BankOperationCodeField_B, BANKOPERCODE_SSTD, 4);
        SetDefaultCodeOfField( BankOperationCodeField_B, BANKOPERCODE_CRED, NOTEXT_VALUE );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_CRTS, BANKOPERCODE_SPAY );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_CRTS, BANKOPERCODE_SPRI );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_CRTS, BANKOPERCODE_SSTD );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_SPAY, BANKOPERCODE_SPRI );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_SPAY, BANKOPERCODE_SSTD );
        AddVeto( BankOperationCodeField_B, BANKOPERCODE_SPRI, BANKOPERCODE_SSTD );

        AddCode( BankOperationCodeField, INSTITUTIONCODE_CHQB, 10 );
        AddCode( BankOperationCodeField, BANKOPERCODE_CRTST,   1  );
        AddCode( BankOperationCodeField, BANKOPERCODE_SPAY,    2  );
        SetDefaultCodeOfField( BankOperationCodeField, BANKOPERCODE_CREDIT, NOTEXT_VALUE );
        AddVeto( BankOperationCodeField, BANKOPERCODE_CRTST, BANKOPERCODE_SPAY );

        AddCode( InstructionCodeField, INSTITUTIONCODE_SDVA, 5);
        AddCode( InstructionCodeField, INSTITUTIONCODE_INTC, 6);
        AddCode( InstructionCodeField, INSTITUTIONCODE_REPA, 23);
        AddCode( InstructionCodeField, INSTITUTIONCODE_CORT, 7);
        AddCode( InstructionCodeField, INSTITUTIONCODE_BONL, 8);
        AddCode( InstructionCodeField, INSTITUTIONCODE_HOLD, 9, true); /* Может содержать дополнительную информацию */
        AddCode( InstructionCodeField, INSTITUTIONCODE_CHQB, 10);
        AddCode( InstructionCodeField, INSTITUTIONCODE_PHOB, 11, true);
        AddCode( InstructionCodeField, INSTITUTIONCODE_TELB, 12, true);
        AddCode( InstructionCodeField, INSTITUTIONCODE_PHON, 13, true);
        AddCode( InstructionCodeField, INSTITUTIONCODE_TELE, 14, true);
        AddCode( InstructionCodeField, INSTITUTIONCODE_PHOI, 15, true);
        AddCode( InstructionCodeField, INSTITUTIONCODE_TELI, 16, true);

        
        AddVeto( InstructionCodeField, INSTITUTIONCODE_CORT, INSTITUTIONCODE_BONL );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_CORT, INSTITUTIONCODE_CHQB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_CORT, INSTITUTIONCODE_HOLD );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_HOLD, INSTITUTIONCODE_CHQB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_INTC, INSTITUTIONCODE_BONL );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_INTC, INSTITUTIONCODE_CHQB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_INTC, INSTITUTIONCODE_HOLD );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_REPA, INSTITUTIONCODE_HOLD );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_REPA, INSTITUTIONCODE_CHQB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_REPA, INSTITUTIONCODE_BONL );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_REPA, INSTITUTIONCODE_CORT );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_PHOB, INSTITUTIONCODE_TELB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_PHOI, INSTITUTIONCODE_TELI );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_PHON, INSTITUTIONCODE_TELE );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_SDVA, INSTITUTIONCODE_CHQB );
        AddVeto( InstructionCodeField, INSTITUTIONCODE_SDVA, INSTITUTIONCODE_HOLD );

        AddCode( SenderToReceiverInformationField, Field72CodesACC, 17, true);
        AddCode( SenderToReceiverInformationField, Field72CodesINT, 18, true);
        AddCode( SenderToReceiverInformationField, Field72CodesBENONLY,  8  );
        AddCode( SenderToReceiverInformationField, Field72CodesCHEQUE,   10 ); 
        AddCode( SenderToReceiverInformationField, Field72CodesCORPTRAD, 7  );
        AddCode( SenderToReceiverInformationField, Field72CodesHOLD,     9, true );
        AddCode( SenderToReceiverInformationField, Field72CodesINTRACOM, 6 );
        AddCode( SenderToReceiverInformationField, Field72CodesPHON,     13, true );
        AddCode( SenderToReceiverInformationField, Field72CodesPHONBEN,  11, true );
        AddCode( SenderToReceiverInformationField, Field72CodesPHONIBK,  15, true );
        AddCode( SenderToReceiverInformationField, Field72CodesTELE,     14, true );
        AddCode( SenderToReceiverInformationField, Field72CodesTELEBEN,  12, true );
        AddCode( SenderToReceiverInformationField, Field72CodesTELEIBK,  16, true );
        AddCode( SenderToReceiverInformationField, Field72CodesBNF,      30, true );
        
        AddCode( DetailsOfPaymentField, Field70CodesRFB, 20, true );
        AddCode( DetailsOfPaymentField, Field70CodesINV, 19, true );        
        AddCode( DetailsOfPaymentField, Field70CodesROC, 21, true );
        AddCode( DetailsOfPaymentField, Field70CodesIPI, 22, true );
    end;

    Initial();
end;
