/*
  $Name:         ufgd574.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО по сообщению ED574 транспорта УФЭБС
*/

import "ufgendoc.mac", BankInter, CTInter, "oralib.mac", MesInter, WldInter;

class TCOSSenderInfo          /*Информация об отправителях ЭС*/
  var UIS:           string,  /*Уникальный идентификатор, присвоенный составителю/получателю ЭС*/
      SWIFTBIC:      string,  /*БИК SWIFT*/
      SWIFTTypeList: TArray;  /*Список типов сообщений SWIFT*/

  macro Construct()
    UIS = "";           
    SWIFTBIC = "";  
    SWIFTTypeList = TArray(); 
  end; 

  Construct();
end;

class TESExchangeInfo              /*Информация об участии в обмене ЭС*/
  var ServicesCode:       integer, /*Код услуги*/
      BeginDate:          date,    /*Дата начала обмена сообщениями*/
      EndDate:            date,    /*Дата исключения из обмена сообщениями*/
      ESSize:             integer, /*Максимальный размер ЭС*/
      ARMNo:              integer, /*Номер АРМ*/
      COSSenderInfoArray: TArray;  /*Информация об отправителях ЭС*/

  macro Construct()
    ServicesCode = 0; 
    BeginDate = date(0,0,0);   
    EndDate = date(0,0,0);   
    ESSize = 0;
    ARMNo = 0;
    COSSenderInfoArray = TArray(); 
  end; 

  Construct();
end;

class TCOSCustomerInfo              /*Реквизиты клиента, по которому предоставляется информация об атрибутах участия в обмене сообщениями*/
  var UIS:                 string,  /*Уникальный идентификатор, присвоенный составителю/получателю ЭС*/
      BIC:                 string,  /*БИК обслуживающего ПБР*/
      SWIFTBIC:            string,  /*БИК SWIFT*/
      IsFreeServices:      integer, /*Признак бесплатности*/
      Name:                string,  /*Наименование клиента*/
      ESExchangeInfoArray: TArray;  /*Информация об участии в обмене ЭС*/

  macro Construct()
    UIS = "";
    BIC = "";
    SWIFTBIC = "";
    IsFreeServices = 0;
    Name = ""; 
    ESExchangeInfoArray = TArray(); 
  end; 

  Construct();
end;


class TED574MessageFields            /*Поля сообщения ED574*/
  var EDNo:                 integer, /*Номер ЭПД в течение опердня*/
      EDDate:               date,    /*Дата составления ЭПД*/
      EDAuthor:             string,  /*Уникальный идентификатор составителя ЭПД*/
      EDReceiver:           string,  /*Уникальный идентификатор получателя ЭС*/
      DictionMode:          string,  /*Вид справочника*/
      InitialEDNo:          integer, /*Номер ЭС в течение опердня для исходного ЭСИС*/
      InitialEDDate:        date,    /*Дата составления ЭС для исходного ЭСИС*/
      InitialEDAuthor:      string,  /*Уникальный идентификатор составителя ЭС для исходного ЭСИС*/
      COSCustomerInfoArray: TArray;  /*Реквизиты клиентов, по которым предоставляется информация об атрибутах участия в обмене сообщениями*/

  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
    EDReceiver = "";
    DictionMode = "";
    InitialEDNo = 0;
    InitialEDDate = date(0,0,0);  
    InitialEDAuthor = "";
    COSCustomerInfoArray = TArray(); 
  end; 

  Construct();
end;

// Макрос чтения полей сообщения ED574
// Входные параметры: ED574MessageFields - структура полей сообщения ED574
private macro ReadED574MessageFields( ED574MessageFields:@TED574MessageFields ) : bool
  var fieldname = "", fieldvalue = "", blockname = "";

  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED574MessageFields.EDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED574MessageFields.EDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED574MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED574MessageFields.EDReceiver = fieldvalue;
      elif(fieldname == "DictionMode")
        ED574MessageFields.DictionMode = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == "EDNo")
        ED574MessageFields.InitialEDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED574MessageFields.InitialEDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED574MessageFields.InitialEDAuthor = fieldvalue;
      end;
    elif(blockname == "COSCustomerInfo")
      if(fieldname == beginField)
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size ] = TCOSCustomerInfo();
      elif(fieldname == "UIS")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].UIS = fieldvalue;
      elif(fieldname == "BIC")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].BIC = fieldvalue;
      elif(fieldname == "SWIFTBIC")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].SWIFTBIC = fieldvalue;
      elif(fieldname == "IsFreeServices")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].IsFreeServices = int(fieldvalue);
      end;
    elif(blockname == "COSCustomerInfo\\Name")
      if(fieldname == "Name")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].Name = fieldvalue;
      end;
    elif(blockname == "COSCustomerInfo\\ESExchangeInfo")
      if(fieldname == beginField)
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size ] = TESExchangeInfo();  
      elif(fieldname == "ServicesCode")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].ServicesCode = int(fieldvalue);
      elif(fieldname == "BeginDate")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].BeginDate = ToDate(fieldvalue);
      elif(fieldname == "EndDate")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].EndDate = ToDate(fieldvalue);
      elif(fieldname == "ESSize")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].ESSize = int(fieldvalue);
      elif(fieldname == "ARMNo")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].ARMNo = int(fieldvalue);
      end;
    elif(blockname == "COSCustomerInfo\\ESExchangeInfo\\COSSenderInfo")
      if(fieldname == beginField)
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
          ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
           ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size ] = TCOSSenderInfo();
      elif(fieldname == "UIS")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
          ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
           ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].UIS = fieldvalue;
      elif(fieldname == "SWIFTBIC")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
          ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
           ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].SWIFTBIC = fieldvalue;
      end;
    elif(blockname == "COSCustomerInfo\\ESExchangeInfo\\COSSenderInfo\\SWIFTTypeList")  
      if(fieldname == beginField)
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
          ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
           ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].SWIFTTypeList[
            ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
             ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
              ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
               ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].SWIFTTypeList.size ] = TArray();
      elif(fieldname == "SWIFTTypeNo")
        ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
         ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
          ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
           ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].SWIFTTypeList[
            ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
             ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray[ 
              ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray[ 
               ED574MessageFields.COSCustomerInfoArray[ ED574MessageFields.COSCustomerInfoArray.size - 1 ].ESExchangeInfoArray.size - 1 ].COSSenderInfoArray.size - 1 ].SWIFTTypeList.size - 1 ] = fieldvalue;
      end;
    end; 
  end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

  return TRUE;
end;

// Макрос получения идентификатора объекта по кодам УИС. БИК и SWIFT
// Входные параметры: UISCode - УИС. SWIFTCode - SWIFT
// Возвращаемое значение: идентификатор объекта или 0. если не найден
private macro GetObjectIDFromUISorSWIFT( UISCode, SWIFTCode )
  var ObjectID:integer = 0;

  var uisselect = " select t_ObjectID " +  
               " from dobjcode_dbt " +  
               " where t_Code = :Code" + 
               "   and t_CodeKind = :CodeKind"; 

  var uisparams = makeArray( SQLParam( "Code",     UISCode ),
                             SQLParam( "CodeKind", PTCK_UIS ) );
                        
  var uisrs = execSQLselect( uisselect, uisparams );
  
  if( uisrs and uisrs.moveNext() )
    ObjectID = int(uisrs.value(0));
  end;

  if( ObjectID == 0 )

    var bicselect = " select t_ObjectID " +  
                    " from dobjcode_dbt " +  
                    " where substr(t_Code, 3, 9) = :Code" + 
                    "   and t_CodeKind = :CodeKind"; 

    var bicparams = makeArray( SQLParam( "Code",     Substr( UISCode, 1, strlen( UISCode ) - 3 ) ),
                               SQLParam( "CodeKind", PTCK_BIC ) );
                          
    var bicrs = execSQLselect( bicselect, bicparams );
    
    if( bicrs and bicrs.moveNext() )
      ObjectID = int(bicrs.value(0));
    end;
  end;

  if( ObjectID == 0 )

    var swiftselect = " select t_ObjectID " +  
                      " from dobjcode_dbt " +  
                      " where t_Code = :Code" + 
                      "   and t_CodeKind = :CodeKind"; 

    var swiftparams = makeArray( SQLParam( "Code",     SWIFTCode ),
                                 SQLParam( "CodeKind", PTCK_SWIFT ) );
                          
    var swiftrs = execSQLselect( swiftselect, swiftparams );
    
    if( swiftrs and swiftrs.moveNext() )
      ObjectID = int(swiftrs.value(0));
    end;
  end;

  if( ObjectID != 0 )
    var PartyRs = existsSQLSelect( " Select 1 "
                                   "   From dparty_dbt "
                                   "  Where t_PartyID = :PartyID "
                                   "    and t_Locked != chr(0) ",
                                   MakeArray( SQLParam( "PartyID", ObjectID )));
    if( PartyRs )
      ObjectID = 0;
    end;
  end;

  return ObjectID;
end;

// Макросы получения информации об участии в обмене ЭС
// Входные параметры: COSCustomerInfo - Реквизиты клиента, по которому предоставляется информация об атрибутах участия в обмене сообщениями, ServicesCode - код услуги
// Возвращаемое значение: соответствующий реквизит
private macro GetMaxBeginDate( COSCustomerInfo: TCOSCustomerInfo, ServicesCode: integer )
  var tmpMaxBeginDate = date(0,0,0);
  var isExistDateForServicesCode = false;

  for( var ESExchangeInfof, COSCustomerInfo.ESExchangeInfoArray )
    if( ESExchangeInfof.ServicesCode == ServicesCode )
      isExistDateForServicesCode = true;  
    end;
  end;

  if( isExistDateForServicesCode == false )
    return date(0,0,0);
  end;

  for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
    if( ( ESExchangeInfo.BeginDate > tmpMaxBeginDate ) and ( ESExchangeInfo.ServicesCode == ServicesCode ) )
      tmpMaxBeginDate = ESExchangeInfo.BeginDate;  
    end;
  end;
  return tmpMaxBeginDate;
end;

private macro GetMinEndDate( COSCustomerInfo: TCOSCustomerInfo, ServicesCode: integer )
  var tmpMinEndDate = date(31,12,9999);
  var isExistDateForServicesCode = false;

  for( var ESExchangeInfof, COSCustomerInfo.ESExchangeInfoArray )
    if( ESExchangeInfof.ServicesCode == ServicesCode )
      isExistDateForServicesCode = true;  
    end;
  end;

  if( isExistDateForServicesCode == false )
    return date(0,0,0);
  end;

  for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
    if( ( ESExchangeInfo.EndDate < tmpMinEndDate ) and ( ESExchangeInfo.ServicesCode == ServicesCode ) )
      tmpMinEndDate = ESExchangeInfo.EndDate;  
    end;
  end;
  return tmpMinEndDate;
end;

private macro GetMinESSize( COSCustomerInfo: TCOSCustomerInfo, ServicesCode: integer )
  var tmpMinESSize = 0;
  var isExistESSizeForServicesCode = false;

  for( var ESExchangeInfoz, COSCustomerInfo.ESExchangeInfoArray )
    if( ESExchangeInfoz.ServicesCode == ServicesCode )
      isExistESSizeForServicesCode = true;  
    end;
  end;

  if( isExistESSizeForServicesCode == false )
    return 0;
  end;

  for( var ESExchangeInfof, COSCustomerInfo.ESExchangeInfoArray )
    if( ESExchangeInfof.ServicesCode == ServicesCode )
      tmpMinESSize = ESExchangeInfof.ESSize;
      break;
    end;
  end;

  for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
    if( ( ESExchangeInfo.ESSize < tmpMinESSize ) and ( ESExchangeInfo.ServicesCode == ServicesCode ) )
      tmpMinESSize = ESExchangeInfo.ESSize;  
    end;
  end;

  return tmpMinESSize;
end;

private macro GetAnyARMNo( COSCustomerInfo: TCOSCustomerInfo, ServicesCode: integer )
  var tmpARMNo = 0;
  var isExistARMNoForServicesCode = false;

  for( var ESExchangeInfof, COSCustomerInfo.ESExchangeInfoArray )
    if( ESExchangeInfof.ServicesCode == ServicesCode )
      isExistARMNoForServicesCode = true;  
    end;
  end;

  if( isExistARMNoForServicesCode == false )
    return 0;
  end;

  for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
    if( ( ESExchangeInfo.ServicesCode == ServicesCode ) and ( ESExchangeInfo.ARMNo != 0 ) )
      tmpARMNo = ESExchangeInfo.ARMNo;
      break;
    end;
  end;
  return tmpARMNo;
end;

// Макрос поиска субъекта в dwlusspfs_dbt
// Входные параметры: ReceiverID, UisSenderCode, ObjectID, ObjectType
// Возвращаемое значение: идентификатор субъекта или 0. если не найден
private macro FindWlusSpFsSubject( ReceiverID, UisSenderCode, ObjectID, ObjectType ):integer
  var resultID = 0;
  var select = " select t_ID " +  
               " from dwlusspfs_dbt " +  
               " where t_ReceiverID = :ReceiverID" + 
               "   and t_UISSenderCode = :UisSenderCode " +
               "   and t_ObjectID = :ObjectID " +
               "   and t_ObjectType = :ObjectType "; 

  var params = makeArray( SQLParam( "ReceiverID",    ReceiverID ),
                          SQLParam( "UisSenderCode", UisSenderCode ),
                          SQLParam( "ObjectID",      ObjectID ),
                          SQLParam( "ObjectType",    ObjectType ) );
                        
  var rs = execSQLselect( select, params );
  
  if( rs and rs.moveNext() )
    resultID = int(rs.value(0));
  end;

  return resultID;
end;

// Макрос обновления или добавления записей в dwlusspfs_dbt
// Входные параметры: wlusspfsID - ID, если задан или 0 при вставке новой записи, ObjectID - ID объекта, CosSenderInfo - информация об отправителях ЭС, ServicesCode - код услуги, 
//                    SwiftReceiverCode - SWIFT код получателя, SenderID - ID отправителя, ReceiverID - ID получателя, 
//                    IsInfoMes - true, если обновляются или добавляются записи по сообщениям, false в противном случае
private macro UpdateOrInsertWlusSpFs( wlusspfsID :integer, ObjectID, CosSenderInfo: TCosSenderInfo, ServicesCode, SwiftReceiverCode, SenderID, ReceiverID, IsInfoMes: bool ) 
  var MessageType = "";
  for( var SWIFTType, CosSenderInfo.SWIFTTypeList )
    if( SWIFTType != "" )
      if( MessageType == "" )
        MessageType = SWIFTType;
      else
        MessageType = MessageType + "," + SWIFTType;
      end;
    end;
  end;
  
  MessageType = SubStr(MessageType, 1, 2000);

  var ObjectType = 0;
  if( IsInfoMes == true )
    ObjectType = OBJTYPE_INFO;
  else
    ObjectType = OBJTYPE_PARTY;
  end;

  var strSql: string = "";
  var params: TArray;

  if( IsInfoMes == true )

    if( wlusspfsID == 0 )
      strSql = " insert into dwlusspfs_dbt ( t_ObjectID, t_ObjectType, t_UISSenderCode, t_SwiftSenderCode, t_ED501Exchange, t_ED503Exchange, " +
               "                             t_MessageType, t_SwiftReceiverCode, t_SenderID, t_ReceiverID ) " +
               " values ( :ObjectID, :ObjectType, :UISSenderCode, :SwiftSenderCode, "; 
      if( ServicesCode == 2 )
        strSql = strSql + " :ED501Exchange, chr(0), ";             
      end;
      if( ServicesCode == 1 )  
        strSql = strSql + " chr(0), :ED503Exchange, ";
      end;
      strSql = strSql + " :MessageType, :SwiftReceiverCode, :SenderID, :ReceiverID ) ";
    else
      strSql = " update dwlusspfs_dbt set t_ObjectID =          :ObjectID, " +
               "                          t_ObjectType =        :ObjectType, " +
               "                          t_UISSenderCode =     :UISSenderCode, " +
               "                          t_SwiftSenderCode =   :SwiftSenderCode, ";
      if( ServicesCode == 2 )
        strSql = strSql + "               t_ED501Exchange =     :ED501Exchange, ";
      end;
      if( ServicesCode == 1 ) 
        strSql = strSql + "               t_ED503Exchange =     :ED503Exchange, ";
      end;

      strSql = strSql + "                 t_MessageType =       :MessageType, " + 
               "                          t_SwiftReceiverCode = :SwiftReceiverCode, " +
               "                          t_SenderID =          :SenderID, " +
               "                          t_ReceiverID =        :ReceiverID ";
      strSql = strSql + " where t_ID = :ID ";
    end;

    params = TArray();
    params[params.size] =   SQLParam( "ObjectID",          ObjectID );
    params[params.size] =   SQLParam( "ObjectType",        ObjectType );
    params[params.size] =   SQLParam( "UISSenderCode",     CosSenderInfo.UIS );
    params[params.size] =   SQLParam( "SwiftSenderCode",   CosSenderInfo.SWIFTBIC );

    if( ServicesCode == 2 )
      params[params.size] = SQLParam( "ED501Exchange",     SET_CHAR );
    end;
    if( ServicesCode == 1 )
      params[params.size] = SQLParam( "ED503Exchange",     SET_CHAR );
    end;                        
    params[params.size] =   SQLParam( "MessageType",       MessageType );
    params[params.size] =   SQLParam( "SwiftReceiverCode", SwiftReceiverCode );
    params[params.size] =   SQLParam( "SenderID",          SenderID );
    params[params.size] =   SQLParam( "ReceiverID",        ReceiverID ); 

    if( wlusspfsID != 0 )
      params[params.size] = SQLParam( "ID",                wlusspfsID );
    end;

    return execSQL( strSql, params );
  else
    if( wlusspfsID == 0 )
      strSql = " insert into dwlusspfs_dbt ( t_ObjectID, t_ObjectType, t_UISSenderCode, t_SwiftSenderCode, t_ED501Exchange, t_ED503Exchange, " +
               "                             t_MessageType, t_SwiftReceiverCode, t_SenderID, t_ReceiverID ) " +
               " values ( :ObjectID, :ObjectType, :UISSenderCode, :SwiftSenderCode, :ED501Exchange, :ED503Exchange, :MessageType, :SwiftReceiverCode, :SenderID, :ReceiverID ) ";
      
      params = TArray();
      params[params.size] =   SQLParam( "ObjectID",          ObjectID );
      params[params.size] =   SQLParam( "ObjectType",        ObjectType );
      params[params.size] =   SQLParam( "UISSenderCode",     CosSenderInfo.UIS );
      params[params.size] =   SQLParam( "SwiftSenderCode",   CosSenderInfo.SWIFTBIC );
      params[params.size] =   SQLParam( "ED501Exchange",     IfThenElse( ServicesCode == 2, "X", "" ) );
      params[params.size] =   SQLParam( "ED503Exchange",     IfThenElse( ServicesCode == 1, "X", "" ) );
      params[params.size] =   SQLParam( "MessageType",       IfThenElse( ServicesCode == 1, MessageType, "" ) );
      params[params.size] =   SQLParam( "SwiftReceiverCode", SwiftReceiverCode );
      params[params.size] =   SQLParam( "SenderID",          SenderID );
      params[params.size] =   SQLParam( "ReceiverID",        ReceiverID ); 
    else
      strSql = " update dwlusspfs_dbt set t_ObjectID =          :ObjectID, " +
               "                          t_ObjectType =        :ObjectType, " +
               "                          t_UISSenderCode =     :UISSenderCode, ";
      if( CosSenderInfo.SWIFTBIC != "" )
        strSql = strSql + "               t_SwiftSenderCode =   :SwiftSenderCode, ";
      end;
      if( ServicesCode == 2 )
        strSql = strSql + "               t_ED501Exchange =     :ED501Exchange, ";
      end;
      if( ServicesCode == 1 ) 
        strSql = strSql + "               t_ED503Exchange =     :ED503Exchange, ";
      end;
      if( ServicesCode == 1 )
        strSql = strSql + "               t_MessageType =       :MessageType, "; 
      end;
      strSql = strSql + "                 t_SwiftReceiverCode = :SwiftReceiverCode, " +
               "                          t_SenderID =          :SenderID, " +
               "                          t_ReceiverID =        :ReceiverID ";
      strSql = strSql + " where t_ID = :ID ";

      params = TArray();
      params[params.size] =   SQLParam( "ObjectID",          ObjectID );
      params[params.size] =   SQLParam( "ObjectType",        ObjectType );
      params[params.size] =   SQLParam( "UISSenderCode",     CosSenderInfo.UIS );
      if( CosSenderInfo.SWIFTBIC != "" )
        params[params.size] = SQLParam( "SwiftSenderCode",   CosSenderInfo.SWIFTBIC );
      end;
      if( ServicesCode == 2 )
        params[params.size] = SQLParam( "ED501Exchange",     SET_CHAR );
      end;
      if( ServicesCode == 1 )
        params[params.size] = SQLParam( "ED503Exchange",     SET_CHAR );
      end;                        
      if( ServicesCode == 1 )
        params[params.size] = SQLParam( "MessageType",       MessageType );
      end;
      params[params.size] =   SQLParam( "SwiftReceiverCode", SwiftReceiverCode );
      params[params.size] =   SQLParam( "SenderID",          SenderID );
      params[params.size] =   SQLParam( "ReceiverID",        ReceiverID ); 
    
      params[params.size] =   SQLParam( "ID",                wlusspfsID );
    
    end;
    return execSQL( strSql, params );
  end;
end;

private macro IsUisSenderCodeInCOSSenderInfo( UISSenderCode, COSCustomerInfo )
  for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
    for( var COSSenderInfo, ESExchangeInfo.COSSenderInfoArray )
      if( UISSenderCode == COSSenderInfo.UIS )
        return true;
      end;
    end;
  end;
  return false;
end;

private macro DeleteWrongWlusSpFsRecords( ReceiverID, ObjectID, ObjectType, COSCustomerInfo )
  var select = " select t_ID, t_UISSenderCode " +  
               " from dwlusspfs_dbt " +  
               " where t_ReceiverID = :ReceiverID" + 
               "   and t_ObjectID   = :ObjectID " +
               "   and t_ObjectType = :ObjectType "; 

  var params = makeArray( SQLParam( "ReceiverID",    ReceiverID ),
                          SQLParam( "ObjectID",      ReceiverID ),
                          SQLParam( "ObjectType",    ObjectType ) );
                        
  var rs = execSQLselect( select, params );

  var wlIDArr = TArray();
  
  while( rs and rs.moveNext() )
     if( not IsUisSenderCodeInCOSSenderInfo( rs.value("t_UISSenderCode"), COSCustomerInfo ) )
       wlIDArr[wlIDArr.size] = rs.value("t_ID");  
     end;       
  end;

  var IDs = "";
  for( var id, wlIDArr )
    if( IDs == "" )
      IDs = id;
    else
      IDs = IDs + "," + id;
    end;
  end;

  if( IDs != "" )
    var strSql = " delete from dwlusspfs_dbt where t_ID in ( " + IDs + " ) "; 
    execSQL( strSql );
  end;

  return true;
end;

private macro IsExistInReqWithSP30( RelatedRef )
  var Department = 0;
  var select = " select 1 " +
               " from dwlmes_dbt wlmes, dwlreq_dbt wlreq, dwlmeslnk_dbt meslnk " +
               " where wlreq.t_Direct = chr(0) " +
               "  and instr(wlreq.t_Queries, '/СП30/') > 0 " +
               "  and meslnk.t_ObjID = wlreq.t_ReqID " +
               "  and meslnk.t_ObjKind = :ObjKind " +
               "  and wlmes.t_MesID = meslnk.t_MesID " +
               "  and wlmes.t_Trn = :Ref ";

  var params = makeArray( SQLParam( "ObjKind", OBJTYPE_REQ ),
                          SQLParam( "Ref",     RelatedRef ) );
                        
  var rs = execSQLselect( select, params );
  
  if( rs and rs.moveNext() )
    return true
  end;

  return false;
end;

private macro IsUisSenderCodeInCOSCustomerInfo( UISCode, TED574MessageFields )
  for( var COSCustomerInfo, TED574MessageFields.COSCustomerInfoArray )
    if( UISCode == COSCustomerInfo.UIS )
      return true;
    end;
  end;
  return false;
end;

private macro DeleteWlusSpFsOnDepartment( Department, TED574MessageFields )
  var select = " select t_ID, t_ReceiverID " +  
               " from dwlusspfs_dbt " +  
               " where t_SenderID   = :SenderID" + 
               "   and t_ObjectType = :ObjectType "; 

  var params = makeArray( SQLParam( "SenderID",   GetDprtPartyID( Department ) ),
                          SQLParam( "ObjectType", OBJTYPE_PARTY ) );
                        
  var rs = execSQLselect( select, params );

  var wlIDArr = TArray();
  
  while( rs and rs.moveNext() )
    var ReceiverUISCode = ПолучитьКодСубъекта( rs.value("t_ReceiverID"), PTCK_UIS, null );
    if( not IsUisSenderCodeInCOSCustomerInfo( ReceiverUISCode, TED574MessageFields ) )
      wlIDArr[wlIDArr.size] = rs.value("t_ID");  
    end;       
  end;

  var IDs = "";
  for( var id, wlIDArr )
    if( IDs == "" )
      IDs = id;
    else
      IDs = IDs + "," + id;
    end;
  end;

  if( IDs != "" )
    var strSql = " delete from dwlusspfs_dbt where t_ID in ( " + IDs + " ) "; 
    execSQL( strSql );
  end;

  return true;
end;


macro GenDoc( addrMes )

  var Narrative;
  var DictionModeStr: string = "";
  SetBuff( wlmes, addrMes );
  ClearRecord( wlinfo );
  PrintLog( 2, "Генерация информационного сообшения по ED574" );
  
  wlinfo.TRN                = wlmes.Trn;
  wlinfo.RelatedRef         = wlmes.RelatedRef;
  wlinfo.Direct             = "X"; /*WLD_MES_IN*/
  wlinfo.OriginatorCode     = wlmes.OutSideAbonentCode;
  wlinfo.OriginatorCodeKind = wlmes.OutSideAbonentCodeKind; 
  wlinfo.OriginatorID       = wlmes.OutSideAbonentID;
  wlinfo.OriginatorName     = Bnk_GetPartyName( wlinfo.OriginatorID );
  wlinfo.RecipientCode      = ПолучитьКодСубъекта( {OurBank}, PTCK_UIS, null );
  wlinfo.RecipientCodeKind  = PTCK_UIS; 
  wlinfo.RecipientID        = {OurBank};
  wlinfo.RecipientName      = Bnk_GetPartyName( wlinfo.RecipientID );
  

  var ED574MessageFields = TED574MessageFields();

  ReadED574MessageFields( @ED574MessageFields );

  if( ED574MessageFields.DictionMode == "1" )
    DictionModeStr = "по регламенту";
  elif( ED574MessageFields.DictionMode == "2" )
    DictionModeStr = "по запросу";
  elif( ED574MessageFields.DictionMode == "3" )
    DictionModeStr = "после корректировки";
  end;  

  Narrative = " /Вид справочника <" + DictionModeStr + ">/";
  
  if( not ВставитьИнфСообщение( wlinfo, Narrative ) )
    std.msg( "Ошибка при вводе ответа" );
    return FALSE;
  end;

  for( var COSCustomerInfo, ED574MessageFields.COSCustomerInfoArray )
    var ReceiverID = GetObjectIDFromUISorSWIFT( COSCustomerInfo.UIS, COSCustomerInfo.SWIFTBIC );
    if( ReceiverID != 0 )
      var ReceiverObj: RsbParty = RsbParty( ReceiverID );
      if( ReceiverObj.Code.GetCode( PTCK_UIS ) == "" )
        ReceiverObj.Code.SetCode( PTCK_UIS, COSCustomerInfo.UIS );  
      end;
      if( ReceiverObj.Code.GetCode( PTCK_SWIFT ) == "" )
        ReceiverObj.Code.SetCode( PTCK_SWIFT, COSCustomerInfo.SWIFTBIC );  
      end; 
      
      // установить субъекту принадлежность "является участником СПФС"
      ReceiverObj.AddOwn(PTK_SPFS);
      
      // обновляется dptspfs_dbt
      var SPFSBeginDate501 = GetMaxBeginDate( COSCustomerInfo, 2 );
      var SPFSEndDate501 = GetMinEndDate( COSCustomerInfo, 2 ); 
      var SPFSEsSize501 = GetMinESSize( COSCustomerInfo, 2 ); 
      var SPFSArmNo501 = GetAnyARMNo( COSCustomerInfo, 2 ); 
      var SPFSBeginDate503 = GetMaxBeginDate( COSCustomerInfo, 1 );
      var SPFSEndDate503 = GetMinEndDate( COSCustomerInfo, 1 );
      var SPFSEsSize503 = GetMinESSize( COSCustomerInfo, 1 );
      var SPFSArmNo503 = GetAnyARMNo( COSCustomerInfo, 1 ); 

      ReceiverObj.SPFSIsFreeServices = IfThenElse( (COSCustomerInfo.IsFreeServices == 1), true, false );
       
      if( SPFSBeginDate501 != date(0,0,0) )
        ReceiverObj.SPFSBeginDate501 = SPFSBeginDate501;
      end;
      if( SPFSEndDate501 != date(0,0,0) )
        ReceiverObj.SPFSEndDate501 = SPFSEndDate501; 
      end;
      if( SPFSEsSize501 != 0 )
        ReceiverObj.SPFSEsSize501 = SPFSEsSize501; 
      end;
      if( SPFSArmNo501 != 0 )
        ReceiverObj.SPFSArmNo501 = SPFSArmNo501; 
      end;
      if( SPFSBeginDate503 != date(0,0,0) )
        ReceiverObj.SPFSBeginDate503 = SPFSBeginDate503;
      end;
      if( SPFSEndDate503 != date(0,0,0) )
        ReceiverObj.SPFSEndDate503 = SPFSEndDate503;
      end;
      if( SPFSEsSize503 != 0 )
        ReceiverObj.SPFSEsSize503 = SPFSEsSize503;
      end;
      if( SPFSArmNo503 != 0 )
        ReceiverObj.SPFSArmNo503 = SPFSArmNo503;
      end; 

      ReceiverObj.Update();

      ReceiverObj = null;

      for( var ESExchangeInfo, COSCustomerInfo.ESExchangeInfoArray )
        for( var COSSenderInfo, ESExchangeInfo.COSSenderInfoArray )
          var SenderID = GetObjectIDFromUISorSWIFT( COSSenderInfo.UIS, COSSenderInfo.SWIFTBIC );
       
          var InfMesID = 0;
          InfMesID = FindWlusSpFsSubject( ReceiverID, COSSenderInfo.UIS, wlinfo.InfoID, OBJTYPE_INFO );

          UpdateOrInsertWlusSpFs( InfMesID, wlinfo.InfoID, CosSenderInfo, ESExchangeInfo.ServicesCode, COSCustomerInfo.SWIFTBIC, SenderID, ReceiverID, true );
                                                                           
          var ReceiverWlusSpFsObjID = 0; 
          ReceiverWlusSpFsObjID = FindWlusSpFsSubject( ReceiverID, COSSenderInfo.UIS, ReceiverID, OBJTYPE_PARTY ); 

          UpdateOrInsertWlusSpFs( ReceiverWlusSpFsObjID, ReceiverID, CosSenderInfo, ESExchangeInfo.ServicesCode, COSCustomerInfo.SWIFTBIC, SenderID, ReceiverID, false ); 
        end;
      end;

      if( ( ED574MessageFields.DictionMode == 2 ) or ( ED574MessageFields.DictionMode == 3 ) )
        DeleteWrongWlusSpFsRecords( ReceiverID, ReceiverID, OBJTYPE_PARTY, COSCustomerInfo );
      end;
    end;    
  end;

  if( ( ED574MessageFields.DictionMode == 2 ) and ( IsExistInReqWithSP30( wlinfo.RelatedRef ) ) )
    DeleteWlusSpFsOnDepartment( wlmes.Department, ED574MessageFields )  
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;  
   