/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                   Макрос проверки сообщений SWIFT-SB                     */
/*                                                                          */
/*  Имя файла: sbsbchk.mac                                                  */
/*  Создан:    16.09.05                                  Фомченкова Л.Н.    */
/*                                                                          */
/****************************************************************************/

import BankInter, "wlchmes.mac", FIInter, "swtools.mac", "wlgenmes.mac";


/* Контроль сообщений SWIFT-SB */ 
macro CheckMes( addrMes )
  var field_name, field_value, field71 = "", FICode, FrmName, SumStr;

  SetBuff( wlmes, addrMes );

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return false;
  end;
  
  FrmName = f_wlmesrls.Name;

  if( not FillBuffPayment( wlmes ) )
     return false;
  end;

  FICode = ПолучитьКодВалютыЛог(wlpmpaym.PayFIID);

  if((FICode == "BYR"))
      SumStr = GetSWIFTAmount(wlpmpaym.PayAmount);
      if(Index(SumStr, ",") != strlen(SumStr))
        std.msg("Если валюта платежа - 'BYR', то сумма платежа должна быть без копеек");
        return false;
      end;
  end;

  if((FICode == "UAN"))
      if((strlen(trim(wlpmrmprop.PayerName)) > 27) 
      or (strlen(trim(wlpmrmprop.ReceiverName)) > 27))
        std.msg("Если валюта платежа - 'UAN', то наименования плательщика и получателя должны быть не более 27 символов");
        return false;
      end;
  end;

  while( СчитатьПоле( field_name, field_value ) )
      if(field_name == "57D")
          if((FICode == "UAN") and ((Index(field_value, "//MF") != 1)
          or  ((Index(field_value, "\n") != 11) and (strlen(field_value) != 10)) 
          or (not int(substr(field_value, 5, 6)))))
             std.msg("Если валюта платежа - 'UAH', то поле 57D должно иметь первую строку вида '//MF<код МФО>'. Код МФО - 6 цифр");
             return false;
          end;
      end;
      if(field_name == "70")
          if((FICode == "KZT") and (FrmName != "202N")
          and (Index(field_value, "KNP") == 0)) 
             std.msg("Если валюта платежа - 'KZT', то поле 70 должно содержать подстроку 'KNP'");
             return false;
          end;
      end;
      if(field_name == "71A")
          field71 = field_value;
      end;
      if(field_name == "72")
          if((Index(field_value, "/FULLPAY/") > 0) 
          and ((FrmName != "103N") or (FICode != "USD") or (field71 != "OUR")))
             std.msg( "Если в поле 72 есть подстрока '/FULLPAY/', |то валюта платежа должна быть 'USD', форма - 103N, поле 71A - 'OUR'");
             return false;
          end;
          if((Index(field_value, "/BNF/") > 0) 
          and ((FrmName != "202N") and (FrmName != "202S") and (FrmName != "204S")))
             std.msg( "Если в поле 72 есть подстрока '/BNF/', то форма должна быть 202N/S, 204S");
             return false;
          end;
          if((FICode == "BYR") and (Index(field_value, "/ACC/") == 0)) 
             std.msg("Если валюта платежа - 'BYR', то поле 72 должно содержать подстроку '/ACC/'");
             return false;
          end;
          if((FICode == "KZT") and (FrmName == "202N")
          and ((Index(field_value, "/REC/") == 0) or (Index(field_value, "KNP") == 0))) 
             std.msg("Если валюта платежа - 'KZT', то если форма 202N, то поле 72 должно содержать подстроки '/REC/' и 'KNP'");
             return false;
          end;
      end;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return false;
end;

