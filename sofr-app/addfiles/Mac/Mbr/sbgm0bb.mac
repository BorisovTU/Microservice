/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*             Генерация сообщения по подтверждению SBRF3                   */
/*                                                                          */
/*  Создан:     15.09.00                                                    */
/*  Программист Бурцев А.А.                                                 */
/****************************************************************************/

import "sbgenmes.mac";

/* Генерация сообщений по подтвердениям по транспорту  */
macro GenMesExec( TypeMes, MacroProc, Parm )
  var CodeKind = PTCK_CLIRING, error, field_value;

  ExecMacro( MacroProc, "DT", TypeMes, Parm );

  if ( wlconf.DKFlag == WL_CREDIT )
     if ( (CodeKind==wlconf.CodeKind) AND (wlconf.CodeValue!="") )
        field_value = wlconf.CodeValue;
     else
        field_value = ПолучитьКодСубъекта( wlconf.BankID, CodeKind, error );
     end;
  else
     field_value = ПолучитьКодСубъекта( {OurBank}, CodeKind, error );
  end;
  if( error != 0 )
    RunError( "|Не найден код клиринга банка участника-получателя" );
  end;
  ExecMacro( MacroProc, "RC", field_value, Parm );

  if ( wlconf.DKFlag == WL_CREDIT )
     field_value = ПолучитьКодСубъекта( {OurBank}, CodeKind, error );
  else
     if ( (CodeKind==wlconf.CodeKind) AND (wlconf.CodeValue!="") )
        field_value = wlconf.CodeValue;
     else
        field_value = ПолучитьКодСубъекта( wlconf.BankID, CodeKind, error );
     end;
  end;
  if( error != 0 )
    RunError( "|Не найден код клиринга банка участника-отправителя" );
  end;
  ExecMacro( MacroProc, "PA", field_value, Parm );

  if(TypeMes == type_603)
    ExecMacro( MacroProc, "CR", "999", Parm );
  else
  ExecMacro( MacroProc, "CR", "000", Parm );
  end;

  if ( (TypeMes>=type_101) AND (TypeMes<=type_105) )
     ExecMacro( MacroProc, "OC", substr(wlmes.TRN,7,10), Parm );
     ExecMacro( MacroProc, "OD", substr(wlmes.TRN,1,6), Parm );
     ExecMacro( MacroProc, "ON", substr(wlmes.TRN,17,6), Parm );
  else
     ExecMacro( MacroProc, "WR", substr(wlconf.Description,1,70), Parm );
  end;

  /* Дата последней обработки */
  ExecMacro( MacroProc,  "LD", ДатаДДММГГ( {curdate} ), Parm );  

  SetParm( 2, Parm );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;

/* Релиз 0B6 */
macro GenMes0B6( addrMes, addrConf )
  var format = true;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog( 2, "Генерация сообщения-подтверждения SBRF3, релиз 0B6");    

  if( wlconf.Cancel == "X" )
      return GenMesExec( type_603, "SB_ЗаписатьПолеЛог", format );
  else
  if( wlconf.DKFlag == WL_CREDIT )
    return GenMesExec( type_601, "SB_ЗаписатьПолеЛог", format );
  else
    return GenMesExec( type_602, "SB_ЗаписатьПолеЛог", format );
  end;
end;
end;

/* Релиз 0B6J */
macro GenMes0B6PIB( addrMes, addrConf )
  var BCI = "";

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog( 2, "Генерация сообщения-подтверждения SBRF3, релиз 0B6J");    

  if( wlconf.Cancel == "X" )
    if( not GenMesExec( type_603, "SB_ЗаполнитьПоле", BCI ) )
      return false;
    end;
  else
  if ( wlconf.DKFlag == WL_CREDIT )
    if( not GenMesExec( type_604, "SB_ЗаполнитьПоле", BCI ) )
      return false;
    end;
  else
    if( not GenMesExec( type_605, "SB_ЗаполнитьПоле", BCI ) )
      return false;
    end;
  end;
  end;

  SB_ЗаписатьПолеЛог( SB_Tag_PIB, BCI, true );

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;

/* Релиз 0BB */
macro GenMes0BB( addrMes, addrConf )
  var format = true;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog( 2, "Генерация сообщения-подтверждения SBRF3, релиз 0BB");    

  if( wlconf.DKFlag == WL_CREDIT )
    return GenMesExec( type_104, "SB_ЗаписатьПолеЛог", format );
  else
    return GenMesExec( type_105, "SB_ЗаписатьПолеЛог", format );
  end;
end;

/* Релиз 0BBJ */
macro GenMes0BBPIB( addrMes, addrConf )
  var BCI = "";

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog( 2, "Генерация сообщения-подтверждения SBRF3, релиз 0BBJ");    

  if ( wlconf.DKFlag == WL_CREDIT )
    if( not GenMesExec( type_104, "SB_ЗаполнитьПоле", BCI ) )
      return false;
    end;
  else
    if( not GenMesExec( type_105, "SB_ЗаполнитьПоле", BCI ) )
      return false;
    end;
  end;

  SB_ЗаписатьПолеЛог( SB_Tag_PIB, BCI, true );

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;
