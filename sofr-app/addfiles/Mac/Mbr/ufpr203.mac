/*
  $Name: ufpr203.mac
  $Module: Межбанковские расчеты
  $Description: Печать формы по сообщению ED203
*/

import "ufgendoc.mac";

macro GetRequestInfo( MesID, OriginatorCode : @String, OriginatorName : @String, RecipientCode : @String, RecipientName : @String )
  var rs = execSQLSelect( " Select wlreq.t_OriginatorCode, wlreq.t_OriginatorName, wlreq.t_RecipientCode, wlreq.t_RecipientName "
                          "   From dwlreq_dbt wlreq, dwlmeslnk_dbt meslnk "
                          "  Where meslnk.t_MesID = :MesID "
                          "    and wlreq.t_ReqID = meslnk.t_ObjID ",
                          MakeArray( SQLParam("MesID", MesID) ));
                         
  if( rs and rs.MoveNext())
    OriginatorCode = rs.value("t_OriginatorCode");
    OriginatorName = rs.value("t_OriginatorName");
    RecipientCode = rs.value("t_RecipientCode");
    RecipientName = rs.value("t_RecipientName");
  end;
end;

macro PrintForm( addrMes, MassCopy )
  var EDNo,
      EDDate,
      OriginatorCode,
      OriginatorName,
      RecipientCode,
      RecipientName,
      Acc,
      StatusCode,
      Description,
      GroupInquiryCode,
      Description2;
    
  SetBuff( wlmes, addrMes );
  
  var field_name, field_value, строка, название;  
  
  СчитатьПоле( field_name, field_value );
  название = SubStr(field_value, 2, 5);
  if(Index(field_value, String("</",название,">")))
     строка = StrSubst(field_value, String("</",название,">"),"");
  else
     строка = StrSubst(field_value, "/>",">");
  end;
  
  while( СчитатьПоле( field_name, field_value ) )
     строка = строка + field_value;
  end;
  
  строка = строка + String("</",название,">");
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(строка) )
    println( string( "Неверный формат сообщения по форме ED108" ) );
    return FALSE;
  end; 
  
  EDNo = ReadAttribute( xml, "EDNo" );
  EDDate = ReadAttribute( xml, "EDDate" );
  GetRequestInfo( wlmes.MesID, @OriginatorCode, @OriginatorName, @RecipientCode, @RecipientName );
  Acc = ReadOptinalAttribute(xml, "Acc");
  StatusCode = ReadAttribute(xml, "StatusCode");
  
  var rs = execSQLSelect( " Select t_Description "
                          "   From dwlcodes_dbt "
                          "  Where t_CodeNum = :CodeNum "
                          "    and t_AlgNum = :AlgNum ",
                          MakeArray(SQLParam("CodeNum", StatusCode),
                                    SQLParam("AlgNum", 42 /*TYPE_CODE_UFBS_ED203*/)));
  if( rs and rs.MoveNext() )
    Description = rs.value("t_Description");
  end;
  
  GroupInquiryCode = ReadAttribute( xml, "GroupInquiryCode");
  
  Description2 = "Идентификаторы ЭПС";

[
  ED203 Запрос по группе ЭПС
  
  Номер ЭС:     #########
  Дата ЭС:      ##########
  Составитель:  ########## ##################################################
  Получатель:   ########## ##################################################
  
  Номер счета, по которому производится запрос (Acc): ####################
  Код статуса ЭПС (StatusCode):                       ## ##################################################
  Код запроса по группе ЭПС (GroupInquiryCode):       #  ##################################################
](EDNo,
  EDDate,
  OriginatorCode,
  OriginatorName,
  RecipientCode,
  RecipientName,
  Acc,
  StatusCode,
  Description,
  GroupInquiryCode,
  Description2
  );
  
  return true;
end;