 /*
 $Name: ufgd206.mac
 $Module: Межбанковские расчеты
 $Description: Генерация подтверждений по сообщениям УФЭБС ED206
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям УФЭБС ED206                        */
/*                                                                          */
/* Имя файла: ufgd206.mac                                                   */
/* Создан:    27.10.04                                      Фомченкова Л.Н. */
/****************************************************************************/
import "ufgendoc.mac", "wltools.mac";

var ver_st = 1;

private macro GetDKFlag(DC : string)
  if(DC == "3")
    return WL_DEBETKREDIT;
  else
    return UF_GetDKFlag(DC);
  end;
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Сумма, Currency, Error, CorrBIK:string;
  var WlPmID = 0;
  var PaymentID = 0;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация подтверждения по ED206" );

  ClearRecord( wlconf );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
    println( string( "Неверный формат сообщения по форме ED206, невозможно загрузить xml-документ" ) );
     return false;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number       = wlmes.TRN;
  wlconf.RelatedRef   = wlmes.RelatedRef;
  wlconf.FIID         = 0;
  wlconf.DateValue    = ToDate(ReadAttribute(xml,"EDDate", "EDRefID"));
  wlconf.Account      = ReadOptinalAttribute(xml,"Acc");
  Сумма = ReadAttribute(xml,"Sum");
  wlconf.Sum          = moneyL( Сумма )/100;
  wlconf.DKFlag = GetDKFlag(ReadAttribute(xml, "DC"));
  wlconf.TransferDate = ToDate(ReadAttribute(xml,"TransDate"));
  wlconf.DocNumber    = ReadAttribute(xml, "AccDocNo", "AccDoc");

  /*ЗАП.№ 110284*/
  if(ver_st == 2)
    ReadAttribute(xml,"TransTime");

    var PaytKind : string = ReadOptinalAttribute(xml, "PaytKind");
    if(PaytKind)
      wlconf.Description = wlconf.Description + "Вид платежа: " + PaytKind;
    end;
  end;

  /* Определяем схему расчетов */
  wlconf.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlconf.FIID, 1 );
  if( wlconf.Corschem == -1 )
    Currency = ПолучитьКодФинИн(wlconf.FIID, error);
    if ( error )
       Currency = "???";
    end;
    std.msg( "Не определена схема расчетов для респондента по валюте " + Currency );
    return FALSE;
  end;

  var select = "select t_PaymentID "
                 "from dwlmes_dbt mes, "
                      "dwlmeslnk_dbt meslnk, "
                      "dwlpm_dbt wlpm "
                 "where mes.t_Trn = :RelatedRef1 "
                   "and meslnk.t_MesID = mes.t_MesID "
                   "and meslnk.t_ObjKind = 501/*OBJTYPE_PAYMENT*/ "
                   "and wlpm.t_WlPmID = meslnk.t_ObjID "
                   "and wlpm.t_Direct = chr(0)/*WLD_MES_OUT*/ ";  
  
  var SettlementTime = GetSettlementTime( xml, wlmes );

  if( SettlementTime != time(0,0,0) )
    // Сохраняем в платеж
    execSQL( "update dpmrmprop_dbt "
             "   set t_SettlementTime = :SettlementTime1 "
             " where t_PaymentID in ( " + select + " )"
             "   and t_SettlementTime < :SettlementTime2 ",
             makeArray( SQLParam("SettlementTime1", time(SettlementTime)),
                        SQLParam("RelatedRef1", wlmes.RelatedRef ),
                        SQLParam("SettlementTime2", time(SettlementTime))
                      )
           );

    // Сохраняем в учетный объект
    if(wlconf.Description)
      wlconf.Description = wlconf.Description + "\n";
    end;
    wlconf.Description = wlconf.Description + 
      "SettlementTime (Время исполнения в ПБР): " + TimeToStr(SettlementTime, "hh:mi:ss");
  end;
  
  var StatusCode = "00";

  // Обновляем поле dwlpm_dbt.t_StatusCode
  if( StatusCode != "" )
    execSQL( "update dwlpm_dbt "
             "   set t_StatusCode = :StatusCode "
             " where t_PaymentID in ( " + select + " )",
             makeArray( SQLParam("StatusCode", StatusCode ),
                        SQLParam("RelatedRef1", wlmes.RelatedRef )
                      )
           );
  end;

  /* Заполняем ссылку на банк плательщика/получателя */
  wlconf.BankID = ToRespID( ReadAttribute(xml, "EDAuthor", "EDRefID"), wlconf.CodeValue );
  if( ПолучитьСубъекта( wlconf.BankID, wlparty ) != 0 )
    std.msg( "Не найден банк плательщик/получатель с идентификатором: " + wlconf.CodeValue );
    return FALSE;
  end; 
  wlconf.CodeKind = PTCK_BIC;
  wlconf.BankName = wlparty.Name;
  
  /* Банк - корреспондент*/
  CorrBIK = ReadOptinalAttribute(xml,"BICCorr");
  if( CorrBIK != "" )
    ClearRecord(wlparty);
      wlconf.CorrCodeKind  = PTCK_BIC;
      wlconf.CorrCodeValue  = RectoreNull(CorrBIK,9);
      wlconf.CorrBankID = ПолучитьКодСубъекта( wlconf.CorrCodeValue, wlconf.CorrCodeKind, Error );
      if( Error )
        std.msg( "Не найдена ссылка на банк корреспондент: " + wlconf.CorrCodeValue );
        return FALSE;
      end;
    if( ПолучитьСубъекта( wlconf.CorrBankID, wlparty ) != 0 )
        std.msg( "Не найден банк корреспондент" );
        return FALSE;
      end; 
    wlconf.CorrBankName = wlparty.Name;
  end;

  /* Вставляем запись в таблицу подтверждений */
  if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вводе подтверждения");
    return FALSE;
  end;
 
  
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
