/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                                                                          */
/*  Отказы МЦИ                                                              */
/*                                                                          */
/*  Создан:     03.10.00                                                    */
/*  Программист Бурцев А.А.                                                 */
/****************************************************************************/

import wlmcgd, wlmctool, wlfindpm;

macro GenDoc( addrMes )
  var ДатаПлатежа;

  var Строка, Причина;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация подтверждения по R-макету" );

  ClearRecord( wlconf );

  if( not СчитатьПоле( FormFldE57, Строка ) )
    return FALSE;
  end;

  wlconf.Cancel     = "X";
  wlconf.Number     = wlmes.TRN;

  if ( substr(Строка, 1,1)=="R" )
     wlconf.RelatedRef = substr(Строка, 2, 6);
     ДатаПлатежа = СчитатьДату( Строка, 8 );
  else
     wlconf.RelatedRef = substr(Строка, 1, 6);
     ДатаПлатежа = СчитатьДату( Строка, 7 );
  end;
  wlconf.Account    = Свой_Корсчет;  

  /* Ищем платеж, на который пришло подтверждение */
  if( not WldFindPmByTRN( true, wlconf.RelatedRef, wlmes.OutsideAbonentID, ДатаПлатежа ) )
    std.msg("Не найден исходящий платеж");
    return false;
  end;

  wlconf.Sum       = wlpmpaym.PayAmount;
  wlconf.DateValue = wlpmpaym.ValueDate;
     wlconf.DKFlag = WL_CREDIT;

  if( СчитатьПоле( FormFldDsc, Причина ) )
     wlconf.Description = Причина;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg( "Ошибка при вставке подтверждения" );
    return FALSE;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;


end;
