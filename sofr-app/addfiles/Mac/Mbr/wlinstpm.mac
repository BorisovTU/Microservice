/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*       Макрофункции для формирования уведомления по срочному платежу      */
/*                                                                          */
/*  Имя файла: wlinstpm.mac                                                 */
/*  Создан:  19.05.2008                                                     */
/****************************************************************************/
import "globals.mac", PTInter, likepy, oralib, InsCarryDoc, PaymInter;

const TYPETEMPLINFO_OK         = 1; //Средства зачислены на счет
const TYPETEMPLINFO_UNKNOWNSUM = 2; //Средства зачислены на счет невыясненых сумм

private macro ОпределитьТранспортСообщения(PaymentID, Trn)
  var TpID = 0;

  var select = "select wts.t_TpID, wm.t_Trn "+
                 "from dWlpm_dbt wp, dWlmeslnk_dbt wml, dWlmes_dbt wm, dwltpshem_dbt wts "+
                "where wp.T_PAYMENTID = :PaymentID "+
                  "and wp.t_Direct = 'X' "+
                  "and wml.t_ObjID = wp.t_WlpmID "+
                  "and wml.t_ObjKind = 501 "+  //OBJTYPE_PAYMENT
                  "and wm.t_MesID = wml.t_MesID "+
                  "and wts.t_TpShemID = wm.t_TpSchemID";

  var params = MakeArray( SQLParam("PaymentID", PaymentID));      
  
  var rs = execSQLselect( select, params, false );
    
  if(rs and rs.moveNext() )
    TpID = rs.value(0);
    SetParm(1, rs.value(1));
  else
    SetParm(1, "");
  end;
  
  return TpID;
end;

//Получить тип кода и код субьекта
// PartyID - обязательный параметр
// CodeKind - если задан, то ищется этот тип кода
// если не задан, то подхватывается любой (и заполняется найденым).
// Code - код типа CodeKind у субъекта PartyID
/*private macro GetCode(PartyID, CodeKind, Code)
  
  var select = "select pt.T_CODEKIND, pt.T_CODE "+
                 "from dpartcode_dbt pt "+
                "where pt.T_PARTYID = :PartyID ";

  var params = MakeArray(SQLParam("PartyID", PartyID));      
  
  if(CodeKind)
    select = select + "and pt.T_CODEKIND = :CodeKind";
    params[params.size] = SQLParam("CodeKind", CodeKind);
  end;
  
  
  var rs = execSQLselect(select, params, false );
    
  if(rs and rs.moveNext())
    SetParm(1, rs.value(0));
    SetParm(2, rs.value(1));
  end;

end;

// если транспорт определен и у него есть основной вид кода, то использовать этот код, иначе 
// использовать первый попавшийся вид кода, который задан для субъекта Wlinfo.OriginatorID = {OurBank}
private macro GetOrigiantorCodeKind(TpID, OriginatorID, Code)
  var WldTpCode = Tbfile("wltpcode.dbt", "r", 2);
  var CodeKind = 0;

  WldTpCode.rec.TpID = TpID;
  WldTpCode.rec.Primary = "X";
  
  if(WldTpCode.GetEQ())
    CodeKind = WldTpCode.rec.CodeKind;
  end;

  GetCode(OriginatorID, CodeKind, Code);
  SetParm(2, Code);
  return CodeKind;

end;*/                                                                      
              
macro ФормированиеУведомленияПоСрочномуПлатежу(PaymentID, TypeTempl)
  
  var Trn = "",
      StrInfo = "",
      TpID = ОпределитьТранспортСообщения(PaymentID, Trn),
      Direct= GetOprStatus( OPR_PAYM_DIRECT ),
      err = 0;

  
  record Party(Party);
  
  var PaymentObj:RsbPayment = RsbPayment( PaymentID );

  if(Direct == OPR_PM_ST_DIR_INTERNAL)
    TpID = 11; //"Почта ЦАБС"
  else
    TpID = ОпределитьТранспортСообщения(PaymentID, Trn); 
  end;

  var SetPrimTpCodeFromOriginator = false;
  record Wlinfo (wlinfo);

  Wlinfo.Direct = "";//WLD_MES_OUT
  Wlinfo.Trn = "";
  Wlinfo.RelatedRef  = Trn;              
  Wlinfo.TypeOper = "139";
  
  //Заполняем отправителя
  Wlinfo.OriginatorID = PaymentObj.ReceiverMesBankID;
  Wlinfo.OriginatorCodeKind = PTCK_CLIRING;
  Wlinfo.OriginatorCode = GetCodeParty(Wlinfo.OriginatorID, Wlinfo.OriginatorCodeKind, err);
  
  if(err != 0)
    msgbox("Не найден код №", Wlinfo.OriginatorCodeKind, " у субъекта с ID = ", Wlinfo.OriginatorID);
    return false;
  end;

  if(ПолучитьСубъекта (Wlinfo.OriginatorID, Party) != 0)
    msgbox("Не найден субъект с ID = ", Wlinfo.OriginatorID);
    return false;
  end;

  Wlinfo.OriginatorName = Party.Name;
  
  //Заполняем получателя
  Wlinfo.RecipientID       = PaymentObj.PayerMesBankID;
  Wlinfo.RecipientCodeKind = PTCK_CLIRING;
  Wlinfo.RecipientCode     = GetCodeParty(Wlinfo.RecipientID, Wlinfo.RecipientCodeKind, err);
  
  if(err != 0)
    msgbox("Не найден код №",Wlinfo.RecipientCodeKind, " у субъекта с ID = ", Wlinfo.RecipientID);
    return false;
  end;

  if(ПолучитьСубъекта(Wlinfo.RecipientID, Party) != 0)
    msgbox("Не найден субъект с ID = ", Wlinfo.RecipientID);
    return false;
  end;

  Wlinfo.RecipientName = Party.Name;
  
  //Заполняем подразделение
  Wlinfo.OfficeID = Wlinfo.OriginatorID;             
  Wlinfo.OfficeCodeKind = Wlinfo.OriginatorCodeKind;                   
  Wlinfo.OfficeCode = Wlinfo.OriginatorCode;

  //Узнаем сколько сейчас времени
  var curTime = Time(), hour, min;
  TimeSplit(curTime, hour, min);
  
  //Найдем ФИО опера
  var Person = Tbfile("person.dbt", "r", "bank.def");
  Person.rec.Oper = {oper};
  Person.GetEQ();
  
  if(TypeTempl == TYPETEMPLINFO_OK)
    StrInfo = "Средства по СПП N " + PaymentObj.Number + " от " + PaymentObj.Date +" "+ PaymentObj.PayerName + " на сумму " + 
               PaymentObj.PayerAmount + " в " + hour + " ч. " + min + " мин. "+ date() + " зачислены на счет N " + 
               PaymentObj.ReceiverAccount + " "+ GetNameClient("",PaymentObj.ReceiverAccount,1,PaymentObj.ReceiverFIID) + ". " + Person.rec.Name;
  else
    StrInfo = "Средства по СПП N " + PaymentObj.Number + " от " + PaymentObj.Date +" "+ PaymentObj.PayerName + " на сумму " + 
               PaymentObj.PayerAmount + " в " + hour + " ч. " + min + " мин. "+ date() + 
               " зачислены на счет невыясненных сумм. " + Person.rec.Name;
  end;

  if(CreateInfoMess(Wlinfo, StrInfo, TpID))
    msgbox("Ошибка создания информационного сообщения.");
    return false;
  end;

  return true;
end;
