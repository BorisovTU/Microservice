 /*
 $Name: znsosprn.mac
 $Module: МБР
 $Description: Печать справки об остатках на счетах
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : znsosprn.mac
  Created     : 28.01.2011
  Programmer  : Popova O.
  Description : Печать справки об остатках на счетах

└───────────────────────────────────────────────────────────────────────────*/
import "fnsprzall.mac";

private var ErrMsg: string = "";

private macro PrintOneDepAcc(this: FNSPrint, Accounts: TArray, CurPos: integer, GetRest, ReqDate: date)
  Array Text, Buttons;
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  if (CurPos < Accounts.Size)
    [
      Таблица 1
      ┌─────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬───────────────────┬──────────────────┬─────────────────────────────────────────────────────────────┬─────────────┬─────────────────────────────────────┐
      │     │                         │                         │                         │                         │   дата открытия   │ дата закрытия    │             дата изменения реквизитов счета                 │цифровой код │ остаток денежных средств на счете   │
      │     │                         │                         │                         │                         │       счета       │     счета        │            (специального банковского счета)                 │валюты счета │  (специальном банковском счете)     │
      │  №  │        № счета          │       вид счета         │        владелец         │       бенефициар        │   (специального   │ (специального    │            (дд.мм.гг); № измененного счета                  │(специального├───────────────────┬─────────────────┤
      │ п/п │     (специального       │     (специального       │         счета           │                         │    банковского    │  банковского     │            (специального банковского счета)                 │банковского  │  дата, на которую │    остаток      │
      │     │    банковского счета)   │    банковского счета)   │                         │                         │ счета) (дд.мм.гг) │ счета) (дд.мм.гг)│            (до изменения / после изменения)                 │   счета)    │   указан остаток  │     (руб.,      │                    
      │     │                         │                         │                         │                         │                   │                  │                                                             │             │      (дд.мм.гг)   │  коп./вал.)     │
      ├─────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼───────────────────┼──────────────────┼─────────────────────────────────────────────────────────────┼─────────────┼───────────────────┼─────────────────┤
      │  1  │           2             │           3             │           4             │            5            │          6        │        7         │                              8                              │      9      │         10        │       11        │
      ├─────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼───────────────────┼──────────────────┼─────────────────────────────────────────────────────────────┼─────────────┼───────────────────┼─────────────────┤
      │     │                         │                         │                         │                         │                   │                  │                                                             │             │                   │                 │
    ];
    var i = CurPos;
    var Dep = Accounts(i).rec.Department;
    var Amount: Money;
    var SeqNumber : Integer = 1;
    while( (i < Accounts.Size) and (Accounts(i).rec.Department == Dep) )
      if( strlen(GetError(Accounts(i).rec.Account, WlRegDecID)) == 0 )
      Amount = ExecMacro2(GetRest, Accounts(i).rec.Account, Accounts(i).rec.Chapter, Accounts(i).rec.Code_Currency, ReqDate, this.RepPriority, this.RepClaimID );
      if( Amount < 0 )             
        Amount = 0;
      end;
      if( AccClaimIncomplete( Accounts[i].rec.Account, Accounts[i].rec.Chapter, Accounts[i].rec.Code_Currency ) )
        ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + Accounts[i].rec.Account;
        Text(0) = string( "По лицевому счету ", Accounts[i].rec.Account, " имеются незавершенные операции, которые учтены при расчете остатка.", 
                          "|Сумма остатка на счете ", Amount, ".|Продолжить печать справки?" );
        if( GetDialogFlag() and ( ConfWin( Text, Buttons ) == 1 ) )
          return 1; 
        end;
      end;

    [ │#####│#########################│#########################│#########################│#########################│###################│##################│#############################################################│#############│###################│#################│]
     ( SeqNumber:c,
       Accounts(i).rec.Account:c, 
       this.GetNameTypeAcc( Accounts(i).rec.Type_Account, Accounts(i).rec.Code_Currency ):c,
       this.GetAccName( Accounts(i).rec.Client ):c:w:4,        // AccOwner
       this.GetAccName( Accounts(i).rec.BeneficiaryID ):c:w:4, // AccBenef
       Accounts(i).rec.Open_date:c,
       Accounts(i).rec.Close_date:c,
       Changes[i]:c,
       GetFICode( Accounts(i).rec.Code_Currency ):c,
       ReqDate:c,
       Amount:c
       );
        SeqNumber = SeqNumber + 1;
      end;
      i = i + 1;
    end;
    [ └─────┴─────────────────────────┴─────────────────────────┴─────────────────────────┴─────────────────────────┴───────────────────┴──────────────────┴─────────────────────────────────────────────────────────────┴─────────────┴───────────────────┴─────────────────┘
    ];
    return i;
  end;
end;


macro PrintReportInfoAboutRestAccounts( this: FNSPrint, ReqDate:date )

  var Accounts:TArray = TArray(), PrnAccounts:TArray = TArray();
  var rs;
  rs = execSQLSelect("select t_Code from ddp_dep_dbt where t_PartyID = " + int(this.RepBankID));
  rs.moveNext();
  var Department = rs.value(0);
  Array Acc;

  if( ( this.Счет.size > 0 ) and ( valtype( this.Счет[0] ) == V_GENOBJ ) )
    PrnAccounts = СортироватьСчета(this.Счет, Department);
  elif( ClientID > 0 )                                     // для RPO счета отбираются по-другому
    Accounts = ВыбратьСчетаКлиента( );
  end;
  Array Text, Buttons;
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  var GetRest;
  
  if( ( this.Счет.size > 0 ) and ( valtype( this.Счет[0] ) == V_GENOBJ ) )
    GetRest = R2M(this, "GetLnkAccRestTax");
  else
    GetRest = @GetFreeAmount;
  end;
  var BlockNum = 1;
  var BlockSubNum = 1;

  this.ЗаголовокСправки_ММ_3_06_178("1114306"); 
  [
                                         Справка
                            об остатках денежных средств на счетах
                                (специальных банковских счетах)
  ]; 
  this.ДанныеБанкаИ_Клиента();
  var i = 0, j = -1;
  while( i < Accounts.Size )
    while( ( j < this.Счет.Size ) )
      j = j + 1;
      if(   ( this.Счет.Size == 0 ) or
          ( ( j < this.Счет.Size ) and 
            ( this.Счет[j] == Accounts[i].rec.Account ) and ( GetFICode( Accounts[i].rec.Code_Currency ) == SubStr( this.Счет[j], 6, 3 ) ) ) )
        PrnAccounts[PrnAccounts.Size] = Accounts[i];
        if( j < this.Счет.Size )
          this.Счет[j] = "";
        end;
        break;
      end;
    end;
    j = -1;
    i = i + 1;
  end;

  if( PrnAccounts.Size <= 0 )
    MsgBox( "Не найдено ни одного открытого счета клиента" );
    return 1;
  elif( ( this.Счет.Size > 0 ) and ( PrnAccounts.Size < this.Счет.Size ) )
    j = 0;
    if( ErrMsg == "" )
      ErrMsg = "Не найден(ы) счет(а) ";
    end;
    while( j < this.Счет.Size )         
      if( this.Счет[j] != "" )
        Text(0) = string("Не найден счет № ", this.Счет[j], ".|Продолжить печать?");
        if( GetDialogFlag() and ( ConfWin( Text, Buttons ) == 1 ) )
          return 1; 
        else
          // запомним все ненайденные счета через ","
          ErrMsg = ErrMsg + IfThenElse( StrBrk( ErrMsg, "0123456789" ),  ", ", "" ) + this.Счет[j];
        end;
      end;
      j = j + 1;
    end;
    if( not GetDialogFlag() and
        ( StrBrk( ErrMsg, "0123456789" ) ) )
      SetMNSWarning( ErrMsg );
    end;
  end;
  i = 0;
  ErrMsg = "По лицевому(вым) счету(ам) ";
  // YES
  var Errors = 0;
  for(i, 0, PrnAccounts.size - 1)
    var Description = GetError(PrnAccounts(i).rec.Account, WlRegDecID);
    if(Description) 
      Errors = Errors + 1;
    end;
  end;
  i = 0;
  if(GetYes(PrnAccounts, WlRegDecID) or (Errors < PrnAccounts.size))
    [
     об остатках денежных средств на счетах (специальных банковских счетах) указанного лица, а также остатков денежных средств, права на которые принадлежат                  ┌───┐
       (денежные средства подлежат передаче при возникновении оснований, предусмотренных договором с банком) указанному лицу, на счетах иных лиц действовавших                │ # │
                                                                                                                                                                              └───┘]
       (GetYes(PrnAccounts, WlRegDecID));
      
    while (i < PrnAccounts.size)
      this.ДанныеФилиала(BlockNum, BlockSubNum, PrnAccounts(i).rec.Department, ReqDate);
      i = PrintOneDepAcc(this, PrnAccounts, i, GetRest, date(ReqDate));
      BlockSubNum = BlockSubNum + 1;
    end;
  end;

  // OP
  var Op = GetOp(PrnAccounts, this.WlRegDecID);
  if(Op)
  [                                                                                                                                                                          ┌───┐
      об отсутствии следующих счетов (специальных банковских счетов) указанного лица, а также счетов иных лиц для совершения операций с денежными средствами, права          │ X │ 
    на которые принадлежали (денежные средства подлежали передаче при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших            └───┘ 
    ################# в:
    ─────────────────
    ]( string( "\"", substr(YYYYMMDD(ReqDate),7,2), "\" ",
                     Месяца(substr(YYYYMMDD(ReqDate),5,2)):c, " ",
                     substr(YYYYMMDD(ReqDate),1,4), " г." ));
    i = 0;
    var SeqNumber : Integer = 1;
    while(i < PrnAccounts.size )
      if( isAccNotFound(PrnAccounts(i).rec.Account, this.WlRegDecID))
        StrSplit( PrnAccounts(i).rec.Account, Acc, 1, 1, 20 );   
        [
          ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐
    ##  № │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│
                └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘
          владелец счета ####################################################################################################
                         ────────────────────────────────────────────────────────────────────────────────────────────────────
        ]( SeqNumber,
     Acc(0), Acc(1), Acc(2), Acc(3), Acc(4), Acc(5), Acc(6), Acc(7), Acc(8), Acc(9),
     Acc(10), Acc(11), Acc(12), Acc(13), Acc(14), Acc(15), Acc(16), Acc(17), Acc(18), Acc(19),
           this.GetAccName( PrnAccounts(i).rec.Client )        // AccOwner
   );
        SeqNumber = SeqNumber + 1;
      end;
      i = i + 1;
    end;
  end;
  
  // NO
  Errors = 0;
  for(i, 0, PrnAccounts.size - 1)
    Description = GetError(PrnAccounts(i).rec.Account, WlRegDecID);
    if( (Description) and (SubStr(strlwr(Description), 1, strlen("Счет не найден")) != strlwr("Счет не найден"))) 
      Errors = Errors + 1;
    end;
  end;
  if( Errors or (PrnAccounts.size == 0) )
  [                                                                                                                                                                          ┌───┐
      об отсутствии счетов (специальных банковских счетов) указанного лица, а также счетов иных лиц для совершения операций с денежными средствами, права                    │ X │
    на которые принадлежали (денежные средства подлежали передаче при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших            └───┘];
   this.ДанныеФилиала(1, 1, PrnAccounts(0).rec.Department, ReqDate);
  end;
  

  // PS
  if(this.Ps)
  [                                                                                                                                                                          ┌───┐
      справка об остатках денежных средств на счетах (специальных банковских счетах) в части информации следующих филиалов банка будет представлена ими самостоятельно:      │ X │
                                                                                                                                                                             └───┘];
  this.ДанныеФилиала(1, 1, PrnAccounts(0).rec.Department, ReqDate);
  end;
  

  if( not GetDialogFlag() and
      ( StrBrk( ErrMsg, "0123456789" ) ) )
    // это не ошибка, просто предупреждение
    SetMNSWarning( ErrMsg + " имеются незавершенные операции, которые учтены при расчете остатка." );
  end;
  ДатаПодписьПредставителяБанка();
  [];
  return 0;
end;
