/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Отчет "Перечень начальных по респондентам"                              */
/*  Имя файла: wllsresp.mac                                                 */
/*  Создан:  02.03.01                                         Бабин А.П.    */
/****************************************************************************/

import "wldoc.mac", FIInter;

const PrintHeadStatUndef  = 0,
      PrintHeadStatDK     = 1,
      PrintHeadStatResp   = 2,
      PrintHeadStatReceiv = 3;

var PrintHeadStat = PrintHeadStatUndef;

var CurRespID, CurAccount, SumReceiver=0, SumCorrespondent=0, SumAll=0, DK, FI;

macro SumToStr(FIID, sum)

   f_wlfininstr.FIID = FIID;
   if ( GetEQ( f_wlfininstr ) )
      return CurToStrAlt( sum, null, null, int(f_wlfininstr.ISO_Number) );
   elif ( FIID>=0 )
      return string(sum) + " (Неизвестная валюта)";
   else return "";
   end;

end;

macro DateToStr( date )
   var day, mon, year, str;
   DateSplit(date, day, mon, year );
   if ( day < 10 )
     day = "0"+string(day);
   else
     day = string(day);
   end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = day + "." + mon + "." + string(year);
  return str;
end;

macro ChangeReceiverHead( account, ClientName )
   var Receiver = "";
   file Party( party ) key 0;   

   if ( (valtype(CurAccount)==V_UNDEF) OR (CurAccount!=account) )

      SumReceiver = 0;
      CurAccount = account;

      if ( account!="NextTable" )
         /*if ( (valType(Client)!=V_UNDEF) AND (Client>0) )
            Party.PartyID = Client;
            if ( GetEQ(Party) )
                Receiver = Party.ShortName;
            end;
         end;*/      
         if ( PrintHeadStat==PrintHeadStatReceiv )
            [|           |----------|-----------------------------------------------------|] ;
         end;
         [|           |Получатель|#####################|Счет ######################### |]          
         ( substr(/*Receiver*/ClientName,1,21), account:f ) ;
         [|           |          |---------------------+-------------------------------|];
         PrintHeadStat = PrintHeadStatReceiv;
     end;
   end;
end;

macro ChangeReceiverFloor( account, ClientName )
   var Receiver = "";
   file Party( party ) key 0;

   if ( (valtype(CurAccount)!=V_UNDEF) AND (((CurAccount!=account) AND (CurAccount!="NextTable")) OR (Account=="NextTable")) )
      [|           |----------|---------------------+-------------------------------|];
      [|           | Итого    | ####################                                |]
      ( SumReceiver:l ) ;
   end;
end;

macro ChangeRespond( RespID, RecAccount, ClientName )
   file Party( party ) key 0;   
   if ( (valtype(curRespID)!=V_UNDEF) AND ((RespID==-1) OR ((curRespID!=RespID) AND (curRespID!=-1))) )
      CurAccount = CurAccount+"update";
      ChangeReceiverFloor( RecAccount, ClientName );
      [|           |----------+-----------------------------------------------------|
      | Итого     | ####################                                           |](SumCorrespondent:l);
   else
      ChangeReceiverFloor( RecAccount, ClientName );
   end;   

   if ( (valtype(curRespID)==V_UNDEF) OR (curRespID!=RespID) )

      SumCorrespondent = 0;
      SumReceiver = 0;
      curRespID = RespID;

      if ( RespID>=0 )
         Party.PartyID = RespID;
         if ( GetEQ(Party) )
            if ( (PrintHeadStat==PrintHeadStatReceiv) OR (PrintHeadStat==PrintHeadStatResp) )
               [|-----------|----------------------------------------------------------------|] ;
            end;
            [|Респондент | #                                                              |]
            ( Party.Name ) ;
            [|-----------|----------+---------------------+-------------------------------|] ;
            PrintHeadStat = PrintHeadStatResp;
         end;
      end;
   end;

   ChangeReceiverHead( RecAccount, ClientName );
end;

macro PrintHeader( DKFlag, Corschem, FIID, DateB, DateE, DepBIC, DepName, DepID )
  var RepName = "Начальные " ;

  FI = FIID;

  /*ChangeRespond( -1, "" );*/

  if( DKFlag == "Д" )
    RepName = RepName + "дебетовые" ;
/*[+-----------+----------------------------------------------------------------+
   Общая сумма платежей: #################### #]( SumAll:a, SumToStr(FIID, SumAll) ) ;*/
println("");
    SumAll = 0;
  else
    RepName = RepName + "кредитовые" ;
  end ;  

  DK = DKFlag;

[ Вид #
  _____

  ##################### документы за период с #          по #
  Филиал: ############### #
  Схема расчетов: N #   
  Валюта: ############### #

]( DKFlag, RepName, DateToStr(DateB), DateToStr(DateE), 
   DepBIC, DepName, 
   Corschem,
   ПолучитьКодФинИн(FIID), ПолучитьИмяФинИн(FIID) ) ;

  println("+----------------------------------------------------------------------------+");
  println("|           |          |         Сумма       |   Введен   |       Номер      |") ;
  println("|-----------|----------+---------------------+-------------------------------|") ;

  PrintHeadStat = PrintHeadStatDK;

  return true;
end;

macro PrintRecord( addrPmPaym, addrRm, /*addrDebet, addrCredit*/addrPmProp )
    var stat, NamePaym, Account, ClientName, RespID, Date_Document, error;

    SetBuff( wlpmpaym, addrPmPaym );    
    SetBuff( wlpmrmprop, addrRm );
    /*SetBuff( wlpmpropdeb, addrDebet );
    SetBuff( wlpmpropcred, addrCredit );*/
    SetBuff( wlpmprop, addrPmProp );    

    if( DK == "К" )
      Account       = wlpmpaym.ReceiverAccount ;
      /*Client        = wlpmpaym.Receiver ;*/
      ClientName    = wlpmrmprop.ReceiverName ;
      RespID        = wlpmpaym.ReceiverBankID;
    else
      Account       = wlpmpaym.PayerAccount ;
      /*Client        = wlpmpaym.Payer ;*/
      ClientName    = wlpmrmprop.PayerName ;
      RespID        = wlpmpaym.PayerBankID;
    end ;
    
    NamePaym = wlpmrmprop.Number;
    Date_Document = wlpmrmprop.Date;    

    /*if ( wlpmpropdeb.PaymentID==wlpmpaym.PaymentID )
        RespID = ПолучитьКодСубъекта( wlpmpropdeb.BankCode, wlpmpropdeb.CodeKind, error );
    else
        RespID = ПолучитьКодСубъекта( wlpmpropcred.BankCode, wlpmpropcred.CodeKind, error );
    end;

    if ( error )
       PrintLn("Не найден код субъекта");
       return false;
    end;*/

    ChangeRespond( RespID, Account, ClientName );

    [|           |          |  ################## | ########## | ################ |]
( wlpmpaym.BaseAmount:a, DateToStr(Date_Document), NamePaym:r ) ; 

    SumReceiver = SumReceiver + wlpmpaym.BaseAmount;
    SumCorrespondent = SumCorrespondent + wlpmpaym.BaseAmount;
    SumAll = SumAll + wlpmpaym.BaseAmount;
    return true;
end;

macro PrintFloor( )

   ChangeRespond( -1, "NextTable" );
[+-----------+----------------------------------------------------------------+
   Общая сумма платежей: #################### #]( SumAll:a, SumToStr(FI, SumAll) ) ;
    return true;
end;