/*
$Name:           ufgd274.mac
$Module:         МБР
$Description:    Генерация ответов по сообщению ED274
*/                                                                            

import "ufgendoc.mac";
import MesInter, WldInter, oralib, likepy, "wluftool.mac";

private macro GetOutMesPayment
( Trn : string, 
  OutMesID : @integer, 
  OutMesState : @integer,
  PaymentID : @integer,
  DocKind : @integer
) : bool

  var select = 
    "Select mes.t_MesID, mes.t_State, pm.t_PaymentID, pm.t_DocKind " +
    "  from dwlmes_dbt mes, dwltpshem_dbt shem, dwlmesrls_dbt rls, dwlmesfrm_dbt frm," +
    "       dwlmeslnk_dbt lnk, dwlpm_dbt wlpm, dpmpaym_dbt pm " +
    " where mes.t_Trn        = :Trn " +
    "   and mes.t_Direct     = chr(0) /* WLD_MES_OUT */" +
    "   and mes.t_TpSchemID  = shem.t_TpShemID " +
    "   and shem.t_TpID      = 9 /* TRANSP_UFBS */ " +
    "   and mes.t_RlsFormID  = rls.t_RlsFormID " +
    "   and rls.t_FormID     = frm.t_FormID " +
    "   and frm.t_Name in ('ED113', 'ED114') " +
    "   and lnk.t_MesID      = mes.t_MesID " +
    "   and lnk.t_Direct     = chr(0) /* WLD_MES_OUT */" +
    "   and lnk.t_ObjKind    = :ObjType " +
    "   and lnk.t_ObjID      = wlpm.t_WlPmID " +
    "   and wlpm.t_PaymentID = pm.t_PaymentID ";

  var params : TArray = makeArray( SQLParam("Trn", Trn),
                                   SQLParam("ObjType", OBJTYPE_PAYMENT) );

  var rs : RsdRecordset = execSQLselect(select, params);

  if(rs and rs.moveNext())
    PaymentID = rs.value("t_PaymentID");
    OutMesID = rs.value("t_MesID");
    OutMesState = rs.value("t_State");
    DocKind = rs.value("t_DocKind");
    return TRUE;
  end;

  return FALSE;
end;

private macro PutOutMesToDelivered(OutMesID : integer, OutMesState : integer)

  // Если статус исходного сообщения != "обработано", присвоить ему статус "доставлено".
  if(OutMesState != WLD_STATUS_MES_CLOSED)
    if( WldChangeStatus(OutMesID, OBJTYPE_MES, WLD_STATUS_MES_DELIV) != 0 )
      RunError("Ошибка при смене статуса исходного сообщения");
    end;
  end;

end;

private macro ProcessAnswer(wlmes, xml : object, wlreq)
  // Поиск платежа
  var OutMesID : integer = 0, OutMesState : integer, 
      PaymentID : integer = 0, DocKind : integer;
  if( not GetOutMesPayment(wlmes.RelatedRef, @OutMesID, @OutMesState, @PaymentID, @DocKind) )
    // Если ни одно сообщение не найдено, завершить процедуру
    return;
  end;

  // Создать связку ответа с платежом
  if( not IsLinkedWlreq(wlreq.ReqID, PaymentID, OBJTYPE_PMCLAIM, "" /*WLD_MES_OUT*/) )
    if( WldCreateReqLink(wlreq.ReqID, PaymentID, OBJTYPE_PMCLAIM, "" /*WLD_MES_OUT*/) != 0 )
      RunError("Не удалось создать связку ответа с платежом");
    end;
  end;

  // Получить значение атрибута "InfoCode" из сообщения ответа
  var InfoCode : integer = int( ReadAttribute(xml, "InfoCode") );

  if( InList(InfoCode, 1, 2, 3, 5, 7) )
    // Вызвать процедуру аннулирования платежа 
    var DenialReason : string = string(InfoCode, "; ", ПолучитьОписаниеЗапросаОтвета(wlreq));
    if( PS_CancelOrder(PaymentID, DocKind, DenialReason) )
      RunError("Ошибка при аннулировании платежа");
    end;

    // Исходное сообщение - в доставленные
    PutOutMesToDelivered(OutMesID, OutMesState);

  elif( InList(InfoCode, 4, 6, 8, 9) )
    // Платеж - в закрытые
    var Order = null;
    if(DocKind == DLDOC_OUT_PMCLAIM)
      Order = RsbOutClaimOrder(PaymentID);
    elif(DocKind == DLDOC_OUT_PMCOL)
      Order = RsbOutCollectOrder(PaymentID);
    else
      RunError("Неверный вид документа");
    end;

    Order.PaymStatus = PM_FINISHED;
    Order.PropStatus = PM_PROP_CLOSED;
    if( Order.Update() != 0 )
      RunError("Ошибка при изменении статуса платежа");
    end;

    // Исходное сообщение - в доставленные
    PutOutMesToDelivered(OutMesID, OutMesState);

  end;

end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация ответа по ED274");

  ClearRecord(wlreq);
  
  var xml : object = ReadXMLField(wlmes);
  
  /* Заполнение учетных буферов */
  wlreq.Trn           = wlmes.Trn;
  wlreq.RelatedRef    = wlmes.RelatedRef;
  wlreq.Direct        = "X";  
  wlreq.Kind          = MESKIND_ANSWER;
  
  FillPartyByUIS_PTCK_UIS( ReadAttribute(xml,"EDAuthor"), @wlreq.OriginatorID,
                                                          @wlreq.OriginatorCodeKind,
                                                          @wlreq.OriginatorCode,
                                                          @wlreq.OriginatorName );
  
  FillPartyByUIS_PTCK_UIS( ReadAttribute(xml,"EDReceiver"), @wlreq.RecipientID,
                                                            @wlreq.RecipientCodeKind,
                                                            @wlreq.RecipientCode,
                                                            @wlreq.RecipientName );
  
  wlreq.InitDateMes   = ToDate(ReadAttribute(xml,"EDDate", "EDRefID"));
  wlreq.InitFormIDMes = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);

  wlreq.Queries       = "InfoCode:" + ReadAttribute(xml, "InfoCode") + "\n" +
                        "EDRefID:"  + GetRefByEDNoDateAuthor(xml, "EDRefID") + "\n" +
                        "SumPT:"    + ReadAttribute(xml, "SumPT", "", false) + "\n" +
                        "AcptSum:"  + ReadAttribute(xml, "AcptSum", "", false) + "\n" +
                        "AcptDate:" + ReadAttribute(xml, "AcptDate", "", false);
          
  var Description     = ReadNodeText(xml, "Annotation");

  var ReqID : integer = 0;
  if( ВставитьОтвет( wlreq, Description, "", "", @ReqID ) )
    wlreq.ReqID = ReqID;
  else
    std.msg("Ошибка при вводе ответа");
    return FALSE;
  end;

  // Выполнить обработку сформированного ответа
  ProcessAnswer(wlmes, xml, wlreq);

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;