/*
  $Name:         ufgd208.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация квитанции по сообщению УФЭБС ED208
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация квитанции по сообщению УФЭБС ED208                             */
/*                                                                          */
/* Имя файла: ufgd208.mac                                                   */
/* Создан:    14.06.11                                           Чукина Т.А.*/
/****************************************************************************/
import "ufgendoc.mac";

private macro НайтиКодРежима(answercode: string) :string

  var result = GetWlcodeDescription(48, answercode);

  if(not result)
    result = "Неизвестный код режима: " + answercode;
  end;
        
  return result;
end;

private macro GetSessMessages(SessionID : integer) : TArray
  var Messages : TArray = TArray();

  var rs : RsdRecordset = execSQLselect
    ( "select t_MesID from dwlmes_dbt where t_SessionID = :SessionID ",
      makeArray( SQLParam("SessionID", SessionID) )
    );

  while(rs and rs.moveNext())
    Messages[ Messages.size ] = rs.value(0);
  end;

  return Messages;
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Error;
  var MesIDs : TArray = TArray();
  var ResultCode, CtrlCode;
  var Code : integer = 0;
  var Descr : string = "";

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация квитанции по сообщению ED208" );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
    println( string( "Неверный формат сообщения по форме ED208" ) );
    return false;
  end;

  var OutMesID : integer = 0;
  
  if ( ReadOptinalAttribute(xml, "EDNo", "EDRefID") )
    OutMesID = GetOutMesIdInOperDprt(TRANSP_UFBS, wlmes.RelatedRef);

    if ( OutMesID )
      MesIDs[0] = OutMesID;
    end;
  end;

  if(MesIDs.size == 0)
    MesIDs[0] = 0;
  end;

  CtrlCode = ReadOptinalAttribute(xml, "CtrlCode");
  ResultCode = ReadAttribute(xml, "ResultCode");
  if (CtrlCode != "")
    Code = int(CtrlCode)
  else
    Code = int(ResultCode);
  end;

  Descr = ReadNodeText(xml, "Annotation", false);
  if (Descr == "")
    if (CtrlCode != "")
      GetElementAndNoteLLVALUES(OBJTYPE_WLRESCODE_UFBS, CtrlCode, NULL, Descr);    
    else
      Descr = НайтиКодРежима(ResultCode);
    end;
  end;

  if (ResultCode == "3")
    for(var DefectMesID : integer, MesIDs)
      if ( not ОшибкаЛогическогоКонтроляСообщения(DefectMesID, Code, Descr) )
        println( string( "Не удалось поместить в отбракованные сообщение ID = ", DefectMesID ) );
        return false;
      end;
    end;
  else
    var NoChangeState : bool = (ResultCode == "1");

    for(var DeliveredMesID : integer, MesIDs)
      if ( not ВставитьПодтверждениеОДоставке(DeliveredMesID, NoChangeState, NULL, Descr, true, Code) )
        println( string("Не удалось поместить в доставленные сообщение ID = ", DeliveredMesID) ); 
        return false;
      end;
    end;
  end;
        
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;
                       
