/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по сообщениям SWIFT MTn91                             */
/*                                                                          */
/*  Имя файла: swgdn91.mac                                                  */
/*  Создан:    22.09.00                                       Бабин А.П.    */
/****************************************************************************/

import "swgendoc.mac", "pmlib.mac";

/* Запрос */
class StatementMessage
 var
  TRF              :string,      /* Ссылочный номер операции */
  RelatedRef       :string,      /* Ссылка */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  OrdBank          :Bank,        /* Банк Приказодателя */
  AccBank          :Bank,        /* Банк-держатель счета */
  Sender           :Bank,        /* Отправитель */
  Charges          :string,      /* Детали расходов */
  RcvInfo          :TRcvInfo100; /* Информация Получателю поле 72 */

  TRF              = "";
  RelatedRef       = "";
  Currency         = "";
  Sum              = $0;
  Charges          = "";

end; /* class StatementMessage */

var MTn91 : StatementMessage;

macro Fill20( TRF )
  MTn91.TRF = TRF;
  return TRUE;
end;

macro Fill21( Refer )
  MTn91.RelatedRef = Refer;
  return TRUE;
end;

macro Fill32B( Cur, Sum )
  MTn91.Currency  = Cur;
  MTn91.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MTn91.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  MTn91.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57A( BIC, Account, System, Code )
  MTn91.AccBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill57B( Branch, Account, System, Code )
  MTn91.AccBank = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill57D( Address, Account, System, Code )
  MTn91.AccBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill71B( Charges )
  MTn91.Charges = Charges;
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MTn91.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MTn91*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld32B = ТПолеФормы( ValueDateCurrencyCodeAmountField_B, FIELD_MANDATORY, "Read32B", "Fill32B", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD_",   "Fill52D", "" ),        
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill57A", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadB",   "Fill57B", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadD_",   "Fill57D", "" ),
    Fld71B = ТПолеФормы( DetailsOfChargesField_B,            FIELD_MANDATORY, "Read71B", "Fill71B", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMTn91( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MTn91.Sender   = Bank( BankCode );       

  return TRUE;
end; /* ConstructMTn90 */

macro GenDoc( addrMes )
  var error, CorBankID;
  MTn91 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТn91");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);
  ClearRecord(wlpmdemand);

  if( not ConstructMTn91( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.Amount      = MTn91.Sum;
  wlpmpaym.PayAmount   = MTn91.Sum;
  wlpmpaym.BaseAmount  = MTn91.Sum;
  wlpmpaym.OrderAmount = MTn91.Sum;

  wlpmpaym.ValueDate = {curdate};
  if( not ПолучитьФинИнПоISO( MTn91.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MTn91.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  if ( MTn91.OrdBank.IsSet() )
     wlpmpaym.PayerBankID = MTn91.OrdBank.PartyID;
  else
     wlpmpaym.PayerBankID = {OurBank};                                 ///???
  end;
  if ( MTn91.AccBank.IsSet() )
    wlpmpaym.ReceiverBankID = MTn91.AccBank.PartyID;  
  else
    wlpmpaym.ReceiverBankID = wlmes.OutsideAbonentID;                ////????
  end;
  /* Счета плательщика/получателя */
  wlpmpaym.PayerAccount = MTn91.OrdBank.Account;

   /* Кредитовые свойства платежа */
  wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;
  
  wlpmpropcred.CodeKind = MTn91.AccBank.CodeKind;
  wlpmpropcred.BankCode = MTn91.AccBank.CodeBank;

  wlpmpropcred.CorrCodeKind = MTn91.Sender.CodeKind;
  wlpmpropcred.CorrCode     = MTn91.Sender.CodeBank;
  wlpmpropcred.CorrAcc      = MTn91.Sender.Account;
  wlpmpropcred.CorrID       = MTn91.Sender.PartyID;

  /* Реквизиты платежа, характерные для R-макета */
  if(MTn91.TRF!=СсылкаОтсутствует)
    wlpmrmprop.Number              = MTn91.TRF;
  end;

  wlpmrmprop.PayDate               = wlpmpaym.ValueDate;
  wlpmrmprop.Date                  = wlpmpaym.ValueDate;
  wlpmrmprop.ClientDate            = wlpmpaym.ValueDate;

  if ( MTn91.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName      = MTn91.OrdBank.Name;
  else
     wlpmrmprop.PayerBankName      = {Name_Bank};
  end;  
  wlpmrmprop.PayerCorrAccNostro    = {CORAC_Bank};
  wlpmrmprop.PayerCorrBankName     = MTn91.Sender.Name;     

  if ( MTn91.AccBank.IsSet() )
     wlpmrmprop.ReceiverBankName   = MTn91.AccBank.Name;
  else
     wlpmrmprop.ReceiverBankName   = MTn91.Sender.Name;
  end;
  wlpmrmprop.ReceiverCorrAccNostro = MTn91.Sender.Account;

  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MTn91.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "05";

  wlpmrmprop.ComissCharges         = PM_CHRG_OUR;
  wlpmrmprop.AdditionalInfo        = MTn91.RcvInfo.Str;
  wlpmrmprop.Ground                = MTn91.Charges;
  
  wlpmdemand.AcceptTerm = PM_DEMAND_TERM_ACCEPT;
  wlpmdemand.Accept     = PM_DEMAND_ACCEPT_WAIT;
  wlpmpropcred.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
  if(wlpmpropcred.Corschem != -1)
    wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  if( MTn91.OrdBank.IsSet() )
    /* Дебетовые свойства платежа */
    wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
    wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
    wlpmpropdeb.CodeKind = MTn91.OrdBank.CodeKind;
    wlpmpropdeb.BankCode = MTn91.OrdBank.CodeBank;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
    
    if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop, PRT_Debet, wlpmdemand ) )   ///!!! pmdemand
      std.msg("Ошибка при сохранении платежа");
     return FALSE;
  end;
  else
    
    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, 0, wlpmrmprop );
  
    if( not ВставитьПлатеж( wlpmpaym, 0, wlpmpropcred, wlpmrmprop, PRT_Debet, wlpmdemand ) )  ///!!! pmdemand
      std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;  
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Банка Бенефициара - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MTn91.AccBank.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MTn91.Sender.GetRoute(), true );
  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MTn91.OrdBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
