/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям МЦИ макета R (документы)                */
/*                                                                          */
/* Имя файла: wlmcgdr.mac                                                   */
/* Создан:    15.08.00                                             AVM      */
/****************************************************************************/
import "wlmcgd.mac", FIInter, "wltreas.mac";

const E0001 = 1,
      E0002 = 2;

macro GenDoc( addrMes, group )
  var Corschem = 0, Сумма, Day, Mon, Year, Error,
      ВИД_ПЛАТЕЖА, Currency, PayerName, ReceiverName, accstr, Credit_Flag;

  if ( valtype(group)==V_UNDEF )
     group = E0001;
  end;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация платежа по R-макету (старый формат до 02.06.03)" );

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmprop);

  if( not СчитатьПоле( FormFldDoc, Строка ) )
    return FALSE;
  end;

  if( group == E0001 )
    Credit_Flag = 1;
  else
    Credit_Flag = 0;
  end; 

  if(Credit_Flag)
    wlpmprop.DebetCredit = 0; /* BANK_DEBET from BankInter */
  else
    wlpmprop.DebetCredit = 1; /* BANK_CREDIT from BankInter */
  end;

  /* Заполнение учетных буферов */
  Сумма = substr( Строка, 100, 18 );
  wlpmpaym.Amount            = moneyL( Сумма )/100;
  wlpmpaym.PayAmount         = moneyL( Сумма )/100;
  wlpmpaym.BaseAmount        = wlpmpaym.PayAmount;

  Day = int( substr( Строка, 18, 2 ) );
  Mon = int( substr( Строка, 20, 2 ) );
  Year= int( substr( Строка, 22, 4 ) );
  wlpmpaym.ValueDate         = date( Day, Mon, Year );

  /* В R-макете ходят только рубли */
  wlpmpaym.FIID              = 0;
  wlpmpaym.PayFIID           = wlpmpaym.FIID;
  wlpmpaym.BaseFIID          = wlpmpaym.FIID;

  /* Пока в R-макете ходят только поручения */
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmprop.TransferDate = {curdate};
  else
     wlpmprop.TransferDate = wlpmpaym.ValueDate;
  end;

  wlpmprop.PayFIID   = wlpmpaym.PayFIID;
  wlpmprop.CodeKind  = PTCK_BIC;
  if(Credit_Flag)
    wlpmprop.BankCode  = substr( Строка, 51, 9 );
  else
    wlpmprop.BankCode  = substr( Строка, 118, 9 );
  end;

  wlpmpaym.PayerAccount      = Trim( substr( Строка, 80, 20 ) );
  wlpmpaym.ReceiverAccount   = Trim( substr( Строка, 147, 20 ) );  

  accstr = substr( Строка, 60, 20 );
  if( accstr != "00000000000000000000" )
    accstr = Trim( accstr );
  else 
    accstr = "";
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = substr( Строка, 2, 5 );

  Day = int( substr( Строка,  8, 2 ) );
  Mon = int( substr( Строка, 10, 2 ) );
  Year= int( substr( Строка, 12, 4 ) );
  wlpmrmprop.PayDate               = date( Day, Mon, Year );

  Day = int( substr( Строка, 34, 2 ) );
  Mon = int( substr( Строка, 36, 2 ) );
  Year= int( substr( Строка, 38, 4 ) );
  wlpmrmprop.Date                  = date( Day, Mon, Year );

  Day = int(substr( Строка, 42, 2 ));
  Mon = int(substr( Строка, 44, 2 ));
  Year= int(substr( Строка, 46, 4 ));
  wlpmrmprop.ClientDate            = date( Day, Mon, Year );

  ВИД_ПЛАТЕЖА = substr( Строка, 28, 1 );

  if( ВИД_ПЛАТЕЖА == "1" )
    PayerName                        = TrimZero( substr( Строка, 423, 160 ) );
    ReceiverName                     = TrimZero( substr( Строка, 583, 160 ) );

    wlpmrmprop.PayerName             = RemoveKPPFromName( PayerName );
    wlpmrmprop.ReceiverName          = RemoveKPPFromName( ReceiverName );
    wlpmrmprop.Ground                = TrimZero( substr( Строка, 213, 210 ) );

    wlpmrmprop.PayerINN              = ConstructINN( TrimZero( substr( Строка, 167, 12 ) ), ElicitKPPFromName(PayerName) );
    wlpmrmprop.ReceiverINN           = ConstructINN( TrimZero( substr( Строка, 179, 12 ) ), ElicitKPPFromName(ReceiverName) );
  else
    wlpmrmprop.IsShortFormat = "X";
    wlpmrmprop.PayerINN              = TrimZero( substr( Строка, 167, 12 ) );
    wlpmrmprop.ReceiverINN           = TrimZero( substr( Строка, 179, 12 ) );
  end;  

  wlpmrmprop.Priority              = int( substr( Строка, 16, 2 ) );

  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = substr( Строка, 26, 2 );

  if(Credit_Flag)
    wlpmpaym.PayerBankID = ПолучитьКодСубъекта( wlpmprop.BankCode, wlpmprop.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк плательщика: " + wlpmprop.BankCode );
      return FALSE;
    end;

    wlpmpaym.ReceiverBankID = {OurBank};

    if( ПолучитьСубъекта( wlpmpaym.PayerBankID, wlparty ) != 0 )
      std.msg( "Не найден банк плательщика" );
      return FALSE;
    end;
    wlpmrmprop.PayerBankName         = wlparty.Name;
    wlpmrmprop.PayerCorrAccNostro    = accstr;

    wlpmrmprop.ReceiverBankName      = {Name_Bank};
    wlpmrmprop.ReceiverCorrAccNostro = "";
    wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropdeb.Corschem != -1)
      wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmprop, 0, wlpmrmprop );
    
    if( not ВставитьПлатеж( wlpmpaym, wlpmprop, 0, wlpmrmprop ) )
      std.msg( "Ошибка при вставке платежа" );
      return FALSE;
    end;

  else

    wlpmpaym.ReceiverBankID = ПолучитьКодСубъекта( wlpmprop.BankCode, wlpmprop.CodeKind, Error );
    if( Error )
      std.msg( "Не найдена ссылка на банк получателя: " + wlpmprop.BankCode );
      return FALSE;
    end;

    wlpmpaym.PayerBankID = {OurBank};

    if( ПолучитьСубъекта( wlpmpaym.ReceiverBankID, wlparty ) != 0 )
      std.msg( "Не найден банк получателя" );
      return FALSE;
    end;
    wlpmrmprop.ReceiverBankName      = wlparty.Name;
    wlpmrmprop.ReceiverCorrAccNostro = accstr;

    wlpmrmprop.PayerBankName         = {Name_Bank};
    wlpmrmprop.PayerCorrAccNostro    = "";
    wlpmpropcred.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
    if(wlpmpropcred.Corschem != -1)
      wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
    end;

    ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, 0, wlpmprop, wlpmrmprop );
    
    if( not ВставитьПлатеж( wlpmpaym, 0, wlpmprop, wlpmrmprop, PRT_Debet ) )
      std.msg( "Ошибка при вставке платежа" );
      return FALSE;
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;

macro GenDocE0001( addrMes )
    return GenDoc( addrMes, E0001 );
end;

macro GenDocE0002( addrMes )
    return GenDoc( addrMes, E0002 );
end;



