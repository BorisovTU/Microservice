/*
  $Name:         wlreqout.mac
  $Module:       МБР
  $Description:  Макрос скроллинга исходящих запросов
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Макрофункции для работы с запросами (исходящие)                         */
/*                                                                          */
/*  Имя файла: wlreqout.mac                                                 */
/*  Создан:  11.02.2004                                       Панов А.Б.    */
/****************************************************************************/

import BankInter, Календарь, globals, WldInter, PaymInter, MesInter;
import "wldoc.mac";

record wl_cshreq( wlcshreq );
record wl_old_cshreq( wlcshreq );

const FIELD_NUMBER = 1,
      FIELD_REQUEST_DATE = 2,
      FIELD_OPERATION_TYPE = 3,
      FIELD_OPERATION_TYPE_NAME = 4,
      FIELD_OPERATION_DATE = 5,
      FIELD_AMOUNT = 6,
      FIELD_SYMBOLS_BUTTON = 7,
      FIELD_SYMBOL = 8,
      FIELD_CUSTOMER = 9,
      FIELD_FORMERS_BUTTON = 10,
      FIELD_FORMER = 11;

/* Обращаемся к полям структур, используемых в макросах выполнения шагов
   операций, для ускорения работы макросов */

var field_finit;

field_finit = route.ObjID;
field_finit = wlfininstr.FIID;
field_finit = wlpm.PaymentID;
field_finit = wlpmpaym.PaymentID;
field_finit = wlpmpropdeb.PaymentID;
field_finit = wlpmpropcred.PaymentID;
field_finit = wlpmrmprop.PaymentID;
field_finit = wlhead.HeadID;
field_finit = wlconf.ConfID;
field_finit = wlreq.ReqID;
field_finit = wlmes.MesID;
field_finit = wlinfo.InfoID;
field_finit = wldlmm.DlMMID;
field_finit = wldeal.DealID;
field_finit = wlsess.SessionID;

field_finit = f_wltransp.TpID;  
field_finit = f_wlfininstr.FIID;
field_finit = f_wlmes.MesID;

MACRO IsDigitNumber( Number )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Number)) )
        ch = SubStr( Number, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1;
        end;
        i = i + 1;
      end;

  return stat;

END;

PRIVATE MACRO GetDepartmentCalendar(Department:integer):integer
  var rset:object;
  var select:string;
  var params:TArray = TArray();

  select = " SELECT T_Calendar FROM DDP_DEP_DBT WHERE T_CODE = :Department ";

  params[params.size] = SQLParam( "Department", Department );
  rset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;

  return 0;
END;

macro Функция_Пользователя()
  return 0;
end;

macro Проверить_документ( Режим, Тип_документа )
  var stat = 0;


  if( Режим == 2  or (Режим == 3) )
    if(wl_cshreq.Number == "")
      MsgBox("Не задан номер заявки");
      return FIELD_NUMBER;
    end;

    if(wl_cshreq.Date == Date(0,0,0))
      MsgBox("Не задана дата заявки");
      return FIELD_REQUEST_DATE;
    end;

    if(wl_cshreq.OperType == "")
      MsgBox("Не задан тип операции");
      return FIELD_OPERATION_TYPE;
    end;

    if(wl_cshreq.OperDate == Date(0,0,0))
      MsgBox("Не задана дата операции");
      return FIELD_OPERATION_DATE;
    end;

    var request = RsbWlRequest(wl_cshreq.ReqID);
    
    if(request.Sum <= $0)
      MsgBox("Не задана сумма заявки");
      return FIELD_AMOUNT;
    end;
    
    if(request.Symbols().Size() == 0)
      MsgBox("Не заданы кассовые символы");
      return FIELD_SYMBOL;
    end;

    if(wl_cshreq.Customer == "")
      MsgBox("Не задан представитель");
      return FIELD_CUSTOMER;
    end;

    if(request.Formers().Size() == 0)
      MsgBox("Не заданы составители");
      return FIELD_FORMER;
    end;

    if( (IsDigitNumber( wl_cshreq.Number )) or (int(wl_cshreq.Number) == 0))
      MsgBox("Некорректный номер заявки");
      return FIELD_NUMBER;
    end;

    if( (GetDateAfterWorkDays( wl_cshreq.Date, 1, GetDepartmentCalendar(request.OriginatorID) ) < wl_cshreq.OperDate )
      or (GetDateAfterWorkDays( wl_cshreq.Date, -1, GetDepartmentCalendar(request.OriginatorID) ) > wl_cshreq.OperDate))
      if( ConfWin( makeArray( "Рекомендуется отправка заявки в электронном виде только накануне или в день выполнения кассовой операции. Изменить дату операции? " ), makeArray( "Да", "Нет")) == 0)
        return FIELD_OPERATION_DATE;
      end;
    end;

    var values = request.Values();

    if( values.Size > 0 )      
      var value : TRecHandler = TRecHandler( "wlcrval.dbt", "bank.def");       

      var valAmount = $0;

      if(not values.First(value))
        valAmount = valAmount + value.rec.Amount;
      end;

      while(not values.Next(value))
        valAmount = valAmount + value.rec.Amount;
      end;

      if(valAmount != request.Sum )
        MsgBox("Сумма по номиналам " + IfThenElse(valAmount < request.Sum, "меньше", "больше") + " суммы по документу, разница " + IfThenElse(valAmount < request.Sum, request.Sum - valAmount, valAmount - request.Sum) );
        return FIELD_AMOUNT;
      end;      
    end;

    var symbol : TRecHandler = TRecHandler( "symbcash.dbt", "bank.def");    
    var symbols = request.Symbols();    

    var amount = $0;

    if(not symbols.First(symbol))
      amount = amount + symbol.rec.Sum;
    end;

    while(not symbols.Next(symbol))
      amount = amount + symbol.rec.Sum;
    end;

    if(amount != request.Sum )
      MsgBox("Сумма по символам " + IfThenElse(amount < request.Sum, "меньше", "больше") + " суммы по документу, разница " + IfThenElse(amount < request.Sum, request.Sum - amount, amount - request.Sum) );
      return FIELD_AMOUNT;
    end;      

  end;  

  return stat;
end;