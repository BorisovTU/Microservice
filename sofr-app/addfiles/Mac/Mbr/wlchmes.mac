/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*      Константы и вспомогательные функции для контроля сообщений          */
/*                                                                          */
/*  Имя файла: wlchmes.mac                                                  */
/*  Создан:  24.01.00                                        Алешин А.В.    */
/****************************************************************************/

import "wldoc.mac", rsd, oralib, likepy;

macro FillBuffPayment( wlmes )
  var rs:object;
  var select:string;
  var params:TArray;

  /* Только для вида сообщения - платеж */
  if( wlmes.Kind != MESKIND_PAYMENT )
    return false;
  end;

  select = "select wlpm.t_PaymentID, wlpm.t_WlPmID from dwlmeslnk_dbt wlmeslnk, dwlpm_dbt wlpm"+
                           " where wlmeslnk.t_MesID = :MesID"+
                           " AND wlmeslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                           " AND wlmeslnk.t_ObjID = wlpm.t_WlPmID";
  params = makeArray( SQLParam("MesID",wlmes.MesID ),
                      SQLParam("OBJTYPE_PAYMENT",OBJTYPE_PAYMENT ));
  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
    wlpm.PaymentID = rs.value(0);
    wlpm.WlPmID    = rs.value(1);
  else
    return false;
  end;

  /* Ищем платеж, сейчас реально нужен только pmpaym для проверки суммы и счетов */
  if( FindPayment( wlpm.PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
    return false;
  end;

  return true;
end;

/* Проверяет, чтобы у платежа не было двух одинаковых сообщений (28217) */
macro CheckMessagesPayment( wlpm )
  var rs:object;
  var select:string;
  var params:TArray;

  /* Проверяем, привязано ли к платежу более одного сообщения с абсолютно одинаковыми */
  /* wlmes.OutsideAbonentID, wlmes.InsideAbonentID, wlmes.AgentID */
  select = "select count(*) from dwlmeslnk_dbt wlmeslnk, dwlmes_dbt wlmes"+
                            " where wlmeslnk.t_ObjID = :WlPmID"+
                            " AND wlmeslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                            " AND wlmeslnk.t_Direct  = chr(0)"+
                            " AND wlmeslnk.t_MesID   = wlmes.t_MesID"+
                            " group by wlmes.t_OutsideAbonentID, wlmes.t_InsideAbonentID, wlmes.t_AgentID"+
                            " having count(*) > 1";
  params = makeArray( SQLParam("WlPmID",wlpm.WlPmID),
                      SQLParam("OBJTYPE_PAYMENT",OBJTYPE_PAYMENT ));
  rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    return false;     /* Ошибка, у платежа есть несколько (более одного) одинаковых сообщений */
  else
    return true;
  end;
end;
