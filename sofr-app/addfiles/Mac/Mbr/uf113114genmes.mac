/*
  $Name:         uf113114genmes.mac
  $Module:       Межбанковские расчеты
  $Description:  Функции для генерации сообщений ED113, ED114
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для генерации сообщений ED113, ED114
//
// Программист  : Чукина Т.А.
//
// Создан       : 16.09.2014
//
//-----------------------------------------------------------------------------

import MesInter, "wltools.mac", "ufgenmes.mac", "bnk_common.mac", "pmlib.mac",
       pm_const;

macro CheckPaym_113_114( addr, err_msg : @string, type : string) : integer
  var DKFlag, err : integer = 0;

  SetBuff( wlpmpaym,  addr );
  var RsPaym : RsbPayment = RsbPayment( wlpmpaym.PaymentID );

  if ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  elif ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  else
     DKFlag = -1;
  end;

  if( not (( RsPaym.PayerGroup    == PAYMENTS_GROUP_EXTERNAL ) or
           ( RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL )) or (DKFlag == -1))
    err_msg = string("Платеж с ID ",RsPaym.PaymentID," не внешний");
    return 1;                  
  end;

  // обязательные поля
  var EmptyAttrs : string = "";
  if(RsPaym.BaseAmount == $0)
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "сумма";
  end;
  if(not RsPaym.Number)
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "номер документа";
  end;
  if(RsPaym.Date == date(0, 0, 0))
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "дата";
  end;
  if( trim(RsPaym.PayerName) == "" )
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "наименование плательщика";
  end;
  if( trim(RsPaym.ReceiverName) == "" )
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "наименование получателя";
  end;
  if( trim(RsPaym.Ground) == "" )
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "назначение платежа";
  end;
  var BIC : string = ПолучитьКодСубъекта(RsPaym.PayerBankID, PTCK_BIC, err, 1);
  if(err or not BIC)
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "БИК банка плательщика";
  end;
  BIC = ПолучитьКодСубъекта(RsPaym.ReceiverBankID, PTCK_BIC, err, 1);
  if(err or not BIC)
    EmptyAttrs = EmptyAttrs + IfThenElse(EmptyAttrs, ", ", "") + "БИК банка получателя";
  end;
  if(EmptyAttrs != "")
    err_msg = err_msg + "Не заполнены реквизиты: " + EmptyAttrs + ". " +
                        "Наличие этих реквизитов необходимо для корректного формирования сообщения " + type + ". ";
  end;

  // проверка форматов полей
  if(RsPaym.Number)
    var НомерДокумента : integer = int( GetLastSymbols(RsPaym.Number, PM_DOCNO_NONZERO_LEN) );
    if( НомерДокумента == 0 )
      err_msg = err_msg + "Номер документа нечисловой. ";
    end;
  end;

  if(strlen(RsPaym.PayerName) > 160)
    err_msg = err_msg + "Количество символов в реквизите 'Наименование плательщика' больше 160. "; 
  end;
  if(strlen(RsPaym.ReceiverName) > 160)
    err_msg = err_msg + "Количество символов в реквизите 'Наименование получателя' больше 160. "; 
  end;

  if(strlen(RsPaym.Ground) > 210)
    err_msg = err_msg + "Количество символов в реквизите 'Назначение платежа' больше 210. "; 
  end;

  var MaxPriority = Bnk_GetRegistryValue( "CB\\PAYMENTS\\MAXPRIORITY", V_INTEGER, 0 );
  if( RsPaym.Priority > MaxPriority )
    err_msg = err_msg + "Недопустимое значение очередности платежа, допускаются значения 1-5 или 0"; 
  end;

  if( type == "ED114" )         
    if( БанкУБР( RsPaym.ReceiverBankID ) and (RsPaym.Date >= date(2, 10, 2017)) and ( CompareStrWithMasks( Bnk_GetRegistryValue( "COMMON\\CHECKTAXPAYMPARAMS\\УФЭБС_СЧЕТ ПОЛУЧАТЕЛЯ", V_STRING, ""), RsPaym.ReceiverAccount ) == 0 ) 
                                         and ( ( RsPaym.TaxAuthorState == "" ) or ( RsPaym.TaxAuthorState == null ) ) )
      err_msg = err_msg + "Для банка получателя, являющегося ПБР, и указанного в документе счета получателя статус составителя должен быть обязательно заполнен"; 
    end;
  end;

  if(err_msg)
    return 1;
  end;
  
  return 0;
end;

private macro GetTransKind(type : string) : string
  if(type == "ED113")
    return "02";
  elif(type == "ED114")
    return "06";
  end;

  RunError("Ошибка программирования: неверное значение параметра type");
end;

private macro GetAccDocNo(PaymNumber : string) : integer
  var НомерДокумента : integer = int( GetLastSymbols(PaymNumber, PM_DOCNO_NONZERO_LEN) );

  if( НомерДокумента == 0 )
    RunError("Шесть последних разрядов номера должны быть отличны от \"000000\"");
  end;

  return НомерДокумента;
end;

private macro GetBankBIC(BankID : integer, IsPayerBank : bool) : string
  var Error : integer = 0;

  var BIC : string = ПолучитьКодСубъекта( BankID, PTCK_BIC, Error, 1 );

  if(Error or not BIC)
    RunError( "|Не найден БИК банка " + 
              IfThenElse(IsPayerBank, "плательщика", "получателя") );
  end;

  return BIC;
end;

macro GenMes_113_114( addrMes, addrPm, type : string )
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  var RsPaym : RsbPayment = RsbPayment( wlpmpaym.PaymentID );

  var xml : object = ActiveX("Microsoft.XMLDOM");  
  var mes : object = xml.createElement(type);
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);

  var EDReceiver = GetEDReceiver(RsPaym.PayerBankID, RsPaym.PayerBankCodeKind, RsPaym.PayerBankCode, "", 1);
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  if( (ed_nda.EDDate >= date(2,7,2018)) and (EDReceiver == "") )
    RunError("Не удалось определить уникальный идентификатор получателя ЭC (EDReceiver)");
  end;
  
  if( EDReceiver != "" )
    mes.setAttribute("EDReceiver", EDReceiver );
  end;

  mes.setAttribute( "Sum", MoneyToStrUFBS(wlpmpaym.BaseAmount) );
  mes.setAttribute( "TransKind", GetTransKind(type) );
  mes.setAttribute( "Priority", RsPaym.Priority );

  if(type == "ED113")
    var pspaydem : TbFile = TbFile("pspaydem.dbt", "R", 0);
    pspaydem.rec.OrderID = wlpmpaym.PaymentID;
    if( not pspaydem.GetEQ() )
      RunError("Не найдена запись в таблице требований РКО");
    end;

    mes.setAttribute( "PaytCondition", GetPaytCondition(pspaydem.rec.AcceptTerm) );
    if(pspaydem.rec.AcceptTerm == 0 /* с акцептом */)
      mes.setAttribute( "AcptTerm", GetAcceptPeriod(pspaydem.rec) );
    end;

    if( RsPaym.DocDispatchDate != date(0, 0, 0) )
      mes.setAttribute( "DocDispatchDate", YYYY_MM_DD(RsPaym.DocDispatchDate) );
    end;

  end;

  if( RsPaym.ReceiverBankMarkDate != date(0, 0, 0) )
    mes.setAttribute( "ReceiptDateCollectBank", YYYY_MM_DD(RsPaym.ReceiverBankMarkDate) );
  end;

  if( RsPaym.I2PlaceDate != date(0, 0, 0) )
    mes.setAttribute( "FileDate", YYYY_MM_DD(RsPaym.I2PlaceDate) );
  end;

  if( RsPaym.UIN )
    CheckUIN(RsPaym.ReceiverAccount, RsPaym.UIN);
    mes.setAttribute( "PaymentID", RsPaym.UIN );
  end;

  var elem : object, subElem : object;

  // AccDoc
  elem = xml.createElement("AccDoc");
  elem.setAttribute( "AccDocNo", GetAccDocNo(RsPaym.Number) );
  elem.setAttribute( "AccDocDate", YYYY_MM_DD(RsPaym.Date) );
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  // Payer
  elem = xml.createElement("Payer");

  if( trim(RsPaym.PayerAccount) != "" )
    elem.setAttribute( "PersonalAcc", RsPaym.PayerAccount );
  end;

  var KPP = "";
  if( RsPaym.PayerINN )
    elem.setAttribute( "INN", RemoveKPP(RsPaym.PayerINN) );
    KPP = RemoveINN(RsPaym.PayerINN);
    if(KPP != "")
      elem.setAttribute( "KPP", KPP );
    end;
  end;

  subElem = xml.createElement("Name");
  if( trim(RsPaym.PayerName) == "" )
    RunError( "|Не заполнено наименование плательщика" );
  end;
  subElem.appendChild(xml.createTextNode( TransTextField(substr(RsPaym.PayerName,1,160)) ));
  elem.appendChild(subElem);

  subElem = xml.createElement("Bank");
  subElem.setAttribute( "BIC", GetBankBIC(RsPaym.PayerBankID, true) );
  if(RsPaym.PayerCorrAccNostro)
    subElem.setAttribute( "CorrespAcc", RsPaym.PayerCorrAccNostro );   
  end;
  subElem.appendChild(xml.createTextNode(""));
  elem.appendChild(subElem);

  mes.appendChild(elem);

  // Payee
  elem = xml.createElement("Payee");

  if( trim(RsPaym.ReceiverAccount) != "" )
    elem.setAttribute( "PersonalAcc", RsPaym.ReceiverAccount );
  end;

  if( RsPaym.ReceiverINN )
    elem.setAttribute( "INN", RemoveKPP(RsPaym.ReceiverINN) );
    KPP = RemoveINN(RsPaym.ReceiverINN);
    if(KPP != "")
      elem.setAttribute( "KPP", KPP );
    end;
  end;

  subElem = xml.createElement("Name");
  if( trim(RsPaym.ReceiverName) == "" )
    RunError( "|Не заполнено наименование получателя" );
  end;
  subElem.appendChild(xml.createTextNode( TransTextField(substr(RsPaym.ReceiverName,1,160)) ));
  elem.appendChild(subElem);

  subElem = xml.createElement("Bank");
  subElem.setAttribute( "BIC", GetBankBIC(RsPaym.ReceiverBankID, false) );
  if(RsPaym.ReceiverCorrAccNostro)
    subElem.setAttribute( "CorrespAcc", RsPaym.ReceiverCorrAccNostro );   
  end;
  subElem.appendChild(xml.createTextNode(""));
  elem.appendChild(subElem);

  mes.appendChild(elem);

  // Purpose
  elem = xml.createElement("Purpose");
  elem.appendChild( xml.createTextNode(substr(TransTextField(RsPaym.Ground),1,210)) );
  mes.appendChild(elem);

  if(type == "ED114")
    // DepartmentalInfo
    if( RsPaym.TaxAuthorState or RsPaym.BttTICode or RsPaym.OKATOCode or
        RsPaym.TaxPmGround or RsPaym.TaxPmPeriod or RsPaym.TaxPmNumber or 
        RsPaym.TaxPmDate or RsPaym.TaxPmType 
      )
      elem = xml.createElement("DepartmentalInfo");

      if(RsPaym.TaxAuthorState)
        elem.setAttribute( "DrawerStatus",   RsPaym.TaxAuthorState );
      end;
        
      if(RsPaym.BttTICode != "")
        elem.setAttribute( "CBC", RsPaym.BttTICode );
      end;
      if(RsPaym.OKATOCode != "")
        elem.setAttribute( "OKATO",          RsPaym.OKATOCode );
      end;
      if(RsPaym.TaxPmGround != "")
        elem.setAttribute( "PaytReason",     RsPaym.TaxPmGround );
      end;
      if(RsPaym.TaxPmPeriod != "")
        elem.setAttribute( "TaxPeriod",      RsPaym.TaxPmPeriod );
      end;
      if(RsPaym.TaxPmNumber != "")
        elem.setAttribute( "DocNo",          TransTextField(RsPaym.TaxPmNumber) );
      end;
      if(RsPaym.TaxPmDate != "")
        elem.setAttribute( "DocDate",        RsPaym.TaxPmDate );
      end;
      if(RsPaym.TaxPmType != "") 
        elem.setAttribute( "TaxPaytKind",    RsPaym.TaxPmType );
      end;
        
      elem.appendChild(xml.createTextNode(""));
      mes.appendChild(elem);
    end;
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
