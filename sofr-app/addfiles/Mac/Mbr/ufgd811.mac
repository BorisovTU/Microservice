/*
  $Name:         ufgd811.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО "Ответ" по сообщениям УФЭБС ED811
*/

import "ufgendoc.mac", PTInter, likepy, oralib, "pmlib.mac", wltools, bnk_dttmfrmt, bnk_dirfile, bnk_ptlib, wluftool;

private class TInitialED
  var EDNo:       integer,   /*Номер ЭС в течение опердня для исходного ЭСИС*/
      EDDate:     date,      /*Дата составления ЭС для исходного ЭСИС*/
      EDAuthor:   string;    /*Уникальный идентификатор составителя ЭС для исходного ЭСИС*/

  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
  end;

  Construct();
end;

private class TLimitInfo /*Значение и использование лимита*/
  var LimitType:       string, /*Тип лимита*/
      OldValue:        moneyL, /*Предыдущее значение лимита*/
      Value:           moneyL, /*Устанавливаемое значение лимита*/
      BIC:             string, /*БИК подчиненного участника пула ликвидности*/
      Account:         string, /*Счет подчиненного участника пула ликвидности*/
      UID:             string, /*УИС уполномоченного составителя требований*/
      ClearingCircuit: string; /*Клиринговая схема*/

  macro Construct()
    OldValue = Value = moneyL(0);
    LimitType = BIC = Account = UID = ClearingCircuit = "";
  end;

  Construct();
end;

class TED811MessageFields /*Поля сообщения ED811*/
  var EDNo:                integer,    /*Номер ЭПД в течение опердня*/
      EDDate:              date,       /*Дата составления ЭПД*/
      EDAuthor:            string,     /*Уникальный идентификатор составителя ЭПД*/
      EDReceiver:          string,     /*Уникальный идентификатор получателя ЭС*/
      BIC:                 string,     /*БИК участника*/
      Acc:                 string,     /*Счет участника*/
      LimitChangeType:     string,     /*Вид изменения лимита*/ 
      LimitActivationDate: date,       /*Дата вступления в действие лимита*/
      LimitInfo:           variant,    /*Значение и использование лимита*/
      InitialED:           variant;    /*Идентификаторы исходного ЭСИС*/

  macro Construct()
    EDNo = 0;
    EDAuthor = EDReceiver = BIC = Acc = LimitChangeType = "";
    LimitActivationDate = EDDate = date(0,0,0);
    LimitInfo = null;
    InitialED = null;
  end; 
  
  Construct();
end;


// Макрос чтения полей сообщения ED811
// Входные параметры: ED811MessageFields - структура полей сообщения ED811
macro ReadED811MessageFields( ED811MessageFields:@TED811MessageFields ) : bool
  var fieldname = "", fieldvalue = "", blockname = "";
  var stat : bool = false;

  /*Считываем только то, что хранится в полях релиза*/
  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED811MessageFields.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED811MessageFields.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED811MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED811MessageFields.EDReceiver = fieldvalue;
      elif(fieldname == "BIC")
        ED811MessageFields.BIC = fieldvalue;
      elif(fieldname == "Acc")
        ED811MessageFields.Acc = fieldvalue;
      elif(fieldname == "LimitChangeType")
        ED811MessageFields.LimitChangeType = fieldvalue;
      elif(fieldname == "LimitActivationDate")
        ED811MessageFields.LimitActivationDate = ToDate( fieldvalue );
      end;
    elif(blockname == "LimitInfo")
      if(fieldname == beginField)
        ED811MessageFields.LimitInfo = TLimitInfo();
      elif(fieldname == "LimitType")
        ED811MessageFields.LimitInfo.LimitType = fieldvalue;
      elif(fieldname == "OldValue")
        ED811MessageFields.LimitInfo.OldValue = moneyL( fieldvalue );
      elif(fieldname == "Value")
        ED811MessageFields.LimitInfo.Value = moneyL( fieldvalue );
      elif(fieldname == "BIC")
        ED811MessageFields.LimitInfo.BIC = fieldvalue;
      elif(fieldname == "Account")
        ED811MessageFields.LimitInfo.Account = fieldvalue;
      elif(fieldname == "UID")
        ED811MessageFields.LimitInfo.UID = fieldvalue;
      elif(fieldname == "ClearingCircuit")
        ED811MessageFields.LimitInfo.ClearingCircuit = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == beginField)
        ED811MessageFields.InitialED = TInitialED();    
      elif(fieldname == "EDNo")
        ED811MessageFields.InitialED.EDNo = int( fieldvalue );
      elif(fieldname == "EDDate")
        ED811MessageFields.InitialED.EDDate = ToDate( fieldvalue );
      elif(fieldname == "EDAuthor")
        ED811MessageFields.InitialED.EDAuthor = fieldvalue;
      end;
    end; 
  end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

  return TRUE;
end;

macro GenDoc( addrMes )
  var Narrative : string = "";
  SetBuff( wlmes, addrMes );
  PrintLog( 2, "Генерация информационного сообшения по ED811" );

  var tmpPartyID = 0;
  
  var ED811MessageFields = TED811MessageFields();
  ReadED811MessageFields( @ED811MessageFields );

  var RsbWldRequestObj : RsbWlRequest = RsbWlRequest(0); // Создаём новый Rsb-объект

  RsbWldRequestObj.Trn = wlmes.Trn;
  RsbWldRequestObj.RelatedRef = wlmes.RelatedRef;
  RsbWldRequestObj.Direct = "X"; /*WLD_MES_IN*/
  RsbWldRequestObj.Kind = MESKIND_ANSWER; /*Создаем ответ*/

  RsbWldRequestObj.OriginatorCode = ED811MessageFields.EDAuthor;
  RsbWldRequestObj.OriginatorCodeKind = PTCK_UIS;
  tmpPartyID = 0;
  tmpPartyID = GetCodeOwnerID( RsbWldRequestObj.OriginatorCodeKind, RsbWldRequestObj.OriginatorCode );
  if( tmpPartyID > 0 )
    RsbWldRequestObj.OriginatorID   = tmpPartyID;
    RsbWldRequestObj.OriginatorName = Bnk_GetPartyName( RsbWldRequestObj.OriginatorID );
  end;
 
  RsbWldRequestObj.RecipientCode      = wlmes.InsideAbonentCode;
  RsbWldRequestObj.RecipientCodeKind  = wlmes.InsideAbonentCodeKind;
  RsbWldRequestObj.RecipientID        = wlmes.InsideAbonentID;
  RsbWldRequestObj.RecipientName      = Bnk_GetPartyName( RsbWldRequestObj.RecipientID );

  FindCorschemIsUBR( ED811MessageFields.BIC, ED811MessageFields.Acc, @RsbWldRequestObj.Corschem, @RsbWldRequestObj.FIID );

  RsbWldRequestObj.ChangeType = ED811MessageFields.LimitChangeType;
  RsbWldRequestObj.StartDate = ED811MessageFields.LimitActivationDate;
  RsbWldRequestObj.Type = ED811MessageFields.LimitInfo.LimitType;
  RsbWldRequestObj.OldValue = ED811MessageFields.LimitInfo.OldValue / 100;
  RsbWldRequestObj.Value = ED811MessageFields.LimitInfo.Value / 100;
  if (ED811MessageFields.LimitInfo.UID != "")
  RsbWldRequestObj.ClaimOriginatorID = GetCodeOwnerID( PTCK_UIS, ED811MessageFields.LimitInfo.UID );
  end;
  RsbWldRequestObj.ClearSchem = ED811MessageFields.LimitInfo.ClearingCircuit;
  
  RsbWldRequestObj.ChangeState( WLD_STATUS_REQ_RECEIV );

  RsbWldRequestObj.SaveChanges();

  /*Создаем связь документа с входящим сообщением*/
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = RsbWldRequestObj.ReqID;
  meslnk.rec.ObjKind = OBJTYPE_REQ;
  var MesLnkObj = RsbWldRequestObj.InWlmesLnk();
  MesLnkObj.Insert( meslnk );
  MesLnkObj.Save();

  /*Вставка подтверждения о доставке*/
  if( ED811MessageFields.InitialED != null)
    var ReqMesID = FindReqOutMesID( RsbWldRequestObj.RelatedRef, wlmes.TpSchemID );
    if( ReqMesID > 0 )
      ВставитьПодтверждениеОДоставке( ReqMesID, null, null, null, false, null, WLD_STATUS_MES_CLOSED );
    end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
