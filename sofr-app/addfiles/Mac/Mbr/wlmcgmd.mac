/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения D-макета                                             */
/*                                                                          */
/*  Имя файла: wlmcgmd.mac                                                  */
/*  Создан:  14.08.00                                               AVM     */
/****************************************************************************/
import wlmcgm, wlfindpm;

macro GenMes( addrMes, addrReq )

  file FileWlPm( wlpm ) key 0;
  file cors(corschem) key 1;
  var НомерДокументаСтр, НомерДокумента,
      MFO_9, СсылкаНаБанк, Сумма, Error,
      День, Месяц, Год, Часов, Минут, Секунд, stat, ObjKind, ObjID;
  var RsPaym;
  SetBuff( wlmes, addrMes );
  SetBuff( wlreq, addrReq );

  PrintLog( 2, "Генерация сообщения D для МЦИ " );

  /* Формируем строку с записью о выгружаемом документе */
  СтрокаДокумента = "D";                         /* 1 */

  cors.Number = wlreq.Corschem;
  cors.FI_Kind = 1; /* вид фи - деньги */
  cors.FIID = wlreq.FIID;

  if ( not GetEQ(cors) )
     msgbox("не найдена схема расчетов " + string(wlreq.Corschem));
     return false;
  end;

  /* Ищем платеж, на который пришло подтверждение */
  if ( not СчитатьСвязьЗапросаСУчетнымОбъектом(wlreq.ReqID, ObjKind, ObjID) )
     msgbox("Запрос по форме D можно формировать только по платежу");
     return false;
  end;

  FileWlPm.wlpmID = ObjID;
  if ( not GetEQ(FileWlPm) )
     msgbox( "Не найден платеж " );
     return false;
  end;
  RsPaym = RsbPayment( wlpmpaym.PaymentID );
  if ( stat )
     msgbox( "Не найден платеж" );
     return false;
  end;

  /* Взять первые пять цифр из номера документа  */
  НомерДокументаСтр = substr( RsPaym.Number, 1, 5 ) ;

  НомерДокумента = int( НомерДокументаСтр ) ;
  if( НомерДокумента  ==  0 )
    /* Номер документа или уже нулевой или не числовой */

    /* Если необходимо, чтобы при нечисловом номере документа номер
    выгруженного становился нулевым автоматически -
    уберите следующие строки */

    if( GetTrue( True, "Номер документа " + RsPaym.Number + " не числовой. Выгрузить документ с N 0 ?" ) == False )
      msgbox( "Нечисловой номер документа" );
      return false;
    end;
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НомерДокумента, 5 ) ;   /* 2-6 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО, 9, Числовое_Поле );  /* 7-15 */

  /* Сумма документа */
  if( RsPaym.ReceiverAmount == $0 )
    msgbox( "Нулевая сумма документа" );
    return false;
  end;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, double( wlreq.Sum )*100, 18 );          /* 16-33 */

  /* Порядковый номер электронного документа */
  /* Используем только уже существующий для идентификации отзываемого документа */
  if( wlreq.RelatedRef == "" )
    msgbox( "Пустой электронный номер документа" );
    return false;
  end ;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, wlreq.RelatedRef, 6 ); /* 34-39 */

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_КорСчет, 20, Числовое_Поле );

  /* Дата составления ЭСИД */
  datesplit( {curdate}, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );

  /* Время составления ЭСИД */
  timesplit( time(), Часов, Минут, Секунд );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Часов,  2 );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Минут,  2 );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Секунд, 2 );

  /* Референс собственно отзыва */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, wlmes.TRN, 6 );

  /* Уникальный номер составителя */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НашКодСоставителя , 10 );

  ЗаписатьПолеЛог( FormFldDel, СтрокаДокумента, TRUE );

  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;




