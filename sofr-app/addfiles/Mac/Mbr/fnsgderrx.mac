/*
  $Name:        fnsgderrx.mac
  $Module:      MBR
  $Description: Генерация УО по сообщению ERROR XML
*/

import WldInter, wlglobal, WLTOOLS, PSInter, bnk_common;

macro GenDoc( addrMes )
 

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация учетного объекта по ERROR XML");

  var ErrList = RsbWlError(0);

  var field_name, field_value, error = 0;
  var wlerr_buf = TRecHandler("wlerror.dbt");

  while( СчитатьПоле( field_name, field_value ) )
    if( field_name == "_Ошибка" )
      wlerr_buf.rec.Code         = SubStr( field_value, 1, Index( field_value, ";" ) - 1 );
      wlerr_buf.rec.Description  = SubStr( field_value, Index( field_value, ";" ) + 1 );
      ErrList.Insert( wlerr_buf );
    end;
  end;
  /* Параметры создаваемого УО */
  var ResultID = 0;
  var ErrCode  = FNS_RP_MES_NOT_APPROVED_IN_BANK;
  var ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode); 

  if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList, "", -1, 0, "", "", -1, 0, "", "", NULL, NULL, ResultID ) )
    std.msg( "Ошибка при вставке подтверждения банка" );
    return FALSE;
  end;
  
  /*Связь подтверждения с входящим сообщением */
  var meslnk = TRecHandler("wlmeslnk.dbt");
  meslnk.rec.MesID   = wlmes.MesID;
  meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
  meslnk.rec.ObjID   = ResultID;
  meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
  var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
  MesLnkObj.Insert( meslnk );
  MesLnkObj.Save();
  return TRUE;
  
  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;