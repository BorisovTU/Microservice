/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED381                                     */
/*                                                                          */
/*  Имя файла: ufgd381.mac                                                  */
/*  Создан:    06.11.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdreqliq.mac";

class (TGenDocLiqReq) TGenDocED381()

  InitTGenDocLiqReq(MESKIND_ANSWER, "ED381");

  class TLimitInfoExt
    var PURBIC = "",
        LimitSum = "",
        LimitTransKind = "",
        BalancePayts = "",
        LimitLiquidity = "";
  end;

  var LimitInfoArray = TArray();

  macro ReadFields() : bool
    var fieldname = "", fieldvalue = "", blockname = "";
    while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if  (blockname == "")
        if  (fieldname == "EDAuthor")
          OriginatorUIS = fieldvalue;
        elif(fieldname == "EDReceiver")
          RecipientUIS = fieldvalue;
        end;
      elif(blockname == "PartInfo")
        if  (fieldname == "PartNo")
          PartNo = fieldvalue;
        elif(fieldname == "PartAggregateID")
          PartAggregateID = fieldvalue;
        end;
      elif(blockname == "LimitInfoExt")
        if  (fieldname == beginField)
          LimitInfoArray[ LimitInfoArray.size ] = TLimitInfoExt();
        elif(fieldname == "PURBIC")
          LimitInfoArray[ LimitInfoArray.size-1 ].PURBIC = fieldvalue;
        elif(fieldname == "LimitSum")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitSum = fieldvalue;
        elif(fieldname == "LimitTransKind")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitTransKind = fieldvalue;
        elif(fieldname == "BalancePayts")
          LimitInfoArray[ LimitInfoArray.size-1 ].BalancePayts = fieldvalue;
        elif(fieldname == "LimitLiquidity")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitLiquidity = fieldvalue;
        end;
      elif(blockname == "EDRefID")
        if  (fieldname == "EDDate")
          EDDate_InitMes = fieldvalue;
        end;
      end; 
    end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

    return TRUE;
  end;

  private macro AddQueries(p_wlreq)
    for(var item, LimitInfoArray)
      if(p_wlreq.Queries)
        p_wlreq.Queries = p_wlreq.Queries + "\n";
      end;
      p_wlreq.Queries = p_wlreq.Queries + "/" + item.LimitTransKind + 
                                          "/" + item.PURBIC +
                                          "/" + item.LimitSum +
                                          "/" + item.BalancePayts +
                                          "/" + item.LimitLiquidity;
    end;
  end;

  macro FillQueries() : bool // при вставке ответа
  
    // "/LimitTransKind/PURBIC/LimitSum/BalancePayts/LimitLiquidity<CRLF>"
    AddQueries(wlreq);
    return TRUE;
  end;

  macro ReFillQueries(p_wlreq) : bool // при обновлении головной части ответа
    AddQueries(p_wlreq);
    return TRUE;
  end;

  macro OnAfterGenWlreq() : bool    

    var ReceiverBIC = wlreq.RecipientCode; // получен из атрибута EDReceiver

    var Department : integer = GetDprtByBIC( ReceiverBIC );

    if(Department <= 0)
      std.msg("Не найден филиал " + ReceiverBIC);
      return FALSE;
    end;

    // обновляются настройки ликвидности для филиала
    
    var DpLiqSet : RsbDpLiqSet = RsbDpLiqSet(Department);

    var Sum : moneyL = $0;
    var PartyID   : integer = -1,
        DpBilLim  : RsbDpBilLim = DpLiqSet.DpBilLim,
        billimRec : TRecHandler = TRecHandler("dpbillim.dbt");

    for(var item, LimitInfoArray)
    
      Sum = StrToMoneyUFBS( item.LimitSum );

      if( ( item.BalancePayts == "" ) OR ( item.LimitLiquidity == "" ) )
        std.msg("Не заполнен обязательный блок LimitInfoExt в сообщении по форме " + FormName);
        return FALSE;
      end;

      if  (item.LimitTransKind == "1")
        DpLiqSet.CurTotalLimit = Sum;

      elif(item.LimitTransKind == "2")
      
        PartyID = ПолучитьКодСубъекта(item.PURBIC, PTCK_BIC);        

        if(PartyID > 0)
          billimRec.Clear();

          if( DpBilLim.FindByParty(PartyID, billimRec) == 0 )
            billimRec.rec.CurLimit = sum;

            if(DpBilLim.Update(billimRec) != 0)
              std.msg("Ошибка при обновлении настроек двустороннего лимита " + 
                      "для филиала " + ReceiverBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          else
            billimRec.rec.PartyID = PartyID;
            billimRec.rec.CurLimit = sum;

            if(DpBilLim.Insert(billimRec) != 0)
              std.msg("Ошибка при вводе настроек двустороннего лимита " + 
                      "для филиала " + ReceiverBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          end;
        end; // Если субъект найден

      elif(item.LimitTransKind == "3")
        DpLiqSet.CurMultilateralLimit = Sum;
      else
        std.msg("Неверное значение LimitTransKind");
        return FALSE;
      end;
    end; // for каждый блок "LimitInfo" 
    
    if(DpLiqSet.Update() != 0)
      std.msg("Ошибка при сохранении настроек ликвидности");
      return FALSE;
    end;

    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       

  var gdObj : TGenDocED381 = TGenDocED381();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
