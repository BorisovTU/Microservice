/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Печать сообщений СМБР в формате СБРФ3                                   */
/*                                                                          */
/*  Имя файла: wlsbrfpr.mac                                                 */
/*  Создан:    11.07.00                                      Алешин А.В.    */
/****************************************************************************/

import "wldoc.mac", "wlsbrftl.mac", "wltools.mac", "adress.mac", rsd, oralib,likepy;

const PRINT_SHIFT = "                     ";

/* блок информации для preprint и postprint */
var MsgPrTmpFileName, OldName;
var MassCopy;

FILE MsgPrTmpFile () txt;
FILE MsgPrWlTpFld (wltpfld) key 1;
FILE MsgPrPerson  (person);
FILE MsgPrWlHistor(wlhistor) key 1;

/* Вспомогательная функция, использует процедуру ПолучитьПоле из MesInter */
macro SB_ПолучитьПоле( field_name, field_value, MesID )
  ПолучитьПоле( MesID,  field_name, field_value );
  SetParm( 1, field_value );
end;

/* Вспомогательная функция, считывает поле из строки в формате SBRF3 (парамеир BCI) */
macro SB_ПолучитьПолеPIB( field_name, field_value, BCI )
  ReadFieldSBRF3( field_value, field_name, BCI );
  SetParm( 1, field_value );
end;

/* подготовка к печати */
macro PrePrintForm( addrMes, _MassCopy )

  SetBuff( wlmes, addrMes );

  MassCopy = _MassCopy;

  /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgPrTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgPrTmpFileName );

  if(not open( MsgPrTmpFile, MsgPrTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgPrTmpFileName) );
    return FALSE;
  end;

  std.RSBankHeader;

  return true;
end;

/* постобработка печати */
macro PostPrintForm()
  /* Конец отчета */
  std.RSBankFooter;

  close(MsgPrTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy != 0)
    rewind(MsgPrTmpFile);
    while( next( MsgPrTmpFile ) )
      println(MsgPrTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
end;

/* Распечатать многострочное поле */
macro PrintMultiFields( поле, LenStr, Numbers, FieldInfo );
  var num, count;
  array dest;

  if( (Numbers == 1) AND (StrLen(поле) > 65 ) )
    РазбитьНаСтроки( поле, dest, 65/*LenStr*/, 6/*Numbers*/ );
    num = Asize( dest );
    count=0;
    while( count<num )
      if ( dest(count)!="" ) 
        if( FieldInfo == true )
          if( count == 0 )
            [ ИНФОРМАЦИЯ УЧАСТНИКА          : #]( dest(count) );
          else
            [                                 #]( dest(count) );
          end;
        else
          [   #]( dest(count) ); 
        end;
      end;
      count=count+1;
    end;
  else
    if( FieldInfo == true )
      [ ИНФОРМАЦИЯ УЧАСТНИКА          : #]( поле );
    else
      [   #]( поле ); 
    end;
  end;
end;

/* Макрос для печати поля */
macro РаспечататьПоле( имя, поле )
  
  if (поле != "")
    MsgPrWlTpFld.TpID = TRANSP_SMBR;
    MsgPrWlTpFld.Name = имя;
  
    if( GetEQ( MsgPrWlTpFld ) )
      [##:#](имя, StrUpr(MsgPrWlTpFld.Description ));
      PrintMultiFields( поле, MsgPrWlTpFld.LenEdit, MsgPrWlTpFld.NumLines, false );
    else
      [##:#](имя, поле);
    end;
  end;

  return TRUE;
end; /* macro РаспечататьПоле */

macro ПолучитьИмяОперациониста(Oper)

  MsgPrPerson.Oper = Oper;
  if ( GetEQ(MsgPrPerson) )
     return string(Oper) + " " + MsgPrPerson.Name;
  else
     return string(Oper);
  end;

end;

macro ПолучитьИмяКонтролера( MesID )
  
  MsgPrWlHistor.ObjID   = MesID;
  MsgPrWlHistor.ObjKind = 502 /*OBJTYPE_MES*/;
  MsgPrWlHistor.State   = 20;  /* Сообщение проконтролировано */
  if( GetEQ(MsgPrWlHistor) )
    return ПолучитьИмяОперациониста( MsgPrWlHistor.UserID );
  else
    return "";
  end;
end;

/* чтение даты из строки в формате dd/mm/gg */
macro YYMMGG_slash(Строка, Позиция )
  var День, Месяц, Год;

  День  = substr( Строка, Позиция,   2 );
  Месяц = substr( Строка, Позиция+2, 2 );
  Год   = substr( Строка, Позиция+4, 2 );

  return День + "/" + Месяц + "/" + Год;
end;

macro GetRequestQueries( MesID )
  var select:string;
  var params:TArray;
  var rs:object;


  select = "select req.t_Queries from dwlmeslnk_dbt lnk, dwlreq_dbt req "+
           "where lnk.t_MesID =: MesID and lnk.t_ObjKind = :OBJTYPE_REQ  and "+
                             "req.t_ReqID = lnk.t_ObjID";
  params = makeArray( SQLParam("MesID", MesID ),
                      SQLParam("OBJTYPE_REQ",OBJTYPE_REQ ));
  rs = execSQLselect( select, params, FALSE );

  if ( rs.MoveNext() )
    if ( rs.value(0)=="" )
       return "";
    else
       return rs.value(0);
    end;
  end; 

  return "";
end; 

/* Макрос печати заголовка формы */
macro PrintHeader( NumberForm, NameForm, DateMessage )
  record RecordAdress ( adress );
  var CodeBuf, error, Name, Place, _PartyID, SenderID, ReceiverID;

  if( wlmes.Direct == "X" )   /* Входящее сообщение */
    SenderID = wlmes.OutsideAbonentID;
    ReceiverID = {OurBank};
  else                        /* Исходящее сообщение */
    SenderID = {OurBank};
    ReceiverID = wlmes.OutsideAbonentID;
  end;

  [Сигнатура     : ### #]( NumberForm, StrUpr(NameForm) );

  /* Отправитель сообщения */
  CodeBuf = ПолучитьКодСубъекта( SenderID, PTCK_SBRF, error );
  
  if( SenderID )
    _PartyID = SenderID;
  else
    _PartyID = {OurBank};
  end;
      
  if( ПолучитьСубъекта( _PartyID, wlparty ) == 0 )
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    Name  = wlparty.Name;
    Place = RecordAdress.Place;
  else
    Name  = "???";
    Place = "???";
  end;
  [От кого       : ### #
                       #](CodeBuf, Name, Place);
  /* Получатель сообщения */
  CodeBuf = ПолучитьКодСубъекта( ReceiverID, PTCK_SBRF, error );
  
  if( ReceiverID )
    _PartyID = ReceiverID;
  else
    _PartyID = {OurBank};
  end;

  if( ПолучитьСубъекта( _PartyID, wlparty ) == 0 )
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    Name  = wlparty.Name;
    Place = RecordAdress.Place;
  else
    Name  = "???";
    Place = "???";
  end;
  [Кому          : ### #
                       #](CodeBuf, Name, Place);
  /* Дата сообщения */
  [Дата создания : ##########](DateMessage:f);
  [______________________________________________________________________________];
  return TRUE;
end;

macro PrintHeader0SA( NumberForm, NameForm, DateMessage )
  record RecordAdress ( adress );
  var CodeBuf, error, Name, Place, _PartyID, SenderID, ReceiverID;

  if( wlmes.Direct == "X" )   /* Входящее сообщение */
    SenderID = wlmes.OutsideAbonentID;
    ReceiverID = {OurBank};
  else                        /* Исходящее сообщение */
    SenderID = {OurBank};
    ReceiverID = wlmes.OutsideAbonentID;
  end;

  if ( NumberForm==sign_0SAERR )
     [            КВИТАНЦИЯ О ПОМЕЩЕНИИ В ОТБРАКОВАННЫЕ ];
  else
     [                     КВИТАНЦИЯ О ДОСТАВКЕ ];
  end;
  [ ];

  /* Отправитель сообщения */  
  CodeBuf = ПолучитьКодСубъекта( SenderID, PTCK_CLIRING, error );
  if ( error )
    CodeBuf = ПолучитьКодСубъекта( SenderID, PTCK_SBRF, error );
  end;
  
  if( SenderID )
    _PartyID = SenderID;
  else
    _PartyID = {OurBank};
  end;
      
  if( ПолучитьСубъекта( _PartyID, wlparty ) == 0 )
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    Name  = wlparty.Name;
    Place = RecordAdress.Place;
  else
    Name  = "???";
    Place = "???";
  end;  
  [ КОД УЧАСТНИКА-ОТПРАВИТЕЛЯ     : ##########](CodeBuf);
  [ УЧАСТНИК-ОТПРАВИТЕЛЬ          : #](string(Name));
  /* Получатель сообщения */
  CodeBuf = ПолучитьКодСубъекта( ReceiverID, PTCK_CLIRING, error );
  if ( error )
     CodeBuf = ПолучитьКодСубъекта( ReceiverID, PTCK_SBRF, error );
  end;
  
  if( ReceiverID )
    _PartyID = ReceiverID;
  else
    _PartyID = {OurBank};
  end;

  if( ПолучитьСубъекта( _PartyID, wlparty ) == 0 )
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    Name  = wlparty.Name;
    Place = RecordAdress.Place;
  else
    Name  = "???";
    Place = "???";
  end;
  [ КОД УЧАСТНИКА-ПОЛУЧАТЕЛЯ      : ##########](CodeBuf);
  [ УЧАСТНИК-ПОЛУЧАТЕЛЬ           : #](string(Name));
  [ ];
  return TRUE;
end;

/* Печать сообщений СМБР в формате СБРФ3 */
macro PrintFormExec( IsPIB, SBRFStr )
  var field_name, field_value;

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return FALSE;
  end;

  /* Печатаем заголовок */
  if( not PrintHeader( f_wlmesfrm.Name, f_wlmesfrm.Description, wlmes.OutsideAbonentDate ) )
    return FALSE;
  end;

  /* Последовательно считываем и печатаем поля сообщения */
  while( ((IsPIB == false) AND СчитатьПоле( field_name, field_value )) OR
         ((IsPIB == true) AND (SB_СчитатьПолеИзСообщения( field_name, field_value, SBRFStr )==0)) )
    if( not РаспечататьПоле( field_name, field_value ) )
      std.msg( String("Ошибка при печати поля сообщения" + string(field_name)) );
      return false;
    end;
  end;

  PostPrintForm();

  return TRUE;
end;

/* Печать сообщений СМБР (все тэги хранятся в отдельных полях)  */
macro PrintForm( addrMes, _MassCopy )

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  return PrintFormExec( false );
end;

/* Печать сообщений СМБР (блок целевой информации хранится в одном поле)  */
macro PrintFormPIB( addrMes, _MassCopy )
  var field_name, field_value;

  if( not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  if( not СчитатьПоле( field_name, field_value ) OR (field_name != SB_Tag_PIB) OR (field_value == "") )
    std.msg( String("В сообщении с референсом ", wlmes.TRN, "|не заполнено поле '", SB_Tag_PIB, "'") );
    return false;
  end;
   
  return PrintFormExec( true, field_value );
end;

macro ПолучитьКодОтветаНаСообщение(answerCode)
  var result = answerCode + " ";
  var ErrDescription;
  if(GetElementAndNoteLLVALUES(OBJTYPE_WLRESCODE_SMFR, answerCode, NULL, ErrDescription))
    result = result + ErrDescription;
  end;
  return result;
end;

macro PrintForm0SA( addrMes, _MassCopy )
  var field_name, field_value;
  var answerCode;

  if( not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  if( not СчитатьПоле( field_name, field_value ) OR (field_name != SB_Tag_PIB) OR (field_value == "") )
    std.msg( String("В сообщении с референсом ", wlmes.TRN, "|не заполнено поле '", SB_Tag_PIB, "'") );
    return false;
  end;

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return FALSE;
  end;

  /* Печатаем заголовок */
  if( not PrintHeader0SA( f_wlmesfrm.Name, f_wlmesfrm.Description, wlmes.OutsideAbonentDate ) )
    return FALSE;
  end;

  answerCode = substr(field_value,strlen(field_value)-1,2);

  [ ИДЕНТИФИКАТОР ДОКУМЕНТА       : #](wlmes.RelatedRef);
  [ СИГНАТУРА БИЗНЕС-СООБЩЕНИЯ    : #](substr(field_value,1,3));
  [ КОД ОТВЕТА                    : #](ПолучитьКодОтветаНаСообщение(answerCode));

  [ ];
  [ ОПЕРАТОР                      : #](ПолучитьИмяОперациониста(wlmes.UserID));
  PostPrintForm();
   
  /*return PrintFormExec( true, field_value );*/
  return true;
end;

/* Печать формы 0BQ "Запрос на получение промежуточной выписки" */
macro PrintForm0BQExec( MacroProc, Parm )
  var error, FieldValue, ИмяКонтролера = "";
  var SenderID, ReceiverID, CreatorID;
 
  if( wlmes.Direct == "X" )
    CreatorID  = wlmes.OutsideAbonentID;
    SenderID   = wlmes.AgentID;
    ReceiverID = wlmes.InsideAbonentID;
  else
    CreatorID  = wlmes.InsideAbonentID;
    SenderID   = wlmes.InsideAbonentID;
    ReceiverID = wlmes.AgentID;
  end;

  [                     ЗАПРОС ВЫПИСКИ                             ];
  [ ДОКУМЕНТ               : ### - ######](709, substr(wlmes.TRN, StrLen(wlmes.TRN)-5, 6) );
  [ ДАТА СОЗДАНИЯ          : ########](YYMMGG_slash(wlmes.TRN, 1));

  ПолучитьСубъекта( CreatorID, wlparty );                                                   
  [ СОЗДАТЕЛЬ              : ######### #](ПолучитьКодСубъекта( CreatorID, PTCK_SBRF, error ), wlparty.Name );

  ПолучитьСубъекта( SenderID, wlparty );
  [ ОТПРАВИТЕЛЬ            : ######### #](ПолучитьКодСубъекта( SenderID, PTCK_SBRF, error ), wlparty.Name );

  ПолучитьСубъекта( ReceiverID, wlparty );
  [ ПОЛУЧАТЕЛЬ             : ######### #](ПолучитьКодСубъекта( ReceiverID, PTCK_SBRF, error ), wlparty.Name );

  ExecMacro( MacroProc, "CU", FieldValue, Parm );
  [ ВИД АКТИВОВ            : ###](FieldValue);

  ExecMacro( MacroProc, "VT", FieldValue, Parm );
  [ ТИП ВЫПИСКИ            : ###](FieldValue);

  ExecMacro( MacroProc, "VB", FieldValue, Parm );
  [ ДАТА ВЫПИСКИ           : ########](YYMMGG_slash(FieldValue, 1));
    
  [ КЛЮЧ                   : ########](""); /*Пусто. Документ данного типа не ключуется*/
  [ ИМЯ ОПЕРАТОРА          : #](ПолучитьИмяОперациониста(wlmes.UserID));

  if( wlmes.State >= 20 )
    ИмяКонтролера = ПолучитьИмяКонтролера(wlmes.MesID);
  end;
  [ ИМЯ КОНТРОЛЕРА         : #](ИмяКонтролера);
  [ ИНФОРМАЦИЯ ОПЕРАТОРА   : #](GetRequestQueries(wlmes.MesID));   

  PostPrintForm();

  return TRUE;
end;

/* Релиз 0BQ */
macro PrintForm0BQ( addrMes, _MassCopy )                     

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  return PrintForm0BQExec( "SB_ПолучитьПоле", wlmes.MesID );
end;

/* Релиз 0BQJ */
macro PrintForm0BQPIB( addrMes, _MassCopy ) 
  var BCI = "";

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  ПолучитьПоле( wlmes.MesID, SB_Tag_PIB, BCI );

  return PrintForm0BQExec( "SB_ПолучитьПолеPIB", BCI );
end;

/* Макрос печати выписки */
macro PrintForm0B9( addrMes, _MassCopy )
  var field_name, field_value;
  var chapter = 1;

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  while( СчитатьПоле(field_name, field_value) )
     if ( field_name=="PA" )
/*Отправитель         7700000000*/
[Отправитель         ##########](field_value);
     elif ( field_name=="RC" )
/*Получатель          7766250000*/
[Получатель          ##########](field_value);
     elif ( field_name=="VT" )
/*Тип выписки         1*/
[Тип выписки         #](field_value);
     elif ( field_name=="VCODE" )
/*Код выписки         019*/
[Код выписки         ###](field_value);
     elif ( field_name=="VNUM" )
/*Номер выписки       1 */
[Номер выписки       #] (field_value);
     elif ( field_name=="CU" )
/*Код валюты          RUR*/
[Код валюты          ###](field_value);
     elif ( field_name=="DATEIN" )
/*За период           14/10/2003           */
[За период         с ##########](field_value);
     elif ( field_name=="DATEOUT" )
[                 по ##########](field_value);
     elif ( field_name=="PVB" )
/*Дата предыд.выписки 13/10/2003*/
[Дата предыд.выписки ##########](field_value);
     elif ( field_name=="VDATE" )
/*Отчет сформирован   15/10/2003 00:35*/
print("Отчет сформирован   ",field_value);
     elif ( field_name=="VTIME" )
[ #####](field_value);
     elif ( field_name=="ACC" )
       if ( chapter==1 )
[ ];
[Раздел 1. Выписка по пассивному счету];
/*Счет №              30301810677000607712*/
[Счет №              ####################](field_value);
       else
[ ];
[Раздел 2. Выписка по активному счету];
/*Счет №              30302810977000607712*/
[Счет №              ####################](field_value);
       end;
     elif ( field_name=="INBL" )
/*Входящее сальдо                                                            0          18854483.52*/
[Входящее сальдо                                                              ####################](field_value);
[№      Вид оп. Кл.№   Кл.дата    Корреспондир.счет    Счет получателя      Д/К              Сумма Платежный документ       Тип  Уч.-пол.   Дата проводки Примечание];
     elif ( field_name=="DOC" )
/*       if ( (chapter==1) OR (chapter==2) OR (chapter==4) OR (chapter==5) )*/
/*[000008 01      000188 13/10/2003 30102810677000000000 40204810577120100001 0                 4.00 141003 7700030000 623109 005  7766250000 14/10/2003                                       ]*/
[############################################################################################################################################################################################](field_value);
/*       end;*/
     elif ( field_name=="DAM" )
/*Операции по дебету                                                                    30783663.54                        1*/
print(
"Операции по дебету                                                           ",field_value:20:r);
     elif ( field_name=="DNUM" )
[                   ######](field_value);
     elif ( field_name=="KAM" )
/*Операции по кредиту                                                                   24762458.39                      691*/
print(
"Операции по кредиту                                                          ",field_value:20:r);
     elif ( field_name=="KNUM" )
[                   ######](field_value);
      if ( chapter==3 )
         chapter = chapter+1;
[ ];
[Раздел 4. Не принятые к обработке документы];
[№      Дата приема Время Кл.№   Кл.дата                 Сумма Платежный документ       Тип];
      end;
     elif ( field_name=="OUTBL" )
/*Исходящее сальдо                                                           0          12833278.37*/
[Исходящее сальдо                                                             ####################](field_value);
      if ( (chapter==1) OR (chapter==2) )
        chapter = chapter+1;
      end;
      if ( chapter==3 )
[ ];
[Раздел 3. Ожидающие проводки документы];
[№      Дата приема Время Кл.№   Кл.дата    Д/К              Сумма Платежный документ       Тип  Примечание];
      end;
     elif ( field_name=="AM" )
print("Всего документов                         ",field_value:20:r);
     elif ( field_name=="NUM" )
      if ( chapter==6 )
[Всего документов                           ######](field_value);
      else
[                   ######](field_value);
      end;
      chapter = chapter+1;
      if ( chapter==5 )
[ ];
[Раздел 5. Обработанные требования-поручения];
[№      Дата приема Время Кл.№   Кл.дата                 Сумма Платежный документ];
      elif ( chapter==6 )
[ ];
[Раздел 6. Обработанные сообщения участнику];
[№      Дата приема Время Информационный документ  Уч.-пол.   Тип];
      end;
     end;
  end;

  PostPrintForm();

  return TRUE;
end;

/* Форма печати информационных сообщений */
macro PrintFormInfoExec( MacroProc, Parm )                 
  var PartyID, CodeKind, FieldValue = "", FieldValue1 = "", error;
  var FieldValue_MP = "", FieldValue_PP = "", FieldValue_PN = "";
    
  [                     ИНФОРМАЦИЯ УЧАСТНИКУ                   ];
  ExecMacro( MacroProc, "DT", FieldValue, Parm );
  ExecMacro( MacroProc, "LD", FieldValue1, Parm );
  [ НОМЕР ДОКУМЕНТА               : ### - ######   ДАТА ДОКУМЕНТА: ##########](FieldValue, substr(wlmes.TRN, StrLen(wlmes.TRN)-5, 6), FieldValue1 );

  ExecMacro( MacroProc, "MT", FieldValue, Parm );
  [ ТИП СООБЩЕНИЯ                 : ###](FieldValue);

  ExecMacro( MacroProc, "PA", FieldValue, Parm );
  [ КОД УЧАСТНИКА_ОТПРАВИТЕЛЯ     : ##########](FieldValue);

  CodeKind = PTCK_CLIRING;

  PartyID = ПолучитьКодСубъекта( FieldValue, CodeKind, Error );

  if( ПолучитьСубъекта(PartyID, wlparty) != 0 )
    FieldValue = "???";
  else  
    FieldValue = wlparty.Name;
  end;

  [ УЧАСТНИК-ОТПРАВИТЕЛЬ          : #](FieldValue);

  ExecMacro( MacroProc, "RC", FieldValue, Parm );
  [ КОД УЧАСТНИКА_ПОЛУЧАТЕЛЯ      : ##########](FieldValue);

  PartyID = ПолучитьКодСубъекта( FieldValue, CodeKind, Error );

  if( ПолучитьСубъекта(PartyID, wlparty) != 0 )
    FieldValue = "???";
  else  
    FieldValue = wlparty.Name;
  end;

  [ УЧАСТНИК-ПОЛУЧАТЕЛЬ           : #]( FieldValue );
  println();

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    return FALSE;
  end;
  
  if( f_wlmesfrm.Name == sign_0B7 ) 
    ExecMacro( MacroProc, "MP", FieldValue_MP, Parm );
    ExecMacro( MacroProc, "PP", FieldValue_PP, Parm );
    ExecMacro( MacroProc, "PN", FieldValue_PN, Parm );
    FieldValue = FieldValue_MP + FieldValue_PP + FieldValue_PN;
  elif( f_wlmesfrm.Name == sign_0BF ) 
    ExecMacro( MacroProc, "ME", FieldValue, Parm );
  end;
  
/*  [ ИНФОРМАЦИЯ УЧАСТНИКА          :];*/
  PrintMultiFields( FieldValue, 0, 1, true);
  println();

  [ РЕКВИЗИТЫ ОТПРАВИТЕЛЯ ];

  FieldValue = "";
  ExecMacro( MacroProc, "SS", FieldValue, Parm );
  [ Расчетная система             : ######](FieldValue);

  ExecMacro( MacroProc, "SN", FieldValue, Parm );
  [ Код банка                     : #](FieldValue);

  ExecMacro( MacroProc, "SK", FieldValue, Parm );
  [ Корсчет банка                 : #](FieldValue);

  ExecMacro( MacroProc, "SB", FieldValue, Parm );
  [ Наименование банка            : #](FieldValue);
  println();
  [ РЕКВИЗИТЫ ПОЛУЧАТЕЛЯ ];

  FieldValue = "";
  ExecMacro( MacroProc, "RS", FieldValue, Parm );
  [ Расчетная система             : ######](FieldValue);

  ExecMacro( MacroProc, "BC", FieldValue, Parm );
  [ Код банка                     : #](FieldValue);

  ExecMacro( MacroProc, "BK", FieldValue, Parm );
  [ Корсчет банка                 : #](FieldValue);

  ExecMacro( MacroProc, "BN", FieldValue, Parm );
  [ Наименование банка            : #](FieldValue);
  println();
  
  [ Операционист                  : #]( ПолучитьИмяОперациониста(wlmes.UserID) );
  if( wlmes.State >= 20 )
    FieldValue = ПолучитьИмяКонтролера(wlmes.MesID);
  else
    FieldValue = "";
  end;

  [ Контролер                     : #]( FieldValue );

  PostPrintForm();

  return TRUE;
end;

/* Релиз 0B7 */
macro PrintFormInfo( addrMes, _MassCopy )                     

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  return PrintFormInfoExec( "SB_ПолучитьПоле", wlmes.MesID );
end;

/* Релиз 0B7J */
macro PrintFormInfoPIB( addrMes, _MassCopy )                 
  var BCI = "";

  if(not PrePrintForm( addrMes, _MassCopy ) ) 
    return false;
  end;

  ПолучитьПоле( wlmes.MesID, SB_Tag_PIB, BCI );

  return PrintFormInfoExec( "SB_ПолучитьПолеPIB", BCI );
end;
