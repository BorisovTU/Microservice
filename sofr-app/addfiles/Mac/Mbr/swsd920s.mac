/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация запросов на выписку по SWIFT-SB MT920S                        */
/*                                                                          */
/*  Имя файла: swsd920S.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Запрос на выписку */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  Currency         :string,        /* Валюта */
  MessageRequested :string;        /* запрашиваемая форма */

  TRF              = "";
  Currency         = "";
  MessageRequested = "";
end; /* class StatementMessage */

var MT920S : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro Fill20( TRF )
  MT920S.TRF = TRF;
  return TRUE;
end;

macro Fill12(MesFrm)
  MT920S.MessageRequested = MesFrm;
  return TRUE;
end;

macro Fill32E(Cur)
  MT920S.Currency = Cur;
  return TRUE;
end;

/* Заполнение списка полей формы  MT920S*/
var FldSN  = ТПолеФормы( УникальныйНомерСообщенияСМФР,    FIELD_MANDATORY, "ReadTRF", "FillSN",  "" ),
    Fld20  = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld12  = ТПолеФормы( SubMessageType,                  FIELD_MANDATORY, "Read12",  "Fill12",  "" ),
    Fld32E = ТПолеФормы( CurrencyOrQuantityOfMetalField,  FIELD_OPTIONAL,  "Read32E", "Fill32E", "" );

macro ConstructMT920S( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  return TRUE;
end; /* ConstructMT920S */

macro GenDoc( addrMes )
  MT920S = StatementMessage();
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса по МТ920S");

  ClearRecord(wlreq);

  if (not ConstructMT920S(wlmes.OutsideAbonentID))
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlreq.TRN = MT920S.TRF;
  if (not ПолучитьФинИнПоISO(MT920S.Currency, wlfininstr))
    std.msg("Не определена валюта сообщения по коду ISO: " + MT920S.Currency);
    return false;
  end;
  wlreq.FIID = wlfininstr.FIID;

  if (not НайтиКорсхемуПоКонтрагентуИВалюте(wlmes.OutsideAbonentID, wlreq.Corschem, wlreq.FIID))
    std.msg("Не определена схема расчетов");
    return FALSE;
  end;  
  
  if (not ВставитьЗапросНаПолучениеВыписки(wlreq))
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
