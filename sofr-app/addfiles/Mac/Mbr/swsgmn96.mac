/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MTn96                                    */
/*                                                                          */
/*  Имя файла: swsgmn96.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

macro GenMes( addrMes, addrReq )
  var field_value, Narrative, Descript, ini_form, mes_id:integer, mes_date:date, mes_direct:string;

  SetBuff(wlmes, addrMes);
  SetBuff(wlReq, addrReq);

  SB_CheckFIID( wlReq.FIID );

  PrintLog(2,"Генерация сообщения по МТn96");

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  ЗаписатьНомерСвязанногоСообщения_SB( wlreq.RelatedRef, mes_id, mes_date, mes_direct, wlReq.Finished == "X" );

  /* RT - тип и подтип исходного сообщения */
  ini_form = ПолучитьНазваниеФормыСвязаногоСообщенияSB( mes_id );
  if( ini_form == "" )
    RunError( String("|Не определен тип и подтип исходного сообщения с референсом: ", wlreq.RelatedRef) );
  end;
  ЗаписатьПолеЛог(ТипИПодтипСообщения, ini_form );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  ЗаписатьПолеЛог( RelatedReferenceField, wlReq.RelatedRef );  

  /* 76 - Answers */
  if( wlReq.Queries == "" )
    RunError("|Не заполнено значение ответа на запрос");
  end;
  StrMultyToFormat( wlReq.Queries, field_value, 35, 6 );
  ЗаписатьПолеЛог( AnswersField, field_value );

  ПрочитатьТекстЗапроса_Ответа( wlReq, Narrative, Descript );

  /* 77A Narrative - Свободный текст */
  if( Narrative != "" )
    StrMultyToFormat( Narrative, field_value, 35, 20 );
    ЗаписатьПолеЛог( NarrativeField, field_value );  
  end;
  
  /* 11а - МТ and Date of Original Message */
  field_value = substr(ini_form, 1, 4) + GetSWIFTDate(mes_date);
  if( mes_direct == "X" )
    ЗаписатьПолеЛогВБлок( "11a", MTandDateOriginalMessage_RField, field_value );
  else
    ЗаписатьПолеЛогВБлок( "11a", MTandDateOriginalMessage_SField, field_value );
  end;
  if( not УстановитьКонтекстБлока( "" ) )
     RunError("|Ошибка при установке пустого контекста блока");
  end;

  /* 79 - Narrative - Описание исходного сообщения */
  if( Descript != "" )
    StrMultyToFormat( Descript, field_value, 50, 35 );
    ЗаписатьПолеЛог( NarrativeDescriptionField, field_value );  
  end;

  /* MFIELDS */
  СчитатьЗаполненныеПоля( mes_id, field_value);
  ЗаписатьПолеЛог(CopyMandatoryFields, field_value);

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;