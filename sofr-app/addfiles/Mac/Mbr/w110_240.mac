/*
$Name:           w110_240.mac
$Module:         МБР
$Description:    Макрос шага
*/

/*
 Блок      : 29002  - "Картотека НОСТРО-счета"
 Шаг       : 240    - "Учет документа на картотеке"
*/

import  InsCarryDoc, OprInter, BankInter, RMInter, "rmconst.mac", "wldoc.mac" ;
import "rmtools.mac", "pm_categ.mac", "pm_common.mac", pm_setst, "pmcarfun.mac",
       "WLTOOLS.MAC", "cor_acc.mac", "pm_answerret.mac";

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

private macro ValueDateForCalcDocIndexNOSTRO(Paym : RsbPayment) : date
  var ValueDate : date = {curdate};
  var DocKind = Paym.PrimDocKind;

  if( GetParentOrEqualDocKindFromList(DocKind, PMDOC_CLIENTPAYMENT) )
    if( Paym.StartDepartment == Paym.Department )
      ValueDate = PM_GetOperDay_BankServiceBalance(Paym.Department);
    else
      ValueDate = PM_GetOperDay_Balance(Paym.Department);
    end;
  end;

  return ValueDate;
end;

private macro GetErrCodeForPB : integer
  var ErrCode : integer = FNS_RP_PAY_NO_EXEC_CORRACC_NO_MONEY;

  if( not IsOprMultiExec() )
    var NumButton : integer = ConfWin
      ( makeArray("Помещение документа в картотеку в ПБР (НОСТРО-счета) произошло из-за недостаточности средств на корреспондентском счете или из-за наличия ограничений на распоряжение денежными средствами?"),
        makeArray("Недостаточность средств", "Наличие ограничений"),
        0 // default button
      );

    if(NumButton == 1) // "Наличие ограничений"
      ErrCode = FNS_RP_PAY_NO_EXEC_CORRACC_CLAIM;
    end;
  end;

  return ErrCode;
end;

// Сообщить о неисполнении
private macro InformAboutNonExecution
  
  record wlmes( wlmes );
  const UNDEFERRCODE = -1;

  if( ДокументПорожденВходящимЭСИДNew(PaymentObj.PaymentID) )

    var initialPaymentID = getInitialPaymentID(PaymentObj.PaymentID, PMLINK_KIND_EXECORDER);
    
    if( initialPaymentID > 0 )
      // Связанное входящее сообщение платежа 
      if( not ПолучитьСообщение( initialPaymentID, OBJTYPE_PAYMENT, 1 /*WLD_MES_IN*/, wlmes, NULL ) )  
          ClearRecord( wlmes );
      end;
    end;

    var MesFormatType = -1;

    if( wlmes.MesID > 0 )
      MesFormatType = GetMesFormatType(wlmes.MesID); 
    end;

    // Параметры создаваемого подтверждения банка
    var ErrCode : integer = UNDEFERRCODE;
    
    if( (initialPaymentID == 0) or (MesFormatType == WLD_FORMAT_XML) )
      ErrCode = GetErrCodeForPB();
    end;
    
    var ErrDescription;
    var ErrList = RsbWlError(0);
    var wlerr_buf = TRecHandler("wlerror.dbt");

    if( (initialPaymentID > 0) and ( wlmes.MesID > 0 ) and (MesFormatType == WLD_FORMAT_TXT) )

      wlerr_buf.rec.Code = "35";
      wlerr_buf.rec.Description  = string("поручение налогового органа № ",
          PaymentObj.GetPMRMPROP().rec.Number, " от ", PaymentObj.GetPMRMPROP().rec.Date,
          " на сумму ", PaymentObj.BaseAmount, " не может быть исполнено в установленный срок в связи с отсутствием ",
          "(недостаточностью) денежных средств на корреспондентском счете банка (филиала банка) БИК " , PaymentObj.PayerBankCode, " ", 
          PaymentObj.PayerBankName );

      if( ErrList.Insert( wlerr_buf ) != 0 )
        RunError("Ошибка при вставке подтверждения банка");
      end;

      // Параметры создаваемого подтверждения банка 
      ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

      if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList ) )
        msgbox( "Ошибка при вставке подтверждения банка" );
      end;

    else

      if( ErrCode == UNDEFERRCODE )
        ErrCode = FNS_RP_PAY_NO_EXEC_CORRACC_NO_MONEY;
      end;

      wlerr_buf.rec.Code = "00";

      wlerr_buf.rec.Description  = "Поручение налогового органа № "+ PaymentObj.Number + " от " + PaymentObj.Date + " на сумму " + PaymentObj.BaseAmount + 
                                   " не может быть исполнено в установленный срок в связи с отсутствием (недостаточностью) денежных средств " +
                                   "на корреспондентском счете банка (филиала банка) БИК " + PaymentObj.PayerBankCode + " " + PaymentObj.PayerBankName +
                                   " или из-за наличия на счете ограничений на распоряжение денежными средствами";

      if( ErrList.Insert( wlerr_buf ) != 0 )
        RunError("Ошибка при вставке подтверждения банка");
      end;
      
      // Параметры создаваемого подтверждения банка 
      ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

      if( not ВставитьПодтверждениеБанкаФНС( NULL, ErrCode, ErrDescription, ErrList, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, PaymentObj.PaymentID, 2 ) )
        msgbox( "Ошибка при вставке подтверждения банка" );
      end;

    end;

  end;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first )

  var FormatFile:integer = -1;
  // Актуализируем проводки
  if( not CarryPlanDocuments(PaymentObj.PaymentID) ) 
    MsgBox("Ошибка при помещении планируемой проводки в проведенные");
    return 1;
  end;

  // Отсекаем валюту
  if( ( PaymentObj.PayerFIID != 0 ) or ( PaymentObj.ReceiverFIID != 0 ) )
    msgBox( "Валютные платежи не ставятся на картотеку корсчета" );
    return 1;
  end;

  // Отсекаем дебетовые
  if( not PaymentObj.IsCredit() )
    msgBox( "Дебетовые платежи не ставятся на картотеку корсчета" );
    return 1;
  end;

  // Если платеж является банковским - то пролетает
  if( PaymentObj.Payer AND
      ВидСубъекта( PaymentObj.Payer, PTK_BANK ) AND
      ( not IsClientPayerAccount( PaymentObj ) ) )
    msgBox( "Платежи банка не ставятся на картотеку корсчета" );
    return 1;
  end;

  // Проводки
  var paymtr1:RsbPaymTransaction = NULL;

  //Если последняя проводка по платежу была на корсчет, удаляем последнюю проводку по платежу
  if(
     (PaymentObj.FuturePayerAccount == PaymentObj.FutureReceiverAccount) and 
     (PaymentObj.FuturePayerFIID == PaymentObj.FutureReceiverFIID)
    )
    if(DeleteLastCarry(PaymentObj))
      msgbox("Ошибка при удалении проводки");
      return 1;
    end;
  end;

  PaymentObj.ValueDate = ValueDateForCalcDocIndexNOSTRO(PaymentObj);

  if( not IsPlacedInUnclosed( PaymentObj.PaymentID ) )
    // Балансовая проводка
    paymtr1 = PaymentObj.MakeTransaction();
    paymtr1.Chapter         = 1;
    paymtr1.FIIDPayer       = PaymentObj.ReceiverFIID;
    paymtr1.Sum             = PaymentObj.ReceiverAmount;
    paymtr1.ResultCarry     = 1;
    paymtr1.Kind_Oper       = " 1";

    if (PaymentObj.ShifrOper != "")
      paymtr1.Shifr_Oper      = PaymentObj.ShifrOper;
    else
    paymtr1.Shifr_Oper      = "09";
    end;

    paymtr1.AccountPayer    = PaymentObj.FuturePayerAccount;
    paymtr1.AccountReceiver = Wl_AccNostroFD( PaymentObj.Payer ).FindAndOpenAccount( "БалКартотекаНОСТРО", 0, {curdate} );
    paymtr1.Date_Carry      = PaymentObj.ValueDate;
    paymtr1.Department      = PaymentObj.Department;
    paymtr1.Ground          = substr( "Постановка в картотеку документа/"+PaymentObj.Ground, 1, 210 );
    paymtr1.Numb_Document   = PaymentObj.Number;
    paymtr1.ClaimID         = GetClaimID( PaymentObj, paymtr1.AccountPayer, paymtr1.Chapter, paymtr1.FIIDPayer );
    if( not paymtr1.Carry() )
      MsgBox("Ошибка при актуализации платежа");
      return 1;
    end;
  else
    // А вдруг он в невыясненных был?!
    if( not ChangePaymentObjDocuments( PaymentObj.PaymentID, 1, paymtr1.AccountPayer, paymtr1.AccountReceiver ) )
      return 1;
    end;
  end;
    
  // Внебалансовая проводка
  var paymtr3:RsbPaymTransaction = PaymentObj.MakeTransaction();
  paymtr3.Chapter         = 3;
  paymtr3.FIIDPayer       = PaymentObj.ReceiverFIID;
  paymtr3.Sum             = PaymentObj.ReceiverAmount;
  paymtr3.ResultCarry     = 1;
  paymtr3.Kind_Oper       = " 1";
  paymtr3.Shifr_Oper      = "09";
  paymtr3.AccountPayer    = Corr_Acc(PaymentObj).FindAndOpenSysAccount( "ВнебалКартотекаКорсчета", 0, {curdate} );
  paymtr3.AccountReceiver = NotBalCorrAcc_FirstDoc("П").FindAndOpenSysAccount( "ВнебалСчетКорресп",     0, {curdate} );
  paymtr3.Date_Carry      = PaymentObj.ValueDate;
  paymtr3.Department      = PaymentObj.Department;
  paymtr3.Ground          = substr( "Постановка в картотеку документа/"+wlpmrmprop.Ground, 1, 210 );
  paymtr3.Numb_Document   = PaymentObj.Number;
  if( not paymtr3.Carry() )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;

  // Освободить все претензии по платежу
  PaymentObj.FreeReserve( PaymentObj.PayerAccount, PaymentObj.Chapter, PaymentObj.PayerFIID );

  PaymentObj.PropStatus     = PM_PROP_CARDFILE;

  PaymentObj.CardFileKind   = WLD_CARD_OUT_CORSCH;
  PaymentObj.CardFileDateIn = {curdate};  

  // Сообщить о неисполнении
  InformAboutNonExecution();

  return 0;

OnError(er)
  ExeptionMessage( er );
  return 1;
end;


macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Num_Step,    /* Номер шага операции (из настроек)               */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* внутренний идентификатор шага операции          */

  var stat = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if ((PaymentObj.DocKind == PS_PAYORDER) or (PaymentObj.DocKind == PS_INRQ))
      var DocKind, Origin;
      var rs = execSQLselect("select nvl(ps.t_DocKind, " + PSPOKIND_REQUEST + "), nvl(ps.t_Origin, inr.t_Origin) from dpspayord_dbt ps, dpsinrq_dbt inr, dpmpaym_dbt pm " +
                             " where pm.t_PaymentID = ? and ps.t_OrderID(+) = pm.t_PaymentID and inr.t_PaymentID(+) = pm.t_PaymentID",
                             makeArray(SQLParam("", PaymentObj.PaymentID)));
      if (rs and rs.MoveNext())
        DocKind = rs.value(0);
        Origin = rs.value(1);
        var Narrative : string = 
          "Невозможность завершить перевод денежных средств на основании распоряжения " +
          "в установленный срок по причине недостаточности денежных средств " +
          "на корреспондентском счете (субсчете) кредитной организации (ее филиала), " +
          "открытом в Банке России, кредитная организация уведомляет о помещении " +
          "инкассового поручения Банком России в очередь неисполненных в срок " +
          "распоряжений и о приеме инкассового поручения к исполнению";
        var Queries : string = "InfoCode:9";
        CreateED274(PaymentObj.PaymentID, Queries, Narrative, ID_Oper, ID_Step);
      end;
    end;
  end;

  return stat;
end;
