/****************************************************************************/
/*                   R-Style Software Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                      Экспорт сообщений SWIFT-SB                          */
/*                                                                          */
/*  Имя файла: swsbout.mac                                                  */
/*  Создан:  09.03.04                                         Панов А.Б.    */
/****************************************************************************/

import "wlexport.mac", "swsbtls.mac", "swsbsign", cryptdlm, oralib, likepy;

const CONTEXTID_NEED_SIGN_SWSB = "ТранспортМБР|8|Формирование_транспортного_файла";

const LenKeyField = 8;

record rRlsForm(wlmesrls);
record rForm(wlmesfrm);

var out = TArray;
var MesSubType, MesDestination, ПрочитаноПервоеПоле, ПервоеПолеИмя, ПервоеПолеБуфер;
var этоКвитанция = 11;

macro InitOut()
  out.size = 1;
  out(0) = "";
end;

macro НоваяСтрока(buf:string)
  if (buf != "") /* Пустые строки не вставляем */
    out(out.size-1) = out(out.size-1) + string(buf);
    out(out.size) = "";
  end;

  return true;
end;

macro СохранитьСтроку(buf:string)
  while(Index(buf, "\n"))
    НоваяСтрока(substr(buf, 1, Index(buf, "\n") - 1));
    buf = substr(buf, Index(buf, "\n") + 1);
  end;
  НоваяСтрока(buf);

  return true;
end;

macro СохранитьБлок(buf:string)
  while(Index(buf, "\n"))
     НоваяСтрока(substr(buf, 1, Index(buf, "\n") - 1));
     buf = substr(buf, Index( buf, "\n" ) + 1);
  end;
  out(out.size-1) = out(out.size-1) + string(buf);

  return true;
end;

macro СохранитьИнформациюВФайле()
  var count = 0, continue0 = true;

  while(continue0 and (count < out.size))
    out(count) = ToANSI(out(count), true);
    if (count<(out.size-1))
      if (not ЗаписатьСтроку(out(count)))
        ErrExport("Ошибка записи строки: " + out(count));
        continue0 = false;
      end;
    else
      if (not ЗаписатьБлок(out(count)))
        ErrExport("Ошибка записи блока: " + out(count));
        continue0 = false;
      end;
    end;
    count = count + 1;
  end;
  InitOut();

  return continue0;
end;

macro ПолучитьИнформациюПоСообщению()
  if (not DefineFormExport(wlmes.RlsFormID))
    return false;
  end;

  /* Определяем подтип сообщения (S, N или I) */
  MesSubType = substr(РелизЭкспорт.Name, strlen(РелизЭкспорт.Name), 1);
  if ((MesSubType != КодПодтипСообщения_КорСчет) and (MesSubType != КодПодтипСообщения_СМФР))
    MesSubType = КодПодтипСообщения_Общий;
  end;

  /* Пока так. Нужно разбираться с MT910 */
  if (РелизЭкспорт.Name == "910")
    MesDestination = КодНазначениеСообщения_МТ910_Подтверждение;
  else
    MesDestination = КодНазначениеСообщения_Общий;
  end;

  return true;
end;

macro СохранитьПоляСообщения(CheckAddBlock)
  var field, buff, str;

  if (ValType(CheckAddBlock) != V_BOOL)
    CheckAddBlock = false;
  end;

  if ((ПрочитаноПервоеПоле == true) AND (CheckAddBlock == false))
    if (ПервоеПолеИмя == CopyMandatoryFields)
      str = string(ПервоеПолеБуфер);
    else
      str = string(":", ПервоеПолеИмя, ":", ПервоеПолеБуфер);
    end;

    if (not СохранитьСтроку(str))
      ErrExport("Ошибка записи поля сообщения " + string(ПервоеПолеИмя));
      return false;
    end;
  end;

  while(СчитатьПоле(field, buff))
    if ((CheckAddBlock == true) AND ((field == "20") OR (field == "V1")))
      ПервоеПолеИмя = field;
      ПервоеПолеБуфер = buff;
      ПрочитаноПервоеПоле = true;
      return true;
    end;
    if (field == CopyMandatoryFields)
      str = string(buff);
    else
      str = string(":", field, ":", buff);
    end;

    if (not СохранитьСтроку(str))
      ErrExport("Ошибка записи поля сообщения " + string(field));
      return false;
    end;
  end;
  
  return true;
end;

macro ЗаписатьБлокСлужебнойИнформацииСМФР( SetSign:bool )
  var RcvBankCode_SBRF, RcvBankCode_SMFR, КодКлирингаОтправителя, OwnerCode,
      SndBankCode_SBRF, SndBankCode_SMFR,
      SndDate, SndTime, KeyDate, Signature = "",
      error, ID, CryptoAPI = RsCryptoAPI(), rsms, StrForSign;

  if (wlmes.InsideAbonentID)
    ID = wlmes.InsideAbonentID;
  else
    ID = {OurBank};
  end;

  SndBankCode_SBRF = ПолучитьОткрытыйКодСубъекта(ID, PTCK_SBRF, error);
  if (error)
    ErrExport("Не найден код абонента СБ РФ Отправителя сообщения!");
    return false;
  end;

  if ((wlmes.OutsideAbonentCodeKind==PTCK_SBRF) AND (wlmes.OutsideAbonentCode != ""))
    RcvBankCode_SBRF = wlmes.OutsideAbonentCode;
    error = 0;
  else
    RcvBankCode_SBRF = ПолучитьОткрытыйКодСубъекта(wlmes.OutsideAbonentID, PTCK_SBRF, error);
  end;  
  if (error) 
    ErrExport("Не найден код абонента СБ РФ Получателя сообщения!");
    return false; 
   end;

/*------------------------------------------------------------------------------------------------*/
   if(( wlmes.kind != этоКвитанция))
      error = ПолучитьНашКодСУчетомТС(PTCK_SMFR, false, SndBankCode_SMFR, OwnerCode, КодКлирингаОтправителя);
      if (error)
         ErrExport("Не найден код СМФР Отправителя сообщения!");
         return false;
      end;

    RcvBankCode_SMFR = ОпределитьПолучателяСообщения(wlmes, КодКлирингаОтправителя, OwnerCode);
    if (RcvBankCode_SMFR == "")
      ErrExport("Не найден код СМФР Получателя сообщения!");
      return false;
    end;

  SndDate = GetSWIFTDate(Date());
  SndTime = GetSWIFTTime(Time());
  KeyDate = GetSWIFTDate({CurDate});
  
  /* Записываем ключ сообщения - только если есть настройка */
  if( SetSign == TRUE )
    if( (РелизЭкспорт.Name != "920S") and (РелизЭкспорт.Name != "950S") )

       /* Конструируем объект сообщение */
       rsms = RsbMessage( wlmes.MesID );

       /* Формирование блока данных */
       if( not ФормированиеБлокаДанных(rsms, StrForSign))
          ErrExport( "Ошибка при формировании блока данных для ключевания сообщения" );
          return false;
       end;

       /* В целях экономии времени записываем в объект БЦИ, чтобы он повторно не рассчитывался */
       rsms.PIB = StrForSign; 

       /* Подписываем сообщение с использованием криптоплагина */
       if( not CryptoAPI.ExecCryptoAction( CONTEXTID_NEED_SIGN_SWSB, rsms ))
         ErrExport( String("Ошибка при расчете ключа сообщения", +GetErrMsg()) );
         return false;
       end;
       
       /* Определяем ключ */
       Signature = SubStr( CryptoAPI.GetLastSignature(), 1, LenKeyField );
       if( Signature == "" )
         ErrExport( "Ошибка при расчете ключа сообщения" );
         return false;
       end;
    end;
  end;

  if (not СохранитьБлок(КодНачалаБлока+НомерБлокаСлужебнойИнформацииСМФР+
                        КодРазделительНомера+КодВерсииФормата+
                        MesSubType+MesDestination+SndBankCode_SMFR+RcvBankCode_SMFR+
                        SndDate+SndTime+SndBankCode_SBRF+RcvBankCode_SBRF+KeyDate+
                        Signature+КодКонцаБлока))
    ErrExport("Ошибка при записи блока служебной информации СМФР"); 
    return false; 
  end;
/*------------------------------------------------------------------------------------------------*/
   else
      if (not СохранитьБлок(КодНачалаБлока+НомерБлокаСлужебнойИнформацииСМФР+
                            КодРазделительНомера+КодВерсииФормата+
                            SndBankCode_SBRF+RcvBankCode_SBRF+КодКонцаБлока))
        ErrExport("Ошибка при записи блока служебной информации СМФР"); 
        return false; 
      end;
   end;   

  return true;
end;

macro ЗаписатьДополнительныйТекстовыйБлок()
  if (not СохранитьСтроку(КодНачалаБлока+НомерДополнительногоТекстовогоБлока+КодРазделительНомера))
    ErrExport("Ошибка при записи Дополнительного Текстового Блока");
    return false;
  end;

  if (not СохранитьПоляСообщения(true))
    return false;
  end;
  
  if (not СохранитьБлок(КодКонцаТекстовогоБлока+КодКонцаБлока))
    ErrExport("Ошибка при записи Дополнительного Текстового Блока");
    return false;
  end;

  return true;
end;

macro ЗаписатьДополнительныйТекстовыйБлокКвитанции()
  if (not СохранитьБлок(КодНачалаБлока+НомерДополнительногоТекстовогоБлока+КодРазделительНомера
    + УникальныйНомерСообщенияСМФР + КодРазделительНомера + wlmes.TRN + КодКонцаБлока))
    ErrExport("Ошибка при записи Дополнительного Текстового Блока");
    return false;
  end;

  return true;

end;


macro ЗаписатьBasicHeaderBlock()
  var BankCode, Destination, BranchCode, НомерТерминала, НомерСессии, ISN;
  var error;

  BankCode = КодSWIFTПоУмолчанию;
  if((РелизЭкспорт.Name == "102N") 
      or ((РелизЭкспорт.Name == "202N") 
      and (КоличествоСообщенийПоУчетномуОбъектуСообщения( wlmes.MesID, OBJTYPE_PAYMENT)>1)))
      BankCode = ПолучитьКодСубъекта(wlmes.OutsideAbonentID, PTCK_SWIFT, error);
      if (error) 
         BankCode = КодSWIFTПоУмолчанию;
      end;
  end;
  Destination = SubStr(BankCode,1,Len_BIC_Destination);
  if (StrLen(Destination) != Len_BIC_Destination) 
    ErrExport("Неправильно указан Получатель сообщения (BIC)!"); 
    return false;
  end;
  BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);
  if ((BranchCode !="" ) AND (StrLen(BranchCode) != Len_BIC_BranchCode)) 
    ErrExport("Неправильно указан Получатель сообщения (код отделения)!"); 
    return false;
  end;
  if (BranchCode == "") 
    BranchCode = MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode); 
  end;

  НомерТерминала = НомерТерминалаПоУмолчанию;
  НомерСессии = НомерСессииПоУмолчанию;
  ISN = ISNПоУмолчанию;

  if (not СохранитьБлок(
                        КодНачалаБлока+НомерБлокаBasicHeader+КодРазделительНомера+
                        КодПриложенияFIN+НомерПриложенияFIN+Destination+НомерТерминала+
                        BranchCode+НомерСессии+ISN+КодКонцаБлока
                       )
    )
    ErrExport( "Ошибка при записи блока Basic Header" ); 
    return false; 
  end;
  
  return true;
end;

macro ЗаписатьApplicationHeaderBlockMOR()
  var BankCode, Destination, BranchCode, НомерФормы, НомерТерминала, НомерСессии, ISN;
  var ID, error, Приоритет;

  if (wlmes.InsideAbonentID)
    ID = wlmes.InsideAbonentID;
  else
    ID = {OurBank};
  end;

  if ( wlmes.Importance==0 )
     Приоритет = КодПриоритетНормальный;
  else
     Приоритет = КодПриоритетСрочный;
  end;

  BankCode = ПолучитьОткрытыйКодСубъекта(ID, PTCK_SWIFT, error);
  if (error)
    BankCode = КодSWIFTПоУмолчанию;
  end;
  Destination = SubStr(BankCode,1,Len_BIC_Destination);
  if (StrLen(Destination) != Len_BIC_Destination) 
    ErrExport("Неправильно указан Отправитель сообщения (BIC)!"); 
    return false; 
  end;
  BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);
  if ((BranchCode != "") AND (StrLen(BranchCode) != Len_BIC_BranchCode)) 
    ErrExport("Неправильно указан Отправитель сообщения (код отделения)!"); 
    return false; 
  end;
  if (BranchCode == "") 
    BranchCode=MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode); 
  end;

  НомерТерминала = НомерТерминалаПоУмолчанию;
  НомерСессии = НомерСессииПоУмолчанию;
  ISN = ISNПоУмолчанию;

  НомерФормы = ФормаЭкспорт.Name;

  if (not СохранитьБлок(
                        КодНачалаБлока+НомерБлокаApplicationHeader+КодРазделительНомера+
                        КодВходящего+НомерФормы+ВремяПоУмолчанию+ДатаПоУмолчанию+
                        Destination+НомерТерминала+BranchCode+НомерСессии+ISN+
                        ДатаПоУмолчанию+ВремяПоУмолчанию+КодПриоритетНормальный+КодКонцаБлока
                       )
    )
    ErrExport("Ошибка при записи блока Application Header");
    return false;
  end;

  return true;
end;

macro ЗаписатьUserHeaderBlock()
  if (not СохранитьБлок(
                        КодНачалаБлока+НомерБлокаUserHeader+КодРазделительНомера+
                        КодНачалаБлока+НомерПоляMUR+КодРазделительНомера+
                        ЗначениеMURПоУмолчанию+КодКонцаБлока+КодКонцаБлока
                       )
    )
    ErrExport("Ошибка при записи блока User Header");
    return false;
  end;

  return true;
end;

macro ЗаписатьTextBlock()
  if (not СохранитьСтроку(КодНачалаБлока+НомерБлокаText+КодРазделительНомера))
    ErrExport("Ошибка при записи текстового блока");
    return false;
  end;
  
  if (not СохранитьПоляСообщения())
    return false;
  end;

  if (not СохранитьБлок(КодКонцаТекстовогоБлока+КодКонцаБлока))
    ErrExport("Ошибка при записи текстового блока");
    return false;
  end;

  return true;
end;

macro СохранитьПоляКвитанции()
  var неуспешность:integer;
  var stat:bool = true;
  var строка:string;

  stat = stat And СохранитьБлок( КодНачалаБлока+НомерПоля177+КодРазделительНомера );
  строка = string(wlmes.sysdate)+string(wlmes.systime);
  строка = StrSubst ( строка, ".", "" );
  строка = StrSubst ( строка, ":", "" );
  строка = Substr   ( строка, 7, 2 ) + Substr ( строка, 3, 2 ) + Substr ( строка, 1, 2 ) + Substr ( строка, 7, 6 );
  stat = stat And СохранитьБлок( строка );
  stat = stat And СохранитьБлок( КодКонцаБлока );

  stat = stat And СохранитьБлок(КодНачалаБлока+НомерПоля451+КодРазделительНомера);
  неуспешность = "0";
  stat = stat And СохранитьБлок( неуспешность + КодКонцаБлока  );

  stat = stat And СохранитьБлок( КодНачалаБлока+НомерПоля405+КодРазделительНомера );
  if(неуспешность == "0")
     stat = stat And СохранитьБлок( "000"+ КодКонцаБлока );
  else
     /*Продолжение - следует... :) */
     return false;
  end;

  stat = stat And СохранитьБлок( КодНачалаБлока+НомерПоляMUR+КодРазделительНомера+
                                  ЗначениеMURПоУмолчанию+КодКонцаБлока );
  
  return stat;
end;

macro ЗаписатьTextBlockКвитанции()
  if (not СохранитьБлок(КодНачалаБлока+НомерБлокаText+КодРазделительНомера))
    ErrExport("Ошибка при записи текстового блока");
    return false;
  end;
  
  if (not СохранитьПоляКвитанции())
    ErrExport("Ошибка при записи текстового блока");
    return false;
  end;

  if (not СохранитьБлок(КодКонцаБлока))
    ErrExport("Ошибка при записи текстового блока");
    return false;
  end;

  return true;
end;

/* Формирование текстового сообщения */
macro ЗаписатьСообщение( SetSign )

  InitOut();

  if (not ЗаписатьБлокСлужебнойИнформацииСМФР( SetSign ))   return false; end;
   if(( wlmes.kind != этоКвитанция))
  if (not ЗаписатьДополнительныйТекстовыйБлок())            return false; end;
  if (not ЗаписатьBasicHeaderBlock()       )                return false; end;
  if (not ЗаписатьApplicationHeaderBlockMOR())              return false; end;
  if (not ЗаписатьUserHeaderBlock()        )                return false; end;
  if (not ЗаписатьTextBlock()              )                return false; end;
   else
      if (not ЗаписатьДополнительныйТекстовыйБлокКвитанции())   return false; end;
      if (not ЗаписатьBasicHeaderBlock()       )                return false; end;
      if (not ЗаписатьTextBlockКвитанции()     )                return false; end;
   end;                                                          
  if (not СохранитьИнформациюВФайле())                      return false; end;
  
  return true;

end;

/* Инструкция №518-4-р: cообщения в транспортном файле разделяются последовательностью символов CR\LF */
macro ЗаписатьРазделительСообщений()
  var count;

  if (not ЗаписатьБлок(SYMB_ENDL))
    ErrExport("Ошибка при записи разделителя сообщений");
    return false;
   end;

  return true;
end;

/* Макрос экспорта  */
macro SWIFTSBOutProc
  var continue0 = 1, Документов = 0, err, NeedSign, CryptoAPI = RsCryptoAPI();  
  
  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if (not СчитатьЗапись(wlmes, err))
    if (not err)
      ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;

  if(( wlmes.kind != этоКвитанция)) 
  /* Считываем один раз настройку необходимости ключевания сообщений */
  NeedSign = CryptoAPI.IsCryptoActionNeeded( CONTEXTID_NEED_SIGN_SWSB, 370 );
  else
     NeedSign = false;
  end;

  /* Последовательно считываем сообщения и формируем файл экспорта */
  while (continue0)
    if (not ПолучитьИнформациюПоСообщению())
      return false;
    end;

    ПрочитаноПервоеПоле = false;
    ПервоеПолеИмя = "";
    ПервоеПолеБуфер = "";

    if( not ЗаписатьСообщение( NeedSign ) )
      return false;
    end;

    if (СчитатьЗапись( wlmes, err, false ))
      ЗаписатьРазделительСообщений();
    else
      if (not err)
        continue0 = 0;
      else
        ErrExport("Ошибка чтения сообщения");
        return false;
      end;
    end;

    Документов = Документов + 1;
    message("Идет выгрузка документов. Отправлено: ", Документов);
  end;

  return true;
end;
