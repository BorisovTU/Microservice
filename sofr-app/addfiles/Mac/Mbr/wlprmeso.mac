/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*         Форма  печати "Транспортное сообщение"                           */
/*                                                                          */
/*  Имя файла: wlprmeso.mac                                                 */
/*  Создан:  9.04.03                                    Бердюкова И.И.      */
/****************************************************************************/

import prpm, wlglobal, wlprtool, "cb_sql.mac", oralib, likepy;  
FILE f_wlobjknd( wlobjknd ) key 0;

/******************************************************************************/
/* Функция получения "главного" сообщения платежа, т.е. того, на которое придет подтверждение */
/*******************************************************************************/
macro ПолучитьСообщениеПлатежа()
  var sqltmp, WlPmID;
  var Direct, OldKey, continue0, MesExist = false;
  var rs:object;
  var select:string;
  var params:TArray;

  
  Direct = "";
  OldKey = KeyNum(f_wlpm, 1);
  ClearRecord( f_wlpm );
  f_wlpm.PaymentID = pr_pmpaym.rec.PaymentID;
  f_wlpm.Direct = Direct;

  select = "select wlpm.t_WlPmID from dwlpm_dbt wlpm "+
           "where wlpm.t_PaymentID = :PaymentID and wlpm.t_Direct = chr(0)";
  params = makeArray( SQLParam("PaymentID", pr_pmpaym.rec.PaymentID) );
  rs = execSQLselect( select, params, FALSE );
  /*Ищем последнюю "шапку" платежа*/
  if( (continue0 = rs.MoveNext()) )
    while( continue0 )
      WlPmID = rs.value(0);
      continue0 = rs.MoveNext();
    end;
  else
    /* Если не найдена запись в файле заголовков платежей, */
    /* то платеж ещё не был выгружен и АРМ в МБР*/
    MsgBox("По платежу не было сформировано сообщение ");
    return 1;
  end;

  select ="select lnk.t_MesID from dwlmeslnk_dbt lnk "+
           "where lnk.t_ObjID =:WlPmID  and lnk.t_ObjKind =:OBJTYPE_PAYMENT and "+
                 "lnk.t_Direct =chr(0)";

  params = makeArray( SQLParam("WlPmID", WlPmID),
                      SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT) );
  rs = execSQLselect( select, params, FALSE );

  /*  Из всех сообщений связанных с платежом выбираем "главное"*/
  /*  (т.е. то на которое придет подтверждение)*/
  continue0 = rs.moveNext();
  while( continue0 )

    ClearRecord( f_wlmes );
    f_wlmes.MesID = rs.value(0);
    if( not GetEQ(f_wlmes) )
      MsgBox("Не найдена запись в файле сообщений");
      return 1;
    end;
                                  
    ClearRecord( f_wlmesrls );
    f_wlmesrls.RlsFormID = f_wlmes.RlsFormID;
    if( not GetEQ(f_wlmesrls) )
      MsgBox("Не найдена запись в файле релизов форм сообщений");
      return 1;
    end;
    
    ClearRecord( f_wlobjknd );
    f_wlobjknd.ObjID    = f_wlmesrls.FormID;
    f_wlobjknd.ObjKind  = OBJTYPE_FORM;
    f_wlobjknd.Type     = MESKIND_PAYMENT;
    f_wlobjknd.Number   = WLD_SUBKIND_PAYM_CLN;
    if( GetEQ(f_wlobjknd) )
      continue0 = false;
    else
      continue0 = rs.moveNext();
    end;    
    if( not MesExist ) 
      MesExist = true;
    end;
  end;

  if( MesExist )  
    copy(wlmes, f_wlmes);
  else
    MsgBox("Отсутствуют сообщения по платежу " );
    return 1;
  end;
end;

/************************************************/
/* Функция получения сообщения учетного объекта.*/
/* ТипДокумента - тип учетного объекта          */
/************************************************/
macro ПолучитьСообщениеДокумента( ТипДокумента )
  var ObjID, Direct, ObjKind, OldKey;
  var rs:object;
  var select:string;
  var params:TArray;

  OldKey = KeyNum(f_wlmeslnk, 1);

  ClearRecord(f_wlmeslnk);

  /* Выписки, уведомления */
  if( ТипДокумента == PRN_HEAD )    
    ObjID   = pr_wlhead.rec.HeadID;
    ObjKind = OBJTYPE_HEAD;
    Direct  = pr_wlhead.rec.Direct;

  /* Запросы/ответы */
  elif( ТипДокумента == PRN_REQ )    
    ObjID   = pr_wlreq.rec.ReqID;
    ObjKind = OBJTYPE_REQ;
    Direct  = pr_wlreq.rec.Direct;

  /* Подтверждения */
  elif( ТипДокумента == PRN_CONF )   
    ObjID   = pr_wlconf.rec.ConfID;
    ObjKind = OBJTYPE_CONF;
    Direct  = pr_wlconf.rec.Direct;

  /* Информационные сообщения*/
  elif( ТипДокумента == PRN_INFO )   
    ObjID   = pr_wlinfo.rec.InfoID;
    ObjKind = OBJTYPE_INFO;
    Direct  = pr_wlinfo.rec.Direct;

  /* Подтверждение сделок МБК */
  elif( ТипДокумента == PRN_DLMM )   
    ObjID   = pr_wldlmm.rec.DlMMID;
    ObjKind = OBJTYPE_DLMM;
    Direct  = pr_wldlmm.rec.Direct;

  else /* Платеж */ 
    ObjKind = OBJTYPE_PAYMENT;
    return ПолучитьСообщениеПлатежа();
  end;
    
  if( Direct == "X" )
    Direct = "'X'"
  else
    Direct = "chr(0)"
  end;

  if( ObjKind != OBJTYPE_PAYMENT ) /* Проверка на всякий случай :). Сообщение платежа находится по-другому в ПолучитьСообщениеПлатежа()*/

    select = "select lnk.t_MesID from dwlmeslnk_dbt lnk where "+
                            "lnk.t_ObjID =:ObjID and " +
                            "lnk.t_ObjKind =:ObjKind and "+
                            "lnk.t_Direct = " + Direct;
    params = makeArray( SQLParam("ObjID", ObjID),
                        SQLParam("ObjKind", ObjKind) );

    rs = execSQLselect( select, params, FALSE );

    if( rs.MoveNext() ) /* Найдена связь уч.объекта с сообщением */
      ClearRecord( f_wlmes );
      f_wlmes.MesID = rs.value(0);
      if( not GetEQ(f_wlmes) )       
        MsgBox("Не найдена запись в файле сообщений");
        return 1;
      end;
    end;
  end;
end;


/****************************************************************************************/
/* Функция печати формы "Транспортное сообщение" для самих сообщений и учетных объектов */
/* Находит сообщение и запускает реализованную на Си макрофункцию печати данного        */
/* сообщения "ПечатьСообщения".                                                         */
/****************************************************************************************/
macro PrintDocument(ncopy, DocKind)
  
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end; 

    
  if( ( DocKind != PRN_MES ) AND ( DocKind != PRN_CONVDEAL) )
    if (ПолучитьСообщениеДокумента( DocKind )) 
       return false;
    end;
    copy(wlmes, f_wlmes);
  else /* При печати сообщения, сообщение искать не надо :). Пользуемся заполненным буфером печати*/
    copy(wlmes, pr_wlmes);
  end;
            
  if( not ПечатьСообщения(wlmes, ncopy, OldName) )
    return false;
  end;

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  return true;
end;
