/*
  $Name:         ufgm512.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED512 транспорта УФЭБС
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения ED512 транспорта УФЭБС
//
//-----------------------------------------------------------------------------

import "ufgenmes.mac", BankInter, CTInter, "oralib.mac", MesInter, WldInter;

const
  SET_CHAR = "X";

macro СортироватьПользователей(buf1, buf2)
  if (buf1.rec.SWIFTReceiverCode == buf2.rec.SWIFTReceiverCode)
    if (buf1.rec.ED501Exchange == buf2.rec.ED501Exchange)
      if (buf1.rec.ED503Exchange == buf2.rec.ED503Exchange)
        return 0;
      elif (buf1.rec.ED503Exchange != SET_CHAR)
        return -1;
      end;
    elif (buf1.rec.ED501Exchange == SET_CHAR)
      return -1;
    end;
  elif (buf1.rec.SWIFTReceiverCode < buf2.rec.SWIFTReceiverCode)
    return -1;
  end;
  return 1;
end;

macro GenMes( addrMes, addrInfo )
  record wlmes(wlmes);
  record wlinfo(wlinfo);
  SetBuff( wlmes, addrMes );
  SetBuff( wlinfo, addrInfo );

  FillEDNoDateAuthorByRef_CompoundRls(wlmes.TRN);
  var Narrative: string = ПрочитатьТекстИнфСообщения(wlInfo);
  var uss = RsbUsSPFS( wlinfo.InfoID, OBJTYPE_INFO ).AsTArray();
  Qsort(uss, @СортироватьПользователей);
  var DictionUpdateMode = substr(trim(substr(Narrative, Index(Narrative, "DictionUpdateMode:") + 18)), 1, 1);
  
  StartBlockLogXML( "ChangeCOSDirDataInfo" );
  if(uss.size == 0)
    msgbox("Не заданы данные о пользователях СПФС, которые требуется передать в Банк России. Список для ввода данных вызывается по Alt+S");
    return false;
  end;
  var LastReceiver = uss[0].rec.SWIFTReceiverCode;
  var i = 0;
  while(i < uss.size)
    if (LastReceiver != uss[i].rec.SWIFTReceiverCode)
      LastReceiver = uss[i].rec.SWIFTReceiverCode;
    end;
    ЗаписатьПолеЛог( "DictionUpdateMode", DictionUpdateMode);
    var j = i;
    if (uss[i].rec.ED501Exchange == SET_CHAR)
      while((j < uss.size) and (uss[j].rec.ED501Exchange == SET_CHAR) and (LastReceiver == uss[j].rec.SWIFTReceiverCode))
        StartBlockLogXML( "Counteragent");
        ЗаписатьПолеЛог( "UIS", uss[j].rec.UISSenderCode);
        FinishBlockLogXML( "Counteragent" );
        j = j + 1;
      end;
      if (j == uss.size)
        j = uss.size - 1;
      end;
    end;
    //пропустим все у кого только ED501
    while ((i <= j) and (uss[i].rec.ED503Exchange != SET_CHAR))
      i = i + 1;
    end;
    if (i >= uss.size)
      break;
    end;
    j = i;
    if ((uss[j].rec.ED503Exchange == SET_CHAR) and (LastReceiver == uss[j].rec.SWIFTReceiverCode))
      StartBlockLogXML( "OURSWIFTBIC");
      ЗаписатьПолеЛог( "SWIFTBIC", LastReceiver);
      while ((j < uss.size) and (uss[j].rec.ED503Exchange == SET_CHAR) and (LastReceiver == uss[j].rec.SWIFTReceiverCode))
        StartBlockLogXML( "SenderSWIFTBIC");
        ЗаписатьПолеЛог( "UIS", uss[j].rec.UISSenderCode);
        ЗаписатьПолеЛог( "SWIFTBIC", uss[j].rec.SWIFTSenderCode);
        var full = trim(uss[j].rec.MessageType);
        var err = "", pos;
        while (full != "")
          pos = index(full, ",");
          var curr;
          if (pos > 0)
            curr = trim(substr(full, 1, pos - 1));
            full = substr(full, pos + 1);
          else
            curr = trim(full);
            full = "";
          end;
          StartBlockLogXML( "SWIFTTypeList");
          ЗаписатьПолеЛог( "SWIFTTypeNo", curr);
          FinishBlockLogXML( "SWIFTTypeList" );
        end;
        FinishBlockLogXML( "SenderSWIFTBIC" );
        j = j + 1;
      end;
      FinishBlockLogXML( "OURSWIFTBIC" );
      if (j == uss.size)
        j = uss.size - 1;
      end;
    end;
    if (LastReceiver != uss[j].rec.SWIFTReceiverCode)
      i = j;
    else
      i = j + 1;
    end;
  end;
  FinishBlockLogXML( "ChangeCOSDirDataInfo" );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro CheckObj(addrInfo)

  record wlInfo (wlinfo);
  SetBuff( wlInfo, addrInfo );
  var Narrative: string = ПрочитатьТекстИнфСообщения(wlInfo);
  var pos = Index(Narrative, "DictionUpdateMode:");
  if (pos > 0)
    var mode:string =trim(substr(Narrative, pos + 18));
    if((mode == "1") or (mode == "2"))
      pos = 1;
    else
      pos = -1;
    end;
  end;
  var Errors = TArray();
  if ((pos < 0) or (pos == 0))
    Errors[Errors.size] = "Недопустимый тип модификации справочника. Допускаются значения 1 или 2. В поле информационного сообщения \"Текст сообщения\" выберите тип модификации по клавише F3";
  end;
  var uss = RsbUsSPFS( wlinfo.InfoID, OBJTYPE_INFO ).AsTArray();
  var i = 0;

  while (i < uss.size)
    if ((not StrIsNumber(uss[i].rec.UISSenderCode)) or (strlen(uss[i].rec.UISSenderCode) != 10))
      Errors[Errors.size] = "Некорректный УИС отправителя " + uss[i].rec.UISSenderCode;
    end;
    if (uss[i].rec.ED503Exchange)
      if (not InList(strlen(uss[i].rec.SWIFTSenderCode), 8, 11))
        Errors[Errors.size] = "Некорректный BIC SWIFT отправителя " + uss[i].rec.SWIFTSenderCode;
      end;
      if ((uss[i].rec.UISSenderCode == "9999999999") and (uss[i].rec.SWIFTSenderCode != "ZZZZRUZZZZZ"))
        Errors[Errors.size] = "Некорректный BIC SWIFT отправителя. При заданном значении УИС следует указать ZZZZRUZZZZZ";
      elif (uss[i].rec.SWIFTSenderCode == "ZZZZRUZZZZZ")
        Errors[Errors.size] = "Некорректный УИС отправителя. При заданном значении BIC SWIFT следует указать 9999999999";
      end;
      var full = trim(uss[i].rec.MessageType);
      var err = "";
      while (full != "")
        pos = index(full, ",");
        var curr;
        if (pos > 0)
          curr = trim(substr(full, 1, pos - 1));
          full = substr(full, pos + 1);
        else
          curr = trim(full);
          full = "";
        end;
        if (strlen(curr) > 5)
          err = err + ", " + curr;
        end;
      end;
      if (err != "")
        Errors[Errors.size] = "Некорректный тип(ы) сообщения SWIFT " + substr(err, 3);
      end;
    end;
    i = i + 1;
  end;

  if (Errors.size > 0)
    MsgBox(join(Errors, "\n"));
    return false;
  end;

  return true;

  OnError(er) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
