/*
$Name:             fnsgdroox.mac
$Module:           МБ Расчеты
$Description:      Генерация учетного объекта по сообщению формы ROO (XML)
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация учетного объекта по сообщению формы ROO (XML)
//
// Программист  : Чукина Т.А.
//
// Создан       : 19.06.2014
//
//-----------------------------------------------------------------------------

import "fnsRegDecAl_RP.mac";

private class (TRegDecision_RP) TROO
  InitTRegDecision_RP();

  var ВидРеш : string = "",
      НомРешПр : string = "",
      ДатаРешПр  : string = "";

  // Для переопределения в классах конкретных видов решений
  macro GetDocTypeName : string
    return "решение об отмене приостановления операций";
  end;

  private macro SaveFieldsRlsSpecific
  ( fieldname : string, 
    fieldvalue : string, 
    blockname : string
  )
    // вызов метода родительского класса TRegDecision_RP
    SaveFieldsRlsSpecific(fieldname, fieldvalue, blockname);

    // сохранение полей релиза ROO
    if(fieldname == "НомРешОт")
      DecisionNumber = fieldvalue;
    elif(fieldname == "ДатаРешОт")
      DecisionDate = fieldvalue;
    elif(fieldname == "ВидРеш")
      ВидРеш = fieldvalue;
    elif(fieldname == "НомРешПр")
      НомРешПр = fieldvalue;
    elif(fieldname == "ДатаРешПр")
      ДатаРешПр = fieldvalue;
    end;
  end;

  private macro FillRgdBuf(wlmes : StrucRef, wlregdec : StrucRef)
    // Вызов метода родительского класса TRegDecision_RP
    FillRgdBuf(wlmes, wlregdec);

    // Специфика ROO
    wlregdec.Type = WLD_TYPE_REGDEC_ROO;
    wlregdec.Kind = int(ВидРеш);
    wlregdec.LnkDocNumber = НомРешПр;
    wlregdec.LnkDocDate = ToDateYYYY_MM_DD(ДатаРешПр);
    wlregdec.Ground = GetWlcodeDescription(TYPE_CODE_FNS_CODEDEC2, КодОснов);
  end;
end;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2, "Генерация учетного объекта по сообщению ROO");

  var ROO : TROO = TROO();

  return GenRegDecision(wlmes, ROO);

  OnError(er)
    ExeptionMessage(er);
    return FALSE;
end;
