/*
$Name: swgm103.mac
$Module: Межбанковские расчеты
$Description: Генерация сообщения по SWIFT MT103
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT103                                       */
/*                                                                          */
/*  Имя файла: swgm103.mac                                                  */
/*  Создан:  21.01.00                                        Алешин А.В.    */
/****************************************************************************/

import "swgenmes.mac";

record Party(party);
const  Relise = "103";

macro GenMes( addrMes, addrPm ,addrOldMes )
  var field_value, Tag, IsTransit, BaseAmount, CurrentCode, error, 
      continue0, field33BPresent = 0, fld33B, fld32A;
  var ОстатокПоля70;
  var Recreate = false;
  var RsPaym;
  var ReceiverSwiftCode = "";
  RECORD combuf( oprsfcom );    
  Record oldwlMes (wlmes);
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( oldwlmes,  addrOldMes );
     Recreate = true;
  end;

  PrintLog(2,"Генерация сообщения по МТ103");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  IsTransit = (((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)) OR PM_PaymentIsChildTransit( RsPaym.PaymentID ));  
  var InMesID = 0;
  if(IsTransit)
    InMesID = getInMesIDByPayment(RsPaym);
  end;

  var CommissCode = GetCommisCode (RsPaym.ComissCharges);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 23B       */
  ЗаписатьПолеЛог( BankOperationCodeField_B, BANKOPERCODE_CRED ) ;

  /* 23Е   берем теперь из dwlpmrmprop_dbt.InstructionCode  */
  StrChangeRusXSet( RsPaym.InstructionCode, field_value );
  ЗаписатьПолеЛогВБлок( "23E", InstructionCodeField, string(field_value), NULL, Recreate, OldwlMes);
  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  
  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  fld32A = field_value;
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );

  /* 33B - Инструктируемая сумма, валюта */
  fld33B = WriteField33B( RsPaym, InMesID, CommissCode, Recreate, OldwlMes );

  /* 36  - Кросс-курс */
  WriteField36( IsTransit, InMesID, fld32A, fld33B, Recreate, OldwlMes );

  /* 50A - приказодатель */
  field_value = FillOrderingCustomerField103( RsPaym, Tag );
  if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден плательщик" );
  else
    ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField + Tag, field_value,NULL,Recreate, OldwlMes );
  end;

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionField(RsPaym, Tag, 0, "103" );
  ЗаписатьПолеВБлокКонтекст0( OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes);

  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  /* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_A;
  field_value = FillSenderCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", Trim(field_value), Recreate, OldwlMes );/*PNV i-support 488816*/

  /* 54a - Корреспондент получателя */
  Tag = SWIFT_Tag_A;
  field_value = FillReceiverCorrespondentField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField+Tag, "54a", field_value, Recreate, OldwlMes );

  /* 55a - Третий рамбурсирующий посрдник */
  Tag = SWIFT_Tag_A;
  field_value = FillThirdReibursBankField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( ThirdReimbursementInstitutionField+Tag, "55a", field_value, Recreate, OldwlMes );    

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField(RsPaym, Tag, null, "MT103" );
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value, Recreate, OldwlMes );  

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField(RsPaym, Tag , null , "MT103");
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );      

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCopyExternalFld(RsPaym);
  if (field_value != "")
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes );   
  end;
  if (field_value == "")
    if((RsPaym.Receiver <= 0) and (RsPaym.ReceiverCode !="" and RsPaym.ReceiverCodeKind ))
      field_value = FillBeneficiaryCustomerField_A( RsPaym );
      if(field_value != "")
        ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_A, field_value, NULL,Recreate, OldwlMes );   
      end;
    elif(RsPaym.Receiver > 0)
      ReceiverSwiftCode = ПолучитьКодСубъекта(RsPaym.Receiver, PTCK_SWIFT, error);
      if((error == 0 ) and (ReceiverSwiftCode != "") )
        field_value = FillBeneficiaryCustomerField_A( RsPaym );
        if(field_value != "")
          ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_A, field_value, NULL,Recreate, OldwlMes );   
        end;
      else
        field_value = FillBeneficiaryCustomerField(RsPaym);/*PNV*/   
        if(field_value != "")
          ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes );/*PNV*/   
        end;  
      end;
    else
      field_value = FillBeneficiaryCustomerField( RsPaym );
      if ( field_value!="" )
        ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes ); 
      end;
    end;       
  end;

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 70  - информация для бенефициара */
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70 );
  WriteFieldEx  (DetailsOfPaymentField, field_value, Recreate, OldwlMes );

  /*  71 */
  field_value = CommissCode;
    WriteFieldEx  (DetailsOfChargesField_A, field_value, Recreate, OldwlMes );

  ЗаписатьКомиссииПлатежа( RsPaym, field_value, TRUE );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT103_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_STANDART, ОстатокПоля70 );
  if( field_value != "" )
    WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

