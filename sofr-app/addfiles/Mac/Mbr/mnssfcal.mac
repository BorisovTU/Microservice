/*
  $Name:         mnssfcal.mac
  $Module:       МБР
  $Description:  Функции для сообщений SFC
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для сообщений SFC0, SFC1, SFC2
//
// Программист  : Чукина Т.А.
//
// Создан       : 09.07.2014
//
//-----------------------------------------------------------------------------

import oralib, likepy, "xmlmestools.mac", "fnsgenmesx.mac", "fnsaccmsg_lib.mac",
       CTInter;

private const UNKNOWN_DATE : date = date(11, 11, 1111);

private macro GetBirthInfoByReq
( req, 
  IsOld : bool,
  BirthDate : @date, 
  BirthPlace : @string
)
  macro ReqFld(FieldName : string) : variant
    return GetReqClientFld(req, FieldName, IsOld);
  end;

  BirthDate = ReqFld("ClientBirthDate");
  BirthPlace = ReqFld("ClientBirthPlace");
end;

private macro GetPaperInfoByReq
( req, 
  IsOld : bool,
  PaperCode : @string,
  PaperSerNum : @string,
  PaperDate : @date
)
  macro ReqFld(FieldName : string) : variant
    return GetReqClientFld(req, FieldName, IsOld);
  end;

  PaperCode = ReqFld("ClientCardType");
  PaperSerNum = ReqFld("ClientCardNum");
  PaperDate = ReqFld("ClientCardDate");
end;

// Физическое лицо, не являющееся предпринимателем
// true - удалось заполнить поля, false - не удалось
macro WriteIndividNotEmployer
( PartyID : integer, 
  BlockName : string, 
  AccMsgKind : integer,
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa,
  IsOld : bool
) : bool

  macro ReqFld(FieldName : string) : variant
    if( reqchnga != null )
      return GetReqClientFld(reqchnga, FieldName, IsOld);
    elif( reqopena != null)
      return GetReqClientFld(reqopena, FieldName);
    elif( reqclosa != null)
      return GetReqClientFld(reqclosa, FieldName);
    end;
  end;

  macro GetReq()
    if( reqchnga != null )
      return reqchnga;
    elif( reqopena != null)
      return reqopena;
    elif( reqclosa != null)
      return reqclosa;
    end;
  end;


  macro GetCorrectBirthDate(BirthDate : date) : string
    if(BirthDate == date(0, 0, 0))
      if(AccMsgKind == ACCMSG_KIND_OPEN)
        RunError("Не указана дата рождения клиента, которому открывается счет");
      else
        BirthDate = UNKNOWN_DATE;
      end;
    end;

    return DDpMMpYYYY(BirthDate);
  end;

  var INN : string = "", BirthDate : date = date(0, 0, 0), BirthPlace : string = "",
      PaperCode : string = "", PaperSerNum : string = "", PaperDate : date = date(0, 0, 0),
      SurName : string = "", Name : string = "", ParentName : string = "";

  // get and check data
  if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg, reqchnga, reqopena, reqclosa, IsOld) )
    return FALSE;
  end;

  var FullName : string = SurName + " " + Name + " " + ParentName;

  // ИНН
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    INN = RemoveKPP( ReqFld("ClientINN") );
  else
    INN = GetOnlyINN(PartyID);
  end;
  if(INN and not IsGoodINN_Ind(INN))
    ErrMsg = "Неверное значение ИНН: " + INN;
    return FALSE;
  end;

  // Дата и место рождения
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    GetBirthInfoByReq( GetReq(), IsOld, @BirthDate, @BirthPlace );
  elif( not GetBirthInfo(PartyID, @BirthDate, @BirthPlace) )
    ErrMsg = "Не найдены данные физического лица" + FullName;
    return FALSE;
  end;
  if(not BirthPlace)
    BirthPlace = "Неизвестно";
  end;

  // Документ, удостоверяющий личность
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    GetPaperInfoByReq( GetReq(), IsOld, @PaperCode, @PaperSerNum, @PaperDate );
  else
    GetPaperInfo(PartyID, @PaperCode, @PaperSerNum, @PaperDate);
  end;
  if(not PaperCode)
    if(AccMsgKind == ACCMSG_KIND_OPEN)
      ErrMsg = "Не найден документ, удостоверяющий личность физ.лица " + FullName;
      return FALSE;
    else
      PaperCode = "91";
    end;
  end;
  if(not PaperSerNum)
    if(AccMsgKind == ACCMSG_KIND_OPEN)
      ErrMsg = "Для клиента " + FullName + " не заданы серия и/или номер документа, удостоверяющего личность";
      return FALSE;
    else
      PaperSerNum = "Неизвестно";
    end;
  end;
  if(PaperDate == date(0,0,0))
    if( InList(PaperCode, "14", "91") and (AccMsgKind != ACCMSG_KIND_OPEN) )
      PaperDate = UNKNOWN_DATE;
    else
      ErrMsg = "Не указана дата выдачи документа, удостоверяющего личность физ.лица " + FullName;
      return FALSE;
    end;
  end;

  // write
  StartBlockLogXML(BlockName);

  if(INN)
    ЗаписатьПолеЛог("ИННФЛ", INN);
  end;

  ЗаписатьПолеЛог("ДатаРожд", GetCorrectBirthDate(BirthDate) );
  ЗаписатьПолеЛог("МестоРожд", BirthPlace);
  ЗаписатьПолеЛог("КодДУЛ", PaperCode);
  ЗаписатьПолеЛог("СерНомДок", PaperSerNum);
  ЗаписатьПолеЛог("ДатаДок", DDpMMpYYYY(PaperDate));

  WriteFIO("ФИОФЛ", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

  return TRUE;
end;

private macro WriteEntityCode(ClientID : integer, ClientType311 : string)
/*
  var LegalForm : integer = PTLEGF_ALL,
      IsIndividEmployer : bool = false,
      NotResident : string = "",
      AttrPartyType : integer = 0; // значение категории "Тип субъекта"

  if( ClientID > 0 )
    if( not GetTaxPayerPartyData(ClientID, @LegalForm, @IsIndividEmployer, @AttrPartyType, @NotResident) )
      RunError("Не найден субъект " + ClientID);
    end;
  end;

  EntityCode = GetEntityCodeFld
    ( LegalForm, 
      NotResident, 
      IsIndividEmployer, 
      AttrPartyType, 
      ClientType311 );
*/
  ЗаписатьПолеЛог("КодЛица", "7");
end;

// Сведения о налогоплательщике (СвНП)
macro WriteTaxPayerInfo_Ind
( ClientID : integer, 
  ClientType311 : string,
  AccMsgKind : integer,
  reqchnga,
  reqopena,
  reqclosa,
  IsOld : bool
)

  var BlockName : string = "СвНП", NPFLBlockName : string = "НПФЛ", ErrMsg : string = "";
  if(IsOld)
    BlockName = "СвНПСтар";
    NPFLBlockName = "НПФЛСтар";
  end;

  StartBlockLogXML(BlockName);

  if(IsOld and (reqchnga != NULL))
    WriteFieldWithCheckAnnul( "ДатаИзмФЛ", DDpMMpYYYY(reqchnga.ClientChangeDate) );
  else
    WriteEntityCode(ClientID, ClientType311);
  end;

  if( not WriteIndividNotEmployer(ClientID, NPFLBlockName, AccMsgKind, @ErrMsg, reqchnga, reqopena, reqclosa, IsOld) )
    RunError(ErrMsg);
  end;

  FinishBlockLogXML(BlockName);
end;

// атрибуты элемента СвСчет
private macro WriteAccInfoAttrs(buff, AccMsgKind : integer)
  WriteFieldWithCheckAnnul( "НомСч", buff.Account );
  ЗаписатьПолеЛог( "ДатаОткрСч", GetAccOpenDateFldVal_BO(buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВидСч", GetAccKindFld_BO(buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВалСч", GetAccCurFldVal(buff.Code_Currency) );
end;

// СвСчет
macro WriteAccInfo_Ind(buff, AccMsgKind : integer, WriteAccInfoOpenClose : ProcRef)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  WriteAccInfoAttrs(buff, AccMsgKind);

  // Файл\Документ\СвСчет\(Открыт|Закрыт)
  ExecMacro2(WriteAccInfoOpenClose, buff);

  FinishBlockLogXML("СвСчет");
end;

private macro CheckIDC( Req, kind : integer )
  var PaperCode : string = "", PaperSerNum : string = "", PaperDate : date = date(0,0,0);
  var ErrMsg : string = "";

  if(kind == ACCMSG_KIND_OPEN)
    GetPaperInfoByReq(Req, null, @PaperCode, @PaperSerNum, @PaperDate);
    var ClientName : string = Req.ClientName;

    if(not PaperCode)
      ErrMsg = "Для клиента " + ClientName + " не задан код документа, удостоверяющего личность";
    elif( strlen(PaperCode) != 2 )
      ErrMsg = "Для клиента " + ClientName + " задан неверный код документа, удостоверяющего личность"
    elif(not PaperSerNum)
      ErrMsg = "Для клиента " + ClientName + " не заданы серия и/или номер документа, удостоверяющего личность";
    elif( (PaperCode != "14") and (PaperDate == date(0,0,0)) )
      ErrMsg = "Для " + ClientName + " не задана дата выдачи документа, удостоверяющего личность";
    end;

    if(ErrMsg)
      RunError(ErrMsg, ErrMsg);
    end;
  end;
end;

private macro CheckBirthInfo(Req, kind : integer)
  var BirthDate : date = date(0, 0, 0), BirthPlace : string = "";
  var ErrMsg : string = "";

  if(kind == ACCMSG_KIND_OPEN)
    GetBirthInfoByReq( Req, null, @BirthDate, @BirthPlace );
    if( not BirthPlace or (BirthDate == date(0,0,0)) )
      ErrMsg = "Не заданы дата и/или место рождения клиента. Для сообщения об открытии счета эти реквизиты должны быть обязательно заполнены";
    end;
  end;

  if(ErrMsg)
    RunError(ErrMsg, ErrMsg);
  end;
end;

macro MNS_CheckObjInd( Req, kind : integer )

  /*if( not MNS_CheckObj_Common(Req, kind, @ClientID, @КодНОБ) )
    return FALSE;
  end;*/

  CheckIDC(Req, kind);
  CheckBirthInfo(Req, kind);

  var MesSender : integer = GetDprtPartyID( Req.Department );
  if(MesSender <= 0)
    RunError("Не найден связанный субъект филиала заявления");
  end;

  var КодНОБ : string = GetCodeNOBWithChecks(MesSender);

  var Bank : TBankInfoAccMsg = TBankInfoAccMsg();
  var ErrMsg : string = "";
  if( not Bank.Fill(MesSender, @ErrMsg) )
    RsbThrow(ErrMsg);
  end;
  var CorrectMesObj : TCorrectMesFNS = TCorrectMesFNS();
  CorrectMesObj.КодНОБ = КодНОБ;
  CorrectMesObj.ИННКО = Bank.ИННКО;
  CorrectMesObj.РегНом = Bank.РегНом;
  CorrectMesObj.НомФ = Bank.НомФ;
  if( not CorrectMesObj.Check1() )
    return FALSE;
  end;

  return TRUE;

  OnError(er)
    ExeptionMessage(er);
    return FALSE;

end;
