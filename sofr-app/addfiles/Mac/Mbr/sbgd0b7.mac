/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация инф. сообщ. по сообщениям SBRF3 сигнатура 0B7, 0BF             */
/*                                                                          */
/*  Имя файла: sbgd0b7.mac                                                  */
/*  Создан:    08.08.00                                      Бабин А.П.     */
/****************************************************************************/

import "sbgendoc.mac";

class InfoMessage
 var
  Originator      :SbBank,      /* Участник - Создатель */
  Recipient       :SbBank,      /* Участник - Получатель  */    
  Payer           :SbBank,      /* Банк-плательщик */    
  Receiver        :SbBank,      /* Банк-получатель */    
  TypeOper        :string,      /* Тип операции */
  Val_MP          :string,      /* Информация Получателю */
  Val_PP          :string,      /* Информация Получателю */
  Val_PN          :string,      /* Информация Получателю */
  Val_ME          :string,      /* Информация Получателю */
  TypeDocument    :string;      /* Тип документа */   
  
  Val_MP = "";
  Val_PP = "";
  Val_PN = "";
  Val_ME = "";
  TypeOper = "";
  TypeDocument = "";
end; /* class InfoMessage */
                     
var Inf : InfoMessage;

/* Разбор полей сообщения */
macro ToMakeInfMessage( IsPIB )
  var field_name, field_value, result = TRUE, CodeKind, SBRFStr = "";

  /* Формируем строку сообщения СБРФ */
  if( not SB_MakeSBRF3Message( IsPIB, SBRFStr ) )
    return false;
  end;

  /* Проверяем ЭЦП входящего сообщения (кроме сообщений свободного формата по форме 0BF) */
  if( f_wlmesfrm.Name != sign_0BF )
    if( not SBCheckKey( SBRFStr ) )
      return false;
    end;
  end;

  /* Последовательно считываем и обрабатываем поля */
  while( result AND (SB_СчитатьПолеИзСообщения( field_name, field_value, SBRFStr) == 0) )

    if(   field_name == "DT" )   /* Тип документа */
      Inf.TypeDocument = field_value;
    elif( field_name == "PA" ) /* Участник - создатель */
      Inf.Originator.CodeBank = field_value;
      Inf.Originator.CodeKind = PTCK_CLIRING;
    elif( field_name == "RC" ) /* Участник получатель */
      Inf.Recipient.CodeBank = field_value;
      Inf.Recipient.CodeKind = PTCK_CLIRING;    
    elif( field_name == "SB" )   /* Наименование банка плательщика */
      Inf.Payer.Name = field_value;
    elif( field_name == "SN" )   /* Код банка плательщика */
      Inf.Payer.CodeBank = field_value;
    elif( field_name == "SK" )   /* Доп. код банка плательщика */
      Inf.Payer.Account = field_value;
    elif( field_name == "SS" )   /* Код расчетной системы банка плательщика */
      CodeKind = ПолучитьВидКодаПоSBRF3( field_value );
      if( CodeKind )
        Inf.Payer.CodeKind = CodeKind;
      else
        std.msg( "|Не определен вид кода банка плательщика по коду расчетной системы: " + field_value );
        result = FALSE;
      end;
    elif( field_name == "BN" )   /* Наименование банка получателя  */
      Inf.Receiver.Name = field_value;
    elif( field_name == "BC" )   /* Код банка получателя */
      Inf.Receiver.CodeBank = field_value;
    elif( field_name == "BK" )   /* Доп. код банка получателя */
      Inf.Receiver.Account = field_value;
    elif( field_name == "RS" )   /* Код расчетной системы банка получателя */
      CodeKind = ПолучитьВидКодаПоSBRF3( field_value );
      if( CodeKind )
        Inf.Receiver.CodeKind = CodeKind;
      else
        std.msg( "|Не определен вид кода банка получателя по коду расчетной системы: " + field_value );
        result = FALSE;
      end;
    elif( field_name == "MP" ) /* Информация участника */
      Inf.Val_MP = field_value;
    elif( field_name == "PP" ) /* Назначение платежа */
      Inf.Val_PP = field_value;
    elif( field_name == "PN" ) /* Информация участника */
      Inf.Val_PN = field_value;
    elif( field_name == "ME" ) /* Дополнительная информация */
      Inf.Val_ME = field_value;
    elif( field_name == "MT" ) /* Условия перевода */
      Inf.TypeOper = field_value;    
    end;
  end;

  if( not result )
    return result;
  end;

  /* Участник - Создатель */
  Inf.Originator.SetBank();
  
  /* Участник - Получатель */
  Inf.Recipient.SetBank();
  
  /* Банк-Платедьщик */
  Inf.Payer.SetBank();
  
  /* Банк-Получатель */
  Inf.Receiver.SetBank();

  /* Тип документа */
  if( Inf.TypeDocument == "" )
    if( f_wlmesfrm.Name == sign_0B7 ) /* Сообщение участнику */
      Inf.TypeDocument = type_007;
    elif( f_wlmesfrm.Name == sign_0BF ) /* Сообщение свободного формата */
      Inf.TypeDocument = type_070;    
    end;
  end;

  return result;
end; /* ToMakeInfMessage */

/* Генерация информационного сообщения по сообщениям СБРФ */
macro GenDocExec( IsPIB ) 
  Inf = InfoMessage();
  ClearRecord(wlinfo);

  /* Предварительное заполнение класса InfoMessage на отснове ответного сообщения */
  if( not ToMakeInfMessage( IsPIB ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  WlInfo.TRN      = WlMes.TRN;
  WlInfo.TypeOper = Inf.TypeOper;

  /* Создатель информационного сообщения */
  if( Inf.Originator.IsSet() )       
    WlInfo.OriginatorID        = Inf.Originator.PartyID;
    WlInfo.OriginatorCodeKind  = Inf.Originator.CodeKind;
    WlInfo.OriginatorCode      = Inf.Originator.CodeBank;
    /* Если банк-плательщик совпадает участником-создателем, копируем название */
    /* Иначе подкачиваем из справочника */
    if( Inf.Originator.PartyID == Inf.Payer.PartyID )
      WlInfo.OriginatorName    = Inf.Payer.Name;
    else
      if( ПолучитьСубъекта( Inf.Originator.PartyID, wlparty ) == 0 )
        WlInfo.OriginatorName  = wlparty.Name;
      end;
    end;
    
  end;

  /* Получатель информационного сообщения */
  if( Inf.Recipient.IsSet() )       
    WlInfo.RecipientID       = Inf.Recipient.PartyID;
    WlInfo.RecipientCodeKind = Inf.Recipient.CodeKind;
    WlInfo.RecipientCode     = Inf.Recipient.CodeBank;
    /* Если банк-плательщик совпадает участником-создателем, копируем название */
    /* Иначе подкачиваем из справочника */
    if( Inf.Recipient.PartyID == Inf.Receiver.PartyID )
      WlInfo.RecipientName     = Inf.Receiver.Name;
    else
      if( ПолучитьСубъекта( Inf.Recipient.PartyID, wlparty ) == 0 )
        WlInfo.RecipientName  = wlparty.Name;
      end;
    end;
  end;

  /* Внутренннее подразделение, определяется в макросе импорта, берем из сообщения */
  WlInfo.OfficeID       = wlmes.InsideAbonentID;
  WlInfo.OfficeCodeKind = wlmes.InsideAbonentCodeKind;
  WlInfo.OfficeCode     = wlmes.InsideAbonentCode;

  /* Для сообщений 0B7 информацию из полей MP, PP, PN */
  /* копируем в основной текст телеграммы  (не в примечания!)*/
  if( (Inf.TypeDocument == type_007) AND (Inf.Val_ME == "") )
    Inf.Val_ME = Inf.Val_MP + Inf.Val_PP + Inf.Val_PN;
  end;

  if( not ВставитьИнфСообщение( wlinfo, Inf.Val_ME ) )
    std.msg("Ошибка при вводе информационного сообщения");
    return FALSE;
  end;
      
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;

/* Релиз 0B7 */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация информационного сообщения по форме 0B7");

  return GenDocExec( false );
end;

/* Релиз 0B7J */
macro GenDocPIB( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация информационного сообщения по форме 0B7J");

  return GenDocExec( true );
end;