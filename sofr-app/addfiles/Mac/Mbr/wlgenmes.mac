/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*         Общие вспомогательные функции для генерации сообщений            */
/*                                                                          */
/*  Имя файла: wlgenmes.mac                                                 */
/*  Создан:  20.07.01                                        Алешин А.В.    */
/****************************************************************************/

import "wlglobal.mac", oralib, "wltools.mac", "bnk_ptlib.mac";

macro ПолучитьУзелОсновнойИнструкцииПлатежа( PaymentID, Direct, route )
  if ( valtype(route)!=V_UNDEF )
     return ПолучитьУзелОсновнойИнструкции( OBJTYPE_PAYMENT, PaymentID, Direct, route );
  else
     return ПолучитьУзелОсновнойИнструкции( OBJTYPE_PAYMENT, PaymentID, Direct );
  end;
end;

macro ПолучитьСамыйДальнийУзелПлатежа( PaymentID, Direct, route )
  if ( valtype(route)!=V_UNDEF )
     return ПолучитьСамыйДальнийУзел( OBJTYPE_PAYMENT, PaymentID, Direct, route );
  else
     return ПолучитьСамыйДальнийУзел( OBJTYPE_PAYMENT, PaymentID, Direct );
  end;
end;

macro ПолучитьБлижайшийУзелПлатежа( PaymentID, Direct, route )
  if ( valtype(route)!=V_UNDEF )
     return ПолучитьБлижайшийУзел( OBJTYPE_PAYMENT, PaymentID, Direct, route );
  else
     return ПолучитьБлижайшийУзел( OBJTYPE_PAYMENT, PaymentID, Direct );
  end;
end;

macro ПолучитьУзелОтправителя( ObjKind, ObjID, route )
  var stat;
  RECORD snd( pmroute );
  stat = ПолучитьУзелОсновнойИнструкции( ObjKind, ObjID, RTDIR_IN, snd );
  if ( not stat )
     stat = ПолучитьБлижайшийУзел( ObjKind, ObjID, RTDIR_IN, snd );
  end;

  if ( stat AND (valtype(route)!=V_UNDEF) )
     copy( route, snd );
  end;

  return stat;
end;

macro ПолучитьУзелПолучателя( ObjKind, ObjID, route )
  var stat;
  RECORD rcv( pmroute );
  stat = ПолучитьУзелОсновнойИнструкции( ObjKind, ObjID, RTDIR_OUT, rcv );
  if ( not stat )
     stat = ПолучитьБлижайшийУзел( ObjKind, ObjID, RTDIR_OUT, rcv );
  end;

  if ( stat AND (valtype(route)!=V_UNDEF) )
     copy( route, rcv );
  end;

  return stat;
end;

macro ПолучитьУзелОтправителяПлатежа( PaymentID, route )
   return ПолучитьУзелОтправителя( OBJTYPE_PAYMENT, PaymentID, route );
end;

macro ПолучитьУзелПолучателяПлатежа( PaymentID, route )
   return ПолучитьУзелПолучателя( OBJTYPE_PAYMENT, PaymentID, route );
end;

/*  Записать поле сообщения с формированием лога */
macro ЗаписатьПолеЛог( field_name, field_value, format )
  var fv_copy;

  if( ValType(format) AND format )
    if ( strlen(field_value)>1000 )
       fv_copy = field_value;
       field_value = "";
       while( fv_copy!="" )
           field_value = field_value + StrSubst( substr(fv_copy,1,1000), "\n", " " );
           fv_copy = substr( fv_copy, 1001 );
       end;
    else
       field_value = StrSubst( field_value, "\n", " " );
    end;
  end;
  if( not ЗаписатьПоле( field_name, field_value ) )
    RunError( "|Невозможно записать поле " + field_name );
  end;
  return true;
end;

/* Попытка установки контекста блока */
macro УстановитьКонтекстБлокаЛог( BlockName )
  if( not УстановитьКонтекстБлока( BlockName ) )
    RunError( "|Ошибка при установке контекста блока " + BlockName );
  end;
end;

/* Получить наименование кода валюты с формированием лога */
macro ПолучитьКодВалютыЛог( FIID )
  f_wlfininstr.FIID = FIID;
  if( not getEQ( f_wlfininstr ) )
    RunError( "|Код финансового инструмента "+FIID+" не найден в справочнике" );
  end;
  return f_wlfininstr.Ccy;
end;

macro GetBlockEdit( field:string, RlsFormID:integer ):bool
  var select:string = "SELECT mesfld.t_BlockOnEdit "
                    + "FROM dwlmesfld_dbt mesfld, "
                    +      "dwltpfld_dbt tpfld "
                    + "WHERE tpfld.T_TPFIELDID = mesfld.T_TPFIELDID "
                    +   "AND tpfld.T_NAME = :FLDNAME "
                    +   "AND mesfld.T_RLSFORMID = :RLSFORMID ";
  
  var params:TArray = makeArray( SQLParam("FLDNAME",   field     ),
                                 SQLParam("RLSFORMID", RlsFormID ) );
  var rs:RsdRecordset = execSQLselect( select, params, FALSE );
  if( not rs.MoveNext() )
    RunError( "|Не найдено поле  " + field + " в справочнике полей сообщений" );
  end;
  return ( rs.value(0) == "X" );
end;


macro WriteFieldEx( FieldName, FieldValue, Recreate, oldMes )

  // Если сообщение пересоздается И поле неважное, то берем его из старого сообщения
  if( ( Recreate == true ) AND (not GetBlockEdit(FieldName, OldMes.RlsFormID)) )
    ПолучитьПоле( OldMes.MesID, FieldName, FieldValue );
  end;
  if( FieldValue )
    ЗаписатьПолеЛог( FieldName, FieldValue );
  end;

end;

/* Записывает блочное поле */
macro ЗаписатьПолеЛогВБлок( Блок, Поле, Str, IsFirst )
var   Recreate, oldMes;
  GetParm(4, Recreate);
  GetParm(5, oldMes  );

  if( Recreate == NULL )
     Recreate = false;
  end;
  if( oldMes == NULL )
    oldMes = NULL
  end;

  if( valtype(IsFirst)==V_UNDEF )
     IsFirst = true;
  end;
  if ( IsFirst )
     if( not УстановитьКонтекстБлока() )
       RunError("|Ошибка при установке нулевого контекста блока");
     end;
  end;
  if ( index(Блок,"/") )
     if( not УстановитьКонтекстБлока( substr(Блок,1,index(Блок,"/")-1) ) )
       RunError("|Ошибка при установке контекста блока " + Блок);
     end;
     ЗаписатьПолеЛогВБлок( substr(Блок,index(Блок,"/")+1), Поле, Str, false )
  else
  if( not УстановитьКонтекстБлока( Блок ) )
    RunError("|Ошибка при установке контекста блока " + Блок);
  end;
  WriteFieldEx (Поле, Str , Recreate, OldMes);
/*     ЗаписатьПолеЛог( Поле, Str ) ; */
  end;
end;


macro ЯвляетсяЛиТелеграммаТранзитной(ObjID, FormID, InMesID) 
    FILE wlobjknd( wlobjknd ) key 0;
    var params:TArray = TArray();
    var res,ss;

    ClearRecord( wlobjknd );
    wlobjknd.ObjID    = FormID;
    wlobjknd.ObjKind  = OBJTYPE_FORM;
    wlobjknd.Type     = MESKIND_INFO;
    wlobjknd.Number   = 1/*WLD_SUBKIND_HEAD_CLN*/;
    if( not GetEQ(wlobjknd) )
       return false;
    end;

    /*Надо найти входящее сообщение*/
    ss = string(" SELECT mes.t_MesID FROM dwlmes_dbt mes, dwlmeslnk_dbt lnk ",
            " where lnk.t_ObjID = :ObjID and lnk.t_ObjKind = :ObjKind ",  
            " and mes.t_MesID = lnk.t_MesID and mes.t_Direct = 'X'");  
    params[params.size] = SQLParam( "ObjID",   ObjID);
    params[params.size] = SQLParam( "ObjKind", OBJTYPE_INFO);
    res = execSQLSelect (ss,params,true);
    if (not res.MoveNext());
        return false;
    else
       SetParm(2, res.Value(0));
       return true;
    end;
end;

macro ПолучитьРегион( Code, CodeKind )
  var error, BankID, BankCode;
  if ( (valtype(CodeKind)==V_UNDEF) OR (CodeKind==PTCK_BIC) )
     return substr( Code, 3, 2 ); /* для России так */
  else
     BankID=ПолучитьКодСубъекта( Code, CodeKind, error );
     if ( not error )
        BankCode = ПолучитьКодСубъекта( BankID, PTCK_BIC, error );
        if ( not error )
           return substr( BankCode, 3, 2 ); /* для России так */
        end
     end;
     return -1;
  end;
end;

macro Общий_Регион( DKFlag )
  var Чужой_Регион;

  if( DKFlag == WL_CREDIT )
    Чужой_Регион = ПолучитьРегион( wlpmpropcred.BankCode, wlpmpropcred.CodeKind ) ;
  else
    Чужой_Регион = ПолучитьРегион( wlpmpropdeb.BankCode, wlpmpropdeb.CodeKind ) ;
  end ;
  if( Свой_Регион == Чужой_Регион )
    return TRUE ;
  /* Москва и Московская обл. считаются одним регионом */
  elif( ( ( Свой_Регион == "45") AND ( Чужой_Регион == "46" ) ) OR
        ( ( Свой_Регион == "46") AND ( Чужой_Регион == "45" ) ) )
    return TRUE;
  else
    return FALSE;
  end;
end;

/* Проверка, что банк-получатель - ПУГ */
MACRO IsAbonentPUG( DKFLAG )
   var BankID;

   if( DKFlag == WL_CREDIT )
      BankID = wlpmpaym.ReceiverBankID;
   else
      BankID = wlpmpaym.PayerBankID;
   end;

   if (IsBankType(BankID, PT_KIND_FIELDOFFICE_CENTRALBANK) == true)  /* ПУ ЦБ */
     return true;
   end;

END;

macro IsAbonentMoskowRegion( DKFlag )
   var Чужой_Регион;

  if( DKFlag == WL_CREDIT )
    Чужой_Регион = ПолучитьРегион( wlpmpropcred.BankCode, wlpmpropcred.CodeKind ) ;
  else
    Чужой_Регион = ПолучитьРегион( wlpmpropdeb.BankCode, wlpmpropdeb.CodeKind ) ;
  end ;

  if( (Чужой_Регион == "46" ) OR (Чужой_Регион == "45") )
    return TRUE;
  end;

  return FALSE;
end;
