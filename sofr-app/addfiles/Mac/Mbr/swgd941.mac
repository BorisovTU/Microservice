/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям SWIFT MT941                              */
/*                                                                          */
/*  Имя файла: swgd941.mac                                                  */
/*  Создан:    22.03.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgendoc.mac";

/* Выписка со счета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */
  Account          :string,        /* Корсчет */
  StatementNumber  :string,        /* Номер выписки */
  SequenceNumber   :integer,       /* Номер страницы выписки */
  OpeningBalance   :Balance,       /* Открывающий баланс */
  NumberAndSumDeb  :NumberSumma,   /* Число и сумма entries по дебету */
  NumberAndSumCred :NumberSumma,   /* Число и сумма entries по кредиту */
  ClosingBalance   :Balance,       /* Закрывающий баланс */
  AvailableBalance :Balance,       /* Доступные средства */
  ForwardAvailableBalance :Balance,/* Доступные средства на дату */
  FlagSetForwardBalance :integer,  /* Флаг доступных средств на дату */
  Information      :string,        /* Информация владельцу счета */
  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank,          /* Получатель */
  Cln              :bool,          /*выписка клиентская*/
  IsCorschem       :bool;          /*Определилась корсхема или нет*/

  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  StatementNumber  = "";
  SequenceNumber   = 0;
  Information      = "";

  /* Начальная инициализация */
  FlagSetForwardBalance = 0;
  Cln = False;  
  IsCorschem = False; 

end; /* class StatementMessage */

var MT941 : StatementMessage;

macro Fill20( TRF )
  MT941.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MT941.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT941.Account = Account;
  return TRUE;
end;

macro Fill28( Number, Page )
  MT941.StatementNumber = Number;
  MT941.SequenceNumber = Page;
  return TRUE;
end;

macro Fill13D( DateInd, GMT:TGMT )
  return TRUE;
end;

macro Fill60F( DKFlag, DateBal, Cur, Sum )
  MT941.OpeningBalance = Balance( DKFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill90D( NumberAndSumDeb  :NumberSumma )
  MT941.NumberAndSumDeb = NumberAndSumDeb;
  return TRUE;
end;

macro Fill90C( NumberAndSumCred  :NumberSumma )
  MT941.NumberAndSumCred = NumberAndSumCred;
  return TRUE;
end;

/* Вставляем заголовок выписки - чтобы было к чему привязывать документы выписки */
macro InsertHead()
  wlhead.Account      = MT941.Account;
  wlhead.FIID         = MT941.ClosingBalance.FIID; 

  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account, wlhead.FIID, false ) )
     if (not(MT941.Cln))
       std.msg( String("Не определена схема расчетов для респондента по корсчету: ", MT941.Account) );
     return FALSE;
    end;
  else
    MT941.IsCorschem = TRUE;
    if( not ВставитьЗаголовокВыписки( wlhead ) )
      std.msg("Ошибка при вставке заголовка выписки");
      return FALSE;
    end;
  end;
  return TRUE;
end;

macro Fill62F( DkFlag, DateBal, Cur, Sum )
  MT941.ClosingBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill64( DkFlag, DateBal, Cur, Sum )
  MT941.AvailableBalance = Balance( DkFlag, DateBal, Cur, Sum );
  return TRUE;
end;

macro Fill65( DkFlag, DateBal, Cur, Sum )
  MT941.ForwardAvailableBalance = Balance( DkFlag, DateBal, Cur, Sum );
  MT941.FlagSetForwardBalance = 1;
  return TRUE;
end;

macro InserAvailableBalance
  if (not(MT941.IsCorschem))
      return TRUE;
  end;
  if ( MT941.FlagSetForwardBalance )
     ClearRecord(wlrest);
     wlrest.DateValue  = MT941.ForwardAvailableBalance.DateBal;
     wlrest.Sum        = MT941.ForwardAvailableBalance.Amount;
     if( MT941.ForwardAvailableBalance.DKFlag == DEBET_ACCOUNT )
        wlrest.DKFlag = WL_DEBET;
     else
        wlrest.DKFlag = WL_CREDIT;
     end;
     if( not ВставитьДоступныеСредства( wlrest ) )
        std.msg("Ошибка при сохранении доступных средств");
        return FALSE;
     end;
     MT941.FlagSetForwardBalance = 0;
  end;
  return TRUE;
end;

macro Fill86( value )
  MT941.Information = value;
  return TRUE;
end;

/* Заполнение списка полей формы  MT941*/
var Fld20   = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21   = ТПолеФормы( RelatedReferenceField,              FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "" ),
    Fld25   = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld28   = ТПолеФормы( StatementSequnceNumberField,        FIELD_MANDATORY, "Read28",  "Fill28",  "" ),
    Fld13D  = ТПолеФормы( DateTimeIndicationField,            FIELD_OPTIONAL,  "Read13D", "Fill13D", "" ),
    Fld60F  = ТПолеФормы( OpeningBalanceField_F,              FIELD_OPTIONAL,  "ReadBal", "Fill60F", "" ),
    Fld90D  = ТПолеФормы( NumberSumDebetEntriesField,         FIELD_OPTIONAL,  "Read90",  "Fill90D", "" ),
    Fld90C  = ТПолеФормы( NumberSumCreditEntriesField,        FIELD_OPTIONAL,  "Read90",  "Fill90C", "" ),
    Fld62F  = ТПолеФормы( ClosingBalanceField_F,              FIELD_MANDATORY, "ReadBal", "Fill62F", "InsertHead" ),
    Fld64   = ТПолеФормы( ClosingAvailableBalanceField,       FIELD_OPTIONAL,  "ReadBal", "Fill64",  "" ),
    Fld65   = ТПолеФормы( ForwardAvailableBalanceField,       FIELD_OPTIONAL,  "ReadBal", "Fill65",  "InserAvailableBalance" ),
    Fld86   = ТПолеФормы( InformationToAccountOwnerField,     FIELD_OPTIONAL,  "Read86",  "Fill86",  "" );

macro ConstructMT941( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  MT941.Cln = МожетЛиВыпискаБытьКлиентской(wlmes.RlsFormID);
  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;
            if( fld.Name ==  ForwardAvailableBalanceField )
              count = count - 1;
            end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT941.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT941.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT941 */

macro GenDoc( addrMes )
  MT941 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация выписки по МТ941");

  ClearRecord(wlhead);

  if( not ConstructMT941( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;
 if(MT941.IsCorschem)
    /* Заполнение учетных буферов */
    wlhead.Number          = MT941.StatementNumber;
    wlhead.Page            = MT941.SequenceNumber;
    wlhead.DateIn          = MT941.OpeningBalance.DateBal;
    wlhead.DateOut         = MT941.ClosingBalance.DateBal;
    wlhead.RestIn          = MT941.OpeningBalance.Amount;
    wlhead.RestOut         = MT941.ClosingBalance.Amount;
    wlhead.DebetFloorTurn  = MT941.NumberAndSumDeb.Amount;
    wlhead.CreditFloorTurn = MT941.NumberAndSumCred.Amount;
    wlhead.Description     = substr( MT941.Information, 1,215 );
  
    if( not ОбновитьЗаголовокВыписки( wlhead ) )
      std.msg("Ошибка при обновлении заголовка выписки");
      return FALSE;
    end;
 else
      ClearRecord(wlinfo);
      /* Заполнение учетных буферов */
      wlinfo.TRN          = MT941.TRF;
      wlinfo.RelatedRef   = MT941.RelatedRef;

      /* Создатель - наш абонент */
      wlinfo.OriginatorID       = MT941.Sender.PartyID;
      wlinfo.OriginatorCodeKind = MT941.Sender.CodeKind;
      wlinfo.OriginatorCode     = MT941.Sender.CodeBank;
      wlinfo.OriginatorName     = MT941.Sender.Name;

      /* Получатель - наш банек */
      wlinfo.RecipientID       = {OurBank};
      wlinfo.RecipientCodeKind = MT941.Receiver.CodeKind;
      wlinfo.RecipientCode     = MT941.Receiver.CodeBank;
      wlinfo.RecipientName     = MT941.Receiver.Name;
      MT941.Information = string("Выписка по клиентскому счёту N ", MT941.Account);
      if( not ВставитьИнфСообщение( wlinfo, MT941.Information ) )
        std.msg("Ошибка при вводе информационного сообщения");
        return FALSE;
      end;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
