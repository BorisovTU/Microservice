/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения платежа транспорта "Почта" и "Телеграф"              */
/*                                                                          */
/*  Имя файла: wlpstgm.mac                                                  */
/*  Создан:  12.07.01                                               BARS    */
/****************************************************************************/
import "wlmcgm.mac";

macro GenMes( addrMes, addrPm )
  var DKFlag = -1;
  var FlagTrans, ShifrOperation, Number, DatePay, Sum, PayerAccount,
      ReceiverAccount, ReceiverCorrAccNostro, ReceiverBankCode, Priority, СтрокаДокумента = "";
  var RsPaym;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog( 2, "Генерация сообщения по платежу (транспорт - почта) " );

  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  if ( RsPaym.BaseFIID!=0 )
    std.out( 2, "PaymentID: " + RsPaym.PaymentID );
    RunError( "|Транспорты /Почта/ и /Телеграф/ предназначены только для рублей" );
  end;

  Priority = int( RsPaym.Priority ) ;
  ShifrOperation = int( substr( RsPaym.ShifrOper, 1, 2) ) ;
  Number = RsPaym.Number;
  DatePay = RsPaym.Date;
  Sum  = (doublel(RsPaym.ReceiverAmount));

  if ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  elif ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  else
     DKFlag = -1;
  end;            

  FlagTrans = IsTransitPayment(RsPaym);  

  if( DKFlag == -1 ) /* документ не внешний */
    RunError( "|Платеж с ID: " + string( RsPaym.PaymentID ) + "не внешний" );
  end ;

  if ( DKFlag==WL_CREDIT )
     PayerAccount          = RsPaym.PayerAccount;
     ReceiverAccount       = RsPaym.ReceiverAccount;
     ReceiverCorrAccNostro = RsPaym.ReceiverCorrAccNostro;
     ReceiverBankCode      = RsPaym.ReceiverBankCode;
  else
     PayerAccount          = RsPaym.ReceiverAccount;
     ReceiverAccount       = RsPaym.PayerAccount;
     ReceiverCorrAccNostro = RsPaym.PayerCorrAccNostro;
     ReceiverBankCode      = RsPaym.PayerBankCode;
  end;

  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ShifrOperation, 2 ) ;         /* 1-2 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Number, 15 ) ;                /* 3-17 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, string(DatePay), 10 ) ;       /* 18-27 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, string(Sum:16:2), 16,Числовое_Поле ); /* 28-43 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, PayerAccount, 20 ) ;          /* 44-63 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ReceiverBankCode, 9 ) ;       /* 64-72 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ReceiverCorrAccNostro, 20 ) ; /* 73-92 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, ReceiverAccount, 20 ) ;       /* 93-112 */  
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Priority, 2 ) ;               /* 113-114 */    

  if( not ЗаписатьПоле( FormFldDoc, СтрокаДокумента ) )
    RunError( "|Невозможно записать поле " + FormFldDoc );
  end;

  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
