/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по SWIFT-SB MT204S                                    */
/*                                                                          */
/*  Имя файла: swsd204s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac", "pmlib.mac";

/* Платежная инструкция "Требование о безакцептном списании" */
class FinancialMarketsDirectDebitMessage
 var
  TRF              :string,      /* Ссылка на операцию */
  RelatedRef       :string,      /* Ссылка на сообщение или сделку */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  Credit           :bool,        /* Признак дебета/кредита */
  Trans            :bool,        /* Признак транзитного */
  OrdBank          :Bank,        /* Банк Приказодатель */
  Sender           :Bank,        /* Отправитель */
  AccBank          :Bank,        /* Банк-держатель счета */
  BnfBank          :Bank,        /* Банк Бенефициар */
  RcvInfo          :TRcvInfo100; /* Информация Получателю */

  TRF              = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Trans            = false;
end; /* class FinancialMarketsDirectDebitMessage */

var MT204S : FinancialMarketsDirectDebitMessage;

/* Найти подразделение в нашей территориальной структуре по счету */
private macro IsValAccountInTS( account:string ) :integer
  var select:string = " select dp.t_PartyID                   "
                    + " from daccount_dbt acc, ddp_dep_dbt dp "
                    + " where acc.t_department = dp.t_code    " 
                    + " And acc.t_account = :account          ";

  var params:TArray = makeArray(SQLParam( "account" , account ));
  var rs:object;

  rs  = execSQLselect( select, params, TRUE );
  if(rs and rs.moveNext())
     return rs.value(0);
  else
     return -1;
  end;  
end;

macro FillSN(TRF)
  MT204S.RelatedRef = TRF;
  return true;
end;

macro Fill20( TRF )
  MT204S.TRF = TRF;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT204S.ValueDate = Date;
  MT204S.Currency  = Cur;
  MT204S.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT204S.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  MT204S.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57A( BIC, Account, System, Code )
  MT204S.AccBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill57D( Address, Account, System, Code )
  MT204S.AccBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill58A( BIC, Account, System, Code )
  MT204S.BnfBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill58D( Address, Account, System, Code )
  MT204S.BnfBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT204S.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MT204S*/
var FldSN  = ТПолеФормы( УникальныйНомерСообщенияСМФР,       FIELD_MANDATORY, "ReadTRF", "FillSN",  "" ),
    Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill57A", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill57D", "" ),
    Fld58A = ТПолеФормы( BeneficiaryInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill58A", "" ),
    Fld58D = ТПолеФормы( BeneficiaryInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill58D", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_MANDATORY,  "Read72",  "Fill72",  "" );

macro ConstructMT204S( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            if( fld.Name == TimeIndicationField )
              count = count - 1;
            end;
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта(RespID, PTCK_SMFR, error);
  MT204S.Sender = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  MT204S.Credit = TRUE; /* входящие банковские переводы - кредитовые */
  MT204S.Trans  = MT204S.AccBank.IsSet(); /* документ транзитный - если указан держатель счета */

  return TRUE;
end; /* ConstructMT204S */

macro GenDoc( addrMes )
  var error;
  MT204S = FinancialMarketsDirectDebitMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(1,"Генерация платежа по МТ204S");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);
  ClearRecord(wlpmdemand);

  if( not ConstructMT204S( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.Amount      = MT204S.Sum;
  wlpmpaym.PayAmount   = MT204S.Sum;
  wlpmpaym.BaseAmount  = MT204S.Sum;
  wlpmpaym.OrderAmount = MT204S.Sum;
  wlpmpaym.ValueDate = {curdate};
  if( not ПолучитьФинИнПоISO( MT204S.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO :" +  MT204S.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  /*=====  В этом блоке разбираемся с полем 52A/D ======*/
  if( MT204S.OrdBank.IsSet() )
    if(MT204S.OrdBank.PartyID != -1)
      wlpmpaym.PayerBankID = MT204S.OrdBank.PartyID;
    else
      wlpmpaym.PayerBankID = IsValAccountInTS( MT204S.OrdBank.Account );
      if(wlpmpaym.PayerBankID == -1)
        wlpmpaym.PayerBankID = {OurBank};
      end;
      MT204S.OrdBank.CodeKind = PTCK_SMFR;
      MT204S.OrdBank.CodeBank = ПолучитьКодСубъекта(wlpmpaym.PayerBankID, PTCK_SMFR, error);
      if(error)
        std.msg("Отсутствует код СМФР у плательщика");
        return FALSE; 
      end;
    end;
  /*====================================================*/
  else
     wlpmpaym.PayerBankID = {OurBank};                                 ///???
  end;
  
  if( MT204S.Trans )
    wlpmpaym.ReceiverBankID = MT204S.AccBank.PartyID;
  else
    wlpmpaym.ReceiverBankID = wlmes.OutsideAbonentID;                ////????
  end;
  /* Счета плательщика/получателя */
  wlpmpaym.PayerAccount = MT204S.OrdBank.Account;

  /* Кредитовые свойства платежа */
  wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;
  
  wlpmpropcred.CodeKind = MT204S.AccBank.CodeKind;
  wlpmpropcred.BankCode = MT204S.AccBank.CodeBank;

  wlpmpropcred.CorrCodeKind = MT204S.Sender.CodeKind;
  wlpmpropcred.CorrCode     = MT204S.Sender.CodeBank;
  wlpmpropcred.CorrAcc      = MT204S.Sender.Account;
  wlpmpropcred.CorrID       = MT204S.Sender.PartyID;


  /* Реквизиты платежа, характерные для R-макета */
  if(MT204S.TRF!=СсылкаОтсутствует)
  wlpmrmprop.Number                = MT204S.TRF;
  end;

  wlpmrmprop.PayDate               = MT204S.ValueDate;
  wlpmrmprop.Date                  = MT204S.ValueDate;
  wlpmrmprop.ClientDate            = MT204S.ValueDate;
  if ( MT204S.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName         = MT204S.OrdBank.Name;
  else
     wlpmrmprop.PayerBankName         = {Name_Bank};
  end;  
  wlpmrmprop.PayerCorrAccNostro    = {CORAC_Bank};
  wlpmrmprop.PayerCorrBankName     = MT204S.Sender.Name;     

  if ( MT204S.AccBank.IsSet() )
     wlpmrmprop.ReceiverBankName      = MT204S.AccBank.Name;
  else
     wlpmrmprop.ReceiverBankName      = MT204S.Sender.Name;
  end;
  wlpmrmprop.ReceiverCorrAccNostro = MT204S.Sender.Account;

  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT204S.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "02";

  wlpmrmprop.ComissCharges         = PM_CHRG_OUR;
  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo        = MT204S.RcvInfo.Str;

  if ( MT204S.RelatedRef!=СсылкаОтсутствует )
     wlpmrmprop.Reference = MT204S.RelatedRef;
  end;

  wlpmdemand.AcceptTerm = PM_DEMAND_TERM_WITHOUTACCEPT;
  wlpmdemand.Accept     = PM_DEMAND_ACCEPT_NONE;

  /* Дебетовые свойства платежа */
  wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  wlpmpropdeb.CodeKind = MT204S.OrdBank.CodeKind;
  wlpmpropdeb.BankCode = MT204S.OrdBank.CodeBank;
  wlpmpropcred.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
  if(wlpmpropcred.Corschem != -1)
    wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop, PRT_Debet, wlpmdemand ) )   ///!!! pmdemand
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Банка Бенефициара - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT204S.AccBank.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT204S.Sender.GetRoute(), true );
  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT204S.OrdBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
