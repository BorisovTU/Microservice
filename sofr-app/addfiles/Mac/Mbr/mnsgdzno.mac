/*
  $Name:         mnsgdzno.mac
  $Module:       МБР
  $Description:  Генерация запросов по сообщению ZNO
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по сообщению ZNO                                      */
/*                                                                          */
/*  Имя файла: mnsgdzno.mac                                                 */
/*  Создан:    26.10.11                                  Chukina T.         */
/****************************************************************************/

import mnsznsal, PTInter, "bnk_common.mac";

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );
  var error = 0, ReqID = -1, i = 0;
  var Accounts:TArray = TArray();

  ВидСправки  = "";
  ВидЗапроса  = "";
  Счет = TArray();
  var Message = MnsMessageFormZNS();

  PrintLog(2,"Генерация запроса по ZNO");

  /* Если при загрузке не определился внутренний абонент, то создаем подтверждение банка с ошибкой */
  if( wlmes.InsideAbonentID <= 0 )

    var ErrList = RsbWlError(0);

    var wlerr_buf = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.Code         = "31";
    wlerr_buf.rec.Description  = "Электронный документ ошибочно направлен налоговым органом не в тот банк (филиал банка). В банке "+
                                 ПолучитьБИКГоловногоФилиала( {OurBank} ) + " нет филиала с БИК " + БИК + " и номером " + НомФ;
    ErrList.Insert( wlerr_buf );
    /* Параметры создаваемого УО */
    var ResultID = 0;
    var ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;
    var ErrDescription = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, ErrCode);

    if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList, "", -1, 0, "", "", -1, 0, "", "", NULL, NULL, ResultID ) )
      std.msg( "Ошибка при вставке подтверждения банка" );
      return FALSE;
    end;
    /*Связь подтверждения с входящим сообщением */
    var meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = ResultID;
    meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
    var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
    MesLnkObj.Insert( meslnk );
    MesLnkObj.Save();
    return TRUE;
  end;
  
  ClearRecord(wlreq);

  /* Заполнение учетных буферов */
  wlreq.Kind                     = MESKIND_REQUEST;
  wlreq.SubKind                  = ВидЗапроса;
  wlreq.Trn                      = НомерЗапроса;
  wlreq.PmDateValue              = date( int( substr( ДатаЗапроса, 1, 2 ) ), int( substr( ДатаЗапроса, 4, 2 ) ), int( substr( ДатаЗапроса, 7 ) ) );
  wlreq.RelatedRef               = wlmes.Trn;
  wlreq.OriginatorID             = ПолучитьКодСубъекта( КодНО, PTCK_MNS, error );
  wlreq.OriginatorCodeKind       = PTCK_MNS;
  wlreq.OriginatorCode           = КодНО;
  wlreq.OriginatorName           = НаимНО; 
  wlreq.RecipientID              = wlmes.InsideAbonentID;
  wlreq.RecipientCodeKind        = wlmes.InsideAbonentCodekind;
  wlreq.RecipientCode            = wlmes.InsideAbonentcode;
  record party(party);
  ПолучитьСубъекта ( wlreq.RecipientID, party );
  wlreq.RecipientName            = party.name;
  wlreq.Corschem                 = -1;
  wlreq.FIID                     = -1;
  wlreq.Queries                  = IfThenElse( ТипЗапроса == "1", "по всем счетам", "" );

  if( not ВставитьЗапрос( wlreq, Message.GetNarrative(), "", NULL, ReqID ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  /* Заполнение связанных счетов */
  if( ReqID > 0 )
    if( ТипЗапроса == "2" )
      var DateFrom : date = ToDate_DDpMMpYYYY(ДатаНач);
      var DateTo : date = ToDate_DDpMMpYYYY(ДатаКон);
      if( InList(wlreq.SubKind, 1, 2) )
        DateFrom = DateTo = GetDateFnsInfoRequest(null, ДатаЗапроса);
      end;

      while( i < Счет.Size )
        ClearRecord( wlacclnk );
        wlacclnk.ObjectID      = ReqID;
        wlacclnk.ObjectType    = OBJTYPE_REQ;
        wlacclnk.Account       = Счет[i];
        wlacclnk.FIID          = Bnk_GetAccountFIID( Счет[i] );
        wlacclnk.Chapter       = 1;
        /*wlacclnk.DateFrom      = DateFrom;
        wlacclnk.DateTo        = DateTo;*/
        if( not ВставитьСвязанныйСчет( wlacclnk ) )
          std.msg("Ошибка при вставке связанного счета");
        end;
        i = i + 1;
      end;
    end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;