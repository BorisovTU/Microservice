/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Печать выписки МЦИ по ЭСИД Е0003                                        */
/*  Имя файла: wlmcprne.mac                                                 */
/*  Создан:    25.10.02                                    Бердюкова И.И.   */
/****************************************************************************/

import "wlmcgd.mac";

macro РаспечататьПоле( имя, Строка, IsOldFormat )
/*|--------+-------------+---------------+-------------+----------------------+--------------------|*/
if ( IsOldFormat )
 [| ###### | ##########  | ##########    | #####       | #################### | ################## |
 ](
   substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l,
   substr(Строка, 25, 5):l, substr(Строка, 30, 20):l, moneyL( doubleL( substr( Строка, 50, 18 ) ) ):l
  );
else
 [| ###### | ##########  | ##########    | #####       | #################### | ################## |
 ](
   substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l,
   substr(Строка, 25, 3):l, substr(Строка, 28, 20):l, moneyL( doubleL( substr( Строка, 48, 18 ) ) ):l
  );
end;

  return TRUE;
end;


/*|--------+-------------+---------------+-------------+----------------------+--------------------|*/

/* Макрос печати заголовка формы */
macro PrintHeader( Имя, Строка )

  std.RSBankHeader;
  [Вид:                              #####](substr(Строка,1,5));
  [Номер ЭСИД:                       ##########](substr(Строка,6,15));
  [Дата составления ЭСИД:            ########## ########](СчитатьДату(Строка,16):f, СчитатьВремя(Строка,34));
  [Идентификатор составителя:        ##########](substr(Строка,24,10));
  [№ счета:                          ####################](Trim( substr( Строка, 41, 20 ) ));
  [Наименование владельца:           #](substr(Строка,61,80));
  [Входящий остаток:                 ##################](moneyL( doubleL( substr( Строка, 141, 18 ) )/100 ):l);
  [Исходящий остаток:                ##################](moneyL( doubleL( substr( Строка, 159, 18 ) )/100 ):l);
  [Лимит:                            ##################](moneyL( doubleL( substr( Строка, 177, 18 ) )/100 ):l);
  [Сумма внутридневного кредита:     ##################](moneyL( doubleL( substr( Строка, 195, 18 ) )/100 ):l);
  [Сумма зарезервированных средств:  ##################](moneyL( doubleL( substr( Строка, 213, 18 ) )/100 ):l);
  println("");

  [+------------------------------------------------------------------------------------------------+
   | Номер  | Дата        | Идентификатор | № документа | Корреспондирующий    | Сумма ЭПД          |
   | ЭПД    | составления | составителя   |             | счет                 |                    |
   |--------+-------------+---------------+-------------+----------------------+--------------------|
  ];
  return TRUE;
end;

macro PrintFoot(СуммаДокументов, Документы)
  var Sum_str; 

  Sum_str = RubToStrAlt(СуммаДокументов); /*Сумма прописью*/

  [|------------------------------------------------------------------------------------------------|
    Итого:  #################  #
    Документов: #
  ](moneyL( doubleL( СуммаДокументов ) ):l, Sum_str:l, string(Документы):l );
end;

/* Макрос печати форм */
macro PrintFormUni( addrMes, MassCopy, IsOldFormat )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  var Sum = $0, NumOfDoc = 0;
  FILE MsgTmpFile() txt;

  if ( valtype(IsOldFormat)==V_UNDEF )
     IsOldFormat = false;
  end;

  SetBuff( wlmes, addrMes );

   /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  /* Печатаем заголовок (заголовок выписки) */
  СчитатьПоле( field_name, field_value );
  if( not PrintHeader( field_name, field_value ) )
    return FALSE;
  end;

  /* Последовательно считываем и печатаем документы выписки */
  while( СчитатьПоле( field_name, field_value ) )
    if( not РаспечататьПоле( field_name, field_value, IsOldFormat ) )
      return FALSE;
    end;
    if ( IsOldFormat )
       Sum = Sum + moneyL( doubleL( substr( field_value, 50, 18 ) ) );
    else
       Sum = Sum + moneyL( doubleL( substr( field_value, 48, 18 ) ) );
    end;
    NumOfDoc = NumOfDoc + 1;
  end;

  PrintFoot(Sum, NumOfDoc);

  /* Конец отчета */
  std.RSBankFooter;
  println("");

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy !=0 )
    rewind(MsgTmpFile);
    while( next(MsgTmpFile) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end;
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;

macro PrintForm( addrMes, MassCopy )
   return PrintFormUni( addrMes, MassCopy, false );
end;

macro PrintFormOldFormat( addrMes, MassCopy )
   return PrintFormUni( addrMes, MassCopy, true );
end;