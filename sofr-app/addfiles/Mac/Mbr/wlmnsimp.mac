/*
  $Name:         wlmnsimp.mac
  $Module:       Межбанковские расчеты
  $Description:  Импорт  сообщений
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                          Импорт  сообщений                               */
/*                                                                          */
/*  Имя файла: wlmnsimp.mac                                                 */
/*  Создан:  01.12.04                                    Фомченкова Л.Н.    */
/****************************************************************************/

import "wlgenmes.mac", "wl365pch.mac";

 /* Общие глобализмы */
var ВидСообщения = "";
var КолБлоков = 0;
var ИмяБлока = "";
var ВидРегОргана = 0;
var ВидСправки = "";
var FormName = "";
var BIC_fldval = "";
var DeptNum_fldval = "";
var NumberOfLines = 0;
var MesFields:TArray = TArray();/*массив полей сообщения*/

const КолБлоковIPB = 1, 
      КолБлоковPOB = 1,
      БлокIPB = "ХарОш",
      БлокPOB = "ХарОш",
      БлокСчет = "Счет",
      БлокСтрИзв = "СтрокаИзвещения",
      БлокПоручПродВал = "ПоручПродВал",
      БлокОшибки = "Ошибки";
      
var ОшибкаФорматаИмениФайла = string("Неверный формат имени файла. Должно быть:\n",
"'ФФФИИИИГГГГММДДНННННН.txt', где:\n",
"ФФФ    - форма сообщения,\n", 
"ИИИИ   - код налогового органа - отправителя,\n",
"КККК - код налогового органа, взаимодействующего с банком,\n",
"ГГГГ   - год,\n", 
"ММ     - месяц,\n", 
"ДД     - число,\n"
"НННННН - номер файла с лидирующими нулями.");

private macro ЗаписатьПолеЛогВСписок( Блок, кодПоля, поле )
  var elem = TArray( 3 );
  elem[0] = Блок;
  elem[1] = кодПоля;
  elem[2] = поле;
  MesFields[MesFields.size] = elem;
end;

macro ОбработатьПоле(Блок, Строка)
  var ind = Index(Строка, ":"), кодПоля, поле;
  if( (FormName != ФормаKWT) and ( FormName != ФормаERROR ) )
    if(ind == 0)
      ErrImport(string("Неверный формат поля: ", Строка));
      return false;
    end;
    кодПоля = SubStr(Строка, 1, ind - 1);
    поле = SubStr(Строка, ind + 1);
  elif( FormName == ФормаKWT )
    кодПоля = "_СтрокаИзвещения";
    Строка = StrSubst(Строка, "===", "");
    Строка = StrSubst(Строка, "@@@", "");
    Строка = StrSubst(Строка, "###", "");
    поле = Строка;    
  elif( FormName == ФормаERROR )
    Блок     = БлокОшибки;
    кодПоля  = "_Ошибка";
    поле     = ImportErrors[0];    
  end;
  if(кодПоля == "ДатаПолСооб") 
    кодПоля = "ДатаПлСооб";
  elif(кодПоля == "НомСч")
    Блок = БлокСчет;
  elif(кодПоля == "БИК")
    BIC_fldval = поле;
  elif(кодПоля == "НомФ")
    DeptNum_fldval = поле;
  elif(кодПоля == "БИКБПл")
    BIC_fldval = поле;
  elif(кодПоля == "НомПорВал")
    Блок = БлокПоручПродВал;
  end;

  if (поле != "")
    ЗаписатьПолеЛогВСписок( Блок, кодПоля, SubStr(поле, 1, МаксимальнаяДлинаСтроки) );

    while( (strlen(поле) > МаксимальнаяДлинаСтроки) and (FormName == ФормаKWT) )
      поле = SubStr(поле, МаксимальнаяДлинаСтроки + 1);
      ЗаписатьПолеЛогВСписок( Блок, кодПоля, SubStr(поле, 1, МаксимальнаяДлинаСтроки) );
    end;
  end;

  return true;
end;

private macro getPartyIDFromBIC(BIC : string) : integer
  // Найти субъект party, где:
  // - код вида 3 равен значению поля "БИК"
  var qGetPartyByBIC : string =
      "select pc.t_PartyID, pc.t_Code " +
      "      from dpartcode_dbt pc " +
      "      where pc.t_CodeKind = " + string(PTCK_BIC) +
      "        and pc.t_Code = :BIC";
  var params : TArray = makeArray( SQLParam("BIC", BIC) );
  var rs : RsdRecordset = execSQLselect(qGetPartyByBIC, params, true);
  
  if ( (rs) and (rs.moveNext) )

    // Найти филиал dp_dep, где dp_dep.partyID = party.partyID
    var qIsPartyIDInTS = "select t.t_PartyID "
                         "  from ddp_dep_dbt t "
                         " where t.t_PartyID = " + rs.value(0);
    var rs1 : RsdRecordset = execSQLselect(qIsPartyIDInTS);

    if ( (rs1) and (rs1.moveNext) )
      return rs1.value("t_PartyID");      
    else
      ErrImport(string("Получатель сообщения ", BIC, " не является филиалом нашего банка") );
      return -1;
    end;

  else
    ErrImport(string("По БИК ", BIC, " не удалось определить получателя сообщения") );
    return -1;
  end;

end;

private macro getOutsideAbonentDateKWT()
  ПерейтиВНачалоФайла();
  var Длина = 0, Строка = "";
  while ( ТекущаяСтрока() < NumberOfLines - 2 )
    Длина = МаксимальнаяДлинаСтроки;
    Строка = СчитатьСтроку( Длина );
  end; // третья строка с конца файла 
  Строка = subStr(Строка, 1, 10); // YYYY-MM-DD
  Строка = strSubst(Строка, "-", ""); // YYYYMMDD
  return ToDateMNS( Строка );
end;

/***************************************************************************/
/*  Функция считывания сообщения МНС                                       */
/*  Возвращает:                                                            */
/*             0 - все ОК                                                  */
/*             1 - сообщение не найдено                                    */
/*             2 - найден конец файла                                      */
/*            -1 - ошибка при обработке сообщения (ругается сама)          */
/***************************************************************************/
macro ОбработатьСообщение( needCheckFormatPB1 )
  Bnk_ToRSTrace("ОбработатьСообщение", "Begin", "");

  /* Вспомогательные переменные */
  var result = 0, Строка;
  var continue0 = 1, Длина, КоличествоБлоков = 0, Блок = "..", КоличествоФрагментов = 0;
  NumberOfLines = 0;
  var CheckFld365P:object = NULL;
  if( NeedCheck365P( FormName ) and ( wlmes.RlsFormID > 0 ) )
    CheckFld365P = FldRlsListForCheck( wlmes.RlsFormID );
  end;

  /* Последовательно читаем поля сообщения */
  while( continue0 )
     Длина = МаксимальнаяДлинаСтроки;
     строка = СчитатьСтроку( Длина );
     if( NeedCheck365P( FormName ) )
       CheckFld365P.InsertFieldData( SubStr( Строка, 1, Index( Строка, ":" ) - 1 ),  SubStr( Строка, Index( Строка, ":" ) + 1 ) );
     end;
     if( (Длина == КонецФайла) OR (Длина == 0) )
        ErrImport(string(Длина) + "Неожиданный конец файла");
        return -1;
     end;
     NumberOfLines = NumberOfLines + 1;
     if((Строка == МНС_КонецФайла))
        continue0 = 0;
     end;
     if(Строка == МНС_КонецФрагмента)
       КоличествоФрагментов = КоличествоФрагментов + 1;
     end;
     if((Строка == МНС_КонецБлока) and (КоличествоФрагментов > 0)) // подсчитывать блоки начинаем после первого фрагмента (служебная часть)
        КоличествоБлоков = КоличествоБлоков + 1;
     end;
     if((Строка != МНС_КонецБлока) and (Строка != МНС_КонецФайла) and (Строка != МНС_КонецФрагмента))
       if(КоличествоБлоков == КолБлоков)
         Блок = ИмяБлока;
       end;

       while( (Длина == МаксимальнаяДлинаСтроки) and (FormName == ФормаKWT) )
         строка = строка + СчитатьСтроку( Длина );

         if( (Длина == КонецФайла) )
           ErrImport(string(Длина) + "Неожиданный конец файла");
           return -1;
         end;
       end;

       if( not ОбработатьПоле( Блок, Строка ) )
         return -1;
       end;
     end;
  end;

  if( needCheckFormatPB1 AND NeedCheck365P( FormName ) )
    Bnk_ToRSTrace("ОбработатьСообщение", "", "Вызов CheckFld365P.CheckFields()");
    CheckFld365P.CheckFields();
  end;

  // Если в сообщении есть поля "БИК" и "НомФ"
  if( InList( FormName, ФормаRPO, ФормаROO, ФормаTRB, ФормаZNO, ФормаPNO ) )
    FnsImp_UpdateInsideAbonent(FormName, wlmes, BIC_fldval, DeptNum_fldval, true);
  end;

  if (FormName == ФормаKWT)
    wlmes.OutsideAbonentDate = getOutsideAbonentDateKWT();
    ОбновитьЗапись(wlmes);
  end;

  if( ( result == 0 ) and ( ВидСправки != "" ) )
    ЗаписатьПолеЛогВСписок( "..", "_ВидСправ", ВидСправки );
  end;
  if( ВидРегОргана )
    ЗаписатьПолеЛогВСписок( "..", "_ВидРегОргана", string(ВидРегОргана) );
  end;

  Bnk_ToRSTrace("ОбработатьСообщение", "End", "");
  return result;
end;

private macro GetOutSideAbonentTime()

   ПерейтиВНачалоФайла(); // поле ИдФайл первое
   var Длина = МаксимальнаяДлинаСтроки,
       Строка = СчитатьСтроку( Длина );

   var ind = Index(Строка, ":"), кодПоля, поле;
   if(ind > 0)
     кодПоля = SubStr(Строка, 1, ind - 1);
     поле    = SubStr(Строка, ind + 1);
     if( кодПоля == "ИдФайл" )                            
       return time( int(SubStr( поле, 30, 2 )), int(SubStr( поле, 32, 2 )), int(SubStr( поле, 34, 2 )) );
     end;
   end;
   return time(0,0,0);
end;

private macro GetBIC_RCC( PartyID )
  f_wldprt.PartyID = PartyID;
  if( not GetEQ(f_wldprt))
    RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
  end;
  return f_wldprt.BIC_RCC;
end;

/* Эта ошибка будет выводиться после отчета о загрузке */
var _ОшибкаФорматаИмениФайла = "";
/*
SBCXabbbbbbb_LLLLDDDDDDDD_RRRRFFFFGGNNNNNN_MMM.
RXXabbbbbbb_ККККDDDDDDDD_NNNNNN,
TRBabbbbbbb_KKKKDDDDDDDD_NNNNNN,
ZNOabbbbbbb_KKKKDDDDDDDD_NNNNNN,
KWTFCB_P,
PNOabbbbbbb_ККККDDDDDDDD_NNNNNN,*/
macro РазобратьИмяФайла(ПолноеИмяФайла, Транспорт, needCheckFormatPB1 )
  Bnk_ToRSTrace( "РазобратьИмяФайла", "Begin", 
                 "ПолноеИмяФайла: " + ПолноеИмяФайла + ", " +
                 "Транспорт: " + Транспорт + ", " +
                 "needCheckFormatPB1: " + string(needCheckFormatPB1)
               );

  var TpShemID, RespID, НОБанка, error, ИмяФормы, Форма, Релиз, ДатаОтправки, 
      Дата, TRN, ИмяФайла, Расширение, Получатель = "", RespCode = "", RespCodeKind, needCheckOrgNameFile = false;

  SplitFile( ПолноеИмяФайла, ИмяФайла, Расширение );
  ИмяФормы = ОпределитьИмяФормыФНС( ИмяФайла, @ВидРегОргана, @ВидСправки );
  if(ИмяФормы == "")
    Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Не определилось ИмяФормы");

    if( NeedCheck365P( FormName ) )
      ImportErrors[ImportErrors.size] = "14;в символах 1-3 имени файла неверно обозначен тип сообщения. Сообщение с типом " + 
                                         SubStr( ИмяФайла, 1, 3 ) + " не может быть принято банком";
      FormName = ИмяФормы = ФормаERROR;
      _ОшибкаФорматаИмениФайла = "Не найдена форма с кодом " + SubStr( ИмяФайла, 1, 3 );
    else
     ErrImport( ОшибкаФорматаИмениФайла );
     return false;
    end;
  else
    FormName = ИмяФормы;
  end;

  Форма = ОпределитьФорму( Транспорт, ИмяФормы, ВидСообщения );
  if ( Форма == -1 )
    Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Не определился идентификатор формы сообщения");

    ImportErrors[ImportErrors.size] = "14;в символах 1-3 имени файла неверно обозначен тип сообщения. Сообщение с типом " + ИмяФормы + 
                                      " не может быть принято банком.";
    FormName = ИмяФормы = ФормаERROR;
    _ОшибкаФорматаИмениФайла = "Не найдена форма с кодом " + SubStr( ИмяФайла, 1, 3 );
  end;

  if( needCheckFormatPB1 )
    /*Проверка формата имени файла*/
    if( NeedCheck365P( FormName ) )
      GetRegistryValue( "PS\\СООБЩЕНИЯ ФНС\\ПРОВЕРКА ФОРМАТА\\ИМЯ ФАЙЛА\\СТРУКТУРА", V_BOOL, needCheckOrgNameFile, error );
      if( not error and needCheckOrgNameFile )
        Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Вызов ПроверкаФорматаИмениФайла365П");

        ПроверкаФорматаИмениФайла365П( ИмяФайла );
        if( ImportErrors.size > 0 )
          Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Обнаружены ошибки макрофункцией ПроверкаФорматаИмениФайла365П");

          FormName = ИмяФормы = ФормаERROR;
          _ОшибкаФорматаИмениФайла = "Неверная структура имени файла сообщения";
        end;
      end;
    end;
  end;

  /*Если были ошибки обработки, меняем загружаемую форму на ERROR*/
  if( ( FormName == ФормаERROR ) and ( ImportErrors.size > 0 ) )
    Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Вызов ImportERROR");
    return ImportERROR( ПолноеИмяФайла, Транспорт, FormName, true );
  elif( ImportErrors.size == 0 )
    if((ИмяФормы == ФормаRPO) or (ИмяФормы == ФормаROO) or (ИмяФормы == ФормаZNS) 
       or (ИмяФормы == ФормаZNO)
      )
        ИмяБлока = БлокСчет;
        КолБлоков = 1;
    elif(ИмяФормы == ФормаPNO)
        ИмяБлока = БлокПоручПродВал;
        КолБлоков = 1;
    elif(ИмяФормы == ФормаKWT)
        ИмяБлока = БлокСтрИзв;
        КолБлоков = 0;
    else
        ИмяБлока = БлокPOB;
        КолБлоков = КолБлоковPOB;
    end;
    
    if((ИмяФормы == ФормаRPO) or (ИмяФормы == ФормаROO) or (ИмяФормы == ФормаZNS)
       or (ИмяФормы == ФормаTRB)  or (ИмяФормы == ФормаZNO) 
      )  
      Получатель = SubStr(ИмяФайла, 5, 7);
      if( Получатель == "" )
        ErrImport( ОшибкаФорматаИмениФайла );
        return false;
      end;

      ДатаОтправки = SubStr(ИмяФайла, 17, ДлинаДаты);

      RespID = ПолучитьКодСубъекта( {MFO_RCC}, PTCK_BIC, error );
      RespCode = {MFO_RCC};
      RespCodeKind = PTCK_BIC;
      if ( error )
         ErrImport( string("Не найден субъект с кодом ", {MFO_RCC}) );
         return false;
      end;
      if ( (ИмяФормы == ФормаTRB) OR (ИмяФормы == ФормаZNO) OR (ИмяФормы == ФормаZNS) )
        TRN = SubStr(ИмяФайла, 1, 4) + StrSubst(SubStr(ИмяФайла,13),"_","");
      else 
        TRN = StrSubst(SubStr(ИмяФайла,5),"_",""); 
      end;
    else
      if (ИмяФормы != ФормаKWT)
        НОБанка = SubStr(ИмяФайла, 8, 4);
        if(НОБанка == "")
          ErrImport( ОшибкаФорматаИмениФайла );
          return false;
        end;
        ДатаОтправки = SubStr(ИмяФайла, 
                              IfThenElse( ( (ИмяФормы == ФормаZNS) or 
                                            (ИмяФормы == ФормаZNO) or 
                                            (ИмяФормы == ФормаPNO) ), 17, 18 ), 
                              ДлинаДаты);  
      end;

      RespCodeKind = IfThenElse( ((ИмяФормы == ФормаZNS) or 
                                  (ИмяФормы == ФормаZNO) or
                                  (ИмяФормы == ФормаKWT) or
                                  (ИмяФормы == ФормаPNO)), PTCK_BIC, PTCK_MNS );
      RespCode = IfThenElse( ( (ИмяФормы == ФормаZNS) or 
                               (ИмяФормы == ФормаZNO) or 
                               (ИмяФормы == ФормаKWT) or 
                               (ИмяФормы == ФормаPNO) ), 
                             GetBIC_RCC({OurBank}), SubStr(ИмяФайла, 14, 4) );
      if(RespCode == "")
         ErrImport( ОшибкаФорматаИмениФайла );
         return false;
      end;
      RespID = ПолучитьКодСубъекта( RespCode, RespCodeKind, error );    

      if ( error )
         ErrImport( string("Не найден субъект с кодом ", RespCode) );
         return false;
      end;

      if (ИмяФормы == ФормаKWT)
        var P = subStr(ИмяФайла, 8);
        // исключаем символы кода БИК (7 символов перед вторым подчеркиванием)
        var ind = index(P, "_");
        ind = index(P, "_", ind + 1); // позиция символа со вторым подчеркиванием
        TRN = subStr(P, 1, ind - 8) + subStr(P, ind + 1);
        // исключаем символы подчеркивания
        TRN = strSubst(TRN, "_", "");
      else
        TRN = IfThenElse( ( (ИмяФормы == ФормаZNS) or 
                            (ИмяФормы == ФормаZNO) or 
                            (ИмяФормы == ФормаPNO) ), 
                         StrSubSt( StrSubSt( ИмяФайла, "_", "" ), SubStr( {MFO_Bank}, 3, 7 ), "" ), 
                         ИмяФайла );
      end;
    end;
   
    if (ИмяФормы != ФормаKWT)
      if(ДатаОтправки == "")
        ErrImport( ОшибкаФорматаИмениФайла );
        return false;
      end;
      Дата = ToDateMNS( ДатаОтправки );
    end;
   
    TpShemID = ОпределитьТранспортнуюСхему(RespID, -1, -1, Транспорт, Форма, Релиз, NULL, NULL, wlsess.TpFrmtID);
    if ( TpShemID == -1 ) 
      Bnk_ToRSTrace( "РазобратьИмяФайла", "", "Не удалось определить транспортную схему для респондента " + RespID + ", транспорта " + Транспорт + ", формы " + Форма );
      return false;      
    end;

    if( not CheckOnDoubling( TRN, Релиз ) )
      return false;
    end;

    /* Заполняем ответное сообщение */
    ClearRecord( wlmes );
    wlmes.TpSchemID              = TpShemID;
    wlmes.RlsFormID              = Релиз;
    wlmes.OutsideAbonentID       = RespID;
    wlmes.AgentID                = wlmes.OutsideAbonentID;
    wlmes.OutsideAbonentCodeKind = RespCodeKind;
    wlmes.AgentCodeKind          = wlmes.OutsideAbonentCodeKind;
    wlmes.OutsideAbonentCode     = RespCode;
    wlmes.AgentCode              = wlmes.OutsideAbonentCode;
    wlmes.Kind                   = ВидСообщения;
    wlmes.TRN                    = TRN;
    wlmes.Department             = {OperDprt};
    if (ИмяФормы != ФормаKWT)
      wlmes.OutsideAbonentDate   = Дата;
    end;
    wlmes.Importance             = 0;
    if( ИмяФормы == ФормаZNS )
      wlmes.OutsideAbonentTime   = GetOutSideAbonentTime();
    end;
    if( ( ИмяФормы == ФормаKWT ) or ( ИмяФормы == ФормаRPO ) or ( ИмяФормы == ФормаROO ) or ( ИмяФормы == ФормаTRB ) )
      wlmes.InsideAbonentID = {OurBank};
      wlmes.InsideAbonentCodeKind = PTCK_BIC;
      wlmes.InsideAbonentCode = {MFO_Bank};
    end;
    if ( not СоздатьЗапись( wlmes ) )
     ErrImport( string("Невозможно создать запись по форме ", ИмяФормы));
     return false;
    end;
  end;

  AddRepElem(wlmes.Kind, ИмяФормы, "RUR", money(0) );
  Bnk_ToRSTrace( "РазобратьИмяФайла", "End", "");
  return true;
end;


macro ImportMessage(TpID, FileName, addrSess)

   var Отправитель, ДатаОтправки, Форма, Релиз, stat, error, needCheckFormatPB1 = true;

   SetBuff( wlsess, addrSess );
   
   /* Инициализируем список ошибок */
   ImportErrors = TArray();

   Транспорт = TpID;

   GetRegistryValue( "PS\\СООБЩЕНИЯ ФНС\\ПРОВЕРКА ФОРМАТА\\ИМЯ ФАЙЛА\\ВЫПОЛНЯТЬ_ПРОВЕРКУ", V_BOOL, needCheckFormatPB1, error );

   if(not(РазобратьИмяФайла(FileName, Транспорт, needCheckFormatPB1)))
     return FALSE;
   end;

   ПерейтиВНачалоФайла();

   stat = ОбработатьСообщение( needCheckFormatPB1 );
   if( stat == -1 )
     Bnk_ToRSTrace( "ImportMessage", "", "Функция ОбработатьСообщение вернула stat=" + stat);
     return FALSE;
   end;
   /*Если были ошибки обработки, меняем загружаемую форму на ERROR*/
   if( ImportErrors.size > 0 )
     if( FormName != ФормаERROR )
       FormName = ФормаERROR;
       MesFields.size = 0;
       ImportERROR( FileName, Транспорт, FormName, false ); 
     end;
     for( var err, ImportErrors )
       ЗаписатьПолеЛогВБлок/*Список*/( "Ошибки", "_Ошибка", err );
     end;
   else
     for( var fld, MesFields )
       ЗаписатьПолеЛогВБлок( fld[0], fld[1], fld[2] );
     end;
   end;
   AddRepElem(wlmes.Kind, FormName, "RUR", money(0) );
   PrintImportReport();
   if( _ОшибкаФорматаИмениФайла != "" )
     std.msg( string(_ОшибкаФорматаИмениФайла) );
   end;
 
   return true;
  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
