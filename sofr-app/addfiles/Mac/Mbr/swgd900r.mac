/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждений по сообщениям SWIFT MT900 стандарт RUR5          */
/*                                                                          */
/*  Имя файла: swgd900r.mac                                                 */
/*  Создан:     8.09.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgnrdoc.mac";

/* Подтверждение дебета */
class StatementMessage
 var
  standart         :integer,     /* Стандарт транслитерации */ 
  TRF              :string,      /* Ссылочный номер операции */
  RelatedRef       :string,      /* Ссылка */
  Account          :string,      /* Корсчет */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  OrdBank          :Bank,        /* Банк Приказодателя */
  RcvInfo          :TRcvInfo100, /* Информация Получателю поле 72 */

  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank;          /* Получатель */

  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;

end; /* class StatementMessage */

var MT900 : StatementMessage;

macro Fill20( TRF )
  MT900.TRF = TRF;
  MT900.standart = ДоопределитьСтандарт( TRF );
  return TRUE;
end;

macro Fill21( TRF )
  MT900.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT900.Account = Account;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT900.ValueDate = Date;
  MT900.Currency  = Cur;
  MT900.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT900.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  StrRestoreStandart( Address, Address, MT900.standart );
  MT900.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  StrRestoreStandart( Narrative, Narrative, MT900.standart );
  MT900.RcvInfo = TRcvInfo100( Str, Narrative );

  StrRestoreStandart( MT900.RcvInfo.Str, MT900.RcvInfo.Str, MT900.standart );  
  StrRestoreStandart( MT900.RcvInfo.NZP, MT900.RcvInfo.NZP, MT900.standart );
  return TRUE;
end;

/* Заполнение списка полей формы  MT900*/
var Fld20   = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21   = ТПолеФормы( RelatedReferenceField,              FIELD_MANDATORY, "ReadTRF", "Fill21",  "" ),
    Fld25   = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A,  FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,          FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,          FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),        
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,    FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMT900( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT900.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT900.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT900 */

macro GenDoc( addrMes )
  MT900 = NULL;
  MT900 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по МТ900");

  ClearRecord(wlconf);

  if( not ConstructMT900( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if( not ПолучитьФинИнПоISO( MT900.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT900.Currency );
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlconf.Number       = MT900.TRF;
  wlconf.RelatedRef   = MT900.RelatedRef;
  wlconf.Account      = MT900.Account;  
  wlconf.FIID         = wlfininstr.FIID;
  wlconf.DateValue    = MT900.ValueDate;
  wlconf.Sum          = MT900.Sum;
  wlconf.DKFlag       = WL_DEBET;

  if( MT900.RcvInfo.NZP != "" )
    wlconf.Description = MT900.RcvInfo.NZP;
  else
    wlconf.Description = substr( MT900.RcvInfo.Str, 1,215 );
  end;

  wlconf.TransferDate = wlconf.DateValue;//wlmes.Date;

  if( not ОпределитьСхемуРасчетовПоКорсчету( wlconf.Corschem, wlmes.OutsideAbonentID, wlconf.Account, wlconf.FIID ) )
    return FALSE;
  end;    

  if ( MT900.OrdBank.IsSet() )
     wlconf.BankID    = MT900.OrdBank.PartyID;
     wlconf.CodeKind  = MT900.OrdBank.CodeKind;
     wlconf.CodeName  = MT900.OrdBank.CodeName;
     wlconf.CodeValue = MT900.OrdBank.CodeBank;
     wlconf.BankName  = MT900.OrdBank.Name;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вводе подтверждения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
