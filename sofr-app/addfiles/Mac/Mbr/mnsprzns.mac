
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mnsprzns.mac
  Created     : 27.01.2011
  Programmer  : Popova O.
  Description : Печать сообщения по запросу ФНС о наличии счетов, остатках на счетах, предоставлении выписок

└───────────────────────────────────────────────────────────────────────────*/

import "wldoc.mac", "mnsznsal.mac";

macro PrintDoc( addrMes, MassCopy )

  SetBuff( wlmes, addrMes );

  var field_name, field_value;
  var MsgTmpFileName, OldName;
  
  FILE MsgTmpFile() txt;
  var stat:bool = true;

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  if( wlmes.Kind != MESKIND_MNS )
    stat = false;
  end;

  if( stat == true )
    stat = MnsMessageFormZNS().PrintDoc() == 0;
  end;

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  if( stat == true )
    wlmes.IsPrinted = "X";
    if( not UpdateMes(wlmes) )
      msgbox("Ошибка при обновлении сообщения");
      stat = false;
    end;
  end;
  if( not GetDialogFlag() and ( GetMNSWarning() != "" ) )// выводим предупреждения в протокол
    msgbox( GetMNSWarning() );
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage(er);
  return false;
end;
