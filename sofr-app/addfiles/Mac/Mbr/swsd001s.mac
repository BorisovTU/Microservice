/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по SWIFT-SB MT001S                                    */
/*                                                                          */
/*  Имя файла: swsd001s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac", "pmlib.mac";

/* Мемордер СМФР */
class МемордерСМФР
 var
  TRF               :string,      /* Ссылочный номер операции */
  RelatedRef        :string,      /* связанный референс */
  ValueDate         :date,        /* Дата валютирования */
  Currency          :string,      /* Код валюты платежа */
  Sum               :moneyl,      /* Сумма перевода */
  OrdCustomer       :Customer,    /* Плательщик */
  BnfCustomer       :Customer,    /* Получатель */
  OrdBank           :Bank,        /* Банк плательщика */
  BnfBank           :Bank,        /* Банк получателя */
  Sender            :Bank,        /* Отправитель */
  DKFlag            :string,      /* Признак дебет\кредит */
  DebitAcc          :string,      /* Дебетуемый счет */
  CreditAcc         :string,      /* Кредитуемый счет */
  RcvInfo           :TRcvInfo100, /* Информация Получателю поле 72 */
  FIID              :integer;    //Валюта сообщения

  TRF               = "";
  ValueDate         = date(0,0,0);
  Currency          = "";
  Sum               = $0;
  FIID              = 0;
end; /* class МемордерСМФР */

var MT001S : МемордерСМФР,
    Cors, Payer, Receiver;

macro ПолучитьИмяСубъекта(ID)
   FILE party(party);
   ClearRecord(party);
   party.PartyID = ID;
   if(not getEQ(party)) 
      return "";
   end;        
   return party.Name;
end;                                     

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

/*macro FillDC(SubType)
  return true;
end; */

macro FillDC(DKFlag)
  MT001S.DKFlag = DKFlag;
  return true;
end;

macro Fill20( TRF )
  MT001S.TRF = TRF;
  return TRUE;
end;

macro Fill21(RelatedRef)
  MT001S.RelatedRef = RelatedRef;
  return true;
end;

macro Fill25D(Acc)
  MT001S.DebitAcc = Acc;
  return true;
end;

macro Fill25C(Acc)
  MT001S.CreditAcc = Acc;
  return true;
end;

macro Fill32A( Date, Cur, Sum )
  MT001S.ValueDate = Date;
  MT001S.Currency  = Cur;
  MT001S.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT001S.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MT001S*/
var FldSN  = ТПолеФормы( УникальныйНомерСообщенияСМФР,       FIELD_MANDATORY, "ReadTRF", "FillSN",  "" ),
    FldRN  = ТПолеФормы( НомерСвязанногоСообщенияСМФР,       FIELD_OPTIONAL,  "ReadTRF", "FillRN",  "" ),
    FldDC  = ТПолеФормы( ДебетКредитФлагСМФР      ,          FIELD_MANDATORY, "ReadDC",  "FillDC",  "" ),
    Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "" ),
    Fld25D = ТПолеФормы( AccountIdentificationField + "D",   FIELD_OPTIONAL,  "Read25" , "Fill25D", "" ),
    Fld25C = ТПолеФормы( AccountIdentificationField + "C",   FIELD_OPTIONAL,  "Read25" , "Fill25C", "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_MANDATORY, "Read72",  "Fill72",  "" );

macro ConstructMT001S( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  if( not ПолучитьФинИнПоISO( MT001S.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT001S.Currency );
    return FALSE;
  end;

  MT001S.FIID = wlfininstr.FIID;
  
  /*Все-таки пользуюсь определением D/K, которое приходит с сообщением GVR??*/
  if (MT001S.DKFlag == "D")
    Payer = {OurBank};
    Receiver = RespID;
    if( not ОпределитьСхемуРасчетовПоКорсчету(Cors, Receiver, MT001S.DebitAcc, MT001S.FIID  ) )
       std.msg("Не определена дебетовая схема расчетов для субъекта " + ПолучитьКодСубъектаПоУмолчанию(wlmes.TpSchemID, Receiver));
       return FALSE;
    end;      
  else
    Payer = RespID;
    Receiver = {OurBank};
    if( not ОпределитьСхемуРасчетовПоКорсчету(Cors, Payer   , MT001S.CreditAcc, MT001S.FIID ) )
       std.msg("Не определена кредитовая схема расчетов для субъекта " + ПолучитьКодСубъектаПоУмолчанию(wlmes.TpSchemID, Payer));
       return FALSE;
    end;
  end;

  /* Отправитель */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MT001S.Sender   = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMT001S */

macro GenDoc( addrMes )
  var error;
  MT001S = МемордерСМФР();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ001S");
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);

  if( not ConstructMT001S( wlmes.OutsideAbonentID ) )
    std.msg("Ошибка при генерации учетных объектов по MT001S");
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1; 
  wlpmpaym.PayerBankID    = Payer;
  wlpmpaym.ReceiverBankID = Receiver;
  wlpmpaym.PayerCode    = ПолучитьКодСубъекта( Payer, PTCK_SMFR, error );
  wlpmpaym.PayerCodeKind  = PTCK_SMFR;
  wlpmpaym.ReceiverCode   = ПолучитьКодСубъекта( Receiver, PTCK_SMFR, error );
  wlpmpaym.ReceiverCodeKind = PTCK_SMFR;
  wlpmpaym.PayAmount      = MT001S.Sum;
  wlpmpaym.ValueDate      = MT001S.ValueDate;

  if (MT001S.DKFlag == "D")
     wlpmpaym.ReceiverAccount  = MT001S.CreditAcc;
  else
     wlpmpaym.PayerAccount     = MT001S.DebitAcc;
  end;

  if( not ПолучитьФинИнПоISO( MT001S.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT001S.Currency );
    return FALSE;
  else
     wlpmpaym.Amount      = wlpmpaym.PayAmount;
     wlpmpaym.BaseAmount  = wlpmpaym.Amount;
     wlpmpaym.OrderAmount = wlpmpaym.BaseAmount;
     wlpmpaym.PayFIID     = wlfininstr.FIID;
     wlpmpaym.FIID        = wlpmpaym.PayFIID;
     wlpmpaym.BaseFIID    = wlpmpaym.FIID;
     wlpmpaym.OrderFIID   = wlpmpaym.BaseFIID;
  end;

  /* Дебетовые свойства платежа */
  if (MT001S.DKFlag == "D")
     wlpmpropdeb.debetcredit  = 0;
  else
     wlpmpropdeb.debetcredit  = 1;
  end;
  
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmpropdeb.TransferDate = {curdate};
  else
     wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  wlpmpropdeb.Corschem = Cors;
  
  if (MT001S.DKFlag == "D")
     wlpmpropdeb.CorrAcc  = MT001S.DebitAcc;
     else
     wlpmpropdeb.CorrAcc  = MT001S.CreditAcc;
  end;

     wlpmpropdeb.CodeKind = MT001S.Sender.CodeKind;
     wlpmpropdeb.BankCode = MT001S.Sender.CodeBank;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = MT001S.TRF;
  wlpmrmprop.PayDate               = MT001S.ValueDate;
  wlpmrmprop.Date                  = MT001S.ValueDate;
  wlpmrmprop.ClientDate            = MT001S.ValueDate;
  /*wlpmrmprop.PayerBankName       = MT001S.Sender.Name;*/
  wlpmrmprop.PayerBankName         = ПолучитьИмяСубъекта(Payer);
  wlpmrmprop.ReceiverBankName      = ПолучитьИмяСубъекта(Receiver);
  
  /*wlpmrmprop.PayerCorrAccNostro    = MT001S.Sender.Account;
  wlpmrmprop.PayerName             = ПолучитьИмяСубъекта(Payer);
  wlpmrmprop.ReceiverName          = ПолучитьИмяСубъекта(Receiver);*/

  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT001S.RcvInfo.REC;
  wlpmrmprop.Ground                = MT001S.RcvInfo.Str;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "01";
  wlpmrmprop.ComissCharges         = PM_CHRG_OUR;  
    
  /*Если платеж дебетовый, то заполняем кредитовые свойства, иначе - дебетовые*/
  if (MT001S.DKFlag == "D")
     wlpmpropcred.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropcred.PayFIID, 1, "Д", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
     if(wlpmpropcred.Corschem != -1)
       wlpmpropcred.CorrPosType = PM_CORRPOS_TYPE_USER;
     end;   
     ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, 0, wlpmpropdeb, wlpmrmprop );
     if( not ВставитьПлатеж( wlpmpaym, 0, wlpmpropdeb, wlpmrmprop, PRT_Debet ) )
       std.msg("Ошибка при сохранении платежа");
       return FALSE;
     end;
  else
     wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
     if(wlpmpropdeb.Corschem != -1)
       wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
     end;

     ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, 0, wlpmrmprop );
     if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, 0, wlpmrmprop ) )
       std.msg("Ошибка при сохранении платежа");
       return FALSE;
     end;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT001S.OrdBank.GetRoute() );
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT001S.Sender.GetRoute(), true );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
