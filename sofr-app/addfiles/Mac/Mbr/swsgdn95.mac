/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по SWIFT-SB MTn95                                     */
/*                                                                          */
/*  Имя файла: swsgdn95.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Запрос */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */  
  Queries          :string,        /* Текст запроса  */
  Narrative        :string,
  InitFormName     :string,        /* Форма инициирующего сообщения */
  InitDate         :date,          /* Дата инициирующего сообщения */
  Description      :string,   

  Sender           :Bank;          /* Отправитель */

  TRF              = "";
  RelatedRef       = "";
  Queries          = "";

  Narrative = "";
  Description = "";
  InitDate = date( 0, 0, 0 );
  InitFormName = "";

end; /* class StatementMessage */

var MTn95 : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro FillRT(SubType)
  return true;
end;

macro Fill20( TRF )
  MTn95.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MTn95.RelatedRef = TRF;
  return TRUE;
end;

macro Fill75( Str )
  MTn95.Queries = str;
  return TRUE;
end;

macro Fill77A( Str )
  MTn95.Narrative = Str;
  return TRUE;
end;

macro Fill11( FormName, Date)
  MTn95.InitFormName = FormName;
  MTn95.InitDate = Date;
  return TRUE;
end;

macro Fill79( Str )
  MTn95.Description = Str;
  return TRUE;
end;

macro FillCopy( Str )
  return TRUE;
end;

/* Заполнение списка полей формы  MTn95*/
var FldSN      = ТПолеФормы( УникальныйНомерСообщенияСМФР,    FIELD_MANDATORY, "ReadTRF",     "FillSN",   "" ),
    FldRN      = ТПолеФормы( НомерСвязанногоСообщенияСМФР,    FIELD_OPTIONAL,  "ReadTRF",     "FillRN",   "" ),
    FldRT      = ТПолеФормы( ТипИПодтипСообщения,             FIELD_OPTIONAL,  "ReadRT",      "FillRT",   "" ),
    Fld20      = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF",     "Fill20",   "" ),
    Fld21      = ТПолеФормы( RelatedReferenceField,           FIELD_MANDATORY, "ReadTRF",     "Fill21",   "" ),
    Fld75      = ТПолеФормы( QueriesField,                    FIELD_MANDATORY, "Read75_76",   "Fill75",   "" ),
    Fld77A     = ТПолеФормы( NarrativeField,                  FIELD_OPTIONAL,  "Read77ASB",     "Fill77A",  "" ),
    Fld11R     = ТПолеФормы( MTandDateOriginalMessage_RField, FIELD_OPTIONAL,  "Read11SB",    "Fill11",   "" ),
    Fld11S     = ТПолеФормы( MTandDateOriginalMessage_SField, FIELD_OPTIONAL,  "Read11SB",    "Fill11",   "" ),
    Fld79      = ТПолеФормы( NarrativeDescriptionField,       FIELD_OPTIONAL,  "Read79",      "Fill79",   "" ),
    FldMFIELDS = ТПолеФормы( CopyMandatoryFields,             FIELD_OPTIONAL,  "ReadMFIELDS", "FillCopy", "" );

macro ConstructMTn95( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MTn95.Sender = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);;

  return TRUE;
end; /* ConstructMTn95 */

macro GenDoc( addrMes )
  MTn95 = StatementMessage();
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса по МТn95");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlreq);

  if( not ConstructMTn95( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlreq.TRN          = MTn95.TRF;
  wlreq.RelatedRef   = MTn95.RelatedRef;
  wlreq.Queries      = MTn95.Queries;
  wlreq.InitDateMes  = MTn95.InitDate;
  wlreq.InitFormIDMes = GetFormIDByName(MTn95.InitFormName,TRANSP_SWIFTSB);
  if ( not НайтиКорсхемуПоКонтрагенту(wlmes.OutsideAbonentID, wlreq.Corschem, wlreq.FIID) )
      std.msg( "Не определена схема расчетов" );
      return FALSE;
  end;  
  
  if( not ВставитьЗапрос( wlreq, MTn95.Narrative, MTn95.Description ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
