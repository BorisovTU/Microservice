/*
$Name:         ufgd465.mac
$Module:       Межбанковские расчеты
$Description:  Генерация учетного объекта ED465
*/

// ED465 Извещение о состоянии ЭСИС (пакета ЭСИС)

import "ufgendoc.mac";

private const TYPE_CODE_UFBS_ED465 = 79;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED463" );

  ClearRecord( wlreq );
  
  var FieldName, FieldValue, BlockName;
  var EDNo, EDDate, EDAuthor, EDReceiver, Annotation, CtrlCode, RefEDNo, 
      RefEDDate, RefEDAuthor, InitialED, InitEDNo, 
      InitEDDate, InitEDAuthor, ErrorDiagnostic, StatusStateCode;
  
  while( СчитатьПоле(FieldName, FieldValue, BlockName) )
    if( BlockName == "" )
      if( FieldName == "CtrlCode" )
        CtrlCode = FieldValue;
      elif( FieldName == "EDNo" )
        EDNo = FieldValue;
      elif( FieldName == "EDDate" )
        EDDate = FieldValue;
      elif( FieldName == "EDAuthor" )
        EDAuthor = FieldValue;
      elif( FieldName == "EDReceiver" )
        EDReceiver = FieldValue;
      elif( FieldName == "StatusStateCode" )
        StatusStateCode = FieldValue;
      end;
    elif( BlockName == "Annotation" )
      if( FieldName == "Annotation" )
        Annotation = FieldValue;
      end;
    elif( BlockName == "EDRefID" )
      if( FieldName == "EDNo" )
        RefEDNo = FieldValue;
      elif( FieldName == "EDDate" )
        RefEDDate = FieldValue;
      elif( FieldName == "EDAuthor" )
        RefEDAuthor = FieldValue;
      end;
    elif( BlockName == "InitialED" )
      InitialED = true;
      if( FieldName == "EDNo")
        InitEDNO = FieldValue;
      elif( FieldName == "EDDate" )
        InitEDDate = FieldValue;
      elif( FieldName == "EDAuthor" )
        InitEDAuthor = FieldValue;
      end;
    elif( BlockName == "ErrorDiagnostic" )
      if( FieldName == "ErrorDiagnostic" )
        ErrorDiagnostic = FieldValue;
      end;
    end;
  end;

  
  if( strlen(wlmes.RelatedRef) == 0 )
    RunError("Не найдено исходящее сообщение");
  end;
  
  var CountMes = 0;
  var CountReq = 0;
  var Error = RsbWlError();
  var ErrDescription = "";
  
  var rs = execSQLSelect( " Select out.t_MesID, out.t_Kind "
                          "   From dwlmes_dbt out, dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
                          "  Where out.t_Trn = :RelatedRef "
                          "    and out.t_Direct = chr(0) "
                          "    and out.t_RlsFormID = rls.t_RlsFormID "
                          "    and rls.t_FormID = frm.t_FormID "
                          "    and frm.t_TpID = :TpID ",
                          MakeArray( SQLParam("RelatedRef", wlmes.RelatedRef),
                                     SQLParam("TpID", 9)));
                                     
  if( rs and rs.MoveNext() )
    CountMes = CountMes + 1;
  else
    rs = execSQLSelect( " Select out.t_MesID, out.t_Kind "
                        "   From dwlmes_dbt out, dwlmesrls_dbt rls, dwlmesfrm_dbt frm, dwlsess_dbt wlsess "
                        "  Where out.t_SessionID = wlsess.t_SessionID "
                        "    and out.t_Direct = chr(0) "
                        "    and out.t_RlsFormID = rls.t_RlsFormID "
                        "    and rls.t_FormID = frm.t_FormID "
                        "    and frm.t_TpID = :TpID "
                        "    and wlsess.t_SessUID = :RelatedRef ",
                        MakeArray( SQLParam("TpID", 9),
                                   SQLParam("RelatedRef", wlmes.RelatedRef)));
  end;
  
  while( rs and rs.MoveNext() )
    CountMes = CountMes + 1;
  end;
  
  if( InitialED )
    var Trn = EDNoDateAuthor();
    Trn.EDNo = InitEDNo;
    Trn.EDDate = ToDate(InitEDDate);
    Trn.EDAuthor = InitEDAuthor;
    
    var req = execSQLSelect( " Select req.t_MesID, req.t_Kind "
                             "   From dwlmes_dbt req, dwlmesrls_dbt rls, dwlmesfrm_dbt frm "
                             "  Where req.t_Trn = :RelatedRef "
                             "    and req.t_Direct = chr(0) "
                             "    and req.t_RlsFormID = rls.t_RlsFormID "
                             "    and rls.t_FormID = frm.t_FormID "
                             "    and frm.t_TpID = :TpID ",
                             MakeArray( SQLParam("RelatedRef", Trn.GetTrn()),
                                        SQLParam("TpID", 9)));
    if(req and req.MoveNext())
      CountReq = CountReq + 1;
    else
      msgbox(string("Не найдено исходящее сообщение № " + InitEDNo + " от " + InitEDDate));
    end;
  end;
  
  
  
  
  wlreq.TRN = wlmes.TRN;
  wlreq.RelatedRef = wlmes.RelatedRef;
  wlreq.Direct = "X";
  wlreq.Kind = MESKIND_ANSWER; // Ответ
  wlreq.OriginatorCode = EDAuthor;
  wlreq.OriginatorCodeKind = PTCK_UIS;
  wlreq.OriginatorID = GetCodeOwnerID(PTCK_UIS, wlreq.OriginatorCode);
  wlreq.OriginatorName = Bnk_GetPartyName(wlreq.OriginatorID);
  wlreq.RecipientCode = EDReceiver;
  wlreq.RecipientCodeKind = PTCK_UIS;
  wlreq.RecipientID = GetCodeOwnerID(PTCK_UIS, wlreq.RecipientCode);
  wlreq.RecipientName = Bnk_GetPartyName(wlreq.RecipientID);
  wlreq.OfficeCode = wlmes.InsideAbonentCode;
  wlreq.OfficeCodeKind = wlmes.InsideAbonentCodeKind;
  wlreq.OfficeID = wlmes.InsideAbonentID;
  wlreq.InitDateMes = ToDate(RefEDDate);
  wlreq.InitFormIDMes = НайтиФормуСообщенияПоРеференсу( wlreq.RelatedRef, 9 );
  
  
  wlreq.Queries = string(StatusStateCode + "; " + ПолучитьКодОтветаНаСообщение(StatusStateCode, TYPE_CODE_UFBS_ED465));

  var Description = string(CtrlCode + "; " + Annotation);
  
  ВставитьОтвет( wlreq, Description );
  
  RECORD wlreq(wlreq);
  ClearRecord(wlreq);
  var sReqID = "";
  
  
  if( (StatusStateCode == "01") or (StatusStateCode == "21") )
    if( CountMes > 0 )
      ОшибкаЛогическогоКонтроляСообщения(int(rs.value("t_MesID")), int(StatusStateCode), ПолучитьКодОтветаНаСообщение( StatusStateCode, TYPE_CODE_UFBS_ED465 ), 100);
      if( rs.value("t_Kind") == WLD_MES_KIND_REQUEST )
        var Obj = execSQLSelect( " Select lnk.t_ObjID, lnk.t_ObjKind "
                                 "   From dwlmeslnk_dbt lnk "
                                 "  Where lnk.t_MesID = :MesID ",
                                 MakeArray( SQLParam("MesID", rs.value("t_MesID")) )
                              );
        if( obj and obj.MoveNext() )
          if( obj.value("t_ObjKind") == OBJTYPE_REQ )
            wlreq.ReqID = obj.value("t_ObjID");
            sReqID = makeObjectID(OBJTYPE_REQ, null, wlreq);
            var Note = string("Заявка аннулирована на основании сообщения ED465 номер " + EDNo + " от " + EDDate + ". Референс входящего сообщения " + wlmes.Trn);          
            AddNoteForObject(OBJTYPE_REQ, sReqID, 6 /* Результат обработки у адресата */, Note );
          end;
        end;
      end;
    end;
  end;
  
  if( CountReq > 0 )
    if( not((StatusStateCode == "01") or (StatusStateCode == "21")) )
      if( CountMes > 0 )
        ОшибкаЛогическогоКонтроляСообщения(req.value("t_MesID"), int(StatusStateCode), ПолучитьКодОтветаНаСообщение( StatusStateCode, TYPE_CODE_UFBS_ED465 ), 100);
        if( req.value("t_Kind") == WLD_MES_KIND_REQUEST )
          var Obj2 = execSQLSelect( " Select lnk.t_ObjID, lnk.t_ObjKind "
                                    "   From dwlmeslnk_dbt lnk "
                                    "  Where lnk.t_MesID = :MesID ",
                                    MakeArray( SQLParam("MesID", req.value("t_MesID")) )
                                );
          if( obj2 and obj2.MoveNext() )
            if( obj2.value("t_ObjKind") == OBJTYPE_REQ )
              var Note2 = string(StatusStateCode + "; " + ПолучитьКодОтветаНаСообщение(StatusStateCode, TYPE_CODE_UFBS_ED465)
                         + "\n\n" + CtrlCode + "; " + Annotation);
              if( strlen(ErrorDiagnostic) > 0 )
                Note2 = Note2 + "\n\n" + ErrorDiagnostic;
              end;
              wlreq.ReqID = obj2.value("t_ObjID");
              sReqID = makeObjectID(OBJTYPE_REQ, null, wlreq);
              AddNoteForObject(OBJTYPE_REQ, sReqID, 6 /* Результат обработки у адресата */, Note2 );
            end;
          end;
        end;
      end;
    else
      ВставитьПодтверждениеОДоставке( req.value("t_MesID"), null, null, ПолучитьКодОтветаНаСообщение(StatusStateCode, TYPE_CODE_UFBS_ED465), true, int(StatusStateCode), WLD_STATUS_MES_CLOSED, null, null, null, 100 );
    end;
  end;
  
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

macro GenDocV2( addrMes )
   return GenDoc( addrMes );
end;
