/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Константы и вспомогательные функции для генерации сообщений МЦИ          */
/*                                                                          */
/*  Имя файла: wlmcgm.mac                                                   */
/*  Создан:  14.08.00                                               AVM     */
/****************************************************************************/
import "wldoc.mac", "wlgenmes.mac", "wlmctool.mac";

const ПутьФлагаЗащитногоКода = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ПЕРЕМЕННЫЕ\\USEPROTECTIVECODE";
var UseProtectiveCode = FALSE ; /* Флаг - использовать защитный код, по умолчанию - использовать */

const Числовое_Поле = true;

/* Десятизначный цифровой уникальный код составителя для МЦИ
   int(НашКодСоставителя) != 0
*/ const НашКодСоставителя = "9876543210";

var СтрокаДокумента = "";

/*
        Замена в строке знаков пробел знаками '0'
*/
macro Заменить_пробелы_нулями( Строка )
  var i;

  i = index( Строка, " " );
  while( i )
    strset( Строка, i, "0" );
    i = index( Строка, " " );
  end;
  return Строка;
end;

macro Форматный_вывод_в_строку( СтрокаВывода, Число, Длина, i)
  var Строка, Число_ведущих_пробелов, Строка_нулей;
  if( valtype( Число ) == V_STRING )
/*      Первым параметром передана строка - отбросить ведущие пробелы   */
    Строка = Число;
    while( index( Строка, " " ) == 1 )
      Строка = substr( Строка, 2 );
    end;
  else
    Строка = string( Число:0:0 );
  end;
  Число_ведущих_пробелов = Длина - strlen( Строка );
  if( Число_ведущих_пробелов < 0 )
/*      Если строка длинней поля вывода - обрезать конец строки         */
    Число_ведущих_пробелов = 0;
    Строка = substr( Строка, 1, Длина );
  end;
                                        /* А не числовое ли это поле? */
  if ( (valtype(Число) == V_STRING) and ( not ( (valtype(i) == V_BOOL) and i) ) )
     Строка_нулей = mkstr( " ", Число_ведущих_пробелов );
     return СтрокаВывода+Строка+Строка_нулей;
  else
     Строка_нулей = mkstr( "0", Число_ведущих_пробелов );
     return СтрокаВывода+Строка_нулей+Строка ;
  end;
end;

macro Платеж_ФОР(RsPaym)
  if (
      (
          ( SubStr(RsPaym.PayerAccount,1,5) == "30202" )
       OR ( SubStr(RsPaym.PayerAccount,1,5) == "30204" )
      )
      OR 
      (
          ( SubStr(RsPaym.ReceiverAccount,1,5) == "30201" )
       OR ( SubStr(RsPaym.ReceiverAccount,1,5) == "30203" )
      )
     )
    return true;
  end;
  return false;
end;

/*****************************************************************************
           Формирование защитного кода (определяется договором с МЦИ)
******************************************************************************/
macro ЗащитныйКод()
  var ПустойКод;

  ПустойКод = mkstr( " ", 196 ) ;
  return ПустойКод;
end;


macro GetProtectiveCodeMode()

  var Error, UseProtectiveCodeReg;

  GetRegistryValue( ПутьФлагаЗащитногоКода, V_BOOL, UseProtectiveCodeReg, Error );

  if( Error == WLD_REG_OK )
     UseProtectiveCode = UseProtectiveCodeReg;
  end;

end;

/* возвращает n последних символов из строки */
macro Последние_Символы( str, n )
  var i = strlen(str) - n + 1;
  if( i > 0 )
    return substr( str, i );
  else
    return str;
  end;
end;

/* Эту функцию можно было сделать проще (только для наших значений вида обработки),
   но решено было сделать ее в общем виде */
macro ПроверитьВидОбработкиИВидОперации( ВидПлатежа, ВидОбработки, ВидОперации, StringError )
    var strErr = "";
    var KindProcessing = TArray;
    var KindOperation = TArray;

    if ( strlen(ВидОбработки)<2 )
        ВидОбработки = "0"+ВидОбработки;
    end;

    if ( strlen(ВидОперации)<2 )
        ВидОперации = "0"+ВидОперации;
    end;

    /* Разрешенные виды обработки для вида платежа */
    KindProcessing(0) = "00 21 22 23 26 28 31 32 33 38 37";
    KindProcessing(1) = "01 10 11 12 14 15";

    /* Разрешенные виды операции для вида обработки */
    KindOperation(0) = "02 03 04 05 06 07 08 09 10 11 16 45 81";
    KindOperation(21) = "02 03 04 05 06 07 08 09 10 11 14 42 45 79 81";
    KindOperation(22) = KindOperation(21);
    KindOperation(23) = KindOperation(21);
    KindOperation(26) = KindOperation(21);
    KindOperation(28) = KindOperation(21);
    KindOperation(31) = "09 12 14 15 45 79 80 81";
    KindOperation(32) = KindOperation(31);
    KindOperation(33) = KindOperation(31);
    KindOperation(38) = KindOperation(31);
    KindOperation(37) = "02 03 04 05 06 07 08 09 10 11 12 14 15 16 81";
    KindOperation(1)  = "01 06 16";
    KindOperation(10) = "01";
    KindOperation(11) = "01";
    KindOperation(12) = "01";
    KindOperation(14) = "01";
    KindOperation(15) = "01";

    if ( int(ВидПлатежа)>1 )
        strErr = "Неизвестный вид платежа";
    end;

    if ( strErr=="" )
       if ( (valtype(KindProcessing(int(ВидПлатежа)))==V_UNDEF) OR 
            (not index(KindProcessing(int(ВидПлатежа)), ВидОбработки)) )
          strErr = "Неправильный вид обработки";
       end;
    end;

    if ( strErr=="" )
       if ( (valtype(KindOperation(int(ВидОбработки)))==V_UNDEF) OR 
            (not index(KindOperation(int(ВидОбработки)), ВидОперации)) )
          strErr = string( "Вид операции ", ВидОперации, " не соответствует виду обработки ", ВидОбработки );
       end;
    end;

    if ( valtype(StringError)!=V_UNDEF )
       setparm( 3, strErr );
    end;

    if ( strErr=="" )
       return true;
    end;


    return false;
end;

macro ПроверитьНомерДокумента( number:string )
   var Shablon = "0123456789", i;
   if ( strlen(number)==0 ) 
      return false; 
   end;

   i = strlen(number);
   while( i>0 )
      if ( index(Shablon,substr(number,i,1))==0 )
         return false;
      end;
      i = i-1;
   end;

   return int(number)!=0;
end;