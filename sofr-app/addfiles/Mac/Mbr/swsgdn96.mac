/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по SWIFT-SB MTn96                                     */
/*                                                                          */
/*  Имя файла: swsgdn96.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Ответ */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */  
  Answers          :string,        /* Текст ответа  */
  Narrative        :string,
  InitFormName     :string,        /* Форма инициирующего сообщения */
  InitDate         :date,          /* Дата инициирующего сообщения */
  Description      :string,   

  Sender           :Bank;          /* Отправитель */

  TRF              = "";
  RelatedRef       = "";
  Answers          = "";
  InitFormName     = "";
  InitDate         = date(0,0,0);

  Narrative = "";
  Description = "";

end; /* class StatementMessage */

var MTn96 : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro FillRT(SubType)
  return true;
end;

macro Fill20( TRF )
  MTn96.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MTn96.RelatedRef = TRF;
  return TRUE;
end;

macro Fill76( Str )
  MTn96.Answers = str;
  return TRUE;
end;

macro Fill77A( Str )
  MTn96.Narrative = Str;
  return TRUE;
end;

macro Fill11( FormName, Date)
  MTn96.InitFormName = FormName;
  MTn96.InitDate = Date;
  return TRUE;
end;

macro Fill79( Str )
  MTn96.Description = Str;
  return TRUE;
end;

macro FillCopy( Str )
  return TRUE;
end;

/* Заполнение списка полей формы  MTn96*/
var FldSN      = ТПолеФормы( УникальныйНомерСообщенияСМФР,    FIELD_MANDATORY, "ReadTRF",     "FillSN",   "" ),
    FldRN      = ТПолеФормы( НомерСвязанногоСообщенияСМФР,    FIELD_OPTIONAL,  "ReadTRF",     "FillRN",   "" ),
    FldRT      = ТПолеФормы( ТипИПодтипСообщения,             FIELD_OPTIONAL,  "ReadRT",      "FillRT",   "" ),
    Fld20      = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF",     "Fill20",   "" ),
    Fld21      = ТПолеФормы( RelatedReferenceField,           FIELD_MANDATORY, "ReadTRF",     "Fill21",   "" ),
    Fld76      = ТПолеФормы( AnswersField,                    FIELD_MANDATORY, "Read75_76",   "Fill76",   "" ),
    Fld77A     = ТПолеФормы( NarrativeField,                  FIELD_OPTIONAL,  "Read77ASB",   "Fill77A",  "" ),
    Fld11R     = ТПолеФормы( MTandDateOriginalMessage_RField, FIELD_OPTIONAL,  "Read11SB",    "Fill11",   "" ),
    Fld11S     = ТПолеФормы( MTandDateOriginalMessage_SField, FIELD_OPTIONAL,  "Read11SB",    "Fill11",   "" ),
    Fld79      = ТПолеФормы( NarrativeDescriptionField,       FIELD_OPTIONAL,  "Read79",      "Fill79",   "" ),
    FldMFIELDS = ТПолеФормы( CopyMandatoryFields,             FIELD_OPTIONAL,  "ReadMFIELDS", "FillCopy", "" );

macro ConstructMTn96( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;
  var buf;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MTn96.Sender = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMTn96 */

macro GenDoc( addrMes )
  MTn96 = StatementMessage();
  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация запроса по МТn96");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlreq);

  if( not ConstructMTn96( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlreq.TRN          = MTn96.TRF;
  wlreq.RelatedRef   = MTn96.RelatedRef;
  wlreq.Queries      = MTn96.Answers;
  wlreq.InitDateMes  = MTn96.InitDate;
  wlreq.InitFormIDMes = GetFormIDByName(MTn96.InitFormName,TRANSP_SWIFTSB);
  if ( not НайтиКорсхемуПоКонтрагенту(wlmes.OutsideAbonentID, wlreq.Corschem, wlreq.FIID) )
      std.msg( "Не определена схема расчетов" );
      return FALSE;
  end;  
  
  if( not ВставитьОтвет( wlreq, MTn96.Narrative, MTn96.Description ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
