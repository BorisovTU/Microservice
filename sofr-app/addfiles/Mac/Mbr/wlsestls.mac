/*
   Отчеты по документам сеанса связи
   Общая часть отчетов

   Подсистема - Межбанковские расчеты
   Создан 24.10.2000
   Программист Антон А. Бурцев (AB)

   Генерация отчетов происходит по алгоритму

   InitAllParms();   /* Инициализация общих параметров отчета                         */
                     /* Если вернуть целое ненулевое знпчение - отчет создан не будет */

   /* Запрос даты и номера сеанса */

   InitReport();     /* дополнительная инициализация с учетом номера сеанса           */

   PrintDoc();       /* Печать или сортировка документов. для отчета по платежам и    */
   PrintDoc();       /* подтверждениям система сортирует документы по Д/К, валюте и   */
   PrintDoc();       /* сумме. Если Вы выпускаете сложный отчет, для которого необхо- */
   PrintDoc();       /* другая сортировка - создайте временный файл с необходимыми    */
   .                 /* ключами и заполните его в этом блоке.                         */
   .
   .
   PrintDoc();       

   DoneReport();     /* Завершающие действия по формированию отчета. Если использова- */
                     /* лась пользовательская сортировка во временном файле, то здесь */
                     /* напечатать отчет.                                             */

ПРИМЕЧАНИЯ

1. Если Вид объектов отчета - платежи (MESKIND_PAYMENT),
   то приходт ObjID == pmpaym.PaymentID.

*/

import "wldoc.mac", FIInter;

var ReportObjects;
/*
Вид объекта отчета. Нужно устанавливать в процедуре InitAllParms
Используется для пердварительной сортировки для отчетов
по платежам (MESKIND_PAYMENT) и по подтверждениям (MESKIND_CONF)
Если переменная не определена, то записи в отчет передаются в порядке загрузки
*/


var ReportObjectsTitle;
/*
Название объектов отчета. Появляется в заголовке формы при запросе
номера и даты сеанса.
*/

/******************************************************************/
/*                ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ                       */
/******************************************************************/

file FileWlsess(wlsess) key 0;
var file_wlsess:TRecHandler = TRecHandler("wlsess.dbt", "bank.def");

macro SumToStr(FIID, sum)

   f_wlfininstr.FIID = FIID;
   if ( GetEQ( f_wlfininstr ) )
      return CurToStrAlt( sum, null, null, int(f_wlfininstr.ISO_Number) );
   else
      return string(sum) + " (Неизвестная валюта)";
   end;

end;

macro GetOperStr(oper)

  file person(person);

  person.Oper = oper;
  if ( GetEQ(person) )
     return string(oper) + " " + person.Name;
  else
     return string(oper);
  end;

end;


macro CommonInitReport(Title, SessionID)

   var SessionAction;

   FileWlsess.SessionID = SessionID;
   if ( not GetEQ(FileWlsess) )
      msgbox("Не найден сеанс: SessionID = " + SessionID);
      return 1;
   end;

   if ( FileWlsess.Direct == "X" )
      SessionAction = "Прием"
   else
      SessionAction = "Отправка"
   end;


   println("                           ",Title, " ", FileWlsess.Number);
[
    Тип сеанса              #
    Имя файла сеанса        #
    Дата проведения сеанса  #
    Операционист сеанса     #

    Отчет подготовлен       #
    Операционист            #

]
(
   SessionAction,
   FileWlsess.FileName,
   FileWlsess.BankDate:f,
   GetOperStr(FileWlsess.UserID),

   {curdate}:f,
   GetOperStr({oper})
);

   return 0;

end;

/* Отчет по учетным объектам сеанса: платежам и подтверждениям */
macro PrintDocument(ncopy)
  var SesTmpFileName, OldName; 
  FILE SesTmpFile() txt;

  while( ncopy !=0 )
      ПечатьУчетныхОбъектовСеанса( file_wlsess.rec.SessionID, MESKIND_CONF );
    ncopy = ncopy - 1;
  end;
  return true;
end;
