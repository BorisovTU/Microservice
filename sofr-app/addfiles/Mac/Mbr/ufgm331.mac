/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED331 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm331.mac                                                  */
/*  Создан:    28.09.12  Chukina T.                                         */
/****************************************************************************/

import "ufgenmes.mac", "wluftool.mac";

private macro ParseQueries( Queries: string, // /N/БИК1,БИК2,БИК3...
                            LiquidityInquiryCode: @string, 
                            PURBICInfo: TArray                            
                          )

  var ind1 = 0, ind2 = 0;

  ind1 = index(Queries, "/");
  ind2 = index(Queries, "/", ind1 + 1);
  if( ind1 and ind2 and (ind2-ind1 > 1) )
    LiquidityInquiryCode = substr(Queries, ind1+1, ind2-ind1-1);
  else
    LiquidityInquiryCode = "";
  end;

  if(not ind2)
    ind2 = ind1;
  end;

  var tmpArray : TArray = StrSplit2ByDelimiter( substr(Queries, ind2+1), "," );
  for(var item, tmpArray)
    PURBICInfo[ PURBICInfo.size ] = item;
  end;

  if( not PURBICInfo.size )
    RunError("Не определен БИК ПУР, состояние ликвидности которого запрашивается");
  end;

end;

macro GenMes( addrMes, addrReq )

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  FillEDNoDateAuthorByRef_CompoundRls(wlmes.TRN);

  // Разобрать поле wlreq.Queries
  var LiquidityInquiryCode = "";
  var PURBICInfo : TArray = TArray();
  ParseQueries(wlReq.Queries, @LiquidityInquiryCode, PURBICInfo);

  if(LiquidityInquiryCode)
    ЗаписатьПолеЛог( "LiquidityInquiryCode", LiquidityInquiryCode );
  end;

  for( var BIC, PURBICInfo )    
    УстановитьКонтекстБлокаЛог( "PURBICInfo" );
    ЗаписатьПолеЛог( beginField, "PURBICInfo" );
    ЗаписатьПолеЛог( "BIC", BIC );    
    ЗаписатьПолеЛог( endField, "PURBICInfo" ) ;
    УстановитьКонтекстБлокаЛог( ".." );
  end;

  /*if(wlmes.RelatedRef)
    УстановитьКонтекстБлокаЛог( "EDRefID" );
    ЗаписатьПолеЛог( beginField, "EDRefID" );

    FillEDNoDateAuthorByRef_CompoundRls(wlmes.RelatedRef);

    ЗаписатьПолеЛог( endField, "EDRefID" ) ;
    УстановитьКонтекстБлокаЛог( ".." );
  end;*/
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;