import "wldoc.mac";

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

const RPO_DOCKIND = 455,
      ROO_DOCKIND = 456;

macro DrawReport( ObjKind )
  var rs:object;
  var select:string;
  var params:TArray;
  var i, j, prevMesID:integer;
  array StringArray;


  file f_person( person );

  select = "select mes.t_MesID, rep.t_TRN, rep.t_SessDate, rep.t_SessTime, rep.t_Account, rep.t_Message, wlst.t_Name "+
             "from dwlrepclm_tmp rep, dwlmes_dbt mes, dwlstatus_dbt wlst "+
            "where rep.t_ObjKind = :ObjKind "+
              "and mes.t_MesID = rep.t_MesID "+
              "and wlst.t_State = mes.t_State "+
              "and wlst.t_TypeState = 1 "+
            "order by mes.t_MesID";

  params = makeArray( SQLParam("ObjKind", ObjKind) );
  rs = execSQLselect( select, params, false );

  if( not rs.moveNext() )
    return;
  end;

  f_person.Oper = {oper};
  if( not GetEQ(f_person) )
    return;
  end;

  [                                                                               ];
  if( ObjKind == RPO_DOCKIND ) 
    [   Протокол процедуры генерации сообщений о приостановлении операций по счетам ];
  elif( ObjKind == ROO_DOCKIND )
    [   Протокол процедуры генерации сообщений об отмене приостановления операций по счетам ];
  end;
  [   Дата отчёта: ##########                                                     ]({curdate});
  [   Исполнитель: # #                                                            ]({oper}, f_person.Name);
  [                                                                               ];
  if( ObjKind == RPO_DOCKIND ) 
    [┌──────────────────────────┬──────────┬───────────┬────────────────────────┬───────────────────────────────┬────────────┐
     │    Референс сообщения    │Дата дост.│Время дост.│ Сформированы претензии │       Причина ошибки          │   Статус   │
     │                          │          │           │      по счетам         │                               │ сообщения  │];
  elif( ObjKind == ROO_DOCKIND )
    [┌──────────────────────────┬──────────┬───────────┬────────────────────────┬───────────────────────────────┬────────────┐
     │    Референс сообщения    │Дата дост.│Время дост.│ Счета, по которым      │ Результат обработки претензии │   Статус   │
     │                          │          │           │ отменяется приост-ние  │            к счету            │ сообщения  │];
  end;

  i = 1; prevMesID = 0;
  while( (i == 1) or rs.moveNext() )

    if( rs.value(0) != prevMesID )
      [├──────────────────────────┼──────────┼───────────┼────────────────────────┼───────────────────────────────┼────────────┤];
    end;

    StrSplit( StrSubst(IfThenElse(rs.value(5)!=nullVal,rs.value(5)," ") , "|", " " ), StringArray, 32 );                

    [│##########################│##########│###########│########################│###############################│############│]
    (
      IfThenElse( rs.value(0) != prevMesID, rs.value(1), "" ),
      IfThenElse( rs.value(0) != prevMesID, date(rs.value(2)):c, "" ),    
      IfThenElse( rs.value(0) != prevMesID, time(rs.value(3)):c, "" ),
      ifThenElse( rs.value(4) != nullVal,   rs.value(4):c, "" ),
      StringArray(0),
      IfThenElse( rs.value(0) != prevMesID, rs.value(6), "" )
    );

    j = 1;
    while ( StrLen(StringArray(j) ) > 0)
      [│                          │          │           │                        │###############################│            │]
      (StringArray(j));
      j = j + 1;
    end;
    
    prevMesID = rs.value(0);
    i = i + 1;
  end;

  [└──────────────────────────┴──────────┴───────────┴────────────────────────┴───────────────────────────────┴────────────┘];

  execSQL("delete from dwlrepclm_tmp where t_ObjKind = "+ObjKind);

end;

macro PrintRepRPO()
  DrawReport( RPO_DOCKIND );
end;

macro PrintRepROO()
  DrawReport( ROO_DOCKIND );
end;

