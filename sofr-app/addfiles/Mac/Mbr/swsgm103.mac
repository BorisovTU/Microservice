/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT103                                    */
/*                                                                          */
/*  Имя файла: swsgm103.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

record Party(party);
const  Relise = "103";

macro GenMes(addrMes, addrPm,  addrOldMes)
  var field_value, Tag, CurrentCode, error, 
      continue0, field33BPresent = 0;
  var ОстатокПоля70;
  var Recreate = false;
  var RsPaym; 

  RECORD combuf( oprsfcom );    
  Record oldwlMes (wlmes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( Oldwlmes,  addrOldMes );
     Recreate = true;
  end;
  PrintLog(2, "Генерация сообщения по МТ103");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );
  SB_CheckFIID( wlpmpaym.BaseFIID );
  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог(TransactionReferenceNumberField, wlmes.TRN);

  /* 23B       */
  ЗаписатьПолеЛог( BankOperationCodeField_B, BANKOPERCODE_CRED ) ;

  /* 23Е   берем теперь из dwlpmrmprop_dbt.InstructionCode  */
  field_value = "";
  StrChangeRusXSet( RsPaym.InstructionCode, field_value );
  ЗаписатьПолеЛогВБлок( "23E", InstructionCodeField, string(field_value), NULL, Recreate, OldwlMes);

  /* Восстанавливаем нулевой контекст блока */
  if (not УстановитьКонтекстБлока())
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  var PaymentIsTransit:bool = (((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)) OR PM_PaymentIsChildTransit( RsPaym.PaymentID ));  
  
  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate(RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );
/*  ЗаписатьПолеЛог(ValueDateCurrencyCodeAmountField_A, field_value);*/

  /* 33B - Инструктируемая сумма, валюта */
  if( (RsPaym.OrderAmount != 0) and ((RsPaym.OrderAmount != RsPaym.FutureBaseAmount) or 
                                     (RsPaym.OrderFIID != RsPaym.BaseFIID)) 
    )
    field_value = ПолучитьКодВалютыЛог(RsPaym.OrderFIID) + GetSWIFTAmount(RsPaym.OrderAmount);
    WriteFieldEx( CurrencyOriginalOrderedAmountField, field_value, Recreate, OldwlMes );
    field33BPresent = 1;
  end;

  /* 36  - Кросс-курс */
  if( PaymentIsTransit )
    field_value = "";
    // из входящего сообщения
    ПолучитьПоле( GetMessageByPayment( RsPaym.PaymentID ), ExchangeRateField, field_value );

    if( field_value )
      WriteFieldEx(ExchangeRateField, field_value, Recreate, OldwlMes );
    end;
  end;

  /* 50A - приказодатель */
  field_value = FillOrderingCustomerField103_A(RsPaym);
  /* 50K - приказодатель */
  if (field_value == "")
    field_value = FillOrderingCustomerField_SB( RsPaym, NULL );  
    if (field_value=="")
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден плательщик" );
    end;
    ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField_K, field_value, NULL,Recreate, OldwlMes );
  else
    ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField_A, field_value, NULL,Recreate, OldwlMes );
  end;

  /* Восстанавливаем нулевой контекст блока */
  if (not УстановитьКонтекстБлока())
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionFieldA_SB(RsPaym, Tag );
    if (field_value == "")
    field_value = FillOrderingInstitutionFieldD_SB(RsPaym, Tag );
    end;
    ЗаписатьПолеВБлокКонтекст0(OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes);

  /* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_A;
  field_value = FillSenderCorrespondentField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value, Recreate, OldwlMes );

  /* 54a - Корреспондент получателя */
  Tag = SWIFT_Tag_A;
  field_value = FillReceiverCorrespondentField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField+Tag, "54a", field_value, Recreate, OldwlMes );

  /* 55a - Третий рамбурсирующий посрдник */
  Tag = SWIFT_Tag_A;
  field_value = FillThirdReibursBankField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( ThirdReimbursementInstitutionField+Tag, "55a", field_value, Recreate, OldwlMes );    

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value, Recreate, OldwlMes );  

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField_SB(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );      

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCustomerFieldA_SB( RsPaym );/*59A*/
  if (field_value == "")
    field_value = FillBeneficiaryCustomerField_SB( RsPaym , NULL, true);/*59*/
    if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден получатель" );
    end;
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL,Recreate, OldwlMes );
  else
    ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField_A, field_value, NULL,Recreate, OldwlMes );
  end;
  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 70  - информация для бенефициара */
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70 );
  WriteFieldEx  (DetailsOfPaymentField, field_value, Recreate, OldwlMes );

  /* 71A - расходы */
  field_value = GetCommisCode (RsPaym.ComissCharges);
    WriteFieldEx  (DetailsOfChargesField_A, field_value, Recreate, OldwlMes );

  ЗаписатьКомиссииПлатежа( RsPaym, field_value, TRUE );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT103_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_STANDART, ОстатокПоля70 );
  if( field_value != "" )
    WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

