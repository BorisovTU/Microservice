 /*
 $Name: zpsosprn.mac
 $Module: МБР
 $Description: Печать справки об остатках на счетах для ПФР
 */
import "fnsprzall.mac", "mnsznsal.mac";

private var FondName = "",
            FondAdress = "",
            BankName = "",
            RecDate,
            RecNum = 0,
            ClientName = "",
            ClientRegNumPFR = "",
            ClientSubCode = "",
            Account = "",
            AccType = 0,
            AccFI = "",
            AccRest = 0.0,
            OpenDate = 0,
            CloseDate = 0,
            AccNotFound = false,
            Op = "",
            AccountNO = 0,
            RepDay = 0,
            RepMonth = 0,
            RepYear = 0,
            OfficerBank = "",
            Name = "",
            Date,
            RDate;

private macro PrintHeader( this:FNSPrint )

  array BankINN;
  array BankKPP;
  array BIC;
  array RegNum;
  array SubCode;
  array ClientINN;
  array ClientKPP;
  var INN;
  var BankBIC = {MFO_Bank};
  INN = ПолучитьКодСубъекта( this.RepBankID, PTCK_INN );
  StrSplit( RemoveKPP( INN ), BankINN, 1, 1, 10 );
  StrSplit( RemoveINN( INN ), BankKPP, 1, 1, 9  );
  StrSplit( BankBIC, BIC, 1, 1, 9 );
  StrSplit( ClientRegNumPFR, RegNum, 1, 1, 13);
  StrSplit( ClientSubCode, SubCode, 1, 1, 5 );
  StrSplit( this.ИНННП, ClientINN, 1, 1, 12 );
  StrSplit( this.КППНП, ClientKPP, 1, 1, 9 );
  
  [                                                                                                                       Приложение №3
                                                                                                          к постановлению Правления ПФР
                                                                                                            от 14 октября 2015 г. №377п
  ];                                                                                                   
  PrintSplitLine( FondName );                                                                                    
  [                                                                                           (наименование территориального органа ПФР)];   
                                                                                            
  PrintSplitLine( FondAdress );                                                                                 
  [                                                                                                     (адрес места нахождения)        ];
  [                                                                                            
                                                                                              
                                                                 Справка
                                                   об остатках денежных средств на счетах      


   Банк
   (филиал банка)#######################################################################################################################
                 ───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
   (полное или сокращенное название банка(филиала банка) в соответветствии с Книгой государственной регистрации кредитных организаций) 
        ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐                     ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
   ИНН  │#│#│#│#│#│#│#│#│#│#│                 КПП │#│#│#│#│#│#│#│#│#│
        └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘                     └─┴─┴─┴─┴─┴─┴─┴─┴─┘
   
        ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
   БИК  │#│#│#│#│#│#│#│#│#│
        └─┴─┴─┴─┴─┴─┴─┴─┴─┘
   
   В соответствии с запросом территориального органа ПФР от ############ № ###############
                                                            ────────────   ───────────────
   в отношении #######################################################################################################################
               ───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                  (полное наименование организации, Ф.И.О. индивидуального предпринимателя)

                               ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐                  ┌─┬─┬─┬─┬─┐    
  Регистрационный номер       │#│#│#│#│#│#│#│#│#│#│#│#│#│    Код           │#│#│#│#│#│ 
  в территориальном органе ПФР└─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘    подчиненности └─┴─┴─┴─┴─┘ 
                                                             
           ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐        ┌─┬─┬─┬─┬─┬─┬─┬─┬─┐
  ИНН/КИО │#│#│#│#│#│#│#│#│#│#│#│#│    КПП │#│#│#│#│#│#│#│#│#│
           └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘        └─┴─┴─┴─┴─┴─┴─┴─┴─┘
  ]( BankName,
     BankINN[0],BankINN[1],BankINN[2],BankINN[3],BankINN[4],BankINN[5],BankINN[6],BankINN[7],BankINN[8],BankINN[9],
     BankKPP[0],BankKPP[1],BankKPP[2],BankKPP[3],BankKPP[4],BankKPP[5],BankKPP[6],BankKPP[7],BankKPP[8],
     BIC[0],BIC[1],BIC[2],BIC[3],BIC[4],BIC[5],BIC[6],BIC[7],BIC[8],
     RecDate,
     RecNum,
     ClientName,
     RegNum[0],RegNum[1],RegNum[2],RegNum[3],RegNum[4],RegNum[5],RegNum[6],RegNum[7],RegNum[8],RegNum[9],RegNum[10],RegNum[11],RegNum[12],
     SubCode[0],SubCode[1],SubCode[2],SubCode[3],SubCode[4],
     ClientINN[0],ClientINN[1],ClientINN[2],ClientINN[3],ClientINN[4],ClientINN[5],ClientINN[6],ClientINN[7],ClientINN[8],ClientINN[9],ClientINN[10],ClientINN[11],
     ClientKPP[0],ClientKPP[1],ClientKPP[2],ClientKPP[3],ClientKPP[4],ClientKPP[5],ClientKPP[6],ClientKPP[7],ClientKPP[8]
     )
end;

private macro PrintTableHeader()

  [1. Предоставляет информацию об остатках денежных средств на следующих счетах, открытых указанному лицу
  
   ┌─────────────────────────────────┬─────────────────────────┬────────────────────────────────┬──────────────────────┬─────────────┬─────────────┐
   │              1                  │            2            │                 3              │        4             │     5       │      6      │
   ├─────────────────────────────────┼─────────────────────────┼────────────────────────────────┼──────────────────────┼─────────────┼─────────────┤
   │                                 │                         │   цифровой код валюты счета    │   остаток денежных   │дата открытия│дата закрытия│
   │          номер счета            │        вид счета        │(в соответствии с Общероссийским│   средств на счете   │    счета    │    счета    │
   │                                 │                         │     классификатором валют)     │                      │  (дд.мм.гг) │  (дд.мм.гг) │
   ├─────────────────────────────────┼─────────────────────────┼────────────────────────────────┼──────────────────────┼─────────────┼─────────────┤
  ];      
  
end;

private macro PrintTableBottom()
  [└─────────────────────────────────┴─────────────────────────┴────────────────────────────────┴──────────────────────┴─────────────┴─────────────┘
  ];
end;

private macro PrintTable( this:FNSPrint )
  var Account,
      AccType,
      AccFI,
      AccRest,
      OpenDate,
      CloseDate;
  var i = 0,
      Amount = 0,
      GetRest = R2M(this, "GetLnkAccRest"),
      RegVal;
      
  GetRegistryValue( "PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\ПОКАЗ_ПУСТУЮ_ТАБЛИЦУ", V_BOOL, RegVal );
  if( this.Счет.size == 0 )
    if( RegVal == true )
      PrintTableHeader();
  [│              -                  │           -             │               -                │          -           │       -     │       -     │
  ];
      PrintTableBottom();
    end;
    return;
  end;

  
  PrintTableHeader();
  while( i < this.Счет.size )
    var ErrDescr = GetError(this.Счет[i].rec.Account, this.WlRegDecID);
    if( (strlen(ErrDescr) == 0) or 
         (Index(ErrDescr, "Счет закрыт") > 0 )
      )
        Amount = ExecMacro2(GetRest, this.Счет[i].rec.Account, this.Счет[i].rec.Chapter, this.Счет[i].rec.Code_Currency, RDate, this.RepPriority, this.RepClaimID );
        [│   #########################     │#########################│              ###               │ #####################│  ########## │  ########## │
        ](this.Счет[i].rec.Account:c,
          this.Счет[i].rec.TypeAcc:c,
          GetFICode( this.Счет[i].rec.Code_Currency ):c,
          StrSubst(string( Amount:18:2 ), ".", "-" ):c,
          this.Счет[i].rec.Open_Date:c,
          this.Счет[i].rec.Close_Date:c
        );
    end;
    i = i + 1;
  end;
  
  PrintTableBottom();

end;

private macro NotFoundAccounts( this:FNSPrint )
  var i = 0;
  if( this.Счет.size() == 0 )
    return false;
  end;
   while( i < this.Счет.size())
    var ErrDescr = GetError(this.Счет[i].rec.Account, this.WlRegDecID);
    if( (strlen(ErrDescr) > 0)  and ( Index(ErrDescr, "Счет закрыт") == 0 ) )
      return true;
    end;
    i = i + 1;
  end;
  return false;
  
end;

private macro PrintAbsence( this:FNSPrint )
  array ANF; //AccNotFound
  var i = 0;
  if( not NotFoundAccounts( this ) )
    return;
  end;
  [2. Сообщает об отсутствии счетов в банке(филиале банка) у указанного лица
  ];
  while( i < this.Счет.size())
    var ErrDescr = GetError(this.Счет[i].rec.Account, this.WlRegDecID);
    if( not((strlen(ErrDescr) > 0)  and ( Index(ErrDescr, "Счет закрыт") == 0 )) )
      i = i + 1;
      continue;
    end;
    StrSplit( this.Счет[i].rec.Account, ANF, 1, 1, 20 );
  [   ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐
   №  │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│
      └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘
  ]( ANF[0],ANF[1],ANF[2],ANF[3],ANF[4],ANF[5],ANF[6],ANF[7],ANF[8],ANF[9],
     ANF[10],ANF[11],ANF[12],ANF[13],ANF[14],ANF[15],ANF[16],ANF[17],ANF[18],ANF[19]);
    i = i + 1;
  end;
  
end;

private macro PrintBottom( this:FNSPrint )

[ 
  
   Указанная информация предоставляется по состоянию на "##" ######## ####г.
                                                         ──  ──────── ────
   
   Уполномоченный представитель банка(филиала банка) ###################################  ##################################################              ##########
                                                     ───────────────────────────────────  ──────────────────────────────────────────────────  ──────────  ──────────
                                                                (должность)                                   (Ф.И.О)                         (подпись)     (дата)
                                                                
                                                                                                              М.П.
  ]( substr(YYYYMMDD(Date),7,2):c,
     Месяца(substr(YYYYMMDD(Date),5,2)):c,
     substr(YYYYMMDD(Date),1,4):c,
     OfficerBank:c,
     Name:c,
     Date:c    
    );

end;

macro PrintReportInfoAboutRestAccounts( this:FNSPrint, ReqDate:date )
  record wlregdec(wlregdec);
  wlregdec.DecisionID = this.WlRegDecID;
  GetEQ(wlregdec);
  FondName = this.НаимНО;
  FondAdress = ReadNoteForObject(525, makeObjectID(525, NULL, wlregdec), 3 );
  
  var rs = execSQLSelect( "Select NVL(t_Name, t_ShortName) from dparty_dbt where t_PartyID = " + int(this.RepBankID) );
  if( rs.MoveNext() )
    BankName = rs.value(0);
  end;
  RecDate = this.ДатаЗапроса;
  RecNum = this.НомерЗапроса;
  ClientName = this.НаимНП;
  if( strlen(this.ClientRegNum) > 0 )
    ClientRegNumPFR = this.ClientRegNum;
  end;
  if( strlen(this.ClientSubCode) > 0 )
    ClientSubCode = this.ClientSubCode;
  end;
  RDate = ReqDate;
  GetRegistryValue( "PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\СПРАВКИ_ФОНДЫ_ДОЛЖ", V_STRING, OfficerBank );
  GetRegistryValue( "PS\\ФОНДЫ_СПРАВКИ_ВЫПИСКИ\\СПРАВКИ_ФОНДЫ_ФИО", V_STRING, Name );
  Date = {curdate};
  
  PrintHeader( this );
  PrintTable( this );
  PrintAbsence( this );
  PrintBottom( this );
  return 0;
end;
