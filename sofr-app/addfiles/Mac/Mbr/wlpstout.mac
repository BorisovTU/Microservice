/*
$Name:           wlpstout.mac
$Module:         Межбанковские расчеты
$Description:    Экспорт сообщений по транспорту Почта
*/

import "wlexport.mac", "swtools.mac", "adress.mac";

macro StringToDate( str )
   return date( int(substr(str,1,2)), int(substr(str,4,2)), int(substr(str,7)) );
end;

class FieldsMes( СтрокаДокумента )
   var ShifrOperation, Number, DatePay, Sum, PayerAccount,
       ReceiverAccount, ReceiverCorrAccNostro, ReceiverBankCode, Priority;

   macro FillFields( СтрокаДокумента )
       ShifrOperation = substr(СтрокаДокумента, 1, 2); 
       Number  = Trim( substr(СтрокаДокумента,3,15) );
       DatePay = StringToDate( substr(СтрокаДокумента, 18, 10) );
       Sum     = moneyl( substr(СтрокаДокумента, 28, 16) );
       PayerAccount = Trim( substr(СтрокаДокумента, 44, 20) );
       ReceiverBankCode = Trim( substr(СтрокаДокумента, 64, 9) );
       ReceiverCorrAccNostro = Trim( substr(СтрокаДокумента, 73, 20) );
       ReceiverAccount = Trim( substr(СтрокаДокумента, 93, 20) );
       Priority = substr(СтрокаДокумента, 113, 2);
   end;   
end;

array Payments;

macro СохранитьСообщение()
  var field, buff;
  var paym = FieldsMes;

  if( СчитатьПоле( field, buff ) )
     paym.FillFields( buff );
     Payments[asize(Payments)] = paym;
  else
     ErrExport("Не прочитаны поля сообщения");
     return false;
  end;
  return true;
end;

macro GetNameBank( RespID )
   FILE Party( party ) key 0;
   party.PartyID = RespID;
   if ( getEQ(party) )
       return party.Name;
   else return "Не найден";
   end;
end;

macro GetShortNameBank( RespID )
   FILE Party( party ) key 0;
   party.PartyID = RespID;
   if ( getEQ(party) )
       return party.ShortName;
   else return "Не найден";
   end;
end;

macro GetPlaceBank( RespID )
   record RecordAdress ( adress );
   if ( НайтиЮридическийАдресСубъекта(RespID,RecordAdress) )
       return RecordAdress.Place;
   else return "";
   end;
end;

macro DateToStr( pDate )
   var str, День, Месяц, Год;
   array month;
   month[1]  = "января";
   month[2]  = "февраля";
   month[3]  = "марта";
   month[4]  = "апреля";
   month[5]  = "мая";
   month[6]  = "июня";
   month[7]  = "июля";
   month[8]  = "августа";
   month[9]  = "сентября";
   month[10] = "октября";
   month[11] = "ноября";
   month[12] = "декабря";

   datesplit( pDate, День, Месяц, Год ) ;

   if ( Год<80 )
     Год = Год + 2000;
   elif ( Год<100 )
     Год = Год + 1900;
   end;

   День = string( День:2 );
   День = strfor( 34 ) + StrSubst( День, " ", "0" ) + strfor( 34 );
   str = string( День, " ", month[Месяц], " ", Год, " г." ); 
   return str;
end;

macro FormatDate( pDate )
   var str, День, Месяц, Год;

   datesplit( pDate, День, Месяц, Год ) ;

   if ( Год<80 )
     Год = Год + 2000;
   elif ( Год<100 )
     Год = Год + 1900;
   end;

   str = StrSubst( string(День:2, ".", Месяц:2, ".", Год:4), " ", "0" ); 
   return str;
end;

macro ЗаписатьСводноеПлатежноеПоручение( Документов, СуммаДокументов, Kind, rec_wlmes )
  var i, N, error, КодУчастникаКорр;
  array strings;

  if ( (rec_wlmes.OutsideAbonentCodeKind==PTCK_BIC) AND (rec_wlmes.OutsideAbonentCode!="") )
     КодУчастникаКорр = rec_wlmes.OutsideAbonentCode;
     error = 0;
  else
     КодУчастникаКорр = ПолучитьОткрытыйКодСубъекта( rec_wlmes.OutsideAbonentID, PTCK_BIC, error );
  end;

  if ( error )
     ErrExport("Не найден код получателя сообщения");
     return false;
  end;

  strings[asize(strings)] = "                                                              ┌───────┐";
  strings[asize(strings)] = "    " + FormatDate({curdate}) + "                                                │0401060│";
  strings[asize(strings)] = "───────────────────                                           └───────┘";
  strings[asize(strings)] = " Пост. в банк плат.";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "СВОДНОЕ";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "ПЛАТЕЖНОЕ ПОРУЧЕНИЕ N " + string(wlsess.Number:6) + "             " + FormatDate({curdate}) + "       " + string(Kind:13:c);
  strings[asize(strings)] = "                                         ──────────       ─────────────";
  strings[asize(strings)] = "                                            Дата           Вид платежа ";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "Сумма   │";
  strings[asize(strings)] = "прописью│ " + RubToStr( СуммаДокументов );
  strings[asize(strings)] = "        │";
  strings[asize(strings)] = "────────┴─────────────────────────────┬────────┬──────────────────────────";
  strings[asize(strings)] = "ИНН                                   │Сумма   │ " + string(СуммаДокументов);
  strings[asize(strings)] = "                                      ├────────┼──────────────────────────";
  strings[asize(strings)] = "                                      │Сч. N   │";
  strings[asize(strings)] = "Плательщик " + string(({Name_Bank}+" "+GetPlaceBank({OurBank})):27) + "│        │ " + {CORAC_Bank};
  strings[asize(strings)] = "──────────────────────────────────────┼────────┤";
  strings[asize(strings)] = "                                      │БИК     │ " + КодУчастникаКорр;
  strings[asize(strings)] = "                                      ├────────┤";
  strings[asize(strings)] = "                                      │Сч. N   │";
  strings[asize(strings)] = "Банк плательщика " + string(GetShortNameBank(rec_wlmes.OutsideAbonentID):21) + "│        │";
  strings[asize(strings)] = "──────────────────────────────────────┼────────┤";
  strings[asize(strings)] = "                                      │БИК     │";
  strings[asize(strings)] = "                                      ├────────┤";
  strings[asize(strings)] = "                                      │Сч. N   │";
  strings[asize(strings)] = "Банк получателя Разные                │        │";
  strings[asize(strings)] = "──────────────────────────────────────┼────────┤";
  strings[asize(strings)] = "ИНН                                   │Сч. N   │";
  strings[asize(strings)] = "                                      │        │";
  strings[asize(strings)] = "                                      ├────────┼──────┬───────────┬───────";
  strings[asize(strings)] = "                                      │Вид оп. │      │Срок плат. │       ";
  strings[asize(strings)] = "                                      ├────────┼──────┼───────────┼───────";
  strings[asize(strings)] = "                                      │Наз. пл.│      │Очер. плат.│ " + Payments[0].Priority;
  strings[asize(strings)] = "                                      ├────────┼──────┼───────────┼───────";
  strings[asize(strings)] = "Получатель Разные                     │Код     │      │Рез. поле  │";
  strings[asize(strings)] = "──────────────────────────────────────┴────────┴──────┴───────────┴───────";
  strings[asize(strings)] = "Назначение платежа";
  strings[asize(strings)] = "Количество документов " + string(Документов);
  strings[asize(strings)] = "Номера документов: " + Payments[0].Number;
  i=1;
  while( i<Документов )
     strings[asize(strings)] = "                   " + Payments[i].Number;
     i = i+1;
  end;
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "                               Подписи               Отметки банка";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "                        ──────────────────────   ";
  strings[asize(strings)] = "         М.П. ";
  strings[asize(strings)] = "                        ──────────────────────   ";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";


  i = 0;
  N = asize(strings);
  while( i<N )
     if( not ЗаписатьСтроку( strings[i] ) )
       ErrExport("Ошибка записи строки сообщения " );
       return false;
     end;
     i = i+1;
  end;  
  return true;
end;

macro ЗаписатьОписьПлатежныхДокументов( Документов, СуммаДокументов, rec_wlmes )
  var i, N, КодУчастникаКорр, error;
  array strings;

  if ( (rec_wlmes.OutsideAbonentCodeKind==PTCK_BIC) AND (rec_wlmes.OutsideAbonentCode!="") )
     КодУчастникаКорр = rec_wlmes.OutsideAbonentCode;
     error = 0;
  else
     КодУчастникаКорр = ПолучитьОткрытыйКодСубъекта( rec_wlmes.OutsideAbonentID, PTCK_BIC, error );
  end;

  if ( error )
     ErrExport("Не найден код получателя сообщения");
     return false;
  end;

  strings[asize(strings)] = string( {Name_Bank}:60:c );
  strings[asize(strings)] = "────────────────────────────────────────────────────────────";
  strings[asize(strings)] = "Наименование кредитной организации";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "БИК " + string({MFO_Bank}:9:r);
  strings[asize(strings)] = "    ─────────";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "Корреспондентский счет (субсчет) " + {CORAC_Bank};
  strings[asize(strings)] = "                                 ────────────────────";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "ГРКЦ/РКЦ  " + string( GetNameBank(rec_wlmes.OutsideAbonentID):50:r);
  strings[asize(strings)] = "          ──────────────────────────────────────────────────";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "БИК " + string(КодУчастникаКорр:9);
  strings[asize(strings)] = "    ─────────";
  strings[asize(strings)] = "";
  strings[asize(strings)] = DateToStr( {curdate} );
  strings[asize(strings)] = "";
  strings[asize(strings)] = "                                                                  ОПИСЬ РАСЧЕТНЫХ ДОКУМЕНТОВ";
  strings[asize(strings)] = "                                                                N " + string(wlsess.Number:8) + " от " + DateToStr( {curdate} );
  strings[asize(strings)] = "┌─────────┬───────┬─────────────────┬────────────┬──────────────────┬──────────────────────┬────────────┬──────────────────────┬──────────────────────┬───────┐";
  strings[asize(strings)] = "│    N    │  Вид  │         N       │    Дата    │  Сумма документа │ Номер лицевого счета │  БИК банка │         Номер        │ Номер лицевого счета │ Отмет │"; 
  strings[asize(strings)] = "│   п/п   │ опера │     документа   │  документа │                  │      плательщика     │ получателя │  корреспондентского  │       получателя     │  ки   │";
  strings[asize(strings)] = "│         │  ции  │                 │            │                  │                      │            │      счета банка     │                      │ банка │";
  strings[asize(strings)] = "│         │       │                 │            │                  │                      │            │      получателя      │                      │       │";
  strings[asize(strings)] = "├─────────┼───────┼─────────────────┼────────────┼──────────────────┼──────────────────────┼────────────┼──────────────────────┼──────────────────────┼───────┤";
  i=0;
  while( i<Документов )
     strings[asize(strings)] = string( "│  ", (i+1):5, "  │  ", Payments[i].ShifrOperation:2, "   │ ",
                               Payments[i].Number:15:r, " │ ", FormatDate(Payments[i].DatePay), " │ ",  Payments[i].Sum:16, " │ ", Payments[i].PayerAccount:20, " │ ",
                               Payments[i].ReceiverBankCode:9, "  │ ", Payments[i].ReceiverCorrAccNostro:20, " │ ", Payments[i].ReceiverAccount:20, " │       │" );
     i = i+1;
  end;
  strings[asize(strings)] = "├─────────┼───────┼─────────────────┼────────────┼──────────────────┼──────────────────────┼────────────┼──────────────────────┼──────────────────────┼───────┤";
  strings[asize(strings)] = "│  Итого  │   X   │         X       │      X     │ " + string(СуммаДокументов:16) + " │           X          │      X     │           X          │          X           │   X   │";
  strings[asize(strings)] = "└─────────┴───────┴─────────────────┴────────────┴──────────────────┴──────────────────────┴────────────┴──────────────────────┴──────────────────────┴───────┘";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "";
  strings[asize(strings)] = "           М.П.                                                                                                                                 Подписи        ";


  i = 0;
  N = asize(strings);
  while( i<N )
     if( not ЗаписатьСтроку( strings[i] ) )
       ErrExport("Ошибка записи строки сообщения " );
       return false;
     end;
     i = i+1;
  end;  
  return true;
end;

/* Процедура экспорта */
macro ExportProc( FileName:string, addrSess, Kind:string )
  var loop = TRUE, Документов = 0, СуммаДокументов = 0, i;

  asize(Payments, 0);
 
  Record PostMes(wlmes);

  SetBuff( wlSess, addrSess );

  if( not СчитатьЗапись( wlmes ) )
    ErrExport("Не найдено ни одного сообщения для отправки");
    return false;
  end;  

  /* Параметры внешнего абонента берем из первого выгружаемого сообщения */
  copy( PostMes, wlmes );

  if( PostMes.OutsideAbonentID == 0 )
    ErrExport("Внешний абонент сообщения по транспорту \"Почта\" не может являться \"нашим\" банком");
    return false;
  end;  

  while( loop )
    if( not СохранитьСообщение() )
      return false;
    end;
    if( not СчитатьЗапись( wlmes ) )
       loop = FALSE;
    end;
    СуммаДокументов = СуммаДокументов + Payments[Документов].Sum;
    Документов = Документов + 1;    
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;

  if( not ЗаписатьСводноеПлатежноеПоручение(Документов, СуммаДокументов, Kind, PostMes) )
    return false;
  end;

  if( not ЗаписатьОписьПлатежныхДокументов(Документов, СуммаДокументов, PostMes) )
    return false;
  end;
  
  return true;
end;

macro PostOutProc( FileName, addrSess )
    return ExportProc( FileName, addrSess, "Почта" )
end;

macro TlgOutProc( FileName, addrSess )
    return ExportProc( FileName, addrSess, "Телеграф" )
end;

