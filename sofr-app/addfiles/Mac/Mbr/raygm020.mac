/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по "Луч" MF020                                       */
/*                                                                          */
/*  Имя файла: raygm020.mac                                                 */
/*  Создан:  30.03.09                                                       */
/****************************************************************************/

import "raygenmes.mac";
macro GenMes(addrMes,addrBuf)
  file llval(llvalues) key 0;
  RECORD DpBuf( dpmsginf );
  var    msginf, msgdoc, msgParty;  
  var    error,PartyCode, field_value, ProcessingCode;

  SetBuff( wlmes,  addrMes );
  SetBuff( DpBuf,  addrbuf );
  
  msginf = RsbDepoInfoMessage(DpBuf.MessageID);

  if ( valtype(msginf)==V_UNDEF )
     RunError( "|Не определен объект класса" );
  end;

  PrintLog(2,"Генерация сообщения по МF010");

  /*Блок ORDER_HEADER*/
  Write_ORDER_HEADER_Block( msginf );

  ProcessingCode = msginf.ProcessingCode;
  /* Блок MF020 */
  ЗаписатьПолеЛогСПроверкой( MF020_Block, BeginOfBlock, MF020_Block );
  msgParty = GetMessageParty(msginf,DPMSGPAT_DEPONENT,0);
  if ( valtype(msgParty)!=V_UNDEF )
    /*dep_acc_c*/ 
    ЗаписатьПолеЛогСПроверкой( dep_acc_c_Field, msgParty.Account);
    /*deponent_c*/
    ЗаписатьПолеЛогСПроверкой( deponent_c_Field, msgParty.PartyCode);
    /*sec_c*/
    ЗаписатьПолеЛогСПроверкой( sec_c_Field, msgParty.Partition);
  else
    RunError( "|Отсутствует информация о депоненте" );
  end; 

  /*corr_sec_c*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    ЗаписатьПолеЛогСПроверкой( corr_sec_c_Field, msgParty.Partition);
  end;

  msginf.RewindMessageDocument();
  msgdoc = msginf.GetMessageDocument();
  while( valtype(msgdoc)!=V_UNDEF )
    if(msgdoc.DocType == DPMSGDOC_GROUND)
      llval.List = OBJTYPE_KINDDOC;
      llval.Element = msgdoc.DocumentKind;
      if ( not getEQ(llval) )
         RunError("|Не найдена запись в справочнике видов документов");
      end;
      /*based_on*/
      ЗаписатьПолеЛог( based_on_Field, llval.Name );
      /*based_numb*/
      ЗаписатьПолеЛог( based_numb_Field, msgdoc.DocumentNumber );
      /*based_date*/
      ЗаписатьПолеЛог( based_date_Field, YYYYMMDD(msgdoc.DocumentDate) );
    end;
    msgdoc = msginf.GetMessageDocument();
  end;

  /*deponent_bic*/
  msgParty = GetMessageParty(msginf,DPMSGPAT_SENDER,0);
  if ( valtype(msgParty)!=V_UNDEF )
    ЗаписатьПолеЛог( deponent_bic_Field, msgParty.PartyCode);
  end; 


  /*Блок seсurities*/
  if( not УстановитьКонтекстБлока( seсurities_Block ) )
    RunError("|Ошибка при установке контекста блока "+seсurities_Block);
  end;
  ЗаписатьПолеЛог( BeginOfBlock, seсurities_Block ) ;
   /*Блок seсurity*/
  if( not УстановитьКонтекстБлока( seсurity_Block ) )
    RunError("|Ошибка при установке контекста блока "+seсurity_Block);
  end;
  ЗаписатьПолеЛог( BeginOfBlock, seсurity_Block ) ;

  /*seсurity_c*/   
  ЗаписатьПолеЛог( security_c_Field, msginf.SecurityCode);
  /*sequrity_q*/      
  ЗаписатьПолеЛог( security_c_Field, msginf.QuantitytoBeSettled);
  /*sequrity_isin*/   
  ЗаписатьПолеЛог( security_isin_Field, FillISIN(msginf));  
 
  ЗаписатьПолеЛог( EndOfBlock, seсurity_Block ) ;
  if( not УстановитьКонтекстБлока( ".." ) )
     RunError("|Ошибка при установке контекста блока ..");
  end; 
  /*Конец блока seсurity*/

  ЗаписатьПолеЛог( EndOfBlock, seсurities_Block ) ;
  if( not УстановитьКонтекстБлока( ".." ) )
     RunError("|Ошибка при установке контекста блока ..");
  end; 
  /*Конец блока seсurities*/

  ЗаписатьПолеЛог( EndOfBlock, MF020_Block );
  /* Конец блока MF020 */
end;


macro CheckMes( addrMes )
  var field_name, field_value;
  SetBuff( wlmes, addrMes );

  while( СчитатьПоле( field_name, field_value ) )
    /*контроль на русские буквы*/
    if( (field_name == deposit_c_Field ) or
        (field_name == contrag_c_Field ) or
        (field_name == dep_acc_c_Field)  or
        (field_name == deponent_c_Field) or
        (field_name == sec_c_Field)      or
        (field_name == corr_sec_c_Field) )
      if(not CheckForRus( field_name, field_value, true))
        return false;
      end; 
    end;
    /*контроль по коду операции*/
  end;

  return true; 

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;