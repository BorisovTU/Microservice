/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SBRF3 сигнатура 0SA                     */
/*                                                                          */
/*  Имя файла: sbgd0SA.mac                                                  */
/*  Создан:    08.04.04                                       Бабин А.П.    */
/****************************************************************************/

import "sbgendoc.mac", rsd, oralib, likepy;


/* Релиз 0SA */
macro GenDoc( addrMes )
  var field_name, field_value;
  var ErrCode:integer, ErrDescription:string;
  var rs:object;
  var select:string;
  var params:TArray;

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по форме 0SA");

  if( (not СчитатьПоле(field_name, field_value)) OR (field_name!=SB_Tag_PIB) OR (field_value=="") )
      std.msg("Неверный формат сообщения по формату 0SA");
      return false;
  end;

  select = "select t_mesID, t_state from dwlmes_dbt where t_direct=chr(0) and T_TRN=:RelatedRef and T_Department = :OperD";
  params = makeArray( SQLParam("RelatedRef", wlmes.RelatedRef),
                      SQLParam("OperD",{OperDprt} ));
  rs = execSQLselect( select, params, FALSE );
  if ( not rs.MoveNext() )
     std.msg( string("|Не найдено исходящее сообщение ", wlmes.RelatedRef) );
     return false;
  end;

  if ( rs.value(1)<30 )
     std.msg( string("|Исходящее сообщение ", wlmes.RelatedRef, " не отправлено") );
     return false;
  end;

  if ( substr(field_value, 26, 2)=="00" )
     if ( rs.value(1)>30 )
        return true;
     end;

     if ( not ВставитьПодтверждениеОДоставке(rs.value(0)) )
        std.msg( string("|Ошибка при переносе в доставленные сообшения ", wlmes.RelatedRef) ); 
        return false;
     end;
  else
     if(not GetElementAndNoteLLVALUES(OBJTYPE_WLRESCODE_SMFR, substr(field_value, 26, 2),ErrCode,ErrDescription))
       ErrCode = 0;
       ErrDescription = substr(field_value, 26, 2);
     end;
     if ( not ОшибкаЛогическогоКонтроляСообщения(rs.value(0), ErrCode, ErrDescription ) ) 
        std.msg( string( "Не удалось поместить в отбракованные сообщение ID = ", rs.value(0)) );
        return false;
     end;
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;