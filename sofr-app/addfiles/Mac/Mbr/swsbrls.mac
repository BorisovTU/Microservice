/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*             Определение релиза сообщений SWIFT-SB для платежей           */
/*                                                                          */
/*  Имя файла: swsbrls.mac                                                  */
/*  Создан:  16.09.05                                     Фомченкова Л.     */
/****************************************************************************/
import RSD, PTInter, "wlglobal.mac";

FILE   Cors( corschem ) key 1;
RECORD tp_shem ( wltpshem );  
RECORD pm_paym( pmpaym );
RECORD pmprop_debet( pmprop );
RECORD pmprop_credit( pmprop );
RECORD pmrmprop( pmrmprop );


/* Определить название формы сообщения */
private macro getFormName( FormID:integer ):string
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_name from dwlmesfrm_dbt where t_formid = :ID";
  params = makeArray( SQLParam("ID", FormID));
  rs = execSQLselect( select, params, FALSE );

  if(rs.MoveNext())
    return rs.value(0);
  else 
    return "";
  end;
end;


/* Определить идентификатор релиза формы сообщения */
private macro getRlsForm( FormID:integer, Name:string ):integer
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_RlsFormID from dwlmesrls_dbt where t_formid = :ID and t_Name = :Name";
  params = makeArray( SQLParam("ID", FormID),
                      SQLParam("Name", Name) );
  rs = execSQLselect( select, params, FALSE );

  if(rs.MoveNext())
    return rs.value(0);
  else 
    return -1;
  end;
end;

/* Определить типы банков - участников маршрута платежа */
private macro ОбойтиМаршрутПлатежа():bool
  var rs:object;
  var select:string;
  var params:TArray;
  var corschem, FIID;
  if( not IsBankType( pm_paym.ReceiverBankID, PT_KIND_SAVINGS_BANK ))
    return false;
  end;

  if( not IsBankType( pm_paym.PayerBankID, PT_KIND_SAVINGS_BANK ))
    return false;
  end;
  
  if((pmprop_credit.Group == PAYMENTS_GROUP_EXTERNAL) and (pmprop_credit.IsSender == ""))
    if( (pmprop_credit.CorrID > 0) AND (not IsBankType( pmprop_credit.CorrID, PT_KIND_SAVINGS_BANK) ))
      return false;
    end;
    corschem = pmprop_credit.Corschem;
    FIID = pmprop_credit.PayFIID;
  else
    if( (pmprop_debet.CorrID > 0) AND (not IsBankType( pmprop_debet.CorrID, PT_KIND_SAVINGS_BANK) ))
      return false;
    end;
    corschem = pmprop_debet.Corschem;
    FIID = pmprop_debet.PayFIID;
  end;
  Cors.Number  = corschem;
  Cors.FIID    = FIID;
  Cors.FI_Kind = 1;
  if( getEQ(Cors) )
    if( not IsBankType( Cors.CorrID, PT_KIND_SAVINGS_BANK) )
        return false;
     end;
  end;

  return true;
end;
      
/*Принадлежит ли платеж одной ТС?*/
macro IsAccoutsInTC(pmpaym):bool
    var rs:object;
    var select:string;
    var params:TArray;

    select = "select count(*) from ddp_dep_dbt where t_partyid  = :ID";
    params = makeArray( SQLParam("ID", pmpaym.PayerBankID ));
    rs = execSQLselect( select, params, FALSE );
    if(rs.MoveNext())
       params = makeArray( SQLParam("ID", pmpaym.ReceiverBankID ));
       rs = execSQLselect( select, params, FALSE );
       if(rs.MoveNext())
           return true;
       end;
    end;
    return false;
end;

/*Найти ID формы по ее имени и номеру транспортной схемы*/
macro FindFormIDTp(Name, TpId):integer
    var rs:object;
    var select:string;
    var params:TArray;

    select = "select t_FormID from dwlmesfrm_dbt where t_NAME =:NAME  And T_TPID = :TPID";
    params = makeArray( SQLParam("NAME",     Name ),
                        SQLParam("TPID", TpID ));
    rs = execSQLselect( select, params, FALSE );

    if(rs.MoveNext())
       return rs.value(0);
    end;
    return -1;
end;

      
/* Процедура доопределения релиза */
/* Возвращаемые значения: >0 - ID релиза формы                 */
/*                         0 - релиз формы не переопределяется */
/*                        -1 - ID релиза формы */
macro DefineRlsNumber( addrPm, TpShem, FormID ):integer
  var FormName:string, Sym:string = "N", rls_id:integer;

//  setbuff( pm_paym, addrPm);
  setbuff( tp_shem,  TpShem);

  FormName = getFormName( FormID );

  if( FormName == "" ) 
    return -1;
  end;

  /* Дооределение формы используется пока только для определенных форм */            
  if( (FormName != "103") and (FormName != "202") and (FormName != "900") and (FormName != "910") )
    return  0;
  end;

  if( (FormName == "103") or (FormName == "202"))

  if( ОбойтиМаршрутПлатежа() )
    Sym = "S";
  end;
 
  elif((FormName == "900") or (FormName == "900"))

  /*Внутри ТС вместо форм 910 и 900 используется 001S*/
     if (IsAccoutsInTC(pm_paym))
        FormName = "001";
      FormID = FindFormIDTp(FormName,tp_shem.TPID);
      Sym = "S";
    else
      return  0;
     end;
  
  end;

  /* Ищем подходящий релиз формы сообщения */
  rls_id = getRlsForm( FormID, FormName + Sym );
  
  return rls_id;
end;
