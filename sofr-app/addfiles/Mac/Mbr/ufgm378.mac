/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED378 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm378.mac                                                  */
/*  Создан:    01.10.12  Chukina T.                                         */
/****************************************************************************/

import "ufgenmes.mac", "wluftool.mac";

private macro ParseQueries( Queries: string, // /N/БИК1,БИК2,БИК3...
                            LimitTransKind: @string, 
                            LimitInfo: TArray                            
                          )

  var ind1 = 0, ind2 = 0;

  ind1 = index(Queries, "/");
  ind2 = index(Queries, "/", ind1 + 1);
  if( ind1 and ind2 and (ind2-ind1 > 1) )
    LimitTransKind = substr(Queries, ind1+1, ind2-ind1-1);
  else
    RunError("Не указан вид устанавливаемого лимита");
  end;

  ind2 = index(Queries, "\n", ind2+1);
  if(not ind2)
    RunError("Не указана информация об устанавливаемом лимите");
  end;

  var tmpArray : TArray = StrSplit2ByDelimiter( substr(Queries, ind2+1), "\n" );
  for(var item, tmpArray)
    LimitInfo[ LimitInfo.size ] = item;
  end;

  if( not LimitInfo.size )
    RunError("Не указана информация об устанавливаемом лимите");
  end;

end;

macro GenMes( addrMes, addrReq )

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  FillEDNoDateAuthorByRef_CompoundRls(wlmes.TRN);

  // Разобрать поле wlreq.Queries
  var LimitTransKind = "";
  var LimitInfo : TArray = TArray();
  ParseQueries(wlReq.Queries, @LimitTransKind, LimitInfo);

  ЗаписатьПолеЛог( "LimitTransKind", LimitTransKind );

  ЗаписатьПолеЛог( "PURBIC", wlReq.OriginatorCode );

  var PURBIC = "", LimitSum = "";
  for( var limit, LimitInfo )    
    УстановитьКонтекстБлокаЛог( "LimitInfo" );
    ЗаписатьПолеЛог( beginField, "LimitInfo" );
    PURBIC = SubStr( limit, 1, index(limit, "/") - 1 );
    if(PURBIC)
      ЗаписатьПолеЛог( "PURBIC", PURBIC );
    end;
    LimitSum = SubStr( limit, index(limit, "/") + 1 );
    ЗаписатьПолеЛог( "LimitSum", LimitSum );    
    ЗаписатьПолеЛог( endField, "LimitInfo" ) ;
    УстановитьКонтекстБлокаЛог( ".." );
  end;

  /*if(wlmes.RelatedRef)
    УстановитьКонтекстБлокаЛог( "EDRefID" );
    ЗаписатьПолеЛог( beginField, "EDRefID" );

    FillEDNoDateAuthorByRef_CompoundRls(wlmes.RelatedRef);

    ЗаписатьПолеЛог( endField, "EDRefID" ) ;
    УстановитьКонтекстБлокаЛог( ".." );
  end;*/
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;