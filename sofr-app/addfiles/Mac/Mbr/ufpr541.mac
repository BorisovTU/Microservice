/*
  $Name: ufpr541.mac
  $Module: Межбанковские расчеты
  $Description: Печатная форма сообщения ED541 транспорта УФЭБС
*/


import MesInter, "wlglobal.mac", "wluftool.mac", "WLTOOLS.mac";

private const OBJTYPE_WLD_REQ = 505;

class TEDRequisite               /*Поля ЭС*/
  var EDTypeNo:          string, /*Тип ЭС*/
      TransportFileName: string, /*Имя файла*/
      EDProcessingTime:  string, /*Время приема/отправки сообщения*/
      EDNo:              integer,/*Номер ЭС в течение опердня*/
      EDDate:            date,   /*Дата составления ЭС*/
      EDAuthor:          string, /*Уникальный идентификатор составителя ЭС - УИС*/
      RefEDNo:           integer,/*Номер исходного ЭС в течение опердня */
      RefEDDate:         date,   /*Дата составления исходного ЭС*/
      RefEDAuthor:       string; /*Уникальный идентификатор составителя исходного ЭС - УИС*/

  macro Construct()
     EDTypeNo = "";
     TransportFileName = "";
     EDProcessingTime = "";
     EDNo = 0;
     EDDate = date(0,0,0);
     EDAuthor = "";
     RefEDNo = 0;
     RefEDDate = date(0,0,0);
     RefEDAuthor = "";
  end; 

  Construct();
end;

class TED541MessageFields       /*Поля сообщения ED541*/
  var EDNo:            integer, /*Номер ЭПД в течение опердня*/
      EDDate:          date,    /*Дата составления ЭПД*/
      EDAuthor:        string,  /*Уникальный идентификатор составителя ЭПД*/
      EDReceiver:      string,  /*Уникальный идентификатор получателя ЭС*/

      PartNo:          string, /*Номер части*/
      PartQuantity:    string, /*Количество частей*/
      PartAggregateID: string,  /*Уникальный идентификатор совокупности частей*/

      InitialEDNo:     integer, /*Номер ЭС в течение опердня для исходного ЭСИС*/
      InitialEDDate:   date,    /*Дата составления ЭС для исходного ЭСИС*/
      InitialEDAuthor: string,  /*Уникальный идентификатор составителя ЭС для исходного ЭСИС*/

      EDRequisiteList: TArray;  /*Список электронных сообщений*/

  macro Construct()
    EDNo = 0;
    EDDate = date(0,0,0);
    EDAuthor = "";
    EDReceiver = "";

    PartNo = "";
    PartQuantity = "";
    PartAggregateID = "";

    InitialEDNo = 0;
    InitialEDDate = date(0,0,0);
    InitialEDAuthor = "";

    EDRequisiteList = TArray();
  end; 

  Construct();
end;

// Макрос чтения полей сообщения ED541
// Входные параметры: ED541MessageFields - структура полей сообщения ED541
private macro ReadED541MessageFields( ED541MessageFields:@TED541MessageFields ) : bool
  var fieldname = "", fieldvalue = "", blockname = "";

  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if(blockname == "")
      if(fieldname == "EDNo")
        ED541MessageFields.EDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED541MessageFields.EDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED541MessageFields.EDAuthor = fieldvalue;
      elif(fieldname == "EDReceiver")
        ED541MessageFields.EDReceiver = fieldvalue;
      end;
    elif(blockname == "PartInfo")
      if(fieldname == "PartNo")
        ED541MessageFields.PartNo = fieldvalue;
      elif(fieldname == "PartQuantity")
        ED541MessageFields.PartQuantity = fieldvalue;
      elif(fieldname == "PartAggregateID")
        ED541MessageFields.PartAggregateID = fieldvalue;
      end;
    elif(blockname == "InitialED")
      if(fieldname == "EDNo")
        ED541MessageFields.InitialEDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED541MessageFields.InitialEDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED541MessageFields.InitialEDAuthor = fieldvalue;
      end;
    elif(blockname == "EDRequisiteList")
      if(fieldname == beginField)
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size ] = TEDRequisite();
      elif(fieldname == "EDTypeNo")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].EDTypeNo = fieldvalue;
      elif(fieldname == "TransportFileName")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].TransportFileName = fieldvalue;
      elif(fieldname == "EDProcessingTime")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].EDProcessingTime = fieldvalue;
      elif(fieldname == "EDNo")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].EDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].EDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].EDAuthor = fieldvalue;
      end;
    elif(blockname == "EDRequisiteList")
      if(fieldname == "EDNo")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].RefEDNo = int(fieldvalue);
      elif(fieldname == "EDDate")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].RefEDDate = ToDate(fieldvalue);
      elif(fieldname == "EDAuthor")
        ED541MessageFields.EDRequisiteList[ ED541MessageFields.EDRequisiteList.size - 1 ].RefEDAuthor = fieldvalue;
      end;
    end; 
  end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

  return TRUE;
end;

private macro EDNoConverting( EDTypeNo ): string
  var tmpStr = "";
  if( (RegExMatch( EDTypeNo, "\\d{4}" ) == true) and (index( EDTypeNo, "0" ) == 1) )
    tmpStr = "ED" + substr( EDTypeNo, 2, 3 );
  end;
  return tmpStr;
end;

private macro GetMessagesListType( RefTRN )
  var ListType = "";
  var select = " select wlreq.t_Queries " +
               " from dwlreq_dbt wlreq " +  
               " where wlreq.t_Direct = chr(0) " +  
               "   and wlreq.t_Trn = :RelatedRef " +
               "   and exists( " + 
               "              select meslnk.t_MesID " +
               "              from dwlmeslnk_dbt meslnk, dwlmes_dbt wlmes, dwltpshem_dbt tpshem " + 
               "              where meslnk.t_ObjID    =  wlreq.t_ReqID " +  
               "                and meslnk.t_ObjKind  = :ObjKind " +  
               "                and wlmes.t_MesID     =  meslnk.t_MesID " +
               "                and tpshem.t_TpShemID =  wlmes.t_TpSchemID " +
               "                and tpshem.t_TpID     = :Transport ) "; 

  var params = makeArray( SQLParam( "RelatedRef",    RefTRN ),
                          SQLParam( "ObjKind",       OBJTYPE_WLD_REQ ),
                          SQLParam( "Transport",     TRANSP_UFBS ) );
                        
  var rs = execSQLselect( select, params );
  
  if( rs and rs.moveNext() )
    if( index( rs.value(0), "/ИП10/" ) )
      ListType = "переданных";
    elif( index( rs.value(0), "/ИП20/" ) )
      ListType = "полученных";
    end;
  else
    ListType = "переданных/полученных";
  end;

  return ListType;
end;

private macro PrintTableInfo( ED541MessageFields: TED541MessageFields, WlmesRelatedRef )

   var Reference = EDNoDateAuthor();

   Reference.EDNo     = ED541MessageFields.EDNo;
   Reference.EDDate   = ED541MessageFields.EDDate;
   Reference.EDAuthor = ED541MessageFields.EDAuthor;

  [
    ED541 Информация о переданных/полученных ЭС

    Номер ЭC:     #########
    Дата ЭC:     ##########
    Референс ЭС: #########################
    Составитель: ########## ##################################################
    Получатель:  ########## ##################################################
  ]( ED541MessageFields.EDNo, 
     DDpMMpYYYY(ED541MessageFields.EDDate), 
     Reference.GetTrn(), 
     ED541MessageFields.EDAuthor, 
     Bnk_GetPartyName( ПолучитьКодСубъекта( ED541MessageFields.EDAuthor, PTCK_UIS, null ) ),
     ED541MessageFields.EDReceiver, 
     Bnk_GetPartyName( ПолучитьКодСубъекта( ED541MessageFields.EDReceiver, PTCK_UIS, null ) ) 
   );

  [

    В ответ на запрос: EDNo ##########   EDDate ##########   EDAuthor ##########
                       Референс запроса:  #########################
  ]( ED541MessageFields.InitialEDNo, 
     DDpMMpYYYY(ED541MessageFields.InitialEDDate), 
     ED541MessageFields.InitialEDAuthor, 
     WlmesRelatedRef
   );

  [

    Часть: ###### из ###### с идентификатором ###########################
  ]( ED541MessageFields.PartNo :r, 
     ED541MessageFields.PartQuantity :r, 
     ED541MessageFields.PartAggregateID 
   );

end;

private macro PrintTableHead( RefTRN )
  [

    Список ###################### сообщений
    ┌─────────┬───────────┬──────────────────────────────────────────────────────────────────────────────┬─────────────────────────┬──────────────┐
    │         │           │                              Идентификаторы ЭС                               │                         │     Время    │
    │  № п/п  │  Тип  ЭС  ├────────────────┬──────────────┬──────────────┬───────────────────────────────┤        Имя файла        │ получ/отправ │
    │         │           │     Номер      │     Дата     │     Автор    │           Референс            │                         │              │  
  ]( 
    GetMessagesListType( RefTRN ) :c 
   );
end;

private macro PrintTableElements( ED541MessageFields: TED541MessageFields )
  var RequisiteNumber = 1;

  for( var EDRequisite, ED541MessageFields.EDRequisiteList )
    var Reference = EDNoDateAuthor();

    Reference.EDNo     = EDRequisite.EDNo;
    Reference.EDDate   = EDRequisite.EDDate;
    Reference.EDAuthor = EDRequisite.EDAuthor;

    [ ├─────────┼───────────┼────────────────┼──────────────┼──────────────┼───────────────────────────────┼─────────────────────────┼──────────────┤
      │   ###   │   #####   │   ##########   │  ##########  │  ##########  │   #########################   │ ####################### │   ########   │      
    ](  RequisiteNumber :c,
        EDNoConverting( EDRequisite.EDTypeNo ) :c,
        EDRequisite.EDNo :c,
        DDpMMpYYYY(EDRequisite.EDDate) :c,
        EDRequisite.EDAuthor :c,
        Reference.GetTrn() :c,
        EDRequisite.TransportFileName :c,
        EDRequisite.EDProcessingTime :c
     );
     RequisiteNumber = RequisiteNumber + 1;
  end;
end;

private macro PrintTableBottom()
  [ └─────────┴───────────┴────────────────┴──────────────┴──────────────┴───────────────────────────────┴─────────────────────────┴──────────────┘   ];
end;

macro PrintDoc( addrMes, MassCopy )


  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var ED541MessageFields = TED541MessageFields();

  ReadED541MessageFields( @ED541MessageFields );

  PrintTableInfo( ED541MessageFields, wlmes.RelatedRef );
  PrintTableHead( wlmes.RelatedRef );
  PrintTableElements( ED541MessageFields );
  PrintTableBottom();

  // Перенаправляем вывод обратно
  //------------------------------------------
  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 

  return true;                                                               

  OnError(er)
    ExeptionMessage(er);
    return false;
end;
