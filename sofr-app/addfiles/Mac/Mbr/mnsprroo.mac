/*
 $Name:   mnsprroo.mac
 $Module: MBR
 $Description: Печать сообщения ROO
*/

import "likepy.mac", "wlglobal.mac", "fnsreadmall.mac";

class (MessageReader) PrintROO
  var НомРешОт = "",
      ДатаРешОт = "",
      ДатаПодп = "",
      НаимНО = "",
      КласЧинРук = "",
      КласЧин = "",
      ФИОРук = "",
      Фамилия = "",
      Имя = "",
      Отчество = "",
      ДатаРешВзыск = "",
      ДатаРешВО = "",
      НомРешВзыск = "",
      НомРешВО = "",
      ДатаРешПр = "",
      НомРешПр = "",
      НаимНП = "",
      ФИОИП = "",
      НаимЮЛ = "",
      ИНННП = "",
      ИННЮЛ = "",
      ИННИП = "",
      ИННФЛ = "",
      КППНП = "",
      КПП = "",
      АдрНП = "",
      НаимКО = "",
      НаимБ = "",
      БИК = "",
      ВидРеш = 0,
      КодОснов = "",
      КодНО,
      AccKindList = TArray(),
      AccList = TArray();
  macro ВидСч(value)
    AccKindList[AccKindList.size] = value;
  end;
  macro НомСч(value)
    AccList[AccList.size] = value;
  end;
  macro _ConvertBlock_(name)
    if (name == "ПлИП")
      ФИОИП = Фамилия + " " + Имя + " " + Отчество;
    elif (name == "Руководитель")
      ФИОРук = Фамилия + " " + Имя + " " + Отчество;
    end;
  end;
  macro _Normalize_()
    if (ДатаПодп)
      // Поле ДатаРешПр есть и в xml, и в txt, но в xml оно в формате YYYY-MM-DD
      ДатаРешПр = DDpMMpYYYY( ToDateYYYY_MM_DD(ДатаРешПр) );
    end;
    if (ДатаРешОт == "")
      ДатаРешОт = DDpMMpYYYY( ToDateYYYY_MM_DD(ДатаПодп) );
    end;
    if (КласЧинРук == "")
      КласЧинРук = КласЧин;
    end;
    if (ДатаРешВзыск == "")
      ДатаРешВзыск = DDpMMpYYYY( ToDateYYYY_MM_DD(ДатаРешВО) );
    end;
    if (НомРешВзыск == "")
      НомРешВзыск = НомРешВО;
    end;
    if (НаимНП == "")
      НаимНП = НаимЮЛ;
    end;
    if (ИНННП == "")
      ИНННП = ИННЮЛ;
    end;
    if (ИНННП == "")
      ИНННП = ИННФЛ;
    end;
    if (ИНННП == "")
      ИНННП = ИННИП;
    end;
    if (КППНП == "")
      КППНП = КПП;
    end;
    if (НаимКО == "")
      НаимКО = НаимБ;
    end;
  end;
end;

macro AddStringToReport( str:string )
  while( str != "" )
    [ #
      ─────────────────────────────────────────────────────────────────────────
    ]( substr(str, 1, 120) );

    str = substr(str, 120);
  end;
end;

macro AddAccToReport( ROO : PrintROO )
  var i = 1;
  
  while( i < ROO.AccList.Size )
    [
      N #                               
        ─────────────────────────────── 
    ]
    ( ROO.AccKindList[i] + " " + ROO.AccList[i] );
    i = i + 1;
  end;
end;

private macro GetAmount( ROO : PrintROO )
  var rs = execSQLSelect( " Select t_Amount "
                          "   From dwlregdec_dbt "
                          "  Where t_Type = :Type "
                          "    and t_Number = :Num "
                          "    and t_Date = TO_DATE(:ReqDate, 'dd.mm.yyyy') "
                          "    and t_RegPartyKind = :PartyKind "
                          "    and t_OriginatorCode = :Code ",
                          MakeArray(SQLParam("Type", WLD_TYPE_REGDEC_ROO),
                                    SQLParam("Num", ROO.НомРешПр),
                                    SQLParam("ReqDate", ROO.ДатаРешПр),
                                    SQLParam("PartyKind", PTLIST_TAXINSTITUTE),
                                    SQLParam("Code", ROO.КодНО)
                          ));
                                    
  if( rs and rs.MoveNext() )
    return (rs.value("t_Amount"));
  else
    return "";
  end;
end;

macro Draw( ROO : PrintROO )

  var Client = "";
  
  var M1 = "",
      M2 = "",
      M3 = "",
      M4 = "",
      M5 = "",
      M6 = "",
      M7 = "",
      M8 = "";
      
  if(ROO.КодОснов == "01")
    M1 = "X";
  elif(ROO.КодОснов == "02")
    M2 = "X";
  elif(ROO.КодОснов == "03")
    M3 = "X";
  elif(ROO.КодОснов == "04")
    M4 = "X";
  elif(ROO.КодОснов == "05")
    M5 = "X";
  elif(ROO.КодОснов == "06")
    M6 = "X";
  elif(ROO.КодОснов == "07")
    M7 = "X";
  elif(ROO.КодОснов == "08")
    M8 = "X";
  end;

  Client = pyOr(ROO.НаимНП, ROO.ФИОИП) + ", " + ROO.ИНННП + IfThenElse(ROO.КППНП != "", "/"+ROO.КППНП, "");
  [
                                                        Решение N #
                                                                 ───────
                             об отмене приостановления операций по счетам налогоплательщика
                       (плательщика сбора, плательщика страховых взносов, налогового агента) в банке,
                                    а также переводов электронных денежных средств

                                                                           "##" ######### #### г.
                        ────────────────────────────                       ──────────────────────
                              (населенный пункт)

         Руководитель (заместитель руководителя)
     #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
              (наименование налогового органа, классный чин, Ф.И.О.)
  ]
  (
    ROO.НомРешОт, 
    substr(ROO.ДатаРешОт,9,2),
    Месяца(substr(ROO.ДатаРешОт,6,2)):c,
    substr(ROO.ДатаРешОт,1,4),
    substr(ROO.НаимНО + ", " + ROO.КласЧинРук + ", " + ROO.ФИОРук,1,120)
  );
 
  AddStringToReport( substr(ROO.НаимНО + ", " + ROO.КласЧинРук + ", " + ROO.ФИОРук, 120) );

  var BankName = substr(ROO.НаимКО, 1, 200)+", "+ROO.БИК;

  [
    рассмотрев:
    решение  налогового  органа о взыскании  налога, сбора, пени, штрафа, процентов  за  
    счет   денежных   средств  на  счетах  налогоплательщика (плательщика  сборов, плательщика страховых взно-
    сов, налогового  агента) в  банках, а также электронных денежных средств (решение об отмене (замене)
    обеспечительных мер) от ########## № #### и документы,
    подтверждающие #
                   ─────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                (полное наименование организации (Ф.И.О. индивидуального предпринимателя,
    #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                        нотариуса, занимающегося частной практикой, адвоката, учредившего адвокатский кабинет)
    #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                - налогоплательщика (плательщика сбора, плательщика страховых взносов, налогового агента), ИНН/КПП, адрес)

    ┌─┐
    │#│ - исполнение обязанности по уплате сумм налогов, сборов, страховых взносов, пеней, штрафов, ука-
    └─┘ занных в решении о взыскании налогов, сборов, страховых взносов, пеней, штрафов, процентов за
     счет денежных средств, а также электронных денежных средств;
    ┌─┐
    │#│ - представление надогоплательщиком (плательщиком сбора, плательщиком страховых взносов, на-
    └─┘ логовым агентов) налоговой декларации;
    ┌─┐
    │#│ - исполнение обязанности по передаче налоговому органу квитанции о приеме требования о пред-
    └─┘ ставлении документов, требования о представлении пояснений и (или) уведомления о вызове в нало-
     говый орган;
    ┌─┐
    │#│ - исполнение налогоплательщиком-организацией установленной пунктом 5.1 статьи 23 Налогового
    └─┘ кодекса российской федерации обязанности по обеспечению получения от налогового органа по 
     месту нахождения организации (по месту учета организации в качестве крупнейшего налогоплательщи-
     ка) документов в электронной форме по телекоммуникационным каналм связи через оператора элек-
     тронного документооборота;
    ┌─┐
    │#│ - представление налоговым агентом расчета сумм налога на доходы физических лиц, исчисленных и
    └─┘ удержанных налоговым агентом;
    ┌─┐
    │#│ - наличие у налогоплательщика (плательщика сбора, плательщика страховых взносов, налогового
    └─┘ агента) денежных средств на счетах в банках, а также электронных денежных средств в размере, пре-
     вышающем сумму, указанную в решении о приостановлении операции по счетам налогоплательщика
     (плательщика сбора, плательщика страховых взносов, налогового агента) и достаточном для исполнения
     решения от   ##########   №   ##########   о взыскании налогов, сборов, страховых взносов, пе-
     ней, штрафов, процентов за счет денежных средств на счетах в банках, а также электронных денежных
     средств;
    ┌─┐
    │#│ - отмену (замену) обеспечительных мер по основаниям, предусмотренным пунктами 10 и 11
    └─┘ статьи 101 Налогового кодекса Российской Федерации;
    ┌─┐
    │#│ - иные документы.
    └─┘
    
                                     РЕШИЛ:


    - отменить Решение от ########## N ######## о приостановлении операций по счетам
    налогоплательщика  (плательщика   сбора, плательщика страховых взносов, налогового   агента) в  банке, а  
    также  переводов  электронных  денежных  средств 
     #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
      (полное наименование организации (Ф.И.О. индивидуального предпринимателя,
    #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
      нотариуса, занимающегося частной правктикой, адвоката, учредившего адвокатский кабинет)
    #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                            - налогоплательщика (плательщика сбора, плательщика страховых взносов, налогового агента),  ИНН/КПП)

    N #                                                        в банке #
      ─────────────────────────────────────────────────────────        ─────────────────────────────────────────────────────────
     (указываются вид и номера счетов, реквизиты корпоративного                        (наименование банка, БИК)
                     электронного средства платежа)
  ]
  ( ROO.ДатаРешВзыск,
    ROO.НомРешВзыск,
    substr(Client,1, 120),
    substr(Client,121,120),
    substr(Client,241,120),
    M1,
    M2,
    M3,
    M4,
    M5,
    M6,
    ROO.ДатаРешВзыск,
    ROO.НомРешВзыск,
    M7,
    M8,
    ROO.ДатаРешПр,
    ROO.НомРешПр,
    substr(Client,1,120),
    substr(Client,121,120),
    substr(Client,241,120),
    substr(ROO.AccKindList[0] + " " + ROO.AccList[0], 1, 40),
    substr(BankName, 1, 40)
  );

  AddAccToReport( ROO );

  if( ROO.ВидРеш == 2 )
    [
      в части  превышения суммы  денежных средств,  указанной в настоящем  Решении о приостановлении 
      операций по счетам  налогоплательщика (плательщика сбора, плательщика страховых взносов налогово-
      го агента) в банке,а также переводов электронных денежных средств в размере ################### рублей.
    ]
    (GetAmount(ROO):c)
  end;

  [

    Настоящее решение вступает в силу с момента принятия.

    Руководитель (заместитель руководителя)
     #
    ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                                 (наименование налогового органа)
     #                                                                 #
    ─────────────────────────────────────────────────── ──────────────── ───────────────────────────────────────────────────────
                (классный чин)                         (подпись)                                (Ф.И.О)

         М.П.
  ]
  ( substr(ROO.НаимНо,1,72),
    substr(ROO.КласЧинРук,1,50),
    substr(ROO.ФИОРук,1,50)
  );

  return true;

end; 

macro PrintDoc( addrMes, MassCopy )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;


  SetBuff( wlmes, addrMes );

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var ROO = PrintROO();
  ROO.ReadMessage();

  Draw( ROO );

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  return true;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage(er);
  return false;
end;
