/*
  $Name:         wlcrdrem.mac
  $Module:       МБР
  $Description:  Формирование извещения о изъятии из картотеки корсчета
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Формирование извещения о изъятии из картотеки корсчета                  */
/*  Имя файла: wlcrdrem.mac                                                 */
/*  Создан:  27.10.00                                         Бабин А.П.    */
/****************************************************************************/
import FIInter, "wldoc.mac", "adress.mac";

FILE acc ("account.dbt") ;
FILE part ("party.dbt") ;
RECORD finin ("fininstr.dbt") ;

macro FindAcc( FIID, Account )
  acc.Chapter        = 1 ; /* Баланс */
  acc.Account        = Account ;
  acc.Code_Currency  = FIID ;
  return GetEQ( acc ) ;
end ;

macro ПолучитьСубъектаПо_счету( FIID )

  return ПолучитьСубъекта( acc.Client ) ;

end ;


macro PrintHead( FIID, Account, DateReport, NumberReport )
  var NameClient, AddressClient ;
  record RecordAdress ( adress );

  if( ( FindAcc( FIID, Account ) == TRUE )
      AND ( ПолучитьСубъектаПо_счету( FIID ) == 0 ) )

    NameClient = acc.NameAccount ;

    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(part.PartyID,RecordAdress);
    AddressClient = RecordAdress.Adress;

[
        #
        #
        (наименование, адрес организации)

        #########################
        (номер расчетного, текущего, бюджетного счета)

        #

                                 ИЗВЕЩЕНИЕ N ######

        1. Помещенные    в   картотеку    из-за   отсутсвия    средств  на
    корреспондентском  счете  банка   расчетные  документы,   средства  по
    которым  ранее  списаны  с  Вашего  расчетного,  текущего,  бюджетного
    счета (указать, какой расчетный документ, N, дату, сумму),

]( NameClient, AddressClient, Account, DateReport:f:m, NumberReport:l ) ;
  end ;
  return TRUE;
end; 

macro CurToStrNoNum( Sum, Целых, Сотых, FIID )
  var str, sumstr,  Len, Валюта = "", Curr_Ref ;

  if( ПолучитьФинИн( FIID, finin ) == 0 )

    Curr_Ref = int(finin.ISO_Number) ;

    RubToStr( Sum, sumstr ) ;

    /* Получаем сумму без копеек */
    Sum = moneyl(int(doublel(Sum))) ;

    str = CurToStrAlt( Sum, Целых, Сотых, Curr_Ref ) ;

    str = substr( str, strlen( sumstr ) + 2 ) ;

    Len = Index( str, " 00" ) - 1 ;

    Валюта = SubStr( str, 1, Len ) ;

  end ;

  return Валюта ;
end ;


macro PrintNode( addrPmPaym, addrDebet, addrCredit, addrRM, Count )
  var Number, Date, TypeDoc, DKFlag;

  SetBuff( wlpmpaym, addrPmPaym );
  SetBuff( wlpmpropdeb, addrDebet );
  SetBuff( wlpmpropcred, addrCredit );
  SetBuff( wlpmrmprop, addrRM );

   if( wlpmpropdeb.PaymentID != 0 )
      DKFlag = "Д" ;       /* если есть дебетовые свойства - это начальный дебетовый платеж */
   end;

   if( wlpmpropcred.PaymentID != 0 )
      if( DKFlag == "Д" )  /* флаг уже заполнен - это транзитный документ */
         if( wlpmpropdeb.IsSender == "X" ) /* Это плательщик и отправитель платежа - кредитовый */
           DKFlag = "К" ;  /* транзитный кредитовый */
         else
           DKFlag = "Д" ;  /* транзитный дебетовый */
         end ;
      else
         DKFlag = "К" ;    /* это обычная кредитовка */
     end ;
   end ;

   if( DKFlag == "Д" )
    TypeDoc = "Требование" ;
  else
    TypeDoc = "Поручение" ;
  end ;

  Number = wlpmrmprop.Number;
  Date = wlpmrmprop.Date;

   [   ##. ########## N ########## от ########## на сумму ############ #]
   ( Count, TypeDoc, Number, Date:f, wlpmpaym.PayAmount:11:2, CurToStrNoNum( wlpmpaym.PayAmount, 18, 2, wlpmpaym.PayFIID ) ) ;

   return TRUE;
end;

macro PrintFloor( DateReport )
[
   оплачены  #  

                                    Подпись операционного работника
      Штамп
                                    #



-------------------------------------------------------------------------
]( DateReport:f:m, {Name_Oper} ) ;

   return TRUE;

   return TRUE;
end;