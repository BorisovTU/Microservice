/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по SWIFT-SB MT202S                                    */
/*                                                                          */
/*  Имя файла: swsd202s.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac", "pmlib.mac";

/* Платежная инструкция "Общий банковский перевод" */
class GeneralFinInstTransfer
 var
  TRF              :string,      /* Ссылка на операцию */
  RelatedRef       :string,      /* Ссылка на сообщение или сделку */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :moneyl,      /* Сумма перевода */
  Credit           :bool,        /* Признак дебета/кредита */
  Trans            :bool,        /* Признак транзитного */
  OrdBank          :Bank,        /* Банк Приказодатель */
  Instructor       :Bank,        /* Инструктирующий банк */
  Sender           :Bank,        /* Отправитель */
  SndCorresp       :Bank,        /* Корреспондент Отправителя */
  RcvCorresp       :Bank,        /* Корреспондент Получателя */
  Intermediary     :Bank,        /* Посредник */
  AccBank          :Bank,        /* Банк-держатель счета */
  BnfBank          :Bank,        /* Банк Бенефициар */
  DirectAcc        :string,      /* Счет для идентификации корсхемы */
  RcvInfo          :TRcvInfo100; /* Информация Получателю */

  TRF              = "";
  RelatedRef       = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Trans            = false;

  DirectAcc = "";
end; /* class GeneralFinInstTransfer */

var MT202 : GeneralFinInstTransfer;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro Fill20( TRF )
  MT202.TRF = TRF;
  return TRUE;
end;

macro Fill21( RelatedRef )
  MT202.RelatedRef = RelatedRef;
  return TRUE;
end;

macro Fill13C( Code, GMT:TGMT )
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT202.ValueDate = Date;
  MT202.Currency  = Cur;
  MT202.Sum       = moneyl(Sum);
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT202.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  MT202.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill53A( BIC, Account, System, Code )
  MT202.SndCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill53B( Branch, Account, System, Code )
  if ( (valtype(Branch)==V_UNDEF) AND (valtype(System)==V_UNDEF) AND 
       (valtype(Code)==V_UNDEF) AND (valtype(Account)!=V_UNDEF) AND (Account!="") )
      MT202.DirectAcc = Account;
  else 
      MT202.SndCorresp = Bank( "", "", Branch, Account, System, Code );
  end;
  return TRUE;
end;

macro Fill53D( Address, Account, System, Code )
  MT202.SndCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill54A( BIC, Account, System, Code )
  MT202.RcvCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill54B( Branch, Account, System, Code )
  MT202.RcvCorresp = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill54D( Address, Account, System, Code )
  MT202.RcvCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT202.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  MT202.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57A( BIC, Account, System, Code )
  MT202.AccBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill57B( Branch, Account, System, Code )
  MT202.AccBank = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill57D( Address, Account, System, Code )
  MT202.AccBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill58A( BIC, Account, System, Code )
  MT202.BnfBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill58D( Address, Account, System, Code )
  MT202.BnfBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT202.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MT202*/
var FldSN  = ТПолеФормы( УникальныйНомерСообщенияСМФР,       FIELD_MANDATORY, "ReadTRF", "FillSN",  "" ),
    FldRN  = ТПолеФормы( НомерСвязанногоСообщенияСМФР,       FIELD_OPTIONAL,  "ReadTRF", "FillRN",  "" ),
    Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21  = ТПолеФормы( RelatedReferenceField,              FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD",   "Fill52D", "" ),
    Fld53A = ТПолеФормы( Sender_sCorrespondentField_A,       FIELD_OPTIONAL,  "ReadA",   "Fill53A", "" ),
    Fld53B = ТПолеФормы( Sender_sCorrespondentField_B,       FIELD_OPTIONAL,  "ReadB",   "Fill53B", "" ),
    Fld53D = ТПолеФормы( Sender_sCorrespondentField_D,       FIELD_OPTIONAL,  "ReadD",   "Fill53D", "" ),
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill57A", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadB",   "Fill57B", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill57D", "" ),
    Fld58A = ТПолеФормы( BeneficiaryInstitutionField_A,      FIELD_OPTIONAL,  "ReadA",   "Fill58A", "" ),
    Fld58D = ТПолеФормы( BeneficiaryInstitutionField_D,      FIELD_OPTIONAL,  "ReadD",   "Fill58D", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_MANDATORY, "Read72SB","Fill72",  "" );

macro ConstructMT202( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            if( fld.Name == TimeIndicationField )
              count = count - 1;
            end;
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MT202.Sender = Bank( "", "", "", MT202.SndCorresp.Account, CODEWORLD_SMFRSBRF, BankCode);

  MT202.Credit = TRUE; /* входящие банковские переводы - кредитовые */
  MT202.Trans  = MT202.AccBank.IsSet(); /* документ транзитный - если указан держатель счета */

  return TRUE;
end; /* ConstructMT202 */

macro GenDoc( addrMes )
  var error;
  MT202 = GeneralFinInstTransfer();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ202S");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMT202( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.Amount      = MT202.Sum;
  wlpmpaym.PayAmount   = MT202.Sum;
  wlpmpaym.BaseAmount  = MT202.Sum;
  wlpmpaym.OrderAmount = MT202.Sum;
  wlpmpaym.ValueDate = MT202.ValueDate;
  if( not ПолучитьФинИнПоISO( MT202.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO :" +  MT202.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  if ( MT202.OrdBank.IsSet() )
     wlpmpaym.PayerBankID = MT202.OrdBank.PartyID;
  else
     wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;
  end;
  if( MT202.Trans )
    wlpmpaym.ReceiverBankID = MT202.AccBank.PartyID;
  else
    wlpmpaym.ReceiverBankID = {OurBank};
  end;
  /* Счета плательщика/получателя */
  wlpmpaym.PayerAccount = MT202.SndCorresp.Account;

  /* Дебетовые свойства платежа */
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmpropdeb.TransferDate = {curdate};
  else
     wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  if ( MT202.OrdBank.IsSet() )
     wlpmpropdeb.CodeKind = MT202.OrdBank.CodeKind;
     wlpmpropdeb.BankCode = MT202.OrdBank.CodeBank;
     if ( MT202.Instructor.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT202.Instructor.CodeKind;
        wlpmpropdeb.CorrCode     = MT202.Instructor.CodeBank;
        wlpmpropdeb.CorrID       = MT202.Instructor.PartyID;
     else
        wlpmpropdeb.CorrCodeKind = MT202.Sender.CodeKind;
        wlpmpropdeb.CorrCode     = MT202.Sender.CodeBank;
        wlpmpropdeb.CorrAcc      = MT202.Sender.Account;
        wlpmpropdeb.CorrID       = MT202.Sender.PartyID;
     end;
  else
     wlpmpropdeb.CodeKind = MT202.Sender.CodeKind;
     wlpmpropdeb.BankCode = MT202.Sender.CodeBank;

     if ( MT202.SndCorresp.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT202.SndCorresp.CodeKind;
        wlpmpropdeb.CorrCode     = MT202.SndCorresp.CodeBank;
        wlpmpropdeb.CorrID       = MT202.SndCorresp.PartyID;
     end;
  end;

  if ( MT202.SndCorresp.IsSet() OR MT202.RcvCorresp.IsSet() )
     if ( (not MT202.OrdBank.IsSet()) AND (not MT202.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_BANK;
     elif ( (not MT202.OrdBank.IsSet()) OR (not MT202.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_CORR;
     end;
  end;

  if ( MT202.RcvCorresp.IsSet() )
     wlpmpropdeb.OurCorrCodeKind = MT202.RcvCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202.RcvCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202.RcvCorresp.PartyID;
  elif ( (MT202.SndCorresp.IsSet()) AND 
         (not MT202.RcvCorresp.IsSet()) AND
         (MT202.SndCorresp.PartyID!=wlpmpropdeb.CorrID) )
     wlpmpropdeb.OurCorrCodeKind = MT202.SndCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT202.SndCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT202.SndCorresp.PartyID;
  end;

  /* Если в 53 указан только счет, считаем это явным заданием корсчета */
  if( MT202.DirectAcc != "" )
    wlpmpropdeb.OurCorrAcc   = MT202.DirectAcc;
    wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
    wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = MT202.TRF;
  wlpmrmprop.PayDate               = MT202.ValueDate;
  wlpmrmprop.Date                  = MT202.ValueDate;
  wlpmrmprop.ClientDate            = MT202.ValueDate;
  if ( MT202.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName         = MT202.OrdBank.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT202.OrdBank.Account;
     if ( MT202.Instructor.IsSet() )
        wlpmrmprop.PayerCorrBankName     = MT202.Instructor.Name;     
     else
        wlpmrmprop.PayerCorrBankName     = MT202.Sender.Name;     
     end;
  else
     wlpmrmprop.PayerBankName         = MT202.Sender.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT202.Sender.Account;
  end;  
  if ( MT202.AccBank.IsSet() )
     wlpmrmprop.ReceiverBankName      = MT202.AccBank.Name;
     wlpmrmprop.ReceiverCorrAccNostro = MT202.AccBank.Account;
  else
     wlpmrmprop.ReceiverBankName      = {Name_Bank};
  end;
  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT202.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "01";

  wlpmrmprop.ComissCharges        = PM_CHRG_OUR;
  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo       = MT202.RcvInfo.Str;

  if ( MT202.RelatedRef!=СсылкаОтсутствует )
     wlpmrmprop.Reference = MT202.RelatedRef;
  end;

  /* Кредитовые свойства платежа */
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmpropcred.TransferDate = {curdate};
  else
     wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;
  wlpmpropcred.CodeKind = MT202.AccBank.CodeKind;
  wlpmpropcred.BankCode = MT202.AccBank.CodeBank;
  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.OrdBank.GetRoute() );
  /* Узел Инструктирующего банка - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.Instructor.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.Sender.GetRoute(), true );
  /* Узел Корреспондента Отправителя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.SndCorresp.GetRoute() );
  /* Узел Корреспондента Получателя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT202.RcvCorresp.GetRoute() );
  /* Узел Посредника - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202.Intermediary.GetRoute() );
  /* Узел Банка Бенефициара - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT202.AccBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;