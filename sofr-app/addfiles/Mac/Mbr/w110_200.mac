 /*
 $Name: w110_200.mac
 $Module: Межбанковские расчеты
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок      : 29005  - "Выгрузка в МБР"
// Шаг       : 200    - "Квитовка"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, OprInter, BankInter, PaymInter, "wldoc.mac", "wlkvit.mac", "cbsttls.mac", "pm_opr.mac", "pm_tools.mac";
import "pmcarfun.mac";

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

const Menu1      = "Поместить в картотеку";
const Menu2      = "Вернуть отправителю";
const Menu3      = "Перепозиционировать";
const ActionName = "Действие при квитовке отказом:";

macro IsClosedOperDay( payment )
  var select:string = " select t_IsClosed " +
                        " from dcurdate_dbt " +
                       " where t_Branch = :Department " +
                         " and t_CurDate = :ValueDate ";

  var params:TArray = makeArray( SQLParam( "NextDepartment", {OperDprt}          ),
                                 SQLParam( "ValueDate"     , payment.ValueDate ) );
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  if( rset and rset.moveNext() and (rset.value("t_IsClosed") == "X"))
    return true;
  end;
  return false;
end;

private macro PutLastMesToDelivered(Payment : RsbPayment) : integer
  var stat : integer = 0;

  var rs : RsdRecordset = execSQLselectPrm
  ( "select mes.t_MesID "
    "  from dwlmeslnk_dbt lnk, dwlmes_dbt mes "
    " where lnk.t_ObjID = :OutWlPmID "
    "   and lnk.t_ObjKind = :OBJTYPE_PAYMENT "
    "   and mes.t_MesID = lnk.t_MesID "
    "   and mes.t_State <> :WLD_STATUS_MES_INVALID "
    "   and mes.t_State < :WLD_STATUS_MES_DELIV "
    " order by mes.t_MesID desc ",
    SQLParam("OutWlPmID", Payment.OutWlPmID),
    SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT),
    SQLParam("WLD_STATUS_MES_INVALID", WLD_STATUS_MES_INVALID),
    SQLParam("WLD_STATUS_MES_DELIV", WLD_STATUS_MES_DELIV)
  );

  if( rs and rs.moveNext() )
    stat = ChangeStatusMesOnStep(rs.value("t_MesID"), WLD_STATUS_MES_DELIV);
  end;

  return stat;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first )

  var KvitSum       = Moneyl(0),
      KvitDate:date,
      UserBreak:bool = false,
      Cancel:string = KVIT_NORMAL,
      menu_item,
      Description   = "";
  var ClaimOrder:object = null;

  var accUncs = null;
  var AccountCarry = "";

  var DenialGround : string = "";

  var   Choise;
  array Text, Buttons;
  // Проводка
  var paymtr:RsbPaymTransaction;

  /* Требуется квитовка исходящего платежа с ответным подтверждением */
  if( Corschem.IsKvitOutPaym == "X" )       
    if( not ПолучитьСуммуКвитовки( PaymentObj, KvitSum, Cancel, Description ) )
      return 1; //  Ошибка создана
    end;
    if( Cancel == KVIT_CALL_BACK )
      MsgBox( "Нельзя квитовать с подтверждением отзывом" );
      return 1;
    end;
  end;
  
  /* Пришло подтверждение отказом */
  if( Cancel == KVIT_CANCEL )

    if( PaymentObj.DocKind != DLDOC_MULTYPM )

      if( УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_CANCEL ) )
        MsgBox( "Возникла ошибка при смене статусов операции платежа" );
        return 1;
      end;

      if( (PaymentObj.DocKind == DLDOC_BANKCLAIM) 
          AND 
          ( (PaymentObj.DemandAcceptTerm == PM_DEMAND_TERM_ACCEPT)
            or
            ( (GetDepIdByPartyId(PaymentObj.PayerBankID) <= 0) and
              CorschemIsNostro(PaymentObj.OutCorschem, PaymentObj.OutCorschemFIID) )
          )
        )

        ClaimOrder            = GenObject( "RsbBankClaim", PaymentObj.DocumentID );
        
        if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE,  OPR_PM_ST_CLOSE ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;

        if(PaymentObj.DemandAcceptTerm == PM_DEMAND_TERM_ACCEPT)
          if( УстановитьСтатусыПлатежа( OPR_PAYM_ACCEPT, OPR_PAYM_ST_ACPT_REJECTED ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
          end;

          DenialGround = "Отказ от акцепта";
        else
          DenialGround = Description;
        end;
    
        // Установить примечание "Причина отказа (возврата)" равным "Отказ от акцепта"
        // Заполнить примечание
        if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, DenialGround ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return 1;
        end;

        return 0;

      elif( not IsMoveDocSpecialScrol() )

        if( not ExecRefusalDocument( PaymentObj, Corschem, Description ) )
          return 1;
        end;

      else
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_EXEC_REFUSAL ) )
          MsgBox("Возникла ошибка при смене статусов операции платежа");
          return 1;
        end;

        if( Description != "" )
          // Заполнить примечание
          if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, Description ) != 0 )
            msgbox( "Ошибка при вставке примечания платежа" );
            return 1;
          end;
        end;

        PaymentObj.PropStatus = PM_PROP_CORREJECTED;
      end;

    else
      if(Description != "")
        // Заполнить примечание
        if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, Description ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return 1;
        end;
      end;
      if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
        MsgBox("Возникла ошибка при смене статусов операции платежа");
        return 1;
      end;

    end;

    if( PutLastMesToDelivered(PaymentObj) != 0 )
      msgbox("Ошибка при помещении сообщения по платежу в доставленные");
      return 1;
    end;
  else  /* Получено успешное подтверждение */

    if( PaymentObj.OutTransferDate > {curdate} )
      msgbox( "ДПП по исходящей схеме расчетов еще не наступила" );
      return 1;
    end;

    PaymentObj.PropStatus = PM_PROP_DELIVERED;

    if( PutLastMesToDelivered(PaymentObj) != 0 )
      msgbox("Ошибка при помещении сообщения по платежу в доставленные");
      return 1;
    end;

    if( PaymentObj.DocKind != DLDOC_MULTYPM )
      
      KvitDate = WldGetKvitDate( PaymentObj, @UserBreak );
      if( UserBreak )
        return 1;
      elif( KvitDate == date( 0, 0, 0 ) )
        MsgBox( string( "Не найден операционный день ДПП подтверждения ", GetConfTransferDate():f,
                        " или в этом дне невозможна проводка" ) );
        return 1;
      end;

      if( ( PaymentObj.FuturePayerAccount == PaymentObj.FutureReceiverAccount ) and
          ( PaymentObj.ValueDate == KvitDate ) 
        )
        // Провести все плановые проводки
        if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
          MsgBox("Ошибка при помещении планируемой проводки в проведенные");
          return 1;
        end;
      
      elif( ( PaymentObj.FuturePayerAccount == PaymentObj.FutureReceiverAccount ) and 
            ( PaymentObj.ValueDate != KvitDate )
          )
        
        if( CheckUnFin(PaymentObj, false) OR IsClosedOperDay( PaymentObj ))
          CarryPaymOnNewDate( PaymentObj, KvitDate ); // Перепровести все проводки платежа датой wlconf.TransferDate 
        else
          // Провести все плановые проводки
          if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
            MsgBox("Ошибка при помещении планируемой проводки в проведенные");
            return 1;
          end;
          // Перепровести проводку зачисления на счет СНР
          accUncs = OutPaymentAccUnclosed( PaymentObj );
          AccountCarry = accUncs.FindAndOpenAccount( PaymentObj.ValueDate );
          if( CarryPaymOnNewAccount( PaymentObj, 
                                     PRT_Credit, 
                                     1, 
                                     PaymentObj.FutureReceiverFIID, 
                                     PaymentObj.FutureReceiverAccount, 
                                     PaymentObj.FutureReceiverFIID,
                                     AccountCarry,
                                     ACCTRN_STATUS_DOCUMENT,
                                     KvitDate ) )
            return 1;
          end;

          PaymentObj.ValueDate = KvitDate;
        
          // обычная завершающая проводка FuturePayerAccount->FutureReceiverAccount
          paymtr = PaymentObj.MakeTransaction();

          paymtr.Chapter         = 1;                                            
          paymtr.Date_Carry      = PaymentObj.ValueDate;
          paymtr.Number_Pack     = PaymentObj.NumberPack;
          paymtr.Numb_Document   = PaymentObj.Number;
          paymtr.ResultCarry     = 1;
          paymtr.Kind_Oper       = " 1";

          if (PaymentObj.ShifrOper != "")
            paymtr.Shifr_Oper      = PaymentObj.ShifrOper;
          else
            paymtr.Shifr_Oper      = "09";
          end;

          paymtr.Ground          = PaymentObj.Ground;
          paymtr.Department      = PaymentObj.Department;
          paymtr.FIIDPayer       = PaymentObj.FuturePayerFIID;
          paymtr.FIIDReceiver    = PaymentObj.FutureReceiverFIID;
          paymtr.SumPayer        = PaymentObj.FuturePayerAmount;
          paymtr.SumReceiver     = PaymentObj.FutureReceiverAmount;
          paymtr.AccountPayer    = PaymentObj.FuturePayerAccount;
          paymtr.AccountReceiver = PaymentObj.FutureReceiverAccount;
          paymtr.ClaimID         = PaymentObj.ClaimID;

          if( ( PaymentObj.CoverAmount != $0            ) and 
              ( PaymentObj.PIList(PRT_Debet ).Size == 0 ) and 
              ( PaymentObj.PIList(PRT_Credit).Size == 0 ) ) 
            paymtr.SumEquivalentCarry = PaymentObj.CoverAmount;
          end;

          if( not paymtr.Carry )
            MsgBox("Ошибка при актуализации платежа");
            return 1;
          end;  
        end;
      
      elif( ( ПлатежЗачисленНаСНР( PaymentObj ) ) and 
            ( PaymentObj.ValueDate == KvitDate ) 
          )
        // Провести все плановые проводки
        if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
          MsgBox("Ошибка при помещении планируемой проводки в проведенные");
          return 1;
        end;
        
        // Иначе перепровести проводку зачисления на счет СНР
        accUncs = OutPaymentAccUnclosed( PaymentObj );
        AccountCarry = accUncs.FindAndOpenAccount();
        if( CarryPaymOnNewAccount( PaymentObj,
                                   PRT_Credit, 
                                   1,
                                   PaymentObj.FutureReceiverFIID, 
                                   AccountCarry,
                                   PaymentObj.FutureReceiverFIID,
                                   PaymentObj.FutureReceiverAccount,
                                   ACCTRN_STATUS_DOCUMENT ) )
          return 1;
        end;

      elif( ( ПлатежЗачисленНаСНР( PaymentObj ) ) and 
            ( PaymentObj.ValueDate != KvitDate ) )

        // Провести все плановые проводки
        if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
          MsgBox("Ошибка при помещении планируемой проводки в проведенные");
          return 1;
        end;
        
        PaymentObj.ValueDate = KvitDate;

        // обычная завершающая проводка FuturePayerAccount->FutureReceiverAccount
        paymtr = PaymentObj.MakeTransaction();

        paymtr.Chapter         = 1;                                            
        paymtr.Date_Carry      = PaymentObj.ValueDate;
        paymtr.Number_Pack     = PaymentObj.NumberPack;
        paymtr.Numb_Document   = PaymentObj.Number;
        paymtr.ResultCarry     = 1;
        paymtr.Kind_Oper       = " 1";

        if (PaymentObj.ShifrOper != "")
          paymtr.Shifr_Oper      = PaymentObj.ShifrOper;
        else
          paymtr.Shifr_Oper      = "09";
        end;

        paymtr.Ground          = PaymentObj.Ground;
        paymtr.Department      = PaymentObj.Department;
        paymtr.FIIDPayer       = PaymentObj.FuturePayerFIID;
        paymtr.FIIDReceiver    = PaymentObj.FutureReceiverFIID;
        paymtr.SumPayer        = PaymentObj.FuturePayerAmount;
        paymtr.SumReceiver     = PaymentObj.FutureReceiverAmount;
        paymtr.AccountPayer    = PaymentObj.FuturePayerAccount;
        paymtr.AccountReceiver = PaymentObj.FutureReceiverAccount;
        paymtr.ClaimID         = PaymentObj.ClaimID;

        if( ( PaymentObj.CoverAmount != $0            ) and 
            ( PaymentObj.PIList(PRT_Debet ).Size == 0 ) and 
            ( PaymentObj.PIList(PRT_Credit).Size == 0 ) ) 
          paymtr.SumEquivalentCarry = PaymentObj.CoverAmount;
        end;

        if( not paymtr.Carry )
          MsgBox("Ошибка при актуализации платежа");
          return 1;
        end;  

      elif( not ПлатежЗачисленНаСНР( PaymentObj ) and
            ( PaymentObj.FuturePayerAccount != PaymentObj.FutureReceiverAccount )
          )

        if( (PaymentObj.DocKind == DLDOC_BANKCLAIM) AND 
            (GetDepIdByPartyId(PaymentObj.PayerBankID) <= 0) and
            CorschemIsNostro(PaymentObj.OutCorschem, PaymentObj.OutCorschemFIID) 
          )

          ClaimOrder            = GenObject( "RsbBankClaim", PaymentObj.DocumentID );

          PaymentObj.ValueDate = KvitDate;

          if( УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_KVIT,
                                        OPR_PAYM_ACCEPT,   OPR_PAYM_ST_ACPT_ACCEPTED,
                                        OPR_PAYM_DO,       OPR_PM_ST_ENTER ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
          end;

          return 0;
        end;
      end;
    
    end;
    
    // Освободить все резервы по платежу
    PaymentObj.FreeReserve( PaymentObj.PayerAccount, PaymentObj.Chapter, PaymentObj.PayerFIID );

    PaymentObj.ValueDate = KvitDate;

    // Установить статусы
    if( УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_KVIT, OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      MsgBox("Возникла ошибка при смене статусов операции платежа");
      return 1;
    end;
      
    PaymentObj.Paymstatus = PM_FINISHED;
    PaymentObj.StatusInfo = "Исполнен";
  end;

  return 0;
end;

macro PrepMassExecuteStep() 
  return execStoredFunc( "WLD_KVTOUTSTEP.MassKvitOutStepPrepare", V_INTEGER );
end;

macro MassExecuteStep()
  var retval = 0;
  var select, select_;
  var params:TArray = TArray();
  var rs:RsdRecordset;
  var strSQL, ErrorMessage;
  var accUncs;
  var AccountCarry;
  RECORD pmpaym(pmpaym);
  var Carry:RsbPaymTransaction;

  params = makeArray(SQLParam( "p_ID_Operation", 1 ),
                     SQLParam( "p_Oper", {oper} ));
  retval = execStoredFunc( "WLD_KVIT.KvitPmTrn", V_INTEGER, params);
  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  execSQL( "UPDATE doprtemp_tmp "
              "SET t_SkipDocument = 2 "
            "WHERE t_SkipDocument = 0 "
              "and t_ErrorStatus = 0" );

  /*1.Обработка платежей зачисленных на СНР, у которых ДПП подтвержения совпадает c датой валютирования платежа*/
  select = " select oprtemp.t_OrderID,"+
                   " pmpaym.t_FuturePayerAccount,"+
                   " pmpaym.t_FIID_FuturePayAcc,"+
                   " pmpaym.t_FutureReceiverAccount,"+
                   " pmpaym.t_FIID_FutureRecAcc,"+
                   " pmpaym.t_Chapter,"+
                   " oprtemp.t_ID_Operation, "+
                   " oprtemp.t_ID_Step "+
             " from doprtemp_tmp oprtemp, dwlkvtlnk_tmp kvtlnk, dpmpaym_dbt pmpaym"+
            " where oprtemp.t_OrderID = kvtlnk.t_PaymentID"+
              " and kvtlnk.t_Type = chr(0)" +
              " and oprtemp.t_ErrorStatus = 0"+
              " and pmpaym.t_PaymentID = oprtemp.t_OrderID"+
              " and pmpaym.t_DocKind <> 311 /*DLDOC_MULTYPM*/ "+
              " and pmpaym.t_ValueDate = WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department )"+
              " and pmpaym.t_FuturePayerAccount <> pmpaym.t_FutureReceiverAccount";
  rs = execSQLselect( select, NULL, false );
  /*Для каждой записи выполнить перепроводку со счета pmpaym.FuturePayerAccount (старый счет) на корсчет pmpaym.FutureReceiverAccount (новый счет)*/
  while(rs.moveNext())
    if( CarryPaymOnNewAccountMass(rs.value("t_OrderID"),
                                  rs.value("t_ID_Operation"),
                                  rs.value("t_ID_Step"),
                                  1,      
                                  rs.value("t_Chapter"),   
                                  rs.value("t_FIID_FuturePayAcc"),   
                                  rs.value("t_FuturePayerAccount"),
                                  rs.value("t_FIID_FutureRecAcc"),   
                                  rs.value("t_FutureReceiverAccount")))
      return 1;
    end;
  end;


  SQL_Execute("DELETE FROM dpmcarryacc_tmp");  

  /*Заполнить временную таблицу pmcarryacc по записям pmpaym из выборки:*/
  strSQL = " INSERT INTO dpmcarryacc_tmp (t_PaymentID, t_PayerAccount, t_PayerAmount, t_PayerFIID, t_ReceiverAccount, t_ReceiverAmount, t_ReceiverFIID, t_Chapter) "+
           " ( SELECT pmpaym.t_PaymentID,"+
                    " pmpaym.t_FuturePayerAccount,"+
                    " pmpaym.t_FuturePayerAmount,"+
                    " pmpaym.t_FIID_FuturePayAcc,"+
                    " pmpaym.t_FutureReceiverAccount,"+
                    " pmpaym.t_FutureReceiverAmount,"+
                    " pmpaym.t_FIID_FutureRecAcc,"+
                    " pmpaym.t_Chapter"+
               " FROM dpmpaym_dbt pmpaym, doprtemp_tmp oprtemp, dwlkvtlnk_tmp kvtlnk, dwlconf_dbt conf "+
              " WHERE oprtemp.t_OrderID = kvtlnk.t_PaymentID"+
                " and kvtlnk.t_Type = chr(0)" +
                " and oprtemp.t_ErrorStatus = 0"+
                " and conf.t_ConfID = kvtlnk.t_ConfID"+
                " and pmpaym.t_PaymentID = oprtemp.t_OrderID"+
                " and pmpaym.t_DocKind <> 311 /*DLDOC_MULTYPM*/ "+
                " and conf.t_TransferDate = pmpaym.t_ValueDate"+
                " and pmpaym.t_FuturePayerAccount <> pmpaym.t_FutureReceiverAccount)";

  execSQL(strSQL, NULL);            

  /*Выполнить процедуру смены тех.полей платежа с привязкой к шагу*/
  retval = execStoredFunc( "PM_CARFUN.ChangePMFutureFields", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  /*Очистить таблицу pmcarryacc*/
  execSQL("DELETE FROM dpmcarryacc_tmp");

  /*2.Обработка платежей зачисленных на корсчет, у которых ДПП подтвержения не совпадает c датой валютирования платежа и они могут быть зачислены на СНР*/
  select_= " select oprtemp.t_OrderID, "+
                  " oprtemp.t_ID_Operation, "
                  " oprtemp.t_ID_Step, "
                  " pmpaym.t_FIID_FuturePayAcc, "
                  " pmpaym.t_FuturePayerAccount, "
                  " pmpaym.t_FuturePayerAmount, "
                  " pmpaym.t_FIID_FutureRecAcc, "
                  " pmpaym.t_FutureReceiverAccount, "
                  " pmpaym.t_FutureReceiverAmount, "
                  " WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department ) as t_TransferDate, "
                  " rm.t_ShifrOper, "
                  " pmpaym.t_Chapter, "
                  " rm.t_Number, "
                  " pmpaym.t_NumberPack, "
                  " rm.t_Ground, "
                  " pmpaym.t_Department, "
                  " pmpaym.t_ValueDate, "
                  " nvl(nvpi.t_CoverAmount, 0) as t_CoverAmount ";
   select =       " from doprtemp_tmp oprtemp,"
                  " dwlkvtlnk_tmp kvtlnk,"
                  " dpmpaym_dbt pmpaym,"
                  " dpmprop_dbt db,"
                  " dpmprop_dbt cr,"
                  " dpmrmprop_dbt rm,"
                  " dpmnvpi_dbt nvpi"
            " where oprtemp.t_OrderID = kvtlnk.t_PaymentID"
              " and kvtlnk.t_Type = chr(0)" 
              " and oprtemp.t_ErrorStatus = 0"
              " and pmpaym.t_PaymentID = oprtemp.t_OrderID"
              " and pmpaym.t_ValueDate <> WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department )"
              " and pmpaym.t_FuturePayerAccount = pmpaym.t_FutureReceiverAccount"
              " and db.t_PaymentID = pmpaym.t_PaymentID"
              " and db.t_DebetCredit = 0"
              " and cr.t_PaymentID = pmpaym.t_PaymentID"
              " and cr.t_DebetCredit = 1"
              " and rm.t_PaymentID = pmpaym.t_PaymentID"     
              " and PM_DPPFUN.MayPlaceToSNR( db.t_Group,"
                                           " db.t_IsSender,"
                                           " cr.t_Group,"
                                           " cr.t_IsSender,"
                                           " pmpaym.t_Department,"
                                           " pmpaym.t_StartDepartment ) = 1"
              " and NOT EXISTS( select 1 " 
                                " from dcurdate_dbt cd" 
                               " where cd.t_Branch = " + {OperDprt} +
                                 " and cd.t_CurDate = pmpaym.t_ValueDate "
                                 " and cd.t_IsClosed = 'X') "
              " and nvpi.t_PaymentID(+) = oprtemp.t_OrderID ";
  rs = execSQLselect( select_ + select );
  /*Для каждой записи */
  while(rs.moveNext())
  /*выполнить перепроводку с корсчета pmpaym.FutureReceiverAccount (старый счет) на счет  СНР*/
    accUncs = OutPaymentAccUnclosed(rs.value("t_OrderID"),rs.value("t_OrderID"));
    AccountCarry = accUncs.FindAndOpenAccount(date(rs.value("t_ValueDate")));  
    if( CarryPaymOnNewAccountMass(rs.value("t_OrderID"),
                                  rs.value("t_ID_Operation"),
                                  rs.value("t_ID_Step"),
                                  1,      
                                  rs.value("t_Chapter"),   
                                  rs.value("t_FIID_FutureRecAcc"),   
                                  rs.value("t_FutureReceiverAccount"),
                                  rs.value("t_FIID_FutureRecAcc"),   
                                  AccountCarry,
                                  rs.value("t_TransferDate")))
      return 1;
    end;


  /*       Выполнить фактическую проводку:
       Счет дебета: pmpaym.FuturePayerAccount
       Счет кредита: pmpaym.FutureReceiverAccount
       Сумма: сумма платежа к оплате
       Дата: wlconf.TransferDate*/
    Carry = RsbPaymTransaction( rs.value("t_OrderID") );

    Carry.Chapter         = rs.value("t_Chapter");
    Carry.Date_Carry      = rs.value("t_TransferDate");
    Carry.Number_Pack     = rs.value("t_NumberPack");
    Carry.Numb_Document   = rs.value("t_Number");
    Carry.ResultCarry     = 1;
    Carry.Kind_Oper       = " 1";
    Carry.Shifr_Oper      = rs.value("t_ShifrOper");
    Carry.Ground          = rs.value("t_Ground");
    Carry.Department      = rs.value("t_Department");
    Carry.FIID            = rs.value("t_FIID_FutureRecAcc");
    Carry.Sum             = rs.value("t_FutureReceiverAmount");
    Carry.AccountPayer    = AccountCarry;
    Carry.AccountReceiver = rs.value("t_FutureReceiverAccount");
    Carry.ID_Operation    = rs.value("t_ID_Operation");
    Carry.ID_Step         = rs.value("t_ID_Step");
    Carry.Reason          = PM_CARRY_REASON_EXECUTION;

    if( rs.value("t_CoverAmount") > $0 )
      Carry.SumEquivalentCarry = rs.value("t_CoverAmount");
    end;

    if( not Carry.Carry( NULL, ErrorMessage ) )
      msgbox("Ошибка проводки с " + Carry.AccountPayer + " на " + Carry.AccountReceiver + " датой " + Carry.Date_Carry + ": " + ErrorMessage);
      return 1;
    end;

  end;
       
  /* В массвом режиме для данной выборки cменить дату валютирования платежей на ДПП подтверждения*/
  strSQL = " update doprtemp_tmp SET t_SkipDocument = 0"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
  
  retval = execStoredFunc( "WLD_KVIT.ChangeVD2DPP", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  retval = execStoredFunc( "PM_OPERATION.PmWriteLogMass", V_INTEGER, makeArray(SQLParam( "p_Message", "Средства проведены по корсчету датой подтверждения через счет незавершенных расчетов")));  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  strSQL = " update doprtemp_tmp SET t_SkipDocument = 2"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
 

  /*3.Обработка платежей зачисленных на корсчет, у которых ДПП подтвержения не совпадает c датой валютирования платежа и они не могут быть зачислены на СНР*/
  select_ = " select oprtemp.t_OrderID,"+
                  " WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department ) as t_TransferDate,"+
                  " oprtemp.t_ID_Operation,"+
                  " oprtemp.T_ID_Step";
  select  =  " from doprtemp_tmp oprtemp,"
                  " dwlkvtlnk_tmp kvtlnk,"
                  " dpmpaym_dbt pmpaym,"
                  " dpmprop_dbt db,"
                  " dpmprop_dbt cr"
            " where oprtemp.t_OrderID = kvtlnk.t_PaymentID"+
              " and kvtlnk.t_Type = chr(0)" +
              " and oprtemp.t_ErrorStatus = 0"+
              " and pmpaym.t_PaymentID = oprtemp.t_OrderID"+
              " and pmpaym.t_ValueDate <> WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department )"+
              " and pmpaym.t_FuturePayerAccount = pmpaym.t_FutureReceiverAccount"+
              " and db.t_PaymentID = pmpaym.t_PaymentID"
              " and db.t_DebetCredit = 0"
              " and cr.t_PaymentID = pmpaym.t_PaymentID"
              " and cr.t_DebetCredit = 1"
              " and ( PM_DPPFUN.MayPlaceToSNR( db.t_Group,"
                                             " db.t_IsSender,"
                                             " cr.t_Group,"
                                             " cr.t_IsSender,"
                                             " pmpaym.t_Department,"
                                             " pmpaym.t_StartDepartment ) <> 1"
                    " OR EXISTS( select 1 "
                                 " from dcurdate_dbt cd"
                                " where cd.t_Branch = " + {OperDprt} +
                                  " and cd.t_CurDate = pmpaym.t_ValueDate "
                                  " and cd.t_IsClosed = 'X' ) )";
  rs = execSQLselect( select_ + select );
  /*Для каждой записи */
  while(rs.moveNext())
  /* Перепровести все проводки платежа датой wlconf.TransferDate*/
    if( CarryPaymOnNewDateMass(rs.value("t_OrderID"), rs.value("t_TransferDate"), rs.value("t_ID_Operation"), rs.value("t_ID_Step")) )
      return 1;
    end;
  end;
  
   /* В массвом режиме для данной выборки cменить дату валютирования платежей на ДПП подтверждения*/
  strSQL = " update doprtemp_tmp SET t_SkipDocument = 0"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
  
  retval = execStoredFunc( "WLD_KVIT.ChangeVD2DPP", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;
  strSQL = " update doprtemp_tmp SET t_SkipDocument = 2"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
 
  retval = execStoredFunc( "PM_OPERATION.PmWriteLogFromQueryPmIDMass", V_INTEGER, makeArray(SQLParam( "p_Message", "Средства перепроведены по корсчету датой подтверждения" ),
                                                                                            SQLParam( "p_select",  "select oprtemp.t_OrderID as t_PaymentID " + select )));
  if(retval)
    MemoryError(retval);
    return 1;
  end;
  
  /*4. Обработка платежей зачисленных на СНР, у которых ДПП подтвержения не совпадает c датой валютирования платежа*/
  
  select_= " select oprtemp.t_OrderID,"
                  " oprtemp.t_ID_Operation, "
                  " oprtemp.t_ID_Step, "
                  " pmpaym.t_FIID_FuturePayAcc, "
                  " pmpaym.t_FuturePayerAccount, "
                  " pmpaym.t_FuturePayerAmount, "
                  " pmpaym.t_FIID_FutureRecAcc, "
                  " pmpaym.t_FutureReceiverAccount, "
                  " pmpaym.t_FutureReceiverAmount, "
                  " WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department ) as t_TransferDate, "
                  " rm.t_ShifrOper, "
                  " pmpaym.t_Chapter, "
                  " rm.t_Number, "
                  " pmpaym.t_NumberPack, "
                  " rm.t_Ground, "
                  " pmpaym.t_Department, "
                  " decode( oprtemp.t_DocKind, 15, md.t_Type_Document, 70, mo.t_TypeDocument, '' ) as t_Type_Document, "
                  " nvl(nvpi.t_CoverAmount, 0) as t_CoverAmount";
  select = " from doprtemp_tmp oprtemp,"
                " dwlkvtlnk_tmp kvtlnk,"
                " dpmpaym_dbt pmpaym,"
                " dpmrmprop_dbt rm,"
                " dcb_doc_dbt   mo,"
                " dmultydoc_dbt md,"
                " dpmnvpi_dbt nvpi,"
                " dpmaddpi_dbt addpi "
            " where oprtemp.t_OrderID = kvtlnk.t_PaymentID"
              " and kvtlnk.t_Type = chr(0)" 
              " and oprtemp.t_ErrorStatus = 0"
              " and pmpaym.t_PaymentID = oprtemp.t_OrderID"
              " and pmpaym.t_ValueDate <> WLD_KVIT.GetKvitDate( kvtlnk.t_TransferDate, pmpaym.t_Department )"
              " and pmpaym.t_FuturePayerAccount <> pmpaym.t_FutureReceiverAccount"
              " and mo.t_DocumentID(+) = oprtemp.t_OrderID "
              " and md.t_AutoKey(+)    = oprtemp.t_OrderID"
              " and rm.t_PaymentID = pmpaym.t_PaymentID"
              " and nvpi.t_PaymentID(+) = oprtemp.t_OrderID"
              " and addpi.t_PaymentID(+) = oprtemp.t_OrderID";
  rs = execSQLselect( select_+select, NULL, false );

  while(rs.moveNext())
  /*   Выполнить проводку:
       Счет дебета: pmpaym.FuturePayerAccount
       Счет кредита: pmpaym.FutureReceiverAccount
       Сумма: сумма платежа к оплате
       Дата: wlconf.TransferDate 
  */
    Carry = RsbPaymTransaction( rs.value("t_OrderID") );

    Carry.Chapter         = rs.value("t_Chapter");
    Carry.Date_Carry      = rs.value("t_TransferDate");
    Carry.Number_Pack     = rs.value("t_NumberPack");
    Carry.Numb_Document   = rs.value("t_Number");
    Carry.ResultCarry     = 1;
    Carry.Kind_Oper       = " 1";
    Carry.Shifr_Oper      = rs.value("t_ShifrOper");
    Carry.Ground          = rs.value("t_Ground");
    Carry.Department      = rs.value("t_Department");
    if( rs.value("t_FIID_FuturePayAcc") == rs.value("t_FIID_FutureRecAcc") )
      Carry.FIID = rs.value("t_FIID_FuturePayAcc");
      Carry.Sum  = rs.value("t_FuturePayerAmount");
    else
      Carry.FIIDPayer     = rs.value("t_FIID_FuturePayAcc");
      Carry.FIIDReceiver  = rs.value("t_FIID_FutureRecAcc");
      Carry.SumPayer      = rs.value("t_FuturePayerAmount");
      Carry.SumReceiver   = rs.value("t_FutureReceiverAmount");
    end;
    Carry.AccountPayer    = rs.value("t_FuturePayerAccount");
    Carry.AccountReceiver = rs.value("t_FutureReceiverAccount");
    Carry.TypeDocument    = rs.value("t_Type_Document");
    Carry.ID_Operation    = rs.value("t_ID_Operation");
    Carry.ID_Step         = rs.value("t_ID_Step");
    Carry.Reason          = PM_CARRY_REASON_EXECUTION;

    if( rs.value("t_CoverAmount") > $0 )
      Carry.SumEquivalentCarry = rs.value("t_CoverAmount");
    end;

    if( not Carry.Carry(NULL,ErrorMessage) )
      msgbox("Ошибка проводки с " + Carry.AccountPayer + " на " + Carry.AccountReceiver + " датой " + Carry.Date_Carry +": " + ErrorMessage);
      return 1;
    end;

  end;


  strSQL = " update doprtemp_tmp SET t_SkipDocument = 0"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
  /* Сменить дату валютирования платежей на ДПП подтверждения*/
  retval = execStoredFunc( "WLD_KVIT.ChangeVD2DPP", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  /*Заполнить временную таблицу pmcarryacc по записям pmpaym из выборки:*/
  strSQL = " INSERT INTO dpmcarryacc_tmp (t_PaymentID, t_PayerAccount, t_PayerAmount, t_PayerFIID, t_ReceiverAccount, t_ReceiverAmount, t_ReceiverFIID, t_Chapter) "+
           " ( SELECT pmpaym.t_PaymentID,"+
                    " pmpaym.t_FuturePayerAccount,"+
                    " pmpaym.t_FuturePayerAmount,"+
                    " pmpaym.t_FIID_FuturePayAcc,"+
                    " pmpaym.t_FutureReceiverAccount,"+
                    " pmpaym.t_FutureReceiverAmount,"+
                    " pmpaym.t_FIID_FutureRecAcc,"+
                    " pmpaym.t_Chapter"+
               " FROM dpmpaym_dbt pmpaym, doprtemp_tmp oprtemp, dwlkvtlnk_tmp kvtlnk, dwlconf_dbt conf "+
              " WHERE oprtemp.t_OrderID = kvtlnk.t_PaymentID"+
                " and kvtlnk.t_Type = chr(0)" +
                " and oprtemp.t_ErrorStatus = 0"+
                " and conf.t_ConfID = kvtlnk.t_ConfID"+
                " and pmpaym.t_PaymentID = oprtemp.t_OrderID"+
                " and pmpaym.t_DocKind <> 311 /*DLDOC_MULTYPM*/ "+
                " and conf.t_TransferDate <> pmpaym.t_ValueDate"+
                " and pmpaym.t_FuturePayerAccount <> pmpaym.t_FutureReceiverAccount)";
  execSQL(strSQL, NULL);            

  /*Выполнить процедуру смены тех.полей платежа с привязкой к шагу*/
  retval = execStoredFunc( "PM_CARFUN.ChangePMFutureFields", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;

  /*Очистить таблицу pmcarryacc*/
  execSQL("DELETE FROM dpmcarryacc_tmp");

  
  strSQL = " update doprtemp_tmp SET t_SkipDocument = 2"+
            " where t_OrderID in ( select oprtemp.t_OrderID " + select + ")";
  execSQL( strSQL, params );
 



  /* 5. Обработка сквитовавшихся платежей  */
  select_ = " select oprtemp.t_OrderID, oprtemp.t_ID_Operation, oprtemp.t_id_Step";           
  select =   " from doprtemp_tmp oprtemp, dwlkvtlnk_tmp kvtlnk, dpmpaym_dbt pmpaym"+
            " where oprtemp.t_OrderID = kvtlnk.t_PaymentID"+
              " and kvtlnk.t_Type = chr(0)" +
              " and oprtemp.t_ErrorStatus = 0"+
              " and pmpaym.t_PaymentID = oprtemp.t_OrderID"+
              " and pmpaym.t_DocKind <> 311 /*DLDOC_MULTYPM*/ ";
  rs = execSQLselect( select_+select, NULL, false );

  var batchTrnActuate:RsbBatchPaymTrn;

  while(rs.moveNext())
    /*выполнить проводку*/
    if(not batchTrnActuate.AddTransactionActuate( rs.value("t_OrderID"), 0, ACCTRN_STATUS_DOCUMENT, rs.value("t_ID_Operation"), rs.value("t_id_Step")))       
      SetErrorsOprTemp(rs.value("t_OrderID"), 1, "Ошибка при помещении планируемых проводок в проведенные" );
    end;
  end;

  if( not batchTrnActuate.Execute( false ) )
    batchTrnActuate = NULL;    
  end;

  var errPaymentID:TArray;
  var errCode:TArray;
  var errMsg:TArray;

  if(not batchTrnActuate.GetErrors(errPaymentID, errCode, errMsg))    
    batchTrnActuate = NULL;    
  end;

  if( not batchTrnActuate )
    SetErrorsOprTemp(errPaymentID, errCode, errMsg);  
  end;  

  retval = execStoredFunc( "WLD_KVTOUTSTEP.MassKvitOutStepExecute", V_INTEGER );  
  if(retval)
    MemoryError(retval);
    return 1;
  end;
  
  // Для платежей, включенных в сводный, если значение сегмента статуса "Квитовка" 
  // равно "Отказ", то скопировать из сводного примечание "Причина отказа (возврата) платежа" 
  select_ = " SELECT oprtmp.t_OrderID, oprtmp.t_ID_Operation, oprtmp.t_ID_Step, "
            "        rsb_kernel.ExtractText(nt.t_text) as t_NoteValue ";
  select  =   " FROM doprtemp_tmp oprtmp, doprcurst_dbt oprcurst, "+
              "      dpmlink_dbt pmlink, dpmpaym_dbt pm, dnotetext_dbt nt "+
             " WHERE oprtmp.t_ErrorStatus    = 0 "+
               " AND pmlink.t_PurposePayment = oprtmp.t_OrderID "+
               " AND pmlink.t_LinkKind       = :PMLINK_KIND_MULTYPM "+
               " AND pmlink.t_InitialPayment = pm.t_PaymentID "+
               " AND pm.t_DocKind            = :DLDOC_MULTYPM "+
               " AND oprcurst.t_ID_Operation = oprtmp.t_ID_Operation "+
               " AND oprcurst.t_DocKind      = oprtmp.t_DocKind "+
               " AND oprcurst.t_StatusKindID = :OPR_PAYM_OUT_KVIT "+
               " AND oprcurst.t_NumValue     = :OPR_PM_ST_CANCEL "+
               " AND nt.t_ObjectType         = 501 /*OBJTYPE_PAYMENT*/ "+
               " AND nt.t_DocumentID         = lpad(pmlink.t_InitialPayment, 10, '0') "+
               " AND nt.t_NoteKind           = :PM_NOTEKIND_DENIALGROUND "+
               " AND nt.t_Date              <= :curdate1 "+
               " AND nt.t_ValidToDate       >= :curdate2 ";

  params = makeArray(SQLParam( "PMLINK_KIND_MULTYPM",      PMLINK_KIND_MULTYPM ),
                     SQLParam( "DLDOC_MULTYPM",            DLDOC_MULTYPM ),
                     SQLParam( "OPR_PAYM_OUT_KVIT",        OPR_PAYM_OUT_KVIT ),
                     SQLParam( "OPR_PM_ST_CANCEL",         OPR_PM_ST_CANCEL ),
                     SQLParam( "PM_NOTEKIND_DENIALGROUND", PM_NOTEKIND_DENIALGROUND),
                     SQLParam( "curdate1",                 {curdate} ),
                     SQLParam( "curdate2",                 {curdate} ));

  rs = execSQLselect( select_ + select, params );

  while(rs and rs.moveNext())
    ClearRecord(pmpaym);    
    pmpaym.PaymentID = rs.value("t_OrderID");
    retval = AddNoteForObject( OBJTYPE_PAYMENT, 
                               makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym), 
                               PM_NOTEKIND_DENIALGROUND, 
                               rs.value("t_NoteValue"), 
                               NULL, 
                               rs.value("t_ID_Operation"), 
                               rs.value("t_ID_Step") );
    if(retval)
      MemoryError(retval);
      return 1;
    end;
  end;

  // Обработка сводных, сквитовавшихся отказом
  select_ = " SELECT oprtmp.t_OrderID, oprtmp.t_ID_Operation, oprtmp.t_ID_Step, "
            "        conf.t_Description as t_NoteValue ";
  select  =   " FROM doprtemp_tmp oprtmp, dwlkvtlnk_tmp kvtlnk, "+
              "      dwlconf_dbt conf, dpmpaym_dbt pm "+
             " WHERE oprtmp.t_ErrorStatus    = 0 "+
               " AND pm.t_PaymentID          = oprtmp.t_OrderID "+
               " AND pm.t_DocKind            = :DLDOC_MULTYPM "+
               " AND kvtlnk.t_PaymentID      = oprtmp.t_OrderID "+
               " AND kvtlnk.t_Type           = :WLD_KVIT_CANCEL "+
               " AND conf.t_ConfID           = kvtlnk.t_ConfID "+
               " AND conf.t_Description is not null "+
               " AND conf.t_Description     != chr(1) ";

  params = makeArray(SQLParam( "DLDOC_MULTYPM",   DLDOC_MULTYPM ),
                     SQLParam( "WLD_KVIT_CANCEL", KVIT_CANCEL ));

  rs = execSQLselect( select_ + select, params );

  while(rs and rs.moveNext())
    ClearRecord(pmpaym);    
    pmpaym.PaymentID = rs.value("t_OrderID");
    retval = AddNoteForObject( OBJTYPE_PAYMENT, 
                               makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym), 
                               PM_NOTEKIND_DENIALGROUND, 
                               rs.value("t_NoteValue"), 
                               NULL, 
                               rs.value("t_ID_Operation"), 
                               rs.value("t_ID_Step") );
    if(retval)
      MemoryError(retval);
      return 1;
    end;
  end;

  /*8. Обработка сквитовавшихся отказом акцептных требований*/
  
  rs = execSQLselect( 
                      " SELECT oprtemp.t_OrderID, oprtemp.t_ID_Operation, oprtemp.t_ID_Step, " +
                      "       CASE " +
                      "           WHEN pmdemand.t_AcceptTerm = :PM_DEMAND_TERM_ACCEPT " +
                      "           THEN " +
                      "               :ACCEPT_CANCELLED " +
                      "           ELSE " +
                      "               wlconf.t_Description " +
                      "       END " +
                      "  FROM doprtemp_tmp   oprtemp, " +
                      "       dwlkvtlnk_tmp  kvtlnk, " +
                      "       dpmpaym_dbt    pmpaym, " +
                      "       dpmdemand_dbt  pmdemand, " +
                      "       dpmprop_dbt    outprop, " +
                      "       dcorschem_dbt  cs, " +
                      "       dwlconf_dbt    wlconf, " +
                      "       doprcurst_dbt  oprcurst " +
                      " WHERE     oprtemp.t_ErrorStatus = 0 " +
                      "       AND oprtemp.t_OrderID = kvtlnk.t_PaymentID " +
                      "       AND kvtlnk.t_Type = :WLD_KVIT_CANCEL " +
                      "       AND pmpaym.t_PaymentID = oprtemp.t_OrderID " +
                      "       AND pmpaym.t_DocKind = :DLDOC_BANKCLAIM " +
                      "       AND wlconf.t_ConfID = kvtlnk.t_ConfID " +
                      "       AND pmdemand.t_PaymentID = oprtemp.t_OrderID " +
                      "       AND (   pmdemand.t_AcceptTerm = :PM_DEMAND_TERM_ACCEPT1 " +
                      "            OR  (" +
                      "                   NOT EXISTS " +
                      "                       (SELECT 1 " +
                      "                          FROM ddp_dep_dbt dep " +
                      "                         WHERE dep.t_PartyID = pmpaym.t_PayerBankID) " +   
                      "               AND cs.t_IsNostro = 'X') )" +
                      "               AND outprop.t_PaymentID = pmpaym.t_PaymentID " +
                      "               AND outprop.t_Group = :PAYMENTS_GROUP_EXTERNAL " +
                      "               AND outprop.t_IsSender = CHR(0) " +
                      "               AND cs.t_Number = outprop.t_Corschem " +
                      "               AND cs.t_FIID = outprop.t_PayFIID " +
                      "               AND cs.t_FI_Kind = :FIKIND_CURRENCY " +
                      "               AND oprcurst.t_ID_Operation = oprtemp.t_ID_Operation "+
                      "               AND oprcurst.t_DocKind      = oprtemp.t_DocKind "+
                      "               AND oprcurst.t_StatusKindID = :OPRSTATUS_OUT_KVIT "+
                      "               AND oprcurst.t_NumValue     = :OPRSTATUS_OUT_KVIT_CANCEL ", 
                      makeArray(
                        SQLParam( "PM_DEMAND_TERM_ACCEPT", PM_DEMAND_TERM_ACCEPT ),
                        SQLParam( "ACCEPT_CANCELLED", "Отказ от акцепта" ),
                        SQLParam( "WLD_KVIT_CANCEL", KVIT_CANCEL),                        
                        SQLParam( "DLDOC_BANKCLAIM", DLDOC_BANKCLAIM ),                        
                        SQLParam( "PM_DEMAND_TERM_ACCEPT1", PM_DEMAND_TERM_ACCEPT ),
                        SQLParam( "PAYMENTS_GROUP_EXTERNAL", PAYMENTS_GROUP_EXTERNAL),
                        SQLParam( "FIKIND_CURRENCY", FIKIND_CURRENCY),
                        SQLParam( "OPRSTATUS_OUT_KVIT", OPR_PAYM_OUT_KVIT ),
                        SQLParam( "OPRSTATUS_OUT_KVIT_CANCEL", OPR_PM_ST_CANCEL )
                        ), false );

  /*Для каждой записи установить примечанию "Причина отказа (возврата)" значение "Отказ от акцепта"*/
  while(rs.moveNext())
    ClearRecord(pmpaym);    
    pmpaym.PaymentID = rs.value(0);
    retval = AddNoteForObject( OBJTYPE_PAYMENT, makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym), PM_NOTEKIND_DENIALGROUND, rs.value(3), {curdate}, rs.value(1), rs.value(2));
    if(retval)
      MemoryError(retval);
      return 1;
    end;
  end;

  return 0;
end;

macro PostMassExecuteStep()
  return 0;
end;
