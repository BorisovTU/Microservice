/*
 $Name: fnsgmsbc3x.mac
 $Module: MBR
 $Description: Генерация сообщения SBC3 по транспорту ФНС в формате XML
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SBC3 по транспорту ФНС в формате XML
//
// Программист  : Тимонин Д.В.
//
// Создан       : 04.06.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac", FIInter;

private const vers : integer = 510; // версия формата 5.10

private macro ЗаписатьПоле( id:integer, value:string) : bool

  var rs:object;
  var params:TArray;
  var select = " SELECT T_NAME FROM DWLTPFLD_DBT WHERE T_TPID = 10 AND T_TPFIELDID = :tpfieldid ";

  params = makeArray( SQLParam( "tpfieldid", id ) );
  rs = execSQLselect( select, params, FALSE );
  if( rs.moveNext() )
    ЗаписатьПолеЛог( rs.value(0), value );
  else
    return FALSE;
  end;
  return TRUE;
end;

private macro GetBankDprt( BankID ):integer
  var select = " select t_Code from ddp_dep_dbt "
               "  where t_PartyID = :BankID ";
  var params:TArray = makeArray( SQLParam("BankID", BankID) );
  var rs:object = execSQLselect( select, params );
  if( rs.moveNext() )
    return rs.value(0);
  end;
  return 0;
end;

macro CheckObj( addrParty )
  record Party(Party);
  SetBuff(Party, addrParty);
  var err;
  CheckReqChangeBank(Party, @err);
  if( err )
    err = substr( @err, 1, 499 );
    MemoryError(1, err);

    return false;
  else 
    return true;
  end;
  
end;

macro GenMes( addrMes, addrParty, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record Party(Party);
  SetBuff(Party, addrParty);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SBC3");

  record GenAcc(account);
  var Счет : string, Валюта : integer, ErrMsg : string = "";

  var err;
  CheckReqChangeBank(Party, @err);
  if( err )
    RunError(err, err);
  end;

  var Department = GetBankDprt(Party.PartyID);
  var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg( Department,
                                                         ACCMSG_KIND_CHNGBNK,
                                                         vers );
  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonSBC);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  ЗаписатьПолеЛог("ИдДок", CommonSBC.ИдДок);
  ЗаписатьПолеЛог("КНД", CommonSBC.КНД);
  ЗаписатьПолеЛог("КодНОБ", CommonSBC.КодНОБ);

  var Серия : string = "", Номер : string = "";

  GetMessageNumber510( Party.PartyID, GenAcc.Account, "", 1, -1, GenAcc.Client,
                       Серия, Номер, OldMes.MesID, true );

  if( (Серия == "") OR ( Номер == "" ) )
    RunError("Ошибка при определении серии или номера сообщения");
  end;

  ЗаписатьПолеЛог("НомСооб", Серия);
  ЗаписатьПолеЛог("ТипСооб", Номер);

  ЗаписатьПолеЛог("ДолжнПрБ", CommonSBC.ДолжнПрБ);
  ЗаписатьФамПрБ (Department);
  ЗаписатьПолеЛог("ТелБанка", CommonSBC.ТелБанка);
  ЗаписатьПолеЛог("ДатаСооб", CommonSBC.ДатаСооб);

  // Файл \ Документ \ СвБанк / СвБанкСтар
  var rs:object;
  var nblk = 0;
  var blkName:TArray = TArray();
  var value, tpfldid;
  
  var select = " SELECT T_VALUE, T_TPFIELDID FROM DWLMESVAL_TMP ORDER BY T_FIELDID ASC ";
  rs = execSQLselect( select, NULL, FALSE );

  while( rs.moveNext() )
    value = rs.value("T_VALUE");
    tpfldid = rs.value("T_TPFIELDID");
    if( (tpfldid == 0) OR ((tpfldid == 1087) AND (value!="")) ) // мегакостыль
      StartBlockLogXML( value );
      blkName[blkName.size()] = value;
    elif( (tpfldid != 1087/*_begin*/) AND ( tpfldid != 1088/*_end*/ ) )
      if( strlen(value) > 0 )
        ЗаписатьПоле( tpfldid, value );
      end;
    elif( (blkName.size() > 0) AND (tpfldid == 1088/*_end*/) )
      FinishBlockLogXML( blkName[blkName.size()-1] );
      blkName.size() = blkName.size()-1;
    end;
  end;
  
  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonSBC._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
  end;

  if(CommonSBC._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
  end;

  return TRUE;

OnError(er)
  RsbShowError(er);
  return FALSE;
end;
