 /*
 $Name: swin503.mac
 $Module: Межбанковские расчеты
 $Description: Импорт сообщений SWIFT503
 */
import "swiftin.mac", "wluftool.mac", "xmlmestools.mac", "wlufsign.mac";

const CONTEXTID_CHECK_SIGN_UFBS = "ТранспортМБР|2|Импорт_файла";

private var CurrentMessage = "";
private var CurrentMPosition = 1;
private var CurrentMLine = 1;

macro СчитатьБлокSWIFT503(длина)
  if (strlen(CurrentMessage) < CurrentMPosition)
    SetParm(0, -1);
    return "";
  end;
  var ret = substr(CurrentMessage, CurrentMPosition, длина);
  CurrentMPosition = CurrentMPosition + strlen(ret);
  SetParm(0, strlen(ret));
  if (index(ret, "\n") > 0)
    CurrentMLine = CurrentMLine + 1;
  end;
  return ret;
end;

macro СчитатьСтрокуSWIFT503(длина)
  if (strlen(CurrentMessage) <= CurrentMPosition)
    SetParm(0, -1);
    return "";
  end;
  if (длина == NULL)
    длина = 1024;
  end;
  var ret = substr(CurrentMessage, CurrentMPosition, длина);
  var p = index(ret, "\n");
  if (p > 0)
    CurrentMLine = CurrentMLine + 1;
    var p2 = index(ret, "\r");
    if (p2 > 0)
      CurrentMPosition = CurrentMPosition + 1;
      if (p2 < p) p = p2 end;
    end;
    ret = substr(ret, 1, p - 1);
    CurrentMPosition = CurrentMPosition + 1;
  end;
  CurrentMPosition = CurrentMPosition + strlen(ret);
  SetParm(0, strlen(ret));
  return ret;
end;

macro ТекущаяСтрокаSWIFT503()
  return CurrentMLine;
end;

macro ОбработатьСообщения( node )
  /* Инициализация*/
  var i = 0;
  var child:object;

  var err;
  var OutsideAbonentCode = ReadAttribute( node, "EDAuthor" );
  var OutsideAbonentCodeKind = PTCK_UIS;
  var OutsideAbonentID = ПолучитьКодСубъекта(OutsideAbonentCode, OutsideAbonentCodeKind, err);
  if (err != 0)
    OutsideAbonentCode = "04" + substr(OutsideAbonentCode, 1, 7);
    OutsideAbonentCodeKind = PTCK_BIC;
    OutsideAbonentID = ПолучитьКодСубъекта(OutsideAbonentCode, OutsideAbonentCodeKind, err);
    if (err != 0)
      OutsideAbonentCode = ReadAttribute( node, "SenderSWIFTBIC" );
      OutsideAbonentCodeKind = PTCK_SWIFT;
      OutsideAbonentID = ПолучитьКодСубъекта(OutsideAbonentCode, OutsideAbonentCodeKind, err);
      if (err != 0)
        OutsideAbonentID = {MFO_RCC};
        OutsideAbonentCodeKind = PTCK_BIC;
        OutsideAbonentCode = ПолучитьКодСубъекта(OutsideAbonentID, OutsideAbonentCodeKind);
      end;
    end;
  end;
  
  var Документов = 0;
  while( i < node.childNodes.length )
    child = node.childNodes.item(i);
    if( child and (child.nodeType==CHILD_NODE) and (GetNodeName(child.NodeName) == "SWIFTContainer") )
      if (not DecodeBase64(StrSubst(StrSubst(ReadNodeText(child, "SWIFTDocument"), "\n", ""), "\r", ""), CurrentMessage))
        ErrImport( "Ошибка при декодировании SWIFTDocument" );
        return false;
      end;
      CurrentMPosition = 1;
      CurrentMLine = 1;
      var КодНачалаБлокаПрочитан, СчитанныйКодНачалаБлока, Отправитель, Получатель, ДатаОтправки;

      /* Считываем заголовок файла */
      if( not СчитатьЗаголовок( Отправитель, Получатель, ДатаОтправки ) )
        return FALSE;
      end;
      var continue0 = TRUE;
      var stat = 0;
      while( continue0 )
        stat = ОбработатьСообщение( КодНачалаБлокаПрочитан, СчитанныйКодНачалаБлока, Отправитель, Получатель, ДатаОтправки );
        if( stat == 1 )
          continue0 = FALSE;
        elif( stat == 3 ) /* найден конец файла */
          continue0 = FALSE;
        elif( stat == -1 )
          return FALSE;
        elif( stat == 0 )
          stat = СчитатьРазделительСообщений(КодНачалаБлокаПрочитан, СчитанныйКодНачалаБлока);
          if( stat == 1 )
            continue0 = FALSE;
          elif( stat == -1 )
            return FALSE;
          end;
        end;
        wlmes.OutsideAbonentID = OutsideAbonentID;
        wlmes.OutsideAbonentCode = OutsideAbonentCode;
        wlmes.OutsideAbonentCodeKind = OutsideAbonentCodeKind;
        ОбновитьЗапись(wlmes);
        Документов = Документов + 1;
        message("Идет прием сообщений. Обработано: ", Документов );
      end;

      /* Считываем код конца файла */
      if( not СчитатьКонцовку() )
         return FALSE;
      end;
    end;
    i=i+1;
  end;

  return TRUE;
end;

macro ImportMessage(TpID, importFileName, addrSess)
  var i:integer;
  var xml:object = ActiveX( "MSXML.DOMDocument" ), child:object, node:object;

  SetBuff( wlsess, addrSess );

  if( not xml.load(ImportFileName) )
    ErrImport( string( "Неверный формат файла импорта|", ImportFileName,  "|Невозможно загрузить xml-документ" ) );
    return false;
  end;

  node = xml.DocumentElement;

  i = 0;
  if( xml.NodeType == DOCUMENT_NODE )
    while( i < xml.childNodes.length )
      child = xml.childNodes.item(i);
      if( child and (child.nodeType==CHILD_NODE) )
        node = child;
        break;
      end;
      i = i+1;
    end;
  end;

  /* Если содержимое оформлено конвертом, то ЭС содержится внутри env:Body */
  if( StrUpr( node.BaseName ) == "ENVELOPE" )
    node = node.selectSingleNode("//env:Body").firstChild;
  end;

  /* Если ЭС оформлен как конверт ЭЦП, то его содержимое находится в sen:Object в виде base64.
     Выполняем преобразование base64 для узла Object */
  if( StrUpr( node.BaseName ) == "SIGENVELOPE" )
    if( not ПроверкаЭЦП( CONTEXTID_CHECK_SIGN_UFBS, node ))
      return false;
    end;
  end;

  Транспорт = 2;
  ВидКодаТранспорта = PTCK_SWIFT;

  if( ((ОпределитьФорматТранспорта( 2, ВидТерминала )) == 0) OR (ВидТерминала == "" ) )
    ErrImport( "Не найден формат по умолчанию для траснпорта №2" );
    return false;
  end;

  if (node.BaseName != NodeED503)
    ErrImport("Формат транспорта SWIFT503 предназначен только для обмена сообщениями по форме ED503. Файл " + ImportFileName + " не содержит сообщения ED503");
    return false;
  end;

  var ДатаСоздания   = ToDate(ReadAttribute(node, "EDDate"));
  var Author:string = ReadAttribute(node, "EDAuthor");
  var НомерДокумента = YYMMDD(ДатаСоздания) + Author + ReadAttribute(node, "EDNo");

  if( not УстановитьИдентификаторСеанса(НомерДокумента) )
    ErrImport( "Ошибка установки идентификатора сеанса" );
    return false;
  end;

  if( addNoteForObject(OBJTYPE_SESS, makeObjectID(OBJTYPE_SESS, null, wlsess), NOTEKIND_SESS_MESFRM, "ED503") )
    RunError("Ошибка при вставке примечания сеанса");
  end;

  /* глобальные замены */
  ReplaceMacro("СчитатьБлок", "СчитатьБлокSWIFT503");
  ReplaceMacro("СчитатьСтроку", "СчитатьСтрокуSWIFT503");
  ReplaceMacro("ТекущаяСтрока", "ТекущаяСтрокаSWIFT503");
  
  /* Импорт всех сообщений из файла */
  var stat = ОбработатьСообщения( node );

  ReplaceMacro("СчитатьБлок");
  ReplaceMacro("СчитатьСтроку");
  ReplaceMacro("ТекущаяСтрока");

  PrintImportReport();

  return stat;

OnError(er) /* Обработка ошибок времени выполонения */
  std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
  return FALSE;
end;