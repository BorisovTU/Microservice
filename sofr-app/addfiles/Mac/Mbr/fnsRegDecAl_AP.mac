/*
  $Name:         fnsRegDecAl_AP.mac
  $Module:       МБР
  $Description:  Функции для решений регистрационных органов APN, APO, APZ
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Функции для решений регистрационных органов APN, APO, APZ
//
// Программист  : Чукина Т.А.
//
// Создан       : 12.01.2018
//
//-----------------------------------------------------------------------------
import "fnsRegDecAl.mac", "xmlmestools.mac";

class (TRegDecision) TRegDecision_AP(p_rgdType : integer)
  InitTRegDecision();

  var rgdType : integer = p_rgdType;

  var LnkDocNumber : string = "",
      LnkDocDate : string = "",
      Сумма : string = "";

  // Блоки "ПриостПоруч", "ВозобнПоруч", "ОтозвПоруч"
  class BlockInstruction
    var НомПоруч : string = "",
        ДатаПоруч : string = "",
        ВидПоруч : string = "",
        НомСчПл : string = "",
        НаимФПоруч : string = "",
        СумПлат : string = "";
  end;

  var InstructionList : TArray = TArray(); 

  private macro СохранитьПоля_Поруч(fieldname : string, fieldvalue : string)

    if(fieldname == beginField)
      InstructionList[InstructionList.size] = BlockInstruction();
    else
      var N : integer = InstructionList.size - 1;

      if(fieldname == "НомПоруч")
        InstructionList[ N ].НомПоруч = fieldvalue;

      elif(fieldname == "ДатаПоруч")
        InstructionList[ N ].ДатаПоруч = fieldvalue;

      elif(fieldname == "ВидПоруч")
        InstructionList[ N ].ВидПоруч = fieldvalue;

      elif(fieldname == "НомСчПл")
        InstructionList[ N ].НомСчПл = fieldvalue;

      elif(fieldname == "НаимФПоруч")
        InstructionList[ N ].НаимФПоруч = fieldvalue;

      elif(fieldname == "СумПлат")
        InstructionList[ N ].СумПлат = fieldvalue;
      end;
    end;

  end;

  private macro SaveFieldsRlsSpecific
  ( fieldname : string, 
    fieldvalue : string, 
    blockname : string
  )
    if(fieldname == "НомРеш")
      DecisionNumber = fieldvalue;
    elif(fieldname == "ДатаРеш")
      DecisionDate = fieldvalue;
    elif(fieldname == "Сумма")
      Сумма = fieldvalue;
    elif(fieldname == "НомОтмРеш")
      LnkDocNumber = fieldvalue;
    elif(fieldname == "ДатаОтмРеш")
      LnkDocDate = fieldvalue;
    elif(fieldname == "ИНННП")
      ClientINN = fieldvalue;
    elif(fieldname == "КППНП")
      ClientKPP = fieldvalue;
    elif( index(blockname, "\\ПриостПоруч") or
          index(blockname, "\\ВозобнПоруч") or
          index(blockname, "\\ОтозвПоруч") 
        )
      СохранитьПоля_Поруч(fieldname, fieldvalue);
    end;
  end;

  private macro GetStartDate : date
    var StartDate : date = date(0, 0, 0);

    var RegMode : integer = Bnk_GetRegistryValue
      ( "PS\\REQOPENACC\\OPERATION\\365-П\\Дата_Действ_РешAPN",
        V_INTEGER,
        2
      );

    if(RegMode == 0)
      StartDate = date();
    elif(RegMode == 1)
      StartDate = {curdate};
    elif(RegMode == 2)
      StartDate = {DprtCurDate};
    end;

    return StartDate;    
  end;

  private macro FillRgdBuf(wlmes : StrucRef, wlregdec : StrucRef)
    // Вызов метода базового класса TRegDecision
    FillRgdBuf(wlmes, wlregdec);

    // Специфика APN, APO, APZ
    wlregdec.Type = rgdType;

    if(Сумма)
      wlregdec.Amount = money(Сумма);
    end;

    wlregdec.LnkDocNumber = LnkDocNumber;
    if(LnkDocDate)
      wlregdec.LnkDocDate = ToDateYYYY_MM_DD(LnkDocDate);
    end;

    wlregdec.StartDate = GetStartDate();
  end;

  private macro InsertLnkRdPm
  ( DecID : integer,
    Instr : BlockInstruction
  )
    RECORD wlrdpm(wlrdpm);
    ClearRecord( wlrdpm );
    wlrdpm.DecisionID    = DecID;
    wlrdpm.Number        = Instr.НомПоруч;
    wlrdpm.Date          = ToDateYYYY_MM_DD(Instr.ДатаПоруч);
    wlrdpm.Type          = IfThenElse(Instr.ВидПоруч == 2, "X", "");
    wlrdpm.PayerAccount  = Instr.НомСчПл;
    wlrdpm.Amount        = money(Instr.СумПлат)/100;
    wlrdpm.FileName      = Instr.НаимФПоруч;

    return ВставитьСвязанноеПоручениеДляРешенияФНС( wlrdpm );
  end;

  // Вставить связанные записи, после того как определился DecisionID
  private macro InsertRelatedRecords(DecID : integer) : bool
    for(var BlockInstr : BlockInstruction, InstructionList)
      if( not InsertLnkRdPm(DecID, BlockInstr) )
        std.msg("Ошибка при вставке связанного поручения для решения НО");
        return FALSE;
      end;
    end;

    return TRUE;
  end;

  private macro GetDocTypeName : string
    var llnote : string = "";
    GetElementAndNoteLLVALUES(OBJTYPE_TYPE_WLDECISION, string(rgdType), null, llnote);

    return llnote;
  end;

  macro CreateErrListWrongBank(wlmes : StrucRef) : RsbWlError
    var ErrList : RsbWlError = RsbWlError(0);

    var wlerr_buf : TRecHandler = TRecHandler("wlerror.dbt");

    // wlerror.Code
    wlerr_buf.rec.Code = "41";

    // wlerror.Description
    var MesReceiver : integer = GetDprtPartyID(wlmes.Department);
    wlerr_buf.rec.Description  = 
      "Электронный документ решение " + GetDocTypeName() + 
      " № " + DecisionNumber + " от " + DecisionDate +
      " ошибочно направлен налоговым органом не в тот банк (филиал банка).";

    if( ErrList.Insert(wlerr_buf) != 0 )
      RunError("Ошибка при заполнении списка ошибок в подтверждении банка");
    end;

    return ErrList;
  end;

end;

macro GenRegDecisionAP(wlmes : StrucRef, rgdType : integer) : bool

  var RegDecAP : TRegDecision_AP = TRegDecision_AP(rgdType);

  return GenRegDecision(wlmes, RegDecAP);
end;
