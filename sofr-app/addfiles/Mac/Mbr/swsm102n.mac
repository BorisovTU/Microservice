/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT102N                                   */
/*                                                                          */
/*  Имя файла: swsm102n.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

record Party(party);

FILE PaymMulty( pmpaym ) key 6;
FILE RlsMulty ( wlmesrls ) key 0 ;
FILE FormMulty( wlmesfrm ) key 0 ;

var subTRN;

/* Ключевой фильтр на сводные платежи */
macro OneStepMultyPaym( FirstDocMulty, PaymentID )
  if( FirstDocMulty )
     return ( (GetEQ( PaymMulty ))  and
              (PaymMulty.SubSplittedPayment == PaymentID) and
              (PaymMulty.CreatedInSS >= 0)
            );
  else
     return ( (Next( PaymMulty )) and
              (PaymMulty.SubSplittedPayment == PaymentID) and
              (PaymMulty.CreatedInSS >= 0)
            );
  end;
end;

macro СформироватьРеференсТранзакции(count)
  var ref;
  ref = string("S102/", subTRN:5, "/", count:2);
  ref = strsubst(ref, " ", "0");
  return ref;
end;

/* Формирование повторяющихся последовательностей */
macro ЗаписатьПлатежиСводного( publicCharges, publicRate, TotalSum, TotalRecCharges, Is52Present, NeedAcc )
  var IsFirst = TRUE, count = 0, field_value, Sum = moneyL(0), ComFIID, 
      ComAmount, IsTransit, BaseAmount, field33BPresent = 0, Tag;
  var RsPaym = RsbPayment( wlpmpaym.PaymentID );  
  RECORD rcv( pmroute );

  TotalRecCharges = $0;

  PaymMulty.SubSplittedPayment = wlpmpaym.PaymentID;
  PaymMulty.CreatedInSS = 1;   
  while( OneStepMultyPaym( IsFirst, wlpmpaym.PaymentID ) )
    IsFirst = FALSE;
    count = count + 1;

    /* Ищем свойства */
    if( FindPayment( PaymMulty.PaymentID, 0, 0, 0, 0, true, 0, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
      std.out( 2, "PaymentID: " + PaymMulty.PaymentID );
      RunError( "|Не найден платеж" );
    end;

    if ( ((wlpmpropdeb.PaymentID==PaymMulty.PaymentID) AND (wlpmpropdeb.IsSender=="X")) OR
        ((wlpmpropcred.PaymentID==PaymMulty.PaymentID) AND (wlpmpropcred.IsSender=="X")) )
     /* Транзитный платеж */
     IsTransit = true;
  else
     IsTransit = false;
  end;
    
    /* 21 - номер транзакции */
    field_value = СформироватьРеференсТранзакции( count );
    ЗаписатьПолеЛог( RelatedReferenceField, String(field_value) );

    /* 32B - код валюты, сумма */

    field_value = FillCurrencyOriginalOrderedAmountField( RsPaym );

    ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_B, field_value );

    Sum = Sum + PaymMulty.PayAmount;

    /* 57a - банк бенефициара */
    if( ПолучитьУзелПолучателяПлатежа(PaymMulty.PaymentID,rcv) AND
        ПолучитьСамыйДальнийУзелПлатежа(PaymMulty.PaymentID, RTDIR_OUT, Route) AND
        (rcv.RouteID!=Route.RouteID) )    
      if ( Route.CodeKind==PTCK_SWIFT )
         ЗаписатьПолеМаршрутаЛог_SB( AccountWithInstitutionField, "57a", Route, SWIFT_Tag_A );
      else
         ЗаписатьПолеМаршрутаЛог_SB( AccountWithInstitutionField, "57a", Route, SWIFT_Tag_C );
      end;
      /* Восстанавливаем контекст блока 'Sequence B' */
      if( not УстановитьКонтекстБлока( ".." ) )
        RunError("|Ошибка при установке контекста блока 'Sequence B' ");
      end;
    end;
    
    /* 59a - бенефициар */
    field_value = FillBeneficiaryCustomerFieldA_SB( PaymMulty, wlpmrmprop, wlpmpropdeb, wlpmpropcred );/*59A*/
    if (field_value == "")
      field_value = FillBeneficiaryCustomerField_SB( PaymMulty, wlpmrmprop, wlpmpropdeb, wlpmpropcred, NULL, NeedAcc );/*59*/
      if ( field_value=="" )
        std.out( 2, "PaymentID: " + PaymMulty.PaymentID );
        RunError( "|Не найден получатель платежа" );
      end;
      /* Устанавливаем контекст блока '59a' */
      if( not УстановитьКонтекстБлока( "59a" ) )
        RunError("|Ошибка при установке контекста блока '59a'");
      end;
      ЗаписатьПолеЛог( BeneficiaryCustomerField, field_value );
    else
      if( not УстановитьКонтекстБлока( "59a" ) )
        RunError("|Ошибка при установке контекста блока '59a'");
      end;
      ЗаписатьПолеЛог( BeneficiaryCustomerField_A, field_value );
    end;
    /* Восстанавливаем контекст блока 'Sequence B' */
    if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока 'Sequence B' ");
    end;

    /* 70  - информация для бенефициара */
    field_value = FillDetailsOfPaymentField( PaymMulty, wlpmrmprop, ALLOWCODES70_MT102_STANDART );
    ЗаписатьПолеЛог( DetailsOfPaymentField, field_value );

    /* 33B - Инструктируемая сумма, валюта */    
    if( (PaymMulty.OrderAmount != 0) and ((PaymMulty.OrderAmount != PaymMulty.FutureBaseAmount) or 
                                          (PaymMulty.OrderFIID != PaymMulty.BaseFIID)) 
      )
      field_value = ПолучитьКодВалютыЛог( PaymMulty.OrderFIID ) + GetSWIFTAmount( PaymMulty.OrderAmount );
      ЗаписатьПолеЛог( CurrencyOriginalOrderedAmountField, field_value );
      field33BPresent = 1;
    end;

    /* 71A - расходы */
    field_value = GetCommisCode (wlpmrmprop.ComissCharges);
    ЗаписатьПолеЛог( DetailsOfChargesField_A, field_value ) ;


    ЗаписатьКомиссииПлатежа( PaymMulty, IsTransit, field_value, false );

    if ( IsTransit OR (field_value==CHARGES_OUR) )
       /*  Вычисляем суммарную комиссию */
       if ( not GetSumCorrespCommis(PaymMulty, ComFIID, ComAmount) )
          RunError("|Ошибка вычисления комиссии");
       end;
          
       if ( ComFIID==wlpmpaym.PayFIID )
           TotalRecCharges = TotalRecCharges + ComAmount;
       else
           TotalRecCharges = TotalRecCharges + GetSumConvert(ComAmount, ComFIID, wlpmpaym.PayFIID);
       end;
    end;

    /* 36  - Кросс-курс */
    if( IsTransit )
      field_value = "";
      // из входящего сообщения
      ПолучитьПоле( GetMessageByPayment( PaymMulty.PaymentID ), ExchangeRateField, field_value );

      if( field_value )
        ЗаписатьПолеЛог( ExchangeRateField, field_value );
      end;
    end;
  end;

  SetParm(2, Sum);
  SetParm(3, TotalRecCharges);
end;

macro ПолучитьОбщиеПараметрыСводного( publicCharges, publicRate, publicBankOperationCode )
   var IsFirst = TRUE, field_value, CurrentCode, error, TextError;

   publicCharges = null;
   publicRate = null;

   PaymMulty.SubSplittedPayment = wlpmpaym.PaymentID;
   PaymMulty.CreatedInSS = 1;   
   while( OneStepMultyPaym( IsFirst, wlpmpaym.PaymentID ) )      

      /* Ищем свойства */
      if( FindPayment( PaymMulty.PaymentID, 0, 0, 0, 0, true, 0, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
         std.out( 2, "PaymentID: " + PaymMulty.PaymentID );
         RunError( "|Не найден платеж" );
      end;

      field_value = GetCommisCode (wlpmrmprop.ComissCharges);

      if ( IsFirst )
          publicCharges = field_value;
      elif ( (valtype(publicCharges)!=V_UNDEF) AND (publicCharges!=field_value) )
          publicCharges = null;
      end;

      if( (PaymMulty.FIID != PaymMulty.PayFIID) AND (PaymMulty.Rate != 0.0) )
         if ( IsFirst )
            publicRate = DefineSWIFTRateByPmPaym(PaymMulty);
         elif ( (valtype(publicRate)!=V_UNDEF) AND (publicRate!=DefineSWIFTRateByPmPaym(PaymMulty)) )
           publicRate = null;
         end;
      else
         publicRate = null;
      end;

      CurrentCode = "";
      NotesPaymentSelve.ReadNextNote( PaymMulty.PaymentID, BankOperationCodeField, CurrentCode, error );
//      if ( not error )
//        ЗаписатьПолеЛог( BankOperationCodeField, CurrentCode ) ;
//      end;
      /* Убедимся, что нет конфликтующих кодов */
      TextError = NotesPaymentSelve.ReadNextNote( PaymMulty.PaymentID, BankOperationCodeField, CurrentCode, error );
      if ( error )
         RunError( "|" + TextError );
      end;
      if ( IsFirst )
         publicBankOperationCode = CurrentCode;
      else
         if ( publicBankOperationCode!=CurrentCode )
            publicBankOperationCode = BANKOPERCODE_CREDIT;
         end;
      end;

      IsFirst = FALSE;
   end;  

   SetParm( 0, publicCharges );
   SetParm( 1, publicRate );
   SetParm( 2, publicBankOperationCode );
end;

macro GenMes( addrMes, addrPm )
  var field_value, field72, error, SumOfAmount, TotalRecCharges, publicCharges, 
      publicRate, publicBankOperationCode = "", field52Present = false, Tag, NeedAcc = true;

  RECORD rcv( pmroute );
  RECORD ocb(pmroute);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  var RsPaym = RsbPayment( wlpmpaym.PaymentID );

  PrintLog(2,"Генерация сообщения по МТ102N");

  SB_CheckFIID( RsPaym.BaseFIID );

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );
  subTRN = substr(wlmes.TRN, strlen(wlmes.TRN)-5);
 
  ПолучитьОбщиеПараметрыСводного( publicCharges, publicRate, publicBankOperationCode );
  if(Index(publicBankOperationCode, INSTITUTIONCODE_CHQB) > 0)
      NeedAcc = false;
  end;
  /* 23 */
  ЗаписатьПолеЛог( BankOperationCodeField, publicBankOperationCode ) ;  

  /* 50a - приказодатель */
  field_value = ГенерацияПоля50 ( RsPaym, Tag, NULL);
      if( field_value == "" )
        std.out( 2, "PaymentID: " + RsPaym.PaymentID );
        RunError( "|Не найден плательщик" );
      end;
  ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField + Tag, field_value );
    /* Восстанавливаем нулевой контекст */
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionFieldA_SB(RsPaym, Tag, NULL);
  if (field_value == "")
    field_value = FillOrderingInstitutionFieldB_SB(RsPaym, Tag, NULL);
      if (field_value == "")
        std.out(2, "PaymentID: "+RsPaym.PaymentID);
        RunError("|Не найден банк приказодателя");
      end;
    end;
  if (field_value != "")
    field52Present = true;
  end;
  ЗаписатьПолеВБлокКонтекст0(OrderingInstitutionField+Tag, "52a", field_value);
  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 71A - расходы */  
  if ( valtype(publicCharges)!=V_UNDEF )
     ЗаписатьПолеЛог( DetailsOfChargesField_A, publicCharges ) ;
  end;

  /* Устанавливаем контекст блока */
  if( not УстановитьКонтекстБлока( "Sequence B" ) )
    RunError("|Ошибка при установке контекста блока 'Sequence B'");
  end;

  ЗаписатьПлатежиСводного( publicCharges, publicRate, SumOfAmount, TotalRecCharges, field52Present, NeedAcc );

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 32A - дата валютирования, код валюты, сумма */
  var Date1 = ДатаПоУмолчанию; // без покрытия
  if( IsPmCoverMode() ) // с покрытием 
    Date1 = GetSWIFTDate( RsPaym.OutTransferDate );
  end;
    field_value = Date1 +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );
  
  if ( TotalRecCharges )
     ЗаписатьПолеЛог( ReceiversChargesField, ПолучитьКодВалютыЛог( RsPaym.ReceiverFIID )
                                                  + GetSWIFTAmount( TotalRecCharges ) ) ;
  end;

  /* 19 - Сумма платежей */
  if( SumOfAmount != RsPaym.ReceiverAmount )
    ЗаписатьПолеЛог( SumOfAmountField, GetSWIFTAmount(SumOfAmount) );
  end;

  /* 53a - корреспондент отправителя */
  if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
      ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, Route) AND
      (rcv.RouteID!=Route.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог_SB( Sender_sCorrespondentField, "53a", Route );
  end;

  /* 54a - Корреспондент получателя */
  if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
      ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, ocb) AND
      (rcv.RouteID!=ocb.RouteID) AND
      ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
      (rcv.RouteID!=Route.RouteID) )
    ЗаписатьПолеМаршрутаКонтекст0Лог_SB( Receiver_sCorrespondentField, "54a", Route);
  end ;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

