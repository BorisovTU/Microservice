/*
  $Name:         wlmatch.mac
  $Module:       Межбанковские расчеты
  $Description:  Отчет "Ведомость расхождения корсчетов"
*/
/*
  Отчет : Ведомость расхождения корсчтеов

  Подсистема - Межбанковские расчеты
  Создан 10.11.2000
  Программист Антон А. Бурцев (AB)

*/


import FIInter, wldoc, pm_common;

var FIID, Amount;
var FirstCall;
var DebetAmountSubTotal, CreditAmountSubTotal;
var OurDebetAmountSubTotal, OurCreditAmountSubTotal;
var CorrDebetAmountSubTotal, CorrCreditAmountSubTotal;
var DebetDocSubTotal, CreditDocSubTotal, Total;

const REPDOC_OURNOTCORR = 0,
      REPDOC_CORRNOTOUR = 1,
      REPDOC_WITHOUTDOC = 2;

macro SumToStr(FIID, sum)

   f_wlfininstr.FIID = FIID;
   if ( GetEQ( f_wlfininstr ) )
      return CurToStrAlt( sum, null, null, int(f_wlfininstr.ISO_Number) );
   else
      return string(sum) + " (Неизвестная валюта)";
   end;

end;

macro GetOperStr(oper)

  file person(person);

  person.Oper = oper;
  if ( GetEQ(person) )
     return string(oper) + " " + person.Name;
  else
     return string(oper);
  end;

end;

macro ResetData()
  Amount = $0;
  DebetAmountSubTotal  = $0;
  CreditAmountSubTotal = $0;
  DebetDocSubTotal  = 0;
  CreditDocSubTotal = 0;
  Total = 0;
end;

macro HeadLine(typeRepDoc)
   if( typeRepDoc == REPDOC_OURNOTCORR )
     [ Реестр платежей, проведенных Банком, но не проведенных корреспондентом];
   elif( typeRepDoc == REPDOC_CORRNOTOUR )
     [ Реестр платежей, проведенных корреспондентом, но не проведенных Банком];
   elif( typeRepDoc == REPDOC_WITHOUTDOC )
     [ Реестр проводок, проведенных Банком без первичных документов];
   end;
end;


macro Line(typeRepDoc)
   if( typeRepDoc == REPDOC_WITHOUTDOC )
     [+----------+---+--------------------+--------------------+-----------------+----------+];
   elif( (typeRepDoc == REPDOC_OURNOTCORR) OR (typeRepDoc == REPDOC_CORRNOTOUR) )
     [+----------+---+--------------------+-----------+--------------------+-----------------+----------+];
   else
     [+----------+--------------------+--------------------+-----------------+----------+];
   end;
end;

macro WriteHeader(typeRepDoc)

  if( typeRepDoc == REPDOC_WITHOUTDOC )
    [|  Номер   |Д/К|  Счет плательщика  |  Счет получателя   |  Сумма платежа  |   Дата   |];
  else
    [|  Номер   |Д/К|  Счет плательщика  |Банк получ |  Счет получателя   |  Сумма платежа  |   Дата   |];
  end;
end;


/* Инициализация ведомости */
macro InitReport(fi, cs, dateB, dateE)

   file cors(corschem) key 1;
   file party(party) key 0;

   cors.Number = cs;
   cors.FI_Kind = 1;
   cors.FIID = fi;

   if ( not GetEQ(cors) )
      msgbox("не найдена схема расчетов");
      return 1;
   end;

   party.PartyID = cors.CorrID;

   if ( not GetEQ(party) )
      msgbox("Не найден корреспондент в справочнике банков");
      return 1;
   end;

   ResetData();   
   FIID = fi;
   FirstCall = true;
   OurDebetAmountSubTotal   = $0;
   OurCreditAmountSubTotal  = $0;
   CorrDebetAmountSubTotal  = $0;
   CorrCreditAmountSubTotal = $0;
   

if( dateB == dateE)
[           СВЕРКА ВЫПИСКИ ПО КОРРЕСПОНДЕНТСКОМУ СЧЕТУ за ##########                    ](dateB:f);
else
[           СВЕРКА ВЫПИСКИ ПО КОРРЕСПОНДЕНТСКОМУ СЧЕТУ за период с ########## по ##########                  ](dateB:f, dateE:f);
end;

[
          Корсчет        :           ####################
          Валюта         :           #
          Схема расчетов :           # #
          Корреспондент  :           #
          Корсчет у корреспондента:  ####################
]
(
   cors.Account,
   ПолучитьИмяФинИн(fi),
   cors.Number,
   cors.Name,
   party.Name,
   cors.CorAccount
);
   
   return 0; /* Все OK */

end;

/* Вывод документа ведомости */
macro PrintDoc(cors, rsd, typeRepDoc)

   var DKFlag = "";   
   if( cors == NULL )
     return 1;
   end;

   //!!! Транзитные!
   if( rsd.value(1) == 0)
         /* начальный дебетовый */
         DKFlag = "Дт"
     else
         /* ответный кредитовый */
         DKFlag = "Кт"
   end;
   
   if( FirstCall == true )
     ResetData();   
     HeadLine(typeRepDoc);
     Line(typeRepDoc);
     WriteHeader(typeRepDoc);
     Line(typeRepDoc);
   end;


[|##########| # |####################|###########|####################|#################|##########|]
(
   rsd.value(0),   
   DKFlag,
   rsd.value(2),
   rsd.value(3),
   rsd.value(4),
   abs(rsd.value(5)):a,
   date(rsd.value(6))
);

   FirstCall = false;
   Amount = Amount + abs(rsd.value(5));
   if( DKFlag == "Дт")
     DebetAmountSubTotal = DebetAmountSubTotal + abs(rsd.value(5));
     DebetDocSubTotal    = DebetDocSubTotal + 1;
   else
     CreditAmountSubTotal = CreditAmountSubTotal + abs(rsd.value(5));
     CreditDocSubTotal    = CreditDocSubTotal + 1;
   end;
   if( DKFlag != "Дт")
    if( DebetDocSubTotal )
      [+--------------------------------------------------------------------------------------------------];
      [|     Итого в Дт корсчета ####################: |Документов: ########|#################|          |](cors.Account, DebetDocSubTotal:l, DebetAmountSubTotal:a:l);
      Line(typeRepDoc);
      Total = DebetDocSubTotal;
    end;
    DebetDocSubTotal = 0;
   end;
end;

macro PrintMemDoc(cors, rsd)

   var DKFlag = "";
   if( rsd.value(1) == 0)
         DKFlag = "Дт"
     else
         DKFlag = "Кт"
   end;

  if( FirstCall == true )
    ResetData();   
    HeadLine(REPDOC_WITHOUTDOC);
    WriteHeader(REPDOC_WITHOUTDOC);
    Line(REPDOC_WITHOUTDOC);
  end;


[|##########| # |####################|####################|#################|##########|]
(
   rsd.value(0),
   DKFlag,
   rsd.value(2),
   rsd.value(3),
   Abs(rsd.value(4)):a,
   rsd.value(5)
);

   FirstCall = false;
   Amount = Amount + Abs(rsd.value(4));
   if( DKFlag == "Дт")
     DebetAmountSubTotal = DebetAmountSubTotal + Abs(rsd.value(4));
     DebetDocSubTotal    = DebetDocSubTotal + 1;
   else
     CreditAmountSubTotal = CreditAmountSubTotal + Abs(rsd.value(4));
     CreditDocSubTotal    = CreditDocSubTotal + 1;
   end;
   if( DKFlag != "Дт")
    if( DebetDocSubTotal )
      [+--------------------------------------------------------------------------------------------------];
      [|     Итого в Дт корсчета ####################: |Документов: ########|#################|          |](cors.AccountDebetDocSubTotal:l, DebetAmountSubTotal:a:l);
      Line(REPDOC_WITHOUTDOC);
      Total = DebetDocSubTotal;
    end;
    DebetDocSubTotal = 0;
   end;

end;

/* Завершение ведомости */
macro DoneReport(cors, typeRepDoc)
    
  if(cors == NULL)
    return 1;
  end;

  if( FirstCall == false ) 
    //Line();
   //[+----------+---+--------------------+-----------+--------------------+-----------------+];  
      [+--------------------------------------------------------------------------------------------------];
    if( DebetDocSubTotal )
      [|     Итого в Дт корсчета ####################: |Документов: ########|#################|          |](cors.Account, DebetDocSubTotal:l, DebetAmountSubTotal:a:l);
      [+-----------------------------------------------+--------------------+-----------------+----------+];
    end;
    if( CreditDocSubTotal )
      [|     Итого в Кт корсчета ####################: |Документов: ########|#################|          |](cors.Account, CreditDocSubTotal:l, CreditAmountSubTotal:a:l);
      [+-----------------------------------------------+--------------------+-----------------+----------+];
    end;
    if( Total OR CreditDocSubTotal)
      [|                                        Всего: |Документов: ########|#################|          |]((Total+CreditDocSubTotal):l, Amount:a:l);
      [+-----------------------------------------------+--------------------+-----------------+----------+];
    end;
    println();
  else
  HeadLine(typeRepDoc);
  println();
  [                 Данных не найдено             ];
  println();
  end;

  if(typeRepDoc == REPDOC_OURNOTCORR)
    OurCreditAmountSubTotal = CreditAmountSubTotal;
    OurDebetAmountSubTotal  = DebetAmountSubTotal;
  elif(typeRepDoc == REPDOC_CORRNOTOUR)
    CorrCreditAmountSubTotal = CreditAmountSubTotal;
    CorrDebetAmountSubTotal  = DebetAmountSubTotal;
  end;

  Amount = $0;
  FirstCall = true;

end;

macro FloorReport(cors, dateB, dateE)
   file party(party) key 0;
   var Difference = $0;
   var Book_Our_Amount, Book_Corr_Amount;
   
   // Вычисляем расхождение корсчетов по исходящим остаткам выписок
   var param = TArray();
   param[0] = SQLParam(":dateE", dateE);
   var rsd = execSQLselect("select wlhead.t_RestOut from dwlhead_dbt wlhead, dwlconf_dbt wlconf, dwlcsrep_tmp wlcsrep "+
       "where wlhead.t_headID = wlconf.t_HeadID and wlconf.t_ConfID = wlcsrep.t_ConfID and wlcsrep.t_confDate <= :dateE order by wlcsrep.t_Confdate desc, wlcsrep.t_ConfID desc",
       param);
   if (rsd.moveNext())
     Book_Corr_Amount = rsd.value(0);
   else
     Book_Corr_Amount = $0;
   end;

   if ( cors != NULL )

     Book_Our_Amount = ПолучитьОстатокНаДату(cors.Account, 1, cors.FIID, dateE);
     Difference = Abs(Book_Corr_Amount) - Abs(Book_Our_Amount);
     if( dateB == dateE)
     [Расхождение корсчетов за ##########                         ##################](dateB:f, Difference:a);
     else
     [Расхождение корсчетов за период с ########## по ##########  ##################](dateB:f, dateE:f, Difference:a);
     end;

     [Остаток корсчета у корреспондента ####################      ##################](cors.CorAccount, Book_Corr_Amount:a);
     [Остаток корсчета в Банке          ####################      ##################](cors.Account, Book_Our_Amount:a);
     println();

   end;
end;

macro PrintRep(DateB, DateE, Corschem, FIID)
  InitReport( FIID, Corschem, DateB, DateE);
  file cors(corschem) key 1;
  cors.Number = Corschem;
  cors.FI_Kind = 1;
  cors.FIID = FIID;
  
  if ( not GetEQ(cors) )
      cors = NULL;
   end;
   
  // Выбираем платежи, проведенные в банке, но не проведенные у корреспондента
  var rsd = execSQLselect("select rep.t_Number, rep.t_DebetCredit, pm.t_PayerAccount, rep.t_ReceiverBankCode, pm.t_ReceiverAccount, rep.t_CarryAmount, rep.t_CarryDate "+
                  "from dwlcsrep_tmp rep, dpmpaym_dbt pm " +
                 "where rep.t_PaymentID <> 0 and rep.t_CarryID <> 0 and rep.t_ConfID = 0 and rep.t_PaymentID = pm.t_PaymentID "+
                  "order by rep.t_DebetCredit, rep.t_CarryDate, rep.t_CarryAmount");
  while (rsd.moveNext())
    PrintDoc(cors, rsd, REPDOC_OURNOTCORR);
  end; 
  DoneReport(cors, REPDOC_OURNOTCORR);
  // Выбираем платежи, проведенные у корреспондента, но не проведенные в банке
  rsd = execSQLselect("select conf.t_DocNumber, rep.t_DebetCredit, conf.t_Account, conf.t_CodeValue, conf.t_ReceiverAccount, rep.t_ConfAmount, rep.t_ConfDate "+
                  "from dwlcsrep_tmp rep, dwlconf_dbt conf " +
                 "where rep.t_PaymentID = 0 and rep.t_CarryID = 0 and rep.t_ConfID <> 0 and rep.t_ConfID = conf.t_ConfID "+
                  "order by rep.t_DebetCredit, rep.t_ConfDate, rep.t_ConfAmount");
  while (rsd.moveNext())
    PrintDoc(cors, rsd, REPDOC_CORRNOTOUR);
  end; 
  DoneReport(cors, REPDOC_CORRNOTOUR);
  FloorReport(cors, DateB, DateE);
  // проводки без документов
  rsd = execSQLselect("select acctrn.t_Numb_Document, rep.t_DebetCredit, acctrn.t_Account_Payer, acctrn.t_Account_Receiver, rep.t_CarryAmount, rep.t_CarryDate "+
                  "from dwlcsrep_tmp rep, dacctrn_dbt acctrn " +
                 "where rep.t_PaymentID = 0 and rep.t_CarryID <> 0 and acctrn.t_AccTrnID = rep.t_CarryID "+
                  "order by rep.t_DebetCredit, rep.t_CarryDate, rep.t_CarryAmount");
  while (rsd.moveNext())
    PrintMemDoc(cors, rsd);
  end; 
  DoneReport(cors, REPDOC_WITHOUTDOC);
    
end;
