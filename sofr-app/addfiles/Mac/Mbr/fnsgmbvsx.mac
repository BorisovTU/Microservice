/*
$Name:               fnsgmbvsx.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения по BVS XML
*/

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "mnsbxtool.mac", "pm_const.mac";

macro CheckObj(addrRegdec)
  SetBuff(wlregdec, addrRegdec);
  return CheckObj_FNSINFO_Xml( wlregdec );
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record wlregdec(wlregdec);
  SetBuff(wlregdec, addrRegdec);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BVS");

  var rs;
  // чтение глобализмов
  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();
  var CountPartMesFNS : integer = GetCountPartMesFNS();

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьФайл("ВЫПБНОСНОВ");

  // Файл \ ВЫПБНОСНОВ
  StartBlockLogXML("ВЫПБНОСНОВ");

  // атрибуты элемента "ВЫПБНОСНОВ"
  ЗаписатьПолеЛог("НомВыпис", wlregdec.Number);
  var ТипСправ : string = ЗаписатьПоле_ТипСправ(wlregdec, "ТипВыпис");
  ЗаписатьПолеЛог("НомЗапр", string(wlregdec.LnkDocNumber));
  ЗаписатьПолеЛог("ДатаЗапр", YYYY_MM_DD(wlregdec.LnkDocDate));
  ЗаписатьНаимПодтв(wlregdec);
  ЗаписатьПолеЛог("ВидВыпис", string(wlregdec.LnkTypeInfo));

  var tmpstr = "";
  if (OldMes.MesID != 0) //Перегенерация?
    tmpstr = GetFldValue(OldMes.MesID, "КолДопФ");
  elif (KindActionFNS == KIND_ACTION_FNS_RESEND)
    tmpstr = GetFldValue(OldfnsMessageID, "КолДопФ");
  elif (CountPartMesFNS > 0)
    tmpstr = string(CountPartMesFNS);
  else
    tmpstr = "0";
  end;
  ЗаписатьПолеЛог( "КолДопФ", tmpstr );
  ЗаписатьПолеЛог("ДатаФорм", YYYY_MM_DD(wlregdec.InputDate));
  ЗаписатьПолеЛог("ДатаНапр", YYYY_MM_DD(date()));

  ПереченьФилиалов(ТипСправ, wlregdec.OriginatorID);//блоки "Самостоят"

  СведенияОНалоговомОргане("СвНО", wlregdec);

  СведенияОБанке("СвБанк", wlmes.InsideAbonentID);

  ЗаписатьПлательщикаВСправкуФНС(wlregdec);

  rs = execSQLselect
    ( "select acclnk.* "
      "       from dwlacclnk_dbt acclnk \n"
      " where acclnk.t_ObjectType = ? "
      "   and acclnk.t_ObjectID = ? "
      "   and acclnk.t_State = " + WLD_STATUS_ACCLNK_CLOSED +
      "   and acclnk.t_Department = (" +
      "SELECT includedDprt.t_code\n"
      "                     FROM ddp_dep_dbt includedDprt\n"
      "                    WHERE includedDprt.t_PartyID = ?)",
      MakeArray( SQLParam("", OBJTYPE_FNSINFO), 
                 SQLParam("", wlregdec.DecisionID), 
                 SQLParam("", wlmes.InsideAbonentID) ) );

  var acclnkRec : TRecHandler = TRecHandler("wlacclnk");
  while (rs.moveNext())
    CopyRSetToFBuff(acclnkRec, rs);
    var LnkObjID : string = strLpad(string(accLnkRec.rec.LnkObjectID), 6, "0");

    if ( not IsLnkAccountPrcError(acclnkRec, WLD_TYPE_REGDEC_IVS) )// если не было ошибок при обработке счета
        var Type_Account : string = GetType_Account( rs.value("t_FIID"), rs.value("t_Account"), CHAPT1 );

        StartBlockLogXML("Сведения");
        ЗаписатьПолеЛог("ПорНом", LnkObjID);
        ЗаписатьПолеЛог("НомСч", rs.value("t_Account"));
        ЗаписатьПолеЛог("КодВал", GetISO_Number(rs.value("t_FIID")));
        ЗаписатьПолеЛог("ДатаНач", YYYY_MM_DD(wlregdec.Date));
        ЗаписатьПолеЛог("ДатаКон", YYYY_MM_DD(wlregdec.EndDate));
        ЗаписатьПолеЛог("ОстатНач", string(rs.value("t_RestIn")));
        ЗаписатьПолеЛог("СуммаДеб", string(rs.value("t_DebetTurn")));
        ЗаписатьПолеЛог("СуммаКред", string(rs.value("t_CreditTurn")));
        ЗаписатьПолеЛог("ОстатКон", string(rs.value("t_RestOut")));
        FinishBlockLogXML("Сведения");
    end;
  end;

  ЗаписатьПредБанка("ПредБанка", wlmes.InsideAbonentID, true);

  FinishBlockLogXML("ВЫПБНОСНОВ");

  FinishBlockLogXML("Файл");

  /*_НомерОтправ*/
  FnsWriteSendNum(OldMes);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;