/*
 $Name:   swgm910r.mac
 $Module: MBR
 $Description: Генерация сообщения по SWIFT MT910 стандарт RUR5
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT910 стандарт RUR5                         */
/*                                                                          */
/*  Имя файла: swgm910r.mac                                                 */
/*  Создан:  12.09.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac", "wlfindpm.mac";

private const Options_For_50a = "F K";

macro GenMes( addrMes, addrConf )
  var field_value;
  var PaymentObj, Tag;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlconf, addrConf );

  PrintLog(2,"Генерация сообщения по МТ910 стандарт RUR5");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 Related Reference */
  field_value = FillRelatedReferenceForDebitCreditConfirmation(wlconf);
  ЗаписатьПолеЛог( RelatedReferenceField, field_value );

  /* 25 - счет подтверждения */
  ЗаписатьПолеЛог( AccountIdentificationField, wlconf.Account );
  
  /* 13D - Date/Time Indication */
  field_value = GetSWIFTDate( date() ) + GetSWIFTTime(Time()) + GetSessTimeZone();
  ЗаписатьПолеЛог( DateTimeIndicationField, field_value );  
  
  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( wlconf.DateValue ) +
                ПолучитьКодВалютыЛог( wlconf.FIID ) +
                GetSWIFTAmount( wlconf.Sum );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );

  PaymentObj = RsbPayment(wlconf.DocumentID);
  // Если плательщик - банк, то заполняем поле 52а, в противном случае - 50a
  if( ((PaymentObj.Payer > 0) and ВидСубъекта(PaymentObj.Payer, PTK_BANK)) or ( not IsClientPayerAccount(PaymentObj) ) )
    /* 52a - банк приказодателя */
    if( (wlconf.CodeValue!="") OR (wlconf.BankName!="") )
      ClearRecord(Route);
      Route.PartyID  = wlconf.BankID;
      Route.CodeKind = wlconf.CodeKind;
      Route.CodeName = wlconf.CodeName;
      Route.CodeValue = wlconf.CodeValue;
      Route.Name = wlconf.BankName;
      Route.InAccount = "";
      Route.OutAccount = "";
      ЗаписатьПолеМаршрутаКонтекст0Лог( OrderingInstitutionField, "52a", Route, "", "", ST_RUR5 );
      if( not УстановитьКонтекстБлока( ".." ) )
         RunError("|Ошибка при установке контекста блока ..");
      end;
    end;
  else
    /* 50A - приказодатель */
    Tag = Options_For_50a;
    field_value = FillOrderingCustomerField103( PaymentObj, Tag, ST_RUR6 );
    if( field_value == "" )
      std.out( 2, "PaymentID: " + PaymentObj.PaymentID );
      RunError( "|Не найден плательщик" );
    else
      ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField + Tag, field_value, NULL );
      /* Восстанавливаем нулевой контекст блока */
      if( not УстановитьКонтекстБлока() )
        RunError("|Ошибка при установке нулевого контекста блока");
      end;
    end;
  end;

  /* 56a - банк приказодателя */
  if( (wlconf.CorrCodeValue!="") OR (wlconf.CorrBankName!="") )
    ClearRecord(Route);
    Route.PartyID  = wlconf.CorrBankID;
    Route.CodeKind = wlconf.CorrCodeKind;
    Route.CodeName = wlconf.CorrCodeName;
    Route.CodeValue = wlconf.CorrCodeValue;
    Route.Name = wlconf.CorrBankName;
    ЗаписатьПолеМаршрутаКонтекст0Лог( IntermediaryField, "56a", Route, "", "", ST_RUR5 );
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  /* 72 - информация для банка-получателя */
  field_value = "";
  field_value = FillNZP_byPaymentGround( field_value, wlconf.Description, ST_RUR5 );
  ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;