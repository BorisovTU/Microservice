/*
  $Name:         fnsMesAccCommon.mac
  $Module:       МБР
  $Description:  Макрос со всеми проверками по всем релизам
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Макрос со всеми проверками по всем релизам
//                работает единая функция MNS_CommonCheck с полным перечнем проверок - выполняются только те проверки, что переданы в массиве из макроса релиза
//                выполняем проверки до конца( не прерываем выполнение на первой не пройденой проверке )
//
// Программист  : Амелин А. Н.
//
// Создан       : 14.05.2018
//
//--------------------------------------------------------------------------------------

import  likepy, pm_tools, "bnk_ptlib.mac", "wlmnstls.mac",
        "fnsaccmsg_lib.mac", "adress.mac";
        
const GenMesCheckBankName                 = 0; // "Наименование банка";
const GenMesCheckBankINN                  = 1; // "ИНН банка";
const GenMesCheckBankSizeINN              = 2; // "Количество символов в ИНН банка";
const GenMesCheckBankKPP                  = 3; // "КПП банка";
const GenMesCheckBankSizeKPP              = 4; // "Количество символов в КПП банка";
const GenMesCheckBankSymbolKPP            = 5; // "Символы в КПП банка";
const GenMesCheckBankExistIFNS            = 6; // "Задана налоговая инспекция банка";
const GenMesCheckIFNShasCode              = 7; // "У налоговой инспекции банка есть код НО";
const GenMesCheckBankRegNumb              = 8; // "Регистрационный номер банка";
const GenMesCheckDepRegNumb               = 9; // "Регистрационный номер филиала";
const GenMesCheckBankBIC                  = 10; // "БИК банка";
const GenMesCheckBankOGRN                 = 11; // "ОГРН банка";
const GenMesCheckBankSizeOGRN             = 12; // "Количество символов в ОГРН банка";
const GenMesCheckClientName               = 13; // "Наименование клиента";
const GenMesCheckClientFIO                = 14; // "ФИО клиента";
const GenMesCheckClientINN                = 15; // "ИНН клиента";
const GenMesCheckClientSizeINN            = 16; // "Количество символов в ИНН клиента";
const GenMesCheckClientKPP                = 17; // "КПП клиента";
const GenMesCheckClientSizeKPP            = 18; // "Количество символов в КПП клиента";
const GenMesCheckClientSymbolKPP          = 19; // "Символы в КПП клиента не совпадают";
const GenMesCheckClientOGRN               = 20; // "ОГРН клиента";
const GenMesCheckClientSizeOGRN           = 21; // "Длина ОГРН клиента";
const GenMesCheckContractNumber           = 22; // "Номер договора";
const GenMesCheckAccountType              = 23; // "Тип счета";
const GenMesCheckEmployeeSigningMSG       = 24; // "Сотрудник, подписывающий сообщение";
const GenMesCheckChangePropertis          = 25; // "Изменение реквизитов при корректировке";
const GenMesCheckBankAddressExistIndex    = 26; // "В адресе банка (филиала) задан индекс";
const GenMesCheckBankAddressSizeIndex     = 27; // "Количество символов в индексе из адреса банка (филиала) ";
const GenMesCheckBankAdressRegionCode     = 28; // "В адресе банка (филиала) задан код региона";
const GenMesCheckBankAdressSizeRegionCode = 29; // "Количество символов в коде региона из адреса банка (филиала) ";
const GenMesCheckBankRegionCodeExist      = 30; // "Код региона из адреса банка есть в справочнике";
const GenMesCheckBankAdressExistLocality  = 31; // "В адресе банка (филиала) задан город или населенный пункт";
const GenMesCheckNotMatchPropetis         = 32; // "Не совпадают реквизиты клиента до и после изменения";
const GenMesCheckDULCode                  = 33; // "Код документа удостоверяющего личность (ДУЛ)";
const GenMesCheckDULSizeCode              = 34; // "Количество символов в коде ДУЛ";
const GenMesCheckDULSeriesAndNumber       = 35; // "Серия и номер ДУЛ";
const GenMesCheckDULDate                  = 36; // "Дата выдачи ДУЛ";
const GenMesCheckClientExistDatePlacBirth = 37; // "Дата и место рождения клиента заданы";
        
//-------- Общие вспомогательный функции для РАЗНЫХ релизов сообщений ------------------

// Формирование поля  "ВидСч"
private macro GetAccKindFld
( AccClient : integer, 
  Type_Account : string, 
  buff, 
  AccMsgKind : integer

) : string

  var AccKindName : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    AccKindName = ПолучитьПолеДляАннул( "ВидСч" );

  elif( buff.Origin != REQ_ACC_ORIGIN_PS )
    AccKindName = GetAccKindFld_BO(buff, AccMsgKind);

  else
    AccKindName = GetAccKindNameGK(AccClient, Type_Account);

  end;

  return AccKindName;
end;


// true - удалось заполнить поля, false - не удалось
private macro WriteTaxPayerRusOrg
( BlockName : string, 
  AccMsgKind : integer,
  INN : string, 
  KPP : string, 
  OGRN : string, 
  OrgName : string,
  ErrMsg : @string

) : bool

  // Check
  if(not INN)
    ErrMsg = "Не задан ИНН для субъекта \"" + OrgName + "\"";
    return FALSE;
  end;
  if(not IsGoodINN_Org(INN))
    ErrMsg = "Неверное значение ИНН: " + INN;
    return FALSE;
  end;

  if(KPP)
    if(not IsGoodKPP(KPP))
      ErrMsg = "Неверное значение КПП: " + KPP;
      return FALSE;
    end;
  elif(AccMsgKind == ACCMSG_KIND_OPEN)
    // Обязательно заполняется в сообщении об открытии счета
    ErrMsg = "Не задан КПП для субъекта \"" + OrgName + "\"";
    return FALSE;
  end;

  if(OGRN)
    if(not IsGoodOGRN_Org(OGRN))
      ErrMsg = "Неверное значение ОГРН: " + OGRN;
      return FALSE;
    end;
  end;

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННРО", INN);
  
  if(KPP)
    ЗаписатьПолеЛог("КПП", KPP);
  end;

  if(OGRN)
    ЗаписатьПолеЛог("ОГРН", OGRN);
  end;

  ЗаписатьПолеЛог("НаимОрг", OrgName);

  FinishBlockLogXML(BlockName);

  return TRUE;
end;


// Российская организация (НПРОТип)
// true - удалось заполнить поля, false - не удалось
macro WriteRusOrg
( PartyID : integer, 
  BlockName : string, 
  AccMsgKind : integer,
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa

) : bool

  var INN : string = "", KPP : string = "", OGRN : string = "", OrgName : string;

  // get INN, KPP, OGRN, OrgName
  if( reqopena != NULL )
    OrgName = reqopena.ClientName;
  elif( reqchnga != NULL )
    OrgName = reqchnga.ClientName;
  elif( reqclosa != NULL )
    OrgName = reqclosa.ClientName;
  end;

  if(not OrgName)
    ErrMsg = "Не задано наименование для субъекта " + PartyID;
    return FALSE;
  end;

  if( (reqchnga != NULL) AND (reqchnga.ClientINN) )
    INN = RemoveKPP(reqchnga.ClientINN);
    KPP = RemoveINN(reqchnga.ClientINN);
  elif( (reqopena != NULL) AND (reqopena.ClientINN) )
    INN = RemoveKPP(reqopena.ClientINN);
    KPP = RemoveINN(reqopena.ClientINN);
  elif( (reqclosa != NULL) AND (reqclosa.ClientINN) )
    INN = RemoveKPP(reqclosa.ClientINN);
    KPP = RemoveINN(reqclosa.ClientINN);
  else
    GetINN_KPP(PartyID, @INN, @KPP);
  end;

  if( (reqchnga != NULL) AND (reqchnga.ClientOGRN) )
    OGRN = reqchnga.ClientOGRN;
  elif( (reqopena != NULL) AND (reqopena.ClientOGRN) )
    OGRN = reqopena.ClientOGRN;
  elif( (reqclosa != NULL) AND (reqclosa.ClientOGRN) )
    OGRN = reqclosa.ClientOGRN;
  else
    OGRN = ПолучитьКодСубъекта(PartyID, PTCK_OGRN);
  end;

  // check and write fields
  return WriteTaxPayerRusOrg( BlockName, AccMsgKind, INN, KPP, OGRN, OrgName, @ErrMsg );
end;


// Реквизиты юридического лица до изменения (ИзмНПЮЛТип)
// true - удалось заполнить поля, false - не удалось
macro WriteRusOrgBeforeChng
( PartyID : integer, 
  BlockName : string, 
  ДатаИзмНП : date,
  ErrMsg : @string,
  reqchnga

) : bool

  var INN : string = "", KPP : string = "", OGRN : string = "", OrgName : string;
  var dt : date = ДатаИзмНП - 1;

  // get INN, KPP, OGRN, OrgName
  OrgName = reqchnga.ClientNameOld;

  if(not OrgName)
    ErrMsg = "Не задано наименование для субъекта " + PartyID;
    return FALSE;
  end;
  
  var FullINN : string = reqchnga.ClientINNOld;
  if(not FullINN)
    ErrMsg = "Не задан ИНН для субъекта \"" + OrgName + "\"";
    return FALSE;
  end;
  SplitFullINN(FullINN, INN, KPP);

  OGRN = reqchnga.ClientOGRNOld;

  // check and write fields
  return WriteTaxPayerRusOrg( BlockName, ACCMSG_KIND_CHNG, INN, KPP, OGRN, OrgName, @ErrMsg );
end;


// true - удалось заполнить поля, false - не удалось
private macro WriteTaxPayerIndEmployer
( 
  PartyID : integer,
  BlockName : string, 
  AccMsgKind : integer,
  EntityCode : string,
  INN : string, 
  OGRN : string, 
  SurName : string,
  Name : string,
  ParentName : string,
  ErrMsg : @string

) : bool

  // Check
  var FullName : string = SurName + " " + Name + " " + ParentName;

  if(not INN)
    ErrMsg = "Не задан ИНН для субъекта " + FullName;
    return FALSE;
  end;
  if(not IsGoodINN_Ind(INN))
    ErrMsg = "Неверное значение ИНН: " + INN;
    return FALSE;
  end;

  if(OGRN)
    if(not IsGoodOGRN_IndEmpl(OGRN))
      ErrMsg = "Неверное значение ОГРН: " + OGRN;
      return FALSE;
    end;
  end;

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННИП", INN);
  
  if(OGRN)
    ЗаписатьПолеЛог("ОГРНИП", OGRN);
  elif( (AccMsgKind == ACCMSG_KIND_OPEN) and
        (not InList(EntityCode, "3", "4", "5"))
      )
    ErrMsg = "Не задан ОГРН для субъекта " + FullName;
    return FALSE;
  end;

  WriteFIO("ФИОИП", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

  return TRUE;
end;


// Индивидуальный предприниматель, нотариус, адвокат (НПИПТип)
// true - удалось заполнить поля, false - не удалось
private macro WriteIndEmployer
( PartyID : integer, 
  BlockName : string, 
  AccMsgKind : integer,
  EntityCode : string,
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa

) : bool

  var INN : string = "", OGRN : string = "", 
      SurName : string = "", Name : string = "", ParentName : string = "";

  // get INN, OGRN, SurName, Name, ParentName
  if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg, reqchnga, reqopena, reqclosa ) )
    return FALSE;
  end;

  if( reqchnga != NULL )
    INN = RemoveKPP( reqchnga.ClientINN );
    OGRN = reqchnga.ClientOGRN;
  elif( reqopena != NULL )
    INN = RemoveKPP( reqopena.ClientINN );
    OGRN = reqopena.ClientOGRN;
  elif( reqclosa != NULL )
    INN = RemoveKPP( reqclosa.ClientINN );
    OGRN = reqclosa.ClientOGRN;
  else
    INN = GetOnlyINN(PartyID);
    OGRN = ПолучитьКодСубъекта(PartyID, PTCK_OGRN);
  end;

  // check and write fields
  return WriteTaxPayerIndEmployer( PartyID, BlockName, AccMsgKind, EntityCode, INN, OGRN, SurName, Name, ParentName, @ErrMsg );
end;


// Индивидуальный предприниматель, нотариус, адвокат до изменения реквизитов (ИзмНПИПТип)
// true - удалось заполнить поля, false - не удалось
macro WriteIndEmployerBeforeChng
( PartyID : integer, 
  BlockName : string, 
  ДатаИзмНП : date,
  EntityCode : string,
  ErrMsg : @string,
  reqchnga
) : bool

  var INN : string = "", OGRN : string = "", 
      SurName : string = "", Name : string = "", ParentName : string = "";
  var dt : date = ДатаИзмНП - 1;

  // get INN, OGRN, SurName, Name, ParentName
  if( reqchnga != NULL )
    var NameIO = SubStr( reqchnga.ClientName, Index(reqchnga.ClientName, "," ) + 1 );
    SurName = SubStr( reqchnga.ClientName, 1, Index(reqchnga.ClientName, "," ) );
    Name = SubStr( NameIO, 1, Index( NameIO, "," ) );
    ParentName = SubStr( NameIO, Index( NameIO, "," ) + 1 );
  else
    ErrMsg = "Ошибка при получении данных для субъекта " + PartyID;
    return FALSE;
  end;
  
  var FullINN : string = reqchnga.ClientINNOld;
  INN = RemoveKPP(FullINN);

  OGRN = reqchnga.ClientOGRNOld;

  // check and write fields
  return WriteTaxPayerIndEmployer( PartyID, BlockName, ACCMSG_KIND_CHNG, EntityCode, INN, OGRN, SurName, Name, ParentName, @ErrMsg );
end;


// Иностранная организация (НПИОТип)
// true - удалось заполнить поля, false - не удалось
private macro WriteForeignOrg
( PartyID : integer, 
  BlockName : string, 
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa

) : bool

  var INN : string = "", KPP : string = "", OGRN : string = "", OrgName : string;

  // get INN, KPP, OGRN, OrgName
  if(reqchnga != NULL)
    OrgName = reqchnga.ClientName;
  elif(reqopena != NULL)
    OrgName = reqopena.ClientName;
  elif(reqclosa != NULL)
    OrgName = reqclosa.ClientName;
  else
    OrgName = Bnk_GetPartyName(PartyID);  
  end;
  
  if(not OrgName)
    ErrMsg = "Не задано наименование для субъекта " + PartyID;
    return FALSE;
  end;

  if( (reqchnga != NULL) AND (reqchnga.ClientINN) )
    INN = RemoveKPP(reqchnga.ClientINN);
    KPP = RemoveINN(reqchnga.ClientINN);
  elif( (reqopena != NULL) AND (reqopena.ClientINN) )
    INN = RemoveKPP(reqopena.ClientINN);
    KPP = RemoveINN(reqopena.ClientINN);
  elif( (reqclosa != NULL) AND (reqclosa.ClientINN) )
    INN = RemoveKPP(reqclosa.ClientINN);
    KPP = RemoveINN(reqclosa.ClientINN);  
  else
    GetINN_KPP(PartyID, @INN, @KPP);
  end;
  
  if( (reqchnga != NULL) AND (reqchnga.ClientOGRN) )
    OGRN = reqchnga.ClientOGRN;
  elif( (reqopena != NULL) AND (reqopena.ClientOGRN) )
    OGRN = reqopena.ClientOGRN;
  elif( (reqclosa != NULL) AND (reqclosa.ClientOGRN) )
    OGRN = reqclosa.ClientOGRN;
  else
    OGRN = ПолучитьКодСубъекта(PartyID, PTCK_OGRN);
  end;

  // check
  if(OGRN)
    if(not IsGoodOGRN_Org(OGRN))
      ErrMsg = "Неверное значение ОГРН: " + OGRN;
      return FALSE;
    end;
  end;

  if(KPP)
    if(not IsGoodKPP(KPP))
      ErrMsg = "Неверное значение КПП: " + KPP;
      return FALSE;
    end;
  end;

  if(not INN)
    ErrMsg = "Не задан ИНН или КИО для субъекта \"" + OrgName + "\"";
    return FALSE;
  end;
  if( strlen(INN) == 5 )
    if(not IsGoodKIO(INN))
      ErrMsg = "Неверное значение КИО: " + INN;
      return FALSE;
    end;
  else
    if(not IsGoodINN_Org(INN))
      ErrMsg = "Неверное значение ИНН: " + INN;
      return FALSE;
    end;
  end;

  // write fields
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("НаимОрг", OrgName);
  if(OGRN)
    ЗаписатьПолеЛог("ОГРН", OGRN);
  end;
  if(KPP)
    ЗаписатьПолеЛог("КПП", KPP);
  end;
  if( strlen(INN) == 5 )
    WriteFieldLogXML("КИО", INN, true);
  else
    WriteFieldLogXML("ИННИО", INN, true);
  end;

  FinishBlockLogXML(BlockName);

  return TRUE;
end;


// Лицо без ИНН (НетИННТип)
// true - удалось заполнить поля, false - не удалось
private macro WritePartyNoINN
( PartyID : integer, 
  BlockName : string, 
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa
) : bool

  var IsLegalFormPersn : bool = IsPerson(PartyID), 
      OGRN : string = "",
      OrgName : string = "", 
      SurName : string = "", Name : string = "", ParentName : string = "";

  if(IsLegalFormPersn)
    if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg) )
      return FALSE;
    end;
  else
    if(reqchnga != NULL)
      OrgName = reqchnga.ClientName;
    elif(reqopena != NULL)
      OrgName = reqopena.ClientName;
    elif(reqclosa != NULL)
      OrgName = reqclosa.ClientName;
    else
      OrgName = Bnk_GetPartyName(PartyID);  
    end;
    
    if(not OrgName)
      ErrMsg = "Не задано наименование для субъекта " + PartyID;
      return FALSE;
    end;
  end;

  // write
  StartBlockLogXML(BlockName);

  if( (reqchnga != NULL) AND (reqchnga.ClientOGRN) )
    OGRN = reqchnga.ClientOGRN;
  elif( (reqopena != NULL) AND (reqopena.ClientOGRN) )
    OGRN = reqopena.ClientOGRN;
  elif( (reqclosa != NULL) AND (reqclosa.ClientOGRN) )
    OGRN = reqclosa.ClientOGRN;
  else
    OGRN = ПолучитьКодСубъекта(PartyID, PTCK_OGRN);
  end; 

  if(OGRN)
    ЗаписатьПолеЛог("ОГРН", OGRN);
  end;

  if(IsLegalFormPersn)
    WriteFIO("ФИОИП", SurName, Name, ParentName);
  else
    ЗаписатьПолеЛог("НаимОрг", OrgName);
  end;

  FinishBlockLogXML(BlockName);

  return TRUE;
end;

private macro WriteTaxRegistration( reqrec, FieldName : string)

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул( FieldName, GetOldfnsMessageID() );
  else
    var Серия : string = "", Номер : string = "",  
        RegDocKindName : string = "";

    if(FieldName == "СвидНУ")
      RegDocKindName = "Свидетельство о постановке на учет";
    else // "СвидГР"
      RegDocKindName = "Свидетельство о госрегистрации";
    end;
    
    if( reqrec.ClientSertif != "" )
      if( RegExMatch( reqrec.ClientSertif, "^ *\\d{2},{1} *\\d{7,9} *$" ) )
        Серия = substr( trim( reqrec.ClientSertif ), 1, index( trim(reqrec.ClientSertif), "," ) -1 );
        Номер = trim( substr( reqrec.ClientSertif, index( reqrec.ClientSertif, "," ) +1 ));
        ЗаписатьПолеЛог( FieldName, string(Серия, ",", Номер) );
      else
        RsbThrow("В документе регистрации \"" + RegDocKindName + "\" неверное значение серии/номера");
      end;
    end;
  
  end;

end;

private macro IsNotINN
( ClientID : integer,
  LegalForm : integer,
  NotResident : string,
  IsIndividEmployer : bool,
  AttrPartyType : integer,
  AccOpenDate : date

) : bool

  var INN : string = GetOnlyINN(ClientID);
  if(INN)
    return FALSE;
  end;

  if( (NotResident != "X") and 
      (LegalForm == PTLEGF_INST) and 
      (AccOpenDate < date(22, 9, 1994))
    )
    return TRUE;
  end;

  if( (NotResident != "X") and 

      ( (LegalForm == PTLEGF_INST) or
        (LegalForm == PTLEGF_PERSN) and IsIndividEmployer or
        (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_NOTARIUS) or
        (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_KFH)
      ) and

      (AccOpenDate < date(22, 9, 1996))
    )
    return TRUE;
  end;

  if( (LegalForm == PTLEGF_PERSN) and (AttrPartyType == PARTY_ATTR_PTTYPE_ADVOCAT) and
      (AccOpenDate < date(1, 1, 2004))
    )
    return TRUE;
  end;

  return FALSE;
end;

// Выбрать блок, который будем заполнять в элементе СвНП
private macro GetTaxPayerChoice
( EntityCode : string, 
  AccMsgKind : integer,
  ClientID : integer, 
  LegalForm : integer, 
  NotResident : string, 
  IsIndividEmployer : bool, 
  AttrPartyType : integer, 
  AccOpenDate : date

) : string

  var Code : integer = int(EntityCode);

  if(Code == 1)
    return "НПРО";

  elif((Code >= 2) and (Code <= 5))

    if( (AccMsgKind == ACCMSG_KIND_CLOSE) and 
        IsNotINN(ClientID, LegalForm, NotResident, IsIndividEmployer, AttrPartyType, AccOpenDate)
      )
      return "НетИНН";
    else
      return "НПИП";
    end;

  elif(Code == 6)
    return "НПИО";
  end;

  RunError("Ошибка программирования: неверное значение параметра EntityCode");
end;

// Сведения о налогоплательщике (СвНП)
macro WriteTaxPayerInfo
( ClientID : integer, 
  AccMsgKind : integer, 
  AccOpenDate : date, 
  ClientType311 : string,
  EntityCode : @string,
  reqchnga,
  reqopena,
  reqclosa
)

  var BlockName : string = "СвНП", ErrMsg : string = "";

  var LegalForm : integer = PTLEGF_ALL,
      IsIndividEmployer : bool = false,
      NotResident : string = "",
      AttrPartyType : integer = 0; // значение категории "Тип субъекта"

  if( ClientID > 0 )
    if( not GetTaxPayerPartyData(ClientID, @LegalForm, @IsIndividEmployer, @AttrPartyType, @NotResident) )
      RunError("Не найден субъект " + ClientID);
    end;
  end;

  EntityCode = GetEntityCodeFld
    ( LegalForm, 
      NotResident, 
      IsIndividEmployer, 
      AttrPartyType, 
      ClientType311 );

  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("КодЛица", EntityCode);

  if( AccMsgKind == ACCMSG_KIND_OPEN)
    WriteTaxRegistration( reqopena, "СвидНУ");
  elif( AccMsgKind == ACCMSG_KIND_CLOSE )
    WriteTaxRegistration( reqclosa, "СвидНУ");
  end;

  var Choice : string = GetTaxPayerChoice( EntityCode, AccMsgKind,
                                           ClientID, LegalForm, NotResident, 
                                           IsIndividEmployer, AttrPartyType, AccOpenDate
                                         );
  if(Choice == "НПРО")
    if( not WriteRusOrg(ClientID, "НПРО", AccMsgKind, @ErrMsg, reqchnga, reqopena, reqclosa) )
      RunError(ErrMsg);
    end;
  elif(Choice == "НПИП")
    if( not WriteIndEmployer(ClientID, "НПИП", AccMsgKind, EntityCode, @ErrMsg, reqchnga, reqopena, reqclosa ) )
      RunError(ErrMsg);
    end;
  elif(Choice == "НПИО")
    if( not WriteForeignOrg(ClientID, "НПИО", @ErrMsg, reqchnga, reqopena, reqclosa) )
      RunError(ErrMsg);
    end;
  elif(Choice == "НЕТИНН")
    if( not WritePartyNoINN(ClientID, "НЕТИНН", @ErrMsg, reqchnga, reqopena, reqclosa) )
      RunError(ErrMsg);
    end;
  else
    RunError("Ошибка программирования: функция GetTaxPayerChoice вернула неверное значение");
  end;

  FinishBlockLogXML(BlockName);
end;

// ДатаОткрСч
macro GetAccOpenDateFldVal( Счет : string, Валюта : integer, AccOpenDate : date, 
                            buff, AccMsgKind : integer ) : string

  var OpenDateStr : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    OpenDateStr = ПолучитьПолеДляАннул( "ДатаОткрСч" );

  elif( buff.Origin != REQ_ACC_ORIGIN_PS )
    OpenDateStr = GetAccOpenDateFldVal_BO(buff, AccMsgKind);

  else
    var OpenDate : date = date(0,0,0);

    var Note : string = ReadNoteForAccount(Счет, Валюта, 42); //42 = ACC_NOTE_KIND_DATE_CHANGE_TO_SETTLEMENT
    if(Note != "") 
      OpenDate = date(StrSubst(Note, " ", "0"));
    else
      if( AccOpenDate != date(0,0,0) )
        OpenDate = AccOpenDate;
      else
        OpenDate = {curdate};
      end;
    end;

    OpenDateStr = DDpMMpYYYY(OpenDate);
  end;

  return OpenDateStr;
end;

// атрибуты элемента СвСчет
macro WriteAccInfoAttrs(Счет : string, Валюта : integer, GenAcc, buff, AccMsgKind : integer)
  WriteFieldWithCheckAnnul( "НомСч", Счет );
  ЗаписатьПолеЛог( "ДатаОткрСч", GetAccOpenDateFldVal(Счет, Валюта, GenAcc.Open_Date, buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВидСч", GetAccKindFld(GenAcc.Client, GenAcc.Type_Account, buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВалСч", GetAccCurFldVal(Валюта) );
end;

// ДатаЗаклДог
macro ЗаписатьПоле_ДатаЗаклДог( DateOpen : date, UnionContrID : integer, 
                                Счет : string, Валюта : integer, buff )

  record SfCntr(sfcontr);

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул("ДатаЗаклДог", GetOldfnsMessageID());
  elif( buff.Origin == REQ_ACC_ORIGIN_LOANS )
    ЗаписатьПолеЛог( "ДатаЗаклДог", DDpMMpYYYY( buff.SfContrDate ) );
  else
    var ContractDate : date = DateOpen;

    if( UnionContrID != 0 )
      var select = "select t_DateConc from dsfunioncontr_dbt sfcontr" +                           //если у договора обслуживания счета есть связанный сводный договор,
                   " where sfcontr.t_unioncontrid = :UNIONCONTRID";                               //то заполняем ДатаРастДог датой заключения сводного договора

      var params = makeArray( SQLParam("UNIONCONTRID", UnionContrID) );
      var rs = execSQLselect( select, params );

      if( rs and rs.MoveNext() )
        ContractDate = date(rs.Value(0));
      end;
    elif( ПолучитьДоговорОбслуживания(Валюта, Счет, SfCntr) == 0 )
      ContractDate = SfCntr.DateConc;
    end;

    ЗаписатьПолеЛог( "ДатаЗаклДог", DDpMMpYYYY( ContractDate ) );
  end;
end;

macro ЗаписатьПоле_ДатаЗакрСч(AccCloseDate : date)

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул( "ДатаЗакрСч", GetOldfnsMessageID() );
  else
    var CloseDate : date = AccCloseDate;

    if( CloseDate == date(0,0,0) )
      CloseDate = {curdate};
    end;

    ЗаписатьПолеЛог( "ДатаЗакрСч", DDpMMpYYYY(CloseDate) );
  end;

end;

macro GetSfUnionContrCloseDate
(
  UnionContrID : integer, 
  SfUnionContrCloseDate : @date

) : bool

  if(UnionContrID != 0)                                                     
                                                                                   
    var select : string = 
             "select t_DateClose from dsfunioncontr_dbt sfcontr"+ 
             " where sfcontr.t_unioncontrid = :UNIONCONTRID";

    var params : TArray = makeArray( SQLParam("UNIONCONTRID", UnionContrID) );
    var rs : RsdRecordset = execSQLselect( select, params );
  
    if( rs and rs.MoveNext() )
      SfUnionContrCloseDate = date( rs.value("t_DateClose") );
      return TRUE;
    end;

  end;

  return FALSE;
end;

private macro AllContractsAreClosed(UnionContrID : integer) : bool
  var select = "select t_DateClose "+
               "  from dsfcontr_dbt sfcontr"+ 
               " where sfcontr.t_unioncontrid = :UNIONCONTRID"+
               "   and sfcontr.t_DateClose = :ZeroDate ";
    
  var params = makeArray( SQLParam("UNIONCONTRID", UnionContrID),
                          SQLParam("ZeroDate", date(0, 0, 0)) );
  if( existsSQLselect(select, params) )
    return FALSE;
  end;

  return TRUE;
end;

private macro GetContractCloseDate
( reqclosa, 
  SfCntrBuf, 
  IsSfUnionContr : bool, // необязательный
  SfUnionContrCloseDate : date // необязательный

) : date
  
  // Если у счета нет договора об-служивания, то заполняется 
  // датой заявления на закрытие счета (заявление от).
  if(SfCntrBuf == null)
    return reqclosa.Date;
  end;

  record SfCntr(sfcontr);
  Copy(SfCntr, SfCntrBuf);

  if(SfCntr.DateClose == date(0, 0, 0))
    return date(0, 0, 0); // договор не закрыт
  end;

  // Проверяем сводный договор
  if( (IsSfUnionContr == null) or (SfUnionContrCloseDate == null) )
    IsSfUnionContr = GetSfUnionContrCloseDate( SfCntr.UnionContrID, 
                                              @SfUnionContrCloseDate );
  end;

  if( // Договор обслуживания счета не связан со сводным договором
      not IsSfUnionContr or

      // Договор обслуживания счета связан со сводным договором и 
      // этот сводный договор закрыт
      (SfUnionContrCloseDate != date(0, 0, 0)) or

      // Договор обслуживания связан с незакрытым сводным договором и  
      // все индивидуальные договоры по этому счету закрыты
      AllContractsAreClosed(SfCntr.UnionContrID)
    )

    // Заполняется датой закрытия договора обслуживания
    return SfCntr.DateClose;
  end;

  return date(0, 0, 0);
end;

macro ЗаписатьПоле_ДатаРастДог( reqclosa, SfCntrBuf, vers : integer, 
                               IsSfUnionContr : bool, SfUnionContrCloseDate : date // могут быть не переданы
                             )

  var FieldValue : string = "";
  var DateClose : date = GetContractCloseDate(reqclosa, SfCntrBuf, IsSfUnionContr, SfUnionContrCloseDate);

  if( DateClose != date(0, 0, 0) )
    FieldValue = DDpMMpYYYY(DateClose);
  else
    if(vers < 510)
      FieldValue = " "; // предписанное поле
    end;
  end;

  if(FieldValue)
    ЗаписатьПолеЛог( "ДатаРастДог", FieldValue );
  end;

end;



private macro GetBirthInfoByReq
( req, 
  IsOld : bool,
  BirthDate : @date, 
  BirthPlace : @string
)
  macro ReqFld(FieldName : string) : variant
    return GetReqClientFld(req, FieldName, IsOld);
  end;

  BirthDate = ReqFld("ClientBirthDate");
  BirthPlace = ReqFld("ClientBirthPlace");
end;

private macro GetPaperInfoByReq
( req, 
  IsOld : bool,
  PaperCode : @string,
  PaperSerNum : @string,
  PaperDate : @date
)
  macro ReqFld(FieldName : string) : variant
    return GetReqClientFld(req, FieldName, IsOld);
  end;

  PaperCode = ReqFld("ClientCardType");
  PaperSerNum = ReqFld("ClientCardNum");
  PaperDate = ReqFld("ClientCardDate");
end;

// Физическое лицо, не являющееся предпринимателем
// true - удалось заполнить поля, false - не удалось
private macro WriteIndividNotEmployer
( PartyID : integer, 
  BlockName : string, 
  AccMsgKind : integer,
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa,
  IsOld : bool
) : bool

  macro ReqFld(FieldName : string) : variant
    if( reqchnga != null )
      return GetReqClientFld(reqchnga, FieldName, IsOld);
    elif( reqopena != null)
      return GetReqClientFld(reqopena, FieldName);
    elif( reqclosa != null)
      return GetReqClientFld(reqclosa, FieldName);
    end;
  end;

  macro GetReq()
    if( reqchnga != null )
      return reqchnga;
    elif( reqopena != null)
      return reqopena;
    elif( reqclosa != null)
      return reqclosa;
    end;
  end;


  macro GetCorrectBirthDate(BirthDate : date) : string
    if(BirthDate == date(0, 0, 0))
      if(AccMsgKind == ACCMSG_KIND_OPEN)
        RunError("Не указана дата рождения клиента, которому открывается счет");
      else
        BirthDate = date(11, 11, 1111); // AAN Это не моё - так было сделано для генерации, а т.к. я только проверки правлю, а не генер., то и править не стал 
      end;
    end;

    return DDpMMpYYYY(BirthDate);
  end;

  var INN : string = "", BirthDate : date = date(0, 0, 0), BirthPlace : string = "",
      PaperCode : string = "", PaperSerNum : string = "", PaperDate : date = date(0, 0, 0),
      SurName : string = "", Name : string = "", ParentName : string = "";

  // get and check data
  if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg, reqchnga, reqopena, reqclosa, IsOld) )
    return FALSE;
  end;

  var FullName : string = SurName + " " + Name + " " + ParentName;

  // ИНН
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    INN = RemoveKPP( ReqFld("ClientINN") );
  else
    INN = GetOnlyINN(PartyID);
  end;
  if(INN and not IsGoodINN_Ind(INN))
    ErrMsg = "Неверное значение ИНН: " + INN;
    return FALSE;
  end;

  // Дата и место рождения
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    GetBirthInfoByReq( GetReq(), IsOld, @BirthDate, @BirthPlace );
  elif( not GetBirthInfo(PartyID, @BirthDate, @BirthPlace) )
    ErrMsg = "Не найдены данные физического лица" + FullName;
    return FALSE;
  end;
  if(not BirthPlace)
    BirthPlace = "Неизвестно";
  end;

  // Документ, удостоверяющий личность
  if( (reqchnga != null) or (reqopena != null) or (reqclosa != null) )
    GetPaperInfoByReq( GetReq(), IsOld, @PaperCode, @PaperSerNum, @PaperDate );
  else
    GetPaperInfo(PartyID, @PaperCode, @PaperSerNum, @PaperDate);
  end;
  if(not PaperCode)
    if(AccMsgKind == ACCMSG_KIND_OPEN)
      ErrMsg = "Не найден документ, удостоверяющий личность физ.лица " + FullName;
      return FALSE;
    else
      PaperCode = "91";
    end;
  end;
  if(not PaperSerNum)
    if(AccMsgKind == ACCMSG_KIND_OPEN)
      ErrMsg = "Для клиента " + FullName + " не заданы серия и/или номер документа, удостоверяющего личность";
      return FALSE;
    else
      PaperSerNum = "Неизвестно";
    end;
  end;
  if(PaperDate == date(0,0,0))
    if( InList(PaperCode, "14", "91") and (AccMsgKind != ACCMSG_KIND_OPEN) )
      PaperDate = date(11, 11, 1111);   // AAN Это не моё - так было сделано для генерации, а т.к. я только проверки правлю, а не генер., то и править не стал 
    else
      ErrMsg = "Не указана дата выдачи документа, удостоверяющего личность физ.лица " + FullName;
      return FALSE;
    end;
  end;

  // write
  StartBlockLogXML(BlockName);

  if(INN)
    ЗаписатьПолеЛог("ИННФЛ", INN);
  end;

  ЗаписатьПолеЛог("ДатаРожд", GetCorrectBirthDate(BirthDate) );
  ЗаписатьПолеЛог("МестоРожд", BirthPlace);
  ЗаписатьПолеЛог("КодДУЛ", PaperCode);
  ЗаписатьПолеЛог("СерНомДок", PaperSerNum);
  ЗаписатьПолеЛог("ДатаДок", DDpMMpYYYY(PaperDate));

  WriteFIO("ФИОФЛ", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

  return TRUE;
end;

// Сведения о налогоплательщике (СвНП)
macro WriteTaxPayerInfo_Ind
( ClientID : integer, 
  ClientType311 : string,
  AccMsgKind : integer,
  reqchnga,
  reqopena,
  reqclosa,
  IsOld : bool
)

  var BlockName : string = "СвНП", NPFLBlockName : string = "НПФЛ", ErrMsg : string = "";
  if(IsOld)
    BlockName = "СвНПСтар";
    NPFLBlockName = "НПФЛСтар";
  end;

  StartBlockLogXML(BlockName);

  if(IsOld and (reqchnga != NULL))
    WriteFieldWithCheckAnnul( "ДатаИзмФЛ", DDpMMpYYYY(reqchnga.ClientChangeDate) );
  else
    ЗаписатьПолеЛог("КодЛица", "7");
  end;

  if( not WriteIndividNotEmployer(ClientID, NPFLBlockName, AccMsgKind, @ErrMsg, reqchnga, reqopena, reqclosa, IsOld) )
    RunError(ErrMsg);
  end;

  FinishBlockLogXML(BlockName);
end;


// атрибуты элемента СвСчет
private macro WriteAccInfoAttrs_Ind(buff, AccMsgKind : integer)
  WriteFieldWithCheckAnnul( "НомСч", buff.Account );
  ЗаписатьПолеЛог( "ДатаОткрСч", GetAccOpenDateFldVal_BO(buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВидСч", GetAccKindFld_BO(buff, AccMsgKind) );
  ЗаписатьПолеЛог( "ВалСч", GetAccCurFldVal(buff.Code_Currency) );
end;

// СвСчет
macro WriteAccInfo_Ind(buff, AccMsgKind : integer, WriteAccInfoOpenClose : ProcRef)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  WriteAccInfoAttrs_Ind(buff, AccMsgKind);

  // Файл\Документ\СвСчет\(Открыт|Закрыт)
  ExecMacro2(WriteAccInfoOpenClose, buff);

  FinishBlockLogXML("СвСчет");
end;


macro ПолучитьСчетСВалютой( buff, Открытие, Счет, Валюта )
  record AccIn(account);
  record AccOut(account);
  FILE reqlinka(reqlinka) key 0;

  Var ТипСчета; 
  var rs:object;
  var select:string;
  var params:TArray;

  ТипСчета = GetAccountType();
  if( ТипСчета == -1 )
    SetParm(2, buff.Account);
    SetParm(3, buff.Code_Currency);
    return;
  end;
  if( not Открытие )  // закрыт
     AccIn.Chapter = 1;
     AccIn.Account = buff.Account;
     AccIn.Code_Currency = buff.Code_Currency;
     select = "select t_AttrID from dobjlink_dbt where t_ObjectType = :OBJTYPE_ACCOUNT"+
                    " and t_ObjectID = :OBID"
                    " and t_GroupID = :TypeScore and t_AttrType = :OBJTYPE_ACCOUNT"; 

     params = makeArray( SQLParam("OBJTYPE_ACCOUNT", OBJTYPE_ACCOUNT),
                         SQLParam("OBID",UniID( AccIn, OBJTYPE_ACCOUNT )),
                         SQLParam("TypeScore",ТипСчета ));
     rs = execSQLselect( select, params, FALSE );
     if( rs.MoveNext())
        RestoreFromUniID(rs.Value(0), AccOut, OBJTYPE_ACCOUNT);
        SetParm(2, AccOut.Account);
        SetParm(3, AccOut.Code_Currency);
     else
        RunError("Не найден связанный счет");
     end;
  else
     ClearRecord(reqlinka);
     reqlinka.DocKind   = PS_REQOPENA;
     reqlinka.RequestID = buff.RequestID;
     reqlinka.GroupID   = ТипСчета;
     reqlinka.Action    = 0;
     if(not getEQ(reqlinka))
        RunError("Не найден связанный счет");
     else
        SetParm(2, reqlinka.Account);
        SetParm(3, reqlinka.Code_Currency);
        if( ТипСчета == 6 )
          buff.Type_Account = "Y";
        end;
     end;
  end;
end;


//-------- Общие вспомогательный функции для РАЗНЫХ релизов сообщений закончились ------



//-------------------- Вспомогательный приватные функции -------------------------------

private macro GetAccDprt( Req, kind ) : integer
  var AccDprt : integer = 0;

  record Acc(account);
  if(kind == ACCMSG_KIND_OPEN)
    AccDprt = Req.AccountDepartment;
  elif(kind == ACCMSG_KIND_CLOSE)
    var Счет : string, Валюта : integer;
    ПолучитьСчетСВалютой(Req, 0 /*Закрытие*/, Счет, Валюта);

    if(Req.Origin == REQ_ACC_ORIGIN_PS)
      ПолучитьСчет(Валюта, Счет, Acc );
    else
      ПолучитьСчет_БО(Валюта, Счет, Req, OBJTYPE_REQCLOSA, Acc);
    end;

    AccDprt = Acc.Department;
  elif(kind == ACCMSG_KIND_CHNG)
    if(Req.Origin == REQ_ACC_ORIGIN_PS)
      ПолучитьСчет(Req.OldAccountFIID, Req.OldAccount, Acc );
    else
      ПолучитьСчет_БО(Req.OldAccountFIID, Req.OldAccount, Req, OBJTYPE_REQCHANGE, Acc);
    end;

    AccDprt = Acc.Department;
  end;

  return AccDprt;
end;

// Проверка на неодинаковые символы 
private macro AllSymbolsAreEqual(s : string) : bool
  var FirstSymbol : string = substr(s, 1, 1),
      Len : integer = strlen(s);

  var i : integer = 2;

  while(i <= Len)
    if( substr(s, i, 1) != FirstSymbol )
      return FALSE;
    end;

    i = i + 1;
  end;

  return TRUE;
end;

private macro HaveFIO( req )
  if( (index( req.ClientName, " " ) > 0) or ( index( req.ClientName, "," ) > 0 ) )
    return true;
  else
    return false;
  end;
end;

private macro GetFieldByName( name ):string
  var select = " SELECT T_VALUE, T_TPFIELDID FROM DWLMESVAL_TMP "
               "  WHERE T_TPFIELDID = (SELECT T_TPFIELDID FROM DWLTPFLD_DBT "
               "                        WHERE T_NAME = ?) ";
  var rs = execSQLselect( select, makeArray( SQLParam("", name) ), FALSE );
  if( rs.moveNext )
    return rs.value(0);
  end;
  return "";
end;

private macro GetFieldArrayByName( name ):TArray
  var result : TArray = TArray();
  
  var select = " SELECT T_VALUE, T_TPFIELDID FROM DWLMESVAL_TMP "
               "  WHERE T_TPFIELDID = (SELECT T_TPFIELDID FROM DWLTPFLD_DBT "
               "                        WHERE T_NAME = ?) ";
  var rs = execSQLselect( select, makeArray( SQLParam("", name) ), FALSE );
  var i = 0;
  while( rs.moveNext )
    result(i) = rs.value(0);
    i = i + 1;
  end;
  return result;
end;

private macro GetCodeAndPartyName( PartyID, code : @string ) : string
  var select = " select t_PartyName, t_Code from dp_dep "
               "  where t_Parentcode = 0 and t_PartyID = ? ";
  var rs = execSQLselect( select, makeArray( SQLParam( "",PartyID ) ), FALSE );
  code = ПолучитьКодСубъекта( PartyID, PTCK_CONTR );
  if( rs.moveNext )
    if( code == "" )
      code = rs.value(1);
    end;
    return rs.value(0);
  end;
  return "";
end;

private macro IsNotHeadOrg( PartyID )
  var select = " select count(1) from ddp_dep_dbt "
               "  where t_Parentcode <> 0 and t_PartyID = ? ";
  var rs = execSQLselect( select, makeArray(SQLParam("",PartyID)), FALSE );
  if( rs.moveNext )
    if( rs.value(0) != 0)
      return true;
    else
      return false;
    end;
  end;
  return false;
end;

private macro CheckSameSymb( KPP ) : bool
  var res = false;
  var ar = StrSplit2( KPP, 1 );
  var t = ar[0];
  for( var i, 1, ar.size-1 )
    if( t != ar[i] )
      res = true;
      break;
    end;
  end;
  return res;
end;

// Код налогового органа
private macro GetTaxInstitutionCodeCheck
( TaxInstitution : integer, 
  TaxInstName : string, 
  FullErrorCheck : @TArray

) : string

  // TaxInstName - необязательный параметр
  if(TaxInstName == null)
    TaxInstName = "";
  end;

  var КодНОБ : string = "";

  // У налоговой инспекции банка есть код НО
  var Error : integer = 0;
  КодНОБ = ПолучитьКодСубъекта( TaxInstitution, PTCK_MNS, Error);
  if( Error or not КодНОБ )
    FullErrorCheck( FullErrorCheck.size ) = "Не определен код налогового органа " + TaxInstName;
  end;

  // AAN По проекту нет такой проверки сейчас, так что убрал пока
  // поле соответсвует шаблону: <xs:pattern value="([0-9]{1}[1-9]{1}|[1-9]{1}[0-9]{1}){2}"/>
  // if( not IsNDigits(КодНОБ, 4, true) or 
      // (substr(КодНОБ, 3, 2) == "00") and (КодНОБ != "0400")
    // )
    // RunError("Неверный код налогового органа банка: " + КодНОБ);
  // end;
  
  return КодНОБ;
end;

//-------------------- Вспомогательный приватные функции закончились -------------------


// CHECK FIELDS: РегНом; НомФ, НаимКО, ИННКО, КППКО, ОГРН, Индекс, КодРегион, Город, НаселПункт, ДатаИзмБанк;
// Проверки при изменении реквизитов банка
macro CheckReqChangeBank( Party, err:@string )
  err = "";
  var РегНом          = GetFieldArrayByName("РегНом"),
      ИННКО           = GetFieldArrayByName("ИННКО"),
      КППКО           = GetFieldArrayByName("КППКО"),
      БИК             = GetFieldArrayByName("БИК"),
      НаимКО          = GetFieldArrayByName("НаимКО"),
      ОГРНКО          = GetFieldArrayByName("ОГРНКО"),
      НомФ            = GetFieldArrayByName("НомФ"),
      Индекс          = GetFieldArrayByName("Индекс"),
      КодРегион       = GetFieldArrayByName("КодРегион"),
      Город           = GetFieldArrayByName("Город"),
      НаселПункт      = GetFieldArrayByName("НаселПункт"); 
      
  var PartyIDBefore   = GetCodeOwnerID(PTCK_BIC, БИК(1)),
      PartyIDAfter    = GetCodeOwnerID(PTCK_BIC, БИК(0)),
      isNotHeadBefore = IsNotHeadOrg(PartyIDBefore),
      isNotHeadAfter  = IsNotHeadOrg(PartyIDAfter),
      PartyNameBefore = Bnk_GetPartyName(PartyIDBefore),
      PartyNameAfter  = Bnk_GetPartyName(PartyIDAfter);
      
  var code;
     
  // Проверки для реквизитов банка ПОСЛЕ изменений    
  if ( not РегНом(0) )
    err = "Для " + PartyNameAfter + " не задан регистрационный номер кредитной организации";
  elif( isNotHeadAfter and ( (НомФ(0) == "") or (НомФ(0) == "0") ) )
    err = "Для " + PartyNameAfter + " не задан порядковый номер филиала";
  elif( НаимКО(0) == "" )
    code = 0;
    if( isNotHeadAfter and (GetCodeAndPartyName(Party.PartyID, @code) == "") )
      err = "Для " + code + " не задано наименование";
    end;
  elif( ИННКО(0) == "" )
    err = "Для " + PartyNameAfter + " не задан ИНН";
  elif( strlen(ИННКО(0)) != 10 )
    err = "Для " + PartyNameAfter + " задано неверное значение ИНН";
  elif( КППКО(0) == "" )
    err = "Для " + PartyNameAfter + " не задан КПП";
  elif( strlen(КППКО(0)) != 9  )
    err = "Для " + PartyNameAfter + " задано неверное значение КПП";
  elif( not CheckSameSymb(КППКО(0))          )
    err = "КПП банка (филиала) " + PartyNameAfter + " не должен состоять из одинаковых цифр";
  elif( ОГРНКО(0) == ""          )
    err = "Для " + PartyNameAfter + " не задан ОГРН";
  elif( strlen(ОГРНКО(0)) != 13                )
    err = "Для " + PartyNameAfter + " задано неверное значение ОГРН";
  elif( (Индекс(0) == "") or (КодРегион(0) == "") )
    if( Индекс(0) == "" )
      err = "В адресе " + PartyNameAfter + " не указаны обязательные элементы: Индекс";
    end;
    if( КодРегион(0) == "" )
      if( err )
        err = err + " КодРегиона";
      else
        err = "В адресе " + PartyNameAfter + " не указаны обязательные элементы: КодРегиона";
      end;
    end;
  elif( (not InList(КодРегион(0),"77", "78", "92")) and ((Город(0) == "") or (НаселПункт(0) == "")) )
    err = "В адресе банка (филиала) " + PartyNameAfter + " должен быть заполнен либо город, либо населенный пункт.";
  
  // Проверки для реквизитов банка ДО изменений    
  elif ( not РегНом(1) )
    err = "Для " + PartyNameBefore + " не задан регистрационный номер кредитной организации";
  elif( isNotHeadBefore and ( (НомФ(1) == "") or (НомФ(1) == "0") ) )
    err = "Для " + PartyNameBefore + " не задан порядковый номер филиала";
  elif( НаимКО(1) == "" )
    code = 0;
    if( isNotHeadAfter and (GetCodeAndPartyName(Party.PartyID, @code) == "") )
      err = "Для " + code + " не задано наименование";
    end;
  elif( ИННКО(1) == "" )
    err = "Для " + PartyNameBefore + " не задан ИНН";
  elif( strlen(ИННКО(1)) != 10 )
    err = "Для " + PartyNameBefore + " задано неверное значение ИНН";
  elif( КППКО(1) == "" )
    err = "Для " + PartyNameBefore + " не задан КПП";
  elif( strlen(КППКО(1)) != 9  )
    err = "Для " + PartyNameBefore + " задано неверное значение КПП";
  elif( not CheckSameSymb(КППКО(1))          )
    err = "КПП банка (филиала) " + PartyNameBefore + " не должен состоять из одинаковых цифр";
  elif( ОГРНКО(1) == ""          )
    err = "Для " + PartyNameBefore + " не задан ОГРН";
  elif( strlen(ОГРНКО(1)) != 13                )
    err = "Для " + PartyNameBefore + " задано неверное значение ОГРН";
  elif( (Индекс(1) == "") or (КодРегион(1) == "") )
    if( Индекс(1) == "" )
      err = "В адресе " + PartyNameBefore + " не указаны обязательные элементы: Индекс";
    end;
    if( КодРегион(1) == "" )
      if( err )
        err = err + " КодРегиона";
      else
        err = "В адресе " + PartyNameBefore + " не указаны обязательные элементы: КодРегиона";
      end;
    end;
  elif( (not InList(КодРегион(1),"77", "78", "92")) and ((Город(1) == "") or (НаселПункт(1) == "")) )
    err = "В адресе банка (филиала) " + PartyNameBefore + " должен быть заполнен либо город, либо населенный пункт.";
  
  //Проверка даты изменения
  elif( date(GetFieldByName("ДатаИзмБанк")) == date(0,0,0) )
    err = "Не указана дата изменения реквизитов банка";
  end;
  
  if( strlen(err) )
    return false;
  else
    return true;
  end;
  
end;

private macro IsChangedClientData( req ) : bool
  
  if( (req.ClientINN == req.ClientINNOld) and (req.ClientOGRN == req.ClientOGRNOld) and (req.ClientName == req.ClientNameOld) )  
    return false;
  end;

  return true;
end;

// Функция, содержащая все проверки сообщений,
// выполняются только проверки, которые есть во входящем массиве, получаем массив из макросе релиза
macro MNS_CommonCheck( Req, ArrayValidations : TArray, kind )
  var PartyID = 0, PartyName = "", ИНН : string = "", КПП : string = "", Error, ParentPartyID = 0, Code : string = "", Name : string = "", 
      ClientID = 0, KPP_Client : string = "", BIC_Bank : string = "", OGRN : string = "", OGRN_Client : string = "", INN_KPP_Client : string = "", INN_Client : string = "";
  var Нотариус, Адвокат, УпрТоварищ, ИП = false, OpenDate = date(0,0,0), Department, КодНОБ = "";
  var FullErrorCheck : TArray = TArray();
  FILE adress(adress) key 0;
  FILE dp_dep( dp_dep ) key 0;
  FILE party( party ) key 0;
  FILE partyClient ( party ) key 0;
  record Acc(account);
  record OldAcc(account);
  record SfCntr(sfcontr);
  
  // Подготовительные действия перед проверками
  if( kind == ACCMSG_KIND_CLOSE )
    if(Req.Origin == REQ_ACC_ORIGIN_PS)
      ПолучитьСчет(Req.Code_Currency, Req.Account, Acc );
    else
      ПолучитьСчет_БО(Req.Code_Currency, Req.Account, Req, OBJTYPE_REQCLOSA, Acc);
    end;

    OpenDate = Acc.Open_Date;
  elif( kind == ACCMSG_KIND_CHNG )
    if(Req.Origin == REQ_ACC_ORIGIN_PS)
      ПолучитьСчет(Req.NewAccountFIID, Req.NewAccount, Acc );
      ПолучитьСчет(Req.OldAccountFIID, Req.OldAccount, OldAcc );
    else
      ПолучитьСчет_БО(Req.NewAccountFIID, Req.NewAccount, Req, OBJTYPE_REQCHANGE, Acc);
      ПолучитьСчет_БО(Req.OldAccountFIID, Req.OldAccount, Req, OBJTYPE_REQCHANGE, OldAcc);
    end;
  end;
  
  if( kind == ACCMSG_KIND_OPEN )
    ClientID = Req.ClientID;
    Department = Req.AccountDepartment;
  elif( kind == ACCMSG_KIND_CLOSE )
    ClientID = Acc.Client;
    Department = Acc.Department;
    OpenDate = Acc.Open_Date;
  elif( kind == ACCMSG_KIND_CHNG )
    ClientID = Acc.Client;
    Department = OldAcc.Department;
  end;
  
  
  
  //--------------------------ПРОВЕРКИ--------------------------
  
  // Наименование банка 
  // Эта проверка выбивается из общей логики работы функции - если GetEQ( dp_dep ) == false, то прерываем работу, т.к. у нас нет Department,
  // PartyID и PartyName, а значит многие последующие проверки не выполнятся, так зачем пытаться их выполнять? 
  if( find( ArrayValidations, GenMesCheckBankName ) >= 0 )
    dp_dep.Code = Department;
    if( GetEQ( dp_dep ) )
      party.PartyID = dp_dep.PartyID;
      if( GetEQ( party ) )
        PartyID = party.PartyID;
        PartyName = party.Name;
        if( party.ShortName == "" )
          FullErrorCheck(FullErrorCheck.size) = "У субъекта, связанного с филиалом " + dp_dep.Name + " не задано наименование" ;
        end;
      else
        RsbThrow( string( "Не найден субъект, связанный с филиалом ", dp_dep.Name ) );
      end;
    else
      RsbThrow( string( "Не найден филиал с кодом ", Department ) );
    end;
  
    if( dp_dep.ParentCode != 0 )
      dp_dep.Code = dp_dep.ParentCode;
      if( GetEQ( dp_dep ) )
        party.PartyID = dp_dep.PartyID;
        ParentPartyID = party.PartyID;
        if( GetEQ( party ) )
          if( party.ShortName == "" )
            FullErrorCheck(FullErrorCheck.size) = "У субъекта, связанного с филиалом " + dp_dep.Name + " не задано наименование";
          end;
        else
          FullErrorCheck(FullErrorCheck.size) = "Не найден субъект, связанный с филиалом " + dp_dep.Name;
        end;
      else
        FullErrorCheck(FullErrorCheck.size) = "Не найден филиал с кодом " + dp_dep.Code;
      end;
    end;
  end;
  
  // ИНН банка 
  if( find( ArrayValidations, GenMesCheckBankINN ) >= 0 )
    ИНН = RemoveKPP( ПолучитьКодСубъекта( PartyID, PTCK_INN, Error, 0) );
    if( Error or (ИНН == "") )
      FullErrorCheck(FullErrorCheck.size) = "Для " + PartyName + " не задан ИНН";
    end;
  end;
  
  
  // Количество символов в ИНН банка 
  if( find( ArrayValidations, GenMesCheckBankSizeINN ) >= 0 )
    if( strlen(ИНН) != 10 )
      FullErrorCheck(FullErrorCheck.size) = "Для " + PartyName + " задано неверное значение ИНН";
    end;
  end;
  
  
  // КПП банка   
  if( find( ArrayValidations, GenMesCheckBankKPP ) >= 0 )
    КПП = RemoveINN( GetPartyINN( PartyID, 1) );
    if( КПП == "" )
      FullErrorCheck(FullErrorCheck.size) = "Для  " + partyname + " не задан КПП";
    end;
  end;
  
  
  // Количество символов в КПП банка 
  if( find( ArrayValidations, GenMesCheckBankSizeKPP ) >= 0 )
    if( strlen( КПП ) != 9)
      FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " задано неверное значение КПП";
    end;
  end;
  
  
  // Символы в КПП банка 
  if( find( ArrayValidations, GenMesCheckBankSymbolKPP ) >= 0 )
    if( AllSymbolsAreEqual( КПП ) )
      FullErrorCheck(FullErrorCheck.size) = "КПП банка (филиала) " + PartyName + " не должен состоять из одинаковых цифр";
    end;
  end;
  
  
  // Задана налоговая инспекция банка + У налоговой инспекции банка есть код НО
  if( (find( ArrayValidations, GenMesCheckBankExistIFNS ) >= 0) or
      (find( ArrayValidations, GenMesCheckIFNShasCode ) >= 0) 
    )
    
    var TaxInstName : string = "";
    var НалоговаяИнспекция : integer = 0;
    // Задана налоговая инспекция банка
    НалоговаяИнспекция = GetPartyTaxInstitution( PartyID, @TaxInstName );
    if( НалоговаяИнспекция > 0 )
      КодНОБ = GetTaxInstitutionCodeCheck( НалоговаяИнспекция, TaxInstName, @FullErrorCheck);
      if( КодНОБ == "" )
        FullErrorCheck(FullErrorCheck.size) = "Не определен код налогового органа " + PartyName;
      end;
    else
      FullErrorCheck(FullErrorCheck.size) = "Не задан налоговый орган по месту учета банка";
    end;
  end;
  
  
  // Регистрационный номер банка 
  if( find( ArrayValidations, GenMesCheckBankRegNumb ) >= 0 )
    Code = ПолучитьКодСубъекта( PartyID, PTCK_BANKREGNUM, Error);
    if( Error or ( Code == "" ) )
      FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " не задан регистрационный номер кредитной организации";
    end;
  end;
  
  
  // Регистрационный номер филиала 
  if( find( ArrayValidations, GenMesCheckDepRegNumb ) >= 0 )
    if( ParentPartyID != 0 )
      if( not ( ( Index( Code,"/") != 0 ) and (strlen(SubStr(Code, Index( Code,"/")+1)) != Department ) ) )
        FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " не задан порядковый номер филиала";
      end;
    end;
  end;
  
  
  // БИК банка 
  if( find( ArrayValidations, GenMesCheckBankBIC ) >= 0 )
    BIC_Bank = ПолучитьКодСубъекта( PartyID, PTCK_BIC, Error, 1, 1 );
    if( Error or ( BIC_Bank == "" ) )
      FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " не задан БИК";
    end;
  end;
  
  
  // ОГРН банка 
  if( find( ArrayValidations, GenMesCheckBankOGRN ) >= 0 )
    OGRN = ПолучитьКодСубъекта( PartyID, PTCK_OGRN, Error);
    if( Error or ( OGRN == "" ) )
      FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " не задан ОГРН";
    end;
  end;
  
  
  // Количество символов в ОГРН банка 
  if( find( ArrayValidations, GenMesCheckBankSizeOGRN ) >= 0 )
    if( strlen( OGRN ) != 13 )
      FullErrorCheck(FullErrorCheck.size) = "Для " + partyname + " задано неверное значение ОГРН";
    end;
  end;
  
  
  // Наименование клиента 
  if( find( ArrayValidations, GenMesCheckClientName ) >= 0 )
    if( ( Req.ClientName == "" ) and ( Req.clientType == PTLEGF_INST ) )
      FullErrorCheck(FullErrorCheck.size) = "Для клиента " + ClientID + " не задано наименование";
    end;
  end;
  
  
  // служебные действия вне проверок, для проверок 
  partyClient.PartyID = ClientID;
  if( not GetEQ( partyClient ) )
    FullErrorCheck(FullErrorCheck.size) = "Не найден субъект экономики " + req.ClientName;
  end;
  
  ИП = IsIndividualEmployer( partyClient.PartyID );
  
  var sClientID : string = UniID( partyClient, OBJTYPE_PARTY );
  CheckObjAttrPresence( Нотариус, OBJTYPE_PARTY, sClientID, 
                        PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Нотариус ); 
  CheckObjAttrPresence( Адвокат,  OBJTYPE_PARTY, sClientID, 
                        PARTY_ATTR_GROUP_PTTYPE, null, "", ТипСубъекта_Адвокат );
  CheckObjAttrPresence( УпрТоварищ, OBJTYPE_PARTY, sClientID, PARTY_ATTR_GROUP_PTTYPE, PARTY_ATTR_PTTYPE_MANAGPARTNER);
  
  
  // ФИО клиента 
  if( find( ArrayValidations, GenMesCheckClientFIO ) >= 0 )
    if( ( partyClient.legalform == PTLEGF_PERSN ) and (not HaveFIO( req )) )
      FullErrorCheck(FullErrorCheck.size) = "Для клиента " + ClientID + " не заданы фамилия и\\или имя";    
    end;
  end;
  
  
  // ИНН клиента 
  if( find( ArrayValidations, GenMesCheckClientINN ) >= 0 )
    INN_KPP_Client = req.ClientINN;
    INN_Client = RemoveKPP( INN_KPP_Client );
    
    if( (INN_Client == "") and (( partyClient.LegalForm == PTLEGF_INST ) or ( ( partyClient.LegalForm == PTLEGF_PERSN ) and ИП ) ))
      if( ( kind == ACCMSG_KIND_CLOSE ) and 
          ( 
            (( OpenDate < date(22,9,1994)) and (partyClient.LegalForm == PTLEGF_INST) and (partyClient.NotResident != "X")) or
            (( OpenDate < date(22,9,1996)) and ((partyClient.LegalForm == PTLEGF_INST) and (partyClient.NotResident == "X") or ИП or Нотариус)) or
            (( OpenDate < date(1,1,2004)) and  (Адвокат))
          )
        ) 
        // проверку считать пройденной
      else
        FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " не задан ИНН";
      end;
    end;
  end;
  
  
  // Количество символов в ИНН клиента 
  if( find( ArrayValidations, GenMesCheckClientSizeINN ) >= 0 )
    if(((partyClient.LegalForm == PTLEGF_INST) and (partyClient.NotResident != "X") and (strlen(INN_Client) != 10 )) or 
       ((partyClient.LegalForm == PTLEGF_INST) and (partyClient.NotResident == "X") and (strlen(INN_Client) != 5 ) and (strlen(INN_Client) != 10)) or 
       ((partyClient.LegalForm != PTLEGF_INST) and (strlen(INN_Client) != 12)))
      FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " задано неверное значение ИНН";
    end;
  end;
  

  // КПП клиента 
  if( find( ArrayValidations, GenMesCheckClientKPP ) >= 0 )
    KPP_Client = RemoveINN( INN_KPP_Client );
    if( (KPP_Client == "") and ( req.ClientType == PTLEGF_INST ) )
      FullErrorCheck(FullErrorCheck.size) = "Для  " + req.ClientName + " не задан КПП";
    end;
  end;

  
  // Количество символов в КПП клиента 
  if( find( ArrayValidations, GenMesCheckClientSizeKPP ) >= 0 )
    if( ( KPP_Client != "" ) and (strlen( KPP_Client ) != 9 ) )
      FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " задано неверное значение КПП";
    end;
  end;


  // Символы в КПП клиента не совпадают 
  if( find( ArrayValidations, GenMesCheckClientSymbolKPP ) >= 0 )
    if( ( KPP_Client != "" ) and AllSymbolsAreEqual( KPP_Client ) )
      FullErrorCheck(FullErrorCheck.size) = "КПП " + req.ClientName + " не должен состоять из одинаковых цифр";
    end;
  end;
  

  // ОГРН клиента 
  if( find( ArrayValidations, GenMesCheckClientOGRN ) >= 0 )
    if( (partyClient.NotResident != "X") and ( not Адвокат ) and ( not Нотариус ) and ( not УпрТоварищ ) )
      OGRN_Client = ПолучитьКодСубъекта( partyClient.PartyID , PTCK_OGRN, Error, 0, 1 );
      if( Error or ( not OGRN_Client ) )
        FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " не задан ОГРН";
      end;
    end;
  end;

  
  // Длина ОГРН клиента 
  if( find( ArrayValidations, GenMesCheckClientSizeOGRN ) >= 0 )
    if( OGRN_Client != "" )
      if( ( strlen( INN_Client ) == 10 ) and ( strlen( OGRN_Client ) != 13 ) )
        FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " задано неверное значение ОГРН";
      elif( ( strlen( INN_Client ) == 12 ) and ( strlen( OGRN_Client ) != 15 ) )
        FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " задано неверное значение ОГРН";
      end;
    end;
  end;
  
  
  // Номер договора 
  if( find( ArrayValidations, GenMesCheckContractNumber ) >= 0 )
    var CodeCur = 0;
    if( kind == ACCMSG_KIND_OPEN )
      CodeCur = req.Code_Currency;
    else
      CodeCur = Acc.Code_Currency;
    end;

    var tmpAcc = "";
    if( kind == ACCMSG_KIND_OPEN )
      tmpAcc = req.Account;
    else
      tmpAcc = Acc.Account;
    end;

    if( ( ПолучитьДоговорОбслуживания( CodeCur, tmpAcc, SfCntr ) != 0 ) 
        and 
        ( (kind == ACCMSG_KIND_OPEN) and (Req.SfContrNum == "") ) 
      )
      FullErrorCheck(FullErrorCheck.size) = "Не задан номер договора счета";
    end;
  end;
  
  
  // Тип счета 
  if( find( ArrayValidations, GenMesCheckAccountType ) >= 0 )
    if( Req.Type_Account == "" )
      FullErrorCheck(FullErrorCheck.size) = "Не задан вид счета в соответствии с Инструкцией Банка России 28-И";
    end;
  end;
  
  
  // Сотрудник, подписывающий сообщение 
  if( find( ArrayValidations, GenMesCheckEmployeeSigningMSG ) >= 0 )
    Error = GetRegValForOPENAC("ИМНС_ФИО", V_STRING, Name);
    if( Error or ( Name == "" ) )
      FullErrorCheck(FullErrorCheck.size) = "Не заданы ФИО сотрудника, подписывающего сообщения в ФНС";
    end;
  end;
  
  
  
  // Изменение реквизитов при корректировке 
  if( find( ArrayValidations, GenMesCheckChangePropertis ) >= 0 )
    var Bank : TBankInfoAccMsg = TBankInfoAccMsg();
    var ErrMsg : string = "";
    var MesSender : integer = GetDprtPartyID( GetAccDprt(Req, kind) );

    if( MesSender <= 0 )
      FullErrorCheck(FullErrorCheck.size) = "Не найден связанный субъект филиала счета заявления";
    end;
    
    if( not Bank.Fill(MesSender, @ErrMsg) )
      FullErrorCheck(FullErrorCheck.size) = ErrMsg;
    end;
    
    var CorrectMesObj : TCorrectMesFNS = TCorrectMesFNS();
    CorrectMesObj.КодНОБ = КодНОБ;
    CorrectMesObj.ИННКО = Bank.ИННКО;
    CorrectMesObj.РегНом = Bank.РегНом;
    CorrectMesObj.НомФ = Bank.НомФ;
    
    if( not CorrectMesObj.Check1() )
      var MsgCheckOneChange = " По сравнению с корректируемым сообщением изменились значения реквизитов ИННКО/КодНОБ/РегНом/НомФ, их корректировка не допускается." +
                              " В случае изменения данных реквизитов следует отменить сообщение и отправить новое первоначальное. Продолжить?";
      if( msgboxEx( MsgCheckOneChange, MB_YES + MB_NO, IND_NO ) == IND_YES )
        FullErrorCheck(FullErrorCheck.size) = "Корректировка реквизитов сообщения ИННКО/КодНОБ/РегНом/НомФ не допускается";
      end;
    end;
    
  end;
  
  
  
  var AdressExist = НайтиЮридическийАдресСубъекта(PartyID, adress);
  
  if( not AdressExist )
    FullErrorCheck(FullErrorCheck.size) = "Для банка (филиала) " + PartyName + " не задан юридический адрес";
  end;
  
  
  // В адресе банка (филиала) задан индекс 
  if( ( find( ArrayValidations, GenMesCheckBankAddressExistIndex ) >= 0) and AdressExist )
    if( trim( adress.PostIndex ) == "" )
      FullErrorCheck(FullErrorCheck.size) = "В адресе банка (филиала) " + PartyName + " не указан почтовый индекс";
    end;
  end;
  
  
  // Количество символов в индексе из адреса банка (филиала) 
  if( (find( ArrayValidations, GenMesCheckBankAddressSizeIndex ) >= 0) and AdressExist )
    if( strlen( adress.PostIndex ) != 6 )
      FullErrorCheck(FullErrorCheck.size) = "Почтовый индекс, указанный в адресе банка (филиала) " +
                                            PartyName + ", должен содержать 6 символов";
    end;
  end;
  
  
  // В адресе банка (филиала) задан код региона 
  if( (find( ArrayValidations, GenMesCheckBankAdressRegionCode ) >= 0) and AdressExist )
    if( ( adress.Country == "RUS" ) and trim( adress.RegionNum ) == "" )
      FullErrorCheck(FullErrorCheck.size) = "В адресе банка (филиала) " + PartyName + " не указан код региона";
    end;
  end;
  
  
  // Количество символов в коде региона из адреса банка (филиала) 
  if( (find( ArrayValidations, GenMesCheckBankAdressSizeRegionCode ) >= 0) and AdressExist )
    if( ( adress.Country == "RUS" ) and ( strlen( adress.RegionNum ) != 2 ) )
      FullErrorCheck(FullErrorCheck.size) = "Код региона, указанный в адресе банка (филиала) " + PartyName + ", должен содержать 2 символа";
    end;
  end;
  
  
  // Код региона из адреса банка есть в справочнике
  if( (find( ArrayValidations, GenMesCheckBankRegionCodeExist ) >= 0) and AdressExist )
    if( ( adress.Country == "RUS" ) and
        ( not existsSQLselect( "select 1 from dptregion_dbt where t_CodeRegion = :CodeReg ", makeArray( SQLParam("CodeReg", adress.RegionNum) ) ) ) )
      FullErrorCheck(FullErrorCheck.size) = "Код региона, указанный в адресе банка (филиала) " +
                                            PartyName + ", отсутствует в справочнике регионов";
    end;
  end;
  
  
  // В адресе банка (филиала) задан город или населенный пункт 
  if( (find( ArrayValidations, GenMesCheckBankAdressExistLocality ) >= 0) and AdressExist )
    var IsRegionCapital : bool = ( (adress.RegionNum == "77") or 
                                   (adress.RegionNum == "78") or
                                   (adress.RegionNum == "92") );
    
    if( not IsRegionCapital and not adress.District and not adress.Place )
      FullErrorCheck(FullErrorCheck.size) = "В адресе банка (филиала) " + PartyName + " должен быть заполнен либо город, либо населенный пункт";
    end;
  end;
  
  
  // Не совпадают реквизиты клиента до и после изменения 
  if( find( ArrayValidations, GenMesCheckNotMatchPropetis ) >= 0 )
    if( req.ClientChange == "X" )
      if( ПроверкаИдентичности( acc, oldacc ) and ( not IsChangedClientData( req ) ) )
        msgbox( "Совпадают актуальные реквизиты клиента и реквизиты до изменения. В сообщение не будет включен блок, содержащий реквизиты клиента до изменения." );
      end;
    end;
  end;
  
  
  // Код документа удостоверяющего личность (ДУЛ) 
  if( find( ArrayValidations, GenMesCheckDULCode ) >= 0 )
    if( req.ClientCardType == "" )
      FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " не задан код документа, удостоверяющего личность";
    end;
  end;
  
  
  // Количество символов в коде ДУЛ 
  if( find( ArrayValidations, GenMesCheckDULSizeCode ) >= 0 )
    if( strlen( req.ClientCardType ) != 2)
      FullErrorCheck(FullErrorCheck.size) = "Для клиента " + req.ClientName + " задан неверный код документа, удостоверяющего личность";
    end;
  end;
  
  
  // Серия и номер ДУЛ 
  if( find( ArrayValidations, GenMesCheckDULSeriesAndNumber ) >= 0 )
    if( req.ClientCardNum == "" )
      FullErrorCheck(FullErrorCheck.size) = "Для " + req.ClientName + " не заданы серия и/или номер документа, удостоверяющего личность";
    end;
  end;
  
  
  // Дата выдачи ДУЛ 
  if( find( ArrayValidations, GenMesCheckDULDate ) >= 0 )
    if( ( Req.ClientCardType != "14" ) and ((Req.ClientCardDate == nullval) or (Req.ClientCardDate == ZeroDate) ) )
      FullErrorCheck(FullErrorCheck.size) = "Для " + Req.ClientName + " не задана дата выдачи документа, удостоверяющего личность";
    end;
  end;
  
  
  // Дата и место рождения клиента заданы 
  if( find( ArrayValidations, GenMesCheckClientExistDatePlacBirth ) >= 0 )
    if( (Req.ClientBirthDate == nullval) or ( Req.ClientBirthDate == ZeroDate ) or ( Req.ClientBirthPlace == "" ) )
      FullErrorCheck(FullErrorCheck.size) = " Не заданы дата и/или место рождения клиента. Для сообщения об открытии счета эти реквизиты должны быть обязательно заполнены ";
    end;
  end;
  
  if( FullErrorCheck.size > 0 )
    
    var err_msg = join( FullErrorCheck, ". |" );
    err_msg = substr( @err_msg, 1, 499 );
    MemoryError(1, err_msg);

    return false;
  end;
  
    
  return true;
end;