/*
  $Name:         wlexport.mac
  $Module:       Межбанковские расчеты
  $Description:  Макрос экспорта
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                   Экспорт сообщений (общая часть)                        */
/*                                                                          */
/*  Имя файла: wlexport.mac                                                 */
/*  Создан:  18.04.01                                         AAV           */
/****************************************************************************/

import "wldoc.mac", rsd, oralib, likepy;

FILE РелизЭкспорт(wlmesrls) key 0;
FILE ФормаЭкспорт(wlmesfrm) key 0;

/* Формирование ошибки при экспорте */
macro ErrExport( str )
  РелизЭкспорт.RlsFormID = wlmes.RlsFormID;
  if( GetEQ( РелизЭкспорт ) )
    std.msg( string( "Сообщение по форме ", РелизЭкспорт.Name,": ", wlmes.TRN, "|",  str) );
  else
    if( wlmes.TRN != "" )
      std.msg( string( "Сообщение ", wlmes.TRN, "|",  str) );
    else
      std.msg( str );
    end;
  end;

  AddErrExpRepInfo(wlmes.MesID, str );
end;

/* Определить наименования формы */
macro DefineFormExport( RlsFormID )
  var rs:object;
  var select:string;
  var params:TArray;

  if( not IsSQL )
    РелизЭкспорт.RlsFormID = RlsFormID;
    if(not GetEQ(РелизЭкспорт))
      ErrExport("Ошибка поиска релиза формы N "+string( RlsFormID ) );
      return false;
    end;

    ФормаЭкспорт.FormID = РелизЭкспорт.FormID;
    if(not GetEQ(ФормаЭкспорт))
      ErrExport("Ошибка поиска формы N " + string(РелизЭкспорт.FormID) );
      return false;
    end;
  else
    select = "select wlmesfrm.t_Name, wlmesrls.t_Name, wlmesfrm.t_Description "+
            "from dwlmesfrm_dbt wlmesfrm, dwlmesrls_dbt wlmesrls where wlmesrls.t_RlsFormID =:RlsFormID " +
                           " AND wlmesfrm.t_FormID = wlmesrls.t_FormID";
    params = makeArray( SQLParam("RlsFormID", RlsFormID));
    rs = execSQLselect( select, params, FALSE );
    if( rs.MoveNext() == false )
      ErrExport("Ошибка поиска формы сообщения, RlsFormID = "+string( RlsFormID ) );
      return false;
    else
      ФормаЭкспорт.Name        = rs.value(0);
      РелизЭкспорт.Name        = rs.value(1);
      ФормаЭкспорт.Description = rs.value(2);
    end;
  end;

  return true;
end;

/* Получить активный код субъекта заданного вида */ 
macro ПолучитьОткрытыйКодСубъекта( PartyID, CodeKind, error )
  var rs:object;
  var select:string;
  var params:TArray;
  
  if( not IsSQL )
    ClearRecord(f_wlobjcode);
    f_wlobjcode.ObjectID = PartyID;
    f_wlobjcode.ObjectType = OBJTYPE_PARTY;
    f_wlobjcode.CodeKind = CodeKind;
    
    if( GetEQ(f_wlobjcode) AND (f_wlobjcode.State==0) )
      error = 0;
      SetParm( 2, error );
      return f_wlobjcode.Code;
    end;
  else
    select = "select objcode.t_Code from dobjcode_dbt objcode where objcode.t_ObjectID =:PartyID "+ 
                             " AND objcode.t_ObjectType =:OBJTYPE_PARTY "+
                             " AND objcode.t_CodeKind = :CodeKind" +
                             " AND objcode.t_State = 0";
    params = makeArray( SQLParam("PartyID", PartyID),
                        SQLParam("OBJTYPE_PARTY", OBJTYPE_PARTY),
                        SQLParam("CodeKind", CodeKind));
    rs = execSQLselect( select, params, FALSE );
    if( rs.MoveNext() == true )
      error = 0;
      SetParm( 2, error );
      return rs.value(0);
    end;
  end;

  error = 1;
  SetParm( 2, error );
  return "";  
end;
