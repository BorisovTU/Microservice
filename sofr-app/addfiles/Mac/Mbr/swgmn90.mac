/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MTn90                                       */
/*                                                                          */
/*  Имя файла: swgmn90.mac                                                  */
/*  Создан:  22.09.00                                         Бабин А.П.    */
/****************************************************************************/

import "swgenmes.mac";

record Party(party);

macro GenMes( addrMes, addrPm )
  var field_value, BNC_Name, field72, INN, account, corschem, Tag, ОстатокПоля70="";
  var RsPaym;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog(2,"Генерация сообщения по МТn90");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 - ссылка */
  ЗаписатьПолеЛог( RelatedReferenceField, "NONREF" );

  /* 25 - счет платежа */
  if (RsPaym.PayerIsSender == "X")
     Corschem = RsPaym.InCorschem;
  else
     Corschem = RsPaym.OutCorschem;
  end;
  account = GetCorAcc( RsPaym.PayerFIID, Corschem, CORS_ACC_ACCOUNT );
  ЗаписатьПолеЛог( AccountIdentificationField, account );

  /* 32 - дата валютирования, код валюты, сумма */     
  field_value = GetSWIFTDate( RsPaym.OutTransferDate  ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );  
  if( not УстановитьКонтекстБлока( "32a" ) )
     RunError("|Ошибка при установке контекста блока " + "32a");
  end;
  if ( RsPaym.PayerAccount==account )
     ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_D, field_value );       
  else     
     ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_C, field_value );       
  end;  
  if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 71B  - расходы */  
  field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MTn90_STANDART, ОстатокПоля70 );
  ЗаписатьПолеЛог( DetailsOfChargesField_B, field_value );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MTn90_STANDART, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MTn90_STANDART, ОстатокПоля70);
  ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );
  
  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionField(RsPaym, Tag );
  ЗаписатьПолеВБлокКонтекст0( OrderingInstitutionField+Tag, "52a", field_value );
  
  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;