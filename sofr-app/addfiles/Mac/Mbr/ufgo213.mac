/*
$Name:           ufgo213.mac
$Module:         Межбанковские расчеты
$Description:    Генерация ответа на запрос ED213
*/                                                                            

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация ответа на запрос ED213
//
// Программист  : Чукина Т.А.
//
// Создан       : 23.10.2014
//
//-----------------------------------------------------------------------------

import MesInter, oralib, likepy, CTInter, PSInter, "wluftool.mac", WldInter, 
       "pm_tools.mac";

private macro GetAcptCode(DemAcceptChoice : integer) : integer
  var AcptCode : integer = 0;

  if(DemAcceptChoice == DEMACCEPTCHOICE_ACCEPT)
    AcptCode = 8;
  elif(DemAcceptChoice == DEMACCEPTCHOICE_PARTACCEPT)
    AcptCode = 4;
  elif(DemAcceptChoice == DEMACCEPTCHOICE_REJECT)
    AcptCode = 3;
  else
    RunError("Ошибка программирования: неверный параметр DemAcceptChoice");
  end;

  return AcptCode;
end;

private macro GetSumPT(xml : object) : money
  return money( ReadAttribute(xml, "Sum", "ED103") )/100;
end;

// Запрашивается вид акцепта и параметры отказа
class UserChoiceAccept(p_SumPT : money)
  var SumPT : money = p_SumPT,
      DenialAmount : money = $0, 
      DenialGround : string = "";

  var ChoiceKind : integer = AskDemAccept($0/*SumPT*/, DenialAmount, DenialGround);
end;

// Формирование ответа
private macro CreateAnswer(wlreq, wlmes, xml : object, AcceptChoice : UserChoiceAccept) : bool
  var AcptCode : integer = GetAcptCode(AcceptChoice.ChoiceKind);
  var SumPT : string = ReadAttribute(xml, "Sum", "ED103");
  var AcptSum : string = MoneyKopStr(AcceptChoice.SumPT - AcceptChoice.DenialAmount);

  // Создать учетный объект "исходящий ответ"
  RECORD answer (wlreq);
  ClearRecord(answer);
  answer.RelatedRef = wlreq.Trn;
  answer.Direct = ""; // WLD_MES_OUT
  answer.Kind = MESKIND_ANSWER;
  answer.OriginatorCode = wlreq.RecipientCode;
  answer.OriginatorCodeKind = wlreq.RecipientCodeKind;
  answer.OriginatorID = wlreq.RecipientID;
  answer.OriginatorName = wlreq.RecipientName;
  answer.RecipientCode = wlreq.OriginatorCode;
  answer.RecipientCodeKind = wlreq.OriginatorCodeKind;
  answer.RecipientID = wlreq.OriginatorID;
  answer.RecipientName = wlreq.OriginatorName;
  answer.InitDateMes = ToDate( ReadAttribute(xml, "EDDate") );
  answer.InitFormIDMes = GetFormIDByRlsID(wlmes.RlsFormID);
  answer.SubKind = WLD_SUBKIND_ANS_ACCEPT;
  answer.Corschem = -1;
  answer.FIID = -1;
  answer.Queries = "AcptCode:" + AcptCode + "\n" +
                   "SumPT:" + SumPT + "\n";
  if(AcptCode == 4)
    answer.Queries = answer.Queries + "AcptSum:" + AcptSum + "\n";
  end;

  var TpID : integer = getTpIDofTpSchem(wlmes.TpSchemID);

  // Открыть панель ответа на редактирование с созданными параметрами 
  if( WlExecuteReqEditPanel( answer, AcceptChoice.DenialGround, "", "", TpID, false, 
                             wlreq.ReqID, OBJTYPE_REQ, "X" ) != 0 )
    return FALSE;
  end;

  return TRUE;
end;

private macro RollbackAnswer(AnswerID : integer, AnswerState : integer) : bool
  var Msg : string = "";

  if(AnswerState < WLD_STATUS_REQ_DEFECT)
    Msg = "На основании запроса акцепта сформировано извещение ED214, которое еще не отправлено адресату. Удалить ED214?";
    if( RsbGetTrue( false, false, Msg ) == false ) 
      return FALSE;
    end;

    // удалить связанный ответ
    if( WldDelReq(AnswerID) != 0 )
      Msg = GetErrMsg();
      RunError( Msg, Msg );
    end;
  else
    Msg = "На основании запроса акцепта сформировано извещение ED214, которое уже отправлено адресату. Откат обработки запроса запрещен";
    RunError( Msg, Msg );
  end;

  return TRUE;
end;

macro GenObj( addrMes, mode )
  RECORD wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  if(not mode)
    mode = WLD_GENOBJ_ACTION_EXECUTE; // по умолчанию - обработка
  end;

  FILE wlreq(wlreq);

  if  (mode == WLD_GENOBJ_ACTION_EXECUTE) // Обработка
    ClearRecord(wlreq);
    var ReqID : integer = GetObjIDByMesLink(wlmes.MesID, OBJTYPE_REQ, "X");

    if(ReqID and (wlreq.ReqID = ReqID) and GetEQ(wlreq))
      var xml : object = ReadXMLField(wlmes);

      var SumPT : money = GetSumPT(xml);
      var AcceptChoice : UserChoiceAccept = UserChoiceAccept(SumPT);
      if(AcceptChoice.ChoiceKind == DEMACCEPTCHOICE_CANCEL)
        return FALSE;
      end;

      if( not CreateAnswer(wlreq, wlmes, xml, AcceptChoice) )
        return FALSE;
      end;
    else
      RunError("Не найден входящий запрос по сообщению с референсом " + wlmes.Trn);
    end;

  elif(mode == WLD_GENOBJ_ACTION_ROLLBACK) // Откат обработки
    // Найти ответ, связанный с запросом
    var AnswerState : integer = 0;
    var AnswerID : integer = GetAnswerIdLinkedWithRequest(wlmes.MesID, @AnswerState);

    if(AnswerID > 0)
      return RollbackAnswer(AnswerID, AnswerState);
    end;
  else
    RunError("Неверный код режима обработки");
  end;

  return TRUE;
  
  OnError(er) // обработка ошибок времени выполнения
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
