/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT910                                    */
/*                                                                          */
/*  Имя файла: swsgm910.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

macro GenMes(addrMes, addrConf)
  var field_value, error;

  SetBuff(wlmes,  addrMes);
  SetBuff(wlconf, addrConf);

  SB_CheckFIID( wlconf.FIID );

  PrintLog(2, "Генерация сообщения по МТ910");

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  if (wlconf.RelatedRef != "")
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, wlconf.RelatedRef);
  end;

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог(TransactionReferenceNumberField, wlmes.TRN);

  /* 21 Related Reference */
  if (wlconf.RelatedRef != "")
    ЗаписатьПолеЛог(RelatedReferenceField, wlconf.RelatedRef);
  else
    ЗаписатьПолеЛог(RelatedReferenceField, СсылкаОтсутствует);
  end;

  /* 25 - счет подтверждения */
  ЗаписатьПолеЛог(AccountIdentificationField, wlconf.Account);

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = ДатаПоУмолчанию +
                ПолучитьКодВалютыЛог( wlconf.FIID ) +
                GetSWIFTAmount( wlconf.Sum );
  ЗаписатьПолеЛог(ValueDateCurrencyCodeAmountField_A, field_value);

  /* 52a - банк приказодателя */
  if ((wlconf.CodeValue!="") OR (wlconf.BankName!=""))
    ClearRecord(Route);
    if((wlconf.BankID == {OurBank}))
        error = ПолучитьНашКодСУчетомТС(PTCK_SWIFT, true, Route.CodeValue, Route.PartyID);
        if(error)
    Route.PartyID  = wlconf.BankID;
    Route.CodeKind = wlconf.CodeKind;
    Route.CodeValue = wlconf.CodeValue;
        else
            Route.CodeKind = PTCK_SWIFT;
        end;
    else
       Route.PartyID  = wlconf.BankID;
       Route.CodeKind = wlconf.CodeKind;
       Route.CodeValue = wlconf.CodeValue;
    end;
    Route.Name = ПолучитьИмяБанка(Route.PartyID);
    ЗаписатьПолеМаршрутаКонтекст0Лог_SB(OrderingInstitutionField, "52a", Route);
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  /* 56a - банк посредник */
  if( (wlconf.CorrCodeValue!="") OR (wlconf.CorrBankName!="") )
    ClearRecord(Route);
    Route.PartyID  = wlconf.CorrBankID;
    Route.CodeKind = wlconf.CorrCodeKind;
    Route.CodeName = wlconf.CorrCodeName;
    Route.CodeValue = wlconf.CorrCodeValue;
    Route.Name = wlconf.CorrBankName;
    ЗаписатьПолеМаршрутаКонтекст0Лог_SB( IntermediaryField, "56a", Route );
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end;
  end;

  StrChangeRusXSet(wlconf.Description, field_value);
  StrMultyToFormat(field_value, field_value, 65, 6);
  ЗаписатьПолеЛог(SenderToReceiverInformationField, field_value);

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;
