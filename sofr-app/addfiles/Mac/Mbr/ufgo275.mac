 /*
 $Name: ufgo275.mac
 $Module: Межбанковские расчеты
 $Description: Генерация ответа на запрос ED275
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответа на запрос ED275                                         */
/*                                                                          */
/*  Имя файла: ufgo275.mac                                                  */
/*  Создан:    16.10.13                                      Чукина Т.А.    */
/****************************************************************************/

import MesInter, oralib, likepy, CTInter, "wluftool.mac", WldInter,
       "pmcarfun.mac", "pmlib.mac", BankInter, pm_const, "pm_opr.mac";

private class TPaymData
  var PaymentID             : integer = 0,
      DocKind               : integer = 0,
      ShifrOper             : string  = "",
      BaseAmount            : money   = $0,
      PayerAccount          : string  = "",
      DbBankCode            : string  = "",
      PayerCorrAccNostro    : string  = "",
      ReceiverAccount       : string  = "",
      CrBankCode            : string  = "",
      ReceiverCorrAccNostro : string  = "",
      PaymStatus            : integer = 0,

      ChildPaymentID        : integer = 0,
      ChildPaymentDocKind   : integer = 0,
      ChildPaymStatus       : integer = 0;

  macro GetRequiredPaymentID : integer
    if(ChildPaymentID > 0)
      return ChildPaymentID;
    end;

    return PaymentID;
  end;

  macro GetRequiredPaymentDocKind : integer
    if(ChildPaymentID > 0)
      return ChildPaymentDocKind;
    end;

    return DocKind;
  end;

  macro GetRequiredPaymentStatus : integer
    if(ChildPaymentID > 0)
      return ChildPaymStatus;
    end;

    return PaymStatus;
  end;
end;

private const 
  ОтзывПлатежа : integer = 1,
  ОтзывРаспоряжения : integer = 2,
  ФормированиеУведомления : integer = 3;

// Поиск отзываемого платежа
private macro FindRevokedPayment(Trn : string) : TPaymData
  var select = 
    "Select pm.t_PaymentID, pm.t_DocKind, rm.t_ShifrOper, pm.t_BaseAmount, " +
    "       pm.t_PayerAccount, db.t_BankCode as DbBankCode, rm.t_PayerCorrAccNostro," +
    "       pm.t_ReceiverAccount, cr.t_BankCode as CrBankCode, rm.t_ReceiverCorrAccNostro, " +
    "       pm.t_PaymStatus " +
    "  from dwlmes_dbt mes, dwltpshem_dbt shem, dwlmesrls_dbt rls, dwlmesfrm_dbt frm," +
    "       dwlmeslnk_dbt lnk, dpmpaym_dbt pm, dpmrmprop_dbt rm, " +
    "       dpmprop_dbt db, dpmprop_dbt cr " +
    " where mes.t_Trn        = :Trn " +
    "   and mes.t_Direct     = 'X' /* WLD_MES_IN */" +
    "   and mes.t_TpSchemID  = shem.t_TpShemID " +
    "   and shem.t_TpID      = 9 /* TRANSP_UFBS */ " +
    "   and mes.t_RlsFormID  = rls.t_RlsFormID " +
    "   and rls.t_FormID     = frm.t_FormID " +
    "   and frm.t_Name in ('ED113', 'ED114') " +
    "   and lnk.t_MesID      = mes.t_MesID " +
    "   and lnk.t_Direct     = 'X' /* WLD_MES_IN */" +
    "   and lnk.t_ObjKind   in ( :OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ, :OBJTYPE_PMCLAIM )" +
    "   and lnk.t_ObjID      = pm.t_PaymentID " +
    "   and rm.t_PaymentID   = pm.t_PaymentID " +
    "   and db.t_PaymentID   = pm.t_PaymentID " +
    "   and db.t_DebetCredit = 0 /* PRT_Debet */ " +
    "   and cr.t_PaymentID   = pm.t_PaymentID " + 
    "   and cr.t_DebetCredit = 1 /* PRT_Credit */ ";

  var params : TArray = makeArray( SQLParam("Trn", Trn),
                                   SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD),
                                   SQLParam("OBJTYPE_PSINRQ", OBJTYPE_PSINRQ),
                                   SQLParam("OBJTYPE_PMCLAIM", OBJTYPE_PMCLAIM)
                                 );

  var rs : RsdRecordset = execSQLselect(select, params);

  if(rs and rs.moveNext())
    var PaymData : TPaymData = PaymData();

    PaymData.PaymentID = rs.value("t_PaymentID");
    PaymData.DocKind = rs.value("t_DocKind");
    PaymData.ShifrOper = rs.value("t_ShifrOper");
    PaymData.BaseAmount = rs.value("t_BaseAmount");
    PaymData.PayerAccount = rs.value("t_PayerAccount");
    PaymData.DbBankCode = rs.value("DbBankCode");
    PaymData.PayerCorrAccNostro = rs.value("t_PayerCorrAccNostro");
    PaymData.ReceiverAccount = rs.value("t_ReceiverAccount");
    PaymData.CrBankCode = rs.value("CrBankCode");
    PaymData.ReceiverCorrAccNostro = rs.value("t_ReceiverCorrAccNostro");
    PaymData.PaymStatus = rs.value("t_PaymStatus");

    return PaymData;
  end;

  // раз мы здесь оказались, значит, ничего не нашли
  RsbThrow("По референсу сообщения " + Trn + " не удалось найти отзываемый платеж, автоматический отзыв невозможен ");
end;

// Проверка реквизитов платежа
private macro CheckPaymAttrs(PaymData : TPaymData, xml : object, wlmes_in) : bool

  macro IsEqOpt(PaymAttr : string, AttrName : string) : bool
    var MesAttr : string = ReadOptinalAttribute(xml, AttrName);
    if(not PaymAttr or not MesAttr or (PaymAttr == MesAttr))
      return TRUE;
    end;

    return FALSE;
  end;

  if( ( PaymData.ShifrOper != ReadAttribute(xml, "TransKind") ) or
      ( PaymData.BaseAmount != money(ReadAttribute(xml, "Sum"))/100 ) or
      ( PaymData.PayerAccount != ReadOptinalAttribute(xml, "PayerPersonalAcc") ) or
      ( PaymData.DbBankCode != ReadAttribute(xml, "PayerBIC") ) or
      not IsEqOpt(PaymData.PayerCorrAccNostro, "PayerCorrespAcc") or
      ( PaymData.ReceiverAccount != ReadOptinalAttribute(xml, "PayeePersonalAcc") ) or
      ( PaymData.CrBankCode != ReadAttribute(xml, "PayeeBIC") ) or
      not IsEqOpt(PaymData.ReceiverCorrAccNostro, "PayeeCorrespAcc")
    )

    // Пока только индивидуальный режим
    //if(not IsOprMultiExec)
      // В индивидуальном режиме вывести запрос 
      // "Нет" - прервать выполнение процедуры. 
      return ( MsgBoxEx
               ( "Реквизиты платежа, соответствующего сообщению с референсом " +
                 wlmes_in.RelatedRef + ", не совпадают с реквизитами отзываемого " +
                 "распоряжения из сообщения с референсом " + wlmes_in.Trn +
                 ". Отозвать платеж?", MB_YES+MB_NO, IND_NO 
               ) == IND_YES 
             );
    //else
    //  // В пакетном режиме прервать выполнение процедуры с ошибкой 
    //  std.msg( "Реквизиты платежа, соответствующего сообщению с референсом " +
    //           wlmes_in.RelatedRef + ", не совпадают с реквизитами отзываемого " +
    //           "распоряжения из сообщения с референсом " + wlmes_in.Trn + "." );
    //  return FALSE;
    //end;
  end;

  return TRUE;
end;

// Проверка вида документа, у отзываемого платежа
private macro CheckPaymDocKind(PaymData : TPaymData) : integer
  var NextStep : integer = ОтзывРаспоряжения;

  if( InList(PaymData.DocKind, PS_PAYORDER, PS_INRQ) )
    NextStep = ОтзывПлатежа;
  else
    var query : string =
      "select pmpaym.t_PaymentID, pmpaym.t_DocKind, pmpaym.t_PaymStatus "
      "  from dpmlink_dbt pmlink, dpmpaym_dbt pmpaym "
      " where pmlink.t_InitialPayment = :InitialPaymentID "
      "   and pmlink.t_LinkKind = :PMLINK_KIND_ESIDPAYMENT "
      "   and pmpaym.t_PaymentID = pmlink.t_PurposePayment ";

    var params : TArray = makeArray
      ( SQLParam("InitialPaymentID", PaymData.PaymentID),
        SQLParam("PMLINK_KIND_ESIDPAYMENT", PMLINK_KIND_ESIDPAYMENT)
      );

    var rs = execSQLselect(query, params);
    if( rs and rs.moveNext() )
      // Если такой платеж найден, то для него, а не для родительского распоряжения, проверить возможность отзыва и далее запустить процедуру отзыва платежа. 
      PaymData.ChildPaymentID = rs.value("t_PaymentID");
      PaymData.ChildPaymentDocKind = rs.value("t_DocKind");
      PaymData.ChildPaymStatus = rs.value("t_PaymStatus");

      NextStep = ОтзывПлатежа;
    end;
  end;

  return NextStep;
end;

private macro DelReqPaymLink(ReqID : integer, PaymentID : integer)
    execSQL( "delete from dwlreqlnk_dbt              " +
             " where t_ReqID     = :ReqID            " + 
             "   and t_ObjID     = :PaymentID        " +
             "   and t_ObjKind  in ( :OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ, :OBJTYPE_PMCLAIM ) ",
             makeArray( SQLParam("ReqID",            ReqID),
                        SQLParam("PaymentID",        PaymentID),
                        SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD),
                        SQLParam("OBJTYPE_PSINRQ",   OBJTYPE_PSINRQ),
                        SQLParam("OBJTYPE_PMCLAIM",  OBJTYPE_PMCLAIM)
                      )
           );

end;

private macro IsLinkedPaym(ReqID : integer) : bool
  var s = "select 1 "
          "  from dwlreqlnk_dbt reqlnk, dpmpaym_dbt pm "
          " where reqlnk.t_ReqID     = :ReqID "
          "   and reqlnk.t_ObjKind  in ( :OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ ) "
          "   and reqlnk.t_ObjDirect = chr(0) "
          "   and pm.t_PaymentID     = reqlnk.t_ObjID "
          "   and pm.t_DocKind      in ( :PS_PAYORDER, :PS_INRQ ) ";

  var params = makeArray( SQLParam("ReqID", ReqID),
                          SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD),
                          SQLParam("OBJTYPE_PSINRQ", OBJTYPE_PSINRQ),
                          SQLParam("PS_PAYORDER", PS_PAYORDER),
                          SQLParam("PS_INRQ", PS_INRQ)
                        );

  var rs : RsdRecordset = execSQLselect(s, params);

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

private macro IsLinkedOrder(ReqID : integer, OrderID : integer) : bool
  var s = "select 1 "
          "  from dwlreqlnk_dbt reqlnk, dpmpaym_dbt pm "
          " where reqlnk.t_ReqID     = :ReqID "
          "   and reqlnk.t_ObjKind   = :OBJTYPE_PMCLAIM "
          "   and reqlnk.t_ObjID     = :OrderID ";

  var params = makeArray( SQLParam("ReqID", ReqID),
                          SQLParam("OBJTYPE_PMCLAIM", OBJTYPE_PMCLAIM),
                          SQLParam("OrderID", OrderID)
                        );

  var rs : RsdRecordset = execSQLselect(s, params);

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

private macro IsLinkedPaymOrOrder(ReqID : integer) : bool
  var s = "select 1 "
          "  from dwlreqlnk_dbt reqlnk, dpmpaym_dbt pm "
          " where reqlnk.t_ReqID     = :ReqID "
          "   and reqlnk.t_ObjKind  in ( :OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ, :OBJTYPE_PMCLAIM ) "
          "   and pm.t_PaymentID = reqlnk.t_ObjID ";

  var params = makeArray( SQLParam("ReqID", ReqID),
                          SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD),
                          SQLParam("OBJTYPE_PSINRQ", OBJTYPE_PSINRQ),
                          SQLParam("OBJTYPE_PMCLAIM", OBJTYPE_PMCLAIM)
                        );

  var rs : RsdRecordset = execSQLselect(s, params);

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

// Отзыв платежа
private macro RevokePayment(PaymData : TPaymData, wlmes, xml : object, wlreq, NextStep : @integer)
  var PaymentID : integer = PaymData.GetRequiredPaymentID(),
      DocKind : integer = PaymData.GetRequiredPaymentDocKind();

  var ObjKind : integer = IfThenElse(DocKind == PS_INRQ, OBJTYPE_PSINRQ, OBJTYPE_PSPAYORD);

  // Создать связку запроса с платежом
  if( not IsLinkedPaym(wlreq.ReqID) )
    if( WldCreateReqLink(wlreq.ReqID, PaymentID, ObjKind, "" /*WLD_MES_OUT*/) != 0 )
      RunError("Не удалось создать связку запроса с платежом");
    end;
  end;

  var Order = null;
  if(DocKind == PS_PAYORDER)
    Order = RsbPSPayOrder(PaymentID);
  elif(DocKind == PS_INRQ)
    Order = RsbRequestOrder(PaymentID);
  else
    RunError("Отзыв платежа: неподдерживаемый вид документа");
  end;

  var Payment : RsbPayment = Order.Payment;

    // Заполнить примечание "Причина отказа (возврата)" 
    var DenialGround = "Распоряжение отозвано банком получателя. Запрос об отзыве № " +
                       wlmes.trn + " от " + GetSessBankDate(wlmes);

  var RecDenial : TRecHandler = TRecHandler("pmdenial");
  RecDenial.Clear();

  RecDenial.rec.DocumentKind = OBJTYPE_PMCHNDOC_KIND_INSTR; // Распоряжение
  RecDenial.rec.Date = ToDate( ReadAttribute(xml, "EDDate") );
  RecDenial.rec.Number = ReadAttribute(xml, "EDNo");
  RecDenial.rec.Ground = DenialGround;
  RecDenial.rec.PaymentID = Payment.PaymentID;
  RecDenial.rec.Department = Payment.StartDepartment;

  if( (DocKind == PS_PAYORDER) and (Order.CurrentState == PSPO_ST_I1) )
    RecDenial.rec.Amount = Payment.BaseAmount;
    RecDenial.rec.IndexNum = PAYMENTS_INDEX_1;
  else
    RecDenial.rec.Amount = Payment.FutureBaseAmount;

    if(Payment.PaymStatus == PM_I2PLACED)
      RecDenial.rec.IndexNum = PAYMENTS_INDEX_2;
    else
      RecDenial.rec.IndexNum = PAYMENTS_INDEX_WP;
    end;
  end;

  var stat : integer = PM_RevokeOrder(Payment.PaymentID, Payment.DocKind, CALL_SOURCE_ED275_PROC, RecDenial);
  if(stat)
    RsbThrow(stat);
  end;

  if(PaymentID == PaymData.ChildPaymentID) // Если отзыв выполнялся по дочернему от распоряжения платежу
    NextStep = ОтзывРаспоряжения;
  else
    NextStep = ФормированиеУведомления;
  end;

  OnError(er) // обработка ошибок
    // Удалить связку запроса с платежом
    DelReqPaymLink(wlreq.ReqID, PaymentID);

    // прервать выполнение процедуры с ошибкой 
    RsbThrow( "Не удалось отозвать платеж, соответствующий сообщению с референсом " +
              wlmes.RelatedRef + ". Причина ошибки: " + RsbGetError(er) );
end;

private macro GetOrderObject(PaymentID : integer, DocKind : integer)
  var Obj = null;

  if(DocKind == DLDOC_OUT_PMCLAIM)
    Obj = RsbOutClaimOrder(PaymentID);
  elif(DocKind == DLDOC_IN_PMCLAIM)
    Obj = RsbInClaimOrder(PaymentID);
  elif(DocKind == DLDOC_OUT_PMCOL)
    Obj = RsbOutCollectOrder(PaymentID);
  elif(DocKind == DLDOC_IN_PMCOL)
    Obj = RsbInCollectOrder(PaymentID);
  end;

  return Obj;
end;

// Отзыв распоряжения
private macro RevokeOrder(PaymData : TPaymData, wlmes, wlreq, NextStep : @integer)

  // Создать связку запроса с распоряжением
  if( not IsLinkedOrder(wlreq.ReqID, PaymData.PaymentID) )
    if( WldCreateReqLink(wlreq.ReqID, PaymData.PaymentID, OBJTYPE_PMCLAIM, "X" /*WLD_MES_IN*/) != 0 )
      RunError("Не удалось создать связку запроса с распоряжением");
    end;
  end;

  var Order = GetOrderObject(PaymData.PaymentID, PaymData.DocKind);
  // присвоить категории платежа "Причина неисполнения платежа" значение 1 "Отозван инициатором"
  if( (Order.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_NON_EXE_PYMENT, 1, null, null, {curdate}) != 0)
      or
      (Order.Update() != 0)
    )
    RunError("Ошибка при установке значения категории платежа \"Причина неисполнения платежа\"");
  end;

  var stat : integer = 0, statDescription : string = "";

  // Проверить текущий статус отзываемого распоряжения
  if(Order.PaymStatus == PM_PREPARING)
    stat = PM_ExecuteOperation(PaymData.PaymentID, PaymData.DocKind);
  elif( not InList(Order.PaymStatus, PM_PREPARING, PM_FINISHED) and (Order.CloseDate == date(0, 0, 0)) )
    stat = PM_ExecuteOperation(PaymData.PaymentID, PaymData.DocKind);
  elif(Order.PaymStatus == PM_FINISHED)
    Order.PaymStatus = PM_REJECTED;
    if(Order.Update() != 0)
      RunError("Ошибка при изменении статуса распоряжения");
    end;
  end;

  if(stat)
    MemoryError(stat);
    statDescription = GetErrMsg();
  end;

  // операция по распоряжению завершена?
  if( PM_IsOperationComplete(PaymData.PaymentID, PaymData.DocKind) )
    NextStep = ФормированиеУведомления;
  else
    if(PaymData.ChildPaymentID > 0)
      msgbox("Отозвано только платежное требование/инкассовое поручение, сформированное на основании отзываемого распоряжения. Уведомление об отзыве будет сформировано автоматически. Распоряжение на оплату необходимо отозвать вручную, запустив по нему операцию или готовый к выполнению шаг");
      NextStep = ФормированиеУведомления;
    else
      var ErrMsg : string = "Не удалось отозвать платеж, соответствующий сообщению с референсом " + wlmes.RelatedRef + ". ";
      if(statDescription)
        ErrMsg = ErrMsg + "Причина ошибки: " + statDescription;
      end;

      RunError(ErrMsg, ErrMsg);
    end;
  end;
end;

private macro LinkAnswerWithReqPayments(AnswerID : integer, ReqID : integer)
  var query : string =
    "select t_ObjID, t_ObjKind, t_ObjDirect "
    "  from dwlreqlnk_dbt "
    " where t_ReqID = :ReqID "
    "   and t_ObjKind in (:OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ, :OBJTYPE_PMCLAIM) ";

  var params : TArray = makeArray
  ( SQLParam("ReqID", ReqID),
    SQLParam("OBJTYPE_PSPAYORD", OBJTYPE_PSPAYORD),
    SQLParam("OBJTYPE_PSINRQ", OBJTYPE_PSINRQ),
    SQLParam("OBJTYPE_PMCLAIM", OBJTYPE_PMCLAIM)
  );

  var rs = execSQLselect(query, params);
  while( rs and rs.moveNext() )
    if( WldCreateReqLink(AnswerID, rs.value("t_ObjID"), rs.value("t_ObjKind"), rs.value("t_ObjDirect")) != 0 )
      RunError("Не удалось создать связку ответа с платежом");
    end;
  end;
end;

// Формирование уведомления
private macro CreateNotification(wlreq, InfoCode : integer, xml : object, wlmes, AnswerID : @integer) : bool
  // Создать учетный объект "исходящий ответ"
  RECORD answer (wlreq);
  ClearRecord(answer);
  answer.RelatedRef = wlreq.RelatedRef;
  answer.Direct = ""; // WLD_MES_OUT
  answer.Kind = MESKIND_ANSWER;
  answer.OriginatorCode = wlreq.RecipientCode;
  answer.OriginatorCodeKind = wlreq.RecipientCodeKind;
  answer.OriginatorID = wlreq.RecipientID;
  answer.OriginatorName = wlreq.RecipientName;
  answer.RecipientCode = wlreq.OriginatorCode;
  answer.RecipientCodeKind = wlreq.OriginatorCodeKind;
  answer.RecipientID = wlreq.OriginatorID;
  answer.RecipientName = wlreq.OriginatorName;
  answer.InitDateMes = ToDate( ReadAttribute(xml, "EDDate") );
  answer.InitFormIDMes = GetFormIDByRlsID(wlmes.RlsFormID);
  answer.Corschem = -1;
  answer.FIID = -1;
  answer.Queries = "InfoCode:" + InfoCode + "\n" +
                   "InitialED:" + wlreq.Trn;
  var TpID : integer = getTpIDofTpSchem(wlmes.TpSchemID);
  answer.SubKind = WLD_SUBKIND_ANS_REC;
  
  // Пока только индивидуальный режим
  //if(not IsOprMultiExec)
    // Открыть панель ответа на редактирование с созданными параметрами 
    if( WlExecuteReqEditPanel(answer, "", "", "", TpID, false, wlreq.ReqID, OBJTYPE_REQ, "X") != 0 )
      // Если есть связка запроса с платежом вывести предупреждение
      if( IsLinkedPaymOrOrder(wlreq.ReqID) )
        msgbox( "Платеж, соответствующий сообщению с референсом " + wlreq.RelatedRef + 
                ", отозван. Автоматическая отмена отзыва выполнена не будет. " +
                "Сформируйте уведомление об отзыве вручную по клавише Alt-F9 " +
                "из списка принятых запросов или вручную откатите отзыв платежа" );
      end;

      return FALSE;
    end;
  //else
  //  // Вставляем безинтерфейсно
  //  if( CreateQuery(answer, "", "", 0, 0, false, 0, 0, TpID, wlreq.ReqID, OBJTYPE_REQ, "X") != 0 )
  //    return FALSE;
  //  end;
  //end;

  // Связь ответа с запросом создается функцией WlExecuteReqEditPanel

  // Связать ответ с платежами запроса
  LinkAnswerWithReqPayments(answer.ReqID, wlreq.ReqID);

  AnswerID = answer.ReqID;

  return TRUE;
end;

private macro GetReqSessBankDate(ReqID : integer) : date
  var rs = execSQLselect( "select sess.t_BankDate " +
                          "  from dwlsess_dbt sess, dwlmeslnk_dbt lnk, dwlmes_dbt mes " +
                          " where sess.t_SessionID = mes.t_SessionID " +
                          "   and mes.t_MesID = lnk.t_MesID " +
                          "   and lnk.t_ObjID = :ReqID " +
                          "   and lnk.t_ObjKind = :ObjKind " +
                          "   and lnk.t_Direct = chr(0) /* WLD_MES_OUT */ ",
                          makeArray( SQLParam("ReqID", ReqID),
                                     SQLParam("ObjKind", OBJTYPE_REQ) )
                        );

  if(rs and rs.moveNext())
    return date(rs.value(0));
  end;

  return date(0, 0, 0);
end;

// Поиск платежа, для которого откатываем отзыв
private macro FindUnRevokedPayments(ReqID : integer) : RsdRecordset
  var select = 
    "Select pm.t_PaymentID, pm.t_DocKind, pm.t_PaymStatus, lnk.t_ObjKind " +
    "  from dwlreqlnk_dbt lnk, dpmpaym_dbt pm, dobjatcor_dbt oac " +
    " where lnk.t_ReqID      = :ReqID " +
    "   and lnk.t_ObjKind   in ( :OBJTYPE_PSPAYORD, :OBJTYPE_PSINRQ, :OBJTYPE_PMCLAIM ) " +
    "   and lnk.t_ObjID      = pm.t_PaymentID " +
    "   and oac.t_ObjectType = :OBJTYPE_PAYMENT " +
    "   and oac.t_Object     = lpad(pm.t_PaymentID, 10, '0') " +
    "   and oac.t_GroupID    = :PM_GROUP_NON_EXE_PYMENT " + // категории "Причина неисполнения платежа" 
    "   and oac.t_AttrID     = 1 " +  // значение "Отозван инициатором"
    "   and oac.t_ValidToDate >= :curdate1 " +
    "   and oac.t_ValidFromDate <= :curdate2 ";

  var params : TArray = makeArray( SQLParam("ReqID", ReqID),
                                   SQLParam("OBJTYPE_PSPAYORD",  OBJTYPE_PSPAYORD),
                                   SQLParam("OBJTYPE_PSINRQ",    OBJTYPE_PSINRQ),
                                   SQLParam("OBJTYPE_PMCLAIM",   OBJTYPE_PMCLAIM),
                                   SQLParam("OBJTYPE_PAYMENT", OBJTYPE_PAYMENT),
                                   SQLParam("PM_GROUP_NON_EXE_PYMENT", OBJ_PAYMENT_GROUP_NON_EXE_PYMENT),
                                   SQLParam("curdate1", {curdate}),
                                   SQLParam("curdate2", {curdate})
                                 );

  var rs : RsdRecordset = execSQLselect(select, params);

  return rs;
end;

private macro GetLastOperStepID(PaymentID : integer, DocKind : integer, OperationID : @integer, StepID : @integer)
  OperationID = StepID = 0;

  var strSQL = "Select opr.t_ID_Operation, max(step.t_ID_Step) as StepID " +
               "  from doprstep_dbt step, doproper_dbt opr " +
               " where step.t_ID_Operation = opr.t_ID_Operation " +
               "   and opr.t_DocKind    = :DocKind " +
               "   and opr.t_DocumentID = LPad( :PaymentID, 34, '0' ) " +
               "   and step.t_IsExecute = :EXECSTEP "
               "group by opr.t_ID_Operation ";

  var params = makeArray( SQLParam("DocKind",   DocKind),
                          SQLParam("PaymentID", PaymentID),
                          SQLParam("EXECSTEP",  "X")
                        );
  var rs : RsdRecordset = execSQLselect(strSQL, params);

  if(rs and rs.moveNext())
    OperationID = rs.value("t_ID_Operation");
    StepID = rs.value("StepID");
  end;
end;

private macro GetInMesLinkReqID(MesID : integer) : integer
  return GetObjIDByMesLink(MesID, OBJTYPE_REQ, "X");
end;

private macro DelNotePaymentOrOrder(PaymentID : integer, DocKind : integer, ObjKind : integer)
  var Obj = null;

  if(ObjKind == OBJTYPE_PMCLAIM)
    Obj = GetOrderObject(PaymentID, DocKind);
  elif(ObjKind == OBJTYPE_PSPAYORD)
    Obj = RsbPSPayOrder(PaymentID);
  elif(ObjKind == OBJTYPE_PSINRQ)
    Obj = RsbRequestOrder(PaymentID);
  else
    RunError("Неподдерживаемый вид документа");
  end;

  if( Obj.Notes.DelNote(PM_NOTEKIND_DENIALGROUND) or Obj.Update() )
    RunError("Ошибка при удалении примечания \"Причина отказа (возврата)\"");
  end;

end;

private macro UnRevokePayment(wlreq, Payms : RsdRecordset)
  var PaymentID : integer = Payms.value("t_PaymentID"),
      DocKind : integer = Payms.value("t_DocKind"),
      PaymStatus : integer = Payms.value("t_PaymStatus"),
      ObjKind : integer = Payms.value("t_ObjKind");

  var NeedRollBackStep : bool = false;
  if( InList(ObjKind, OBJTYPE_PSPAYORD, OBJTYPE_PSINRQ) and (PaymStatus == PM_FINISHED)
      or
      (ObjKind == OBJTYPE_PMCLAIM)
    )
    NeedRollBackStep = true;
  end;

  if(NeedRollBackStep)
    var OperationID : integer = 0, StepID : integer = 0;
    GetLastOperStepID(PaymentID, DocKind, @OperationID, @StepID);
    var stat = PM_RollbackStep(PaymentID, OperationID, StepID);
    if(stat)
      var msg="Не удалось откатить отзыв " + IfThenElse(ObjKind == OBJTYPE_PMCLAIM, "распоряжения", "платежа") + ", " +
              "соответствующего сообщению с референсом " + wlreq.RelatedRef + ". " +
              "Уведомление об отзыве платежа удалено. Откатите операцию по платежу вручную или " + 
              "восстановите уведомление об отзыве" ;

      if(not IsOprMultiExec)
        MemoryError(stat);
        DisplayError();
        msgbox(msg);
      else
        //std.msg(msg);
        CreateWarning(CWM_NOPREFIX, WLD_WARN_MACRO, msg);
      end;
    end;
  end;

  // Очистить категорию платежа "Причина неисполнения платежа", примечание "Причина отказа (возврата)"
  RECORD pmpaym(pmpaym);
  pmpaym.PaymentID = PaymentID;
  DisconnectObjAttr(OBJTYPE_PAYMENT, makeObjectID(OBJTYPE_PAYMENT, null, pmpaym), OBJ_PAYMENT_GROUP_NON_EXE_PYMENT);
  // при откате шага примечание грохается само
  if(not NeedRollBackStep)
    DelNotePaymentOrOrder(PaymentID, DocKind, ObjKind);
  end;

  DelReqPaymLink( wlreq.ReqID, PaymentID );

end;

macro GenObj( addrMes, mode )
  RECORD wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  if(not mode)
    mode = WLD_GENOBJ_ACTION_EXECUTE; // по умолчанию - обработка
  end;

  FILE wlreq(wlreq);
  ClearRecord(wlreq);
  wlreq.ReqID = GetInMesLinkReqID(wlmes.MesID);
  if(not wlreq.ReqID or not GetEQ(wlreq))
    RunError("Не найден входящий запрос по сообщению с референсом " + wlmes.Trn);
  end;

  FILE answer(wlreq);
  var AnswerID : integer = 0;

  if  (mode == WLD_GENOBJ_ACTION_EXECUTE) // Обработка
    // Поиск отзываемого платежа
    var PaymData : TPaymData = FindRevokedPayment(wlmes.RelatedRef);

    var xml : object = ReadXMLField(wlmes);

    // Проверка реквизитов платежа
    if( not CheckPaymAttrs(PaymData, xml, wlmes) )
      return FALSE;
    end;

    // Проверка вида документа, у отзываемого платежа
    var NextStep : integer = CheckPaymDocKind(PaymData);

    if(NextStep == ОтзывПлатежа)
      // Проверка возможности отзыва платежа
      if(not IsPaymentRevocable(PaymData.GetRequiredPaymentID(), PaymData.GetRequiredPaymentStatus()))
        NextStep = ФормированиеУведомления;
      end;
    end;

    var InfoCode : integer = 2; // "Безотзывное ЭС"
    if(NextStep == ОтзывПлатежа)
      // Отзыв платежа. Вся обработка ошибок - внутри RevokePayment
      RevokePayment(PaymData, wlmes, xml, wlreq, @NextStep);

      // Если RevokePayment не выбросила RunError, значит, отзыв выполнен успешно
      InfoCode = 1; // "ЭС отозвано"
    end;

    if(NextStep == ОтзывРаспоряжения)
      // Отзыв распоряжения
      RevokeOrder(PaymData, wlmes, wlreq, @NextStep);

      // Если RevokeOrder не выбросила RunError, значит, отзыв выполнен успешно
      InfoCode = 1; // "ЭС отозвано"
    end;

    // Формирование уведомления
    if( (NextStep == ФормированиеУведомления) and 
        CreateNotification(wlreq, InfoCode, xml, wlmes, @AnswerID)
      )
      /*// Связать ответ с платежом (чтобы иметь возможность проверять на предобработке для отложенных)
      if(InfoCode == 1)
        if( WldCreateReqLink(AnswerID, PaymData.PaymentID, OBJTYPE_PSPAYORD, "" /*WLD_MES_OUT*/) != 0 )
          RunError("Не удалось создать связку ответа с платежом");
        end;
      end;*/
    else
      return FALSE;
    end;
    
  elif(mode == WLD_GENOBJ_ACTION_ROLLBACK) // Откат обработки
    // Найти связанные объекты, подлежащие откату или удалению

    // Найти уведомление о результатах отзыва (ответ ED276 wlreq_new), 
    // связанное с запросом
    ClearRecord(answer);
    answer.ReqID = GetAnswerIdLinkedWithRequest(wlmes.MesID);

    if(answer.ReqID and GetEQ(answer))
      if(answer.State >= WLD_STATUS_REQ_SEND)
        std.msg( "Уведомление о результатах отзыва № " + answer.Trn + " от " +
                 GetReqSessBankDate(answer.ReqID) + " уже отправлено инициатору платежа, откатить отзыв невозможно");
        return FALSE;
      else
        // удалить уведомление
        if( WldDelReq(answer.ReqID) != 0 )
          // Если удалить уведомление не удалось, прервать выполнение процедуры
          std.msg( GetErrMsg() );
          return FALSE;
        end;
      end;
    end;

    // Найти платежи, связанные с запросом
    var Payms : RsdRecordset = FindUnRevokedPayments(wlreq.ReqID);
    while(Payms and Payms.moveNext())
      UnRevokePayment(wlreq, Payms);
    end;
  else
    RunError("Неверный код режима обработки");
  end;

  return TRUE;
  
  OnError(er) // обработка ошибок времени выполнения
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

// Формирование уведомления о результатах отзыва ED276
// Вызывается из  МБР / Входящие / Запросы / Принятые / Alt-F9, 
// только в индивидуальном интерфейсном режиме
macro CreateNotifED276(addrReq) : bool

  macro GetXMLField(wlmes, FormName) : object
    var field_value;  
    if( not ПолучитьПоле( wlmes.MesID, XMLField, field_value ) )
      msgbox("В сообщении по форме: " + FormName + " референс: " + wlmes.TRN + " " +
             "|не заполнено поле '" + XMLField + "'" );
      return NULL;
    end;

    var xml:object = ActiveX( "MSXML.DOMDocument" );
    if ( not xml.loadXML(field_value) )
      msgbox( "Неверный формат сообщения по форме " + FormName );
      return NULL;
    end;

    return xml;
  end;

  // Параметры для передачи в CreateNotification
  RECORD wlreq(wlreq);
  var    InfoCode : integer; // Признак "ЭС отозвано" (1) / "Безотзывное ЭС" (2)
  var    xml : object;
  FILE   wlmes(wlmes);

  SetBuff(wlreq, addrReq);

  var rs = execSQLselect("select t_MesID from dwlmeslnk_dbt where t_ObjID = :ReqID " +
                         "and t_ObjKind = :ObjType and t_Direct = 'X' /*WLD_MES_IN*/ ",
                          makeArray( SQLParam("ReqID", wlreq.ReqID),
                                     SQLParam("ObjType", OBJTYPE_REQ) )
                        );
  if(rs and rs.moveNext() and (wlmes.MesID = rs.value(0)) and GetEQ(wlmes))
    xml = GetXMLField(wlmes, "ED275");
    if(xml == null)
      return FALSE;
    end;
  else
    msgbox("Не найдено входящее сообщение запроса " + wlreq.Trn);
    return FALSE;
  end;

  InfoCode = ConfWin( makeArray( "Выберите код уведомления, направляемый в ответ на запрос об отзыве" ),
                      makeArray( " ЭС отозвано ", " Безотзывное ЭС " ), 27 );
  if(InfoCode == 27)
    msgbox("Выполнение процедуры прервано пользователем");
    return FALSE;
  end;
  InfoCode = InfoCode + 1;

  // Формирование уведомления
  return CreateNotification(wlreq, InfoCode, xml, wlmes);

end;
