/*
  $Name:         swgm102.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения по SWIFT MT102
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT102                                       */
/*                                                                          */
/*  Имя файла: swgm102.mac                                                  */
/*  Создан:  01.10.01                                        Алешин А.В.    */
/****************************************************************************/

import "swgenmes.mac";

record Party(party);

FILE PaymMulty( pmpaym ) key 0;
FILE RlsMulty ( wlmesrls ) key 0 ;
FILE FormMulty( wlmesfrm ) key 0 ;

var RefIDMulty;

/* Ключевой фильтр на сводные платежи */
macro OneStepMultyPaym( FirstDocMulty, rs:RsdRecordset )
  if( FirstDocMulty )
     return rs.moveFirst;
  else
     return rs.moveNext;
  end;
end;

/* Если в форме сообщнения указан референс формирования номеров транзакций, */
/* то используем его, иначе просто счетчик */
macro СформироватьРеференсТранзакции( count )
  return  String( count );
end;

/* Формирование повторяющихся последовательностей */
macro ЗаписатьПлатежиСводного( publicCharges, publicRate, TotalSum, TotalRecCharges, Is52Present )
  var IsFirst = TRUE, count = 0, field_value, Sum = moneyL(0), ComFIID, 
      ComAmount, IsTransit, BaseAmount, Tag, error, DKFlag, tempv;
  var RsPaym;
  RECORD rcv( pmroute );

  TotalRecCharges = $0;

  var select;
  var params:TArray = TArray();
  var rs:RsdRecordset;

  select = "select link.t_PurposePayment from dpmlink_dbt link where link.t_InitialPayment = :PaymentID and link.t_LinkKind = :LinkKind";
  params = makeArray(SQLParam( "PaymentID", wlpmpaym.PaymentID ),
                     SQLParam( "LinkKind",  PMLINK_KIND_MULTYPM ));

  rs = execSQLselect( select, params, TRUE );

  while( rs.moveNext )
    IsFirst = FALSE;
    count = count + 1;

    RsPaym = RsbPayment( rs.value(0) );
    
    /* 21 - номер транзакции */
    field_value = СформироватьРеференсТранзакции( count );
    ЗаписатьПолеЛог( RelatedReferenceField, String(field_value) );

    /* 32B - код валюты, сумма */
    field_value = FillCurrencyOriginalOrderedAmountField( RsPaym );
    ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_B, field_value );

    /* 50a - приказодатель */
    /* Устанавливаем контекст блока '50a' */
    if( not УстановитьКонтекстБлока( "50a" ) )
      RunError("|Ошибка при установке контекста блока '50a'");
    end;
    field_value = FillOrderingCustomerField103( RsPaym, Tag );
    if (field_value != "")
      ЗаписатьПолеЛог( OrderingCustomerField + Tag, field_value );    
    end;
    /* Восстанавливаем контекст блока 'Sequence B' */
    if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока 'Sequence B' ");
    end;

    /* 52a - банк приказодателя */
    if( ПолучитьУзелОтправителяПлатежа(RsPaym.PaymentID, rcv) AND 
        ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_IN, Route) AND 
        (rcv.RouteID!=Route.RouteID) )

      if (IsBankUSB(RsPaym.ReceiverBankID))
        ЗаписатьПолеМаршрутаЛог( OrderingInstitutionField, "52a", Route, SWIFT_Tag_B );
      elif ( Route.CodeKind==PTCK_SWIFT )
        ЗаписатьПолеМаршрутаЛог( OrderingInstitutionField, "52a", Route, SWIFT_Tag_A );
      else
        ЗаписатьПолеМаршрутаЛог( OrderingInstitutionField, "52a", Route, SWIFT_Tag_B );
      end;

      /* Восстанавливаем контекст блока 'Sequence B' */
      if( not УстановитьКонтекстБлока( ".." ) )
        RunError("|Ошибка при установке контекста блока 'Sequence B' ");
      end;
    end;

    /* 57a - банк бенефициара */
    if( ПолучитьУзелПолучателяПлатежа(PaymMulty.PaymentID,rcv) AND
        ПолучитьСамыйДальнийУзелПлатежа(PaymMulty.PaymentID, RTDIR_OUT, Route) AND
        (rcv.RouteID!=Route.RouteID) )    
      if ( Route.CodeKind==PTCK_SWIFT )
         ЗаписатьПолеМаршрутаЛог( AccountWithInstitutionField, "57a", Route, SWIFT_Tag_A );
      else
         ЗаписатьПолеМаршрутаЛог( AccountWithInstitutionField, "57a", Route, SWIFT_Tag_C );
      end;
      /* Восстанавливаем контекст блока 'Sequence B' */
      if( not УстановитьКонтекстБлока( ".." ) )
        RunError("|Ошибка при установке контекста блока 'Sequence B' ");
      end;
    end;
    
    /* 59a - бенефициар */
      if( not УстановитьКонтекстБлока( "59a" ) )
        RunError("|Ошибка при установке контекста блока '59a'");
      end;
    field_value = FillBeneficiaryCustomerField_A( RsPaym );/*59A*/
    if (field_value == "")
      field_value = FillBeneficiaryCustomerField( RsPaym );/*59*/
      if ( field_value=="" )
        std.out( 2, "PaymentID: " + RsPaym.PaymentID );
        RunError( "|Не найден получатель" );
      end;
      ЗаписатьПолеЛог( BeneficiaryCustomerField, field_value );
    else
      ЗаписатьПолеЛог( BeneficiaryCustomerField_A, field_value );
    end;
    /* Восстанавливаем контекст блока 'Sequence B' */
    if( not УстановитьКонтекстБлока( ".." ) )
      RunError("|Ошибка при установке контекста блока 'Sequence B' ");
    end;

    /* 70  - информация для бенефициара */
    field_value = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT102_STANDART );    
    ЗаписатьПолеЛог( DetailsOfPaymentField, field_value );

    var CommissCode = GetCommisCode (RsPaym.ComissCharges);

    IsTransit = (((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) AND (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)) OR PM_PaymentIsChildTransit( RsPaym.PaymentID ));  
    
    var InMesID = 0;
    if(IsTransit)
      InMesID = getInMesIDByPayment(RsPaym);
    end;

    /* 33B - Инструктируемая сумма, валюта */
    WriteField33B( RsPaym, InMesID, CommissCode, false, null );

    /* 71A - расходы */
    field_value = CommissCode;
         ЗаписатьПолеЛог( DetailsOfChargesField_A, field_value ) ;

    /* 71F */
    if( field_value == CHARGES_BEN )
      field_value = ПолучитьКодВалютыЛог( RsPaym.ReceiverFIID ) + GetSWIFTAmount(0.0);
      if( not УстановитьКонтекстБлока( "71F" ) )
        RunError("|Ошибка при установке контекста блока '71F'");
      end;
      ЗаписатьПолеЛог( SendersChargesField, field_value);
      /* Восстанавливаем контекст блока 'Sequence B' */
      if( not УстановитьКонтекстБлока( ".." ) )
        RunError("|Ошибка при установке контекста блока 'Sequence B' ");
      end;
    end;
  end;
end;  


macro GenMes( addrMes, addrPm )
  var field_value, field72, error, SumOfAmount, TotalRecCharges, publicCharges, 
      publicRate, publicBankOperationCode, field52Present = false, Tag;
  RECORD rcv( pmroute );
  RECORD ocb(pmroute);
  var RsPaym;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  RsPaym = RsbPayment( wlpmpaym.PaymentID );
  PrintLog(2,"Генерация сообщения по МТ102");
  RefIDMulty = -1;

  /* Ищем валютные свойства */
  if( FindPayment( wlpmpaym.PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
    std.out( 2, "PaymentID: " + wlpmpaym.PaymentID );
    RunError( "|Не найден платеж" );
  end;

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );
 
  /* 23 */
  ЗаписатьПолеЛог( BankOperationCodeField, BANKOPERCODE_CREDIT ) ;  

  /* Устанавливаем контекст блока */
  if( not УстановитьКонтекстБлока( "Sequence B" ) )
    RunError("|Ошибка при установке контекста блока 'Sequence B'");
  end;

  ЗаписатьПлатежиСводного( publicCharges, publicRate, SumOfAmount, TotalRecCharges, field52Present );

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );

  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );

/* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_A;
  field_value = FillSenderCorrespondentField(RsPaym, Tag );
  if(Tag != SWIFT_Tag_B)
    ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

