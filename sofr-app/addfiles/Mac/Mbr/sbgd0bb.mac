/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация подтверждения SBRF3 сигнатура 0BB, 0B6                         */
/*                                                                          */
/*  Создан:     15.09.00                                                    */
/*  Программист Бурцев А.А.                                                 */
/****************************************************************************/

import "sbgendoc.mac";

/* Генерация подтверждений по сообщениям СБРФ */
macro GenDocExec( IsPIB )
  var field_name, field_value, Sender, Receiver, first_letter, SBRFStr = "",TransferDate;

  /* Формируем строку сообщения СБРФ */
  if( not SB_MakeSBRF3Message( IsPIB, SBRFStr ) )
    return false;
  end;

  /* Проверяем ЭЦП входящего сообщения */
  if( not SBCheckKey( SBRFStr ) )
    return false;
  end;

  ClearRecord(wlconf);

  wlconf.DKFlag = -1;

  /* Последовательно считываем и обрабатываем поля */
  while( SB_СчитатьПолеИзСообщения( field_name, field_value, SBRFStr) == 0 )

    if(   field_name == "DT" )   /* Тип документа */
      if   ( (substr(field_value,2,2) == "02") or (substr(field_value,2,2) == "03"))
         wlconf.DKFlag = WL_CREDIT;
      elif ( substr(field_value,2,2) == "01" ) wlconf.DKFlag = WL_DEBET;
      else std.msg("Неверное значение поля DT"); return false;
      end;
      if   ( substr(field_value,1,2) == "60" )
         wlconf.Cancel = "X";
      end;
    elif( field_name == "RC" )   /* Участник отправитель */
       Sender   = field_value;
    elif( field_name == "PA" )   /* Участник получатель  */
       Receiver = field_value;
    elif( field_name == "WR" )   /* Причина отказа */
       wlconf.Description = field_value;
    elif( field_name == "LD" )   
       TransferDate = field_value;
    end;
  end;

  /* Если поля DT не нашли - DKFlag неправильный */
  if ( wlconf.DKFlag < 0 )
     std.msg("Нет обязательного поля DT");
     return false;
  end;  

  wlconf.Number     = wlmes.TRN;  
  wlconf.RelatedRef = wlmes.TRN;

  wlconf.DateValue = wlpmpaym.ValueDate;

/*  if( wlconf.DateValue==date(0,0,0) )
    wlconf.TransferDate = {curdate};
  else
    wlconf.TransferDate = wlconf.DateValue;
  end;*/
  wlconf.TransferDate = date(substr(TransferDate,1,2)+"."+substr(TransferDate,3,2)+"."+substr(TransferDate,5,2)); //#118037

  wlconf.Sum = wlpmpaym.PayAmount;

  if   ( ( wlpmpropcred.PaymentID == wlpmpaym.PaymentID ) and ( wlconf.DKFlag == WL_DEBET  ) )
     wlconf.FIID = wlpmpropcred.PayFIID;
     wlConf.CorSchem = wlpmpropcred.CorSchem;
  elif ( ( wlpmpropdeb.PaymentID  == wlpmpaym.PaymentID ) and ( wlconf.DKFlag == WL_CREDIT ) )
     wlconf.FIID = wlpmpropdeb.PayFIID;
     wlConf.CorSchem = wlpmpropdeb.CorSchem;
  else

     if ( wlconf.DKFlag == WL_DEBET )
        if( wlconf.Cancel == "X" ) 
          std.msg("Неверный тип документа в ответном сообщении. Ожидалось DT:602");
        else
          std.msg("Неверный тип документа в ответном сообщении. Ожидалось DT:102");
        end;        
     else
        if( wlconf.Cancel == "X" ) 
          std.msg("Неверный тип документа в ответном сообщении. Ожидалось DT:601");
        else
          std.msg("Неверный тип документа в ответном сообщении. Ожидалось DT:101");
        end;
     end;
     
     return false;
  end;

  if( not ВставитьПодтверждение( wlconf ) )
    std.msg("Ошибка при вставке ответного подтверждения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполонения */
    ExeptionMessage(er);
    return FALSE;
end;

/* Релиз 0BB, 0B6 */
macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по форме 0BB, 0B6");

  return GenDocExec( false );
end;

/* Релиз 0BBJ, 0B6J */
macro GenDocPIB( addrMes )

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по форме 0BBJ, 0B6J");

  return GenDocExec( true );
end;
