/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Константы и вспомогательные функции для генерации сообщений SBRF3       */
/*                                                                          */
/*  Имя файла: sbgenmes.mac                                                 */
/*  Создан:  18.07.00                                        Алешин А.В.    */
/****************************************************************************/

import "wlgenmes.mac", "wlsbrftl.mac";

/* При генерации сообщений СБРФ переводим все в верхний регистр */
/* format == true - символ "\n"преобразуется в " " */
macro SB_ЗаписатьПолеЛог( field_name, field_value, format )
  /* Поля сообщения преобразуем в верхний регистр, кроме тэга "OS", который регистрозависим */
  if( (field_name != SB_Tag_OperContent) AND (field_name != SB_Tag_PIB) )
    field_value = StrUpr(String(field_value));
  end;
  return ЗаписатьПолеЛог( field_name, String(field_value), format );
end;

macro SB_ЗаписатьПолеЛогВБлок( Блок, Поле, Str, format, IsFirst )
  /* Поля сообщения преобразуем в верхний регистр, кроме тэга "OS", который регистрозависим */
  if( (Поле != SB_Tag_OperContent) AND (Поле != SB_Tag_PIB) )
    Str = StrUpr(String(Str));
    if(format)
       Str = StrSubst(Str,"\n"," ");
    end;
  end;
  
  return ЗаписатьПолеЛогВБлок( Блок, Поле, Str, IsFirst );
end;

/* Формирование одного поля обобщенного релиза */
macro SB_ЗаполнитьПоле( field_name, field_value, Str )
  var SBRFStr:string  = Str;
  var to_upr_str:bool = true;

  /* Поля сообщения преобразуем в верхний регистр, кроме тэга "OS", который регистрозависим */
  if( field_name == SB_Tag_OperContent )
    to_upr_str = false;
  end;

  if( field_value != "" )
    SBRFStr = SBRFStr + MakeFieldSBRF3( field_name, field_value, false, to_upr_str );
  end;

  SetParm( 2, SBRFStr );
end;


macro SB_ЗаписатьТранзитноеСообщение( MesID:integer )
   var params:TArray = TArray();
   var res,ss;

   ss = string("select wltpfld.t_Name, wlmesval.t_Value", 
               " from dwltpfld_dbt wltpfld, dwlmesval_dbt wlmesval ",
               " where wlmesval.t_MesID = :MesID",
               " AND wlmesval.t_TpFieldID = wltpfld.t_TpFieldID",
               " order by wlmesval.t_Index"
              ) ;

   params[params.size] = SQLParam( "MesID",   MesID);

   res = execSQLSelect (ss,params,true);

   while( res.MoveNext() )
      if (res.value(0) != "KS")/*Ключ не писать*/  
         ЗаписатьПолеЛог( res.value(0), res.value(1));
      end;
   end;

  return true;
end;
