/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по BUV                                               */
/*                                                                          */
/*  Имя файла: mnsgmbuv.mac                                                 */
/*  Создан:  21.02.11                                    Чукина Т.А.        */
/****************************************************************************/
                                                              
import "wlgenmes.mac", "wlmnstls.mac", WldInter, "bnk_common.mac";

// Сформировать поле: <код филиала>:<БИК филиала>;<Наименование филиала>
private macro constructDepCodeBICNameFldValue( ndlnkRec : TRecHandler ) : string
  var result : string = "";
  var Error = 0, ind = 0;

  var DepCode = "", DepBIC = "", DepName = "";
  var strSQL = "select t_PartyID " +
               "  from ddp_dep_dbt " +
               " where t_Code = " + ndlnkRec.rec.Department;
  var rs : RsdRecordset = execSQLselect(strSQL);
  if(rs and rs.moveNext())
        
    // код филиала
    DepCode = ПолучитьКодСубъекта (rs.value("t_PartyID"), PTCK_BANKREGNUM, Error, 0);
    // взять символы после слеша
    ind = index(DepCode, "/");
    if( (ind > 0) and (ind < strlen(DepCode)) )
      DepCode = subStr(DepCode, ind + 1);
    else
      DepCode = "";
    end;
    // дополнить слева нулями до 4-х символов
    while( strlen(DepCode) < 4 )
      DepCode = "0" + DepCode;
    end;

    // БИК филиала
    DepBIC = GetPartyCode(rs.value("t_PartyID"), PTCK_BIC);

    // Наименование филиала
    DepName = ndlnkRec.rec.Name;

    result = DepCode + ":" + DepBIC + ";" + DepName;    
  end;

  return result;  
end;

macro GenMes( addrMes, addrNotFns, addrOldMes )
  record OldMes(wlmes);
  ClearRecord(OldMes);
  record wlnotfns(wlnotfns);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlnotfns, addrNotFns );
  SetBuff( OldMes,   addrOldMes );

  PrintLog(2,"Генерация сообщения по BUV"); 

  var tmpstr = "";

  УстановитьКонтекстБлокаЛог("СтрокаИзвещения");
  
  // Поле 1
  tmpstr = GetPartyCodeOnDate(wlnotfns.OriginatorID, PTCK_BIC, date());
  if(tmpstr)
    tmpstr = SubStr(tmpstr, 3);
  end;
  tmpstr = tmpstr + "@@@";
  ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );
  // Поле 2
  tmpstr = wlnotfns.OriginatorName + "@@@";
  ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );
  // Поле 3
  tmpstr = wlnotfns.Action + "@@@";
  ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );

  if( (wlnotfns.Action=="11") or (wlnotfns.Action=="31") )
    
    // Поля n
    var ndlnkObj : RsbWlNDLnk = RsbWlNDLnk(wlnotfns.NoticeID);
    var ndlnkRec : TRecHandler = TRecHandler("wlndlnk.dbt");
    
    if( ndlnkObj.First(ndlnkRec) == 0 )
      // Записать поле: <код филиала>:<БИК филиала>;<Наименование филиала>
      tmpstr = constructDepCodeBICNameFldValue( ndlnkRec );
      if(tmpstr != "")
        ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );
        ЗаписатьПолеЛог( "_СтрокаИзвещения", "###" );
      end;

      while( ndlnkObj.Next(ndlnkRec) == 0 )
        // Записать поле: <код филиала>:<БИК филиала>;<Наименование филиала>
        tmpstr = constructDepCodeBICNameFldValue( ndlnkRec );
        if(tmpstr != "")
          ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );
          ЗаписатьПолеЛог( "_СтрокаИзвещения", "###" );
        end;
      end;
    end;

    // Поле n + 1
    ЗаписатьПолеЛог( "_СтрокаИзвещения", "@@@" );

  end;

  // Поле n + 2
  tmpstr = YYYY_MM_DD( wlnotfns.Date ) + "@@@";
  ЗаписатьПолеЛог( "_СтрокаИзвещения", tmpstr );

  УстановитьКонтекстБлокаЛог( ".." ); // Блок "СтрокаИзвещения"

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;

