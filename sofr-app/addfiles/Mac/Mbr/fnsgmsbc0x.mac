/*
  $Name:         fnsgmsbc0x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SBC0 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SBC0 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 21.05.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac", FIInter;

private const vers : integer = 510; // версия формата 5.10

private macro WriteAccInfoOpen(Счет : string, Валюта : integer, reqopena)
  StartBlockLogXML("Открыт");

  WriteFieldWithCheckAnnul( "КодСостСч", "1" );
  WriteFieldWithCheckAnnul( "НомерДог", reqopena.SfContrNum );
  WriteFieldWithCheckAnnul( "КодСостДог", "0" );
  ЗаписатьПоле_ДатаЗаклДог(reqopena.Date, reqopena.UnionContrID, Счет, Валюта, reqopena);

  FinishBlockLogXML("Открыт");
end;

// СвСчет
private macro WriteAccInfo(Счет : string, Валюта : integer, GenAcc, reqopena)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  WriteAccInfoAttrs(Счет, Валюта, GenAcc, reqopena, ACCMSG_KIND_OPEN);

  // Файл\Документ\СвСчет\Открыт
  WriteAccInfoOpen(Счет, Валюта, reqopena);

  FinishBlockLogXML("СвСчет");
end;

/* Проверки перед генерацией сообщения */
macro CheckObj( addrReq )
  record Req(reqopena);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckClientKPP,
                                              GenMesCheckClientSizeKPP,
                                              GenMesCheckClientSymbolKPP,
                                              GenMesCheckClientOGRN,
                                              GenMesCheckClientSizeOGRN,
                                              GenMesCheckContractNumber,
                                              GenMesCheckAccountType,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality
                                             );
  
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_OPEN );
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record reqopena(reqopena);
  SetBuff(reqopena, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SBC0");

  record GenAcc(account);
  var Счет : string, Валюта : integer, ErrMsg : string = "";

  ПолучитьСчетСВалютой(reqopena, 1 /*Открытие*/, Счет, Валюта);
  GenAcc.Client = reqopena.ClientID;
  GenAcc.Open_Date = reqopena.Date;
  GenAcc.Type_Account = reqopena.Type_Account;
  GenAcc.Department = reqopena.AccountDepartment;

  var CommonSBC : TCommonDataAccMsg = TCommonDataAccMsg( GenAcc.Department, 
                                                         ACCMSG_KIND_OPEN, 
                                                         vers );

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonSBC);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonSBC, Счет, "", Валюта, GenAcc.Client, OldMes.MesID);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonSBC, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo(GenAcc.Client, ACCMSG_KIND_OPEN, null, reqopena.ClientType311, NULL, NULL, reqopena, NULL);

  // Файл \ Документ \ СвСчет
  WriteAccInfo(Счет, Валюта, GenAcc, reqopena);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonSBC._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
  end;

  if(CommonSBC._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
