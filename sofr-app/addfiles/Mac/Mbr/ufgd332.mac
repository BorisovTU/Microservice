/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED332                                     */
/*                                                                          */
/*  Имя файла: ufgd332.mac                                                  */
/*  Создан:    01.11.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdreqliq.mac";

class (TGenDocLiqReq) TGenDocED332()

  InitTGenDocLiqReq(MESKIND_ANSWER, "ED332");

  class LiquidityInfo
    var BIC = "",
        LiquiditySum = "";
  end;

  var TotalLiquidity = LiquidityInfo(),
      PURLiquidity = TArray();

  macro ReadFields() : bool
    var fieldname = "", fieldvalue = "", blockname = "";
    while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if  (blockname == "")
        if  (fieldname == "EDAuthor")
          OriginatorUIS = fieldvalue;
        elif(fieldname == "EDReceiver")
          RecipientUIS = fieldvalue;
        end;
      elif(blockname == "PartInfo")
        if  (fieldname == "PartNo")
          PartNo = fieldvalue;
        elif(fieldname == "PartAggregateID")
          PartAggregateID = fieldvalue;
        end;
      elif(blockname == "TotalLiquidity")
        if  (fieldname == "BIC")
          TotalLiquidity.BIC = fieldvalue;
        end;
      elif(blockname == "TotalLiquidity\\Liquidity")
        if  (fieldname == "LiquiditySum")
          TotalLiquidity.LiquiditySum = fieldvalue;
        end;
      elif(blockname == "PURLiquidity")
        if  (fieldname == beginField)
          PURLiquidity[ PURLiquidity.size ] = LiquidityInfo();
        elif(fieldname == "BIC")
          PURLiquidity[ PURLiquidity.size-1 ].BIC = fieldvalue;
        end;
      elif(blockname == "PURLiquidity\\Liquidity")
        if  (fieldname == "LiquiditySum")
          PURLiquidity[ PURLiquidity.size-1 ].LiquiditySum = fieldvalue;
        end;
      elif(blockname == "InitialED")
        if  (fieldname == "EDDate")
          EDDate_InitMes = fieldvalue;
        end;
      end; 
    end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

    return TRUE;
  end;

  private macro AddQueries(p_wlreq)
    for(var item, PURLiquidity)
      p_wlreq.Queries = p_wlreq.Queries + "\n" + "/" + item.BIC + "/" + item.LiquiditySum;
    end;
  end;

  macro FillQueries() : bool // при вставке ответа

    // "/BIC/TotalLiquidity<CRLF>/BICPUR/PURLiquidity<CRLF>"
    if(TotalLiquidity.BIC and TotalLiquidity.LiquiditySum)
      wlreq.Queries  = "/" + TotalLiquidity.BIC + "/" + TotalLiquidity.LiquiditySum;
    end;
    AddQueries(wlreq);

    return TRUE;
  end;

  macro ReFillQueries(p_wlreq) : bool // при обновлении головной части ответа
    AddQueries(p_wlreq);
    return TRUE;
  end;

  private macro SetCurLiquidity(p_BIC : string, p_SumStr : string) : bool
    var Department : integer = GetDprtByBIC(p_BIC);

    if(Department > 0) // Если филиал найден
      var Dpliqset : RsbDpLiqSet = RsbDpLiqSet(Department);
      Dpliqset.CurLiquidity = StrToMoneyUFBS(p_SumStr);
      if(Dpliqset.Update() != 0)
        std.msg("Ошибка при сохранении суммы текущей ликвидности");
        return FALSE;
      end;
    end;

    return TRUE;
  end;

  macro OnAfterGenWlreq() : bool    
    var stat : bool = TRUE;
    
    // Если в сообщении есть  блок "TotalLiquidity"
    if(TotalLiquidity.BIC and TotalLiquidity.LiquiditySum)
      if(stat)
        stat = SetCurLiquidity(TotalLiquidity.BIC, TotalLiquidity.LiquiditySum);
      end;
    end;
    
    for(var item, PURLiquidity)
      if(stat)
        stat = SetCurLiquidity(item.BIC, item.LiquiditySum);
      end;
    end;

    return stat;
  end;

end;

macro GenDoc( addrMes )                       

  var gdObj : TGenDocED332 = TGenDocED332();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
