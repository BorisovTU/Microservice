/*
  $Name:         ufgd501.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО по сообщению ED501 транспорта УФЭБС
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация УО по сообщению ED501 транспорта УФЭБС
//
// Программист  : Чукина Т.А.
//
// Создан       : 23.12.2014
//
//-----------------------------------------------------------------------------

import "oralib.mac", MesInter, WldInter, "uf501.mac", rsexts;

private macro DeleteFile(FName : string)
  if( not RemoveFile(FName) )
    RunError("Не удалось удалить файл " + FName);
  end;
end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED501" );

  var xml : object = ReadXMLField(wlmes);

  // Заполнение учетных буферов
  ClearRecord( wlinfo );
  wlinfo.TRN         = wlmes.Trn;
  wlinfo.RelatedRef  = wlmes.RelatedRef;
  wlinfo.Direct      = "X"; // WLD_MES_IN
  FillPartyByUIS_PTCK_UIS( ReadAttribute(xml,"EDAuthor"), 
                          @wlinfo.OriginatorID, 
                          @wlinfo.OriginatorCodeKind, 
                          @wlinfo.OriginatorCode, 
                          @wlinfo.OriginatorName );
  FillPartyByUIS_PTCK_UIS( ReadAttribute(xml,"ActualReceiver"), 
                          @wlinfo.RecipientID, 
                          @wlinfo.RecipientCodeKind, 
                          @wlinfo.RecipientCode, 
                          @wlinfo.RecipientName );

  var Narrative : string = "";
  var NewFNameDoc : string = "";
  var Att : string = ReadOptinalNodeText(xml, "ProprietoryAttachment");
  if( StrLwr( SubStr(Att, 1, StrLen(START_FILENAME_STR)) ) == StrLwr(START_FILENAME_STR) )
    Narrative = Att;

    var ind : integer = index(Narrative, "\n");
    var start_pos : integer = StrLen(START_FILENAME_STR) + 1;
    NewFNameDoc = SubStr( Narrative, 
                          start_pos, 
                          IfThenElse(ind, ind - start_pos, null) );
  end;

  if( not ВставитьИнфСообщение( wlinfo, Narrative ) )
    std.msg("Ошибка при вводе информационного сообщения");
    return FALSE;
  end;


  // Привязать ранее сохраненные файлы к информационному сообщению в качестве прикрепленных объектов
  var sInfoID : string = GetWlinfoStrID(wlinfo.InfoID);

  //   Документ
  var FNameDoc : string = GetFileNameED501(wlmes.Trn, "Doc");
  if(NewFNameDoc)
    NewFNameDoc = GetFileDirectoryED501() + "\\" + NewFNameDoc;
    if(not RenameFile(FNameDoc, NewFNameDoc) )
      RsbThrow("Не удалось переименовать файл " + FNameDoc + " в " + NewFNameDoc);
    end;

    FNameDoc = NewFNameDoc;
  end;

  var FileList : TDirList = TDirList(FNameDoc, "F");
  if(FileList.Count)
    if( not LoadImageObj(FNameDoc, OBJTYPE_INFO, sInfoID, WLD_INFO_IMGTYPE_DOCUMENT) )
      RunError("Ошибка прикрепления файла " + FNameDoc + " к инф. сообщению " + wlinfo.InfoID);
    end;
  else
    RunError("Не найден файл " + FNameDoc);
  end;

  //   Приложение к документу
  var ExistsAttFile : bool = false;
  var FNameAtt : string = GetFileNameED501(wlmes.Trn, "Att");
  FileList = TDirList(FNameAtt, "F");
  if(FileList.Count) // Если файл существует ...
    ExistsAttFile = true;

    if( not LoadImageObj(FNameAtt, OBJTYPE_INFO, sInfoID, WLD_INFO_IMGTYPE_DOC_ATTACH) )
      RunError("Ошибка прикрепления файла " + FNameAtt + " к инф. сообщению " + wlinfo.InfoID);
    end;
  end;


  // Удалить файлы после их привязки к объекту
  var NeedSaveFile : bool = Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ED501\\SAVEFILE", V_BOOL, false);
  if(not NeedSaveFile)
    DeleteFile(FNameDoc);

    if(ExistsAttFile)
      DeleteFile(FNameAtt);
    end;
  end;


  return TRUE;

  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
