/*
$Name: swgenmes.mac
$Module: Межбанковские расчеты
$Description: Константы и вспомогательные функции для генерации сообщений SWIFT
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Константы и вспомогательные функции для генерации сообщений SWIFT        */
/*                                                                          */
/*  Имя файла: swgenmes.mac                                                 */
/*  Создан:  21.01.00                                        Алешин А.В.    */
/****************************************************************************/

import FIInter, OprInter, RMInter, "wlgenmes.mac", "wldoc.mac", "wlnote.mac", 
       "wltools.mac", "pmtlscom", "adress.mac", rsd, oralib, likepy, "pm_common.mac";
import "swsplit.mac", "pm_tools.mac", "pmlib.mac", LCInter, "bnk_common.mac", "or_tools.mac";

import "bnk_ptlib.mac";

const WLD_SUBKIND_PAYM_CLN       = 1,    /* клиентский перевод               */
      WLD_SUBKIND_PAYM_BANK_ACC  = 2,    /* банковский перевод на свой счет  */
      WLD_SUBKIND_PAYM_BANK_GEN  = 3,    /* общий банковский перевод         */
      WLD_SUBKIND_PAYM_CHARGES   = 4;    /* согласование расходов            */

/* Виды страниц выписки */
const WLD_EXT_KIND_SINGLE = 1, /* Единственная */
      WLD_EXT_KIND_FIRST  = 2, /* Первая       */
      WLD_EXT_KIND_MID    = 3, /* Средняя      */
      WLD_EXT_KIND_LAST   = 4; /* Последняя    */

/* Виды корсхем */
const WLD_COR_ERROR  = 0, /* Не найдена корсхема*/
      WLD_COR_NOSTRO = 1, /* НОСТРО */
      WLD_COR_LORO   = 2; /* ЛОРО       */


var NotesPaymentSelve = TNotesPaymentSelve;

const ПутьНастройкиИскатьSWIFTКод = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\SWIFT\\FindBIC";
const ПутьНастройкиКонтрольДлины  = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\SWIFT\\LengthControl";
/* Возвращает корсчет, если на данного корреспондта настроено */
/* более одной активной корсхемы в данной валюте */
macro ПолучитьКорсчетКонтрагента(Corschem, FIID)
  record CorrAccRec("account.dbt");
  record LinkAccRec("account.dbt");
  var CorrAcc="", LinkAcc = "", CorrID, Department;
  var rs:object;
  var select:string;
  var params:TArray;
  select = "select cors.t_CorrID, cors.t_Account, cors.t_Department "+
                           "from dcorschem_dbt cors "+
                           "where cors.t_Number=:Corschem and "+
                           "cors.t_FIID=:FIID and "+
                           "cors.t_FI_Kind=:FIKIND_CURRENCY";
  params = makeArray( SQLParam("Corschem", Corschem),
                      SQLParam("FIID", FIID),
                      SQLParam("FIKIND_CURRENCY", FIKIND_CURRENCY));
  rs = execSQLselect( select, params, FALSE );

  if (not rs.moveNext())
    return "";
  end;
  CorrID  = rs.value(0);
  CorrAcc = rs.value(1);
  Department = rs.value(2);

  CorrAccRec.Account       = CorrAcc;
  CorrAccRec.Code_Currency = FIID;
  CorrAccRec.Chapter       = CHAPT1;

  if( not GetLinkedObject( 47, OBJTYPE_ACCOUNT, UniID( CorrAccRec, OBJTYPE_ACCOUNT ), OBJTYPE_ACCOUNT, LinkAccRec) )
    LinkAcc = LinkAccRec.account;
  end;

  select = "select count(*) "+
           "from dcorschem_dbt cors "+
           "where cors.t_FIID=:FIID "+
           "and cors.t_FI_Kind=:FIKIND_CURRENCY "+
           "and cors.t_CorrID=:CorrID "+
           "and cors.t_Number<>:Corschem "+
           "and cors.t_State=0 "+
           "and cors.t_Department = :Department "+
           "and cors.t_Account <> :Account";
  params = makeArray( SQLParam("FIID", FIID),
                      SQLParam("FIKIND_CURRENCY", FIKIND_CURRENCY),
                      SQLParam("CorrID", CorrID),
                      SQLParam("Corschem", Corschem),
                      SQLParam("Department", Department),
                      SQLParam("Account",LinkAcc));

  rs = execSQLselect( select, params, FALSE );

  if (not rs.moveNext()) 
    return "";
  end;
  if (rs.value(0) < 1)
    return "";
  else
    return CorrAcc;
  end;
end;

macro GetTagsForFldAndRls(Field)

  var rs:object;
  var select:string;
  var params:TArray;
  var Tags = "";
    
  select = " select fld.t_Name"+
           " from  dwltpfld_dbt fld, dwlmesfld_dbt mesfld"+
           " where mesfld.t_RlsFormID = :RlsFormID"+
           " and mesfld.t_TpFieldID = fld.t_TpFieldID" +
           " and fld.t_TpID = :TpID" +
           " and fld.t_Name LIKE '" + Field + "%'";

  params = makeArray( SQLParam("RlsFormID", wlmes.RlsFormID),
                      SQLParam("TpID", TRANSP_SWIFT)
                    );
  rs = execSQLselect( select, params, FALSE );

  while ( rs.moveNext()) 
    Tags = Tags + substr(rs.value(0), strlen(rs.value(0)));
  end;
  return Tags;
end;

// Выбрать опцию для заполнения поля, идентифицирующего фин. организацию
macro GetTagForFinOrgIdent
(
  Field       : string,  // номер поля
  CodeKind    : integer, // вид кода, указанный в платеже (маршруте, участнике аккредитива)
  CodeValue   : string,  // код, указанный в платеже (маршруте, участнике аккредитива)
  PartyID     : integer, // субъект экономики, указанный в платеже (маршруте, участнике аккредитива)
  AllowedTags : string   // допустимые опции
): string

  // 1. Определяем набор допустимых опций
  if(AllowedTags == null)
    AllowedTags = "";
  end;
  if( (AllowedTags == "") and Field )
    AllowedTags = GetTagsForFldAndRls(Field);

    // GetTagsForFldAndRls вынимает последний символ из названия поля в релизе, поэтому
    // если в релизе поле без опции, то последний символ в названии поля будет цифровой
    var ind = StrBrk(AllowedTags, "0123456789");
    if(ind)
      StrSubst(AllowedTags, substr(AllowedTags, ind, 1), SWIFT_Tag_0);
    end;
  end;

  // 2. Выбираем опцию среди допустимых
  var Tag = AllowedTags;
  if ( strlen(Tag) != 1 )
    if( (index(Tag, SWIFT_Tag_A)) or (Tag == "") )
      if ( (CodeValue!="") AND (CodeKind==PTCK_SWIFT) )
        Tag = SWIFT_Tag_A;
      else
        var FindBic, err;
        GetRegistryValue( ПутьНастройкиИскатьSWIFTКод, V_BOOL, FindBic, err );
        if ((err == WLD_REG_OK) and FindBic) 
          ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, err );
          if(not err)
            Tag = SWIFT_Tag_A;
          end;
        end;
      end;
    end;
  
    if((strlen(Tag) != 1) and (index(Tag,SWIFT_Tag_A)) )
      if(((CodeKind == PTCK_ABA) and (CodeValue == "")) or
         ((CodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_A, substr(CodeValue,1,2))))
        )
        Tag = SWIFT_Tag_A;
      end;
    end;
  
    if((strlen(Tag) != 1) and (index(Tag, SWIFT_Tag_B)) )
      if( ПринадлежностьБанкаНашейТС(PartyID) )
        Tag = SWIFT_Tag_B;
      end;
    end;

    if((strlen(Tag) != 1) and (index(Tag, SWIFT_Tag_C)) )
      if( (Route.CodeKind == PTCK_ABA) or 
          (Route.CodeKind == PTCK_CHIPS) or 
          (Route.CodeKind == PTCK_BIC) or 
         ((Route.CodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_C, substr(Route.CodeValue,1,2))))
        )
        Tag = SWIFT_Tag_C;
       end;
    end;

    if((strlen(Tag) != 1) and ((index(Tag, SWIFT_Tag_D)) or (Tag == "")) )
      Tag = SWIFT_Tag_D;
    end;

    if( (strlen(Tag) != 1) and index(Tag, SWIFT_Tag_0) )
      Tag = SWIFT_Tag_0;
    end;
  end;

  return Tag;
end;

macro FillFinOrgIdentField_A(Account: string, CodeKind: integer, CodeValue: string, PartyID: integer) : string
  var field_value : string = "";

  if( account != "" )
    field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
  elif((CodeKind == PTCK_NCSC) and index(SWIFT_NCSC_TAG_A, substr(CodeValue,1,2)))
    field_value = SYMB_DSLASH + CodeValue + SYMB_ENDL;
  elif((CodeKind == PTCK_ABA) and (CodeValue == ""))
    field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + SYMB_ENDL;
  end;

  if(CodeKind==PTCK_SWIFT)
    field_value = field_value + CodeValue;
  else
    var FindBic, err;
    GetRegistryValue( ПутьНастройкиИскатьSWIFTКод, V_BOOL, FindBic, err );
    if ((err == WLD_REG_OK) and FindBic) 
      var BankCode = ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, err );
      field_value = field_value + BankCode;
    end;
  end;

  return field_value;
end;

macro FillFinOrgIdentField_B(Account : string, Name : string, PartyID, DKFlag): string
  var field_value : string = "";

  if(DKFlag and PartyID)
    var AccDep = 0, // филиал счета
        PartyDep = 0; // филиал, связанный с субъектом

    var select = "select t_Department from daccount_dbt " +
                 "where t_Account ='" + Account +"' and t_chapter = 1";
    var rs = execSQLselect( select, NULL );
    if(rs and rs.moveNext())
      AccDep = rs.value(0);
      rs = null;
      select = "select t_Code from ddp_dep_dbt " +
               " where t_PartyID = " + PartyID + " and t_NodeType = 1";
      rs = execSQLselect(select);
      if(rs and rs.moveNext())
        PartyDep = rs.value(0);
        if( ( (DKFlag == "D") and (PartyDep != AccDep) ) or ( (DKFlag == "C") and (PartyDep == AccDep) ) )
          field_value = field_value + SYMB_SLASH + "D";
        else
          field_value = field_value + SYMB_SLASH + "C";
        end;
      end;
    end;
  end;

  field_value = field_value + SYMB_SLASH + Account + SYMB_ENDL;
  field_value = field_value + Name;
    
  return field_value;
end;

macro FillFinOrgIdentField_C(Account: string, CodeKind: integer, CodeValue: string):string
  var field_value : string = "";

  if ( account != "" )
    if( CodeKind==PTCK_BIC )
      field_value = string( field_value, SYMB_DSLASH , CODEWORLD_BIC, CodeValue );
      field_value = string( field_value, ".", account );
    else
      field_value = field_value + SYMB_SLASH + account;
    end;
  elif (CodeKind == PTCK_ABA)
    field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + CodeValue;
  elif(CodeKind == PTCK_CHIPS)
    field_value = SYMB_DSLASH + CODEWORLD_CHIPS + CodeValue;
  elif(CodeKind == PTCK_BIC) 
    field_value = SYMB_DSLASH + CODEWORLD_BIC + CodeValue;
  elif((CodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_C, substr(CodeValue,1,2))))
    field_value = SYMB_DSLASH + CodeValue;
  end;

  return field_value;
end;

macro GetNameBank_Tag_D(Tag, Name, BankID)

  file party(party);
  if((Tag == SWIFT_Tag_D) and (Name == "") and (BankID > 0))
    party.PartyID = BankID;
    if(GetEQ(party))
      return party.Name;
    end;
  end;

  return Name;
end;

macro FillFinOrgIdentField_D( Account: string, CodeKind: integer, CodeValue: string, 
                              PartyID: integer, PartyName : string, Tag, standart
                            ) : string

  var field_value : string = "", tempv, Name, INN;
  record RecordAdress ( adress );

  if ( (standart==ST_RUR4) OR (standart==ST_RUR5) OR (standart==ST_RUR6) )
    if ( account != "" )
      if( CodeKind==PTCK_BIC )
        field_value = string( field_value, SYMB_DSLASH , CODEWORLD_BIC, CodeValue );
        field_value = string( field_value, ".", account );
      else
        field_value = field_value + SYMB_SLASH + account;
      end;
      field_value = field_value + SYMB_ENDL;
    elif (CodeKind == PTCK_ABA)
      field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + CodeValue + SYMB_ENDL;
    elif(CodeKind == PTCK_CHIPS)
      field_value = SYMB_DSLASH + CODEWORLD_CHIPS + CodeValue + SYMB_ENDL;
    elif(CodeKind == PTCK_BIC) 
      field_value = SYMB_DSLASH + CODEWORLD_BIC + CodeValue + SYMB_ENDL;
    elif((CodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_D, substr(CodeValue,1,2))))
      field_value = SYMB_DSLASH + CodeValue + SYMB_ENDL;
    end;
  else
    if( account != "")
      field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
    elif (CodeKind == PTCK_ABA)
      field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + CodeValue + SYMB_ENDL;
    elif(CodeKind == PTCK_CHIPS)
      field_value = SYMB_DSLASH + CODEWORLD_CHIPS + CodeValue + SYMB_ENDL;
    elif(CodeKind == PTCK_BIC) 
      field_value = SYMB_DSLASH + CODEWORLD_BIC + CodeValue + SYMB_ENDL;
    elif((CodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_D, substr(CodeValue,1,2))))
      field_value = SYMB_DSLASH + CodeValue + SYMB_ENDL;
    end;
  end;

  INN = GetPartyINN( PartyID );
  if( INN != "" )
    field_value = String( field_value, "INN", INN, SYMB_ENDL );
  end;

  Name = GetNameBank_Tag_D(Tag, PartyName, PartyID);
  if( Name != "" )
    if( (PartyID) AND НайтиЮридическийАдресСубъекта(PartyID, RecordAdress) AND
        (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
      Name = string( Name, SYMB_ENDL, trim(RecordAdress.Place) );
    end;
    StrChangeRusXSet( Name, tempv, standart );
    /*StrMultyToFormat( tempv, tempv, 35, 5 );**/
    tempv = SwiftStrSplit( tempv, 35, 4 );
    field_value = field_value + tempv;
  end;

  return field_value;
end;

macro FillFinOrgIdentField_0( Account: string, PartyID: integer, PartyName: string,
                              standart 
                            )

  var field_value : string = "", tempv, Name, INN;
  record RecordAdress ( adress );

  if( account != "")
    field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
  end;

  INN = GetPartyINN( PartyID );
  if( INN != "" )
    field_value = String( field_value, "INN", INN, SYMB_ENDL );
  end;

  if( Name != "" )
    if( (PartyID) AND НайтиЮридическийАдресСубъекта(PartyID, RecordAdress) AND
        (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
      Name = string( Name, SYMB_ENDL, trim(RecordAdress.Place) );
    end;
    StrChangeRusXSet( Name, tempv, standart );
    /*StrMultyToFormat( tempv, tempv, 35, 5 );**/
    tempv = SwiftStrSplit( tempv, 35, 4 );
    field_value = field_value + tempv;
  end;

  return field_value;
end;

macro FillFinOrgIdentField( Field : string, Tag, 
                            Account : string, CodeKind : integer, CodeValue : string, 
                            PartyID: integer, PartyName : string, DKFlag, standart
                          ) : string

  var field_value : string = "";

  if( valtype(Tag) == V_UNDEF )
    Tag = "";
  end;
  // Если не передали конкретную опцию или передали набор допустимых, то подбираем
  if ( strlen(Tag) != 1 )
    Tag = GetTagForFinOrgIdent(Field, CodeKind, CodeValue, PartyID, Tag);
  end;

  if  ( Tag == SWIFT_Tag_A )
    field_value = FillFinOrgIdentField_A(account, CodeKind, CodeValue, PartyID);
  elif( Tag == SWIFT_Tag_B )    
    field_value = FillFinOrgIdentField_B(Account, PartyName, PartyID, DKFlag);
  elif( Tag == SWIFT_Tag_C )
    field_value = FillFinOrgIdentField_C(account, CodeKind, CodeValue);
  elif( Tag == SWIFT_Tag_D )
    field_value = FillFinOrgIdentField_D( account, CodeKind, CodeValue, 
                                          PartyID, PartyName, Tag, standart );
  elif( Tag == SWIFT_Tag_0 )
    field_value = FillFinOrgIdentField_0( account, PartyID, PartyName, standart );
    Tag = "";// для возврата
  end;

  setparm( 1, Tag );

  return field_value;
end;

macro СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart, usingCode )
  record RecordAdress ( adress );
  FILE dp_dep( dp_dep ) key 2;

  var field_value = "", tempv, Name, BankCode, INN, error, account, FindBic, err, PartyID = 0;
  var rs:object;
  var select:string;
  var params:TArray;

  if( valtype(Tag) == V_UNDEF )
    Tag = "";
  end;

  if ( Route.Sort>0 )
     account = Route.InAccount;
  elif ( Route.Sort<0 )
     account = Route.OutAccount;
  else
     account = "";
  end;

  /* Если не передали конкретную опцию или передали набор допустимых, то подбираем, иначе пытаемся заполнить по переданной*/
  if ( strlen(Tag) != 1 )
    Tag = GetTagForFinOrgIdent(null, Route.CodeKind, Route.CodeValue, Route.PartyID, Tag);
  end;
  
  if( Tag == SWIFT_Tag_A )
    field_value = FillFinOrgIdentField_A(account, Route.CodeKind, Route.CodeValue, Route.PartyID);
  elif ( Tag == SWIFT_Tag_B )    
    field_value = FillFinOrgIdentField_B(Route.InAccount, Route.Name, Route.PartyID, DKFlag);
  elif( Tag == SWIFT_Tag_C )
    field_value = FillFinOrgIdentField_C(account, Route.CodeKind, Route.CodeValue);
  else 
    field_value = FillFinOrgIdentField_D( account, Route.CodeKind, Route.CodeValue, 
                                          Route.PartyID, Route.Name, Tag, standart );
  end;

  setparm( 1, Tag );

  return field_value;
end;

macro ЗаписатьПолеВБлок( НазваниеПоля, НазваниеБлока, field_value , Recreate, oldMes )
  if( Recreate == NULL )
     Recreate = false;
  end;

  if( oldMes == NULL )
    oldMes = NULL;
  end;
  if ( (valtype(field_value)!=V_UNDEF) AND ((valtype(field_value)!=V_STRING) OR (field_value!="")) )
     if( not УстановитьКонтекстБлока( НазваниеБлока ) )
       RunError("|Ошибка при установке контекста блока " + НазваниеБлока);
     end;
  end;
  if( Recreate )
     if( not GetBlockEdit(НазваниеПоля, OldMes.RlsFormID ) )
        ПолучитьПоле(OldMes.MesID, НазваниеПоля, field_value );
        if (field_value)
            ЗаписатьПолеЛог( НазваниеПоля, field_value );
        end;
     end;
  else 
     ЗаписатьПолеЛог( НазваниеПоля, field_value );
  end;
end;

macro ЗаписатьПолеВБлокКонтекст0( НазваниеПоля, НазваниеБлока, field_value )
var   Recreate, oldMes;
 GetParm(3, Recreate);
 GetParm(4, oldMes  );
 if( Recreate == NULL )
    Recreate = false;
 end;
 if( oldMes == NULL )
   oldMes = NULL;
 end;
 if ( (valtype(field_value)!=V_UNDEF) AND ((valtype(field_value)!=V_STRING) OR (field_value!="")) )
    if( not УстановитьКонтекстБлока() )
       RunError("|Ошибка при установке нулевого контекста блока");
    end;

    ЗаписатьПолеВБлок( НазваниеПоля, НазваниеБлока, field_value, NULL,  Recreate, OldMes );
 end;
end;

/* Записать поле маршрута от текущего контекста с формированием лога (тип узла - B) */
macro ЗаписатьПолеМаршрутаЛог( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode )
  var field_value, Recreate, oldMes;       
 GetParm(7, Recreate);
 GetParm(8, oldMes  );
 if( Recreate == NULL )
     Recreate = false;
 end;

 if( oldMes == NULL )
     oldMes = NULL;
 end;

  field_value = СформироватьЗаписьМаршрута(Route, Tag, DKFlag, standart, usingCode);

  ЗаписатьПолеВБлок( НазваниеПоля + Tag, НазваниеБлока, field_value, NULL, Recreate, OldMes );
end;

/* Записать поле маршрута от нулевого контекста блока */
macro ЗаписатьПолеМаршрутаКонтекст0Лог( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode )
  var  Recreate, oldMes;       
   GetParm(7, Recreate);
   GetParm(8, oldMes  );
   if( Recreate == NULL )
       Recreate = false;
   end;

   if( oldMes == NULL )
       oldMes = NULL;
   end;

  /* Устанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  /* Запишем узел маршрута */
  if( valtype(Tag) == V_UNDEF )
    ЗаписатьПолеМаршрутаЛог( НазваниеПоля, НазваниеБлока, Route, "", DKFlag, Recreate, OldMes );
  else
    ЗаписатьПолеМаршрутаЛог( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode, Recreate, OldMes );
  end;
end;

macro ДобавитьОстатокПоля70( field_value, ОстатокПоля70, standart )
  var i = 0;

  while( i<strlen(ОстатокПоля70) )
     if( substr(ОстатокПоля70,i,1) == SYMB_ENDL )
         ОстатокПоля70 = substr( ОстатокПоля70, 1, i-1 ) + substr( ОстатокПоля70, i+1 );
     end;
     i = i+1;
  end;

  if ( ОстатокПоля70!="" )
      ОстатокПоля70 = "ZP/" + substr(ОстатокПоля70,1,70);
      if ( (valtype(standart)!=V_UNDEF) AND (standart==ST_RUR4) )
         StrMultyToFormat( ОстатокПоля70, ОстатокПоля70, 33, 3, "CheckBreakRUR4" );
      else
        ОстатокПоля70 = SwiftStrSplit( ОстатокПоля70, 33, 3, true );
        //StrMultyToFormat( ОстатокПоля70, ОстатокПоля70, 33, 3 );
      end;
      ОстатокПоля70 = "/N" + ОстатокПоля70;

      i = 0;
      while( i<strlen(ОстатокПоля70) )
         if( substr(ОстатокПоля70,i,1)=="\n" )
             ОстатокПоля70 = substr( ОстатокПоля70, 1,i ) + "//" + substr( ОстатокПоля70, i+1 );
             i = i+2;
         end;
         i = i+1;
      end;
      field_value = field_value + ОстатокПоля70;
  end;
  return field_value;
end;

macro FillNZP_byPaymentGround( field_value, PaymentGround, standart )
  var i = 0;
  if( PaymentGround != "" )
    StrChangeRusXSet( PaymentGround, PaymentGround, standart );
    PaymentGround = "ZP/" + PaymentGround;
    if( (valtype(standart)!=V_UNDEF) and (standart==ST_RUR4) )
      StrMultyToFormat( PaymentGround, PaymentGround, 33, 6, "CheckBreakRUR4" );
    else
      StrMultyToFormat( PaymentGround, PaymentGround, 33, 6 );
    end;
    PaymentGround = "/N" + PaymentGround;

    while( i < strlen(PaymentGround) )
      if( substr(PaymentGround,i,1) == "\n" )
        PaymentGround = substr( PaymentGround, 1,i ) + "//" + substr( PaymentGround, i+1 );
        i = i+2;
      end;
      i = i+1;
    end;
    field_value = field_value + PaymentGround;
  end;
  return field_value;
end;

macro ДобавитьБлокВПоле72( field_value, ИмяБлока, Содержимое, standart )
  var i;

  if ( Содержимое!="" )
      i=0;
      while( (ИмяБлока != "") and (i<(strlen(ИмяБлока)+2)) ) 
         Содержимое = SYMB_BLANK + Содержимое;
         i = i+1;
      end;

      if ( (valtype(standart)!=V_UNDEF) AND (standart==ST_RUR4) )
         StrMultyToFormat( Содержимое, Содержимое, 33, 6, "CheckBreakRUR4" );
      else
         StrMultyToFormat( Содержимое, Содержимое, 33, 6 );
      end;

      if( ИмяБлока != "" )
        Содержимое = SYMB_SLASH + ИмяБлока + SYMB_SLASH + substr( Содержимое, strlen(ИмяБлока)+3 );
      end;

      i = 0;
      while( i<strlen(Содержимое) )
         if( substr(Содержимое,i,1)==SYMB_ENDL )
             Содержимое = substr( Содержимое, 1,i ) + SYMB_DSLASH + substr( Содержимое, i+1 );
             i = i+2;
         end;
         i = i+1;
      end;
      if ( (field_value!="") AND (substr(field_value, strlen(field_value), 1)!=SYMB_ENDL) )
        field_value = field_value + SYMB_ENDL;
      end;
      field_value = field_value + Содержимое;
  end;
  return field_value;
end;

/* Заполнить поле 26T*/
macro FillTransactionTypeCodeField(RsPaym, Standart)  
  var field_value="";

  /* Используется только для RUR6 */
  if (Standart != ST_RUR6)
    return "";
  end;

  if ((RsPaym.PayerFIID==0) AND (RsPaym.PaymentID))
    if (RsPaym.TaxAuthorState != "")
      field_value = "S" + RsPaym.TaxAuthorState;
    else
      return "";
    end;
  else
    return "";
  end;
  
  return field_value;
end;

/* Заполнить поле 77B */ 
macro FillRegulatoryReportingField(RsPaym, Standart)
  var field_value="", str = "";

  /* Используется только для RUR6 */
  if (Standart != ST_RUR6)
    return "";
  end;

  if (RsPaym.PayerFIID==0)
    /* Строка 1 */
    field_value = field_value + "/N10/";
    if (RsPaym.TaxPmType != "")
      StrChangeRusXSet(RsPaym.TaxPmType, str, Standart);
      field_value = field_value + str;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + "/N4/";
    if (RsPaym.BttTICode != "")
      field_value = field_value + RsPaym.BttTICode;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + SYMB_ENDL;

    /* Строка 2 */
    field_value = field_value + "/N5/";
    if (RsPaym.OKATOCode != "")
      field_value = field_value + RsPaym.OKATOCode;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + "/N6/";
    if (RsPaym.TaxPmGround != "")
      StrChangeRusXSet(RsPaym.TaxPmGround, str, Standart);
      field_value = field_value + str;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + "/N7/";
    if (RsPaym.TaxPmPeriod != "")
      StrChangeRusXSet(RsPaym.TaxPmPeriod, str, Standart);
      field_value = field_value + str;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + SYMB_ENDL;
    
    /* Строка 3 */
    field_value = field_value + "/N8/";
    if (RsPaym.TaxPmNumber != "")
      StrChangeRusXSet(RsPaym.TaxPmNumber, str, Standart);
      field_value = field_value + str;
    else
      field_value = field_value + "0";
    end;
    field_value = field_value + "/N9/";
    if (RsPaym.TaxPmDate != "")
      StrChangeRusXSet(RsPaym.TaxPmDate, str, Standart);
      field_value = field_value + str;
    else
      field_value = field_value + "0";
    end;
  else
    return "";
  end;
  
  return field_value;  
end;

private macro GetAccountClient( Account, FIID ) 
  file acc ("account.dbt" ) key 0;

  acc.Chapter       = 1;
  acc.Account       = Account;
  acc.Code_Currency = FIID;
  if( getEQ( acc ) )
    return acc.Client;
  end;
  return -1;
end;
//Определение плательщика по платежу
macro GetPaymPayer( RsPaym )
  var Payer = -1
  ;
  var rs:object;
  var select:string;
  var params:TArray;

  if( RsPaym.Payer > 0 )
    Payer = RsPaym.Payer;
  else
    if(RsPaym.PayerAccount != "")
      select = "select cors.t_CorrID "+
               "from dcorschem_dbt cors, dobjcode_dbt code "+
               "where cors.t_Account=:Account "+
               "and cors.t_FIID=:FIID "+
               "and cors.t_State = 0 "+
               "and cors.t_CorrID = code.t_ObjectID "+
               "and code.t_CodeKind = :PTCK_SWIFT "+
               "and code.t_ObjectType = :OBJTYPE_PARTY";
      params = makeArray( SQLParam("Account", RsPaym.PayerAccount),
                          SQLParam("FIID", RsPaym.PayerFIID),
                          SQLParam("PTCK_SWIFT", PTCK_SWIFT),
                          SQLParam("OBJTYPE_PARTY",OBJTYPE_PARTY));
      rs = execSQLselect( select, params, FALSE );
      if(rs.moveNext())
        Payer = rs.value(0);
      else
        Payer = GetAccountClient(RsPaym.PayerAccount, RsPaym.PayerFIID);
      end;     
    end;
  end;

  return Payer;
end;

/* Заполнить поле приказодателя */
/* Заполнение 50a Option A */
/* Вернет "" если заполнить не удалось */
macro FillOrderingCustomerField103_A( RsPaym, Standart )
  var field_value = "", PayerID, PayerCode, error;

  PayerID = GetPaymPayer( RsPaym );
  
  if (PayerID > 0)
      PayerCode = ПолучитьКодСубъекта(PayerID, PTCK_SWIFT, error);
      if ((error != 0 ) OR (PayerCode == ""))                      
      return ""; 
  end;

  if (RsPaym.PayerAccount != "")
     field_value = "/" + RsPaym.PayerAccount + SYMB_ENDL;
  end;

  field_value = String(field_value, PayerCode);
  end;      
      
  return field_value;
end;

macro FillOrderingCustomerField( RsPaym, Standart )
  var field_value = "", PayerID, INN, KPP = "", field_Client, field_place, rest = "", err, LenghtControl:bool, numstr = 5;
  record RecordAdress ( adress );

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;

     if ( RsPaym.PayerAccount!="" )
        field_value = "/" + RsPaym.PayerAccount + SYMB_ENDL;
        numstr = numstr - 1;
     end;

  PayerID = GetPaymPayer( RsPaym );

  if (Standart == ST_RUR6)
    if ( RsPaym.PayerINN!="" )
      INN = RemoveKPP(RsPaym.PayerINN);
      field_value = String( field_value, "INN", INN );
      KPP = RemoveINN(RsPaym.PayerINN);
      if (KPP != "")
        if ((KPP == "0") and (not IsPerson(PayerID)))
          KPP = "000000000";
        end;
        field_value = String(field_value, ".KPP", KPP);
      end;
      field_value = String(field_value, SYMB_ENDL);
      numstr = numstr - 1;
    end;
  end;
     
  StrChangeRusXSet( RsPaym.PayerName, field_Client, Standart );

  if( (Standart != ST_RUR6) and (ПолучитьСубъекта(PayerID, wlparty) == 0) )
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    if ( RecordAdress.Adress!="" )
      StrChangeRusXSet( RecordAdress.Adress, field_place, Standart );
      field_Client = String( field_Client, " ", field_place );
    end;
    if( RecordAdress.Place != "" )
      StrChangeRusXSet( RecordAdress.Place, field_place, Standart );
      field_Client = String( field_Client, SYMB_ENDL, field_place );
    end;
  end;  

  if ( Standart==ST_RUR4 )
     StrMultyToFormat( field_Client, field_Client, 35, numstr, "CheckBreakRUR4" );
  elif(Standart == ST_UNDEF)
    field_Client = SwiftStrSplit( field_Client, 35, numstr , true, @rest);
    /*StrMultyToFormat( field_value, field_value, 35, 4 );*/
  else
    field_Client = SwiftStrSplit( field_Client, 35, numstr, true, @rest );
    /*StrMultyToFormat( field_value, field_value, 35, 5 );*/
  end;
  
  GetRegistryValue( ПутьНастройкиКонтрольДлины, V_BOOL, LenghtControl, err );
  if ((err == WLD_REG_OK) and LenghtControl and (rest!="") )
    RunError("|Слишком длинная строка в поле 50K");
  end;

  field_value = field_value + field_Client;    

  return field_value;
end;

macro FillOrderingCustomerField103_K( RsPaym, Standart )
  var field_value;

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;

  field_value = FillOrderingCustomerField( RsPaym, Standart );

  return field_value;
end;

macro GetCountryCode(PartyID)
  file country("country.dbt") key 2;
  record Adress(adress);
  if( not НайтиЮридическийАдресСубъекта(PartyID,Adress))
    НайтиАдресСубъекта(PartyID,PTADDR_REAL,Adress);
  end;
  if(Adress.Country != "")
    country.CodeLat3 = Adress.Country;
    if( GetEQ(country) )
      if(country.CodeLat2 != "")
        return country.CodeLat2;
      end;
    end;
  end;
  return "RU";
end;

macro FillOrderingCustomerField103_F( RsPaym, Standart )
  FILE persnidc(persnidc) key 0;
  FILE persn(persn) key 0;
  FILE party(party) key 0;
  record Adress(adress);
  var field_value = "", INN = "", field_name, field_place;
  var SubStr8 = "";
  var PayerID = GetPaymPayer( RsPaym );
  var IsPersn = IsPerson(PayerID);
  var IBEICode = "", error;

  if(PayerID == -1)
    return "";
  end;
  if ( RsPaym.PayerINN!="" )
     INN = RemoveKPP(RsPaym.PayerINN);
  end;

  persnidc.PersonID = PayerID;
  persnidc.PaperKind = 0;

  persn.PersonID = PayerID;
  GetEQ(persn);

  IBEICode = ПолучитьКодСубъекта(PayerID, PTCK_IBEI, error);

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;

  if((RsPaym.PayerAccount != "") and (IsClientPayerAccount(RsPaym) or (not IsPersn)))
    field_value = SYMB_SLASH + RsPaym.PayerAccount;
  elif( GetEQ(persnidc) and IsPersn )
    field_value = "CCPT/" + GetCountryCode(PayerID) + SYMB_SLASH + persnidc.PaperNumber ;
  else
    return "";
  end;

  if(strlen(field_value)>35)
    SubStr8 = substr(field_value,36,33);
    field_value = substr(field_value,1,35);
  end;
  field_value = field_value + SYMB_ENDL;


  field_name = RsPaym.PayerName;
  StrChangeRusXSet( field_name, field_name, Standart );
  
  var NeedINN;
  GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\SWIFT\\FILLINGFIELDS\\50F_INN", V_BOOL, NeedINN);
  if( IsPersn and ( INN != "" ) and NeedINN )
    field_name = field_name + ",INN" + INN;
  end;

  while(strlen(field_name) > 33)
   field_value = field_value + "1/" + substr(field_name,1,33) + SYMB_ENDL;
   field_name = substr(field_name,34);
  end;
  field_value = field_value + "1/" + field_name + SYMB_ENDL;

  if( IsPersn and (persn.Born != date(0,0,0)) and (persn.BirsPlase != ""))
    field_value = field_value + "4/" + GetSWIFTDate(persn.Born) +  SYMB_ENDL;
    StrChangeRusXSet( persn.BirsPlase, field_place , Standart); 
    field_place =  GetCountryCode(PayerID) + SYMB_SLASH + field_place;
    field_value = field_value + "5/" + substr(field_place,1,33) + SYMB_ENDL;
  elif( IsPersn and ( INN != "" ) )
    field_value = field_value + "1/" + "INN" + INN + SYMB_ENDL;
  elif( НайтиЮридическийАдресСубъекта(PayerID,Adress) or     НайтиАдресСубъекта(PayerID,PTADDR_REAL,Adress))
    field_place = string(Adress.Street," ",Adress.CodeStreet," ",Adress.House);
    StrChangeRusXSet( field_place, field_place, Standart);
    while(strlen(field_place) > 33)
     field_value = field_value + "2/" + substr(field_place,1,33) + SYMB_ENDL;
     field_place = substr(field_place,34);
    end;
    field_value = field_value + "2/" + field_place + SYMB_ENDL;
    field_place = "";
    if( Adress.PostIndex != "" )
      field_place = Adress.PostIndex + " ";
    end;
    if(Adress.Region != "" )
      field_place = field_place + Adress.Region + " ";
    end;
    if(Adress.CodeRegion != "" )
      field_place = field_place + Adress.CodeRegion + " ";
    end;
    if(Adress.Province != "" )
      field_place = field_place + Adress.Province + " " ;
    end;
    if(Adress.CodeProvince  != "" )
      field_place = field_place + Adress.CodeProvince + " ";
    end;
    if(Adress.District != "" )
      field_place = field_place + Adress.District + " ";
    end;
    if(Adress.CodeDistrict != "" )
      field_place = field_place + Adress.CodeDistrict + " ";
    end;
    if(Adress.Place != "" )
      field_place = field_place + Adress.Place + " ";
    end;
    if(Adress.CodePlace != "" )
      field_place = field_place + Adress.CodePlace + " ";
    end;
    field_place = Trim(field_place);
    
    StrChangeRusXSet( field_place, field_place, Standart); 
    field_place = GetCountryCode(PayerID) + "/" + field_place;
    field_value = field_value + "3/" + substr(field_place,1,33) + SYMB_ENDL;
  end;

  if(SubStr8 != "")
    field_value = field_value + "8/" + SubStr8;
  end;
  
  StrMultyToFormat( field_value, field_value, 35, 5 );  
    
  return field_value;
end;

/* Проверяем является ли субъект нерезидентом. Если является - возвращаем true.
   В случае ошибки - false. */
private macro PartyIsNotResident( PartyID )
   var PartyObj:RsbParty = RsbParty( PartyID );
   return PartyObj.IsNotResident;
end;

macro FillOrderingCustomerField103RUR_F( RsPaym, Standart )
  record Adress(adress);
  var field_value = "", INN = "", KPP = "", field_name, field_place;
  var SubStr8 = "";
  var PayerID = GetPaymPayer( RsPaym );
  var IsPersn = IsPerson(PayerID);
  var error, FillType;
                                                            
  // Первая строка поля заполняется номером счета
  if(RsPaym.PayerAccount != "")
    field_value = SYMB_SLASH + RsPaym.PayerAccount;
    if(strlen(field_value) > 35)
      SubStr8 = substr(field_value, 36, 33);
      field_value = substr(field_value, 1, 35);
    end;
    field_value = field_value + SYMB_ENDL;
  end;

  // определяем ИНН и КПП
  if( RsPaym.PayerINN != "" )
    INN = RemoveKPP( RsPaym.PayerINN );
    KPP = RemoveINN( RsPaym.PayerINN) ;
  end;

  // Если в платеже указан ИНН или КИО, то заполнить вторую строку подстрокой "1 Коды налогоплательщиков"
  if((INN == "") and (not IsPersn))
    INN = "0";
  end;
  if( INN != "" )
    field_value = field_value + "1/INN" + INN;
    if( KPP != "" )
      if ((KPP == "0") and (not IsPersn))
        KPP = "000000000";
      end;
      field_value = field_value + ".KPP" + KPP;
    end;
    field_value = field_value + SYMB_ENDL;
  end;

  // Наименование плательщика
  StrChangeRusXSet( RsPaym.PayerName, field_name, Standart );
  while(strlen(field_name) > 33)
   field_value = field_value + "1/" + substr(field_name,1,33) + SYMB_ENDL;
   field_name = substr(field_name,34);
  end;
  field_value = field_value + "1/" + field_name + SYMB_ENDL;

  if( (INN == "") and IsPersn )
    
    if( НайтиЮридическийАдресСубъекта(PayerID,Adress) or НайтиАдресСубъекта(PayerID,PTADDR_REAL,Adress) )
       field_place = string(Adress.Street," ",Adress.CodeStreet," ",Adress.House);
       StrChangeRusXSet( field_place, field_place, Standart);
       while(strlen(field_place) > 33)
        field_value = field_value + "2/" + substr(field_place,1,33) + SYMB_ENDL;
        field_place = substr(field_place,34);
       end;
       field_value = field_value + "2/" + field_place + SYMB_ENDL;
       field_place = "";
       if( Adress.PostIndex != "" )
         field_place = Adress.PostIndex + " ";
       end;

       if(Adress.CodePlace != "" )
         field_place = field_place + Adress.CodePlace + " ";
       end;
       if(Adress.Place != "" )
         field_place = field_place + Adress.Place + " ";
       end;

       if(Adress.District != "" )
         field_place = field_place + Adress.District + " ";
       end;
       if(Adress.CodeDistrict != "" )
         field_place = field_place + Adress.CodeDistrict + " ";
       end;

       if(Adress.Province != "" )
         field_place = field_place + Adress.Province + " " ;
       end;
       if(Adress.CodeProvince  != "" )
         field_place = field_place + Adress.CodeProvince + " ";
       end;

       if(Adress.Region != "" )
         field_place = field_place + Adress.Region + " ";
       end;
       if(Adress.CodeRegion != "" )
         field_place = field_place + Adress.CodeRegion + " ";
       end;

       field_place = Trim(field_place);
       
       StrChangeRusXSet( field_place, field_place, Standart); 
       field_place = GetCountryCode(PayerID) + "/" + field_place;
       field_value = field_value + "3/" + substr(field_place,1,33) + SYMB_ENDL;
    end;

  end;

  if(SubStr8 != "")
    field_value = field_value + "8/" + SubStr8;
  end;
  
  StrMultyToFormat( field_value, field_value, 35, 5 );  
    
  return field_value;
end;

macro FillOrderingCustomerField103(RsPaym, Tag, Standart)
  var rs:object;
  var select:string;
  var params:TArray;
  var InitPaymID = getInitialPaymentID(RsPaym.PaymentID,PMLINK_KIND_RETREDIR);
  var PaymentID, PayerID;
  var field_value = "";
  
  if( RsPaym.IsTransit or (InitPaymID != 0))
    if(InitPaymID != 0)
      PaymentID = InitPaymID
    else
      PaymentID = RsPaym.PaymentID;
    end;

    select = " select mesval.t_TpFieldID, mesval.t_Value"+
             " from dwlmeslnk_dbt meslnk, dwlmes_dbt wlmes, dwlpm_dbt wlpm, dwltpshem_dbt wltpshem, dwlmesval_dbt mesval"+
             " where wlmes.t_MesID = meslnk.t_MesID"+
             " and meslnk.t_ObjID = wlpm.t_WlPmID"+
             " and meslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
             " and wlpm.t_Direct = 'X'"+
             " and wlpm.t_PaymentID = :PaymentID"+
             " and wlpm.t_WlPmNum = 0"+
             " and wltpshem.t_TpShemID = wlmes.t_TpSchemID"+
             " and wltpshem.t_TpID = :TRANSP_SWIFT"+
             " and mesval.t_MesID = wlmes.t_MesID"+
             " and mesval.t_TpFieldID IN(108, 145, 611)"+
             " and mesval.t_Index = (SELECT MIN(mesval1.t_Index)"+
                                   " FROM dwlmesval_dbt mesval1"+
                                   " WHERE mesval1.t_MesID = wlmes.t_MesID"+
                                   " and mesval1.t_TpFieldID IN(108, 145, 611))";
    params = makeArray( SQLParam("OBJTYPE_PAYMENT",OBJTYPE_PAYMENT),
                        SQLParam("PaymentID", PaymentID),
                        SQLParam("TRANSP_SWIFT",TRANSP_SWIFT));
    rs = execSQLselect( select, params, FALSE );
    if(rs.moveNext())
      field_value = rs.value(1);
      if(rs.value(0) == 108)
        Tag = SWIFT_Tag_K;
      elif(rs.value(0) == 145)
        Tag = SWIFT_Tag_A
      elif(rs.value(0) == 611)
        Tag = SWIFT_Tag_F;
      end;
      setparm( 1, Tag );  
      return field_value;
    end;    
  end;

  PayerID = GetPaymPayer( RsPaym );
/*
  if( Standart == ST_RUR6 )
    // определяем опцию
    if( index(RsPaym.PayerName, "//") and index(Tag, SWIFT_Tag_K) )
      Tag = SWIFT_Tag_K;
    elif( index(Tag, SWIFT_Tag_F) )
      Tag = SWIFT_Tag_F;
    elif( index(Tag, SWIFT_Tag_K) )
      Tag = SWIFT_Tag_K;
    end;
    // формируем поле по выбранной опции
    if( Tag == SWIFT_Tag_K )
      field_value = FillOrderingCustomerField103_K( RsPaym, Standart );
    elif( Tag == SWIFT_Tag_F )
      field_value = FillOrderingCustomerField103RUR_F( RsPaym, Standart );      
    else
      RunError("Нет доступной опции поля");
    end;
  else
    if(IsPerson(PayerID))
      Tag = SWIFT_Tag_F;
      field_value = FillOrderingCustomerField103_F( RsPaym, Standart );
    else
      if((PayerID == {OurBank}) and (not PM_IsBankAccount(RsPaym.PayerAccount)))
        Tag = SWIFT_Tag_K;
        field_value = FillOrderingCustomerField103_K( RsPaym, Standart );
      else
        Tag = SWIFT_Tag_A;
        field_value = FillOrderingCustomerField103_A( RsPaym, Standart );
      end;
      if( field_value == "")
        if( (RsPaym.PayerAccount == "") or (PayerID == {OurBank})  or (RsPaym.Payer == -1) )
          Tag = SWIFT_Tag_K;
          field_value = FillOrderingCustomerField103_K( RsPaym, Standart );
        else
          Tag = SWIFT_Tag_F;
          field_value = FillOrderingCustomerField103_F( RsPaym, Standart );      
        end;
      end; 
    end;
  end;
*/

  Tag = SWIFT_Tag_K;
  field_value = FillOrderingCustomerField103_K( RsPaym, Standart );

  setparm( 1, Tag );  
  return field_value;
end;

macro FillOrderingCustomerField_LC(LcObj : RsbLetterOfCredit) : string
  var field_value = "",
      Lcparty : TRecHandler = TRecHandler("lcparty");

  if( LcObj.Parties().Find(LCPARTY_KIND_APPLICANT, Lcparty) == 0 )
    if(Lcparty.rec.LatName)
      field_value = Lcparty.rec.LatName;
    else
      field_value = Lcparty.rec.Name;
    end;

    if(field_value)
      StrChangeRusXSet( field_value, field_value );
      field_value = SwiftStrSplit( field_value, 35, 4, true );
    end;
  end;

  return field_value;
end;

/* Заполнить поле бенефициара */
/* Заполнение 59a Option A */
/* Вернет "" если заполнить не удалось */
macro FillBeneficiaryCustomerField_A( RsPaym, Standart )
  var field_value = "";
  var ReceiverID, ReceiverCode, error;

  if (RsPaym.ReceiverCodeKind != PTCK_SWIFT)
    if (RsPaym.ReceiverCode != "")
      ReceiverID = ПолучитьКодСубъекта(RsPaym.ReceiverCode, RsPaym.ReceiverCodeKind, error);
      if (error != 0)
        ReceiverID = RsPaym.Receiver;                            
      end;
    else
      ReceiverID = RsPaym.Receiver;                            
    end;
    ReceiverCode = ПолучитьКодСубъекта(ReceiverID, PTCK_SWIFT, error);
    if ((error != 0 ) OR (ReceiverCode == ""))                      
      return ""; /* у ReceiverID нет BIC ISO - возвращаем "" */
    end;
  else
    if (RsPaym.ReceiverCode != "")
      ReceiverCode = RsPaym.ReceiverCode; /* BIC ISO */
    else
      ReceiverID = RsPaym.Receiver;                            
      ReceiverCode = ПолучитьКодСубъекта(ReceiverID, PTCK_SWIFT, error);
      if ((error != 0 ) OR (ReceiverCode == ""))                      
        return ""; /* у ReceiverID нет BIC ISO - возвращаем "" */
      end;
    end;
  end;

  if (RsPaym.ReceiverAccount != "")
     field_value = "/" + RsPaym.ReceiverAccount + SYMB_ENDL;
  end;

  field_value = String(field_value, ReceiverCode);
      
  return field_value;
end;

macro FillBeneficiaryCustomerField( RsPaym, Standart )
  var field_value, ReceiverID, BankCode, Error, field_Client, field_place, INN, KPP="", rest = "", LenghtControl:bool, numstr = 5;
  record RecordAdress ( adress );

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;

  field_value = "";  
  ReceiverID = RsPaym.Receiver;  

  if ((Standart==ST_RUR5) OR (Standart==ST_RUR6))
     BankCode = ПолучитьКодСубъекта( ReceiverID, PTCK_BIC, Error );

     if ( (not Error) AND (ReceiverID) AND (Standart==ST_RUR5))
        field_value = string( "//RU", BankCode );
        if( RsPaym.ReceiverAccount != "" )
          field_value = string( field_value, ".", RsPaym.ReceiverAccount );
        end;
        field_value = field_value + SYMB_ENDL;
        numstr = numstr - 1;
     else
       if( wlpmpaym.ReceiverAccount != "" )
          field_value = SYMB_SLASH + RsPaym.ReceiverAccount + SYMB_ENDL;
          numstr = numstr - 1;
       end;
     end;
  else
     if( RsPaym.ReceiverAccount != "" )
        field_value = SYMB_SLASH + RsPaym.ReceiverAccount + SYMB_ENDL;
        numstr = numstr - 1;
     end;
  end;

  if ( RsPaym.ReceiverINN!="" )
     INN = RemoveKPP( RsPaym.ReceiverINN );
     if (Standart == ST_RUR6)
       KPP = RemoveINN(RsPaym.ReceiverINN);
     end;
  elif ( ReceiverID )
     INN = RemoveKPP( GetPartyINN( ReceiverID ) );
     if (Standart == ST_RUR6)
       KPP = RemoveINN(GetPartyINN(ReceiverID));
     end;
  else
     INN = "";
     KPP = "";
  end;

  if( INN != "" )
    field_value = String( field_value, "INN", INN );
    if (KPP != "")
      field_value = String(field_value, ".KPP", KPP);
    end;
    field_value = String(field_value, SYMB_ENDL);
    numstr = numstr - 1;
  end;
   
  StrChangeRusXSet( RsPaym.ReceiverName, field_Client, Standart );

  ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
  if( ReceiverID AND НайтиЮридическийАдресСубъекта(ReceiverID,RecordAdress) )
     if ( RecordAdress.Adress!="" )
       StrChangeRusXSet( RecordAdress.Adress, field_place, Standart );
       field_Client = String( field_Client, " ", field_place );
     end;
     if( RecordAdress.Place != "" )
       StrChangeRusXSet( RecordAdress.Place, field_place, Standart );
       field_Client = String( field_Client, SYMB_ENDL, field_place );
     end;
  end;  

  if ( Standart==ST_RUR4 )
     StrMultyToFormat( field_Client, field_Client, 35, numstr, "CheckBreakRUR4" );
  else
    field_Client = SwiftStrSplit( field_Client, 35, numstr, true, @rest );
    /*StrMultyToFormat( field_value, field_value, 35, 5 );*/
  end;

  field_value = field_value + field_Client;

  GetRegistryValue( ПутьНастройкиКонтрольДлины, V_BOOL, LenghtControl, error );
  if ((error == WLD_REG_OK) and LenghtControl and (rest!="") )
    RunError("|Слишком длинная строка в поле 59");
  end;

  return field_value;
end;

macro FillBeneficiaryCustomerField_0(Name : string, Account : string) : string
  var field_value = "";

  if( Account )
    field_value = SYMB_SLASH + Account + SYMB_ENDL;
  end;

  StrChangeRusXSet( Name, Name );
  var rest = "", error, LengthControl:bool = false;
  Name = SwiftStrSplit( Name, 35, 4, true, @rest );
  GetRegistryValue( ПутьНастройкиКонтрольДлины, V_BOOL, LengthControl, error );
  if( (error == WLD_REG_OK) and LengthControl and (rest!="") )
    RunError("|Слишком длинная строка в поле 59");
  end;

  return field_value + Name;
end;

/* Проверяет на наличие такого кода среди разрешенных */
macro IsAllowCode( AllowCodes, code )
    var str = AllowCodes, curCode, i;
    while( str!="" )
        i = Index(str, SYMB_BLANK);
        if ( i )
           if ( i>1 )
              curCode = substr( str, 1, i-1 );
           else
              curCode = "";
           end;
           str = substr( str, i+1 );
        else
           curCode = str;
           str = "";
        end;        
        if ( code==curCode )
           return true;
        end;
    end;

    return false;
end;

macro ДобавитьБлокВПоле70( field_value, CurrentCode, field70, standart )
    var len, i, ch, prevCh;

    StrChangeRusXSet( field70, field70, standart );    

    if ( field_value!="" )
       field_value = field_value + SYMB_DSLASH;
    end;

    field_value = field_value + SYMB_SLASH + CurrentCode + SYMB_SLASH;

    if ( CurrentCode==Field70CodesRFB )
       field70 = substr( field70, 1, 16 );
    end;

    len = strlen( field70 );

    i = 1;
    prevCh = "";
    while( i<=len )
       ch = substr( field70, i, 1 );
       if ( (ch!=SYMB_BLANK) AND (ch!=SYMB_ENDL) )
          field_value = field_value + ch;
       elif ( (prevCh!=SYMB_BLANK) AND (prevCh!=SYMB_ENDL) AND (prevCh!="") )
          field_value = field_value + SYMB_DSLASH;
       end;
       prevCh = ch;
       i = i + 1;
    end;

    return field_value;
end;

/* Заполнить поле 70 */
macro FillDetailsOfPaymentField( RsPaym, AllowCodes, ОстатокПоля, Standart, passVOCode )
  var field_value, rest:string, PaymentDetails, field70, i, ch;

  if( valtype(Standart)==V_UNDEF )
    Standart = ST_UNDEF;
  end;
  StrChangeRusXSet( RsPaym.Ground, PaymentDetails, Standart, null, passVOCode );

  i = 1;
  while( i <= strlen(PaymentDetails) )
    ch = SubStr( PaymentDetails, i, 1 );
    if( ch == SYMB_ENDL )
      PaymentDetails = SubStr( PaymentDetails, 1, i-1 ) + " " + SubStr( PaymentDetails, i+1 );
    end;
    i = i + 1;
  end;

  field_value = PaymentDetails;
  if( Standart == ST_RUR4 )
    rest = StrMultyToFormat( field_value, field_value, 35, 4, "CheckBreakRUR4" );
  else
    field_value = SwiftStrSplit( field_value, 35, 4, true, @rest );
    /*rest = StrMultyToFormat( field_value, field_value, 35, 4 );*/
  end;

  ОстатокПоля = rest;
  setparm( 2, ОстатокПоля );

  return field_value;
end;

macro GetField70_SW103RUR6( RsPaym : RsbPayment, ОстатокПоля : @string) : string
  return FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_RUR6, ОстатокПоля, ST_RUR6, true/*передавать код ВО*/ );
end;

macro ПолучитьИмяФормы( PaymentID, TpID, FormName )
  var Direct;
  FILE   wldfrm( wlmesfrm ) key 0;
  FILE   wldrls( wlmesrls ) key 0;
  RECORD wldmes( wlmes  );

  if( not PaymentID )
    return FALSE;
  end;

  var paym = RsbPayment( PaymentID );
  
  if( not paym.IsExternal )
    return FALSE;
  end;

  if( paym.IsExternalIncoming )
    Direct = 1;  /* Входящее */
  else
    Direct = 0;  /* Исходящее */
  end;

  if( not ПолучитьСообщение( PaymentID, OBJTYPE_PAYMENT, Direct, wldmes, NULL, WLD_SUBKIND_PAYM_CLN ) )
    return FALSE;
  end;

  wldrls.RlsFormID = wldmes.RlsFormID;
  if(not GetEQ(wldrls))
    return FALSE;
  end;

  wldfrm.FormID = wldrls.FormID;
  if(not GetEQ(wldfrm))
    return FALSE;
  end;

  SetParm( 1, wldfrm.TpID );
  SetParm( 2, wldfrm.Name );

  return TRUE;
end;

// Запрлнить подполе "Transaction Type Identification Code" поля 61 "Statement Line"
macro FillTypeOperField( PaymentID, FormName ) : string
  var TypeOper : string = StatLine_TypeOperField_SWIFTTag;

  // То, что PaymentID задан (проводка связана с платежом) и платеж внешний, 
  // проверяется перед вызовом текущей функции в macro ПолучитьИмяФормы
  if( ПолучитьУзелОсновнойИнструкцииПлатежа(PaymentID, RTDIR_OUT) or
      ПолучитьУзелОсновнойИнструкцииПлатежа(PaymentID, RTDIR_IN)
    )
    TypeOper = TypeOper + CoverMessage;
  else
    TypeOper = TypeOper + FormName;
  end;

  return TypeOper;
end;

macro GetSWIFTDKFlagPaymProp( RsPaym )
   var DKFlag;   
  if( RsPaym.PayerIsSender == "X" )
         DKFlag = "C" ;
      else
         DKFlag = "D" ;
      end;
   return DKFlag;
end;

macro GetSWIFTDKFlagPayment( RsPaym )
   return GetSWIFTDKFlagPaymProp(RsPaym);
end;

/* Проверка, что банк-получатель - Тербанк СБ РФ */
MACRO IsBankUSB(BankID)

  if (IsBankType(BankID, PT_KIND_SAVINGS_BANK) == true)
    return true;
  end;
  
  return false;

END;

macro ПолучитьИмяБанка( PartyID )
   var Name;
   file party( party ) key 0;
   record PartyAdress ( adress );

   if ( PartyID )
      party.PartyID = PartyID;
   else 
      party.PartyID = {OurBank};
   end;

   if ( getEQ(party) )
      name = party.ShortName;
      ClearRecord(PartyAdress);
      НайтиЮридическийАдресСубъекта(party.PartyID,PartyAdress);
      if (PartyAdress.Place)
         name = name + SYMB_ENDL + PartyAdress.CodePlace + "." + PartyAdress.Place;
      end;
      return name; 
   end;
   return "";
end;

private macro GetFieldValue52D(BankID, RsPaym, Standart)
  var Tag = SWIFT_Tag_D; 
  var field_value:string = "", BIC_CB = "", tempv;
  var DKFlag;
  var INN = "", KPP = "", INNKPP = "";
  if (BankID != RsPaym.Payer)
    ClearRecord(Route);
    Route.Sort = -1;
    Route.OutAccount = RsPaym.PayerAccount;
    Route.PartyID = BankID;

    DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
    Route.InAccount = "";
    field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
  else
    f_wldprt.PartyID = RsPaym.PayerBankID;
    var CorrAcc = "";
    if (GetEQ(f_wldprt))
      CorrAcc = f_wldprt.CorAcc;
    end;
    if ((RsPaym.PayerBankCorrAcc == "") or (RsPaym.PayerBankCorrAcc == CorrAcc))
      BIC_CB = ПолучитьКодСубъекта(RsPaym.PayerBankID, PTCK_BIC);
      field_value = string("//RU", BIC_CB, ".", CorrAcc, SYMB_ENDL);
    else
      INNKPP = GetPartyINN( RsPaym.PayerBankID, 1 );
      INN = RemoveKPP(INNKPP);
      KPP = RemoveINN(INNKPP);
      if(INN!="")
        INN = "INN" + INN;
      end;
      if(KPP!="")
        KPP = "." + "KPP" + KPP;
      end;
      field_value = string("/", RsPaym.PayerBankCorrAcc, SYMB_ENDL, INN, KPP, SYMB_ENDL);
    end;
    StrChangeRusXSet( ПолучитьИмяБанка(RsPaym.PayerBankID), tempv, standart );
    tempv = SwiftStrSplit( tempv, 35, 4 );
      
    field_value = field_value + tempv;
  end;
  return field_value;
end;

/* Заполняет поле 52 (для платежа) */
macro FillOrderingInstitutionField(RsPaym, Tag, standart, form)
   var field_value = "", Name, DKFlag, error, SideIdent="", SideIdent_Tmp="";
   var BankID = 0;

   RECORD snd( pmroute );
   record RecordAdress ( adress );
   var CorrAcc = "";
   /*Если направление платежа в Сбербанк России заполняется только по опции D */
   if ( (Tag == "") and ((IsBankUSB(RsPaym.ReceiverBankID)) or ((standart == ST_RUR6) and (form == "202RUR6"))))
     Tag = SWIFT_Tag_D;
   else 
     Tag = "";
   end;

   // может ли плательщик являться банком
   var CanPayerBeBank : bool = ( substr(form, 1, 3) == "202" );

   if (CanPayerBeBank)
     if(IsPmCoverMode() == false)
       if(ВидСубъекта(RsPaym.Payer, PTK_BANK) and (Bnk_GetDeptPartyID(wlmes.Department) != RsPaym.Payer))
         BankID = RsPaym.Payer;
       end;
     elif(IsPmCoverMode() == true)
       if(Bnk_GetDeptPartyID(wlmes.Department) != RsPaym.PayerBankID)
         BankID = RsPaym.PayerBankID;
       end;
     end;
   else
     BankID = RsPaym.PayerBankID;
   end;
        
   if (not CanPayerBeBank)
     if (substr(form, 1, 3) == "103") // форма 103
       if((Bnk_GetDeptPartyID(wlmes.Department) != RsPaym.PayerBankID))
         if (not ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_IN, Route))
           ClearRecord(Route);
           Route.Sort = -1;
           Route.OutAccount = RsPaym.PayerCorrAccNostro;
           Route.PartyID = BankID;
           Route.Name = RsPaym.PayerBankName;
           Route.CodeKind = RsPaym.PayerBankCodeKind;
           Route.CodeName = RsPaym.PayerBankCode;
         end;
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         Route.InAccount = "";
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("52");
         end;       
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
       end;
     else
       if( (standart != ST_RUR6) AND (standart != ST_RUR5) AND ПолучитьУзелОтправителяПлатежа(RsPaym.PaymentID, snd) AND 
         ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_IN, Route) AND 
         (snd.RouteID!=Route.RouteID))
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         Route.InAccount = "";
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("52");
         end;       
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
       else
         field_value = GetFieldValue52D(BankID, RsPaym, Standart);
       end;
     end;
   else
     if(BankID != 0)  
       if ((standart != ST_RUR6) AND (standart != ST_RUR5))
         ClearRecord(Route);
         Route.Sort = -1;
         Route.OutAccount = RsPaym.PayerAccount;
         Route.PartyID = BankID;

         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         Route.InAccount = "";
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("52");
         end;       
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
       else
         field_value = GetFieldValue52D(BankID, RsPaym, standart);
       end;
     end;
   end;

   if ( Tag=="" )
      Tag = SWIFT_Tag_A;
   end;
   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 52 (для требования) */
macro FillPayerBankField(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, BIC_CB="", SideIdent="", SideIdent_Tmp="";

   RECORD snd( pmroute );
   record RecordAdress ( adress );

   /*Если направление платежа в Сбербанк России заполняется только по опции D */
   if ( IsBankUSB(RsPaym.PayerBankID) )
       Tag = SWIFT_Tag_D;
   else 
       Tag = "";
   end;

   if ( Tag=="" )
      tempv = ПолучитьКодСубъекта( RsPaym.PayerBankID, PTCK_SWIFT, error);            
      if ( error )
        Tag = SWIFT_Tag_D;
      else
        Tag = SWIFT_Tag_A;
      end;
   end;      

   if ( Tag==SWIFT_Tag_A )         
      field_value = field_value + tempv;
   else
      if (standart == ST_RUR6)
         BIC_CB = ПолучитьКодСубъекта(RsPaym.PayerBankID, PTCK_BIC, error);
         SideIdent = "";
         if (not error)
            f_wldprt.PartyID = RsPaym.PayerBankID;
            if (GetEQ(f_wldprt))
               SideIdent = string(SideIdent, "//RU", BIC_CB, ".", f_wldprt.CorAcc, SYMB_ENDL);
            end;
         end;
      end;
      if ( RsPaym.PaymentID )
         Name = RsPaym.PayerBankName;
         if( НайтиЮридическийАдресСубъекта(RsPaym.PayerBankID,RecordAdress) AND
           (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
           Name = string( Name, SYMB_ENDL, trim(RecordAdress.Place) );
         end;
      else
         Name = ПолучитьИмяБанка(RsPaym.PayerBankID);
      end;

      if( Name != "" )      
         StrChangeRusXSet( Name, tempv, standart );
         if ( (valtype(Standart)!=V_UNDEF) AND (Standart==ST_RUR4) )
          StrMultyToFormat( tempv, tempv, 35, 5, "CheckBreakRUR4" );
         else
          StrMultyToFormat( tempv, tempv, 35, 5 );
         end;
         field_value = field_value + SideIdent + tempv;
      end;
   end;

   if ( Tag=="" )
      Tag = SWIFT_Tag_A;
   end;
   setparm( 1, Tag );

   return field_value;

end;

/* Заполняет поле 53 (для платежа) */
macro FillSenderCorrespondentField(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, OurCorrAcc="", oldkey;
   var FIID, Corschem;
   RECORD rcv(pmroute);
   var rs:object;
   var select:string;
   var params:TArray;

   
   oldkey = KeyNum( f_wlcors, 1 );
   f_wlcors.Number  = RsPaym.OutCorschem;
   f_wlcors.FI_Kind = 1; 
   f_wlcors.FIID    = RsPaym.OutCorschemFIID;
   getEQ(f_wlcors);
   KeyNum( f_wlcors, oldKey );

   /*есть другая корсхема с этим же корреспондентом и в такой же валюте*/  
   select = " select *"+
              " from dcorschem_dbt cors"+
             " where cors.t_Number <> :CorNumber"+
               " and cors.t_FIID = :FIID"+
               " and cors.t_CorrID = :CorrID"+
               " and cors.t_Department = :Department"+
               " and cors.t_State = 0";

   params = makeArray( SQLParam("CorNumber", f_wlcors.Number),
                       SQLParam("FIID", f_wlcors.FIID),
                       SQLParam("CorrID", f_wlcors.CorrID),
                       SQLParam("Department", f_wlcors.Department));
   rs = execSQLselect( select, params, FALSE );

   Tag = "";

   ClearRecord(Route);       
   Route.PartyID    = f_wlcors.CorrID;
   Route.OutAccount = Route.InAccount = IfThenElse(f_wlcors.IsNostro == "X",
                                                   f_wlcors.CorAccount,
                                                   f_wlcors.Account);
   Route.Sort = -1; 
   Route.CodeValue = ПолучитьКодСубъекта( Route.PartyID, PTCK_SWIFT, error );
   
   if ( rs.moveNext() or (RsPaym.BaseFIID != RsPaym.OutCorschemFIID))      
       Tag = SWIFT_Tag_B;
   elif(f_wlcors.Department != wlmes.Department)
       ClearRecord( f_wlmesrls );
       f_wlmesrls.RlsFormID = wlmes.RlsFormID;
       GetEQ(f_wlmesrls);

       /*если RUR*/
       if((standart==ST_RUR4) OR (standart==ST_RUR5) OR (standart==ST_RUR6))
         Tag = SWIFT_Tag_B;
       /*если субъект филиала исходящей схемы расчетов имеет свой код вида 6*/
       elif(not error)
         Tag = SWIFT_Tag_A;
         Route.CodeKind = PTCK_SWIFT;
       /*если название релиза подходит под маску "102*", то используется опция C*/
       elif( substr(f_wlmesrls.Name,1,3) == "102")
         Tag = SWIFT_Tag_C;
       /*Иначе, если название релиза подходит под маску "103, 200, 202"*/
       elif((f_wlmesrls.Name == "103") or (f_wlmesrls.Name == "200") or (f_wlmesrls.Name == "202") or (f_wlmesrls.Name == "202COV"))
         Tag = SWIFT_Tag_B;         
       end;            
   end;

   if(Tag != "")
     DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
     field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );   
   end;
   
   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 54 (для платежа) */
macro FillReceiverCorrespondentField(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error;
   RECORD rcv(pmroute);
   RECORD ocb(pmroute);

   if ( valtype(Tag)==V_UNDEF )
       Tag = SWIFT_Tag_A; /* По умолчанию считаем - A */
   end;

   if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, ocb) AND
       (rcv.RouteID!=ocb.RouteID) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
       (rcv.RouteID!=Route.RouteID) )
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       if(Tag == "")
         Tag =  GetTagsForFldAndRls("54");
       end;       
       field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );   
   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 55 (для платежа) */
macro FillThirdReibursBankField(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error;
   RECORD rcv(pmroute);
   RECORD ocb(pmroute);
   RECORD irb(pmroute);

   if ( valtype(Tag)==V_UNDEF )
       Tag = SWIFT_Tag_A; /* По умолчанию считаем - A */
   end;

   if( ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
       ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
       ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       if(Tag == "")
         Tag =  GetTagsForFldAndRls("55");
       end;       
       field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );   
   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 56 (для платежа) */
macro FillIntermediaryField(RsPaym, Tag, standart, form)
   var field_value = "", Name, tempv, DKFlag, error, coresp, IsEq;
   var CorrCodeKind, CorrCode, CorrName, CorrAcc, CorrID;
   RECORD rcv(pmroute);
   RECORD acc(pmroute);
   RECORD route_1(pmroute);
   
   if(valtype(form)==V_UNDEF)
      form = ""; 
   end;

   if ( RsPaym.ReceiverIsSender!="X") 
      CorrCodeKind  = RsPaym.ReceiverBankCorrCodeKind;
      CorrCode      = RsPaym.ReceiverBankCorrCode;  
      CorrName      = RsPaym.ReceiverBankCorrName;
      CorrAcc       = RsPaym.ReceiverBankCorrAcc;
      CorrID        = RsPaym.ReceiverBankCorrID;      
   else
      CorrCodeKind  = RsPaym.PayerBankCorrCodeKind;
      CorrCode      = RsPaym.PayerBankCorrCode;  
      CorrName      = RsPaym.PayerBankCorrName;
      CorrAcc       = RsPaym.PayerBankCorrAcc;
      CorrID        = RsPaym.PayerBankCorrID;
   end;

   if ( valtype(Tag)==V_UNDEF )
       Tag = "";
   elif(form == "MT103+")
       Tag = SWIFT_Tag_A;
   end;

   if(form == "MT202COV")
     if(ПолучитьУзелОсновнойИнструкцииПлатежа(RsPaym.PaymentID, RTDIR_OUT, acc) AND
        (wlmes.OutsideAbonentID != acc.PartyID) AND
        ПолучитьПредыдущийУзел(OBJTYPE_PAYMENT, RsPaym.PaymentID, Route_1) AND
        (wlmes.OutsideAbonentID != Route_1.PartyID) AND
        ПолучитьПредыдущийУзел(OBJTYPE_PAYMENT, RsPaym.PaymentID, Route) AND
        (wlmes.OutsideAbonentID != Route.PartyID)
       )
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);      
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("56");
         end;
         Route.OutAccount = Route.InAccount = CorrAcc;          
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart ); 
     end;
   else
     
     if( ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, acc) AND
         (wlmes.OutsideAbonentID != acc.PartyID) AND
         ПолучитьПредыдущийУзел(OBJTYPE_PAYMENT, RsPaym.PaymentID, Route) AND 
         (wlmes.OutsideAbonentID != Route.PartyID)        
       )
          DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);      
          if(Tag == "")
            Tag = GetTagsForFldAndRls("56");
          end;
          Route.OutAccount = Route.InAccount = CorrAcc;          
          field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );      
     elif( CorrID > 0 )
       ClearRecord(Route);
       Route.CodeKind   = CorrCodeKind;
       Route.CodeValue  = CorrCode;
       Route.Name       = CorrName;
       Route.PartyID    = CorrID;
       Route.OutAccount = Route.InAccount = CorrAcc;          
       Route.Sort = -1;
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       if(Tag == "")
         Tag =  GetTagsForFldAndRls("56");
       end;
       field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
     end;
   
   /*
   elif(form == "MT910") Для MT910 заполенение реализовано в маросе генерации
     if( CorrID > 0 )
       ClearRecord(Route);
       Route.CodeKind   = CorrCodeKind;
       Route.CodeValue  = CorrCode;
       Route.Name       = CorrName;
       Route.PartyID    = CorrID;
       Route.OutAccount = Route.InAccount = CorrAcc;          
       Route.Sort = -1;
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       if(Tag == "")
         Tag =  GetTagsForFldAndRls("56");
       end;
       field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
     end;*/

   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 57 (для платежа) */
macro FillAccountWithInstitutionField(RsPaym, Tag, standart, form)
   var field_value = "", Name, tempv, DKFlag, error, coresp, IsEq, BankCode, INN, oldkey,CorrAcc;
   var corSchem, payFIID, BankCodeT;
   var CorrCodeKind, CorrCode, CorrName, CorrID;

   RECORD prop( pmprop );
   RECORD rcv(pmroute);
   RECORD acc(pmroute);
   record RecordAdress ( adress );

   if ( RsPaym.ReceiverIsSender!="X") 
      corSchem      = RsPaym.OutCorschem;
      payFIID       = RsPaym.ReceiverFIID;
      BankCodeT     = RsPaym.ReceiverBankCodeKind;
      CorrAcc       = RsPaym.ReceiverCorrAccNostro;
      CorrCodeKind  = RsPaym.ReceiverBankCorrCodeKind;
      CorrCode      = RsPaym.ReceiverBankCorrCode;  
      CorrName      = RsPaym.ReceiverBankCorrName;
      CorrID        = RsPaym.ReceiverBankCorrID;  
      /*Chesnokov D.S. 21.02.2019 поправил коррсчет в 57A*/
      CorrAcc       = GetCorAcc(RsPaym.ReceiverFIID, RsPaym.OutCorschem, V_UNDEF, null)
   else
      corSchem      = RsPaym.InCorschem;
      payFIID       = RsPaym.PayerFIID;
      BankCodeT     = RsPaym.PayerBankCodeKind;
      CorrAcc       = RsPaym.PayerCorrAccNostro;
      CorrCodeKind  = RsPaym.PayerBankCorrCodeKind;
      CorrCode      = RsPaym.PayerBankCorrCode;  
      CorrName      = RsPaym.PayerBankCorrName;
      CorrID        = RsPaym.PayerBankCorrID;
   end;

   if( valtype(form) == V_UNDEF)
     form = "";
   end;

   if( valtype(Tag) == V_UNDEF )
     Tag = "";
   end;

   /*if ( IsBankUSB(RsPaym.ReceiverBankID)==true )
     /* При передаче в Сбербанк России используется форма D */
     Tag = SWIFT_Tag_D;
   end;*/     

   if((form == "MT103") or (form == "MT202"))
     if( ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, acc) AND
         (acc.PartyID != wlmes.OutsideAbonentID) )
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("57");
         end;       
         acc.OutAccount = acc.InAccount = CorrAcc;
         field_value = СформироватьЗаписьМаршрута( acc, Tag, DKFlag, standart );
     end;
       
   elif(form == "MT202COV")
     if( ПолучитьУзелОсновнойИнструкцииПлатежа(RsPaym.PaymentID, RTDIR_OUT, acc) AND 
         (acc.PartyID != wlmes.OutsideAbonentID) AND
         ПолучитьПредыдущийУзел(OBJTYPE_PAYMENT, RsPaym.PaymentID, Route) AND
         (Route.PartyID != wlmes.OutsideAbonentID)
       )
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("57");
         end;       
         Route.OutAccount = Route.InAccount = CorrAcc;
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
     else
       if((ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, Route) == true) AND 
           (Route.PartyID != wlmes.OutsideAbonentID) AND (CorrID > 0)            
         )
         ClearRecord(Route);
         Route.CodeKind   = CorrCodeKind;
         Route.CodeValue  = CorrCode;
         Route.Name       = CorrName;
         Route.PartyID    = CorrID;
         Route.OutAccount = Route.InAccount = CorrAcc;          
         Route.Sort = -1;
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("57");
         end;
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
       end;          
     end;
   
   elif( not ПолучитьУзелОсновнойИнструкцииПлатежа(RsPaym.PaymentID, RTDIR_OUT) and
          ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, Route) )
         /* Проверим, не совпадает ли он с корреспондетом */         
         oldkey = KeyNum( f_wlcors, 1 );
         f_wlcors.Number  = corSchem;
         f_wlcors.FI_Kind = 1; /* валюта */
         f_wlcors.FIID    = payFIID;
         if( (getEQ(f_wlcors) AND (f_wlcors.CorrID!=Route.PartyID)))
            DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
            if ( Tag=="" )
             Tag =  GetTagsForFldAndRls("57");
            end;
            Route.OutAccount = Route.InAccount = CorrAcc;
            field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
         end;
         KeyNum( f_wlcors, oldKey );
    end;

   if ( Tag=="" )
      /* Если Tag так и не определился (field_value=""), то заполняем его силой, что бы потом крика не было */
      Tag = SWIFT_Tag_A;
   end; 

   setparm( 1, Tag );

   return field_value;
end;

macro FillAccountWithInstitutionField_n91(RsPaym, Tag, standart)
  var field_value = "", Name, error;
  var NeedAcc, account = "", ID, INN, Code, tempv;
  Tag = "";
  
  f_wlcors.Number  = RsPaym.OutCorschem;
  f_wlcors.FI_Kind = 1; /* валюта */
  f_wlcors.FIID    = RsPaym.ReceiverFIID;
  
  if( getEQ(f_wlcors) )
    if(f_wlcors.CorrID == RsPaym.PayerBankID)
      NeedAcc = false;
      ID      = RsPaym.ReceiverBankID;
    else
      NeedAcc = true;
      ID      = f_wlcors.CorrID;
    end;
    
    Code = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error );
    account = f_wlcors.CorAccount;

    if (not error)
       if ( NeedAcc )
          field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
       end;
       field_value = field_value + Code;
       Tag = SWIFT_Tag_A;
    else
       
       if ( NeedAcc )
          field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
       end;
       Tag = SWIFT_Tag_D;
       Code = ПолучитьКодСубъекта( ID, PTCK_BIC, error );

       if ( (not error) AND ((standart==ST_RUR4) OR (standart==ST_RUR5) OR (standart==ST_RUR6)) )
          field_value = string( "//RU", Code );                  
          field_value = field_value + SYMB_ENDL;
       end;

       INN = GetPartyINN( ID );
       if( INN != "" )
         field_value = String( field_value, "INN", INN, SYMB_ENDL );
       end;
               
       Name = ПолучитьИмяБанка(ID);
               
       if( ID AND НайтиЮридическийАдресСубъекта(ID,wladdress) AND
           (wladdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(wladdress.Place)))==0) )
         Name = string( Name, SYMB_ENDL, trim(wladdress.Place) );
       end;

       if( Name != "" )      
         StrChangeRusXSet( Name, tempv, standart );
         if ( (valtype(Standart)!=V_UNDEF) AND (Standart==ST_RUR4) )
            StrMultyToFormat( tempv, tempv, 35, 5, "CheckBreakRUR4" );
         else
            StrMultyToFormat( tempv, tempv, 35, 5 );
         end;
         field_value = field_value + tempv;
       end;
    end;
  end;

  setparm( 1, Tag );
  return field_value;
end;
/* Проверка на платеж с порытием */
macro CheckCoverPayment( RsPaym )
  var rs:object;
  var select:string;
  var params:TArray;

  /* Проверяем, привязано ли к платежу более одного сообщения с абсолютно одинаковыми */
  /* wlmes.OutsideAbonentID, wlmes.InsideAbonentID, wlmes.AgentID */
  select = "select count(*) from dwlmeslnk_dbt wlmeslnk, dwlmes_dbt wlmes, dwlpm_dbt wlpm"+
                            " where wlpm.t_PaymentID = :PaymentID"+
                            " AND wlmeslnk.t_ObjKind = :OBJTYPE_PAYMENT"+
                            " AND wlmeslnk.t_ObjID   = wlpm.t_WlPmID"+
                            " AND wlmeslnk.t_Direct  = chr(0)"+
                            " AND wlmeslnk.t_MesID   = wlmes.t_MesID"+
                            " AND wlpm.t_wlpmnum     = 0"+
                            " AND wlmes.t_state      <> 7 "+/*WLD_STATUS_MES_INVALID*/
                            " group by wlpm.t_PaymentID"+
                            " having count(*) > 1";
  params = makeArray( SQLParam("PaymentID",RsPaym.PaymentID),
                      SQLParam("OBJTYPE_PAYMENT",OBJTYPE_PAYMENT ));
  rs = execSQLselect( select, params, FALSE );
  if( rs.MoveNext() )
    return true;    
  else
    return false;
  end;
end;

/* Заполняет поле 58 (для платежа) */
macro FillBeneficiaryInstitutionField(RsPaym, Tag, standart, form)
   var field_value = "", Name, tempv, DKFlag, error, coresp, IsEq, BankCode, INN, BenPartyID, BenAccount,FindBic;
   var BenCodeKind, BenBankCode, BenINN, BenName;   
   record RecordAdress ( adress );

   if ( RsPaym.ReceiverIsSender!="X") 
      BenCodeKind  = RsPaym.ReceiverCodeKind;
      BenBankCode  = RsPaym.ReceiverCode;
      BenAccount   = RsPaym.ReceiverAccount;
      BenPartyID   = RsPaym.Receiver;
      BenINN       = RsPaym.ReceiverINN;
      BenName      = RsPaym.ReceiverName;
   else
      BenCodeKind  = RsPaym.PayerCodeKind;
      BenBankCode  = RsPaym.PayerCode;
      BenAccount   = RsPaym.PayerAccount;
      BenPartyID   = RsPaym.Payer;
      BenINN       = RsPaym.PayerINN;
      BenName      = RsPaym.PayerName;
   end;

   if( valtype(form) == V_UNDEF)
     form = "";
   end;

   if ( valtype(Tag)==V_UNDEF )
       Tag = "";
   end;

   if ( form == "MT202")      
      /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */      
      if ( Tag=="" )
         if((BenBankCode!="") AND (BenCodeKind==PTCK_SWIFT))
            Tag = SWIFT_Tag_A;
         else
           GetRegistryValue( ПутьНастройкиИскатьSWIFTКод, V_BOOL, FindBic, error );
           if ((error == WLD_REG_OK) and FindBic) 
             ПолучитьКодСубъекта( BenPartyID, PTCK_SWIFT, error );
             if(not error)
               Tag = SWIFT_Tag_A;
             end;
           end;
         end;       
         if(Tag=="")
            Tag = SWIFT_Tag_D;
         end;
      end;      

      if ( Tag==SWIFT_Tag_A )            
        if( BenAccount != "" )
          field_value = field_value + SYMB_SLASH + BenAccount + SYMB_ENDL;
        elif((BenCodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_A, substr(BenBankCode,1,2))))
          field_value = SYMB_DSLASH + BenBankCode + SYMB_ENDL;
        elif((BenCodeKind == PTCK_ABA) and (BenBankCode == ""))
          field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + SYMB_ENDL;
         end;

        if(BenCodeKind==PTCK_SWIFT)
          field_value = field_value + BenBankCode;
        else
          GetRegistryValue( ПутьНастройкиИскатьSWIFTКод, V_BOOL, FindBic, error );
          if ((error == WLD_REG_OK) and FindBic) 
            BankCode = ПолучитьКодСубъекта( BenPartyID, PTCK_SWIFT, error );
            field_value = field_value + BankCode;
          end;
        end;
      elif ( Tag==SWIFT_Tag_D )            

         if ( (standart==ST_RUR4) OR (standart==ST_RUR5) OR (standart==ST_RUR6) )   
            if ( BenAccount!="" )
              if( BenCodeKind==PTCK_BIC )
                field_value = string( field_value, SYMB_DSLASH , CODEWORLD_BIC, BenBankCode );
                field_value = string( field_value, ".", BenAccount );
              else
                 field_value = field_value + SYMB_SLASH + BenAccount;
              end;
              field_value = field_value + SYMB_ENDL;
            elif (BenCodeKind == PTCK_ABA)
              field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + BenBankCode + SYMB_ENDL;
            elif(BenCodeKind == PTCK_CHIPS)
              field_value = SYMB_DSLASH + CODEWORLD_CHIPS + BenBankCode + SYMB_ENDL;
            elif(BenCodeKind == PTCK_BIC) 
              field_value = SYMB_DSLASH + CODEWORLD_BIC + BenBankCode + SYMB_ENDL;
            elif((BenCodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_D, substr(BenBankCode,1,2))))
              field_value = SYMB_DSLASH + BenBankCode + SYMB_ENDL;
            end;
         else
           if( BenAccount != "")
            field_value = field_value + SYMB_SLASH + BenAccount + SYMB_ENDL;
           elif (BenCodeKind == PTCK_ABA)
             field_value = SYMB_DSLASH + CODEWORLD_FEDWIRE + BenBankCode + SYMB_ENDL;
           elif(BenCodeKind == PTCK_CHIPS)
             field_value = SYMB_DSLASH + CODEWORLD_CHIPS + BenBankCode + SYMB_ENDL;
           elif(BenCodeKind == PTCK_BIC) 
             field_value = SYMB_DSLASH + CODEWORLD_BIC + BenBankCode + SYMB_ENDL;
           elif((BenCodeKind == PTCK_NCSC) and (index(SWIFT_NCSC_TAG_D, substr(BenBankCode,1,2))))
             field_value = SYMB_DSLASH + BenBankCode + SYMB_ENDL;
           end;
         end;

         if ( (standart==ST_RUR4) OR (standart==ST_RUR5) OR (standart==ST_RUR6) )   
           INN = GetPartyINN( BenPartyID );
         if( INN != "" )
            if(RemoveINN(INN))
               field_value = String( field_value, "INN", RemoveKPP(INN), 
                                                 ".KPP", RemoveINN(INN), SYMB_ENDL );
         else
               field_value = String( field_value, "INN", RemoveKPP(INN), SYMB_ENDL );
            end;
         else
              if(RemoveINN(BenINN))
                 field_value = String( field_value, "INN", RemoveKPP(BenINN), 
                                                   ".KPP", RemoveINN(BenINN), SYMB_ENDL );
              elif( BenINN != "" )
                 field_value = String( field_value, "INN", RemoveKPP(BenINN), SYMB_ENDL );
              end;
            end;
         end;

         Name = GetNameBank_Tag_D(Tag, BenName, BenPartyID);
         if( Name != "" )
           if( (BenPartyID) AND НайтиЮридическийАдресСубъекта(BenPartyID,RecordAdress) AND
               (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
               Name = string( Name, SYMB_ENDL, trim(RecordAdress.Place) );
           end;
           StrChangeRusXSet( Name, tempv, standart );
           if ( (valtype(Standart)!=V_UNDEF) AND (Standart==ST_RUR4) )
              StrMultyToFormat( tempv, tempv, 35, 5, "CheckBreakRUR4" );
           else
              tempv = SwiftStrSplit( tempv, 35, 4 );
           end;
           field_value = field_value + tempv;
         end;
      end;
   elif( form == "MT202COV")
      /* Заполнеие поля 58 для покрытия */
      if( ПолучитьУзелОсновнойИнструкцииПлатежа( RsPaym.PaymentID, RTDIR_OUT, Route) )
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         if(Tag == "")
           Tag =  GetTagsForFldAndRls("58");
         end;       
         field_value = СформироватьЗаписьМаршрута( Route, Tag, DKFlag, standart );
      end;
   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 58 (для аккредитива) */
macro FillBeneficiaryInstitutionField_LC(LcObj : RsbLetterOfCredit, Tag : string): string
  var field_value = "", needFill = true;

  var Lcparty : TRecHandler = TRecHandler("lcparty");

  if( (LcObj.Parties().Find(LCPARTY_KIND_NEGOTIATING_BANK, Lcparty) == 0) AND (Lcparty.rec.PartyID != {ourBank}) )
    Tag = "";
  elif(LcObj.AnyBankCond == "X")
    Tag = SWIFT_Tag_D;
    Lcparty.Clear();
    Lcparty.rec.Name = "negotiating bank";
  else
    needFill = false;
  end;
  
  if(needFill)
    field_value = FillFinOrgIdentField( "58", Tag, Lcparty.rec.Account, 
                                        Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                        Lcparty.rec.PartyID, Lcparty.rec.Name );
  end;

  SetParm( 1, Tag );
  return field_value;
end;

/* Заполняет поле 72 (для платежа) */
/* Добавление в лексему ACC инфы из PaymentDetails(Ground) и PartyInfo */
macro AddGroundAndPartyInfo(field72, Ground, PartyInfo)    

  if ( (valtype(Ground)!=V_UNDEF) AND (PartyInfo != "") )
    if (field72 != "")
       field72 = field72 + SYMB_ENDL;
    end;
    field72 = field72 + PartyInfo;
  end;

  if ( (valtype(Ground)!=V_UNDEF) AND (Ground != "") )
    if (field72 != "")
       field72 = field72 + SYMB_ENDL;
    end;
    field72 = field72 + Ground;
  end;

  return field72;
end;

/* Заполнить поле 72 */
macro FillInformationOfPaymentField(RsPaym, AllowCodes, ОстатокПоля70, standart, IsTaxPm )
   var field_value = "", field72, tmpfield72, PartyInfo, 
       PayerChargeOffDate,
       PayerBankEnterDate,
       PayerBankMarkDate, 
       I2PlaceDate,
       IsReceiverFIIDrub = IsRUR(RsPaym.ReceiverFIID);

   RECORD ord( pmroute );
   RECORD rcv( pmroute );
   RECORD ocb( pmroute );
   RECORD irb( pmroute );
                                                      
   if( valtype(standart)==V_UNDEF )
     standart = ST_UNDEF;
   end;

   /* Если в форме есть поле 70, то сюда попадает остаток из него.         */
   /* Если в поля 70 нет, то сюда попадает инфа, предназначенная в поле 70 */
   if ( IsAllowCode(AllowCodes, Field72CodesNZP) )
      /* Используется только для RUR4, RUR5, RUR6 */
      if ( (valtype(ОстатокПоля70)!=V_UNDEF) AND (ОстатокПоля70!="") )
        field_value = ДобавитьОстатокПоля70( field_value, ОстатокПоля70, standart );
        ОстатокПоля70 = "";
      end;
   end;

   if( (IsAllowCode(AllowCodes, Field72CodesINS)==TRUE) AND
       ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_IN, ord) AND 
       ПолучитьУзелОтправителяПлатежа(RsPaym.PaymentID, Route) AND
       (Route.RouteID!=ord.RouteID) )
     field_value = String( field_value, SYMB_SLASH, Field72CodesINS, SYMB_SLASH, Route.CodeValue );
   end;

   if (IsAllowCode(AllowCodes, Field72CodesDAS)) /* только для MT103 RUR6 */
     if ( IsReceiverFIIDrub )
       PayerChargeOffDate = RsPaym.PayerChargeOffDate;
       PayerBankEnterDate = RsPaym.PayerBankEnterDate;
       PayerBankMarkDate = RsPaym.PayerBankMarkDate;
       I2PlaceDate = RsPaym.I2PlaceDate;
       if ((PayerChargeOffDate != Date(0, 0 ,0)) or 
            (PayerBankEnterDate != Date(0, 0 ,0)) or
            (PayerBankMarkDate != Date(0, 0 ,0)) or
            (I2PlaceDate != Date(0, 0 ,0)) 
           )
         if (field_value != "")
           field_value = string(field_value, SYMB_ENDL);
         end;
         field_value = String(field_value, SYMB_SLASH, Field72CodesDAS, SYMB_SLASH, 
                                GetSWIFTDate(PayerChargeOffDate), ".",
                                GetSWIFTDate(PayerBankEnterDate), ".",
                                GetSWIFTDate(PayerBankMarkDate), ".",
                                GetSWIFTDate(I2PlaceDate)
                               );
       end;
     end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesEARLY) ) /* только для срочных платежей */
     if ( RsPaym.Instancy )
       if (field_value != "")
          field_value = string(field_value, SYMB_ENDL);
       end;
       field_value = String(field_value, SYMB_SLASH, Field72CodesEARLY, SYMB_SLASH);
     end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesRPP) )
      /* Используется только для RUR4, RUR5, RUR6 */
      if ( IsReceiverFIIDrub )
        if( field_value != "" )
          field_value = String( field_value, SYMB_ENDL );
        end;
        field_value = String( field_value, SYMB_SLASH, Field72CodesRPP, SYMB_SLASH, substr(RsPaym.Number,1,5), ".", GetSWIFTDate(RsPaym.PayDate), "." );
        if ( RsPaym.Priority<10 )
          field_value = String( field_value, string(RsPaym.Priority), "." );
        else
          field_value = String( field_value, "9", "." )
        end;     

        if ( RsPaym.PaymentKind=="П" )
           field_value = String( field_value, "POST" );
        elif ( RsPaym.PaymentKind=="Т" )
           field_value = String( field_value, "TELG" )
        elif ( RsPaym.PaymentKind=="С" )
           field_value = String( field_value, "BESP" )
        else
           field_value = String( field_value, "ELEK" )
        end;
      else
        /* Валютный платеж */
        if( field_value != "" )
          field_value = String( field_value, SYMB_ENDL );
        end;
        field_value = String( field_value, SYMB_SLASH, Field72CodesRPP, SYMB_SLASH, substr(RsPaym.Number,1,5), ".", GetSWIFTDate(RsPaym.Date), ".6.ELEK" );
      end;
      if( (Strlen(RsPaym.ShifrOper) == 2))
        field_value = String( field_value, ".", GetSWIFTDate(RsPaym.ValueDate), ".", RsPaym.ShifrOper ); 
      else
        field_value = String( field_value, ".", GetSWIFTDate(RsPaym.ValueDate) ); 
      end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesUIP) )
     if (RsPaym.UIN != "")
       field_value = String( field_value, SYMB_ENDL, SYMB_SLASH, Field72CodesUIP, SYMB_SLASH, RsPaym.UIN);
     elif (IsTaxPm)
       field_value = String( field_value, SYMB_ENDL, SYMB_SLASH, Field72CodesUIP, SYMB_SLASH, "0");
     end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesRPO) and IsReceiverFIIDrub )
      /* Используется только для RUR4, RUR5, RUR6 */
      if ( RsPaym.PartPaymNumber )

         field_value = String( field_value, SYMB_ENDL, SYMB_SLASH, 
                                            Field72CodesRPO, 
                                            SYMB_SLASH, 
                                            substr(String(RsPaym.PartPaymNumber),1,3), ".",
                                            substr(RsPaym.PartPaymShifrMain,1,2), ".",
                                            substr(RsPaym.PartPaymNumMain,1,6), ".",
                                            GetSWIFTDate(RsPaym.PartPaymDateMain), 
                                            SYMB_ENDL, SYMB_SLASH, SYMB_SLASH,
                                            substr(GetSWIFTAmount(RsPaym.PartPaymRestAmountMain),1,18)
                                          );
      end;
   end;


   if( IsAllowCode(AllowCodes, Field72CodesRCB) AND
       ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
       ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
       ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
      if( field_value != "" )
       field_value = String( field_value, SYMB_ENDL );
      end;
      field_value = String( field_value, SYMB_SLASH, Field72CodesRCB, SYMB_SLASH, Route.CodeValue );
   end;
   
   PartyInfo = RsPaym.PartyInfo;
   if (PartyInfo != "") 
     if(substr(PartyInfo,1,1)== SYMB_SLASH)
       tmpfield72 = SubStr( PartyInfo, 2 );
       PartyInfo = SYMB_SLASH + SubStr( tmpfield72, 1, Index(tmpfield72, SYMB_SLASH) );
       StrChangeRusXSet( SubStr(tmpfield72, Index(tmpfield72, SYMB_SLASH)+1), tmpfield72, standart );
       PartyInfo = PartyInfo + tmpfield72;
       field_value = ДобавитьБлокВПоле72( field_value, "", PartyInfo, standart );
       PartyInfo = "";
     else
       if (IsAllowCode(AllowCodes, Field72CodesREC)==TRUE) 
         StrChangeRusXSet( PartyInfo, PartyInfo, standart );
         field_value = ДобавитьБлокВПоле72( field_value, Field72CodesREC, PartyInfo, standart );
         PartyInfo = "";
       end;/*Если в поле формы нет лексемы REC, то PartyInfo добавим в ACC*/
     end;

   end;

   /* ACC в поле 72 формы разрешено,*/
   /*и туда еще ничего не писали - ликвидируем этот недостаток :))*/
   field72 = "";
   //mda 12/02/2019 по инф от банка вместо асс надо использовать BNF
   var Field72CodesBNF = "BNF";
   if (IsAllowCode(AllowCodes, Field72CodesBNF/*Field72CodesACC*/))
     field72 = AddGroundAndPartyInfo(field72, ОстатокПоля70, PartyInfo);
     if (field72 != "")
       field_value = ДобавитьБлокВПоле72( field_value, Field72CodesBNF/*Field72CodesACC*/, field72, standart );
     end;
   end;

   /* Добавляем информацию, введенную вручную пользователем в панели ввода платежки */
   field72 = RsPaym.AdditionalInfo;

   if( SubStr( field72, 1, 1 ) == SYMB_SLASH )
     // в случае если в поле уже содержится кодовое слово, то нужно исключить его из транслитерации
     // и передавать в сообщение как есть (SCR 144401)
     tmpfield72 = SubStr( field72, 2 );
     field72 = SYMB_SLASH + SubStr( tmpfield72, 1, Index(tmpfield72, SYMB_SLASH) );
     StrChangeRusXSet( SubStr(tmpfield72, Index(tmpfield72, SYMB_SLASH)+1), tmpfield72, standart );
     field72 = field72 + tmpfield72;
     field_value = ДобавитьБлокВПоле72( field_value, "", field72, standart );
   else
     StrChangeRusXSet( field72, field72, standart );
     field_value = ДобавитьБлокВПоле72( field_value, Field72CodesBNF/*Field72CodesACC*/, field72, standart );
   end;

   return field_value;
end;

/* Получает очередную комиссию платежа */
macro GetPaymentCommission( RsPaym, belong, combuf )
   var retVaiue = false, DocKind, DocID;
   var rs:object;
   var select:string;
   var params:TArray;

   if ( RsPaym.IsTransit == true )
       /* Транзитный платеж */
       DocKind = WL_INDOC;
       DocID   = RsPaym.PaymentID;
   else
       DocKind = RsPaym.DocKind;
       DocID   = RsPaym.DocumentID;
   end;

   select = "select op.t_ID_Operation from doproper_dbt op where "+
                            "op.t_DocKind =:DocKind and "+
                            "TO_NUMBER(op.t_DocumentID) = :DocID";
   params = makeArray( SQLParam("DocKind", DocKind),
                       SQLParam("DocID", DocID));
   rs = execSQLselect( select, params, FALSE );

   if ( rs.MoveNext() )
      retVaiue = GetCommission( rs.value(0), ID_ALL_STEP, belong, combuf );
   end;

   return retVaiue;
end;

/* Получает оригинальную сумму платежа */
macro GetOrigenSumPayment( RsPaym)
   var continue0, OrigenAmount = RsPaym.PayerAmount;
   RECORD combuf( oprsfcom );

   if ( RsPaym.IsTransit == true )
       /* Транзитный платеж */
       /*                    КОМИССИЯ НАШЕГО БАНКА                               */
       ClearRecord(combuf); 
       /* Перебираем комиссии нашего банка */
       continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
       while( continue0 )          
          if ( combuf.FIID_Sum==RsPaym.PayerFIID )
             OrigenAmount = OrigenAmount + combuf.Sum + combuf.SumNDS;
          else
             /* Пересчитываем сумму */
             OrigenAmount = OrigenAmount + GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, RsPaym.PayerFIID);
          end;
          continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
       end;
   else
      /*                    КОМИССИЯ НАШЕГО БАНКА                               */
      if ( RsPaym.ComissCharges == BEN )  /* За счет получателя */
         ClearRecord(combuf); 
         continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
         while( continue0 )     
           if ( combuf.FIID_Sum == RsPaym.PayerFIID )
              OrigenAmount = OrigenAmount + combuf.Sum + combuf.SumNDS;
           else
              /* Пересчитываем сумму */
              OrigenAmount = OrigenAmount + GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, RsPaym.PayerFIID);
           end;
           continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
         end;
      end;

      /*                   КОМИССИЯ КОРРЕСПОНДЕНТА          */
      if ( RsPaym.ComissCharges == OUR )  /* За наш счет */
         ClearRecord(combuf); 
         /* Перебираем комиссии корреспондента */
         continue0 = GetPaymentCommission( RsPaym, CORRESP, combuf );
         while( continue0 )
            if ( combuf.FIID_Sum == RsPaym.PayerFIID )
               OrigenAmount = OrigenAmount - combuf.Sum - combuf.SumNDS;
            else
               /* Пересчитываем сумму */
               OrigenAmount = OrigenAmount - GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, RsPaym.PayerFIID);
            end;
            continue0 = GetPaymentCommission( RsPaym, CORRESP, combuf );
         end;
      end;
   end;

   return OrigenAmount;
end;

macro GetSumOwnCommis( RsPaym, 
                       FIID,   /* заполняется здесь */
                       Amount  /* сумма */
                     )
   RECORD combuf( oprsfcom );
   var continue0;


   Amount = $0;
   FIID = RsPaym.ReceiverFIID;
   ClearRecord(combuf); 
   continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
   while( continue0 )
      if ( FIID==combuf.FIID_Sum )
         Amount = Amount + combuf.Sum + combuf.SumNDS;
      else
         Amount = Amount + GetSumConvert(combuf.Sum+combuf.SumNDS, combuf.FIID_Sum, FIID);
      end;
      continue0 = GetPaymentCommission( RsPaym, OWN, combuf );
   end;

   setparm( 1, FIID );
   setparm( 2, Amount );

   return true;

   OnError(er) /* обработка ошибок */
      ExeptionMessage(er);
      return FALSE;
end;

macro GetSumCorrespCommis( RsPaym, 
                           FIID,   /* заполняется здесь */
                           Amount  /* сумма */
                         )
   RECORD combuf( oprsfcom );
   var continue0, OwnSum, ;
   RECORD sleeve( pmsleeve );

   if ( RsPaym.IsTransit )
      /* Транзитный платеж */
      if ( ПолучитьРасходыРеспондента(RsPaym.PaymentID, sleeve, {OurBank}) )
          if ( not GetSumOwnCommis(RsPaym, FIID, Amount) )
              return false;
          end;

          if ( FIID==sleeve.FIID )
             Amount = sleeve.Amount - Amount;
          else
             Amount = sleeve.Amount - GetSumConvert( Amount, FIID, sleeve.FIID );
             FIID = sleeve.FIID;
          end;

          if ( Amount<$0 )
             Amount = $0;
          end;
      else
          Amount = $0;
          FIID = 0;
      end;
   else
      Amount = $0;
      FIID = RsPaym.ReceiverFIID;
      ClearRecord(combuf); 
      continue0 = GetPaymentCommission( RsPaym, CORRESP, combuf );
      while( continue0 )
         if ( FIID==combuf.FIID_Sum )
            Amount = Amount + combuf.Sum + combuf.SumNDS;
         else
            Amount = Amount + GetSumConvert(combuf.Sum + combuf.SumNDS, combuf.FIID_Sum, FIID);
         end;
         continue0 = GetPaymentCommission( RsPaym, CORRESP, combuf );
      end;
   end;

   setparm( 1, FIID );
   setparm( 2, Amount );

   return true;

   OnError(er) /* обработка ошибок */
      ExeptionMessage(er);
      return FALSE;
end;

/* Записывает комисси платежа в сообщение */
macro ЗаписатьКомиссииПлатежа( RsPaym, TypeCharges, UseC19 )
    var continue0, ComAmount, ComFIID, comflag;
    RECORD sleeve( pmsleeve );

    if( valtype(UseC19) != V_BOOL )
      UseC19 = false;  /* Использовать ли Network Validated Rules MT103 С19 */
    end;

  if ( RsPaym.IsTransit )
       /* Для транзитного записываем цепочку комиссий */
       comflag = false;
       ClearRecord( sleeve );
     continue0 = ПолучитьРасходыРеспондента(RsPaym.PaymentID, sleeve);
       while( continue0 AND (sleeve.PartyID!={OurBank}) )

          comflag = true;

          if( not УстановитьКонтекстБлока( "71F" ) )
              RunError("|Ошибка при установке контекста блока '71F'");
          end;

          ЗаписатьПолеЛог( SendersChargesField, ПолучитьКодВалютыЛог( sleeve.FIID )
                                                + GetSWIFTAmount( sleeve.Amount ) ) ;

          /* Восстанавливаем контекст блока */
          if( not УстановитьКонтекстБлока("..") )
             RunError("|Ошибка при восстановлении контекста блока");
          end;

        continue0 = ПолучитьРасходыРеспондента(RsPaym.PaymentID, sleeve);
       end;

       if ( comflag OR (TypeCharges!=CHARGES_OUR) )
          /* Запысываем сумму нашей комиссий */
        if ( not GetSumOwnCommis(RsPaym, ComFIID, ComAmount) )
            RunError("|Ошибка вычисления комиссии");
          end;
          if ( ComAmount )
            if( not УстановитьКонтекстБлока( "71F" ) )
                RunError("|Ошибка при установке контекста блока '71F'");
            end;
            ЗаписатьПолеЛог( SendersChargesField, ПолучитьКодВалютыЛог( ComFIID )
                                                  + GetSWIFTAmount( ComAmount ) ) ;
            /* Восстанавливаем контекст блока */
            if( not УстановитьКонтекстБлока("..") )
               RunError("|Ошибка при восстановлении контекста блока");
            end;
          end;
       end;

       if ( TypeCharges==CHARGES_OUR )
        if ( not GetSumCorrespCommis(RsPaym, ComFIID, ComAmount) )
            RunError("|Ошибка вычисления комиссии");
          end;

          if ( ComAmount )
            /* C19 The currency code in the fields 71G and 32A must be the same */
          if( (UseC19 == FALSE) OR ((UseC19 == TRUE) AND (RsPaym.ReceiverFIID == ComFIID)) )
              ЗаписатьПолеЛог( ReceiversChargesField, ПолучитьКодВалютыЛог( ComFIID ) + GetSWIFTAmount( ComAmount ) ) ;
            else
              RunError("|Код валюты в полях 32A и 71G должен быть одинаковым");
            end;
          end;
       end;
    else
       if ( TypeCharges==CHARGES_OUR )
        if ( not GetSumCorrespCommis(RsPaym, ComFIID, ComAmount) )
             RunError("|Ошибка вычисления комиссии");
          end;
          if ( ComAmount )
            /* C19 The currency code in the fields 71G and 32A must be the same */
          if( (UseC19 == FALSE) OR ( (UseC19 == TRUE) AND (RsPaym.ReceiverFIID == ComFIID)) )
              ЗаписатьПолеЛог( ReceiversChargesField, ПолучитьКодВалютыЛог( ComFIID ) + GetSWIFTAmount( ComAmount ) ) ;
            else
              RunError("|Код валюты в полях 32A и 71G должен быть одинаковым");
            end;
          end;
       elif ( TypeCharges==CHARGES_BEN )
        if ( not GetSumOwnCommis(RsPaym, ComFIID, ComAmount) )
              RunError("|Ошибка вычисления комиссии");
          end;
          if ( ComAmount )
             if( not УстановитьКонтекстБлока( "71F" ) )
                RunError("|Ошибка при установке контекста блока '71F'");
             end;
             ЗаписатьПолеЛог( SendersChargesField, ПолучитьКодВалютыЛог( ComFIID )
                                                              + GetSWIFTAmount( ComAmount ) ) ;
             /* Восстанавливаем контекст блока */
             if( not УстановитьКонтекстБлока("..") )
                RunError("|Ошибка при восстановлении контекста блока");
             end;
          end;
       end;
    end;
end;

//---------------------------------------------------------------------
// Получение входящего сообщения платежа
//---------------------------------------------------------------------
MACRO GetMessageByPayment( PaymentID:integer ):integer

  RECORD wldmes( wlmes  );
  if( not ПолучитьСообщение( PaymentID, OBJTYPE_PAYMENT, 1, wldmes, NULL ) )
    return 0;
  end;

  return wldmes.MesID;
END;

// Получить ИД входящего сообщения по платежу (входящему или транзитному)
macro getInMesIDByPayment(RsPaym): integer
  var MesID = 0;
  if(RsPaym.DocKind == WL_WIPM) // введен вручную без входящего сообщения
    MesID = 0;
  else // загружен по сообщению или создан по исполнению транзитного
    MesID = GetMessageByPayment
              ( IfThenElse(RsPaym.DocKind == WL_INDOC, RsPaym.PaymentID,
                           getInitialPaymentID(RsPaym.PaymentID, PMLINK_KIND_RETREDIR))                                           
              );
  end;
  return MesID;
end;

//---------------------------------------------------------------------
// Записать Поле 71F
//---------------------------------------------------------------------
MACRO ЗаписатьПоле71F( RsPaym:RsbPayment, IsTransit, TypeCharges, fld33B, fld32A )

  MACRO СчитатьПоля71F( PaymentID:integer ):TArray
    var query:string = "select val.T_VALUE " +
                        " from dwlmesval_dbt val, " +
                             " dwltpfld_dbt  fld  " +
                       " where val.t_MesID = :MesID " +
                        " and fld.t_TpFieldID = val.t_TpFieldID " +
                        " and fld.t_Name = :FieldName " + 
                        " order by val.t_index";

    var params:TArray = makeArray( SQLParam( "MesID"    , GetMessageByPayment( PaymentID ) ), 
                                   SQLParam( "FieldName", SendersChargesField ) );

    var rs:RsdRecordset = execSQLselect( query, params, true );
    var flst:TArray = TArray();
    while( rs.MoveNext() )
      flst.value( flst.size ) = rs.value(0);
    end;
    return flst;
  END;
  
  var flst:TArray=TArray();
  var i:integer=0;
  var СуммаИт:Money=0., Сумма71F:Money=0., Сумма32A:Money=0., Сумма33B:Money=0.;
  var Валюта32A, Валюта33B;
  
  if( (TypeCharges == CHARGES_BEN) OR (TypeCharges == CHARGES_SHA) )
  
    // документ порожден транзитным платежом или сам транзитный
    if( IsTransit )
      // в исходящее сообщение копируются все существующие поля 71F из входящего сообщения в том же порядке
      flst = СчитатьПоля71F( RsPaym.PaymentID );
      i = 0;
      while( i < flst.Size )
        // переносим поле
        if( not УстановитьКонтекстБлока( "71F" ) )
          RunError( "|Ошибка при установке контекста блока '71F'" );
        end;
        ЗаписатьПолеЛог( SendersChargesField, flst.value(i) );
        if( not УстановитьКонтекстБлока( ".." ) )
          RunError( "|Ошибка при восстановлении контекста блока" );
        end;
        
        //посчитаем сумму
        Сумма71F = Сумма71F + Money( substr( strsubst( flst.value(i), ",", "." ), 4 ) );
        
        i = i + 1;
      end;
    end;

    // получим данные поля 32A
    ПолучитьФинИнПоISO( substr( fld32A, 7, 3 ), wlfininstr );
    Валюта32A = wlfininstr.FIID;
    Сумма32A = Money( substr( strsubst( fld32A, ",", "." ), 10 ) );

    // получим данные поля 33B
    ПолучитьФинИнПоISO( substr( fld33B, 1, 3 ), wlfininstr );
    Валюта33B = wlfininstr.FIID;
    Сумма33B = Money( substr( strsubst( fld33B, ",", "." ), 4 ) );

    if( Валюта33B == Валюта32A )
      СуммаИт = Сумма33B - Сумма32A;
    else
      СуммаИт = GetSumConvert( Сумма33B, Валюта33B, Валюта32A ) - Сумма32A;
      //СуммаИт = ConvertSum( Сумма33B, RsPaym.FactRate.Rate, RsPaym.FactRate.Scale, RsPaym.FactRate.Point, RsPaym.FactRate.IsInverse ) - Сумма32A;
    end;

    // вычитаются все имеющиеся суммы в полях 71F
    СуммаИт = СуммаИт - Сумма71F;
     
    // результат, больший или равный 0, записывается в новое поле 71F
    if( СуммаИт >= 0.0 )
      if( not УстановитьКонтекстБлока( "71F" ) )
        RunError( "|Ошибка при установке контекста блока '71F'" );
      end;
      ЗаписатьПолеЛог( SendersChargesField, ПолучитьКодВалютыЛог( Валюта32A ) + GetSWIFTAmount( СуммаИт ) );
      if( not УстановитьКонтекстБлока( ".." ) )
        RunError( "|Ошибка при восстановлении контекста блока" );
      end;
    end;

  elif( TypeCharges == CHARGES_OUR )
    // Поле отсутствует в сообщении
  end; 

END;

macro DefineSWIFTRateByPmPaym( RsPaym )
    var Rate, i = 0;
    
    if ( RsPaym.FactRate.Rate == 0.0 ) 
       return "";
    end;

    Rate = RsPaym.FactRate.Rate/RsPaym.FactRate.Scale;
    while( i<RsPaym.FactRate.Point )
       Rate = Rate/10;
       i = i+1;
    end;
       
    if ( RsPaym.FactRate.IsInverse=="X" )
        return GetSWIFTRate(Trim( string((Rate):16:7) ));
    else
        return GetSWIFTRate( Trim( string( (1.0/Rate):16:7 ) ) );
    end;
end;

macro НайтиКорсхемуНеЕдинственнуюУКонтрагента( Account, FIID, Corschem )
   var rs:object;
   var select:string;
   var params:TArray;

   select = "select cors.t_number, cors.t_FIID, cors.t_FI_Kind, cors.t_CorrID, cors.t_TpID, cors.t_Name, cors.t_account from dcorschem_dbt cors where "+
                              "cors.t_Account =:Account and "+
                              "cors.t_FIID =:FIID and "+
                              "Exists(select * from dcorschem_dbt corsIn where corsIn.t_CorrID=cors.t_CorrID and (corsIn.t_Number!=cors.t_Number or corsIn.t_FIID!=cors.t_FIID))";
   params = makeArray( SQLParam("Account", Account),
                       SQLParam("FIID", FIID));
   rs = execSQLselect( select, params, FALSE );

   if ( not rs.moveNext() ) 
        return false;
   end;

   ClearRecord(Corschem);
   Corschem.number = rs.value(0);
   Corschem.FIID = rs.value(1);
   Corschem.FI_Kind = rs.value(2);
   Corschem.CorrID = rs.value(3);
   Corschem.TpID = rs.value(4);
   Corschem.Name = rs.value(5);
   if ( Corschem.Name=="" )
      Corschem.Name = "";
   end;
   Corschem.Account = rs.value(6);
   if ( Corschem.Account=="" )
      Corschem.Account = "";
   end;
   return true;
end;

// Возвращет вид корсхемы (0 - корсхема не найдена)
macro ПолучитьКорСхему (Number, FIID)
   var rs:object;
   var select:string;
   var params:TArray;

   select = "select t_isnostro from dcorschem_dbt cors where "+
                              "cors.t_Number = :NumberCor and "+
                              "cors.t_FIID =:FIID and cors.t_FI_Kind = 1";
   params = makeArray( SQLParam("NumberCor",Number),
                       SQLParam("FIID", FIID));
   rs = execSQLselect( select, params, FALSE );

   if ( not rs.moveNext() ) 
        return WLD_COR_ERROR;
   end;
   if (rs.value(0)=="X") return WLD_COR_NOSTRO;
   end;
   return WLD_COR_LORO;
end;

private macro GetPmLinkAmount(PaymentID)
  var Amount = 0;  
  var select = "select pmlink.t_Amount " +
               "  from dpmlink_dbt pmlink " +
               " where pmlink.t_PurposePayment = :PaymID1 " +
               "   and pmlink.t_InitialPayment = :PaymID2 " +
               "   and pmlink.t_LinkKind       = :LinkKind "; 
  var params = makeArray( SQLParam( "PaymID1" , PaymentID ),
                          SQLParam( "PaymID2" , PaymentID ),
                          SQLParam( "LinkKindID" , PMLINK_KIND_KVITING )
                        );
  var rs = execSQLselect( select, params, TRUE );
  if(rs and rs.moveNext)
    Amount = rs.value(0);
  end;
  return Amount;
end;


/*Заполнение поля 32*/
macro FillCurrencyOriginalOrderedAmountField( RsPaym )
  var field_value = "",
      FIID   : integer = -1,
      Amount : money = $0; 

  if(RsPaym.IsCredit)
    FIID = RsPaym.FutureReceiverFIID;
    Amount = RsPaym.FutureReceiverAmount;
  else
    FIID = RsPaym.FuturePayerFIID;
    Amount = RsPaym.FuturePayerAmount;
  end;

  if( (RsPaym.PaymStatus == PM_FINISHED) and (Amount == $0) )

    amount = GetPmLinkAmount(RsPaym.PaymentID);
  
  end;
  
  field_value = ПолучитьКодВалютыЛог( FIID ) + GetSWIFTAmount( Amount );
  return field_value;

end;

// Заполнение поля 41a
macro FillAvailableWithByField_LC(LcObj : RsbLetterOfCredit, Tag): string
  var field_value = "";

  var Lcparty : TRecHandler = TRecHandler("lcparty");

  if( LcObj.Parties().Find(LCPARTY_KIND_CONFIRMING_BANK, Lcparty) == 0 )
    field_value = FillFinOrgIdentField( "41", Tag, "", 
                                        Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                        Lcparty.rec.PartyID, Lcparty.rec.Name );

    var note = "";
    GetElementAndNoteLLVALUES(OBJTYPE_LC_SO, string(LcObj.PayCond), null, note);
    field_value = field_value + SYMB_ENDL + note;
  end;

  SetParm( 1, Tag );
  return field_value;
end;

// Заполнение поля 42a для аккредитива
macro FillDraweeField_LC(LcObj : RsbLetterOfCredit, Tag : string): string
  var field_value = "";

  var Lcparty : TRecHandler = TRecHandler("lcparty");

  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_ACCEPTANCE)
    if( LcObj.Parties().Find(LCPARTY_KIND_DRAWEE, Lcparty) == 0 )
      field_value = FillFinOrgIdentField( null, Tag, Lcparty.rec.Account, 
                                          Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                          Lcparty.rec.PartyID, Lcparty.rec.Name );
    end;
  end;

  SetParm( 1, Tag );
  return field_value;
end;

// N - общее число строк
// M - количество символов в строке
macro SWIFTWriteFieldExNM( BlockName, FieldName, FieldValue, Recreate, oldMes, N : integer, M : integer )

  // Если сообщение пересоздается И поле неважное, то берем его из старого сообщения
  if( ( Recreate == true ) AND (not GetBlockEdit(FieldName, OldMes.RlsFormID)) )
    var q = "SELECT t_Value "
            "  FROM dwlmesval_dbt mesval, dwltpfld_dbt tpfld, dwlmesfld_dbt mesfld, dwlmesfld_dbt mesfldM "
            " WHERE mesval.t_MesID = :MesID "
            "   AND mesval.t_TpFieldID = tpfld.t_TpFieldID AND tpfld.t_Name = :FieldName "
            "   AND mesfld.t_FieldID = mesval.t_FieldID "
            "   AND mesfld.t_Master = mesfldM.t_FieldID AND mesfldM.t_BlockName = :BlockName ";

    var rs = execSQLselect( q, makeArray( SQLParam("MesID", OldMes.MesID),
                                          SQLParam("FieldName", FieldName),
                                          SQLParam("BlockName", BlockName) ) );
    while(rs and rs.moveNext())
      ЗаписатьПолеЛогВБлок( BlockName, FieldName, rs.value(0) );

      // Восстанавливаем нулевой контекст блока
      if( not УстановитьКонтекстБлока() )
        RunError("|Ошибка при установке нулевого контекста блока");
      end;
    end;

  else

    var RestS = FieldValue, RestN = N, CurrN = 0;
    while(RestS and RestN)
      CurrN = IfThenElse(RestN > 30, 30, RestN);
      RestN = RestN - CurrN;
      FieldValue = SwiftStrSplit(RestS, M, 30, true, @RestS);
      if(FieldValue)
        ЗаписатьПолеЛогВБлок( BlockName, FieldName, FieldValue );

        // Восстанавливаем нулевой контекст блока
        if( not УстановитьКонтекстБлока() )
          RunError("|Ошибка при установке нулевого контекста блока");
        end;
      end;
    end;
  end;

end;

macro FillRelatedReferenceForDebitCreditConfirmation(wlconf) : string
  var RelatedReference : string = wlconf.RelatedRef;

  if( (strlen(RelatedReference) > SW_Len_RelatedReference) or (RelatedReference == ""))
    RelatedReference = GetLastSymbols(wlconf.Number, SW_Len_RelatedReference);
  end;

  return RelatedReference;
end;

macro FillBeneficiaryCopyExternalFld(RsPaym) //Копируем поле 59 из транзитного платежа, если он таковым является
  var field_value, query, rs, mesid;
  if((RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL) and (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL))
    query = "SELECT wlmes.t_mesid FROM dwlmes_dbt wlmes, dwlmeslnk_dbt wlmeslnk, dwlpm_dbt wlpm, dwltpshem_dbt wltpshem "
            "WHERE    Wlmes.t_MesID = wlmeslnk.t_MesID "
            "AND    Wlmeslnk.t_ObjKind = 501 " //OBJTYPE_PAYMENT
            "AND    Wlmeslnk.t_ObjID = wlpm.t_WlPmID "
            "AND    Wlpm.t_Direct = 'X'" //WLD_MES_IN
            "AND    Wlpm.t_PaymentID = :paymentid "
            "AND    Wlpm.t_WlPmNum = 0 "
            "AND    wltpshem.t_tpshemid = WLMES.T_TPSCHEMID "
            "AND    wltpshem.t_tpid = 2 " //WLD_TRNP_SWIFT
            "GROUP BY wlmes.t_mesid;"
  else
    query = "SELECT wlmes.t_mesid FROM dwlmes_dbt wlmes, dwlmeslnk_dbt wlmeslnk, dwlpm_dbt wlpm, dpmlink_dbt pmlink,dwltpshem_dbt wltpshem "
            "WHERE    Wlmes.t_MesID = wlmeslnk.t_MesID "
            "AND    Wlmeslnk.t_ObjKind = 501 " //OBJTYPE_PAYMENT
            "AND    Wlmeslnk.t_ObjID = wlpm.t_WlPmID "
            "AND    Wlpm.t_Direct = 'X' " //WLD_MES_IN
            "AND    Wlpm.t_WlPmNum = 0 "
            "AND    Wlpm.t_PaymentID = pmlink.t_InitialPayment "
            "AND    Pmlink.t_LinkKind = 7 " //PMLINK_KIND_RETREDIR  
            "AND    Pmlink.t_PurposePayment = :paymentid "
            "AND    wltpshem.t_tpshemid = WLMES.T_TPSCHEMID "
            "AND    wltpshem.t_tpid = 2 " //WLD_TRNP_SWIFT
            "GROUP BY wlmes.t_mesid;";
  end;

  rs = execSQLselect( query, makeArray( SQLParam("paymentid", RsPaym.paymentid)));

  if(rs.moveNext())
    mesid = rs.value(0);
  else
    return "";
  end;

  query = "SELECT wlmesval.t_value from dwlmesval_dbt wlmesval "
          "WHERE WLMESval.t_mesid = :mesid "
          "AND wlmesval.t_tpfieldid in (SELECT wltpfld.t_tpfieldid from dwltpfld_dbt wltpfld "
          "                             WHERE (wltpfld.t_name like '59' "
          "                                    OR wltpfld.t_name like '59A' "
          "                                    OR wltpfld.t_name like '59F') "
          "                             AND wltpfld.t_tpid =2) "
          "ORDER BY wlmesval.t_index;";
  rs = execSQLselect( query, makeArray( SQLParam("mesid", mesid)));
  if(rs.moveNext())
    field_value = rs.value(0);
  else
    return "";
  end;

  return field_value;       
end;

macro CheckSubStrCount(str: string) //Считаем кол-во строк в поле
  var count = -1;
  var ind: integer = 0;

  ind = Index(str, SYMB_ENDL);
  while(ind != 0)
    count = count + 1;
    str = substr(str, ind + 1);  
    ind = Index(str, SYMB_ENDL);
  end;
  return count;                                  
end;

macro FillBeneficiaryCustomerField_F(RsPaym)
  var buf = "", field_value = "", CountryCode = "";
  record Adr(adress);
  
  if(RsPaym.ReceiverAccount != "")
    buf = "/"+ RsPaym.ReceiverAccount + SYMB_ENDL;   
    field_value = field_value + buf;
  end;
  
  if(RsPaym.ReceiverName != "")
    StrChangeRusXSet( RsPaym.ReceiverName, buf );
    buf = "1/" + buf;
    buf = trim(buf);
    var len = strLen(buf);

    while(len > 33)
      field_value = field_value + trim(substr(buf,1,33)) + SYMB_ENDL;              
      buf = string("1/", substr(buf, 34)) ;
      len = strlen(buf);
      if(CheckSubStrCount(field_value) == 4) //если строк больше не осталось - возвращаем сформированное поле
        return field_value;
      end;       
    end;
    field_value = field_value + buf + SYMB_ENDL;
  end;
  
  if( НайтиАдресСубъекта(RsPaym.Receiver,PTADDR_LEGAL,Adr) or НайтиАдресСубъекта(RsPaym.Receiver,PTADDR_REAL,Adr))
    buf = "";
    CountryCode = GetCountryCode(RsPaym.Receiver);
    if(Adress.Street!="")
      buf = buf + Adress.Street + " "; 
    end;
    if(Adress.CodeStreet!="")
      buf = buf + Adress.CodeStreet + " ";
    end;
    if(Adress.House!="")
      buf = buf + Adress.House + " ";
    end;
    if(buf!="")
      buf = "2/" + buf;
    end;

    buf = trim (buf);
    len = strLen(buf);
    
    while(len > 33)
      
      field_value = field_value + trim(substr(buf,1,33)) + SYMB_ENDL ;
      buf = string("2/",substr(buf, 34));
      len = strlen(buf);
    end;
    if(buf!="")
      field_value = field_value + buf + SYMB_ENDL;
    end;
    
    if(CountryCode != "")
      buf = string( CountryCode, "/");
    end;
    
    if( Adr.PostIndex != "" )
      buf = buf + Adress.PostIndex + " ";
    end;
    if(Adr.Region != "" )
      buf = buf + Adress.Region + " ";
    end;
    if(Adr.CodeRegion != "" )
      buf = buf + Adress.CodeRegion + " ";
    end;
    if(Adr.Province != "" )
      buf = buf + Adress.Province + " " ;
    end;
    if(Adr.CodeProvince  != "" )
      buf = buf + Adress.CodeProvince + " ";
    end;
    if(Adr.District != "" )
      buf = buf + Adress.District + " ";
    end;
    if(Adr.CodeDistrict != "" )
      buf = buf + Adress.CodeDistrict + " ";
    end;
    if(Adr.Place != "" )
      buf = buf + Adress.Place + " ";
    end;
    if(Adr.CodePlace != "" )
      buf = buf + Adress.CodePlace + " ";
    end;
    trim(buf);
    if(buf != "")
      buf = "3/" + buf;
    end;

    len = strLen(buf);
    
    while(len > 33)
      field_value = field_value + trim(substr(buf,1,33)) + SYMB_ENDL ;
      buf = string("3/",substr(buf, 34));
      len = strlen(buf);
    end;

    field_value = field_value + buf;

  else
    if(CountryCode != "")
      buf = string("3/", CountryCode, SYMB_ENDL);
      field_value = field_value + buf;
    end;
  end;

  var ind = 0, ind2 = 0;
  
  if(CheckSubStrCount(field_value) == 4)
    StrChangeRusXSet( field_value, field_value );   
    return field_value;
  elif(CheckSubStrCount(field_value) > 4)     
    if(ind = index(field_value, "2/"))    //нашли строку, начинающуюся с "2/"
      if(ind2 = index(field_value, "3/")) //нашли строку, начинающуюся с "3/"             
        buf = substr(field_Value, ind2 ); //Обрезали все до "3/"
        field_value = substr(field_value,1 , ind - 1 ); //взяли все до "2/"
        field_value = field_value + buf;  //склеили получившиеся строки, без "2/"
      END;
    end;
  end;

  if(CheckSubStrCount(field_value) > 4)                     
    if(ind = index(field_value, "3/"))              
      buf = string("3/", CountryCode );
      field_value = substr(field_value, 1 , ind - 1);
      field_value = field_value + buf;
    end;
  end;

  StrChangeRusXSet( field_value, field_value );
  return field_value; 
end;

macro FillConversionRate(pmpaym, fld33B, fld32A) : string
  var field_value = "";
  var Валюта32A = 0;
  var Валюта33B = 0;
  if(fld33B)
   
    // получим данные поля 32A
    ПолучитьФинИнПоISO( substr( fld32A, 7, 3 ), wlfininstr );
    Валюта32A = wlfininstr.FIID;

    // получим данные поля 33B
    ПолучитьФинИнПоISO( substr( fld33B, 1, 3 ), wlfininstr );
    Валюта33B = wlfininstr.FIID;

    if(Валюта32A != Валюта33B)    
      if ( pmpaym.rate == 0.0 ) 
        field_value = "";
      else
        var Rate = pmpaym.rate;
        
        if( pmpaym.scale != 0 )
          Rate = Rate / pmpaym.scale;
        end;

        Rate = IfThenElse( pmpaym.IsInverse == "X", Rate / pow(10, pmpaym.point), pow(10, pmpaym.point) / Rate );        
        field_value = GetSWIFTRate(Trim( string(Rate:0:*, pmpaym.point) ) );
      end;    
    end;
  end;

  return field_value;
end;

macro FillCodeCurrencyAndAmount(RsPaym, wlmes, CommissCode, InMesID) : string
    
  var field_value = "";
  var PmLinkAmount = $0;
  var field_71F = "";
  var field_71G = "";
  var ReceiverCode = "";
  var SenderCode = "";
  var Need33B = false;
  var CountryCodes = "AD, AT, BE, BG, BV, CH, CY, CZ, DE, DK, ES, EE, FI, FR, GB, "
                     "GF, GI, GP, GR, HU, IE, IS, IT, LI, LT, LU, LV, MC, MQ, MT, "
                     "NL, NO, PL, PM, PT, RE, RO, SE, SI, SJ, SK, SM, TF, VA";
  
  if( not ПолучитьПоле(wlmes.OutsideAbonentID, SendersChargesField, field_71F) )
    InitError();
  end;
  
  if( not ПолучитьПоле(wlmes.OutsideAbonentID, ReceiversChargesField, field_71G) )
    InitError();
  end;
  
  ReceiverCode = ПолучитьКодСубъекта(wlmes.OutsideAbonentID, PTCK_SWIFT, null);
  SenderCode = ПолучитьКодСубъекта(GetDprtPartyID(wlmes.Department), PTCK_SWIFT, null);

  if(RsPaym.FutureReceiverAmount == $0)
    PmLinkAmount = GetPmLinkAmount(RsPaym.PaymentID);
  else
    PmLinkAmount = RsPaym.FutureReceiverAmount; 
  end;

  if(((strlen(ReceiverCode) >= 6) AND (Index ( CountryCodes, substr(ReceiverCode,5,2)) != 0)) OR
     ((strlen(SenderCode) >= 6)   AND (Index ( CountryCodes, substr(SenderCode,5,2)) != 0)))
    Need33B = true;    
  elif(CommissCode == CHARGES_BEN)
    Need33B = true;
  elif((field_71F != "") or (field_71G !=""))
    Need33B = true;
  elif((RsPaym.PayerAmount != PmLinkAmount) or (RsPaym.PayerFIID != RsPaym.FutureReceiverFIID))
    Need33B = true;
  end;

  if( Need33B == true )  
    field_value = ПолучитьКодВалютыЛог( RsPaym.PayerFIID ) + GetSWIFTAmount( RsPaym.PayerAmount );
  end;  

  return field_value;

end;

macro WriteField33B( RsPaym, InMesID : integer, CommissCode : string, Recreate : bool, OldwlMes, check:bool ) : string
  /* 33B - Инструктируемая сумма, валюта */
  
  var field_value = "";
  if( InMesID > 0 )
    if( not ПолучитьПоле(InMesID, CurrencyOriginalOrderedAmountField, field_value) )
      InitError();
    end;
  else
    field_value = FillCodeCurrencyAndAmount(RsPaym, wlmes, CommissCode, InMesID);
  end;

  if( field_value and (not check) )
    WriteFieldEx( CurrencyOriginalOrderedAmountField, field_value, Recreate, OldwlMes );
  end;
  
  return field_value;
end;

macro WriteField36( IsTransit : bool, InMesID : integer, fld32A : string, fld33B : string, Recreate : bool, OldwlMes, check:bool ) : string
  
  var field_value = "";
  
  if( IsTransit and ( InMesID > 0 ) )
    if(not ПолучитьПоле( InMesID, ExchangeRateField, field_value ))
      InitError();
    end;
  else 
    field_value = FillConversionRate(wlpmpaym, fld33B, fld32A);
  end;
  
  if( field_value and (not check) )
    WriteFieldEx( ExchangeRateField, field_value, Recreate, OldwlMes );
  end;
  
  return field_value;
end;
