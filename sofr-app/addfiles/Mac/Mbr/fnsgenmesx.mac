/*
  $Name:         fnsgenmesx.mac
  $Module:       МБР
  $Description:  Генерация сообщений транспорта ФНС в формате XML
*/
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщений транспорта ФНС в формате XML (общие функции)
//
// Программист  : Чукина Т.А.
//
// Создан       : 30.06.2014
//
//-----------------------------------------------------------------------------

import "wlmnstls.mac", "bnk_ptlib.mac", "wlgenmes.mac", "xmlmestools.mac", "likepy.mac";

private macro FillFIO(arrFIO : TArray, NameF : @string, NameI : @string, NameO : @string)
  var NamesCount : integer = arrFIO.size;

  if(NamesCount >= 1)
    NameF = arrFIO[0];
  end;
  if(NamesCount >= 2)
    NameI = arrFIO[1];
  end;
  if(NamesCount >= 3)
    NameO = arrFIO[2];
  end;    
end;

macro ЗаписатьФайл(ТипИнф : string)
  ЗаписатьПолеЛог("ИдЭС",       GetGuidFnsX());
  ЗаписатьПолеЛог("ТипИнф",     ТипИнф);
  ЗаписатьПолеЛог("ВерсПрог",   "RS-Bank V.6");
  ЗаписатьПолеЛог("ТелОтпр",    FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ДолжнОтпр",  FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ФамОтпр",    FILL_AT_EXPORT);
  ЗаписатьПолеЛог("ВерсФорм",   "3.00");
end;

// Фамилия, имя, отчество физического лица (ФИОТип) - получение данных
// true - удалось заполнить поля, false - не удалось
macro GetDataFIO
( PartyID : integer, 
  NameF : @string, 
  NameI : @string, 
  NameO : @string, 
  ErrMsg : @string,
  reqchnga,
  reqopena,
  reqclosa,
  IsOld : bool

) : bool
  var arrFIO : TArray;
  if( reqchnga != NULL )
    arrFIO = split( GetReqClientFld(reqchnga, "ClientName", IsOld), "," );
    FillFIO(arrFIO, @NameF, @NameI,@NameO);
    NameF = trim(NameF);
    NameI = trim(NameI);
    NameO = trim(NameO);
  elif(reqopena != NULL)
    arrFIO = split( GetReqClientFld(reqopena, "ClientName", IsOld), "," );
    FillFIO(arrFIO, @NameF, @NameI,@NameO);
    NameF = trim(NameF);
    NameI = trim(NameI);
    NameO = trim(NameO);
  elif(reqclosa != NULL)
    arrFIO = split( GetReqClientFld(reqclosa, "ClientName", IsOld), "," );
    FillFIO(arrFIO, @NameF, @NameI,@NameO);
    NameF = trim(NameF);
    NameI = trim(NameI);
    NameO = trim(NameO);
  elif( not GetPersonNames(PartyID, @NameF, @NameI, @NameO) )
    ErrMsg = "Не найдены данные физ.лица  ИД = " + PartyID;
    return FALSE;
  end;

  // обязательные поля в ФИОТип
  if(not NameF)
    ErrMsg = "Не задана фамилия физ.лица  ИД = " + PartyID;
    return FALSE;
  end;

  if(not NameI)
    ErrMsg = "Не задано имя физ.лица  ИД = " + PartyID;
    return FALSE;
  end;

  return TRUE;
end;

// Фамилия, имя, отчество физического лица (ФИОТип) - запись в сообщение
// Передаваемые данные должны быть уже проеверены!
macro WriteFIO
( BlockName : string, 
  F : string, 
  I : string, 
  O : string
)
  // запись полей
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("Фамилия", F);
  ЗаписатьПолеЛог("Имя", I);
  if(O)
    ЗаписатьПолеЛог("Отчество", O);
  end;

  FinishBlockLogXML(BlockName);

  return TRUE;
end;

private macro SelectPaper(PersonID: integer, PaperKind: @integer, PaperCode: @string):bool
  const ALLPAPERKIND : integer = -1,
        PAPERKIND_PASSPORT_RF = 0;

  PaperKind = ALLPAPERKIND; PaperCode = "";

  var q : string =
    "select persnidc.t_PaperKind, paprkind.t_CodeDocum, paprkind.t_Name "
    "  from dpersnidc_dbt persnidc, dpaprkind_dbt paprkind "
    " where persnidc.t_PaperKind = paprkind.t_PaperKind and persnidc.t_PersonID = :PersonID ";
  var rs : RsdRecordset = execSQLselectPrm(q, SQLParam("PersonID", PersonID));

  var НайденаПодстрокаПаспорт : bool = false, 
      НайденаПодстрокаУдостоверение : bool = false,
      PaperKindName : string = "";
  while( rs and rs.moveNext() )
    if( rs.value("t_PaperKind") == PAPERKIND_PASSPORT_RF )
      PaperKind = rs.value("t_PaperKind");
      PaperCode = rs.value("t_CodeDocum");
      break; // паспорт РФ имеет первый приоритет, поэтому, найдя его, прекращаем поиск
    end;

    PaperKindName = StrLwr( rs.value("t_Name") ); // без учета регистра
    if(not НайденаПодстрокаПаспорт)
      if( index(PaperKindName, "паспорт") )
        PaperKind = rs.value("t_PaperKind");
        PaperCode = rs.value("t_CodeDocum");
        НайденаПодстрокаПаспорт = true;
      elif( not НайденаПодстрокаУдостоверение )
        if( index(PaperKindName, "удостовер") AND rs.value("t_CodeDocum") )
          PaperKind = rs.value("t_PaperKind");
          PaperCode = rs.value("t_CodeDocum");
          НайденаПодстрокаУдостоверение = true;
        end;
      end;
    end;

    // Любой другой документ - в последнюю очерель 
    var PapCode:integer = int(rs.value("t_CodeDocum"));
    if( not НайденаПодстрокаПаспорт and 
        not НайденаПодстрокаУдостоверение
      )
      if( ( (PapCode >= 1) AND (PapCode <= 15) ) OR ( (PapCode >= 21) AND (PapCode <= 24) ) OR InList(PapCode,18,26,27,81) )
        PaperCode = rs.value("t_CodeDocum");
      else
        PaperCode = "91";
      end;
      PaperKind = rs.value("t_PaperKind");
    end;
  end;

  if( strlen(PaperCode) > 2 )
    PaperCode = substr( PaperCode, 1, 2 );
  end;
  if(PaperKind == ALLPAPERKIND)
    return FALSE;
  end;

  return TRUE;
end;

macro GetPersnIdcInfo
( PersonID : integer, 
  PaperKind : integer, 
  PaperSerNum : @string, 
  PaperDate : @date
) : bool

  var query : string =
    "select DECODE( t_PaperSeries, CHR(1), '', t_PaperSeries || ' ' ) || t_PaperNumber as PaperSerNum, "
    "       t_PaperIssuedDate as PaperDate "
    "  from dpersnidc_dbt "
    " where t_PersonID = :PersonID "
    "   and t_PaperKind = :PaperKind ";
  var rs : RsdRecordset = execSQLselectPrm( query, 
                                            SQLParam("PersonID", PersonID),
                                            SQLParam("PaperKind", PaperKind) );
  if( rs and rs.moveNext() )
    PaperSerNum = trim( rs.value("PaperSerNum") );
    PaperDate = date( rs.value("PaperDate") );
    return TRUE;
  end;

  return FALSE;
end;

macro GetPaperInfo
( PersonID : integer,
  PaperCode : @string,
  PaperSerNum : @string,
  PaperDate : @date
) : bool

  var PaperKind : integer;

  if( not SelectPaper(PersonID, @PaperKind, @PaperCode) )
    return FALSE;
  end;

  if( not GetPersnIdcInfo(PersonID, PaperKind, @PaperSerNum, @PaperDate) )
    return FALSE;
  end;

  return TRUE;
end;

// Адрес в Российской Федерации (АдрРФТип) - запись в сообщение
// Передаваемые данные должны быть уже проеверены!
macro WriteAddrRF(Addr : TAddrRF, BlockName : string)

  macro ЗаписатьПолеЛогЗагл(field_name, field_value)
    field_value = StrUpr(field_value);
    ЗаписатьПолеЛог(field_name, field_value);
  end;

  macro WriteAddrItemOptional(field_name : string, AddrItem : string)
    if(AddrItem)
      ЗаписатьПолеЛогЗагл(field_name, AddrItem);
    end;
  end;

  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛогЗагл("Индекс", Addr.Индекс);
  ЗаписатьПолеЛогЗагл("КодРегион", Addr.КодРегион);
  WriteAddrItemOptional("Район", Addr.Район);
  WriteAddrItemOptional("Город", Addr.Город);
  WriteAddrItemOptional("НаселПункт", Addr.НаселПункт);
  WriteAddrItemOptional("Улица", Addr.Улица);
  WriteAddrItemOptional("Дом", Addr.Дом);
  WriteAddrItemOptional("Корпус", Addr.Корпус);
  WriteAddrItemOptional("Кварт", Addr.Кварт);

  FinishBlockLogXML(BlockName);

  return true;
end;

// Сведения о налоговом органе
// true - удалось заполнить поля, false - не удалось
macro WriteTaxInstitution
( PartyID : integer, 
  PartyName: string,
  PartyCode: string,  
  BlockName : string, 
  ErrMsg : @string

) : bool

  var КодНО, НаимНО, Error;
  if (PartyID > 0)
    КодНО = GetTaxInstitutionCodeWithChecks(PartyID);
    НаимНО = Bnk_GetPartyName(PartyID);
  else
    КодНО = PartyCode;
    НаимНО = PartyName;
  end;

  // check
  if(not НаимНО)
    ErrMsg = "Не задано наименование налогового органа";
    return FALSE;
  end;

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("КодНО", КодНО);
  
  ЗаписатьПолеЛог("НаимНО", НаимНО);

  FinishBlockLogXML(BlockName);

  return TRUE;

end;

// Плательщик - организация
// true - удалось заполнить поля, false - не удалось
macro WritePayerInst
(
  BlockName : string, 
  PartyID : integer,
  INN : string,
  KPP : string,
  OrgName : string

) : bool

  // get data
  if( PartyID > 0 )
    if(not INN)
      GetINN_KPP(PartyID, @INN, @KPP);
    end;

    if(not OrgName)
      OrgName = Bnk_GetPartyShortName(PartyID);
    end;
  end;

  // check
  var ErrMsg : string = "";
  if(not INN)
    ErrMsg = "Не задан ИНН для субъекта \"" + OrgName + "\"";
    RunError(ErrMsg, ErrMsg);
  end;
  if(not IsGoodINN_Org(INN) and not IsGoodKIO(INN))
    ErrMsg = "Неверное значение ИНН: " + INN;
    RunError(ErrMsg, ErrMsg);
  end;

  if(not KPP)
    ErrMsg = "Не задан КПП для субъекта \"" + OrgName + "\"";
    RunError(ErrMsg, ErrMsg);
  end;
  if(not IsGoodKPP(KPP))
    ErrMsg = "Неверное значение КПП: " + KPP;
    RunError(ErrMsg, ErrMsg);
  end;

  if(not OrgName)
    ErrMsg = "Не задано наименование организации ИД = " + PartyID;
    RunError(ErrMsg, ErrMsg);
  end;

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННЮЛ", INN);
  
  ЗаписатьПолеЛог("КПП", KPP);

  ЗаписатьПолеЛог("НаимЮЛ", OrgName);

  FinishBlockLogXML(BlockName);

  return TRUE;

end;

private macro GetCommonDataForPayerPersn
(
  PartyID : integer,
  FioWithSpaces : string, // фамилия, имя, отчество, разделенные пробелами
  INN : @string,
  SurName : @string,
  Name : @string,
  ParentName : @string
)
  var ErrMsg : string = "";

  // ИНН
  if( (PartyID > 0) and not INN )
    INN = GetOnlyINN(PartyID);
  end;

  if(not INN)
    ErrMsg = "Не задан ИНН для субъекта " + FioWithSpaces;
    RunError(ErrMsg, ErrMsg);
  end;
  if(not IsGoodINN_Ind(INN))
    RunError("Неверное значение ИНН: " + INN);
  end;

  // ФИО
  if( (PartyID > 0) and not FioWithSpaces )
    // get and check
    if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg) )
      RunError(ErrMsg, ErrMsg);
    end;
  elif( FioWithSpaces )
    var arrFIO : TArray = split(FioWithSpaces, " ");
    var NamesCount : integer = arrFIO.size;

    if(NamesCount < 1)
      RunError("Не задана фамилия");
    elif(NamesCount < 2)
      RunError("Не задано имя");
    end;

    SurName = arrFIO[0];
    Name = arrFIO[1];
    if(NamesCount >= 3)
      ParentName = arrFIO[2];
    end;
  else
    RunError("Не заданы фамилия, имя, отчество");
  end;
end;

// Плательщик - ИП, нотариус, адвокат
macro WritePayerPersnEmployer
(
  BlockName : string, 
  PartyID : integer,
  INN : string,
  FioWithSpaces : string // фамилия, имя, отчество, разделенные пробелами
)
  var SurName : string = "", Name : string = "", ParentName : string = "";

  GetCommonDataForPayerPersn(PartyID, FioWithSpaces, @INN, @SurName, @Name, @ParentName);

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННИП", INN);
  
  WriteFIO("ФИО", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

end;

// Плательщик - физическое лицо (краткие данные)
macro WritePayerPersnShortData
(
  BlockName : string, 
  PartyID : integer,
  INN : string,
  FioWithSpaces : string // фамилия, имя, отчество, разделенные пробелами
)
  var SurName : string = "", Name : string = "", ParentName : string = "";

  GetCommonDataForPayerPersn(PartyID, FioWithSpaces, @INN, @SurName, @Name, @ParentName);

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННФЛ", INN);
  
  WriteFIO("ФИО", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

end;

// Плательщик - физическое лицо (Полные данные)
macro WritePayerPersn
(
  BlockName : string, 
  PartyID : integer,
  INN : string,
  FioWithSpaces : string // фамилия, имя, отчество, разделенные пробелами
)
  var ErrMsg : string = "";

  var SurName : string = "", Name : string = "", ParentName : string = "",
      BirthDate : date = date(0, 0, 0), BirthPlace : string = "",
      PaperCode : string = "", PaperSerNum : string = "", PaperDate : date = date(0, 0, 0),
      Addr : TAddrRF = TAddrRF(), IsAddr : bool = false;

  GetCommonDataForPayerPersn(PartyID, FioWithSpaces, @INN, @SurName, @Name, @ParentName);

  if( (PartyID > 0) AND GetDataAddrRF(PartyID, Addr, @ErrMsg) )
    IsAddr = true;
  end;

  // write
  StartBlockLogXML(BlockName);

  ЗаписатьПолеЛог("ИННФЛ", INN);
  
  if( GetBirthInfo(PartyID, @BirthDate, @BirthPlace) )
    if( BirthDate != date(0,0,0) )
      ЗаписатьПолеЛог("ДатаРожд", YYYY_MM_DD(BirthDate) );
    end;
    if( BirthPlace )
      ЗаписатьПолеЛог("МестоРожд", BirthPlace);
    end;
  end;

  if( GetPaperInfo(PartyID, @PaperCode, @PaperSerNum, @PaperDate) )
    ЗаписатьПолеЛог("КодДУЛ", PaperCode);
    ЗаписатьПолеЛог("СерНомДок", PaperSerNum);
    ЗаписатьПолеЛог("ДатаДок", DDpMMpYYYY(PaperDate));
  end;

  WriteFIO("ФИО", SurName, Name, ParentName);

  if(IsAddr)
    WriteAddrRF(Addr, "АдрПлат");
  end;

  FinishBlockLogXML(BlockName);

  return TRUE;

end;

// Руководитель (заместитель руководителя) налогового органа
// true - удалось заполнить поля, false - не удалось
macro WriteTaxOrgChief
( PartyID : integer, 
  BlockName : string, 
  ErrMsg : @string

) : bool

  var SurName : string = "", Name : string = "", ParentName : string = "";

  // get and check
  if( not GetDataFIO(PartyID, @SurName, @Name, @ParentName, @ErrMsg) )
    return FALSE;
  end;

  // write
  StartBlockLogXML(BlockName);

  // Алгоритм заполнения пока отсутсвует
  //ЗаписатьПолеЛог("КласЧин", КласЧин);

  WriteFIO("ФИО", SurName, Name, ParentName);

  FinishBlockLogXML(BlockName);

  return TRUE;

end;

macro FnsWriteSendNum(OldMes : StrucRef)
  var SendNum : string = "1";

  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();

  if(KindActionFNS == KIND_ACTION_FNS_CORRECT) //Корректировка
    if( not ПолучитьПоле (OldfnsMessageID, "_НомерОтправ", SendNum) )
      SendNum = "2";
      InitError();
    else
      SendNum = string( int(SendNum) + 1 );
    end;

  elif( (KindActionFNS == KIND_ACTION_FNS_RESEND) //Повторная отправка
        or
        (OldMes.MesID > 0) // Перегенерация
      )
    if(not ПолучитьПоле (OldfnsMessageID, "_НомерОтправ", SendNum))
      SendNum = "1";
      InitError();
    end;

  end;

  ЗаписатьПолеЛог( "_НомерОтправ", SendNum );
end;
