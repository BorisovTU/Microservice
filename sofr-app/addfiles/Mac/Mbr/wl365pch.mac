 /*
 $Name: wl365pch.mac
 $Module: МБР
 $Description: Проверки форматов сообщений ФНС по 365-П
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*               Проверки форматов сообщений ФНС по 365-П                   */
/*                                                                          */
/*  Имя файла: wl365pch.mac                                                 */
/*  Создан:  18.04.12                                    Popova O.          */
/****************************************************************************/

import "wlmnstls.mac", "wlimport.mac", Календарь, "pmlib.mac";
/*
 * Список ошибок при импорте сообщений из ФНС
 */
var ImportErrors:TArray = TArray();

/* Список проверяемых форм */
macro NeedCheck365P( FormName )
  return InList( FormName, ФормаRPO, ФормаROO, ФормаTRB, ФормаZNO, ФормаPNO, 
                 ФормаZSN, ФормаZSO, ФормаZSV/*, ФормаTRG - еще не реализовано */,
                 ФормаAPN, ФормаAPO, ФормаAPZ
               );
end;
/*
 * Проверяемые релизы
 */
private const RlsZNO = 350;
private const RlsRPO = 355;
private const RlsROO = 356;
private const RlsTRB = 357;
private const RlsPNO = 358;
/*
 * Ошибки при проверке полей
 */
private macro ErrFormatK( fname, fval )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в реквизите " + fname + " указано недопустимое значение кода \"" + fval + "\".";
end;

private macro ErrFormatN( fname, fval )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в значении реквизита " + fname + " использованы недопустимые для его формата символы \"" + fval + "\".";
end;

private macro ErrFormatD( fname, fval, what )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в реквизите " + fname + " указан" + what + " в неверном формате \"" + fval + "\".";
end;

private macro ErrFormatINN( fname, fval )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в реквизите " + fname + " ИНН не соответствует формату \"" + fval + "\".";
end;

private macro ErrFormatKPP( fname, fval )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в реквизите " + fname + " КПП не соответствует формату \"" + fval + "\".";
end;

private macro ErrFormatФИОИП( fname, fval )
  ImportErrors[ImportErrors.size] = "15;" + fname + ":" + fval + ":в реквизите " + fname + " отсутствует обязательный элемент \"фамилия\" и/или \"имя\"";
end;

/* Поиск поля с заданным name */
private macro getFld( flds:TArray, name:string )
  for( var i, 0, flds.size - 1 )
    if( flds.value(i).FieldName == name )
      return flds.value(i);
    end;
  end;
  RunError(); /*не нашли поля в списке полей релиза - даже и не знаю, что сказать.. */
end;

/*
 * Проверки полей ( true == все ок )
 */

/* Проверка условно обязательного реквизита - при выполнении условия cond он должен присутствовать и быть заполнен */
macro checkCondMandatoryReq( fld, cond:bool, descr:string ):bool
  if( ( cond == true ) and not fld.isPresent )
    ImportErrors[ImportErrors.size] = "15;не найден условно-обязательный реквизит " + fld.FieldName + " при выполненном условии - " + descr;
    return false;
  end;
  if( ( cond == true ) and ( fld.FieldValue == "" ) )
    ImportErrors[ImportErrors.size] = "15;не заполнен условно-обязательный реквизит " + fld.FieldName + " при выполненном условии - " + descr;
    return false;
  end;
  return true;
end;

/* Проверки на соответствие различным форматам полей */
/* Формат N(число) - помимо цифр допускаются еще '.' и '-' */
private macro checkN( fld ):bool
  return IsDigitalNumber( StrSubSt( StrSubSt( fld.FieldValue, ".", "" ), "-", "" ) ) == 0;
end;
/* Формат D - строка вида DD.MM.YYYY */
private macro checkDate( fld ):bool
  /* общая проверка строки на соответствие формату DD.MM.YYYY*/
  if( ( strlen( fld.FieldValue ) != 10 ) or
      ( IsDigitalNumber( StrSubSt( fld.FieldValue, ".", "" ) ) != 0 ) or
      ( SubStr( fld.FieldValue, 3, 1 ) != "." ) or
      ( SubStr( fld.FieldValue, 6, 1 ) != "." ) )
    ErrFormatD( fld.FieldName, fld.FieldValue, "а дата" );
    return false;
  end;
  /* проверка года */
  if( IsDigitalNumber( SubStr( fld.FieldValue, 7 ) ) != 0 )
    ErrFormatD( fld.FieldName, fld.FieldValue, " год" );
    return false;
  end;
  /* проверка месяца */
  if( not InList( SubStr( fld.FieldValue, 4, 2 ), "01", "02", "03", "04", "05", "06", "07", "08", "09", "10", "11", "12" ) )
    ErrFormatD( fld.FieldName, fld.FieldValue, " месяц" );
    return false;
  end;
  /* проверка дня */
  if( ( IsDigitalNumber( SubStr( fld.FieldValue, 1, 2 ) ) != 0 ) or
      ( int( SubStr( fld.FieldValue, 1, 2 ) ) < 1 ) or
      ( int( SubStr( fld.FieldValue, 1, 2 ) ) > GetDaysInMonth( date( 1, int( SubStr( fld.FieldValue, 4, 2 ) ), int( SubStr( fld.FieldValue, 7 ) ) ) ) ) )
    ErrFormatD( fld.FieldName, fld.FieldValue, " день" );
    return false;
  end;
  return true;
end;

/* Формат T* - текстовое поле с определенным набором допустимых/недопустимых символов */
/* Допустимые символы */
private const UpperCaseLat = "ABCDEFGHIJKLNMOPQRSTUVWXYZ";
private const UpperCaseRus = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЬЫЪЭЮЯ";
private const Numbers      = "1234567890";
private const T2Format = UpperCaseLat + UpperCaseRus + Numbers + "- ";
/* Проверка строки на наличие только допустимых символов */
private macro IsValidStr( Str:string, ValidChars:string )
  var stat = 0, i = 1, ch;
    while( ( not stat ) and ( i <= strlen( Str ) ) )
      ch = SubStr( Str, i, 1 );
      if( not Index( ValidChars, ch ) )
        stat = 1;
      end;
      i = i + 1;
    end;
  return stat;
end;

/* Функции проверки конкретных полей в формате "check" + FieldName */
private macro checkКодОснов( flds:TArray, i:integer ):bool
    if( not existsSQLselect( "select 1 from dwlcodes_dbt where t_algnum = 55 and t_code = :code", makeArray( SQLParam( "code", flds.value(i).FieldValue ) ) ) )
      ErrFormatK( flds.value(i).FieldName, flds.value(i).FieldValue );
      return false;
    end;
    return true;
end;

private macro checkСумВзыск( flds:TArray, i:integer ):bool
  if( flds.value(i).isPresent and ( flds.value(i).FieldValue != "" ) and not checkN( flds.value(i) ) )
    ErrFormatN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkДатаРешПр( flds:TArray, i:integer ):bool
  return checkDate( flds.value(i) );
end;

private macro checkДатаРешОт( flds:TArray, i:integer ):bool
  return checkDate( flds.value(i) );
end;

private macro checkДатаПоруч( flds:TArray, i:integer ):bool
  return checkDate( flds.value(i) );
end;

private macro checkДатаНач( flds:TArray, i:integer ):bool
  if( not checkCondMandatoryReq( flds.value(i), getFld( flds, "ВидЗапр" ).FieldValue == 3, "ВидЗапр = 3" ) )
    return false;
  end;
  if( flds.value(i).FieldValue != "" )
    return checkDate( flds.value(i) );
  end;
  return true;
OnError
  return false;
end;

private macro checkДатаКон( flds:TArray, i:integer ):bool
  return checkДатаНач( flds, i );
end;

private macro checkДатаТреб( flds:TArray, i:integer ):bool
  return checkDate( flds.value(i) );
end;

private macro checkСрокИсп( flds:TArray, i:integer ):bool
  return checkDate( flds.value(i) );
end;

private macro checkИНННП( flds:TArray, i:integer ):bool
  if( not InList( StrLen( flds.value(i).FieldValue ), 5, 10, 12 ) or not checkN( flds.value(i) ) )
    ErrFormatINN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkКППНП( flds:TArray, i:integer ):bool
  if( not checkCondMandatoryReq( flds.value(i), ( InList( StrLen( getFld( flds, "ИНННП" ).FieldValue ), 5, 10 ) ), "длина ИНННП = 10 символов" ) )
    return false;
  end;
  if( ( flds.value(i).FieldValue != "" ) and ( ( StrLen( flds.value(i).FieldValue ) != 9 ) or not checkN( flds.value(i) ) ) )
    ErrFormatKPP( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
OnError
  return false;
end;

private macro checkКППЮЛ( flds:TArray, i:integer ):bool
  return checkКППНП( flds, i );
end;

private macro checkНаимНП( flds:TArray, i:integer ):bool
  return checkCondMandatoryReq( flds.value(i), InList( StrLen( getFld( flds, "ИНННП" ).FieldValue ), 5, 10 ), "длина ИНННП = 10 символов" );
OnError
  return false;
end;

private macro checkФИОИП( flds:TArray, i:integer ):bool
  var LastName  = SubStr( flds.value(i).FieldValue, 1, Index( flds.value(i).FieldValue, "," ) - 1 ); /* фамилия - все, что до первой запятой */
  var FirstName = SubStr( flds.value(i).FieldValue, Index( flds.value(i).FieldValue, "," ) + 1,      /* имя - все, что между первой и второй запятой */
                          Index( SubStr( flds.value(i).FieldValue, Index( flds.value(i).FieldValue, "," ) + 1 ), "," ) - 1 ); 
  if( flds.value(i).isPresent and ( flds.value(i).FieldValue != "" ) and  /* если поле есть и заполнено */
      ( ( StrSubSt( LastName , " ", "" ) == "" )    or   /* фамилия должна быть задана  */
        ( StrSubSt( FirstName, " ", "" ) == "" )    or   /* имя должно быть задано      */
        ( IsValidStr( StrUpr( SubStr( flds.value(i).FieldValue, 1, 1 ) ), UpperCaseLat + UpperCaseRus ) != 0  ) or /* первый символ - только буква */
        ( IsValidStr( StrUpr( LastName  ), T2Format ) != 0 )  or   /* фамилия должна соответствовать формату Т2    */
        ( IsValidStr( StrUpr( FirstName ), T2Format ) != 0 ) ) )   /* имя должно соответствовать формату Т2        */
    ErrFormatФИОИП( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  if( not checkCondMandatoryReq( flds.value(i), ( StrLen( getFld( flds, "ИНННП" ).FieldValue ) == 12 ), "длина ИНННП = 12 символов" ) )
    return false;
  end;
  return true;
OnError
  return false;
end;

private macro checkНомФ( flds:TArray, i:integer ):bool
  if( not checkN( flds.value(i) ) )
    ErrFormatN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkВидРеш( flds:TArray, i:integer ):bool
  if( not InList( flds.value(i).FieldValue, 1, 2 ) )
    ErrFormatK( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkНомПоруч( flds:TArray, i:integer ):bool
  if( not checkN( flds.value(i) ) )
    ErrFormatN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkСумПоруч( flds:TArray, i:integer ):bool
  if( not checkN( flds.value(i) ) )
    ErrFormatN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkОчерПл( flds:TArray, i:integer ):bool
  var maxpriority:integer = PM_DefaultMaxPriority();
  if( ( flds.value(i).FieldValue < 0 ) or ( flds.value(i).FieldValue > maxpriority ) )
    ErrFormatK( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkВидЗапр( flds:TArray, i:integer ):bool
  if( not InList( flds.value(i).FieldValue, 1, 2, 3 ) )
    ErrFormatK( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkТипЗапр( flds:TArray, i:integer ):bool
  if( not InList( flds.value(i).FieldValue, 1, 2 ) )
    ErrFormatK( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;

private macro checkСуммаПлат( flds:TArray, i:integer ):bool
  if( not checkN( flds.value(i) ) )
    ErrFormatN( flds.value(i).FieldName, flds.value(i).FieldValue );
    return false;
  end;
  return true;
end;
/* Поле обязательно? */
private macro FieldFlagMandatory( FieldID ):string
  var rs:RsdRecordset = execSQLselect( "select t_MandatoryFlag from dwlmesfld_dbt where t_TpFieldID = :fieldid", makeArray( SQLParam( "fieldid", FieldID ) ), TRUE );
  if( rs and rs.moveNext() )
    return rs.value(0);
  end;
  return "";
end;
/* Блок есть в сообщении? (т.е. есть ли в сообщении хотя бы одно поле из этого блока)*/
private macro BlockIsPresent( BlockID ):string 
  /* Все наименования полей блока склеим в одну строку с разделителем : */
  var BlockFields = "";                        
  var rs:RsdRecordset = execSQLselect( "select t_Name from dwltpfld_dbt where t_TpFieldID in( select t_TpFieldID from dwlmesfld_dbt where t_Master = :blockid )", makeArray( SQLParam( "blockid", BlockID ) ), TRUE );
  if( rs )                                  
    while( rs.moveNext() )
      BlockFields = BlockFields + rs.value(0) + ":";
    end;
  end;

  if( BlockFields == "" )
    return "";
  end;
  /* Есть ли в файле хотя бы одно поле блока? */
  var Строка      = "", 
      Длина       = 0,
      result      = "";
  
  var nStr = ТекущаяСтрока(); /* Запомним текущую позицию, чтобы потом вернуться */

  ПерейтиВНачалоФайла();
  while( result == "" )
    Длина = МаксимальнаяДлинаСтроки;
    Строка = СчитатьСтроку( Длина );                    
    if( ( Строка == МНС_КонецФайла ) or ( Длина == -1 ) ) 
      break; /* все, выходим */ 
    end;
    var FName = SubStr( Строка, 1, Index( Строка, ":" ) - 1 );
    if( ( FName != "" ) and ( Index( BlockFields, FName ) != 0 ) ) 
      result = "X";
    end;  
  end;
  /* Вернемся на исходную позицию */
  ПерейтиВНачалоФайла();
  while( nStr > 1 )
    Строка = СчитатьСтроку( Длина );
    nStr = nStr - 1;
  end;
  return result;
end;
/* Список полей релиза с данными для проверки */
class (TArray) FldRlsListForCheck( RlsID:integer )
  
/* Запись поля релиза и необходимые для проверки реквизиты */
private class FldRecord( _fName, _mFlag, _fSize )

  var FieldName      = _fName;  /* Наименование поля */
  var FieldValue     = "";      /* Значение поля */
  var FieldSize      = _fSize;  /* Размер поля - какой должен быть */
  var MandatoryFlag  = _mFlag;  /* Флаг обязательности поля */
  var isPresent      = false;   /* Признак "Поле есть в сообщении" */
  var needAddChecks  = false;   /* Необходимость доп.проверок поля */
end;

  var m_RlsID;
  InitTArray; 
  private macro Init( RlsID:integer )
    /* В конструкторе отберем все поля релиза */
    m_RlsID = RlsID;
    private var select:string = "SELECT tpfld.t_Name, mesfld.t_MandatoryFlag, tpfld.t_Size, mesfld.t_Master "
                      + "FROM dwlmesfld_dbt mesfld, "
                      +      "dwltpfld_dbt tpfld "
                      + "WHERE mesfld.t_RlsFormID = :RlsID "
                      +   "AND mesfld.t_TpFieldID = tpfld.t_TpFieldID ";
    
    private var params:TArray = makeArray( SQLParam("RlsID", RlsID ) );
    private var rs:RsdRecordset = execSQLselect( select, params, FALSE );
    while( rs.MoveNext() )
      var FieldIsMandatory = false;                              /* Поле обязательно, если            */
      if( rs.value(3) == 0 )                                     /* оно не входит в блок              */
        FieldIsMandatory = rs.value(1);                          /* и установлен MandatoryFlag        */
      else                                                       /* или оно входит в блок             */
        FieldIsMandatory = FieldFlagMandatory( rs.value(3) );    /* и (блок обязателен                */
        if( FieldIsMandatory == "" ) 
          FieldIsMandatory = BlockIsPresent( rs.value(3) );      /* или присутствует в сообщении)     */
        end; 
      end;                 
      value(this.size) = FldRecord( rs.value(0), FieldIsMandatory, rs.value(2) );
    end;
  end;

  /* Заполнение дополнительных данных поля */
  macro InsertFieldData( fName:string, fValue:string )
    private var i = 0; 
    while( ( i < this.size ) and ( fName != value(i).FieldName ) )
      i = i + 1;
    end;
    if( ( i < this.size ) and ( fName == value(i).FieldName ) )
      value(i).isPresent  = true;
      value(i).FieldValue = fValue;
    end;
  end;

  /* Список полей релизов, для которых реализованы проверки */
  private macro needAddChecksField( FieldName ):bool
    return 
    (
      (   ( FieldName == "КодОснов"     ) and ( InList( m_RlsID, RlsRPO ) ) )                            or
      (   ( FieldName == "СумВзыск"     ) and ( InList( m_RlsID, RlsRPO ) ) )                            or
      (   ( FieldName == "ДатаРешПр"    ) and ( InList( m_RlsID, RlsRPO, RlsROO ) ) )                    or
      (   ( FieldName == "ИНННП"        ) /* для всех релизов */ )                                       or
      ( ( ( FieldName == "КППНП"        ) or 
          ( FieldName == "КППЮЛ"        ) ) and ( InList( m_RlsID, RlsZNO, RlsRPO, RlsROO, RlsTRB ) ) )  or
      (   ( FieldName == "НаимНП"       ) and ( InList( m_RlsID, RlsZNO, RlsRPO, RlsROO ) ) )            or
      (   ( FieldName == "ФИОИП"        ) and ( InList( m_RlsID, RlsZNO, RlsRPO, RlsROO ) ) )            or
      (   ( FieldName == "НомФ"         ) /* для всех релизов */ )                                       or
      (   ( FieldName == "ДатаРешОт"    ) and ( InList( m_RlsID, RlsROO ) ) )                            or
      (   ( FieldName == "ВидРеш"       ) and ( InList( m_RlsID, RlsROO ) ) )                            or
      (   ( FieldName == "НомПоруч"     ) and ( InList( m_RlsID, RlsTRB, RlsPNO ) ) )                    or
      (   ( FieldName == "ДатаПоруч"    ) and ( InList( m_RlsID, RlsTRB, RlsPNO ) ) )                    or
      (   ( FieldName == "СумПоруч"     ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "НомСчПл"      ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "НомСчБПл"     ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "НомСчПол"     ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "НомСчБПол"    ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "ОчерПл"       ) and ( InList( m_RlsID, RlsPNO ) ) )                            or
      (   ( FieldName == "ВидЗапр"      ) and ( InList( m_RlsID, RlsZNO ) ) )                            or
      (   ( FieldName == "ТипЗапр"      ) and ( InList( m_RlsID, RlsZNO ) ) )                            or
      ( ( ( FieldName == "ДатаНач"      ) or 
          ( FieldName == "ДатаКон"      ) ) and ( InList( m_RlsID, RlsZNO ) ) )                          or
      (   ( FieldName == "ДатаТреб"     ) and ( InList( m_RlsID, RlsTRB ) ) )                            or
      (   ( FieldName == "СуммаПлат"    ) and ( InList( m_RlsID, RlsTRB ) ) )                            or
      (   ( FieldName == "СрокИсп"      ) and ( InList( m_RlsID, RlsTRB ) ) )                   
    );
  end;

  /* Проверки всего массива полей */
  macro CheckFields()
    Bnk_ToRSTrace( "FldRlsListForCheck", "CheckFields", "Начало проверки" );
    var saveErrSize : integer = ImportErrors.size;

    private var i = 0;
    while( i < this.size ) 
      if( value(i).MandatoryFlag == "X" ) 
        /* Присутствие обязательных полей */
        if( not value(i).isPresent )
          ImportErrors[ImportErrors.size] = "15;Не найден обязательный реквизит " + value(i).FieldName;
          i = i + 1;
          continue;  /* по каждому полю фиксируем только одну ошибку */
        /* Заполненность обязательных полей */
        elif( value(i).FieldValue == "" )
          ImportErrors[ImportErrors.size] = "15;"+ value(i).FieldName + "::не заполнен обязательный реквизит " + value(i).FieldName;
          i = i + 1;
          continue;
        end;
      end;
      /* Соответствие длины максимально допустимой для релиза */
      if( strlen( value(i).FieldValue ) > value(i).FieldSize )
        ImportErrors[ImportErrors.size] = "15;"+ value(i).FieldValue + ":Количество символов в реквизите " + value(i).FieldName + 
                                          " превышает максимально допустимое значение";
          i = i + 1;
        continue;
      end;
      /* Дополнительные проверки для избранных */
      if( ( needAddChecksField( value(i).FieldName ) ) and 
          ( ExecMacro2( string( "check" + value(i).FieldName ), this, i ) == false ) )
        i = i + 1;
        continue;
      end;
      i = i + 1;
    end;

    Bnk_ToRSTrace( "FldRlsListForCheck", "CheckFields", "Проверка окончена. Обнаружено ошибок: " + ( ImportErrors.size - saveErrSize ) );
  end;

  Init( RlsID );
end;

/* проверка строки на соответствие формату YYYYMMDD - true, если подходит */
private macro CheckStrOnYYYYMMDD( str ):bool
  return ( strlen( str ) == 8 ) and
         ( IsDigitalNumber( SubStr( str, 1, 4 ) ) == 0 ) and
         ( IsDigitalNumber( SubStr( str, 5, 2 ) ) == 0 ) and ( int( SubStr( str, 5, 2 ) ) >= 1 ) and ( int( SubStr( str, 5, 2 ) ) <= 12 ) and
         ( IsDigitalNumber( SubStr( str, 7, 2 ) ) == 0 ) and ( int( SubStr( str, 7, 2 ) ) >= 1 ) and ( int( SubStr( str, 7, 2 ) ) <= 31 );
end;

/* Импорт сообщения по форме ERROR вместо загружаемого в случае, если не пройдены проверки и был заполнен список ошибок  */
macro ImportERROR( ПолноеИмяФайла, Транспорт, FormName, isNew:bool )
  Bnk_ToRSTrace( "ImportERROR", "Begin", 
                 "ПолноеИмяФайла: " + ПолноеИмяФайла + ", " +
                 "Транспорт: " + Транспорт + ", " +
                 "FormName: " + FormName + ", " +
                 "isNew: " + string(isNew)
               );

  var error = 0, Релиз, ВидСообщения, ИмяФайла, Расширение;
                   /* исключаем символы '_' и БИК(7 символов перед первым '_' ) */
  var RespID  = ПолучитьКодСубъекта( {MFO_RCC}, PTCK_BIC, error );
  SplitFile( ПолноеИмяФайла, ИмяФайла, Расширение );
  var TRN     = StrSubst( SubStr( ИмяФайла, 1, index( ИмяФайла, "_" ) - 8 ) + SubStr( ИмяФайла, index( ИмяФайла, "_" ) + 1 ), "_" ,"" );
  var Форма   = ОпределитьФорму( Транспорт, FormName, ВидСообщения );
  if( Форма == -1 )
    ErrImport( string( "Не найдена форма с кодом ", FormName ) );
    return false;
  end;

  var TpShemID  = ОпределитьТранспортнуюСхему( RespID, -1, -1, Транспорт, Форма, Релиз, NULL, NULL, wlsess.TpFrmtID );
  if( TpShemID == -1 ) 
    Bnk_ToRSTrace( "ImportERROR", "", "Не удалось определить транспортную схему для респондента " + RespID + ", транспорта " + Транспорт + ", формы " + Форма );
    return false;      
  end;
  if( not CheckOnDoubling( TRN, Релиз ) )
    return false;
  end;
  /* Заполняем ответное сообщение */
  if( isNew )
    ClearRecord( wlmes );
  end;
  wlmes.TpSchemID              = TpShemID;
  wlmes.RlsFormID              = Релиз;
  wlmes.OutsideAbonentID       = RespID;
  wlmes.AgentID                = wlmes.OutsideAbonentID;
  wlmes.OutsideAbonentCodeKind = PTCK_BIC;
  wlmes.AgentCodeKind          = wlmes.OutsideAbonentCodeKind;
  wlmes.OutsideAbonentCode     = {MFO_RCC};
  wlmes.AgentCode              = wlmes.OutsideAbonentCode;
  wlmes.Kind                   = ВидСообщения;
  wlmes.TRN                    = TRN;  
  if( not CheckStrOnYYYYMMDD( SubStr( ИмяФайла, index( SubStr( ИмяФайла, index( ИмяФайла, "_" ) + 1 ), "_" ) + index( ИмяФайла, "_" ) - 8, 8 ) ) )
    wlmes.OutsideAbonentDate   = date();
  else
    wlmes.OutsideAbonentDate   = ToDateMNS( SubStr( ИмяФайла, index( SubStr( ИмяФайла, index( ИмяФайла, "_" ) + 1 ), "_" ) + index( ИмяФайла, "_" ) - 8, 8 ) );
  end;
  wlmes.Importance             = 0;
  wlmes.Department             = {OperDprt};
  if( isNew and not СоздатьЗапись( wlmes ) )
    ErrImport( string( "Невозможно создать запись по форме ", FormName ) );
    return false;
  elif( not isNew )
    ОбновитьЗапись( wlmes );
  end;

  Bnk_ToRSTrace( "ImportERROR", "End", "" );
  return true;
end;

/* Импорт сообщения по форме ERROR_300 вместо загружаемого в случае, если не пройдены проверки и был заполнен список ошибок  */
macro ImportERROR_XML( FileFullName, Tp, FormName )
  Bnk_ToRSTrace( "ImportERROR_XML", "Begin", 
                 "ПолноеИмяФайла: " + FileFullName + ", " +
                 "Транспорт: " + Tp + ", " +
                 "FormName: " + FormName
               );


  var error = 0, RlsFormID , MesKind, FileName, Extension;
  var RespID  = ПолучитьКодСубъекта( {MFO_RCC}, PTCK_BIC, error );
  SplitFile( FileFullName, FileName, Extension );

  var idx = index( FileName, "_");
  
  var TRN = IfThenElse(idx > 0, StrSubst(SubStr(FileName, 1, idx - 9), "_", "") + StrSubst(SubStr(FileName, idx), "_", ""),
          StrSubst(FileName, "_", ""));
 
  var Form   = ОпределитьФорму( Tp, FormName, MesKind );
  if( Form == -1 )
    ErrImport( string( "Не найдена форма с кодом ", FormName ) );
    return false;
  end;

  var TpShemID  = ОпределитьТранспортнуюСхему( RespID, -1, -1, Tp, Form, RlsFormID , NULL, NULL, wlsess.TpFrmtID );
  if( TpShemID == -1 ) 
    ErrImport( "Не определены параметры обмена" );
    Bnk_ToRSTrace( "ImportERROR_XML", "", "Не удалось определить транспортную схему для респондента " + RespID + ", транспорта " + Tp + ", формы " + Form );
    return false;      
  end;
  
  var query =
    "select 1 from dwlmes_dbt mes where mes.t_Direct = :Direct and mes.t_Department = :OperDprt and mes.t_TRN = :Trn and mes.t_RlsFormID = :RlsFormID ";
    
  var rs = execSQLselect( query, makeArray(
                                            SQLParam("Direct", "X"),
                                            SQLParam("OperDprt", {OperDprt} ),
                                            SQLParam("Trn", TRN ),
                                            SQLParam(RlsFormID, RlsFormID ) ), false );
  
  if(rs and (rs.MoveNext()) )
    ErrImport( "Сообщение уже было принято - игнорируется" );
    return false;
  end; 
  
  ClearRecord( wlmes );
  
  wlmes.TpSchemID              = TpShemID;
  wlmes.RlsFormID              = RlsFormID;
  wlmes.OutsideAbonentID       = RespID;
  wlmes.AgentID                = wlmes.OutsideAbonentID;
  wlmes.OutsideAbonentCodeKind = PTCK_BIC;
  wlmes.AgentCodeKind          = wlmes.OutsideAbonentCodeKind;
  wlmes.OutsideAbonentCode     = {MFO_RCC};
  wlmes.AgentCode              = wlmes.OutsideAbonentCode;
  wlmes.Kind                   = MesKind;
  wlmes.TRN                    = TRN;  
  if( not CheckStrOnYYYYMMDD( SubStr( FileName, index( SubStr( FileName, index( FileName, "_" ) + 1 ), "_" ) + index( FileName, "_" ) - 8, 8 ) ) )
    wlmes.OutsideAbonentDate   = {curdate};
  else
    wlmes.OutsideAbonentDate   = ToDateMNS( SubStr( FileName, index( SubStr( FileName, index( FileName, "_" ) + 1 ), "_" ) + index( FileName, "_" ) - 8, 8 ) );
  end;
  wlmes.Importance             = 0;
  wlmes.Department             = {OperDprt};
  
  if( not СоздатьЗапись( wlmes ) )
    ErrImport( string( "Невозможно создать запись по форме ", FormName ) );
    return false;
  end;

  Bnk_ToRSTrace( "ImportERROR_XML", "End", "" );
  return true;
end;

/*
 * Структура имени файла сообщений по 365-П, поступающих из НО
 * XXXabbbbbbb_KKKKDDDDDDDD_NNNNNN.txt
 *где:
 * XXX       - условные символы для обозначения типа сообщения
 * a         - признак первичного(1) или исправленного(2-9) файла
 * bbbbbbb   - код банка, через который направляется сообщение
 * KKKK      - код налогового органа, направившего сообщение, по СОУН
 * DDDDDDDD  - дата формирования сообщения
 * NNNNNN    - порядковый номер сообщения
 */
macro ПроверкаФорматаИмениФайла365П( ИмяФайла:string, isXML:bool )
  Bnk_ToRSTrace( "ПроверкаФорматаИмениФайла365П", "Begin", "ИмяФайла: " + ИмяФайла, "IsXML: " + string(isXML) );
   
  /* Проверка идет до первой найденной ошибки только для txt */
  var error = 0, needCheckBIC = false;
  var LenFName : string = strlen( ИмяФайла );

  /* Общее количество символов в имени файла без расширения == 31 */
  if( LenFName != 31 )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";неверное количество символов в имени файла " + ИмяФайла;
    if(not isXML)
      return;
    end;
  end;

  /*В позициях 12 и 25 указан символ "_" */
  if( (LenFName >= 12) and ( SubStr( ИмяФайла, 12, 1 ) != "_" ) 
      or 
      (LenFName >= 25) and ( SubStr( ИмяФайла, 25, 1 ) != "_" ) 
    )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";неверное значение в символе " + IfThenElse( SubStr( ИмяФайла, 12, 1 ) == "_", 25, 12 ) + 
                                      ". Должен быть символ \"_\"";
    if(not isXML)
      return;
    end;
  end;

  /* Символ в 4 позиции имени файла цифровой, != 0 */
  if( (LenFName >= 4) and 
      ( ( IsDigitalNumber( SubStr( ИмяФайла, 4, 1 ) ) != 0 ) or ( int( SubStr( ИмяФайла, 4, 1 ) ) == 0 ) )
    )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";в символе 4 имени файла указано неверное значение " + SubStr( ИмяФайла, 4, 1 );
    if(not isXML)
      return;
    end;
  end;

  /* Символы в позициях 5-11 должны быть 3-9 символами БИКа филиала операциониста или хотя бы просто цифровыми - в зависимости от настройки */
  GetRegistryValue( "PS\\СООБЩЕНИЯ ФНС\\ПРОВЕРКА ФОРМАТА\\ИМЯ ФАЙЛА\\БИК", V_BOOL, needCheckBIC, error );
  if( (LenFName >= 5) and 
      (
        (     needCheckBIC and ( SubStr( ИмяФайла, 5, 7 ) != SubStr( {MFO_Bank}, 3, 7 ) ) ) 
        or
        ( not needCheckBIC and ( IsDigitalNumber( SubStr( ИмяФайла, 5, 7 ) ) != 0 ) ) 
      )
    )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";в символах 5-11 имени файла указан БИК не того банка, в который поступило сообщение " + SubStr( ИмяФайла, 5, 7 );
    if(not isXML)
      return;
    end;
  end;

  /* Символы в позициях 13-16 имени файла являются цифрами */
  if( (LenFName >= 13) and (IsDigitalNumber( SubStr( ИмяФайла, 13, 4 ) ) != 0) )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";в символах 13-16 имени файла указан не код налогового органа по СОУН " + SubStr( ИмяФайла, 13, 4 );
    if(not isXML)
      return;
    end;
  end;

  /* Символы в позициях 17-24 имени файла являются датой:17-20 - цифры, 21-22 - число от 1 до 12, 23-24 - число от 1 до 31 */
  if( (LenFName >= 17) and not CheckStrOnYYYYMMDD( SubStr( ИмяФайла, 17, 8 ) ) )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";в символах 17-24 имени файла указано значение " + SubStr( ИмяФайла, 17, 8 ) + ", не являющееся датой";
    if(not isXML)
      return;
    end;
  end;

  /* Символы в позициях 26-31 имени файла являются цифрами */
  if( (LenFName >= 26) and (IsDigitalNumber( SubStr( ИмяФайла, 26, 6 ) ) != 0) )
    ImportErrors[ImportErrors.Size] = IfThenElse(isXML == true, "04", "14") + ";в номере электронного документа (символы 26-31 имени файла) содержатся не только цифры " + SubStr( ИмяФайла, 26, 6 );
    if(not isXML)
      return;
    end;
  end;

  Bnk_ToRSTrace( "ПроверкаФорматаИмениФайла365П", "End", "" );
end;

