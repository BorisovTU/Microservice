 /*
 $Name: ufgm804.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения ED804 транспорта УФЭБС
 */

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wltools.mac";

private macro GetQueueType( Query )
  var Pos1 = 0, Pos2 = 0; var Type = "";
  
  Pos1 = Index( Query, "/" );
  Pos2 = Index( Query, "/", Pos1 + 1 );
  
  Type = SubStr(Query, Pos1 + 1, Pos2 - Pos1 - 1);
  
  return Type;  
end;

macro GenMes( addrMes, addrReq )
  var tmpstr = "", error;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  var ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  ЗаписатьПолеЛог( "EDNo", ed_nda.EDNo );
  ЗаписатьПолеЛог( "EDDate", YYYYMMDD(ed_nda.EDDate) );
  ЗаписатьПолеЛог( "EDAuthor", ed_nda.EDAuthor );
    
  ЗаписатьПолеЛог( "PoolInfoFlag", "0");
  
  var CodeOwner, BIC = "" , Account = "";
  var rs = execSQLSelect( " Select dep.t_PartyID, cor.t_CorAccount "
                          "   From ddp_dep_dbt dep, dcorschem_dbt cor "
                          "  Where dep.t_Code = cor.t_Department "
                          "    and cor.t_Number = :Corschem "
                          "    and cor.t_FIID = :FIID ",
                          MakeArray(SQLParam("Corschem", wlreq.Corschem),
                                    SQLParam("FIID", wlreq.FIID)));
  if( rs and rs.MoveNext() )
    GetPartyCodeEx(rs.value("t_PartyID"), PTCK_BIC, @BIC, @CodeOwner);
    Account = rs.value("t_CorAccount");
  end;
  
  ЗаписатьПолеЛог( "BIC", BIC );
  ЗаписатьПолеЛог( "Acc", Account );
  
  
  var IncludeWaitForSettle = "0";
  if( wlreq.IsForSess == "X" )
    IncludeWaitForSettle = "1";
  end;
  
  ЗаписатьПолеЛог( "IncludeWaitForSettle", IncludeWaitForSettle );
  
  var QueueType = GetQueueType(wlreq.Queries);
  if( strlen(QueueType) > 0 )
    ЗаписатьПолеЛог( "QueueType", QueueType);
  end;
   
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
