/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям SWIFT MT942                              */
/*                                                                          */
/*  Имя файла: swgd942.mac                                                  */
/*  Создан:    21.03.00                                      Бабин А.П.     */
/****************************************************************************/

import "swgendoc.mac";

/* Выписка со счета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */
  Account          :string,        /* Корсчет */
  StatementNumber  :string,        /* Номер выписки */
  SequenceNumber   :integer,       /* Номер страницы выписки */
  FloorLimitIndF   :FloorLimit,    /* Нижний предел */
  FloorLimitIndN   :FloorLimit,    /* Нижний предел */
  DateTimeInd      :TDateTime,     /* Дата и время отчета */
  DocExtrLine      :StatementLine, /* Строка выписки */
  NumberAndSumDeb  :NumberSumma,   /* Число и сумма entries по дебету */
  NumberAndSumCred :NumberSumma,   /* Число и сумма entries по кредиту */
  Information      :string,        /* Информация владельцу счета */
  Sender           :Bank,          /* Отправитель */
  Receiver         :Bank,          /* Получатель */

  /* Вспомогательная информация */
  DebNum           :integer,       /* Число дебетовых в выписке */
  DebTurn          :moneyl,        /* Оборот дебетовых */
  CredNum          :integer,       /* Число кредитовых в выписке */
  CredTurn         :moneyl,        /* Оборот кредитовых */
  Cln              :bool,          /*выписка клиентская*/
  IsCorschem       :bool;          /*Определилась корсхема или нет*/

  TRF              = "";
  RelatedRef       = "";
  Account          = "";
  StatementNumber  = "";
  SequenceNumber   = 0;
  Information      = "";

  /* Начальная инициализация */
  DebNum = 0;
  CredNum = 0;
  DebTurn = moneyl(0);
  CredTurn = moneyl(0);
  Cln = False;  
  IsCorschem = False; 

end; /* class StatementMessage */

var MT942 : StatementMessage;

macro Fill20( TRF )
  MT942.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MT942.RelatedRef = TRF;
  return TRUE;
end;

macro Fill25( Account )
  MT942.Account = Account;
  return TRUE;
end;

macro Fill28C( Number, Page )
  MT942.StatementNumber = Number;
  MT942.SequenceNumber = Page;
  return TRUE;
end;

macro Fill34FF( FloorLimitInd:FloorLimit )
  MT942.FloorLimitIndF = FloorLimitInd;
  return TRUE;
end;

macro Fill34FN( FloorLimitInd:FloorLimit )
  MT942.FloorLimitIndN = FloorLimitInd;
  return TRUE;
end;

macro Fill13( DateTimeInd:TDateTime )
   MT942.DateTimeInd = DateTimeInd;
   return TRUE;
end;

/* Вставляем заголовок выписки - чтобы было к чему привязывать документы выписки */
macro InsertHead()
  wlhead.Account      = MT942.Account;
  wlhead.FIID         = MT942.FloorLimitIndF.FIID;

  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account, wlhead.FIID, false) )
     if (not(MT942.Cln))
        std.msg( String("Не определена схема расчетов для респондента по корсчету: ", MT942.Account) );
     return FALSE;
    end;
  else
    MT942.IsCorschem = TRUE;
    if( not ВставитьЗаголовокВыписки( wlhead ) )
      std.msg("Ошибка при вставке заголовка выписки");
      return FALSE;
    end;
  end;
  return TRUE;
end;

/* Вставить документ выписки */
macro InsertStatementLine()
  if (not(MT942.IsCorschem))
      return TRUE;
  end;
  if( MT942.DocExtrLine.IsSet() )
    ClearRecord(wlconf);
    wlconf.DateValue  = MT942.DocExtrLine.DateVal;
    wlconf.Sum        = MT942.DocExtrLine.Amount;
    wlconf.Number     = MT942.DocExtrLine.AccRef;
    wlconf.RelatedRef = MT942.DocExtrLine.Ref;
    if ( wlconf.Number=="" ) 
       wlconf.Number = wlconf.RelatedRef;
    end;
    if ( MT942.DocExtrLine.DateInp == date(0,0,0) )
      wlconf.TransferDate = MT942.DateTimeInd.LocalDate;//Wlhead.DateOut
    else
      wlconf.TransferDate = MT942.DocExtrLine.DateInp;
    end;
    wlconf.Description = substr( MT942.DocExtrLine.Details + "\n" + MT942.Information, 1, 215 );
    wlconf.MessageType = SubStr( MT942.DocExtrLine.TypeOper, 2, 3 );
    if( MT942.DocExtrLine.DKFlag == DEBET_ACCOUNT )
      wlconf.DKFlag = WL_DEBET;
      MT942.DebNum  = MT942.DebNum + 1;
      MT942.DebTurn = MT942.DebTurn + MT942.DocExtrLine.Amount;
    else
      wlconf.DKFlag = WL_CREDIT;
      MT942.CredNum  = MT942.CredNum + 1;
      MT942.CredTurn = MT942.CredTurn + MT942.DocExtrLine.Amount;
    end;
    if( not ВставитьДокументВыписки( wlconf ) )
      std.msg("Ошибка при сохранении документа выписки");
      return FALSE;
    end;

    MT942.DocExtrLine = StatementLine();
    MT942.Information = "";
    return TRUE;
  end;
  return TRUE;
end;

macro Fill61( DocExtr:StatementLine )
  MT942.DocExtrLine = DocExtr;
  return TRUE;
end;

macro Fill86( value )
  MT942.Information = value;
  return TRUE;
end;

macro Fill90D( NumberAndSumDeb  :NumberSumma )
  MT942.NumberAndSumDeb = NumberAndSumDeb;
  return TRUE;
end;

macro Fill90C( NumberAndSumCred  :NumberSumma )
  MT942.NumberAndSumCred = NumberAndSumCred;
  return TRUE;
end;

/* Заполнение списка полей формы  MT942*/
var Fld20   = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld21   = ТПолеФормы( RelatedReferenceField,              FIELD_OPTIONAL,  "ReadTRF", "Fill21",  "" ),
    Fld25   = ТПолеФормы( AccountIdentificationField,         FIELD_MANDATORY, "Read25",  "Fill25",  "" ),
    Fld28C  = ТПолеФормы( StatementSequnceNumberCField,       FIELD_MANDATORY, "Read28",  "Fill28C", "" ),
    Fld34FF = ТПолеФормы( FloorLimitIndicatorField,           FIELD_MANDATORY, "Read34F", "Fill34FF","" ),
    Fld34FN = ТПолеФормы( FloorLimitIndicatorField,           FIELD_OPTIONAL,  "Read34F", "Fill34FN","" ),
    Fld13D  = ТПолеФормы( DateTimeIndicationField,            FIELD_MANDATORY, "Read13D", "Fill13",  "InsertHead" ),
    Fld61   = ТПолеФормы( StatementLineField,                 FIELD_OPTIONAL,  "Read61",  "Fill61",  "" ),
    Fld86   = ТПолеФормы( InformationToAccountOwnerField,     FIELD_OPTIONAL,  "Read86",  "Fill86",  "InsertStatementLine" ),
    Fld90D  = ТПолеФормы( NumberSumDebetEntriesField,         FIELD_OPTIONAL,  "Read90",  "Fill90D", "" ),
    Fld90C  = ТПолеФормы( NumberSumCreditEntriesField,        FIELD_OPTIONAL,  "Read90",  "Fill90C", "" ),
    Fld86_H = ТПолеФормы( InformationToAccountOwnerField,     FIELD_OPTIONAL,  "Read86",  "Fill86",  "" );

macro ConstructMT942( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  MT942.Cln = МожетЛиВыпискаБытьКлиентской(wlmes.RlsFormID);
  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          if ( (NeedRead) AND (fld.Name==InformationToAccountOwnerField) )
              count = count - 2;
          end;
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;
            if( fld.Name == InformationToAccountOwnerField )
              count = count - 2;
            end;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT942.Sender   = Bank( BankCode );
  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  MT942.Receiver = Bank( BankCode );

  return TRUE;
end; /* ConstructMT942 */

macro GenDoc( addrMes )
  MT942 = NULL;
  MT942 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация выписки по МТ942");

  ClearRecord(wlhead);

  if( not ConstructMT942( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  if(MT942.IsCorschem)
      /* Заполнение учетных буферов */
      wlhead.Number          = MT942.StatementNumber;
      wlhead.Page            = MT942.SequenceNumber;
      if ( MT942.FloorLimitIndF.DKFlag == DEBET_ACCOUNT )
         wlhead.DebetFloor   = MT942.FloorLimitIndF.Amount;
         wlhead.CreditFloor  = MT942.FloorLimitIndN.Amount;
      elif ( MT942.FloorLimitIndF.DKFlag == CREDIT_ACCOUNT )
         wlhead.DebetFloor   = MT942.FloorLimitIndN.Amount;
         wlhead.CreditFloor  = MT942.FloorLimitIndF.Amount;
      else 
         wlhead.DebetFloor   = MT942.FloorLimitIndF.Amount;
         wlhead.CreditFloor  = MT942.FloorLimitIndF.Amount;
      end;
      wlhead.DebetFloorTurn  = MT942.NumberAndSumDeb.Amount;
      wlhead.CreditFloorTurn = MT942.NumberAndSumCred.Amount;
      wlhead.Description     = substr( MT942.Information, 1,215 );
      wlhead.DateIn          = MT942.DateTimeInd.LocalDate;
      wlhead.DateOut         = MT942.DateTimeInd.LocalDate;
      wlhead.DebetNumDoc     = MT942.DebNum;
      wlhead.DebetTurn       = MT942.DebTurn;
      wlhead.CreditNumDoc    = MT942.CredNum;
      wlhead.CreditTurn      = MT942.CredTurn;
    
      if( not ОбновитьЗаголовокВыписки( wlhead ) )
        std.msg("Ошибка при обновлении заголовка выписки");
        return FALSE;
      end;
  else
      ClearRecord(wlinfo);
      /* Заполнение учетных буферов */
      wlinfo.TRN          = MT942.TRF;
      wlinfo.RelatedRef   = MT942.RelatedRef;

      /* Создатель - наш абонент */
      wlinfo.OriginatorID       = MT942.Sender.PartyID;
      wlinfo.OriginatorCodeKind = MT942.Sender.CodeKind;
      wlinfo.OriginatorCode     = MT942.Sender.CodeBank;
      wlinfo.OriginatorName     = MT942.Sender.Name;

      /* Получатель - наш банек */
      wlinfo.RecipientID       = {OurBank};
      wlinfo.RecipientCodeKind = MT942.Receiver.CodeKind;
      wlinfo.RecipientCode     = MT942.Receiver.CodeBank;
      wlinfo.RecipientName     = MT942.Receiver.Name;
      MT942.Information = string("Выписка по клиентскому счёту N ", MT942.Account);
      if( not ВставитьИнфСообщение( wlinfo, MT942.Information ) )
        std.msg("Ошибка при вводе информационного сообщения");
        return FALSE;
      end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
