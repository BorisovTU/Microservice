/*
 $Name: ufgd209.mac
 $Module: MBR
 $Description: Генерация выписок по сообщениям УФЭБС ED209
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям УФЭБС ED209                              */
/*                                                                          */
/* Имя файла: ufgd209.mac                                                   */
/* Создан:    06.07.06                                     Гайворонский Е.Г.*/
/****************************************************************************/
import "ufgendoc.mac";

var ver_st = 1;

macro НайтиКодРежима(answercode: string) :string

  var result = "Неизвестный код режима: " + answercode;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_description from dwlcodes_dbt where t_algnum = 43 and t_code=:code";
  
  params = makeArray( SQLParam("code", answercode));
  rs = execSQLselect( select, params, FALSE );
  if ( rs and rs.MoveNext() )
     result = rs.value(0);
  end;
        
  return result;
end;

macro GenDoc( addrMes )
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, Строка2, OperCodeStr, Receiver, Error, Корсчёт;
  var StatusCode, i = 0, Page, EDDate, j = 0;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED209" );

  ClearRecord( wlinfo );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED209" ) );
     return false;
  end;

  EDDate = ReadAttribute(xml,"EDDate");
  OperCodeStr = НайтиКодРежима(ReadAttribute(xml,"OperatingModeCode"));

  /* Заполнение учетных буферов */
  wlinfo.TRN                = wlmes.Trn;
  
  var uis = ReadAttribute(xml,"EDAuthor");
  FillPartyByUIS_PTCK_UIS(uis, @wlinfo.OriginatorID, 
                                      @wlinfo.OriginatorCodeKind, 
                                      @wlinfo.OriginatorCode, 
                                      @wlinfo.OriginatorName );

  Receiver = ReadOptinalAttribute(xml,"EDReceiver");
  wlinfo.RecipientCodeKind  = PTCK_BIC;
  if(Receiver)
     wlinfo.RecipientID     = ToRespID(Receiver);
  else
     wlinfo.RecipientID     = {OurBank};
  end;
  wlinfo.RecipientCode      = ПолучитьКодСубъекта(wlinfo.RecipientID, wlinfo.RecipientCodeKind, Error);

  Корсчёт = ReadAttribute(xml,"Acc",/*path*/NULL,/*IsMandatory*/false);

  if(ver_st == 1)
     Строка2 = "Дата сообщения: "   + EDDate      + "\n";
     if( strlen(Корсчёт) > 0 )
       Строка2 = Строка2 + "Номер счета:    " + Корсчёт     + "\n";
     end;
     Строка2 = Строка2 + "Режим счета:    "   + OperCodeStr + "\n" ;
  else
     Строка2 = "Дата сообщения: " + EDDate      + "\n";
     if( strlen(Корсчёт) > 0 )
       Строка2 = Строка2 + "Номер счета:    " + Корсчёт     + "\n";
     end;
     Строка2 = Строка2 + "БИК:            " + ReadAttribute(xml,"BIC") + "\n" +
                         "Режим счета:    " + OperCodeStr   + "\n" ;
  end;
  
  if( not ВставитьИнфСообщение( wlinfo, Строка2 ) )
    std.msg("Ошибка при вводе информационного сообщения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
