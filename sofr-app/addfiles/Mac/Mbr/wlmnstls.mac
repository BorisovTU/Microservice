/*
  $Name:         wlmnstls.mac
  $Module:       МБР
  $Description:  Общие функции для работы с МНС
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Общие константы и вспомогательные функции для работы с МНС               */
/*                                                                          */
/*  Имя файла: wlmnstls.mac                                                 */
/*  Создан:  02.12.04                                  Фомченкова Л. Н.     */
/****************************************************************************/

import rsd, PTInter, SfInter, "wlgenmes.mac", oralib, likepy, "WLTOOLS.mac" , OprInter, 
       InsCarryDoc, "rsberror.mac", "bnk_dttmfrmt.mac", "bnk_ptlib.mac", WldInter, PSInter,
       "adress.mac", "AccOperations.mac", "pm_common.mac";

//----------------------------------------------------------------------------------
// Сохранение предупреждений печати для вывода в массовом режиме в протокол
//----------------------------------------------------------------------------------

private var MassMNSWarning = "";

macro SetMNSWarning( warning:string )
  MassMNSWarning = MassMNSWarning + IfThenElse( MassMNSWarning == "", "", "\n" ) + warning;
end;

macro GetMNSWarning():string
  return MassMNSWarning;
end;

const МаксимальнаяДлинаСтроки = 2000;

const ДлинаДаты = 8;
      
const ФормаIPB = "IPB",
      ФормаRPO = "RPO",
      ФормаROO = "ROO",
      ФормаZNS = "ZNS",
      ФормаTRB = "TRB",
      ФормаZNO = "ZNO",
      ФормаKWT = "KWT",
      ФормаPNO = "PNO",
      ФормаBUV = "BUV",
      ФормаZSO = "ZSO",
      ФормаZSN = "ZSN",
      ФормаZSV = "ZSV",
      ФормаAPN = "APN",
      ФормаAPO = "APO",
      ФормаAPZ = "APZ",
      ФормаERROR = "ERROR";
      
const МНС_КонецБлока = "###",
      МНС_КонецФрагмента = "@@@",
      МНС_КонецФайла = "===";

const FnsXml = "PS\\СООБЩЕНИЯ ФНС\\XML\\";
const СвидетельствоГосРегистрации = 4;
const СвидетельствоОПостановкеНаУчет = 14;

const ТипСубъекта_Нотариус = "2",
      ТипСубъекта_Адвокат  = "3"; 

const ZeroDateFromlmnstls = date(0,0,0);

// Виды сообщений по счетам (SBC)
const ACCMSG_KIND_OPEN  = 1,
      ACCMSG_KIND_CLOSE = 2,
      ACCMSG_KIND_CHNG  = 3,
      ACCMSG_KIND_CHNGBNK=4;

// значение для полей, которые будут заполняться на экспорте, а не при генерации
const FILL_AT_EXPORT : string = " ";


macro IsGoodDateYYYYMMDD( strYYYYMMDD )
  return RegExMatch(strYYYYMMDD, "[0-9]{4}(0[1-9]|1[012])(0[1-9]|1[0-9]|2[0-9]|3[01])");
end;

macro ToDateMNS( strYYYYMMDD )
  return date( int(substr(strYYYYMMDD, 7, 2)), int(substr(strYYYYMMDD, 5, 2)), int(substr(strYYYYMMDD, 1, 4)) );
end;

/*дд.мм.гггг*/
macro StrToDate( str )
  if( substr(str, 5, 1) == "-" )
    return date( int(substr(str, 9, 2)), int(substr(str, 6, 2)), int(substr(str, 1, 4)) );  
  else
    return date( int(substr(str, 1, 2)), int(substr(str, 4, 2)), int(substr(str, 7, 4)) );
  end;
end;

macro ПолучитьИДГНИ( PartyID, Series, Number, RegDate, RegDocKind )
  var ID = -1, rsstr, dt;
  var rs:object;
  var select:string;
  var params:TArray;

  if( valtype(RegDocKind) == V_UNDEF )
    RegDocKind = СвидетельствоОПостановкеНаУчет;
  end;

  select = " select t_RegPartyID, t_Series, t_Number, T_DOCDATE from dobjrgdoc_dbt " +
                      " where t_objecttype = :OBJTYPE_PARTY and t_objectid = :PartyID" + 
                      " AND t_regpartykind = :PTLIST_TAXINSTITUTE " +
                      " AND t_regdockind = :RegDocKind" +
                      " and t_IsClosed <> 'X'" +
                      " and T_STARTDATE <= :CurDate" +
                      " and (T_FINISHDATE >= :CurDate" +
                      "      or T_FINISHDATE = to_date('01.01.0001', 'DD-MM-YYYY:HH24:MI:SS'))";
  params = makeArray( SQLParam("OBJTYPE_PARTY", OBJTYPE_PARTY),
                      SQLParam("PartyID", PartyID),
                      SQLParam("PTLIST_TAXINSTITUTE", PTLIST_TAXINSTITUTE),
                      SQLParam("RegDocKind", RegDocKind),
                      SQLParam("CurDate", {curdate}));
  /// Это для отладки 
/*
   println (  string ("  select t_RegPartyID, t_Series, t_Number, T_DOCDATE from dobjrgdoc_dbt \n" ,
                      " where t_objecttype = ",OBJTYPE_PARTY, " and t_objectid = ",PartyID, 
                      " \n AND t_regpartykind = ",PTLIST_TAXINSTITUTE,
                      " \n AND t_regdockind = ",СвидетельствоОПостановкеНаУчет ,
                      " \n and T_STARTDATE <= to_date('",{curdate},"', 'DD-MM-YYYY:HH24:MI:SS')",
                      " \n and (T_FINISHDATE >= to_date('",{curdate},"', 'DD-MM-YYYY:HH24:MI:SS')" ,
                      " \n or T_FINISHDATE = to_date('01.01.0001', 'DD-MM-YYYY:HH24:MI:SS'))") 
          );
*/
  rs = execSQLselect( select, params, FALSE );
  if (rs.MoveNext())
     ID = rs.Value(0);
     if(ValType(Series) != V_UNDEF)
         SetParm(1, rs.Value(1));
     end;
     if(ValType(Number) != V_UNDEF)
         SetParm(2, rs.Value(2));
     end;
     if(ValType(RegDate) != V_UNDEF)
         DtTmSplit( rs.Value(3), dt );
         SetParm(3, dt);
     end;
  end;
  return ID;
end;  

macro НашБанкГоловной(PartyID)
  if (GetHeadDprtPartyID() == PartyID)
    return true;
  else
    return false;
  end;

end;

macro УдалитьЗапрещенные311ПСимволы( Str:@string )

  Str = StrSubst(Str,","," ");
  Str = StrSubst(Str,"."," ");

end;

macro СформироватьПодстрокуАдреса( Str:string ):string
  var error, Строка = "", AddStr = "";
  
  УдалитьЗапрещенные311ПСимволы(@Str);
  УдалитьЗапрещенные311ПСимволы(@AddStr);

  if( getparm(1, AddStr) and (AddStr != "") )
    Строка = Str + " " + AddStr;
  else
    Строка = Str;
  end;

  Строка = StrUpr(Строка);
  return Строка;
end;

macro ПолучитьАдрес( PartyID )
  var error, Строка = "";
  FILE adress(adress) key 0;
  adress.PartyID = PartyID;
  adress.Type    = PTADDR_LEGAL;
  if(not getEQ(adress))
    return Строка;
  end;

  var КодСтраны = "", 
      КодРегиона = "", 
      Район = "", 
      Город = "";

  var select;
  var params:TArray = TArray();  
  var rs:RsdRecordset ;
  select = " select country.t_CODELAT3"+
           " from dcountry_dbt country"+
           " where country.t_CODECYR3 = :Country" +
           " or country.t_CODELAT3 = :Country" +
           " or country.t_CODELAT2 = :Country" +
           " or country.t_CODENUM3 = :Country";

  params[params.size] = ( SQLParam( "Country" , adress.Country ));

  rs = execSQLselect(select,params,true);
  if( rs.MoveNext() )
    КодСтраны = СформироватьПодстрокуАдреса(rs.value(0));
    
    if(КодСтраны != "RUS")
      КодРегиона = "00";
    else
      КодРегиона = СформироватьПодстрокуАдреса(string(adress.RegionNum));
    end;

    if((КодРегиона == "77") or (КодРегиона == "78"))
      Район = "";
    else
      Район = СформироватьПодстрокуАдреса(adress.Province, adress.CodeProvince); 
    end;

    Строка = string( СформироватьПодстрокуАдреса(adress.PostIndex), ",", КодРегиона, ",", Район, ",", 
                     СформироватьПодстрокуАдреса(adress.District, adress.CodeDistrict) , ",", 
                       СформироватьПодстрокуАдреса(adress.Place, adress.CodePlace), ",",
                       СформироватьПодстрокуАдреса(adress.Street, adress.CodeStreet), ",", 
                       СформироватьПодстрокуАдреса(adress.House), ",", 
                       СформироватьПодстрокуАдреса(adress.NumCorps), ",", 
                       СформироватьПодстрокуАдреса(adress.Flat) 
                     );
    end;
  return Строка;
end;  

macro ПолучитьТелефон( PartyID ) : string

  var PhoneNumber : string = "";
  FindAdressContactValue(PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
  if (PhoneNumber == "")
    FindAdressContactValue(PartyID, PTADDR_REAL, CNTADR_PHONENUMBER, PhoneNumber);
  end;
  if (PhoneNumber == "")
    FindAdressContactValue(PartyID, PTADDR_POST, CNTADR_PHONENUMBER, PhoneNumber);
  end;
  return PhoneNumber;
  
end;

macro ПолучитьИдентификаторОтправителя()
  var INN = GetPartyINN({OurBank}, 1);
  return string(RemoveKPP(INN):10, "**", RemoveINN(INN):9);
end;  

macro ПолучитьДоговорОбслуживания( FIID : integer, Acc : string, SfCntrBuf ) : integer
  var stat : integer = 0;

  file SfCntr( "sfcontr.dbt" );
  var OldKey = KeyNum( SfCntr, 1 );

  SfCntr.ServKind    = PTSK_PAY;
  SfCntr.objectType  = SF_ACCOUNT; 
  SfCntr.FIID        = FIID;       
  SfCntr.object      = Acc; 

  if( getEQ( SfCntr ) )
    Copy(SfCntrBuf, SfCntr);
  else
    stat = 1;
  end;

  keyNum( SfCntr, OldKey );
  return stat;
end;

macro ПолучитьСчет( FIID, Acc, buff, SkipRunError ) : integer
  var rs:object;
  var select:string;
  var params:TArray;

  buff.Account = Acc;
  buff.Code_Currency = FIID;
  buff.Chapter = 1;

  select = "select t_Open_Date, t_Client, t_Type_Account, t_Close_Date, t_Department from daccount_dbt "  
          "where t_Account = :Acc and t_chapter = 1 and t_Code_Currency = :FIID";
  params = makeArray( SQLParam("Acc", Acc),
                      SQLParam("FIID", FIID ));
  rs = execSQLselect( select, params, FALSE );

  if (rs and rs.MoveNext())
     DtTmSplit( rs.Value(0), buff.Open_Date );
     buff.Client = rs.Value(1);
     buff.Type_Account = rs.value(2);
     buff.Close_Date = rs.value(3);
     buff.Department = rs.value(4);
     return 0;
  elif (SkipRunError)
    return 1;
  else
     RunError(string("Не найден лицевой счет с номером ", Acc));
  end;
end;

macro ПолучитьСчет_БО( FIID, Acc, req, ObjType, buff )
  ClearRecord(buff);
  buff.Account = Acc;
  buff.Code_Currency = FIID;
  buff.Chapter = 1;

  if(ObjType == OBJTYPE_REQOPENA)
    buff.Client = req.ClientID;
    buff.Type_Account = req.Type_Account;
  elif(ObjType == OBJTYPE_REQCHANGE)
    buff.Client = req.ClientID;
  elif(ObjType == OBJTYPE_REQCLOSA)
    buff.Client = req.ClientID;
    buff.Open_Date = ReadNoteForObject(ObjType, UniID(req, ObjType), REQCLOSA_NOTE_KIND_ACC_OPENDATE);
  end;

  buff.Department = req.Department;
end;

// Информация из блока "Характеристики ошибок"
// в квитанциях о принятии/непринятии и извещениях об ошибках
// по сообщениям об открытии/закрытии/изменении счета
class TErrInfo

  var ErrCode       :string   = "", // КодОшибки
      ErrName       :string   = "", // НаимОшибки
      AttrCode      :string   = "", // КодРекв
      AttrValue     :string   = ""; // ЗначРекв
end;

macro fillWlerror(ResultID : integer, ErrInfo : TErrInfo, wlerror_buf)
  record wlerror(wlerror);
  ClearRecord(wlerror);

  wlerror.ErrorID = 0;
  wlerror.ResultID = ResultID;        
  wlerror.Code = ErrInfo.ErrCode;

  var s : string = ErrInfo.ErrName;
  if(ErrInfo.AttrCode != "")
    s = s + " // Код реквизита: " + ErrInfo.AttrCode;
  end;
  if(ErrInfo.AttrValue != "")
    s = s + " // Значение реквизита: " + ErrInfo.AttrValue;
  end;
  wlerror.Description = s;

  Copy(wlerror_buf, wlerror);
end;

var ПоляМНС = TArray; 

class ТПолеМНС( _name, _fill_fun, _fill_fun2 )
  var Name:string,
      FillFun:string,
      FillFun2:string;

  Name          = _name;
  FillFun       = _fill_fun;
  FillFun2      = _fill_fun2;

  /* Добавляем поле в список полей формы */
  ПоляМНС(ПоляМНС.Size) = this;

  macro ОбработатьПолеФормы( stream, obj )
    if( FillFun != "" )
      return ExecMacro2( FillFun, stream, obj );
    else
      return TRUE;
    end;
  end;

  macro ОбработатьПолеФормы2( stream, obj )
    if(FillFun2)
      return ExecMacro2( FillFun2, stream, obj );
    else
      return TRUE;
    end;
  end;
end; /* class ТПолеМНС */


macro IndexMNS(Name)
   var count = 0, fld;
   while(count < ПоляМНС.Size)
       fld = ПоляМНС.Value(count);
       if (fld.Name == Name)
           return count;
       end;
       count = count + 1;
   end;
   return -1;
end;

macro YYYYMMDDXML( dt )
  var day, mon, year;
  DateSplit(dt, day, mon, year );
  return StrSubst ( string(year:4, "-", mon:2, "-", day:2), " ", "0" );
end;

macro YYYYMMDD( dt )
  return DateToStr_YYYYMMDD( dt );
end;

macro HHMMSS( tm )
   var ss, mm, hh, str;
   TimeSplit(tm, hh, mm, ss );
   str = string(hh:2, mm:2, ss:2) ;
   str = StrSubst ( str, " ", "0" );
   return str;
end;

PRIVATE MACRO GetDepartmentName(Department:integer):string
  var rset:object;
  var select:string;
  var params:TArray = TArray();

  select = " SELECT T_NAME FROM DDP_DEP_DBT WHERE T_CODE = :Department ";

  params[params.size] = SQLParam( "Department", Department );
  rset = execSQLselect( select, params, TRUE );

  if( rset and rset.moveNext() )
    return rset.value(0);
  end;

  return "";
END;

// Получить строковое значение настройки PS\REQOPENACC\OPERATION\Setup
// для филиала DepCode (dp_dep.Code)
// return 0 - OK, иначе ошибка
MACRO GetDprtStringRegValForOPENAC( Setup:string, DepCode:integer, RegVal:@string ):integer
   
  var stat:integer = 0;

  var RegStr:string = RegOpenAcc + Setup;

  var params : TArray = makeArray( SQLParam("p_RegValue", V_STRING, RSDBP_OUT),
                                   SQLParam("p_KeyPath",  RegStr, RSDBP_IN),
                                   SQLParam("p_RegKind",  REGVAL_KIND_DPRT, RSDBP_IN_OUT),
                                   SQLParam("p_ObjectID", DepCode, RSDBP_IN_OUT),
                                   SQLParam("p_UserValueBlocked", V_STRING, RSDBP_OUT),
                                   SQLParam("p_ExpDep", V_INTEGER, RSDBP_OUT) ); 
  stat = execStoredFunc("RSB_COMMON.RSI_GetStringRegVal", V_INTEGER, params);
  
  if(not stat)
    RegVal = string(params.value(0).value);
  else
    RegVal = "";
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  return 1;
END;

// Получить целочисленное значение настройки PS\REQOPENACC\OPERATION\Setup
// для филиала DepCode (dp_dep.Code)
// return 0 - OK, иначе ошибка
MACRO GetDprtIntegerRegValForOPENAC( Setup:string, DepCode:integer, RegVal:@integer ):integer
   
  var stat:integer = 0;

  var RegStr:string = RegOpenAcc + Setup;
  var params : TArray = makeArray( SQLParam("p_RegValue", V_INTEGER, RSDBP_OUT),
                                   SQLParam("p_KeyPath",  RegStr, RSDBP_IN),
                                   SQLParam("p_RegKind",  REGVAL_KIND_DPRT, RSDBP_IN_OUT),
                                   SQLParam("p_ObjectID", DepCode, RSDBP_IN_OUT),
                                   SQLParam("p_UserValueBlocked", V_STRING, RSDBP_OUT),
                                   SQLParam("p_ExpDep", V_INTEGER, RSDBP_OUT) ); 
  stat = execStoredFunc("RSB_COMMON.RSI_GetIntegerRegVal", V_INTEGER, params);

  if(not stat)
    RegVal = params.value(0).value;
  else
    RegVal = 0;
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  return 1;
END;

// Получить целочисленное значение настройки PS\СООБЩЕНИЯ ФНС\XML\Setup
// для филиала DepCode (dp_dep.Code)
// return 0 - OK, иначе ошибка
MACRO GetDprtIntegerRegValForFNSXML( Setup:string, DepCode:integer, RegVal:@integer ):integer
   
  var stat:integer = 0;

  var RegStr:string = FnsXml + Setup;
  var params : TArray = makeArray( SQLParam("p_RegValue", V_INTEGER, RSDBP_OUT),
                                   SQLParam("p_KeyPath",  RegStr, RSDBP_IN),
                                   SQLParam("p_RegKind",  REGVAL_KIND_DPRT, RSDBP_IN_OUT),
                                   SQLParam("p_ObjectID", DepCode, RSDBP_IN_OUT),
                                   SQLParam("p_UserValueBlocked", V_STRING, RSDBP_OUT),
                                   SQLParam("p_ExpDep", V_INTEGER, RSDBP_OUT) ); 
  stat = execStoredFunc("RSB_COMMON.RSI_GetIntegerRegVal", V_INTEGER, params);
  
  if(not stat)
    RegVal = params.value(0).value;
  else
    RegVal = 0;
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  return 1;
END;

private macro GetLastResErrCode( MesID )
  var rs:object;
  var select:string;
  var params:TArray;

  select = " select resprm.t_ErrCode"+
           " from dwlresprm_dbt resprm"+
           " where resprm.t_InitialMesID = :MesID"+
           " order by resprm.t_ResultID";
  params = makeArray( SQLParam("MesID", MesID));
  rs = execSQLselect( select, params, FALSE );

  if ( rs.MoveNext() )
    return rs.value(0);
  else
    return 0;
  end;
end;

macro MakeSixSymb(Number)
   var len;
   len = strlen(Number); 
   if( len < 6 )
     while( len != 6 )
       Number = "0" + Number;
       len = len + 1;
     end;
   else
     Number = string( Number:6 );
   end;
   return Number;
end;

macro MakeLastSixSymb(Number)
   var len;
   len = strlen(Number); 
   if( len < 6 )
     while( len != 6 )
       Number = "0" + Number;
       len = len + 1;
     end;
   else
     Number = subStr( Number, len - 5 );
   end;
   return Number;
end;

macro getRsDpDepByPartyID (PartyID:integer):RsdRecordset
  var query : string = "select * " +
                       "  from ddp_dep_dbt " +
                       " where t_PartyID = :PartyID ";
  var rs:RsdRecordset = execSQLselect(query, makeArray( SQLParam("PartyID", PartyID) ));
  return rs;
end;

macro GetFileNamebySess(SessID)
  var FormatFile : integer = GetFormatFileBySess(SessID);
  var rs:object;
  var select : string = "", from : string = "", where : string = "";
  var params:TArray;

  select = " select pcode.t_Code as t_BIC, "+
           "        val_Num.t_Value as t_NumMes";

  if(FormatFile == WLD_FORMAT_XML)
    select = select + ", " +
           "        pcodeNOB.t_Code as t_CodeNOB, " +
           "        val_Type.t_Value as t_TypeMes ";
  else
    select = select + ", " +
           "        val_Code.t_Value as t_CodeNOB ";
  end;

  from =   " from dwlmes_dbt mes,"+
           "      dwlsess_dbt sess,"+
                " dwlmesval_dbt val_Num," +
                " dwltpshem_dbt tpshem," +
                " ddp_dep_dbt dpdep," +
                " dpartcode_dbt pcode";

  if(FormatFile == WLD_FORMAT_XML)
    from = from + ", " +
                " ddp_dep_dbt dpdepMes, " +
                " dparty_dbt ptDepMes, " +
                " dpartcode_dbt pcodeNOB, " +
                " dwlmesval_dbt val_Type ";
  else
    from = from + ", " +
                " dwlmesval_dbt val_Code ";
  end;

  where =  " where sess.t_sessionid = :SessID"+
             " and mes.t_sessionid = sess.t_sessionid" + 
             " and tpshem.T_TPSHEMID = mes.T_TPSCHEMID" +
             " and dpdep.T_CODE = tpshem.T_DEPARTMENT" +
             " and pcode.T_PARTYID = dpdep.T_PARTYID" +
             " and pcode.T_CODEKIND = 3" +
             " and val_Num.T_MESID  = mes.T_MESID"  +
             " and val_Num.t_TpFieldID = 496"; // НомСооб

  if(FormatFile == WLD_FORMAT_XML)
    where = where + " and dpdepMes.t_Code = mes.t_Department " +
                    " and ptDepMes.t_PartyID = dpdepMes.t_PartyID " +
                    " and pcodeNOB.t_PartyID = ptDepMes.t_TaxInstitution " +
                    " and pcodeNOB.t_CodeKind = 28 " + // КодНОБ
                    " and val_Type.T_MESID  = mes.T_MESID " +
                    " and val_Type.t_TpFieldID = 1118 "; // ТипСооб
  else
    where = where + " and val_Code.T_MESID = mes.T_MESID"  +
                    " and val_Code.t_TpFieldID = 573"; // КодНОБ
  end;

  params = makeArray( SQLParam("SessID", SessID));
  
  rs = execSQLselect( select + from + where, params, FALSE );

  if ( rs.MoveNext() )
     
    var bbbbbbb = substr(rs.Value("t_BIC"), 3, 7);
    var LLLL = strLpad( rs.Value("t_CodeNOB"), 4, "0" );

    var RRRRFFFFGGNNNNNN : string = "";
    if(FormatFile == WLD_FORMAT_XML)
      RRRRFFFFGGNNNNNN = rs.Value("t_NumMes");
    else
      RRRRFFFFGGNNNNNN = substr(rs.Value("t_NumMes"), 1, 16);
    end;

    var MMM : string = "";
    if(FormatFile == WLD_FORMAT_XML)
      MMM = rs.Value("t_TypeMes");
    else
      MMM = substr(rs.Value("t_NumMes"), 18, 3);
    end;
     
    var a : string = "";
    if( substr(MMM, 2, 2) == "77" )
      a = 3;
    elif( substr(MMM, 2, 2) == "00" )
      a = 1;
    else
      a = 2;
    end;

    return string(a + bbbbbbb +"_"+ LLLL + YYYYMMDD(date()) + "_" + RRRRFFFFGGNNNNNN + "_" + MMM);
  else
    return -1;
  end;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

// Получить филиал сообщения
private macro getPartyIDForMesDepartment( MesID:integer )
  var select  = "select dp.t_PartyID "
                 " from dwlmes_dbt mes, "
                       " ddp_dep_dbt dp "
                " where mes.t_MesID = :MesID "
                  " and dp.t_Code = mes.t_Department ";
  var params = makeArray( SQLParam("MesID", MesID));
  var rs = execSQLselect(select, params, true);
  if (rs and rs.moveNext())
    return rs.value("t_PartyID");
  end;
  return 0;
end;

private macro getSendNumXml(MesID : integer) : string
  var SendNum : string = "1";

  if( not ПолучитьПоле (MesID, "_НомерОтправ", SendNum) )
    InitError(); // ПолучитьПоле формирует ошибку, если не находит поле
    SendNum = "1";
  end;

  return SendNum;
end;

private macro getSendNumForFnsInfo(MesID:integer, f_wlregdec, IsXML:bool) : string
  var a : string = "1";

  if(IsXML)
    a = getSendNumXml(MesID);
  else
      if( not ПолучитьПоле (MesID, "_НомерОтправ", a) )
        var select : string = "", rs : RsdRecordset, params : TArray;
        select = "select count(1) as t_Count" +
                 "  from dwlmes_dbt wlmes, " +
                 "       dwlexphist_dbt wlexphist, " +
                 "       dwlmeslnk_dbt wlmeslnk " +
                 " where wlmes.t_MesID <> :MesID " +
                 "   and wlmes.T_MesID = wlexphist.t_MesID " +
                 "   and wlmes.t_MesID = wlmeslnk.t_MesID " +
                 "   and wlmeslnk.t_ObjKind = :OBJTYPE_FNSINFO " +
                 "   and wlmeslnk.t_ObjID = :ObjID ";
        params = makeArray( SQLParam("MesID", MesID),
                            SQLParam("OBJTYPE_FNSINFO", OBJTYPE_FNSINFO),
                            SQLParam("ObjID", f_wlregdec.DecisionID )
                          );
        rs = execSQLselect(select, params, true);

        if (rs and rs.moveNext())
          a = string( int(rs.value("t_Count")) + 1);
        else
          InitError;
          a = "1";
        end;
      end;
  end;

  return a;
end;

private macro getFileNamePartsForFNSINFO(MesID:integer, IsXML:bool, a:@string, P:@string)
  FILE f_wlregdec (wlregdec) key 0;
  var params;
  var select : string = "", rs : RsdRecordset;
  var DecisionID : integer = GetObjIDByMesLink(MesID, OBJTYPE_FNSINFO);
  if( DecisionID > 0 )
    f_wlregdec.DecisionID = DecisionID;
    if ( getEQ(f_wlregdec) )
      
      // а = номер отправленного сообщения
      a = getSendNumForFnsInfo(MesID, f_wlregdec, IsXML);

      // P - наименование полученного файла запроса, решения о приостановлении 
      // операций по счетам налогоплательщика (без расширения)
      if (f_wlregdec.LnkDocID != 0)
        select = "select wlsess.t_FileName " +
                 "  from dwlsess_dbt wlsess, dwlmes_dbt wlmes, dwlmeslnk_dbt wlmeslnk " +
                 " where wlsess.t_SessionID = wlmes.t_SessionID " +
                 "   and wlmes.t_MesID = wlmeslnk.t_MesID " +
                 "   and wlmeslnk.t_ObjKind = :LnkDocType " +
                 "   and wlmeslnk.t_ObjID = :LnkDocID ";

        params = makeArray( SQLParam("LnkDocType", f_wlregdec.LnkDocType ),
                            SQLParam("LnkDocID", f_wlregdec.LnkDocID )
                          );

        rs = execSQLselect(select, params, true);
        if (rs and rs.moveNext())
          SplitFile(rs.value("t_FileName"), P);
        else
          return 1;
        end;
      else
        
        // если заполнено примечание объекта wlregdec 
        // вида "Имя файла запроса ФНС или решения рег. органа", 
        // то установить Р = значению примечания
        P = readNoteForObject(OBJTYPE_FNSINFO, 
                              makeObjectID(OBJTYPE_FNSINFO, NULL, f_wlregdec), 
                              NOTEKIND_FNSINFO_INITIALFILENAME);
        if(P == "") // примечание не заполнено
          
          // Далее идет расхождение по формату транспорта                                                            
          // Эти алгоритмы описаны в двух разных проектах, но тут волей судьбы они вместе
          // PRJ_000317_Сообщения в РО. Транспорт ФНС в xml формате.doc и PRJ_000317_Сообщения в РО. Транспорт ФНС.doc
          // Все что выше пока одинаково, ниже расхождение                                                                      

          var bbbbbbb:string = "";
          var KKKK:string = "";
          var DDDDDDDD:string = "";
          var NNNNNN:string = "";

          if( IsXML )

            if( (f_wlregdec.LnkDocType == OBJTYPE_DECISION) and
                (f_wlregdec.LnkDocNumber != "") and
                (f_wlregdec.LnkDocDate != ZeroDateFromlmnstls) )

              // RBN1bbbbbbb_ККККDDDDDDDD_NNNNNN

              bbbbbbb = substr(ПолучитьКодСубъекта( getPartyIDForMesDepartment( MesID ), PTCK_BIC), 3, 7);
              KKKK = "";
              if (f_wlregdec.RecipientID > 0)
                KKKK = ПолучитьКодСубъекта(f_wlregdec.RecipientID, PTCK_MNS);
              else
                KKKK = f_wlregdec.RecipientCode;
              end;

              DDDDDDDD = YYYYMMDD( f_wlregdec.LnkDocDate );

              if( GenerateReference( WLD_REFERENCE_ANSWER_FOR_FNS_DAY, NNNNNN ) )
                return 1;
              end;

              P = "RBN1" + bbbbbbb + "_" + KKKK + DDDDDDDD + "_" + NNNNNN;     

            elif( (f_wlregdec.LnkDocType == OBJTYPE_REQ ) and
                  (f_wlregdec.LnkDocExtID != "") )

              // XXX1bbbbbbb_ККККDDDDDDDD_NNNNNN
              var XXX = "";

              if( f_wlregdec.Type == WLD_TYPE_REGDEC_IOS )
                XXX = "ZBO";
              elif( f_wlregdec.Type == WLD_TYPE_REGDEC_INS )
                XXX = "ZBN";
              elif( f_wlregdec.Type == WLD_TYPE_REGDEC_IVS )
                XXX = "ZBV";
              end;

              bbbbbbb = substr(ПолучитьКодСубъекта( getPartyIDForMesDepartment( MesID ), PTCK_BIC), 3, 7);
              var ККККDDDDDDDD_NNNNNN = f_wlregdec.LnkDocExtID;

              P = XXX + "1" + bbbbbbb + "_" + ККККDDDDDDDD_NNNNNN;     

            end;

          else

            if( (f_wlregdec.LnkDocType == OBJTYPE_DECISION) and
                (f_wlregdec.LnkDocNumber != "") and
                (f_wlregdec.LnkDocDate != ZeroDateFromlmnstls) )

              // RBN1bbbbbbb_ККККDDDDDDDD_NNNNNN
              bbbbbbb = substr(ПолучитьКодСубъекта(f_wlregdec.OriginatorID, PTCK_BIC), 3, 7);
              KKKK = "";
              if (f_wlregdec.RecipientID > 0)
                KKKK = ПолучитьКодСубъекта(f_wlregdec.RecipientID, PTCK_MNS);
              else
                KKKK = f_wlregdec.RecipientCode;
              end;
              DDDDDDDD = YYYYMMDD( f_wlregdec.LnkDocDate );
              NNNNNN = MakeSixSymb(f_wlregdec.LnkDocNumber);
              P = "RBN1" + bbbbbbb + "_" + KKKK + DDDDDDDD + "_" + NNNNNN;     

            end;

          end;

        end;  
      end;
    end;
  else
    return 1;
  end;

  return 0;
end;

private macro getFileNameBNP(MesID:integer, P:@string)
  FILE f_wlresprm (wlresprm) key 0;
  var select : string = "";
  var ResultID : integer = GetObjIDByMesLink(MesID, OBJTYPE_MESDEFECT);
  if( ResultID > 0 )
    f_wlresprm.resultID = ResultID;
    if ( GetEQ(f_wlresprm) )
      SplitFile(f_wlresprm.FileName, P); 
    else
      return 1;
    end;
  else
    return 1;
  end;

  return 0;
end;

private macro getFileNameFNSPB( MesID:integer, a:@string, P:@string )
  FILE f_wlresprm( wlresprm ) key 0;
  var ResultID : integer = GetObjIDByMesLink(MesID, OBJTYPE_MESDEFECT);
  if( ResultID > 0 )
    f_wlresprm.ResultID = ResultID;
    if ( getEQ( f_wlresprm ) )
      
      // а = номер подтверждения
      if( ( f_wlresprm.ErrCode == 0 ) or ( f_wlresprm.ErrCode == 8 ) )
        a = "1";
      else
        a = "2";
      end;

      // P - наименование полученного файла - задано в панели
      P = f_wlresprm.FileName;
    else
      return 1;
    end;
  else
    return 1;
  end;

  return 0;
end;


private macro getFileNameForFNSNOTICE(CurMesID:integer, FileFormat)
  var resFileName : string = "";

  var strSQL =  " select  substr(wlsess.t_FileName, instr(wlsess.t_FileName, '\\', -1, 1) + 1) as  t_AnnulFileName,  " +
                "                     RSI_RSBPARTY.PT_GetPartyCode( curmes.t_InsideAbonentID, :BIC ) as t_BIC,      " +
                "                     dp.t_Code as t_DpCode                                                         " +
                "                from dwlmes_dbt curmes,                                                            " +
                "                     dwlmeslnk_dbt curmeslnk,                                                      " +
                "                     dwlsess_dbt wlsess,                                                           " +
                "                     ddp_dep_dbt dp,                                                               " +
                "                     dwlmes_dbt mes,                                                               " +
                "                     dwlmeslnk_dbt meslnk                                                          " +
                "               where curmes.t_MesID = :CurMesID1                                                   " +
                "                 and curmeslnk.t_MesID(+) = curmes.t_MesID                                         " +
                "                 and curmeslnk.t_ObjKind(+) = :OBJTYPE_FNSNOTICE1                                  " +
                "                 and meslnk.t_ObjID(+) = curmeslnk.t_ObjID                                         " +
                "                 and meslnk.t_ObjKind(+) = :OBJTYPE_FNSNOTICE2                                     " +
                "                 and mes.t_MesID(+) <> :CurMesID2                                                  " +
                "                 and mes.t_IsAnnul(+) = 'X'                                                        " +
                "                 and mes.t_MesID(+) = meslnk.t_MesID                                               " +
                "                 and wlsess.t_SessionID(+) = mes.t_SessionID                                       " +
                "                 and dp.t_PartyID(+) = curmes.t_InsideAbonentID                                    " +
                "               order by nvl(mes.t_MesID, 0) DESC                                                   "/* чтобы выбрать max MesID */;

  var params = makeArray( SQLParam("BIC", PTCK_BIC),
                          SQLParam("CurMesID1", CurMesID),
                          SQLParam("OBJTYPE_FNSNOTICE1", OBJTYPE_FNSNOTICE ),
                          SQLParam("OBJTYPE_FNSNOTICE2", OBJTYPE_FNSNOTICE ),
                          SQLParam("CurMesID2", CurMesID)
                        );


  var rs = execSQLselect(strSQL, params, true);

  if( rs and rs.moveNext() )

     if( (FileFormat == WLD_FORMAT_TXT)  and (strlen(rs.value("t_AnnulFileName")) > 0) )
      SplitFile(rs.value("t_AnnulFileName"), resFileName);
      // 4-й (начиная с 1) символ заменить на "9"
      StrSet(resFileName, 4, "9");
    else

      if( not (strlen( rs.value("t_BIC")) > 0) )
        return -1;
      end;
      
      if( not (rs.value("t_DpCode") > 0) )
        return -1;
      end;

      var a : string = IfThenElse( strlen(rs.value("t_AnnulFileName")) > 0, "9", "1" ),
          bbbbbbb : string = subStr( rs.value("t_BIC"), 3 ), 
          DDDDDDDD : string = YYYYMMDD( date() ), 
          NNNNNN = "",
          RefID:integer = 0;
              
      if( FileFormat == WLD_FORMAT_XML )
        if( GetDprtIntegerRegValForFNSXML("ОПИСАТЕЛЬ РЕФЕРЕНСА УВЕДОМЛЕНИЙ", rs.value("t_DpCode"), @RefID) != 0 )
          RefID = 273;
        end;
      else
        if( GetDprtIntegerRegValForOPENAC("ОПИСАТЕЛЬ_РЕФЕРЕНСА", rs.value("t_DpCode"), @RefID) != 0 )
          return -1;
        end;
      end;
      
      if( GenerateReference (RefID, NNNNNN) != 0 )
        return -1;
      end;
      
      resFileName = "BUV" + a + bbbbbbb + "_" + DDDDDDDD + "_" + NNNNNN;

    end;
  
  else
    return -1; //Такого сообщения нет
  end;

  return resFileName;
end;

private macro GetBankRegNum( MesID:integer ):string

  var Code = "";
  var select = " select t_InsideAbonentID   " +
               "   from dwlmes_dbt          " +
               "     where t_MesID = :MesID ";
  var params = makeArray( SQLParam("MesID", MesID));
  var rs = execSQLselect( select, params, FALSE );

  if( rs and rs.moveNext() )

    Code = ПолучитьКодСубъекта(rs.Value(0), PTCK_BANKREGNUM);
    if( StrLen( Code ) > 0 )
      var ind = index( Code, "/" );
      if( ind > 0 )
        return strLpad( SubStr(Code,ind+1), 4, "0" );
      end;
    end;
  end;

  return "0000";
end;

//<PBq_P_FFFF.xml>
private macro getFileNameFNSPBxml( MesID:integer ):string

  var fileName = "";
  FILE f_wlresprm( wlresprm ) key 0;
  var ResultID : integer = GetObjIDByMesLink(MesID, OBJTYPE_MESDEFECT);
  var q = "", p = "", F = "";

  if( ResultID > 0 )
    f_wlresprm.ResultID = ResultID;
    if ( getEQ( f_wlresprm ) )
      
      // q = номер подтверждения
      if( ( f_wlresprm.ErrCode == 0 ) or ( f_wlresprm.ErrCode == 8 ) )
        q = "1";
      else
        q = "2";
      end;
      fileName = fileName + q;

      // P - наименование полученного файла - задано в панели
      var idx = Index(f_wlresprm.FileName, "." );
      fileName = fileName + "_" + IfThenElse(idx > 0, SubStr(f_wlresprm.FileName, 1, idx - 1), f_wlresprm.FileName);

      if(q == "2")
        fileName = fileName + "_" + GetBankRegNum( MesID );
      end;
    else return "";
    end;
  else
    return "";
  end;
 
 return filename;
end;

private macro GetMesField( MesID, FieldName ) : string
  var select = " SELECT val.t_value "
               " FROM dwlmes_dbt mes, dwlmesval_dbt val, dwltpfld_dbt fld "
               " WHERE     mes.t_mesid = :MesID "
               "       AND mes.t_mesid = val.t_mesid "
               "       AND fld.t_tpfieldid = val.t_tpfieldid "
               "       AND fld.t_name = :FieldName ";

  var params = makeArray( SQLParam("MesID", MesID),
                          SQLParam("FieldName", FieldName ) );
  var rs = execSQLselect( select, params, FALSE );
  if( rs and rs.moveNext() )
    return rs.value(0);
  end;
  return "";
end;

macro GetFileNameMNS( FormName, SessID, MesID )
  var a = "", P = "", nn = "", rs;
  VAR dt : string = DateToStr_YYYYMMDD( date() );

  if ( (FormName == "BOS") or (FormName == "BNS") or (FormName == "BV") or (FormName == "BVS") )
    var IsXML = GetFormatFileBySess(SessID) == WLD_FORMAT_XML;
    
    if (not getFileNamePartsForFNSINFO(MesID, IsXML, @a, @P) )
      if (FormName == "BV")
        if( ПолучитьПоле(MesID, "НомЧасти", nn) )
          if( strLen(nn) == 1 )
            nn = "0" + nn;
          end;
        else  // не найдено значение поля $(НомЧасти)
          InitError;
          nn = "00";
        end;
        return a + nn + "_" + P;
      else
        if( not IsXML )
          return a + "_" + P;
        else
          rs = execSQLselect(" SELECT DECODE( INSTR(t_Code, '/'), 0, '0', NVL( SUBSTR( t_Code, INSTR(t_Code, '/')+1 ), '0') ) "
                             "   FROM dobjcode_dbt oc, dwlmes_dbt mes "
                             "  WHERE mes.t_MesID = ? "
                             "    AND mes.t_InsideAbonentID = oc.t_ObjectID "
                             "    AND oc.t_ObjectType = 3 "
                             "    AND oc.t_CodeKind = ? "
                             "    AND oc.t_State = 0 ",
                             makeArray(SQLParam("", MesID), SQLParam("", PTCK_BANKREGNUM)));
          if (not rs.MoveNext())
            return -1;
          end;
          return string(a, "_", P, "_", dt, "_", strLpad( rs.value(0), 4, "0" ) );
        end;
      end;
    else
      return -1;
    end;
  elif((FormName == "BNP"))
    if (not getFileNameBNP(MesID, @P))
      rs = execSQLselect(" SELECT DECODE( INSTR(t_Code, '/'), 0, '0', NVL( SUBSTR( t_Code, INSTR(t_Code, '/')+1 ), '0') ) "
                         "   FROM dobjcode_dbt oc, dwlmes_dbt mes "
                         "  WHERE mes.t_MesID = ? "
                         "    AND mes.t_InsideAbonentID = oc.t_ObjectID "
                         "    AND oc.t_ObjectType = 3 "
                         "    AND oc.t_CodeKind = ? "
                         "    AND oc.t_State = 0 ",
                         makeArray(SQLParam("", MesID), SQLParam("", PTCK_BANKREGNUM)));
      if (not rs.MoveNext())
        return -1;
      end;

      a = getSendNumXml(MesID);

      return string(a, "_", P, "_", dt, "_", strLpad( rs.value(0), 4, "0" ) );
    else
      return -1;
    end;
  elif( FormName == "PB" )
    if( GetFormatFileBySess(SessID) == WLD_FORMAT_TXT )
      if( not getFileNameFNSPB( MesID, @a, @P ) )
        return a + "_" + P;
      end;
    else
      return getFileNameFNSPBxml(MesID);
    end;
  elif (FormName == "BUV")
    return getFileNameForFNSNOTICE(MesID, GetFormatFileBySess(SessID));
  elif (FormName == "BVD")
    if (not getFileNamePartsForFNSINFO(MesID, GetFormatFileBySess(SessID) == WLD_FORMAT_XML, @a, @P) )
      rs = execSQLselect(string("select CASE "
                                "         WHEN INSTR(t_Code,'/')>0 "
                                "         THEN LPAD( SubStr( t_Code, INSTR(t_Code, '/')+1 ), 4, '0'  ) "
                                "         ELSE '0000' "
                                "       END, "
                                "       LPAD(numAcc.t_Value, 6, '0'), LPAD(numFile.t_Value, 6, '0') ",
                                "  from dobjcode_dbt oc, dwlmes_dbt mes, dwlmesval_dbt numAcc, dwlmesval_dbt numFile ",
                                " where mes.t_MesID = ? and mes.t_InsideAbonentID = oc.t_ObjectID and oc.t_ObjectType = 3 and oc.t_CodeKind = ? AND oc.t_State = 0 ",
                                "   and numAcc.t_mesId = mes.t_MesID and numAcc.t_tpfieldID = ? ",
                                "   and numFile.t_mesId = mes.t_MesID and numFile.t_tpfieldID = ? "), 
                              makeArray(SQLParam("", MesID), SQLParam("", PTCK_BANKREGNUM), SQLParam("", 1161/*ПорНом*/), SQLParam("", 1163/*ПорНомДФ*/)));
      if ((not rs) or (not rs.MoveNext()))
        return -1;
      end;
      return string(a, "_", P, "_", dt, "_", rs.value(0), "_", rs.value(1), "_", rs.value(2)); 
    else
      return -1;
    end;
  elif (FormName == "BZ1")
    return FormName + "_" + GetMesField( MesID, "ИмяФайла" ) + "_" + dt;
  else
    return GetFileNamebySess(SessID);
  end;
end;

macro ПолучитьНаимКо( Department )
  var НаимКО = "";

  var HeadPartyID : integer = GetHeadDprtPartyID();
  if( HeadPartyID > 0 )
    НаимКО = Bnk_GetPartyName(HeadPartyID);

    var PartyID : integer = GetDprtPartyID(Department);
    if( PartyID > 0 )
      if( PartyID != HeadPartyID )
        var PartyName = Bnk_GetPartyName(PartyID);
        if(PartyName)
          НаимКО = НаимКО + ", филиал " + PartyName;
        end;
      end;
    end;
  end;
  return НаимКО;
end;

macro ПроверитьИНН()
  var INN = GetPartyINN({OurBank}, 1);
  if(RemoveKPP(INN) == "")
    RsbThrow( "Для " + {Name_Bank} + " не задан ИНН" );
  elif(StrLen(RemoveKPP(INN)) != 10)
    RsbThrow( "Для " + {Name_Bank} + " задано неверное значение ИНН" );
  elif(RemoveINN(INN) == "")
    RsbThrow( "Для " + {Name_Bank} + " не задан КПП" );
  elif(StrLen(RemoveINN(INN)) != 9)
    RsbThrow( "Для " + {Name_Bank} + " задано неверное значение КПП" );
  end;
end;

macro РазделитьЗапятымиФИО( ФИО: string ):string
  var pos1 = Index(ФИО, " "); 
  var pos2 = Index(SubStr( ФИО, pos1 + 1 ), " ");
  var result:string = StrSubst(ФИО, " ", ",");
  if( pos1 == 0 )
    result = result + ",,";
  elif( pos2 == 0 )
    result = result + ",";
  end;
  return result;
end;

/*Возвращает часть строки ФИО только до второго символа div*/
macro ФамилияИмя( ФИО: string, div:string ):string
  var pos1 = Index(ФИО, div);                            /* первый div в ФИО */
  var pos2 = Index( substr( ФИО, pos1 + 1 ), div );      /* второй div в ФИО */
  return substr( ФИО, 1, pos1 + pos2 - 1 );
end;

macro ПолучитьФИОПрБ( regval:string )
  var ФИО = РазделитьЗапятымиФИО( Trim( regval ) );
  if( strlen( ФИО ) > 184 )
    ФИО = substr( ФамилияИмя( ФИО, "," ), 1, 184 );
  end;
  return ФИО;
end;

macro DeleteWlRepClm( DecisionID )

   var cmd = RSDCommand("delete from dwlrepclm_tmp where t_DecisionID = :DecisionID" );
   cmd.addParam( "", RSDBP_IN, DecisionID );

   cmd.execute();

end;

macro InsertWlRepClm( wldec, ObjKind, ObjID, Account, MesStr )
  execSQL( "insert into dwlrepclm_tmp "
           " ( t_DecisionId, t_ObjKind, t_ObjID, t_Account, t_Message )"
           " VALUES ( :DecisionID, :ObjKind, :ObjID, :Account, :MesStr ) ",
           makeArray( SQLParam("DecisionID", wldec.DecisionID),
                      SQLParam("ObjKind",    ObjKind),
                      SQLParam("ObjID",      ObjID),
                      SQLParam("Account",    Account),
                      SQLParam("MesStr",     MesStr) )
         );
end;

macro ЗаписатьПолеДляАннул( FieldName, MesID )
   ЗаписатьПолеЛог(FieldName, GetFldValue( MesID, FieldName ));
end;

// Получить записи wlacclnk, связанные с объектом
macro getRsWlacclnk(ObjectID:integer, ObjectType:integer):RsdRecordset
  var select = " select *" +                                              
               "   from dwlacclnk_dbt" +
               "  where t_ObjectID    = :ObjID " +
               "    and t_ObjectType  = :ObjType " +
               " order by t_Account ";
  var params = makeArray( SQLParam("ObjID", ObjectID),
                          SQLParam("ObjType", ObjectType) );
  var rs = execSQLselect( select, params );
  return rs;
end;

macro ПолучитьВидСчДляСправки(Type_Acc : string): string
  if( (index(Type_Acc, "Ч") != 0) or (index(Type_Acc, "X") != 0) )
    return "расчетный счет";
  elif( index(Type_Acc, "Б") != 0 )
    return "бюджетный счет";
  elif( index(Type_Acc, "К") != 0 )
    return "корреспондентский счет";
  elif( index(Type_Acc, "Я") != 0 )
    return "спец. банковский";
  elif( index(Type_Acc, "Q") != 0 )
    return "спец. брокерский";
  elif( (index(Type_Acc, "J") != 0) OR (index(Type_Acc, "G") != 0) )
    return "депозитный счет";
  end;
  return "расчетный счет";
end;

macro IsNDigits(s : string, N : integer, BanFirstZeros : bool) : bool
  if( strlen(s) != N )
    return false;
  end;

  if(not StrIsNumber(s))
    return false;
  end;

  if(BanFirstZeros)
    if( substr(s, 1, 2) == "00" )
      return false;
    end;
  end;

  // все проверки пройдены
  return true;
end;

// проверка для организации
macro IsGoodINN_Org(INN : string) : bool
  return IsNDigits(INN, 10, true);
end;

// проверка для физика
macro IsGoodINN_Ind(INN : string) : bool

  if( IsNDigits(INN, 12, true) )
    return TRUE;
  end;

  return FALSE;
end;

macro IsGoodKIO(KIO : string) : bool
  return IsNDigits(KIO, 5, false);
end;

macro IsGoodKPP(KPP : string) : bool
  return IsNDigits(KPP, 9, true);
end;

macro IsGoodOGRN_Org(OGRN : string) : bool
  return IsNDigits(OGRN, 13, false);
end;

macro IsGoodOGRN_IndEmpl(OGRN : string) : bool
  return IsNDigits(OGRN, 15, false);
end;

macro IsGoodBIC(BIC : string) : bool
  if( substr(BIC, 1, 2) != "04" )
    return FALSE;
  end;

  if( not IsNDigits( substr(BIC, 3), 7, false ) )
    return FALSE;
  end;

  return TRUE;
end;

// Код налогового органа
macro GetTaxInstitutionCodeWithChecks
( TaxInstitution : integer, 
  TaxInstName : string, 
  ErrMesNoCode : string

) : string

  // TaxInstName - необязательный параметр
  if(TaxInstName == null)
    TaxInstName = "";
  end;

  var КодНОБ : string = "";

  // У налоговой инспекции банка есть код НО
  var Error : integer = 0;
  КодНОБ = ПолучитьКодСубъекта( TaxInstitution, PTCK_MNS, Error);
  if(Error or not КодНОБ)
    if(not ErrMesNoCode)
      ErrMesNoCode = "Не определен код налогового органа " + TaxInstName;
    end;

    RsbThrow(ErrMesNoCode);
  end;

  // поле соответсвует шаблону: <xs:pattern value="([0-9]{1}[1-9]{1}|[1-9]{1}[0-9]{1}){2}"/>
  if( not IsNDigits(КодНОБ, 4, true) or 
      (substr(КодНОБ, 3, 2) == "00") and (КодНОБ != "0400")
    )
    RunError("Неверный код налогового органа банка: " + КодНОБ);
  end;

  return КодНОБ;
end;

macro GetCodeNOBWithChecks
( BankID : integer, 
  ErrMesNoTaxInst : string, 
  ErrMesNoCode : string

) : string

  var КодНОБ : string = "";

  var TaxInstName : string = "";

  // Задана налоговая инспекция банка
  var НалоговаяИнспекция : integer = GetPartyTaxInstitution(BankID, @TaxInstName);
  if(НалоговаяИнспекция <= 0)
    if(not ErrMesNoTaxInst)
      ErrMesNoTaxInst = "Не задан налоговый орган по месту учета банка";
    end;

    RsbThrow(ErrMesNoTaxInst);
  end;

  КодНОБ = GetTaxInstitutionCodeWithChecks(НалоговаяИнспекция, TaxInstName, ErrMesNoCode);

  return КодНОБ;
end;

macro ПолучитьБИКГоловногоФилиала( PartyID )

  var DpHeadBIC = {MFO_Bank};

  var DpHeadID : integer = GetTSHeadPartyID( PartyID );
  if( DpHeadID > 0 )
    var err;
    DpHeadBIC = ПолучитьКодСубъекта( DpHeadID, PTCK_BIC, err );
  end;

  return DpHeadBIC;
end;

// Проверки справок ФНС
macro CheckObj_FNSINFO( regdec )
  
  FILE party( party ) key 0; // ключ по PartyID
  var Code = "", Error = 0;
  
  //--------------------------------------------------------------------------
  // Originator
  var PartyName = "", ParentPartyID = 0;
  FILE dp_dep( dp_dep ) key 0; // ключ по Code

  // Наименование банка
  party.PartyID = regdec.OriginatorID;
  if( GetEQ( party ) )
    PartyName = party.Name;
    if( party.Name == "" )
      RsbThrow( string( "Для ", regdec.OriginatorCode, " не задано наименование " ) );
    end;
  else
    RsbThrow( string( "Не найден субъект ", regdec.OriginatorID ) );
  end;
  //Если связанный с субъектом филиал не является головной организацией (dp_dep.ParentCode), 
  // то также задано полное наименование субъекта - головной организации
  var rs_dp_dep = getRsDpDepByPartyID( regdec.OriginatorID );
  if( ( rs_dp_dep ) and ( rs_dp_dep.moveNext() ) )
    if( rs_dp_dep.value("t_ParentCode") != 0 )
      ParentPartyID = GetTSHeadPartyID( regdec.OriginatorID );
      if( ParentPartyID > 0 )
        party.PartyID = ParentPartyID;
        if( GetEQ( party ) )
          if( party.Name == "" )
            RsbThrow( string( "Для ", regdec.OriginatorCode, " не задано наименование " ) );
          end;
        else
          RsbThrow( string( "Не найден субъект, связанный с филиалом ", rs_dp_dep.value("t_Name") ) );
        end;
      else
        RsbThrow( "Не найден головной филиал" );
      end;
    end;
  else
    RsbThrow( string( "Субъект ", regdec.OriginatorID, " не является филиалом нашего банка" ) );
  end;

  /*ИНН банка*/
  var ИНН : string = RemoveKPP( ПолучитьКодСубъекта(regdec.OriginatorID, PTCK_INN, Error, 0) );
  if( Error or (ИНН == "") )
    RsbThrow(string("Для ", PartyName, " не задан ИНН"));
  end;
  
  /*Количество символов в ИНН банка*/
  if(strlen(ИНН) != 10)
    RsbThrow( string( "Для ", PartyName, " задано неверное значение ИНН" ) );
  end;
  
  /*КПП банка*/
  var КПП : string = RemoveINN( GetPartyINN( regdec.OriginatorID, 1) );
  if( КПП == "" )
    RsbThrow(string("Для  ", PartyName, " не задан КПП"));
  end;

  /*Количество символов в КПП банка*/
  if(strlen(КПП) != 9)
    RsbThrow(string("Для ", PartyName, " задано неверное значение КПП"));
  end;

  /*Регистрационный номер банка*/
  Code = ПолучитьКодСубъекта( regdec.OriginatorID, PTCK_BANKREGNUM, Error);
  if(Error)
    RsbThrow(string("Для ", PartyName, " не задан регистрационный номер кредитной организации"));
  end;

  // Регистрационный номер филиала
  if( ParentPartyID != 0 )
    if( not ( ( Index( Code,"/") != 0 ) and (strlen(SubStr(Code, Index( Code,"/")+1)) != 0) ) )
      RsbThrow(string("Для ", PartyName, " не задан порядковый номер филиала"));
    end;
  end;  
  
  /*БИК банка*/
  ПолучитьКодСубъекта( regdec.OriginatorID, PTCK_BIC, Error, 1, 1);
  if(Error)
    RsbThrow(string("Для ", PartyName, " не задан БИК"));
  end;

  //--------------------------------------------------------------------------
  // Client
  record ClientParty(party);
  if (regdec.ClientID > 0)
    if( ПолучитьСубъекта(regdec.ClientID, ClientParty) )
      RsbThrow(string("Не найден налогоплательщик ", regdec.ClientID));
    end;
    Code = ПолучитьКодСубъекта( regdec.ClientID, PTCK_CONTR, Error);
    if(Error)
      Code = "";
    end;
  else
    Code = "";
  end;
        
  /*Наименование клиента*/
  var ClientName = "";
  if ( strlen(regdec.ClientINN) != 12 )
    // Наименование клиента юридического лица
    if (regdec.ClientID > 0)
      if(ClientParty.Name)
        ClientName = ClientParty.Name;
      else
        RsbThrow(string("Для клиента ", Code, " не задано наименование"));      
      end;
    else
      if(regdec.ClientName)
        ClientName = regdec.ClientName;
      else
        RsbThrow(string("Для клиента ", Code, " не задано наименование"));
      end;
    end;
  else
    // Наименование клиента физического лица
    if (regdec.ClientID > 0)
      var persn = Tbfile("persn.dbt", "r");
      persn.rec.PersonID = regdec.ClientID;
      if( GetEQ(persn) )
        if(persn.rec.Name1)
          ClientName = persn.rec.Name1;
        else
          RsbThrow( string("Для клиента ", Code, " не задана фамилия") );
        end;
        if(persn.rec.Name2)
          ClientName = ClientName + " " + persn.rec.Name2;
        else
          RsbThrow( string("Для клиента ", Code, " не задано имя") );
        end;
      else
        RsbThrow( string("Не найдено физическое лицо ", regdec.ClientID) );
      end;
    else
      ClientName = Trim(regdec.ClientName);
      if(ClientName == "")
        RsbThrow( string("Для клиента ", Code, " не задана фамилия") );
      end;
      if( index(ClientName, " ") == 0 )
        RsbThrow( string("Для клиента ", Code, " не задано имя") );
      end;
    end;
  end;

  if(regdec.ClientINN == "")
    RsbThrow( string("Для ", ClientName, " не задан ИНН") );
  end;

  /*Количество символов в ИНН клиента*/
  if(regdec.ClientID > 0)
    if( ((ClientParty.LegalForm == PTLEGF_INST) and 
         (ClientParty.NotResident != "X") and 
         (strlen(regdec.ClientINN) != 10)
        ) or 
        ((ClientParty.LegalForm == PTLEGF_INST) and 
         (ClientParty.NotResident == "X") and 
         (strlen(regdec.ClientINN) != 5 ) and (strlen(regdec.ClientINN) != 10)
        ) or 
        ((ClientParty.LegalForm == PTLEGF_PERSN) and (strlen(regdec.ClientINN) != 12)) )
      RsbThrow(string("Для ", ClientName, " задано неверное значение ИНН"));
    end;
  end;
  
  if( strlen(regdec.ClientINN) == 10 ) 
    /*КПП клиента*/
    if (regdec.ClientKPP == "")
      RsbThrow(string("Для ", ClientName, " не задан КПП"));
    end;
    /*Количество символов в КПП клиента*/
    if ( strlen(regdec.ClientKPP) != 9 )
      RsbThrow(string("Для ", ClientName, " задано неверное значение КПП"));
    end;
  end;

  /*Сотрудник, подписывающий сообщение*/
  var tmpstr = "";
  Error = GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ДОЛЖ", rs_dp_dep.value("t_Code"), @tmpstr );
  if( Error or ( Trim(tmpstr) == "") )
    RsbThrow(string("Не задана должность сотрудника, подписывающего сообщения в ФНС"));
  end;
  Error = GetDprtStringRegValForOPENAC( "СПРАВКИ_ИФНС_ФИО", rs_dp_dep.value("t_Code"), @tmpstr );
  tmpstr = Trim(tmpstr);
  if( Error or (tmpstr == "") or (index(tmpstr, " ") == 0) )  
    RsbThrow(string("Не заданы ФИО сотрудника, подписывающего сообщения в ФНС"));
  end;
/*  Error = GetRegValForOPENAC("СПРАВКИ_ИФНС_ДОЛЖ", V_STRING, tmpstr);
  if( Error or ( Trim(tmpstr) == "") )
    RsbThrow(string("Не заданы ФИО сотрудника, подписывающего сообщения в ФНС"));
  end;
  Error = GetRegValForOPENAC("СПРАВКИ_ИФНС_ФИО", V_STRING, tmpstr);
  if( Error or (tmpstr == "") or (index(tmpstr, " ") == 0) )
    RsbThrow(string("Не заданы ФИО сотрудника, подписывающего сообщения в ФНС"));
  end;*/
  
  
  /*Имя файла исходного сообщения*/ 
  var note : string = readNoteForObject(OBJTYPE_FNSINFO, 
                                        makeObjectID(OBJTYPE_FNSINFO, NULL, regdec), 
                                        NOTEKIND_FNSINFO_INITIALFILENAME);
  if(note)
    /*Количество символов в имени файла*/
    if(strlen(note) != 31)
      RsbThrow("Неверно задано имя файла запроса ФНС или решения рег. органа");
    end;
  else
    RsbThrow("Невозможно определить имя файла исходного запроса. Задайте имя файла в примечании или идентификатор запроса в справке/выписке");
  end;

  /*Символы в  номере */
  if ( (strlen(regdec.Number) > 6) or (not StrIsNumber(regdec.Number)) )
    RsbThrow("Неверно задан номер справки (выписки)");
  end;

  /*Символы в номере связанного объекта*/
  if ( (strlen(regdec.LnkDocNumber) > 6) or (not StrIsNumber(regdec.LnkDocNumber)) )
    RsbThrow("Неверно задан номер связанного объекта");
  end;

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;

// Найти ID юр.лица по его ИНН
macro GetLegalPersonIDByINN( inn:string )
  var rs:object;
  var select:string;
  var params:TArray;
    
  select = " select pt.t_PartyID"+
             " from dpartcode_dbt code, dparty_dbt pt"+
            " where code.t_PartyID = pt.t_PartyID"+
              " and pt.t_LegalForm = :legalForm"+
              " and SUBSTR (code.t_Code, 1, 10) like :Code"+
              " and code.t_CodeKind = :CodeKind";
  params = makeArray( SQLParam("legalForm",PTLEGF_INST),
                      SQLParam("Code", inn ),
                      SQLParam("CodeKind",PTCK_INN));
  
  rs = execSQLselect( select, params, FALSE );
  
  if(rs.moveNext())
    return rs.value(0);
  end;     

  return 0;
end;

/* Было ли сообщение уже загружено */
macro CheckOnDoubling( TRN, Релиз ):bool
               
  var rs:object;
  var select:string;
  var params:TArray;
  /* Если уже было такое сообщение от корреспондента - не закачиваем */
  select = "select mes.t_RlsFormID from dwlmes_dbt mes "+
           "where  mes.t_Direct='X' and "
                  "mes.t_Department =:OperD and "+ 
                  "mes.t_TRN =:NumberDoc and "+
                  "mes.t_RlsFormID = :RlsFormID";  
  params = makeArray( SQLParam("OperD",     {OperDprt}),
                      SQLParam("NumberDoc", TRN),
                      SQLParam("RlsFormID", Релиз) );  

  rs = execSQLselect( select, params, FALSE );

  var continue0 = rs.MoveNext();
  while( continue0 )
    if( rs.value(0) == Релиз )
      std.out( 1, "Cообщение уже было принято - игнорируется" );
      return false;
    end;
    continue0 = rs.MoveNext();
  end;
  return true;
end;

/***************************************************************************/
/*  Функция формирования идентификатора файла                              */
/*  Возвращает в случае неудачи пустую строку                              */
/***************************************************************************/
macro MakeIDFile(ExportFileName : string, FormName : string)
   var retStr : string = "", Num : string = "", fname : string = "", ext : string = "";

   if( (FormName == "BOS") or 
       (FormName == "BNS") or
       (FormName == "BV") )
     Num = execStoredFunc( "sys_guid", V_STRING );
   else
     SplitFile ( ExportFileName, fname,ext );
     Num = SubStr(fname, 9, 6);
   end;
   if(not Num)
      return Num;
   end;
   retStr = string(ПолучитьИдентификаторОтправителя(), YYYYMMDD(date()), Num);
   return retStr;
end;

macro PutPersData_Default(Name1, Name2, Name3, Officer, Phone): integer
  Name1 = Name2 = Name3 = Officer = Phone = "";

  var q = "SELECT persn.t_Name1, persn.t_Name2, persn.t_Name3, "
          "       officer.t_Post, officer.t_PhoneNumber "
          "  FROM dpersn_dbt persn, dofficer_dbt officer, dperson_dbt person "
          " WHERE officer.t_PersonID = person.t_PartyID "
          "   AND persn.t_PersonID   = person.t_PartyID "
          "   AND person.t_Oper      = :Oper "
          "   AND officer.t_PartyID = :OurBank ";

  var rs = execSQLselect(q, makeArray( SQLParam("Oper", {Oper}),
                                       SQLParam("OurBank", {OurBank}) )
                        );

  if(rs and rs.moveNext())
    Name1 = rs.value(0);
    Name2 = rs.value(1);
    Name3 = rs.value(2);
    Officer = rs.value(3);
    Phone = rs.value(4);
  end;

  SetParm(0, Name1);
  SetParm(1, Name2);
  SetParm(2, Name3);
  SetParm(3, Officer);
  SetParm(4, Phone);

  return 0;
end;

macro GetRegOrgKindByFormName( ИмяФормы : string ) : integer

  var ВидРегОргана : integer = 0;

  if( InList(ИмяФормы, "SBE", "SBF", "SBK", "KWT",
                       "SFE", "SFF", "SFK"))
    ВидРегОргана = REG_ORGAN_KIND_FNS;
  elif( InList(ИмяФормы, "SBP", "SBN",
                         "SFP", "SFN") )
    ВидРегОргана = REG_ORGAN_KIND_PFR;
  elif( InList(ИмяФормы, "SBR", "SBT",
                         "SFR", "SFT") )
    ВидРегОргана = REG_ORGAN_KIND_FSS;
  end;

  return ВидРегОргана;
end;

macro ОпределитьИмяФормыФНС
( ИмяФайла : string, 
  ВидРегОргана : @integer, 
  ВидСправки : @string 

) : string

  var ИмяФормыФНС : string = "";

  var ИмяФормы = StrUpr( SubStr( ИмяФайла, 1, 3 ) );

  ВидРегОргана = GetRegOrgKindByFormName( ИмяФормы );

  if( InList(ИмяФормы, "SBF", "SBE", "SBP", "SBR", "SFF", "SFE", "SFK", "SFP", "SFR", "SFT", "SFN") )
    ИмяФормыФНС = "SBF";
  elif( InList(ИмяФормы, "SBK", "SBN", "SBT") )
    ИмяФормыФНС = "SBK";
  elif( (ИмяФормы=="ZNS") or (ИмяФормы=="ZOS") or (ИмяФормы=="ZVS") )
    ИмяФормыФНС = "ZNS";
    ВидСправки = SubStr( ИмяФайла, 2, 2 );
  elif( InList(ИмяФормы, "RPO", "ROO", "TRB", "ZNO", "KWT", "PNO", "BUV", 
                         "ZSO", "ZSN", "ZSV", "PPD", "APN", "APO", "APZ") )
    ИмяФормыФНС = ИмяФормы;
  end;

  return ИмяФормыФНС;
end;

// Поиск исходящего сообщения SBC для квитовки со входящим SBF, SBK
macro GetKvitSBC
(
  InMesID : integer,
  NumMes : string,
  InsideAbonentID : @integer,
  InsideAbonentCodeKind : @integer,
  InsideAbonentCode : @string,
  InMesFileName : @string,
  TRN : @string,
  State : @integer

) : integer

  var OutMesID : integer = 0;

  var rs : object;
  var select : string;
  var params : TArray;
  var v_InMesFileName, v_OutMesFileName, isFoundMes;

  // Получаем наименование входящего файла

  select = "select substr(sess.t_filename, instr(sess.t_filename, '\\', -1, 1) + 1) t_ShortFileName"+
            " from dwlmes_dbt mes, dwlsess_dbt sess"+ 
           " where mes.t_MesID = :MesID"+
             " and mes.t_sessionid = sess.t_sessionid";

  params = makeArray( SQLParam("MesID", wlmes.MesID) );

  rs = execSQLSelect( select, params, true );

  if( rs.MoveNext() )

    v_InMesFileName = rs.value("t_ShortFileName");


    // Сообщение банка об открытии (закрытии) счета, об изменении реквизитов счета и 
    // квитанция о принятии (непринятии) сообщения банка квитуются по совпадению 
    // (кроме третьей буквы) имен файлов

    v_OutMesFileName = StrUpr(substr(v_InMesFileName,1,2) + "C" + substr(v_InMesFileName,4));

    select = "select mes.t_MesID, "
                    "mes.t_state, "
                    "mes.t_TRN, "+
                    "mes.t_InsideAbonentID, "
                    "mes.t_InsideAbonentCodeKind, "
                    "mes.t_InsideAbonentCode "
             " from dwlmes_dbt mes,"+ 
                  " dwlsess_dbt sess"
               " where upper(substr(sess.t_filename, instr(sess.t_filename, '\\', -1, 1) + 1)) = :OutMesFileName "+ 
                " and mes.t_sessionid = sess.t_sessionid";


    params = makeArray( SQLParam("OutMesFileName", v_OutMesFileName) );

    rs = execSQLSelect( select, params, true );

    isFoundMes = rs.MoveNext();

    if( not isFoundMes and (NumMes != "") )
        // вторая попытка - ищем по полю НомСооб
        select = "select mes.t_MesID, "
                        "mes.t_state, "
                        "mes.t_TRN, "+
                        "mes.t_InsideAbonentID, "
                        "mes.t_InsideAbonentCodeKind, "
                        "mes.t_InsideAbonentCode "
                       "from dwltpfld_dbt fld, "
                            "dwlmesval_dbt val, "
                            "dwlmes_dbt mes " 
                       "where fld.t_TpID =  " + TRANSP_MNS +
                         "and fld.t_Name = 'НомСооб' "
                         "and val.t_TpFieldID = fld.t_tpFieldID " +
                         "and val.t_Value = :Val "
                         "and mes.t_Direct = chr(0) " +
                         "and mes.t_MesID = val.t_MesID ";
        
        params = makeArray( SQLParam( "Val", NumMes ) );

        rs = execSQLSelect( select, params, true );

        isFoundMes = rs.MoveNext();
    end;
  end;

  if( isFoundMes )
    OutMesID = rs.value("t_MesID");
    InsideAbonentID = rs.value("t_InsideAbonentID");
    InsideAbonentCodeKind = rs.value("t_InsideAbonentCodeKind");
    InsideAbonentCode = rs.value("t_InsideAbonentCode");
    InMesFileName = v_InMesFileName;
    TRN = rs.value("t_TRN");
    State = rs.value("t_State");
  end;

  return OutMesID;
end;

macro GetInsideAbonentForSBF
( MesDprt : integer, 
  InsideAbonentID : @integer,
  InsideAbonentCodeKind : @integer,
  InsideAbonentCode : @string

) : bool
  var rs = execSQLSelect( "select dpd.t_PartyID, pc.t_Code " +
                            "from ddp_dep_dbt dpd " +
                            "join dpartcode_dbt pc on pc.t_PartyID = dpd.t_PartyID " +
                           "where pc.t_CodeKind = :PTCK_CONTR and dpd.t_Code = :Dprt ",
                          makeArray( SQLParam("PTCK_CONTR", PTCK_CONTR),
                                     SQLParam("Dprt", MesDprt) )
                        );

  if(rs and rs.moveNext())
    InsideAbonentID = rs.value("t_PartyID");
    InsideAbonentCodeKind = PTCK_CONTR;
    InsideAbonentCode = rs.value("t_Code");
    return TRUE;
  end;

  return FALSE;
end;

private macro GetRegPartyKindShortName( RegPartyKind : integer ) : string

  var Tb_Objkrgpt = Tbfile("Objkrgpt.dbt");
  Tb_Objkrgpt.rec.RegPartyKind = RegPartyKind;

  if(GetEQ(Tb_Objkrgpt))
     return Tb_Objkrgpt.rec.ShortName;
  end;

  return "";
end;

macro GetConfDelivDescription(RegOrgKind : integer) : string
  var Description : string = Bnk_GetLLValuesName(OBJTYPE_WLRESCODE_MNS, FNS_RP_APPROVED);

  if( strlen(Description) > 0 )
    Description = Description + " подразделением " + GetRegPartyKindShortName(RegOrgKind);
  end;

  return Description;
end;

private macro IsDeptNumInRegNum(RegNum : string, DeptNum : string) : bool
  var SlashPos : integer = index(RegNum, "/");

  // У головного в коде вида 13 номер филиала может быть не задан
  if( (DeptNum == "0") and ( (SlashPos == 0) or (SlashPos == strlen(RegNum)) ) )
    RETURN true;
  end;

  // Если номер филиала задан, он должен совпадать с НомФ
  if( SlashPos and (substr(RegNum, SlashPos + 1) == DeptNum) )
    RETURN true;
  end;

  RETURN false;
end;

private macro GetSubPartyIdByBicAndDeptNum
(
  ParentID : integer,
  BIC : string, 
  DeptNum : string

) : integer

  var PartyID : integer = -1;

  var qSubDepts : string =
    " select dp.t_PartyID "
    "   from ddp_dep_dbt dp, dpartcode_dbt pc13 "
    "  where pc13.t_PartyID = dp.t_PartyID " +
    "    and pc13.t_CodeKind = " + string(PTCK_BANKREGNUM) +
    "    and ( :DepNum = '0' and " +
    "          (instr(pc13.t_Code, '/') = 0 or instr(pc13.t_Code, '/') = length(pc13.t_Code)) " +
    "         or " +
    "          substr(pc13.t_Code, instr(pc13.t_Code, '/') + 1) = :DepNum1 " +
    "        ) "
    "  start with dp.t_PartyID = :ParentID "
    "  connect by dp.t_ParentCode = prior dp.t_Code ";

  var params : TArray = makeArray( SQLParam("DepNum", DeptNum),
                                   SQLParam("DepNum1", DeptNum),
                                   SQLParam("ParentID", ParentID) );

  var rs : RsdRecordset = execSQLselect(qSubDepts, params, true);
  
  while( rs and rs.moveNext() )
    var SubPartyID : integer = rs.value("t_PartyID");

    var DprtBIC : string = ПолучитьКодСубъекта(SubPartyID, PTCK_BIC, null, 1);
    if(DprtBIC == BIC)
      PartyID = SubPartyID;
      break;
    end;
  end;

  return PartyID;
end;

// Макрофункция вызывается из сишника, поэтому при изменении сигнатуры нужно править сишник
macro getPartyIDFromBICandDeptNum
(
  BIC : string, 
  DeptNum : string, 
  IsDpDepOptional : bool

) : integer

  // Найти субъект party, где:
  // - код вида 3 равен значению поля "БИК" с учетом иерархии 
  // - с заданным кодом вида 13
  // - Если значение поля "НомФ" = "0", то:
  //   В коде вида 13 нет символов после слеша 
  //   Иначе
  //   Символы после слеша в коде вида 13 равны значению поля "НомФ"
  var PartyID : integer = -1;

  var BicOwner : integer = ПолучитьКодСубъекта(BIC, PTCK_BIC);
  if(BicOwner > 0)
    var RegNum : string = ПолучитьКодСубъекта(BicOwner, PTCK_BANKREGNUM);

    if( IsDeptNumInRegNum(RegNum, DeptNum) )
      PartyID = BicOwner;
    else
      PartyID = GetSubPartyIdByBicAndDeptNum(BicOwner, BIC, DeptNum);
    end;

    if(PartyID > 0)
      // Если нужно проверять, что субъект входит в ТС
      if(not IsDpDepOptional)
        // Найти филиал dp_dep, где dp_dep.partyID = party.partyID
        var qIsPartyIDInTS = "select 1 "
                         "  from ddp_dep_dbt t "
                         " where t.t_PartyID = :PartyID ";

        var rs : RsdRecordset = execSQLselectPrm( qIsPartyIDInTS, 
                                                  SQLParam("PartyID", PartyID) );

        if( rs and rs.moveNext() )
          // проверка пройдена
        else
          PartyID = -1;
        end;
      end;
    end;
  end;

  return PartyID;

end;

// у филиала DprtID есть подчиненные филиалы
private macro HasSubDprt(DprtID : integer) : bool
  var rs = execSQLselectPrm
  (
    "select 1 "
    "  from dual "
    " where exists "
    " ( select 1 "
    "     from ddp_dep_dbt "
    "    where t_ParentCode = :DprtID "
    " ) ",
    SQLParam("DprtID", DprtID)
  );

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

private macro FnsImp_ЗаполнитьАбонентаПоБИКБПл
( BIC : string, 
  НомСчПл : TArray,
  wlmes : StrucRef
)
  var error : integer = 0;
  var PartyID : integer = ПолучитьКодСубъекта(BIC, PTCK_BIC, error);
  var DprtID : integer = -1;
  if(not error)
    DprtID = GetDepIdByPartyId(PartyID);
  end;

  // Если субъект  найден, то
  if(DprtID > 0)
    wlmes.InsideAbonentCodeKind = PTCK_BIC;
    wlmes.InsideAbonentCode = BIC;

    if( HasSubDprt(DprtID) )
      var AccPartyID : integer = 0;
      var AccBIC : string = "";
      for(var AccNum : string, НомСчПл)
        var AccRec : TRecHandler = TRecHandler("account.dbt");
        if( PM_GetAccountRecord( AccNum, Bnk_GetAccountFIID(AccNum), CHAPT1, AccRec ) )
          AccPartyID = GetDprtPartyID(AccRec.rec.Department);
          AccBIC = GetPartyCode(AccPartyID, PTCK_BIC);
          if (AccBIC == BIC)
          break;
          else
             AccPartyID = 0;
          end;
        end;
      end;

      if(AccPartyID)
        wlmes.InsideAbonentID = AccPartyID;
      else
        wlmes.InsideAbonentID = PartyID;
      end;
    else
      wlmes.InsideAbonentID = PartyID;
    end;
  else
    wlmes.AgentCodeKind = PTCK_BIC;
    wlmes.AgentCode = BIC;
  end;
end;

macro FnsImp_UpdateInsideAbonent
( FormName : string, 
  wlmes : StrucRef,
  BIC : string,
  DeptNum : string,
  IsDpDepOptional : bool,
  ЕстьПолеБИКБПл : bool,
  НомСчПл : TArray
)
  Bnk_ToRSTrace( "FnsImp_UpdateInsideAbonent", "Begin", 
                 "BIC="+BIC+", DeptNum="+DeptNum+", ЕстьПолеБИКБПл="+
                 IfThenElse(ЕстьПолеБИКБПл, "true", "false")
               );

  // Если в сообщении есть поля "БИК" и "НомФ"
  if( BIC and DeptNum )
    var PartyID = getPartyIDFromBICandDeptNum(BIC, DeptNum, IsDpDepOptional);

    if(PartyID > 0)
      wlmes.InsideAbonentID = PartyID;
      wlmes.InsideAbonentCodeKind = PTCK_BIC;
      wlmes.InsideAbonentCode = BIC;      

      ОбновитьЗапись(wlmes);
    end;
  elif(ЕстьПолеБИКБПл)
    FnsImp_ЗаполнитьАбонентаПоБИКБПл(BIC, НомСчПл, wlmes);
    ОбновитьЗапись(wlmes);
  end;

  Bnk_ToRSTrace( "FnsImp_UpdateInsideAbonent", "End", 
                 " wlmes.InsideAbonentID = " + wlmes.InsideAbonentID +
                 " wlmes.InsideAbonentCodeKind = " + wlmes.InsideAbonentCodeKind +
                 " wlmes.InsideAbonentCode = " + wlmes.InsideAbonentCode
               );
end;

class TAddrRF
  var Индекс : string = "",
      КодРегион : string = "",
      Район : string = "",
      Город : string = "",
      НаселПункт : string = "",
      Улица : string = "",
      Дом : string = "",
      Корпус : string = "",
      Кварт : string = "";
end;

// Адрес в Российской Федерации (АдрРФТип) - получение данных
// true - удалось заполнить поля, false - не удалось
macro GetDataAddrRF(PartyID : integer, Addr : TAddrRF, ErrMsg : @string) : bool

  macro GetAddrItemWithCode(AddrItem : string, AddrItemCode : string) : string
    var field_value : string = AddrItem;

    if(field_value)
      if(AddrItemCode)
        field_value = field_value + " " + AddrItemCode;
      end;

      // Формат полей НаселПункт, Улица - A50
      field_value = substr(field_value, 1, 50);
    end;

    return field_value;
  end;


  record adress(adress);

  // Проверим, есть ли у нас данные для заполнения обязательных полей
  if( not НайтиЮридическийАдресСубъекта(PartyID, adress) )
    ErrMsg = "Не найден юридический адрес субъекта " + PartyID;
    return FALSE;
  end;

  // Индекс
  if(adress.PostIndex)
    Addr.Индекс = SubStr(adress.PostIndex, 1, 6);
  else
    ErrMsg = "Не указан почтовый индекс в юридическом адресе субъекта " + PartyID;
    return FALSE;
  end;

  // КодРегион
  if(adress.RegionNum)
    Addr.КодРегион = SubStr(adress.RegionNum, 1, 2);
  elif(adress.Country != "RUS")
    Addr.КодРегион = "99";
  else
    ErrMsg = "Не указан код региона в юридическом адресе субъекта " + PartyID;
    return FALSE;
  end;

  // Район
  if( adress.Province and not InList(adress.RegionNum, "77", "78", "92") )
    Addr.Район = adress.Province;
  end;

  // Город
  if(adress.District)
    Addr.Город = adress.District;
  elif(not adress.Place)
    if(adress.RegionNum == "77")
      Addr.Город = "Москва";
    elif(adress.RegionNum == "78")
      Addr.Город = "Санкт-Петербург";
    elif(adress.RegionNum == "92")
      Addr.Город = "СЕВАСТОПОЛЬ";
    end;
  end;

  Addr.НаселПункт = GetAddrItemWithCode(adress.Place, adress.CodePlace);
  Addr.Улица = GetAddrItemWithCode(adress.Street, adress.CodeStreet);
  Addr.Дом = adress.House;
  Addr.Корпус = adress.NumCorps;
  Addr.Кварт = adress.Flat;

  return TRUE;
end;

// -----------------------------------------------------------------------------------
// данные кредитной организации для генерации сообщений по счетам в формате XML
// -----------------------------------------------------------------------------------
private macro ПолучитьПризнТер(PartyID : integer) : string
  var q : string = "select 1 "
                   "  from dadress_dbt "
                   " where t_PartyID = :PartyID "
                   "   and t_Type = :PTADDR_LEGAL "
                   "   and t_Country <> 'RUS' ";

  var params : TArray = makeArray( SQLParam("PartyID", PartyID),
                                   SQLParam("PTADDR_LEGAL", PTADDR_LEGAL) );

  var rs : RsdRecordset = execSQLselect(q, params);
  if( rs and rs.moveNext() )
    return "1";
  end;

  return "";
end;

private macro ПолучитьРегНомИНомФ
( PartyID : integer, 
  ПризнТер : string, 
  РегНом : @string, 
  НомФ : @string

) : bool

  var Error : integer = 0;
  var Code : string = ПолучитьКодСубъекта( PartyID, PTCK_BANKREGNUM, Error);

  if(not Error and Code)
    // до знака "/"
    РегНом = GetSymbolsBeforeSlash(Code);
    // без лидирующих нулей
    РегНом = strLtrim(РегНом, "0");

    if( (ПризнТер == "1") or НашБанкГоловной(PartyID) )
      НомФ = "0";
    else
      // после знака "/"
      НомФ = GetSymbolsAfterSlash(Code);
      // без лидирующих нулей
      НомФ = strLtrim(НомФ, "0");
    end;

    if(РегНом and НомФ)
      return TRUE;
    end;
  end;

  return FALSE;
end;

private macro GetINNKPP_CreditOrg
( PartyID : integer, 
  ИННКО : @string, 
  КППКО : @string, 
  ErrMsg : @string

) : bool

  GetINN_KPP(PartyID, @ИННКО, @КППКО, 1/*recursive*/);

  if(ИННКО)
    if( not IsGoodINN_Org(ИННКО) )
      ErrMsg = "Неверное значение ИНН: " + ИННКО;
      return FALSE;
    end;

    if( not IsGoodKPP(КППКО) )
      ErrMsg = "Неверное значение КПП: " + КППКО;
      return FALSE;
    end;
  else
    ErrMsg = "Не задан ИНН банка";
    return FALSE;
  end;

  return TRUE;
end;

class TBankInfoAccMsg
  var РегНом : string = "", НомФ : string = "", БИК : string = "", НаимКО : string = "",
      ИННКО : string = "", КППКО : string = "", ОГРНКО : string = "", ПризнТер : string = "",
      Addr : TAddrRF = TAddrRF();

  macro Fill(PartyID : integer, ErrMsg : @string, Is365P : bool) : bool
    var Error : integer = 0;
    var PartyShortName : string = Bnk_GetPartyShortName(PartyID);

    // ПризнТер
    ПризнТер = ПолучитьПризнТер(PartyID);

    // РегНом, НомФ
    if(not ПолучитьРегНомИНомФ(PartyID, ПризнТер, @РегНом, @НомФ))
      ErrMsg = "Не задан регистрационный номер банка и/или порядковый номер филиала \"" + PartyShortName + "\"";
      return FALSE;
    end;

    var HeadID : integer = 0;
    if(ПризнТер == "1")
      HeadID = GetTSHeadPartyID(PartyID);
      if(not HeadID)
        ErrMsg = "Не найден головной филиал банка \"" + PartyShortName + "\"";
        return FALSE;
      end;
    end;

    var FillBankID : integer = IfThenElse(ПризнТер == "1", HeadID, PartyID);

    // БИК
    Error = 0;
    БИК = ПолучитьКодСубъекта( FillBankID, PTCK_BIC, Error, 1);
    if(not БИК)
      ErrMsg = "Не задан БИК банка \"" + PartyShortName + "\"";
      return FALSE;
    elif(not IsGoodBIC(БИК))
      ErrMsg = "Неверный БИК банка: " + БИК;
      return FALSE;
    end;

    // НаимКО
    НаимКО = PartyShortName;
    if(ПризнТер == "1")
      // party.shortName для головного банка и в скобках party.name самого субъекта
      НаимКО = Bnk_GetPartyShortName(HeadID) + " (" + НаимКО + ")";
    end;
    if(not НаимКО)
      ErrMsg = "Не задано краткое наименование банка с ИД субъекта " + PartyID;
      return FALSE;
    end;

    // ИННКО, КППКО
    if(not GetINNKPP_CreditOrg(FillBankID, @ИННКО, @КППКО, @ErrMsg))
      return FALSE;
    end;

    if(not Is365P)
      // ОГРНКО
      Error = 0;
      ОГРНКО = ПолучитьКодСубъекта( FillBankID, PTCK_OGRN, Error);
      if(not ОГРНКО)
        ErrMsg = "Не задан ОГРН банка \"" + PartyShortName + "\"";
        return FALSE;
      elif(not IsGoodOGRN_Org(ОГРНКО))
        ErrMsg = "Неверный ОГРН банка: " + ОГРНКО;
        return FALSE;
      end;

      // АдрМНКО
      if( not GetDataAddrRF(FillBankID, Addr, @ErrMsg) )
        return FALSE;
      end;
    end;

    return TRUE;
  end;
end;

macro GetGuidFnsX : string

    macro SeparateParts(Str : string, PartsLengths : TArray) : string
      var Result : string = "";

      for(var PartLen : integer, PartsLengths)
        if(Result)
          Result = Result + "-";
        end;

        Result = Result + substr(Str, 1, PartLen);
        Str = substr(Str, PartLen + 1);
      end;

      return Result;
    end;

//begin
  var FnsGuid : string = "";

  var OracleGuid : string = execStoredFunc( "sys_guid", V_STRING );
  FnsGuid = SeparateParts(OracleGuid, makeArray(8, 4, 4, 4, 12));

  return FnsGuid;
end;

macro GetReqClientFld(req, FieldName : string, IsOld : bool) : variant
  if(IsOld)
    FieldName = FieldName + "Old";
  end;

  var FieldIndex : integer = FldIndex(req, FieldName);

  return req(FieldIndex);
end;

private macro GetReg_ДАТА_СВЕДЕНИЙ_ПО_ЗАПРОСУ : integer
  return Bnk_GetRegistryValue
    ( "PS\\REQOPENACC\\OPERATION\\365-П\\ДАТА_СВЕДЕНИЙ_ПО_ЗАПРОСУ",
      V_INTEGER,
      0
    );
end;

private macro GetReqDateFld(ReqID : integer, ReqDateFld : @string) : bool
  var query : string =
    "select mesval.t_Value "
    "  from dwlmeslnk_dbt lnk, dwlmesval_dbt mesval, dwltpfld_dbt tpfld "
    " where lnk.t_ObjKind = " + OBJTYPE_REQ +
    "   and lnk.t_ObjID = :ReqID "
    "   and lnk.t_Direct = 'X' "
    "   and mesval.t_MesID = lnk.t_MesID "
    "   and mesval.t_TpFieldID = tpfld.t_TpFieldID "
    "   and tpfld.t_Name = 'ДатаЗапр' ";

  var rs = execSQLselectPrm( query, SQLParam("ReqID", ReqID) );

  if(rs and rs.moveNext())
    ReqDateFld = rs.value("t_Value");
    return true;
  end;

  return false;
end;

macro GetDateFnsInfoRequest(wlregdec, ПолеДатаЗапр : string) : date

  if(GetReg_ДАТА_СВЕДЕНИЙ_ПО_ЗАПРОСУ() == 1)

    if( (wlregdec != null) and (wlregdec.LnkDocType == OBJTYPE_REQ) and (wlregdec.LnkDocID > 0) )
      if( GetReqDateFld(wlregdec.LnkDocID, @ПолеДатаЗапр) )
        return ToDate_DDpMMpYYYY(ПолеДатаЗапр);
      end;

    elif(ПолеДатаЗапр)
      return ToDate_DDpMMpYYYY(ПолеДатаЗапр);

    end;

  end;

  return {curdate};
end;
