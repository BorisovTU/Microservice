 /*
 $Name: swm103r6.mac
 $Module: Межбанковские расчеты
 $Description: Генерация сообщения по SWIFT MT103RUR6
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT103RUR6                                   */
/*                                                                          */
/*  Имя файла: swm103r6.mac                                                 */
/*  Создан:  10.07.03                                        Панов А.Б.     */
/****************************************************************************/

import "swgenmes.mac";

const  Relise = "103RUR6";
private const  Options_For_50a = "F K";

macro GenMes( addrMes, addrPm, AddrOldMes )
  var field_value, BNC_Name, field72, INN, Tag, 
      ComAmount, ComFIID, IsTransit, BaseAmount, 
      CurrentCode, error, continue0, ОстатокПоля70, IsTaxPm = false;
  var Recreate = false;
  var RsPaym;
  Record oldwlMes (wlmes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( Oldwlmes,  addrOldMes );
     Recreate = true;
  end;

  PrintLog(2,"Генерация сообщения по МТ103RUR6");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 23B       */
  ЗаписатьПолеЛог( BankOperationCodeField_B, BANKOPERCODE_CRED ) ;


  /* 23Е   берем теперь из dwlpmrmprop_dbt.InstructionCode  */
  StrChangeRusXSet( RsPaym.InstructionCode, field_value );
  ЗаписатьПолеЛогВБлок( "23E", InstructionCodeField, string(field_value), NULL, Recreate, OldwlMes);

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  
  /* 26T - информация о налоговых платежах */
  field_value = FillTransactionTypeCodeField(RsPaym, ST_RUR6);
  if (field_value != "")
    IsTaxPm = true;
    WriteFieldEx  (TransactionTypeCodeField, field_value, Recreate, OldwlMes );
/*    ЗаписатьПолеЛог(TransactionTypeCodeField, field_value); */
  else
    IsTaxPm = false;
  end;

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate  ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );
/*  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );*/

  /* 50A - приказодатель */
  Tag = Options_For_50a;
  field_value = FillOrderingCustomerField103( RsPaym, Tag, ST_RUR6 );
  if ( field_value=="" )
      std.out( 2, "PaymentID: " + RsPaym.PaymentID );
      RunError( "|Не найден плательщик" );
  else
    ЗаписатьПолеЛогВБлок( "50a", OrderingCustomerField + Tag, field_value,NULL,Recreate, OldwlMes );
  end;

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 52a - банк приказодателя */
  field_value = FillOrderingInstitutionField(RsPaym, Tag, ST_RUR6, "103RUR6" );
  ЗаписатьПолеВБлокКонтекст0( OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes );

  /* 53a - корреспондент отправителя */
  Tag = SWIFT_Tag_B;
  field_value = FillSenderCorrespondentField(RsPaym, Tag, ST_RUR6 );
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value, Recreate, OldwlMes );

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField(RsPaym, Tag, ST_RUR6 );
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value, Recreate, OldwlMes );  

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField(RsPaym, Tag, ST_RUR6 );
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );      

  /* 59 - бенефициар */
  field_value = FillBeneficiaryCustomerField( RsPaym, ST_RUR6 );
  if ( field_value=="" )
    std.out( 2, "PaymentID: " + RsPaym.PaymentID );
    RunError( "|Не найден получатель" );
  end;
  ЗаписатьПолеЛогВБлок( "59a", BeneficiaryCustomerField, field_value, NULL, Recreate, OldwlMes );

  /* Восстанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;

  /* 70  - информация для бенефициара */
  field_value = GetField70_SW103RUR6( RsPaym, @ОстатокПоля70 );
  WriteFieldEx  (DetailsOfPaymentField, field_value, Recreate, OldwlMes );

  /* 71A - расходы */
    field_value = GetCommisCode (RsPaym.ComissCharges);
    WriteFieldEx  (DetailsOfChargesField_A, field_value, Recreate, OldwlMes );

  ЗаписатьКомиссииПлатежа( RsPaym, field_value, TRUE );

  /* 72 - информация для банка-получателя */
  if (not IsAllowCode(ALLOWCODES_MT103_RUR6, Field72CodesNZP))
    ОстатокПоля70 = "";
  end;
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_RUR6, ОстатокПоля70, NULL, IsTaxPm );
  if( field_value != "" )
    WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );
  end;

  /* 77B - обязательная отчетность */
  if (IsTaxPm == true)
    field_value = FillRegulatoryReportingField(RsPaym, ST_RUR6);
    if (field_value != "")
      WriteFieldEx  (RegulatoryReportingField, field_value, Recreate, OldwlMes );
    end;
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

