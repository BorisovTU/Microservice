/*
  $Name:         wlcrdpls.mac
  $Module:       МБР
  $Description:  Формирование извещения о помещении в картотеку корсчета
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Формирование извещения о помещении в картотеку корсчета                 */
/*  Имя файла: wlcrdpls.mac                                                 */
/*  Создан:  27.10.00                                         Бабин А.П.    */
/****************************************************************************/

import FIInter, "wldoc.mac", "adress.mac";

FILE acc ("account.dbt") ;
FILE part ("party.dbt") ;
RECORD finin ("fininstr.dbt") ;

macro FindAcc( FIID, Account )
  acc.Chapter       = 1 ; /* Баланс */
  acc.Account       = Account ;
  acc.Code_Currency = FIID ;
  return GetEQ( acc );
end ;

macro ПолучитьСубъектаПо_счету( FIID )
    return ПолучитьСубъекта( acc.Client ) ;
end ;

macro PrintHead( FIID, Account, DateReport, DatePaym, NumberReport )
  var NameClient, AddressClient ;
  record RecordAdress ( adress );

  if( ( FindAcc( FIID, Account ) == TRUE )
      AND ( ПолучитьСубъектаПо_счету( FIID ) == 0 ) )

    NameClient = acc.NameAccount ;

    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(part.PartyID,RecordAdress);
    AddressClient = RecordAdress.Adress ;

[
        #
        #
        (наименование, адрес организации)

        #########################
        (номер расчетного, текущего, бюджетного счета)

        #

                                ИЗВЕЩЕНИЕ N ######

        1. Предъявленные    Вами     ###################### платежные
    поручения (указать N, дату, сумму)

]( NameClient, AddressClient, Account, DateReport:f:m, NumberReport:l, DatePaym:f:m ) ;
  end ;
  return TRUE;
end; 

macro CurToStrNoNum( Sum, Целых, Сотых, FIID )
  var str, sumstr,  Len, Валюта = "", Curr_Ref ;

  if( ПолучитьФинИн( FIID, finin ) == 0 )

    Curr_Ref = int(finin.ISO_Number) ;

    RubToStr( Sum, sumstr ) ;

    /* Получаем сумму без копеек */
    Sum = moneyl(int(doublel(Sum))) ;

    str = CurToStrAlt( Sum, Целых, Сотых, Curr_Ref ) ;

    str = substr( str, strlen( sumstr ) + 2 ) ;

    Len = Index( str, " 00" ) - 1 ;

    Валюта = SubStr( str, 1, Len ) ;

  end ;

  return Валюта ;
end ;

macro PrintNode( addrPmPaym, addrDebet, addrCredit, addrRM, Count )
  var Number, Date;

  SetBuff( wlpmpaym, addrPmPaym );
  SetBuff( wlpmpropdeb, addrDebet );
  SetBuff( wlpmpropcred, addrCredit );
  SetBuff( wlpmrmprop, addrRM );
   
   Number = wlpmrmprop.Number;
   Date = wlpmrmprop.Date;

   [    ##. N ############ от ########## на сумму ############### #]
   ( Count, Number, Date:f, wlpmpaym.PayAmount:14:2, CurToStrNoNum( wlpmpaym.PayAmount, 18, 2, wlpmpaym.PayFIID ) ) ;

   return TRUE;
end;

macro PrintFloor( )
[
   не оплачены из-за отсутствия средств  на  корреспондентском  счете
   банка и помещены в картотеку.
       2. В картотеку к  корреспондентскому  счету  из-за  отсутствия
   средств  на  нем  помещены инкассовые поручения и другие расчетные
   документы,   предъявленные   к   Вашему   (расчетному,   текущему,
   бюджетному) счету (указать N, дату, сумму)
   __________________________________________________________________
   __________________________________________________________________
   __________________________________________________________________


                                    Подпись операционного работника
      Штамп
                                    #



---------------------------------------------------------------------
]( {Name_Oper} ) ;

   return TRUE;
end;
