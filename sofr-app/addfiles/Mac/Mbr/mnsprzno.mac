 /*
 $Name: mnsprzno.mac
 $Module: МБР
 $Description: Печать сообщения по запросу ФНС о наличии счетов, остатках на счетах, предоставлении выписок
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mnsprzns.mac
  Created     : 27.01.2011
  Programmer  : Popova O.
  Description : Печать сообщения по запросу ФНС о наличии счетов, остатках на счетах, предоставлении выписок

└───────────────────────────────────────────────────────────────────────────*/

import "wldoc.mac", "fnsprzall.mac";

macro PrintDoc( addrMes, MassCopy )

  SetBuff( wlmes, addrMes );

  var field_name, field_value;
  var MsgTmpFileName, OldName;
  
  FILE MsgTmpFile() txt;
  var stat:bool = true;

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  if( wlmes.Kind != MESKIND_MNS )
    stat = false;
  end;

  if( stat == true )
    var ReqID = 0;
    var rs:RsdRecordset = execSQLselect( "select req.t_reqid, req.t_subkind, nvl( rgd.t_DecisionID, 0 ) "+
                                         "  from dwlreq_dbt req, dwlmeslnk_dbt lnk, dwlregdec_dbt rgd " +
                                         " where lnk.t_MesID         = " + wlmes.MesID   +  
                                         "   and lnk.t_ObjKind       = " + OBJTYPE_REQ   + 
                                         "   and req.t_reqid         = lnk.t_objid "     +
                                         "   and rgd.t_lnkdocid(+)   = req.t_reqid "     +
                                         "   and rgd.t_lnkdoctype(+) = " + OBJTYPE_REQ   );
            
    if( rs.moveNext() )            
      if( rs.value(2) > 0 )
        ReqID = rs.value(0);
        var prn: FNSPrint = FNSPrint(rs.value(2));
        if  ( rs.value(1) == 1 ) prn.ВидСправки = "NS";
        elif( rs.value(1) == 2 ) prn.ВидСправки = "OS";
        else              /* 3 */prn.ВидСправки = "VS";
        end;
      
        var rgd : Tbfile = Tbfile("wlregdec.dbt", "r", 0);
        rgd.rec.DecisionID = rs.value(2);
        if( getEQ(rgd) )
          prn.LnkTypeInfo         = rgd.rec.LnkTypeInfo;
          prn.НаимНО              = rgd.rec.RecipientName;
          prn.АдрНО               = GetNOAdress( rgd.rec.RecipientID );
          prn.RepBankID           = rgd.rec.OriginatorID;
          prn.НаимКО              = rgd.rec.OriginatorName;
          prn.ДатаЗапроса         = rgd.rec.LnkDocDate;
          prn.НомерЗапроса        = rgd.rec.LnkDocNumber;
          prn.НаимНП              = rgd.rec.ClientName;
          prn.ИНННП               = rgd.rec.ClientINN;
          prn.КППНП               = rgd.rec.ClientKPP;
          prn.Счет                = GetLnkAccounts(rgd.rec.DecisionID);
          prn.ClientID            = rgd.rec.ClientID;

          if( (prn.ВидСправки == "NS") or (prn.ВидСправки == "OS") )
            prn.ПоСостояниюНаДату   = rgd.rec.Date;
          elif( prn.ВидСправки == "VS" )
            prn.НачалоПериода       = rgd.rec.Date;
            prn.КонецПериода        = rgd.rec.EndDate;
          end;       
          stat = prn.PrintDoc() == 0;
        end;
      else
        msgbox( "Не найдена " + IfThenElse( rs.value(1) == 3, "выписка", "справка") + ", сформированная по запросу.|Возможно, запрос не обработан." );
      end;
    else 
      stat = false;
    end;
  end;

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  if( stat == true )
    wlmes.IsPrinted = "X";
    if( not UpdateMes(wlmes) )
      msgbox("Ошибка при обновлении сообщения");
      stat = false;
    end;
  end;
  if( not GetDialogFlag() and ( GetMNSWarning() != "" ) )// выводим предупреждения в протокол
    msgbox( GetMNSWarning() );
  end;
  return stat;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage(er);
  return false;
end;
