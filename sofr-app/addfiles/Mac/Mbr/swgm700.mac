// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6.0
//                 Copyright (c) R-Style Software Lab
//
//  Описание    : Генерация сообщения по SWIFT MT700
//
//  Программист : Чукина Т.А.
//
//  Создан      : 23.07.2013
//
// --------------------------------------------------------------------

import "swgenmes.mac", LCInter;

private macro FillChargesField_LC(LcObj : RsbLetterOfCredit)
  var field_value : string = "";

  var Lccost : TRecHandler = TRecHandler("lccost");
  var IsFound : bool = ( LcObj.Costs.First(Lccost) == 0 );
  while(IsFound)
    if(Lccost.rec.Kind == LCCOST_KIND_OTHER)
      if(field_value) 
        field_value = field_value + " "; 
      end;

      field_value = field_value + Lccost.rec.Info;
    else
      field_value = field_value + "/" + 
                    GetllValueElement(OBJTYPE_LC_VZ, Lccost.rec.Kind) + "/" +
                    ПолучитьКодВалютыЛог(Lccost.rec.FIID) + 
                    GetSWIFTAmount(Lccost.rec.Amount) + 
                    Lccost.rec.Info;

    end;

    IsFound = ( LcObj.Costs.Next(Lccost) == 0 );
  end;

  return field_value;
end;

macro GenMes( addrMes, addrLcdoc, addrOldMes )

  var Recreate = false;
  Record oldMes(wlmes);
  Record wllcdoc(lcdoc);

  SetBuff( wlmes,   addrMes );
  SetBuff( wllcdoc, addrLcdoc );
  
  if( ValType(AddrOldMes) == V_MEMADDR )
    SetBuff( oldmes, addrOldMes );
    Recreate = true;
  end;

  PrintLog(2, "Генерация сообщения по МТ700");
  var LcObj : RsbLetterOfCredit = RsbLetterOfCredit( wllcdoc.LcdocID ),
      Lcparty : TRecHandler = TRecHandler("lcparty"),
      field_value : string = "", Tag = "";

  // 27 - номер по порядку
  WriteFieldEx( SequenceOfTotalField, "1/"+LcObj.CountMes, Recreate, oldMes );

  // 40A - форма аккредитива
  GetElementAndNoteLLVALUES(OBJTYPE_LC_FORM, "\""+LcObj.Form+"\"", null, field_value);
  ЗаписатьПолеЛог( FormOfDocumentaryCreditField, field_value );

  // 20 - номер транзакции (документа)
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wllcdoc.Number );

  // 31C - дата выпуска
  ЗаписатьПолеЛог( DateOfIssueField, GetSWIFTDate(wllcdoc.OpenDate) );

  // 40E - правила
  field_value = wllcdoc.Rules;
  if(wllcdoc.RulesDescr)
    field_value = field_value + "/" + wllcdoc.RulesDescr;
  end;
  ЗаписатьПолеЛог( ApplicableRulesField_E, field_value );

  // 31D - дата и место предоставления документов
  ЗаписатьПолеЛог( DateAndPlaceOfExpiryField, GetSWIFTDate(wllcdoc.ValidDate)+wllcdoc.DocsPlace );

  // 51a
  if( (LcObj.Parties().Find(LCPARTY_KIND_APPLICANT_BANK, Lcparty) == 0) and
      (Lcparty.rec.PartyID != {OurBank})
    )
    Tag = "";
    field_value = FillFinOrgIdentField( "51", Tag, Lcparty.rec.Account, 
                                        Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                        Lcparty.rec.PartyID, Lcparty.rec.Name );
    if(field_value)
      ЗаписатьПолеЛогВБлок( "51a", SendingInstitutionField + Tag, field_value );

      // Восстанавливаем нулевой контекст блока
      if( not УстановитьКонтекстБлока() )
        RunError("|Ошибка при установке нулевого контекста блока");
      end;
    end;
  end;

  // 50 - приказодатель
  field_value = FillOrderingCustomerField_LC(LcObj);
  if(field_value)
    ЗаписатьПолеЛог( OrderingCustomerField, field_value );
  else
    RunError( "|Не найден приказодатель" );
  end;

  // 59 - бенефициар
  field_value = "";
  if( LcObj.Parties().Find(LCPARTY_KIND_BENEFICIARY, Lcparty) == 0 )
    field_value = FillBeneficiaryCustomerField_0( Lcparty.rec.Name, Lcparty.rec.Account );
  end;
  if(field_value)
    ЗаписатьПолеЛог( BeneficiaryCustomerField, field_value );
  else
    RunError( "|Не найден получатель" );
  end;

  // 32B - код валюты, сумма
  field_value = ПолучитьКодВалютыЛог(LcObj.FIID) + GetSWIFTAmount(LcObj.Amount);
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_B, field_value );

  // 39A - допустимый процент отклонения
  if(LcObj.MisCalc == LCMISCALC_KIND_DEVIATION)
    field_value = string(LcObj.ErrPlus, "/", LcObj.ErrMinus);
    ЗаписатьПолеЛог( PercentageCreditAmountToleranceField, field_value );
  end;

  // 39B - максимальная сумма
  if(LcObj.MisCalc == LCMISCALC_KIND_LIMITS)
    ЗаписатьПолеЛог( MaximumCreditAmountField, "NOT EXCEEDING" );
  end;

  // 39C - покрытие дополнительные суммы
  if(LcObj.AddAmountCond)
    field_value = SwiftStrSplit( LcObj.AddAmountCond, 35, 4 );
    ЗаписатьПолеЛог( AdditionalAmountsCoveredField, field_value );
  end;

  // 41a - Реквизиты подтверждающего банка
  Tag = "";
  field_value = FillAvailableWithByField_LC(LcObj, Tag);
  if(field_value)
    ЗаписатьПолеЛогВБлок( "41a", AvailableWithByField + Tag, field_value );

    // Восстанавливаем нулевой контекст блока
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;
  else
    RunError("Не найден подтверждающий банк");
  end;

  // 42C
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_ACCEPTANCE)
    field_value = SwiftStrSplit( LcObj.DirectBill, 35, 3 );
    ЗаписатьПолеЛог( DraftsAtField, field_value );
  end;

  // 42a - банк-трассат
  Tag = "AD";
  field_value = FillDraweeField_LC(LcObj, Tag);
  if(field_value)
    ЗаписатьПолеЛогВБлок( "42a", DraweeField + Tag, field_value );

    // Восстанавливаем нулевой контекст блока
    if( not УстановитьКонтекстБлока() )
      RunError("|Ошибка при установке нулевого контекста блока");
    end;
  end;

  // 42M
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_MIXED_PYMT)
    field_value = SwiftStrSplit( LcObj.MixPayDescr, 35, 4 );
    ЗаписатьПолеЛог( MixedPaymentDetailsField, field_value );
  end;

  // 42P
  if(LcObj.PayCond == LCDOC_PAYCOND_KIND_BY_DEF_PAYMENT)
    field_value = SwiftStrSplit( LcObj.DeferPayDescr, 35, 4 );
    ЗаписатьПолеЛог( DeferredPaymentDetailsField, field_value );
  end;

  // 43P - частичные отгрузки
  if(LcObj.PartShipping)
    ЗаписатьПолеЛог( PartialShipmentsField, LcObj.PartShipping );
  end;

  // 43T - перегрузки
  if(LcObj.OverShipping)
    ЗаписатьПолеЛог( TransshipmentField, LcObj.OverShipping );
  end;

  // 44A - место отгрузки/передачи
  if(LcObj.ShippingPlace)
    ЗаписатьПолеЛог( PlaceOfTakingInChargeField, LcObj.ShippingPlace );
  end;

  // 44E - порт отправления
  if(LcObj.DeparturePort)
    ЗаписатьПолеЛог( PortOfDepartureField, LcObj.DeparturePort );
  end;

  // 44F - порт назначения
  if(LcObj.DestinationPort)
    ЗаписатьПолеЛог( PortOfDestinationField, LcObj.DestinationPort );
  end;

  // 44B - конечный пункт
  if(LcObj.EndPlace)
    ЗаписатьПолеЛог( PlaceOfFinalDestinationField, LcObj.EndPlace );
  end;

  // 44C - Дата окончания периода отгрузки
  if(LcObj.ShippingDateCond == "X")
    ЗаписатьПолеЛог( LatestDateOfShipmentField, GetSWIFTDate(LcObj.ShippingDate) );
  end;

  // 44D - Период отгрузки
  if(LcObj.ShippingDateCond != "X")
    field_value = SwiftStrSplit( LcObj.ShippingPeriod, 65, 6 );
    ЗаписатьПолеЛог( ShipmentPeriodField, field_value );
  end;

  // 45A - Описание товаров/услуг
  if(LcObj.Flag45 == "X")
    SWIFTWriteFieldExNM( "45a", "45A", LcObj.DescGoods, Recreate, oldMes, 100, 65 );
  end;

  // 46A - Требуемые документы
  if(LcObj.Flag46 == "X")
    SWIFTWriteFieldExNM( "46a", "46A", LcObj.DocReq, Recreate, oldMes, 100, 65 );
  end;

  // 47A - Дополнительные условия аккредитива
  if(LcObj.Flag47 == "X")
    SWIFTWriteFieldExNM( "47a", "47A", LcObj.AddCond, Recreate, oldMes, 100, 65 );
  end;

  // 71B - Комиссии
  field_value = FillChargesField_LC(LcObj);
  if(field_value)
    field_value = SwiftStrSplit( field_value, 35, 6 );
    ЗаписатьПолеЛог( DetailsOfChargesField_B, field_value );
  end;

  // 48 - период представления документов
  if(LcObj.DocsPeriod)
    field_value = SwiftStrSplit( LcObj.DocsPeriod, 35, 4 );
    ЗаписатьПолеЛог( PeriodForPresentationField, field_value );
  end;

  // 49 
  ЗаписатьПолеЛог( ConfirmationInstructionsField, "CONFIRM" );

  // 53a - корреспондент отправителя
  if( LcObj.Parties().Find(LCPARTY_KIND_REIMBURSING_BANK, Lcparty) == 0 )
    Tag = "";
    field_value = FillFinOrgIdentField( "53", Tag, Lcparty.rec.Account, 
                                        Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                        Lcparty.rec.PartyID, Lcparty.rec.Name );
    if(field_value)
      ЗаписатьПолеЛогВБлок( "53a", Sender_sCorrespondentField + Tag, field_value );

      // Восстанавливаем нулевой контекст блока
      if( not УстановитьКонтекстБлока() )
        RunError("|Ошибка при установке нулевого контекста блока");
      end;
    end;
  end;

  // 78
  if( LcObj.Parties().Find(LCPARTY_KIND_EXEC_BANK, Lcparty) == 0 )
    field_value = SwiftStrSplit(Lcparty.rec.PartyInfo, 65, 12);
    ЗаписатьПолеЛог( InstructionsToPayingAcceptingNegotiationBankField, field_value );
  end;

  // 57a - банк бенефициара
  if( LcObj.Parties().Find(LCPARTY_KIND_ADVISING_BANK, Lcparty) == 0 )
    Tag = "";
    field_value = FillFinOrgIdentField( "57", Tag, Lcparty.rec.Account, 
                                        Lcparty.rec.CodeKind, Lcparty.rec.Code, 
                                        Lcparty.rec.PartyID, Lcparty.rec.Name );
    if(field_value)
      ЗаписатьПолеЛогВБлок( "57a", AccountWithInstitutionField + Tag, field_value );

      // Восстанавливаем нулевой контекст блока
      if( not УстановитьКонтекстБлока() )
        RunError("|Ошибка при установке нулевого контекста блока");
      end;
    end;
  end;

  // 72 - информация для банка-получателя
  if( LcObj.Parties().Find(LCPARTY_KIND_CONFIRMING_BANK, Lcparty) == 0 )
    field_value = SwiftStrSplit(Lcparty.rec.PartyInfo, 35, 6);
    ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
