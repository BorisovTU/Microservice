/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT210                                       */
/*                                                                          */
/*  Имя файла: swgm210.mac                                                  */
/*  Создан:  14.08.00                                        Бабин А.П.     */
/****************************************************************************/

import "swgenmes.mac";

RECORD Corschem( corschem );

macro GenMes( addrMes, addrHead )
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT, oldKeyNum;
  var StatementLine, TpID, NameForm, stat, KindPageExt, CorrID;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlhead, addrHead );

  PrintLog(2,"Генерация сообщения по МТ210");

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );
  
  /* 25 - счет уведомления */
  /* Это поле заполняется только если на корреспондента настроено
     несколько корсхем                                             */
  /* Определяем корреспондента */

  if ( НайтиКорсхемуНеЕдинственнуюУКонтрагента(wlhead.Account, wlhead.FIID, Corschem) )
     ЗаписатьПолеЛог( AccountIdentificationField, wlhead.Account );
  end;

  /* 30 - Дата валютирования */
  ЗаписатьПолеЛог( ValueDateField, GetSWIFTDate(wlhead.DateIn) );
  
  /******************** Заполняем документы выписки ******************/
  УстановитьФильтрДокументовВыписки(wlhead.HeadID);
  while( ПолучитьДокументВыписки(wlConf) )

     if ( wlConf.RelatedRef )
        ЗаписатьПолеЛогВБлок( "A", RelatedReferenceField, wlConf.RelatedRef );
     else
        ЗаписатьПолеЛогВБлок( "A", RelatedReferenceField, СсылкаОтсутствует );
     end;

     ЗаписатьПолеЛогВБлок( "A", ValueDateCurrencyCodeAmountField_B, ПолучитьКодВалютыЛог( wlConf.FIID )+GetSWIFTAmount( wlConf.Sum ) );

     /* 52a - банк приказодателя */
     if( (wlconf.CodeValue!="") OR (wlconf.BankName!="") )
       ClearRecord(Route);
       Route.PartyID  = wlconf.BankID;
       Route.CodeKind = wlconf.CodeKind;
       Route.CodeName = wlconf.CodeName;
       Route.CodeValue = wlconf.CodeValue;
       Route.Name = wlconf.BankName;
       ЗаписатьПолеМаршрутаЛог( OrderingInstitutionField, "52a", Route );
       if( not УстановитьКонтекстБлока( ".." ) )
         RunError("|Ошибка при установке контекста блока ..");
       end;
     end;     
  
     /* 56a - банк приказодателя */
     if( (wlconf.CorrCodeValue!="") OR (wlconf.CorrBankName!="") )
       ClearRecord(Route);
       Route.PartyID  = wlconf.CorrBankID;
       Route.CodeKind = wlconf.CorrCodeKind;
       Route.CodeName = wlconf.CorrCodeName;
       Route.CodeValue = wlconf.CorrCodeValue;
       Route.Name = wlconf.CorrBankName;
       ЗаписатьПолеМаршрутаЛог( IntermediaryField, "56a", Route, "", "", ST_UNDEF, true );
     end; 
  end;
  /*******************************************************************/  

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;