/*
  $Name:        Fnsgdtrbx.mac
  $Module:      MBR
  $Description: Генерация учетного объекта по сообщению формы TRB
*/

/*
//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация учетного объекта по сообщению формы TRB
//
// Программист  : Тимонин Д.В.
//
// Создан       : 08.08.2014
//
//-----------------------------------------------------------------------------
*/

import MesInter, "wlmnstls.mac", "xmlmestools.mac", "fnsreadmall.mac";

private class (MessageReader)TMesData
  var OriginatorCode : string = "", КодНО     : string = "",
      OriginatorName : string = "", НаимНО    : string = "",
      RecipientName  : string = "", НаимБанк  : string = "",
      Number         : string = "", НомТреб   : string = "",
      Date           : string = "", ДатаТреб  : string = "",
      Amount         : string = "", СуммаПлат : string = "",

      NameUL         : string = "", НаимЮЛ    : string = "",
      Name           : string = "", Фамилия   : string = "",  
                                    Имя       : string = "",  
                                    Отчество  : string = "",

      ClientINN      : string = "", ИННЮЛ     : string = "",
                                    ИННФЛ     : string = "",
                                    ИННИП     : string = "",

      ClientKPP      : string = "", КПП       : string = "",
      Ground         : string = "", СведВзыск : string = "",
      LnkDocNumber   : string = "", НомПоруч  : string = "",
      LnkDocDate     : string = "", ДатаПоруч : string = "",
      LnkMesTrn      : string = "", НомСооб   : string = "",
      LnkMesDate     : string = "", ДатаСооб  : string = "",
      ExecuteDate    : string = "", СрокИсп   : string = "",
      BTTTICode      : string = "", КБК       : string = "",
      OKATOCode      : string = "", ОКТМО     : string = "",

      BIC            : string = "", БИК       : string = "",
      NumDprt        : string = "", НомФил    : string = "";

  macro _ConvertBlock_(blockname)
    if( blockname == "ПлЮЛ" )
      ClientINN = ИННЮЛ;
    elif( blockname == "ПлИП" )
      ClientINN = ИННИП;
      Name = Фамилия + IfThenElse( Имя, " " + Имя + IfThenElse( Отчество, " " + Отчество, "" ), "" );
    elif( blockname == "ПлФЛ" )
      ClientINN = ИННФЛ;
      Name = Фамилия + IfThenElse( Имя, " " + Имя + IfThenElse( Отчество, " " + Отчество, "" ), "" );
    elif( (blockname == "Руководитель") AND (Name == "") )
      Name = Фамилия + IfThenElse( Имя, " " + Имя + IfThenElse( Отчество, " " + Отчество, "" ), "" );
    end;
  end;

  macro _Normalize_()
    OriginatorCode = КодНО;
    OriginatorName = НаимНО;
    RecipientName  = НаимБанк;
    Number         = НомТреб;
    NumDprt        = НомФил;
    Date           = ДатаТреб;
    Amount         = СуммаПлат;
    NameUL         = НаимЮЛ;
    BIC            = БИК;
    ClientKPP      = КПП;
    Ground         = СведВзыск;
    LnkDocNumber   = НомПоруч;
    LnkDocDate     = ДатаПоруч;
    LnkMesTrn      = НомСооб;
    LnkMesDate     = ДатаСооб;
    ExecuteDate    = СрокИсп;
    BTTTICode      = КБК;
    OKATOCode      = ОКТМО;
  end;
end;

private macro GetNameLLVALUES( llvList, llvElem )
  FILE llval(llvalues) key 0;
  llval.List    = llvList;
  llval.Element = llvElem;
  if( getEQ(llval) )
    return llval.Name;
  end;
  return "";
END;

private macro GetBankDate( SessID : integer ) : date
  FILE f_wlsess(wlsess) key 0;
  f_wlsess.SessionID = SessID;
  GetEQ(f_wlsess);
  return f_wlsess.SysDate;
end;

private macro GetNameAndCodeDprt( Department : integer, Name : @string, Code : @string ) : bool
  FILE dep(dp_dep) key 0;
  FILE prt(party) key 0;
  dep.Code = Department;
  if( GetEQ(dep) )
    prt.PartyID = dep.PartyID;
    if( GetEQ(prt) )
      Name = prt.Name;
      Code = ПолучитьКодСубъекта( prt.PartyID, PTCK_BIC );
      return TRUE;
    end;
  end;
  return FALSE;
end;

private macro StrToDate( MesDate:string ):date

  if( StrLen( MesDate ) == 10 ) 
    return date( int( SubStr(MesDate,9,2) ), int( SubStr(MesDate,6,2) ), int( SubStr(MesDate,1,4) ) );
  end;

  return date( 0,0,0 );
end;

macro GenDoc( addrMes )
  SetBuff(wlmes, addrMes);
  var wlreqdec : TRecHandler,
      meslnk   : TRecHandler;

  PrintLog(2, "Генерация учетного объекта по сообщению TRB");

  var Mes : TMesData = TMesData();
  Mes.ReadMessage();

  if( wlmes.InsideAbonentID <= 0 )
    var ErrList = RsbWlError(0);
    var ErrCode = FNS_RP_CANNOT_EXECUTED_BANK;
    var ErrDescription : string = ""; 
    var FullINN = "", Name = "", Code = "";

    if( GetNameAndCodeDprt( wlmes.Department, @Name, @Code ) == false )
      RunError( "Не найден субъект филиала " + wlmes.Department );
    end;

    var wlerr_buf = TRecHandler("wlerror.dbt");
    wlerr_buf.rec.Code         = "41";
    wlerr_buf.rec.Description  = "Электронный документ требование о перечислении налога № " +
                                 Mes.Number + " от " + Mes.Date +
                                 " ошибочно направлен налоговым органом не в тот банк (филиал банка). В банке " +
                                 Code + " " + Name +
                                " нет филиала с БИК " + Mes.BIC + " и номером " + Mes.NumDprt;

    ErrList.Insert( wlerr_buf );

    ErrDescription = Bnk_GetLLValuesName( OBJTYPE_WLRESCODE_MNS, ErrCode );
    if(not ErrDescription)
      RunError("Не найден код ошибки " + ErrCode);
    end;

    var ResultID : integer = 0, error = 0;
    if( not ВставитьПодтверждениеБанкаФНС( wlmes, ErrCode, ErrDescription, ErrList, "", -1, 0, "", "", -1, 0, "", "", NULL, NULL, ResultID ) )
      std.msg( "Ошибка при вставке подтверждения банка" );
      return FALSE;
    end;

    // Связь подтверждения с сообщением
    meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = ResultID;
    meslnk.rec.ObjKind = OBJTYPE_MESDEFECT;
    var MesLnkObj : RsbWlMesLnk = RsbWlMesLnk( meslnk.rec.ObjKind, 
                                               meslnk.rec.ObjID, 
                                               meslnk.rec.Direct );
    if( (MesLnkObj.Insert( meslnk ) != 0) or
        (MesLnkObj.Save() != 0)
      )
      std.msg( "Ошибка при создании связи подтверждения с сообщением" );
      return FALSE;
    end;

  else
    ClearRecord(wlregdec);

    wlregdec.Type               = WLD_TYPE_REGDEC_TRB;
    wlregdec.OriginatorCodeKind = PTCK_MNS;
    wlregdec.OriginatorCode     = Mes.OriginatorCode;
    wlregdec.OriginatorName     = Mes.OriginatorName;
    wlregdec.OriginatorID       = ПолучитьКодСубъекта( wlregdec.OriginatorCode, wlregdec.OriginatorCodeKind );
    wlregdec.RecipientCodeKind  = wlmes.InsideAbonentCodeKind;
    wlregdec.RecipientCode      = wlmes.InsideAbonentcode;
    wlregdec.RecipientName      = Mes.RecipientName;
    wlregdec.RecipientID        = wlmes.InsideAbonentID;
    wlregdec.Number             = Mes.Number;
    wlregdec.Date               = StrToDate(Mes.Date);
    wlregdec.DeliveryDate       = GetBankDate( wlmes.SessionID );
    wlregdec.Amount             = Mes.Amount;

    if( Mes.NameUL )
      wlregdec.ClientName = Mes.NameUL;
    else
      wlregdec.ClientName = Mes.Name;
    end;
    wlregdec.ClientINN = Mes.ClientINN;
    wlregdec.ClientKPP    = Mes.ClientKPP;

    FullINN = IfThenElse( Mes.ClientKPP == "", wlregdec.ClientINN, wlregdec.ClientINN + "/" + Mes.ClientKPP );

    wlregdec.ClientID = ПолучитьКодСубъекта( FullINN, PTCK_INN, error ); 
    if( error )
      wlregdec.ClientID = GetLegalPersonIDByINN( wlregdec.ClientINN );
    end;

    wlregdec.Ground       = Mes.Ground;
    wlregdec.LnkDocNumber = Mes.LnkDocNumber;
    wlregdec.LnkDocDate   = StrToDate(Mes.LnkDocDate);
    wlregdec.LnkMesTrn    = Mes.LnkMesTrn;
    wlregdec.LnkMesDate   = StrToDate(Mes.LnkMesDate);
    wlregdec.ExecuteDate  = StrToDate(Mes.ExecuteDate);
    wlregdec.BTTTICode    = Mes.BTTTICode;
    wlregdec.OKATOCode    = Mes.OKATOCode;

    var DecID : integer = 0;
    if( not ВставитьТребованиеФНС( wlregdec, DecID ) )
      std.msg("Ошибка при вставке решения регистрационного органа");
      return FALSE;
    end;
  end;
   
  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
