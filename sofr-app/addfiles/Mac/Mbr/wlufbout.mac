/*
$Name:           wlufbout.mac
$Module:         Межбанковские расчеты
$Description:    Экспорт сообщений УФЭБС
*/                                                                            

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*               Экспорт сообщений УФЭБС                                    */
/*                                                                          */
/*  Имя файла: wlufbout.mac                                                 */
/*  Создан:  05.10.04                                           BARS        */
/****************************************************************************/

import "wlexport.mac", "ufgenmes.mac", "wlufsign.mac", oralib, likepy, "uf501.mac",
       "bnk_ptlib.mac", "arm_kbr_tools.mac";

const CONTEXTID_SET_SIGN_UFBS      = "ТранспортМБР|9|Экспорт_файла";
const CONTEXTID_SET_ZK_UFBS        = "ТранспортМБР|9|ЗК_для_ЭД";
const CONTEXTID_SET_ZK_EPD_UFBS    = "ТранспортМБР|9|ЗК_для_ПЭД";
const CONTEXTID_SET_ZK_ED_EPD_UFBS = "ТранспортМБР|9|ЗК_для_ЭД_ПЭД";

const ОформлятьСлужебныйПакет = false;

var ver_st = 1;
var HasED501 : bool = false;
var ESSize501 = 0;

private macro set_xmlns(node : object)

  var xmlnsValue : string = "";
  if(ver_st == 1)
    xmlnsValue = "urn:cbr-ru:ed:v1.1";
  else
    xmlnsValue = "urn:cbr-ru:ed:v2.0";
  end;

  var saveNode : object = null;
  var i : integer = 0;
  if(node.attributes.length > 0)
    for(i, 0, node.attributes.length - 1)
      if (node.attributes(i).name == "xmlns")
        node.removeAttributeNode(node.attributes(i));
        break;
      end;
    end;
  end;
  node.setAttribute("xmlns", xmlnsValue);
end;

private macro CreateElementEDxxx(node : @object, IsPack)
  var mes:object = ActiveX("Microsoft.XMLDOM");    
  var str1 = GetUFBSMesFormName(wlmes.RlsFormID);
  if (str1)
    node = mes.createElement(str1);

    if (not IsPack)
      set_xmlns(node);
    end;

    return true;
  else
    ErrExport("Не удалось определить форму сообщения ");
    return false;
  end;
end;

/* Записать поля сообщения в файл */
macro ЗаписатьСообщение( СуммаДокумента, obj:@object, IsPack, НуженЗКдляЭД, НуженЗКдляЭДПЭД, SessUID : string, CurMesTrn : string )
  var field, buff, block, err, str1;
  var mes:object = ActiveX("Microsoft.XMLDOM");    
  var node, child;

  if( IsPack and (valtype(SessUID) != V_UNDEF) and (valtype(CurMesTrn) != V_UNDEF) )
    if( SubStr( SessUID, 1, 6 ) != SubStr( CurMesTrn, 1, 6 ) ) 
      ErrExport("Реквизит ЭС <Дата составления ЭС> (EDDate) сообщения с референсом " + CurMesTrn + " не совпадает с реквизитом <Дата составления пакета ЭС>. Формирование пакета невозможно");
      return false;
    end;
  end;

  СуммаДокумента = 0;

  while( СчитатьПоле( field, buff, block ) )
    if ( field=="xml" )
       //if(IsPack)
         // Теперь namespace удаляем всегда, а если в родительском узле он не задан 
         // (IsPack == false), то потом добавим.
         // Иначе возникает проблема с добавлением дополнительных узлов: 
         // в них вставляется xmlns=""
         str1 = SubStr(buff,index(buff,"xmlns=\"") + strlen("xmlns=\""));
         buff = SubStr(buff,1,index(buff,"xmlns")-1) + SubStr(str1,index(str1,"\"") + 1);
       //end;

       if ( not mes.LoadXML(buff) )
          ErrExport("Ошибка чтения строки xml: " + string(buff));
          return false;
       end;         

       if ( mes.selectSingleNode("//@Sum") )
           СуммаДокумента = СуммаДокумента + double( mes.selectSingleNode("//@Sum").nodeValue );
       end;

       node = mes.firstChild;
    elif(field == "CreditTransferTrans")
       if ( not mes.LoadXML(buff) )
          ErrExport("Ошибка чтения строки xml: " + string(buff));
          return false;
       end;

       if(node)
         node.appendChild(mes.firstChild);
       else // такого не должно быть, но на всякий случай
         node = mes.firstChild;
       end;
    else // в релизе много полей

      // корень
      if( not CreateElementEDxxx(@node, IsPack) ) 
        return false;
      end;

      // первое поле
      node.SetAttribute(field, buff);

      // остальные поля
      var AllFldsAreRead : bool = false;
      if( not ExportMesToXmlNode(@node, wlmes, @AllFldsAreRead) )
        return false;
      end;
      if(AllFldsAreRead)
        
        if ( node.selectSingleNode("//@Sum") )
          СуммаДокумента = СуммаДокумента + double( node.selectSingleNode("//@Sum").nodeValue );
        end;
                
        break;
      end;
      
    end;
  end;

  if (node)
    if(not IsPack)
      set_xmlns(node);
    end;

    if( GetNodeName(node.NodeName) == "ED501" )
      HasED501 = true;
      PrepareForExportED501(node, wlmes, @ESSize501);
    end;

    obj.appendChild(node);
  end;  
        
  SetParm( 0, СуммаДокумента );

  if ( IsPack )
     if ( НуженЗКдляЭДПЭД)
        if(not ПодписаниеЗК( CONTEXTID_SET_ZK_ED_EPD_UFBS, obj ))
           ErrExport("Ошибка подписи сообщения ЗК");
           return false;
        end;
     end;
  else
     if ( НуженЗКдляЭД)
        if(not ПодписаниеЗК( CONTEXTID_SET_ZK_UFBS, obj ))
           ErrExport("Ошибка подписи сообщения ЗК");
           return false;
        end;
     end;
  end;
  return true;
end;

macro GetCurrentDateTime()
  var q = "select cast( (systimestamp at time zone 'UTC') as date ) as SysdateInUTC "
          "  from dual ";

  var rs : RsdRecordset = execSQLselect(q);
  if(rs and rs.moveNext())
    var dt : date = date( rs.value("SysdateInUTC") );
    var tm : time = time( rs.value("SysdateInUTC") );
    return string( YYYYMMDD(dt), "T", StrSubst(string(tm)," ","0"), "Z" );
  else
    RunError("Ошибка при определении даты и времени создания сообщения");
  end;
end;

private macro GetSenderAddress(SenderDepartment : integer) : string
  var PartyID : integer = GetDprtPartyID(SenderDepartment);
  var UIS : string = GetEDAuthor(PartyID);

  return "uic:" + UIS + "00";
end;

macro ОформитьСлужебныйПакет( xmlIni:object, SenderDepartment : integer )
   var envelope:object, Header:object, MesInfo:object;
   var item:object, body:object, child:object;
   var xml:object = ActiveX("Microsoft.XMLDOM");
   var i;

   xml.appendChild( xml.createProcessingInstruction("xml",
                                  " version='1.0' encoding='UTF-8'") );  

   envelope = xml.createElement("Envelope");
   envelope.setAttribute("xmlns", "http://www.w3.org/2003/05/soap-envelope");
   Header = xml.createElement("Header");
   MesInfo = xml.createElement("MessageInfo");
   
   if(ver_st == 1)
      MesInfo.setAttribute("xmlns", "urn:cbr-ru:msg:props:v1.1");
   else
      MesInfo.setAttribute("xmlns", "urn:cbr-ru:msg:props:v1.2");
   end;
   
   item = xml.createElement("To");
   item.appendChild(xml.createTextNode("uri") );
   MesInfo.appendChild( item );
   item = xml.createElement("From");
   item.appendChild(xml.createTextNode( GetSenderAddress(SenderDepartment) ) );
   MesInfo.appendChild( item );
   item = xml.createElement("MessageID");
   item.appendChild(xml.createTextNode("uri") );
   MesInfo.appendChild( item );
   item = xml.createElement("MessageType");
   item.appendChild(xml.createTextNode("1") );
   MesInfo.appendChild( item );
   item = xml.createElement("Priority");
   item.appendChild(xml.createTextNode("1") );
   MesInfo.appendChild( item );
   item = xml.createElement("CreateTime");
   item.appendChild(xml.createTextNode(GetCurrentDateTime())); 
   MesInfo.appendChild( item );
   Header.appendChild(MesInfo);
   envelope.appendChild(Header);
   body = xml.createElement("Body");  

   i=0;
   while( i < xmlIni.childNodes.length )
     child = xmlIni.childNodes.item(i);
     if( child and (child.nodeType==CHILD_NODE) )
        body.appendChild(child);
        i = xmlIni.childNodes.length;
     end;
     i = i+1;
   end;

   envelope.appendChild(body);
   xml.appendChild(envelope);

   setParm(0,xml);
end;

private macro GetEDReceiverForED273(wlmes) : string
  return GetEDReceiver( wlmes.OutsideAbonentID, 
                        wlmes.OutsideAbonentCodeKind, 
                        wlmes.OutsideAbonentCode );
end;

private macro GetEDReceiverForExport(wlmes) : string
  var EDReceiver : string = "";

  EDReceiver = GetEDReceiver( wlmes.OutsideAbonentID, 
                        wlmes.OutsideAbonentCodeKind, 
                        wlmes.OutsideAbonentCode, "", 1 );
  
  return EDReceiver;
end;

private macro CreatePacket(PacketName : string, HasParentNode : bool) : object
  var xml:object = ActiveX("Microsoft.XMLDOM");
  var packet : object = xml.createElement(PacketName);
  var Err = "";
  if(not HasParentNode)
    set_xmlns(packet);
  end;

  var WlSessUID = EDNoDateAuthor();
  WlSessUID.ConstructByTrn(wlSess.SessUID);
  var EDDate : date = WlSessUID.EDDate;
    
  packet.setAttribute("EDNo",     WlSessUID.EDNo );
  packet.setAttribute("EDDate",   YYYYMMDD( EDDate ) );
  packet.setAttribute("EDAuthor", WlSessUID.EDAuthor );
    
  
  return packet;
end;

private macro SetEDCreationTime( IsPack, PacketName, packet )

  if(IsPack and (   ((PacketName == "PacketEID") and Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\PACKETEID_EDCREATIONTIME", V_BOOL, false))
                  or 
                    ((PacketName == "PacketESID") and Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\PACKETESID_EDCREATIONTIME", V_BOOL, false))
                )
    )
    packet.setAttribute("EDCreationTime", TimeToStr(time(), "hh:mi:ss") );
  end;

end;

/***************************************************************************/
/* Макрос экспорта                                                         */
/***************************************************************************/
macro UFBSOutProc( fileName, addrSess )
  HasED501 = false;

  var continue0 = TRUE, Документов = 0, err;
  var xml:object = ActiveX("Microsoft.XMLDOM");
  var doc:object, packet:object;
  var obj:object, item:object, subItem:object;
  var СуммаДокументов = 0, СуммаДокумента = 0;
  var IsPack;
  var MaxExpMes : integer = 0, PacketType : integer = 0;
  var WlmesDepartment : integer = 0, WlmesStartDepartment : integer = 0;
  var WlMesTRN : string = "";
  var UseArmKBR = false;

  RECORD lastMes("wlmes.dbt");

  SetBuff( wlSess, addrSess );

  xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='UTF-8'") );  

  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if ( not err )
       ErrExport("Не найдено ни одного сообщения для отправки");
    else
       ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;
  
  WlmesDepartment = wlmes.Department;
  WlmesStartDepartment = wlmes.StartDepartment;
  WlMesTRN = wlmes.TRN;
  
  if( not UseArmKBR )
    UseArmKBR = GetTpSchemSign(wlmes.TpSchemID);
  end;
       
  /* Используем ли пакеты ЭПД/ЭСИД/EID */
  if( not (GetModePacketEPD_ESID( wlmes, @IsPack, @PacketType, @MaxExpMes )) )
    ErrExport( "Ошибка при определении режима использования пакетов ЭПД/ЭСИД" );
    return false;
  end;
  
  var PacketName : string = Bnk_GetLLValuesName(OBJTYPE_PACKETTYPE_MESFRM, PacketType);
  /* Формирование пакета */
  if( IsPack )
    packet = CreatePacket(PacketName, false);
    
    doc = xml.appendChild(packet);
  else
    doc = xml;
  end;
  
  /* Последовательно считываем сообщения и формируем файл экспорта */
  var refED273 : string = "", RootNodeCount : integer = 0;
  var CommonEDReceiver : string = GetEDReceiverForExport(wlmes), IsEqualEDReceiver : bool = true;
  var SysCode = GetMesSystemCode( wlmes.MesID );
  var CurSysCode = "";
  var IsRepeat = true;

  while( continue0 )
    
    var curr_doc : object = doc, curr_IsPack : bool = IsPack;
    
    if( not ЗаписатьСообщение( СуммаДокумента, @curr_doc, curr_IsPack, false, false, wlSess.SessUID, wlmes.Trn) )
      return false;
    end;
    
    if( IsPack and (PacketName == "PacketEPD") and IsRepeat )
      CurSysCode = GetMesSystemCode( wlmes.MesID );
      if( (CurSysCode != SysCode) )
        IsRepeat = false;
      end;
    end;

    if( IsPack )

      if(IsEqualEDReceiver)
        var CurEDReceiver : string = GetEDReceiverForExport(wlmes);

        if(CurEDReceiver != CommonEDReceiver)
          IsEqualEDReceiver = false;
        end;
      end;

    end;

    copy(lastMes, wlmes);

    if( not СчитатьЗапись( wlmes, err, false ) )
       if ( not err )
          continue0 = FALSE;
       else
          ErrExport("Ошибка чтения сообщения");
          return false;
       end;
    else
      if( not UseArmKBR )
        UseArmKBR = GetTpSchemSign(wlmes.TpSchemID);
      end;
    end;

    Документов = Документов + 1;
    СуммаДокументов = СуммаДокументов + СуммаДокумента;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;  
  
  if( IsPack and (PacketName == "PacketEPD"))
    var TextErr, SystemCode;
    SystemCode = GetSystemCode(lastMes, @TextErr, IsPack, PacketName, wlSess.SessionID, IsRepeat);
    if( strlen(TextErr) != 0 )
      ErrExport(TextErr);
      return false;
    end;
    packet.setAttribute("SystemCode", SystemCode);
  end;
  
  if ( IsPack and InList(PacketName, "PacketEPD", "PacketESID", "PacketEID") )
    packet.setAttribute("EDQuantity", Документов );
  end;

  if ( IsPack and (PacketName == "PacketEPD") )
    packet.setAttribute("Sum", СуммаДокументов );
  end;

  SetEDCreationTime( IsPack, PacketName, packet );

  if( IsPack )
    if(IsEqualEDReceiver and CommonEDReceiver)
      packet.setAttribute( "EDReceiver", CommonEDReceiver );
    end;
  end;

  if ( ОформлятьСлужебныйПакет )
     ОформитьСлужебныйПакет(xml, WlmesDepartment);
  end;

  if( UseARMKBR )
    var xml_string = StrSubst(xml.xml, "<?xml version=\"1.0\"?>", "<?xml version=\"1.0\" encoding=\"WINDOWS-1251\"?>");

    xml_string = ToANSI(xml_string, true);

    var conditions = GetConditions( wlsess.SessionID, xml.childNodes.item(1).baseName, WlmesStartDepartment, WlmesDepartment );

    var ids = makeArray(string(wlsess.SessionID));
    var xmls = makeArray(xml_string);
                            
    var outIDs = TArray;
    var outXMLs = TArray;
    var checkStats = TArray;
    var checkErrTexts = TArray;
    var error : string;

    var SigningResult = 0;
    var SigningError = "";
    var isPacket = false;

    var query = "SELECT 1 FROM dllvalues_dbt llvalues WHERE llvalues.t_Name = :MessageType AND llvalues.t_List = :OBJTYPE_PACKETTYPE_MESFRM";

    var rs = execSQLselect( query, makeArray( SQLParam("MessageType", xml.childNodes.item(1).baseName), SQLParam("OBJTYPE_PACKETTYPE_MESFRM", 2106 ) ) );

    if( rs and rs.moveNext() )
      isPacket = true;
    else
      isPacket = Index(Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\ЗАЩИТА ЭС\\ТЕГИ_ПАКЕТОВ_ЭС", V_STRING, ""), xml.childNodes.item(1).baseName ) != 0;   
    end;

    SigningResult = BNK_UFEBS_makeZK(ids, xmls, OemToUtf8( conditions ), isPacket, outIDs, outXMLs, checkStats, checkErrTexts, SigningError);
      
    if(SigningResult != 0)

      if( SigningError == "" )
        SigningError = "Ошибка при подписании ЗК: " + string(SigningResult);
      else
        SigningError = string(SigningResult) + " " + SigningError;
      end;  

      ErrExport( SigningError );
      return false;
    elif(outXMLs.size > 0)
      SigningResult = checkStats(0);    
      SigningError = checkErrTexts(0);
    end;

    if(SigningResult != 0)    

      if( SigningError == "" )
        SigningError = "Ошибка при подписании ЗК: " + string(SigningResult);
      else
        SigningError = string(SigningResult) + " " + SigningError;
      end;

      ErrExport( SigningError );
      return false;
    else
      xml_string = outXMLs(0);
    end;
    
    if( saveXMLInSessionData( xml_string, wlsess.SessionID ) )
      ErrExport( "Ошибка при сохранении данных сеанса" );
      return false;
    end;
    
    var update = "";
    var parm : TArray = TArray;  
    
    parm[0] = SQLParam("State", 30);
    parm[1] = SQLParam("MessageType", xml.childNodes.item(1).baseName);
    
    if(not isPacket )
      update = "UPDATE dwlsess_dbt SET t_State = :State, t_MessageType = :MessageType, t_SessUID = :SessUID where t_SessionID = :SessionID ";
      parm[2] = SQLParam("SessUID", WlMesTRN);    
    else
      update = "UPDATE dwlsess_dbt SET t_State = :State, t_MessageType = :MessageType where t_SessionID = :SessionID ";
    end;
    
    parm[parm.Size] = SQLParam("SessionID", wlsess.SessionID);
    
    execSQL(update, parm, true);

  else
    xml.Save(fileName);
  end;

  if(HasED501)
    CheckFileSizeED501(fileName, ESSize501);
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ErrExport(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro UFBSOutProcV2( fileName, addrSess )
  ver_st = 2;
  return UFBSOutProc( fileName, addrSess );
end;