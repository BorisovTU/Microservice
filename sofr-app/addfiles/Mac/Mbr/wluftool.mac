/*
$Name:           wluftool.mac
$Module:         Межбанковские расчеты
$Description:    Общие константы и вспомогательные функции для работы с УФЭБС
*/                                                                            

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Общие константы и вспомогательные функции для работы с УФЭБС             */
/*                                                                          */
/*  Имя файла: wluftool.mac                                                 */
/*  Создан:  06.10.04                                              BARS     */
/****************************************************************************/
import PTInter, FIInter, wldoc, oralib, likepy, "cb_sql.mac", "pmcaches.mac", "wltools.mac",
       "xmlmestools.mac", "bnk_common.mac", "bnk_dttmfrmt.mac", "bnk_ptlib.mac", "rmcmptl.mac";

/* Введите Ваши реквизиты */
const XMLField        = "xml";

const UFBSDocExtField = "DocExt";      /* Повторяющееся поле - документ выписки ED211 */
const NodeTransInfo   = "TransInfo";   /* Узел - документы выписки ED211 */
const NodeED211       = "ED211";       
const NodeEDRefID     = "EDRefID";

const NodeEDInfo      = "EDInfo";
const NodeED207       = "ED207";       

const NodeEDCopy      = "EDCopy";
const NodeED221       = "ED221";       

const UFBSDocInfoField= "DocInfo";
const UFBSAccDocInfoField  = "AccDocInfo";
const NodeCreditInfo  = "CreditInfo";
const NodeAccDocInfo  = "AccDocInfo";

const NodeED217       = "ED217";       

const UFBSRepConStrField= "RepConStr";
const NodeReportCon     = "ReportCont";
const NodeReportContent = "ReportContent";
const NodeED219         = "ED219";       

const UFBSDescrStrField = "DescrStr";
const NodeDescrCorr     = "DescrCorr";
const NodeCorrectionDescription = "CorrectionDescription";
const NodeED330         = "ED330";

const NodeEDRequisiteList = "EDRequisiteList";
const NodeEDRqstList = "EDRqstList";
const NodeED241       = "ED241";    
const NodePacketDate  = "PacketDate";

const NodeED215       = "ED215";    
const NodeED501       = "ED501";
const NodeED503       = "ED503";

const NodeED108       = "ED108";
const UFBSCreditTransferTransField = "CreditTransferTrans";
const NodeCreditTransferTransactionInfo = "CreditTransferTransactionInfo";
const NodeCreditTransferTrans = "CreditTransferTrans";

// "сложные" формы
const NodeED107   = "ED107";
const NodeED374   = "ED374";    
const NodeED243   = "ED243";
const NodeED244   = "ED244";
const NodeED245   = "ED245";
const NodeED332   = "ED332";
const NodeED379   = "ED379";    
const NodeED381   = "ED381";
const NodeED375   = "ED375";
const NodeED508   = "ED508";
const NodeED530   = "ED530";
const NodeED541   = "ED541";
const NodeED574   = "ED574";
const NodeED280   = "ED280";
const NodeED463   = "ED463";
const NodeED465   = "ED465";
const NodeED318   = "ED318";
const NodeED510   = "ED510";
const NodeED802   = "ED802";
const NodeED805   = "ED805";
const NodeED807   = "ED807";
const NodeED808   = "ED808";
const NodeED811   = "ED811";
const NodeED814   = "ED814";


macro ToDate( strYYYYMMDD )
  return ToDateYYYY_MM_DD(strYYYYMMDD);
end;

private var PtIdCache:TPartyIdsByCode = TPartyIdsByCode();

/* По уникальному составителю документа (УИС) найти ID субъекта */
macro ToRespID( uis:string ):integer
  var ID:integer, bic_str:string, val;
  
  /* Формируем БИК ЦБ */
  bic_str = "04" + substr( uis, 1, 7);
  var stat:bool = PtIdCache.Get( PtIdCache.Key( PTCK_BIC, bic_str ), @ID );
  if( not stat )
    RunError( "Не найден субъект по БИК'у " + bic_str );
  end;
  
  /* Сыылка на "наш банк" */
  if( ID == 0 )
    ID = {OurBank};
  end;
  
  /* Возвращаем если нужно БИК ЦБ */
  if( GetParm( 1, val ) )
    SetParm( 1, bic_str );
  end;

  return ID;
end;  

macro GetPartyIdByUIS
( uis: string, 
  PartyCodeKind : @integer,
  PartyCode : @string
) : integer

  // Сначала ищем по коду вида 47 UIS
  PartyCode       = uis;
  PartyCodeKind   = PTCK_UIS;
  var PartyID : integer = GetCodeOwnerID(PartyCodeKind, PartyCode);

  return PartyID;

OnError(er) // Поиск субъекта "отвалился"
  PartyID       = -1;
  return PartyID;
end;

macro FillPartyByUIS(uis : string, PartyID : @integer, 
                                   PartyCodeKind : @integer,
                                   PartyCode : @string,
                                   PartyName : @string )

  var bic_str = "";
  PartyID        = ToRespID(uis, bic_str);
  PartyCodeKind  = PTCK_BIC;
  PartyCode      = bic_str;

  FILE party(party) key 0;
  party.PartyID = PartyID;
  if(GetEQ(party))
    PartyName    = party.Name;
  end;
  
  OnError(er) // ToRespID не нашла субъекта
    PartyID       = -1;
    PartyCodeKind = PTCK_BIC;
    PartyCode     = "04" + substr( uis, 1, 7);
    PartyName     = "";
end;

macro FillPartyByUIS_PTCK_UIS(uis: string, PartyID : @integer, 
                                           PartyCodeKind : @integer,
                                           PartyCode : @string,
                                           PartyName : @string )

  PartyID         = GetPartyIdByUIS(uis, @PartyCodeKind, @PartyCode);

  if(PartyID > 0)
    PartyName     = Bnk_GetPartyName(PartyID);
  else
    PartyName = "";
  end;

end;

macro GetNodes( node:object, fullPath:string):Tarray
  var chName, i:integer, child:object, textNode:string;
  var Nodes = Tarray();
 
  if( node.NodeType==DOCUMENT_NODE )
     i=0;
     while( i < node.childNodes.length )
       child = node.childNodes.item(i);
       if( child and (child.nodeType==CHILD_NODE) )
         Nodes = GetNodes( child, fullPath, false );
         return Nodes;
       end;
       i = i+1;
     end;
     return Nodes;;
  end;

  if ( index(fullPath,"/")==0 )
     chName = fullPath;
  else
     chName = substr(fullPath,1,index(fullPath,"/")-1);
  end;

  i=0;
  while( i < node.childNodes.length )
    child = node.childNodes.item(i);
    if( child and (child.nodeType==CHILD_NODE) )    
      if( (GetNodeName(child.NodeName) == chName) )      
        if(index(fullPath,"/")==0 )
           Nodes[Nodes.size] = child;
        else
           Nodes = GetNodes( child, substr(fullPath,index(fullPath,"/")+1));
             return Nodes; 
        end;
      end;
    end;
    i=i+1;
  end;
  return Nodes;
end;

/* Поиск сеанса связи по UID сеанса */
macro WldFindSessByUID( TpID:integer, SessDate:date, SessUID:string ):integer
  var select:string;
  var params:TArray;
  var rs:object;

  select = 
    "select t_SessionID " +
    "  from dwlsess_dbt " +
    " where t_TpID = :TpID " +
    "   and t_SessUID = :SessUID " +
    "   and t_Direct = chr(0) /*WLD_MES_OUT*/ ";

  if(SessDate != date(0, 0, 0))
    select = select +
      " and t_SysDate = :SessDate ";
  end;

  params = makeArray( SQLParam( "TpID", TpID),
                      SQLParam( "SessUID",  SessUID) 
                    );

  if(SessDate != date(0, 0, 0))
    params[ params.size ] = SQLParam( "SessDate", SessDate);
  end;

  rs = execSQLselect( select, params, true ); 
  if( rs.MoveNext() )
    return rs.value(0);
  end;

  return 0;
end;

macro OnlyDigit( val:string )
  var count, len = strlen(val), symb;
  
  count = 1;
  while( count <= len )
    symb = substr(val, count, 1);
    if( (symb >= "0") and (symb <= "9") )
      count = count + 1;
    else
      return false;
    end;
  end;
  return true;
end;

class EDNoDateAuthor
  var EDNo = ""; //номер сообщения
  var EDDate = date(0,0,0);
  var EDAuthor = ""; //автор сообщения (с 000 на конце)

  
  //Формат референса: YYMMDDAAAAAAA000NNNNNN, где:
  //YYMMDD - дата сообщения
  //AAAAAAAA  - автор сообщения
  //NNNNNN - номер сообщения 

  //Функция разбирает референс (по вышепривeдённому формату) и инициализирует EDNo, EDDate, EDAuthor
  macro ConstructByTrn(Trn)
    EDNo = substr(Trn, 17);
    EDDate = date( (substr(Trn,5,2))+"."+(substr(Trn,3,2))+"."+(substr(Trn,1,2)) );
    EDAuthor = substr(Trn, 7, 10);
  end;

  //Ищет сообщение связанного платежа и инициализирует EDNo, EDDate, EDAuthor по сообщению связанного платежа
  //
  macro ConstructByInitialPmMes_mes(PurposePaymentMesID):bool
    var rs:object = NULL;
    var select:string = "";
    var params:TArray = NULL;
    
    select = " SELECT i_wlmes.t_Trn"
             " FROM"
             "   dwlmes_dbt p_wlmes,"       //purpose
             "   dwlmeslnk_dbt p_wlmeslnk," //purpose
             "   dwlpm_dbt p_wlpm,"         //purpose
             "   dpmlink_dbt pmlink,"
             "   dwlpm_dbt i_wlpm,"         //initial
             "   dwlmeslnk_dbt i_wlmeslnk," //initial
             "   dwlmes_dbt i_wlmes"        //initial
             " WHERE"
             "   p_wlmes.t_MesID = ? AND "
             "   p_wlmeslnk.t_MesID = p_wlmes.t_MesID AND" 
             "   p_wlmeslnk.t_ObjKind = ? AND"
             "   p_wlmeslnk.t_Direct = chr(0) AND"
             "   p_wlpm.t_WlPmID = p_wlmeslnk.ObjID AND"
             "   pmlink.t_PurposePayment = p_wlpm.t_PaymentID AND"
             "   pmlink.t_LinkKind in (?,?,?) AND" 
             "   i_wlpm.t_PaymentID = pmlink.t_InitialPayment AND"
             "   i_wlpm.t_Direct = 'X' AND"
             "   i_wlpm.t_WlPmNum = 0 AND"
             "   i_wlmeslnk.t_ObjID = i_wlpm.t_WlPmID AND"
             "   i_wlmeslnk.t_ObjKind = ? AND"
             "   i_wlmeslnk.t_Direct = 'X' AND"
             "   i_wlmes.t_MesID = i_wlmeslnk.t_MesID"
             ;
    params = makeArray( SQLParam("", PurposePaymentMesID),
                        SQLParam("", OBJTYPE_PAYMENT),
                        SQLParam("", PMLINK_KIND_RETREDIR), SQLParam("", PMLINK_KIND_CLOSACC), SQLParam("", PMLINK_KIND_PROCUNKN),
                        SQLParam("", OBJTYPE_PAYMENT)
                      );
    rs = execSQLselect( select, params, FALSE );
    if ( rs.moveNext() )
      ConstructByTrn(rs.value(0));
      return true;
    else
      return false;
    end;
  end;

  //Ищет сообщение связанного платежа и инициализирует EDNo, EDDate, EDAuthor по сообщению связанного платежа
  macro ConstructByInitialPmMes_pm(PurposePaymentID):bool
    var rs:object = NULL;
    var select:string = "";
    var params:TArray = NULL;
    
    select = " SELECT i_wlmes.t_Trn"
             " FROM"
             "   dpmlink_dbt pmlink,"
             "   dwlpm_dbt i_wlpm,"         //initial
             "   dwlmeslnk_dbt i_wlmeslnk," //initial
             "   dwlmes_dbt i_wlmes,"       //initial
             "   dwltpshem_dbt wltpshem"
             " WHERE"
             "   pmlink.t_PurposePayment = ? AND"
             "   pmlink.t_LinkKind in (?,?,?) AND" 
             "   i_wlpm.t_PaymentID = pmlink.t_InitialPayment AND"
             "   i_wlpm.t_Direct = 'X' AND"
             "   i_wlpm.t_WlPmNum = 0 AND"
             "   i_wlmeslnk.t_ObjID = i_wlpm.t_WlPmID AND"
             "   i_wlmeslnk.t_ObjKind = ? AND"
             "   i_wlmeslnk.t_Direct = 'X' AND"
             "   i_wlmes.t_MesID = i_wlmeslnk.t_MesID AND"
             "   i_wlmes.t_TpSchemID = wltpshem.t_TpShemID AND"
             "   wltpshem.t_TpID = ?"
             ;
    params = makeArray( SQLParam("", PurposePaymentID),
                        SQLParam("", PMLINK_KIND_RETREDIR), SQLParam("", PMLINK_KIND_CLOSACC), SQLParam("", PMLINK_KIND_PROCUNKN),
                        SQLParam("", OBJTYPE_PAYMENT),
                        SQLParam("", TRANSP_UFBS)
                      );
    rs = execSQLselect( select, params, FALSE );
    if ( rs.moveNext() )
      ConstructByTrn(rs.value(0));
      return true;
    else
      return false;
    end;
  end;


  //Получает референс в нужном формате из EDNo, EDDate, EDAuthor 
  macro GetTrn():string
    return YYMMDD(EDDate) + EDAuthor + EDNo;
  end;


end;

// Получить название и значение поля из строки вида "Атрибут: Значение"
// Если задан NamesToSearch, то Атрибут проверяется на вхождение в NamesToSearch
macro getFieldFromQueriesLine(QLine : string, NamesToSearch : TArray): TMesField
  macro IsInSearchArray(s:string, SearchArray:TArray):bool
    for(var i, 0, SearchArray.size() - 1)
      if(s == SearchArray(i))
        return true;
      end;
    end;
    return false;
  end;

  var s = Trim( SubStr(QLine, 1, index(QLine, ":")-1) );
  if( s and ( (NamesToSearch == null) or IsInSearchArray(s, NamesToSearch) ) )
    return TMesField(s, Trim( SubStr(QLine, index(QLine, ":")+1) ) );
  end;
  return NULL;
end;

// Найти в Queries подстроку AttrName и вернуть значение после AttrName до конца строки
macro getFldValFromQueries(Queries : string, AttrName) : string
  var val = ""; // значение атрибута

  var ind1 = index(Queries, AttrName);
  if(ind1)
    ind1 = ind1 + StrLen(AttrName); 
    var ind2 = index(Queries, "\n", ind1);
    val = SubStr(Queries, ind1, IfThenElse(ind2, ind2 - ind1, null));
    val = Trim(val);
  end;

  return val;
end;

macro GetRelatedRefFromWlreq(wlreq, RefLabel : string, ErrMsg : string) : string
  var ref_str = wlreq.RelatedRef;

  if( (not ref_str) and RefLabel )
    ref_str = getFldValFromQueries(wlreq.Queries, RefLabel + ":");
  end;

  if( (not ref_str) and ErrMsg )
    RunError(ErrMsg, TRsbError(null, 0, ErrMsg));
  end;

  return ref_str;
end;

macro PaytKindToPaymentKind(PaytKind : integer, FormName : string) : string
  var PaymentKind : string = "";

  if ( PaytKind==0 )
    PaymentKind = "Н";
  elif( PaytKind == 1 )
    PaymentKind = "Э";
  elif( PaytKind == 2 )
    PaymentKind = "П";
  elif( PaytKind == 3 )
    PaymentKind = "Т";
  elif( (PaytKind == 4) and (FormName != "ED111") )
    PaymentKind = "С";
  end;

  return PaymentKind;
end;

macro GetPayerBankEnterDate( sessionID:integer ) : date
  
  var query:string = "SELECT wlsess.t_BankDate " +
                     " FROM dwlsess_dbt wlsess" +
                     " WHERE wlsess.t_Sessionid = :SessionID;" ;

  var params:TArray = makeArray( SQLParam( "SessionID", SessionID  ) );
  var rs:RsdRecordset = execSQLselect( query, params, TRUE );
  
  var PayerBankEnterDate:date;

  if( rs and rs.moveNext() )
    PayerBankEnterDate = rs.value(0);
  else
    PayerBankEnterDate = {curdate};
  end;

  return PayerBankEnterDate;
end;

macro FillPayment_113_114(xml:object, wlmes, Payment:object)
  var tmpstr = "";
  Payment.Number = ReadAttribute(xml,"AccDocNo", "AccDoc");
  Payment.Reference = wlmes.Trn;
  Payment.Date = ToDate(ReadAttribute(xml,"AccDocDate", "AccDoc"));
  Payment.ValueDate = wlmes.OutsideAbonentDate;

  Payment.BaseFIID = NATCUR;
  Payment.BaseAmount = moneyL( ReadAttribute(xml,"Sum") )/100;

  // Payer
  var ClientINN = ReadOptinalAttribute(xml, "INN", "Payer");
  tmpstr = ReadOptinalAttribute(xml, "KPP", "Payer");
  if( tmpstr and ClientINN )
    ClientINN = ClientINN + "/" + tmpstr;
  end;   
  var ClientName = ReadNodeText(xml, "Payer/Name");  
  Payment.SetPayerPI(
          PAYMENTS_GROUP_INTERNAL,
          -1,
          PTCK_BIC,
          ReadAttribute(xml, "BIC", "Payer/Bank"),
          "",
          ReadOptinalAttribute(xml, "CorrespAcc", "Payer/Bank"),
          GetFIIDByCodeInAccount(substr(ReadOptinalAttribute(xml, "PersonalAcc", "Payer"), 6, 3)),
          1, /*CHAPT1*/
          ReadOptinalAttribute(xml, "PersonalAcc", "Payer"),
          null,
          ClientName,
          ClientINN
          );

  // Receiver
  ClientINN = ReadOptinalAttribute(xml, "INN", "Payee");
  tmpstr = ReadOptinalAttribute(xml, "KPP", "Payee");
  if( tmpstr and ClientINN )
    ClientINN = ClientINN + "/" + tmpstr;
  end; 
  ClientName = ReadNodeText(xml, "Payee/Name");  
  Payment.SetReceiverPI(
          PAYMENTS_GROUP_EXTERNAL,
          -1,
          PTCK_BIC,
          ReadAttribute(xml, "BIC", "Payee/Bank"),
          "",
          ReadOptinalAttribute(xml, "CorrespAcc", "Payee/Bank"),
          NATCUR,
          1, /*CHAPT1*/
          ReadOptinalAttribute(xml, "PersonalAcc", "Payee"),
          null,
          ClientName,
          ClientINN
          );

  Payment.Ground = ReadNodeText(xml, "Purpose");
  //Payment.ReceiverIsSender = "X"; /*??? read-only*/
  Payment.Priority = ReadAttribute(xml, "Priority");
  Payment.UIN = ReadOptinalAttribute(xml,"PaymentID");

  tmpstr = ReadOptinalAttribute(xml, "ReceiptDateCollectBank");
  if(tmpstr)  
    Payment.ReceiverBankMarkDate = ToDate(tmpstr);
  end;
  tmpstr = ReadOptinalAttribute(xml, "FileDate");
  if(tmpstr)  
    Payment.I2PlaceDate = ToDate(tmpstr);
  end;

  Payment.Origin = PAYMENT_OR_ELECTR;

  Payment.PayerBankEnterDate = GetPayerBankEnterDate( wlmes.sessionID );
end; 

macro GetEDNoDateAuthorFromXml(xml : object, path : string) : EDNoDateAuthor
  var ed_nda : EDNoDateAuthor = EDNoDateAuthor();
  ed_nda.EDNo     = ReadAttribute(xml, "EDNo", path);
  ed_nda.EDDate   = ToDate(ReadAttribute(xml, "EDDate", path));
  ed_nda.EDAuthor = ReadAttribute(xml, "EDAuthor", path);

  return ed_nda;
end;

macro GetRefByEDNoDateAuthor(xml : object, path : string) : string
  var ed_nda : EDNoDateAuthor = GetEDNoDateAuthorFromXml(xml, path);
  return ed_nda.GetTrn();
end;

macro ReadEDNoDateAuthorField(fieldname : string, fieldvalue : string, ed_nda : EDNoDateAuthor)
      if (fieldname == "EDNo")
        ed_nda.EDNo = fieldvalue;
      elif (fieldname == "EDDate")
        ed_nda.EDDate = ToDate(fieldvalue);
      elif (fieldname == "EDAuthor")
        ed_nda.EDAuthor = fieldvalue;
      end;
end;

// Получить имя формы УФЭБС по значению RlsFormID
macro GetUFBSMesFormName(RlsFormID : integer)
  var strSQL : string = "select frm.t_Name " +
                        "  from dwlmesfrm_dbt frm, dwlmesrls_dbt rls " +
                        " where rls.t_RlsFormID = :RlsFormID " + 
                        "   and frm.t_FormID = rls.t_FormID ";

  var rs = execSQLselect( strSQL, makeArray( SQLParam("RlsFormID", RlsFormID) ) );

  if ( rs and rs.MoveNext() )
    return substr( rs.value(0), 1, 5 ); // первые пять символов wlmesfrm.Name
  end;

  return "";
end;

macro StrToXml(s : string) : object
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if( not xml.loadXML(s) )
    RunError( "Текст не соответствует формату XML" );
  end;

  if( xml.nodeType == DOCUMENT_NODE )
    return xml.DocumentElement;
  end;

  return xml;
end;

macro ReadXMLField(wlmes) : object
  var field_name, field_value;  
  if( not СчитатьПоле(field_name, field_value) or (field_name != XMLField) )
    RunError("Не найдено поле " + XMLField + " в сообщении с референсом " + wlmes.TRN + ".");
  end;

  return StrToXml(field_value);
end;

macro GetPaytCondition( AcceptTerm: integer )
  if (AcceptTerm != 0)
    return 1;
  else
    return 2;
  end;
end;

macro GetAcceptTermByPaytCond(PaytCond : string) : integer
  if(PaytCond == "1")
    return PM_DEMAND_TERM_PREACCEPT;
  else
    return PM_DEMAND_TERM_ACCEPT;
  end;
end;

macro GetAcceptPeriodByAcptTerm( AcptTerm : integer ) : integer
  var RegVal = 0;
  if( AcptTerm == PM_DEMAND_TERM_PREACCEPT )
    return 0;
  elif( AcptTerm == PM_DEMAND_TERM_ACCEPT)
    return AcptTerm;
  elif( GetRegistryValue( "PS\\PAYORDER\\K1_PAYPERIOD", V_INTEGER, RegVal ) )
    return RegVal;
  else
    return 5;
  end;
end;

macro GetAcceptPeriod( pspaydem ) : integer
  if ( pspaydem.AcceptPeriod <= 0 )
    return 5;
  else
    return pspaydem.AcceptPeriod;
  end;
end;

// sumstrUFBS - Сумма в копейках
macro StrToMoneyUFBS(sumstrUFBS : string) : moneyL
  return moneyL(sumstrUFBS) / 100;
end;

macro MoneyToStrUFBS(sum : money) : string
  return string( (sum * 100):0:0 );
end;

//
macro FindCorschemIsUBR( BIC : string, Acc : string, CorNumber : @integer, CorFIID : @integer )
  var isFound : bool = false;

  var query : string = " SELECT corschem.t_Number as t_Number, corschem.t_FIID as t_FIID " +
                       " FROM dcorschem_dbt corschem, dpartcode_dbt partcode, ddp_dep_dbt dp_dep " +
                       " WHERE PM_COMMON.IsBankUBR_Num( corschem.t_CorrID ) = 1 " +
                       "   AND corschem.t_Department = dp_dep.t_Code "
                       "   AND dp_dep.t_PartyID = partcode.t_PartyID "
                       "   AND partcode.t_CodeKind = :CodeKind "
                       "   AND partcode.t_Code = :Code ";
  if( (valtype(Acc) != V_UNDEF) and (Acc != "") )
    query = query + "      AND corschem.t_CorAccount = :CorAccount ";
  end;

  var params:TArray = makeArray( SQLParam( "CodeKind",   PTCK_BIC ),
                                 SQLParam( "Code",       BIC ) );
  if( (valtype(Acc) != V_UNDEF) and (Acc != "") )
      params[params.size] =      SQLParam( "CorAccount", Acc )
  end;                               

  var rs : RsdRecordset = execSQLselect( query, params );

  if( rs and rs.moveNext() )
    isFound = true;
    if( valtype(CorNumber) != V_UNDEF )
      CorNumber = rs.value( "t_Number" );
    end;
    if( valtype(CorFIID) != V_UNDEF )
      CorFIID = rs.value( "t_FIID" );
    end;
  end;

  return isFound;
end;

macro FindReqOutMesID( outMesTRN:string, inTpSchemID:integer ): integer

  var outMesID = 0;
  var rs = execSQLselect( "select reqmes.t_MesID "
                            "from dwlmes_dbt reqmes, "
                                 "dwltpshem_dbt out_tpsh, "
                                 "dwltpshem_dbt in_tpsh "
                            "where reqmes.t_Trn = :outMesTRN "
                              "and reqmes.t_Direct = chr(0)/*WLD_MES_OUT*/ "
                              "and in_tpsh.t_TpShemID = :inTpSchemID " 
                              "and out_tpsh.t_TpShemID = reqmes.t_TpSchemID "
                              "and out_tpsh.t_TpID = in_tpsh.t_TpID", 
                              makeArray( SQLParam("outMesTRN", outMesTRN),
                                         SQLParam("inTpSchemID", inTpSchemID)
                                       ), true 
                        );

  if ( rs and rs.MoveNext() )
     outMesID = rs.value("t_MesID");
  end;

  return outMesID;
end;

private macro processRecords( query:string, params:TArray, name:string, PartyID : @integer )
  
  var rs = execSQLselect( query, params );
  
  while( rs and ( rs.moveNext() ) and ( PartyID == -1 ) )
    if( rs.value(1) > 1 )
      if( not CompareNamesByClientID( rs.value(0), name, 0, "", 0 ) )
        PartyID = rs.value(0);
      end;
    else
      PartyID = rs.value(0);
    end;
  end;
end;

macro IdentifyParticipantByAccountAndName( account : string, name : string ) : integer

  var PartyID = -1;
  
  if( CompareStrWithMasks( Bnk_GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\УФЭБС\\СЧЕТА_В_ПБР", V_STRING, ""), account ) == 0 )    
    processRecords( "SELECT T_PARTYID, COUNT (T_PARTYID) OVER (PARTITION BY T_ACCNT) " +
                    "    FROM DBANKDPRTACCNT_DBT " +
                    "   WHERE T_ACCNT = :ACCOUNT ",
                    makeArray( SQLParam("ACCOUNT", account) ),
                    name,
                    @PartyID
                  );
  end;

  if( PartyID == -1 )
    processRecords( "  SELECT DP_DEP.T_PARTYID, " +
                    "           COUNT (DP_DEP.T_PARTYID) OVER (PARTITION BY ACCOUNT.T_ACCOUNT) " +
                    "      FROM DACCOUNT_DBT ACCOUNT " +
                    "           INNER JOIN DDP_DEP_DBT DP_DEP ON DP_DEP.T_CODE = ACCOUNT.T_DEPARTMENT " +
                    "     WHERE ACCOUNT.T_ACCOUNT = :ACCOUNT " +
                    "  GROUP BY DP_DEP.T_PARTYID, ACCOUNT.T_ACCOUNT ",
                  makeArray( SQLParam("ACCOUNT", account) ),
                  name,
                  @PartyID
                );
  end;

  if( PartyID == -1 )
    processRecords( "  SELECT CORSCHEM.T_CORRID, " +
                    "           COUNT (CORSCHEM.T_CORRID) OVER (PARTITION BY CORSCHEM.T_CORACCOUNT) " +
                    "      FROM DCORSCHEM_DBT CORSCHEM " +
                    "     WHERE CORSCHEM.T_CORACCOUNT = :ACCOUNT " +
                    "  GROUP BY CORSCHEM.T_CORRID, CORSCHEM.T_CORACCOUNT ",
                  makeArray( SQLParam("ACCOUNT", account) ),
                  name,
                  @PartyID
                );
  end;
  
  if( PartyID == -1 )
    processRecords( "  SELECT SETTACC.T_PARTYID, " +
                    "           COUNT (SETTACC.T_PARTYID) OVER (PARTITION BY SETTACC.T_ACCOUNT) " +
                    "      FROM DSETTACC_DBT SETTACC " +
                    "     WHERE SETTACC.T_ACCOUNT = :ACCOUNT AND SETTACC.T_PARTYID <> -1" +
                    "  GROUP BY SETTACC.T_PARTYID, SETTACC.T_ACCOUNT ",
                  makeArray( SQLParam("ACCOUNT", account) ),
                  name,
                  @PartyID
                );
  end;

  return PartyID;
end;