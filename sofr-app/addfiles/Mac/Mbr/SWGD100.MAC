/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT MT100                             */
/*                                                                          */
/*  Имя файла: swgd100.mac                                                  */
/*  Создан:    08.02.00                                      Алешин А.В.    */
/****************************************************************************/

import "swgendoc.mac", "pmlib.mac";

/* Платежная инструкция "Клиентский перевод" */
class CustomerTransfer
 var
  TRF              :string,      /* Ссылочный номер операции */
  ValueDate        :date,        /* Дата валютирования */
  Currency         :string,      /* Код валюты */
  Sum              :money,      /* Сумма перевода */
  Credit           :bool,        /* Признак дебета/кредита */
  Trans            :bool,        /* Признак транзитного */
  OrdCustomer      :Customer,    /* Приказодатель */
  OrdBank          :Bank,        /* Банк Приказодателя */
  Instructor       :Bank,        /* Инструктирующий банк */
  Sender           :Bank,        /* Отправитель */
  SndCorresp       :Bank,        /* Агент Отправителя */
  InterReimbur     :Bank,        /* Рамбурсирующий Посредник */
  RcvCorresp       :Bank,        /* Агент Получателя */
  Intermediary     :Bank,        /* Посредник */
  AccBank          :Bank,        /* Банк-держатель счета */
  BnfCustomer      :Customer,    /* Бенефициар */
  BnfInfo          :TBnfInfo,    /* Информация Бенефициару */
  ComissCharges    :integer,     /* Расходы корреспондентов */
  DirectAcc        :string,      /* Счет для идентификации корсхемы */
  RcvInfo          :TRcvInfo100; /* Информация Получателю поле 72 */

  TRF        = "";
  ValueDate  = date(0,0,0);
  Currency   = "";
  Sum        = $0;
  Credit     = false;
  Trans      = false;

  ComissCharges = PM_CHRG_OUR;
  DirectAcc = "";
end; /* class CustomerTransfer */

var MT100 : CustomerTransfer;

macro Fill20( TRF )
  MT100.TRF = TRF;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT100.ValueDate = Date;
  MT100.Currency  = Cur;
  MT100.Sum       = money(Sum);
  return TRUE;
end;

macro Fill50( Address, Account, System, Code, INN )
  MT100.OrdCustomer = Customer( Address, Account, "", INN );  
  return TRUE;
end;

macro Fill52A( BIC, Account, System, Code )
  MT100.OrdBank = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill52D( Address, Account, System, Code )
  MT100.OrdBank = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill53A( BIC, Account, System, Code )
  MT100.SndCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill53B( Branch, Account, System, Code )
  if ( (valtype(Branch)==V_UNDEF) AND (valtype(System)==V_UNDEF) AND 
       (valtype(Code)==V_UNDEF) AND (valtype(Account)!=V_UNDEF) AND (Account!="") )
      MT100.DirectAcc = Account;
  else 
      MT100.SndCorresp = Bank( "", "", Branch, Account, System, Code );
  end;
  return TRUE;
end;

macro Fill53D( Address, Account, System, Code )
  MT100.SndCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill54A( BIC, Account, System, Code )
  MT100.RcvCorresp = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill54B( Branch, Account, System, Code )
  MT100.RcvCorresp = Bank( "", "", Branch, Account, System, Code );
  return TRUE;
end;

macro Fill54D( Address, Account, System, Code )
  MT100.RcvCorresp = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT100.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  MT100.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57a( BIC, Name, Account, CliringSystem, CliringCode, INN )
  MT100.AccBank = Bank( BIC, Name, "", Account, CliringSystem, CliringCode, INN );
  return TRUE;
end;

macro Fill59( Address, Account, System, Code, INN )
  MT100.BnfCustomer = Customer( Address, Account, "", INN );
  return TRUE;
end;

macro Fill70( Str )
  MT100.BnfInfo = TBnfInfo( Str );    
  return TRUE;
end;

macro Fill71A( ComissCharges )
  MT100.ComissCharges = ComissCharges;
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT100.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MT100*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld50  = ТПолеФормы( OrderingCustomerField,              FIELD_MANDATORY, "ReadD_",  "Fill50",  "" ),
    Fld52A = ТПолеФормы( OrderingInstitutionField_A,         FIELD_OPTIONAL,  "ReadA",   "Fill52A", "" ),
    Fld52D = ТПолеФормы( OrderingInstitutionField_D,         FIELD_OPTIONAL,  "ReadD_",  "Fill52D", "" ),
    Fld53A = ТПолеФормы( Sender_sCorrespondentField_A,       FIELD_OPTIONAL,  "ReadA",   "Fill53A", "" ),
    Fld53B = ТПолеФормы( Sender_sCorrespondentField_B,       FIELD_OPTIONAL,  "ReadB",   "Fill53B", "" ),
    Fld53D = ТПолеФормы( Sender_sCorrespondentField_D,       FIELD_OPTIONAL,  "ReadD_",  "Fill53D", "" ),
    Fld54A = ТПолеФормы( Receiver_sCorrespondentField_A,     FIELD_OPTIONAL,  "ReadA",   "Fill54A", "" ),
    Fld54B = ТПолеФормы( Receiver_sCorrespondentField_B,     FIELD_OPTIONAL,  "ReadB",   "Fill54B", "" ),
    Fld54D = ТПолеФормы( Receiver_sCorrespondentField_D,     FIELD_OPTIONAL,  "ReadD_",  "Fill54D", "" ),
    Fld56A = ТПолеФормы( IntermediaryField_A,                FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                FIELD_OPTIONAL,  "ReadD_",  "Fill56D", "" ),
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadBlock57aTag_A",  "Fill57a", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadBlock57aTag_B",  "Fill57a", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadBlock57aTag_D",  "Fill57a", "" ),
    Fld59  = ТПолеФормы( BeneficiaryCustomerField,           FIELD_MANDATORY, "ReadD_",  "Fill59",  "" ),
    Fld70  = ТПолеФормы( DetailsOfPaymentField,              FIELD_OPTIONAL,  "Read70",  "Fill70",  "" ),
    Fld71A = ТПолеФормы( DetailsOfChargesField_A,            FIELD_OPTIONAL,  "Read71A", "Fill71A", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMT100( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            std.msg("Ошибка при выполнении функции блока " + fld.Name);
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT100.Sender   = Bank( BankCode );

  MT100.Credit = TRUE; /* входящие клиентские переводы - кредитовые */
  MT100.Trans  = MT100.AccBank.IsSet(); /* документ транзитный - если указан банк бенефициара */

  /* Инструктирующий банк */
  if( MT100.RcvInfo.INS != "")
    /* настоятельно рекомендуется в INS передавать BIC */
    if( ReadBIC( MT100.RcvInfo.INS, 1, BankCode ) ) /* попытка чтения из INS кода BIC инструктирующего банка */
      MT100.Instructor = Bank( BankCode );
    else /* если не BIC, то Name & Address */
      MT100.Instructor = Bank( "", MT100.RcvInfo.INS );
    end;
  end;

  /* Корреспондент Получателя */
  if( MT100.RcvInfo.RCB != "")
    /* Рамбурсирующий посредник - указан в корреспонденте получателя */
    MT100.InterReimbur = MT100.RcvCorresp;
    /* настоятельно рекомендуется в INS передавать BIC */
    if( ReadBIC( MT100.RcvInfo.RCB, 1, BankCode ) ) /* попытка чтения из INS кода BIC инструктирующего банка */
      MT100.RcvCorresp = Bank( BankCode );
    else /* если не BIC, то Name & Address */
      MT100.RcvCorresp = Bank( "", MT100.RcvInfo.RCB );
    end;
  end;

  return TRUE;
end; /* ConstructMT100 */

macro GenDoc( addrMes )
  var error;
  MT100 = CustomerTransfer();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ100");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMT100( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.PayerAccount = MT100.OrdCustomer.Account;
  wlpmpaym.ReceiverAccount = MT100.BnfCustomer.Account;
  wlpmpaym.Amount      = MT100.Sum;
  wlpmpaym.PayAmount   = MT100.Sum;
  wlpmpaym.BaseAmount  = MT100.Sum;
  wlpmpaym.OrderAmount = MT100.Sum;
  wlpmpaym.ValueDate = MT100.ValueDate;
  if( not ПолучитьФинИнПоISO( MT100.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO: " +  MT100.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  if( MT100.OrdBank.IsSet() )
    wlpmpaym.PayerBankID = MT100.OrdBank.PartyID;
  else
    wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;
  end;

  /* Дебетовые свойства платежа */
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmpropdeb.TransferDate = {curdate};
  else
     wlpmpropdeb.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  if ( MT100.OrdBank.IsSet() )
     wlpmpropdeb.CodeKind = MT100.OrdBank.CodeKind;
     wlpmpropdeb.BankCode = MT100.OrdBank.CodeBank;
     if( MT100.Instructor.IsSet() )
       wlpmpropdeb.CorrCodeKind = MT100.Instructor.CodeKind;
       wlpmpropdeb.CorrCode     = MT100.Instructor.CodeBank;
       wlpmpropdeb.CorrID       = MT100.Instructor.PartyID;
     else
       wlpmpropdeb.CorrCodeKind = MT100.Sender.CodeKind;
       wlpmpropdeb.CorrCode     = MT100.Sender.CodeBank;
       wlpmpropdeb.CorrAcc      = MT100.Sender.Account;
       wlpmpropdeb.CorrID       = MT100.Sender.PartyID;
     end;
  else
     wlpmpropdeb.CodeKind = MT100.Sender.CodeKind;
     wlpmpropdeb.BankCode = MT100.Sender.CodeBank;

     if ( MT100.SndCorresp.IsSet() )
        wlpmpropdeb.CorrCodeKind = MT100.SndCorresp.CodeKind;
        wlpmpropdeb.CorrCode     = MT100.SndCorresp.CodeBank;
        wlpmpropdeb.CorrID       = MT100.SndCorresp.PartyID;
     end;
  end;

  if ( MT100.SndCorresp.IsSet() OR
       MT100.InterReimbur.IsSet() OR
       MT100.RcvCorresp.IsSet() )
     if ( (not MT100.OrdBank.IsSet()) AND (not MT100.Instructor.IsSet()) )
        wlpmpropdeb.InstructionAbonent = INS_ABONENT_BANK;
     elif ( (not MT100.OrdBank.IsSet()) OR (not MT100.Instructor.IsSet()) )
       wlpmpropdeb.InstructionAbonent = INS_ABONENT_CORR;
     end;
  end;

  if ( MT100.RcvCorresp.IsSet() )
     wlpmpropdeb.OurCorrCodeKind = MT100.RcvCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT100.RcvCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT100.RcvCorresp.PartyID;
  elif ( (MT100.SndCorresp.IsSet()) AND 
         (not MT100.RcvCorresp.IsSet()) AND
         (MT100.SndCorresp.PartyID!=wlpmpropdeb.CorrID) )
     wlpmpropdeb.OurCorrCodeKind = MT100.SndCorresp.CodeKind;
     wlpmpropdeb.OurCorrCode     = MT100.SndCorresp.CodeBank;
     wlpmpropdeb.OurCorrID       = MT100.SndCorresp.PartyID;
  end;

  /* Если в 53 указан только счет, считаем это явным заданием корсчета */
  if( MT100.DirectAcc != "" )
    wlpmpropdeb.OurCorrAcc   = MT100.DirectAcc;
    wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
    wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = MT100.TRF;
  wlpmrmprop.PayDate               = MT100.ValueDate;
  wlpmrmprop.Date                  = MT100.ValueDate;
  wlpmrmprop.ClientDate            = MT100.ValueDate;
  if ( MT100.OrdBank.IsSet() )
     wlpmrmprop.PayerBankName         = MT100.OrdBank.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT100.OrdBank.Account;
     if ( MT100.Instructor.IsSet() )
        wlpmrmprop.PayerCorrBankName     = MT100.Instructor.Name;
     else
        wlpmrmprop.PayerCorrBankName     = MT100.Sender.Name;     
     end;
  else
     wlpmrmprop.PayerBankName         = MT100.Sender.Name;
     wlpmrmprop.PayerCorrAccNostro    = MT100.Sender.Account;
  end;
  wlpmrmprop.PayerName             = MT100.OrdCustomer.Name;
  wlpmrmprop.PayerINN              = MT100.OrdCustomer.INN;
  wlpmrmprop.ReceiverName          = MT100.BnfCustomer.Name;
  wlpmrmprop.ReceiverINN           = MT100.BnfCustomer.INN;
  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT100.RcvInfo.REC;
  wlpmrmprop.Ground                = MT100.BnfInfo.Text;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "01";

  wlpmrmprop.ComissCharges         = MT100.ComissCharges;

  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo       = MT100.RcvInfo.Str;

  /* Кредитовые свойства платежа */
  if ( wlpmpaym.ValueDate==date(0,0,0) )
     wlpmpropcred.TransferDate = {curdate};
  else
     wlpmpropcred.TransferDate = wlpmpaym.ValueDate;
  end;
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;

  SetReceiverBankForInPaymentBy57a( wlmes, MT100.AccBank, wlpmpaym, wlpmpropcred, wlpmrmprop );
  
  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, "", NULL, false, wlmes.TpSchemID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.OrdBank.GetRoute() );
  /* Узел Инструктирующего банка - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.Instructor.GetRoute() );
  /* Узел Отправителя - A */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.Sender.GetRoute(), true );
  /* Узел Корреспондента Отправителя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.SndCorresp.GetRoute() );
  /* Узел Рамбурсирующего Посредника - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.InterReimbur.GetRoute() );
  /* Узел Корреспондента Получателя - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT100.RcvCorresp.GetRoute() );
  /* Узел Посредника - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT100.Intermediary.GetRoute() );
  /* Узел Банка Бенефициара - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT100.AccBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;
  
  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;