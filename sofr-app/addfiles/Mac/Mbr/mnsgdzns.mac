/*
  $Name:         mnsgdzns.mac
  $Module:       МБР
  $Description:  Генерация запросов по сообщению ZNS
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация запросов по сообщению ZNS                                      */
/*                                                                          */
/*  Имя файла: mnsgdzns.mac                                                 */
/*  Создан:    26.01.11                                  Popova O.          */
/****************************************************************************/

import mnsznsal, CTInter, "bnk_common.mac";

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );
  var error = 0, ReqID = -1, i = 0;
  var Accounts:TArray = TArray();

  ВидСправки  = "";
  ВидЗапроса  = "";
  Счет = TArray();
  var Message = MnsMessageFormZNS();

  PrintLog(2,"Генерация запроса по ZNS");

  ClearRecord(wlreq);

  /* Заполнение учетных буферов */
  wlreq.Kind                     = MESKIND_REQUEST;
  wlreq.SubKind                  = Message.GetSubKind();
  wlreq.Trn                      = НомерЗапроса;
  wlreq.PmDateValue              = date( int( substr( ДатаЗапроса, 1, 2 ), int( substr( ДатаЗапроса, 4, 2 ), int( substr( ДатаЗапроса, 7 ) ) ) ) );
  wlreq.RelatedRef               = wlmes.Trn;
  wlreq.OriginatorID             = ПолучитьКодСубъекта( КодНО, PTCK_MNS, error );
  wlreq.OriginatorCodeKind       = PTCK_MNS;
  wlreq.OriginatorCode           = КодНО;
  wlreq.OriginatorName           = НаимНО; 
  wlreq.RecipientID              = wlmes.InsideAbonentID;
  wlreq.RecipientCodeKind        = wlmes.InsideAbonentCodeKind;
  wlreq.RecipientCode            = wlmes.InsideAbonentCode;
  var rs = execSQLselect("select p.t_Name from dparty_dbt p where p.t_PartyID = " + wlregdec.RecipientID);
  if (rs and rs.MoveNext())
    wlreq.RecipientName            = rs.value(0);
  end;
  wlreq.Corschem                 = -1;
  wlreq.FIID                     = -1;
  wlreq.Queries                  = IfThenElse( ТипЗапроса == "1", "по всем счетам", "" );

  if( not ВставитьЗапрос( wlreq, Message.GetNarrative(), "", NULL, ReqID ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  /* Заполнение связанных счетов */
  if( ReqID > 0 )
    if( ТипЗапроса == "1" )
      Accounts = ВыбратьСчетаКлиента( GetLegalPersonIDByINN(ИНННП) );
      while( i < Accounts.Size )
        ClearRecord( wlacclnk );
        wlacclnk.ObjectID      = ReqID;
        wlacclnk.ObjectType    = OBJTYPE_REQ;
        wlacclnk.Account       = Accounts[i].rec.Account;
        wlacclnk.FIID          = Accounts[i].rec.Code_Currency;
        wlacclnk.Chapter       = 1;
        if( not ВставитьСвязанныйСчет( wlacclnk ) )
          std.msg("Ошибка при вставке связанного счета");
        end;
        i = i + 1;
      end;
    else
      while( i < Счет.Size )
        ClearRecord( wlacclnk );
        wlacclnk.ObjectID      = ReqID;
        wlacclnk.ObjectType    = OBJTYPE_REQ;
        wlacclnk.Account       = Счет[i];
        wlacclnk.FIID          = Bnk_GetAccountFIID( Счет[i] );
        wlacclnk.Chapter       = 1;
        if( not ВставитьСвязанныйСчет( wlacclnk ) )
          std.msg("Ошибка при вставке связанного счета");
        end;
        i = i + 1;
      end;
    end;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;