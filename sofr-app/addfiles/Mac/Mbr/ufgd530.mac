/*
  $Name:         ufgd530.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация УО по сообщению ED530 транспорта УФЭБС
*/

import "ufgendoc.mac";

private macro GetPtCodeID( OriginatorCode: string )
  var PartyID : integer = -1;
  
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t_partyid FROM dpartcode_dbt WHERE t_Code like '" + OriginatorCode + "' AND t_Codekind = 47;";

  rs = RsdRecordset( strSql );

  if( rs and rs.MoveNext() )
    PartyID = int(rs.value( 0 ));
  end;
    
  return PartyID;      
end;

macro GenDoc( addrMes )

  SetBuff( wlmes, addrMes );
  ClearRecord( wlinfo );
  
  PrintLog( 2, "Генерация информационного сообшения по ED530" );
  
  wlinfo.TRN = wlmes.Trn;
  wlinfo.RelatedRef = wlmes.RelatedRef;
  wlinfo.Direct = "X" /*WLD_MES_IN*/;

  var fieldname : string = "", fieldvalue : string = "", blockname : string = "";
  var Narrative : string = "";

  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

    if ( (fieldname == "EDAuthor") and (blockname == "") )
      wlinfo.OriginatorCode = fieldvalue;
      wlinfo.OriginatorCodeKind = PTCK_UIS;
      wlinfo.OriginatorID = GetPtCodeID( wlinfo.OriginatorCode );
      wlinfo.OriginatorName = Bnk_GetPartyName( wlinfo.OriginatorID );
    end;

    if ( (fieldname == "EDReceiver") and (blockname == "") )
      var EDReceiver = fieldvalue;
      if( EDReceiver != "" )
        wlinfo.RecipientCode = EDReceiver;
      end;
      wlinfo.RecipientCodeKind = PTCK_UIS;
      wlinfo.RecipientID = GetPtCodeID( wlinfo.RecipientCode );
      wlinfo.RecipientName = Bnk_GetPartyName( wlinfo.RecipientID );
      wlinfo.OfficeID = wlinfo.RecipientID;
      wlinfo.OfficeCodeKind = wlinfo.RecipientCodeKind;
      wlinfo.OfficeCode = wlinfo.RecipientCode;
    end;

    if ( (fieldname == "CorrectionDescriptio") and (blockname == "CorrectionDescriptio") )
      Narrative = fieldvalue;
    end;

  end;

  if( not ВставитьИнфСообщение( wlinfo, Narrative ) )
    std.msg("Ошибка при вводе информационного сообщения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
