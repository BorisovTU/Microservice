/*
  $Name:         ufgm101.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED101 транспорта УФЭБС
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED101 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm101.mac                                                  */
/*  Создан:    29.09.04                                       Бабин А.П.    */
/****************************************************************************/

import BankInter, OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "cb_sql.mac", 
       oralib, likepy, pm_tools, "pmcarfun.mac", "pmlib.mac", "pmbudg_lib.mac",
       pm_const, "pmtax.mac", "wltools.mac", pm_common;

FILE wl_psdem  (pspaydem) key 0;
FILE wl_payord (pspayord) key 0;
FILE wl_pmdemand(pmdemand) key 0;
FILE dp_dep (dp_dep) key 0;
FILE bankdprt (bankdprt) key 0;

/*  Отдельные реквизиты ED103 не заполняются, начиная с версии Альбома УФЭБС 2.4.0 вплоть до особого распоряжения Банка России*/
private const flag_2_4_0 = true;

const ED101 = "ED101",
      ED102 = "ED102",
      ED103 = "ED103",
      ED104 = "ED104",
      ED105 = "ED105",
      ED110 = "ED110";

const ED101v3 = 444,
      ED103v3 = 445,
      ED104v3 = 446,
      ED105v3 = 447;

macro GetDateAkkr( AkkrDate:date )
   var DateAkkrStr = "";
   if ( AkkrDate!=date(0,0,0) )
      DateAkkrStr = YYYYMMDD(AkkrDate);
   end;
   return DateAkkrStr;
end;

/* Вид аккредитива - признак покрытия/депонирования. 
   (1 - покрытый/депонированный, 2 - непокрытый/гарантированный).
   В системе pmakkr.Type принимает значения:
   Н - Безотз. непокр.   
   Б - Безотз. покрытый  
   П - Отзывный непокр.  
   О - Отзывный покрытый */
macro GetReimbursement( AkkrType:string )
   if ( (AkkrType=="Н") OR (AkkrType=="П") )
      return 2;
   elif ( (AkkrType=="Б") OR (AkkrType=="О") )
      return 1;
   end;
      
   return 0;
end;

/* Вид аккредитива. (1 - отзывный, 2 - безотзывный). 
   В системе pmakkr.Type принимает значения:
   Н - Безотз. непокр.   
   Б - Безотз. покрытый  
   П - Отзывный непокр.  
   О - Отзывный покрытый */
macro GetCreditForm( AkkrType:string )
   if ( (AkkrType =="Н") OR (AkkrType =="Б") )
      return 2;
   elif((AkkrType =="П") OR (AkkrType =="О") )
      return 1;
   end;

   return 0; 
end;

macro GetAcceptSum()
   if ( wl_psdem.ReqSum==wlpmpaym.PayAmount )
      return 0;
   else
      return wl_psdem.ReqSum;
   end;
end;

macro GenMes( addrMes, addrPm, type, ver_st )
  /*var xml:object = ActiveX("MSXML.DOMDocument");*/
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem:object, subElem:Object;
  var InitXml:object;
  var FlagTrans, IsPacket:bool;
  var DKFlag, БИКБанкаПлательщика, СсылкаНаБанк, Error, MFO_9, КорсчетБанкаПлательщика, PayerName;
  var ShifrOperation, НомерДокумента, PAYERCHARGEOFFDATE;
  var PayerINN, ReceiverINN, PayerKPP, ReceiverKPP, БИКБанкаПолучателя, IsDepartmentalInfo = false;
  var СчетБанкаПолучателя = "", oldKey, IDStr, field_value, TagPayer, TagPayee ;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  var RsPaym: RsbPayment = RsbPayment( wlpmpaym.PaymentID );
  var rs:RsdRecordset;  

  ClearRecord(wl_psdem);

  if ( RsPaym.TaxAuthorState!="" ) 
    IsDepartmentalInfo = true;
  end;

  var IsZeroFill : bool = ( InList(type, ED101, ED104, ED105) and
                            IsBudgetPaymWithCheckAcc( RsPaym.TaxAuthorState, 
                                                      RsPaym.ReceiverAccount, 
                                                      RsPaym.PayType )
                          );


  if( type == ED110 )
      TagPayer = "PayerBrf";
      TagPayee = "PayeeBrf";
  else
      TagPayer = "Payer";
      TagPayee = "Payee";
  end;


  if ( type==ED102 ) 
     if ( RsPaym.ShifrOper != "08" )
        RunError( "|Данный ПД не является аккредитивом" );
     end;
  end;

  if( type == ED103 )
    if( RsPaym.DocKind != DLDOC_BANKCLAIM ) 
     wl_psdem.OrderID=RsPaym.DocumentID;
     if ( not getEQ(wl_psdem) )
        RunError( "|Не найдена запись в таблице требований" );
     end;
   else
     wl_pmdemand.PaymentID = RsPaym.DocumentID;
     if ( not getEQ(wl_pmdemand) )
        RunError( "|Не найдена запись в таблице требований" );
     end;
   end;
  end;

  if ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  elif ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  else
     DKFlag = -1;
  end;
  FlagTrans = IsTransitPayment(RsPaym);      

  if( DKFlag == -1 ) /* документ не внешний */
    RunError( "|Платеж с ID: " + string( RsPaym.PaymentID ) + "не внешний" );
  end ;

  PayerINN    = RemoveKPP(RsPaym.PayerINN);
  ReceiverINN = RemoveKPP(RsPaym.ReceiverINN);
  PayerKPP    = RemoveINN(RsPaym.PayerINN);
  ReceiverKPP = RemoveINN(RsPaym.ReceiverINN);

  PayerINN    = Str0(PayerINN,    IsZeroFill);
  PayerKPP    = Str0(PayerKPP,    IsZeroFill);
  ReceiverINN = Str0(ReceiverINN, IsZeroFill);
  ReceiverKPP = Str0(ReceiverKPP, IsZeroFill);

  dp_dep.Code = RsPaym.EndDepartment;
  if( not GetEQ( dp_dep ))
      RunError( "|Не найден конечный филиал платежа." );
  end;
  БИКБанкаПлательщика = ПолучитьКодСубъекта( dp_dep.PartyID, PTCK_BIC, Error, 1 );
  if(Error)
    RunError( "|Не найден БИК банка плательщика." );
  end;
  bankdprt.PartyID = dp_dep.PartyID;
  if( not GetEQ( bankdprt ))
      RunError( "|Не найден банк плательщика в справочнике." );
  end;
  КорсчетБанкаПлательщика = bankdprt.CorAcc;

  if( DKFlag == WL_CREDIT )

    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.ReceiverBankCode, RsPaym.ReceiverBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
        //if( ((not IsAbonentPUG(DKFlag)) OR (not IsAbonentMoskowRegion(DKFlag))) AND 
        //    (substr( MFO_9, strlen( MFO_9 )-2, 3 ) == "002") AND
        //    (MFO_9 != "044501002") AND (MFO_9 != "044536002") AND (MFO_9 != "044537002") )
        //  RunError( "|Получатель (БИК = " + RsPaym.ReceiverBankCode + ") является учреждением ЦБ РФ" );
        //end;
        БИКБанкаПолучателя = MFO_9;
      else
        RunError( "|Не найден БИК получателя" );
      end;
    else
      RunError( "|Не найдена ссылка на банк получателя" );
    end;
  else
    БИКБанкаПолучателя = Свой_МФО;

    СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.PayerBankCode, RsPaym.PayerBankCodeKind, Error ) ;
    if( Error == 0 )
      MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
      if( Error == 0 )
      else
        RunError( "|Не найден БИК банка плательщика" );
      end ;
    else
      RunError( "|Не найдена ссылка на банк плательщика" );
    end ;
  end;

  if ( (not Общий_Регион(DKFlag)) or (not IsAbonentPUG(DKFlag)) or (not IsAbonentMoskowRegion(DKFlag)) )
    if( DKFlag == WL_CREDIT )
      if ( (substr(MFO_9,7,3)!="000") AND (substr(MFO_9,7,3)!="001") AND (substr(MFO_9,7,3)!="002") )
         СчетБанкаПолучателя = RsPaym.ReceiverCorrAccNostro;
      end;
    else
      if ( (substr(Свой_МФО,7,3)!="000") AND (substr(Свой_МФО,7,3)!="001") AND (substr(Свой_МФО,7,3)!="002") )
         СчетБанкаПолучателя = Свой_Корсчет;
      end;
    end;
  end;

  ShifrOperation = int( substr( RsPaym.ShifrOper, 1, 2) ) ;

  НомерДокумента = int(GetLastSymbols( RsPaym.Number, PM_DOCNO_NONZERO_LEN )) ;

  if( НомерДокумента  ==  0 )
    /* Если необходимо, чтобы при нечисловом номере документа номер
    выгруженного становился нулевым автоматически -
    уберите следующие строки */

    if( RsbGetTrue( True, False, "Номер документа " + RsPaym.Number + " не числовой. Выгрузить документ с N 0 ?" ) == False )
      RunError( "|Нечисловой номер документа" );
    end;
  end;    

  mes = xml.createElement(type);

  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;

  var ed_nda = NULL;
  
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  mes.setAttribute("EDNo",    ed_nda.EDNo );
  mes.setAttribute("EDDate",  YYYYMMDD(ed_nda.EDDate) );
  mes.setAttribute("EDAuthor",ed_nda.EDAuthor );

  var PaytKind : string = "";
  if( (type == ED110) and (ver_st >= 2016) )
    PaytKind = "";
  else
    PaytKind = GetPaytKind(type, RsPaym.PaymentKind, wlmes.TpSchemID);
  end;
  if(PaytKind)
    mes.setAttribute("PaytKind", PaytKind);
  end;
  
  mes.setAttribute("Sum", MoneyToStrUFBS(RsPaym.ReceiverAmount) );
  
  if( (InList(type, ED101, ED103, ED104, ED105) and (ver_st == 3) ))
    // Приоритет платежа
    if(RsPaym.GetPMRMPROP().rec.Precedence != "")
      mes.setAttribute("PaymentPrecedence", RsPaym.GetPMRMPROP().rec.Precedence);
    end;

    // Запрошенная (требуемая) дата исполнения распоряжения
    var ReqSettlementDate : date = GetReqSettlementDate
      ( RsPaym.OutTransferDate, 
        ed_nda.EDDate,
        RsPaym.PaymentKind );

    if(ReqSettlementDate != date(0,0,0))
      mes.setAttribute("ReqSettlementDate", YYYYMMDD(ReqSettlementDate));
    end;
  end;
  
  if( (type == ED101) and (ver_st == 2) )
    mes.setAttribute("TransKind", "01");
  else
    mes.setAttribute("TransKind", RsPaym.ShifrOper); 
  end;
  if ( (type==ED102) )
     if ( RsPaym.AkkrAccRealReceiver!="" )
        mes.setAttribute("BeneficiaryAccNo", TransTextField(RsPaym.AkkrAccRealReceiver) );
     else
        RunError( "|Не заполнен счет получателя аккредитива" );
     end;
  end;

  if ( type==ED103 )
     var PaytCondition : string = GetPaytCondition( IfThenElse( RsPaym.DocKind != DLDOC_BANKCLAIM, wl_psdem.AcceptTerm, wl_pmdemand.AcceptTerm ) );
     if ( PaytCondition )
        mes.setAttribute("PaytCondition", PaytCondition );
     else
        RunError( "|Не найдено условие оплаты" );
     end;
  end;

  if ( type==ED103 )
     if( ( RsPaym.DocKind != DLDOC_BANKCLAIM ) and ( wl_psdem.acceptTerm == 0 ) )
        mes.setAttribute("AcptTerm", GetAcceptPeriod(wl_psdem) );
     elif( ( RsPaym.DocKind == DLDOC_BANKCLAIM ) and ( wl_pmdemand.acceptTerm == 0 ) )
        mes.setAttribute("AcptTerm", "5" );
     end;
  //#147965. Атрибут временно не заполняется
  //   if ( GetAcceptSum() )
  //      mes.setAttribute("AcptSum", double(GetAcceptSum())*100 );
  //   end;
  end;

  /* Аккредитивы */
  if ( type==ED102 )     

     /* Номер счета по учету аккредитивов к оплате (40901). 
        Указывается в случае осуществления операции по покрытому (депонированному) аккредитиву. 
        В случае непокрытого (гарантированного) аккредитива атрибут не заполняется */
     if ( GetReimbursement(RsPaym.AkkrType) == 1 )
        if( RsPaym.ReceiverAccount!="" )
           mes.setAttribute("AcptAccNo", TransTextField(RsPaym.ReceiverAccount) );
        else
           RunError( "|Не задан счет по учету аккредитива к оплате (40901)" );
        end;
     end;

     if ( GetDateAkkr(RsPaym.AkkrDate)!="" )
        mes.setAttribute( "ExpiryDate", GetDateAkkr(RsPaym.AkkrDate) );
     else
        RunError( "|Не определен срок действия аккредитива" );
     end;

     if ( GetReimbursement(RsPaym.AkkrType) )
        mes.setAttribute( "DocCreditReimbursement", GetReimbursement(RsPaym.AkkrType) );
     else
        RunError( "|Невозможно определить тип аккредитива (покрытый/непокрытый)" );
     end;

     if ( GetCreditForm(RsPaym.AkkrType) )
        mes.setAttribute( "DocCreditForm", GetCreditForm(RsPaym.AkkrType) );
     else
        RunError( "|Невозможно определить тип аккредитива (отзывный/безотзывный)" );
     end;

  end;         

  if ( (type==ED101) OR (type==ED103) OR (type==ED104) )
     if ( not IsBankAccount( RsPaym.PayerGroup, RsPaym.Payer, RsPaym.PayerAccount ) )
        PayerChargeOffDate = PM_GetPayerChargeOffDate(RsPaym.PaymentID);
        if( PayerChargeOffDate == date( 0, 0, 0 ) )
          mes.setAttribute("ChargeOffDate", YYYYMMDD({curdate}) );
        else
          mes.setAttribute("ChargeOffDate", YYYYMMDD(PayerChargeOffDate) );
        end ;
     end;
  end;

  if ( (type==ED103) and (RsPaym.DocDispatchDate!=date(0,0,0)) )
     mes.setAttribute("DocDispatchDate", YYYYMMDD(RsPaym.DocDispatchDate) );
  end;

  if ( (type==ED103) OR (type==ED104) )
     if ( (not IsBankAccount( RsPaym.PayerGroup, RsPaym.Payer, RsPaym.PayerAccount )) and (RsPaym.ReceiverBankMarkDate!=date(0,0,0)) )
         mes.setAttribute("ReceiptDateCollectBank", YYYYMMDD(RsPaym.ReceiverBankMarkDate) );
     end;
  end;

  if ( (type==ED103) and ( ( (RsPaym.DocKind != DLDOC_BANKCLAIM) and (wl_psdem.acceptTerm == 0) and (wl_psdem.AcceptDate != date(0,0,0) ) )
                     or ( (RsPaym.DocKind == DLDOC_BANKCLAIM ) and ( wl_pmdemand.acceptTerm == 0) and (wl_pmdemand.AcceptDate != date(0,0,0) ) ) ) )
      mes.setAttribute("MaturityDate", YYYYMMDD(IfThenElse( RsPaym.DocKind != DLDOC_BANKCLAIM, wl_psdem.AcceptDate, wl_pmdemand.AcceptDate ) ) );
  end;

  if ( (type==ED101) OR (type==ED103) OR (type==ED104)  )
     /* Дата поступления расчетного документа в банк плательщика */
     if ( not IsBankAccount( RsPaym.PayerGroup, RsPaym.Payer, RsPaym.PayerAccount ) )
       if( RsPaym.PayerChargeOffDate != date(0,0,0) )
         mes.setAttribute( "ReceiptDate", YYYYMMDD( IfThenElse( RsPaym.PayerBankEnterDate != date(0,0,0), RsPaym.PayerBankEnterDate, RsPaym.PayerChargeOffDate ) ) );
       end;
     end;  
  end;

  if ( (type==ED101) OR (type==ED103) OR (type==ED104) )
     /* Дата помещения в картотеку */
     if( not IsBankAccount( RsPaym.PayerGroup, RsPaym.Payer, RsPaym.PayerAccount ) )
       if( ((RsPaym.PayerBankEnterDate != date(0,0,0)) or (RsPaym.PayerChargeOffDate != date(0,0,0))) and (wlpmpaym.I2PlaceDate != date(0,0,0)) )  
          mes.setAttribute("FileDate", YYYYMMDD( RsPaym.I2PlaceDate ) );
       end;
     end;       
  end;

  if ( type!=ED102 )
     mes.setAttribute("Priority", RsPaym.Priority );  
  end;

  /* Поле "Содержание операции" (TransContent) для ED105 (платежный ордер) должно 
    содержать строку "Частичная оплата". */
  if( (type==ED105) AND (RsPaym.DocKind==PS_PAYORDER) )
    var tmp = RsPaym.GetPM_PAYM().rec.ContentOperation;
    if (tmp != "")
      mes.setAttribute("TransContent", GetContentOperationString(tmp, RsPaym.GetPM_PAYM()) );
    end;
  end;
  
  var Err;
  var SystemCode;
  if( GetSystemCodeForGenMes( type, wlmes, @Err, @SystemCode ) )
    if( strlen(Err) != 0 )
      RunError(Err);
    else
      mes.setAttribute("SystemCode",  SystemCode );
    end;
  end;


  if(type != ED110)
    var UIN : string = RsPaym.GetPMRMPROP().rec.UIN;
    if(UIN)
      mes.setAttribute("PaymentID", UIN);
    end;
  end;

  elem = xml.createElement("AccDoc");
  elem.setAttribute("AccDocNo", НомерДокумента );
  elem.setAttribute("AccDocDate", YYYYMMDD(RsPaym.Date) );
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  elem = xml.createElement(TagPayer);
  if ( PayerINN!="" )
     elem.setAttribute("INN", PayerINN );
  end;
  if ( (type==ED101) OR (type==ED104) OR (type==ED105) )
     if ( PayerKPP!="" )
        elem.setAttribute("KPP", PayerKPP );
     end;
  end;

  if( (RsPaym.DocKind == DLDOC_BANKCLAIM) or (RsPaym.PayerBankCode == БИКБанкаПлательщика) )
    if ( trim(RsPaym.PayerAccount)!="" )
       elem.setAttribute("PersonalAcc", RsPaym.PayerAccount );
    end;
    if ( type!=ED110 )
       subElem = xml.createElement("Name");
       if ( trim(RsPaym.PayerName)=="" )
          RunError( "|Не заполнено наименование плательщика" );
       end;
       subElem.appendChild(xml.createTextNode( TransTextField(substr(RsPaym.PayerName,1,160)) ));
       elem.appendChild(subElem);
    end;
  else
    if(ПолучитьВсеПроводки(RsPaym.PaymentID,@rs) and rs.moveLast())
      if( rs.value("t_ReceiverAccount") == RsPaym.FutureReceiverAccount )
        elem.setAttribute("PersonalAcc", rs.value("t_PayerAccount") );
      else
        elem.setAttribute("PersonalAcc", RsPaym.FuturePayerAccount );
      end;
    end;  
    if ( type!=ED110 )
       subElem = xml.createElement("Name");
       PayerName = RsPaym.PayerName;
       if((RsPaym.DocKind != DLDOC_BANKCLAIM) and (trim(RsPaym.PayerAccount)!="") )
         PayerName = PayerName  + " сч. " + RsPaym.PayerAccount;
       end;
       if((RsPaym.DocKind != DLDOC_BANKCLAIM) and (trim(RsPaym.PayerBankName)!="") )
         PayerName = PayerName  + ", " + RsPaym.PayerBankName;
       end;

       subElem.appendChild(xml.createTextNode( TransTextField(substr(PayerName,1,160)) ));
       elem.appendChild(subElem);
    end;

  end;

  subElem = xml.createElement("Bank");

  if( RsPaym.DocKind == DLDOC_BANKCLAIM )
    БИКБанкаПлательщика = ПолучитьКодСубъекта( RsPaym.PayerBankID, PTCK_BIC, Error, 1 );

    if(Error)
      RunError( "|Не найден БИК банка плательщика." );
    end;

    КорсчетБанкаПлательщика = RsPaym.PayerCorrAccNostro;
  end;

  subElem.setAttribute("BIC", БИКБанкаПлательщика );
  subElem.setAttribute("CorrespAcc", КорсчетБанкаПлательщика );   

  subElem.appendChild(xml.createTextNode("" ));
  elem.appendChild(subElem);
  mes.appendChild(elem);

  elem = xml.createElement(TagPayee);
  if ( ReceiverINN!="" )
     elem.setAttribute("INN", ReceiverINN );
  end;
  if ( (type==ED101) OR (type==ED104) OR (type==ED105) )
     if ( ReceiverKPP!="" )
        elem.setAttribute("KPP", ReceiverKPP );
     end;
  end;

  if ( type!=ED102 )      

     var PersonalAccount = RsPaym.ReceiverAccount;    

     if( RsPaym.DocKind == DLDOC_BANKCLAIM )     

      var query_receiver_account = 
      "  SELECT carries.t_ReceiverAccount " +
      "    FROM dpmpaym_dbt    pmpaym, " +
      "         dpmprop_dbt    pmprop_out, " +
      "         dcorschem_dbt  cors, " +
      "         ddp_dep_dbt    dep, " +
      "         TABLE (PM_CARFUN.GetPaymentCarries (:PaymentID1))  carries "
      "   WHERE pmpaym.t_PaymentID = :PaymentID2 " +
      "         AND pmprop_out.t_PaymentID = pmpaym.t_PaymentID " +
      "         AND pmprop_out.t_Group = :PAYMENTS_GROUP_EXTERNAL " +
      "         AND pmprop_out.t_IsSender = CHR(0) " +
      "         AND cors.t_Number = pmprop_out.t_Corschem " +
      "         AND cors.t_FIID = pmprop_out.t_PayFIID " +
      "         AND cors.t_FI_Kind = :FIKIND_CURRENCY " +
      "         AND dep.t_Code = cors.t_Department " +
      "         AND pmpaym.t_ReceiverBankID != dep.t_PartyID " +
      "         AND carries.t_AUTOKEY = (SELECT MAX( t_AutoKey ) FROM TABLE( PM_CARFUN.GETPAYMENTCARRIES( :PaymentID3 ))) " +
      "         AND carries.t_PayerAccount = cors.t_Account ";

      var rs_receiver_account = execSQLselect( query_receiver_account, makeArray( SQLParam( "PaymentID1", RsPaym.PaymentID ),
                                                      SQLParam( "PaymentID2", RsPaym.PaymentID ),
                                                      SQLParam( "PAYMENTS_GROUP_EXTERNAL", PAYMENTS_GROUP_EXTERNAL ),
                                                      SQLParam( "FIKIND_CURRENCY", FIKIND_CURRENCY),
                                                      SQLParam( "PaymentID3", RsPaym.PaymentID ) ) );

       if( (rs_receiver_account and rs_receiver_account.MoveNext()) and  (rs_receiver_account.value(0) != "") )
         PersonalAccount = rs_receiver_account.value(0);
       end;     
     end;

     elem.setAttribute("PersonalAcc", PersonalAccount );
  end;

  if ( type!=ED110 )
     subElem = xml.createElement("Name");
     if ( trim(RsPaym.ReceiverName)=="" )
        RunError( "|Не заполнено наименование получателя" );
     end;
     subElem.appendChild(xml.createTextNode( TransTextField(substr(RsPaym.ReceiverName,1,160)) ));
     elem.appendChild(subElem);
  end;
  subElem = xml.createElement("Bank");

  if( RsPaym.DocKind == DLDOC_BANKCLAIM )
    
   var query_receiver_bank_code =
   "     SELECT partcode.t_Code " +
   "       FROM dpartcode_dbt  partcode, " +
   "            dpmprop_dbt    pmprop_out, " +
   "            dcorschem_dbt  cors, " +
   "            ddp_dep_dbt    dep " +
   "     WHERE     partcode.t_PartyID = dep.t_PartyID " +
   "           AND partcode.t_codeKind = :PTCK_BIC " +
   "           AND partcode.t_State = 0 " +
   "           AND pmprop_out.t_PaymentID = :PaymentID " +
   "           AND pmprop_out.t_Group = :PAYMENTS_GROUP_EXTERNAL " +
   "           AND pmprop_out.t_IsSender = CHR (0) " +
   "           AND cors.t_Number = pmprop_out.t_Corschem " +
   "           AND cors.t_FIID = pmprop_out.t_PayFIID " +
   "           AND cors.t_FI_Kind = :FIKIND_CURRENCY " +
   "           AND dep.t_Code = cors.t_Department ";

   var rs_receiver_bank_code = execSQLselect( query_receiver_bank_code, makeArray( SQLParam( "PTCK_BIC", PTCK_BIC ),
                                                      SQLParam( "PaymentID", RsPaym.PaymentID ),
                                                      SQLParam( "PAYMENTS_GROUP_EXTERNAL", PAYMENTS_GROUP_EXTERNAL ),
                                                      SQLParam( "FIKIND_CURRENCY", FIKIND_CURRENCY) ) );


   if( rs_receiver_bank_code and rs_receiver_bank_code.MoveNext() )
     БИКБанкаПолучателя = rs_receiver_bank_code.value(0);                                                        
   else
     RunError( "|Не найден БИК банка получателя." );
   end;

   var query_receiver_corracc =
   "     SELECT bankdprt.t_CorAcc " +
   "       FROM dbankdprt_dbt bankdprt, " +
   "            dpmprop_dbt    pmprop_out, " +
   "            dcorschem_dbt  cors, " +
   "            ddp_dep_dbt    dep " +
   "      WHERE pmprop_out.t_PaymentID = :PaymentID " +
   "            AND pmprop_out.t_Group = :PAYMENTS_GROUP_EXTERNAL " +
   "            AND pmprop_out.t_IsSender = CHR (0) " +
   "            AND cors.t_Number = pmprop_out.t_Corschem " +
   "            AND cors.t_FIID = pmprop_out.t_PayFIID " +
   "            AND cors.t_FI_Kind = :FIKIND_CURRENCY " +
   "            AND dep.t_Code = cors.t_Department " +
   "            AND bankdprt.t_PartyID = dep.t_PartyID ";

   var rs_receiver_corracc = execSQLselect( query_receiver_corracc, makeArray( SQLParam( "PaymentID", RsPaym.PaymentID ),
                                                      SQLParam( "PAYMENTS_GROUP_EXTERNAL", PAYMENTS_GROUP_EXTERNAL ),
                                                      SQLParam( "FIKIND_CURRENCY", FIKIND_CURRENCY) ) );


   if( rs_receiver_corracc and rs_receiver_corracc.MoveNext() )
     СчетБанкаПолучателя = rs_receiver_corracc.value(0);                                                        
   else
     RunError( "|Не найден корсчет банка получателя." );
   end;
  end;

  subElem.setAttribute("BIC", БИКБанкаПолучателя );
  if ( СчетБанкаПолучателя!="" )
     subElem.setAttribute("CorrespAcc", СчетБанкаПолучателя );   
  end;
  subElem.appendChild(xml.createTextNode("" ));
  elem.appendChild(subElem);
  mes.appendChild(elem);

  if ( type==ED102 )
     elem = xml.createElement("GoodsDescription");
     elem.appendChild( xml.createTextNode(substr(TransTextField(RsPaym.Ground),1,260)) );
     mes.appendChild(elem);  

     /*wl_payord.OrderID = wlpmpaym.DocumentID;*/
     field_value = RsPaym.AkkrRepresentation;
     if ( field_value!="" )
        elem = xml.createElement("DocRequired");
        elem.appendChild( xml.createTextNode(substr(TransTextField(field_value),1,170)) );
        mes.appendChild(elem);
     else
        RunError( "|Не заполнено примечание <Платежи по представлению>" );
     end;

     field_value = RsPaym.AkkrAddCondition;
     if ( field_value!="" )
        elem = xml.createElement("AdditionalConditions");
        elem.appendChild( xml.createTextNode(substr(TransTextField(field_value),1,170)) );
        mes.appendChild(elem);       
     end;
  end;

  if ( (type!=ED102) and (type!=ED110) )
     elem = xml.createElement("Purpose");
     elem.appendChild( xml.createTextNode(substr(TransTextField(RsPaym.Ground),1,210)) );
     mes.appendChild(elem);  
  end;

  if ( (type==ED101) OR (type==ED104) OR (type==ED105) )

     if( IsDepartmentalInfo )
        elem = xml.createElement("DepartmentalInfo");
        elem.setAttribute("DrawerStatus",   RsPaym.TaxAuthorState );
        
        if(RsPaym.BttTICode!="")
          elem.setAttribute("CBC", RsPaym.BttTICode );
        else
          if( type != ED104 )
            elem.setAttribute("CBC","0");
          elif(IsBudgetPaymWithCheckAcc("", RsPaym.ReceiverAccount, RsPaym.PayType))
            elem.setAttribute("CBC","0");
          end;
        end;

        if(RsPaym.OKATOCode!="")
          elem.setAttribute("OKATO",          RsPaym.OKATOCode );
        else
          elem.setAttribute("OKATO", "0" );
        end;
        if(RsPaym.TaxPmGround != "")
          elem.setAttribute("PaytReason",     RsPaym.TaxPmGround );
        else
          elem.setAttribute("PaytReason", "0" );
        end;
        if(RsPaym.TaxPmPeriod != "")
          elem.setAttribute("TaxPeriod",      RsPaym.TaxPmPeriod );
        else
          elem.setAttribute("TaxPeriod", "0" );
        end;
        if(RsPaym.TaxPmNumber != "")
          elem.setAttribute("DocNo",          TransTextField(RsPaym.TaxPmNumber) );
        else
          elem.setAttribute("DocNo", "0" );
        end;
        if(RsPaym.TaxPmDate != "")
          elem.setAttribute("DocDate",        RsPaym.TaxPmDate );
        else
          elem.setAttribute("DocDate", "0");
        end;
        if(RsPaym.TaxPmType != "") 
          elem.setAttribute("TaxPaytKind",    RsPaym.TaxPmType );
        end;
        
        elem.appendChild(xml.createTextNode("" ));
        mes.appendChild(elem);
     end;
  end;

  ed_nda = EDNoDateAuthor();

  if ( (type==ED101) OR (type==ED105) )
     if ( ed_nda.ConstructByInitialPmMes_pm(RsPaym.PaymentID) )
        elem = xml.createElement("InitialED");
        elem.setAttribute("EDNo",    ed_nda.EDNo );
        elem.setAttribute("EDDate",  YYYYMMDD(ed_nda.EDDate) );
        elem.setAttribute("EDAuthor",ed_nda.EDAuthor );
        elem.appendChild(xml.createTextNode(""));
        mes.appendChild(elem);
     end;
  end;

  if ( type==ED105 )
     elem = xml.createElement("PartialPayt");
     if ( (RsPaym.DocKind==PS_PAYORDER) AND (RsPaym.PartPaymNumber>0) )
        elem.setAttribute("PaytNo", GetLastSymbols( string(RsPaym.PartPaymNumber), 3 ) );
     end;
     elem.setAttribute("TransKind", substr(RsPaym.PartPaymShifrMain,1,2) );
     /* Сумма остатка платежа. В случае последнего частичного платежа (т.е. сумма остатка равна нулю) 
        не заполняется */
     if ( wlpmpaym.PartPaymRestAmountMain>$0 )
        elem.setAttribute("SumResidualPayt", MoneyToStrUFBS(RsPaym.PartPaymRestAmountMain) );
     end;
     subElem = xml.createElement("AccDoc");     
     if ( (int(GetLastSymbols( RsPaym.PartPaymNumMain, PM_DOCNO_NONZERO_LEN ))!=0) and 
          (RsPaym.PartPaymDateMain!=date(0,0,0)) 
        )
        subElem.setAttribute("AccDocNo", int(GetLastSymbols( RsPaym.PartPaymNumMain, PM_DOCNO_NONZERO_LEN )) );
     else
        RunError( "|Неверные реквизиты исходного документа" );
     end;
     subElem.setAttribute("AccDocDate", YYYYMMDD(RsPaym.PartPaymDateMain) );
     subElem.appendChild(xml.createTextNode(""));
     elem.appendChild(subElem);
     mes.appendChild(elem);
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMes101( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED101, 1 );
end;

macro GenMes102( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED102, 1 );
end;

macro GenMes103( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED103, 1 );
end;

macro GenMes104( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED104, 1 );
end;

macro GenMes105( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED105, 1 );
end;

macro GenMes110( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED110, 1 );
end;

macro GenMes101V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED101, 2 );
end;

macro GenMes102V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED102, 2 );
end;

macro GenMes103V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED103, 2 );
end;

macro GenMes104V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED104, 2 );
end;

macro GenMes105V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED105, 2 );
end;

macro GenMes110V2( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED110, 2 );
end;

macro GenMes110v2016( addrMes, addrPm )
   return GenMes( addrMes, addrPm, ED110, 2016 );
end;

macro GenMes101v3( addrMes, addrPm )
    return GenMes( addrMes, addrPm, ED101, 3);
end;

macro GenMes103v3( addrMes, addrPm )
    return GenMes( addrMes, addrPm, ED103, 3);
end;

macro GenMes104v3( addrMes, addrPm )
    return GenMes( addrMes, addrPm, ED104, 3);
end;

macro GenMes105v3( addrMes, addrPm )
    return GenMes( addrMes, addrPm, ED105, 3);
end;

private macro CheckPaym( addr, err_msg:@string, type):integer

  var params:TArray;
  var rs:RsdRecordset;
  var query:string;
  var err, BIC;
  var DKFlag, НомерДокумента;
  /*var Name:string, FormatFile:integer, MaxExpMes:integer;*/
  
  SetBuff( wlpmpaym,  addr );
  var RsPaym: RsbPayment = RsbPayment( wlpmpaym.PaymentID );

  if ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  elif ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  else
     DKFlag = -1;
  end;

  if( not (( RsPaym.PayerGroup    == PAYMENTS_GROUP_EXTERNAL ) or
           ( RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL )) or (DKFlag == -1))
      err_msg = string("Платеж с ID ",RsPaym.PaymentID," не внешний");
      SetParm( 1, err_msg );
      return 1;                  
  end;

  query = " select objcode.T_OBJECTID " +
          " from dobjcode_dbt objcode " +
          " where objcode.T_CODEKIND = :CodeKind " +
          " and objcode.T_CODE = :Code "
          " and objcode.T_OBJECTTYPE = 3";
  
  if( DKFlag == WL_CREDIT )
     params = makeArray( SQLParam( "CodeKind", RsPaym.ReceiverBankCodeKind ),
                         SQLParam( "Code"    , RsPaym.ReceiverBankCode ) );
     rs = execSQLselect( query, params, true );
     if( not rs.moveNext() )
       err_msg = "Не найдена ссылка на банк получателя";
       SetParm( 1, err_msg );
       return 1;                  
     else
       BIC = ПолучитьКодСубъекта( rs.value(0), PTCK_BIC, err);
       if( err )
         err_msg = "Не найден БИК банка получателя";
         SetParm( 1, err_msg );
         return 1;                  
       end;

       // if( ((not IsAbonentPUG(DKFlag)) OR (not IsAbonentMoskowRegion(DKFlag))) AND 
       //     (substr( BIC, strlen( BIC )-2, 3 ) == "002") AND
       //     ( BIC != "044501002") AND ( BIC != "044536002") AND (BIC != "044537002") )
       //  err_msg = string("Получатель (БИК =",BIC,") является учреждением ЦБ РФ");
       //  SetParm( 1, err_msg );
       //  return 1;                  
       //end;
     end;
  elif(DKFlag = WL_DEBET)
     params = makeArray( SQLParam( "CodeKind", RsPaym.PayerBankCodeKind ),
                         SQLParam( "Code"    , RsPaym.PayerBankCode ) );
     rs = execSQLselect( query, params, true );
     if( not rs.moveNext() )
       err_msg = "Не найдена ссылка на банк плательщика";
       SetParm( 1, err_msg );
       return 1;                  
     else
       BIC = ПолучитьКодСубъекта( rs.value(0), PTCK_BIC, err);
       if( err )
         err_msg = "Не найден БИК банка плательщика";
         SetParm( 1, err_msg );
         return 1;                  
       end;
     end;
  end;   

  НомерДокумента = int(GetLastSymbols( RsPaym.Number, PM_DOCNO_NONZERO_LEN )) ;
  if( НомерДокумента == 0 )
     err_msg = "Хотя бы одна цифра номера документа из последних шести должна отличаться от 0";
     SetParm( 1, err_msg );
     return 1;                  
  end;

  /*if( (ОпределитьФорматТранспорта( TRANSP_UFBS, Name, FormatFile, MaxExpMes ) == 0) ) //!!!!
    err_msg = "Ошибка при определении режима использования пакетов ЭПД/ЭСИД";
    SetParm( 1, err_msg );
    return 1;                  
  end;*/

  if ( type==ED102 ) 
    if ( RsPaym.ShifrOper != "08" )
       err_msg = "|Данный ПД не является аккредитивом" ;
       SetParm( 1, err_msg );
       return 1;                  
    end;
    if ( RsPaym.AkkrAccRealReceiver == "" )
       err_msg = "Не заполнен счет получателя аккредитива" ;
       SetParm( 1, err_msg );
       return 1;                  
    end;

     if ( GetReimbursement(RsPaym.AkkrType) == 1 )
        if( RsPaym.ReceiverAccount == "" )
           err_msg = "Не задан счет по учету аккредитива к оплате (40901)" ;
           SetParm( 1, err_msg );
           return 1;                  
        end;
     end;

     if ( GetDateAkkr(RsPaym.AkkrDate) == "" )
        err_msg = "Не определен срок действия аккредитива" ;
        SetParm( 1, err_msg );
        return 1;                  
     end;

     if ( not GetReimbursement(RsPaym.AkkrType) )
        err_msg = "Невозможно определить тип аккредитива " ;
        SetParm( 1, err_msg );
        return 1;                  
     end;

     if ( RsPaym.AkkrRepresentation == "" )
        err_msg = "Не заполнено примечание <Платежи по представлению>" ;
        SetParm( 1, err_msg );
        return 1;                  
     end;
     if(strlen(RsPaym.Ground) > 260)
       err_msg = "Длина поля 'Назначение платежа' превышает 260 символов"; 
       SetParm( 1, err_msg );
       return 1;                  
     end;
  end;

  if ( type==ED103 ) 
    if( RsPaym.DocKind != DLDOC_BANKCLAIM )
     ClearRecord(wl_psdem);
     wl_psdem.OrderID=RsPaym.DocumentID;
     if ( not getEQ(wl_psdem) )
        err_msg =  "Документ не является клиентским требованием";
        SetParm( 1, err_msg );
        return 1;                  
     end;
    else
       ClearRecord(wl_pmdemand);
       wl_pmdemand.PaymentID = RsPaym.DocumentID;
       if( not getEQ(wl_pmdemand))
         err_msg = "Документ не является требованием банка";
         SetParm( 1, err_msg );
         return 1;
       end;
    end;
  end;

  if( type == ED105 )
    if ( (int(GetLastSymbols( RsPaym.PartPaymNumMain, PM_DOCNO_NONZERO_LEN )) == 0) and 
         (RsPaym.PartPaymDateMain == date(0,0,0)) )

        err_msg = "Неверные реквизиты исходного документа";
        SetParm( 1, err_msg );
        return 1;                  
    end;
  end;


  if ( type!=ED110 )
     if ( trim(RsPaym.PayerName)=="" )
        err_msg = "Не заполнено наименование плательщика" ;
        SetParm( 1, err_msg );
        return 1;                  
      end;
     if ( trim(RsPaym.ReceiverName)=="" )
       err_msg = "Не заполнено наименование получателя"; 
       SetParm( 1, err_msg );
       return 1;                  
     end;

     if(strlen(RsPaym.PayerName) > 160)
       err_msg = "Длина поля 'Наименование плательщика' превышает 160 символов"; 
       SetParm( 1, err_msg );
       return 1;                  
     end;
     if(strlen(RsPaym.ReceiverName) > 160)
       err_msg = "Длина поля 'Наименование получателя' превышает 160 символов"; 
       SetParm( 1, err_msg );
       return 1;                  
     end;

     if(type!=ED102)
       if(strlen(RsPaym.Ground) > 210)
         err_msg = "Длина поля 'Назначение платежа' превышает 210 символов"; 
         SetParm( 1, err_msg );
         return 1;                  
       end;
     end;
     
     var UIN : string = RsPaym.UIN;
     if(UIN)
       CheckUIN(RsPaym.ReceiverAccount, UIN);
     end;
  end;

  if( InList( type, ED101, ED103, ED104, ED105, ED110 ) )
    var MaxPriority = Bnk_GetRegistryValue( "CB\\PAYMENTS\\MAXPRIORITY", V_INTEGER, 0 );
    if( RsPaym.Priority > MaxPriority )
      err_msg = "Недопустимое значение очередности платежа, допускаются значения 1-5 или 0"; 
      SetParm( 1, err_msg );
      return 1;                  
    end;
  end;
  
  if( InList( type, ED101, ED104, ED105 ) and (RsPaym.Date >= date(2, 10, 2017)))         
    if( БанкУБР( RsPaym.ReceiverBankID ) and ( CompareStrWithMasks( Bnk_GetRegistryValue( "COMMON\\CHECKTAXPAYMPARAMS\\УФЭБС_СЧЕТ ПОЛУЧАТЕЛЯ", V_STRING, ""), RsPaym.ReceiverAccount ) == 0 ) 
                                         and ( ( RsPaym.TaxAuthorState == "" ) or ( RsPaym.TaxAuthorState == null ) ) )
      err_msg = "Для банка получателя, являющегося ПБР, и указанного в документе счета получателя статус составителя должен быть обязательно заполнен"; 
      SetParm( 1, err_msg );
      return 1;
    end;
  end;

  if( InList( type, ED101, ED103, ED104, ED105 ) )
    var rlsFormID : integer = 0;
    if( IsDebetPaym( RsPaym ) )
      rlsFormID = RsPaym.GetDEBET().rec.RlsFormID;
    else
      rlsFormID = RsPaym.GetCREDIT().rec.RlsFormID;
    end;
    if( InList( rlsFormID, ED101v3, ED103v3, ED104v3, ED105v3 ) )
      var PaymentPrecedence = RsPaym.GetPMRMPROP().rec.Precedence;
      var PaymentKind = RsPaym.PaymentKind;
      if( not CheckPaymentPrecedence( PaymentPrecedence, PaymentKind ) )
        err_msg = "Некорректное значение приоритета распоряжения. Для срочного сервиса допустимы значения \"13\" или в диапазонах \"50-59\", \"60-69\". Для несрочного сервиса только значения в диапазоне \"70-79\""; 
        SetParm( 1, err_msg );
        return 1;
      end;
    end;
  end;
  
  if( CheckTaxPropForUfebsMes(RsPaym, type, @err_msg) )
    SetParm( 1, err_msg );
    return 1;
  end;
  
OnError(er)
  err_msg = RsbGetError(er);
  SetParm( 1, err_msg );
  return 1;
end;

macro CheckPaym101( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED101);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym101V3( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED101);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym103V3( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED103);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym104V3( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED104);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym105V3( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED105);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym102( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED102);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;

  return true;
end;

macro CheckPaym103( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED103);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym104( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED104);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym105( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED105);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckPaym110( addr )
  var err_msg:string = "", err;
  err = CheckPaym(addr, err_msg, ED110);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

private macro CheckMes( addrMes, err_msg:@string, type ):integer

  
  
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка;
  
  SetBuff( wlmes, addrMes );

  if( not СчитатьПоле( XMLField, Строка ) )
    return 1;
  end;

  if( not xml.loadXML(Строка) )
    err_msg = string( "Неверный формат сообщения по форме " + type );
    SetParm( 1, err_msg );
    return 1;
  end;

  if( not OnlyDigit(ReadOptinalAttribute(xml, "CBC", "DepartmentalInfo")) )
    err_msg = string( "Недопустимое значение атрибута 'CBC'" );
    SetParm( 1, err_msg );
    return 1;
  end;

  return 0;
end;

macro CheckMes101V2( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED101);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes101V3( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED101);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes103V3( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED103);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes104V3( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED104);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes105V3( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED105);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes104V2( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED104);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;

macro CheckMes105V2( addrMes )
  var err_msg:string = "", err;
  err = CheckMes(addrMes, err_msg, ED105);
  if(err)
    MemoryError(err, err_msg);
    return false;
  end;
  return true;
end;
