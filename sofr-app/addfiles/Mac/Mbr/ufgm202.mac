/*
  $Name:         ufgm202.mac
  $Module:       Межбанковские расчеты
  $Description:  описание
*/
/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED202 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm202.mac                                                  */
/*  Создан:    23.06.06                                  Гайворонский Е.Г.  */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac";

var ver_st = 1;

/*macro ВычислитьКод( Строка )
   if((StrLen(Строка) == 1)  And ( Index(Строка, "1") or Index(Строка, "2") or Index(Строка, "3")))
      return true;
   else
      return false;
   end;
end;*/

private macro GetInquiryCode( query )
    
  if( strlen(query) == 0 )
    return "";
  end;
  
  var rs = execSQLSelect( " Select t_Code "
                          "   From dwlcodes_dbt "
                          "  Where t_AlgNum = 41 "
                          "    and t_Code = :Code ",
                          MakeArray( SQLParam( "Code", query )));
  
  if( rs and rs.MoveNext() )
    return rs.value("t_Code");
  else 
    return "";
  end;
  
end;

/* Заполнение полей сообщения-запроса на получение выписки ED210 */
macro GenMes( addrMes, addrReq )

  SetBuff( wlmes,  addrMes );
  SetBuff( wlReq,  addrReq );

  var strXml;
  var InitMesId:integer = 0;
  var IsRequestPacket:bool = false;
  var NoInitMes:bool = false;
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object, elem: object;
  var iXml:object = ActiveX("Microsoft.XMLDOM");  
  
  var rs:object;
  var select:string;
  var params:TArray;
  var ObjID:integer, ObjKind:integer;
  var EDReceiver:string;
  var ErrMsg : string = "";

  mes = xml.createElement("ED202");
  
  if(ver_st == 1)
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v1.1");
  else
     mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  end;

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  

  EDReceiver = GetEDReceiver(wlreq.RecipientID, wlreq.RecipientcodeKind, wlreq.RecipientCode, "", 1);
  if (EDReceiver != "")
      mes.setAttribute("EDReceiver", EDReceiver);
  end;
  
  if( wlreq.RelatedRef == "" )
    RunError("Не заполнена ссылка на исходный ЭПД (пакет ЭПД)");
  end;  

  // Если задана форма сообщения - ищем конкретный ЭПД, иначе ищем пакет ЭПД
  if( wlreq.InitFormIDMes == 0 )
    IsRequestPacket = true;
  end;

  // Ищем пакет ЭПД по журналу сеансов
  if( IsRequestPacket )
    if( not WldFindSessByUID( TRANSP_UFBS, wlreq.InitDateMes, wlreq.RelatedRef ) )
      RunError( String("Не найден исходящий пакет ЭПД|с номером '", wlreq.RelatedRef, "' за дату ", wlreq.InitDateMes) );
    end;
  end;

  /*if(ВычислитьКод( wlreq.Queries ))  
     mes.setAttribute("EDInquiryCode",  wlreq.Queries );
  else
     RunError( String("Задан непонятный запрос по ЭПД: '", wlreq.Queries, "'" ));
  end;*/
  
  var EDInquiryCode = GetInquiryCode( wlreq.Queries );
  
  if( strlen(EDInquiryCode) != 0 )
    mes.setAttribute("EDInquiryCode", EDInquiryCode);
  else
    RunError( "Выберите значение из справочника в поле 'Сообщение'" );
  end;

  elem = xml.createElement("EDRefID");

  if( wlreq.RelatedRef )
    ed_nda = EDNoDateAuthor();
    ed_nda.ConstructByTrn(wlreq.RelatedRef); 

    elem.setAttribute("EDNo",       ed_nda.EDNo );
    elem.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
    elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  
  
  else

    ErrMsg = "Идентификаторы запрашиваемого ЭПС/пакета ЭПС должны быть заполнены. Задайте в запросе ссылку на запрашиваемое ЭС";
    RunError(ErrMsg, ErrMsg);
  end;

  mes.appendChild(elem);
  
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; // Успешное завершение

  OnError( er ) // обработка ошибок
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro GenMesV2( addrMes, addrReq )
  ver_st = 2;
  return GenMes( addrMes, addrReq );
end;
