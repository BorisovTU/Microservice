/*
$Name:          Fnsprapo.mac
$Module:        МБ Расчеты
$Description:   Печать сообщения APO - об отмене приостановления НО
*/

import "wldoc.mac", "fnsprapall.mac", "fnsreadmall.mac";

class ( MessageReader ) PrintAPO
  var НомРеш = "",
      ДатаРеш = "",
      НаимНо = "",
      ДатаОтмРеш = "",
      НомОтмРеш = "",
      
      БанкПл = "",
      БИКБПл = "",
      Фамилия = " ",
      Имя = " ",
      Отчество = "",
      FIO = "",
      ВидПоруч = "",
      ДатаПоруч = "",
      НомПоруч = "",
      ИНННП = "",
      КППНП = "",
      КласЧин = "",
      НаимБ = "",
      
  ListReg = TArray(),
  AccKindList = TArray(),
  AccList = TArray();
      
  macro ВидСч( value )
    AccKindList[AccKindList.size] = value;
  end;
  
  macro НомСчПл( value )
    AccList[AccList.size] = value;
  end;
  
  macro _ConvertBlock_( name )
    if ( name == "Руководитель" )
      FIO = Фамилия + " " + Имя + " " + Отчество;
    end;
    
    if( name == "ВозобнПоруч" )
    var TempStr = "";
      if( ВидПоруч == 1 )
        TempStr = "на списание и перечисление денежных средств со счетов налогоплательщика (плательщика сбора," + 
                 " плательщика страховых взносов, налогового агента) в бюджетную систему Российской Федерации";
      elif( ВидПоруч == 2 )
        TempStr = "на перевод электронных денежных средств со счетов налогоплательщика (плательщика сбора, " +
                 "плательщика страховых взносов, налогового агента) в бюджетную систему Российской Федерации"
      end;
      TempStr = TempStr + " от " + ДатаПоруч + " N " + НомПоруч;
      
      ListReg[ListReg.Size] = TempStr; 
      
    end;
    
    
  end;
  
  macro _Normalize_()
    if (ДатаРеш == "")
      ДатаРеш = DDpMMpYYYY( ToDateYYYY_MM_DD(ДатаРеш) );
    end;
    
    
    
  end;
  
  
end;

macro Draw( APO : PrintAPO, mesId : integer, kind : integer )

  [
                                                        Решение N #
                                                                 ───────
                            об отмене приостановления действия поручений на списание и перечисление
                                денежных средств со счетов налогоплательщика (плательщика сбора, 
                               плательщика страховых взносов, налогового агента), а также на перевод 
                              электронных денежных средств налогоплательщика (плательщика сбора, 
                                 плательщика страховых взносов, налогового агента) в бюджетную 
                                                 систему Российской Федерации

                                                   от "##" ######### #### г.
                                                      ───────────────────
  ]
  (
    APO.НомРеш,
    substr( APO.ДатаРеш, 9, 2),
    Месяца( substr( APO.ДатаРеш, 6, 2 ) ):c,
    substr( APO.ДатаРеш, 1, 4 )    
  );
  
  PrintTaxNameHead( APO );
    
  
  [                                             РЕШИЛ:
  
    Отменить Решение о приостановлении действия поручений на списание и перечисление денежных средств со счетов 
    налогоплательщика (плательщика сбора, плательщика страховых взносов, налогового агента), а также на перевод
    электронных денежных средств налогоплательщика (плательщика сбора, плательщика страховых взносов, 
    налогового агента) в бюджетную систему Российской Федерации    
    от "##" ######### #### г. N #
    ─────────────────────────────────────────────────────────────
  ]
  (
    substr( APO.ДатаРеш, 9, 2),
    Месяца( substr( APO.ДатаРеш, 6, 2 ) ):c,
    substr( APO.ДатаРеш, 1, 4 ),
    APO.НомОтмРеш
  );
  
  
  [
    и возобновить действие:
    
  ];
  
  PrintBodyAP( APO, mesId, kind );
  
  return true;

end; 


macro PrintDoc( addrMes, MassCopy )
  var field_name, field_value;
  var MsgTmpFileName, OldName;
  FILE MsgTmpFile() txt;


  SetBuff( wlmes, addrMes );

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpfnsprnapo");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var APO = PrintAPO();
  APO.ReadMessage();

  Draw( APO, wlmes.MESID, wlmes.KIND );

  close( MsgTmpFile );
  SetOutPut( OldName, true ); /* Перенаправляем вывод обратно */

  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;

  return true;

OnError(er) /* обработка ошибок времени выполнения */
  ExeptionMessage( er );
  return false;
end;