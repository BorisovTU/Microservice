/*
  $Name: ufpr218.mac
  $Module: Межбанковские расчеты
  $Description: Печать формы по сообщению ED218
*/

import "ufgendoc.mac";

private const TYPE_CODE_UFBS_ED218_ALT = 66;
private const TYPE_CODE_UFBS_ED218 = 67;

private macro GetRequest( MesID, OriginatorCode : @String, OriginatorName : @String, RecipientCode : @String, RecipientName : @String )
  var rs = execSQLSelect( " Select wlreq.t_OriginatorCode, wlreq.t_OriginatorName, wlreq.t_RecipientCode, wlreq.t_RecipientName "
                          "   From dwlreq_dbt wlreq, dwlmeslnk_dbt meslnk "
                          "  Where meslnk.t_MesID = :MesID "
                          "    and wlreq.t_ReqID = meslnk.t_ObjID ",
                          MakeArray( SQLParam("MesID", MesID) ));
                         
  if( rs and rs.MoveNext())
    OriginatorCode = rs.value("t_OriginatorCode");
    OriginatorName = rs.value("t_OriginatorName");
    RecipientCode = rs.value("t_RecipientCode");
    RecipientName = rs.value("t_RecipientName");
  end;
end;

private macro GetReportIDCode( ReportID )

  var rs = execSQLSelect( " Select t_Description "
                          "   From dwlcodes_dbt "
                          "  Where t_Code = :ReportID "
                          "    and t_AlgNum = :AlgNum ",
                          MakeArray( SQLParam("ReportID", ReportID),
                                     SQLParam("AlgNum", TYPE_CODE_UFBS_ED218_ALT)));
  if( rs and rs.MoveNext() )
    return rs.value("t_Description");
  else
    return "";
  end;
  
end;

private macro GetMakingStatusCode( MakingStatusCode )

  var rs = execSQLSelect( " Select t_Description "
                          "   From dwlcodes_dbt "
                          "  Where t_Code = :MakingStatusCode "
                          "    and t_AlgNum = :AlgNum ",
                          MakeArray( SQLParam("ReportID", MakingStatusCode),
                                     SQLParam("AlgNum", TYPE_CODE_UFBS_ED218)));
  if( rs and rs.MoveNext() )
    return rs.value("t_Description");
  else
    return "";
  end;
  
end;


macro PrintForm( addrMes, MassCopy )
  var EDNo,
      EDDate,
      OriginatorCode,
      OriginatorName,
      RecipientCode,
      RecipientName,
      ReportID,
      Description,
      ReportDate,
      MakingStatusCode,
      Description2;
    
  SetBuff( wlmes, addrMes );
  
  var field_name, field_value, строка, название;  
  
  СчитатьПоле( field_name, field_value );
  название = SubStr(field_value, 2, 5);
  if(Index(field_value, String("</",название,">")))
     строка = StrSubst(field_value, String("</",название,">"),"");
  else
     строка = StrSubst(field_value, "/>",">");
  end;
  
  while( СчитатьПоле( field_name, field_value ) )
     строка = строка + field_value;
  end;
  
  строка = строка + String("</",название,">");
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(строка) )
    println( string( "Неверный формат сообщения по форме ED108" ) );
    return FALSE;
  end; 
  
  EDNo = ReadAttribute( xml, "EDNo" );
  EDDate = ToDateYYYY_MM_DD(ReadAttribute( xml, "EDDate"));
  
  
  GetRequest( wlmes.MesID, @OriginatorCode, @OriginatorName, @RecipientCode, @RecipientName );
  
  ReportID = ReadOptinalAttribute( xml, "ReportID");
  
  if( ReportID > 0 )
    Description = GetReportIDCode( ReportID );
  else
    Description = "";
  end;
  
  ReportDate = ToDateYYYY_MM_DD(ReadAttribute( xml, "ReportDate" ));
  
  MakingStatusCode = ReadAttribute( xml, "MakingStatusCode" );
  
  Description2 = GetMakingStatusCode( MakingStatusCode );
  
  
[
  ED218 Запрос выходной формы
  
  Номер ЭС:     #########
  Дата ЭС:      ##########
  Составитель:  ########## ##################################################
  Получатель:   ########## ##################################################
  
  Идентификатор формы (ReportID): ####### ####################################################################################################
  
  Дата выходной формы (ReportDate): ##########
  
  Признак формирования формы (MakingStatusCode): # ####################################################################################################
](EDNo,
  EDDate,
  OriginatorCode,
  OriginatorName,
  RecipientCode,
  RecipientName,
  ReportID,
  Description,
  ReportDate,
  MakingStatusCode,
  Description2);
  
  return true;
end;