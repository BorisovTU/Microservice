/*
$Name:               fnsgmbnsx.mac
$Module:            МБ Расчеты
$Description:      Генерация сообщения по BNS XML
*/

import oralib, likepy, "xmlmestools.mac", "wlmnstls.mac", "mnsbxtool.mac", 
       "pm_const.mac", "bnk_common.mac";


macro CheckObj(addrRegdec)
  SetBuff(wlregdec, addrRegdec);
  return CheckObj_FNSINFO_Xml( wlregdec );
end;

macro GenMes( addrMes, addrRegdec, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record wlregdec(wlregdec);
  SetBuff(wlregdec, addrRegdec);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по BNS");

  var rs;
  // чтение глобализмов
  var OldfnsMessageID : integer = GetOldfnsMessageID();
  var KindActionFNS   : integer = GetKindActionFNS();

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  ЗаписатьФайл("СПРБННАЛИЧ");

  // Файл \ СПРБННАЛИЧ
  StartBlockLogXML("СПРБННАЛИЧ");

  // атрибуты элемента "СправНалич"
  ЗаписатьПолеЛог("НомСправ", string(wlregdec.Number));
  var ТипСправ : string = ЗаписатьПоле_ТипСправ(wlregdec);
  ЗаписатьПолеЛог("НомЗапр", string(wlregdec.LnkDocNumber));
  ЗаписатьПолеЛог("ДатаЗапр", YYYY_MM_DD(wlregdec.LnkDocDate));
  ЗаписатьНаимПодтв(wlregdec);
  ЗаписатьПолеЛог("ВидСпр", string(wlregdec.LnkTypeInfo));
  ЗаписатьПолеЛог("ДейстПоСост", YYYY_MM_DD(wlregdec.Date));
  ЗаписатьПолеЛог("ДатаФорм", YYYY_MM_DD(wlregdec.InputDate));
  ЗаписатьПолеЛог("ДатаНапр", YYYY_MM_DD(date()));

  ПереченьФилиалов(ТипСправ, wlregdec.OriginatorID);//блоки "Самостоят"

  СведенияОНалоговомОргане("СвНО", wlregdec);

  СведенияОБанке("СвБанк", wlmes.InsideAbonentID);

  ЗаписатьПлательщикаВСправкуФНС(wlregdec);

  rs = execSQLselect
    ( "select acclnk.* "
      "  from dwlacclnk_dbt acclnk \n"
      " where acclnk.t_ObjectType = ? "
      "   and acclnk.t_ObjectID = ? "
      "   and acclnk.t_Department = (" +
      "                   SELECT includedDprt.t_code\n"
      "                     FROM ddp_dep_dbt includedDprt\n"
      "                    WHERE includedDprt.t_PartyID = ?)", 
      MakeArray( SQLParam("", OBJTYPE_FNSINFO), 
                 SQLParam("", wlregdec.DecisionID), 
                 SQLParam("", wlmes.InsideAbonentID) ) );

  var acclnkRec : TRecHandler = TRecHandler("wlacclnk");

  while (rs.moveNext())
    CopyRSetToFBuff(acclnkRec, rs);

    if ( not IsLnkAccountPrcError(acclnkRec, WLD_TYPE_REGDEC_INS) )// если не было ошибок при обработке счета
        StartBlockLogXML("Сведения");
        ЗаписатьПолеЛог("НомСч", rs.value("t_Account"));
        ЗаписатьПолеЛог("ВидСч", rs.value("t_TypeAcc"));
        ЗаписатьПолеЛог("ДатаОткр", YYYY_MM_DD(rs.value("t_DateOpen")));
        if ((date(rs.value("t_DateClose")) != date(0,0,0)) and (date(rs.value("t_DateClose")) < wlregdec.EndDate))
          ЗаписатьПолеЛог("ДатаЗакр", YYYY_MM_DD(rs.value("t_DateClose")));
        end;
        ЗаписатьПолеЛог("КодВал", GetISO_Number( rs.value("t_FIID") ));
        FinishBlockLogXML("Сведения");
    end;
  end;

  ЗаписатьПредБанка("ПредБанка", wlmes.InsideAbonentID, true);

  FinishBlockLogXML("СПРБННАЛИЧ");

  FinishBlockLogXML("Файл");

  /*_НомерОтправ*/
  FnsWriteSendNum(OldMes);

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;