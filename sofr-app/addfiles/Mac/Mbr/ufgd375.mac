/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED375                                     */
/*                                                                          */
/*  Имя файла: ufgd375.mac                                                  */
/*  Создан:    06.11.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdreqliq.mac";

class (TGenDocLiqReq) TGenDocED375()

  InitTGenDocLiqReq(MESKIND_ANSWER, "ED375");

  class TCancelledLimitInfo
    var LimitTransKind = "",
        PURBIC = "",
        LimitSum = "";
  end;

  var LimitInfoArray = TArray();

  macro ReadFields() : bool
    var fieldname = "", fieldvalue = "", blockname = "";
    while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
      if  (blockname == "")
        if  (fieldname == "EDAuthor")
          OriginatorUIS = fieldvalue;
        elif(fieldname == "EDReceiver")
          RecipientUIS = fieldvalue;
        end;
      elif(blockname == "CancelledLimitInfo")
        if  (fieldname == beginField)
          LimitInfoArray[ LimitInfoArray.size ] = TCancelledLimitInfo();
        elif(fieldname == "LimitTransKind")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitTransKind = fieldvalue;
        elif(fieldname == "PURBIC")
          LimitInfoArray[ LimitInfoArray.size-1 ].PURBIC = fieldvalue;
        elif(fieldname == "LimitSum")
          LimitInfoArray[ LimitInfoArray.size-1 ].LimitSum = fieldvalue;        
        end;
      elif(blockname == "InitialED")
        if  (fieldname == "EDDate")
          EDDate_InitMes = fieldvalue;
        end;
      end; 
    end; // while ( СчитатьПоле(fieldname, fieldvalue, blockname) )

    return TRUE;
  end;

  macro FillQueries() : bool

    // "/LimitTransKind/PURBIC/LimitSum<CRLF>"

    if(LimitInfoArray.size < 1)
      std.msg("Не заполнен обязательный блок CancelledLimitInfo в сообщении по форме " + FormName);
      return FALSE;
    end;

    for(var item, LimitInfoArray)
      if(wlreq.Queries)
        wlreq.Queries = wlreq.Queries + "\n";
      end;
      wlreq.Queries = wlreq.Queries + "/" + item.LimitTransKind + 
                                      "/" + item.PURBIC +
                                      "/" + item.LimitSum;
    end;

    return TRUE;
  end;

  macro OnAfterGenWlreq() : bool    

    var ReceiverBIC = wlreq.RecipientCode; // получен из атрибута EDReceiver

    var Department : integer = GetDprtByBIC( ReceiverBIC );

    if(Department <= 0)
      std.msg("Не найден филиал " + ReceiverBIC);
      return FALSE;
    end;

    // обновляются настройки ликвидности для филиала
    
    var DpLiqSet : RsbDpLiqSet = RsbDpLiqSet(Department);

    var Sum : moneyL = $0;
    var PartyID   : integer = -1,
        DpBilLim  : RsbDpBilLim = DpLiqSet.DpBilLim,
        billimRec : TRecHandler = TRecHandler("dpbillim.dbt");

    for(var item, LimitInfoArray)
    
      if  (item.LimitTransKind == "1")
        DpLiqSet.CurTotalLimit = Sum;

      elif(item.LimitTransKind == "2")
      
        PartyID = ПолучитьКодСубъекта(item.PURBIC, PTCK_BIC);        

        if(PartyID > 0)
          billimRec.Clear();

          if( DpBilLim.FindByParty(PartyID, billimRec) == 0 )
            billimRec.rec.CurLimit = Sum;

            if(DpBilLim.Update(billimRec) != 0)
              std.msg("Ошибка при обновлении настроек двустороннего лимита " + 
                      "для филиала " + ReceiverBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          else
            billimRec.rec.PartyID = PartyID;
            billimRec.rec.CurLimit = sum;

            if(DpBilLim.Insert(billimRec) != 0)
              std.msg("Ошибка при вводе настроек двустороннего лимита " + 
                      "для филиала " + ReceiverBIC + " с субъектом " + item.PURBIC);
              return FALSE;
            end;
          end;
        end; // Если субъект найден

      elif(item.LimitTransKind == "3")
        DpLiqSet.CurMultilateralLimit = Sum;
      end;
    end; // for каждый блок "CancelledLimitInfo" 
    
    if(DpLiqSet.Update() != 0)
      std.msg("Ошибка при сохранении настроек ликвидности");
      return FALSE;
    end;

    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       

  var gdObj : TGenDocED375 = TGenDocED375();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
