/*
$Name:         ufgo511.mac
$Module:       Межбанковские расчеты
$Description:  Обработка учетного объекта ED511
*/

// ED511 Счет за услуги, предоставленные Банком России пользователю СПФС

import "ufgendoc.mac";

private class TPayServicesCodeList
  var ServicesCode = TArray(),
      ReceiverSWIFTBIC = TArray(),
      ServicesDate = TArray(),
      ServicesQuantity = TArray(),
      ServicesRate = TArray(),
      Sum = TArray();
end;

private macro GetRmDate(xml) : date
  var RmDate : date = date(0, 0, 0);

  var EDDate : date = ToDateYYYY_MM_DD(ReadAttribute( xml, "EDDate"));
  if(EDDate > {curdate})
    RmDate = EDDate;
  else
    RmDate = {curdate};
  end;

  RmDate = GetNextDate( RmDate, {OperDprt}, CALENDAR_SERVICE_BANK, true );

  return RmDate;
end;

private macro GetGround(xml, InsideAbonentID : integer) : string
  var Ground : string =
    "Оплата услуг, предоставленных участнику СПФС " + 
    ПолучитьКодСубъекта(InsideAbonentID, PTCK_BIC, null, 1) + " " +
    Bnk_GetPartyShortName(InSideAbonentID) + ", за период с " + 
    ToDateYYYY_MM_DD( ReadAttribute(xml, "ServicesBeginDate") ) + " по " +
    ToDateYYYY_MM_DD( ReadAttribute(xml, "ServicesEndDate") );

  return Ground;
end;

private macro CreatePayment(wlmes, wlinfo, xml)
  var BankPayment : object = RsbBankPayment(0);
  var Payment : RsbPayment = BankPayment.Payment;

  // субъект-получатель сообщения 
  var InsideAbonentID : integer = wlmes.InsideAbonentID;
  if(not InsideAbonentID)
    InsideAbonentID = wlinfo.RecipientID;
  end;
  
  Payment.DocKind = 16;
  Payment.PrimDocKind = 16;
  Payment.Number = ReadAttribute(xml, "EDNo");
  Payment.Reference = wlmes.Trn;
  Payment.Date = 
  Payment.ValueDate = GetRmDate(xml);
  Payment.BaseFIID = NATCUR;
  Payment.BaseAmount = StrToMoneyUFBS(ReadAttribute(xml, "Sum"));
  Payment.Ground = GetGround(xml, InsideAbonentID);
  //Payment.ReceiverIsSender = "X"; /*Read only*/

  Payment.ShifrOper = "01";
  Payment.PaymentKind = "Н";
  
  var rs = execSQLSelect( " Select sa.t_INN, sa.t_RecName, sa.t_Account, sa.t_BankID, sa.t_BankCodeKind, sa.t_BankCode, sa.t_CorrAcc "
                          "   From dsettacc_dbt sa, dpmautoac_dbt aa "
                          "  Where aa.t_PartyID = :PartyID "
                          "    and aa.t_Purpose = :Purpose" 
                          "    and aa.t_SettaccID = sa.t_SettaccID ",
                          MakeArray(SQLParam("PartyID", InsideAbonentID),
                                    SQLParam("Purpose", 79 /*PM_PURP_SPFS*/)));
                                    
  var Code = "", CodeOwner = "";                                  
  
  if( rs and rs.MoveNext() )
    Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,
                        rs.value("t_BankID"),
                        rs.value("t_BankCodeKind"),
                        rs.value("t_BankCode"),
                        "",
                        rs.value("t_CorrAcc"),
                        Payment.BaseFIID,
                        1,
                        rs.value("t_Account"),
                        null,
                        rs.value("t_RecName"),
                        rs.value("t_INN")
                        );
  else
    GetPartyCodeEx(InsideAbonentID, PTCK_BIC, @Code, @CodeOwner);
    Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,
                        InsideAbonentID,
                        PTCK_BIC,
                        Code,
                        "",
                        "",
                        Payment.BaseFIID,
                        1,
                        "",
                        InsideAbonentID,
                        Bnk_GetPartyName(InsideAbonentID),
                        GetPartyINN(InsideAbonentID, 1)
                        );
  end;

  var BankCode : string = ReadAttribute( xml, "BIC"),
      BankCodeKind : integer = PTCK_BIC,
      ReceiverBankID : integer = GetCodeOwnerID(BankCodeKind, BankCode);
  Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF,
                         ReceiverBankID,
                         BankCodeKind,
                         BankCode,
                         "",
                         "",
                         Payment.BaseFIID,
                         1,
                         ReadAttribute(xml, "PayeePersonalAcc"),
                         ReceiverBankID,
                         "",
                         string(ReadAttribute(xml, "INN") + "/" + ReadAttribute(xml, "KPP"))
                         );
  var i = 0;
  array FlgRM;
  while( i < PNRMPM_COUNT )
    FlgRM[i] = 1;
    i = i + 1;
  end;  

  if( PM_ProcessPanel(Payment, 1, null, FlgRM) == 0 )
    InitError();
    var stat : integer = BankPayment.Update();
    if(stat)
      std.msg( "Ошибка при сохранении платежа" );
      return false;
    end;

    var meslnk = TRecHandler("wlmeslnk.dbt");
    meslnk.rec.MesID   = wlmes.MesID;
    meslnk.rec.Direct  = "X";/*WLD_MES_IN*/
    meslnk.rec.ObjID   = Payment.PaymentID;
    meslnk.rec.ObjKind = OBJTYPE_BANKPAYMENT;
    var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );            
    if( (MesLnkObj.Insert( meslnk ) != 0) or (MesLnkObj.Save() != 0) )
      std.msg("Ошибка при создании связи платежа с входящим сообщением");
      return FALSE;
    end;
    return true;
  else
    InitError();
    std.msg("Выполнение процедуры прервано пользователем");
    return false;
  end;
end;

private macro CloseInfo(wlinfo)
  wlinfo.State = WLD_STATUS_INFO_CLOSED;
  ОбновитьИнфСообщение(wlinfo);
end;

private macro RollBackPayment()
  var rs = execSQLSelect( " Select pm.t_PaymentID, pm.t_PaymStatus "
                          "   From dpmpaym_dbt pm, dwlmeslnk_dbt lnk "
                          "  Where lnk.t_MesID = :MesID "
                          "    and lnk.t_Direct  = chr(88) "
                          "    and lnk.t_ObjID = pm.t_PaymentID "
                          "    and lnk.t_ObjKind = :ObjKind ",
                          MakeArray( SQLParam("MesID", wlmes.MesID),
                                     SQLParam("ObjKind", OBJTYPE_BANKPAYMENT)));
                                     
  if( rs and rs.MoveNext() )
    if(not InList( rs.value("t_PaymStatus"), PM_PREPARING))
      std.msg("По платежу начата операция, невозможно удалить");
      return false;
    else
      PM_DeletePmDocument(rs.value("t_PaymentID"));
      var meslnk = TRecHandler("wlmeslnk.dbt");
      meslnk.rec.MesID   = wlmes.MesID;
      meslnk.rec.ObjID   = rs.value("t_PaymentID");
      meslnk.rec.ObjKind = OBJTYPE_BANKPAYMENT;
      var MesLnkObj = RsbWlMesLnk( meslnk.rec.ObjKind, meslnk.rec.ObjID, meslnk.rec.Direct );
      MesLnkObj.Delete(meslnk);
      MesLnkObj.Save();
    end;
  end;
  
  return true;
    
end;

macro GenObj( addrMes, mode )

  RECORD wlmes(wlmes);
  SetBuff( wlmes, addrMes );
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка;
  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED201" ) );
     return false;
  end;

  if(not mode)
    mode = WLD_GENOBJ_ACTION_EXECUTE; // по умолчанию - обработка
  end;
  
  FILE wlinfo(wlinfo);
  ClearRecord(wlinfo);
  wlinfo.InfoID = GetObjIDByMesLink(wlmes.MesID, OBJTYPE_INFO, "X");
  if( not wlinfo.InfoID or not GetEQ(wlinfo))
    return FALSE;
  end;
  
  array Text;
  array Buttons;
  
  if( mode == WLD_GENOBJ_ACTION_EXECUTE)
    
    Text[0] = "Сформировать платеж на оплату полученного счета?";
    Buttons[0] = "Сформировать платеж";
    Buttons[1] = "Закрыть без платежа";
    Buttons[2] = "Отказаться";
    
    var Action = ConfWin( Text, Buttons );
    
    if(Action == 0)
      return CreatePayment(wlmes, wlinfo, xml);
    elif(Action == 1)
      CloseInfo(wlinfo);
      return TRUE;
    elif(Action == 2)
      std.msg("Выполнение процедуры прервано пользователем");
      return FALSE;
    end;
  elif(mode == WLD_GENOBJ_ACTION_ROLLBACK)
    return RollBackPayment();
  end;;
  
  return TRUE;
    
  OnError(er) // обработка ошибок времени выполнения
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;