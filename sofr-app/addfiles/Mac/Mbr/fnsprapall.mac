/*
$Name:          fnsprapall.mac
$Module:        МБ Расчеты
$Description:   Общие функции для печати сообщений AP: APN, APO и APZ
*/
import RSD; 
import CTInter; 
import "likepy.mac";

// получение имени операциониста для сообщений APN, APO и APZ
macro GetOperName ( mesId : integer )
  var OperName = "";
  
  var rsd = rsdcommand( "  SELECT Persn.t_name " +
                       "    FROM dwlhistor_dbt his, dperson_dbt Persn " +
                       "   WHERE his.t_userId = Persn.t_oper " +
                       "     AND his.t_objId = ? " +
                       "     AND his.t_objKind = ? " +
                       "ORDER BY his.t_wlhistorId asc "
                      );
  rsd.addParam("mesId", RSDBP_IN, mesId );
  rsd.addParam("kind", RSDBP_IN, OBJTYPE_MES);
  
  rsd.execute();
  var data = RsdRecordset( rsd );
  if( data.movenext() == true )
    OperName = data.value( "T_NAME" );
  end;
  
  return OperName;
end;


// Строка в формате <поручения <реквизиты поручения 1>, :, поручения <реквизиты поручения n>,  для сообщений APN, APO и APZ
macro GetDateRec( mesId : integer )
  var DataRec = "";
  var rsd = rsdcommand("  SELECT his.t_BankDate " +
                      "    FROM dwlhistor_dbt his " +
                      "   WHERE his.t_objId = ? " +
                      "     AND his.t_objKind = ? "+
                      "ORDER BY his.t_wlhistorId asc "
                      );
  rsd.addParam("mesId", RSDBP_IN, mesId );
  rsd.addParam("kind", RSDBP_IN, OBJTYPE_MES);
  
  rsd.execute();
  var data = RsdRecordset( rsd );
  if( data.movenext() == true )
    DataRec = data.value( "t_BankDate" );
  end;
  
  return DataRec;
end;


// получение для сообщений APN, APO и APZ
// Выводятся значения всех полей <НомСчПл>, <ВидСч> всех блоков <ПриостПоруч>, а также значения полей <ИНННП> и <КППНП>. 
// Данные в отчет выводить в следующем формате: <<ВидСч1> <НомСчПл1>, :, <ВидСчn> <НомСчПлn>, ИНН/КПП <ИНННП>/<КППНП>>. 
// Если элемент <КППНП> в сообщении отсутствует или не заполнен, то выводить реквизиты плательщика в формате <ИНН <ИНННП>>.
macro GetAccount( AP )
  
  var resultStr = "";
  var countAccount = 0;
  
  while( countAccount < AP.AccKindList.Size )
    if( countAccount > 0 )
      resultStr = resultStr + ", ";
    end;
    resultStr = resultStr + AP.AccKindList[countAccount] + " " + AP.AccList[countAccount];
    
    countAccount = countAccount + 1;
  end;
  
  resultStr = resultStr + ", " + AP.ИНННП + IfThenElse( AP.КППНП != "", "/" + AP.КППНП, "");
  
  return resultStr;
end;

// получение имени операциониста для сообщений APN, APO и APZ
macro GetRegKind( AP )
  var RegKind = "";
  var CountBlock = 0;
  
  while( CountBlock < AP.ListReg.Size )
    if( CountBlock > 0 )
      RegKind = RegKind + ", ";
    end;
    
    RegKind = RegKind + "поручения " + AP.ListReg[CountBlock];
    CountBlock = CountBlock + 1;
  end;
  
  return RegKind;
end;

macro PrintTaxNameHead ( AP )
  var arrayTaxName : TArray;
  var countTaxName = 0;
  arrayTaxName = strSplit2( AP.НаимНо, 37, 37, 2);
  
  while( countTaxName < arrayTaxName.size )
    if( countTaxName == 0 )
      [ #####################################  в соответствии с пунктом 4.1 статьи 46 Налогового кодекса]
      (
        arrayTaxName[countTaxName]
      );
    elif( countTaxName == 1 )
      [ #####################################  Российской Федерации:]
      (
        arrayTaxName[countTaxName]
      );
    else
      [ #####################################]
      (
        arrayTaxName[countTaxName]
      );
    end;
    countTaxName = countTaxName + 1;
  end;
  [ ─────────────────────────────────────
      (наименование налогового органа)];
end;


macro PrintBodyAP ( AP, mesId )

  var arrayRegKind : TArray;
  var countRegKind = 0;
  arrayRegKind = strSplit2( GetRegKind( AP ), 124 );
  
  while( countRegKind < arrayRegKind.size )
    [ #]
    (
      arrayRegKind[countRegKind]
    );
    countRegKind = countRegKind + 1;
  end;
  
  [ ─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
    (указывается вид поручений: на списание и перечисление денежных средств со счетов налогоплательщика (плательщика сбора, 
    плательщика страховых взносов, налогового агента) в бюджетную систему Российской Федерации, на перевод электронных денежных
    средств налогоплательщика (плательщика сбора, плательщика страховых взносов, налогового агента) в бюджетную систему 
                                         Российской Федерации, их даты и номера)
  ];
                              
  
  
  var arrayAccount : TArray;
  var countAccount = 0;
  arrayAccount = strSplit2( GetAccount( AP ), 103 );
  
  while( countAccount < arrayAccount.size )
    if( countAccount == 0 )
      [
        выставленных к счетам #
      ]
      (
        arrayAccount[countAccount]
      );
    else
      [                       #]
      (
        arrayAccount[countAccount]
      );
    end;
  
    countAccount = countAccount + 1;
  end;
  [                       ────────────────────────────────────────────────────────────────────────────────────────────────────────
                        (указываются вид и номера счетов, реквизиты корпоративного электронного средства платежа налогоплательщика
                            (плательщика сбора, плательщика страховых взносов, налогового агента), ИНН/КПП)
  ];
  
  if( GenClassName( AP ) == "PRINTAPO" )  // имя всегда капсом, иначе не сработает условие, особенность работы GenClassName
    [
      в банке  #
               ─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                             (полное наименование банка, БИК)
    ]
    (
      AP.БанкПл + ", " + AP.БИКБПл
    );
  elif( GenClassName( AP ) == "PRINTAPZ" )  // имя всегда капсом, иначе не сработает условие, особенность работы GenClassName
    [
      в сумме  #                     рублей.
               ─────────────────────
    ]
    (
      AP.СумПлат
    );
  end;
  
  [
    Руководитель (заместитель руководителя)
    
  ];
  var countTaxName = 0;
  var arrayTaxName = strSplit2( AP.НаимНо, 125 );
  
  while( countTaxName < arrayTaxName.size )
    [ #]
    (
      arrayTaxName[countTaxName]
    );
    countTaxName = countTaxName + 1;
  end;
  
  [ ─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                                    (наименование налогового органа)
                                                    
  ];
  
  
  var arrayClassOff : TArray, arrayFIO : TArray;
  var countClassOff = 0;
  
  arrayClassOff = strSplit2( AP.КласЧин, 51);
  arrayFIO = strSplit2( AP.FIO, 56 );
  
  var ClassOff = "", FIO = "";
  
  while( ( countClassOff < arrayClassOff.size ) or ( countClassOff < arrayFIO.size ) )
    if( arrayClassOff[countClassOff] )
        ClassOff = arrayClassOff[countClassOff]
    else
        ClassOff = "";
    end;
    
    if( arrayFIO[countClassOff] )
        FIO = arrayFIO[countClassOff]
    else
        FIO = "";
    end;
    
    [ #                                                                    #
    ]
    (
      ClassOff,
      FIO
    );
    
    countClassOff = countClassOff + 1;
  end;
  
  [ ─────────────────────────────────────────────────── ──────────────── ────────────────────────────────────────────────────────
                  (классный чин)                         (подпись)                                (Ф.И.О)
  ];
  
  
  
  
  [
    Настоящее решение получил
    
    #
    ─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                     (Ф.И.О. уполномоченного работника банка)
   
    #
    ─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                                           (полное наименование банка)

                                           #                     
    ──────────────────────────────        ─────────────────────
              (подпись)                           (дата)            
  ]
  (
    GetOperName( mesId ),
    AP.БанкПл,
    substr( string(GetDateRec( mesId )), 1, 10 )
  );
end;
