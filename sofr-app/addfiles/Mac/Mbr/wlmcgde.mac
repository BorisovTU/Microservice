/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                                                                          */
/*  Выписка МЦИ по Е0003                                                    */
/*                                                                          */
/*  Создан:     02.10.2002                                                  */
/*  Программист Бердюкова И.И.                                                 */
/****************************************************************************/

import "wlmcgd.mac", FIInter, "wlfindpm.mac";

macro GenDoc( addrMes, IsOldFormat )
  var error, Currency, val, Сумма, result = true, continue0 = 1;
  var PaymDKFlag:integer, PaymentDirect;

  SetBuff( wlmes, addrMes );

  if ( valtype(IsOldFormat)==V_UNDEF )
     IsOldFormat = false;
  end;

  PrintLog(2,"Генерация выписки по E0003");

  ClearRecord(wlhead);

  if( not СчитатьПоле( FormFldHeadExt, val ) ) /*Считываем заголовок выписки*/
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlhead.FIID           = 0;
  wlhead.Account        = substr(val,41,20);
  if( not ОпределитьСхемуРасчетовПоКорсчету( wlhead.Corschem, wlmes.OutsideAbonentID, wlhead.Account ) )
    return FALSE;
  end;    

  wlhead.Number         = Trim(substr(val,6,10));
  wlhead.DateIn         = СчитатьДату(val,16);
  wlhead.DateOut        = wlhead.DateIn;
  wlhead.RestIn         = moneyL(substr(val,141, 18))/100;
  wlhead.RestOut        = moneyL(substr(val,159,18))/100;
  wlhead.OverNightLimit = moneyL(substr(val,177, 18))/100;
  wlhead.IntraDayCredit = moneyL(substr(val,195,18))/100;
  wlhead.SumReserve     = moneyL(substr(val,213,18))/100;

  if( not ВставитьЗаголовокВыписки( wlhead ) )
    std.msg("Ошибка при сохранении заголовка выписки");
    return FALSE;
  end;

  while(continue0)
    if( not СчитатьПоле( FormFldDocExt, val ) ) /*Считываем документ выписки*/ 
      continue0 = 0      
    else
      ClearRecord(wlconf);

      /* Заполнение учетных буферов */
      if ( IsOldFormat )
         wlconf.Number       = Trim(substr(val, 25, 5 ));
      else
        wlconf.Number       = Trim(substr(val, 25, 3 ));
      end;
      wlconf.RelatedRef   = substr(val,1,6);
      if ( IsOldFormat )
         wlconf.Account      = substr(val, 30, 20 );
      else
         wlconf.Account      = substr(val, 28, 20 );
      end;
      wlconf.FIID         = 0;
      wlconf.DateValue    = СчитатьДату(val,7);

      if ( wlconf.DateValue==date(0,0,0) )
         wlconf.TransferDate = {curdate};
      else
         wlconf.TransferDate = wlconf.DateValue;
      end;

      if ( IsOldFormat )
         Сумма = substr( val, 50, 18 );
      else
         Сумма = substr( val, 48, 18 );
      end;

      wlconf.Sum = moneyL( Сумма )/100;

      PaymDKFlag = WL_DKUNDEF;

           wlconf.DKFlag       = WL_CREDIT;
      
      /* Вставляем подтверждение */
      if( not ВставитьДокументВыписки( wlconf ) )
        std.msg("Ошибка при сохранении документа выписки");
        result = FALSE;
        continue0 = 0;
      end;

      wlhead.CreditNumDoc = wlhead.CreditNumDoc +1;      
      wlhead.CreditTurn = wlhead.CreditTurn + wlconf.Sum
    end;
  end;

  if( result == FALSE )
    return FALSE;
  end;

  if( not ОбновитьЗаголовокВыписки( wlhead ) )
    std.msg("Ошибка при обновлении заголовка выписки");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;


macro GenDocE0003( addrMes )
   return GenDoc( addrMes, false );
end;

macro GenDocOldFormat( addrMes )
   return GenDoc( addrMes, true );
end;