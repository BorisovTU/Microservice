/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Отчеты по подтверждениям                                                */
/*  Имя файла: wlrepcnf.mac                                                 */
/*  Создан:  28.02.01                                         Бабин А.П.    */
/****************************************************************************/

import "wldoc.mac", FIInter;

var Code, DK, CurFIID=-10, Sum=0, Num=0;

macro SumToStr(FIID, sum)

   f_wlfininstr.FIID = FIID;
   if ( GetEQ( f_wlfininstr ) )
      return CurToStrAlt( sum, null, null, int(f_wlfininstr.ISO_Number) );
   elif ( FIID>=0 )
      return string(sum) + " (Неизвестная валюта)";
   else return "";
   end;

end;

macro DateToStr( date )
   var day, mon, year, str;
   DateSplit(date, day, mon, year );
   if ( day < 10 )
     day = "0"+string(day);
   else
     day = string(day);
   end;

  if ( mon <10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = day + "." + mon + "." + string(year);
  return str;
end;

macro ChangeFI(FIID, FlagEnd )

   if ( ((CurFIID!=FIID) AND (CurFIID>=0)) OR (valtype(FlagEnd)!=V_UNDEF) )
[+----------------------------+-----------------------------+-+----------+-------------------------+
Итого      ######################   ######################################################################################################################
Документов ######

](Sum:a, SumToStr(CurFIID, Sum):l, Num:l);
   end;

   if ( (CurFIID!=FIID) AND (FIID>=0) )
[
           ############### #
+----------------------------+-----------------------------+-+----------+-------------------------+
](ПолучитьКодФинИн(FIID), StrUpr(ПолучитьИмяФинИн(FIID)));

   end;

   if( CurFIID!=FIID )
      CurFIID = FIID;
      Sum = $0;
      Num =  0;
   end;

end;

macro PrintHeader( Mode, DKFlag, CorrID, TpID, DateB, DateE, DepBIC, DepName, DepID )
  var NameRep, error;

  file party(party);

  Code = ПолучитьКодСубъекта( CorrID, PTCK_CONTR, error );

  if ( error )
     msgBox("Не найден код корреспондента");
     return false;
  end;

  party.PartyID = CorrID;
  GetEQ(party);

  f_wltransp.TpID = TpID;
  GetEQ(f_wltransp);

  if ( mode==5 )
     NameRep = "ОТЧЕТ ПО НЕСКВИТОВАННЫМ ПОДТВЕРЖДЕНИЯМ";
  elif ( mode==6 )
     NameRep = "ОТЧЕТ ПО СКВИТОВАННЫМ ПОДТВЕРЖДЕНИЯМ";  
  end;

  DK = DKFlag;
  if ( DKFlag=="К" )
     NameRep = NameRep + " - " + "КРЕДИТ";
  else
     NameRep = NameRep + " - " + "ДЕБЕТ";
  end;

  if ( CurFIID!=-10 )
     //ChangeFI(-1, 1);
  else 
     ChangeFI(-1);
  end;

[      #######################################################################################

            Филиал          ############### #
            Корреспондент   ############### #
](NameRep:c, DepBIC, DepName, Code, party.Name);

  if (f_wltransp.Name)
[           Транспорт       #
](f_wltransp.Name);
  end;

[           с               #
            по              #
]
( DateToStr(DateB), DateToStr(DateE) ) ;

[
+----------------------------+-----------------------------+-+----------+-------------------------+
|     Корреспондентский      |         Номер               |В|   Дата   |                         |
|           счет             |     подтверждения           |И| валютир. +          Сумма          |
|                            |                             |Д|          |                         |
+----------------------------+-----------------------------+-+----------+-------------------------+];

  return true;
end;

macro PrintRecord( addrConf )
    var stat;
    SetBuff( wlconf, addrConf );

    file cors( corschem ) key 1;

    ChangeFI(wlconf.FIID);              

    cors.Number  = wlconf.Corschem;
    cors.FI_KIND = 1;
    cors.FIID    = wlconf.FIID;    
    GetEQ( cors );

    [|############################|#############################|#|##########|#########################|]
    ( cors.Account,
                                    wlconf.Number,
                                                   DK, DateToStr(wlConf.DateValue),
                                                                  wlconf.Sum:a ) ;
    Sum = Sum+wlconf.Sum;
    Num = Num+1;
    return true;
end;

macro PrintFloor( )
    ChangeFI(-1, 1);
    return true;
end;