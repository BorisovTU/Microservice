/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Возврат платежа из картотеки корсчета                                   */
/*  Имя файла: w110_270.mac                                                 */
/*  Создан:  17.10.00                                         Бабин А.П.    */
/****************************************************************************/

import  InsCarryDoc, OprInter, BankInter, RMInter, "rmconst.mac", "wldoc.mac", globals;

RECORD Corschem( corschem );
var PaymentObj:RsbPayment;

var DEBET = "Д", KREDIT = "К" ;

const ПутьСчетаКорреспонденцииСКартотеками = "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ПЕРЕМЕННЫЕ\\CARDFILEACCOUNT";

macro ExecuteStep( doc, first )
  var err, DKFlag = "", FlagTrans = 0, stat;
  var НомерСчетаКорреспонденцииСКартотеками;
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();

  GetRegistryValue( ПутьСчетаКорреспонденцииСКартотеками, V_STRING, НомерСчетаКорреспонденцииСКартотеками, err );
  if( err != 0 )
    std.msg( "Не определен номер счета корреспонденции с картотеками в настройках банка" );
    return 1;
  end;

  if ( PaymentObj.IsCredit() )
     DKFlag = KREDIT;
  else 
     DKFlag = DEBET;
  end;

  FlagTrans = PaymentObj.IsTransit();  

  if ( (PaymentObj.Receiver AND ВидСубъекта(PaymentObj.Receiver, PTK_BANK)) OR
       ((PaymentObj.DocKind!=PS_PAYORDER) AND
        (PaymentObj.DocKind!=PS_CPORDER) AND 
        (PaymentObj.DocKind!=WL_INDOC) AND
        (PaymentObj.DocKind!=WL_WIPM) ) )
    if ( not CarryPlanDocuments(PaymentObj.PaymentID) )
       MsgBox("Ошибка при помещении планируемой проводки в проведенные");
       return 1;
    end;
  end;

  if( PaymentObj.ReceiverFIID == 0 )
    /* Заполняем поля документа */
    paymtr.Chapter = 1 ;
    paymtr.FIIDPayer = wlpmpaym.PayFIID ;
    paymtr.Sum = wlpmpaym.PayAmount ;
    paymtr.ResultCarry = 1 ;

    paymtr.Kind_Oper  = " 1" ;
    paymtr.Shifr_Oper = "09" ;

    /* Реальные счета и коды банков */
    if( FlagTrans == 1 )
      if( DKFlag == KREDIT )
        paymtr.AccountPayer      = wlpmpaym.FuturePayerAccount;
        paymtr.AccountReceiver   = GetCorAcc( wlpmpropdeb.PayFIID, wlpmpropdeb.Corschem, CORS_ACC_ACCOUNT ) ;
      else
        paymtr.AccountPayer      = GetCorAcc( wlpmpropcred.PayFIID, wlpmpropcred.Corschem, CORS_ACC_ACCOUNT )  ;
        paymtr.AccountReceiver   = wlpmpaym.FutureReceiverAccount;
      end ;
    else
      if( DKFlag == KREDIT )
        paymtr.AccountPayer        = wlpmpaym.FuturePayerAccount;
        paymtr.AccountReceiver     = wlpmpaym.PayerAccount;
      else
        paymtr.AccountPayer        = wlpmpaym.ReceiverAccount;
        paymtr.AccountReceiver     = wlpmpaym.FutureReceiverAccount;
      end ;
    end ;

    paymtr.Date_Carry = {curdate};
    paymtr.Department = wlpmpaym.Department ;

    paymtr.Ground = substr( "Возврат из картотеки корсчета документа/"+wlpmrmprop.Ground, 1, 210 ) ;
    paymtr.Numb_Document = wlpmrmprop.Number ;
  else
    msgBox( "Валютные платежи не ставятся и не возвращаются из картотеки корсчета" );
    return 1;
  end ;

  if( not paymtr.Carry() )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;
    
  /* Заполняем поля документа */
  paymtr = PaymentObj.MakeTransaction();
  paymtr.Chapter = 3 ;
  paymtr.FIIDPayer = PaymentObj.PayFIID ;
  paymtr.Sum = PaymentObj.PayAmount ;
  paymtr.ResultCarry = 1 ;

  paymtr.Kind_Oper  = " 1" ;
  paymtr.Shifr_Oper = "09" ;

  /* Реальные счета и коды банков */
  if( FlagTrans == 1 )
    if( DKFlag == KREDIT )
      paymtr.AccountPayer      = НомерСчетаКорреспонденцииСКартотеками;
      paymtr.AccountReceiver   = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_CARDFILELORO ) ;
    else
      paymtr.AccountPayer      = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_CARDFILELORO ) ;
      paymtr.AccountReceiver   = НомерСчетаКорреспонденцииСКартотеками;
    end ;
  else
    if( DKFlag == KREDIT )
      paymtr.AccountPayer        = НомерСчетаКорреспонденцииСКартотеками;
      paymtr.AccountReceiver     = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_CARDFILELORO ) ;
    else
      paymtr.AccountPayer        = GetCorAcc( PaymentObj.ReceiverFIID, PaymentObj.OutCorschem, CORS_ACC_CARDFILELORO ) ;
      paymtr.AccountReceiver     = НомерСчетаКорреспонденцииСКартотеками;
    end ;
  end ;

  paymtr.Date_Carry = {curdate};
  paymtr.Department = PaymentObj.Department ;

  
  paymtr.Ground = substr( "Возврат из картотеки корсчета документа/"+PaymentObj.Ground, 1, 210 ) ;
  paymtr.Numb_Document = PaymentObj.Number ;
    
  
  if( not paymtr.Carry() )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;
    
  PaymentObj.PaymStatus = PM_FINISHED;

  PaymentObj.PropStatus = PM_PROP_CLOSED;

  if ( PaymentObj.CardFileKind==WLD_CARD_OUT_CORSCH )
    PaymentObj.CardFileDateOut = {curdate};
  end;
  return 0 ;
end ;