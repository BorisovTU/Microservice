/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT202                                    */
/*                                                                          */
/*  Имя файла: swsgm202.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

const  Relise = "202";
macro GenMes( addrMes, addrPm,  addrOldMes )
  var field_value, Tag, PaymentDetails;
  var Recreate = false;
  var RsPaym;
  RECORD rcv(pmroute);
  RECORD ocb(pmroute);
  RECORD irb(pmroute);
  Record oldwlMes (wlmes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( Oldwlmes,  addrOldMes );
     Recreate = true;
  end;

  PrintLog(2,"Генерация сообщения по МТ202");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );
  SB_CheckFIID( wlpmpaym.BaseFIID );

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  if (wlmes.RelatedRef != "")
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, wlmes.RelatedRef);
  end;

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 - Ссылка на документ или сделку */
  if (wlmes.RelatedRef != "")
    WriteFieldEx  (RelatedReferenceField, field_value, Recreate, OldwlMes );
  else
      WriteFieldEx  (RelatedReferenceField, СсылкаОтсутствует, Recreate, OldwlMes );
  end;

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate  ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );

  /* 72 - информация для банка-получателя */
  PaymentDetails = "";
  StrChangeRusXSet(RsPaym.Ground, PaymentDetails);
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT202_STANDART, PaymentDetails);      
  WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );

  if( IsPmCoverMode() == FALSE )
     /* 52a - банк приказодателя */
     field_value = FillOrderingInstitutionFieldA_SB(RsPaym, Tag);
     if (field_value == "")
       field_value = FillOrderingInstitutionFieldD_SB(RsPaym, Tag);
     end;
     ЗаписатьПолеВБлокКонтекст0(OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes);

     /* 53a - корреспондент отправителя */
     Tag = SWIFT_Tag_A;
     field_value = FillSenderCorrespondentField_SB(RsPaym, Tag );     
     ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value, Recreate, OldwlMes );     

     /* 54a - корреспондент получателя */
     Tag = SWIFT_Tag_A;
     field_value = FillReceiverCorrespondentField_SB(RsPaym, Tag );     
     ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField+Tag, "54a", field_value, Recreate, OldwlMes );     


     /* 57a - банк бенефициара */
     Tag = "";
     field_value = FillIntermediaryField_SB(RsPaym, Tag );
     ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );

     /* 58a - банк-бенефициар */
     Tag = "";
     field_value = FillBeneficiaryInstitutionField_SB(RsPaym, Tag );
     ЗаписатьПолеВБлокКонтекст0( BeneficiaryInstitutionField+Tag, "58a", field_value, Recreate, OldwlMes );
  else /* покрытие */
     /* банк рамбурсирующий посредник -> посредник */
     if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
         ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, ocb) AND
         (rcv.RouteID!=ocb.RouteID) AND
         ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
         (rcv.RouteID!=Route.RouteID) )
       ЗаписатьПолеМаршрутаКонтекст0Лог_SB( IntermediaryField, "56a", Route, Recreate, OldwlMes );
     end;

     /* корреспондент получателя -> банк бенефициара */
     if( ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
         ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
         ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
         (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
         ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
        ЗаписатьПолеМаршрутаКонтекст0Лог_SB( AccountWithInstitutionField, "57a", Route, Recreate, OldwlMes );
     end;

     /* банк получатель -> банк-бенефициар */
     Tag = "";
     field_value = FillBeneficiaryInstitutionField_SB(RsPaym, Tag );
     ЗаписатьПолеВБлокКонтекст0( BeneficiaryInstitutionField+Tag, "58a", field_value, Recreate, OldwlMes );
  end;

  return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

