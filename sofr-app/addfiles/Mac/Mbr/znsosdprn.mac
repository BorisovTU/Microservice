 /*
 $Name: znsosdprn.mac
 $Module: МБР
 $Description: Печать справки об остатках на вкладах (депозитах)
 */
import "fnsprzall.mac";

private macro PrintOneDepAcc(this: FNSPrint, Accounts: TArray, CurPos: integer, ReqDate:date)
  if (CurPos < Accounts.Size)
    [
      Таблица 1
      ┌───┬────────────────────┬────────────────┬─────────────────┬──────────────────┬──────────────────┬───────────────────────────────────────────────────────┬───────────────┬───────────────────────────────────┐
      │ № │     № счета        │      вид       │  дата открытия  │  дата истечения  │  дата закрытия   │     дата изменения реквизитов вклада (депозита)       │ цифровой код  │   остаток денежных средств по     │
      │п/п│    по вкладу       │     вклада     │     вклада      │ действия вклада  │     вклада       │     (дд.мм.гг.); № измененного счета по вкладу        │    валюты     │         вкладу (депозиту)         │
      │   │    (депозиту)      │   (депозита)   │   (депозита)    │   (депозита)     │   (депозита)     │                     (депозиту)                        │    вклада     ├─────────────────┬─────────────────┤
      │   │                    │                │   (дд.мм.гг)    │   (дд.мм.гг)     │   (дд.мм.гг)     │           (до изменения / после изменения)            │  (депозита)   │дата, на которую │    остаток      │
      │   │                    │                │                 │                  │                  │                                                       │               │ указан остаток  │(руб.,коп./вал.) │
      │   │                    │                │                 │                  │                  │                                                       │               │   (дд.мм.гг)    │                 │
      ├───┼────────────────────┼────────────────┼─────────────────┼──────────────────┼──────────────────┼───────────────────────────────────────────────────────┼───────────────┼─────────────────┼─────────────────┤
      │ 1 │         2          │       3        │        4        │         5        │         6        │                           7                           │       8       │       9         │       10        │
      ├───┼────────────────────┼────────────────┼─────────────────┼──────────────────┼──────────────────┼───────────────────────────────────────────────────────┼───────────────┼─────────────────┼─────────────────┤
    ];                                                                                                                                                                                                                       
    var i = CurPos;                                                                                                                                                                                                          
    var Dep = Accounts(i).rec.Department;
    var Amount: Money;
    var SeqNumber : Integer = 1;
    var Account,
        TypeAcc,
        DateOpen,
        DepositDateEnd,
        DateClose,
        Change,
        FICode,
        AccRest;
    while( (i < Accounts.Size) and (Accounts(i).rec.Department == Dep) )
      if( this.Счет[i].TblName == "merge" )
        Account = Accounts(i).rec.Account;
        TypeAcc = Accounts(i).rec.TypeAcc;
        DateOpen = Accounts(i).rec.DateOpen;
        DepositDateEnd = Accounts(i).rec.DepositDateEnd;
        DateClose = Accounts(i).rec.DateClose;
        Change = Changes[i];
        FICode = GetFICode( Accounts(i).rec.FIID );
        AccRest = GetLnkAccRest(Accounts(i).rec.Account, Accounts(i).rec.FIID, Accounts(i).rec.Chapter);
      else
        Account = Accounts(i).rec.Account;
        TypeAcc = GetNameTypeAcc( Accounts(i).rec.Type_Account, Accounts(i).rec.Code_Currency );
        DateOpen = Accounts(i).rec.Open_Date;
        DepositDateEnd = GetDepositDateEnd( Accounts(i) );
        DateClose = Accounts(i).rec.Close_Date;
        Change = FillAcc( Account, Accounts(i).rec.Code_Currency );
        FICode = GetFICode( Accounts(i).rec.Code_Currency );
        AccRest = GetLnkAccRest(Accounts(i).rec.Account, Accounts(i).rec.Code_Currency, Accounts(i).rec.Chapter)
      end;
      if( strlen(GetError(Accounts(i).rec.Account, WlRegDecID)) == 0 )
    [ │###│####################│################│#################│##################│##################│#######################################################│###############│#################│#################│]
     ( SeqNumber:c,
       Account:c, 
       TypeAcc:c,
       DateOpen:c,
       DepositDateEnd:c,
       DateClose:c,
       Change:c,
       FICode:c,
       ReqDate:c,
       AccRest:c
       );
        SeqNumber = SeqNumber + 1;
      end;
      i = i + 1;
    end;
    [ └───┴────────────────────┴────────────────┴─────────────────┴──────────────────┴──────────────────┴───────────────────────────────────────────────────────┴───────────────┴─────────────────┴─────────────────┘
    ];
    return i;
  end;
end;

private macro isAccNotFound(Acc):bool
  var select = " select t_description from dwlacclnk_dbt where t_account = :ACC ";
  var rs = execSQLselect( select, makeArray(SQLParam("ACC",Acc)), FALSE);
  if( rs.moveNext() )
    return SubStr(strlwr(rs.value("t_Description")), 1, strlen("Счет не найден")) == strlwr("Счет не найден");
  end;
  return false;
end;

private macro GetOp(Acc):string
  var Op = "";
  for( var i, 0, Acc.size()-1 )
    if( isAccNotFound(Acc(i).rec.Account) )
      Op = "X";
      break;
    end;
  end;
  return Op;
end;

macro PrintReportInfoAboutRestAccounts( this: FNSPrint, ReqDate:date )

  var Accounts:TArray;
  Array Acc;
  var rs;
  rs = execSQLSelect("select t_Code from ddp_dep_dbt where t_PartyID = " + int(this.RepBankID));
  rs.moveNext();
  var Department = rs.value(0);

  if( this.Счет.size > 0 )
    Accounts = СортироватьСчета(this.Счет, Department);
  elif( ClientID > 0 )
    Accounts = ВыбратьСчетаКлиента( );
    if( Accounts.size() > 0 )
      this.Счет = Accounts;
      this.FillChanges;
    end;
  end;
  var BlockNum = 1;
  var BlockSubNum = 1;

  this.ЗаголовокСправки_ММ_3_06_178("1114312"); 
  [
                                         Справка
                     об остатках денежных средств на вкладах (депозитах)
  ]; 
  this.ДанныеБанкаИ_Клиента();

  var i = 0;
  //YES
  var Errors = 0;
  for(i, 0, Accounts.size - 1)
    var Description = GetError(Accounts(i).rec.Account, WlRegDecID);
    if(Description) 
      Errors = Errors + 1;
    end;
  end;
  i = 0;
  if(GetYes(Accounts, WlRegDecID) or (Errors < Accounts.size))
  while (i < Accounts.size)
    [                                                                                                                                                                        ┌───┐  
       об остатках денежных по вкладам (депозитам), действовавшим                                                                                                            │ # │   
                                                                                                                                                                             └───┘]
      (GetYes(Accounts, WlRegDecID)); 
    this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(i).rec.Department, ReqDate);
    i = PrintOneDepAcc(this, Accounts, i, ReqDate);
    BlockSubNum = BlockSubNum + 1;
  end;
  end;

  // OP
  var Op = GetOp(Accounts, this.WlRegDecID);
  if(Op)
  [                                                                                                                                                                          ┌───┐
      об отсутствии следующих вкладов (депозитов) указанного лица, дейвствовавших ################# в:                                                                       │ X │
                                                                                  ─────────────────                                                                          └───┘ 
                                                                                                                                                                               
      
    ]( string( "\"", substr(YYYYMMDD(ReqDate),7,2), "\" ",
                     Месяца(substr(YYYYMMDD(ReqDate),5,2)):c, " ",
                     substr(YYYYMMDD(ReqDate),1,4), " г." ));
    i = 0;
    var SeqNumber : Integer = 1;
    while(i < Accounts.size )
      if( isAccNotFound(Accounts(i).rec.Account, this.WlRegDecID))
        StrSplit( Accounts(i).rec.Account, Acc, 1, 1, 20 );   
        [
          ┌─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┬─┐
    ##  № │#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│#│
                └─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┴─┘
          владелец счета ####################################################################################################
                         ────────────────────────────────────────────────────────────────────────────────────────────────────
        ]( SeqNumber,
     Acc(0), Acc(1), Acc(2), Acc(3), Acc(4), Acc(5), Acc(6), Acc(7), Acc(8), Acc(9),
           Acc(10), Acc(11), Acc(12), Acc(13), Acc(14), Acc(15), Acc(16), Acc(17), Acc(18), Acc(19),
           this.GetAccName( Accounts(0).rec.Client )        // AccOwner
   );
        SeqNumber = SeqNumber + 1;
      end;
      i = i + 1;
    end;
  end;
  // NO
  Errors = 0;
  for(i, 0, Accounts.size - 1)
    Description = GetError(Accounts(i).rec.Account, WlRegDecID);
    if( (Description) and (SubStr(strlwr(Description), 1, strlen("Счет не найден")) != strlwr("Счет не найден"))) 
      Errors = Errors + 1;
    end;
  end;
  if( Errors or (Accounts.size == 0) )
  [                                                                                                                                                                          ┌───┐
    об отсутствии вкладов (депозитов) указанного лица, действовавших                                                                                                         │   │
                                                                                                                                                                             └───┘];
   this.ДанныеФилиала(1, 1, Accounts(0).rec.Department, ReqDate);
  end;
  //PS
  [                                                                                                                                                                          ┌───┐
    справка об остатках денежных средств по вкладам (депозитам) в части информации следующих филиалов банка будет представлена ими самостоятельно:                           │ # │
                                                                                                                                                                             └───┘]
  (this.Ps);
  this.ДанныеФилиала(1, 1, Accounts(0).rec.Department, ReqDate);

  ДатаПодписьПредставителяБанка();
  [];
  return 0;
end;
