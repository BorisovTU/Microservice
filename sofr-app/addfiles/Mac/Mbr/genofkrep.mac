/*
  $Name:         genofkrep.mac
  $Module:       МБР
  $Description:  Протокол выполнения процедуры формирования сообщений для ГИС ГМП
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : genofkrep.mac
  Created     : 21.12.2012
  Programmer  : Lazarenko M.
  Description : Протокол выполнения процедуры формирования сообщений для ГИС ГМП

└───────────────────────────────────────────────────────────────────────────*/
import globals, oralib, likepy, WldInter, MesInter, CTInter;

private macro GetOperName( oper:integer ):string

  var fPerson = Tbfile ("person.dbt");
  var Name:string;

  ClearRecord( fPerson.rec );
  fPerson.rec.Oper = Oper;
  if( not fPerson.getEQ () )
    Name = "Не найден текущий пользователь";
  else
    Name = fPerson.rec.Name;
  end;

  return Name;
end;

private macro GetDepName( dep:string ):string

  var query = " select party.t_Name from dparty_dbt party, ddp_dep_dbt dep " +
              " where party.t_PartyID = dep.t_PartyID " +
              " and dep.t_Code = " + dep;
  var rs:RsdRecordset = execSQLselect(query);
  if (rs and rs.moveNext() )
    return rs.Value(0);
  end;

end;

/*Вывести заголовок*/
macro PrintHead()

   var operName:string = GetOperName( {oper} );
[
 Отчет о результатах формирования сообщений для ГИС ГМП

 Исполнитель ##### #
 Дата отчета ##########

 
]
( {oper}, operName, {curdate} );

end;

/* Вывести шапку таблицы для филиала */
macro PrintHeaderTable( dep:string )

  
[ Филиал ########## ####################################################################################################
 ┌─────┬──────────┬───────────────┬──────────┬─────────────────────────┬───────────────────────────────────┬───────────────┬────────────────────┬──────────────────────────────────────────────────┬──────────┬──────────────────────────────────────────────────┐
 │ N   │   Дата   │     Вид       │ID док-та │ Счет плательщика        │        Получатель                 │               │                    │                                                  │   ID     │                                                  │
 │док  │  докум.  │    докум.     │          │                         ├─────────┬─────────────────────────┤     Сумма     │        КБК         │               Назначение платежа                 │сообщения │        Результат обработки                       │
 │     │          │               │          │                         │   БИК   │        Счет             │               │                    │                                                  │          │                                                  │
 ├─────┼──────────┼───────────────┼──────────┼─────────────────────────┼─────────┼─────────────────────────┼───────────────┼────────────────────┼──────────────────────────────────────────────────┼──────────┼──────────────────────────────────────────────────┤]
( dep, GetDepName( dep ) );
end;  

/*Напечатать заголовок ВСП*/
macro PrintBranch( dep )

[│ ВСП ########## ####################################################################################################
 ├───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤]
( dep, GetDepName( dep ) ); 
end;

/*Напечатать отчёт*/
macro PrintGGReport()
  
  var i, j:integer;
  array StringArray;
  var DepPrev:integer = 0;
  var query = "( select ggrep.t_Department as dep, ggrep.t_Branch as br, obj.t_Name, ggrep.t_MesID, rm.t_Number, rm.t_Date,       " 
              "          pm.t_PaymentID, pm.t_PayerAccount, prop.t_BankCode, pm.t_ReceiverAccount,pm.t_BaseAmount, rm.t_BTTTiCode," 
              "          rm.t_Ground, ggrep.t_Result, ggrep.t_ObjectType as tp, ggrep.t_ObjectID as id                            "
              "     from dggrep_tmp ggrep, dobjects_dbt obj, dpmpaym_dbt pm, dpmrmprop_dbt rm, dpmprop_dbt prop                   "
              "       where  ggrep.t_ObjectID = prop.t_PaymentID                                                                  "
              "         and rm.t_PaymentID = pm.t_PaymentID                                   "
              "         and prop.t_PaymentID = pm.t_PaymentID                                 "
              "         and  prop.t_DebetCredit = 1                                                                               "
              "         and ggrep.t_ObjectType = obj.t_ObjectType )                                                               "
              "  union                                                                                                            "
              " ( select ggrep.t_Department as dep, ggrep.t_Branch as br, obj.t_Name, ggrep.t_MesID, chr(1), bdtr.t_Date,         "
              "          bdtr.t_BdTransfID, bdtr.t_PayerAccount, bdtr.t_ReceiverBankCode, bdtr.t_ReceiverAccount, bdtr.t_Amount,  "
              "          bdtr.t_BTTTiCode, bdtr.t_Ground, ggrep.t_Result, ggrep.t_ObjectType as tp, ggrep.t_ObjectID as id        "
              "     from dggrep_tmp ggrep, dobjects_dbt obj, dbdtransf_dbt bdtr                                                   "
              "       where ggrep.t_ObjectID = bdtr.t_BdTransfID                                                                  "
              "         and ggrep.t_ObjectType = obj.t_ObjectType  )                                                              "
              "   order by dep, br, tp, id " ;                                                      
  var rs:RsdRecordset = execSQLselect(query);

  PrintHead();
  while( rs and rs.moveNext() )
  
    if( rs.Value(0) != DepPrev )
      PrintHeaderTable( rs.Value(0) );
      DepPrev = rs.Value(0);
    end;
    if( rs.Value(1) != rs.Value(0) )
      PrintBranch( rs.Value(1) );
    end;

    StrSplit( StrSubst(IfThenElse(rs.value(13)!="",rs.value(13)," ") , "|", " " ), StringArray, 50 );

[│#####│##########│###############│##########│#########################│#########│#########################│###############│####################│##################################################│##########│##################################################│
 ]
( rs.Value(4), date(rs.Value(5)), rs.Value(2), int(rs.Value(6)), rs.Value(7), rs.Value(8), rs.Value(9), rs.Value(10), rs.Value(11), rs.Value(12), rs.Value(3), StringArray(0) );

    j = 1;
    while ( StrLen(StringArray(j) ) > 0)
[│     │          │               │          │                         │         │                         │               │                    │                                                  │          │##################################################│]
(StringArray(j));
    j = j + 1;
    end;

[└─────┴──────────┴───────────────┴──────────┴─────────────────────────┴─────────┴─────────────────────────┴───────────────┴────────────────────┴──────────────────────────────────────────────────┴──────────┴──────────────────────────────────────────────────┘
]
  end;

end;