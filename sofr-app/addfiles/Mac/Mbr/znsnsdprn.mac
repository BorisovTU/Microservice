 /*
 $Name: znsnsdprn.mac
 $Module: МБР
 $Description: Печать справки о наличии вкладов (депозитов)
 */

import "fnsprzall.mac";

private macro PrintOneDepAcc(this: FNSPrint, Accounts: TArray, CurPos: integer)
  if (CurPos < Accounts.Size)
    [
      Таблица 1
      ┌───┬────────────────────┬──────────────┬─────────────────┬─────────────────────────┬─────────────────┬───────────────────────────────────────────────────────┬───────────────┐
      │ № │     № счета        │    вид       │  дата открытия  │ дата истечения действия │  дата закрытия  │       дата изменения реквизитов вклада (депозита)     │ цифровой код  │
      │п/п│    по вкладу       │   вклада     │вклада (депозита)│     вклада (депозита)   │вклада (депозита)│ (дд.мм.гг.); № измененного счета по вкладу (депозиту) │ валюты вклада │
      │   │   (депозиту)       │ (депозита)   │   (дд.мм.гг)    │        (дд.мм.гг)       │    (дд.мм.гг)   │             (до изменения / после изменения)          │   (депозита)  │
      ├───┼────────────────────┼──────────────┼─────────────────┼─────────────────────────┼─────────────────┼───────────────────────────────────────────────────────┼───────────────┤
      │ 1 │         2          │      3       │        4        │            5            │        6        │                           7                           │       8       │
      ├───┼────────────────────┼──────────────┼─────────────────┼─────────────────────────┼─────────────────┼───────────────────────────────────────────────────────┼───────────────┤
    ];
    var i = CurPos;
    var Dep = Accounts(i).rec.Department;
    var Account,
        TypeAcc,
        DateOpen,
        DepositDateEnd,
        DateClose,
        Change,
        FICode;
        
    while( (i < Accounts.Size) and (Accounts(i).rec.Department == Dep) )
      if( this.Счет.size > 0 )
        Account = Accounts(i).rec.Account;
        TypeAcc = Accounts(i).rec.TypeAcc;
        DateOpen = Accounts(i).rec.DateOpen;
        DepositDateEnd = Accounts(i).rec.DepositDateEnd;
        DateClose = Accounts(i).rec.DateClose;
        Change = Changes[i];
        FICode = GetFICode( Accounts(i).rec.FIID );
      elif( this.ClientID > 0 )
        Account = Accounts(i).rec.Account;
        TypeAcc = GetNameTypeAcc( Accounts(i).rec.Type_Account, Accounts(i).rec.Code_Currency );
        DateOpen = Accounts(i).rec.Open_Date;
        DepositDateEnd = GetDepositDateEnd( Accounts(i) );
        DateClose = Accounts(i).rec.Close_Date;
        Change = FillAcc( Account, Accounts(i).rec.Code_Currency );
        FICode = GetFICode( Accounts(i).rec.Code_Currency );
      end;
      if( strlen(GetError(Accounts(i).rec.Account, WlRegDecID)) == 0 )
        [ │###│####################│##############│#################│#########################│#################│#######################################################│###############│
        ]( i+1:c,
           Account:c,
           TypeAcc:c,
           DateOpen:c,
           DepositDateEnd:c,
           DateClose:c,
           Change:c,
           FICode:c
         );
      end;
      i = i + 1;
    end;
    [ └───┴────────────────────┴──────────────┴─────────────────┴─────────────────────────┴─────────────────┴───────────────────────────────────────────────────────┴───────────────┘
    ];
    return i;
  end;
end;

macro PrintReportInfoAboutExistenceAccounts( this: FNSPrint, ReqDate:date )

  this.ЗаголовокСправки_ММ_3_06_178("1114311");
  [
                                           Справка
                                 о наличии вкладов (депозитов)
  ];

  this.ДанныеБанкаИ_Клиента();

  var Department;
  var BlockNum = 1;
  var BlockSubNum = 1;
  var rs;
  var Accounts:TArray = TArray();
  rs = execSQLSelect("select t_Code from ddp_dep_dbt where t_PartyID = " + int(this.RepBankID));
  rs.moveNext();
  Department = rs.value(0);
  if( this.Счет.size > 0 )
    Accounts = СортироватьСчета(this.Счет, Department);
  elif( this.ClientID > 0 )
    Accounts = this.ВыбратьСчетаКлиента( );
    if (Accounts.size > 0)
      Department = Accounts(0).rec.Department;
    end;
  end;
  // для отчета дата "По состоянию на.." может быть задана отдельно
  if( this.ПоСостояниюНаДату != date( 0, 0, 0 ) )
    ReqDate = this.ПоСостояниюНаДату;
  end;
  
  var i = 0;
  var DepInfo: string = "";
  //YES
  var Errors = 0;
  for(i, 0, Accounts.size - 1)
    var Description = GetError(Accounts(i).rec.Account, WlRegDecID);
    if(Description) 
      Errors = Errors + 1;
    end;
  end;
  i = 0;
  if(GetYes(Accounts, WlRegDecID) or (Errors < Accounts.size))                                                                                                                 
    [ о вкладах (депозитах) указанного лица, действовавших                                                                                                                     ┌───┐
                                                                                                                                                                               │ # │
                                                                                                                                                                               └───┘]
    (GetYes(Accounts, WlRegDecID)); 
    while (i < Accounts.size)
      this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(i).rec.Department, ReqDate);
      i = PrintOneDepAcc(this, Accounts, i);
      BlockSubNum = BlockSubNum + 1;
    end;
  end;
  // NO
  if( not GetYes(Accounts, WlRegDecID) )
     [                                                                                                                                                                          ┌───┐
      об отсутствии вкладов (депозитов) указанного лица, действовавших                                                                                                          │ X │
                                                                                                                                                                                └───┘];
      BlockSubNum = 0;
      this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(0).rec.Department, ReqDate);
  end;
  //PS
  if(this.Ps)
     [
                                                                                                                                                                                ┌───┐
      справка о наличии вкладов (депозитов) в части информации следующих филиалов банка будет представлена ими самостоятельно:                                                  │ # │
                                                                                                                                                                                └───┘]
      (this.Ps);
      this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(0).rec.Department, ReqDate);
  end;

  this.ДатаПодписьПредставителяБанка();
  [];
  return 0;
end;
