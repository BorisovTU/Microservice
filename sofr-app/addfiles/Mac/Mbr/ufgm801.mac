/*
  $Name:         ufgm801.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED801 транспорта УФЭБС
*/

import BankInter, CTInter, oralib, MesInter, WldInter, ufgenmes, bnk_ptlib, bnk_common;

private macro GetInfoTypeCode( Queries )
  var InfoTypeCode : string = "";

  var select = " select REGEXP_SUBSTR( :Queries, '\\/\\w{4}\\/' ) as t_InfoTypeCode from dual ";

  var params = makeArray( SQLParam( "Queries", Queries ) );
                        
  var rs = execSQLselect( select, params );
  
  if( rs and rs.moveNext() )
    InfoTypeCode = rs.value("t_InfoTypeCode");
  end;

  if( (strlen( InfoTypeCode ) == 6) and Index( InfoTypeCode, "/" ) )
    InfoTypeCode = SubStr( InfoTypeCode, 2, strlen( InfoTypeCode ) - 2 );
  else
    InfoTypeCode = "";
  end;

  return InfoTypeCode;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);
  
  SetBuff( wlReq, addrReq );
  SetBuff( wlmes, addrMes );

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  
  ЗаписатьПолеЛог ( "EDNo",     ed_nda.EDNo, TRUE );
  ЗаписатьПолеЛог ( "EDDate",   YYYYMMDD(ed_nda.EDDate), TRUE );
  ЗаписатьПолеЛог ( "EDAuthor", ed_nda.EDAuthor, TRUE );

  var InfoTypeCode = GetInfoTypeCode( wlreq.Queries );
  if( InfoTypeCode == "" )
    RunError("Не задан вид запроса информации. Выберите нужное значение из справочника в поле <Сообщение>");       
  else
    ЗаписатьПолеЛог ( "InfoTypeCode", InfoTypeCode, TRUE );
  end;

  var PoolInfoFlag : string = "0";
  ЗаписатьПолеЛог ( "PoolInfoFlag", PoolInfoFlag, TRUE );


  var BIC : string = "";
  GetPartyCodeEx( GetDprtPartyID( Bnk_GetCorschemDepartment( wlReq.Corschem, wlReq.FIID ) ), PTCK_BIC, @BIC );
  ЗаписатьПолеЛог ( "BIC", BIC, TRUE );

  var CorAccount = Bnk_GetCorschemCorAccount( wlReq.Corschem, wlReq.FIID );
  ЗаписатьПолеЛог ( "Account", CorAccount, TRUE );

  return true; /*Успешное завершение*/
  
  OnError(er)
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;