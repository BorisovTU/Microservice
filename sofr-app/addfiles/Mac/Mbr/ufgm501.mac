/*
  $Name:         ufgm501.mac
  $Module:       Межбанковские расчеты
  $Description:  Генерация сообщения ED501 транспорта УФЭБС
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения ED501 транспорта УФЭБС
//
// Программист  : Чукина Т.А.
//
// Создан       : 17.12.2014
//
//-----------------------------------------------------------------------------

import "ufgenmes.mac", CTInter, "oralib.mac", MesInter, WldInter;

private macro GetEDReceiverForED501(wlinfo) : string
  return GetEDReceiver(wlinfo.RecipientID, wlinfo.RecipientCodeKind, wlinfo.RecipientCode);
end;

private macro WlinfoHasAttachedObjects(InfoID : integer) : bool
  var rs : RsdRecordset = execSQLselectPrm
  ( "select 1 "
    "  from dimgdata_dbt "
    " where t_ObjectType = :OBJTYPE_WLD_INFO "
    "   and t_ObjectID   = :sInfoID ",
    SQLParam( "ObjectType", OBJTYPE_INFO ),
    SQLParam( "ObjectID", GetWlinfoStrID(InfoID) )
  );

  if(rs and rs.moveNext())
    return TRUE;
  end;

  return FALSE;
end;

macro GenMes( addrMes, addrInfo )
  record wlmes(wlmes);
  record wlinfo(wlinfo);

  SetBuff( wlmes, addrMes );
  SetBuff( wlinfo, addrInfo );

  var xml : object = ActiveX("Microsoft.XMLDOM");  
  var mes : object = xml.createElement("ED501");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);
  mes.setAttribute("ActualReceiver", GetEDReceiverForED501(wlinfo) );

  var elem : object = null;

  // ProprietoryDocument
  elem = xml.createElement("ProprietoryDocument");  
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  // ProprietoryAttachment
  var Narrative : string = "";
  if(not WlinfoHasAttachedObjects(wlinfo.InfoID))
    Narrative = ПрочитатьТекстИнфСообщения(wlinfo);
  end;
  elem = xml.createElement("ProprietoryAttachment");  
  elem.appendChild(xml.createTextNode(Narrative));
  mes.appendChild(elem);

  if(wlinfo.RelatedRef)
    elem = xml.createElement("InitialED");  
    FillEDNoDateAuthorByRef_XMLField(elem, wlinfo.RelatedRef);
    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
