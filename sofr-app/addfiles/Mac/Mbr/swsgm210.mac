/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT-SB MT210                                    */
/*                                                                          */
/*  Имя файла: swsgm210.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgmes.mac";

RECORD Corschem( corschem );

macro GenMes( addrMes, addrHead )
  var field_value, dKflag, dKnum, CUR, amountSWIFT, dateSWIFT, oldKeyNum, error;
  var StatementLine, TpID, NameForm, stat, KindPageExt, CorrID, oneConf = true;

  SetBuff( wlmes,  addrMes );
  SetBuff( wlhead, addrHead );

  SB_CheckFIID( wlhead.FIID );

  PrintLog(2,"Генерация сообщения по МТ210");

  /* SN - уникальный системный номер сообщения в СМФР */
  ЗаписатьПолеЛог(УникальныйНомерСообщенияСМФР, wlmes.TRN);

  /* RN - уникальный системный номер исходного сообщения в СМФР */
  if (wlmes.RelatedRef != "")
    ЗаписатьПолеЛог(НомерСвязанногоСообщенияСМФР, wlmes.RelatedRef);
  end;

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );
  
  /* 25 - счет уведомления */
  /* Это поле заполняется только если на корреспондента настроено
     несколько корсхем                                             */
  /* Определяем корреспондента */

  if (НайтиКорсхемуНеЕдинственнуюУКонтрагента(wlhead.Account, wlhead.FIID, Corschem))
    ЗаписатьПолеЛог( AccountIdentificationField, wlhead.Account );
  end;

  /* 30 - Дата валютирования */
  ЗаписатьПолеЛог( ValueDateField, GetSWIFTDate(wlhead.DateIn) );
  
  /******************** Заполняем документы выписки ******************/
  УстановитьФильтрДокументовВыписки(wlhead.HeadID);
  while( ПолучитьДокументВыписки(wlConf) )
     if(not oneConf)
         RunError("|Для уведомления должно быть не более одного документа");
     end;
     oneConf = false;
     if ( wlConf.RelatedRef )
        ЗаписатьПолеЛог( RelatedReferenceField, wlConf.RelatedRef );
     else
        ЗаписатьПолеЛог( RelatedReferenceField, СсылкаОтсутствует );
     end;

     ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_B, ПолучитьКодВалютыЛог( wlConf.FIID )+GetSWIFTAmount( wlConf.Sum ) );

     /* 52a - банк приказодателя */
     if( (wlconf.CodeValue!="") OR (wlconf.BankName!="") )
       ClearRecord(Route);
       if((wlconf.BankID == {OurBank}))
           error = ПолучитьНашКодСУчетомТС(PTCK_SWIFT, true, Route.CodeValue, Route.PartyID);
           if(error)
       Route.PartyID  = wlconf.BankID;
       Route.CodeKind = wlconf.CodeKind;
       Route.CodeValue = wlconf.CodeValue;
           else
               Route.CodeKind = PTCK_SWIFT;
           end;
       else
          Route.PartyID  = wlconf.BankID;
          Route.CodeKind = wlconf.CodeKind;
          Route.CodeValue = wlconf.CodeValue;
       end;
       Route.Name = ПолучитьИмяБанка(Route.PartyID);
       ЗаписатьПолеМаршрутаЛог_SB( OrderingInstitutionField, "52a", Route );
       if( not УстановитьКонтекстБлока( ".." ) )
         RunError("|Ошибка при установке контекста блока ..");
       end;
     end;     
  
     /* 56a - банк приказодателя */
     if( (wlconf.CorrCodeValue!="") OR (wlconf.CorrBankName!="") )
       ClearRecord(Route);
       Route.PartyID  = wlconf.CorrBankID;
       Route.CodeKind = wlconf.CorrCodeKind;
       Route.CodeName = wlconf.CorrCodeName;
       Route.CodeValue = wlconf.CorrCodeValue;
       Route.Name = wlconf.CorrBankName;
       ЗаписатьПолеМаршрутаЛог_SB( IntermediaryField, "56a", Route, "", "", ST_UNDEF, true );
     end; 
  end;
  /*******************************************************************/  

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;