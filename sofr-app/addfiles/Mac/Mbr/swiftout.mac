 /*
 $Name: swiftout.mac
 $Module: Межбанковские расчеты
 $Description: Экспорт сообщений
 */

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                          Экспорт сообщений                               */
/*                                                                          */
/*  Имя файла: swiftout.mac                                                 */
/*  Создан:  29.02.00                                         Бабин А.П.    */
/****************************************************************************/

import "wltools.mac", "wlexport.mac", "swtools.mac", oralib, likepy;

const TURBO_SWIFT_HEADER72 = true,
      TURBO_SWIFT_DECIMAL  = true;

private const TRANSP_SPFSCBR = 51; //А.Киселев 29.09.2019 пользовательский транспорт СПФСЦБРФ

var ВидТерминала;
var НомерСессии, ISN;

var out = TArray;



//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.
private macro GetObjParm( MessId :integer, ObjId :@integer, ObjKind :@integer ) :integer
var cmd,
    Query :string = "";


  Query = " DECLARE " +
          "  v_ObjId      dwlmeslnk_dbt.T_OBJID%TYPE; " +
          "  v_ObjKind    dwlmeslnk_dbt.T_OBJKIND%TYPE; " +
          "  v_MessId     ddvndeal_dbt.T_ID%TYPE := :p_MessId; " +
          "  p_ObjId      dwlmeslnk_dbt.T_OBJID%TYPE; " +
          "  p_ObjKind    dwlmeslnk_dbt.T_OBJKIND%TYPE; " +

          " BEGIN " +
          "  BEGIN " +

          "   SELECT A.T_OBJID, A.T_OBJKIND INTO v_ObjId, v_ObjKind FROM dwlmeslnk_dbt A " +
          "   WHERE A.T_MESID = v_MessId; " +
//если объект-подтверждение из МБК          
          "   IF v_ObjKind = 512 THEN " +

          "     SELECT T_DEALID, 103 INTO v_ObjId, v_ObjKind FROM dwldlmm_dbt " +
          "     WHERE T_DLMMID = v_ObjId; " +

          "   END IF; " +


          "   :p_ObjId := v_ObjId; " +
          "   :p_ObjKind := v_ObjKind; " +


          "  EXCEPTION " +
          "   WHEN OTHERS THEN " +
          "   BEGIN " +
          "    :p_ObjId := -1; " +
          "    :p_ObjKind := -1; " +
          "   END; " +
          "  END; " +

          " END; ";

  cmd = RSDCommand(Query);
  cmd.AddParam("p_MessId", RSDBP_IN, MessId );
  cmd.AddParam("p_ObjId", RSDBP_OUT, V_INTEGER, 0 );
  cmd.AddParam("p_ObjKind", RSDBP_OUT, V_INTEGER, 0 );
  cmd.execute();

  if( (ValType(cmd.value("p_ObjId")) == V_UNDEF) or (ValType(cmd.value("p_ObjId")) == 26))
   ObjId = -1;
   return 1;
  else
   ObjId = cmd.value("p_ObjId");
  end;

  if( (ValType(cmd.value("p_ObjKind")) == V_UNDEF) or (ValType(cmd.value("p_ObjKind")) == 26))
   ObjKind = -1;
   return 1;
  else
   ObjKind = cmd.value("p_ObjKind");
  end;
 
  return 0;

onError(err)
  ObjId = -1;
  ObjKind = -1;
  return 1;
end;
//А.Киселев 01.04.2019 Ищем ИД сделки/платежа и т.д.


//А.Киселев 01.04.2019 Ищем ИД транспорта ответа по сделке/платежа и т.д.
private macro GetTpIdByObjParm( ObjId, ObjKind ) :integer
var Params :TArray,
    DataSet :RsdRecordset,
    Query :string = "";

  if( ( ObjKind == 148 ) or ( ObjKind == 145 ) ) //сделки ФИСИКО
   Query = " SELECT T_CONFTPID FROM ddvndeal_dbt " +
           " WHERE T_ID = :ObjId ";
  elif( ( ObjKind == 103 ) )  //сделки МБК
   Query = " SELECT T_CONFTPID FROM ddl_tick_dbt " +
           " WHERE T_DEALID = :ObjId ";
  elif( ( ObjKind == 108 ) ) //сделки НЕТТИНГА
   Query = " SELECT T_CONFIRMTRANSPORT FROM ddl_nett_dbt " +
           " WHERE T_NETTINGID = :ObjId ";
  end;
 
  if( Query != "" )
   Params = Null;
   Params = makeArray( SQLParam("ObjId", ObjId) );
   DataSet = execSQLSelect( Query, Params );
   if( DataSet.MoveNext )
    return DataSet.value( 0, Null, V_INTEGER );
   else
    return -1;
   end;
   Params = Null;
  else
   return -1;
  end;

onError(err)

 Params = Null;
 return -1;

end;
//А.Киселев 01.04.2019 Ищем ИД транспорта ответа по сделке/платежа и т.д.



macro InitOut()
    out.size = 1;
    out( 0 ) = "";
end;

macro НоваяСтрока( buf:string )
    if ( buf!="" )
       /* Пустые строки не вставляем */
       out( out.size-1 ) = out( out.size-1 ) + string(buf);
       out( out.size ) = "";
    end;
    return true;
end;

macro СохранитьСтроку( buf:string )
    while( Index( buf, "\n" ) )
       НоваяСтрока( substr(buf, 1, Index( buf, "\n" )-1) );
       buf = substr( buf, Index( buf, "\n" )+1 );
    end;
    НоваяСтрока( buf );
    return true;
end;

macro СохранитьБлок( buf:string )
    while( Index( buf, "\n" ) )
       НоваяСтрока( substr(buf, 1, Index( buf, "\n" )-1) );
       buf = substr( buf, Index( buf, "\n" )+1 );
    end;
    out( out.size-1 ) = out( out.size-1 ) + string(buf);
    return true;
end;

macro ВычислитьРазмерИнформации()
    var count = 0, size = 0;

    while( count<out.size )
        size = size + strlen( out(count) );
        if ( count<(out.size-1) )
           size = size + 2;  /* Переход на другую строку */
        end;
        count = count + 1;
    end;

    return size;
end;

macro СохранитьИнформациюВФайле()
    var count = 0, continue0 = true;

    while( continue0 AND (count<out.size) )
        if ( count<(out.size-1) )
           if ( not ЗаписатьСтроку(out(count)) )
              ErrExport("Ошибка записи строки: " + out(count));
              continue0 = false;
           end;
        else
           if ( not ЗаписатьБлок(out(count)) )
              ErrExport("Ошибка записи блока: " + out(count));
              continue0 = false;
           end;
        end;
        count = count + 1;
    end;

    InitOut();

    return continue0;
end;

macro WldFindPaym( MesID )
  var PaymentID : integer = GetMesPaymentID(MesID);
                                 
  if ( not PaymentID )
    return false;
  else
    if( FindPayment( PaymentID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
      return false;
    end;
  end;
  return true;
end;

/* Определить код валюты */
macro ПолучитьКодВалюты( FIID )
  f_wlfininstr.FIID = FIID;
  if( not getEQ( f_wlfininstr ) )
    ErrExport( "Код финансового инструмента "+FIID+" не найден в справочнике" );
  end;
  return f_wlfininstr.Ccy;
end;

/******************************************************************/
/*            Заголовок сообщения по TURBO SWIFT                  */
/******************************************************************/
macro ЗаписатьAAIHeader()
    var AAIHeader, headerSize;

    macro СформироватьСтрокуРазмера(headSize, InfoSize, useDecimal )
        const Table = "0123456789ABCDEF";
        var numSize = headSize + InfoSize - 4, strSize, dig;

        if ( useDecimal==false )
            strSize = "";
            while( numSize )
               dig = (numSize - (int(numSize/16))*16);
               strSize = substr(Table, dig+1, 1) + strSize;
               numSize = int(numSize/16);
            end;
        else
            strSize = string( numSize );
        end;
        while( strlen(strSize)<4 ) strSize = string( "0", strSize ); end;
        if ( strlen(strSize)>4 )
           strSize = "****";
        end;
        return strSize;
    end;

    if ( TURBO_SWIFT_HEADER72==true )
       headerSize = 72;
    else
       headerSize = 210; 
    end;

     /* Размер AAI Message */
    AAIHeader = СформироватьСтрокуРазмера(headerSize, ВычислитьРазмерИнформации(), TURBO_SWIFT_DECIMAL );
    if ( AAIHeader=="****" )
       ErrExport( "Превышение размера сообщения. Используйте 16-ричную систему счисления" );
       return FALSE;
    end;
    AAIHeader = AAIHeader + "SWI";
    /* Optional User Key */
    AAIHeader = AAIHeader + "                    ";
    /* DUP Flag */
    AAIHeader = AAIHeader + "0"; 
    /* Department Name (по умолчанию "KORR    ") */
    AAIHeader = AAIHeader + "KORR    ";
    /* Queue Name (по умолчанию "_VER1   ")      */
    AAIHeader = AAIHeader + "_VER1   ";

    if ( (TURBO_SWIFT_HEADER72==false) AND WldFindPaym(wlmes.MesID) )
       while( strlen(AAIHeader)<72 ) AAIHeader = string( AAIHeader, " " ); end;

       AAIHeader = AAIHeader + substr( GetSWIFTDate(wlpmpaym.ValueDate), 1, 6 );
       AAIHeader = AAIHeader + substr( ПолучитьКодВалюты( wlpmpaym.PayFIID ), 1, 3 );
       AAIHeader = AAIHeader + substr( GetSWIFTAmount( wlpmpaym.amount ), 1, 21 );

       while( strlen(AAIHeader)<102 ) AAIHeader = string( AAIHeader, " " ); end;
    end;

    while( strlen(AAIHeader)<headerSize ) AAIHeader = string( AAIHeader, " " ); end;

    if( not ЗаписатьБлок( AAIHeader ) )
       ErrExport( "Ошибка при записи начала сообщения" );
       return FALSE;
    end;

    return true;
end;

/***************************************************************************/
/* Сформировать блок 1 заголовка Basic Header Block                        */
/***************************************************************************/
macro ЗаписатьBasicHeaderBlock()
  var BankCode, Destination, BranchCode, НомерТерминала;
  var error;

  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  if( error ) ErrExport( "Не найден SWIFT-код Отправителя сообщения! " ); return FALSE; end;

  Destination = SubStr(BankCode,1,Len_BIC_Destination);
  if(StrLen(Destination) != Len_BIC_Destination) ErrExport( "Неправильно указан Отправитель сообщения (BIC)!" ); return FALSE; end;
  BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);

/* ВР. Проверка не нужна из-за выгрузки Телекса по каналу СВИФТа
  if( (BranchCode!="")AND(StrLen(BranchCode) != Len_BIC_BranchCode) ) ErrExport( "Неправильно указан Отправитель сообщения (код отделения)!" ); return FALSE; end;
*/

  if(BranchCode=="") BranchCode=MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode); end;
  /* !!! Код приложения всегда FIN, ApplicationID=01 */

  if( ВидТерминала == TPFRMT_PCC )
     НомерТерминала = "A";
     if(НомерСессии == NULL)
       НомерСессии = ".SS.";
     end;
     if(ISN == NULL)
       ISN =  ".SEQ..";
     end;
  else
     НомерТерминала = НомерТерминалаПоУмолчанию;
     if(НомерСессии == NULL)
       НомерСессии = НомерСессииПоУмолчанию;
     end;
     if(ISN == NULL)
       ISN = ISNПоУмолчанию;
     end;
  end;

  if( not СохранитьБлок(
      КодНачалаБлока+НомерБлокаBasicHeader+КодРазделительНомера+
      КодПриложенияFIN+НомерПриложенияFIN+Destination+НомерТерминала+
      BranchCode+НомерСессии+ISN+КодКонцаБлока) )
     ErrExport( "Ошибка при записи блока Basic Header" ); return FALSE; end;
  return TRUE;
end;

/***************************************************************************/
/* Сформировать блок 2 заголовка Application Header Block                  */
/***************************************************************************/
macro ЗаписатьApplicationHeaderBlockMIR()
  var BankCode, Destination, BranchCode, Delivery, Priority, НомерФормы, Категория;
  var error;

  if ( (wlmes.OutsideAbonentCodeKind==PTCK_SWIFT) AND (wlmes.OutsideAbonentCode!="") )
     BankCode = wlmes.OutsideAbonentCode;
     error = 0;
  else
     BankCode = ПолучитьОткрытыйКодСубъекта( wlmes.OutsideAbonentID, PTCK_SWIFT, error );
  end;
  if( error ) ErrExport( "Не найден SWIFT-код Получателя сообщения!" ); return FALSE; end;
  Destination = SubStr(BankCode,1,Len_BIC_Destination);
  if(StrLen(Destination) != Len_BIC_Destination) ErrExport( "Неправильно указан Получатель сообщения (BIC)!" ); return FALSE; end;
  BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);

/* ВР. Проверка не нужна из-за выгрузки Телекса по каналу СВИФТа
  if((BranchCode!="")AND(StrLen(BranchCode) != Len_BIC_BranchCode)) ErrExport( "Неправильно указан Получатель сообщения (код отделения)!" ); return FALSE; end;
*/

  if(BranchCode=="") BranchCode=MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode); end;

  if( wlmes.Importance == 0 ) 
     Priority = КодПриоритетНормальный;
  else
     Priority = КодПриоритетСрочный;
  end;

  Delivery = wlmes.DeliveryNotification;
  if(Delivery==0)
    Delivery = "";
  else
    Delivery = string(Delivery);
  end;

  if( not DefineFormExport( wlmes.RlsFormID ) )
    return FALSE;
  end;

  НомерФормы = substr(ФормаЭкспорт.Name,1,3);

  if( not СохранитьБлок(
    КодНачалаБлока+НомерБлокаApplicationHeader+КодРазделительНомера+КодИсходящего+НомерФормы
    +Destination+СимволBICПоУмолчанию+BranchCode+Priority+Delivery+КодКонцаБлока)
    )
     ErrExport( "Ошибка при записи блока Application Header" );
     return FALSE;
  end;
  return TRUE;
end;

/***************************************************************************/
/* Сформировать блок 3 заголовка User Header Block                         */
/***************************************************************************/
macro ЗаписатьUserHeaderBlock()
  var BlockStandart = "";

  if( substr(РелизЭкспорт.Name, strlen(РелизЭкспорт.Name)+1-strlen(ЗначениеRUR5), strlen(ЗначениеRUR5))==ЗначениеRUR5 )
    BlockStandart = КодНачалаБлока+НомерПоляStandart+
                    КодРазделительНомера+ЗначениеRUR5+КодКонцаБлока;
  end;

  if( substr(РелизЭкспорт.Name, strlen(РелизЭкспорт.Name)+1-strlen(ЗначениеRUR6), strlen(ЗначениеRUR6))==ЗначениеRUR6 )
    BlockStandart = КодНачалаБлока+НомерПоляStandart+
                    КодРазделительНомера+ЗначениеRUR6+КодКонцаБлока;
  end;

  if( РелизЭкспорт.Name == НазваниеФормы103Plus )
    BlockStandart = BlockStandart + КодНачалаБлока+НомерПоляSTP+
                    КодРазделительНомера+ЗначениеSTP+КодКонцаБлока;
  end;
  
  if( index(РелизЭкспорт.Name, "COV")  != 0 )
    BlockStandart = BlockStandart + КодНачалаБлока+НомерПоляSTP+
                    КодРазделительНомера+"COV"+КодКонцаБлока;       
  end;

  if( wlmes.TrackNumber != "" )
    BlockStandart = BlockStandart + КодНачалаБлока + FieldNumberUETETR +
                    КодРазделительНомера + wlmes.TrackNumber + КодКонцаБлока;
  end;

  if( not СохранитьБлок(
    КодНачалаБлока+НомерБлокаUserHeader+КодРазделительНомера+BlockStandart+КодНачалаБлока
    +НомерПоляMUR+КодРазделительНомера+ЗначениеMURПоУмолчанию+КодКонцаБлока+КодКонцаБлока)
    )
     ErrExport( "Ошибка при записи блока User Header" );
     return FALSE;
  end;
  return TRUE;
end;

private class TSwLongFields(/* fname1, fname2, ... */)
  class TLongField(fname : string)
    var FieldName    : string = fname,
        IsFNameSaved : bool   = false;
  end;

  var flds : TArray = TArray();

  // имя поля сохранено в файл?
  macro IsFNameSaved(fname : string) : bool
    for (var fld, flds)
      if(fld.FieldName == fname)
        return fld.IsFNameSaved;
      end;
    end;

    return false;
  end;

  // пометить, что ися поля сохранено в файле, если оно входит в список длинных полей
  macro MarkSavedIfLongField(fname : string)
    var i = 0;
    for (i, 0, flds.size - 1)
      if(flds[i].FieldName == fname)
        flds[i].IsFNameSaved = true;
        break;
      end;
    end;
  end;

  // конструктор
  private var i = 1, field_name = "";
  while( GetParm(i, field_name) )
    flds.value(i-1) = TLongField(field_name);
    i = i + 1;
  end;
end;

macro СохранитьПоляСообщения()
   var field, buff, str;
   var SwLongFields = TSwLongFields("45A", "45B", "46A", "46B", "47A", "47B");

   while( СчитатьПоле( field, buff ) )   
      if ( (field==CopyMandatoryFields) or SwLongFields.IsFNameSaved(field) )
          str = string(buff);
      else
          str = string(":",field, ":", buff);
          SwLongFields.MarkSavedIfLongField(field);
      end;
      if( not СохранитьСтроку( str ) )
          ErrExport("Ошибка записи поля сообщения" + string(field));
          return false;
      end;
   end;
   return true;
end;

/***************************************************************************/
/* Сформировать блок 4 - текст сообщения                                   */
/***************************************************************************/
macro ЗаписатьTextBlock()
  if( not СохранитьСтроку(
      КодНачалаБлока+НомерБлокаText+КодРазделительНомера)
    )
      ErrExport( "Ошибка при записи текстового блока" );
      return FALSE;
  end;
  if( not СохранитьПоляСообщения() )
     return FALSE;
  end;
  if( not СохранитьБлок( КодКонцаТекстовогоБлока+КодКонцаБлока ) )
     ErrExport( "Ошибка при записи текстового блока" );
     return FALSE;
  end;
  return TRUE;
end;

/***************************************************************************/
/*  Функция формирования сообщения SWIFT                                   */
/*  Возвращает:                                                            */
/*             TRUE или FALSE                                              */
/***************************************************************************/
macro ЗаписатьСообщение(NeedSaveToFile: Bool)

  if (NeedSaveToFile == NULL)
    NeedSaveToFile = True;
  end;

  InitOut();

  if( ВидТерминала == TPFRMT_PCC )
     if( not СохранитьБлок( КодНачалаСообщения ) )
        ErrExport( "Ошибка при записи кода начала сообщения" );
        return FALSE;
     end;
     if( not ЗаписатьBasicHeaderBlock()          ) return false; end;
     if( not ЗаписатьApplicationHeaderBlockMIR() ) return false; end;
     if( not ЗаписатьUserHeaderBlock()           ) return false; end;
     if( not ЗаписатьTextBlock()                 ) return false; end;
     if( not СохранитьБлок( КодКонцаСообщения ) )
       ErrExport( "Ошибка при записи кода конца сообщения" );
       return FALSE;
    end;
  else
     if( not ЗаписатьBasicHeaderBlock()          ) return false; end;
     if( not ЗаписатьApplicationHeaderBlockMIR() ) return false; end;
     if( not ЗаписатьUserHeaderBlock()           ) return false; end;
     if( not ЗаписатьTextBlock()                 ) return false; end;
  end;

  if ( ВидТерминала == TPFRMT_TURBOSWIFT )
     if ( not ЗаписатьAAIHeader()         ) return false; end;
  end;

  if (NeedSaveToFile)
     if ( not СохранитьИнформациюВФайле() ) return false; end;
  end;

  return TRUE;
end;

/***************************************************************************/
/* Функция формирования разделителей сообщений SWIFT в файле               */
/***************************************************************************/
macro ЗаписатьРазделительСообщений()
 var count;

 if( ВидТерминала == TPFRMT_PCC )
    /* Дополним сообщение пробелами до размера, кратного 512 байт */
    count = int(ТекущийРазмерФайла() / 512);
    if( not ЗаписатьБлок( MkStr( " ", (count+1) * 512 - ТекущийРазмерФайла() ) ) )
       ErrExport( "Ошибка при дополнении файла пробелами" );
       return FALSE;
    end;
 elif ( ВидТерминала == TPFRMT_RJE )
    /*if( not ЗаписатьСтроку( КодРазделительСообщений ) )*/
    if( not ЗаписатьБлок( КодРазделительСообщений ) )
       ErrExport( "Ошибка при записи разделителя сообщений" );
       return FALSE;
    end;
 end;

 return TRUE;
end;

/***************************************************************************/
/* Функция формирования заголовка файла сообщения SWIFT                    */
/***************************************************************************/
macro ЗаписатьЗаголовок()

 if ( ВидТерминала == TPFRMT_TURBOSWIFT )
    if( not ЗаписатьБлок( FormatCode ) )
       ErrExport( "Ошибка при записи кода формата" );
       return FALSE;
    end;
 end;

 return TRUE;

end;

/***************************************************************************/
/* Функция формирования кода конца файла сообщения                         */
/***************************************************************************/
macro ЗаписатьКонцовку()
 var count;

 if( ВидТерминала == TPFRMT_PCC )
    /* Дополним файл пробелами до размера, кратного 512 байт */
    count = int(ТекущийРазмерФайла() / 512);
    if( not ЗаписатьБлок( MkStr( " ", (count+1) * 512 - ТекущийРазмерФайла() ) ) )
       ErrExport( "Ошибка при дополнении файла пробелами" );
       return FALSE;
    end;
 end;

 return TRUE;
end;

private class Pair( message_record, message_text)
    var message_record_ = TRecHandler( "wlmes.dbt" );
    copy( message_record_, message_record );
    var message_text_ = TArray();
    var item : string;    
    for( item, message_text )
      message_text_[ message_text_.Size() ] = item;
    end;
end;

// Формирование блока S
private macro FormBlockS( messages ) : bool
  var item : Pair;
  var rs : RsdRecordset;
  var ids = TArray();
  var texts = TArray();
  var error = 0;
  
  for( item, messages )
    rs = execSQLselect( 
                        " SELECT WLTPSHEM.T_SWFORMSIGN " +
                        "  FROM DWLTPSHEM_DBT WLTPSHEM " +
                        " WHERE WLTPSHEM.T_TPSHEMID = :TPSHEMID ",
                       makeArray(
                        SQLParam("TPSHEMID", item.message_record_.rec.TpschemID)
                        ) );
    if( rs and rs.MoveNext() and (rs.value(0) == "X") )
      ids[ ids.Size() ] = string(item.message_record_.rec.MesID);
      texts[ texts.Size() ] = ToANSI(trim(join(item.message_text_, "\r\n")), true);
      item.message_text_ = makeArray( texts[ texts.Size() - 1 ] );
    else      
      item.message_text_ = makeArray( ToANSI(trim(join(item.message_text_, "\n")), true) );
    end;
  end;
  
  if( ids.Size() )
    var mdgs = TArray();
    var out_ids = TArray();
    var error_text = "";
    
    //вызвать функцию формирования метки подлинности LAU
    error = BNK_SWIFT_makeLAU( ids, texts, {oper}, IsShedulerRunning(), out_ids, mdgs, error_text);
    
    var form_report = Bnk_GetRegistryValue("МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ПЕРЕМЕННЫЕ\\CHECKUSEREPORTEXPORT", V_BOOL, false);
    
    if( error )
      ErrExport( IfThenElse( error_text != "", string(error) + " " + error_text, "Ошибка при формировании метки подлинности LAU: " + string(error) ) );
      return false;
    else
      var idx : integer;
      for( idx, 0, mdgs.Size() - 1 )
        for( item, messages )
          if( string(item.message_record_.rec.MesID) == out_ids[idx] )
            if( SubStr( mdgs[idx], 1, 6 ) != "Error:" )
              item.message_text_[0] = StrSubst(item.message_text_[0], "\r\n", "\n") + "{S:{MDG:" + mdgs[idx] + "}}";
            elif( form_report )
              wlmes.MesID = item.message_record_.rec.MesID;
              wlmes.TRN = item.message_record_.rec.TRN;
              ErrExport( SubStr( mdgs[idx], 7 ) );
              if( not error )
                error = 1;
              end;              
            else
              ErrExport( "Формирование метки подлинности LAU для сообщения " + item.message_record_.rec.TRN + " завершилось с ошибкой: " + SubStr( mdgs[idx], 7 ) );
              return false;
            end;
            break;
          end;
        end;
      end;
    end;
  end;
  
  if( error != 0 )
    return false;
  end;
  
  return true;
  
end;

/* Макрос экспорта  */
macro SWIFTOutProc( ExportFileName, addrSess )
  var continue0 = 1, Документов = 0, err;
  var messages = TArray();

  var state :integer = 0,
      ObjId :integer = 0,
      ObjKind :integer = 0,
      TpIdConf :integer = 0;


  SetBuff( wlsess, addrSess );
  НомерСессии = ISN = NULL;
  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if( not err )
      ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end; 

  if( ((ОпределитьФорматТранспорта( wlsess.TpID, ВидТерминала )) == 0) OR (ВидТерминала == "" ) )
    ErrExport( string("Не найден формат по умолчанию для траснпорта № ", wlsess.TpID) );
    return false;
  end;

  if( not ЗаписатьЗаголовок() ) return FALSE; end;
  
  /* Последовательно считываем сообщения и формируем файл экспорта */
  while( continue0 )
    if( not ЗаписатьСообщение( false ) )
      return false;
    else
     //А.Киселев 30.09.2019 врезка для транспорта СПФС
     // Поискать сделку
     if( (wlsess.TpId == TRANSP_SWIFT) or (wlsess.TpId == TRANSP_TELEX) or (wlsess.TpId == TRANSP_SPFSCBR) )
       state = GetObjParm( wlmes.MesID, @ObjId, @ObjKind );
       if( state == 0 )
        TpIdConf = GetTpIdByObjParm( ObjId, ObjKind );
        if( TpIdConf == TRANSP_SPFSCBR )
         out[out.size-1] = out[out.size-1] + "{5:{MAC:00000000}{CHK:00000000}}";
        end;
       end;

     end;
     //А.Киселев 30.09.2019 врезка для транспорта СПФС

      messages[ messages.Size() ] = Pair( wlmes, out );
    end;
    if( not СчитатьЗапись( wlmes, err, false ) )       
      if ( not err )
         continue0 = 0;
      else
         ErrExport("Ошибка чтения сообщения");
         return false;
      end;
    end;

    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end; 
  
  if( not FormBlockS( messages ) )
    return false;
  end;
  
  var idx;
  
  for( idx, 0, messages.Size() - 1 )    
      out = makeArray(messages[idx].message_text_[0]);
    
    if( not СохранитьИнформациюВФайле() )
      ErrExport("Ошибка записи сообщения в файл");
      return false;
    end;
    
    if( idx != messages.Size() - 1 )
      ЗаписатьРазделительСообщений();
    end;
  end;

  if( not ЗаписатьКонцовку() )
    return false;
  end; 

  return true;
end;

macro SwNDCOutProc(ExportFileName, addrSess)
  var continue0 = 1, Документов = 0, err;
  SetBuff( wlsess, addrSess );
  НомерСессии = ISN = NULL;
  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  if( not СчитатьЗапись( wlmes, err ) )
    if( not err )
      ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;

  ВидТерминала = TPFRMT_RJE;

  if( not ЗаписатьЗаголовок() ) return FALSE; end;
  
  /* Последовательно считываем сообщения и формируем файл экспорта */
  while( continue0 )
    if( not ЗаписатьСообщение() )
      return false;
    end;
    if( СчитатьЗапись( wlmes, err, false ) )
       ЗаписатьРазделительСообщений();
    else
      if ( not err )
         continue0 = 0;
      else
         ErrExport("Ошибка чтения сообщения");
         return false;
      end;
    end;

    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;  

  if( not ЗаписатьКонцовку() )
    return false;
  end;

  return true;
end;