 /*
 $Name: znsnsprn.mac
 $Module: МБР
 $Description: Печать справки о наличии счетов в банке
 */
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : znsnsprn.mac
  Created     : 28.01.201
  Programmer  : Popova O.
  Description : Печать справки о наличии счетов в банке

└───────────────────────────────────────────────────────────────────────────*/

import "fnsprzall.mac";

private macro PrintOneDepAcc(this: FNSPrint, Accounts: TArray, CurPos: integer)
  if (CurPos < Accounts.Size)
    [
      Таблица 1
      ┌───┬────────────────────┬──────────────┬────────────────────────┬───────────────────────┬────────────────┬────────────────┬──────────────────────────────────┬──────────────┐
      │   │                    │              │                        │                       │  дата открытия │  дата закрытия │  дата изменения реквизитов счета │ цифровой код │         
      │ № │      № счета       │   вид счета  │        владелец        │                       │      счета     │     счета      │ (специального банковского счета) │ валюты счета │
      │п/п│   (специального    │(специального │         счета          │       бенефициар      │  (специального │  (специального │ (дд.мм.гг.); № измененного счета │(специального │
      │   │ банковского счета) │ банковского  │                        │                       │   банковского  │   банковского  │ (специального банковского счета) │ банковского  │
      │   │                    │    счета)    │                        │                       │счета)(дд.мм.гг)│счета)(дд.мм.гг)│ (до изменения / после изменения) │    счета)    │  
      ├───┼────────────────────┼──────────────┼────────────────────────┼───────────────────────┼────────────────┼────────────────┼──────────────────────────────────┼──────────────┤
      │ 1 │          2         │       3      │           4            │           5           │       6        │       7        │                 8                │       9      │
      ├───┼────────────────────┼──────────────┼────────────────────────┼───────────────────────┼────────────────┼────────────────┼──────────────────────────────────┼──────────────┤ 
    ];
    var i = CurPos;
    var Dep = Accounts(i).rec.Department;
    while( (i < Accounts.Size) and (Accounts(i).rec.Department == Dep) )
      if( strlen(GetError(Accounts(i).rec.Account, WlRegDecID)) == 0 )
    [ │ # │####################│##############│########################│#######################│################│################│##################################│##############│
    ]( i+1, Accounts(i).rec.Account, 
       this.GetNameTypeAcc( Accounts(i).rec.Type_Account, Accounts(i).rec.Code_Currency ):c, 
       this.GetAccName( Accounts(i).rec.Client ):c,        // AccOwner
       this.GetAccName( Accounts(i).rec.BeneficiaryID ):c, // AccBenef
       Accounts(i).rec.Open_date:c,
       Accounts(i).rec.Close_date:c,
       Changes[i]:c,
       GetFICode( Accounts(i).rec.Code_Currency ):c );
      end;
      i = i + 1;
    end;
    [ └───┴────────────────────┴──────────────┴────────────────────────┴───────────────────────┴────────────────┴────────────────┴──────────────────────────────────┴──────────────┘                                                                                                                                                                           
    ];
    return i;
  end;
end;

macro PrintReportInfoAboutExistenceAccounts( this: FNSPrint, ReqDate:date )

  this.ЗаголовокСправки_ММ_3_06_178("1114305");
  [
                                           Справка
                         о наличии счетов (специальных банковских счетов)
  ];

  this.ДанныеБанкаИ_Клиента();

  var Department;
  var BlockNum = 1;
  var BlockSubNum = 1;
  var rs;
  var Accounts:TArray = TArray();
  rs = execSQLSelect("select t_Code from ddp_dep_dbt where t_PartyID = " + int(this.RepBankID));
  rs.moveNext();
  Department = rs.value(0);
  if( this.Счет.size > 0 )
    Accounts = СортироватьСчета(this.Счет, Department);
  elif( this.ClientID > 0 )
    Accounts = this.ВыбратьСчетаКлиента( );
    if (Accounts.size > 0)
      Department = Accounts(0).rec.Department;
    end;
  end;
  // для отчета дата "По состоянию на.." может быть задана отдельно
  if( this.ПоСостояниюНаДату != date( 0, 0, 0 ) )
    ReqDate = this.ПоСостояниюНаДату;
  end;
  
  var i = 0;
  var DepInfo: string = "";
  //YES
  var Errors = 0;
  for(i, 0, Accounts.size - 1)
    var Description = GetError(Accounts(i).rec.Account, WlRegDecID);
    if(Description) 
      Errors = Errors + 1;
    end;
  end;
  i = 0;
  if(GetYes(Accounts, WlRegDecID) or (Errors < Accounts.size))
    [
      о счетах (специальных банковских счетах) указанного лица, а также о счетах иных лиц для совершения        ┌───┐
      операций с денежными средствами, права на которые принадлежали (денежные  средства подлежали передаче     │ # │
      при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших           └───┘]
      (GetYes(Accounts, WlRegDecID));
  end;

    while (i < Accounts.size)
      this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(i).rec.Department, ReqDate);
      i = PrintOneDepAcc(this, Accounts, i);
      BlockSubNum = BlockSubNum + 1;
    end;
    
  // NO
  if( not GetYes(Accounts, WlRegDecID) )
   [                                                                                                                                                                          ┌───┐
     об отсутствии счетов (специальных банковских счетов) указанного лица, а также счетов иных лиц для совершения операций с денежными средствами, права на которые           │ X │
     принадлежали денежные средства подлежали передаче при возникновении оснований, предусмотренных договором с банком) названному лицу, действовавших                        └───┘];
    BlockSubNum = 0;
    this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(0).rec.Department, ReqDate);
  end;
  // PS
  if(this.Ps)
   [
                                                                                                                                                                              ┌───┐
     справка о наличии счетов (специальных банковских счетах) в части информации следующих филиалов банка будет представлена ими самостоятельно:                              │ X │
                                                                                                                                                                              └───┘]
    (this.Ps);
    this.ДанныеФилиала(BlockNum, BlockSubNum, Accounts(0).rec.Department, ReqDate);
  end;

  this.ДатаПодписьПредставителяБанка();
  [];
  return 0;
end;
