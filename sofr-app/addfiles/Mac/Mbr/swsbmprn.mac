/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Печать сообщений SWIFT-SB                                                */
/*                                                                          */
/*  Имя файла: swsbmprn.mac                                                 */
/*  Создан:    18.03.04                                      Панов А. Б.    */
/****************************************************************************/
import "wlglobal.mac", "swsbpars.mac", "adress.mac", rsd;

file bicdir   (ptbicdir) key 0;
file party    (party) key 0;
file f_wltpfld(wltpfld) key 1;

const TpID = 8;

/*Разбиение строки по символам переноса на однострочные подстроки:
  с одним параметром - возвращение количества таких подстрок; с тремя -
  возвращение вторым параметром n-ной подстроки (n - третий параметр) */
macro РазбНаСтрПоПеренСтр(sourse)
  array aStr;
  var nStr, i, dest, number, ch;
  i=0;
  nStr=1;
  aStr(0)=0;
  While (i<strlen(sourse))
     if (  ((ch = SubStr ( sourse, i, 1))=="\n") /*or (ch=="\r")*/)  /*!!*/
        aStr(nStr)=i;
        nStr=nStr+1; 
     end; 
     i=i+1;
  end; 
  aStr(nStr)=i-1;
  if ( getparm(2,number) and (nStr>1) )
     dest=SubStr ( sourse, aStr(number-1)+1, aStr(number)-aStr(number-1));
     setparm(1,dest);
     elif (getparm(2,number) and (nStr==1))
     setparm(1,sourse);
  end;
  return nStr;
end;
  
/*Получение имя поля по тегам (:ЧЧБ:) внутри строки*/
macro ПолучИмяПоляИзСтр(sourse) : string
  var str, str1;
  str = StrSubst ( StrSubst ( sourse, "\n", "" ), ":", "\n" );
  if(РазбНаСтрПоПеренСтр(str)>1) 
     РазбНаСтрПоПеренСтр(str , str1 , 2);
  else
     /*msgbox(sourse);*/
     return "";
  end;
  return StrSubst ( str1, "\n", ""); 
end;


/* Форматирование и печать */
macro PrintField(Name, Containment)
  var LenStr, NumLines, Description, count, str;
  array Cnt, Dsc;

  f_wltpfld.TpID = TpID;
  f_wltpfld.Name = Name;
  if (not GetEQ(f_wltpfld))
    return false;
  end;
  Description = f_wltpfld.Description;
  NumLines    = f_wltpfld.NumLines;
  LenStr      = f_wltpfld.LenEdit;

  Description = ":" + Name + "/" + Description;
  count = strlen(Description)/35 + 1;
  if (count > NumLines)
    NumLines = count;
  end;

  StrMultyToFormat(Description, str, 35, NumLines);
  РазбитьНаСтроки(str, Dsc, 35, NumLines);

  if (NumLines > 1)
    РазбитьНаСтроки(Containment, Cnt, LenStr, NumLines);
  else
    Cnt(0) = Containment;
  end;

  /* Форматируем и печатаем первую строку */
  Cnt(0) = ":" + Cnt(0);
  Cnt(0) = string(Dsc(0):39, Cnt(0));
  println(Cnt(0));
  
  /* Форматируем и печатаем остальные строки, если они есть */
  count = 1;
  while(count < NumLines)
    if ((Cnt(count) != "") or (Dsc(count) != ""))
      Cnt(count) = string(Dsc(count):39, Cnt(count));
      println(Cnt(count));
    end;
    count = count + 1;
  end;         

  return true;
end;

/* Форматирование и печать с раскрытием полей "MFIELDS" */
macro PrintField2(Name, Containment)
   var f_field_value, i, str;
   if ((Name != "SN") and (Name != "RN") and (Name != "RT") and (Name != "MFIELDS"))
      if (not PrintField(Name, Containment))
         return false;
      end;
   elif (Name == "MFIELDS")    
         i = 1;
         println("(Copy of Mandatory Fields from Original Message)");
         while(i <= РазбНаСтрПоПеренСтр(StrSubst ( Containment, "\n\n", "\n" )))      
            РазбНаСтрПоПеренСтр(StrSubst ( Containment, "\n\n", "\n" ),f_field_value,i);
            i=i+1;
            str = ПолучИмяПоляИзСтр(f_field_value);
            if(StrLen(str)==0)
               println("":39,StrSubst ( f_field_value, "\n", "" ));
            elif (not PrintField( str, StrSubst ( SubStr(f_field_value , StrLen(str)+3 ) ,"\n","")))
               return false;
            end;
         end;
   end;
   return true;
end;

/* Получить время в формате HH:MM */
macro GetTime2Format(time)
  var Час, Минута;
  TimeSplit(time, Час, Минута);
  return StrSubst(string(Час:2, ":", Минута:2), " ", "0");
end;

/* Получить дату и время отправки/получения сообщения в СМФР */
/* в формате YYMMDD HH:MM */
macro GetSendDateTime(wlmes, SendDateTime, ReceiveDateTime)
  file f_wlsess(wlsess) key 0;
  var DateSess, TimeSess, DateMes, TimeMes;

  f_wlsess.SessionID = wlmes.SessionID;
  if (GetEQ(f_wlsess))
    DateSess = GetSWIFTDate(f_wlsess.FileDate)+" ";
    TimeSess = GetTime2Format(f_wlsess.FileTime);    
  else
    DateSess = "";
    TimeSess = "";
  end;

  if ((wlmes.OutsideAbonentDate != Date(0,0,0)) and (wlmes.OutsideAbonentTime != Time(0,0)))
    DateMes = GetSWIFTDate(wlmes.OutsideAbonentDate)+" ";
    TimeMes = GetTime2Format(wlmes.OutsideAbonentTime);
  else
    DateMes = "";
    TimeMes = "";
  end;

  if (wlmes.Direct == "X") /* Входящее сообщение */
    SendDateTime    = DateMes+TimeMes;
    ReceiveDateTime = DateSess+TimeSess;
  else                     /* Исходящее сообщение */
    SendDateTime    = DateSess+TimeSess;
    ReceiveDateTime = DateMes+TimeMes;
  end;

  SetParm(1, SendDateTime);
  SetParm(2, ReceiveDateTime);
  return true;
end;

/* Макрос печати заголовка формы */
macro PrintHeader(NumberForm, NameForm, SenderID, ReceiverID, wlmes)
  var CodeBuf, error, InstitutionName, 
      SendDateTime, ReceiveDateTime, Importance,
      Originator="", Destination="";

  GetSendDateTime(wlmes, SendDateTime, ReceiveDateTime);

  std.RSBankHeader;
  
  println("MT", NumberForm, " (",StrUpr(NameForm), ")");
  println("──────────────────────────────────────────────────────────────────────────────");
  println("TYPE       :", NumberForm);

  /* Отправитель сообщения */
  CodeBuf = ПолучитьКодСубъекта(SenderID, PTCK_SMFR, error);
  if (SenderID)
    bicdir.PartyID = SenderID;
    party.PartyID = SenderID;
  else 
    bicdir.PartyID = {OurBank};
    party.PartyID = {OurBank};
  end;
  if (getEQ(bicdir))
    InstitutionName = bicdir.InstitutionName;
  elif(getEQ(party))
    InstitutionName = party.Name;
  else
    InstitutionName = " ";
  end;
  println("ОТПРАВИТЕЛЬ:", CodeBuf:4, " ", InstitutionName:32, " ОТПР:", SendDateTime);

  /* Получатель сообщения */
  CodeBuf = ПолучитьКодСубъекта(ReceiverID, PTCK_SMFR, error);
  if (ReceiverID)
    bicdir.PartyID = ReceiverID;
    party.PartyID = ReceiverID;
  else 
    bicdir.PartyID = {OurBank};
    party.PartyID = {OurBank};
  end;
  if (getEQ(bicdir))
    InstitutionName = bicdir.InstitutionName;
  elif(getEQ(party))
    InstitutionName = party.Name;
  else
    InstitutionName = " ";
  end;
  println("ПОЛУЧАТЕЛЬ :", CodeBuf:4, " ", InstitutionName:32, " ПЛЧН:", ReceiveDateTime);

  /* Эти поля пока бут пустыми */
  Originator = "";
  SendDateTime = "";
  println("ORIGINATOR :", Originator:37, " SENT:", SendDateTime);

  Destination = "";
  ReceiveDateTime = "";
  println("DESTINATION:", Destination:37, " RCPT:", ReceiveDateTime);

  println("NUMBER     :",wlmes.TRN);

  /* Срочность */
  NumberForm = substr(NumberForm, 1, 3);
  if ((NumberForm != "102") and (NumberForm != "103") and (NumberForm != "202"))
    return true;
  end;
  if (wlmes.Importance == 0)
    Importance = "NORMAL";
  else
    Importance = "URGENT";
  end;
  println("***-----------------------------",Importance,"----------------------------------------");

  return true;
end;

/* Макрос печати форм */
macro PrintForm( addrMes, MassCopy )
  var field_name, field_value, SenderID, ReceiverID;
  var MsgTmpFileName, OldName;

  SetBuff( wlmes, addrMes );

  /* Определяем форму сообщения */
  if( not WlDefineForm( wlmes.RlsFormID ) )
    std.msg( String("Не найдена форма сообщения, RlsFormID = ", wlmes.RlsFormID) );
    return false;
  end;

  FILE MsgTmpFile() txt;

  if( wlmes.Direct == "X" )   /* Входящее сообщение */
    SenderID = wlmes.OutsideAbonentID;
    ReceiverID = {OurBank};
  else                        /* Исходящее сообщение */
    SenderID = {OurBank};
    ReceiverID = wlmes.OutsideAbonentID;
  end;

  /* Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные */
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if(not open( MsgTmpFile, MsgTmpFileName ) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  /* Печатаем заголовок */
  if (not PrintHeader(f_wlmesrls.Name, f_wlmesfrm.Description, SenderID, ReceiverID, wlmes))
    return FALSE;
  end;

  println();
  
  /* Последовательно считываем и печатаем поля сообщения */
  while(СчитатьПоле(field_name, field_value))
     if (not PrintField2(field_name, field_value))
        std.msg("Ошибка при печати поля сообщения" + string(field_name));
        return false;
     end;
  end;
  println();

  close(MsgTmpFile);
  SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

  while( MassCopy != 0)
    rewind(MsgTmpFile);
    while( next( MsgTmpFile ) )
      println(MsgTmpFile.str);
    end;
    MassCopy = MassCopy - 1;
  end; 

  return TRUE;
end;
