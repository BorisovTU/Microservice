/*
  $Name:         fnsgmsfc2x.mac
  $Module:       МБР
  $Description:  Генерация сообщения SFC2 по транспорту ФНС в формате XML
*/

//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SFC2 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 29.07.2014
//
//-----------------------------------------------------------------------------
import "fnsMesAccCommon.mac";

private const vers : integer = 510, // версия формата 5.10
              IsIndivid : bool = true,
              AccMsgKind : integer = ACCMSG_KIND_CLOSE;

macro CheckObj( addrReq )
  record Req(reqclosa);
  SetBuff(Req, addrReq);
  
  // массив, в хранящий список номеров проверок, соответствие номера и названия см. fnsMesAccCommon.mac
  var ArrayValidations : TArray = makeArray ( GenMesCheckBankName,
                                              GenMesCheckBankINN,
                                              GenMesCheckBankSizeINN,
                                              GenMesCheckBankKPP,
                                              GenMesCheckBankSizeKPP,
                                              GenMesCheckBankSymbolKPP,
                                              GenMesCheckBankExistIFNS,
                                              GenMesCheckIFNShasCode,
                                              GenMesCheckBankRegNumb,
                                              GenMesCheckDepRegNumb,
                                              GenMesCheckBankBIC,
                                              GenMesCheckBankOGRN,
                                              GenMesCheckBankSizeOGRN,
                                              GenMesCheckClientName,
                                              GenMesCheckClientFIO,
                                              GenMesCheckClientINN,
                                              GenMesCheckClientSizeINN,
                                              GenMesCheckEmployeeSigningMSG,
                                              GenMesCheckChangePropertis,
                                              GenMesCheckBankAddressExistIndex,
                                              GenMesCheckBankAddressSizeIndex,
                                              GenMesCheckBankAdressRegionCode,
                                              GenMesCheckBankAdressSizeRegionCode,
                                              GenMesCheckBankRegionCodeExist,
                                              GenMesCheckBankAdressExistLocality,
                                              GenMesCheckClientExistDatePlacBirth
                                             );
  
  return MNS_CommonCheck( Req, ArrayValidations, ACCMSG_KIND_CLOSE );
end;

private macro GetContractNumber(reqclosa) : string
  var ContractNumber : string = "";

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ContractNumber = ПолучитьПолеДляАннул( "ВидСч" );
  else
    var ObjType : integer = OBJTYPE_REQCLOSA, 
        NoteKind : integer = REQCLOSA_NOTE_KIND_CONTRACT_NUMBER;

    ContractNumber = ReadNoteForObject(ObjType, UniID(reqclosa, ObjType), NoteKind);
  end;

  return ContractNumber;
end;

private macro ЗаписатьКодСостДогИДатаРастДог(reqclosa)

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    var OldfnsMessageID : integer = GetOldfnsMessageID();
    ЗаписатьПолеДляАннул( "КодСостДог", OldfnsMessageID );
    ЗаписатьПолеДляАннул( "ДатаРастДог", OldfnsMessageID );
  else
    var ContractCloseDate : date = ReadNoteForObject( OBJTYPE_REQCLOSA, 
                                                      UniID(reqclosa, OBJTYPE_REQCLOSA), 
                                                      REQCLOSA_NOTE_KIND_CONTRACT_CLOSEDATE
                                                    );

    if( ContractCloseDate == date(0,0,0) )
      ЗаписатьПолеЛог( "КодСостДог", "0" );
    else
      ЗаписатьПолеЛог( "КодСостДог", string(reqclosa.SubKind) );
      ЗаписатьПолеЛог( "ДатаРастДог", DDpMMpYYYY(ContractCloseDate) );
    end;
  end;

end;

// Файл\Документ\СвСчет\Закрыт
private macro WriteAccInfoClose(reqclosa)
  StartBlockLogXML("Закрыт");

  WriteFieldWithCheckAnnul( "КодСостСч", "0" );
  var AccCloseDate : string = IfThenElse(reqclosa.AccCloseDate != date(0, 0, 0), DDpMMpYYYY(reqclosa.AccCloseDate), "");
  WriteFieldWithCheckAnnul( "ДатаЗакрСч", AccCloseDate );
  ЗаписатьПолеЛог         ( "НомерДог", GetContractNumber(reqclosa) );
  ЗаписатьКодСостДогИДатаРастДог(reqclosa);

  FinishBlockLogXML("Закрыт");
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record Req(reqclosa);
  SetBuff(Req, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SFC2");

  var ErrMsg : string = "";

  var CommonAccMsg : TCommonDataAccMsg = TCommonDataAccMsg
                                         ( Req.Department, 
                                           AccMsgKind, 
                                           vers,
                                           IsIndivid );

  var ClientID : integer = Req.ClientID;

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonAccMsg);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonAccMsg, "", "", ALLFININSTR, ClientID, OldMes.MesID, IsIndivid);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonAccMsg, "СвБанк", @ErrMsg) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo_Ind(ClientID, Req.ClientType311, AccMsgKind, NULL, NULL, Req);

  // Файл \ Документ \ СвСчет
  WriteAccInfo_Ind(Req, AccMsgKind, @WriteAccInfoClose);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonAccMsg._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonAccMsg._ВидРегОргана);
  end;

  if(CommonAccMsg._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonAccMsg._СпОбм);
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
