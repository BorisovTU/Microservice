/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация информационных сообщений по SWIFT-SB MTn99                     */
/*                                                                          */
/*  Имя файла: swsgdn99.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/
import "swsbgdoc.mac";

/* Подтверждение дебета */
class StatementMessage
 var
  TRF              :string,        /* Ссылочный номер операции */
  RelatedRef       :string,        /* Ссылка */  
  Information      :string,        /* Информация  */
  Sender           :Bank;          /* Отправитель */

  TRF              = "";
  RelatedRef       = "";
  Information      = "";

end; /* class StatementMessage */

var MTn99 : StatementMessage;

macro FillSN(TRF)
  return true;
end;

macro FillRN(RelatedRef)
  return true;
end;

macro Fill20( TRF )
  MTn99.TRF = TRF;
  return TRUE;
end;

macro Fill21( TRF )
  MTn99.RelatedRef = TRF;
  return TRUE;
end;

macro Fill79( Str, Narrative )
  MTn99.Information = Str;
  return TRUE;
end;

/* Заполнение списка полей формы  MTn99*/
var FldSN = ТПолеФормы( УникальныйНомерСообщенияСМФР,    FIELD_MANDATORY, "ReadTRF", "FillSN", "" ),
    FldRN = ТПолеФормы( НомерСвязанногоСообщенияСМФР,    FIELD_OPTIONAL,  "ReadTRF", "FillRN", "" ),
    Fld20 = ТПолеФормы( TransactionReferenceNumberField, FIELD_MANDATORY, "ReadTRF", "Fill20", "" ),
    Fld21 = ТПолеФормы( RelatedReferenceField,           FIELD_OPTIONAL,  "ReadTRF", "Fill21", "" ),
    Fld79 = ТПолеФормы( NarrativeDescriptionField,       FIELD_MANDATORY, "Read79",  "Fill79", "" );

macro ConstructMTn99( RespID )
  var field_name, field_value, loop = 1, count = 0, NeedRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( NeedRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "В сообщении не найдено обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        elif( not fld.ВыполнитьФункцияБлока() )
          std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
          result = FALSE;
          loop = 0;
        else          
          NeedRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока() )
            NeedRead = 1;            
          else
            std.msg( String("Ошибка при выполнении функции блока ", fld.Name, " '", fld.BlockFun, "'") );
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SMFR, error );
  MTn99.Sender = Bank( "", "", "", "", CODEWORLD_SMFRSBRF, BankCode);

  return TRUE;
end; /* ConstructMTn99 */

macro GenDoc( addrMes )
  var BankCode, error;
  MTn99 = StatementMessage();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация подтверждения по МТn99");
  /* Проверяем ключ входящего сообщения */
  if( not CheckMesKey( wlmes ))
    return false;
  end;

  ClearRecord(wlinfo);

  if( not ConstructMTn99( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  wlinfo.TRN          = MTn99.TRF;
  wlinfo.RelatedRef   = MTn99.RelatedRef;

  /* Создатель - наш абонент */
  wlinfo.OriginatorID       = MTn99.Sender.PartyID;
  wlinfo.OriginatorCodeKind = MTn99.Sender.CodeKind;
  wlinfo.OriginatorCode     = MTn99.Sender.CodeBank;

  /* Получатель - наш банк */
  BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SMFR, error );
  wlinfo.RecipientID       = {OurBank};
  wlinfo.RecipientCodeKind = PTCK_SMFR;
  wlinfo.RecipientCode     = BankCode;

  if( not ВставитьИнфСообщение( wlinfo, MTn99.Information ) )
    std.msg("Ошибка при вводе информационного сообщения");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;
