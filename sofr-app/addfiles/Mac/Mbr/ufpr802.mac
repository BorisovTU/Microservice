/*
  $Name: ufpr802.mac
  $Module: Межбанковские расчеты
  $Description: Печатная форма сообщения ED802 транспорта УФЭБС
*/


import oralib, MesInter, wlglobal, wluftool, WLTOOLS, WldInter, rsexts, ufgd802, bnk_dirfile;


private macro PrintTableInfo( ED802MessageFields: TED802MessageFields )
  var RepType = "";
  if( ED802MessageFields.InfoTypeCode == "FIRR" )
    RepType = "Информация о ликвидности (полная)";
  elif( ED802MessageFields.InfoTypeCode == "SIRR" )
    RepType = "Информация о ликвидности (краткая)";
  end;

  [
    ED802 ##################################################

    Номер ЭC:     #########
    Дата ЭC:     ##########
    Составитель: ########## ##################################################
  ]( RepType,
     ED802MessageFields.EDNo, 
     DDpMMpYYYY(ED802MessageFields.EDDate), 
     ED802MessageFields.EDAuthor, 
     Bnk_GetPartyName( ПолучитьКодСубъекта( ED802MessageFields.EDAuthor, PTCK_UIS, null ) )
   );

  if( ED802MessageFields.EDReceiver != "" )
    [ Получатель:  ########## ##################################################
    ]( ED802MessageFields.EDReceiver, 
       Bnk_GetPartyName( ПолучитьКодСубъекта( ED802MessageFields.EDReceiver, PTCK_UIS, null ) ) 
     );
  end;

  [ Код причины формирования: #### ################################################################################
    Дата и время создания:  #########################
    Вид представления информации: #### ################################################################################
  ]( ED802MessageFields.CreationReason, 
     GetWlcodeDescription( TYPE_CODE_UFBS_ED802_CREATIONREASON, ED802MessageFields.CreationReason ), 
     ED802MessageFields.CreationDateTime, 
     ED802MessageFields.InfoTypeCode,
     GetWlcodeDescription( TYPE_CODE_UFBS_ED801_INFOTYPECODE, "/" + ED802MessageFields.InfoTypeCode + "/" )
   );

  if( ED802MessageFields.InitialED != null )
    [

      Информация об исходном ЭСИС:
           Номер ЭС: #########
           Дата ЭС: ##########
           Составитель: ########## ##################################################
    ]( ED802MessageFields.InitialED.EDNo, 
       DDpMMpYYYY(ED802MessageFields.InitialED.EDDate), 
       ED802MessageFields.InitialED.EDAuthor,
       Bnk_GetPartyName( ПолучитьКодСубъекта( ED802MessageFields.InitialED.EDAuthor, PTCK_UIS, null ) ) 
     );
  end;

end;

private macro PrintLiquidityInformation( Account : TAccount )
  var sum: money;
  var sum1: money;
  [

    ИНФОРМАЦИЯ О ЛИКВИДНОСТИ:
         По счету (Account): ####################
         Участника (BIC): #########
  ]( Account.Account, 
     Account.BIC     
   );

  var amcItr = 0;
  for( var AccountModeChange, Account.AccountModeChangesArr )
    if( amcItr == 0 )
      [      Изменения в режиме работы счета (AccountChangeType): #### ################################################################################
      ]( AccountModeChange.AccountChangeType,
         GetWlcodeDescription( TYPE_CODE_UFBS_ED802_ACCOUNTCHANGETYPE, AccountModeChange.AccountChangeType )
       );
    else
      [                                                           #### ################################################################################
      ]( AccountModeChange.AccountChangeType,
         GetWlcodeDescription( TYPE_CODE_UFBS_ED802_ACCOUNTCHANGETYPE, AccountModeChange.AccountChangeType )
       );
    end;
    amcItr = amcItr + 1;
  end;

  if( Account.SpecialModeIndicator == 1 )
    [
      ────────────────────────────────────────────────────────────────────────────────────────────────────────────────
      Внимание! К счету имеется очередь не исполненных в срок распоряжений
      ────────────────────────────────────────────────────────────────────────────────────────────────────────────────
    
    ];
  end;

  if( (Account.LiquidityInfo != null) and ((Account.LiquidityInfo.Balance != null) or (Account.LiquidityInfo.NetAvailableLiquidity != null)))
    [ Информация о ликвидности (LiquidityInfo) ];   
    if( Account.LiquidityInfo.Balance != null )
      [      Остаток на счете (Balance): #################### ] 
        ( IfThenElse( Account.LiquidityInfo.Balance == moneyL(0), "0.00", Account.LiquidityInfo.Balance / 100 ):l );
    end;
    if( Account.LiquidityInfo.NetAvailableLiquidity != null )
      [      Чистая доступная ликвидность (NetAvailableLiquidity): #################### ] 
        ( IfThenElse( Account.LiquidityInfo.NetAvailableLiquidity == moneyL(0), "0.00", Account.LiquidityInfo.NetAvailableLiquidity / 100 ):l  );
    end;
  end;

  if( (Account.ReservationInfo != null) and ((Account.ReservationInfo.NonCashReservedSum != null) or (Account.ReservationInfo.CashReservedSum != null )) )
    [ Информация о резервированиях (ReservationInfo) ];
    if( Account.ReservationInfo.NonCashReservedSum != null ) 
      [      Сумма резервирования, кроме снятия наличных (NonCashReservedSum): #################### ] 
        ( IfThenElse( Account.ReservationInfo.NonCashReservedSum == moneyL(0), "0.00", Account.ReservationInfo.NonCashReservedSum / 100 ):l );
    end;
    if( Account.ReservationInfo.CashReservedSum != null )
      [      Сумма резервирования на снятие наличных (CashReservedSum): #################### ] 
        ( IfThenElse( Account.ReservationInfo.CashReservedSum == moneyL(0), "0.00", Account.ReservationInfo.CashReservedSum / 100 ):l  );
    end;
  end;
                         
  if( Account.LimitInfoArr.size > 0 )
    [ Информация о лимитах (LimitInfo)
      ┌─────────────┬─────────────────┬───────────────────────┬─────────────┬────────────────────┐ 
      │ Тип лимита  │ Значение лимита │ Текущее использование │БИК участника│ Получатель средств │    
      │ (LimitType) │     (Value)     │ лимита (Utilization)  │    (BIC)    │       (UID)        │
      ├─────────────┼─────────────────┼───────────────────────┼─────────────┼────────────────────┤
    ];
    for( var LimitInfo, Account.LimitInfoArr )
      sum =  IfThenElse( LimitInfo.Value == null, 0, LimitInfo.Value )/100;
      sum1 =  IfThenElse( LimitInfo.Utilization == null, 0, LimitInfo.Utilization )/100;
      [ │     ####    │ ############### │ ##################### │  #########  │     ##########     │ ]
        ( LimitInfo.LimitType,
          sum, 
          sum1, 
          LimitInfo.BIC, LimitInfo.UID
        );
    end;

    [ └─────────────┴─────────────────┴───────────────────────┴─────────────┴────────────────────┘
    ];
  end;

  if( (Account.ArrestInfo != null) and ((Account.ArrestInfo.TotalAmount != null) or (Account.ArrestInfo.ArrestedAmount != null) or (Account.ArrestInfo.OutstandingAmount != null) or (Account.ArrestInfo.IndeterminateAmountFlag and (Account.ArrestInfo.IndeterminateAmountFlag == "1")) or (Account.ArrestInfo.ArrestDetailedInfoArr.size > 0)) )
    [ Информация об арестах (ArrestInfo) ];
    if( Account.ArrestInfo.TotalAmount != null )
      [      Общая сумма арестов/ограничений (TotalAmount): #################### ] 
        ( IfThenElse( Account.ArrestInfo.TotalAmount == moneyL(0), "0.00", Account.ArrestInfo.TotalAmount / 100 ):l );
    end;
    if( Account.ArrestInfo.ArrestedAmount != null )
      [      Сумма средств, зарезервированных по документам об аресте (ArrestedAmount): #################### ] 
        ( IfThenElse( Account.ArrestInfo.ArrestedAmount == moneyL(0), "0.00", Account.ArrestInfo.ArrestedAmount / 100 ):l );
    end;
    if( Account.ArrestInfo.OutstandingAmount != null )
      [      Сумма арестов, за вычетом исполненных распоряжений (OutstandingAmount): #################### ] 
        ( IfThenElse( Account.ArrestInfo.OutstandingAmount == moneyL(0), "0.00", Account.ArrestInfo.OutstandingAmount / 100 ):l );
    end;
    if( Account.ArrestInfo.IndeterminateAmountFlag and (Account.ArrestInfo.IndeterminateAmountFlag == "1") )
      [      Наличие решения НО о приостановлении операций без суммы (IndeterminateAmountFlag): Да ];
    end;
  end;

  if( (Account.ArrestInfo != null) and ( Account.ArrestInfo.ArrestDetailedInfoArr.size > 0 ) )
    [ Список арестов и/или ограничений (ArrestDetailedInfo)
      ┌────────────┬───────┬────────────────┬─────────────────────┬────────────┬──────────────┬─────────────────────────────────┐ 
      │ ID ареста  │  Без  │  Сумма ареста  │  Сумма за вычетом   │  Дата док. │ Номер док-та │        Описание документа       │
      │ (ArrestID) │ суммы │ (ArrestAmount) │     исполненных     │  основания │   основания  │            основания            │
      │            │       │                │ (OutstandingAmount) │  (DocDate) │  (DocNumber) │          (Description)          │
      ├────────────┼───────┼────────────────┼─────────────────────┼────────────┼──────────────┼─────────────────────────────────┤
    ];


    for( var ArrestDetailedInfo, Account.ArrestInfo.ArrestDetailedInfoArr )
      sum =  IfThenElse( ArrestDetailedInfo.ArrestAmount == null, 0, ArrestDetailedInfo.ArrestAmount ) /100;
      sum1 =  IfThenElse( ArrestDetailedInfo.OutstandingAmount == null, 0, ArrestDetailedInfo.OutstandingAmount )/100;
      if( ArrestDetailedInfo.RestrictionBasisDoc != null )
        [ │ ########## │  ###  │ ############## │ ################### │ ########## │ ############ │ ############################### │]
          ( ArrestDetailedInfo.ArrestID,
            IfThenElse( ArrestDetailedInfo.IndeterminateAmountFlag == "1", "Да", "" ),
                sum, 
                sum1, 
            DDpMMpYYYY( ArrestDetailedInfo.RestrictionBasisDoc.DocDate ),
            ArrestDetailedInfo.RestrictionBasisDoc.DocNumber,
            ArrestDetailedInfo.RestrictionBasisDoc.Description:w          
          );
      else
        [ │ ########## │  ###  │ ############## │ ################### │            │              │                                 │]
          ( ArrestDetailedInfo.ArrestID,
            IfThenElse( ArrestDetailedInfo.IndeterminateAmountFlag == "1", "Да", "" ),
            sum, 
            sum1 
          );
      end;
    end;

    [ └────────────┴───────┴────────────────┴─────────────────────┴────────────┴──────────────┴─────────────────────────────────┘
    ];
  end;

  if( (Account.DebitQueuedPayments != null) and ( (Account.DebitQueuedPayments.DebitQueuedGroupArr.size > 0) ) )
    var byCreditExist = false;

    [ 
      Информация о распоряжениях на списание в очередях (DebitQueuedPayments):
    ];
    
    if( Account.DebitQueuedPayments.DebitQueuedGroupArr.size > 0 )
      [ Распоряжения на списание по группам (DebitQueuedGroup)
        ┌──────────────────────────────────────────────────────────┬──────────────────┬─────────────────────────────┐ 
        │                  Код группы (GroupCode)                  │ Количество (Cnt) │ Сумма распоряжений (Amount) │
        ├──────────────────────────────────────────────────────────┼──────────────────┼─────────────────────────────┤
      ];

      for( var DebitQueuedGroup, Account.DebitQueuedPayments.DebitQueuedGroupArr )
        sum =  IfThenElse( DebitQueuedGroup.Amount == null, 0, DebitQueuedGroup.Amount )/100;
        [ │ ##### ################################################## │ ################ │ ########################### │]
          ( DebitQueuedGroup.GroupCode,
            GetWlcodeDescription( TYPE_CODE_UFBS_ED802_GROUPCODE, DebitQueuedGroup.GroupCode ),
            DebitQueuedGroup.Cnt,
            sum 
          );
        if( DebitQueuedGroup.ByCreditAccountArr.size > 0 )
          byCreditExist = true;
        end;
      end;

      [ └──────────────────────────────────────────────────────────┴──────────────────┴─────────────────────────────┘
      ];
    end;

    if( byCreditExist )
      [ Распоряжения на списание по счетам для зачисления (ByCreditAccount)
        ┌─────────────┬───────────┬──────────────────────┬────────────┬────────────────────┐ 
        │ Код группы  │ БИК (BIC) │     Номер счета      │ Количество │ Сумма распоряжений │
        │ (GroupCode) │           │      (Account)       │    (Cnt)   │     (Amount)       │
        ├─────────────┼───────────┼──────────────────────┼────────────┼────────────────────┤
      ];

      for( var DebitQueuedGroupBC, Account.DebitQueuedPayments.DebitQueuedGroupArr )
        for( var ByCreditAccount, DebitQueuedGroupBC.ByCreditAccountArr )
          sum =  IfThenElse( ByCreditAccount.Amount == null, 0, ByCreditAccount.Amount )/100;
          [ │    #####    │ ######### │ #################### │ ########## │ ################## │]
            ( DebitQueuedGroupBC.GroupCode,
              ByCreditAccount.BIC,
              ByCreditAccount.Account,
              ByCreditAccount.Cnt,
              Sum  
            );
        end;
      end;

      [ └─────────────┴───────────┴──────────────────────┴────────────┴────────────────────┘
      ];

    end;
  end;

  if( (Account.CreditQueuedPayments != null) and ((Account.CreditQueuedPayments.CreditQueuedGroup != null)))
    var byDebitExist = false;

    [ 
      Информация о распоряжениях на зачисление в очередях (CreditQueuedPayments):
    ];
    
    if( Account.CreditQueuedPayments.CreditQueuedGroup != null )
      [ Распоряжения на зачисление по группам (CreditQueuedGroup)
        ┌──────────────────────────────────────────────────────────┬─────────────────────────────┐ 
        │                  Код группы (GroupCode)                  │ Сумма распоряжений (Amount) │
        ├──────────────────────────────────────────────────────────┼─────────────────────────────┤
      ];
      sum =  IfThenElse( Account.CreditQueuedPayments.CreditQueuedGroup.Amount == null, 0, Account.CreditQueuedPayments.CreditQueuedGroup.Amount )/100;

      [ │ ##### ################################################## │ ########################### │]
        ( Account.CreditQueuedPayments.CreditQueuedGroup.GroupCode,
          GetWlcodeDescription( TYPE_CODE_UFBS_ED802_GROUPCODE, Account.CreditQueuedPayments.CreditQueuedGroup.GroupCode ),
          sum  
        );

      if( Account.CreditQueuedPayments.CreditQueuedGroup.ByDebitAccountArr.size > 0 )
        byDebitExist = true;
      end;

      [ └──────────────────────────────────────────────────────────┴─────────────────────────────┘
      ];
    end;

    if( byDebitExist )
      [ Распоряжения на зачисление по счетам списания (ByDebitAccount)
        ┌─────────────┬───────────┬──────────────────────┬────────────────────┐ 
        │ Код группы  │ БИК (BIC) │     Номер счета      │ Сумма распоряжений │
        │ (GroupCode) │           │      (Account)       │     (Amount)       │
        ├─────────────┼───────────┼──────────────────────┼────────────────────┤
      ];

      for( var ByDebitAccount, Account.CreditQueuedPayments.CreditQueuedGroup.ByDebitAccountArr )
         sum =  IfThenElse( ByDebitAccount.Amount == null, 0, ByDebitAccount.Amount )/100;
      [ │    #####    │ ######### │ #################### │ ################## │]
        ( Account.CreditQueuedPayments.CreditQueuedGroup.GroupCode,
          ByDebitAccount.BIC,
          ByDebitAccount.Account,
          sum
        );
      end;

      [ └─────────────┴───────────┴──────────────────────┴────────────────────┘
      ];

    end;
  end;


end;


private macro PrintTableElements( ED802MessageFields: TED802MessageFields )
  for( var Account, ED802MessageFields.AccountArr )
    PrintLiquidityInformation( Account )
  end;
end;

macro PrintForm( addrMes, MassCopy )
  var stat = false;
  var FileDirectory = Bnk_GetTxtFileDir();
  var XmlObj : object = ActiveX("Microsoft.XMLDOM");


  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;

  var ImageID = 0;
  var sWlMesID : string = makeObjectID( OBJTYPE_MES, null, wlmes );

  ImageID = Bnk_GetImageID( OBJTYPE_MES, sWlMesID, WLD_MES_IMGTYPE_XML_FILE );

  if( ImageID )
    /* Записываем прикрепленный ED802 во временный XML-файл */
    var tmpXMLFileName = FileDirectory + "\\" + wlmes.TRN + ".xml";
    stat = WEB_ShowImageByID( ImageID, tmpXMLFileName );
    if( not stat )
      XmlObj = null;
    else
      if ( not XmlObj.load( tmpXMLFileName ) )
        std.msg( "Неверный формат сообщения" );
        return FALSE;
      end;
    end;
    /* Удаляем временный XML-файл с ED802 */
    RemoveFile( tmpXMLFileName );
  else
    XmlObj = null;
  end;

  var ED802MessageFields = TED802MessageFields();

  ReadED802MessageFields( @ED802MessageFields, XmlObj );

  PrintTableInfo( ED802MessageFields );
  PrintTableElements( ED802MessageFields );

  // Перенаправляем вывод обратно
  //------------------------------------------
  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 

  return true;                                                               

  OnError(er)
    ExeptionMessage(er);
    return false;
end;
