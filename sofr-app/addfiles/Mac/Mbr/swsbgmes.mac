/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6                       */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Константы и вспомогательные функции для генерации сообщений SWIFT-SB     */
/*                                                                          */
/*  Имя файла: swsbgmes.mac                                                 */
/*  Создан:  18.03.04                                        Панов А.Б.     */
/****************************************************************************/

import "swgenmes.mac", "swsbtls.mac", "cb_sql.mac", "oralib.mac", oralib, likepy, "pm_tools.mac";

/* Записать референс финансового сообщения для служебно-информационного сообщения */
macro ЗаписатьНомерСвязанногоСообщения_SB( TRN:string, MesID, MesDate, MesDirect, ИскатьПоСсылкеИзНайденного )
  var rs, rMesID, rDate, rDirect, rTrn, rsstr;
  var params:TArray = TArray();

  /* Если ссылка отсутствует - прерываем генерацию сообщения */
  if( (TRN == "") OR (TRN == СсылкаОтсутствует) )
    RunError( "|Не задан ссылочный номер исходного сообщения в СМФР" );
  end;

  rsstr = string("select wlmes.t_MesID, wlmes.t_OutsideAbonentDate, wlmes.t_Direct, wlmes.t_RelatedRef", 
                           " from dwlmes_dbt wlmes",
                           " where wlmes.t_TRN = :Trn ", 
                           " AND wlmes.t_Department = :OperDprt "
                           " AND (wlmes.t_Direct = 'X' OR wlmes.t_Direct = chr(0))"
                          ) ;

  params[params.size] = SQLParam( "Trn",   TRN);
  params[params.size] = SQLParam( "OperDprt", {OperDprt});
  rs = execSQLSelect (rsstr, params, true);
  if( not rs.MoveNext() )
    RunError( String( "|Не найдено исходное сообщение со ссылочным номером: ", TRN) );
  end;

  rMesID = rs.value(0);
  rDate = sqlDate2date(rs.value(1));
  rDirect = rs.value(2);
  rTrn = rs.value(3);
  if(ИскатьПоСсылкеИзНайденного and ( rTrn!= ""))
     params = TArray();
     params[params.size] = SQLParam( "Trn",   rTrn);
     params[params.size] = SQLParam( "OperDprt", {OperDprt});
     rs = execSQLSelect (rsstr, params, true);
     if( rs.MoveNext() )
        rMesID = rs.value(0);
        rDate = sqlDate2date(rs.value(1));
        rDirect = rs.value(2);
        TRN = rTrn;
     end;
  end;
  ЗаписатьПолеЛог( НомерСвязанногоСообщенияСМФР, TRN );

  SetParm( 1, rMesID );
  SetParm( 2, rDate );
  SetParm( 3, rDirect );
end;

/* Формировании копии как минимум обязательных полей исходного сообщения */
/* Реально перечисляем все поля исходного сообщения */
macro СчитатьЗаполненныеПоля( MesID:integer, field_value:string )
  var tmpstr = "";
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select wltpfld.t_Name, wlmesval.t_Value"+
                           " from dwltpfld_dbt wltpfld, dwlmesval_dbt wlmesval, dwlmes_dbt wlmes"+
                           " where wlmes.t_MesID =:MesID"+
                           " AND wlmes.t_MesID = wlmesval.t_MesID"+
                           " AND wlmesval.t_TpFieldID = wltpfld.t_TpFieldID"+
                           " order by wlmesval.t_Index";
  params = makeArray( SQLParam("MesID", MesID));
  rs = execSQLselect( select, params, FALSE );

  while( rs.MoveNext() )
    if( (rs.value(0) != CopyMandatoryFields) AND 
        (rs.value(0) != УникальныйНомерСообщенияСМФР) AND
        (rs.value(0) != НомерСвязанногоСообщенияСМФР) AND
        (rs.value(0) != ТипИПодтипСообщения) )

       tmpstr = tmpstr + КодНачалаПоля + rs.value(0) + КодНачалаПоля + rs.value(1) + SYMB_ENDL;
    end;
  end;

  SetParm( 1, tmpstr );

  return true;
end;

  

macro СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart, usingCode )
  record RecordAdress ( adress );
  var field_value = "", tempv, Name, tmpName, BankCode, INN, error, account, TerBankID;

  if( valtype(Tag) == V_UNDEF )
    Tag = "";
  end;

  if ( Route.Sort>0 )
     account = Route.InAccount;
  elif ( Route.Sort<0 )
     account = Route.OutAccount;
  else
     account = "";
  end;

  if ( Tag=="" )
    if ( (Route.CodeValue!="") AND (Route.CodeKind==PTCK_SWIFT) )
      Tag = SWIFT_Tag_A;
    else
      Tag = SWIFT_Tag_D;
    end;
  end;
  
  if( Tag == SWIFT_Tag_A )    
    if( account != "" )
      field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
    end;
    field_value = field_value + Route.CodeValue;
  elif ( Tag == SWIFT_Tag_B )
    if( account != "")
       if ( (valtype(DKFlag)!=V_UNDEF) AND (DKFlag!="") )
         field_value = field_value + SYMB_SLASH + DKFlag;
       end;
    
      field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
    end;

  elif( Tag == SWIFT_Tag_C )
    BankCode = ПолучитьКодСубъекта( Route.PartyID, PTCK_BIC, error );
    if ( (not Error) AND (Route.PartyID) )
       field_value = string( field_value, "//RU", BankCode );
       if ( account != "" )
         field_value = string( field_value, ".", account );
       end;
       field_value = field_value;
    else
      if( account != "")
         field_value = field_value + SYMB_SLASH + account;
      end;
    end;
  else 

      if( account != "")
         field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
      end;

    TerBankID = НайтиТерБанк(Route.PartyID);
    if(TerBankID > 0)
        Name = ПолучитьИмяБанка(TerBankID);
        if( Name != "" )
          field_value = field_value + Name + SYMB_ENDL;
        end;
    end;
    
    tmpName = GetNameBank_Tag_D(Tag, Route.Name, Route.PartyID);
    if( tmpName != "" )
      Name = tmpName;   
      field_value = field_value + Name + SYMB_ENDL;
    end;
    
    if( (Route.PartyID) AND НайтиЮридическийАдресСубъекта(Route.PartyID,RecordAdress) AND
        (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
        field_value = string( field_value, SYMB_ENDL, trim(RecordAdress.Place) );
    end;
    StrChangeRusXSet( field_value, field_value, standart );
    StrMultyToFormat( field_value, field_value, 35, 5 );
  end;

  setparm( 1, Tag );

  return field_value;
end;

/* Заполняет поле 52 (для платежа) */
macro FillOrderingInstitutionFieldA_SB(RsPaym, Tag, Standart)
  var field_value = "", Code, error, ID;
  record crs(corschem);

  Tag = "";
  /* транзитный и не от сбера */
  if ((ExistsExternalInProp(RsPaym)) and
      ( IsBankType(RsPaym.PayerBankID, PT_KIND_SAVINGS_BANK )))
      if ( RsPaym.PaymentID and (RsPaym.PayerIsSender=="X") )
         if ( RsPaym.PayerOurCorrID<=0 )
            ClearRecord(crs);
            crs.FI_Kind=1;
            crs.FIID = RsPaym.PayerFIID;
            crs.Number = RsPaym.InCorschem;
            if ( not getEQ(crs) )
               return field_value;
            end;
            ID = crs.CorrID;
    else
            ID = RsPaym.PayerOurCorrID;
    end;    
      else
         if ( RsPaym.ReceiverOurCorrID<=0 )
            ClearRecord(crs);
            crs.FI_Kind=1;
            crs.FIID = RsPaym.ReceiverFIID;
            crs.Number = RsPaym.OutCorschem;
            if ( not getEQ(crs) )
               return field_value;
            end;
            ID = crs.CorrID;
         else
            ID = RsPaym.ReceiverOurCorrID;
         end;
      end;
  else
    ID = RsPaym.PayerBankID;    
  end;
  if(ID == {OurBank})
    error = ПолучитьНашКодСУчетомТС(PTCK_SWIFT, true, Code, ID);
    if (not error)
      field_value = Code;
      Tag = SWIFT_Tag_A;
    end;
  else
    Code = ПолучитьКодСубъекта(ID, PTCK_SWIFT, error);
    if (not error)
      field_value = Code;
      Tag = SWIFT_Tag_A;
    end;
  end;

  SetParm(1, Tag);
  return field_value;
end;

macro FillOrderingInstitutionFieldB_SB(RsPaym, Tag,  Standart)
  var field_value = "";

  Tag = "";
  if (not ExistsExternalInProp(RsPaym))
    field_value = ПолучитьИмяБанка(RsPaym.PayerBankID);
    StrChangeRusXSet(field_value, field_value, Standart );
    Tag = SWIFT_Tag_B;
  end;
  SetParm(1, Tag);
  return field_value;
end;

/*Адрес для поля 52D*/
private macro ПолучитьПолноеИмяБанка( PartyID )
   var name;
   file party( party ) key 0;

   if ( PartyID )
      party.PartyID = PartyID;
   else 
      party.PartyID = {OurBank};
   end;

   if ( getEQ(party) )
      name = party.Name;
      return name; 
   end;
   return "";
end;

macro FillOrderingInstitutionFieldD_SB(RsPaym, Tag, NeedAcc, Standart, NeedTer)
   
   var field_value = "", Code, error, ID, account = "", Name, tempv, INN, TerBankID = -1;
   record crs(corschem);

   if (ValType(NeedAcc) == V_UNDEF)
      NeedAcc = false;
   end;

   if (ValType(NeedTer) == V_UNDEF)
      NeedTer = false;
   end;

   Tag = "";
   /* транзитный и не от сбера */
   if ((ExistsExternalInProp(RsPaym)) and
      ( IsBankType(RsPaym.PayerBankID, PT_KIND_SAVINGS_BANK )))
      if ( RsPaym.PaymentID and (RsPaym.PayerIsSender=="X") )
         if ( RsPaym.PayerOurCorrID<=0 )
            ClearRecord(crs);
            crs.FI_Kind=1;
            crs.FIID = RsPaym.PayerFIID;
            crs.Number = RsPaym.InCorschem;
            if ( not getEQ(crs) )
                return field_value;
            end;
            ID = crs.CorrID;
         else
            ID = RsPaym.PayerOurCorrID;
         end;
      else
         if ( RsPaym.ReceiverOurCorrID<=0 )
            ClearRecord(crs);
            crs.FI_Kind=1;
            crs.FIID = RsPaym.ReceiverFIID;
            crs.Number = RsPaym.OutCorschem;
            if ( not getEQ(crs) )
               return field_value;
            end;
            ID = crs.CorrID;
         else
            ID = RsPaym.ReceiverOurCorrID;
         end;
      end;

   elif(IsDebetPaym(RsPaym))
      ID = RsPaym.PayerBankID;
      TerBankID = НайтиТерБанк(RsPaym.PayerBankID);
      if(ID < 0 )
         return "";
      end;

   else
      ID = {OurBank};
      error = ПолучитьНашКодСУчетомТС(PTCK_SWIFT, true, Code, TerBankID);
      if (error)
         return "";
      end;
   
   end;

   account = RsPaym.PayerAccount;

   if ((NeedAcc) and (account != ""))
      field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
   end;

   if(TerBankID > 0)
      if( ((ID != {OurBank}) and (NeedTer)) or (ID == {OurBank}))
         Name = ПолучитьПолноеИмяБанка(TerBankID);
         if (Name != "")
            field_value = string(field_value, Name, SYMB_ENDL); 
         end;
      end;
   end;

   if(ID != TerBankID)
      Name = ПолучитьПолноеИмяБанка(ID);
      if (Name != "")
         field_value = string(field_value, Name, SYMB_ENDL); 
      end;
   end;

   if(Standart == ST_NONTR)
      INN = GetPartyINN(ID);
      if (INN != "")
         field_value = String(field_value, "INN", INN, SYMB_ENDL);
      end;
   end;

   if (ID AND НайтиЮридическийАдресСубъекта(ID,wladdress) AND
      /*Добавляем к адресу trim(wladdress.Place) пробел, чтобы не наткнуться на Советск - ОСБ Советское*/
      (wladdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(wladdress.Place) + SYMB_BLANK))==0))
      field_value = string(field_value, trim(wladdress.Place), SYMB_BLANK); 
      if((ID != {OurBank}) and ( wladdress.Country != "") 
         AND (Index(strUpr(Name), strUpr(trim(wladdress.Country)))==0))
         field_value = string(field_value, trim(wladdress.Country)); 
      end;
   end;

   StrChangeRusXSet(field_value, field_value, Standart);
   StrMultyToFormat(field_value, field_value, 35, 5);

   Tag = SWIFT_Tag_D;

   SetParm(1, Tag);
   return field_value;

end;

macro FillOrderingCustomerField_SB( RsPaym, Standart)
  var field_value = "", PayerID, INN, field_Client, field_place;
  record RecordAdress ( adress );

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;

  if ( CompareStrWithMasks ("40912*,40913*", RsPaym.PayerAccount)
     and ( RsPaym.PayerAccount!="" ))
        field_value = "/" + RsPaym.PayerAccount + SYMB_ENDL;
     end;

  if ( RsPaym.Payer==0 )
     PayerID = {OurBank};
  else 
     PayerID = RsPaym.Payer;
  end;

  StrChangeRusXSet( RsPaym.PayerName, field_Client, Standart );
    field_value = field_value + field_Client + SYMB_ENDL;    

  if( ПолучитьСубъекта(PayerID, wlparty) != 0 )
    /* Не найден плательщик */
    if ( RsPaym.PayerINN!="" )
       field_value = String( field_value, "INN", RemoveKPP(RsPaym.PayerINN), SYMB_ENDL );
    end;
     
  else
    if ( RsPaym.PayerINN!="" )
       INN = RemoveKPP(RsPaym.PayerINN);
    else
       INN = RemoveKPP( GetPartyINN( PayerID ) );
    end;
    if( INN != "" )
      field_value = String(field_value, "INN", INN, SYMB_ENDL);
    end;
    
    ClearRecord(RecordAdress);
    НайтиЮридическийАдресСубъекта(wlparty.PartyID,RecordAdress);
    if ( RecordAdress.Adress!="" )
      StrChangeRusXSet( RecordAdress.Adress, field_place, Standart );
      field_value = String( field_value, field_place );
    end;
    if( RecordAdress.Place != "" )
      StrChangeRusXSet( RecordAdress.Place, field_place, Standart );
      field_value = String( field_value, " ",field_place );
    end;
  end;  

    StrMultyToFormat( field_value, field_value, 35, 4 );
  return field_value;
end;

/* Записать поле маршрута от текущего контекста с формированием лога (тип узла - B) */
macro ЗаписатьПолеМаршрутаЛог_SB( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode )
  var field_value;

  field_value = СформироватьЗаписьМаршрута_SB(Route, Tag, DKFlag, standart, usingCode);

  ЗаписатьПолеВБлок( НазваниеПоля + Tag, НазваниеБлока, field_value );
end;


/* Записать поле маршрута от нулевого контекста блока */
macro ЗаписатьПолеМаршрутаКонтекст0Лог_SB( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode )
  /* Устанавливаем нулевой контекст блока */
  if( not УстановитьКонтекстБлока() )
    RunError("|Ошибка при установке нулевого контекста блока");
  end;
  /* Запишем узел маршрута */
  if( valtype(Tag) == V_UNDEF )
    ЗаписатьПолеМаршрутаЛог_SB( НазваниеПоля, НазваниеБлока, Route, "", DKFlag );
  else
    ЗаписатьПолеМаршрутаЛог_SB( НазваниеПоля, НазваниеБлока, Route, Tag, DKFlag, standart, usingCode );
  end;
end;

/* Заполняет поле 53 (для платежа) */
macro FillSenderCorrespondentField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, OurCorrAcc="";
   var FIID, Corschem;
   RECORD rcv(pmroute);

   if (valtype(Tag)==V_UNDEF)
     Tag = SWIFT_Tag_A; /* По умолчанию считаем - A */
   end;

   if (ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, Route) AND
       (rcv.RouteID!=Route.RouteID) )
     DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
     field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );
   else
     /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */
     Tag = SWIFT_Tag_B; /* В формах, зовущих эту функцию, опция В есть всегда*/
     if ( RsPaym.ReceiverIsSender!="X")
       OurCorrAcc = RsPaym.ReceiverOurCorrAcc;
       FIID       = RsPaym.ReceiverFIID;
       Corschem   = RsPaym.OutCorschem;
       else
       OurCorrAcc = RsPaym.PayerOurCorrAcc;
       FIID       = RsPaym.PayerFIID;
       Corschem   = RsPaym.InCorschem;
       end;
       
     if (OurCorrAcc == "")/* Корсчет явно не задан */
         OurCorrAcc = ПолучитьКорсчетКонтрагента(Corschem, FIID);
       end;
       if (OurCorrAcc != "")
         /* Корсчет задан явно или для контрагента настроено более одной корсхемы */
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         field_value = SYMB_SLASH + DKFlag;
         field_value = field_value + SYMB_SLASH + OurCorrAcc + SYMB_ENDL;
       end;            
     end;
   
   setparm( 1, Tag );

   return field_value;
end;


/* Заполняет поле 54 (для платежа) */
macro FillReceiverCorrespondentField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error;
   RECORD rcv(pmroute);
   RECORD ocb(pmroute);

   if ( valtype(Tag)==V_UNDEF )
       Tag = SWIFT_Tag_A; /* По умолчанию считаем - A */
   end;

   if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, ocb) AND
       (rcv.RouteID!=ocb.RouteID) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
       (rcv.RouteID!=Route.RouteID) )
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );   
   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 55 (для платежа) */
macro FillThirdReibursBankField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error;
   RECORD rcv(pmroute);
   RECORD ocb(pmroute);
   RECORD irb(pmroute);

   if ( valtype(Tag)==V_UNDEF )
       Tag = SWIFT_Tag_A; /* По умолчанию считаем - A */
   end;

   if( ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
       ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
       ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );   
   end;

   setparm( 1, Tag );

   return field_value;
end;

macro Get_CodeName(CodeKind)
  var rs:object;
  var select:string;
  var params:TArray;
  select = "select ObjCode.t_ShortName "              +
           " from DObjKCode_dbt ObjCode "        +
           " where ObjCode.t_ObjectType= 3 and " +
           " ObjCode.t_CodeKind = :CodeKind" ;

  params = makeArray( SQLParam("CorrCodeKind" , CodeKind) );
  rs = execSQLselect( select, params, FALSE );

  if (not rs.moveNext())
    return "";
  end;
  return rs.value(0);
end;

/* Заполняет поле 56 (для платежа) */
macro FillIntermediaryField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, coresp, IsEq, TerBankID;
   var CorrID, CorrCodeKind, CorrCode, CorrCodeName, CorrAcc;
   RECORD rcv(pmroute);
   RECORD acc(pmroute);

   if ( RsPaym.ReceiverIsSender!="X") 
      CorrID       = RsPaym.ReceiverBankCorrID;
      CorrCodeKind = RsPaym.ReceiverBankCorrCodeKind;
      CorrCode     = RsPaym.ReceiverBankCorrCode;
      CorrCodeName = Get_CodeName (CorrCodeKind);
      CorrAcc      = RsPaym.ReceiverBankCorrAcc;
   else
      CorrID       = RsPaym.PayerBankCorrID;
      CorrCodeKind = RsPaym.PayerBankCorrCodeKind;
      CorrCode     = RsPaym.PayerBankCorrCode;
      CorrCodeName = Get_CodeName (CorrCodeKind);
      CorrAcc      = RsPaym.PayerBankCorrAcc;
   end;

   if ( valtype(Tag)==V_UNDEF )
       Tag = "";
   end;

   if ( IsBankUSB(RsPaym.ReceiverBankID)==false )
      if( ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, acc) AND
          ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND
          (rcv.RouteID!=acc.RouteID) AND
          ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
          (Route.RouteID!=acc.RouteID) )
          DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);      
          field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );
      else
         /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */               
         if ( Tag=="" )
            TerBankID = НайтиТерБанк(CorrID);
            if(TerBankID > 0)
               Tag = SWIFT_Tag_A;
               field_value = ПолучитьКодСубъекта(CorrID, PTCK_SWIFT, error);
            else
               Tag = SWIFT_Tag_D;
               ClearRecord(Route);
               Route.PartyID   = CorrID;
               Route.CodeKind  = CorrCodeKind;
               Route.CodeValue = CorrCode;
               Route.CodeName  = CorrCodeName;
               Route.OutAccount= CorrAcc;
               field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, NULL, standart );
            end;
         end;      
      end;
   else
      TerBankID = НайтиТерБанк(RsPaym.ReceiverBankID);
      if((TerBankID != RsPaym.ReceiverBankID) and (TerBankID != -1))
         Tag = SWIFT_Tag_A;
         field_value = ПолучитьКодСубъекта(TerBankID, PTCK_SWIFT, error);
      end;
   end;

   setparm( 1, Tag );

   return field_value;
end;

/* Заполняет поле 57 (для платежа) */
macro FillAccountWithInstitutionField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, 
       coresp, IsEq, BankCode, INN, oldkey, TerBankID;
   var corSchem, payFIID, BankCodeT, CodeKind,CodeName;
   RECORD rcv(pmroute);
   record RecordAdress ( adress );
   if ( RsPaym.ReceiverIsSender!="X") 
      corSchem     = RsPaym.OutCorschem;
      payFIID      = RsPaym.ReceiverFIID;
      BankCodeT    = RsPaym.ReceiverBankCodeKind;
      CodeKind     = RsPaym.ReceiverCodeKind; 
      CodeName     = Get_CodeName (CodeKind) 
   else
      corSchem     = RsPaym.InCorschem;
      payFIID      = RsPaym.PayerFIID;
      BankCodeT    = RsPaym.PayerBankCodeKind;
      CodeKind     = RsPaym.ReceiverCodeKind; 
      CodeName     = Get_CodeName (CodeKind) 
   end;
   Route.CodeKind  = CodeKind;
   Route.CodeName  = CodeName;

   if ( valtype(Tag)==V_UNDEF )
       Tag = "";
   end;
   TerBankID = НайтиТерБанк(RsPaym.ReceiverBankID);
   if(TerBankID < 0)
   if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND
       ПолучитьСамыйДальнийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, Route) AND
       (Route.RouteID!=rcv.RouteID) )
       DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
       field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );
   else
      /* Если покрытия не будет */
      if( not ПолучитьУзелОсновнойИнструкцииПлатежа(RsPaym.PaymentID, RTDIR_OUT) )
         /* Проверим, не совпадает ли он с корреспондетом */         
         oldkey = KeyNum( f_wlcors, 1 );
         f_wlcors.Number  = corSchem;
         f_wlcors.FI_Kind = 1; /* валюта */
         f_wlcors.FIID    = payFIID;
         if( getEQ(f_wlcors) AND (f_wlcors.CorrID!=RsPaym.ReceiverBankID) )
            /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */      
            if ( Tag=="" )
               if ( (CodeKind==PTCK_SWIFT) AND (BankCodeT!="") )
                  Tag = SWIFT_Tag_A;
               else
                  Tag = SWIFT_Tag_D;
               end;
            end;      

            if ( Tag==SWIFT_Tag_A )                           
               field_value = field_value + BankCodeT;
            elif ( Tag==SWIFT_Tag_D )
               
               Name = GetNameBank_Tag_D(Tag, RsPaym.ReceiverBankName, RsPaym.ReceiverBankID);
               field_value = field_value + Name + SYMB_ENDL;

               if( (IsCreditPaym(RsPaym)) AND НайтиЮридическийАдресСубъекта(RsPaym.ReceiverBankID,RecordAdress) AND
                    (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
                   field_value = string( field_value, trim(RecordAdress.Place) );
               end;
               StrChangeRusXSet( field_value, field_value, standart );
                  StrMultyToFormat( field_value, field_value, 35, 5 );
               end;
            end;
         KeyNum( f_wlcors, oldKey );
      end;
   end;
   elif(TerBankID == RsPaym.ReceiverBankID)
      Tag = SWIFT_Tag_A;
      field_value = ПолучитьКодСубъекта( RsPaym.ReceiverBankID, PTCK_SWIFT, error );
   else
      ClearRecord(Route);
      Route.PartyID   = RsPaym.ReceiverBankID;
      Route.CodeKind  = CodeKind;
      Route.CodeValue = BankCodeT;
      Route.CodeName  = CodeName;
      Route.Name      = RsPaym.ReceiverBankName;
      field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, NULL, standart );

   end;
   if ( Tag=="" )
      /* Если Tag так и не определился (field_value=""), то заполняем его силой, что бы потом крика не было */
      Tag = SWIFT_Tag_A;
   end; 

   setparm( 1, Tag );

   return field_value;
end;

macro FillAccountWithInstitutionFieldn91_SB(RsPaym, Tag, standart)
  var field_value = "", Name, error;
  var NeedAcc, account = "", ID, INN, Code;
  Tag = "";
  
  f_wlcors.Number  = RsPaym.OutCorschem;
  f_wlcors.FI_Kind = 1; /* валюта */
  f_wlcors.FIID    = RsPaym.ReceiverFIID;
  if( getEQ(f_wlcors) )
    if(f_wlcors.CorrID == RsPaym.PayerBankID)
      NeedAcc = false;
      ID      = RsPaym.ReceiverBankID
    else
      NeedAcc = true;
      ID      = f_wlcors.CorrID;
    end;
    
    Code = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error );
    account = f_wlcors.CorAccount;

    if (not error)
       if ( NeedAcc )
          field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
       end;
       field_value = field_value + Code;
       Tag = SWIFT_Tag_A;
    else
       
       if ( NeedAcc )
          field_value = field_value + SYMB_SLASH + account + SYMB_ENDL;
       end;

       Name = ПолучитьИмяБанка( ID );
       if (Name != "")
          field_value = string(field_value, Name, SYMB_ENDL); 
       end;
       
       
       INN = GetPartyINN( ID );
       if (INN != "")
         field_value = String(field_value, "INN", INN, SYMB_ENDL);
       end;
       

       if (ID AND НайтиЮридическийАдресСубъекта(ID,wladdress) AND
         (wladdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(wladdress.Place)))==0))
         field_value = string(field_value, trim(wladdress.Place), SYMB_ENDL); 
         if((ID != {OurBank}) and ( wladdress.Country != "") 
             AND (Index(strUpr(Name), strUpr(trim(wladdress.Country)))==0))
           field_value = string(field_value, trim(wladdress.Country), SYMB_ENDL); 
         end;
       end;

       StrChangeRusXSet(field_value, field_value, Standart);
       StrMultyToFormat(field_value, field_value, 35, 5);

       Tag = SWIFT_Tag_D;
     end;
  end; 
  setparm( 1, Tag );
  return field_value;
end;

/* Заполняет поле 58 (для платежа) */
macro FillBeneficiaryInstitutionField_SB(RsPaym, Tag, standart)
   var field_value = "", Name, tempv, DKFlag, error, coresp, IsEq, BankCode, INN, BankCodeT, CodeKind;
   record RecordAdress ( adress );

   if ( RsPaym.ReceiverIsSender!="X") 
      BankCodeT    = RsPaym.ReceiverBankCodeKind;
      CodeKind     = RsPaym.ReceiverCodeKind; 
   else
      BankCodeT    = RsPaym.PayerBankCodeKind;
      CodeKind     = RsPaym.PayerCodeKind; 
   end;

   if ( valtype(Tag)==V_UNDEF )
       Tag = "";
   end;

   if ( IsPmCoverMode() == FALSE )      
      /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */      
      if ( Tag=="" )
         if ( CodeKind==PTCK_SWIFT )
            Tag = SWIFT_Tag_A;
         else
            Tag = SWIFT_Tag_D;
         end;
      end;      

      if ( Tag==SWIFT_Tag_A )            
         field_value = field_value + BankCodeT;
      elif ( Tag==SWIFT_Tag_D )            
           if ( wlpmpaym.ReceiverAccount!="" )
            field_value = field_value + SYMB_SLASH + RsPaym.ReceiverAccount + SYMB_ENDL;
           end;

         Name = GetNameBank_Tag_D(Tag, RsPaym.ReceiverBankName, RsPaym.ReceiverBankID);
         field_value = String( field_value, Name, SYMB_ENDL );

         if( НайтиЮридическийАдресСубъекта(RsPaym.ReceiverBankID,RecordAdress) AND
             (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
             field_value = string( field_value, trim(RecordAdress.Place) );
         end;
         StrChangeRusXSet( field_value, field_value, standart );
            StrMultyToFormat( field_value, field_value, 35, 5 );
         end;
   else
      /* Заполнеие поля 58 для покрытия */
      if( ПолучитьУзелПолучателяПлатежа( RsPaym.PaymentID, Route ) == TRUE )
         DKFlag = GetSWIFTDKFlagPaymProp(RsPaym);
         field_value = СформироватьЗаписьМаршрута_SB( Route, Tag, DKFlag, standart );
      else
         /* Узла маршрута нет, но это не означет, что мы ничем не заполним это поле */      
         if ( Tag=="" )
            if ( (CodeKind==PTCK_SWIFT) AND (BankCodeT!="") )
               Tag = SWIFT_Tag_A;
            else
               Tag = SWIFT_Tag_D;
            end;
         end;      

         if ( Tag==SWIFT_Tag_A )            
            if ( RsPaym.ReceiverCorrAccNostro!="" )
               field_value = field_value + SYMB_SLASH + RsPaym.ReceiverCorrAccNostro + SYMB_ENDL;
            end;
            field_value = field_value + BankCodeT;
         elif ( Tag==SWIFT_Tag_D )
              if ( RsPaym.ReceiverCorrAccNostro!="" )
               field_value = field_value + SYMB_SLASH + RsPaym.ReceiverCorrAccNostro + SYMB_ENDL;
              end;

            Name = GetNameBank_Tag_D(Tag, RsPaym.ReceiverBankName, RsPaym.ReceiverBankID);            
            field_value = String( field_value, Name, SYMB_ENDL );

            if( (IsCreditPaym(RsPaym)) AND НайтиЮридическийАдресСубъекта(RsPaym.ReceiverBankID,RecordAdress) AND
                (RecordAdress.Place!="") AND (Index(strUpr(Name), strUpr(trim(RecordAdress.Place)))==0) )
                field_value = string( field_value, trim(RecordAdress.Place) );
            end;
            StrChangeRusXSet( field_value, field_value, standart );
               StrMultyToFormat( field_value, field_value, 35, 5 );
            end;
      end;
   end;

   setparm( 1, Tag );

   return field_value;
end;


macro FillBeneficiaryCustomerField_SB( RsPaym, Standart, NeedAcc )
  var field_value, ReceiverID, BankCode, Error, field_Client, field_place, INN;
  record RecordAdress ( adress );

  if ( valtype(Standart)==V_UNDEF )
     Standart = ST_UNDEF;
  end;
  if ( valtype(NeedAcc)==V_UNDEF )
     NeedAcc = false;
  end;

  field_value = "";  
  ReceiverID = RsPaym.Receiver;  

  if( (NeedAcc) and (RsPaym.ReceiverAccount != "" ))
        field_value = SYMB_SLASH + RsPaym.ReceiverAccount + SYMB_ENDL;
     end;
  StrChangeRusXSet( RsPaym.ReceiverName, field_Client, Standart );

  field_value = field_value + field_Client + SYMB_ENDL;
  if(ExistsExternalInProp(RsPaym))
  if(Standart == ST_NONTR)
      if ( RsPaym.ReceiverINN!="" )
            INN = RemoveKPP( RsPaym.ReceiverINN );
      elif ( ReceiverID )
         INN = RemoveKPP( GetPartyINN( ReceiverID ) );
      else
         INN = "";
      end;
      if( INN != "" )
           field_value = String( field_value, "INN", INN, SYMB_ENDL );
      end;
  end;
  ClearRecord(RecordAdress);
  if( ReceiverID AND НайтиЮридическийАдресСубъекта(ReceiverID,RecordAdress) )
     if ( RecordAdress.Adress!="" )
       StrChangeRusXSet( RecordAdress.Adress, field_place, Standart );
       field_value = String( field_value, field_place );
     end;
     end;
  end;  
  StrMultyToFormat( field_value, field_value, 35, 5 );

  return field_value;
end;

macro ГенерацияПоля50 ( RsPaym, Tag, Standart)
   var rs,field_value,error, FIID, Number;
   field_value = "";
   if ( (RsPaym.Payer == RsPaym.PayerBankID) 
    and (ПолучитьКодСубъекта( RsPaym.PayerBankID, PTCK_SWIFT, error))
    and (RsPaym.PayerGroup == PAYMENTS_GROUP_EXTERNAL)
    and (RsPaym.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)
    and ((RsPaym.ReceiverIsSender=="X") 
    and (ПолучитьКорСхему(RsPaym.OutCorschem, RsPaym.ReceiverFIID) == WLD_COR_LORO))
    or  ((RsPaym.PayerIsSender=="X") 
    and (ПолучитьКорСхему(RsPaym.InCorschem, RsPaym.PayerFIID) == WLD_COR_LORO))
    )
    field_value = FillOrderingCustomerField103_A( RsPaym, Standart);
    SetParm(1, SWIFT_Tag_A);
  else
    field_value = FillOrderingCustomerField_SB( RsPaym, Standart );  
    SetParm(1, SWIFT_Tag_K);
   end;
   return field_value;
end;


/* Заполнить поле бенефициара */
/* Заполнение 59a Option A */
/* Вернет "" если заполнить не удалось */
macro FillBeneficiaryCustomerFieldA_SB( RsPaym, Standart )
  var field_value = "", ReceiverID, ReceiverCode, error, CorrID;
  record crs(corschem);
  if ( RsPaym.PaymentID )
        ClearRecord(crs);
        crs.FI_Kind=1;
        crs.FIID = RsPaym.PayerFIID;
        crs.Number = RsPaym.InCorschem;
        if ( not getEQ(crs) )
           return field_value;
        end;
        CorrID = crs.CorrID;
  else
        ClearRecord(crs);
        crs.FI_Kind=1;
        crs.FIID = RsPaym.ReceiverFIID;
        crs.Number = RsPaym.OutCorschem;
        if ( not getEQ(crs) )
           return field_value;
        end;
        CorrID = crs.CorrID;
  end;
  if(RsPaym.Receiver != CorrID)
      return "";
  end;

  if (RsPaym.ReceiverCodeKind != PTCK_SWIFT)
    ReceiverCode = ПолучитьКодСубъекта(RsPaym.Receiver, PTCK_SWIFT, error);
    if ((error != 0 ) OR (ReceiverCode == ""))                      
      return ""; /* у ReceiverID нет BIC ISO - возвращаем "" */
    end;
  else
    if (RsPaym.ReceiverCode != "")
      ReceiverCode = RsPaym.ReceiverCode; /* BIC ISO */
    else
      ReceiverCode = ПолучитьКодСубъекта(RsPaym.Receiver, PTCK_SWIFT, error);
      if ((error != 0 ) OR (ReceiverCode == ""))                      
        return ""; /* у ReceiverID нет BIC ISO - возвращаем "" */
      end;
    end;
  end;

  field_value = "/" + crs.Account + SYMB_ENDL;

  field_value = String(field_value, ReceiverCode);
      
  return field_value;
end;


macro FillInformationOfPaymentField_SB(RsPaym, AllowCodes, ОстатокПоля70, standart )
   var field_value = "", field72, PartyInfo, CurrentCode, continue0, error;
   var IsPartyInfoAndGroundAdded = 0, SWCode, OwnerCode;
   var PayerChargeOffDate;
   RECORD ord( pmroute );
   RECORD rcv( pmroute );
   RECORD ocb( pmroute );
   RECORD irb( pmroute );
                                                      
   if ( valtype(standart)==V_UNDEF )
       standart = ST_UNDEF;
   end;

   /* Если в форме есть поле 70, то сюда попадает остаток из него.         */
   /* Если в поля 70 нет, то сюда попадает инфа, предназначенная в поле 70 */
   if ( IsAllowCode(AllowCodes, Field72CodesNZP) )
      /* Используется только для RUR4, RUR5, RUR6 */
      if ( (valtype(ОстатокПоля70)!=V_UNDEF) AND (ОстатокПоля70!="") )
        field_value = ДобавитьОстатокПоля70( field_value, ОстатокПоля70, standart );
        ОстатокПоля70 = "";
      end;
   end;

   if( (IsAllowCode(AllowCodes, Field72CodesINS)==TRUE))
     error = ПолучитьНашКодСУчетомТС(PTCK_SWIFT, false, SWCode, OwnerCode);
     if((not error) and (ExistsExternalInProp(RsPaym)) and
        (not IsBankType(RsPaym.PayerBankID, PT_KIND_SAVINGS_BANK )))
         field_value = String( field_value, SYMB_SLASH, Field72CodesINS, SYMB_SLASH, SWCode);
         if(OwnerCode != {OurBank})
             field_value = String( field_value, SYMB_SLASH, SYMB_SLASH, {Name_Bank});
         end;
     elif((not error) and (OwnerCode != {OurBank}))
         field_value = String( field_value, SYMB_SLASH, Field72CodesINS, SYMB_SLASH, {Name_Bank});
     end;
     StrChangeRusXSet( field_value, field_value, standart );
   end;

   if (IsAllowCode(AllowCodes, Field72CodesDAS)) /* только для MT103 RUR6 */
     if (RsPaym.ReceiverFIID==0)
       PayerChargeOffDate = RsPaym.PayerChargeOffDate;
       if (PayerChargeOffDate != Date(0, 0 ,0))
         if (field_value != "")
           field_value = string(field_value, SYMB_ENDL);
         end;
         field_value = String(field_value, SYMB_SLASH, Field72CodesDAS, SYMB_SLASH, GetSWIFTDate(PayerChargeOffDate));
       end;
     end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesEARLY) ) /* только для срочных платежей */
     if ( RsPaym.Instancy )
       if (field_value != "")
          field_value = string(field_value, SYMB_ENDL);
       end;
       field_value = String(field_value, SYMB_SLASH, Field72CodesEARLY, SYMB_SLASH);
     end;
   end;

   if ( IsAllowCode(AllowCodes, Field72CodesRPP) )
      /* Используется только для RUR4, RUR5, RUR6 */
      if ( RsPaym.ReceiverFIID==0 )
        if( field_value != "" )
          field_value = String( field_value, SYMB_ENDL );
        end;
        field_value = String( field_value, SYMB_SLASH, Field72CodesRPP, SYMB_SLASH, substr(RsPaym.Number,1,5), ".", GetSWIFTDate(RsPaym.PayDate), "." );
        if ( RsPaym.Priority<10 )
          field_value = String( field_value, "0", string(RsPaym.Priority), "." );
        else
          field_value = String( field_value, string(RsPaym.Priority), "." )
        end;     

        if ( RsPaym.PaymentKind=="П" )
           field_value = String( field_value, "POST" );
        elif ( RsPaym.PaymentKind=="Т" )
           field_value = String( field_value, "TELG" )
        else
           field_value = String( field_value, "ELEK" )
        end;
      else
        /* Валютный платеж */
        if( field_value != "" )
          field_value = String( field_value, SYMB_ENDL );
        end;
        field_value = String( field_value, SYMB_SLASH, Field72CodesRPP, SYMB_SLASH, substr(RsPaym.Number,1,5), ".", GetSWIFTDate(RsPaym.Date), ".06.ELEK" );
      end;
   end;

   if( IsAllowCode(AllowCodes, Field72CodesRCB) AND
       ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
       ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
       ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
       (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
       ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
      if( field_value != "" )
       field_value = String( field_value, SYMB_ENDL );
      end;
      field_value = String( field_value, SYMB_SLASH, Field72CodesRCB, SYMB_SLASH, Route.CodeValue );
   end;
   
   PartyInfo = RsPaym.PartyInfo;
   if (PartyInfo != "") 
     StrChangeRusXSet( PartyInfo, PartyInfo, standart );
     if (IsAllowCode(AllowCodes, Field72CodesREC)==TRUE) 
       field_value = ДобавитьБлокВПоле72( field_value, Field72CodesREC, PartyInfo, standart );
       PartyInfo = "";
     end;/*Если в поле формы нет лексемы REC, то PartyInfo добавим в ACC*/
   end;

   /* ACC в поле 72 формы разрешено,*/
   /*и туда еще ничего не писали - ликвидируем этот недостаток :))*/
   field72 = "";
   if (IsAllowCode(AllowCodes, Field72CodesACC))
     field72 = AddGroundAndPartyInfo(field72, ОстатокПоля70, PartyInfo);
     if (field72 != "")
       field_value = ДобавитьБлокВПоле72( field_value, Field72CodesACC, field72, standart );
     end;
   end;

   /* Добавляем информацию, введенную вручную пользователем в пенли ввода платежки */
   field72 = RsPaym.AdditionalInfo;
   StrChangeRusXSet( field72, field72, standart );
   field_value = ДобавитьБлокВПоле72( field_value, "", field72, standart );

  return field_value;
end;
