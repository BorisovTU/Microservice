/*
    Отчет "Документы сеанса - платежи"
    
    Подсистема - Межбанковские расчеты
    Создан 24.10.2000
    Программист Антон А. Бурцев (AB)
*/

import rsd, wldoc, FIInter, wlsestls,oralib,likepy;

macro Line()
[+----------+-----------+--------------------+-----------+--------------------+-----------------+];
end;

macro WriteHeader()
[|  Номер   |Код плател |  Счет плательщика  |Код получат|  Счет получателя   |  Сумма платежа  |];
Line();
end;

var TotalAmount, TotalCount, CurFIID, CurDK, Direct;

macro InitAllParm()
   ReportObjects = MESKIND_PAYMENT; /* Тип объектов отчета - платежи */
   ReportObjectsTitle = "Платежи сеанса";
end;

macro DefinePaymentDK()
   if ( IsDebetPaymBuff(wlpmpropdeb,wlpmpropcred) )
      return WL_DEBET;
   else
      return WL_CREDIT;
   end;
end;


macro InitReport(SessionID);
   var stat = CommonInitReport("ПЕРЕЧЕНЬ ПЛАТЕЖЕЙ СЕАНСА", SessionID);
   if ( stat != 0 )
      return stat;
   end;
   TotalAmount = $0;
   TotalCount = 0;
   CurFIID = -1;
   CurDK = -1;
   Direct = wlsess.Direct;
   return 0;
end;


/* Отрисовка разделителя по валютам */
macro HandleDKChange(NewDK)
   if ( CurDK != -1 ) /* Если не первый вызов */
      Line();
   end;
   if ( NewDK != -1 ) /* Если не последний вызов */
      println();
      if   ( NewDK == WL_CREDIT )
         println("                           ОБОРОТЫ ПО КРЕДИТУ");
      elif ( NewDK == WL_DEBET )
         println("                           ОБОРОТЫ ПО ДЕБЕТУ");
      end;
      Line();
      WriteHeader();
   end;
   CurDK = NewDK;
   CurFIID = -1;  /* сбрасываем предыдущую группировку */
end;

/* Отрисовка разделителя по Дебету/Кредиту */
macro HandleFIIDChange(NewFIID)
   if ( CurFIID != -1 ) /* Если не первый вызов */
Line();
[Общая сумма платежей: ##################
##############################################################################################################]
( TotalAmount:a:r , SumToStr(CurFIID, TotalAmount):l );
[Общее количество платежей: #############]
( TotalCount:l );
   end;
   if ( NewFIID != -1 ) /* Если не последний вызов */
      println();
      println(StrUpr(ПолучитьИмяФинИн(NewFIID)));
      Line();
   end;
   CurFIID = NewFIID;
   TotalAmount = $0;
   TotalCount = 0;
end;

/* Вывод строки отчета */
macro PrintDoc(ObjKind, ObjID)
   var Number, DK, PayerCode, ReceiverCode;
   var select:string;
   var params:TArray;
   var rs:object;
   ClearRecord(wlpmpaym);
   ClearRecord(wlpmrmprop);
   ClearRecord(wlpmpropcred);
   ClearRecord(wlpmpropdeb);
   if ( not IsSQL )
      if ( FindPayment( ObjID, 0, 0, 0, 0, true, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) != 0 )
         return;
      end;
   else
     select = "select pmpaym.t_PayFIID, pmpaym.t_PayerAccount, pmpaym.t_ReceiverAccount, pmpaym.t_PayAmount, "+ 
                      "Nvl(cred.t_PaymentID,0), Nvl(deb.t_PaymentID,0), Nvl(cred.t_BankCode,chr(0)), Nvl(deb.t_BankCode,chr(0)), rm.t_Number "+
                      "from dpmpaym_dbt pmpaym, dpmprop_dbt cred, dpmprop_dbt deb, dpmrmprop_dbt rm "+
              "where pmpaym.t_PaymentID=:ObjID and deb.t_PaymentID(+)=pmpaym.t_PaymentID and "+
                      "deb.t_DebetCredit(+)=0 and deb.t_Group(+)=1 and cred.t_PaymentID(+)=pmpaym.t_PaymentID and "
                      "cred.t_DebetCredit(+)=1 and cred.t_Group(+)=1 and rm.t_PaymentID=pmpaym.t_PaymentID";
     params = makeArray( SQLParam("ObjID", ObjID ));
     rs = execSQLselect( select, params, FALSE );
     if ( not rs.moveNext() ) 
        return;
     end;
     wlpmpaym.PayFIID = rs.value(0);
     wlpmpaym.PayerAccount = rs.value(1);
     if ( wlpmpaym.PayerAccount=="" )
         wlpmpaym.PayerAccount="";
     end;
     wlpmpaym.ReceiverAccount = rs.value(2);
     if ( wlpmpaym.ReceiverAccount=="" )
         wlpmpaym.ReceiverAccount="";
     end;
     wlpmpaym.PayAmount = rs.value(3);
     wlpmpropcred.PaymentID = rs.value(4);
     wlpmpropdeb.PaymentID = rs.value(5);
     wlpmpropcred.BankCode = rs.value(6);
     if ( wlpmpropcred.BankCode=="" )
        wlpmpropcred.BankCode = "";
     end;
     wlpmpropdeb.BankCode = rs.value(7);
     if ( wlpmpropdeb.BankCode=="" )
        wlpmpropdeb.BankCode = "";
     end;
     wlpmrmprop.Number = rs.value(8);
   end;
   /* Верхний уровень группировки - Дебет/Кредит */
   DK = DefinePaymentDK();
   if ( DK != CurDK )
      HandleDKChange(DK);
   end;
   /* Следующий уровень группировки - по валютам */
   if ( CurFIID != wlpmpaym.PayFIID )
      HandleFIIDChange(wlpmpaym.PayFIID);
   end;
   Number = wlpmrmprop.Number;   
   if ( Direct != "X" )
      PayerCode    = wlpmpropdeb.BankCode;
      ReceiverCode = wlpmpropcred.BankCode;
   else
      PayerCode    = wlpmpropcred.BankCode;
      ReceiverCode = wlpmpropdeb.BankCode;
   end;
[|##########|###########|####################|###########|####################|#################|]
(
   Number:c,
   PayerCode,
   wlpmpaym.PayerAccount,
   ReceiverCode,
   wlpmpaym.ReceiverAccount,
   wlpmpaym.PayAmount:a
);
   TotalAmount = TotalAmount + wlpmpaym.PayAmount;
   TotalCount  = TotalCount + 1;
end;

macro DoneReport()
   HandleFIIDChange(-1);
   HandleDKChange(-1);
end;
