/*
 $Name:        wlinfogt.mac
 $Module:      Межбанковские расчеты
 $Description: Генерация текста запросов - ответов по сверке остатков
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация текста запросов - ответов по сверке остатков                  */
/*                                                                          */
/*  Имя файла: wlinfogt.mac                                                 */
/*  Создан:  17.11.04                                    Фомченкова Л.Н.    */
/****************************************************************************/

import FIInter, CurrInter, "wldoc.mac", "wltools.mac", oralib, likepy;

private const SET_CHAR = "X";

macro ПолучитьИмяСубъекта(ID)
  FILE party(party);

   ClearRecord(party);
   party.PartyID = ID;
   if(not getEQ(party)) 
      return "";
   end;        
   return party.Name;
end;                                     

macro ПолучитьШапку(CorrID, FIID, ReportDate, Запрос, Размер)
  FILE party(party);
  ClearRecord(party);

  FILE corschem(corschem);
  ClearRecord(corschem);

    Var RetStr = "", БИК = "<???>",_БИК = "", ВидКода, Error, EndYou, EndAcc, 
        Год, Месяц, День, ЗапросИлиОтвет;
    var rs:object;
    var select:string;
    var params:TArray;

    corschem.Number  = CorrID;
    corschem.FIID    = FIID;
    corschem.FI_Kind = FIKIND_CURRENCY;
    if(not GetEQ( corschem ))
       std.msg("Не найдена корсхема");
       return RetStr;
    end;

    party.PartyID = corschem.CorrID;

    if(not GetEQ( party ))
       std.msg("Не найден корреспондент");
       return RetStr;
    end;

    if( party.NotResident == SET_CHAR )
       ВидКода = 6;
    else
       ВидКода = 3;
    end;

    _БИК = ПолучитьКодСубъекта( party.PartyID, ВидКода, Error );

    if( Error )
       select = "select t_Code from dobjcode_dbt where t_ObjectID= :PartyID and t_ObjectType=:OBJTYPE_PARTY order by t_Code";
       params = makeArray( SQLParam("PartyID",party.PartyID ),
                           SQLParam("OBJTYPE_PARTY",OBJTYPE_PARTY));
       rs = execSQLselect( select, params, FALSE );
       if ( rs.MoveNext() )
          БИК = rs.Value(0);
       end;
    else
       БИК = _БИК;
    end;

    if (Размер == 1)
       EndYou = "ему";
       EndAcc = "у";
    else
       EndYou = "им";
       EndAcc = "ам";
    end;

    if (Запрос)
       ЗапросИлиОтвет =  ". Просим вас подтвердить сальдо по Ваш";
    else
       ЗапросИлиОтвет =  ". Подтверждаем сальдо по наш";
    end;

    DateSplit(ReportDate, День, Месяц, Год);
    /*Шапка*/
    RetStr = string("Главному бухгалтеру ", party.ShortName, " БИК ", БИК, ЗапросИлиОтвет,
                    EndYou, "\nсчет", EndAcc, " у нас по состоянию на ", День, " число ", 
                    StrLwr(ПолучитьМесяцПоПадежу(Месяц, 2)), " месяца ", Год, " г.:\n" );
    return RetStr;
end;


macro БлокА(CorrID, ReportDate)
    Var RetStr = "", Error, sum: moneyl; 

    FILE corschem(corschem);
    ClearRecord(corschem);
    corschem.Number  = CorrID;
    corschem.FIID    = NATCUR;
    corschem.FI_Kind = FIKIND_CURRENCY;

    if(not GetEQ( corschem ))
       std.msg("Не найдена корсхема");
       return RetStr;
    end;
    sum = RestA(corschem.Account, ReportDate);

    RetStr = string("Счет ", corschem.CorAccount, " в сумме ", sum, "(", RubToStrAlt(sum), ").\n");
    return RetStr;
end;

macro БлокБ(CorrID, FIID, ReportDate)
    Var RetStr = "", Error, sum: moneyl, sumEQV: moneyl; 

    FILE corschem(corschem);
    ClearRecord(corschem);

    FILE finin(fininstr);
    ClearRecord(finin);

    FILE accountAC("account.dbt");
    ClearRecord(accountAC);

    corschem.Number  = CorrID;
    corschem.FIID    = FIID;
    corschem.FI_Kind = FIKIND_CURRENCY;

    if(not GetEQ( corschem ))
       std.msg("Не найдена корсхема");
       return RetStr;
    end;

    if( ПолучитьФинИн( FIID, finin ) != 0 )
        std.msg("Не найден финансовый инструмент");
        return RetStr;
    end ;
    AccountAC.Account = corschem.Account;
    AccountAC.Chapter = 1;
    AccountAC.Code_Currency = FIID;
    if( not getEQ( AccountAC ) )
        std.msg("Не найден валютный счет " + corschem.Account);
        return RetStr;
   end ;

    sum = RestAC(corschem.Account, finin.FIID, ReportDate);
    sumEQV = RestA(accountAC.Connect_Account, ReportDate);
    
    RetStr = string("Счет ", corschem.CorAccount, " в сумме ", sum, "(", CurToStrAlt( sum, NULL, NULL, int(finin.ISO_Number) ), 
                    "), рублевый эквивалент ", sumEQV, "(", RubToStrAlt(sumEQV), ").\n");
    return RetStr;
end;

macro ПолучитьСотрудника(ID, buff, ErrStr)
   Var Error = 0;
   buff.PartyID = {OurBank};
   buff.PersonID = ID;
   if (not getEQ(buff)) 
       std.msg(ErrStr);
       error = 1;
   end;
   return Error;
end;

macro Подписи(FirstSignID, SecondSignID, ExecutorID)

    Var RetStr = "";

    FILE firstsign(officer);
    ClearRecord(firstsign);
    FILE secondsign(officer);
    ClearRecord(secondsign);
    FILE executor(officer);
    ClearRecord(executor);
      
    if( ПолучитьСотрудника(FirstSignID, firstsign, "Не найдено первое лицо для подписи"))
       return RetStr;
    end;

    if( ПолучитьСотрудника(SecondSignID, secondsign, "Не найдено второе лицо для подписи"))
       return RetStr;
    end;

    if( ПолучитьСотрудника(ExecutorID, executor, "Не найден исполнитель"))
       return RetStr;
    end;

    RetStr = string("Подписи:\n", 
                    firstsign.Post:35:l, ПолучитьИмяСубъекта(FirstSignID):35:r, "\n",
                    Secondsign.Post:35:l, ПолучитьИмяСубъекта(SecondSignID):35:r, "\n\n", 
                    "Исполнитель: ", ПолучитьИмяСубъекта(ExecutorID), ", тел. ", executor.PhoneNumber:15:r, "\n" );
    return RetStr;
end;

/*--------------------------------------------------------------------*/
/* Функции для печати протокола                                       */ 
/*--------------------------------------------------------------------*/

macro ДатаДДММГГГГ( Дата )
  var День, Месяц, Год;

  datesplit( Дата, День, Месяц, Год );

  if( День  < 10 )   День  = String( "0", день  );  end;
  if( Месяц < 10 )   Месяц = String( "0", месяц );  end;
  Год = String( год );

  return String( День,".", Месяц,".", Год );
end;


macro PrintHeader(Дата);

[  ];
[                                                                                                            #]({Name_Bank}:r);
[  ];
[                 Протокол формирования запросов на подтверждение остатка на #                                ](ДатаДДММГГГГ( Дата ));
[  ];
[+-----------------------------------+--------------------+--------------------------------------------------+];
[|Корреспондент                      |Корреспондентский   |Результат                                         |];
[|                                   |счет                |                                                  |];
[+-----------------------------------+--------------------+--------------------------------------------------+];

end;

macro PrintLine(Name, Acc, First, stat)
   var msg = "";
   if(First)
       if(stat)
           msg = GetErrMsg(stat);
       else
           msg = "Успешно";

       end;
   end;
[|###################################|####################|##################################################|](Name, Acc, msg:w);
[+-----------------------------------+--------------------+--------------------------------------------------+];

end;



macro PrintFloor(gacc, bacc);
[  ];
[              ИТОГО:];
[         Обработано  # счет(ов)](gacc);
[         Отвергнуто  # счет(ов)](bacc);
end;