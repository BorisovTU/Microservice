/*
 $Name:        wlofkggout.mac
 $Module:      MBR
 $Description: Экспорт сообщений ОФК для ГИС ГМП
*/

/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                        Экспорт сообщений "ОФК для ГИС ГМП"               */
/*                                                                          */
/*  Имя файла: wlofkggout.mac                                               */
/*  Создан:    17.12.12                                                     */
/****************************************************************************/

import "wlexport.mac", oralib, likepy, WldInter, MesInter, cryptdlm, BankInter, "wluftool.mac", "wlufsign.mac";
  file OutFile("") txt write;

const PTCK_URN  = 61;
const TYPE_CODE_OFK_TYPECODE = 70; // Тип сообщения (TypeCode)
const TYPE_CODE_OFK_STATUS   = 71; // Статус сообщения (Status)

/*Чтение настроек из BLOB*/
PRIVATE CLASS TBLOB
  /* порядок хранения настроек в BLOB*/
  /* данные поля должны соответствать структуре WLGGProcSaveParm(WLINS.H:692)*/
  const len_ValueDate        = 4;
//  const len_PayerAccount     = 620; // in other blob, length 1001: #  231910
  const len_BankCode         = 101;
//  const len_ReceiverAccount  = 101; // in other blob, length 1001: #  231910
  const len_KBK              = 1001; // increased from 620 to 1001: #  231910
  const len_Pack             = 101;
  const len_Ground           = 101;
  const len_CodeKind         = 2;
  const len_Code             = 21;
  const len_Name             = 81;
  const len_PartyName        = 201; // GISGMP_FLD_PARTNAME         A8
  const len_TypeCode         = 11;  // GISGMP_FLD_TYPECODE         A9
  const len_Status           = 11;  // GISGMP_FLD_STATUS           A10
  const len_ExchangeType     = 11;  // GISGMP_FLD_EXCHANGETYPE     A11
  const len_SenderRole       = 11;  // GISGMP_FLD_SENDERIDENTIFIER A13

  private macro getPosFromName( valName ):integer

    var props  = GetObjProps(this), 
        offset = 0,
        isValidFld = false;
    for( var item, props )
      if( StrLwr("len_" + valName) == StrLwr(item) );
        isValidFld = true;
        break;
      end;
      offset = offset + GenGetProp( this, item );
    end;

    return IfThenElse( isValidFld, offset+1/*BLOB begin with first symbols*/, 0 );
  end;

  private macro getPINFPRMID()
    var rs:RsdRecordSet = execSQLselect( " select t_keyid from dregparm_dbt where t_name = 'PINFPRM' ", NULL, false );
    if( rs.movenext() )
      return rs.value(0);
    end;
    return 0;
  end;

  macro getRegValSavedValues( PartyName : @string, TypeCode : @string, Status : @string, ExchangeType : @string, SenderRole : @string )
    
    var query = "SELECT DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, :LENGTH_PARTY_NAME, :OFFSET_PARTY_NAME) PartyName," +
                "       DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, :LENGTH_TYPE_CODE, :OFFSET_TYPE_CODE) TypeCode, " +
                "       DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, :LENGTH_STATUS, :OFFSET_STATUS) Status, " +
                "       DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, :LENGTH_EXCHANGE_TYPE, :OFFSET_EXCHANGE_TYPE) ExchangeType, " +
                "       DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, :LENGTH_SENDER_ROLE, :OFFSET_SENDER_ROLE) SenderRole " +
                "FROM dregval_dbt                                       " +
               "WHERE     t_keyid = (SELECT NVL (t_keyid, 0)            " +
                "                    FROM dregparm_dbt                  " +
                "                   WHERE t_name = 'PINFPRM')           " +
                "         AND t_RegKind = :RegKind                      " +
                "         AND t_ObjectID = :ObjectID                    ";

    var params = makeArray(   SQLParam( "LENGTH_PARTY_NAME",  GenGetProp( this, "len_PartyName" ) ),
                              SQLParam( "OFFSET_PARTY_NAME",  getPosFromName( "PartyName" ) ),
                              SQLParam( "LENGTH_TYPE_CODE",  GenGetProp( this, "len_TypeCode" ) ),
                              SQLParam( "OFFSET_TYPE_CODE",  getPosFromName( "TypeCode" ) ),
                              SQLParam( "LENGTH_STATUS",  GenGetProp( this, "len_Status" ) ),
                              SQLParam( "OFFSET_STATUS",  getPosFromName( "Status" ) ),
                              SQLParam( "LENGTH_EXCHANGE_TYPE",  GenGetProp( this, "len_ExchangeType" ) ),
                              SQLParam( "OFFSET_EXCHANGE_TYPE",  getPosFromName( "ExchangeType" ) ),
                              SQLParam( "LENGTH_SENDER_ROLE",  GenGetProp( this, "len_SenderRole" ) ),
                              SQLParam( "OFFSET_SENDER_ROLE",  getPosFromName( "SenderRole" ) ),
                              SQLParam( "RegKind", 1/*REGVAL_KIND_DPRT**/ ),
                              SQLParam( "ObjectID", {operDprt})
                          );

   var rs:RsdRecordSet = execSQLselect( query, params, true );
    if( rs.movenext() )
      PartyName = rs.value("PartyName");  
      TypeCode = rs.value("TypeCode");
      Status = rs.value("Status");
      ExchangeType = rs.value("ExchangeType");
      SenderRole = rs.value("SenderRole");
    else
      params[10] = SQLParam( "RegKind", 0 );                              
      params[11] = SQLParam( "ObjectID", 0);

      rs = execSQLselect( query, params, true );

      if( rs.movenext() )
        PartyName = rs.value("PartyName");  
        TypeCode = rs.value("TypeCode");
        Status = rs.value("Status");
        ExchangeType = rs.value("ExchangeType");
        SenderRole = rs.value("SenderRole");
      end;
    end;
  end;

END;

macro TransFieldName( strInit ):string
   
  if( strInit == "CorrespondentBankAcc" )
    return "CorrespondentBankAccount";
  elif( strInit == "AdressKind" )
    return "kind";
  elif( strInit == "UnifoTransferMsgResp" )
    return "UnifoTransferMsgResponse";
  elif( strInit == "PaymentIdentificatio" )
    return "PaymentIdentificationData";
  elif( strInit == "RecipientServicesIde" )
    return "RecipientServicesIdentifier";
  elif( strInit == "PaymentIdentificatio" )
    return "PaymentIdentificationData";
  elif( strInit == "Account" )
    return "AccountNumber";
  else
    return strInit;
  end;

end;

private macro YYYY_MM_DD( dateInit )
  var day, mon, year, str;

  DateSplit(dateInit, day, mon, year );
  if ( day < 10 )
    day = "0"+string(day);
  else
    day = string(day);
  end;

  if ( mon < 10 )
    mon = "0"+string(mon);
  else
    mon = string(mon);
  end;
  str = string(year) + "-" + mon + "-" + day ;
  return str;
end;

macro GetDateTime():string

  var DateTime:string = "";
  var query_time = "select to_char(systimestamp, 'HH24:MI:SS.FF3') FROM dual ";
  var rs_time:RsdRecordset = execSQLselect(query_time);
  if (rs_time and rs_time.moveNext() )
    DateTime = string( YYYY_MM_DD(date()), "T", rs_time.value(0) );
  end;  
  var query_zone = "select tz_offset(to_char(DBTIMEZONE)) from dual";
  var rs_zone:RsdRecordset = execSQLselect(query_zone);
  if (rs_zone and rs_zone.moveNext() )
    DateTime = DateTime + rs_zone.value(0);
  end;
  return DateTime;
end;

/* Ищет дочерний узел в рамках узла. В дочерние входит только в рамках указанного пути */
private macro FindChildNode( node:object, nodeName, path )   
   var i, child:object;
   var result = "", chName;

   if( index( path, "/") == 0 )
     chName = path;
   else
     chName = substr( path, 1, index( path, "/" ) - 1 );
   end;

   i = 0;
   while( i < node.childNodes.length )
     child = node.childNodes.item( i );
       if( child and ( child.nodeType == CHILD_NODE ) and ( GetNodeName( child.NodeName ) == chName ) )
         if( index( path, "/" ) == 0 )
           return FindChildNode( child, nodeName, "" );
         else
           return FindChildNode( child, nodeName, substr( path, index( path, "/" ) + 1 ) );
         end;
       end;
     i = i + 1;
   end;
   i = 0;
   return node;
end;

private macro GetFinalPaymentStr( xmlxml, count ):string
  if( count > 0)
    var outxml = "";
    var StartIndex:integer = 0;

    for(var i, 1, count)
      StartIndex = Index( xmlxml, "<pi:FinalPayment", StartIndex + 1 );
      if(StartIndex == 0)
        return "";
      end;
    end;

    var EndIndex:integer = Index( xmlxml, "</pi:FinalPayment>", StartIndex ) + 18;
    if(EndIndex == 18)
      return "";
    end;

    outxml = SubStr( xmlxml, StartIndex, EndIndex - StartIndex );
    return outxml;
  else
    return "";
  end;  
end;

private macro ReWriteFP( xmlxml, OutStrPm, count )
  if( count > 0)
    var outxml = "";
    var StartIndex:integer = 0;

    for(var i, 1, count)
      StartIndex = Index( xmlxml, "<pi:FinalPayment", StartIndex + 1 );
      if(StartIndex == 0)
        return "";
      end;
    end;

    var EndIndex:integer = Index( xmlxml, "</pi:FinalPayment>", StartIndex ) + 18;
    if(EndIndex == 18)
      return "";
    end;

    outxml = SubStr( xmlxml, 1, StartIndex-1 ) + OutStrPm + SubStr( xmlxml, EndIndex );
    return outxml;

  else
    return "";
  end;
end;

macro ConvertToUTF8( strconv:string ):string
  
  var select = "";
  var rs;
  var i = 1;
  var newxml = "";
  var params:TArray = TArray();

  while ( i < StrLen(strconv) )
    select = "SELECT CONVERT (:STR, 'UTF8') FROM DUAL;";
    params = makeArray( SQLParam("STR", SubStr( strconv, i, 1024 )) );
    rs = execSQLselect( select, params, FALSE );
    if( rs.moveNext() )
      newxml = newxml + rs.Value(0);
    end;
    i = i + 1024;                                               
  end;

  return newxml;
end;

private macro isLikeFormat( str ):bool // "\d{1,2}\.\d{2}"
  var pos = 0;
  if( pos=index(str, ".") ) 
    if( not IsDigitalNumber(SubStr(str, 1, pos-1)) AND
        not IsDigitalNumber(SubStr(str, pos+1))    AND
        InList(pos,2,3)                            AND 
        (strlen(str)-pos == 2) 
      )
      return true;
    end;
  end;
  return false;
end;

private macro CheckCorrectDataGG( err : @string, name, mnemonic, service ):bool
  if( (name == "") and ((mnemonic == "") or (service == "")) )
    err = "Не заданы параметры электронного сервиса ГИС ГМП";
  elif( service and (isLikeFormat( service ) == false) )
    err = "Некорректный формат версии электронного сервиса ГИС ГМП";
  end;
  SetParm( 0, err ); // если вдруг при вызове не поставили @
  return IfThenElse( err, false, true );
end;

private macro CreateAttrib( xml:object, Node:object, Attrib:string, value:string )
  var Code = xml.createElement(Attrib);
  Node.AppendChild( Code );
  Code.text = value;
end;

private macro CorrectRequestID( ID )
  var stat = 0, mean = "";
  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SymbolPaymentID", V_STRING, mean, stat );
  if( (stat == 0) AND mean AND (SubStr(ID, 2, 1) != "_") )
    mean = SubStr( mean, 1, 1 ) + "_" + SubStr( ID, 1,  8 ) + "-" +
                                        SubStr( ID, 9,  4 ) + "-" +
                                        SubStr( ID, 13, 4 ) + "-" +
                                        SubStr( ID, 17, 4 ) + "-" +
                                        SubStr( ID, 21 ); // + GUID
  else
    mean = ID;
  end;
  return mean;
end;

private macro GetPartyName(ID):string
  var query = " SELECT t_Name "
              "   FROM dparty_dbt "
              "  WHERE t_PartyID = :ID ";

  var rs:RsdRecordset = execSQLselect(query, makeArray(SQLParam("", ID)), FALSE);
  if(rs and rs.moveNext())
    return rs.value(0);
  end;
  return "";
end;

private macro GetFormName( MesID ):string
  var rs = execSQLselect( " SELECT rls.t_name "
                          "   FROM dwlmes_dbt mes, dwlmesrls_dbt rls "
                          "  WHERE mes.t_MesID = :MesID "
                          "    AND rls.t_rlsformid = mes.t_rlsformid",
                          makeArray(SQLParam("MesID", MesID)), FALSE );
  if( rs and rs.moveNext() )
    return strUpr(rs.value(0));
  end;
  return "";
end;

private macro GetQueries(MesID):TArray
  var Fields:TArray = TArray(TArray());
  var rs = execSQLselect( " SELECT wlreq.t_Queries " +
                          " FROM dwlmeslnk_dbt wlmeslnk, dwlreq_dbt wlreq, dwlmes_dbt wlmes " +
                          "  WHERE     wlmeslnk.t_ObjKind = :OBJTYPE_WLD_REQ " +
                          "    AND wlmeslnk.t_ObjID = wlreq.t_ReqID " +
                          "    AND wlmes.t_MesID = wlmeslnk.t_MesID " +
                          "    AND wlmes.t_MesID = :MesID ", 
                          makeArray(SQLParam("OBJTYPE_WLD_REQ", 505), SQLParam("MesID", MesID)), FALSE );

  if( rs and rs.moveNext() )
    var que  = rs.value(0);
    var pos  = index(que, ":"), 
        endl = 0;
    while( pos > 0 )
      endl = index(que, "\n");
      if(endl > 0)
        Fields[Fields.size] = makeArray(substr(que, 1, pos-1), substr(que, pos+1, endl-pos-1));
      else
        endl = strlen(que)-pos;
        Fields[Fields.size] = makeArray(substr(que, 1, pos-1), substr(que, pos+1));
        break;
      end;
      que = substr(que, endl+1);
      pos = index(que,":"); 
    end;
  end;
  return Fields;
end;

private macro GetWlcodeCode(AlgNum : integer, CodeNum : integer) : string
  var select = " select t_Code " +
               "   from dwlcodes_dbt " +
               "  where t_AlgNum = :AlgNum " +
               "    and t_CodeNum = :Code ";
  var params = makeArray( SQLParam("AlgNum", AlgNum),
                          SQLParam("Code",   CodeNum) );
  var rs = execSQLselect(select, params);
  if(rs and rs.moveNext)
    return rs.value("t_Code");
  end;
  return "";
end;

private macro CreateGGTransMsg( xml:object, wlmes, wlsess, ServiceName, Mnemonic, Version, GUID:string, FormName:string ):object
  var PartyName : string = "",
      TypeCode  : string = "",
      Status : string = "",
      ExchangeType : string = "",
      SenderRole : string = "";

  TBLOB.getRegValSavedValues( @PartyName, @TypeCode, @Status, @ExchangeType, @SenderRole );

  if( PartyName == "" )
    PartyName = GetPartyName(wlmes.OutsideAbonentID);

    if( PartyName == "" )
      PartyName = "Казначейство России";
    end;
  end; 

  var GISGMPTransferMsg:object = xml.createElement("unifo:GISGMPTransferMsg");
    var Message = xml.createElement("smev:Message");
    GISGMPTransferMsg.appendChild(Message);
      var Sender  = xml.createElement("smev:Sender");
      Message.appendChild( Sender );
        CreateAttrib( xml, Sender, "smev:Code", GetCodeParty( GetDprtPartyID(wlmes.Department), PTCK_SMEV ) );
        CreateAttrib( xml, Sender, "smev:Name", Bnk_GetPartyName( GetDprtPartyID(wlmes.Department) ) );
      var Recipient = xml.createElement( "smev:Recipient" );
      Message.appendChild(Recipient);
        CreateAttrib( xml, Recipient, "smev:Code", wlmes.OutSideAbonentCode );
        CreateAttrib( xml, Recipient, "smev:Name", PartyName );

      if( ServiceName )
        CreateAttrib( xml, Message, "smev:ServiceName", ServiceName );
      else
        var Service = xml.createElement( "smev:Service" );
        Message.appendChild(Service);
        CreateAttrib( xml, Service, "smev:Mnemonic", Mnemonic );
        CreateAttrib( xml, Service, "smev:Version",  Version );
      end;

      if(FormName == "PACKAGE")
        var isPackage = true;
        var QueFields = GetQueries(wlmes.MesID);
        for( var item, QueFields )
          if(item[0] == "TypeCode")
            TypeCode = item[1];
          elif(item[0] == "Status")
            Status = item[1];
          elif(item[0] == "ExchangeType")
            ExchangeType = item[1];
          elif(item[0] == "SenderRole")
            SenderRole = item[1];
          end;
        end;
        if( TypeCode and (TypeCode != "") )
          TypeCode = GetWlcodeCode( TYPE_CODE_OFK_TYPECODE, int(TypeCode) );
        end;
        if( Status and (Status != "") )
          Status = GetWlcodeCode( TYPE_CODE_OFK_STATUS, int(Status) );
        end;
        if( addNoteForObject(OBJTYPE_MES, makeObjectID(OBJTYPE_MES, null, wlmes), 2/*Идентификатор сообщения в ГИС ГМП*/, GUID) )
          RunError("Ошибка при вставке примечания сообщения");
        end;
      end;

      if(TypeCode == "")
        TypeCode = "GSRV";
      end;

      if(Status == "")
        Status = "REQUEST";
      end;

      if(ExchangeType == "")
        ExchangeType = "0";
      end;

      if(SenderRole == "")
        SenderRole = "АП";
      end;

      CreateAttrib( xml, Message, "smev:TypeCode", TypeCode ); // GSRV, GFNC, OTHR
      CreateAttrib( xml, Message, "smev:Status",   Status );   // REQUEST, RESULT, REJECT, INVALID, ACCEPT, PING, GETRESULT, PROCESS, NOTIFY, FAILURE, CANCEL, STATE, PACKET
      CreateAttrib( xml, Message, "smev:Date",     GetDateTime() );//"Системные дата и время создания сообщения в формате UTC в формата 'yyyy-MM-dd'T'HH:mm:ss.SSSZ'" );
      CreateAttrib( xml, Message, "smev:ExchangeType", ExchangeType );

      var MessageData    = xml.createElement( "smev:MessageData" );
        GISGMPTransferMsg.appendChild(MessageData);
        var AppData        = xml.createElement( "smev:AppData" );
        MessageData.appendChild(AppData);
          var RequestMessage = xml.createElement( "msg:RequestMessage" );
          AppData.appendChild(RequestMessage);

          RequestMessage.setAttribute( "Id",               GUID );
          RequestMessage.setAttribute( "timestamp",        GetDateTime() );//"Системные дата и время создания сообщения в формате UTC в формата 'yyyy-MM-dd'T'HH:mm:ss.SSSZ'" );
          RequestMessage.setAttribute( "senderIdentifier", GetCodeParty( GetDprtPartyID(wlmes.Department), PTCK_URN ) );
          RequestMessage.setAttribute( "senderRole",       SenderRole );
          if( index(FormName, "INFP") > 0 )
            if( addNoteForObject(OBJTYPE_SESS, makeObjectID(OBJTYPE_SESS, null, wlsess), NOTEKIND_SESS_PACKETID, GUID) )
              RunError("Ошибка при вставке примечания сеанса");
            end;

            var ImportRequest = xml.createElement( "gisgmp:ImportRequest" );
            RequestMessage.appendChild(ImportRequest);
              var Package     = xml.createElement( "pirq:Package" );
              ImportRequest.appendChild(Package);
          end;

  return GISGMPTransferMsg;
end;

private macro CorrectNamespace( parent, field, namespace )
  if( InList( field, "PayerName", "PayerAccount", "FinalPayment",         "SupplierBillID",   "Narrative", "Amount",   "PaymentDate",  "BudgetIndex", 
                     "PaymentIdentificatio", "SystemIdentifier", "AccDoc",    "AccDocNo", "AccDocDate",   "Payer", 
                     "PayeeName",            "payeeINN",         "payeeKPP",  "Payee",    "PayeeBankAcc", "ChangeStatus", 
                     "KBK",                  "Priority",         "OKTMO", 
                     "PaytNo", "PartialPayt", "SumResidualPayt" ) AND
      (InList( field, "PayerIdentifier", "PayerName", "PayerAccount" ) == 0) )
    namespace = "pi:";
  elif( InList( field, "Status", "Purpose", "TaxPeriod", "TaxDocNumber", "TaxDocDate", "PaymentType" ) )
    namespace = "bdi:";
  elif( InList( field, "PayerIdentifier", "TransKind", "AdditionalData" ) or ( InList( field, "Name", "Value" ) and index(parent,"AdditionalData") ) )
    namespace = "com:";    
  elif( InList( field, "Name", "BIK", "Account", "CorrespondentBankAcc" ) )
    namespace = "org:";
  elif( index(field, "Bank") and index(parent,"PaymentIdentificatio") )
    namespace = "pi:";
  elif( index(field, "Bank") and index(parent,"PayeeBankAcc") )
    namespace = "org:";
  elif( index(field, "Document") )
    namespace = "pirq:";
  elif( index(field, "PackageID") )
    namespace = "psr:";
  elif( index(field, "PackageStatusRequest") )
    namespace = "gisgmp:";
  end;
  SetParm( 2, namespace );
end;

private macro UpdateSessUID( wlsess, GUID:string )
  execSQL( "update dwlsess_dbt set t_SessUID = :SessUID where t_SessionID = :SessID ",
           makeArray( SQLParam( "", GUID ),
                      SQLParam( "", wlsess.SessionID ) ) );
end;

private macro OutCommonProc( fileName, addrSess, isFormat1_16 )
  var xml:object = ActiveX("Microsoft.XMLDOM");
  var Envelope:object, Body:object, Head:object;
  var CurNode:object, FirstNode:object, item:object;
  var TransferMsg:object;
  var field, field_value, err:integer, namespace;
  var RegVal:bool = false, RegErr:integer = 0;
  var CryptoAPI = RsCryptoAPI();
  var Error = false;
  var FinPMString = "";
  var ServiceName = "",
      Mnemonic    = "",
      Version     = "",
      SymbolPaymentID = "";

  SetBuff( wlSess, addrSess );

  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SERVICE\\NAME",     V_STRING, ServiceName, "" );
  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SERVICE\\MNEMONIC", V_STRING, Mnemonic,    "" );
  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SERVICE\\VERSION",  V_STRING, Version,     "" );

  GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SYMBOLPAYMENTID",  V_STRING, SymbolPaymentID, "" );
  if( not SymbolPaymentID or (index( "ABCDEFJHIGKLMNOPQRSTUVWXYZ", substr(SymbolPaymentID, 1, 1) ) == 0) )
    RunError("Невозможно формирование ID документов для сообщений ГИС ГМП. Задайте символ в диапазоне A-Z в настройке МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ГИС ГМП\\SymbolPaymentID");
    return false;
  end;

  xml.appendChild( xml.createProcessingInstruction("xml"," version='1.0' encoding='utf-8'") );
  Envelope = xml.createElement("SOAP-ENV:Envelope");
  xml.appendChild( Envelope );

  Envelope.setAttribute("xmlns:SOAP-ENV", "http://schemas.xmlsoap.org/soap/envelope/"  );
  Envelope.setAttribute("xmlns:wsu",      "http://docs.oasis-open.org/wss/2004/01/oasis-200401-wss-wssecurity-utility-1.0.xsd"  );
  Envelope.setAttribute("xmlns:smev",     "http://smev.gosuslugi.ru/rev120315"  );
  Envelope.setAttribute("xmlns:pirq",     "http://roskazna.ru/gisgmp/xsd/116/PGU_ImportRequest"  );
  Envelope.setAttribute("xmlns:gisgmp",   "http://roskazna.ru/gisgmp/xsd/116/MessageData"  );
  Envelope.setAttribute("xmlns:msg",      "http://roskazna.ru/gisgmp/xsd/116/Message"  );
  Envelope.setAttribute("xmlns:unifo",    "http://roskazna.ru/gisgmp/02000000/SmevGISGMPService/"  );

  Head = xml.createElement("SOAP-ENV:Header");
  Envelope.appendChild( Head );
  Head.Text = "";

  Body = xml.createElement("SOAP-ENV:Body");
  Envelope.appendChild( Body );
  Body.setAttribute("wsu:Id", "body");

  /* Определяем, есть ли сообщения, подлежащие выгрузке */
  var isFirstMsg = true;
  var GUID = CorrectRequestID(execStoredFunc( "sys_guid", V_STRING ));
  UpdateSessUID( wlSess, GUID );
//Идентификатор пакета в ГИС ГМП
  var cnt = 0;
  while( СчитатьЗапись( wlmes, err ) )

    var FormName = GetFormName(wlmes.MesID);

    cnt = cnt + 1;
    if( isFormat1_16 and isFirstMsg ) // сделаем проверки и добавим узел GISGMPTransferMsg по данным первого сообщения (т.к. данные одинаковые)
      TransferMsg = CreateGGTransMsg( xml, wlmes, wlsess, ServiceName, Mnemonic, Version, GUID, FormName );
      Body.appendChild( TransferMsg );

      /* CHECK */
      var codeSMEV = GetCodeParty( GetDprtPartyID(wlmes.Department), PTCK_SMEV );
      var codeURN  = GetCodeParty( GetDprtPartyID(wlmes.Department), PTCK_URN );
      if( (codeSMEV == "") OR (codeURN == "") )
        ErrExport( "Для отправителя сообщений не задан(ы) код(ы): " + codeSMEV + IfThenElse( codeSMEV and codeURN, ", ", "" ) + codeURN + 
                   ". Задайте значения код(ов) в справочнике субъектов для " + Bnk_GetPartyName( GetDprtPartyID(wlmes.Department) ) + " и повторите экспорт сообщений" );
        return false;
      end;

      codeSMEV = GetCodeParty( wlmes.OutSideAbonentID, PTCK_SMEV );
      if( (codeSMEV == "") AND (wlmes.OutsideAbonentCodeKind == 0) )
        ErrExport( "Для получателя сообщений не задан код ИС в СМЭВ. Задайте значение кода в справочнике субъектов для " +
                   Bnk_GetPartyName( wlmes.OutSideAbonentID ) + " и повторите экспорт сообщений" );
        return false;
      end;

      var ErrTxt="";
      if( CheckCorrectDataGG( ErrTxt, ServiceName, Mnemonic, Version ) == false )
        ErrExport( ErrTxt );
        return false;
      end;
      /* END CHECK */
      isFirstMsg = false;
    end;

    var parent = "";
    /* Формируем файл экспорта */
    var NodeName = IfThenElse( index(FormName, "INFP") > 0, "//pirq:Package", "//msg:RequestMessage" );
    CurNode = FirstNode = IfThenElse( isFormat1_16, xml.selectSingleNode(NodeName), Body );
    while( СчитатьПоле( field, field_value ) )
      if( (field == "_begin") )
        CorrectNamespace( parent, field_value, namespace );
        parent = field_value;
        item = xml.createElement(pyOR(namespace,"") + TransFieldName(field_value));
        if( (not isFormat1_16) or (isFormat1_16 == false) )
          if( field_value == "n1:UnifoTransferMsg" )
            item.setAttribute("xmlns:n1", "http://roskazna.ru/SmevUnifoService/");
          end;
        end;

        if( field_value == "FinalPayment" )
          item.setAttribute("xmlns:pi",  "http://roskazna.ru/gisgmp/xsd/116/PaymentInfo"  );
          item.setAttribute("xmlns:com", "http://roskazna.ru/gisgmp/xsd/116/Common"  );      
          item.setAttribute("xmlns:bdi", "http://roskazna.ru/gisgmp/xsd/116/BudgetIndex"  ); 
          item.setAttribute("xmlns:org", "http://roskazna.ru/gisgmp/xsd/116/Organization"  );
        elif( field_value == "PackageStatusRequest" )
          item.setAttribute("xmlns:psr", "http://roskazna.ru/gisgmp/xsd/116/PackageStatusRequest" );
        end;

        CurNode = CurNode.appendChild(item);
      elif( field == "_end" )
        if( index( CurNode.nodename, TransFieldName(field_value) ) == 0 )
          ErrExport(string("Нарушена структура сообщения ",wlmes.Trn));
          return false;
        end;
        CurNode = CurNode.parentNode;
      else
        if( isFormat1_16 and InList( field, "originatorID", "meaning", "ID" ) )
          if( field == "ID" ) 
            field = "Id";
            field_value = CorrectRequestID(field_value);
          end;
          CurNode.setAttribute( field, field_value );
        elif( ((not isFormat1_16) or (isFormat1_16 == false)) and InList( field, "Date", "TimeStamp" ) )
          item = xml.createElement(namespace + field);
          CurNode.appendChild(item);
          item.Text = GetDateTime();
        elif( ((not isFormat1_16) or (isFormat1_16 == false)) and InList( field, "Version", "kind" ) )
          CurNode.setAttribute( field, field_value );
        else

          if( Index( CurNode.nodename, ":" ) != 0 )
            namespace = substr( CurNode.nodename, 1, Index( CurNode.nodename, ":" ) );
          else
            namespace = "";
          end;

          CorrectNamespace( parent, field, namespace );
          item = xml.createElement(pyOR(namespace,"") + TransFieldName(field));
          CurNode.appendChild(item);
          item.Text = field_value;
        end;      
      end;
    end;    

    if(CurNode.nodename != FirstNode.nodename)
      ErrExport(string("Нарушена структура сообщения ", wlmes.Trn));
      return false;
    end;
  end;

  var OutStr = "", OutStrPm = "", xmlxml = "";
  xmlxml = ConvertToUTF8(xml.xml);
  // подписать сущность
  if( CryptoAPI.IsCryptoActionNeeded( "ГИСГМП|подпись_сущности", 0 /*370*/ ) )

    for(var iMes, 1, cnt)

      FinPMString = GetFinalPaymentStr( xmlxml, iMes );
      if( FinPMString == "" )
        break;
      end;
      if( not CryptoAPI.SignBuffer( "ГИСГМП|подпись_сущности", FinPMString,  @OutStrPm ) )
        PrintLog( 2, CryptoAPI.GetErrorDescription );
        return Error; 
      end;

      xmlxml = ReWriteFP( xmlxml, OutStrPm, iMes );
    end;
  end;

  xmlxml = "<?xml version=\"1.0\" encoding=\"utf-8\"?>" + substr( xmlxml, 22 );
  // подписать файл
  if( CryptoAPI.IsCryptoActionNeeded( "ГИСГМП|экспорт", 0 /*370*/ ) )
    if( not CryptoAPI.SignBuffer( "ГИСГМП|экспорт", xmlxml,  @OutStr ) )
      return Error; 
    end;

  else
    ErrExport( "Не сформирована внешняя ЭЦП запроса к СМЭВ, обращение к web-серверу СМЭВ и передача запроса не выполнялись" );
    //В случае запуска процедуры экспорта в интерфейсном режиме (не из планировщика) для одного или группы сообщений И 
    GetRegistryValue( "МЕЖБАНКОВСКИЕ РАСЧЕТЫ\\ПЕРЕМЕННЫЕ\\CHECKUSEREPORTEXPORT", V_BOOL, RegVal, RegErr);
    if( not GetDialogFlag() and RegVal == false )
      MsgBoxEx( "Не сформирована внешняя ЭЦП запроса к СМЭВ, обращение к web-серверу СМЭВ и передача запроса не выполнялись" );
    end;
    OutStr = xmlxml;
  end;

  Error = Open( OutFile, fileName );
  if((Error == true))
    OutFile.str = OutStr;
    insert(OutFile);

    close(OutFile);
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполнения */
    ErrExport(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

/* Экспорт сообщений ГИС ГМП */
macro OutProc( fileName, addrSess )
  return OutCommonProc( fileName, addrSess );
end;

/* Экспорт сообщений ГИС ГМП 1.16 */
macro OutProc_1_16( fileName, addrSess )
  return OutCommonProc( fileName, addrSess, true /*use format 1.16*/ );
end;
