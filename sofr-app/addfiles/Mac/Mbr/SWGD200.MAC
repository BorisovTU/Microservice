/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация платежей по сообщениям SWIFT MT200                             */
/*                                                                          */
/*  Имя файла: swgd200.mac                                                  */
/*  Создан:    28.02.00                                      Алешин А.В.    */
/****************************************************************************/

import "swgendoc.mac", "pmlib.mac";

/* Платежная инструкция "Банковский перевод на свой счет" */
class FinInstTransferOwnAccount
 var
  TRF              :string,    /* Ссылка на операцию */
  ValueDate        :date,      /* Дата валютирования */
  Currency         :string,    /* Код валюты */
  Sum              :money,    /* Сумма перевода */
  Credit           :bool,      /* Признак дебета/кредита */
  Trans            :bool,      /* Признак транзитного */
  Sender           :Bank,      /* Отправитель */
  SndCorrespAcc    :string,    /* Явный счет дебетования*/
  DirectDKFlag     :string,             /* признак дебета-кредита счета для идентификации корсхемы */
  Intermediary     :Bank,      /* Посредник */
  AccBank          :Bank,      /* Банк-держатель счета */
  BnfBank          :Bank,      /* Банк-бенефициар (всегда Отправитель) */
  RcvInfo          :TRcvInfo100; /* Информация Получателю */

  TRF              = "";
  ValueDate        = date(0,0,0);
  Currency         = "";
  Sum              = $0;
  Credit           = false;
  Trans            = false;

  SndCorrespAcc = "";
end; /* class FinInstTransferOwnAccount */

var MT200 : FinInstTransferOwnAccount;

macro Fill20( TRF )
  MT200.TRF = TRF;
  return TRUE;
end;

macro Fill32A( Date, Cur, Sum )
  MT200.ValueDate = Date;
  MT200.Currency  = Cur;
  MT200.Sum       = money(Sum);
  return TRUE;
end;

macro Fill53B( Branch, Account, System, Code, DKFlag )
  if( valtype( Account ) != V_STRING )
    MT200.SndCorrespAcc = "";
  else
    MT200.SndCorrespAcc = Account;
    MT200.DirectDKFlag  = DKFlag;
  end;
  return TRUE;
end;

macro Fill56A( BIC, Account, System, Code )
  MT200.Intermediary = Bank( BIC, "", "", Account, System, Code );
  return TRUE;
end;

macro Fill56D( Address, Account, System, Code )
  MT200.Intermediary = Bank( "", Address, "", Account, System, Code );
  return TRUE;
end;

macro Fill57a( BIC, Name, Account, CliringSystem, CliringCode, INN )
  MT200.AccBank = Bank( BIC, Name, "", Account, CliringSystem, CliringCode, INN );
  return TRUE;
end;

macro Fill72( Str, Narrative )
  MT200.RcvInfo = TRcvInfo100( Str, Narrative );
  return TRUE;
end;

/* Заполнение списка полей формы  MT200*/
var Fld20  = ТПолеФормы( TransactionReferenceNumberField,    FIELD_MANDATORY, "ReadTRF", "Fill20",  "" ),
    Fld32A = ТПолеФормы( ValueDateCurrencyCodeAmountField_A, FIELD_MANDATORY, "Read32A", "Fill32A", "" ),
    Fld53B = ТПолеФормы( Sender_sCorrespondentField_B,       FIELD_OPTIONAL,  "ReadB",   "Fill53B", "" ),
    Fld56A = ТПолеФормы( IntermediaryField_A,                FIELD_OPTIONAL,  "ReadA",   "Fill56A", "" ),
    Fld56D = ТПолеФормы( IntermediaryField_D,                FIELD_OPTIONAL,  "ReadD_",   "Fill56D", "" ),
    Fld57A = ТПолеФормы( AccountWithInstitutionField_A,      FIELD_OPTIONAL,  "ReadBlock57aTag_A",   "Fill57a", "" ),
    Fld57B = ТПолеФормы( AccountWithInstitutionField_B,      FIELD_OPTIONAL,  "ReadBlock57aTag_B",   "Fill57a", "" ),
    Fld57D = ТПолеФормы( AccountWithInstitutionField_D,      FIELD_OPTIONAL,  "ReadBlock57aTag_D",  "Fill57a", "" ),
    Fld72  = ТПолеФормы( SenderToReceiverInformationField,   FIELD_OPTIONAL,  "Read72",  "Fill72",  "" );

macro ConstructMT200( RespID )
  var field_name, field_value, loop = 1, count = 0, wasRead = 1,
      result = TRUE, error, fld, BankCode;

  /* Последовательно считываем поля и заполняем лексемы */
  while( loop )
    fld = ПоляФормы.Value(count);
    if( wasRead AND (not СчитатьПоле( field_name, field_value )) )
      loop = 0;
    else
      if( (field_name != fld.Name) )
        if( fld.ПолеОбязательно )
          std.msg( "Не указано обязательное поле " + fld.Name );
          result = FALSE;
          loop = 0;
        else
          /* Пропускаем необязательные поля */
          wasRead = 0;
        end;
      else
        if( fld.ОбработатьПолеФормы( field_value ) )
          if( fld.ВыполнитьФункцияБлока  )
            wasRead = 1;
          else
            result = FALSE;
            loop = 0;
          end;
        else
          std.msg("Ошибка при обработке поля формы " + fld.Name);
          result = FALSE;
          loop = 0;
        end;
      end;
      count = count + 1;
    end;
  end;

  if( result == FALSE )
    return result;
  end;

  /* Отправитель - респондент */
  BankCode = ПолучитьКодСубъекта( RespID, PTCK_SWIFT, error );
  MT200.Sender   = Bank( BankCode, "", "", MT200.SndCorrespAcc );
  /* явный счет кредитования через МТ200 задать нельзя */
  MT200.BnfBank  = MT200.Sender;

  MT200.Credit = TRUE; /* входящие банковские переводы - кредитовые */
  MT200.Trans  = MT200.AccBank.IsSet() ; /* документ транзитный - если указан держатель счета */

  return TRUE;
end; /* ConstructMT200 */

macro GenDoc( addrMes )
  var error;
  MT200 = NULL;
  MT200 = FinInstTransferOwnAccount();

  SetBuff( wlmes, addrMes );

  PrintLog(2,"Генерация платежа по МТ200");

  ClearRecord(wlpmpaym);
  ClearRecord(wlpmrmprop);  
  InitPMPROP(wlpmpropdeb);
  InitPMPROP(wlpmpropcred);

  if( not ConstructMT200( wlmes.OutsideAbonentID ) )
    return FALSE;
  end;

  /* Заполнение учетных буферов */
  /* Платеж */
  wlpmpaym.Payer = -1;
  wlpmpaym.Amount      = MT200.Sum;
  wlpmpaym.PayAmount   = MT200.Sum;
  wlpmpaym.BaseAmount  = MT200.Sum;
  wlpmpaym.OrderAmount = MT200.Sum;
  wlpmpaym.ValueDate = MT200.ValueDate;
  if( not ПолучитьФинИнПоISO( MT200.Currency, wlfininstr ) )
    std.msg( "Не определена валюта сообщения по коду ISO :" +  MT200.Currency );
    return FALSE;
  end;
  wlpmpaym.FIID      = wlfininstr.FIID;
  wlpmpaym.PayFIID   = wlfininstr.FIID;
  wlpmpaym.BaseFIID  = wlfininstr.FIID;
  wlpmpaym.OrderFIID = wlfininstr.FIID;

  wlpmpaym.PayerAccount = MT200.SndCorrespAcc;

  wlpmpaym.PayerBankID = wlmes.OutsideAbonentID;

  /* Дебетовые свойства платежа */
  wlpmpropdeb.PayFIID  = wlpmpaym.PayFIID;
  wlpmpropdeb.CodeKind = MT200.Sender.CodeKind;
  wlpmpropdeb.BankCode = MT200.Sender.CodeBank;

  if( MT200.SndCorrespAcc != "" )
    wlpmpropdeb.OurCorrAcc   = MT200.SndCorrespAcc;
    if(MT200.DirectDKFlag == "C")
      wlpmpropdeb.InOurBalance = ""; /* Счет указан в балансе корреспондента */
    elif(MT200.DirectDKFlag == "D")
      wlpmpropdeb.InOurBalance = "X"; /* Счет указан в нашем балансе */
    end;
    if( wlpmpropdeb.OurCorrID <= 0 )
      wlpmpropdeb.OurCorrID    = wlmes.OutsideAbonentID; /* Явно прописываем корреспондента, чтобы правильно определилась с/р */
    end;
  end;

  /* Реквизиты платежа, характерные для R-макета */
  wlpmrmprop.Number                = MT200.TRF;
  wlpmrmprop.PayDate               = MT200.ValueDate;
  wlpmrmprop.Date                  = MT200.ValueDate;
  wlpmrmprop.ClientDate            = MT200.ValueDate;
  wlpmrmprop.PayerBankName         = MT200.Sender.Name;
  wlpmrmprop.PayerCorrAccNostro    = MT200.Sender.Account;
  wlpmrmprop.Priority              = PM_DefaultMaxPriority();
  wlpmrmprop.PartyInfo             = MT200.RcvInfo.REC;
  wlpmrmprop.PaymentKind           = "Э";
  wlpmrmprop.ProcessKind           = " 1";
  wlpmrmprop.ShifrOper             = "01";
  wlpmrmprop.ComissCharges         = PM_CHRG_OUR;

  /* Записываем полное содержимое поля */
  wlpmrmprop.AdditionalInfo       = MT200.RcvInfo.Str;

  /* Кредитовые свойства платежа */
  wlpmpropcred.TransferDate = {curdate};
  wlpmpropcred.PayFIID  = wlpmpaym.PayFIID;

  SetReceiverBankForInPaymentBy57a( wlmes, MT200.AccBank, wlpmpaym, wlpmpropcred, wlpmrmprop );

  if( MT200.Trans )
    wlpmpaym.ReceiverAccount = GetCorAcc( wlpmpaym.PayFIID, wlpmpropcred.Corschem, CORS_ACC_ACCOUNT );
  end;
  wlpmpropdeb.Corschem = ПолучитьКорсхемуПоУмолчанию( wlmes.OutsideAbonentID, wlpmpropdeb.PayFIID, 1, "К", -1, -1, 0, -1, wlpmpropdeb.OurCorrAcc , 
                                                      NULL, false, wlmes.TpSchemID, wlpmpropdeb.InOurBalance, wlpmpropdeb.OurCorrID);
  if(wlpmpropdeb.Corschem != -1)
    wlpmpropdeb.CorrPosType = PM_CORRPOS_TYPE_USER;
  end;

  FillDebetCreditFIID(wlpmpaym, wlpmpropdeb, wlpmpropcred);

  wlpmpropdeb.TransferDate = GetInTransferDate(wlpmpropdeb.Corschem, wlpmpropdeb.PayFIID, wlpmpaym.ValueDate);

  ПользовательскаяДоработкаПлатежа( wlmes, wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop );
  
  if( not ВставитьПлатеж( wlpmpaym, wlpmpropdeb, wlpmpropcred, wlpmrmprop ) )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  var PaymentObj = RsbPayment( wlpmpaym.PaymentID );

  /* Узел Приказодателя - A,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_IN, MT200.Sender.GetRoute() );
  /* Узел Посредника - A(реком.),D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT200.Intermediary.GetRoute() );
  /* Узел Банка Бенефициара - A,B,D */
  PaymentObj.Routes.InsertRouteNode( RTDIR_OUT, MT200.AccBank.GetRoute() );

  if( PaymentObj.Update() )
    std.msg("Ошибка при сохранении платежа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;