/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*         Форма отчета для файла сеанса связи МЦИ (R-Макет)                */
/*          пока только импорт :(                                           */
/*                                                                          */
/*  Имя файла: wlmcprfs.mac                                                  */
/*  Создан:  10.01.03                                    Бердюкова И.И.     */
/****************************************************************************/

import "wlglobal.mac", wlmcgd;

const ДлинаСтроки = 12345;  /* Максимальная длина строки (полноформатный документ) */
const МакетЭСИД = "E000";
const КонецФайла = -1;

var Документы = 0; /* Количество документов */
var Итог = $0; /* Итоговая сумма документов группы */
var ПричинаОтказа = "", ПричинаЗадержки = "";

macro ПричинаЗадержки_E0004( Строка )
 if ( strlen(Строка)>=26 )
   if ( substr( Строка, 25, 2 )=="00" )
      ПричинаЗадержки = "Аннулирован";
   elif ( substr( Строка, 25, 2 )=="01" )
      ПричинаЗадержки = "Отложен, недостаток средств на счете плательщика" ;
   elif ( substr( Строка, 25, 2 )=="02" )
      ПричинаЗадержки = "Отложен, возникновение пассивного остатка на активном счете" ;
   else
      ПричинаЗадержки = "Отложен, неизвестная причина отказа" ;       
   end;
 else
    ПричинаЗадержки = "Недостаточно средств - отложен" ;
 end;

end;
macro ИдентификаторРеестра(Строка)
 [
     Идентификатор реестра:      
     № рейса:                    ##
     № пакета:                   ######
     Дата составления:           ##########
     Дата опердня:               ##########
     Идентификатор составителя:  ##########

 ](  substr(Строка, 1, 2):l, 
     substr(Строка, 3, 8):l,
     СчитатьДату(Строка, 9):f:l,
     СчитатьДату(Строка, 27):f:l,
     substr(Строка, 17, 26):l
 );
end;

macro PrintHeadOfGroup( Строка )
  [   Вид:                        #####
      Номер ЭСИД:                 ##########
      Дата составления:           ########## ########
      Идентификатор составителя:  ##########
  ](  substr(Строка, 1, 5):l,
      substr(Строка, 6, 10):l,
      СчитатьДату(Строка, 16):f:l, СчитатьВремя(Строка,34):l,
      substr(Строка, 24, 10):l
  );
end;

macro PrintHeadOfTable( НомерГруппы, IsOldFormat )
  if( (НомерГруппы == 1) OR (НомерГруппы == 2) OR (НомерГруппы == 6))
    println("");

    if ( IsOldFormat )
    [+----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
     | Вид | Номер | Д |  Срок      | Код очер.| Дата сост. | Вид  | Вид | Вид   | Номер | Дата       |   Дата     | Признак | БИК банка-  |      Счет банка-     |      Номер счета     |  Сумма документа   | БИК банка-  |      Счет банка-     |      Номер счета     | ИНН          |  ИНН         |  Номер | Идентификатор | Тип       | Вид   | Резервное поле |
     |     | докум |   |  платежа   | платежа  | ЭПД        | опер.| ЭПД | обраб.| авизо | документа  |   приема   | года    | плательщика |      плательщика     |      плательщика     |                    | получателя  |      получателя      |      получателя      | плательщика  |  получателя  |  ЭПД   | составителя   | обработки | авизо |                |
     |-----+-------+---+------------+----------+------------+------+-----+-------+-------+------------+------------+---------+-------------+----------------------+----------------------+--------------------+-------------+----------------------+----------------------+--------------+--------------+--------+---------------+-----------+-------+----------------|
    ];
    else
    [+---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
     | Вид | Номер  | Дата сост. |  Идентификатор | БИК банка-  |      Номер счета     |      Счет банка-     |   Номер   |   Дата     | Вид  | БИК банка-  |      Номер счета     |      Счет банка-     |  Сумма документа   | Код очер.| ИНН          | КПП         |  ИНН         |  КПП         | Дата поступления | Дата помещ. | Дата спис. |Вид| Тип   | Признак | Д | Вид   | Номер |  Вид  | Признак |
     |     |  ЭПД   |    ЭПД     |   составителя  | плательщика |      плательщика     |      плательщика     | документа | документа  | опер.| получателя  |      получателя      |      получателя      |                    | платежа  | плательщика  | плательщика |  получателя  |  получателя  | в банк плательщ. | в картотеку |со сч. плат.|ЭПД| обраб.| года    |   | обраб.| авизо | авизо |сост. ЭПД|
     |-----+--------+------------+----------------+-------------+----------------------+----------------------+-----------+------------+------+-------------+----------------------+----------------------+--------------------+----------+--------------+-------------+--------------+--------------+------------------+-------------+------------+---+-------+---------+---+-------+-------+-------+---------|
    ];
    end;
  elif( НомерГруппы == 3 )
    println("");

    [+------------------------------------------------------------------------------------------------+
     | Номер  | Дата        | Идентификатор | № документа | Корреспондирующий    | Сумма ЭПД          |
     | ЭПД    | составления | составителя   |             | счет                 |                    |
     |--------+-------------+---------------+-------------+----------------------+--------------------|
    ];
  elif( НомерГруппы == 4 )  
    println("");

    [+---------------------------------------------------------------------------------------+
     | Номер ЭПД | Дата составления | Идентификатор составителя | Причина задержки           |
     |-----------+------------------+---------------------------+----------------------------|
    ];

  elif( НомерГруппы == 5 )
    println("");
    [+----------------------------------------------------------+
     | Номер ЭПД | Дата составления | Идентификатор составителя | 
     |-----------+------------------+---------------------------|
    ];
  elif( НомерГруппы == 7 )
    println("");
    [+-----------------------------------------------------------+
     | Номер ЭПД | Номер авизо | Дата авизо | Сумма авизо        |
     |-----------+-------------+------------+--------------------|
    ];
  end;  
end;

macro PrintBotOfTable( НомерГруппы, IsOldFormat )
  var Sum_str; 

  Sum_str = RubToStrAlt(Итог); /*Сумма прописью*/

  if( (НомерГруппы == 1) OR (НомерГруппы == 2) OR (НомерГруппы == 6))
    if ( IsOldFormat )
       [|----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|];  
    else
       [|---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|];
    end;
  elif( НомерГруппы == 3 )
    [|------------------------------------------------------------------------------------------------|];
  elif( НомерГруппы == 4 )
    [|---------------------------------------------------------------------------------------|];
  elif( НомерГруппы == 5 )
    [|----------------------------------------------------------|];
  elif( НомерГруппы == 7 )
    [|-----------------------------------------------------------|];
  end;
  if( (НомерГруппы!=4) AND (НомерГруппы!=5) )
    [ Итого:  #################  #]( moneyL( doubleL(Итог)):r, Sum_str:l );
  end;
  [ Документов: # ]( string(Документы):l );
  println("");
end;

macro СтрокаОтчета( Строка, НомерГруппы, IsOldFormat )
  if( (НомерГруппы == 1) OR (НомерГруппы == 2) OR (НомерГруппы == 6))   
   if ( IsOldFormat )
       /*|-----+-------+---+------------+----------+------------+------+-----+-------+-------+------------+------------+---------+-------------+----------------------+----------------------+--------------------+-------------+----------------------+----------------------+--------------+--------------+--------+---------------+-----------+-------+----------------|*/
       [|  #  | ##### | # | ########## |    ##    | ########## |  ##  |  #  |  ##   |  ###  | ########## | ########## |    #    |  #########  | #################### | #################### | ################## |  #########  | #################### | #################### | ############ | ############ | ###### |  ##########   |     #     |  ##   |      ###       |
       ]( substr(Строка, 1, 1):l, substr(Строка, 2, 5):l, substr(Строка, 7, 1):l, СчитатьДату(Строка, 8):f:l, substr(Строка, 16, 2):l, СчитатьДату(Строка, 18):f:l, substr(Строка, 26, 2):l, substr(Строка, 28, 1):l, substr(Строка, 29, 2):l, substr(Строка, 31, 3):l, СчитатьДату(Строка, 34):f:l, СчитатьДату(Строка, 42):f:l, substr(Строка, 50, 1):l, substr(Строка, 51, 9):l,
          substr(Строка, 60, 20):l, substr(Строка, 80, 20):l, moneyL( doubleL(substr(Строка, 100, 18))):r, substr(Строка, 118, 9):l, substr(Строка, 127, 20):l, substr(Строка, 147, 20):l, substr(Строка, 167, 12):l, substr(Строка, 179, 12):l, substr(Строка, 191, 6):l, substr(Строка, 197, 10):l, substr(Строка, 207, 1):l, substr(Строка, 208, 2):l, substr(Строка, 210, 3):l
       );
       if( НомерГруппы == 6 )
         [| Причина                  : #](ПричинаОтказа);
       end;
       if( substr(Строка, 28, 1 ) == "1") /* ЭПД ПФ*/                
         [| Назначение платежа       : #
          | Наименование плательщика : #
          | Наименование получателя  : #
         ]( substr(Строка, 213, 210 ):l, substr(Строка, 423, 160 ):l, substr(Строка, 583, 160 ):l );
       end;
       Итог = Итог + moneyL( doubleL( substr( Строка, 100, 18 ) ) );
    else
     /* |-----+--------+------------+----------------+-------------+----------------------+----------------------+-----------+------------+------+-------------+----------------------+----------------------+--------------------+----------+--------------+-------------+--------------+--------------+------------------+-------------+------------+---+-------+---------+---+-------+-------+-------+---------|*/
       [|  #  | ###### | ########## |   ##########   |  #########  | #################### | #################### |    ###    | ########## |  ##  |  #########  | #################### | #################### | ################## |     #    | ############ |  #########  | ############ |  #########   |    ##########    | ##########  | ########## | # |   #   |    #    | # |   ##  |  ###  |   ##  |    #    |
       ]( substr(Строка, 1, 1):l, substr(Строка, 2, 6):l, СчитатьДату(Строка, 8):f:l, substr(Строка, 16, 10):l, 
          substr(Строка, 26, 9):l, substr(Строка, 35, 20):l, substr(Строка, 55, 20):l, substr(Строка, 75, 3):l, 
          СчитатьДату(Строка, 78):f:l, substr(Строка, 86, 2):l, substr(Строка, 88, 9):l, substr(Строка, 97, 20):l, substr(Строка, 117, 20):l,
          moneyL( doubleL(substr(Строка, 137, 18))):r, substr(Строка, 155, 1):l, substr(Строка, 156, 12):l, substr(Строка, 168, 9):l, substr(Строка, 337, 12):l, substr(Строка, 349, 9):l, 
          СчитатьДату(Строка, 728):f:l, СчитатьДату(Строка, 736):f:l, СчитатьДату(Строка, 744):f:l, substr(Строка, 865, 1):l, substr(Строка, 866, 1):l, substr(Строка, 867, 1):l,
          substr(Строка, 868, 1):l, substr(Строка, 869, 2):l, substr(Строка, 871, 3):l, substr(Строка, 874, 2):l, substr(Строка, 876, 1):l
       );

       if( НомерГруппы == 6 )
         [| Причина                  : #](ПричинаОтказа);
       end;

       if( substr(Строка, 865, 1 ) == "1") /* ЭПД ПФ*/         
         [| Назначение платежа           : #
          | Наименование плательщика     : #
          | Наименование получателя      : #
          | Реквизиты налогового платежа : ## ################### ########### ## ########## ############### ########## ##
         ]( substr(Строка, 518, 210 ):l, substr(Строка, 177, 160 ):l, substr(Строка, 358, 160 ):l,
            substr(Строка, 760, 2 ):l, substr(Строка, 762, 19 ):l, substr(Строка, 781, 11 ):l, substr(Строка, 792, 2 ):l, substr(Строка, 794, 10 ):l, substr(Строка, 804, 15 ):l, substr(Строка, 819, 10 ):l, substr(Строка, 829, 2 ):l );
       end;
       Итог = Итог + moneyL( doubleL( substr( Строка, 137, 18 ) ) );
    end;    
  elif( НомерГруппы == 3 )
    if ( IsOldFormat )
      /*|--------+-------------+---------------+-------------+----------------------+--------------------|*/
       [| ###### | ##########  | ##########    | #####       | #################### | ################## |
       ](
         substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l,
         substr(Строка, 25, 5):l, substr(Строка, 30, 20):l, moneyL( doubleL(substr( Строка, 50, 18 ))):r
        );
       Итог = Итог + moneyL( doubleL( substr( Строка, 50, 18 ) ) );
    else
      /*|--------+-------------+---------------+-------------+----------------------+--------------------|*/
       [| ###### | ##########  | ##########    | ###         | #################### | ################## |
       ](
         substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l,
         substr(Строка, 25, 3):l, substr(Строка, 28, 20):l, moneyL( doubleL(substr( Строка, 48, 18 ))):r
        );
       Итог = Итог + moneyL( doubleL( substr( Строка, 48, 18 ) ) );
    end;
    
  elif( НомерГруппы == 4 )
   /*|-----------+------------------+---------------------------+----------------------------|*/
    [| ######    | ##########       | ##########                | ########################## |
    ](substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l, ПричинаЗадержки:w
    );
  elif( НомерГруппы == 5 )
  /* |-----------+------------------+---------------------------|*/
    [| ######    | ##########       | ##########                |
    ]( substr(Строка, 1, 6):l, СчитатьДату(Строка, 7):f:l, substr(Строка, 15, 10):l
    );
  elif( НомерГруппы == 7 )
  /* |-----------+-------------+------------+--------------------|*/
    [| ######    | ###         | ########## | ################## |
    ](substr(Строка, 1, 6):l, substr(Строка, 8, 3):l, СчитатьДату(Строка, 12):f:l, moneyL( doubleL(substr(Строка, 21, 18))):r
     );
    Итог = Итог + moneyL( doubleL( substr( Строка, 21, 18 ) ) );
  end;
end;

macro ВывестиГруппу( Строка, НомерГруппы )
  if( НомерГруппы == 1 ) /* ЭСИД 1 группы (Е0001) */
    println("   ЭСИД 1 группы - зачисленные ЭПД:");
    PrintHeadOfGroup( Строка );                                             
  elif( НомерГруппы == 2 ) /* ЭСИД 2 группы (Е0002) */
    println("   ЭСИД 2 группы - списанные ЭПД:");
    PrintHeadOfGroup( Строка );
  elif( НомерГруппы == 3 ) /* ЭСИД 3 группы (Е0003) */
    [   ЭСИД 3 группы - зачисление/списание по итогам рейса:
        Вид:                              #####](substr(Строка,1,5));
    [   Номер ЭСИД:                       ##########](substr(Строка,6,15));
    [   Дата составления ЭСИД:            ########## ########](СчитатьДату(Строка,16):f, СчитатьВремя(Строка,34));
    [   Идентификатор составителя:        ##########](substr(Строка,24,10));
    [   № счета:                          ####################](Trim( substr( Строка, 41, 20 ) ));
    [   Наименование владельца:           #](substr(Строка,61,80));
    [   Входящий остаток:                 ##################](moneyL( doubleL( substr( Строка, 141, 18 ) ) ):r);
    [   Исходящий остаток:                ##################](moneyL( doubleL( substr( Строка, 159, 18 ) ) ):r);
    [   Лимит:                            ##################](moneyL( doubleL( substr( Строка, 177, 18 ) ) ):r);
    [   Сумма внутридневного кредита:     ##################](moneyL( doubleL( substr( Строка, 195, 18 ) ) ):r);
    [   Сумма зарезервированных средств:  ##################](moneyL( doubleL( substr( Строка, 213, 18 ) ) ):r);
  elif( НомерГруппы == 4 ) /* ЭСИД 4 группы (Е0004 или Е0005) */
    println("   ЭСИД 4 группы - отложенные/аннулированные:");
    PrintHeadOfGroup( Строка );
  elif( НомерГруппы == 5 ) /* ЭСИД 5 группы (Е0006) */
    println("   ЭСИД 5 группы - аннулированные по отзыву:");
    PrintHeadOfGroup( Строка );
  elif( НомерГруппы == 6 ) /* ЭСИД 6 группы (Е0007) */
    println("   ЭСИД 6 группы - не принятые к обработке:");
    PrintHeadOfGroup( Строка );
  elif( НомерГруппы == 7 ) /* ЭСИД 7 группы (Е0008) */
    println("   ЭСИД 7 группы - с дополнительной информацией:");
    PrintHeadOfGroup( Строка );
  end;
end;

macro PRN_MCIFileSess( ncopy )
 var длина, Строка, Макет, continue0, first_rec, ВидЭСИД, IsOldFormat;
 var NG = 0, isGroupExist = false; 

 IsOldFormat = false;

  /* Считываем загловок */
 длина = ДлинаСтроки;
 Строка = СчитатьСтроку( длина );
 ИдентификаторРеестра(Строка);

 /*Последовательно считываем строки и заполняем отчет*/
 continue0 = true;
 while(continue0)
   длина = ДлинаСтроки;
   Строка = СчитатьСтроку( длина );
      
   if( длина == КонецФайла )
     if ( not isGroupExist )
        PrintHeadOfTable( NG, IsOldFormat );
     end;
     PrintBotOfTable( NG );
     continue0 = false; /* Достигнут конец файла */
   elif ( (substr(Строка, 1, 4) == МакетЭСИД)) /*Новая группа*/
     /* Закрываем предыдущую */
     if( NG != 0 ) 
       if ( not isGroupExist )
          PrintHeadOfTable( NG, IsOldFormat );
       end;
       PrintBotOfTable( NG, IsOldFormat );
     end;
     if( (NG = int(substr(Строка, 5, 1))) >= 5 ) 
       NG = NG-1;
     end;
     ВывестиГруппу( Строка, NG );     
     isGroupExist = false;
     Итог = $0;
     Документы = 0;
   elif(длина != КонецФайла) /* Повторяющиеся реквизиты текущей группы */
     if( (NG == 6) AND (substr(Строка, 1, 1) != "R") )
       ПричинаОтказа = substr( Строка, 1, 100 );
     else       
       if ( not isGroupExist )
         IsOldFormat = false;
         if ( (NG==1) OR (NG==2) OR (NG==6) )
           if ( strlen(Строка)<880 )
             IsOldFormat = true; 
           end;
         elif ( NG==3 )
           if ( strlen(Строка)>65 )
             IsOldFormat = true; 
           end;
         end;
         PrintHeadOfTable( NG, IsOldFormat );
         isGroupExist = true;
       end;       

       if( NG == 4 ) ПричинаЗадержки_E0004(Строка); end;
       СтрокаОтчета( Строка, NG, IsOldFormat );
       ПричинаОтказа = "";       
       Документы = Документы + 1;
     end;
   end;
 end;
end;