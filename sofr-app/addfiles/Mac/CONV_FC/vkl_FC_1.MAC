/*      RS-Retail                                     R-Style Software Lab

        Выгрузка данных из п/с "Частные вклады" АБС   RS-Bank в
        текстовые файлы известного формата
        2000 год

        Создан 27.10.2000
        Рыжиков Александр Александрович ( RA )
*/

import "vkl_fc_1_1.mac";
import "vkl_fc_1_2.mac";

/*========================= Константы и переменные =======================*/
array DobFileName;

const KolFileLaros = Asize( DobFileName );

/*========================== Файлы данных ================================*/

/*========================= Макрофункции =================================*/

macro OpenTxtFiles()
   var stat = True;
   var Br   = "";

   Br = String( Branch:2 );
   Br = SetZeroToSpace( Br );


   /* Открытие текстовых файлов-приемников */
   fnmerge( InFileName, DestinDir, Br + NumSession + "01", "txt" );
   if( not Open( ext_doc, InFileName ) )
      stat = False;
      Protocol.Error( "Ошибка при открытии файла", InFileName );
   end;

   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "00", "txt" );
     if( not Open( ext_dep, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;
   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "03", "txt" );
     if( not Open( ext_cln, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;
   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "05", "txt" );
     if( not Open( ext_trust, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;
   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "06", "txt" );
     if( not Open( ext_clus, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;

   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "07", "txt" );
     if( not Open( ext_addacc, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;

   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "08", "txt" );
     if( not Open( ext_card, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;

   if( not IsLock_Gate ) /* Если не шлюз открываем дополнительные файлы */
      if( stat )
        fnmerge( InFileName, DestinDir, Br + "0002" + "00", "txt" );
        if( not Open( ext_doc_2, InFileName ) )
           stat = False;
           Protocol.Error( "Ошибка при открытии файла", InFileName );
        else
           close( ext_doc_2 );
        end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "03", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         else
            close( ext_doc_2 );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "04", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         else
            close( ext_doc_2 );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "01", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "_limit", "txt" );
         if( not Open( ext_limit, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;

   return stat;
end;

macro Constructor()
   var stat = true;

   Protocol = clProtocol( ProtocolPath );
   Protocol.Header( ConvVersion );

   stat = OpenTxtFiles();

   if( stat )
      fnmerge( InFileName, WorkDir, "sb_depst", "dbt" );
      if( not create( report, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при создании файла", InFileName );
      else
         if( not open( report, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;
   if( stat )
      fnmerge( InFileName, RsBankDir, "country", "dbt" );
      if( not open( country, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при открытии файла", InFileName );
      end;
   end;

   return stat;
end;

/* Вывод отчета о сумме остатков по видам вкладов, выгруженных в текстовик */
macro OutReport()
   var i       = 0;
   var Itogo   = $0;
   var ItogAcc = 0;

[+--------------------------------------------------------------------+
 | Вид вклада     | Код валюты |    Остаток    | Открытых  | Закрытых |
 +----------------+------------+---------------+-----------+----------+];

   keynum( report, 4 );
   rewind( report    );

   while( next( report ) )

[|################|      ####  |###############|###########|##########|]
( report.Kind, report.CodCur, report.Rest, report.ColOpenedAcc, report.ColClosedAcc );
[+----------------+------------+---------------+-----------+----------+];
      Itogo   = Itogo   + report.Rest;
      ItogAcc = ItogAcc + report.ColOpenedAcc;
   end;
[+----------------+------------+---------------+-----------+----------+
 |     Итого                   |###############|###########|          |
 +-------------------------------------------------------------------+]( Itogo, ItogAcc );
end;

/* Ввод даты конвертирования или диапазона дат для загрузки */
macro GetDateCnv()
   var stat = True;

   if( IsLock_Gate )
      EndLockDate = BegLockDate = {curdate};

      stat = GetDate( BegLockDate, "Введите дату начала загрузки данных" );
      if( stat )
        stat = GetDate( EndLockDate, "Введите дату окончания загрузки данных" );
      end;
      if( EndLockDate < BegLockDate )
         MsgBox( String( "Дата окончания периода загрузки ", EndLockDate, "|меньше даты начала ",
                         BegLockDate,".| Работа прервана!" ) );
         stat = False;
      end;
      if( stat )
         DateSplit( BegLockDate, ConvDay, ConvMon, CurY );
      end;
   else
      MsgBox( "Напоминаю:|Дата конвертирования должна быть НЕ МЕНЬШЕ|"
              "даты последней работы в исходной программе + 1 день!" );

      stat = GetDate( DateConvert, " Введите дату конвертирования" );
      if( stat )
        DateSplit( DateConvert, ConvDay, ConvMon, CurY );
      end;
   end;

   return stat;
end;

/* Движение и обработка файлов другой АБС */
macro WorkWithData()
   var stat       = True;
   var NumFile    = 0;
   var Br         = "";
   var Dt         = "";
   var D          = 0;

   CountDate  = date(0,0,0);

   if( IsLock_Gate )
      stat = GetNumSessn( NumSession ); /* Взяли следующий номер сессии за текущую дату */
      if( not stat )
         Protocol.Error( "Не определен номер сессии " );
      end;
   end;

   if( stat )
      stat = Constructor();
   end;

   if( stat )

      Br = SetZeroToSpace( String( Branch:2 ) );

      /*
      Выгрузка вкладчиков
      */
      fnmerge( InFileName, DirConvert, "depclnt", "dbt" );
      Protocol.Message( "Начата обработка файла", InFileName );

      if( not( Open( vkl_cln, InFileName ) ) )
         Protocol.Error("Невозможно открыть файл", InFileName );
         MsgBox( "Невозможно открыть файл " + InFileName );
         stat = False;
      else
         ConvClient( vkl_cln, 0 );
      end;

      /* Направление обработки подписей вкладчиков */ 
      If( IsConvertSign )
         fnmerge( InFileName, DirConvert, "signs", "dbt" );
         Protocol.Message(" ");
         Protocol.Message( " Начата обработка файла подписей ", InFileName );

         if( not( Open( vkl_sgn, InFileName ) ) )
            Protocol.Error( " Невозможно открыть файл", InFileName );
            MsgBox( "Невозможно открыть файл " + InFileName );
            stat = False;
         else   
            If( Clone( rt_sign ) ) /* Пересоздаем коненчый файл подписей */
               ConvClientSign( vkl_sgn );
            else
               Protocol.Error( " Невозможно создать файл подписей ",rt_sign ); 
               stat = False;
            end;  
         end;
      end;

      if( stat )
         fnmerge( InFileName, DirConvert, "depositr", "dbt" );
         Protocol.Message( "Начата обработка файла", InFileName );

         if( not( Open( vkl_acc, InFileName ) ) )
            Protocol.Error("Невозможно открыть файл", InFileName );
            MsgBox( "Невозможно открыть файл " + InFileName );
            stat = False;
         end;
      end;

      if( stat )
         /*
         Выгрузка доверенностей
         */
         fnmerge( InFileName, DirConvert, "trast", "dbt" );
         Protocol.Message( "Начата обработка файла", InFileName );

         if( not( Open( vkl_dov, InFileName ) ) )
            Protocol.Error("Невозможно открыть файл", InFileName );
            MsgBox( "Невозможно открыть файл " + InFileName );
            stat = False;
         else
            ConvClient( vkl_dov, 1 );
         end;

         close( vkl_acc );

      end;

      if( stat )

         /*
         Выгрузка счетов
         */
         fnmerge( InFileName, DirConvert, "depositr", "dbt" );
         Protocol.Message( "Начата обработка файла", InFileName );

         if( not( Open( vkl_acc, InFileName ) ) )
            Protocol.Error("Невозможно открыть файл", InFileName );
            MsgBox( "Невозможно открыть файл " + InFileName );
            stat = False;
         else
            ConvAccount( vkl_acc );
         end;

      end;

      if( stat )

         /*
         Выгрузка операций
         */
         fnmerge( InFileName, DirConvert, "depdoc", "dbt" );
         Protocol.Message( "Начата обработка файла", InFileName );

         if( not( Open( vkl_doc, InFileName ) ) )
            Protocol.Error("Невозможно открыть файл", InFileName );
            MsgBox( "Невозможно открыть файл " + InFileName );
            stat = False;
         else
            ConvDocument( vkl_doc );
         end;

         OutReport();
         Close( vkl_cln );
         Close( vkl_acc );
         Close( vkl_doc );
      end;
   end;
end;

/* Основная процедуру */
macro main()
  var stat = True;

  stat = GetDateCnv();

  if( stat )
     WorkWithData();
     /*
       Просмотр протокола
     */
     Protocol.Footer();
     Protocol.View();
  else
     if( IsLock_Gate )
        MsgBox( "Неправильно задан диапазон дат для загрузки" );
     else
        MsgBox( "Не введена дата конвертация" );
     end;
  end;

end;

main();
