/*
  RS-Retail
  Конвертирование данных из
    п/с "Частные вклады" АБС   RS-Bank в
    п/с "Обслуж.физ.лиц" САРБУ RS-Retail
  2000 год

  Вторая часть конвертера:
  Конвертирование данных об операциях по счетам вкладчиков ( depdoc.dbt )

  Рыжиков Александр Александрович
  25.08.2000
*/
import "vkl_num_fc.mac";
import "opercode.mac";

/*
  Переменные
*/


macro OutLimitDate()
   var stat = True;

   ext_limit.Str = MakeTxtStr( extrn_limit );
   if( not Insert( ext_limit ) )
      Protocol.Error( "По счету", extrn_limit( LIMIT_ACCOUNT ) , "не вставлена запись за критическую",
                        extrn_limit( LIMIT_DATE ) );
   end;
   return stat;
end;

macro FillRepArrays( CodeCur )
   var stat = True;

   /* Найдем соответствующую запись во временном файле отчета */
   keynum     ( report, 0 );
   rewind     ( report    );
   ClearRecord( report    );

   report.FNCash  = Branch;
   report.IsCur   = FlagCur;
   report.Kind    = extrn_doc( DOC_TYPE_ACCOUNT );
   report.CodCur  = CodeCur;

   stat = ( GetGE( report ) and ( report.FNCash  == Branch                        ) and
                                ( report.IsCur   == FlagCur                       ) and
                                ( report.Kind    == extrn_doc( DOC_TYPE_ACCOUNT ) ) and
                                ( report.CodCur  == CodeCur                       )     );
   if( stat )
      /* нашли запись */
      report.Rest   = report.Rest + extrn_doc( DOC_REST );
      report.CodCur = CodeCur;

      if( extrn_doc( DOC_TYPE_COMPLEX_OPER ) != 10 )
         report.ColOpenedAcc = report.ColOpenedAcc + 1;
      else
         report.ColClosedAcc = report.ColClosedAcc + 1;
      end;

      report.ColAccounts = report.ColOpenedAcc + report.ColClosedAcc;

      if( not Update( report ) )
         Protocol.BtrError( "Не обновлена запись в файлк отчета по вкладу", report.Kind,
                            " код валюты ", report.CodCur );

      end;
   else
      /* Занесем новый элемент */
      ClearRecord( report );

      report.FNCash  = Branch;
      report.IsCur   = FlagCur;
      report.CodCur  = CodeCur;
      report.Kind    = extrn_doc( DOC_TYPE_ACCOUNT );
      report.Rest    = extrn_doc( DOC_REST );

      if( extrn_doc( DOC_TYPE_COMPLEX_OPER ) != 10 )
         report.ColOpenedAcc = 1;
      else
         report.ColClosedAcc = 1;
      end;
      report.ColAccounts = 1;

      if( not Insert( report ) )
         Protocol.BtrError( "Не вставлена запись в файл отчета по вкладу", report.Kind,
                            " код валюты ", report.CodCur );
      end;
   end;
end;

/* Проверка закрытых счетов для конвертера */
macro CheckCloseAcc()
   var stat = True;

   if( not Islock_Gate )  stat = ( depositr.Open_Close != "З" ); end;

   return stat;
end;

macro NeedLimit( Type_Account, DepDate )
   var stat = False;

   if( ( ( Type_Account == "ДоВостреб"    )  or ( Type_Account == "ДоВостр.Нрзд" ) or
         ( Type_Account == "ДоВостребRUR" )  or ( Type_Account == "ДоВостребDDM" ) or
         ( Type_Account == "ДоВостребEUR" ) ) and
       ( DepDate<= Date( 31, 12, 1999 ) ) )
      stat = True;
   end;

   return stat;
end;

macro CheckDateHistr( FileID )
   var DateHistr  = date( 31, 12, 2000 ); /* NullDate */

   /* Новый счет */
   rewind( vkl_acc );
   vkl_acc.Account       = FileID.Account;
   vkl_acc.Code_Currency = FileID.Code_Currency;
   
   if( not GetEQ( vkl_acc ) )
      Protocol.Error( "Проверка даты операциии! Не найден счет", FileID.Account, ", валюта ", FileID.Code_Currency );
   else
     if( DateHistr > vkl_acc.Final_Date )
        DateHistr = vkl_acc.Final_Date;
     end;
   end;      

   if( FileID.Date_Document >= DateHistr )
      return True;
   else
      return False;
   end;

end;

macro ConvDocument( FileID )
/*
  Цикл по записям файла
*/
   var stat;
   var NextAccount   = False;
   var WasWork       = False;
   var WasFillLimit  = False;
   var WasFirstOper  = False;
   var WasFirstProc  = False;
   var NeedOutStr    = True;
   var Account       = "";
   var Type_Account  = "";
   var Code_Currency = 0;
   var DOC_KINDOP    = 0;
   var ApplType      = 0;
   var NumDayDoc     = 0;
   var Lim_Date      = NullDate;
   var SaveDateDoc   = NullDate;
   var ComplexDate   = NullDate;
   var KolView       = 0;
   var KolWork       = 0;

   Nrec = NRecords( FileID );
   InitProgress( Nrec, "", "Конвертация операций" );

   while( Next( FileID ) )
      NeedOutStr = True;
     if( CheckDateHistr( FileID ) and
         ( FileID.KindOp        !=         8 )  and
         ( FileID.KindOp        !=         9 )     )
      if( ( FileID.Account != Account ) or ( FileID.Code_Currency != Code_Currency ) )

         /* Дообработка предыдущего счета, если он обработан без ошибок */
         if( NextAccount and stat )
            if( WasFillLimit )
               stat = OutLimitDate();
            end;
            if( not stat )
               Protocol.Error( "Не выгружены данные за критическую дату по счету", extrn_doc( DOC_ACCOUNT ) );
            end;
            if( stat )
               FillRepArrays( Code_Currency );
            end;
         end;

         /* Новый счет */
         rewind( vkl_acc );
         vkl_acc.Account       = FileID.Account;
         vkl_acc.Code_Currency = FileID.Code_Currency;
         stat = NextAccount = GetEQ( vkl_acc );
         if( not stat )
            Protocol.Error( "Не найден счет", FileID.Account, ", валюта ", FileID.Code_Currency );
         else
            NextAccount = ( ( vkl_acc.TAccount == vkl_acc.CAccount_connect ) and
                            ( NeedConvertType( FileID.Account )            ) and
                            ( IsNeedConv     ( FileID.Account )            ) and
                            ( vkl_acc.Open_Close == ""                     ) and
                            ( NeedConvVAL( FlagCur, vkl_acc.Code_Currency  )     ) );
         end;
         if( NextAccount )
            Lim_Date      = vkl_acc.Previos_Date;
            Account       = FiLeID.Account;
            Code_Currency = FileID.Code_Currency;
            WasFillLimit  = False;
            WasFirstOper  = False;
            WasFirstProc  = False;
            SaveDateDoc   = NullDate;
            ComplexDate   = NullDate;
            NumDayDoc     = 0;
            WasWork       = False;
         end;
      else
         stat = True;
      end;
/*
      ОБРАБОТКА ОПЕРАЦИИ
*/
      if( NextAccount and stat ) /*and ( FileID.Lic_Bok == 1 ) )*/
         if( stat )
   /*
            Заполнение полей для выгрузки в текстовый файл операций
   */
            asize( extrn_doc, 0 );
            extrn_doc( DOC_ACCOUNT      ) = vkl_acc.TAccount;

            extrn_doc( DOC_TYPE_ACCOUNT ) = vkl_acc.CAccount;
            extrn_doc( DOC_DATE         ) = FileID.Date_Document;
            extrn_doc( DOC_DEP_DATE     ) = extrn_doc( DOC_DATE );

            extrn_doc( DOC_IN_SUM       ) = $0;
            extrn_doc( DOC_OUT_SUM      ) = $0;

            stat = GetNumOper( FileID.KindOp,
                               DOC_KINDOP,
                               extrn_doc( DOC_TYPE_OPER         ),
                               extrn_doc( DOC_TYPE_COMPLEX_OPER ),
                               extrn_doc( DOC_APPL_TYPE         ),
                               extrn_doc( DOC_GROUND            ),
                               extrn_doc( DOC_VID               ) );

            if( not stat )
               Protocol.Error(" Для счета ", FileID.Account,
                              " не найдена операция ", String( FileID.KindOp ), " в списке операций" );

            end;
         end;

         if( stat )
            if( not WasFirstOper )
               WasFirstOper = True;
               if( extrn_doc( DOC_VID )  )
                  extrn_doc( DOC_TYPE_OPER         ) = Открытие_Наличными;
                  extrn_doc( DOC_TYPE_COMPLEX_OPER ) = extrn_doc( DOC_TYPE_OPER );
               else
                  extrn_doc( DOC_TYPE_OPER         ) = Открытие_Безналичными;
                  extrn_doc( DOC_TYPE_COMPLEX_OPER ) = extrn_doc( DOC_TYPE_OPER );
               end;
            end;

            extrn_doc( DOC_IN_SUM  ) = FileID.InSum;
            extrn_doc( DOC_OUT_SUM ) = FileID.OutSum;
            if(  ( extrn_doc( DOC_TYPE_OPER) == 73 ) and 
                 ( extrn_doc( DOC_IN_SUM   ) == $0 )  )
              /* это списание */
               extrn_doc( DOC_TYPE_OPER) = 63;
               extrn_doc( DOC_TYPE_COMPLEX_OPER ) = extrn_doc( DOC_TYPE_OPER );
            end;


         end;

         if( stat )
            extrn_doc( DOC_REST         ) = FileID.Rest;
            extrn_doc( DOC_CODE_CLIENT  ) = "";
            extrn_doc( DOC_PERC_OPR_SUM ) = $0;
            extrn_doc( DOC_PERC_REST    ) = $0;
            extrn_doc( DOC_LINK_FLAG    ) = "";
            extrn_doc( DOC_IS_CONTROL   ) = "";
            extrn_doc( DOC_AUTHOR       ) = "";

            if( SaveDateDoc != extrn_doc( DOC_DATE ) )
               SaveDateDoc = extrn_doc( DOC_DATE );
               NumDayDoc   = 0;
            end;
            NumDayDoc = NumDayDoc + 1; /* Увеличим счетчик документов за день */
            extrn_doc( DOC_NUM_DAY_DOC ) = NumDayDoc * 100;

            if( extrn_doc( DOC_DEP_DATE ) <= Lim_Date )
               /* Операции до "Критической даты" */
               WasFillLimit = True;
               extrn_limit( LIMIT_ACCOUNT              ) = extrn_doc( DOC_ACCOUNT );
               extrn_limit( LIMIT_DATE                 ) = Lim_Date;
               extrn_limit( LIMIT_SUM_CALC_ALL         ) = $0;
               extrn_limit( LIMIT_SUM_PAY_ALL          ) = $0;
               extrn_limit( LIMIT_SUM_CALC_NO_PAY_2001 ) = $0;
               extrn_limit( LIMIT_SUM_CALC_NO_PAY_2002 ) = $0;
               extrn_limit( LIMIT_FORWARD_PERCENT      ) = $0;
               extrn_limit( LIMIT_SUM_PERC_OUT         ) = $0;
               extrn_limit( LIMIT_SUM_COMPENS_IN       ) = $0;
               extrn_limit( LIMIT_SUM_COMPENS_OUT      ) = $0;
               extrn_limit( LIMIT_DATE_NEXT_CALC       ) = NullDate;
               extrn_limit( LIMIT_REST                 ) = extrn_doc( DOC_REST );
            else
               /* После:
                  1.Поднимаем флаг выгрузки операций в файл второй сессии ( только для конвертера )
                  2.Нужно связать комплексные операции и только выдачу % за
                  одну дату документа
               */

               WasWork = True;
               if( stat )
                  /* 1. проверим, есть ли ссылка на операции другого вклада*/
                  if( GetApplTpForObject( FlagCur, 
                                          extrn_doc( DOC_TYPE_ACCOUNT ), 
                                          3010 ) )
                     /* найдена ссылка */
                     TYPE_ACCOUNT = pc_apltp.ApType;
                  else
                     TYPE_ACCOUNT = extrn_doc( DOC_TYPE_ACCOUNT );
                  end;

                  /* 2. Проверим код операции после критической даты */
                  Keynum( sb_typop, 0 );

                  sb_typop.IsCur    = FlagCur;
                  sb_typop.Kind     = TYPE_ACCOUNT;
                  sb_typop.NumOpert = extrn_doc( DOC_TYPE_OPER    );
                  if( not GetEQ( sb_typop ) )
                     Protocol.Error( "Для вклада", TYPE_ACCOUNT, " (", extrn_doc( DOC_TYPE_ACCOUNT ), 
                                     ") не найдена операция с кодом ", extrn_doc( DOC_TYPE_OPER ), 
                                     "во исходной БД код операции", FileID.KindOp );
                     Protocol.Message( "Операции по счету", extrn_doc( DOC_ACCOUNT ), "старый", FileID.Account,
                                       "после критической даты не выгружены" );
                     stat = False;
                  end;
               end;
               /*
               /* проверим нужно ли оформлять операцию причисления % как прологацию */
               if( stat )
                  if( ( extrn_doc( DOC_DEP_DATE ) >= akt_acc.End_DateDep ) and
                      ( sb_dtyp.FormContr > 0 ) and
                      ( not WasFirstProc      ) )
                     WasFirstProc = True;
                     extrn_doc( DOC_TYPE_COMPLEX_OPER ) = Пролонгация_Депозитов;
                  end;
               end;
               */
               if( stat )
                  /* Установка признака главного документа */
                  if( extrn_doc( DOC_TYPE_OPER ) == extrn_doc( DOC_TYPE_COMPLEX_OPER ) )
                     extrn_doc( DOC_IS_CONTROL ) = StrFor( 1 );
                  elif( ( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == Закрытие_Наличными    ) or
                        ( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == Закрытие_Безналичными )   )
                     if( ( extrn_doc( DOC_TYPE_OPER ) == Расход                ) or
                        ( extrn_doc( DOC_TYPE_OPER ) == Списание_По_Раз_Поруч )    )
                        extrn_doc( DOC_IS_CONTROL ) = StrFor( 1 );
                     end;
                  elif( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == Пролонгация_Депозитов )
                     extrn_doc( DOC_IS_CONTROL ) = StrFor( 1 );
                  end;

               end;
            end;
            extrn_doc( DOC_OPER              ) = FileID.Oper;
            extrn_doc( DOC_ACTION            ) = 0;
            extrn_doc( DOC_OBJECT_PERC       ) = 0;
         end;
   /*
         Запись в файлы
   */
         if( stat and NeedOutStr )
            if( not WasWork )
               ext_doc.Str = MakeTxtStr( extrn_doc );
               if( not Insert( ext_doc ) )
                  Protocol.Error( "Для счета", extrn_doc( DOC_ACCOUNT ),
                                 "не выгружена операция", extrn_doc( DOC_DATE ) );
                  stat = FALSE;
               end;
            else
               ext_doc_2.Str = MakeTxtStr( extrn_doc );
               if( not Insert( ext_doc_2 ) )
                  Protocol.Error( "Для счета", extrn_doc( DOC_ACCOUNT ),
                                 "не выгружена операция", extrn_doc( DOC_DATE ) );
                  stat = FALSE;
               end;
            end;
         end;

         if( stat )
            KolWork = KolWork + 1;
         end;
      end;
     end;
      UseProgress( KolView = KolView + 1 );
   end; /* elihw */
   RemProgress( KolView );
   Protocol.Message( "Окончена обработка. Просмотрено ", KolView,
                     "записей. Сконвертировано ", KolWork );

   if( NextAccount and stat )
      if( WasFillLimit )
         stat = OutLimitDate();
      end;
      if( not stat )
         Protocol.Error( "Не выгружены данные за критическую дату по счету", extrn_doc( DOC_ACCOUNT ) );
      end;
      if( stat )
         FillRepArrays( Code_Currency );
      end;
   end;
end;




