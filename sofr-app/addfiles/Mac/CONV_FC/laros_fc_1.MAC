/*      RS-Retail                                     R-Style Software Lab

        Выгрузка данных из базы Ларос в текстовые файлы известного формата

        Создан 21.09.99
        Рыжиков Александр Александрович ( RA )
*/

import "comm_fc.mac";
import "laros_fc_1_1.mac";
import "laros_fc_1_2.mac";
import "laros_fc_1_3.mac";

/*========================= Константы и переменные =======================*/
array DobFileName;
      DobFileName(0) = "b";
      DobFileName(1) = "d";

const KolFileLaros = Asize( DobFileName );

/*========================== Файлы данных ================================*/

/*========================= Макрофункции =================================*/

macro OpenTxtFiles()
   var stat = True;
   var Br   = "";

   Br = String( Branch:2 );
   Br = SetZeroToSpace( Br );


   /* Открытие текстовых файлов-приемников */
   fnmerge( InFileName, DestinDir, Br + NumSession + "01", "txt" );
   if( not Open( ext_doc, InFileName ) )
      stat = False;
      Protocol.Error( "Ошибка при открытии файла", InFileName );
   end;

   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "00", "txt" );
     if( not Open( ext_dep, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;
   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "03", "txt" );
     if( not Open( ext_cln, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;
   if( stat )
     fnmerge( InFileName, DestinDir, Br + NumSession + "04", "txt" );
     if( not Open( ext_resrv, InFileName ) )
        stat = False;
        Protocol.Error( "Ошибка при открытии файла", InFileName );
     end;
   end;

   if( not IsLock_Gate ) /* Если не шлюз открываем дополнительные файлы */
      if( stat )
        fnmerge( InFileName, DestinDir, Br + "0002" + "00", "txt" );
        if( not Open( ext_doc_2, InFileName ) )
           stat = False;
           Protocol.Error( "Ошибка при открытии файла", InFileName );
        else
           close( ext_doc_2 );
        end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "03", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         else
            close( ext_doc_2 );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "04", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         else
            close( ext_doc_2 );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "0002" + "01", "txt" );
         if( not Open( ext_doc_2, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
      if( stat )
         fnmerge( InFileName, DestinDir, Br + "_limit", "txt" );
         if( not Open( ext_limit, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;

   return stat;
end;

/*  Создаем базу документов за период */
macro SetDocInd()
   var stat          = False;
   var Dt            = "";
   var D             = 0;
   var GateCountDate = BegLocKDate;
   var Br            = "";
   var NumFile       = 0;

   Br = SetZeroToSpace( String( Branch:2 ) );

   while( GateCountDate < EndLockDate )
      DateSplit( GateCountDate, D, Null, Null );
      Dt = SetZeroToSpace( String( D:2 ) );

      fnmerge( InFileName, DirConvert + GateDir, "d" + Br + "xi" + Dt, "dbf" );

      if( ExistFile( InFileName ) )
         stat = Open( FLAROS, InFileName );
         if( not stat )
            Protocol.Error("Невозможно открыть файл", InFileName );
            /*MsgBox( "Невозможно открыть файл" + InFileName );*/
         end;
      else
         stat = False;
         Protocol.Warning( "Не существует файла опердневника", InFileName, "за", GateCountDate );
      end;

      if( stat )
         next( Flaros ); /* первая строка дата опердневника */
         while( next( Flaros ) )

            message( "Создание индекса", InFileName, " счет...", Flaros.Nbals," + ", Flaros.NomLs );
            /*println( " счет...", Flaros.Nbals," + ", Flaros.NomLs, " ", FLAROS.DATAV );*/

            ClearRecord( indfile );
            indfile.Nbals = Trim( Flaros.Nbals );
            indfile.Nomls = Trim( Flaros.NomLs );
            indfile.Oper  = Trim( Flaros.Oper  );

            if( ( indfile.Nbals != "" ) and ( indfile.Nomls != ""    ) and
                ( indfile.Oper  != "" ) and ( Flaros.Oper   != "111" )
              )
               if( ( Int( Trim( indfile.Oper ) ) < 190 ) or
                   ( Int( Trim( indfile.Oper ) ) > 197 )
                 )
                  indfile.Oper  = "";
               end;
               if( not insert( indfile ) )
                  Protocol.Error( "Не вставлена запись в индексный файл по счету", indfile.Nbals,
                                   indfile.Nomls );
               end;
            end;
         end;
         Close( Flaros );
      end;

      if ( ( GateCountDate == Date(31,3) ) or ( GateCountDate == Date(30,6) ) or
           ( GateCountDate == Date(30,9) ) or ( GateCountDate == Date(31,12) )
         )
         NumFile = 0;

         while ( NumFile < KolFileLaros )

            fnmerge( InFileName, DirConvert + GateDir, "s" + Br + DobFileName (NumFile) + "i" + Dt, "dbf" );

            if( ExistFile( InFileName ) )
               stat = Open( FLAROS, InFileName );
               if( not stat )
                  Protocol.Error("Невозможно открыть файл", InFileName );
                  /*MsgBox( "Невозможно открыть файл" + InFileName );*/
               end;
            else
               stat = False;
               Protocol.Warning( "Не существует файла ", InFileName, "за", GateCountDate );
            end;
            if( stat )
               while( next( Flaros ) )
                  message( "Создание индекса", InFileName, " счет...", Flaros.Nbals," + ", Flaros.NomLs );
                  /*println( " счет...", Flaros.Nbals," + ", Flaros.NomLs, " ", FLAROS.DATAV );*/

                  ClearRecord( indfile );
                  indfile.Nbals = Trim( Flaros.Nbals );
                  indfile.Nomls = Trim( Flaros.NomLs );
                  indfile.Oper  = Trim( Flaros.Oper  );

                  if( ( indfile.Nbals != "" ) and ( indfile.Nomls != ""    ) and
                      ( indfile.Oper  != "" ) and ( Flaros.Oper   != "111" )
                    )
                     if( ( Int( Trim( indfile.Oper ) ) < 190 ) or
                         ( Int( Trim( indfile.Oper ) ) > 197 )
                       )
                        indfile.Oper  = "";
                     end;
                     if( not insert( indfile ) )
                        Protocol.Error( "Не вставлена запись в индексный файл по счету", indfile.Nbals,
                                         indfile.Nomls );
                     end;
                  end;
               end;
               Close( Flaros );
            end;
            NumFile = NumFile + 1;
         end;
      end;

      GateCountDate = GateCountDate + 1;
   end;
end;

macro Constructor()
   var stat = true;

   Protocol = clProtocol( ProtocolPath );
   Protocol.Header( ConvVersion );

   stat = OpenTxtFiles();

   if( stat )
      fnmerge( InFileName, WorkDir, "depositr", "dbt" );
      if( not create( depositr, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при создании файла", InFileName );
      else
         if( not open( depositr, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;

   if( stat )
      fnmerge( InFileName, WorkDir, "sb_depst", "dbt" );
      if( not create( report, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при создании файла", InFileName );
      else
         if( not open( report, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;
   if( stat )
      fnmerge( InFileName, "..\\DBFILE\\RSBANK\\", "country", "dbt" );
      if( not open( country, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при открытии файла", InFileName );
      end;
   end;

   if( stat and IsLock_Gate )
      fnmerge( InFileName, WorkDir, "indfile", "dbt" );
      if( not create( indfile, InFileName ) )
         stat = False;
         Protocol.Error( "Ошибка при создании файла", InFileName );
      else
         if( not open( indfile, InFileName ) )
            stat = False;
            Protocol.Error( "Ошибка при открытии файла", InFileName );
         end;
      end;
   end;

   if( stat and IsLock_Gate )
      SetDocInd();
   end;

   return stat;
end;

/* Вывод отчета о сумме остатков по видам вкладов, выгруженных в текстовик */
macro OutReport()
   var i       = 0;
   var Itogo   = $0;
   var ItogAcc = 0;

[+--------------------------------------------------------------------+
 | Вид вклада     | Код валюты |    Остаток    | Открытых  | Закрытых |
 +----------------+------------+---------------+-----------+----------+];

   keynum( report, 4 );
   rewind( report    );

   while( next( report ) )

[|################|      ####  |###############|###########|##########|]
( report.Kind, report.CodCur, report.Rest, report.ColOpenedAcc, report.ColClosedAcc );
[+----------------+------------+---------------+-----------+----------+];
      Itogo   = Itogo   + report.Rest;
      ItogAcc = ItogAcc + report.ColOpenedAcc;
   end;
[+----------------+------------+---------------+-----------+----------+
 |     Итого                   |###############|###########|          |
 +-------------------------------------------------------------------+]( Itogo, ItogAcc );
end;

/* Вывод отчета о суммах выгруженных через шлюз документов  */
macro OutGateRep()
    var SaveDate = NullDate;
    var KredSum = 0, DebSum = 0;
    var stat = True;

[+----------------------------------------------+
 | Вид вклада     |    Приход    |   Расход     |
 +----------------+--------------+--------------+];

   keynum( report, 3 );
   rewind( report    );
   Next  ( report    );

   SaveDate = report.Date;
[       Обороты за ############ ]( SaveDate );
[+----------------------------------------------+];

   while( stat )
       if( SaveDate != report.Date )
          SaveDate = report.Date;
[+----------------+-------------+---------------+
 |     Итого      |#############|###############|
 +----------------------------------------------+]( KredSum, DebSum );
[       Обороты за ############ ]( SaveDate );
[+----------------------------------------------+];
          KredSum = 0;
          DebSum  = 0;
       end;

[|################|#############|###############|]
( report.Kind, report.KredSum, report.DebSum );
[+----------------+---------------+-------------+];

       KredSum = KredSum + report.KredSum;
       DebSum  = DebSum  + report.DebSum;

       stat = Next (report);
   end;
[+----------------+-------------+---------------+
 |     Итого      |#############|###############|
 +----------------------------------------------+]( KredSum, DebSum );
end;

/* Ввод даты конвертирования или диапазона дат для загрузки */
macro GetDateCnv()
   var stat = True;

   if( IsLock_Gate )
      EndLockDate = BegLockDate = {curdate};

      stat = GetDate( BegLockDate, "Введите дату начала загрузки данных" );
      if( stat )
        stat = GetDate( EndLockDate, "Введите дату окончания загрузки данных" );
      end;
      if( EndLockDate < BegLockDate )
         MsgBox( String( "Дата окончания периода загрузки ", EndLockDate, "|меньше даты начала ",
                         BegLockDate,".| Работа прервана!" ) );
         stat = False;
      end;
      if( stat )
         DateSplit( BegLockDate, ConvDay, ConvMon, CurY );
      end;
   else
      MsgBox( "Напоминаю:|Дата конвертирования должна быть НЕ МЕНЬШЕ|"
              "даты последней работы в исходной программе + 1 день!" );

      stat = GetDate( DateConvert, " Введите дату конвертирования" );
      if( stat )
        DateSplit( DateConvert, ConvDay, ConvMon, CurY );
      end;
   end;

   return stat;
end;

/* Движение и обработка файлов Ларос */
macro WorkWithData()
   var stat       = True;
   var NumFile    = 0;
   var Br         = "";
   var Dt         = "";
   var D          = 0;

   CountDate  = date(0,0,0);

   if( IsLock_Gate )
      stat = GetNumSessn( NumSession ); /* Взяли следующий номер сессии за текущую дату */
      if( not stat )
         Protocol.Error( "Не определен номер сессии " );
      end;
   end;

   if( stat )
      stat = Constructor();
   end;

   if( stat )
   /*
      Цикл по исходным файлам Laros
   */
      keynum( depositr, 8 );
      rewind( depositr    );

      Br = SetZeroToSpace( String( Branch:2 ) );

      if( not IsLock_Gate )

         while( NumFile < KolFileLaros )

            fnmerge( InFileName, DirConvert, "f" + Br + DobFileName( NumFile ), "dbf" );
            Protocol.Message( "Начата обработка файла", InFileName );

            if( NumFile > 0 )
               Close( FLAROS );
            end;
            if( not( Open( FLAROS, InFileName ) ) )
               Protocol.Error("Невозможно открыть файл", InFileName );
               /*MsgBox( "Невозможно открыть файл " + InFileName );*/
               Exit(1);
            else
               Laros_fc_1_1();
            end;
            NumFile = NumFile + 1;
         end;

         NumFile = 0;
         close( FLAROS );

         NumStrFile = 1;     /* Количество сконвертированных записей     */
         KolStrFile = 0;     /* Количество просмотренных записей         */
         KolView    = 0;
         Nrec       = 0;

         while( NumFile < KolFileLaros )
            fnmerge( InFileName, DirConvert, "s" + Br + DobFileName( NumFile ), "dbf" );
            Protocol.Message( "Начата обработка файла", InFileName );

            if( NumFile > 0 )
               Close( FLAROS );
            end;
            if( not( Open( FLAROS, InFileName ) ) )
               Protocol.Error("Невозможно открыть файл", InFileName );
               /*MsgBox( "Невозможно открыть файл " + InFileName );*/
               Exit(1);
            else
               Laros_fc_1_2();
            end;
            NumFile = NumFile + 1;
         end;

         NumFile = 0;
         close( FLAROS );

         NumStrFile = 1;     /* Количество сконвертированных записей     */
         KolStrFile = 0;     /* Количество просмотренных записей         */
         KolView    = 0;
         Nrec       = 0;

         while( NumFile < KolFileLaros )
            fnmerge( InFileName, DirConvert, "s" + Br + DobFileName( NumFile ) + "r", "dbf" );
            Protocol.Message( "Начата обработка файла", InFileName );

            if( NumFile > 0 )
               Close( FLAROS );
            end;
            if( not( Open( FLAROS, InFileName ) ) )
               Protocol.Error("Невозможно открыть файл", InFileName );
               /*MsgBox( "Невозможно открыть файл " + InFileName );*/
            else
               Laros_fc_1_3();
            end;
            NumFile = NumFile + 1;
         end;
         OutReport();

      else
         CountDate = BegLocKDate;
         while( CountDate < EndLockDate )
            DateSplit( CountDate, D, Null, Null );
            Dt = SetZeroToSpace( String( D:2 ) );

            NumFile = 0;
            close( FLAROS );

            NumStrFile = 1;     /* Количество сконвертированных записей     */
            KolStrFile = 0;     /* Количество просмотренных записей         */
            KolView    = 0;
            Nrec       = 0;

            while( NumFile < KolFileLaros )
               fnmerge( InFileName, DirConvert + GateDir, "f" + Br + DobFileName( NumFile ) + "i" + Dt, "dbf" );
               Protocol.Message( "Начата обработка файла", InFileName );

               if( NumFile > 0 )
                  Close( FLAROS );
               end;
               if( not( Open( FLAROS, InFileName ) ) )
                  Protocol.Error("Невозможно открыть файл", InFileName );
                  /*MsgBox( "Невозможно открыть файл " + InFileName );*/
               else
                  Laros_fc_1_1();
               end;
               NumFile = NumFile + 1;
            end;

            NumFile = 0;
            close( FLAROS );

            NumStrFile = 1;     /* Количество сконвертированных записей     */
            KolStrFile = 0;     /* Количество просмотренных записей         */
            KolView    = 0;
            Nrec       = 0;

            while( NumFile < KolFileLaros )
               fnmerge( InFileName, DirConvert + GateDir, "s" + Br + DobFileName( NumFile ) + "i" + Dt, "dbf" );
               Protocol.Message( "Начата обработка файла", InFileName );

               if( NumFile > 0 )
                  Close( FLAROS );
               end;
               if( not( Open( FLAROS, InFileName ) ) )
                  Protocol.Error("Невозможно открыть файл", InFileName );
                  /*MsgBox( "Невозможно открыть файл " + InFileName );*/
               else

                  if( MustDecode == True )
                     Open_table(InFileName);
                  end;

                  Laros_fc_1_2();
               end;
               NumFile = NumFile + 1;
            end;
            CountDate = CountDate + 1;
         end;
         OutGateRep();
      end;
   end;
end;

/* Основная процедуру */
macro main()
  var stat = True;

  stat = GetDateCnv();

  if( stat )
     WorkWithData();
     /*
       Просмотр протокола
     */
     Protocol.Footer();
     Protocol.View();
  else
     if( IsLock_Gate )
        MsgBox( "Неправильно задан диапазон дат для загрузки" );
     else
        MsgBox( "Не введена дата конвертация" );
     end;
  end;

end;

main()
