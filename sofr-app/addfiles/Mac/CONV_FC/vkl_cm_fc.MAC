/*
  RS-Retail
  Конвертирование данных из
    п/с "Частные вклады" АБС   RS-Bank в
    п/с "Обслуж.физ.лиц" САРБУ RS-Retail
  2000 год

  Общие функции

  27.10.00 Рыжиков Александр Александрович ( RA )

  RA 09.12.99 - добавил процедуры, необходимые для конвертации валюты
*/
import "retconst.mac";
import "files.mac";
import "supplib.mac";
import "cnv_log.mac";
import DeprIntr;
import Календарь;

/*===========================  Настройки ================================*/
var Branch     = NumFNCash();
var FlagCur    = NumFlagCur();
var Numsession = "0001";

var DirConvert   = "..\\VKLADY.DB\\";

if( FlagCur ) DirConvert   = "..\\VKLADY.DB\\"; end;

var GateDir = "\\FIZM\\";

const ProtocolPath = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR"     , true );
const WorkDir      = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR"     , true );
const DestinDir    = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", true );
const RsBankDir    = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE"  , true );

const ConvVersion  = "27.10.00_01";  /* версия конвертера - день.месяц.номер версии*/

const IsLock_Gate = False;        /* Флаг работы в режиме шлюза или конвертера:
                                     True  - работаем как шлюз
                                     False - конвертер
                                  */

const MustDecode = False;         /* Флаг - декодировать или нет.
                                     True  - декодировать
                                     False - нет  */


/* Раздел для обработки подписи вкладчика */
const IsConvertSign = False;      /* Флаг - переносить подписи.
                                    True  - Да
                                    False - нет  */

const CreatSignFile = False;     /* Флаг - формировать файл граф. подписи .
                                    True  - Да                
                                    False - нет  */           

/*========================== Файлы данных ================================*/
file vkl_cln    ( "depclnt.dbt" , "..\\VKLADY.DB\\bank.def" );
file vkl_acc    ( "depositr.dbt", "..\\VKLADY.DB\\bank.def" ) write;
file vkl_doc    ( "depdoc.dbt"  , "..\\VKLADY.DB\\bank.def" );
file vkl_dov    ( "trast.dbt"   , "..\\VKLADY.DB\\bank.def" );
file vkl_tax    ( "dtaxcard.dbt", "..\\VKLADY.DB\\bank.def" );

/*----------------------- Дополнительный раздел  обработки пописей вкладчиков */
file vkl_sgn ( "signs.dbt" , "..\\VKLADY.DB\\bank.def" ); /*Подписи вкладичков*/
file rt_sign ( "rtsign.dbt", "sbbank.def") write; /* Файл подписей вкладчиков RETAIL  */

RECORD RTSIGN_REC_OUT ("byte.rec" ,"sbbank.def");/* Структура для копирования переменой части*/ 
RECORD RTSIGN_REC_IN  ("byte.rec" ,"sbbank.def");/* Структура для копирования переменой части*/

Var COMSPEC = GetEnv( "COMSPEC" );; /* спец. переменная для испольтзования функции COPY */
/*---------------------------------------------------------------------------*/

file ext_dep    () txt 512 write;
file ext_doc    () txt 512 write;
file ext_doc_2  () txt 512 write;
file ext_cln    () txt 512 write;
file ext_limit  () txt 512 write;
file ext_trust  () txt 512 write;
file ext_clus   () txt 512 write;
file ext_addacc () txt 512 write;
file ext_card   () txt 512 write;

file report   ( "sb_depst.dbt" ) write; /* временный файл отчета        */


/*======================== Переменные и массивы ==========================*/
var ConvertKind = "";             /* ;Конвертируемые в текущем сеансе виды вкладов
                                     "" - конвертируем все виды вкладов */
array NumTestAcc;                 /* Конвертируемые в текущем сеансе номера счетов, проверка
                                  */
array NotAcc;

var InFileName = "";
var BegLockDate,
    EndLockDate,
    CountDate;

var Protocol : clProtocol;  /* Объект - протокол конвертации */

array extrn_dep;
array extrn_doc;
array extrn_cln;
array extrn_resrv;
array extrn_limit;
array extrn_trust;
array extrn_clus;
array extrn_addacc;
array extrn_card;

array TmpAccNum;

/**********************************************************/

const OperToAlt      = 85;  /* Перевод счета в альтернативный режим */
var   Global_EndDate = NullDate;/* Глобальная переменная- дата конца договора */
const BeginNumDoc    = 100;  /* Номер первой операции п осчету за день */
var   MonForPen      = 11;   /* Для Пенсионного- месяц, начиная с которого */
var   {curdate};

const slu = "Служебный";

/**********************************************************/

/* Массив коротких номеров счетов, конвертируемых в данном сеансе */
macro SetTestAcc()
/*
   NumTestAcc( 0 ) = "100046900";
   NumTestAcc( 1 ) = "100058420";

   NumTestAcc( 2 ) = "030000811";
   NumTestAcc( 3 ) = "010000211";
   NumTestAcc( 4 ) = "020000711";
   NumTestAcc( 5 ) = "030000811";
*/
end;

/* Массив номеров счетов, которые не конвертируются*/
macro SetNotAcc()
/*
   NotAcc( 0 ) = "01117535";
   NotAcc( 1 ) = "01117582";
   NotAcc( 2 ) = "53000121";
   NotAcc( 3 ) = "53000123";
*/
end;

macro IsNeedConv
(
 Account        /* проверяемый номер счета      */
)
/*
  Возврат:
    TRUE  - есть в списке
    FALSE - нет в списке
*/
   var stat = False;
   var i = 0;

   SetNotAcc();
   Account = Trim( Account );
   stat = ( ArrayIndex( NotAcc, Account ) == -1 ); /* В массиве данных нет */
   if( not stat )
      println( "Выброшены при конвертации ", Account );
   end;
   return stat;
end;


/***********************************************************
   Таблица видов вкладов, конвертируемых в текущий сеанс
***********************************************************/

macro NeedConvertType
(
 Account        /* проверяемый номер счета      */
)
/*
  Возврат:
    TRUE  - есть в списке
    FALSE - нет в списке
*/
   var stat = False;
   var Type:string;

   SetTestAcc();
   Account = Trim( Account );
   if( Asize( NumTestAcc ) != 0 )
      stat = ( ArrayIndex( NumTestAcc, Account ) != -1 );
   else
      stat = TRUE;
   end;
   return stat;
end;

/*
  Конвертация только рублей или только валюты
*/

macro NeedConvVAL( FlagCur, Code_Currency )

   if( ( ( Code_Currency != 0 ) and ( FlagCur != 0 ) ) or
       ( ( Code_Currency == 0 ) and ( FlagCur == 0 ) )    )
      return True;
   else
      return False;
   end;
end;


/***********************************************************
   Работа с видами вкладов. Таблица соответствия.
***********************************************************/
macro GetRsKindForOtherABS( OtherId, RsKind, CodeCur )
   var stat = True;
   RsKind  = "";
   CodeCur = -1;

   if   ( OtherID == "DV" )
      RsKind = "DV";

   elif ( OtherID == "DVN"   )
      RsKind = "DVN";

   elif ( OtherID == "DV_LEGAL" )
      RsKind = "DV_LEGAL";

   elif ( OtherID == "SRD-12"  )
      RsKind = "SRD-12";

   elif ( OtherID == "SRD-3"  )
      RsKind = "SRD-3";
   elif ( OtherID == "SRD-6"  )
      RsKind = "SRD-6";

   elif ( OtherID == "ДВ"  )
      RsKind = "ДВ";
   elif ( OtherID == "ДВН"  )
      RsKind = "ДВН";

   elif ( OtherID == "Visa-C-ZP(R)"  )
      RsKind = "Visa-C-ZR";
   elif ( OtherID == "Visa-C-KK(R)"  )
      RsKind = "Visa-C-KK";
   elif ( OtherID == "VISA-C-KK(N)"  )
      RsKind = "Visa-C-KK";
   elif ( OtherID == "Visa-O-KZ(R)"  )
      RsKind = "VISA-O-ZR";
   elif ( OtherID == "Visa-O-SK(R)"  )
      RsKind = "Visa-O-SK";
   else
      stat = False;
   end;

   SetParm( 1, RsKind  );
   SetParm( 2, CodeCur );

   return stat;
end;

/**********************************************************/
macro SetCurrency( Code_Currency )
  var Cur = "";

  if(   Code_Currency == 0 )
     Cur = 398;
  elif( Code_Currency == 1 )
     Cur = 840;
  elif( Code_Currency == 2 )
     Cur = 826;
  elif( Code_Currency == 3 )
     Cur = 978;
  elif( Code_Currency == 48 )
     Cur = 276;
  elif( Code_Currency == 58 )
     Cur = 810;
  end;

  return Cur;
end;


/**********************************************************/

macro GetAccNumber_New
(
 Acc    /* Вход - Счет Laros */
)
/*
  Возвращает номер счета в группе нумерации (по Ларос)
*/
  var Num;
  Num = SubStr(Acc,6);
  return Num;
end;

/**********************************************************/

macro GetTypeRecord
(
 TypeAccount,   /* Вид вклада   */
 FlagCur        /* Рубли\Валюта */
)
/*
  Поиск строки в справочнике видов вкладов
  Возврат:
    TRUE - найдено
    FALSE - не найдено
*/
  var stat;

  keynum( SB_DTYP, 2 );
  rewind( SB_DTYP    );

  SB_DTYP.FlagCur = FlagCur;
  SB_DTYP.Kind    = TypeAccount;

  stat = GetEQ (SB_DTYP);
  return stat;
end;

/***********************************************************/

macro GetDepositrRecordReferenc
(
 Referenc
)
/*
  Поиск строки в файле счетов
  Возврат:
    TRUE - найдено
    FALSE - не найдено
*/
  var stat;

  KeyNum ( DEPOSITR, 8 );
  DEPOSITR.Referenc = Referenc;
  stat = GetEQ (DEPOSITR);
  return stat;
end;

/**********************************************************/

macro LastDay
(
 m,     /* Месяц        */
 y      /* Год          */
)
/*
  Последний день месяца
*/
  var d;
  m = m + 1;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;
  DateSplit (Date(1,m,y)-1,d);
  return d;
end;

macro SumDate
(
 BegDate,       /* Дата, к которой добавляем            */
 Term,          /* Добавляемое значение (>0 или <0)     */
 KindTerm       /* Вид срока                            */
)
/*
  Возвращает дату- результат
*/
  var ResDate = Date(0,0,0);
  var d,m,y;

  if (KindTerm == R_QW)
    Term = Term * 3;
    KindTerm = R_MON;
  end;
  DateSplit (BegDate,d,m,y);
  if (KindTerm == R_YEAR)
    y = y + Term;
    if ((m == 2) AND (d > 28))
      d = LastDay (m,y);
    end;
    ResDate = Date(d,m,y);
  elif (KindTerm == R_DAY)
    ResDate = BegDate + Term;
  /* ЛСВ 10.07.98 */
  elif (KindTerm == R_REALDAY)
    ResDate = BegDate + Term;
  elif (KindTerm == R_MON)
    m = m + Term;
    while ((m < 1) OR (m > 12))
      if (m < 1)
        m = m + 12;
        y = y - 1;
      elif (m > 12)
        m = m - 12;
        y = y + 1;
      end;
    end;
    while (d > LastDay(m,y))
      d = d - 1;
    end;
    ResDate = Date(d,m,y);
  end;
  return ResDate;
end;

/***********************************************************
     После 1.1.1998- деноминация (деление сумм на 1000)
***********************************************************/

macro DenomSum
(
 Sum,           /* Деноминируемая сумма */
 DateDoc,       /* Дата документа       */
 TypeOper       /* Тип операции         */
)
/*
  Макрос взят 8.1.98 из goto98 системы RS-Bank 4.3
*/
  var SumDen;
  if ((DateDoc < DateDen) AND (TypeOper != OP_DENOM))
    if (Sum < 0)
      SumDen = -MoneyL (Floor (DoubleL (-Sum)/1000 + 0.5));
    else
      SumDen = MoneyL (Floor (DoubleL (Sum)/1000 + 0.5));
    end;
  else
    SumDen = Sum;
  end;
  return SumDen;
end;

/**********************************************************/

macro WorkWithDate
(
 Cur_Date,      /* Дата, от которой идет расчет (текущая) */
 Graf,          /* Стратегия расчета (оплаты) */
 Day,           /* День расчета (если стратегия- заданного числа каждого месяца */
 GrafDate       /* Выход- дата следующего расчета (оплаты) */
)
/*
  Определение даты текущего расчета (оплаты) процентов
*/
  var stat = TRUE;
  var d,m,y;
  var d1,m1,y1;


  DateSplit( Cur_Date, d, m, y );
  GrafDate = Date(0,0,0);
  if (Graf == 1)        /* В конце дня  */
    GrafDate = Cur_Date;
  elif (Graf == 2)      /* В конце месяца */
    DateSplit( Cur_Date + 1, d1, m1, y1 );
    if( m == m1 )
       if( m == 12 )
         m = 1;
         y = y + 1;
       else
         m = m + 1;
       end;
       GrafDate = Date( 1, m, y ) - 1;
    else/* После увеличения даты на единицу перешли через месяц.
           Дата конвертации - это дата следующего расчета */
       GrafDate = Cur_Date;
    end;
  elif (Graf == 3)      /* В конце квартала     */
    if (m > 9)
      m = 1;
      y = y + 1;
    elif (m > 6)
      m = 10;
    elif (m > 3)
      m = 7;
    else
      m = 4
    end;
    GrafDate = Date(1,m,y) - 1;
  elif (Graf == 4)      /* В конце года */
    GrafDate = Date(31,12,y);
  elif (Graf == 5)      /* Day числа каждого месяца */
    if (Day > 0)
      if (d > Day)
        m = m + 1;
        if (m > 12)
          m = 1;
          y = y + 1;
        end;
      end;
      GrafDate = Date(Day,m,y);
    end;
  end;
  SetParm (3,GrafDate);
  return stat;
end;

/**********************************************************/
macro WorkWithDateBack
(
 Cur_Date,      /* Дата, от которой идет расчет (текущая) */
 Graf,          /* Стратегия расчета (оплаты) */
 Day,           /* День расчета (если стратегия- заданного числа каждого месяца */
 GrafDate       /* Выход- дата предыдущего расчета (оплаты) */
)
/*
  Определение даты текущего расчета (оплаты) процентов
*/
  var stat = TRUE;
  var d,m,y;

  DateSplit (Cur_Date,d,m,y);
  GrafDate = Date(0,0,0);
  if (Graf == 1)        /* В конце дня  */
    GrafDate = Cur_Date - 1;
  elif (Graf == 2)      /* В конце месяца */
    GrafDate = Date( 1, m, y ) - 1;
  elif (Graf == 3)      /* В конце квартала     */
    if  ( m <  4 )
      m = 1;
    elif( m <  7 )
      m = 4;
    elif( m < 10 )
      m = 7;
    elif( m > 10 )
      m = 10;
    end;
    GrafDate = Date(1,m,y) - 1;
  elif (Graf == 4)      /* В конце года */
    GrafDate = Date( 31, 12, y-1);
  elif (Graf == 5)      /* Day числа каждого месяца */
    if (Day > 0)
      if (d < Day)
        m = m - 1;
        if (m < 1)
          m = 12;
          y = y - 1;
        end;
      end;
      GrafDate = Date(Day,m,y);
    end;
  end;

  SetParm (3,GrafDate);
  return stat;
end;

/*********************************************************/
macro GetDEPOSITR_Cycle_Key4 (Num, FNcash)
/*
  Организация цикла по счетам
*/
  var stat;
  if (Num == 0)
    KeyNum (DEPOSITR,4);
    DEPOSITR.FNcash     = FNcash;
    DEPOSITR.NumSession = 0;
    stat = GetGE (DEPOSITR);
  else
    stat = Next (DEPOSITR);
  end;
  if (stat)
    stat = (DEPOSITR.FNcash == FNcash);
  end;
  return stat;
end;

/***********************************************************
     Алгоритмы операций
***********************************************************/

macro GetAlgOp
(
 Type_Account,  /* Вид вклада   */
 NumOper,       /* Номер операции */
 NumStep,       /* Номер шага операции */
 BegDate       /* Дата, начала действия шага
                     нулевая - поиск первой записи
                     определенная- поиск от конца */
)
/*
  Получение строки файла алгоритмов операций
*/
  var stat;

  KeyNum( SB_ALGOP, 0 );
  SB_ALGOP.IsCur      = depositr.IsCur;
  SB_ALGOP.Kind       = depositr.Type_Account;
  SB_ALGOP.NumOpert   = NumOper;
  SB_ALGOP.NumStepAlg = NumStep;
  SB_ALGOP.BegDate    = BegDate;
  if (BegDate == Date(0,0,0))
    stat = GetGE (SB_ALGOP);
  else
    stat = GetLE (SB_ALGOP);
  end;
  if (stat)
    stat = ((SB_ALGOP.IsCur      == depositr.FlagCur      ) AND
            (SB_ALGOP.Kind       == depositr.Type_Account ) AND
            (SB_ALGOP.NumOpert   == NumOper               ) AND
            (SB_ALGOP.NumStepAlg == NumStep               )    );
  end;
  return stat;
end;
/*********************************************************/

macro GetAlgOpStep265
(
                /* ВХОД */
 Type_Account,  /* Вид вклада   */
 NumOper,       /* Номер операции */
 BegDate,       /* Дата, начала действия шага
                     нулевая - поиск первой записи
                     определенная- поиск от конца */
                /* ВЫХОД  */
 ForDay,        /* Учет дня операции:
                       0 - текущим днем
                       1 - предыдущим днем
                       2 - следующим днем */
 ForCompens     /* "X" - компенсировать приход */
)
/*
  Получение параметров заданной операции для шага 265
  (Учет дня операции)

  Возврат:
    True - запись найдена
    False - запись не найдена в файле описания алгоритмов операций
            или шаг не активен
*/
  var stat;
  var ForDay_     = 0,
      ForCompens_ = "";

  stat = GetAlgOp (Type_Account,NumOper,265,BegDate);
  if (stat)
    if (SB_ALGOP.FlagEXE == "X")
      ForDay_ = SB_ALGOP.prmI2;
      ForCompens_ = SB_ALGOP.prmC;
    else
      stat = False; /* Шаг алгоритма не активен */
    end;
  end;
  SetParm (3,ForDay_);
  SetParm (4,ForCompens_);
  return stat;
end;

/**********************************************************/
macro GetResid (Nbals)
/*
  По номеру балансового счета определяем тип пользователя:
  резидент\не резидент

  Возврат:
    0 - резидент
    1 - не резидент
*/
  var Resid;

  /*If(Int(Nbals) < 42400)*/
  if( SubStr( Nbals, 1, 3) != "426" )
    Resid = 0;
  else
    Resid = 1;
  end;
  return Resid;
end;

/***********************************************************/
macro GetPc_ALgRec( IsCur, ApType, BegDate )
   var stat  = True;
   var Found = False;

   keynum     ( pc_alg, 0 );
   ClearRecord( pc_alg    );
   rewind     ( pc_alg    );

   pc_alg.FlagCur    = IsCur;
   pc_alg.Referenc   = ApType;
   pc_alg.ObjectType = 1003;
   pc_alg.BegDate    = VeryBigDate;

   stat = GetLE( pc_alg );
   while( stat and ( pc_alg.FlagCur    == IsCur    ) and
                   ( pc_alg.Referenc   == ApType   ) and
                   ( pc_alg.ObjectType == 1003     )     )

      if( pc_alg.BegDate <= BegDate )
         Found = True;
      end;
      if( not Found ) stat = prev( pc_alg );
      else            stat = False;
      end;
   end;

   return Found;
end;

/***********************************************************/
macro GetPC_ALG( IsCur, Type_Account, Type_Object, BegDate )
   var stat  = True;
   var Found = False;

   keynum     ( pc_apltp, 0 );
   ClearRecord( pc_apltp    );
   rewind     ( pc_apltp    );

   pc_apltp.IsCur   = IsCur;
   pc_apltp.TypeRec = 1003;
   pc_apltp.Type    = Type_Account;

   stat = GetGE( pc_apltp );

   while( stat and ( pc_apltp.IsCur   == IsCur         ) and
                   ( pc_apltp.TypeRec == 1003          ) and
                   ( pc_apltp.Type    == Type_Account  )     )
      if( pc_apltp.ApplType == Type_Object )
         Found = True;
      end;
      if ( not Found ) stat = next( pc_apltp );
      else             stat = False;
      end;
   end;

   /* Ищем алгоритм рачета для заданного условия */
   if( Found )
      stat = GetPc_ALgRec( IsCur, pc_apltp.ApType, BegDate );
   end;
   /*MsgBox( stat, " ", pc_alg.Referenc, pc_alg.RestUseType );*/

   return stat;
end;

/***********************************************************
***********************************************************/
macro GetPC_APLTP
(
 IsCur,
 Type_Account,
 Num
)
/*
  Следующая строка PC_APLTP
*/
  var stat;
  if (Num == 0)
     keynum     ( pc_apltp, 0 );
     ClearRecord( pc_apltp    );
     rewind     ( pc_apltp    );

     pc_apltp.IsCur   = IsCur;
     pc_apltp.TypeRec = 1003;
     pc_apltp.Type    = Type_Account;

     stat = GetGE( pc_apltp );
  else
    stat = Next( PC_APLTP );
  end;
  if (stat)
    stat = ( ( PC_APLTP.IsCur == IsCur        ) AND
             ( PC_APLTP.Type  == Type_Account )     );
  end;
  return stat;
end;

/***********************************************************
***********************************************************/
macro GetApplTpForObject( IsCur, Type_Account, Type_Object )
   var stat  = True;
   var Found = False;

   keynum     ( pc_apltp, 0 );
   ClearRecord( pc_apltp    );
   rewind     ( pc_apltp    );

   pc_apltp.IsCur   = IsCur;
   pc_apltp.TypeRec = 1003;
   pc_apltp.Type    = Type_Account;

   stat = GetGE( pc_apltp );

   while( stat and ( pc_apltp.IsCur   == IsCur         ) and
                   ( pc_apltp.TypeRec == 1003          ) and
                   ( pc_apltp.Type    == Type_Account  )     )
      if( pc_apltp.ApplType == Type_Object )
         Found = True;
      end;
      if ( not Found ) stat = next( pc_apltp );
      else             stat = False;
      end;
   end;

   return Found;
end;


macro MakeTxtStr( Arr )
   const Delim = "|";

   var i   = 0;
   var Str = "";

   /*Str = Str + Delim;*/
   while( i < Asize( Arr ) )
      Str = Str + String( Arr( i ) ) + Delim;
      i = i + 1;
   end;

   return Str;
end;

/* Поиск следующего номера сессии выгрузки */
/* Исправлено Головановым В.В.             */
macro GetNumSessn( NumSesn )
   var stat     = True,
       FindStat = True;
   var NumSesn_ = 0;

   ClearRecord( fc_sesn    );
   Keynum     ( fc_sesn, 1 );
   Rewind     ( fc_sesn    );

   fc_sesn.FNCash  = Branch;
  /* fc_sesn.Sesdate = date();*/

   FindStat = GetGE( fc_sesn );
   while( FindStat and ( fc_sesn.FNCash == Branch ) /*and ( fc_sesn.Sesdate == date() )*/ )
      if( fc_sesn.NumSession > NumSesn_ )
         NumSesn_ = fc_sesn.NumSession;
      end;
      FindStat = Next( fc_sesn );
   end;
   NumSesn_ = NumSesn_ + 1;

   NumSesn = SetZeroToSpace( String( NumSesn_ : 4 ) );
   SetParm( 0, NumSesn );

   return stat;
end;

/*=========================== Процедуры валюты ===================================*/

/* Получение кода валюты */
macro GetRealCurrency( Curr )
   var stat = True;

   keynum( currency, 1 );
   currency.ExternalCode = Curr;
   if( GetEQ( currency ) )
      DEPOSITR.Code_Currency = currency.Code_Currency ;
   else
      stat = False;
      Protocol.Error( "Не найдены данные о валюте с кодом ", Curr );
   end;

   return stat;
end;

