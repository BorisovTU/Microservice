/*       RS-Retail                                 R-Style Software Lab

           Вспомогательные функции для работы с именами файлов.

  VS  (c)  1995,97,98. - функции для работы с именами файлов.
  RA  (c)  1999.       - пополнение библиотеки

*/

/*********************************************************************
   Поиск номера заданного элемента в массиве.
   Возвращает номер этого элемента в массиве или -1.
*********************************************************************/
macro ArrayIndex( arr, element )
  var size = asize( arr ),
      i    = 0,
      find = -1;
  while( (i < size) and (find < 0) )
    if( arr(i) == element ) find = i; end;
    i = i + 1;
  end;
  return find;
end;

macro StrRChr( str, sym )   /* Позиция последнего включения символа */
 var i = index( str, sym ),
     k = 0;
  while( i )
    str = substr( str, i+1 );
    k = k + i;
    i = index( str, sym );
  end;
  return k;
end;


/* Получить полное имя пути из составляющих. Возвращает полное имя файла. */
macro fnmerge
(
  Path,         /* Сюда помещается полное имя, оно же возвращается. */
  Dir,          /* Директория вместе с диском                       */
  Fname,        /* Имя файла без расширения                         */
  Ext           /* Расширение                                       */
)
  Path = "";
  Dir = trim(Dir);  Fname = trim(Fname);  Ext = trim(Ext);
  if( Dir != "" )
    if( substr( Dir, strlen(Dir), 1 ) != "\\" )
      Dir = Dir + "\\";
    end;
    Path = Path + Dir;
  end;
  if( Fname != "" )
    Path = Path + Fname;
    if( Ext != "" )
      if( substr( Fname, strlen(Fname), 1 ) != "." )
        Path = Path + ".";
      end;
      if( substr( Ext, 1, 1 ) == "." ) Ext = substr( Ext, 2 ); end;
      Path = Path + Ext;
    end;
  end;
  setparm( 0, Path );
  return Path;
end;

/* Разбить входное имя файла Path на составляющие. */
macro fnsplit
(
  Path,        /* Входное полное имя файла     */
  Dir,         /* Директория вместе с диском   */
  Fname,       /* Имя файла без расширения     */
  Ext          /* Расширение                   */
)
  var last = strrchr( Path = trim(Path), "\\" );
  Dir = Fname = Ext = "";
  if( last )
    Dir  = substr( Path, 1, last-1 );
    Fname= substr( Path, last+1 );
  else
    Fname = Path;
  end;
  if( last = strrchr( Fname, "." ) )
    Ext   = substr( Fname, last+1 );
    Fname = substr( Fname, 1, last-1 );
  end;
  setparm( 1, Dir   );
  setparm( 2, Fname );
  setparm( 3, Ext   );
end;

/* Усечение имени пути до указанной длины, например
   TrimPath( "c:\\Bank.43\\TxtFile\\MBB.Out", 23 ) даст
   результат "c:\...3\TxtFile\MBB.Out"
*/
macro TrimPath( Path, len )
  var plen = strlen( Path ),
      pos;
  if( plen > len )
    if( substr( Path, 2, 1 ) == ":" )    /* Задан полный путь с диском */
      Path = substr( Path, 1, 3 ) + "..." + substr( Path, plen-len+7 );

    else /*if( substr( Path, 1, 2 ) == "\\\\" )*/    /* Задан сетевой путь или     */
                                                     /* Задан относительный путь   */
      pos = 2 + index( substr( Path, 3 ), "\\" );
      pos = pos + index( substr( Path, pos+1 ), "\\" );
      Path = substr( Path, 1, pos ) + "..." + substr( Path, plen-len+pos+4 );
    end;
  end;
  return Path;
end;

/*  Запись нулей перед строкой */
macro SetSimbol( Str, Simb, len, Left )
   var i = 0;

   Str = Trim( String( Str ) );
   i   = StrLen( Str );
   while( i < len )
      if( Left )
         Str = Simb + Str;
      else
         Str = Str  + Simb ;
      end;
      i = i + 1;
   end;
   return Str;
end;

/*********************************************************************
  Дату в строку формата ДД.ММ.ГГГГ
*********************************************************************/
macro StrDate( InDate, Year_Len )
   var dd, mm, yyyy;

   DateSplit( InDate, dd, mm, yyyy );
   yyyy = String( yyyy );
   if( Year_Len <= 2 )
      yyyy = SubStr( yyyy, 3 );
   end;
   return SetSimbol( dd, "0", 2, 1 ) + "." + SetSimbol( mm, "0", 2, 1 ) + "." + yyyy;
end;

/*********************************************************************
  Выделение даты из формата ДД.ММ.ГГ
*********************************************************************/
macro btrDate( InDate )
  var day, month, year;
  day = month = year = 0;

  day   = int(substr( InDate, 1, 2 ));
  month = int(substr( InDate, 4, 2 ));
  year  = int(substr( InDate, 7 ));

  if( year and month and day )   /* как в RS-Bank */
    if( (year >= 0) and (year <= 99) )
       year = 1900 + year;
    else
       year = 2000 + year;
    end;
  elif( year )
     year = 1900 + year;
  end;

  return date( day, month, year );
end;

/*********************************************************************/
/*       Деньги в строку                                             */
/*********************************************************************/
macro MoneyToStr( Sum )
   return String( Sum );
end;

/*********************************************************************/
/*      Строку в деньги                                              */
/*********************************************************************/
Macro StrToMoney( Sum )
   Sum = double( Trim( Sum ) );
   return Money( Sum*100 );
end;

/* Удаление пробелов в строке */
macro DeleteSpaces( str )
  var pos;
                    /* Ищем пробел */
  while( pos = index( str, " " ) )
    str = substr( str, 1, pos - 1 ) + substr( str, pos + 1 );
  end;

  return str;
end;

/* Удаление двойных пробелов в строке */
macro CompressSpaces( str )
  var pos, i;
                    /* Ищем ДВА пробела подряд. */
  while( pos = index( str, "  " ) )
    i = 1;
    while( substr( str, pos+i, 1 ) == " " )
      i = i + 1;
    end;
    str = substr( str, 1, pos ) + substr( str, pos+i );
  end;

  return str;
end;

/*
   Замена в строке str символов What на символ Symb.
*/
macro SubstituteSymbol( str, What, Symb )
  var i;

  while( (i = index( str, What )) != 0 )
    strset( str, i, Symb );
  end;
  return str;
end;

/*********************************************************************/
/*      Подсчет числа записей открытого текстового или DBF файла     */
/*********************************************************************/
macro NRecordsTxt( idfile )
  var i = 0;
  rewind( idfile );
  while( next( idfile ) )
    i = i + 1;
  end;
  rewind( idfile );
  return i;
end;

/* Проверка на наличие в строке одних цифр */
macro TestDigit( Str )

   Macro TestOneSimb( Simb )
      var stat = False;
      var Zero = CodeFor( "0" ),
          Nine = CodeFor( "9" );

      Simb = CodeFor( Simb );
      while( not stat and ( Zero <= Nine ) )
         if( Zero == Simb )
            stat = True;
         end;
         Zero = Zero + 1;
      end;
      return stat;
   end;

   var Simb = "";
   var i = 0;
   var stat = True;

   Str = Trim( Str );
   while( stat and ( i < StrLen( Str ) ) )
      Simb = SubStr( Str, 1 + i, 1 );
      If( Simb != "." )
         stat = TestOneSimb( Simb );
      end;
      i = i + 1;
   end;
   return stat;
end;
/*
                       Расчет контрольного ключа
               В соответствии с письмом ЦБ №515 от 08.09.97
    08.01.98 AS   Установка признака РКЦ по справочнику банков
*/
/*
const БИК = "041403633";
const Код_Отделения = "0";
*/

macro GetDigit ( d1 , d2 )

  var k;
  /*  При использовании клиринговой валюты возможно алфавитное    */
  /*  значение в 6м разряде лицевого счета                        */

  if   ( Index ( "AaАа" , d1 ) != 0 )  d1 = "0";
  elif ( Index ( "BbБб" , d1 ) != 0 )  d1 = "1";
  elif ( Index ( "CcСс" , d1 ) != 0 )  d1 = "2";
  elif ( Index ( "EeЕе" , d1 ) != 0 )  d1 = "3";
  elif ( Index ( "HhНн" , d1 ) != 0 )  d1 = "4";
  elif ( Index ( "KkКк" , d1 ) != 0 )  d1 = "5";
  elif ( Index ( "MmМм" , d1 ) != 0 )  d1 = "6";
  elif ( Index ( "PpРр" , d1 ) != 0 )  d1 = "7";
  elif ( Index ( "TtТт" , d1 ) != 0 )  d1 = "8";
  elif ( Index ( "XxХх" , d1 ) != 0 )  d1 = "9";
  end;

  k = int( d1 ) * int ( d2 );
  return ( k - 10*( k/10 ) );

end;

macro GetAccountKey( ac, mfo )

  VAR
    Maket   = "71371371371371371371371", /* Макет для расчета ключа         */
    Sum     = int( 0 ),                /* Контрольный разряд                */
    KeyPos  = int( 9 ),                /* Позиция ключевого разряда в счете */
    i       = int( 1 ),                /* Вспомогательная переменная        */
    lenm    = strlen( Maket ),         /* Длина строки макета               */
    lena    = strlen( ac ),            /* Дина счета                        */
    lend    = strlen( mfo ),           /* Длина кода МФО                    */
    newac   = ac,                      /* Счет с рассчитанным ключом        */
    rcc;                               /* 1 - Расчет для РКЦ,               */
                                       /* 0 - расчет для кредитных орг-ций  */


  /*  08.01.98      Определим признак РКЦ по справочнику банков   */

/*
  dprt.RCC_Flag   = "X";
  dprt.MFO_Depart = mfo;

  if ( getEQ( dprt ) )
     rcc = 1;
  else
     rcc = 0;
  end;
  */

  rcc = 0;

  while ( i <= lenm )

    if (i <= 3)

      /*   Цикл по позициям в БИК   */

      if ( rcc == 0 )
         if ( i+6 <= lend )
            Sum = Sum + GetDigit( SubStr( mfo, i+6, 1 ) , SubStr( maket, i, 1 ) );
         end;
      else
         if ( ( i > 1 ) and ( i + 3 <= lend ) )
            Sum = Sum + GetDigit( SubStr( mfo, i+3, 1 ) , SubStr( maket, i, 1 ) );
         end;
      end;

    /*     Цикл по позициям в счете    */

    elif ( ( i  !=  KeyPos+3 ) and ( i > 3 ) )

      Sum = Sum + GetDigit( SubStr( newac, i-3, 1) , SubStr( maket, i, 1 ) );

    end;

    i = i + 1;

  end;

  Sum = Sum - 10*( Sum / 10 );
  Sum = Sum * 3;
  Sum = Sum - 10*( Sum / 10 );

  StrSet ( newac , KeyPos , MkStr( Sum + 48  ,  1 ) );

  return newac;
end;

macro SetZeroToSpace( Str )
   var i;

   while ( ( i = index( str, " " ) ) )
     strset( str, i, "0" );
   end;
   return str;

end;


