/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1999 год

  Конвертация подписей вкладчиков

  Ромодин Александр Васильевич
   26.02.99
*/
Import "common.mac", DeprIntr;

/*
  Настройки
*/
var FileNameMaket = "f"+FNcash;
var FileNameLaros;
array DobFileName;
      DobFileName(0) = "b.dbf";
      DobFileName(1) = "d.dbf";
const KolFileLaros = Asize(DobFileName);
/*
  Файлы
*/
file FLAROS() dbf;

file DEPCLNT   (depclnt)  write;       /* Список клиентов (вкладчиков) */

file LogFile() txt 250;         /* Для просмотра протокола */
/*
  Переменные
*/
var NumStrFile   = 0;   /* Количество сконвертированных записей     */
var KolStrFile   = 0;   /* Количество просмотренных записей         */

/***********************************************************
   Обработка строк файла Laros
***********************************************************/

macro WorkWithFileLaros
/*
  Цикл по записям файла
*/
  var ErrCode,
      ErrText;
  var MayCont;
  var KolView = 0;
  var Resid = 0;
  var Type_Account = "";
  var Number = 0;
  var Account;
  var KindInLaros;
  var RunParm;
  var Sign1, Sign2;
  var stat;
  var OffSet;

  while (Next(FLAROS))
    MayCont = TRUE;
    if(FLAROS.DATAO == Date(0,0,0))
      if ((FLAROS.PRIZS == "") OR (FLAROS.PRIZS == " "))
        MayCont = False;        /* Откидываем служебную запись */
      else
        PutConvMessage (M_MESSAGE, "Для счета "+
                        FLAROS.Nbals+" + "+Trim(FLAROS.NOMLS)+
                        " не определена дата открытия.");
      end;
    end;
    if (MayCont)
/*      KindInLaros = GetTypeAccountInLaros (FLAROS.NOMLS);*/
      KindInLaros = SubStr(FLAROS.Tip_vk,1,1);
      if (NeedConvertType (KindInLaros)) /* Если конвертируем в данном сеансе */
/*
        Чистка буферов файлов
*/
        ClearRecord (DEPOSITR);
/*
        Файл клиентов
*/
        Resid = GetResid (FLAROS.Nbals);
        MayCont = GetRsKindForLaros_New (FLAROS.Tip_vk,
                                         FLAROS.DATAO,
                                         FLAROS.PRIZU,
                                         Type_Account);
        if (NOT MayCont)
          PutConvMessage (M_WARN, "Для счета "+
                          FLAROS.Nbals+" + "+Trim(FLAROS.NOMLS)+
                          " не определен RS-вид вклада. Счет не сконвертирован.");
        end;
        if (MayCont)
          Number       = Int (GetAccNumber_New (FLAROS.Nomls));
          Account      = FormAccountNumber (Currency,
                                            Type_Account,
                                            Number,
                                            Resid);
          if (Account == "")
            PutConvMessage (M_ERROR," Для счета Ларос "+     " в строке "+string(KolStrFile)+
                            FLAROS.Nbals+" + "+FLAROS.Nomls+" не определен счет RS-Retail");
            PutConvMessage (0,"      Счет не сконвертирован.");
            MayCont = False;
          end;
        end;
        if (MayCont)
          KeyNum (DEPOSITR,7);
          DEPOSITR.FNcash = Int(FNcash);
          DEPOSITR.Account = Account;
          if (GetEQ (DEPOSITR))
/*            PutConvMessage (0,"  Счет "+Account); */
            OffSet = DBF_OffSet (FLAROS,"Sign");
            Sign1 = ExtractLong (FLAROS,OffSet,4);
            Sign2 = ExtractLong (FLAROS,OffSet+4,2) - 2;
            if ((Sign1 == -1) OR (Sign2 == -1))
              PutConvMessage (M_ERROR," Ошибка чтения поля Sign для счета "+Account);
              MayCont = False;
            end;
          else
            PutConvMessage (M_ERROR," В базе Retail не найден счет "+Account);
            MayCont = False;
          end;
        end;
        if (MayCont)
            RunParm = DirSign + "sign"+FNcash+".lib" + " " +
                      DirSign + "data32x.bmp" + " " +
                      String(Sign1) + " " + String(Sign2);


            PutConvMessage (0,"  Параметры "+RunParm);


            stat = Run ("lib2bmp", RunParm, Null, Null);
            if (stat != 0)
              PutConvMessage (M_ERROR," Ошибка считывания подписи "+String(stat)+
                              " для счета "+Account);
              MayCont = False;
            else
              stat = SignLong (DEPOSITR.CodClient,3,"signscl.dbt",
                               DirSign + "data32x.bmp");
              if (stat != 0)
                PutConvMessage (M_ERROR," Ошибка загрузки подписи "+String(stat)+
                                " для счета "+Account);
                MayCont = False;
              else 
                PutConvMessage (M_MESSAGE," Ok "+String(stat)+ 
                                " для счета "+Account);
              end;
            end;

/* data\sign3.lib pic\data32x.bmp 1047 819 */

            /* Run (".exe",RunParm,Null,Null); */

            /*********/


        end;
        if (MayCont)
          NumStrFile = NumStrFile + 1;
        end;
      end;
      KolStrFile = KolStrFile + 1;
      KolView    = KolView    + 1;
      if ((KolView == PeriodView) OR (KolStrFile == 1))
        Message (" Часть 1. Обработано ",KolStrFile," записей. Сконвертировано ", NumStrFile, " записей ...");
        if (KolStrFile != 1)
          KolView = 0;
        end;
      end;
    end;
  end;
end;
/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro Lar_Sign (AutoW)
/*
  Головной макрос
*/
  var NumFile;
  var MayCont = True;;

  AutoWork = AutoW;

  if (MayCont)
/*
  Заголовок отчета
*/
    if (NOT AutoWork)
      if (GetTrue(TRUE,"Очистить файл протокола?"))
        SetOutput(NameLogFile,False);
        SetOutput();
        SetVersion();
      end;
    end;
    Начало_Конец_отчета ();
    PutConvMessage (M_TITLE, TitleString +
     "|Конвертирование подписей вкладчиков базы Ларос|"+
      "----------------------------------------------|");
/*
    Цикл по исходным файлам Laros
*/
    NumFile = 0;
    while (NumFile < KolFileLaros)
      FileNameLaros = DirLaros + FileNameMaket + DobFileName(NumFile);
      PutConvMessage (0, "");
      PutConvMessage (M_MESSAGE, " Начата обработка файла "+FileNameLaros);
      if (NumFile > 0)
        Close (FLAROS);
      end;
      if (not(Open(FLAROS,FileNameLaros)))
        PutConvMessage (M_VERYERROR,"Невозможно открыть файл "+FileNameLaros);
        MsgBox("Невозможно открыть файл "+FileNameLaros);
        Exit(1);
      else
        WorkWithFileLaros ();
      end;
      NumFile = NumFile + 1;
      PutConvMessage (M_MESSAGE, " Промежуточный итог: сконвертировано "+NumStrFile+" записей базы Laros");
    end;
/*
    Просмотр протокола
*/
    PutConvMessage (M_TITLE, "||Обработано "+KolStrFile+" записей базы Laros|"+
                    "Сконвертировано "+NumStrFile+" записей базы Laros");

    Начало_Конец_отчета ();
    Open (LogFile,NameLogFile);
    Close (LogFile);
    if (NOT AutoWork)
      ViewFile (LogFile);
    end;
  else
    [ Отказались от конвертирования ];
  end;
end;
 Lar_Sign(false)