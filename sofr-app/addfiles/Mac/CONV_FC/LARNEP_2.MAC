/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1998 год

  Вторая часть конвертера:
  Копирование данных по операциям в Универсальный промежуточный файл
  (файлы sXXb.dbf, sXXd.dbt)

  Рыжиков Александр Александрович
  10.06.98
*/
Import "common.mac","oper_num.mac";

/*
  Настройки
*/
var FileNameStatic = "SNB.DBF";
var FileNameUnited = "SOBS.DBF";
var FileNameLaros;
var {oper};

/*
  Файлы
*/
/*VIG 14.12.99*/
/*
file FLAROS() dbf;
*/
record SBD (sbdepdoc);
record SAVEDEPOSITR (depositr);
file LogFile() txt 250;         /* Для просмотра протокола */
/*
  Переменные
*/
var NumStrFile = 0;     /* Количество сконвертированных записей     */
var KolStrFile = 0;     /* Количество просмотренных записей         */
var KolStrNo   = 0;     /* Количество выброшенных строк             */
var KolStrDob  = 0;     /* Количество добавленных строк             */

/**********************************************************/
macro GetDepStaticUnited(FNcash,SvodAccount,Static)
   var OldKey;
   var GroupNumb;
   var Stat = True;

   if(Static)
     GroupNumb = "Неподвижный";
   else
     GroupNumb = "Объединенный";
   end;

   OldKey = KeyNum(DEPOSITR,0);

   rewind(DEPOSITR);
   ClearRecord(DEPOSITR);

   DEPOSITR.FNcash = FNcash;
   DEPOSITR.GroupNumb = GroupNumb;
   DEPOSITR.Code_Currency = 0;
   DEPOSITR.Number = Int(SubStr(SvodAccount,3));
/*   MsgBox("ac ",DEPOSITR.Account,GroupNumb,FNcash);*/

   if(NOT GetEQ(DEPOSITR))
      stat = False;
   end;
   KeyNum(DEPOSITR,OldKey);
   return stat;
end;

/***********************************************************/

macro TestRestForSvod(Pref)
/*
  Добавление записей в промежуточный файл по каждому
  сводному неподвижному счету
*/

  var stat = True;
  var ErrCode,ErrText;

  rewind(DEPOSITR);
  while (stat AND Next(DEPOSITR))
    if (SubStr(DEPOSITR.Account,1,2) == Pref)
      ClearRecord (PROMF);
      PROMF.ReservDoc = "          ";
/*
      Заполнение полей Промежуточного файла
*/
      PROMF.Referenc = DEPOSITR.Referenc;
      PROMF.NumDayDoc = 10;
      PROMF.Type_Account = DEPOSITR.PrevAccount;
      PROMF.FNCash = DEPOSITR.FNCash;
      PROMF.Account = DEPOSITR.Account;
      PROMF.Date_Document = DateConvert;

      PROMF.TypeOper = 62;
      PROMF.TypeComplexOper = 0;
      PROMF.ApplType = 0;
      PROMF.VidDoc = 0;

      PROMF.InSum = DEPOSITR.Sum_Rest;
      PROMF.Rest = DEPOSITR.Sum_Rest;
      PROMF.DepDate_Document = PROMF.Date_Document;
      PROMF.IsCur = FlagCur;
      PROMF.Code_Currency = DEPOSITR.Code_Currency;
      PROMF.Oper = {oper};
      PROMF.YesSbook = "";
      PROMF.CodClient = DEPOSITR.CodClient;
      PROMF.Ground = "Перевод на" + DEPOSITR.GroupNumb + DEPOSITR.PrevAccount;
      if (NOT Insert(PROMF))
        ErrCode = Status(ErrText);
        PutConvMessage (M_ERROR," Для сводного счета "+ DEPOSITR.Account +
         " не введена операция за "+String(DateConvert));
        PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
        stat = False;
      end;
    end;
  end;
  return stat;
end;

/**********************************************************/

macro WorkWithFileStatic
/*
  Цикл по записям файла
*/
  var ErrCode,
      ErrText;
  var KindInLaros;
  var MayCont;
  var GrOper;
  var YearOp, MonOp, DayOp;
  var Cont = FALSE;
  var NeedUpdDep,
      NeedUpdStatic;
  var SavePos = 0;
  var Pref = "#Н";
  var Count = 0;
  var NewFN;
  var Account;
  var OldReferenc = 0;
  var Y,M,D;

  while (Next(FLAROS))
    MayCont = TRUE;
    NeedUpdDep = FALSE;
    NeedUpdStatic = False;

    KindInLaros = FLAROS.VIDVK;
    Count = Count + 1;
/*
      Строка счета
*/
    MayCont = GetNewFNCash(Trim(FLAROS.NOMFL),NewFN);
    if (NOT MayCont)
      PutConvMessage (M_ERROR," Не найден номер филиала " + Trim(FLAROS.NOMFL) + " по счету "+
       Trim(FLAROS.NOMLS)+". Выполните Часть 1 Конвертера");
    else
      Account = SetNumAccountForLaros (FLAROS.NOMLS,NewFN,FLAROS.VIDVK);
      MayCont = GetDepositrRecord (NewFN,Account);
    end;
    if (NOT MayCont)
      PutConvMessage (M_ERROR," Не найдена строка по счету "+
       Trim(Account)+". Выполните Часть 1 Конвертера");
    else
      SavePos = GetPos(DEPOSITR);
      Copy(SAVEDEPOSITR,DEPOSITR);
    end;

    /* VIG 12.04.00 */
    if ( OldReferenc != SAVEDEPOSITR.Referenc )
       Count = 1;
       OldReferenc = SAVEDEPOSITR.Referenc;
    end;

/*
    Строка счета неподвижного вклада
*/
    if (MayCont)
      MayCont = GetDepStaticUnited (DEPOSITR.FNcash,DEPOSITR.SvodAccount,True);
      if (NOT MayCont)
        PutConvMessage (M_ERROR," Не найден сводный неподвижный счет по виду вклада "+
          DEPOSITR.Type_Account +". Выполните Часть 1 Конвертера");
      end;
    end;
    if(MayCont)
      MayCont = GetTypeRecord (SAVEDEPOSITR.Type_Account,FlagCur);
      if (NOT MayCont)
        PutConvMessage (M_ERROR," Для счета "+Trim(SAVEDEPOSITR.Account)+
         " не найден вид вклада "+Trim(SAVEDEPOSITR.Type_Account));
      end;
    end;
/*
     Чистка буферов файлов
*/
    if(MayCont)
      ClearRecord (PROMF);
      PROMF.ReservDoc = "          ";
/*
      Заполнение полей Промежуточного файла
*/
      PROMF.Referenc = SAVEDEPOSITR.Referenc;
      PROMF.NumDayDoc = Count * 10;
      PROMF.Type_Account = SAVEDEPOSITR.Type_Account;
      PROMF.FNCash = SAVEDEPOSITR.FNCash;
      PROMF.Account = SAVEDEPOSITR.Account;
      PROMF.Date_Document = FLAROS.DATAV;
      DateSplit (PROMF.Date_Document,D,M,Y);
      if ((D == 1) AND (M == 1))
        if ((FLAROS.OPER == "206") OR (FLAROS.OPER == "ДЕН"))
          PROMF.Date_Document = PROMF.Date_Document - 1;
        else
          PutConvMessage (M_MESSAGE," Для счета "+Trim(Account)+
           " недопустимая операция "+String(FLAROS.OPER)+" за дату "+
           String(FLAROS.DATAV));
        end;
      end;
      MayCont = GetNumOper (FLAROS.OPER,
         PROMF.KindOp,
         PROMF.TypeOper,
         PROMF.TypeComplexOper,
         PROMF.ApplType,
         PROMF.Ground,
         PROMF.VidDoc);
      if (NOT MayCont)
        PutConvMessage (M_ERROR," Для счета "+Trim(Account)+
         " не найдена операция "+String(FLAROS.OPER)+" в списке");
      end;
    end;
    if (MayCont)
      if (FLAROS.OPER == "206")
        PROMF.PercOprSum = Money(Double(FLAROS.SUMMA)*100);
      end;
    end;
    if(MayCont)
      if (PROMF.KindOp == D_IN)
        PROMF.InSum = Money(Double(FLAROS.SUMMA)*100);
/*        PROMF.InSum = DenomSum(PROMF.InSum,PROMF.Date_Document,PROMF.TypeOper);*/
      elif (PROMF.KindOp == D_OUT)
        PROMF.OutSum = Money(Double(FLAROS.SUMMA)*100);
/*        PROMF.OutSum = DenomSum(PROMF.OutSum,PROMF.Date_Document,PROMF.TypeOper);*/
      else
        MayCont = FALSE;
        PutConvMessage (M_ERROR," Для операции "+String(FLAROS.OPER)+
         " в списке не определен вид: дебет или кредит");
      end;
    end;
    if (MayCont)
      if ((FLAROS.OPER == "ДЕН") AND (FLAROS.Vidvk == "0"))
        PROMF.PercOprSum = PROMF.InSum;
        PROMF.PercRest = PROMF.InSum;
      end;
      PROMF.DepDate_Document = PROMF.Date_Document;
      PROMF.IsCur = FlagCur;
      PROMF.Code_Currency = DEPOSITR.Code_Currency;
      PROMF.Oper = 9999;
      PROMF.Rest = Money(Double(FLAROS.OSTAT)*100);
/*      PROMF.Rest = DenomSum(PROMF.Rest,PROMF.Date_Document,PROMF.TypeOper);*/
      PROMF.YesSbook = "";
      PROMF.CodClient = SAVEDEPOSITR.CodClient;
/*
    Добавление в сводный счет еще одного субсчета,
    обновление записи по субсчету.
*/
      if( FLAROS.PRIZN == "П")
        DEPOSITR.S_NumAccounts = DEPOSITR.S_NumAccounts + 1;
        DEPOSITR.Sum_Rest = DEPOSITR.Sum_Rest + PROMF.Rest;
   /*VIG 13.12.99*/
   /*     DEPOSITR.CashRest = 0;*/
        NeedUpdStatic = True;
        /*VIG 18.12.99*/
        /*SAVEDEPOSITR.Open_Date = FLAROS.DATAV;*/
        SAVEDEPOSITR.Final_Date = FLAROS.DATAV;
        SAVEDEPOSITR.Sum_Rest = PROMF.Rest;
        SAVEDEPOSITR.StaticRest = PROMF.Rest;
        SAVEDEPOSITR.StaticPaid = "";
        NeedUpdDep = True;

/*        GetPcacc();  ????  */
      end;
    end;
/*
    Запись в файлы
*/
    if (MayCont)
      if (NOT Insert(PROMF))
        ErrCode = Status(ErrText);
        PutConvMessage (M_ERROR," Для счета "+Trim(Promf.Account)+
         " не введена операция за "+String(FLAROS.DATAV));
        PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
        MayCont = FALSE;
      end;
    end;
    if (MayCont AND NeedUpdStatic)
        if (NOT Update(DEPOSITR))
          ErrCode = Status(ErrText);
          PutConvMessage (M_ERROR," Не обновлена запись по сводному неподвижному счету "+
            DEPOSITR.Account);
          PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
          MayCont = FALSE;
        end;
    end;
    if (MayCont AND NeedUpdDep)
        GetDirect(DEPOSITR,SavePos);
        Copy(DEPOSITR,SAVEDEPOSITR);
        if (NOT Update(DEPOSITR))
          ErrCode = Status(ErrText);
          PutConvMessage (M_ERROR," Не обновлена запись по счету "+
            Trim(FLAROS.NOMLS));
          PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
          MayCont = FALSE;
        end;
    end;
    if (MayCont)
      NumStrFile = NumStrFile + 1;
    end;
    KolStrFile = KolStrFile + 1;
    Message (" Часть 2. Обработано ",KolStrFile," записей. Сконвертировано ", NumStrFile, " записей ...");
  end;
  MayCont = TestRestForSvod(Pref);
  If(NOT MayCont)
    PutConvMessage (M_ERROR," Не введена запись по сводному неподвижному счету ");
  end;
end;

/**********************************************************/

macro WorkWithFileUnited
/*
  Цикл по записям файла объединенного файла
*/
  var ErrCode,
      ErrText;
  var MayCont;
  var Rest;
  var NeedUpdDep;
  var Pref = "#О";
  var Count = 0;
  var NewFN;
  var Account, Number;
  var D,M,Y;

  while (Next(FLAROS))
    MayCont = TRUE;
    NeedUpdDep = FALSE;
    Count = Count + 1;
/*
      Строка счета
*/
    MayCont = GetNewFNCash(Trim(FLAROS.KODFL),NewFN);
    if (NOT MayCont)
      PutConvMessage (M_ERROR," Не найден номер филиала "+Trim(FLAROS.KODFL)+ " по счету "+
       Trim(FLAROS.NOMOS)+". Выполните Часть 1 Конвертера");
    else
      Number = Int(FLAROS.NOMOS);
      Account = SetNumAccountForUnited (Trim (FLAROS.NOMOS));
      MayCont = GetDepositrRecord (NewFN,Trim(Account));
      if (NOT MayCont)
        PutConvMessage (M_ERROR," Не найдена строка по счету "+
         Trim(Account)+". Выполните Часть 1 Конвертера");
      end;
    end;
    if (MayCont)
      Rest = Money(Double(FLAROS.OSTAT)*100);
      if (FLAROS.PRIZN == "П")
        DEPOSITR.Sum_Rest = Rest;
        /*VIG 13.12.99*/
        /*DEPOSITR.CashRest = 0;*/
        NeedUpdDep = True;
      end;
    end;
/*
    Заполнение промежуточного файла
*/
    if(MayCont)
      ClearRecord (PROMF);
      PROMF.ReservDoc = "          ";
/*
      Заполнение полей Промежуточного файла
*/
      PROMF.Referenc = DEPOSITR.Referenc;
      PROMF.NumDayDoc = Count * 10;
      PROMF.Type_Account = DEPOSITR.Type_Account;
      PROMF.FNCash = DEPOSITR.FNCash;
      PROMF.Account = DEPOSITR.Account;
      PROMF.Date_Document = FLAROS.DATAV;
      DateSplit (PROMF.Date_Document,D,M,Y);
      if ((D == 1) AND (M == 1))
        if ((FLAROS.OPER == "206") OR (FLAROS.OPER == "ДЕН"))
          PROMF.Date_Document = PROMF.Date_Document - 1;
        else
          PutConvMessage (M_MESSAGE," Для счета "+Trim(Account)+
           " недопустимая операция "+String(FLAROS.OPER)+" за дату "+
           String(FLAROS.DATAV));
        end;
      end;
      MayCont = GetNumOper (FLAROS.OPER,
         PROMF.KindOp,
         PROMF.TypeOper,
         PROMF.TypeComplexOper,
         PROMF.ApplType,
         PROMF.Ground,
         PROMF.VidDoc);
      if (NOT MayCont)
        PutConvMessage (M_ERROR," Для счета "+Trim(Account)+
         " не найдена операция "+String(FLAROS.OPER)+" в списке");
      end;
    end;
    if(MayCont)
      if (PROMF.KindOp == D_IN)
        PROMF.InSum = Money(Double(FLAROS.SUMMA)*100);
      elif (PROMF.KindOp == D_OUT)
        PROMF.OutSum = Money(Double(FLAROS.SUMMA)*100);
      else
        MayCont = FALSE;
        PutConvMessage (M_ERROR," Для операции "+String(FLAROS.OPER)+
         " в списке не определен вид: дебет или кредит");
      end;
    end;
    if (MayCont)
      PROMF.DepDate_Document = PROMF.Date_Document;
      PROMF.IsCur = FlagCur;
      PROMF.Code_Currency = DEPOSITR.Code_Currency;
      PROMF.Oper = 9999;
      PROMF.Rest = Money(Double(FLAROS.OSTAT)*100);
      PROMF.YesSbook = "";
      PROMF.CodClient = DEPOSITR.CodClient;
    end;
/*
    Запись в файлы
*/
    if (MayCont)
      if (NOT Insert(PROMF))
        ErrCode = Status(ErrText);
        PutConvMessage (M_ERROR," Для счета "+Trim(FLAROS.NOMLS)+
         " не введена операция за "+String(FLAROS.DATAV));
        PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
        MayCont = FALSE;
      end;
    end;
    if (MayCont AND NeedUpdDep)
      if (NOT Update(DEPOSITR))
        ErrCode = Status(ErrText);
        PutConvMessage (M_ERROR," Не обновлена запись по объединенному счету "+
          DEPOSITR.Account);
        PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
        MayCont = FALSE;
      end;
    end;
    if (MayCont)
      NumStrFile = NumStrFile + 1;
    end;
    KolStrFile = KolStrFile + 1;
    Message (" Часть 2. Обработано ",KolStrFile," записей. Сконвертировано ", NumStrFile, " записей ...");
  end;
end;

/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro Convert_2 (AutoW)
/*
  Головной макрос
*/
  var NumFile;
  var MayCont;
  var IsViewLog = TRUE;
  var Account;
  var NeedTest;
  var ErrCode, ErrText;
  var dd;
  var kol = 0;
  var opr;
  var GrOper;
  var Rest;
  var IsAlways;
  var EndForAcc;
  var Kol206, NeedKol206;
  var PrStat;
  var pos1,pos2;
  var NeedTestRest = False;

  AutoWork = AutoW;
/*
  Ввод параметров с клавиатуры
*/
  MayCont = GetDateConvert (2);

  if (MayCont)
/*
  Заголовок отчета
*/
    if (NOT AutoWork)
      if (GetTrue(Null,"Очистить файл протокола?"))
        SetOutput(NameLogFile,False);
        SetOutput();
      end;
    end;
    Начало_Конец_отчета ();
    PutConvMessage (M_TITLE, TitleString +
     "|Часть 2||Конвертирование операций в Универсальный промежуточнй файл|"+
               "------------------------------------------------------|");
/*
    Открытие Универсального промежуточного файла
*/
    if (NOT AutoWork)
      if (GetTrue(TRUE,"Очистить (создать заново) Промежуточный файл?"))
        MayCont = Clone (PROMF,NamePromFile);
      end;
    else
      MayCont = Clone (PROMF,NamePromFile);
    end;
    if (NOT MayCont)
      PutConvMessage (M_VERYERROR, "Не очищен (создан заново) Универсальный промежуточный файл "+
       Trim(NamePromFile));
    else
      MayCont = Open(PROMF,NamePromFile);
    end;
    if (NOT MayCont)
      PutConvMessage (M_VERYERROR, "Не открыт Универсальный промежуточный файл "+
       Trim(NamePromFile));
    end;
  else
    [ Отказались от конвертирование ];
    IsViewLog = FALSE;
  end;
/*
    Цикл по исходным файлам Laros
*/
  if (MayCont)
/*
    Конвертирование неподвижных вкладов
*/
    FileNameLaros = DirLaros + FileNameStatic ;
    PutConvMessage (0, "");
    PutConvMessage (M_MESSAGE, " Начата обработка файла операций неподвижных вкладов"+FileNameLaros);
    if (not(Open(FLAROS,FileNameLaros)))
      PutConvMessage (M_VERYERROR,
       "Невозможно открыть файл "+FileNameLaros);
      if (NOT AutoWork)
        MsgBox("Невозможно открыть файл "+FileNameLaros);
      end;
    else
      WorkWithFileStatic ();
    end;
    PutConvMessage (M_MESSAGE, " Промежуточный итог: сконвертировано "+NumStrFile+" записей базы Laros");
    PutConvMessage (M_MESSAGE, " Закончена обработка файла неподвижных вкладов ");
    Close (FLAROS);
/*
    Конвертирование объединенных вкладов
*/
    FileNameLaros = DirLaros + FileNameUnited ;
    PutConvMessage (0, "");
    PutConvMessage (M_MESSAGE, " Начата обработка файла операций объединенных вкладов "+FileNameLaros);
    if (not(Open(FLAROS,FileNameLaros)))
      PutConvMessage (M_VERYERROR,
       "Невозможно открыть файл "+FileNameLaros);
      if (NOT AutoWork)
        MsgBox("Невозможно открыть файл "+FileNameLaros);
      end;
    else
       WorkWithFileUnited ();
    end;
    PutConvMessage (M_MESSAGE, " Промежуточный итог: сконвертировано "+NumStrFile+" записей базы Laros");
    PutConvMessage (M_MESSAGE, " Закончена обработка файла объединенных вкладов ");
    Close (FLAROS);
  end;
/*
  Просмотр протокола
*/
  if (IsViewLog)
    PutConvMessage (M_TITLE, "||Обработано "+KolStrFile+" записей базы Laros|"+
     "Сконвертировано "+NumStrFile+" записей базы Laros|"+
     "Выброшено при конвертировании "+KolStrNo+" записей базы Laros|"+
     "Добавлено при конвертировании "+KolStrDob+" записей");
    Начало_Конец_отчета ();
    Open(LogFile,NameLogFile);
    Close (LogFile);
    if (NOT AutoWork)
      ViewFile (LogFile);
    end;
  end;
end;

