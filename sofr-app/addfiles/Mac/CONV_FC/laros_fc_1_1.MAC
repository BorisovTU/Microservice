/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1999 год

  Первая часть конвертера:
  Конвертирование данных по счетам и вкладчикам (depositr и depclnt)
  (файлы fXXb.dbf, fXXd.dbt)

  Рыжиков Александр Александрович
  21.09.99
*/
import "comm_fc.mac";

/*
  Переменные
*/
var NumStrFile   = 0;   /* Количество сконвертированных записей     */
var KolStrFile   = 0;   /* Количество просмотренных записей         */
var LastReferenc = 0;
var KolView      = 0;
var Nrec         = 0;
var KindInLaros  = "";   /* Группа по размещению данных в поле PASP  */

array Type_Acc, Rests, CountOpen, CountClose;


/**********************************************************
    Перекодировка строки из одной таблицы в другую.
**********************************************************/

/* Таблица перекодировки фамилий */
const strRus = "АаВЕеЕеЗКкМмНОоРрСсТХх";
const strLat = "AaBEeЁё3KkMmHOoPpCcTXx";

macro RecodeString( InpStr, InpABC, OutABC )
  var i   = 1,          /* Позиция символа во входной строке          */
      k   = 0,          /* позиция этого символа во входном алфавите  */
      Lit = "",         /* соответствующая литера после перекодировки */
      OutStr = "";      /* выходная строка                            */

   while( i <= strlen( InpStr ) )
      Lit = substr( InpStr, i, 1 );
      k = index( InpABC, Lit );
      if( k != 0 ) Lit = substr( OutABC, k, 1 ); end;
      OutStr = OutStr + Lit;
      i = i + 1;
   end;

  return OutStr;
end;

/**********************************************************
    Преобразование Ф.И.О. в формат Retail.
**********************************************************/
const FIODelim = " .";

macro GetFIO
(
 FIOStr,        /* Исходная строка      */
 F,             /* Фамилия              */
 I,             /* Имя                  */
 O              /* Отчество             */
)
/*
  Получение фамилии, имени и отчества из строки
*/
  var F_ = "",
      I_ = "",
      O_ = "",
      ptr= 0;

  FIOStr = RecodeString( trim( FIOStr ), strLat, strRus );

  FIOStr = StrUpr( FIOStr );

  ptr = strbrk( FIOStr, FIODElim );
  if( ptr > 0 )
    F_     = trim( substr( FIOStr, 1, ptr ) );
    FIOStr = trim( substr( FIOStr, ptr+1 ) );

    ptr = strbrk( FIOStr, FIODElim );
    if( ptr > 0 )
      I_     = trim( substr( FIOStr, 1, ptr ) );
      FIOStr = trim( substr( FIOStr, ptr+1 ) );

      O_ = trim( FIOStr );
    else
      I_ = trim( FIOStr );
    end;
  else
    if( strlen( FIOstr ) )   /* Задана только фамилия */
      F_ = FIOstr;
    else
      F_ = "Б\\и";
    end;
  end;

  setparm( 1, F_ );
  setparm( 2, I_ );
  setparm( 3, O_ );
end;
/**********************************************************/

macro GetGRUPN
(
 GRUPN  /* Категория в Laros */
)
/*
  Возвращает группу населения в RS-Retail
*/
  var Kategor = 0;

  if (GRUPN == "1")
    Kategor = 3;        /* Пенсионер    */
  elif ((GRUPN=="С") OR (GRUPN=="с"))
    Kategor = 2;        /* Служащий     */
  elif ((GRUPN=="р") OR (GRUPN=="Р"))
    Kategor = 1;        /* Рабочий      */
  elif ((GRUPN=="к") OR (GRUPN=="К"))
    Kategor = 4;        /* Фермер (колхозник, крестьянин) */
  elif ((GRUPN=="П") OR (GRUPN=="п"))
    Kategor = 0;        /* Прочие       */
  end;

   return Kategor;
end;

macro GetCOUNT( Codc )
   var stat = "";

   keynum( country, 3 );
   rewind( country    );
   country.CodeNum = Codc;
   if( not GetEQ( country ) );
      Protocol.Message( "Не найден код страны ", Codc, ". Устанавливаем РФ!" );
      return "RUS";
   else
      return country.CodLat2;
   end;
end;

/**********************************************************/
macro GetUserTypeAccount
(
  PrizS,        /* Поле PRIZS базы Laros */
  PrizU         /* Поле PRIZU базы Laros */
)
/*
  Заполнение полей DEPOSITR на основании поля PREIZS
  "Статус вклада" базы Laros
*/
  if ((PrizS == "Д") OR (PrizS == "д"))         /* Действующий          */
    DEPOSITR.Givebook = "X";
  elif ((PrizS == "В") OR (PrizS == "в"))       /* Открыт безналично    */
    DEPOSITR.Givebook = "";
  elif ((PrizS == "А") OR (PrizS == "а"))       /* Арестован            */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Т";
  elif ((PrizS == "З") OR (PrizS == "з"))       /* Закрыт               */
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "Z") OR (PrizS == "z"))       /* Утеря с/к            */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Y";
  elif ((PrizS == "У") OR (PrizS == "у"))       /* Условно закрыт       */
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "W") OR (PrizS == "w"))       /* Выдан в отделении    */
    ;
  elif ((PrizS == "N") OR (PrizS == "n"))       /* Переведен в неподвижные*/
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "С") OR (PrizS == "с"))       /* Выдана ссуда         */
    ;
  elif ((PrizS == "P") OR (PrizS == "p"))       /* Выплата на похороны  */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Я";
  elif ((PrizS == "Н") OR (PrizS == "н"))       /* Выдана доля вклада наследнику*/
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Я";
  end;
end;

macro SetRemoveType( Symb, String, Set )
   var stat = True;
   var pos = 0;

   Symb   = Trim( Symb   );
   String = Trim( String );

   pos = Index( String, Symb );
   if( pos and not Set )
      String = SubStr( String, 1, pos-1 ) + SubStr( String, pos + 1 );
   elif( Set )
      String = String + Symb;
   end;

   return String;
end;


/**********************************************************/
macro GetUserTypeAcc_Gate
(
  PrizS,        /* Поле PRIZS базы Laros */
  PrizU,        /* Поле PRIZU базы Laros */
  Oper
)
/*
  Заполнение полей DEPOSITR на основании поля PREIZS
  "Статус вклада" базы Laros
*/
  if   ( ( PrizS ==   "Д" ) OR ( PrizS == "д" ) or
         ( Oper  == "196" ) )                         /* Действующий           */
    DEPOSITR.Givebook = "X";
  elif ( ( PrizS ==   "А" ) OR ( PrizS == "а" ) or
         ( Oper  == "190" ) )                         /* Арестован             */
    DEPOSITR.UserTypeAccount = SetRemoveType( "T", DEPOSITR.UserTypeAccount, True );
  elif ( ( PrizS ==   "Z" ) OR ( PrizS == "z" ) or
         ( Oper  == "194" ) )                         /* Утеря с/к            */
    DEPOSITR.UserTypeAccount = SetRemoveType( "Y", DEPOSITR.UserTypeAccount, True );
  elif ( ( PrizS ==   "У" ) OR ( PrizS == "у" ) )     /* Условно закрыт      */
    DEPOSITR.UserTypeAccount = SetRemoveType( "Z", DEPOSITR.UserTypeAccount, True );
  elif ( Oper  == "191" )                         /* Аннулирование ареста */
    DEPOSITR.UserTypeAccount = SetRemoveType( "T", DEPOSITR.UserTypeAccount, False );
  end;
end;

/**********************************************************/
macro GetDateDep
/*
  Заполнение полей договоров для записи по счету
  Возврат:
    TRUE - нет противоречий
*/
   var stat = TRUE;
   var EndDate;
   var EndD = Date(0,0,0);
   var d,m,y, d1,m1,y1;
   var Term, KindTerm, BegDate;
   var NeedPlus;
   var i;

   if (SB_DTYP.FormContr == 1)           /* Срочный вклад        */
     DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
     DEPOSITR.Term          = SB_DTYP.Term;
     DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
     EndDate = SumDate( DEPOSITR.Start_DateDep,DEPOSITR.Term,DEPOSITR.KindTerm );
     if (DateConvert <= EndDate) /* Дата окончания еще не наступила */
       DEPOSITR.End_DateDep = EndDate;
     else
       DEPOSITR.End_DateDep = Date (0,0,0);
     end;
     if ( ((SubStr(FLAROS.OSNOV,10,1)) == "z") AND
      ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
       (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p")))
       DEPOSITR.UseAlternate = 1;
       Protocol.Warning( " По счету ", Trim( DEPOSITR.Account ),
                         " зафиксирована выдача под обязательство или наследство" );
       Protocol.Message( "    Если операции 145 нет в конвертируемой ",
                         "истории операций, занесите ее руками в Промежуточный файл");
     end;

   elif (SB_DTYP.FormContr == 20)        /* Депозиты             */
     DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
     DEPOSITR.Term          = SB_DTYP.Term;
     DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
     if (GroupKindInLaros(KindInLaros) == 3)
       DEPOSITR.End_DateDep = GetDateFromPasp(11,FLAROS.PASP);
     else
       DEPOSITR.End_DateDep = GetDateFromPasp(1,FLAROS.PASP);
     end;
     EndD = DEPOSITR.End_DateDep;
     if (DEPOSITR.End_DateDep != Date(0,0,0))
       DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
     end;
/*     if (KindInLaros == "3") */ /* Старые депозиты */
       if ((DEPOSITR.End_DateDep < Date(1,1,CurY)) AND
        (DEPOSITR.Open_Close != "З"))
         Protocol.Warning( " Договор завершен не в текущем году для счета "+
                           Trim( DEPOSITR.Account ) );
         Protocol.Warning( "     Счет переведен в альтернативное состояние.");

         DEPOSITR.End_DateDep = Date (0,0,0);
         EndD = Date (0,0,0);
         DEPOSITR.UseAlternate = 1;
         DEPOSITR.Prol_DateDep = Date(1,1,CurY);
       end;
/*     end; */
     if ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
         (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p"))
       if (SubStr(FLAROS.OSNOV,10,1) == "z")
         DEPOSITR.UseAlternate = 1;
         Protocol.Warning(
             " По счету ", Trim(DEPOSITR.Account),
             " зафиксирована выдача под обязательство или наследство");
         Protocol.Message( "    Если операции 145 нет в конвертируемой "+
             "истории операций, занесите ее руками в Промежуточный файл");
       else
         Protocol.Warning(
             " Для счета ", Trim(DEPOSITR.Account),
             " не могу определить, нарушены условия депозита или нет");
       end;
     end;

   elif (SB_DTYP.FormContr == 30)        /* Пролонгируемые       */
     DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
     DEPOSITR.Term          = SB_DTYP.Term;
     DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
     DEPOSITR.Prol_DateDep  = GetDateFromPasp (25, FLAROS.PASP);
/*    if (DEPOSITR.Prol_DateDep != Date(0,0,0))
      DEPOSITR.Prol_DateDep = DEPOSITR.Prol_DateDep - 1;
    end;
*/
     DEPOSITR.Term_Prol     = SB_DTYP.Term_Prol;
     DEPOSITR.KindTerm_Prol = SB_DTYP.KindTerm_Prol;
     DEPOSITR.End_DateDep   = GetDateFromPasp( 1, FLAROS.PASP);
     EndD = DEPOSITR.End_DateDep;

/*  RA 13.04.99  Вернул назад исправление                */

    if( DEPOSITR.End_DateDep  != Date( 0, 0, 0 ) )
       DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
    end;

    if ((FLAROS.PRIZS == "Н") or (FLAROS.PRIZS == "н") or
        (FLAROS.PRIZS == "P") or (FLAROS.PRIZS == "p"))
      DEPOSITR.UseAlternate = 1;
      Protocol.Warning(
          " По счету ",Trim(DEPOSITR.Account),
          " зафиксирована выдача под обязательство или наследство");
      Protocol.Message("    Если операции 145 нет в конвертируемой ",
          "истории операций, занесите ее руками в Промежуточный файл");
    end;
  end;
/*
  Уточнение
*/
  if (GroupKindInLaros(KindInLaros) == 3) /* Детский и Школьный */
    DEPOSITR.End_DateDep = GetDateFromPasp(11,FLAROS.PASP);
  end;
/*
  Проверка срока окончания и уточнение
*/
  if( ( EndD != Date( 0, 0, 0 ) ) and NeedControlTerm ( KindInLaros ) )
/*    println( depositr.Account, KindInLaros );*/
    DateSplit (EndD, d,m,y);
    DateSplit (DEPOSITR.End_DateDep, d1,m1,y1);
    if ((d == 1) OR (d1 == 1))          /* Необходимо уточнение */
      if (DEPOSITR.Prol_DateDep != Date(0,0,0))
        Term     = DEPOSITR.Term_Prol;
        KindTerm = DEPOSITR.KindTerm_Prol;
        BegDate  = DEPOSITR.Prol_DateDep - 1;
      else
        Term     = DEPOSITR.Term;
        KindTerm = DEPOSITR.KindTerm;
        BegDate  = DEPOSITR.Start_DateDep;
      end;
      NeedPlus = FALSE; /* Плюс 1 день */
      if (KindTerm == R_DAY)
        i = 1;
        while (i < 12)
          if (Term == (i * 30))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
          elif (Term == (i * 30 + 1))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
            NeedPlus = TRUE;
          end;
          i = i + 1;
        end;
      end;
      EndDate = SumDate(BegDate,Term,KindTerm);
      if (NeedPlus)
        EndDate = EndDate + 1;
      end;
      if (DEPOSITR.End_DateDep != EndDate)
        Protocol.Warning(
            " По счету "+Trim(DEPOSITR.Account)+
            " корректируется дата конца договора");
        Protocol.Message("    Старое значение: "+String(DEPOSITR.End_DateDep));
        Protocol.Message("    Новое значение: "+String(EndDate));
        DEPOSITR.End_DateDep = EndDate;
      end;
    end;
  end;

  return stat;
  OnError (MyErr)
     Protocol.Error ("Код ошибки ", MyErr.Code, " ", MyErr.Message,
                     " Модуль ", MyErr.Module, " Строка ", MyErr.Line,
                     " Номер счета ", Depositr.Account);
     Return False;
end;

/***********************************************************
   Обработка строк файла Laros
***********************************************************/

macro WorkWithFileLaros
/*
  Цикл по записям файла
*/
  var MayCont;
  var Resid = 0;
  var ind   = 0;
  var NeedFullInfo = 1;  /* -1 - выгрузка без операций
                             0 - выгружать по счету,
                             1 - не надо              */
  var stat = True;
  var KeyNumber = 0;

  NRec = Nrec + Nrecords( FLAROS );
  if( IsLock_Gate )
     InitProgress( Nrec, "", "Выгрузка данных по счетам и вкладчиках" );
  else
     InitProgress( Nrec, "", "Конвертация вкладчиков и их счетов" );
  end;

  while( Next( FLAROS ) )
    NeedFullInfo = 1;
    MayCont      = TRUE;
    if( FLAROS.DATAO == Date( 0, 0, 0 ) )
       if( (FLAROS.PRIZS == "" ) OR ( FLAROS.PRIZS == " " ) )
          MayCont = False;        /* Откидываем служебную запись */
       else
          Protocol.Error( "Для счета ", FLAROS.Nbals, Trim( FLAROS.NOMLS ),
                          "не определена дата открытия." );
       end;
    end;

    if( IsLock_Gate )
       /* Шлюз выгружает те счета, по которым есть операции с датой из диапазона дат загрузки
          Если счет открыт в этот дипазон, то по нему выгружаем всю информацию */

       ClearRecord( indfile    );
       keynum     ( indfile, 0 );
       rewind     ( indfile    );

       indfile.Nbals = Trim( FLAROS.Nbals );
       indfile.NomLs = Trim( FLAROS.Nomls );
       MayCont = GetEQ( indfile ); /* Нашли, что по счету есть запись в опердневнике */

       if( MayCont and ( FLAROS.DATAO >= BegLockDate ) and ( FLAROS.DATAO <= EndLockDate ) )
          NeedFullInfo = 0;
       end;
    end;

    if( MayCont )
      KindInLaros = SubStr( FLAROS.Tip_vk, 1, 1 );
      if( NeedConvertType( KindInLaros, Int( FLAROS.Nomls ) ) ) /* Если конвертируем в данном сеансе */
/*
        Файл клиентов
*/
        extrn_cln( CLN_CODE_CLIENT ) = "";
        extrn_cln( CLN_ACCOUNT     ) = "";;
        GetFIO( FLAROS.FIOVK, extrn_cln( CLN_SNAME ),
                              extrn_cln( CLN_NAME  ),
                              extrn_cln( CLN_PNAME ) ); /* Ф.И.О. */
        extrn_cln( CLN_BDATE ) = FLAROS.GODR;
        if( GroupKindInLaros( KindInLaros ) != 3 )
          if( extrn_cln( CLN_BDATE ) == Date( 0, 0, 0 ) )
             if( Int( FLAROS.GODR ) )
                extrn_cln( CLN_BDATE ) = Date( 1, 1, Int( FLAROS.GODR ) ); /* Дата рождения (только год) */
             else
                extrn_cln( CLN_BDATE ) = Date( 1, 1, CurY );   /* Совсем кривая дата рождения */
             end;
          end;
        else  /* Детские и Школьные */
          if( extrn_cln( CLN_BDATE ) == Date( 0, 0, 0 ) )
            extrn_cln( CLN_BDATE ) = GetDateFromPasp( 1, FLAROS.PASP );
          end;
          if( extrn_cln( CLN_BDATE ) == Date( 0, 0, 0 ) )
            Protocol.Error( "Не задана дата рождения для вкладчика ",
                             Trim( FLAROS.FIOVK ), "со счетом", extrn_cln( CLN_ACCOUNT ) );
          end;
        end;
        extrn_cln( CLN_ADDRESS      ) = Trim( FLAROS.ADRES );     /* И в адрес прописки и в адрес проживания пишется одно и то же */
        extrn_cln( CLN_GROUP_WILL   ) = "";                       /* Завещание       */
        extrn_cln( CLN_GROUP_AUTHOR ) = "";                       /* Доверенность    */

        if( ( Trim( FLAROS.S_PASP ) != "" ) OR ( FLAROS.N_PASP != 0 ) )
           extrn_cln( CLN_PERS_ID_TYPE  ) = 0; /* Паспорт */
           extrn_cln( CLN_PERS_ID_SER   ) = Trim  (      FLAROS.S_PASP   );
           extrn_cln( CLN_PERS_ID_NUM   ) = String( Int( FLAROS.N_PASP ) );
           extrn_cln( CLN_PERS_ID_EXTRA ) = String( Trim( FLAROS.DAN_PAS ), " Выд. ", Trim( FLAROS.DAT_P) );
        end;

        Resid = GetResid( FLAROS.Nbals );
        if( Resid == 0 )
           extrn_cln( CLN_CITIZENSHIP    ) = "";     /* Резидент */
           extrn_cln( CLN_TAKE_INCOM_TAX ) = "";
        else
           extrn_cln( CLN_CITIZENSHIP    ) = 1;    /* Не резидент */
           extrn_cln( CLN_TAKE_INCOM_TAX ) = StrFor(88);
        end;
        extrn_cln( CLN_ACTION         ) = 0;
        extrn_cln( CLN_SOCIAL_NUMBER  ) = 0;
        extrn_cln( CLN_REGISTR_DATE   ) = NullDate;
        extrn_cln( CLN_KATEGOR        ) = GetGRUPN( Trim( FLAROS.GRUPN ) ); /* Категория населения */
        extrn_cln( CLN_COUNTRY        ) = GetCOUNT( Int(  FLAROS.CODC  ) ); /* Страна */
        extrn_cln( CLN_VID_DOC        ) = "";
        extrn_cln( CLN_TYPE           ) = "0";

/*
        Файл счетов
*/
        ClearRecord( DEPOSITR );

/*        DEPOSITR.Referenc      = Int( String( Int( FLAROS.Nbals ),
                                              Int( FLAROS.NOMLS ) ) ); /* Уникальный идентификатор счета */
*/
        depositr.Referenc = LastReferenc + NumStrFile;
/*        println (depositr.Referenc, " ", FLAROS.NOMLS, " ", FLAROS.NBALS);*/

        if( depositr.Referenc == 0 )
           Protocol.Error ("Для счета ", FLAROS.NBALS, " ", FLAROS.NOMLS, " не был сформирован refetrenc");
        end;

        depositr.CodClient     = Int( FLAROS.Nbals );
        depositr.IsCur         = 0;                          /* 0-рубли, 1-валюта    */
        depositr.FNCash        = Branch;                     /* Номер филиала        */
        depositr.Code_Currency = 0;
        depositr.Oper          = 9999;
        if( IsLock_Gate )
           ClearRecord( indfile    );
           keynum     ( indfile, 1 );
           rewind     ( indfile    );

           indfile.Nbals = Trim( FLAROS.Nbals );
           indfile.NomLs = Trim( FLAROS.Nomls );
           indfile.Oper  = "190";
           stat = ( GetGE( indfile ) and ( indfile.Nbals == Trim( FLAROS.Nbals ) )and
                                         ( indfile.Nomls == Trim( FLAROS.Nomls ) )and
                                         ( indfile.Oper  <= "197"                )     );
           while( stat and ( indfile.Nbals == Trim( FLAROS.Nbals ) )and
                           ( indfile.Nomls == Trim( FLAROS.Nomls ) )and
                           ( indfile.Oper  >= "190"                )and
                           ( indfile.Oper  <= "197"                )     )
              NeedFullInfo = -1;
              GetUserTypeAcc_Gate( FLAROS.PRIZS, FLAROS.PRIZU, indfile.Oper );
              stat = Next ( indfile );
           end;
        else
           GetUserTypeAccount ( FLAROS.PRIZS, FLAROS.PRIZU );  /* Открыт/Закрыт и пользовательсикй тип счета */
        end;

        MayCont = GetRsKindForLaros_New( FLAROS.Tip_vk,
                                         FLAROS.DATAO,
                                         FLAROS.PRIZU,
                                         DEPOSITR.Type_Account );

        if( ( NeedFullInfo == -1 ) and ( depositr.Type_Account == "Пенсионный" ) )
           depositr.Type_Account = "ДО ВОСТРЕБ.";
           protocol.Message ("Меняем для счета ЛАРОС ", FLAROS.NBALS, FLAROS.NOMLS, " вид вклада с Пенсионный на До Востребования");
        end;

        if( NOT MayCont )
           Protocol.Error( "Для счета ", FLAROS.Nbals, Trim( FLAROS.NOMLS ),
                           " не определен RS-вид вклада. Счет не сконвертирован." );
        else
           if( depositr.Type_Account == "Пенсионный" )
              extrn_cln( CLN_SOCIAL_NUMBER  ) = Int( Substr( FLAROS.PASP, 1, 7 ) );
           elif( ( depositr.Type_Account == "Ср.пенсион."  ) or
                 ( depositr.Type_Account == "Ср.пенс.6мес" )    )
              extrn_cln( CLN_SOCIAL_NUMBER  ) = Int( Substr( FLAROS.PASP, 12, 10 ) );;
           end;
           extrn_cln( CLN_REGISTR_DATE   ) = NullDate;
        end;
        if( MayCont )
           /* Определим вид вклада, для взятия условий для расчета процентов.
              Например, для "Срочного с доп. взносами" условия берутся по "Срочному"
           */
           MayCont = GetPC_APLTP( depositr.IsCur, depositr.Type_Account, 0 );
           if( not MayCont )
              Protocol.Error( "По счету", FLAROS.Nbals, Trim( FLAROS.NOMLS ), "для вклада",
                              depositr.Type_Account, " не найден вид вклада для основных условий" );

           else
              /* Поиск записи вида вклада для основных условий */
              MayCont = GetTypeRecord( pc_apltp.ApType, pc_apltp.IsCur );
              if( NOT MayCont )
                 Protocol.Error( "Для счета ", FLAROS.Nbals, Trim( FLAROS.NOMLS ),
                                 "не найден вид вклада \"", pc_apltp.ApType,
                                 "\"основных условий для вклада \"",Trim( DEPOSITR.Type_Account ),
                                 "\". Счет не сконвертирован." );
              else
                 depositr.FormContr = SB_DTYP.FormContr;
                 if( Resid == 0 )
                    DEPOSITR.GroupNumb = SB_DTYP.NewGroupNumb;
                 else
                    DEPOSITR.GroupNumb = string( -int( SB_DTYP.NewGroupNumb ) );
                 end;

                 DEPOSITR.Number       = GetAccNumber_New( FLAROS.Nomls );

                 KeyNumber = KeyNum (depositr);
                 KeyNum (depositr, 0);
                 Rewind (depositr   );

                 stat = GetEQ (depositr);

                 DEPOSITR.PrevAccount  = Trim( FLAROS.Old_Nls );
                 DEPOSITR.Account      = FormAccountNumber( DEPOSITR.Code_Currency,
                                                            DEPOSITR.Type_Account,
                                                            DEPOSITR.Number,
                                                            Resid );

                 if( DEPOSITR.Account == "" )
                    Protocol.Error( "Для счета Ларос", FLAROS.Nbals, FLAROS.Nomls,
                                    " не определен счет RS-Retail" );
                    Protocol.Message("      Счет не сконвертирован.");
                    Protocol.Message( "Код валюты", DEPOSITR.Code_Currency,
                                      "Вид Вклада", DEPOSITR.Type_Account,
                                      "Номер,    ", DEPOSITR.Number,
                                      "Группа нумерации", DEPOSITR.GroupNumb,
                                      "Резидент  ", Resid );

                    MayCont = False;
                 end;
              end;
           end;
        end;

/* Вставка от 6.10.98 VS */
        if( MayCont )
           if( ( ( Resid == 0 ) AND ( FLAROS.Nbals != SB_DTYP.BalAcc       ) ) OR
               ( ( Resid != 0 ) AND ( FLAROS.Nbals != SB_DTYP.BalAccNotRez ) )    )
              MayCont = False;
              Protocol.Error(" Для счета", DEPOSITR.Account, "(вид вклада",
                             FLAROS.Tip_vk, "-", DEPOSITR.Type_Account,
                             ") не соответствуют номера балансовых в настройках и в Ларос" );
              Protocol.Message( "Счет", DEPOSITR.Account, " не сконвертирован." );
           end;
        end;
/* end of Вставка */
        if( MayCont )
           extrn_cln( CLN_ACCOUNT ) = DEPOSITR.Account;
           DEPOSITR.Perc_Type     = 0;
           DEPOSITR.Sum_Out       = 0;
           DEPOSITR.Sum_In        = 0;
           DEPOSITR.Sum_Rest      = 0;
           DEPOSITR.Open_Date     = FLAROS.DATAO;
           DEPOSITR.Close_Date    = Date( 0, 0, 0 );
           DEPOSITR.NCurLine      = 0;
           DEPOSITR.Limit         = 0;
           DEPOSITR.FlagWill      = StrFor( Int( Trim( FLAROS.PRIZD ) ) ); /* Завещание       */
           DEPOSITR.FlagTrast     = StrFor( Int( Trim( FLAROS.PRIZZ ) ) ); /* Доверенность    */
           MayCont = GetDateDep ();
        end;

        if( MayCont )
/* 6.10.98 VS */
          if( ( FLAROS.Tip_vk == "Т1" ) AND ( FLAROS.GRUPN == 1 ) AND
              ( Int( SubStr( FLAROS.RES, 19, 2 ) ) < MonForPen ) )
/*
            По Пенсионному- в альтернативном состоянии
*/
            DEPOSITR.UseAlternate = 1;
          end;
/*
          Проценты
*/
          DEPOSITR.DateBeginPerc = DEPOSITR.Open_Date;
          DEPOSITR.DateEndPerc   = Date( 31, 12, 2099 );
          DEPOSITR.ReceiverPerc  = DEPOSITR.Referenc;
        end;
/*
        Запись в файлы
*/
        if( MayCont )
           DEPOSITR.NumSession = -1;
           DEPOSITR.StaticPaid = KindInLaros;
           DEPOSITR.Action     = NeedFullInfo;  /* -1 - выгрузка без операций
                                                    0 - выгружать по счету,
                                                    1 - не надо            */

           if( stat )
              if( not Update (depositr) )
                 Protocol.BtrError( "Счет ", Trim( DEPOSITR.Account ), " не обновлен в базе данных.",
                                    "Вкладчик: ", trim( FLAROS.FIOVK ) );
                 Protocol.Message( "Референс счета      ",depositr.Referenc,
                                   "Группа нумерации:   ",DEPOSITR.GroupNumb,
                                   "Номер в группе:     ",DEPOSITR.Number,
                                   "Оригинальный номер: ",FLAROS.Nbals, FLAROS.Nomls,
                                   "Резидент:           ",Resid );
              end;
           else
              if( Not Insert( DEPOSITR ) )
                 Protocol.BtrError( "Счет ", Trim( DEPOSITR.Account ), " не введен в базу данных.",
                                    "Вкладчик: ", trim( FLAROS.FIOVK ) );
                 Protocol.Message( "Референс счета      ",depositr.Referenc,
                                   "Группа нумерации:   ",DEPOSITR.GroupNumb,
                                   "Номер в группе:     ",DEPOSITR.Number,
                                   "Оригинальный номер: ",FLAROS.Nbals, FLAROS.Nomls,
                                   "Резидент:           ",Resid );

                 MayCont = FALSE;
              end;
           end;

           KeyNum (depositr, KeyNumber);
           if( MayCont )
              If( not IsLock_Gate or ( NeedFullInfo != 1 ) )
                 ext_cln.Str = MakeTxtStr( extrn_cln );
                 if( not Insert( ext_cln ) )
                    Protocol.Error( "Не вставлена запись о вкладчике по счету", depositr.Account );
                    MayCont = FALSE;
                 end;
              end;
           end;
        end;

        if( MayCont )
           NumStrFile = NumStrFile + 1;
        end;
      end;
      KolStrFile = KolStrFile + 1;
    end; /* first fi */
    UseProgress( KolView = KolView + 1 );
  end; /* elihw */
  LastReferenc = LastReferenc + NumStrFile;
  RemProgress( KolView );
end;

/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro Laros_fc_1_1()
/*
  Головной макрос
*/

   WorkWithFileLaros ();
   Protocol.Message( "Промежуточный итог: просмотрено", KolView,"сконвертировано", NumStrFile );

end;

