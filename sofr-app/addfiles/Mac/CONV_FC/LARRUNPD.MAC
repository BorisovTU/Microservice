/*
  RS-Retail
  Конвертирование неподвижных и объединенных счетов Ларос в RS-Retail
  1998 год

  Вызов частей Конвертера либо организация сквозного прохода

  Ромодин Александр Васильевич
  1,.06.98
*/
Import "common.mac", "oper_num.mac";

file LFile() txt 250;         /* Для просмотра протокола */

array Pmenu;
const Prompt = " Конвертирование идет в 3 этапа либо запускается сразу. Esc - отказаться";
Pmenu(0)  = " ~1~. Первая часть- демография";
Pmenu(1)  = " ~2~. Вторая часть- операции";
Pmenu(2)  = " ~3~. Третья часть- обработка Универсального файла";
Pmenu(3)  = " ~С~квозной проход";

const AllWorkNumber = 3;        /* Номер пункта меню "Сквозной проход" */

var NumM;
var AWork = False;
var iView = False;
var cont = True;
var againC = True;
var WasWork = False;

if (FlagCur == 0)
  while (againC)
    cont = True;
/*
    Меню -- выбор части конвертирования
*/
    NumM = Menu(Pmenu,Prompt,"Неподвижные и объединенные Ларос");
/*
    Выполнение
*/
    if ((NumM < 0) OR (NumM >= Asize(Pmenu)))
      againC = False;
      if (NOT WasWork)
        [ Отказались от конвертирования];
      end;
    else
      WasWork = True;
      if (NumM == AllWorkNumber)
        AWork = True; /* Сквозной проход */
        againC = False;
      end;
      if ((NumM == 0) OR (NumM == AllWorkNumber))
        if (ExecMacroFile ("set_datl.mac","SetCurDate_",True))
          ExecMacroFile ("larnep_1.mac","Convert_1",True);
          iView = True;
        else
          cont = False;
        end;
      end;
      if (cont AND ((NumM == 1) OR (NumM == AllWorkNumber)))
        ExecMacroFile ("larnep_2.mac","Convert_2",AWork);
      end;
      if (cont AND ((NumM == 2) OR (NumM == AllWorkNumber)))
        ExecMacroFile ("larnep_3.mac","Convert_3",AWork);
      end;
/*
      Просмотр протокола
*/
      if (cont AND ((NumM == AllWorkNumber) OR iView))
        Open(LFile,NameLogFile);
        Close (LFile);
        ViewFile (LFile);
        iView = False;
      end;
    end;
  end;
else
  MsgBox (" Система настроена на работу с валютой|"+
   "Рублевый конвертер запускать запрещено| |"+
   "Сходите в пункт \"Справочники/Обороты учреждения\"|"+
   "и нажмите <Alt-F11> для настройки системы на работу с рублями");
end;
