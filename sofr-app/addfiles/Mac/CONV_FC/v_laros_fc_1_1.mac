/*
  RS-Retail                                             R-Style Software Lab
  Конвертирование данных из Laros в RS-Retail
  1999 год

  Первая часть конвертера:
  Конвертирование данных по счетам и вкладчикам (depositr и depclnt)
  (файлы fXXb.dbf, fXXd.dbt)

  Рыжиков Александр Александрович
  09.12.99
*/
import "comm_fc.mac";

/*
  Переменные
*/
var NumStrFile   = 0;   /* Количество сконвертированных записей     */
var KolStrFile   = 0;   /* Количество просмотренных записей         */
var LastReferenc = 0;
var KolView      = 0;
var Nrec         = 0;
var KindInLaros  = "";   /* Группа по размещению данных в поле PASP  */

array Type_Acc, Rests, CountOpen, CountClose;


/**********************************************************
    Перекодировка строки из одной таблицы в другую.
**********************************************************/

/* Таблица перекодировки фамилий */
const strRus = "АаВЕеЕеЗКкМмНОоРрСсТХх";
const strLat = "AaBEeЁё3KkMmHOoPpCcTXx";

macro RecodeString( InpStr, InpABC, OutABC )
  var i   = 1,          /* Позиция символа во входной строке          */
      k   = 0,          /* позиция этого символа во входном алфавите  */
      Lit = "",         /* соответствующая литера после перекодировки */
      OutStr = "";      /* выходная строка                            */

   while( i <= strlen( InpStr ) )
      Lit = substr( InpStr, i, 1 );
      k = index( InpABC, Lit );
      if( k != 0 ) Lit = substr( OutABC, k, 1 ); end;
      OutStr = OutStr + Lit;
      i = i + 1;
   end;

  return OutStr;
end;

/**********************************************************
    Преобразование Ф.И.О. в формат Retail.
**********************************************************/
const FIODelim = " .";

macro GetFIO
(
 FIOStr,        /* Исходная строка      */
 F,             /* Фамилия              */
 I,             /* Имя                  */
 O              /* Отчество             */
)
/*
  Получение фамилии, имени и отчества из строки
*/
  var F_ = "",
      I_ = "",
      O_ = "",
      ptr= 0;

  FIOStr = RecodeString( trim( FIOStr ), strLat, strRus );

  FIOStr = StrUpr( FIOStr );

  ptr = strbrk( FIOStr, FIODElim );
  if( ptr > 0 )
    F_     = trim( substr( FIOStr, 1, ptr ) );
    FIOStr = trim( substr( FIOStr, ptr+1 ) );

    ptr = strbrk( FIOStr, FIODElim );
    if( ptr > 0 )
      I_     = trim( substr( FIOStr, 1, ptr ) );
      FIOStr = trim( substr( FIOStr, ptr+1 ) );

      O_ = trim( FIOStr );
    else
      I_ = trim( FIOStr );
    end;
  else
    if( strlen( FIOstr ) )   /* Задана только фамилия */
      F_ = FIOstr;
    else
      F_ = "Б\\и";
    end;
  end;

  setparm( 1, F_ );
  setparm( 2, I_ );
  setparm( 3, O_ );
end;

/********************************************************************
  Получение кода страны
********************************************************************/
macro GetCOUNT( Codc )
   var stat = "";

   keynum( country, 3 );
   rewind( country    );
   country.CodeNum = Codc;
   if( not GetEQ( country ) );
      Protocol.Error( "Не найден код страны ", Codc, ". Устанавливаем РФ!" );
      return "RUS";
   else
      return country.CodLat2;
   end;
end;

/********************************************************************
  Заполнение полей DEPOSITR на основании поля PRIZS "Статус счета"
********************************************************************/
macro GetUserTypeAccount
(
  PrizS /* Поле PRIZS базы Laros */
)
  if   ((PrizS == "Д") OR (PrizS == "д"))       /* Действующий             */
    DEPOSITR.Givebook = "X";
  elif ((PrizS == "В") OR (PrizS == "в"))       /* Открыт безналично       */
    DEPOSITR.Givebook = "";
  elif ((PrizS == "А") OR (PrizS == "а"))       /* Арестован               */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Т";
  elif ((PrizS == "З") OR (PrizS == "з"))       /* Закрыт                  */
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "Z") OR (PrizS == "z"))       /* Утеря сберкнижки        */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Y";
  elif ((PrizS == "н") OR (PrizS == "Н") OR (PrizS == "р") OR (PrizS == "Р")) /* Блокирован  */
    DEPOSITR.UserTypeAccount = DEPOSITR.UserTypeAccount + "Я";
  elif ((PrizS == "У") OR (PrizS == "у"))       /* Условно закрыт          */
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "N") OR (PrizS == "n"))       /* Переведен в неподвижные */
    DEPOSITR.Open_Close = "З";
  end;
end;

macro SetRemoveType( Symb, String, Set )
   var stat = True;
   var pos = 0;

   Symb   = Trim( Symb   );
   String = Trim( String );

   pos = Index( String, Symb );
   if( pos and not Set )
      String = SubStr( String, 1, pos-1 ) + SubStr( String, pos + 1 );
   elif( Set )
      String = String + Symb;
   end;

   return String;
end;

/**********************************************************/
macro GetUserTypeAcc_Gate
(
  PrizS,        /* Поле PRIZS базы Laros */
  Oper
)
/*
  Заполнение полей DEPOSITR на основании поля PREIZS
  "Статус вклада" базы Laros
*/
  if   ( ( PrizS ==   "Д" ) OR ( PrizS == "д" ) or
         ( Oper  == "196" ) )                         /* Действующий           */
    DEPOSITR.Givebook = "X";
  elif ( ( PrizS ==   "А" ) OR ( PrizS == "а" ) or
         ( Oper  == "190" ) )                         /* Арестован             */
    DEPOSITR.UserTypeAccount = SetRemoveType( "T", DEPOSITR.UserTypeAccount, True );
  elif ( ( PrizS ==   "Z" ) OR ( PrizS == "z" ) OR
         ( Oper  == "194" ) )                         /* Утеря с/к            */
    DEPOSITR.UserTypeAccount = SetRemoveType( "Y", DEPOSITR.UserTypeAccount, True );
  elif ( ( PrizS ==   "У" ) OR ( PrizS == "у" ) )     /* Условно закрыт      */
    DEPOSITR.UserTypeAccount = SetRemoveType( "Z", DEPOSITR.UserTypeAccount, True );
  elif ( (PrizS == "н") OR (PrizS == "Н") OR
         (PrizS == "р") OR (PrizS == "Р") )           /* Блокирован  */
    DEPOSITR.UserTypeAccount = SetRemoveType( "Я", DEPOSITR.UserTypeAccount, True );
  elif ( ( Oper  == "191" ) )                         /* Аннулирование ареста */
    DEPOSITR.UserTypeAccount = SetRemoveType( "T", DEPOSITR.UserTypeAccount, False );
  end;
end;

/* Опеределение даты пролонгации депозита */
macro MakeDateFormat( st )
   var Len  = 0;

   st  = Trim  ( st );
   Len = Strlen( st );

   if(   Len == 8 )
      return ( Date( Int(        SubStr( st, 1, 2 ) ),
                     Int(        SubStr( st, 4, 2 ) ),
                     Int( "19" + SubStr( st, 7, 2 ) ) ) );
   elif( Len == 10 )
      return ( Date( Int( SubStr( st, 1, 2 ) ),
                     Int( SubStr( st, 4, 2 ) ),
                     Int( SubStr( st, 7, 4 ) ) ) );
   else
      return date( 0, 0, 0 );
   end;
end;

/********************************************************************
        Заполнение полей договоров для записи по счету
********************************************************************/
macro GetDateDep
(
 dat1, /* дата начала    */
 dat2  /* дата окончаеия */
)
/*
  Возврат:
    TRUE - нет противоречий
*/
  var stat = TRUE;
  var EndDate;
  var EndD = Date(0,0,0);
  var d,m,y,d1,m1,y1,st,st1;
  var Term, KindTerm, BegDate;
  var NeedPlus;
  var i;

  if (SB_DTYP.FormContr == 1)           /* Срочный вклад        */
    DEPOSITR.Start_DateDep = dat1;
    DEPOSITR.Term          = SB_DTYP.Term;
    DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
    EndDate = SumDate(dat1,DEPOSITR.Term,DEPOSITR.KindTerm);
    if (DateConvert <= EndDate) /* Дата окончания еще не наступила */
      DEPOSITR.End_DateDep = EndDate;
    else
      DEPOSITR.End_DateDep = Date (0,0,0);
    end;
    if ( ( (FLAROS.PRIZS == "z") OR (FLAROS.PRIZS == "Z") )AND
     ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
      (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p")))
      DEPOSITR.UseAlternate = 1;
       Protocol.Warning( " По счету ", Trim( DEPOSITR.Account ),
                         " зафиксирована выдача под обязательство или наследство" );
       Protocol.Message( "    Если операции 145 нет в конвертируемой ",
                         "истории операций, занесите ее руками в Промежуточный файл");
    end;
  elif (SB_DTYP.FormContr == 20)        /* Депозиты             */
    DEPOSITR.Start_DateDep = dat1;
    DEPOSITR.Term          = SB_DTYP.Term;
    DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
    if (GroupKindInLaros_cur(KindInLaros) == 2)
      DEPOSITR.End_DateDep = dat2;
    end;
    EndD = DEPOSITR.End_DateDep;
    if (DEPOSITR.End_DateDep != Date(0,0,0))
      DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
    end;
    if ((DEPOSITR.End_DateDep < Date(1,1,CurY)) AND
     (DEPOSITR.Open_Close != "З"))
      Protocol.Warning( " Договор завершен не в текущем году для счета "+
                        Trim( DEPOSITR.Account ) );
      Protocol.Warning( "     Счет переведен в альтернативное состояние.");
      DEPOSITR.End_DateDep  = Date(0,0,0);
      DEPOSITR.Prol_DateDep = Date(1,1,CurY);
      EndD = Date(0,0,0);
      DEPOSITR.UseAlternate = 1;
    end;
  elif (SB_DTYP.FormContr == 30)        /* Пролонгируемые       */
    st1 = MakeDateFormat( Flaros.OSNOV );
    DEPOSITR.Start_DateDep = dat1;
    DEPOSITR.Term          = SB_DTYP.Term;
    DEPOSITR.KindTerm      = SB_DTYP.KindTerm;
    DEPOSITR.Prol_DateDep  = st1;
    DEPOSITR.Term_Prol     = SB_DTYP.Term_Prol;
    DEPOSITR.KindTerm_Prol = SB_DTYP.KindTerm_Prol;
    DEPOSITR.End_DateDep   = dat2;
    EndD = DEPOSITR.End_DateDep;
    if (DEPOSITR.End_DateDep != Date(0,0,0))
      DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
    end;
    if ((DEPOSITR.End_DateDep < DateConvert) AND
     (DEPOSITR.Open_Close != "З"))
      Protocol.Warning( "Дата конца договора не больше даты конвертирования для счета ",
                        Trim( DEPOSITR.Account ) );
    end;
    if ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
        (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p")   )
      DEPOSITR.UseAlternate = 1;
      Protocol.Warning(
          " По счету ",Trim(DEPOSITR.Account),
          " зафиксирована выдача под обязательство или наследство");
      Protocol.Message("    Если операции 145 нет в конвертируемой ",
          "истории операций, занесите ее руками в Промежуточный файл");
    end;
  end;

  /* Уточнение */

  if (GroupKindInLaros_cur(KindInLaros) == 2)
    if ((KindInLaros == "1") AND (dat2 < DateConvert))
        DEPOSITR.End_DateDep = Date(0,0,0);
    elif (KindInLaros == "7")
      ;
    elif (KindInLaros != "3")
      DEPOSITR.End_DateDep = dat2 - 1;
    end;
  end;

  /* Проверка срока окончания и уточнение */

  if ((EndD != Date(0,0,0)) AND NeedControlTerm(KindInLaros))
    DateSplit(EndD, d,m,y);
    DateSplit(DEPOSITR.End_DateDep,d1,m1,y1);
    if ((d == 1) OR (d1 == 1)) /* Необходимо уточнение */
      if (DEPOSITR.Prol_DateDep != Date(0,0,0))
        Term     = DEPOSITR.Term_Prol;
        KindTerm = DEPOSITR.KindTerm_Prol;
        BegDate  = DEPOSITR.Prol_DateDep - 1;
      else
        Term     = DEPOSITR.Term;
        KindTerm = DEPOSITR.KindTerm;
        BegDate  = DEPOSITR.Start_DateDep;
      end;
      NeedPlus = FALSE; /* Плюс 1 день */
      if (KindTerm == R_DAY)
        i = 1;
        while (i < 12)
          if (Term == (i * 30))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
          elif (Term == (i * 30 + 1))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
            NeedPlus = TRUE;
          end;
          i = i + 1;
        end;
      end;
      EndDate = SumDate(BegDate,Term,KindTerm);
      if (NeedPlus)
        EndDate = EndDate + 1;
      end;
      if (DEPOSITR.End_DateDep != EndDate)
        Protocol.Warning(
            " По счету "+Trim(DEPOSITR.Account)+
            " корректируется дата конца договора");
        Protocol.Message("    Старое значение: "+String(DEPOSITR.End_DateDep));
        Protocol.Message("    Новое значение: "+String(EndDate));
        DEPOSITR.End_DateDep = EndDate;
      end;
    end;
  end;

  return stat;
end;



/***********************************************************
   Обработка строк файла Laros
***********************************************************/

macro WorkWithFileLaros
/*
  Цикл по записям файла
*/
  var MayCont;
  var Resid = 0;
  var ind   = 0;
  var stat;
  var NeedFullInfo = 1;  /* -1 - выгрузка без операций
                             0 - выгружать по счету,
                             1 - не надо              */

  var mayterm;
  var Currency,         /* Код валюты */
      Nbals,
      Number;
  var date_op,
      date_op1, date_cl,
      date_prol, st;

  NRec = Nrec + Nrecords( FLAROS );
  if( IsLock_Gate )
     InitProgress( Nrec, "", "Выгрузка данных по счетам и вкладчиках" );
  else
     InitProgress( Nrec, "", "Конвертация вкладчиков и их счетов" );
  end;

  while( Next( FLAROS ) )
    NeedFullInfo = 1;
    MayCont      = TRUE;

    Resid = 0;
    date_prol = Date(0,0,0);
    date_op1  = Date(0,0,0);
    date_op   = FLAROS.DATAO; /* Дата открытия */

    if( FLAROS.DATAO == Date( 0, 0, 0 ) )
       if( (FLAROS.PRIZS == "" ) OR ( FLAROS.PRIZS == " " ) )
          MayCont = False;        /* Откидываем служебную запись */
       else
          Protocol.Error( "Для счета ", Trim( FLAROS.NOMLS ), "не определена дата открытия." );
       end;
    end;
    if( IsLock_Gate )
       /* Шлюз выгружает те счета, по которым есть операции с датой из диапазона дат загрузки
          Если счет открыт в этот дипазон, то по нему выгружаем всю информацию */

       ClearRecord( v_idfile    );
       keynum     ( v_idfile, 0 );
       rewind     ( v_idfile    );

       v_idfile.NomLs = Trim( FLAROS.Nomls );
       v_idfile.Oper  = "";

       MayCont = GetGE( v_idfile ); /* Нашли, что по счету есть запись в опердневнике */
       if( MayCont and ( v_idfile.Nomls == Flaros.Nomls )
           and ( FLAROS.DATAO >= BegLockDate ) and ( FLAROS.DATAO <= EndLockDate ) )
          NeedFullInfo = 0;
       end;
    end;

    if( MayCont )
      date_cl = FLAROS.DATZ ; /* Дата закрытия */

      KindInLaros = Trim( FLAROS.VIDVKL );
      /* получим атрибуты счета */
      GetTypeAccountInLaros_currency( FLAROS.NOMLS, NBals, Currency, Number );

      if( NeedConvertType( KindInLaros, Number ) ) /* Если конвертируем в данном сеансе */
/*
        Файл клиентов
*/
        extrn_cln( CLN_CODE_CLIENT ) = "";
        extrn_cln( CLN_ACCOUNT     ) = "";;
        GetFIO( FLAROS.FIOVK, extrn_cln( CLN_SNAME ),
                              extrn_cln( CLN_NAME  ),
                              extrn_cln( CLN_PNAME ) ); /* Ф.И.О. */
        /*println( Trim( FLAROS.NOMLS ), Int( FLAROS.GODR ) );*/
        if( Int( FLAROS.GODR ) != 0 )
           extrn_cln( CLN_BDATE ) = Date( 1, 1, Int( FLAROS.GODR ) );
        else
           extrn_cln( CLN_BDATE ) = Date( 0, 0, 0 );
        end;
        if( extrn_cln( CLN_BDATE ) == Date( 0, 0, 0 ) )
           Protocol.Error( "Не задана дата рождения для вкладчика ",
                            Trim( FLAROS.FIOVK ), "со счетом", extrn_cln( CLN_ACCOUNT ) );
        end;
        extrn_cln( CLN_ADDRESS       ) = Trim( FLAROS.ADRES );      /* И в адрес прописки и в адрес проживания пишется одно и то же */

        extrn_cln( CLN_GROUP_WILL    ) = "";
        extrn_cln( CLN_GROUP_AUTHOR  ) = "";
        extrn_cln( CLN_KATEGOR       ) = "";
        extrn_cln( CLN_PERS_ID_TYPE  ) = 0; /* Паспорт */
        extrn_cln( CLN_PERS_ID_SER   ) = String( FLAROS.SPASP );
        extrn_cln( CLN_PERS_ID_NUM   ) = String( FLAROS.NPASP );
        extrn_cln( CLN_PERS_ID_EXTRA ) = "";

        if( FLAROS.RESID == 2 ) Resid = 1; end; /* Не резидент */
        if( Resid == 0 )
           extrn_cln( CLN_CITIZENSHIP    ) = "";     /* Резидент */
           extrn_cln( CLN_TAKE_INCOM_TAX ) = "";
        else
           extrn_cln( CLN_CITIZENSHIP    ) = 1;    /* Не резидент */
           extrn_cln( CLN_TAKE_INCOM_TAX ) = StrFor(88);
        end;
        extrn_cln( CLN_ACTION         ) = 0;
        extrn_cln( CLN_SOCIAL_NUMBER  ) = 0;
        extrn_cln( CLN_REGISTR_DATE   ) = NullDate;
/*
        Файл счетов
*/
        ClearRecord( DEPOSITR );

/*        DEPOSITR.Referenc      = Int( String( Int( FLAROS.Nbals ),
                                              Int( FLAROS.NOMLS ) ) ); /* Уникальный идентификатор счета */
*/
        depositr.Referenc = LastReferenc + NumStrFile;
/*        println (depositr.Referenc, " ", FLAROS.NOMLS);*/

        if( depositr.Referenc == 0 )
           Protocol.Error ("Для счета ", FLAROS.NOMLS, " не был сформирован refetrenc");
        end;

        FLAROS.NomLs = Trim( FLAROS.NomLs );

        depositr.CodClient     = NBals;
        depositr.IsCur         = FlagCur;                    /* 0-рубли, 1-валюта    */
        depositr.FNCash        = Branch;                     /* Номер филиала        */
        /*Maycont = GetRealCurrency( Currency );*/
        depositr.Code_Currency = Currency;
        if( Maycont )
           depositr.Oper          = 9999;
           if( IsLock_Gate )
              ClearRecord( v_idfile    );
              keynum     ( v_idfile, 0 );
              rewind     ( v_idfile    );

              v_idfile.NomLs = Trim( FLAROS.Nomls );
              v_idfile.Oper  = "190";
              stat = ( GetGE( v_idfile ) and ( v_idfile.Nomls == Trim( FLAROS.Nomls ) )and
                                             ( v_idfile.Oper  <= "197"                )    );
              while( stat and ( v_idfile.Nomls == Trim( FLAROS.Nomls ) )and
                              ( v_idfile.Oper  <= "197"                )     )
                 NeedFullInfo = -1;
                 GetUserTypeAcc_Gate( FLAROS.PRIZS, v_idfile.Oper );
                 stat = Next (v_idfile);
              end;
           else
              GetUserTypeAccount ( FLAROS.PRIZS );  /* Открыт/Закрыт и пользовательсикй тип счета */
           end;

           mayterm = 0;
           date_prol = MakeDateFormat( FLAROS.OSNOV );
           date_op1 = date_op ;
           if( date_cl != Date(0,0,0) )
             if (date_prol != Date(0,0,0) )
               mayterm  = GetLengthDep( date_prol, date_cl );
               date_op1 = date_prol;
             else
               mayterm  = GetLengthDep( date_op,   date_cl );
             end;
           end;

           MayCont = GetRsKindForLaros_currency( KindInLaros, mayterm, date_op1,
                                                 DEPOSITR.Type_Account        );
           if( NOT MayCont )
              Protocol.Error( "Для счета ", Trim( FLAROS.NOMLS ),
                              " не определен RS-вид вклада. Счет не сконвертирован." );
           end;
        end;
        if( MayCont )
           /* Определим вид вклада, для взятия условий для расчета процентов.
              Например, для "Срочного с доп. взносами" условия берутся по "Срочному"
           */
           MayCont = GetPC_APLTP( depositr.IsCur, depositr.Type_Account, 0 );
           if( not MayCont )
              Protocol.Error( "По счету", Trim( FLAROS.NOMLS ), "для вклада",
                              depositr.Type_Account, " не найден вид вклада для основных условий" );

           else
              /* Поиск записи вида вклада для основных условий */
              MayCont = GetTypeRecord( pc_apltp.ApType, pc_apltp.IsCur );
              if( NOT MayCont )
                 Protocol.Error( "Для счета ", FLAROS.NOMLS,
                                 "не найден вид вклада \"", pc_apltp.ApType,
                                 "\"основных условий для вклада \"",Trim( DEPOSITR.Type_Account ),
                                 "\". Счет не сконвертирован." );
              else
                 depositr.FormContr = SB_DTYP.FormContr;
                 if( Resid == 0 )
                    DEPOSITR.GroupNumb = SB_DTYP.NewGroupNumb;
                 else
                    DEPOSITR.GroupNumb = string( -int( SB_DTYP.NewGroupNumb ) );
                 end;

                 DEPOSITR.Number       = Number;
                 DEPOSITR.PrevAccount  = Trim( FLAROS.NOMLS_ALT );
                 DEPOSITR.Account      = Flaros.NomLs;

                 if( DEPOSITR.Account == "" )
                    Protocol.Error( "Для счета Ларос", FLAROS.Nomls,
                                    " не определен счет RS-Retail" );
                    Protocol.Message("      Счет не сконвертирован.");
                    Protocol.Message( "Код валюты", DEPOSITR.Code_Currency,
                                      "Вид Вклада", DEPOSITR.Type_Account,
                                      "Номер,    ", DEPOSITR.Number,
                                      "Группа нумерации", DEPOSITR.GroupNumb,
                                      "Резидент  ", Resid );

                    MayCont = False;
                 end;
              end;
           end;
        end;

/* Вставка от 6.10.98 VS */
        if( MayCont )
           if( ( ( Resid == 0 ) AND ( Nbals != SB_DTYP.BalAcc       ) ) OR
               ( ( Resid != 0 ) AND ( Nbals != SB_DTYP.BalAccNotRez ) )    )
              MayCont = False;
              Protocol.Error(" Для счета", DEPOSITR.Account, "(вид вклада",
                             FLAROS.VidVkl, "-", DEPOSITR.Type_Account,
                             ") не соответствуют номера балансовых в настройках и в Ларос" );
              Protocol.Message( "Счет", DEPOSITR.Account, " не сконвертирован." );
              Protocol.Message( "Резидент= ", Resid, ", Nbals=", Nbals, ", BalAcc=", SB_DTYP.BalAcc, ", notRezAcc=", SB_DTYP.BalAccNotRez );
           end;
        end;
/* end of Вставка */
        if( MayCont )
           extrn_cln( CLN_ACCOUNT ) = DEPOSITR.Account;
           DEPOSITR.Perc_Type       = 0;
           DEPOSITR.Sum_Out         = 0;
           DEPOSITR.Sum_In          = 0;
           DEPOSITR.Sum_Rest        = 0;
           DEPOSITR.Open_Date       = FLAROS.DATAO;
           DEPOSITR.Close_Date      = date( 0, 0, 0 );
           DEPOSITR.NCurLine        = 0;
           DEPOSITR.Limit           = 0;

           if ( FLAROS.PRIZD == 1 )                                      /* Завещание       */
              DEPOSITR.FlagWill = StrFor( Int (FLAROS.PRIZD) );
           else
              DEPOSITR.FlagWill = StrFor( 0 );
           end;
           if ( FLAROS.PRIZZ == 1 )                                      /* Доверенность    */
              DEPOSITR.FlagTrast = StrFor( Int (FLAROS.PRIZZ) );
           else
              DEPOSITR.FlagTrast = StrFor( 0 );
           end;
/*
           DEPOSITR.FlagWill        = StrFor( Int (FLAROS.PRIZD) ); /* Завещание       */
           DEPOSITR.FlagTrast       = StrFor( Int (FLAROS.PRIZZ) ); /* Доверенность     */
*/
           MayCont = GetDateDep( date_op, date_cl );

        end;

        if( MayCont )
/*
          Проценты
*/
          DEPOSITR.DateBeginPerc = DEPOSITR.Open_Date;
          DEPOSITR.DateEndPerc   = Date( 31, 12, 2099 );
          DEPOSITR.ReceiverPerc  = DEPOSITR.Referenc;
        end;
/*
        Запись в файлы
*/
        if( MayCont )
           DEPOSITR.NumSession = -1;
           DEPOSITR.StaticPaid = KindInLaros;
           DEPOSITR.Action     = NeedFullInfo;  /* -1 - выгрузка без операций
                                                    0 - выгружать по счету,
                                                    1 - не надо            */
           if( Not Insert( DEPOSITR ) )
              Protocol.BtrError( "Счет ", Trim( DEPOSITR.Account ), " не введен в базу данных.",
                                 "Вкладчик: ", trim( FLAROS.FIOVK ) );
              Protocol.Message( "Референс счета      ",depositr.Referenc,
                                "Группа нумерации:   ",DEPOSITR.GroupNumb,
                                "Номер в группе:     ",DEPOSITR.Number,
                                "Оригинальный номер: ",Nbals, FLAROS.Nomls,
                                "Резидент:           ",Resid );

              MayCont = FALSE;
           end;
           if( MayCont )
              If( not IsLock_Gate or ( NeedFullInfo != 1 ) )
                 ext_cln.Str = MakeTxtStr( extrn_cln );
                 if( not Insert( ext_cln ) )
                    Protocol.Error( "Не вставлена запись о вкладчике по счету", depositr.Account );
                    MayCont = FALSE;
                 end;
              end;
           end;
        end;

        if( MayCont )
           NumStrFile = NumStrFile + 1;
        end;
      end;
      KolStrFile = KolStrFile + 1;
    end; /* first fi */
    UseProgress( KolView = KolView + 1 );
  end; /* elihw */
  LastReferenc = LastReferenc + NumStrFile;
  RemProgress( KolView );
end;

/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro V_Laros_fc_1_1()
/*
  Головной макрос
*/
   WorkWithFileLaros ();

   Protocol.Message( "Промежуточный итог: просмотрено", KolView,"сконвертировано", NumStrFile );
end;

