/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1999 год

  Третья часть конвертера:
  Конвертирование данных резерва % (pcestim)
  (файлы sXXbr.dbf, sXXdr.dbt)

  Голованов Виктор (VIG)
*/

macro HandleOneRecordReserv ()
   var
      MayCont = True;

   KeyNum( depositr, 4 );
   rewind( depositr    );
   ClearRecord( depositr );

   depositr.CodClient = Int( FLAROS.Nbals );
   depositr.IsCur     = 0;
   depositr.Number    = Int( FLAROS.NOMLS );

   if( GetEQ (depositr) )

      extrn_resrv ( RESRV_ACCOUNT        ) = depositr.Account;
      extrn_resrv ( RESRV_DATE_END_PER   ) = date (0, 0, 0);
      extrn_resrv ( RESRV_SUMMA_PROGN    ) = $0;
      extrn_resrv ( RESRV_DATE_LAST_CALC ) = FLAROS.DATAV;
      extrn_resrv ( RESRV_SUMMA_CALC     ) = FLAROS.ROST_TEK + FLAROS.ROST_RES + FLAROS.PROC_96;
      extrn_resrv ( RESRV_DATE_LAST_PAY  ) = FLAROS.DATAV;
      extrn_resrv ( RESRV_SUMMA_PAY      ) = FLAROS.ROST_TEK + FLAROS.ROST_RES + FLAROS.PROC_96;
      extrn_resrv ( RESRV_SUMMA_PAY_97   ) = FLAROS.PROC_96;

      ext_resrv.Str = MakeTxtStr( extrn_resrv );
      NumStrFile = NumStrFile + 1;
      if( not Insert( ext_resrv ) )
         Protocol.Error( "Не вставлена запись о вкладчике по счету", depositr.Account );
         MayCont = FALSE;
      end;
   end;
   KolStrFile = KolStrFile + 1;
end;

macro WorkWithFileLarosReserv ()
/*
  Цикл по записям файла
*/
   var stat = True;
   var MayCont = True;

   NRec = Nrec + Nrecords( FLAROS );
   if( IsLock_Gate )
      InitProgress( Nrec, "", "Выгрузка данных по резерву %" );
   else
      InitProgress( Nrec, "", "Конвертация резерва %" );
   end;

   while( Next( FLAROS ) )
      if( FLAROS.Prizn == "П" )
         if( not IsLock_Gate )
            HandleOneRecordReserv ();
         else
            if( ( FLAROS.DATAV > BegLockDate ) and ( FLAROS.DATAV < EndLockDate ) )
               HandleOneRecordReserv ();
            end;
         end;
      end;
      UseProgress( KolView = KolView + 1 );
   end;
   RemProgress( KolView );
end;

macro Laros_fc_1_3 ()
   WorkWithFileLarosReserv ();
   Protocol.Message( " Промежуточный итог: всего", KolView, "сконвертировано",
                     NumStrFile, "записей базы Laros" );
end;
