/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1998 год

  Первая часть конвертера:
  Конвертирование данных по счетам и вкладчикам (depositr и depclnt)
  (файлы fXXb.dbf, fXXd.dbt)

  Рыжиков Александр Александрович
  09.06.98
*/
Import "common.mac";

/*
  Настройки
*/
var FileNameStatic = "FNB.DBF";
var FileNameUnited = "SOBS.DBF";
var FileNameLaros;
var {oper};

/*
  Файлы
*/
file DEPCLNT   (depclnt)  write;       /* Список клиентов (вкладчиков) */
file SB_APLTP  (sb_apltp);             /* Файл ссылок на использованные типы вкладов */

record SAVEDEPOSITR  (depositr);

file LogFile() txt 250;         /* Для просмотра протокола */
/*
  Переменные
*/
var ReferencNumb = 0;   /* Номер последнего введенного в базу референса */
var LastMaxReferencNumb = 0;/* Номер последнего референса до запуска макроса */
var NumStrFile = 0;     /* Количество сконвертированных записей     */
var KolStrFile = 0;     /* Количество просмотренных записей         */
var KindInLaros = 0;    /* Группа по размещению данных в поле PASP  */

/**********************************************************/

/*VIG: наверное не понадобится*/
macro GetMaxReferencNumb
/*
  Получение наибольшего номера референса в базе счетов
*/
  /*VIG: Изменил формирование возвращаемого значения. Зачем оно надо - не постиг.*/
  KeyNum      (DEPOSITR, 8);
  Rewind      (DEPOSITR   );
  ClearRecord (DEPOSITR   );
  Prev (DEPOSITR);
  return DEPOSITR.Referenc;
end;

/**********************************************************/

/*VIG - здесь вроде все в порядке*/
macro GetFIO
(
 FIOStr,        /* Исходная строка      */
 F,             /* Фамилия              */
 I,             /* Имя                  */
 O              /* Отчество             */
)
/*
  Получение фамилии, имени и отчества из строки
*/
  var F_="",I_="",O_="",ptr=0;

  FIOStr = trim(FIOStr);

  ptr = StrBrk(FIOStr," ");
  if (ptr > 0)
    F_  = trim(substr(FIOStr,1,ptr));
    FIOStr = trim(substr(FIOStr,ptr));

    ptr = StrBrk(FIOStr," ");
    if (ptr > 0)
      I_  = trim(substr(FIOStr,1,ptr));
      FIOStr = trim(substr(FIOStr,ptr));

      O_ = trim(FIOStr);
    else
      I_ = Trim (FIOStr);
    end;
  else
    F_ = "Б\\и";
  end;

  SetParm(1,F_);
  SetParm(2,I_);
  SetParm(3,O_);
end;
/**********************************************************/

/*VIG - здесь вроде все в порядке*/
macro GetGRUPN
(
 GRUPN  /* Категория в Laros */
)
/*
  Возвращает группу населения в RS-Retail
*/
  var Kategor = 0;

  if (GRUPN == "1")
    Kategor = 3;        /* Пенсионер    */
  elif ((GRUPN=="С") OR (GRUPN=="с"))
    Kategor = 2;        /* Служащий     */
   elif ((GRUPN=="р") OR (GRUPN=="Р"))
    Kategor = 1;        /* Рабочий      */
   elif ((GRUPN=="к") OR (GRUPN=="К"))
    Kategor = 4;        /* Фермер (колхозник, крестьянин) */
   elif ((GRUPN=="П") OR (GRUPN=="п"))
    Kategor = 0;        /* Прочие       */
   end;

   return Kategor;
end;
/**********************************************************/

/*VIG - здесь вроде все в порядке*/
macro GetUserTypeAccount
(
  PrizS,        /* Поле PRIZS базы Laros */
  PrizU         /* Поле PRIZU базы Laros */
)
/*
  Заполнение полей DEPOSITR на основании поля PREIZS
  "Статус вклада" базы Laros
*/
  DEPOSITR.UserField1 = "";
  if ((PrizS == "Д") OR (PrizS == "д"))         /* Действующий          */
    DEPOSITR.Givebook = "X";
  elif ((PrizS == "В") OR (PrizS == "в"))       /* Открыт безналично    */
    DEPOSITR.Givebook = "";
  elif ((PrizS == "А") OR (PrizS == "а"))       /* Арестован            */
    DEPOSITR.UserTypeAccount = "Т";
  elif ((PrizS == "З") OR (PrizS == "з"))       /* Закрыт               */
    DEPOSITR.Open_Close = "З";
  elif ((PrizS == "Z") OR (PrizS == "z"))       /* Закрыт               */
    DEPOSITR.Open_Close = "З";
    DEPOSITR.UserField1 = "Закрыт из-за потери сбер.книжки";
  elif ((PrizS == "У") OR (PrizS == "у"))       /* Условно закрыт       */
    DEPOSITR.Open_Close = "З";
    DEPOSITR.UserField1 = "Условно закрыт";
  elif ((PrizS == "W") OR (PrizS == "w"))       /* Выдан в отделении    */
    DEPOSITR.UserField1 = "Выдан в отделении";
  elif ((PrizS == "N") OR (PrizS == "n"))       /* Переведен в неподвижные*/
    DEPOSITR.Open_Close = "З";
    DEPOSITR.UserField1 = "Переведен в неподвижные";
  elif ((PrizS == "С") OR (PrizS == "с"))       /* Выдана ссуда         */
    DEPOSITR.UserField1 = "Выдана ссуда";
  elif ((PrizS == "P") OR (PrizS == "p"))       /* Выплата на похороны  */
    DEPOSITR.UserField1 = "Выплата под обязательство";
  elif ((PrizS == "Н") OR (PrizS == "н"))       /* Выдана доля вклада наследнику*/
    DEPOSITR.UserField1 = "Выдана доля вклада наследнику";
  end;
  if (PrizU == "k")
    DEPOSITR.UserField1 = DEPOSITR.UserField1 +
     "   Открыт при зачислении компенсации на неподвижный счет";
  end;
end;
/**********************************************************/

/*VIG - здесь вроде все в порядке*/
macro GetDateDep
/*
  Заполнение полей договоров для записи по счету
  Возврат:
    TRUE - нет противоречий
*/
  var stat = TRUE;
  var EndDate;
  var EndD = Date(0,0,0);
  var d,m,y,d1,m1,y1;
  var Term, KindTerm, BegDate;
  var NeedPlus;
  var i;

  if (SB_DTYP.FormContr == 1)           /* Срочный вклад        */
    DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
    DEPOSITR.Term = SB_DTYP.Term;
    DEPOSITR.KindTerm = SB_DTYP.KindTerm;
    EndDate = SumDate(DEPOSITR.Start_DateDep,DEPOSITR.Term,DEPOSITR.KindTerm);
    if (DateConvert <= EndDate) /* Дата окончания еще не наступила */
      DEPOSITR.End_DateDep = EndDate;
    else
      DEPOSITR.End_DateDep = Date (0,0,0);
    end;
    if ( ((SubStr(FLAROS.OSNOV,10,1)) == "z") AND
     ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
      (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p")))
      DEPOSITR.UseAlternate = 1;
      PutConvMessage (M_WARN,
       " По счету "+Trim(DEPOSITR.Account)+
       " зафиксирована выдача под обязательство или наследство");
      PutConvMessage (0,"    Если операции 145 нет в конвертируемой "+
       "истории операций, занесите ее руками в Промежуточный файл");
    end;
  elif (SB_DTYP.FormContr == 20)        /* Депозиты             */
    DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
    DEPOSITR.Term = SB_DTYP.Term;
    DEPOSITR.KindTerm = SB_DTYP.KindTerm;
    if (GroupKindInLaros(KindInLaros) == 3)
      DEPOSITR.End_DateDep = GetDateFromPasp(11,FLAROS.PASP);
    else
      DEPOSITR.End_DateDep = GetDateFromPasp(1,FLAROS.PASP);
    end;
    EndD = DEPOSITR.End_DateDep;
    if (DEPOSITR.End_DateDep != Date(0,0,0))
      DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
    end;
/*    if (KindInLaros == "3") */ /* Старые депозиты */
      if ((DEPOSITR.End_DateDep < Date(1,1,CurY)) AND
       (DEPOSITR.Open_Close != "З"))
        PutConvMessage (M_WARN,
         " Договор завершен не в текущем году для счета "+
         Trim(DEPOSITR.Account));
        PutConvMessage (M_WARN,
         "     Счет переведен в альтернативное состояние.");
        DEPOSITR.End_DateDep = Date (0,0,0);
        EndD = Date (0,0,0);
        DEPOSITR.UseAlternate = 1;
        DEPOSITR.Prol_DateDep = Date(1,1,CurY);
      end;
/*    end; */
    if ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
      (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p"))
      if (SubStr(FLAROS.OSNOV,10,1) == "z")
        DEPOSITR.UseAlternate = 1;
        PutConvMessage (M_WARN,
         " По счету "+Trim(DEPOSITR.Account)+
         " зафиксирована выдача под обязательство или наследство");
        PutConvMessage (0,"    Если операции 145 нет в конвертируемой "+
         "истории операций, занесите ее руками в Промежуточный файл");
      else
        PutConvMessage (M_WARN,
         " Для счета "+ Trim(DEPOSITR.Account) +
         " не могу определить, нарушены условия депозита или нет");
      end;
    end;
  elif (SB_DTYP.FormContr == 30)        /* Пролонгируемые       */
    DEPOSITR.Start_DateDep = DEPOSITR.Open_Date;
    DEPOSITR.Term = SB_DTYP.Term;
    DEPOSITR.KindTerm = SB_DTYP.KindTerm;
    DEPOSITR.Prol_DateDep = GetDateFromPasp(25,FLAROS.PASP);
/*    if (DEPOSITR.Prol_DateDep != Date(0,0,0))
      DEPOSITR.Prol_DateDep = DEPOSITR.Prol_DateDep - 1;
    end;*/
    DEPOSITR.Term_Prol = SB_DTYP.Term_Prol;
    DEPOSITR.KindTerm_Prol = SB_DTYP.KindTerm_Prol;
    DEPOSITR.End_DateDep = GetDateFromPasp(1,FLAROS.PASP);
    EndD = DEPOSITR.End_DateDep;
    if (DEPOSITR.End_DateDep != Date(0,0,0))
      DEPOSITR.End_DateDep = DEPOSITR.End_DateDep - 1;
    end;
/***    if ((DEPOSITR.End_DateDep < DateConvert) AND
     (DEPOSITR.Open_Close != "З"))
      PutConvMessage (M_WARN,
       " Дата конца договора не больше даты конвертирования для счета "+
       Trim(DEPOSITR.Account));
    end; ****/
/*    EndDate = SumDate(DEPOSITR.Start_DateDep,DEPOSITR.Term,DEPOSITR.KindTerm);
    if (EndDate < (DateConvert-3)) /* Договор был пролонгирован */
      DEPOSITR.Prol_DateDep = SumDate (DEPOSITR.End_DateDep,
       (-1 * DEPOSITR.Term_Prol),DEPOSITR.KindTerm_Prol);
    end; */
    if ((FLAROS.PRIZS == "Н") OR (FLAROS.PRIZS == "н") OR
      (FLAROS.PRIZS == "P") OR (FLAROS.PRIZS == "p"))
      DEPOSITR.UseAlternate = 1;
      PutConvMessage (M_WARN,
       " По счету "+Trim(DEPOSITR.Account)+
       " зафиксирована выдача под обязательство или наследство");
      PutConvMessage (0,"    Если операции 145 нет в конвертируемой "+
       "истории операций, занесите ее руками в Промежуточный файл");
    end;
  end;
/*
  Уточнение
*/
  if (GroupKindInLaros(KindInLaros) == 3) /* Детский и Школьный */
    DEPOSITR.End_DateDep = GetDateFromPasp(11,FLAROS.PASP);
  end;
/*
  Проверка срока окончания и уточнение
*/
  if ((EndD != Date(0,0,0)) AND
   NeedControlTerm(KindInLaros))
    DateSplit (EndD, d,m,y);
    DateSplit (DEPOSITR.End_DateDep, d1,m1,y1);
    if ((d == 1) OR (d1 == 1))          /* Необходимо уточнение */
      if (DEPOSITR.Prol_DateDep != Date(0,0,0))
        Term = DEPOSITR.Term_Prol;
        KindTerm = DEPOSITR.KindTerm_Prol;
        BegDate = DEPOSITR.Prol_DateDep - 1;
      else
        Term = DEPOSITR.Term;
        KindTerm = DEPOSITR.KindTerm;
        BegDate = DEPOSITR.Start_DateDep;
      end;
      NeedPlus = FALSE; /* Плюс 1 день */
      if (KindTerm == R_DAY)
        i = 1;
        while (i < 12)
          if (Term == (i * 30))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
          elif (Term == (i * 30 + 1))
            Term = i;
            i = 1000;
            KindTerm = R_MON;
            NeedPlus = TRUE;
          end;
          i = i + 1;
        end;
      end;
      EndDate = SumDate(BegDate,Term,KindTerm);
      if (NeedPlus)
        EndDate = EndDate + 1;
      end;
      if (DEPOSITR.End_DateDep != EndDate)
        PutConvMessage (M_WARN,
         " По счету "+Trim(DEPOSITR.Account)+
         " корректируется дата конца договора");
        PutConvMessage (0,"    Старое значение: "+String(DEPOSITR.End_DateDep));
        PutConvMessage (0,"    Новое значение: "+String(EndDate));
        DEPOSITR.End_DateDep = EndDate;
      end;
    end;
  end;
  return stat;
end;

/***********************************************************/
Macro MakeNumSvodAccount(Pref,NumSvodAccount)
   var Len,
       i = 0;

   NumSvodAccount = String(NumSvodAccount);
   Len = StrLen(NumSvodAccount);
   while(i < (6 - Len))
     NumSvodAccount = "0" + NumSvodAccount;
     i = i + 1;
   end;
   NumSvodAccount = Pref + NumSvodAccount;
   return NumSvodAccount;
end;

/***********************************************************/
Macro GetSvodAccount(NumSvodAccount,Static,SvodAccount)
  var Pref;
  var OldKey;
  var Type_Account = "";
  var _SvodAccount = "",
      _NumSvodAccount = NumSvodAccount;
  var  NotFind = True;
  var Stat = True;
  var ErrCode,
      ErrText;

  If(Static)
    Pref = "#Н";
    Type_Account = KindNoMobil;
  else
    Pref = "#О";
    Type_Account = KindUnited;
  end;

  Copy(SAVEDEPOSITR,DEPOSITR);
  OldKey = KeyNum(DEPOSITR);
  KeyNum(DEPOSITR,1);
  Rewind(DEPOSITR);
/*
  DEPOSITR.FNCash уже определен ранее
*/
  DEPOSITR.IsCur  = FlagCur;
  DEPOSITR.Open_Close = "";
  DEPOSITR.Type_Account = Type_Account;
  if( GetGE(DEPOSITR) AND (DEPOSITR.FNCash == SAVEDEPOSITR.FNCash)
      AND (DEPOSITR.Type_Account == Type_Account) )
    if(DEPOSITR.PrevAccount == SAVEDEPOSITR.Type_Account)
/*
       Найден уже заведенный неподвижный/объединенный счет
*/
      _SvodAccount = DEPOSITR.Account;
      _NumSvodAccount = NumSvodAccount - 1;
      NotFind = False;
    else  /* Продолжаем поиск */
      while( next(DEPOSITR) AND (DEPOSITR.FNCash == SAVEDEPOSITR.FNCash)
             AND (DEPOSITR.Type_Account == Type_Account) )
        if(DEPOSITR.PrevAccount == SAVEDEPOSITR.Type_Account)
          _SvodAccount = DEPOSITR.Account;
          _NumSvodAccount = NumSvodAccount - 1;
          NotFind = False;
        end;
      end;
    end;
  end;
  if(NotFind)
/*
    Вставлем запись в DEPOSITR о неподвижном/объединенном счете
*/
    ClearRecord(DEPOSITR);
    DEPOSITR.SumPercOut = 0;
    DEPOSITR.Referenc = FormReference();      /* Уникальный идентификатор счета */
    DEPOSITR.CodClient = 0;                /* Код вкладчика- владельца счета */
    DEPOSITR.IsCur = FlagCur;               /* 0-рубли, 1-валюта    */
    DEPOSITR.Oper = {oper};

    /*VIG изменено*/

    DEPOSITR.Account = MakeNumSvodAccount(Pref,NumSvodAccount);
    _SvodAccount = DEPOSITR.Account;

    DEPOSITR.FNCash = SAVEDEPOSITR.FNCash;
    DEPOSITR.Number = NumSvodAccount;
    DEPOSITR.GroupNumb = Type_Account;
    DEPOSITR.Type_Account = DEPOSITR.GroupNumb;
    DEPOSITR.Code_Currency = Currency;

    DEPOSITR.PrevAccount = SAVEDEPOSITR.Type_Account;
    DEPOSITR.Perc_Type = 0;
    DEPOSITR.Sum_Out = 0;
    DEPOSITR.Sum_In = 0;
    DEPOSITR.Sum_Rest = 0;
    /*VIG 13.12.99*/
    /*DEPOSITR.CashRest = DEPOSITR.Sum_Rest;*/
    DEPOSITR.Open_Close = "";
    DEPOSITR.Open_Date = DateConvert;
    DEPOSITR.Final_Date = Date(0,0,0);
    /*VIG 13.12.99*/
    /*DEPOSITR.Previos_Date = Date(0,0,0);*/
    DEPOSITR.StaticRest = 0;
    DEPOSITR.StaticPaid  = "";
    DEPOSITR.NCurLine = 0;
    DEPOSITR.Limit = 0;
    DEPOSITR.NumSession = -1;
    if (Not Insert(DEPOSITR))
      ErrCode = Status(ErrText);
      PutConvMessage (M_ERROR," Счет "+Trim(DEPOSITR.Account)+
       " не введен в базу данных (1).");
      PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
      PutConvMessage (0,"      Счет "+Trim(DEPOSITR.Account)+
       " не сконвертирован.");
      Stat = FALSE;
    end;
  end;
  KeyNum(DEPOSITR,OldKey);
  if (Not Copy(DEPOSITR,SAVEDEPOSITR))
    ErrCode = Status(ErrText);
    PutConvMessage (M_ERROR," Для счета "+Trim(SAVEDEPOSITR.Account)+
     " не восстановлены данные, измененные при поиске сводного счета.");
    PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
    PutConvMessage (0,"      Счет "+Trim(SAVEDEPOSITR.Account)+
     " не сконвертирован.");
    Stat = FALSE;
  end;
  if(_NumSvodAccount != NumSvodAccount)
    SetParm(0,_NumSvodAccount);
  end;
  SetParm(2,_SvodAccount);
  ClearRecord(SAVEDEPOSITR);
  return Stat;
end;

/***********************************************************
   Обработка строк файла неподвижных вкладов
***********************************************************/

macro WorkWithFileStatic
/*
  Цикл по записям файла неподвижных вкладов
*/
  var ErrCode,
      ErrText;
  var MayCont;
  var KolView = 0;
  var StrGrNum = "";
  var TmpStrGrNum = "";
  var SaveType = "";
  var SaveFilial ="";
  var SvodAccount ="",
      NumSvodAccount = 0;
  var Chance = 0;
  var stat = True;

  while (Next(FLAROS))
    MayCont = TRUE;
/*
      Чистка буферов файлов
*/
    ClearRecord (DEPCLNT);
    ClearRecord (DEPOSITR);
    Chance = 0;
/*
    Номер филиала
*/
    if(MayCont)
      MayCont = GetNewFNCash(Trim(FLAROS.NOMFL),DEPOSITR.FNCash);  /* Номер филиала        */
      if (NOT MayCont)
        PutConvMessage (M_WARN, "Для счета "+Trim(FLAROS.NOMLS)+
           " не определен номер филиала. Счет не сконвертирован.");
      end;
    end;
/*
      Файл клиентов
*/
/* VIG пока убил, т.к. не правильно работает, но если надо можно исправить*/
/*    if (FLAROS.RESID != "")
       DEPCLNT.Citizenship = "";
    end;
*/
    DEPCLNT.CodClient = FormClientCode();                   /* Код клиента */
    GetFIO (FLAROS.FIO,DEPCLNT.Sname,DEPCLNT.Name,DEPCLNT.Pname); /* Ф.И.О. */
    DEPCLNT.FNCash = DEPOSITR.FNCash;
/*
      Файл счетов
*/
    KindInLaros = Trim(FLAROS.VIDVK);
    MayCont = GetRsKindForLaros(KindInLaros,"","","","",DEPOSITR.Type_Account);
/*
    MsgBox("1 ",DEPOSITR.Type_Account);
*/
    if (NOT MayCont)
        PutConvMessage (M_WARN, "Для счета "+Trim(FLAROS.NOMLS)+
         " не определен RS-вид вклада. Счет не сконвертирован.");
    end;
    if( MayCont )
      If( SaveType != DEPOSITR.Type_Account )  /* Cменился вид вклада */
         NumSvodAccount = NumSvodAccount + 1;
         SaveType = DEPOSITR.Type_Account;
         MayCont = GetSvodAccount(NumSvodAccount,True,SvodAccount);
      elif( SaveFilial != DEPOSITR.FNCash)        /* Cменился код филиала */
         NumSvodAccount = NumSvodAccount + 1;
         SaveFilial = DEPOSITR.FNCash;
         MayCont = GetSvodAccount(NumSvodAccount,True,SvodAccount);
      else

      end;
    end;
    if(MayCont)
      DEPOSITR.SumPercOut = 0;
      DEPOSITR.Referenc = FormReference();      /* Уникальный идентификатор счета */
      DEPOSITR.CodClient=DEPCLNT.CodClient;   /* Код вкладчика- владельца счета */
      DEPOSITR.IsCur = FlagCur;               /* 0-рубли, 1-валюта    */
/*      DEPOSITR.Number = Int(FLAROS.NOMLS);*/

        if (NOT GetTypeRecord (DEPOSITR.Type_Account,FlagCur))/* Поиск вида вклада */
          PutConvMessage (M_WARN, "Для счета "+Trim(FLAROS.NOMLS)+
           " не найден вид вклада \""+Trim(DEPOSITR.Type_Account)+
           "\". Счет не сконвертирован.");
        else
          TmpStrGrNum = "";
          TmpStrGrNum = SubStr ( SB_DTYP.GroupNumb, 1, 11 );
          /*MsgBox (TmpStrGrNum);*/
          StrGrNum = "НК" + TmpStrGrNum;
          Depositr.GroupNumb = StrGrNum;
          /*MsgBox (Depositr.GroupNumb);*/
        end;


      /*VIG вставка 13.12.99*/
/*
      if (DEPCLNT.Citizenship == "")
         DEPOSITR.Account = FormAccountNumber( DEPOSITR.Code_Currency,
                                          DEPOSITR.Type_Account, 0);
      else
         DEPOSITR.Account = FormAccountNumber( DEPOSITR.Code_Currency,
                                          DEPOSITR.Type_Account, 0, 0);
      end;
*/
   /*   DEPOSITR.PrevAccount = SetNumAccountForLaros (FLAROS.NOMLS,
      DEPOSITR.FNCash,FLAROS.VIDVK);
    */
      /*VIG изменено 13.12.99*/

      DEPOSITR.Account = SetNumAccountForLaros (FLAROS.NOMLS,
       DEPOSITR.FNCash,FLAROS.VIDVK);         /* Номер счета          */

      DEPOSITR.Oper = {oper};
    end;
    if (MayCont)
      DEPOSITR.Code_Currency = Currency;
      DEPOSITR.Perc_Type = 0;
      DEPOSITR.Sum_Out = 0;
      DEPOSITR.Sum_In = 0;
      DEPOSITR.Sum_Rest = 0;
     /*VIG 13.12.99*/
     /* DEPOSITR.CashRest = DEPOSITR.Sum_Rest;*/
      DEPOSITR.Final_Date = Date(0,0,0);
     /*VIG 13.12.99*/
      /*DEPOSITR.Previos_Date = Date(0,0,0);*/
      DEPOSITR.Open_Date = Date( 01, 01, CurY-1 );
      DEPOSITR.Limit_Date = date (31, 12, CurY-1);

/* RA 02.12.98 Если ставить признак закрытия счета, то проценты при закрытии
	       не начисляются
      DEPOSITR.Close_Date = DateConvert;
*/
      DEPOSITR.Close_Date = Date(0,0,0);
      DEPOSITR.UserTypeAccount = "Н";
      DEPOSITR.SvodAccount = SvodAccount;
     /*VIG 13.12.99*/
     /* DEPOSITR.UserField1 = "Переведен на неподвижный " + SvodAccount +" счета";
      */
/* RA 02.12.98
      DEPOSITR.Open_Close = "З";
*/
      DEPOSITR.Open_Close = "";
      DEPOSITR.StaticRest = 0;
      DEPOSITR.StaticPaid  = "";
      DEPOSITR.NCurLine = 0;
      DEPOSITR.Limit = 0;
/*      MayCont = GetDateDep ();*/
    end;
/*
      Проценты
*/

/* VIG не нужно вставлять никаких записей про проценты */
/*
      Запись в файлы
*/
    if (MayCont)
/*
          Проценты
*/
      DEPOSITR.DateBeginPerc = DEPOSITR.Open_Date;
      DEPOSITR.DateEndPerc   = Date( 31, 12, 2099 );
      DEPOSITR.ReceiverPerc  = DEPOSITR.Referenc;
      DEPCLNT.NumSession = -1;
      if (NOT Insert(DEPCLNT))
        ErrCode = Status(ErrText);
        PutConvMessage (M_ERROR," Для счета "+Trim(DEPOSITR.Account)+
         " не введен клиент с кодом "+String(DEPCLNT.CodClient));
        PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
        PutConvMessage (0,"      Счет "+Trim(DEPOSITR.Account)+
         " не сконвертирован.");
        MayCont = FALSE;
      end;
    end;
    if (MayCont)
      DEPOSITR.NumSession = -1;
      depositr.Number = MakeNumber (Trim (FLAROS.NOMLS), Chance);
      stat = Insert(DEPOSITR);
      if (Not stat)
      /*VIG Здесь возникает ошибка*/
      /*MsgBox (DEPOSITR.FNCash," ",DEPOSITR.GroupNumb," ",DEPOSITR.Code_Currency," ", DEPOSITR.Number," ",DEPOSITR.Type_Account);
       */
        ErrCode = Status(ErrText);
        while ( (ErrCode == 5) and (Chance < 100) and not stat )
           Chance = Chance + 1;
           stat = Insert(DEPOSITR);
           depositr.Number = MakeNumber (Trim (FLAROS.NOMLS), Chance);
        end;

        if (not stat)
           PutConvMessage (M_ERROR," Счет "+Trim(DEPOSITR.Account)+
            " не введен в базу данных (2).");
           PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
           PutConvMessage (0,"      Счет "+Trim(DEPOSITR.Account)+
            " не сконвертирован.");
           MayCont = FALSE;
        end;
      end;
    end;
    if (MayCont)
      NumStrFile = NumStrFile + 1;
    end;
    KolStrFile = KolStrFile + 1;
    KolView = KolView + 1;
    if ((KolView == PeriodView) OR (KolStrFile == 1))
      Message (" Часть 1. Обработано ",KolStrFile," записей. Сконвертировано ", NumStrFile, " записей ...");
      if (KolStrFile != 1)
        KolView = 0;
      end;
    end;
  end;
end;

/***********************************************************
   Обработка строк файла объединенных вкладов
***********************************************************/

macro WorkWithFileUnited
/*
  Цикл по записям файла объединенных вкладов
*/
  var ErrCode,
      ErrText;
  var MayCont;
  var KolView = 0;
  var SaveType = "";
  var SaveFilial ="";
  var SvodAccount ="",
      NumSvodAccount = 0;
  var Len, Account;
  var Type_Account;
  var KindInLaros;
  var NewFNcash;
  var Chance = 0;
  var stat = True;
  var CurKey = 0;

  while (Next(FLAROS))
    MayCont = TRUE;
    Chance  = 0;
/*
    Чистка буферов файлов
*/
    ClearRecord (DEPOSITR);
/*
    Формирование номера счета
*/
    MayCont = GetNewFNCash(Trim(FLAROS.KODFL),NewFNcash);  /* Номер филиала        */
    if (MayCont)
      MayCont = GetDepositrRecord(NewFNCash,Account);
    end;
    if (NOT MayCont) /* Если счет еще не заведен */
/*
      Заполнение остальных полей
*/
      DEPOSITR.Number = MakeNumber(Trim (FLAROS.NOMOS), Chance);
      DEPOSITR.Account = SetNumAccountForUnited (Trim (FLAROS.NOMOS));
      DEPOSITR.FNCash = NewFNcash;
      DEPOSITR.Referenc = FormReference();      /* Уникальный идентификатор счета */
      DEPOSITR.CodClient = 0;              /* Код вкладчика- владельца счета */
      DEPOSITR.IsCur = FlagCur;               /* 0-рубли, 1-валюта    */
      DEPOSITR.Oper = {oper};
      DEPOSITR.Type_Account = KindUnited;
      DEPOSITR.GroupNumb = DEPOSITR.Type_Account;
      DEPOSITR.Code_Currency = Currency;
      DEPOSITR.Open_Date = FLAROS.Datav;
/*      DEPOSITR.S_NumAccounts = Int(FLAROS.Ostat)*100; *//* Фиктивное число (копейки) */
      DEPOSITR.S_NumAccounts = 9999; /* Фиктивное число (копейки) */
      KindInLaros = GetTypeAccountForUnited (FLAROS.NOMOS);
      MayCont = GetRsKindForLaros(KindInLaros,"","","","",DEPOSITR.PrevAccount);
      if (NOT MayCont)
        PutConvMessage (M_WARN, "Для счета "+Trim(FLAROS.NOMLS)+
         " не определен RS-вид вклада. Счет не сконвертирован.");
      end;
      if (MayCont)
        DEPOSITR.NumSession = -1;
        stat = Insert(DEPOSITR);
        if (Not stat)
          ErrCode = Status(ErrText);
          if (ErrCode == 5)
             CurKey = KeyNum (depositr);
             Rewind      (depositr   );
             KeyNum      (depositr, 7);
             stat = GetEQ (depositr);
             KeyNum (depositr, CurKey);
             while ( (ErrCode == 5) and (Chance < 100) and not stat )
                Chance = Chance + 1;
                stat = Insert(DEPOSITR);
                depositr.Number = MakeNumber (Trim (FLAROS.NOMOS), Chance);
             end;
          end;
          if (not stat)
             PutConvMessage (M_ERROR," Счет "+Trim(DEPOSITR.Account)+
              " не введен в базу данных (1).");
             PutConvMessage (0,"      Код ошибки "+String(ErrCode)+", "+ErrText);
             PutConvMessage (0,"      Счет "+Trim(DEPOSITR.Account)+
              " не сконвертирован.");
          end;
        end;
      end;
    end;
    if (MayCont)
      NumStrFile = NumStrFile + 1;
    end;
    KolStrFile = KolStrFile + 1;
    KolView = KolView + 1;
    if ((KolView == PeriodView) OR (KolStrFile == 1))
      Message (" Часть 1. Обработано ",KolStrFile," записей. Сконвертировано ", NumStrFile, " записей ...");
      if (KolStrFile != 1)
        KolView = 0;
      end;
    end;
  end;
end;

/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro Convert_1 (AutoW)
/*
  Головной макрос
*/
  var NumFile;
  var MayCont;

  AutoWork = AutoW;
/*
  Ввод параметров с клавиатуры
*/
  MayCont = GetDateConvert (2);

  if (MayCont)
/*
  Заголовок отчета
*/
    if (NOT AutoWork)
      if (GetTrue(TRUE,"Очистить файл протокола?"))
        SetOutput(NameLogFile,False);
        SetOutput();
      end;
    end;
    Начало_Конец_отчета ();
    PutConvMessage (M_TITLE, TitleString +
     "|Часть 1||Конвертирование счетов и вкладчиков (без операций)|"+
               "----------------------------------------------|");
/*VIG - закоментировал 13.12.99*/
/*
    Параметры
*/
/*    ReferencNumb = GetMaxReferencNumb();
    LastMaxReferencNumb = ReferencNumb;
*/
/*
    Конвертирование неподвижных вкладов
*/
    FileNameLaros = DirLaros + FileNameStatic ;
    PutConvMessage (0, "");
    PutConvMessage (M_MESSAGE, " Начата обработка файла неподвижных вкладов"+FileNameLaros);
    if (not(Open(FLAROS,FileNameLaros)))
      PutConvMessage (M_VERYERROR,
       "Невозможно открыть файл "+FileNameLaros);
      if (NOT AutoWork)
        MsgBox("Невозможно открыть файл "+FileNameLaros);
      end;
    else
      WorkWithFileStatic ();
    end;
    PutConvMessage (M_MESSAGE, " Промежуточный итог: сконвертировано "+NumStrFile+" записей базы Laros");
    PutConvMessage (M_MESSAGE, " Закончена обработка файла неподвижных вкладов ");
    Close (FLAROS);

/*закоментировал VIG 13.12.99*/
/*
    Параметры
*/
/*    ReferencNumb = GetMaxReferencNumb();
    LastMaxReferencNumb = ReferencNumb;
*/
/*
    Конвертирование объединенных вкладов
*/
    FileNameLaros = DirLaros + FileNameUnited ;
    PutConvMessage (0, "");
    PutConvMessage (M_MESSAGE, " Начата обработка файла объединенных вкладов "+FileNameLaros);
    if (not(Open(FLAROS,FileNameLaros)))
      PutConvMessage (M_VERYERROR,
       "Невозможно открыть файл "+FileNameLaros);
      if (NOT AutoWork)
        MsgBox("Невозможно открыть файл "+FileNameLaros);
      end;
    else
       WorkWithFileUnited ();
    end;
    PutConvMessage (M_MESSAGE, " Промежуточный итог: сконвертировано "+NumStrFile+" записей базы Laros");
    PutConvMessage (M_MESSAGE, " Закончена обработка файла объединенных вкладов ");
    Close (FLAROS);

/*
    Просмотр протокола
*/
    PutConvMessage (M_TITLE, "||Обработано "+KolStrFile+" записей базы Laros|"+
     "Сконвертировано "+NumStrFile+" записей базы Laros");
    Начало_Конец_отчета ();
    Open(LogFile,NameLogFile);
    Close (LogFile);
    if (NOT AutoWork)
      ViewFile (LogFile);
    end;
  else
    [ Отказались от конвертирования ];
  end;
end;
