/*
  RS-Retail
  Конвертирование данных из Laros в RS-Retail
  1999 год

  Конвертирование данных по операциям в текстовые файлы, а также выгрузка
  информации по счетам за "критическую дату"
  (файлы sXXb.dbf, sXXd.dbt)

  Рыжиков Александр Александрович
  25.02.99
*/
/*import "comm_fc.mac";*/
import "oper_cur_fc.mac";
import "opercode.mac";

/*
  Переменные
*/

var DatePrevCalc = NullDate;
var DateNextCalc = NullDate;
var CriticalDate = NullDate;

/*
   Поиск записи в pc__calc.dbt
*/
macro GetPcCalc
(
  CalcDate,
  Object_Type,
  Type          /* Тип поиска: 0 - ищем для заданной даты документа,
                               1 - ищем первую запись,
                               2 - ищем послед запись,
                               3 - для заданной даты ищем предыдущий расчет */
)
   var stat = True;

   Keynum     ( pc__calc, 0 );
   ClearRecord( pc__calc    );
   rewind     ( pc__calc    );

   pc__calc.Referenc    = depositr.Referenc;
   pc__calc.ObjectType  = Object_Type;
   pc__calc.TypeRecord  = 1;
   pc__calc.EndDate     = CalcDate;

   if(   Type < 2 ) /* поиск записи о расчете для заданной даты */
      pc__calc.EndDate = CalcDate;
      if( Type == 1 )
         pc__calc.EndDate = date( 0, 0, 0 );
      end;
    stat = ( GetGE ( pc__calc ) and ( pc__calc.Referenc   == depositr.Referenc ) and
                                    ( pc__calc.ObjectType == Object_Type       ) and
                                    ( pc__calc.TypeRecord == 1                 )     );
   elif( Type == 2 ) /* последняя запись */
      pc__calc.EndDate = VeryBigDate;
      stat = ( GetLE ( pc__calc ) and ( pc__calc.Referenc   == depositr.Referenc ) and
                                      ( pc__calc.ObjectType == Object_Type       ) and
                                      ( pc__calc.TypeRecord == 1                 )     );
   elif( Type == 3 ) /* предыдущий расчет */
      pc__calc.EndDate = CalcDate;
      stat = ( GetLE ( pc__calc ) and ( pc__calc.Referenc   == depositr.Referenc ) and
                                      ( pc__calc.ObjectType == Object_Type       ) and
                                      ( pc__calc.TypeRecord == 1                 )     );
   end;
   return stat;
end;

macro GetApplTpForObject( IsCur, Type_Account, Type_Object )
   var stat  = True;
   var Found = False;

   keynum     ( pc_apltp, 0 );
   ClearRecord( pc_apltp    );
   rewind     ( pc_apltp    );

   pc_apltp.IsCur   = IsCur;
   pc_apltp.TypeRec = 1003;
   pc_apltp.Type    = Type_Account;

   stat = GetGE( pc_apltp );

   while( stat and ( pc_apltp.IsCur   == IsCur         ) and
                   ( pc_apltp.TypeRec == 1003          ) and
                   ( pc_apltp.Type    == Type_Account  )     )
      if( pc_apltp.ApplType == Type_Object )
         Found = True;
      end;
      if ( not Found ) stat = next( pc_apltp );
      else             stat = False;
      end;
   end;

   return Found;
end;

macro IsIntresting( Kind )
   return ( Kind == "Ср.1мес.выпл" );
end;

/* Получение даты начала и конца периода расчета */
macro SetCalcDates()
   var stat = True;

   WorkWithDate( DateConvert, PC_ALG.GrafCalc, PC_ALG.DayCalc, DateNextCalc );
   /*println( depositr.Account, " " , DateConvert, " " , DateNextCalc );*/

   if( DateNextCalc != NullDate )
      /* Определили дату следующего причисления */
      WorkWithDateBack( DateNextCalc,
                        PC_ALG.GrafCalc,
                        PC_ALG.DayCalc,
                        DatePrevCalc );
      if( DatePrevCalc == NullDate )
         Protocol.Error("Для счета ", depositr.Account,
                                  "не определена дата начала периода расчета" );
         stat = False;
      end;
      if( IsIntresting( Trim( depositr.Type_Account ) ) ) /* Пока для срочного с ежемесячной выплатой */
         /* Определили дату следующего причисления */

         WorkWithDateBack( DatePrevCalc,
                           PC_ALG.GrafCalc,
                           PC_ALG.DayCalc,
                           DatePrevCalc );
         if( DatePrevCalc == NullDate )
            Protocol.Error( "Для \"интересного\" вида вклада", sb_dtyp.Kind,
                            " и счета ", depositr.Account,
                            "не определена дата начала периода расчета" );
            stat = False;
         end;
      end;
   else
      Protocol.Error( "Для счета ", depositr.Account,
                      "не определена дата окончания периода расчета" );
      stat = False;

   end;
   /*println( DatePrevCalc, " ", DateNextCalc );*/


   return stat;
end;

/* Получение "Критической" даты по счету */
macro GetCriticalDate( Object )
    var stat     = True;
    var Found    = True;
    var NextCalc     = NullDate;
    var FormContr    = sb_dtyp.FormContr;


    if( Object != 2001 )
       stat = GetApplTpForObject( depositr.IsCur,
                                  depositr.Type_Account,
                                  Object );
    else
       stat = True;
    end;

    if( stat )
       /* ищем запись в sb_dtype для связки Object_Type -- ApType */
       stat = GetPc_ALgRec( depositr.IsCur, pc_apltp.ApType, DateConvert );
       if( not stat )
          Protocol.Error( "Не найден алгоритм расчета на", DateConvert,
                          "по вкладу", pc_apltp.ApType );
       end;
    end;

    if( stat )
       if  ( FormContr ==  1 ) /* для срочных                  */
          if( depositr.End_DateDep != NullDate )
             if( PC_ALG.GrafCalc != 0 ) /* задан график расчета процентов */
                 stat = SetCalcDates();
             else
                DateNextCalc = depositr.End_DateDep;
                DatePrevCalc = depositr.Start_DateDep;
             end;
          else
             stat = SetCalcDates();
          end;
       elif( FormContr == 20 ) /* для депозитов                */
          if( Object == 2001 )
             if( PC_ALG.GrafCalc != 0 ) /* задан график расчета процентов */
                 stat = SetCalcDates();
             else
                DateNextCalc = depositr.End_DateDep;
                DatePrevCalc = depositr.Start_DateDep;
             end;
          else
             stat = SetCalcDates();
          end;
       elif( FormContr == 30 ) /* для пролонгируемых депозитов */
          DateNextCalc = depositr.End_DateDep;
          if( depositr.Prol_DateDep != NullDate )
             DatePrevCalc = depositr.Prol_DateDep  - 1;
          else
             DatePrevCalc = depositr.Start_DateDep;
          end;
       else                    /* для недоговоров              */
          stat = SetCalcDates();
       end;
    end;
    if( stat )
       /*CriticalDate = DatePrevCalc + 1;*/
       CriticalDate = DatePrevCalc;
       if( CriticalDate < depositr.Open_Date )
          CriticalDate = depositr.Open_Date;
       end;
    end;

    if( CriticalDate == depositr.Open_Date )
       CriticalDate = NullDate;
    end;

    /*[ #                           ############  # ]( depositr.Account, CriticalDate, Object );*/
    return stat;
end;

macro OutDepositr()
   var stat = True;

   if( IsLock_Gate and ( depositr.Action == 1 ) )
      return stat;
   end;

   depositr.Sum_Rest   = extrn_doc( DOC_REST );

   if( depositr.Open_Close == "З" )
      if( IsLock_Gate )
         /* Закрыть счет должен сам Retail */
         depositr.Open_Close = "";
         depositr.Close_Date = NullDate;
      else
         /* дату закрытия делаем равной дате последней операции */
         depositr.Close_Date = extrn_doc( DOC_DEP_DATE );
         depositr.Limit_Date = depositr.Close_Date;
         /*println( depositr.Limit_Date, " ", depositr.Close_Date );*/
      end;
   end;

   extrn_dep( DEP_TYPE_ACCOUNT    ) = depositr.Type_Account;
   extrn_dep( DEP_ACCOUNT         ) = depositr.Account;
   extrn_dep( DEP_CODE_CURRENCY   ) = depositr.Code_Currency;
   extrn_dep( DEP_NUMBER          ) = depositr.Number;
   extrn_dep( DEP_GROUP_NUMB      ) = depositr.GroupNumb;
   extrn_dep( DEP_CODE_CLIENT     ) = "";
   extrn_dep( DEP_OPER            ) = depositr.Oper;
   /*extrn_dep( DEP_REST            ) = depositr.Sum_Rest;*/

   extrn_dep( DEP_OPEN_DATE       ) = depositr.Open_Date;
   extrn_dep( DEP_BEGIN_DATE      ) = depositr.Start_DateDep;
   extrn_dep( DEP_END_DATE        ) = depositr.End_DateDep;
   extrn_dep( DEP_ACTION          ) = depositr.Action;
   extrn_dep( DEP_PROL_DATE       ) = depositr.Prol_DateDep;
   extrn_dep( DEP_OPEN_CLOSED     ) = depositr.Open_Close;
   if( depositr.Limit_Date == depositr.Open_Date )
      extrn_dep( DEP_LIMIT_DATE ) = NullDate;
   else
      extrn_dep( DEP_LIMIT_DATE ) = depositr.Limit_Date;
   end;
   extrn_dep( DEP_CLOSE_DATE      ) = depositr.Close_Date;
   extrn_dep( DEP_USE_ALTERNATE   ) = depositr.UseAlternate;
   extrn_dep( DEP_DATE_BEGIN_PERC ) = depositr.DateBeginPerc;
   extrn_dep( DEP_PREV_ACCOUNT    ) = depositr.PrevAccount;
   extrn_dep( DEP_GIVE_BOOK       ) = depositr.GiveBook;
   extrn_dep( DEP_FLAG_Trast      ) = depositr.FlagTrast;
   extrn_dep( DEP_FLAG_WILL       ) = depositr.FlagWill;
   extrn_dep( DEP_USER_TYPE_ACC   ) = Trim (depositr.UserTypeAccount);
   ext_dep.Str = MakeTxtStr( extrn_dep );

   if( not Insert( ext_dep ) )
      Protocol.Error( "По счету", depositr.Account, "не вставлена запись в текстовый файл" );
   end;

   return stat;
end;

macro OutLimitDate()
   var stat = True;

   if( depositr.Limit_Date != NullDate )
      ext_limit.Str = MakeTxtStr( extrn_limit );
      if( not Insert( ext_limit ) )
         Protocol.Error( "По счету", depositr.Account, "не вставлена запись за критическую",
                          extrn_limit( LIMIT_DATE ) );
      end;
   end;
   return stat;
end;

macro FillRepArrays()
   var stat = True;

   /* Найдем соответсвующую запись во временном файле отчета */
   keynum     ( report, 0 );
   rewind     ( report    );
   ClearRecord( report    );

   report.FNCash  = Branch;
   report.IsCur   = FlagCur;
   report.Kind    = depositr.Type_Account;
   report.CodCur  = depositr.Code_Currency;

   stat = ( GetGE( report) and ( report.FNCash  == Branch                  ) and
                               ( report.IsCur   == FlagCur                 ) and
                               ( report.Kind    == depositr.Type_Account   ) and
                               ( report.CodCur  == depositr.Code_Currency  )     );
   if( stat )
      /* нашли запись */
      report.Rest   = report.Rest + depositr.Sum_Rest;
      report.CodCur = depositr.Code_Currency;

      if( depositr.Open_Close != "З" )
         report.ColOpenedAcc = report.ColOpenedAcc + 1;
      else
         report.ColClosedAcc = report.ColClosedAcc + 1;
      end;

      report.ColAccounts = report.ColOpenedAcc + report.ColClosedAcc;

      if( not Update( report ) )
         Protocol.BtrError( "Не обновлена запись в файлк отчета по вкладу", report.Kind,
                            " код валюты ", report.CodCur );

      end;
   else
      /* Занесем новый элемент */
      ClearRecord( report );

      report.FNCash  = Branch;
      report.IsCur   = FlagCur;
      report.CodCur  = depositr.Code_Currency;
      report.Kind    = depositr.Type_Account;
      report.Rest    = depositr.Sum_Rest;

      if( depositr.Open_Close != "З" )
         report.ColOpenedAcc = 1;
      else
         report.ColClosedAcc = 1;
      end;
      report.ColAccounts = 1;

      if( not Insert( report ) )
         Protocol.BtrError( "Не вставлена запись в файл отчета по вкладу", report.Kind,
                            " код валюты ", report.CodCur );
      end;
   end;
end;

macro FillRep_Gate( KindOp )
   var stat = True;

   /* Найдем соответсвующую запись во временном файле отчета */
   keynum     ( report, 1 );
   rewind     ( report    );
   ClearRecord( report    );

   report.FNCash  = Branch;
   report.CodCur  = depositr.Code_currency;
/*   Protocol.Message (Flaros.Datav," ",extrn_doc( DOC_DATE )," ",extrn_doc( DOC_DEP_DATE ) );*/
   report.Date    = extrn_doc( DOC_DATE );/* Исправлено Головановым В.В. */
   report.Kind    = depositr.Type_Account;
   report.FlagRez = 0;

   stat = GetEQ( report);

   if( stat )
      /* нашли запись */
      if( KindOp == D_OUT )
         report.DebSum  = report.DebSum + extrn_doc( DOC_OUT_SUM  );
      else
         report.KredSum = report.KredSum + extrn_doc( DOC_IN_SUM  );
      end;
      if( not Update( report ) )
         Protocol.BtrError( "Не обновлена запись в файлк отчета по вкладу", report.Kind,
                            "за", report.Kind );
      end;
   else
      /* Занесем новый элемент */
      ClearRecord( report );

      report.FNCash  = Branch;
      report.CodCur  = depositr.Code_currency;
      report.Date    = extrn_doc( DOC_DATE ); /*Исправлено Головановым В.В.*/
      report.Kind    = depositr.Type_Account;
      report.FlagRez = 0;

      if( KindOp == D_OUT )
         report.DebSum  = extrn_doc( DOC_OUT_SUM  );
      else
         report.KredSum = extrn_doc( DOC_IN_SUM  );
      end;

      if( not Insert( report ) )
         Protocol.BtrError( "Не вставлена запись в файлк отчета по вкладу", report.Kind,
                            "за", report.Kind );
      end;
   end;
end;

/* Проверка - нужно ли конвертировать операцию */
macro IsNeedConvert()
   var stat = True;

   if( IsLock_Gate )
      stat = ( ( CheckKindSbereg() ) or
               ( ( CountDate >= BegLockDate ) and ( CountDate <= EndLockDate ) ) );
   end;
   if( stat )
      stat = ( ( FLAROS.DATAV != date( 0, 0, 0 ) ) AND
               ( FLAROS.OPER  != "NAL"           ) AND    /* Откидываем служебную запись  */
               ( FLAROS.OPER  != "908"           ) AND    /* Откидываем служебную запись  */
               ( FLAROS.OPER  != "254"           ) AND
               ( ( FLAROS.OPER  < "191" ) OR
                 ( FLAROS.OPER  > "199" )        )     )/* Откидываем служебную запись  */
   end;

   return stat;
end;

/* Проверка закрытых счетов для конвертера */
macro CheckCloseAcc()
   var stat = True;

   if( not Islock_Gate )  stat = ( depositr.Open_Close != "З" ); end;

   return stat;
end;

/* проверка на отношение счета к срочным-пенсионным */
macro IsSrochnPens()
   var stat = True;

   stat = ( ( Trim( depositr.Type_Account ) == "Ср.пенсион."  ) or
            ( Trim( depositr.Type_Account ) == "Ср.пенс.6мес" )    );
   return stat;
end;

macro WorkWithFileLarosOper
/*
  Цикл по записям файла
*/
   var KindInLaros;
   var KindInLarosAcc;
   var MayCont;
   var GrOper;
   var YearOp, MonOp, DayOp;
   var OpenYear;
   var Found         = False;
   var ForFisrtDate  = False;
   var NextAccount   = False;
   var WasWork       = False;
   var WasOpenOper   = False;
   var NeedOutStr    = True;

   var Account       = "";
   var ComplexDate   = NullDate;
   var DOC_KINDOP    = 0;
   var ApplType      = 0;
   var NumDayDoc     = 0;
   var Summa253      = $0;
   var Data253       = Nulldate;
   var SaveDateDoc   = NullDate;

   var Currency,
       Nbals,
       Number;


   Nrec = Nrec + NRecords( FLAROS );
   if( IsLock_Gate )
      InitProgress( Nrec, "", "Выгрузка данных об операциях" );
   else
      InitProgress( Nrec, "", "Конвертация операций" );
   end;

   while( Next( FLAROS ) )
      MayCont    = TRUE;
      NeedOutStr = True;
/*
      if( IsNeedConvert() )
         println( Flaros.Nbals, " ", Flaros.Nomls, " ", Flaros.Datav, " ", Flaros.Oper );
      end;
 */

      if( Trim( FLAROS.NOMLS ) != Account )

         /* Дообработка предыдущего счета, если он обработан без ошибок */
         if( NextAccount )
            MayCont = OutDepositr();
            if( MayCont )
               if( ( not IsLock_Gate ) and ( depositr.Open_Close != "З" ) )
                  MayCont = OutLimitDate();
                  if( not MayCont )
                     Protocol.Error( "Не выгружены данные за критическую дату по счету", depositr.Account );
                  end;
               end;
            else
               Protocol.Error( "ошибка при выгрузке счета", depositr.Account );
            end;
            if( Maycont )
               if( not IsLock_Gate ) FillRepArrays(); end;
            end;
         end;

         /* Новый счет */
         Account      = Trim( FLAROS.NOMLS );
         NextAccount  = False;
         ForFisrtDate = False;
         SaveDateDoc  = NullDate;
         NumDayDoc    = 0;
         WasWork      = False;
         WasOpenOper  = False;

         KeyNum( depositr, 7 );
         rewind( depositr    );
         ClearRecord( depositr );

         depositr.FNCash  = Branch;
         depositr.Account = Account;
         /*MsgBox( Account, " ", Nbals, " ", Number);*/

         if( GetEQ( depositr ) )
            KindInLaros    = depositr.StaticPaid;
            KindInLarosAcc = GetTypeAccountInLaros_currency( FLAROS.NOMLS, NBals, Currency, Number );
            /* Если счет есть в пром.файле- конвертируем его */
/*            NextAccount = ( ( NeedConvertType( KindInLaros ) ) and
                            ( depositr.Open_Close != "З"     )     ); /* Если конвертируем в данном сеансе */*/
            NextAccount = NeedConvertType( KindInLaros, depositr.Number ); /* Если конвертируем в данном сеансе */
            MayCont     = NextAccount;
            if( MayCont )
              /* Поиск записи вида вклада для основных условий */
               MayCont = GetPC_APLTP( depositr.IsCur, depositr.Type_Account, 0 );
               if( NOT MayCont )
                  Protocol.Error( "Для счета ", Trim( FLAROS.NOMLS ),
                                  "не найден вид вклада \"", pc_apltp.ApType,
                                  "\"основных условий для вклада \"",Trim( DEPOSITR.Type_Account ),
                                  "\". Счет не сконвертирован." );
               else

                  MayCont = GetTypeRecord( pc_apltp.ApType, pc_apltp.IsCur );
                  if( NOT MayCont )
                     Protocol.Error( "Для счета ", Trim( DEPOSITR.Account ),
                                     "не найдена запись о виде вклада ", Trim( DEPOSITR.Type_Account ) );
                  end;
               end;
/*
               Группа по операциям
*/
               if( MayCont )
                  GrOper = GroupOperInLaros_currency( KindInLarosAcc );
                  if( GrOper == 0 )
                     Protocol.Error( " При конвертации счета ", depositr.Account,
                                     " вид вклада ", depositr.Type_Account,"(", KindInLarosAcc ,")",
                                     " не отнесен к группе операций");
                     MayCont = FALSE;
                  end;
               end;
               if( not IsLock_Gate )

                  if( MayCont )
                     if( depositr.UseAlternate )
                        MayCont = GetCriticalDate( 2002 );
                     else
                        MayCont = GetCriticalDate( 2001 );
                     end;
                     if( not MayCont )
                        Protocol.Error( "Не определена критическая дата по счету", depositr.Account );
                     end;
                  end;
                  if( MayCont )
                     /* Заносим данные по умолчанию */
                     extrn_limit( LIMIT_ACCOUNT              ) = depositr.Account;
                     extrn_limit( LIMIT_DATE                 ) = CriticalDate;
                     extrn_limit( LIMIT_SUM_CALC_ALL         ) = $0;
                     extrn_limit( LIMIT_SUM_PAY_ALL          ) = $0;
                     extrn_limit( LIMIT_SUM_CALC_NO_PAY_2001 ) = $0;
                     extrn_limit( LIMIT_SUM_CALC_NO_PAY_2002 ) = $0;
                     extrn_limit( LIMIT_FORWARD_PERCENT      ) = $0;
                     extrn_limit( LIMIT_SUM_PERC_OUT         ) = $0;
                     extrn_limit( LIMIT_SUM_COMPENS_IN       ) = $0;
                     extrn_limit( LIMIT_SUM_COMPENS_OUT      ) = $0;
                     extrn_limit( LIMIT_REST                 ) = $0;
                     extrn_limit( LIMIT_DATE_NEXT_CALC       ) = NullDate;
                     depositr.Limit_Date                       = extrn_limit( LIMIT_DATE );
                  end;
               end;
            end; /* fi первый Maycont */
         else
            if( NeedConvertType( KindInLaros, Number ) and not Islock_Gate )
              Protocol.Error( "Для счета", Account, "не найдено записи в файле счетов" );
            end;
         end;
      end; /* fi для нового счета */
/*
      ОБРАБОТКА ОПЕРАЦИИ
*/
      if( NextAccount AND IsNeedConvert() )
          if( MayCont AND
              ( ( FLAROS.OPER == "145" ) AND ( DEPOSITR.UseAlternate == 1 ) AND
                ( SB_DTYP.FormContr == 30                                 )     ) OR    /* Для пролонгируемых */
              ( ( FLAROS.OPER == "777" ) AND ( FLAROS.NOPER == 1          ) AND
                ( DEPOSITR.Type_Account == KindPension                    )     )     ) /* Для пенсионных */
              DEPOSITR.UseAlternate = 0;
          end;

          if( MayCont AND ( KindInLaros != "Г" ) )
             if( FLAROS.DATAV < DEPOSITR.Open_Date )
                Protocol.Error( " По счету ", Trim( FLAROS.NOMLS ),
                                " есть операция с датой, меньшей даты открытия" );
             end;
          end;

          if( MayCont )
/*
             Заполнение полей для выгрузки в текстовый файл операций
*/
             extrn_doc( DOC_ACCOUNT      ) = DEPOSITR.Account;
             extrn_doc( DOC_TYPE_ACCOUNT ) = DEPOSITR.Type_Account;
             extrn_doc( DOC_DATE         ) = FLAROS.DATAV;
             extrn_doc( DOC_IN_SUM       ) = $0;
             extrn_doc( DOC_OUT_SUM      ) = $0;

             MayCont = GetNumOper_currency( FLAROS.OPER,
                                            DOC_KINDOP,
                                            extrn_doc( DOC_TYPE_OPER         ),
                                            extrn_doc( DOC_TYPE_COMPLEX_OPER ),
                                            extrn_doc( DOC_APPL_TYPE         ),
                                            extrn_doc( DOC_GROUND            ),
                                            extrn_doc( DOC_VID               ) );


             if( not MayCont )
               Protocol.Error(" Для счета ", depositr.Account,
                              " не найдена операция ",String( FLAROS.OPER )," в списке" );

             end;
          end;

          if( MayCont )
             if  ( DOC_KINDOP == D_IN  )
               extrn_doc( DOC_IN_SUM  ) = Money( Double( FLAROS.SUMMA ) * 100 );
             elif( DOC_KINDOP == D_OUT )
               extrn_doc( DOC_OUT_SUM ) = Money( Double( FLAROS.SUMMA ) * 100 );
             else
               MayCont = FALSE;
               Protocol.Error(" Для операции ", String( FLAROS.OPER ),
                              " в списке не определен вид: дебет или кредит");
             end;
             if( ( extrn_doc( DOC_VID ) == 1 ) and ( depositr.GiveBook == "" ) )
                depositr.Givebook = "X";
             end;

             extrn_doc( DOC_DEP_DATE     ) = extrn_doc( DOC_DATE );
             if( MustDecode )
                extrn_doc( DOC_REST      ) = Money( STOD( "OSTAT", GetPos( FLAROS ) ) * 100 );
             else
                extrn_doc( DOC_REST      ) = Money( Double( FLAROS.OSTAT ) * 100 );
             end;
             extrn_doc( DOC_CODE_CLIENT  ) = "";
             extrn_doc( DOC_PERC_OPR_SUM ) = $0;
             extrn_doc( DOC_PERC_REST    ) = $0;
             extrn_doc( DOC_LINK_FLAG    ) = "";
               extrn_doc( DOC_IS_CONTROL ) = StrFor( 0 );
             if( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == 10 )
                if( depositr.Open_Close != "З" )
                   Protocol.Warning( "Счет", depositr.Account, "закрыт конвертером" );
                   depositr.Open_Close = "З";
                end;
                depositr.CLose_Date = extrn_doc( DOC_DEP_DATE );
             end;
          end;

          if( ( extrn_doc( DOC_TYPE_OPER ) == 1 ) or ( extrn_doc( DOC_TYPE_OPER ) == 51 ) )
              WasOpenOper = True;
          end;


          if( MayCont )
             DateSplit( FLAROS.DATAV, DayOp, MonOp, YearOp );
             if( ( DayOp == 1 ) AND ( MonOp == 1 ) )
                if( ( FLAROS.OPER == "206" ) OR ( FLAROS.OPER == "908" ) OR
                    ( FLAROS.OPER == "245" ) OR
                    ( FLAROS.OPER == "999" ) OR ( FLAROS.OPER == "  1" )    )

                   ForFisrtDate = True;
                   extrn_doc( DOC_DATE     ) = Date( 31, 12, ( YearOp - 1 ) );
                   extrn_doc( DOC_DEP_DATE ) = extrn_doc( DOC_DATE        );
                end;
             end;

             if( ( DayOp == 31 ) AND ( MonOp == 12 ) )
                if( ( FLAROS.OPER == "206" ) OR ( FLAROS.OPER == "908" ) OR
                    ( FLAROS.OPER == "245" ) OR
                    ( FLAROS.OPER == "999" ) OR ( FLAROS.OPER == "  1" )    )

/*                   println ("2 Счет ", DEPOSITR.Account, " Номер ", FLAROS.Noper,
                            " День ", DayOp, " Месяц ", MonOp,
                            " Сумма ", extrn_limit( LIMIT_SUM_CALC_ALL ));
*/
                   ForFisrtDate = True;
                   extrn_doc( DOC_DATE     ) = FLAROS.Datav;
                   extrn_doc( DOC_DEP_DATE ) = extrn_doc( DOC_DATE );
                end;
             end;

             DateSplit( extrn_doc( DOC_DEP_DATE ), DayOp, MonOp, YearOp );

             /*[ #                       #             #]
             ( depositr.Account, ForFisrtDate, WasOpenOper );*/

             if( ( sb_dtyp.FormContr     == 20 ) and ( depositr.Type_Account != KindChild ) and
                 ( depositr.UseAlternate == 0  ) )
                If( not IsLock_Gate  and ForFisrtDate and
                    not WasOpenOper )
                   DateSplit( depositr.Open_Date, Null, Null, OpenYear );
                   /*[ #           #        #]( OpenYear, Yearop, OpenYear);*/
                   if( ( OpenYear == Yearop ) and ( OpenYear == ( CurY - 1 ) ) )
                      /*[ # ]( depositr.Account );*/
                      /* Установили, что операции открытия не было,
                         а только операция зачисления % за 31.12 прошлого года */
                      ForFisrtDate = False;
                      /* Делаем операцию открытия*/
                      extrn_doc( DOC_TYPE_OPER         ) = 1;
                      extrn_doc( DOC_TYPE_COMPLEX_OPER ) = 1;

                      extrn_doc( DOC_IN_SUM   ) = extrn_doc( DOC_REST );
                      extrn_doc( DOC_DATE     ) = depositr.Open_Date;
                      extrn_doc( DOC_DEP_DATE ) = extrn_doc( DOC_DATE );
                      extrn_limit( LIMIT_DATE ) = NullDate;
                      depositr.Limit_Date       = extrn_limit( LIMIT_DATE );
                      CriticalDate              = extrn_limit( LIMIT_DATE );

                   end;
                end;
             end;

             if( depositr.FormContr == 30 )
                /* Операция зачисления процентов за дату пролонгации должна
                   быть в карточке на день раньше */
                if( ( extrn_doc( DOC_TYPE_OPER ) == 72                    ) and
                    ( extrn_doc( DOC_DEP_DATE  ) == depositr.Prol_DateDep ) )
                   extrn_doc( DOC_DEP_DATE ) = extrn_doc( DOC_DEP_DATE ) - 1;
                   if( IsLock_Gate )
                      /* зададим правильно дату учета в опердневнике */
                      /*Исправлено Головановым В.В.*/
/*
                      while( IsHoliday( extrn_doc( DOC_DATE ) ) )
                         extrn_doc( DOC_DATE ) = extrn_doc( DOC_DATE ) + 1;
                      end;
 */
                   end;
                   extrn_doc( DOC_TYPE_OPER )         = 82;
                   extrn_doc( DOC_TYPE_COMPLEX_OPER ) = extrn_doc( DOC_TYPE_OPER );
                end;
             elif( depositr.FormContr == 20 )
                if( ( extrn_doc( DOC_TYPE_OPER ) == 72                                  ) and
                    ( ( extrn_doc( DOC_DEP_DATE  ) == ( depositr.End_DateDep + 1 ) ) or
                      ( extrn_doc( DOC_DEP_DATE  ) ==   depositr.End_DateDep       )    ) )
                    extrn_doc( DOC_TYPE_OPER )         = 82;
                    extrn_doc( DOC_TYPE_COMPLEX_OPER ) = extrn_doc( DOC_TYPE_OPER );
                    if( extrn_doc( DOC_DEP_DATE  ) == ( depositr.End_DateDep + 1 ) )
                       extrn_doc( DOC_DEP_DATE ) = extrn_doc( DOC_DEP_DATE ) - 1;
                    end;
                end;
             end;

             if( SaveDateDoc != extrn_doc( DOC_DATE ) )
                SaveDateDoc = extrn_doc( DOC_DATE );
                NumDayDoc   = 0;
             end;
             NumDayDoc = NumDayDoc + 1; /* Увеличим счетчик документов за день */
             extrn_doc( DOC_NUM_DAY_DOC ) = NumDayDoc * 100;

             if( CheckCloseAcc() ) /* Данные за критическую дату заносим только для открытых */

                if( not IsLock_Gate and ( extrn_doc( DOC_DEP_DATE ) <= CriticalDate ) )
                   /* Операции до "Критической даты" */

                   if(   GrOper == 1 ) /* Срочные         */
                      if( ForFisrtDate )
                         ForFisrtDate = False;
                         extrn_limit( LIMIT_SUM_CALC_ALL ) = Money( Double( FLAROS.OPROC ) * 100 );
                         extrn_limit( LIMIT_SUM_PAY_ALL  ) = Money( Double( FLAROS.OPROC ) * 100 );
                      end;
                   elif( GrOper == 2 ) /* Депозиты        */
                      if( ForFisrtDate )
                         ForFisrtDate = False;
                         extrn_limit( LIMIT_SUM_CALC_ALL ) = Money( Double( FLAROS.OPROC ) * 100 );
                         extrn_limit( LIMIT_SUM_PAY_ALL  ) = Money( Double( FLAROS.OPROC ) * 100 );
                      end;
                   elif( GrOper == 3 ) /* До востребоваия */
                      ;
                   elif( GrOper == 4 ) /* Срочн с ежемес. выплатой */
                      if( ForFisrtDate )
                         ForFisrtDate = False;
                         if (FLAROS.Oper == 206)
                            extrn_limit( LIMIT_SUM_CALC_ALL ) = Money( Double( FLAROS.OPROC ) * 100 ) - Money( Double( FLAROS.SUMMA ) * 100 );
                            extrn_limit( LIMIT_SUM_PAY_ALL  ) = extrn_limit( LIMIT_SUM_CALC_ALL );
                         else
                            extrn_limit( LIMIT_SUM_CALC_ALL ) = Money( Double( FLAROS.OPROC ) * 100 );
                            extrn_limit( LIMIT_SUM_PAY_ALL  ) = extrn_limit( LIMIT_SUM_CALC_ALL );
                         end;
/*                   println ("3 Счет ", DEPOSITR.Account, " Номер ", FLAROS.Noper,
                            " День ", DayOp, " Месяц ", MonOp,
                            " Сумма ", extrn_limit( LIMIT_SUM_CALC_ALL ));*/
                      end;
                   elif( GrOper == 5 ) /* Детские         */
                      if( ForFisrtDate )
                         ForFisrtDate = False;
                         if(   Flaros.OPER == "206" )
                            extrn_limit( LIMIT_SUM_CALC_NO_PAY_2001 ) = Money( Double( FLAROS.OPROC ) * 100 );
                            extrn_doc( DOC_IN_SUM  ) = $0.0;
                         elif( Flaros.OPER == "  1" )
                            extrn_limit( LIMIT_SUM_CALC_NO_PAY_2002 ) = Money( Double( FLAROS.OPROC ) * 100 );
                            extrn_doc( DOC_IN_SUM  ) = $0.0;
                         end;
                      end;
                   else
                       ;
                   end;

                   extrn_limit( LIMIT_REST         ) = extrn_doc( DOC_REST );
                   /*println( depositr.Account, " ", extrn_doc( DOC_TYPE_OPER ), " ", extrn_limit( LIMIT_SUM_CALC_ALL ) );*/
                   /* Обработка зачисленных и выданных процентов для пролонгируемых, и для тех,
                      у которых график причисления не в конце года */

                    /* Если конец года - не надо прибавлять
                       сумму операции, т.к. она учтена в OPROC*/
                   if( ( depositr.Formcontr == 30 ) or ( PC_ALG.GrafCalc != 4 ) )
                      if( ( ( extrn_doc( DOC_TYPE_OPER ) == 72 ) or
                            ( extrn_doc( DOC_TYPE_OPER ) == 82 ) )
                          and (FLAROS.Oper != 208) )
                         /* Операция причисления процентов увеличивает сумму рассчитанных % */
                         extrn_limit( LIMIT_SUM_CALC_ALL ) = extrn_limit( LIMIT_SUM_CALC_ALL ) + extrn_doc( DOC_IN_SUM );
/*                   println ("4 Счет ", DEPOSITR.Account, " Номер ", FLAROS.Noper,
                            " День ", DayOp, " Месяц ", MonOp,
                            " Сумма ", extrn_limit( LIMIT_SUM_CALC_ALL ));*/
/*                                                             DenomSum( extrn_doc( DOC_IN_SUM    ),
                                                                       extrn_doc( DOC_DEP_DATE      ),
                                                                       extrn_doc( DOC_TYPE_OPER ) );*/
                      elif(   ( extrn_doc( DOC_TYPE_OPER ) ==  6 )
/* Изменения от VIG */
                           or ( (FLAROS.Oper == 243) and (FLAROS.Tipz == "n-$") )
                           or ( (FLAROS.Oper == 243) and (FLAROS.Tipz == "1ВВ") ) )
                         /* Операция выдачи процентов уменьшает сумму рассчитанных % */
                         extrn_limit( LIMIT_SUM_CALC_ALL ) = extrn_limit( LIMIT_SUM_CALC_ALL ) - extrn_doc( DOC_OUT_SUM );
/*                                                             DenomSum( extrn_doc( DOC_OUT_SUM   ),
                                                                       extrn_doc( DOC_DEP_DATE      ),
                                                                       extrn_doc( DOC_TYPE_OPER ) );*/
                      end;

/*                      println (" Дата ", extrn_doc( DOC_DEP_DATE ),
                               " Сумма в Retail ", extrn_limit( LIMIT_SUM_CALC_ALL ),
                               " Сумма в ЛАРОС ", FLAROS.Oproc);*/

                      extrn_limit( LIMIT_SUM_PAY_ALL ) = extrn_limit( LIMIT_SUM_CALC_ALL );
                   end;

                   /* Обработка зачисления и выдачи компенсации */
                   if  ( extrn_doc( DOC_TYPE_OPER ) == 74 )
                      extrn_limit( LIMIT_SUM_COMPENS_IN  ) = extrn_limit( LIMIT_SUM_COMPENS_IN ) + extrn_doc( DOC_IN_SUM );
/*                                                           DenomSum( extrn_doc( DOC_IN_SUM    ),
                                                                     extrn_doc( DOC_DEP_DATE      ),
                                                                     extrn_doc( DOC_TYPE_OPER ) );*/

                   elif( extrn_doc( DOC_TYPE_OPER ) == 15 )
                      extrn_limit( LIMIT_SUM_COMPENS_OUT ) = extrn_limit( LIMIT_SUM_COMPENS_OUT ) + extrn_doc( DOC_IN_SUM );
/*                                                           DenomSum( extrn_doc( DOC_IN_SUM    ),
                                                                     extrn_doc( DOC_DEP_DATE      ),
                                                                     extrn_doc( DOC_TYPE_OPER ) );*/
                   end;
                else
                   /* После:
                      1.Поднимаем флаг выгрузки операций в файл второй сессии ( только для конвертера )
                      2.Нужно связать комплексные операции и только выдачу % за
                        одну дату документа
                   */
                   if( not IsLock_Gate ) WasWork = True; end;

                 /*Включаем списание центов в состав отчисления %%*/
                   if ( (FLAROS.Oper == 243) and (FLAROS.Tipz == "n-$") )
                      extrn_doc( DOC_TYPE_OPER )         = 62;
                      extrn_doc( DOC_APPL_TYPE )         = 11;
                      extrn_doc( DOC_TYPE_COMPLEX_OPER ) = 6;
                   end;

                   if( FLAROS.Oper == "253" )
                       /* Отчисление процентов */
                       Summa253 = extrn_doc( DOC_OUT_SUM  );
                       Data253  = extrn_doc( DOC_DEP_DATE );
                       [ #                         #              #]
                       ( depositr.Account, Summa253, Data253 );
                       NeedOutStr = False; /* не обрабатывать эту операцию */
                   end;

                   if( MayCont )
                      /* Проверим код операции после критической даты */
                      Keynum( sb_typop, 0 );

                      sb_typop.IsCur    = depositr.IsCur;
                      sb_typop.Kind     = depositr.Type_Account;
                      sb_typop.NumOpert = extrn_doc( DOC_TYPE_OPER );
                      if( not GetEQ( sb_typop ) )
                         Protocol.Error( "Для вклада", depositr.Type_Account, "не найдена операция с кодом ",
                                          extrn_doc( DOC_TYPE_OPER ), "в Ларосе код операции", FLAROS.OPER );
                         Protocol.Message( "Операции по счету", depositr.Account, "после критической даты не выгружены" );
                         Maycont = False;
                      end;
                   end;
                   if( Maycont )
                      if( ( extrn_doc( DOC_TYPE_OPER ) == 72 ) or
                          ( extrn_doc( DOC_TYPE_OPER ) == 62 ) )
                         /* Определим было ли это зачисление % зачислением перед выдачей */

                         ComplexDate = extrn_doc( DOC_DEP_DATE );
                         GetPos( Flaros );
                         Found = False;
                         while( not Found and next( Flaros ) and ( flaros.Datav == Complexdate ) and
                                ( FLAROS.OPER != "206" ) )
                            /* Перебираем операции за заданную дату*/
                            if( ( FLAROS.OPER == "142" ) or ( FLAROS.OPER == "141" ) )
                               /* Нашли выдачу % */
                               Found = True;
                            elif( IsLock_Gate and ( FLAROS.OPER == "154" ) )
                               /* Нашли закрытие */
                               Found = True;
                            end;
                         end;
                         if( Found and ( FLAROS.OPER != "154" )  )
                            extrn_doc( DOC_LINK_FLAG ) = "1";
                            if( IsSrochnPens() and ( FLAROS.OPER  == "141" ) )
                               ApplType = 4;
                            end;
                            extrn_doc( DOC_TYPE_COMPLEX_OPER ) = 6;
                            extrn_doc( DOC_APPL_TYPE         ) = ApplType;
                            /*if( extrn_doc( DOC_APPL_TYPE ) == 4 )
                               extrn_doc( DOC_PERC_REST    ) = - extrn_doc( DOC_IN_SUM );
                            end;*/
                         elif( Found and ( FLAROS.OPER == "154" ) )
                            extrn_doc( DOC_LINK_FLAG         ) = "1";
                            extrn_doc( DOC_TYPE_COMPLEX_OPER ) = 10;
                         else
                            ComplexDate = NullDate;
                         end;

                         /*println( extrn_doc( DOC_ACCOUNT   ), " ",
                                  extrn_doc( DOC_LINK_FLAG ), " ",
                                  extrn_doc( DOC_DATE      ), " ",
                                  extrn_doc( DOC_TYPE_OPER )      );*/
                         GetDirect( Flaros );

                      elif ( ( extrn_doc( DOC_TYPE_OPER )         == 6 ) and
                             ( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == 6 ) )

                         ComplexDate = extrn_doc( DOC_DEP_DATE );
                         GetPos( Flaros );
                         Found = False;
                         while( not Found and next( Flaros ) and ( flaros.Datav == Complexdate ) and
                                ( FLAROS.OPER != "206" ) )
                            /* Перебираем операции за заданную дату*/
                            if( ( FLAROS.OPER == "243" ) and (FLAROS.Tipz == "n-$") )
                               /* Нашли списание валютной мелочи */
                               Found = True;
                            end;
                         end;
                         if( Found )
                            extrn_doc( DOC_LINK_FLAG ) = "1";
                         end;
                         GetDirect( Flaros );

                      elif( ComplexDate != NullDate  )
                         if( ( FLAROS.OPER == "142" ) or ( FLAROS.OPER == "141" ) )
                            /* Выдача % */
                            extrn_doc( DOC_APPL_TYPE ) = ApplType;
                            extrn_doc( DOC_LINK_FLAG ) = "";
                         elif( ( FLAROS.OPER == "154" ) )
                            extrn_doc( DOC_TYPE_COMPLEX_OPER ) = 10;
                         end;

                         if( ComplexDate == Data253 )
                            extrn_doc( DOC_OUT_SUM  ) = extrn_doc( DOC_OUT_SUM  ) + Summa253;
                         end;

                         ComplexDate = NullDate;
                         Summa253    = $0;
                         Data253     = Nulldate;
                         ApplType    = 0;
                      end;
                   end;
                   extrn_doc( DOC_IS_CONTROL ) = StrFor( 0 );
                   if( extrn_doc( DOC_TYPE_OPER ) == extrn_doc( DOC_TYPE_COMPLEX_OPER ) )
                      extrn_doc( DOC_IS_CONTROL ) = StrFor( 1 );
                   elif( ( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == Закрытие_Наличными    ) or
                         ( extrn_doc( DOC_TYPE_COMPLEX_OPER ) == Закрытие_Безналичными )   )
                      if( ( extrn_doc( DOC_TYPE_OPER ) == Расход                ) or
                          ( extrn_doc( DOC_TYPE_OPER ) == Списание_По_Раз_Поруч )    )
                         extrn_doc( DOC_IS_CONTROL ) = StrFor( 1 );
                      end;
                   end;
                end;
             end;
             extrn_doc( DOC_OPER              ) = 9999;
             extrn_doc( DOC_ACTION            ) = 0;
             extrn_doc( DOC_OBJECT_PERC       ) = 0;
          end;
/*
          Запись в файлы
*/
          if( MayCont and NeedOutStr )
             if( not WasWork )
                ext_doc.Str = MakeTxtStr( extrn_doc );
                if( not Insert( ext_doc ) )
                   Protocol.Error( "Для счета", depositr.Account,
                                   "не выгружена операция", extrn_doc( DOC_DEP_DATE ) );
                   MayCont = FALSE;
                end;
             else
                ext_doc_2.Str = MakeTxtStr( extrn_doc );
                if( not Insert( ext_doc_2 ) )
                   Protocol.Error( "Для счета", depositr.Account,
                                   "не выгружена операция", extrn_doc( DOC_DEP_DATE ) );
                   MayCont = FALSE;
                end;
             end;
          end;

          if( MayCont )
             if( IsLock_Gate ) Fillrep_Gate( DOC_KINDOP ); end;
             NumStrFile = NumStrFile + 1;
          end;
          NextAccount = MayCont;
          KolStrFile  = KolStrFile + 1;
      end;
      UseProgress( KolView = KolView + 1 );
   end; /* elihw */
   RemProgress( KolView );

   if( NextAccount )
      MayCont = OutDepositr();
      if( MayCont )
         if( ( not IsLock_Gate ) and ( depositr.Open_Close != "З" ) )
            MayCont = OutLimitDate();
            if( not MayCont )
               Protocol.Error( "Не выгружены данные за критическую дату по счету", depositr.Account );
            end;
         end;
      else
         Protocol.Error( "ошибка при выгрузке счета", depositr.Account );
      end;
      if( Maycont )
         if( not IsLock_Gate ) FillRepArrays(); end;
      end;
   end;
end;


/***********************************************************
     Головной макрос конвертирования счетов и вкладчиков
***********************************************************/

macro v_laros_fc_1_2()

   WorkWithFileLarosOper ();
   Protocol.Message( " Промежуточный итог: всего", KolView, "сконвертировано",
                     NumStrFile, "записей базы Laros" );
end;




