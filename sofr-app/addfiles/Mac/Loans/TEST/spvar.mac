/*******************************************************************************
 * RS-Loans                                                                    *
 * Programmer: PDS                                                             *
 * 30.11.11                                                                    *
 * Макрос тестирования Спецпеременных                                          *
 * F9 для запуска(а не F6 как написано в статус строке)                           *
 *******************************************************************************/

IMPORT RsbObjFactory, SbCrdInter, Total, RsbDataSet;

VAR RecHandler = TRecHandler ("t_spvar", "sbcrd.lbr",TRUE);

var fname = string({oper})+".txt";
var mindate=date(0,0,0);
PRIVATE VAR SpVar = TBFile ("SpVariable.dbt","r",0,"SpVariable.dbt","loans.def");
PRIVATE VAR SpMenu = Tarray, ObjMenu = Tarray, ObjIDArray = Tarray, _SpType = 1;

CONST SpName    = 1,
      SpType    = 2,
      SpOb      = 3,
      BeginDate = 4,
      endDate   = 5,
      rest_in   = 6,
      rest_out  = 7,
      SpVal     = 8;

CONST
   CF_spvType_Num  = 1,
   CF_spvType_Date = 2,
   CF_spvType_Str  = 3;
var rest_type=2;

MACRO isNull( str)
    IF(str == NULLVAL)             RETURN "Не определено";
    END;
    return str;
END;


MACRO ratio()
   IF (RecHandler.rec.rest_in == "X")
      RecHandler.rec.rest_in = "" ;
      RecHandler.rec.rest_out= "X";
      rest_type =2;
   ELSE
      RecHandler.rec.rest_in = "X" ;
      RecHandler.rec.rest_out= "";
      rest_type =1;
   END;
END;

MACRO FillSpName()
   VAR RS = CRSDCommand("SELECT T_SPVARIABLENAME FROM DSPVARIABLE_DBT WHERE T_FLAGPAYM <> 'X' ORDER BY T_SPVARIABLEID", V_GENOBJ);

   WHILE (RS.MoveNext())
      SpMenu(SpMenu.Size) = RS.Value(0);
   END;
END;

MACRO FillObj()
   VAR RS = CRSDCommand("SELECT T_OBJECTID, T_SHORTNAME, T_OBJECTNAME FROM DLOBJECT_DBT ORDER BY T_OBJECTID", V_GENOBJ);

   WHILE (RS.MoveNext())
      ObjMenu(ObjMenu.Size) = RS.Value(0) + "  " + RS.Value(2);
      ObjIDArray(ObjIDArray.Size) = RS.Value(0);
   END;
END;

MACRO Start()
   var ret = 0;
   var result =0;   
   VAR RSDcmd;
   SpVar.AddFilter("T_SPVARIABLENAME = '" + RecHandler.rec.SpName + "'");
   IF (SpVar.next)   
      if (RecHandler.rec.EndDate == mindate)
          RSDcmd = RsdCommand(RslDefCon, "begin ? := RSO_Loans_SpVariable.GetSPVariable(?,?,?,?,?,?); end;");
          RSDcmd.addParam("p_retval",    RSDBP_OUT, V_NUMERIC);    
          IF(SpVar.rec.Type==CF_spvType_Num)
              RSDcmd.addParam("p1",          RSDBP_OUT ,V_NUMERIC);
         ELIF(SpVar.rec.Type==CF_spvType_Date)
              RSDcmd.addParam("p1",          RSDBP_OUT ,V_DATE);   
          ELIF(SpVar.rec.Type==CF_spvType_Str)
              RSDcmd.addParam("p1",          RSDBP_OUT ,V_STRING); 
          END;

          RSDcmd.addParam("p2", RSDBP_IN); RSDcmd.value("p2") = int(SpVar.rec.SPVARIABLEID);
          RSDcmd.addParam("p3", RSDBP_IN); RSDcmd.value("p3") = int(_SpType);                          
          RSDcmd.addParam("p4", RSDBP_IN); RSDcmd.value("p4") = int(RecHandler.rec.SpOb); 
          RSDcmd.addParam("p5", RSDBP_IN); RSDcmd.value("p5") = RecHandler.rec.beginDate; 
          RSDcmd.addParam("p7", RSDBP_IN); RSDcmd.value("p7") = rest_type;
          RSDcmd.execute();
         
         ret = RSDcmd.value("p_retval");   
         if (ret == 0)
            result = RSDcmd.value("p1");
            RecHandler.rec.SPVar = isNull(result);
         ELSE
           result = RSDcmd.value("p1");

            MsgBox("Ошибка расчета спецпеременной"+ret+isNULL(result));    
         END;
      ELSE
         PrintLn("Спецпеременная:", RecHandler.rec.SpName);
         RSDcmd = RsdCommand(RslDefCon,"select * from table(RSO_LOANS_SPvariable.GetSPVariable(?,?,?,?,?,?))");
         RSDcmd.addParam("p2", RSDBP_IN); RSDcmd.value("p2") = int(SpVar.rec.SPVARIABLEID);
          RSDcmd.addParam("p3", RSDBP_IN); RSDcmd.value("p3") = int(_SpType);
          RSDcmd.addParam("p4", RSDBP_IN); RSDcmd.value("p4") = int(RecHandler.rec.SpOb);
          RSDcmd.addParam("p5", RSDBP_IN); RSDcmd.value("p5") = RecHandler.rec.beginDate;
         RSDcmd.addParam("p6", RSDBP_IN); RSDcmd.value("p6") = RecHandler.rec.endDate;
         RSDcmd.addParam("p7", RSDBP_IN); RSDcmd.value("p7") = rest_type;
          RSDcmd.execute();
         //выводим массив
         var rs = RsdRecordset(RSDcmd);
         if (( rs==null ) or (rs==nullval)  )
             PrintLn("Ошибка расчета спецпеременной"); 
          end;
         var i=0;
         while(rs.MoveNext())
             if(rs.value(0, null, V_date)==null)
                PrintLn("Ошибка расчета спецпеременной");
             end;       
             print("\r",rs.value(0, null, V_date)," ");
             IF(SpVar.rec.Type==CF_spvType_Num)
                 printLn(isNull(rs.value(1, null, V_NUMERIC)));
            ELIF(SpVar.rec.Type==CF_spvType_Date)
                printLn(isNull(rs.value(2, null, V_DATE)));   
              ELIF(SpVar.rec.Type==CF_spvType_Str)
                printLn(isNull(rs.value(3, null, V_STRING))); 
              END;
             if(rs.value(4, null, V_NUMERIC)!=null)
                printLn(isNull(rs.value(4, null, V_INTEGER)));
             end;
             if(rs.value(5, null, V_NUMERIC)!=null)
                printLn(isNull(rs.value(5, null, V_INTEGER)));
             end;

             i = i + 1;
          END;
          if (i==0)
             PrintLn("Ошибка расчета спецпеременной");
          END;          
      END;
   END;
END;



MACRO EventHandler(dlg, cmd, id, key)
VAR i = 0;
   IF (key == 32)
      IF ((id == rest_in-1) OR (id == rest_out-1 )) ratio();  END;
      UpdateFields(dlg);
   END;

   IF (KEY == 317) 
      IF (ID == 0)
         I = Menu(SpMenu, "~ENTER~ выбор, ~ESC~ отмена", " Выберите спецпеременную");
         IF (I > -1)
            RecHandler.rec.SpName = SpMenu[i];
         END;
      END;

       IF (ID == 1)
          I = Menu(ObjMenu, "~ENTER~ выбор, ~ESC~ отмена", " Выберите вид объекта");
          IF (I > -1)
	         _SpType = ObjIDArray[I];
             RecHandler.rec.SpType = lnSelectValue("SELECT T_SHORTNAME FROM DLOBJECT_DBT WHERE T_OBJECTID = " + _SpType, V_STRING);
          END;
       END;
   END;

   IF ((key == 323) OR (key == 316)) // F9   выполнить
      Start();
      UpdateFields(dlg);
      IF (RecHandler.rec.EndDate != mindate)
         RETURN CM_CANCEL;
      END;
   END;
                
   RETURN cmd;
END;



MACRO SetDefaultValue()

   RecHandler.rec.SpName     = "_СуммаКредита";
   Rechandler.rec.SpOb       = 0;
   RecHandler.rec.SpType     = "КД"; _SpType = 1;
   RecHandler.rec.BeginDate  = {curdate};
   RecHandler.rec.EndDate    = mindate;
   RecHandler.rec.rest_out   = "X";
END;

   FillSpName();
   FillObj();

   SetDefaultValue();
   RunDialog(RecHandler, "EventHandler");
