/*
$Name:             information.mac
$Module:           Кредитование
$Description:      Передача сведений по залогам клиента
*/

import total, SbCrdInter; 

CLASS COregReq
   VAR ReqOrg      :String;  // Код ФССП в RS-Connect
   VAR ReqOrgName  :String;  // Наименование <ФССП>
   VAR InternalKey :String;  // Уникальный иден-тификатор запроса в АС ФССП
END;

CLASS StructClient;
   VAR TypeID   :String; // Код вида идентификатора клиента
   VAR LocalID  :String; // ИД клиента - дублера
END;

CLASS CClient
   VAR Client;
END;

CLASS CFIO
   VAR Surname   : String; // Фамилия
   VAR FirstName : String; // Имя
   VAR Patronymic: String; // Отчество
END;

CLASS CIDoc
   VAR TypeDoc: String;  // Тип ДУЛ
   VAR SerDoc : String;  // Серия
   VAR NumDoc : String;  // Номер
END;

// Идентификационные реквизиты
CLASS CReqFindClient
   VAR Type     : string; // Тип субъекта
   VAR FullName : String;  // Наименование/ ФИО
   VAR FIO;               // CFIO
   VAR INN      : String; // ИНН
   VAR KPP      : String; // КПП
   VAR OGRN     : String; // ОГРН/ОРГНИП
   VAR BirthDate: Date;   // Дата рождения ФЛ
   VAR Snils    : String; // СНИЛС
   VAR IDoc;              // CIDoc, Удостоверение личности ФЛ 
END;

CLASS FSSPParm
   VAR ReqID;                         // GUID, Уникальный иден-тификатор запроса в RS-Connect
   VAR OrigReq;                       // COregReq, Определяет пара-метры исходного Запроса от внешней АС (ФССП)
   VAR TypeID       : string;         // Код вида идентификатора клиента
   VAR ClientID     : string;         // Идентификатор клиента
   VAR ReqFindClient;                 // Идентификационные реквизиты CReqFindClient
   VAR DateReq      : Date;           // Дата, по состоянию на которую формируются сведения
   VAR NoVip        : String;         // Признак не обрабатывать для VIP
   VAR Twins;                         // Array Список клиентов - дублеров 
END;

// Залогода-тель/залогодержатель
CLASS CDetMortgageContagentsnfo
   VAR Type       : String; // Тип
   VAR Name       : String; // Наименование
   VAR Adr        : String; // Адрес
   VAR Born       : Date;   // Дата рождения
   VAR Bornadr    : String; // Место рождения
   VAR INN        : String; // ИНН
   VAR KPP        : String; // КПП
   VAR OGRN       : String; // ОГРН
   VAR SNILS      : String; // СНИЛС
   VAR TypeDocCode: String; // Код вида ДУЛ
   VAR SerDoc     : String; // Серия ДУЛ                  
   VAR NumDoc     : String; // Номер ДУЛ
END;

CLASS CData
   VAR MortgageContagentType: String; // Отношение к залогу
   VAR PropertyType         : String; // Вид имущества
   VAR MortgageKind         : String; // Вид обязательства
   VAR MortgageNoticeNumber : String; // Номер Уведомления о залоге
   VAR MortgageNoticeDate   : Date;   // Дата Уведомления
   VAR NameContract         : String; // Наименование договора
   VAR ContractDate         : Date;   // Дата выдачи договора
   VAR ContractNumber       : String; // Номер договора
   VAR ContractEndDate      : Date;   // Дата окончания договора
   VAR MortgageDescription  : String; // Описание предмета залога
   VAR MortgageVIN          : String; // VIN
   VAR MortgageId           : String; // Идентификатор имущества
   VAR Amount               : Money;  // Сумма по договору
   VAR DetMortgageContagentsInfo;     // Информация о залогодателях/ залогодержателях
END;

// Параметры ответа на запрос
CLASS CReturnParm
   VAR ReqID         : String;  // Уникальный идентификатор исходного запроса GUID
   VAR SenderID      : String;  // Идентификатор (код) АС, источник сведений
   VAR RealDateTime;            //Time Дата, время актуальности сведений
   VAR CountClient   : Integer; // Количество клиентов
   VAR ClientID      : String;  // Уникальный ИД клиента в АС
   VAR ClientFullName: String;  // Наименование/ФИО
   VAR Vip           : String;  // Особый статус
   VAR CountMortgage : Integer; // Количество  Залогов
   VAR Mortgages;               // Список Залогов, CData

END;

CLASS RSCFaultResponse
   VAR ReqID       :String;/*(GUID)*/  // Из запроса
   VAR ClientID    :String;            // Идентификатор клиента.
                                       // Может быть установлено, если без ошибок выполнилась идентификация клиента
   VAR faultType   :Integer;           // Тип ошибки
   VAR faultCode   :Integer;           // Код ошибки
   VAR faultString :String;            // Текст ошибки
END;

PRIVATE VAR response: RSCFaultResponse;

VAR ErrorText:string = "";

macro isNotUndef(Type)
    IF (ValType(Type) == V_UNDEF)
       Return false;
    end;

    Return true;
end;

// Функция определения задан ли параметр 
// RETURN: TRUE - задан, FALSE - не задан
MACRO ParmIsSet(val)
   VAR stat:bool = TRUE;   
  
   IF (isNotUndef(val))
     IF (ValType(val) == V_STRING)
       IF (val == "")
         stat = FALSE;
       END;
     ELIF (ValType(val) == V_DATE)
       IF (val == ZeroValue(V_DATE))
         stat = FALSE;
       END;     
     END;
   ELSE 
     stat = FALSE;
   END;

   RETURN stat;
END;

// Определение MortgageKind - "Вид обязательства"
MACRO GetMortgageKind(DateRequest:date, EnsContractID:integer, PropertyType:string, CondEP:string, CountlinkCrd:integer):string
  VAR MortgageKind:string = "";
                              
       IF (CountLinkCrd == 1)  // ДО привязан к одному КД  
         VAR TcrExistInMortgage; 
         VAR TcrExistInAvto;         
         VAR RSD_FC = NULL;

         RSD_FC = CRSDCommand("SELECT "
                                  "(SELECT DECODE(COUNT(1), 0, '', 'X') "
                                     "FROM DFEDERAL_CREDIT_DBT FedCrd "
                                    "WHERE FedCrd.T_FLAG = 1 "                
                                      "AND FedCrd.T_CREDIT = tcrd.T_CREDITTYPEID "
                                      "AND FedCrd.T_ID = 1) AS a_ExistInMortgage, "
                                  "(SELECT DECODE(COUNT(1), 0, '', 'X') " 
                                     "FROM DFEDERAL_CREDIT_DBT FedCrd "
                                    "WHERE FedCrd.T_FLAG = 2 "                 
                                      "AND FedCrd.T_CREDIT = tcrd.T_CREDITTYPEID "
                                      "AND FedCrd.T_ID = 1) AS a_ExistInAvto "                                                                
                                     "FROM (SELECT TC.T_CREDITTYPEID "
                                         "FROM dens_crd_dbt ec " 
                                         "JOIN dcredit_c_dbt c   ON (C.T_CREDITNUMBER = EC.T_CREDITNUMBER_REF) "
                                         "JOIN dtype_crd_dbt tc ON (TC.T_CREDITTYPEID = C.T_CREDITTYPEID_REF) "
                                        "WHERE  EC.T_ENSCONTRACTID_REF = ? ) tcrd ",
                       "EnsContrID", EnsContractID,                                                                  
                        V_GENOBJ); 
         IF (RSD_FC.MoveNext())
           TcrExistInMortgage = SQL_NVL(RSD_FC.Value("a_ExistInMortgage", NULL, V_STRING), V_STRING); 
           TcrExistInAvto     = SQL_NVL(RSD_FC.Value("a_ExistInAvto", NULL, V_STRING), V_STRING);
         ELSE
           TcrExistInMortgage = ""; 
           TcrExistInAvto     = "";
         END;
         
         IF (TcrExistInMortgage == "X")
           MortgageKind = "1";
         ELIF (TcrExistInAvto == "X")
           MortgageKind = "2";
         ELIF (PropertyType == "2") 
           IF (CondEP == "X")         
             MortgageKind = "3"; 
           ELSE
             MortgageKind = "4"; 
           END;
         ELIF (PropertyType == "1") 
            MortgageKind = "4";
         END;   
       ELSE // ДО привязан к нескольким КД
         IF (PropertyType == "2")
           IF (CondEP == "X")         
             MortgageKind = "3"; 
           ELSE
             MortgageKind = "4"; 
           END;
         ELIF (PropertyType == "1") 
           MortgageKind = "4";
         END;
       END;
  
  RETURN MortgageKind;
END;
 
// Получить информацию о Залогодатель/залогодержатель 
MACRO GetDetMortgageContagentsInfo(FNCash:RsbParty, DateRequest:date) : CDetMortgageContagentsnfo  
  VAR adr;
  VAR DetMortContsnfo : CDetMortgageContagentsnfo;
  VAR RSDcmd;
      
  DetMortContsnfo.Type = "1";  // ЮР
  DetMortContsnfo.Name = FNCash.FullName; 

  adr = FNCash.Address(PTADDR_POST);
  IF (adr.AddressType != 0)    
     DetMortContsnfo.Adr = adr.Address; 
  ELSE
     adr = FNCash.Address(PTADDR_REAL);
     IF (adr.AddressType != 0)
        DetMortContsnfo.Adr = adr.Address;
     ELSE           
        adr = FNCash.Address(PTADDR_LEGAL); 
        IF (adr.AddressType != 0)
           DetMortContsnfo.Adr = adr.Address;  
        END;             
     END;
  END;

  RSDcmd = null;
  RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansKernel.GetPartyCode(?, ?, ?); end;");
  RSDcmd.AddParam("Code",     RSDBP_RETVAL, V_STRING);
  RSDcmd.addParam("PartyID",  RSDBP_IN,     FNCash.PartyID);
  RSDcmd.addParam("CodeKind", RSDBP_IN,     PTCK_INN);  
  RSDcmd.addParam("Opdate",   RSDBP_IN,     DateRequest);
  RSDcmd.execute();  
  
  DetMortContsnfo.INN  = SQL_NVL(RSDcmd.value("Code", NULL, V_STRING), V_STRING);
  
  RSDcmd = null;
  RSDcmd = RsdCommand(RslDefCon, "begin ? := LoansKernel.GetPartyCode(?, ?, ?); end;");
  RSDcmd.AddParam("Code",     RSDBP_RETVAL, V_STRING);
  RSDcmd.addParam("PartyID",  RSDBP_IN,     FNCash.PartyID);
  RSDcmd.addParam("CodeKind", RSDBP_IN,     PTCK_OGRN);  
  RSDcmd.addParam("Opdate",   RSDBP_IN,     DateRequest);
  RSDcmd.execute();
  
  DetMortContsnfo.OGRN = SQL_NVL(RSDcmd.value("Code", NULL, V_STRING), V_STRING); 

  RETURN DetMortContsnfo;       
END;

MACRO GetEMailList(EnsContrID_List)
   var PatInfo;
   var cont;
   VAR str_EnsContrIDList:string = "";

   var EMail_List = TArray(); 
   VAR PartyIDInfo_List = TArray();

   VAR FlagInfoUser = CRSDCommand("SELECT T_INFORM FROM dlist_fnp_dbt WHERE T_ID = 1", V_STRING);

   FOR(var EnsContrID, EnsContrID_List)
     IF (str_EnsContrIDList == "")
        str_EnsContrIDList = EnsContrID;       
     ELSE
        str_EnsContrIDList = str_EnsContrIDList + "," + EnsContrID;       
     END;
   END;
   
   VAR RSD_PartyID_Info;    
   IF (FlagInfoUser == "X") //установлен признак <Информирование ответственного пользователя о результатах обмена>
      // проверяем указанных пользователей для информирования
      RSD_PartyID_Info = NULL;
      RSD_PartyID_Info = CRSDCommand("SELECT PERSON.T_PARTYID "
                                           "FROM DFEDERAL_SERVICE_DBT  fs "
                                           "JOIN DPERSON_DBT person ON (PERSON.T_OPER = FS.T_PARTYID) "
                                          "WHERE FS.T_ID = 1", V_GENOBJ);
                                    
      WHILE(RSD_PartyID_Info.MoveNext())
         PartyIDInfo_List[PartyIDInfo_List.size] = RSD_PartyID_Info.Value("T_PARTYID", NULL, V_INTEGER); 
      END;

      // Если пользователей для информирования не заданы, то информируем ответственных за договора
      IF (PartyIDInfo_List.size == 0)
         RSD_PartyID_Info = NULL;
         RSD_PartyID_Info = CRSDCommand(
             "SELECT DISTINCT PERSON.T_PARTYID "
               "FROM dens_crd_dbt ec "
               "JOIN dcredit_c_dbt c ON (C.T_CREDITNUMBER = EC.T_CREDITNUMBER_REF) "
               "JOIN doper_crd_dbt oc ON (OC.T_OBJECTTYPEID_REF = C.T_CRD_KIND AND "
                                         "OC.T_OBJECTID = C.T_CREDITNUMBER) "
               "JOIN DPERSON_DBT person ON (PERSON.T_OPER = OC.T_OPER) "                                       
              "WHERE EC.T_ENSCONTRACTID_REF IN (" + str_EnsContrIDList + ") ",
             V_GENOBJ);
             
          WHILE(RSD_PartyID_Info.MoveNext())
            PartyIDInfo_List[PartyIDInfo_List.size] = RSD_PartyID_Info.Value("T_PARTYID", NULL, V_INTEGER); 
          END;
      END; 

      FOR(var indx, 0, PartyIDInfo_List.size - 1)
         PatInfo = NULL;
         PatInfo = RsbParty(PartyIDInfo_List[indx]);
         
         cont = NULL;
         cont = PatInfo.PartyContact();
       
         var flag = true; 
         WHILE ((cont.next) and (flag == true))
           IF (cont.ContactKind == CNTK_EMAIL)
              EMail_List[EMail_List.size] = cont.Value;
              flag = false;
           END;    
         END; 
      END; 
   END;
   
   RETURN EMail_List;
END;

// Информирования пользователей, ответственных за обмен
MACRO InfoUser(parm, EnsContrID_List, ClientFullName)
   VAR BodyMail:string;
   VAR SubjectMail:string;   
   VAR InfoSystemName:string;
   VAR ReqID; 
   VAR ReqOrg, ReqOrgName, InternalKey; 
   VAR EnsContractName; 
   VAR KindCrdName;   
      
   record enscontr ("enscontr.dbt");    
   record credit ("credit_c.dbt");
   
   IF ((GenPropID(parm, "ReqID") != -1) AND (ParmIsSet(parm.ReqID)))
     ReqID = parm.ReqID;
   ELSE
     ReqID = "";
   END;
   
   IF ((GenPropID(parm, "OrigReq") != -1) AND (ParmIsSet(parm.OrigReq)))
     IF ((GenPropID(parm.OrigReq, "ReqOrg") != -1) AND (ParmIsSet(parm.OrigReq.ReqOrg)))
       ReqOrg      = parm.OrigReq.ReqOrg;
     ELSE 
       ReqOrg  = "";
     END;
     
     IF ((GenPropID(parm.OrigReq, "ReqOrgName") != -1) AND (ParmIsSet(parm.OrigReq.ReqOrgName)))
       ReqOrgName  = parm.OrigReq.ReqOrgName;
     ELSE 
       ReqOrgName  = "";
     END;
     
     IF ((GenPropID(parm.OrigReq, "InternalKey") != -1) AND (ParmIsSet(parm.OrigReq.InternalKey)))
       InternalKey = parm.OrigReq.InternalKey;
     ELSE
       InternalKey = "";
     END;
   ELSE
     ReqOrg      = "";
     ReqOrgName  = "";
     InternalKey = "";
   END;
   
   // Тема письма
   SubjectMail = "Внимание! Обмен с ФССП. Запрос сведений о залогах клиента " + ClientFullName; 
   
   InfoSystemName = CRSDCommand("SELECT T_NAMESYSTEM FROM dlist_fnp_dbt WHERE T_ID = 1", V_STRING);
   
   // Тело письма
   BodyMail = "Информационная система    " + InfoSystemName + "\n"
              "ID запроса ИС                          " + ReqID + "\n" 
              "Клиент                                      " + ClientFullName + "\n\n"

              "Код ФССП                            " + ReqOrg + "\n"
              "Наименование ФССП         " + ReqOrgName + "\n"
              "ID запроса ФССП                 " + InternalKey + "\n\n"              
              
              "В ФССП была предоставлена информация по следующим договорам залога клиента:\n\n";
              
   VAR nEns = 1;                         
   FOR(VAR EnsID, EnsContrID_List)
      Loans_FindEnsContract (EnsID, ensContr);
      
      IF (ensContr.EnsContractName != "")
        EnsContractName = ensContr.EnsContractName;
      ELSE
        EnsContractName = CRSDCommand("SELECT T_TYPEENSNAME FROM dtype_ens_dbt WHERE T_TYPEENSID = ?",
                           "TYPEENSID", ensContr.EnsSysType, 
                           V_STRING)
      END;
      
      BodyMail = BodyMail + "" + nEns +    ". " + EnsContractName + " № " + ensContr.EnsContractNumber + 
                    " от " + ensContr.EnsContractFirstDate + ". Обеспечение предоставлено по договору:\n";
       
      VAR RSD_CRD = NULL;
      RSD_CRD = CRSDCommand("SELECT T_CREDITNUMBER_REF "
                              "FROM dens_crd_dbt "
                             "WHERE T_ENSCONTRACTID_REF = ? ",
                   "EnsContrID", ensContr.EnsContractID, V_GENOBJ);
      
      WHILE(RSD_CRD.MoveNext())
         Loans_FindCrdContract(RSD_CRD.Value("T_CREDITNUMBER_REF", NULL, V_INTEGER), credit);
         
         KindCrdName = CRSDCommand("SELECT T_OBJECTNAME FROM dlobject_dbt WHERE T_OBJECTID = ?",
                           "KindCrd", credit.Crd_kind, 
                           V_STRING);
         
         BodyMail = BodyMail + "      " + KindCrdName + " № " + credit.UsCreditNumber + " от " + credit.RegDate + "\n";
      END;             
              
      nEns = nEns + 1;
   END;
              
   // Создание списка EMail на которые нужно отправить письма в рамках информирования   
   var EMail_List = GetEMailList(EnsContrID_List);
   
   // Отправка письма
   VAR MacroNameInfo = CRSDCommand("SELECT T_MACROMAIL FROM dlist_fnp_dbt WHERE T_ID = 1", V_STRING);
   FOR(VAR EMail, EMail_List)       
      ExecMacroFile(MacroNameInfo, "FSSP_SendEMail", SubjectMail, BodyMail, EMail);
   END; 
END;
 
//Поиск клиента по реквизитам
MACRO GetClientID(parm)
   VAR ClientID = 0;
   VAR SqlFindClient:string;    // строка sql-запроса для поиска клиента
   VAR DateRequest;   // Дата, по состоянию на которую формируются сведения 
   VAR sqlstr:string = " "; 
   
   IF ((GenPropID(parm, "DateReq") != -1) AND (ParmIsSet(parm.DateReq)))
      DateRequest = parm.DateReq;
   ELSE 
      DateRequest = {CURDATE};
   END; 

   SqlFindClient = "SELECT P.T_PARTYID "
                   "FROM DPARTY_DBT P, DPERSN_DBT psn "
                   "WHERE PSN.T_PERSONID(+) = P.T_PARTYID " ;  

   IF ((GenPropID(parm.ReqFindClient, "Type") != -1) AND (ParmIsSet(parm.ReqFindClient.Type)))
      IF (parm.ReqFindClient.Type == "1")   // ЮЛ
        sqlstr = " AND P.T_LEGALFORM = 1 "; 
      ELIF (parm.ReqFindClient.Type == "2") // ФЛ
        sqlstr = " AND P.T_LEGALFORM = 2 ";
      ELIF (parm.ReqFindClient.Type == "3") // ИП
        sqlstr = " AND P.T_LEGALFORM = 2 AND PSN.T_ISEMPLOYER = 'X' ";       
      END;        
      SqlFindClient = SqlFindClient + sqlstr;       
   ELSE
      ErrorText = "Параметр ReqFindClient.Type не задан!";
      RunError(ErrorText);
   END;
        
   IF ((GenPropID(parm.ReqFindClient, "FullName") != -1) AND (ParmIsSet(parm.ReqFindClient.FullName)))
      IF (parm.ReqFindClient.Type == "1")
         sqlstr = " AND (UPPER(P.T_NAME) = UPPER('" + parm.ReqFindClient.FullName + "') OR UPPER(P.T_SHORTNAME) = UPPER('" + parm.ReqFindClient.FullName + "')) ";
      ELIF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
         sqlstr = " AND UPPER(P.T_NAME) = UPPER('" + parm.ReqFindClient.FullName + "') ";
      ELSE   // parm.ReqFindClient.Type == "4" - неопределен 
         sqlstr = " AND ((P.T_LEGALFORM = 2  AND UPPER(P.T_NAME) = UPPER('" + parm.ReqFindClient.FullName + "')) "
                          "OR "
                        "(P.T_LEGALFORM = 1 AND (UPPER(P.T_NAME) = UPPER('" + parm.ReqFindClient.FullName + "')  OR "
                                                "UPPER(P.T_SHORTNAME) = UPPER('" + parm.ReqFindClient.FullName + "')))) ";
      END;

      SqlFindClient = SqlFindClient + sqlstr;        
   ELSE
      ErrorText = "Параметр ReqFindClient.FullName не задан!";
      RunError(ErrorText);        
   END;

   IF ((GenPropID(parm.ReqFindClient, "FIO") != -1) AND (ParmIsSet(parm.ReqFindClient.FIO)))
      IF ((GenPropID(parm.ReqFindClient.FIO, "Surname") != -1) AND (ParmIsSet(parm.ReqFindClient.FIO.Surname)))
        IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
          sqlstr = " AND UPPER(PSN.T_NAME1) = UPPER('" + parm.ReqFindClient.FIO.Surname + "') ";
        ELIF (parm.ReqFindClient.Type == "4") 
          sqlstr = " AND ((P.T_LEGALFORM = 2 AND UPPER(PSN.T_NAME1) = UPPER('" + parm.ReqFindClient.FIO.Surname + "')) OR "
                         "(P.T_LEGALFORM != 2 AND 1 = 1)) ";             
        END;
        
        SqlFindClient = SqlFindClient + sqlstr;
      ELSE 
        ErrorText = "Параметр ReqFindClient.FIO.Surname не задан!";
        RunError(ErrorText);
      END;

      IF ((GenPropID(parm.ReqFindClient.FIO, "FirstName") != -1) AND (ParmIsSet(parm.ReqFindClient.FIO.FirstName)))
        IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
          sqlstr = " AND UPPER(PSN.T_NAME2) = UPPER('" + parm.ReqFindClient.FIO.FirstName + "') ";
        ELIF (parm.ReqFindClient.Type == "4")  
          sqlstr = " AND ((P.T_LEGALFORM = 2 AND UPPER(PSN.T_NAME2) = UPPER('" + parm.ReqFindClient.FIO.FirstName + "')) OR "
                         "(P.T_LEGALFORM != 2 AND 1 = 1)) ";          
        END;  
          SqlFindClient = SqlFindClient + sqlstr;
      ELSE
        ErrorText = "Параметр ReqFindClient.FIO.FirstName не задан!";
        RunError(ErrorText);        
      END; 
        
      IF ((GenPropID(parm.ReqFindClient.FIO, "Patronymic") != -1) AND (ParmIsSet(parm.ReqFindClient.FIO.Patronymic)))
        IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
          sqlstr = " AND UPPER(PSN.T_NAME3) = UPPER('" + parm.ReqFindClient.FIO.Patronymic + "') ";
        ELIF (parm.ReqFindClient.Type == "4") 
          sqlstr = " AND ((P.T_LEGALFORM = 2 AND UPPER(PSN.T_NAME3) = UPPER('" + parm.ReqFindClient.FIO.Patronymic + "')) OR "
                         "(P.T_LEGALFORM != 2 AND 1 = 1)) ";
        END;
        SqlFindClient = SqlFindClient + sqlstr;
      END;
   END;           

   IF ((GenPropID(parm.ReqFindClient, "INN") != -1) AND (ParmIsSet(parm.ReqFindClient.INN)))
      sqlstr = " AND '" + parm.ReqFindClient.INN + "' = LoansKernel.GetPartyCode(P.T_PARTYID, " + PTCK_INN + ", TO_DATE('" + DateRequest + "', 'dd.mm.yyyy')) ";
      SqlFindClient = SqlFindClient + sqlstr;
   END;
      
   IF ((GenPropID(parm.ReqFindClient, "OGRN") != -1) AND (ParmIsSet(parm.ReqFindClient.OGRN)))
      sqlstr = " AND '" + parm.ReqFindClient.OGRN + "' = LoansKernel.GetPartyCode(P.T_PARTYID, " + PTCK_OGRN + ", TO_DATE('" + DateRequest + "', 'dd.mm.yyyy')) ";
      SqlFindClient = SqlFindClient + sqlstr;
   END;  

   IF ((GenPropID(parm.ReqFindClient, "BirthDate") != -1) AND (ParmIsSet(parm.ReqFindClient.BirthDate)))
      IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
          sqlstr = " AND PSN.T_BORN = TO_DATE('" + parm.ReqFindClient.BirthDate + "', 'dd.mm.yyyy') ";
      ELIF (parm.ReqFindClient.Type == "4") 
         sqlstr = " AND ((P.T_LEGALFORM = 2 AND PSN.T_BORN = TO_DATE('" + parm.ReqFindClient.BirthDate + "', 'dd.mm.yyyy')) "
                          "OR "
                        "(P.T_LEGALFORM != 2 AND 1 = 1)) ";  
      END;
      SqlFindClient = SqlFindClient + sqlstr;
   END;

   IF ((GenPropID("parm.ReqFindClient, Snils") != -1) AND (ParmIsSet(parm.ReqFindClient.Snils)))
      IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))     
         sqlstr = " AND '" + parm.ReqFindClient.Snils + "' = LoansKernel.GetPartyCode(P.T_PARTYID, " + PTCK_SNILS + ", TO_DATE('" + DateRequest + "', 'dd.mm.yyyy')) ";      
      ELIF (parm.ReqFindClient.Type == "4") 
         sqlstr = 
           " AND ((P.T_LEGALFORM = 2 AND '" + parm.ReqFindClient.Snils + "' = LoansKernel.GetPartyCode(P.T_PARTYID, " + PTCK_SNILS + ", TO_DATE('" + DateRequest + "', 'dd.mm.yyyy'))) "
                  "OR "   
                 "(P.T_LEGALFORM != 2 AND 1 = 1)) "; 
      END; 
      SqlFindClient = SqlFindClient + sqlstr;
   END;  

   IF ((GenPropID(parm.ReqFindClient, "IDoc") != -1) AND (ParmIsSet(parm.ReqFindClient.IDoc)))           
     IF ((GenPropID(parm.ReqFindClient.IDoc, "TypeDoc") == -1) OR (NOT ParmIsSet(parm.ReqFindClient.IDoc.TypeDoc)))
       ErrorText = "Параметр ReqFindClient.IDoc.TypeDoc не задан!";
       RunError(ErrorText);
     END;

     IF ((GenPropID(parm.ReqFindClient.IDoc, "SerDoc") == -1) OR (NOT ParmIsSet(parm.ReqFindClient.IDoc.SerDoc)))
       ErrorText = "Параметр parm.ReqFindClient.IDoc.SerDoc не задан!";
       RunError(ErrorText);
     END;

     IF ((GenPropID(parm.ReqFindClient.IDoc, "NumDoc") == -1) OR (NOT ParmIsSet(parm.ReqFindClient.IDoc.NumDoc)))
       ErrorText = "Параметр parm.ReqFindClient.IDoc.NumDoc не задан!";
       RunError(ErrorText);
     END;

     IF ((parm.ReqFindClient.Type == "2") OR (parm.ReqFindClient.Type == "3"))
       sqlstr = " AND EXISTS(SELECT 1 "
                              "FROM DPERSNIDC_DBT "
                             "WHERE T_PERSONID = P.T_PARTYID "
                               "AND T_PAPERKIND = " + parm.ReqFindClient.IDoc.TypeDoc + " "
                               "AND T_PAPERSERIES = '" + parm.ReqFindClient.IDoc.SerDoc + "' "
                               "AND T_PAPERNUMBER = '" + parm.ReqFindClient.IDoc.NumDoc + "') ";
                               
     ELIF (parm.ReqFindClient.Type == "4")   
       sqlstr = 
         " AND ((P.T_LEGALFORM = 2 AND EXISTS(SELECT 1 "
                                               "FROM DPERSNIDC_DBT "
                                              "WHERE T_PERSONID = P.T_PARTYID "
                                                "AND T_PAPERKIND = " + parm.ReqFindClient.IDoc.TypeDoc + " "
                                                "AND T_PAPERSERIES = '" + parm.ReqFindClient.IDoc.SerDoc + "' "
                                                "AND T_PAPERNUMBER = '" + parm.ReqFindClient.IDoc.NumDoc + "')) "
                  "OR "
               "(P.T_LEGALFORM != 2 AND 1 = 1)) "
     END;
       SqlFindClient = SqlFindClient + sqlstr;
   END;
   
   ClientID = CRSDCommand(SqlFindClient, V_INTEGER);

   RETURN ClientID;
END; 

// Параметр parm имеет тип FSSPParm
MACRO Availability(parm)

   VAR rp: CReturnParm;
   VAR PartyID;       // ID субъекта(клиента),по которому получают информацию по залогам
   VAR DateRequest;   // Дата, по состоянию на которую формируются сведения 
   VAR CountClient;   // Кол-во клиентов
   VAR indx;
   VAR RSDcmd;
   VAR client;
   VAR ReqID = "", ClientID = "";

   IF ((GenPropID(parm, "DateReq") != -1) AND (ParmIsSet(parm.DateReq)))
     DateRequest = parm.DateReq;
   ELSE 
     DateRequest = {CURDATE};
   END; 

   IF ((GenPropID(parm, "ReqID") == -1) OR (NOT ParmIsSet(parm.ReqID)))
     ErrorText = "Параметр ReqID не задан!";
     RunError(ErrorText);
   END;
   ReqID = parm.ReqID;

   IF ((GenPropID(parm, "OrigReq") != -1) AND (ParmIsSet(parm.OrigReq)))
     IF ((GenPropID(parm.OrigReq, "ReqOrg") == -1) OR (NOT ParmIsSet(parm.OrigReq.ReqOrg)))
       ErrorText = "Параметр OrigReq.ReqOrg не задан!";
       RunError(ErrorText);
     END;

     IF ((GenPropID(parm.OrigReq, "ReqOrgName") == -1) OR (NOT ParmIsSet(parm.OrigReq.ReqOrgName)))
       ErrorText = "Параметр OrigReq.ReqOrgName не задан!";
       RunError(ErrorText);
     END;
     
     IF ((GenPropID(parm.OrigReq, "InternalKey") == -1) OR (NOT ParmIsSet(parm.OrigReq.InternalKey)))
       ErrorText = "Параметр OrigReq.InternalKey не задан!";
       RunError(ErrorText);
     END;
   END;

   // Поиск клиента
   IF ((GenPropID(parm, "ClientID") != -1) AND (ParmIsSet(parm.ClientID))) 
      client = RsbParty(parm.ClientID);     
    
      PartyID = client.PartyID;
     
      IF ((GenPropID(parm, "TypeID") == -1) OR (NOT ParmIsSet(parm.TypeID)))
         ErrorText = "Параметр TypeID не задан!";
         RunError(ErrorText);
      END;
   ELSE
      IF ((GenPropID(parm, "TypeID") == -1) OR (ParmIsSet(parm.TypeID)))
         ErrorText = "Параметр ClientID не задан!";
         RunError(ErrorText);
      END;
    
      IF ((GenPropID(parm, "ReqFindClient") != -1) AND (ParmIsSet(parm.ReqFindClient)))
         PartyID = GetClientID(parm);       
      ELSE
         IF ((GenPropID(parm, "TypeID") == -1) OR (NOT ParmIsSet(parm.TypeID)))
            ErrorText = "Параметр ReqFindClient не задан!";
            RunError(ErrorText);
         END;
      END;   
   END;

   ClientID = PartyID;
         
   // Формируем список клиентов (включая дублеров) 
   VAR ClientIDList:TArray = TARRAY(); // список клиентов   
   ClientIDList[ClientIDList.size] = PartyID;
 
   IF ((GenPropID(parm, "Twins") != -1) AND (ParmIsSet(parm.Twins)))
     IF (parm.Twins.size == 0)
       ErrorText = "Список Twins пустой!";
       RunError(ErrorText); 
     ELSE
       FOR(indx, 0, parm.Twins.size-1)      
         IF ((GenPropID(parm.Twins[0], "LocalID") == -1) OR (NOT ParmIsSet(parm.Twins[0].LocalID)))
           ErrorText = "Для " + indx + "-го элемента параметра Twins не задан параметр LocalID!";
           RunError(ErrorText);      
         END;         
       END;
     END;

     FOR(indx, 0, parm.Twins.size-1)
       ClientIDList[ClientIDList.size] = parm.Twins[indx].LocalID;       
     END;
   END;
   
   CountClient = ClientIDList.size;   

   var SqlWhereClient = " ";
   IF (ClientIDList.size > 0)
     SqlWhereClient = "AND CP.T_PARTYID IN ("; 
     FOR(indx, 0, ClientIDList.size-1)       
       SqlWhereClient = SqlWhereClient + ClientIDList[indx];
       IF (indx != ClientIDList.size-1)
         SqlWhereClient = SqlWhereClient + ","; 
       END;
     END;
     SqlWhereClient = SqlWhereClient + ") "; 
   END;
   
   record enscontr ("enscontr.dbt");
   record ensobj ("ensobj.dbt");
   record enstype("ensType.dbt");
   record credit ("credit_c.dbt");
   
   // Получим список ДО для клиентов (включая дублеров)      
   VAR RSD_EnsContr = CRSDCommand("SELECT EC.T_ENSCONTRACTID "
                                    "FROM denscontr_dbt ec "
                                    "JOIN dtype_ens_dbt te ON (TE.T_TYPEENSID = EC.T_ENSSYSTYPE) "
                                    "JOIN dcounterpart_dbt cp ON (CP.T_ENSCONTRACTID = EC.T_ENSCONTRACTID) "
                                   "WHERE TE.T_SYSTYPEENSID_REF NOT IN (:VOUCHER, :GUARANTEE_ENS) "
                                     "AND EC.T_ENSCONTRACTFIRSTDATE <= :DateRequest "
                                     "AND EC.T_ENSCONTRACTLASTDATE > :DateRequest " 
                                     + SqlWhereClient +
                                     "AND CP.T_CHANGEDATE = (SELECT MAX(T_CHANGEDATE) " 
                                                              "FROM dcntpartview_dbt "
                                                             "WHERE T_ENSCONTRACTID = CP.T_ENSCONTRACTID "
                                                               "AND T_CHANGEDATE <= :DateRequest) ",
                                 "VOUCHER", Поручительство,
                                 "GUARANTEE_ENS", Гарантия_Обеспечение,
                                 "DateRequest", DateRequest,                                 
                                 V_GENOBJ);
   
   indx = 0;
   VAR EnsContrID_List = TArray();
   VAR str_EnsContrIDList:string = ""; 
   VAR EnsContrExist:bool = FALSE;  
   VAR EnsContrID, EnsObjID;
   VAR FNCash;  
   VAR CountLinkCrd, TypeCrdID;   
   VAR RSD_FC;
   WHILE (RSD_EnsContr.MoveNext())
      IF (NOT EnsContrExist)
         EnsContrExist = TRUE;
      END;
      
      EnsContrID = RSD_EnsContr.Value("T_ENSCONTRACTID",   NULL, V_INTEGER);
      EnsContrID_List[EnsContrID_List.size] = EnsContrID;
      
      Loans_FindEnsContract (EnsContrID, ensContr);
      
      VAR FNCashPartyID = CRSDCommand("SELECT T_PARTYID FROM ddp_dep_dbt WHERE T_CODE = ?",
                                      "FNCash", enscontr.FNCash, 
                                       V_INTEGER);
      
      // Получим список ОО по ДО
      VAR RSD_EnsObj = CRSDCommand("SELECT EE.T_ENSOBJECTID_REF "
                                      "FROM  decn_eob_dbt ee "
                                     "WHERE EE.T_ENSCONTRACTID_REF = ? "
                                     "ORDER BY EE.T_ENSOBJECTID_REF ",
                                 "EnsContrID", EnsContrID,                                                                  
                                 V_GENOBJ);                                  
      
      WHILE (RSD_EnsObj.MoveNext())
         IF (NOT ParmIsSet(rp.Mortgages))
            rp.Mortgages = TArray(); 
         END;
     
         EnsObjID = RSD_EnsObj.Value("T_ENSOBJECTID_REF",   NULL, V_INTEGER);
     
         Loans_FindEnsObject(EnsObjID, ensobj);
         Loans_FindEnsType(ensobj.EnsTypeID_Ref, ensType);
          
         // Заполним информацию о ОО       
         VAR data = NULL;
         data = CData();
         data.MortgageContagentType = "1";
     
         IF (ensType.TypeTransport == 0)
            data.PropertyType = "1";
         ELSE
            data.PropertyType = "2";
         END;
     
         IF (ensContr.EnsContractName != "")
            data.NameContract = ensContr.EnsContractName;
         ELSE 
            data.NameContract = CRSDCommand("SELECT T_TYPEENSNAME FROM dtype_ens_dbt WHERE T_TYPEENSID = ?",
                                            "TYPEENSID", ensContr.EnsSysType, 
                                           V_STRING);
     
         END;
        
         data.ContractDate   = ensContr.EnscontractFirstDate;
         data.ContractNumber = ensContr.EnscontractNumber;
         data.ContractEndDate = ensContr.EnscontractLastDate;
         data.MortgageDescription = ensobj.EnsObjectName;
     
         IF (ensType.TypeTransport == 1) // Транспортное средство
            RSDcmd = null;
            RSDcmd = RsdCommand(RslDefCon, "begin ? := loansUserFunction.GetUsFieldValue(?, ?, ?, ?); end;");
            RSDcmd.AddParam("UsFieldValue", RSDBP_RETVAL,   V_STRING);
            RSDcmd.addParam("usfieldtype",  RSDBP_IN, 212); // тип польз. поля 
            RSDcmd.addParam("Opdate",       RSDBP_IN, DateRequest);
            RSDcmd.addParam("objk",         RSDBP_IN, LO_ENSOBJECT);
            RSDcmd.addParam("objn",         RSDBP_IN, ensobj.EnsObjectID); 
            RSDcmd.execute();
            
            data.MortgageVIN = SQL_NVL(RSDcmd.value("UsFieldValue", NULL, V_STRING), V_STRING);          
            IF (data.MortgageVIN == "")
               data.MortgageVIN = NULL;
            END;
         END;
         
         IF (ensType.TypeTransport != 1) // Транспортное средство
            RSDcmd = null; 
            RSDcmd = RsdCommand(RslDefCon, "begin ? := loansUserFunction.GetUsFieldValue(?, ?, ?, ?); end;");
            RSDcmd.AddParam("UsFieldValue", RSDBP_RETVAL,   V_STRING);
            RSDcmd.addParam("usfieldtype",  RSDBP_IN, 213); // тип польз. поля 
            RSDcmd.addParam("Opdate",       RSDBP_IN, DateRequest);
            RSDcmd.addParam("objk",         RSDBP_IN, LO_ENSOBJECT);
            RSDcmd.addParam("objn",         RSDBP_IN, ensobj.EnsObjectID); 
            RSDcmd.execute();

            data.MortgageId = SQL_NVL(RSDcmd.value("UsFieldValue", NULL, V_STRING), V_STRING);          
            IF (data.MortgageId == "")
               data.MortgageId = NULL;
            END;
         END; 
        
         VAR rsum, msum, rezsum;
         RSDcmd = null; 
         RSDcmd = RsdCommand(RslDefCon, "begin ? := loansKernel.getensobjsum(?, ?, ?, ?, ?, ?); end;");
         RSDcmd.AddParam("retval",      RSDBP_RETVAL,  V_MONEY);
         RSDcmd.addParam("objid",       RSDBP_IN,      ensobj.EnsObjectID); // тип польз. поля 
         RSDcmd.addParam("enscontrid",  RSDBP_IN,      ensContr.EnsContractID);
         RSDcmd.addParam("opdate",      RSDBP_IN,      DateRequest+1);
         RSDcmd.addParam("rsum",        RSDBP_IN_OUT,  rsum); 
         RSDcmd.addParam("msum",        RSDBP_IN_OUT,  msum);
         RSDcmd.addParam("rezsum",      RSDBP_IN_OUT,  rezsum); 
         RSDcmd.execute();
        
         data.Amount = RSDcmd.value("rsum", NULL, V_MONEY);
        
         VAR  CondEP = CRSDCommand(
                         "SELECT DECODE(COUNT(1), 0, '', 'X') "
                          "FROM DNOTICE_DBT n1 "
                         "WHERE n1.T_DATENOTICE <= :DateRequest "
                           "AND n1.T_TYPENOTICE = 1 " // о возникновении залога движимого имущества
                           "AND n1.T_STATUSNOTICE = 10 " //Зарегистрировано 
                           "AND n1.T_ENSCONTRACTID = :EnsContrID "
                           "AND (NOT EXISTS(SELECT 1 "
                                             "FROM DNOTICE_DBT n2 "
                                            "WHERE n2.T_DATENOTICE <= :DateRequest "
                                              "AND n2.T_TYPENOTICE = 3 " // об исключении сведений о залоге 
                                              "AND n2.T_ENSCONTRACTID = n1.T_ENSCONTRACTID) "
                                    "OR "
                                 "NOT EXISTS(SELECT 1 "
                                              "FROM DNOTICE_DBT n3 "
                                             "WHERE n3.T_DATENOTICE <= :DateRequest "
                                               "AND n3.T_TYPENOTICE = 2 " // Об изменении залога 
                                               "AND n3.T_ENSCONTRACTID = n1.T_ENSCONTRACTID))",
                         "DateRequest", DateRequest, 
                         "EnsContrID", ensContr.EnsContractID,                        
                         V_STRING);
        
         CountLinkCrd = CRSDCommand("SELECT COUNT(T_CREDITNUMBER_REF) "
                                     "FROM dens_crd_dbt "
                                    "WHERE T_ENSCONTRACTID_REF = ? ",
                             "EnsContrID", ensContr.EnsContractID, V_INTEGER);
        
         // Определение MortgageKind - "Вид обязательства"       
         data.MortgageKind = GetMortgageKind(DateRequest, ensContr.EnsContractID, data.PropertyType, CondEP, CountlinkCrd);
         
         // Номер Уведомления о залоге - MortgageNoticeNumber
         // Дата Уведомления - MortgageNoticeDate
         // ******************************************************************************************         
         IF ((data.MortgageKind == "2") OR (data.MortgageKind == "3") OR (data.MortgageKind == "4")) 
          
          VAR RSD_RND = NULL;
          RSD_RND = CRSDCommand(
                     "SELECT N1.T_REGNUM, N1.T_REGISTRATIONDATE "
                       "FROM DNOTICE_DBT n1 "
                      "WHERE n1.T_TYPENOTICE = 1 " // о возникновении залога движимого имущества 
                        "AND n1.T_STATUSNOTICE = 10 " //Зарегистрировано 
                        "AND n1.T_ENSCONTRACTID = :EnsContrID "
                        "AND n1.T_DATENOTICE = (SELECT MAX(n2.T_DATENOTICE) "
                                                 "FROM DNOTICE_DBT n2 "
                                                "WHERE n2.T_TYPENOTICE = n1.T_TYPENOTICE "
                                                  "AND n2.T_STATUSNOTICE = n1.T_STATUSNOTICE  "
                                                  "AND n2.T_ENSCONTRACTID = n1.T_ENSCONTRACTID "
                                                  "AND n2.T_DATENOTICE <= :DateRequest) "
                        "AND (NOT EXISTS(SELECT 1 "
                                          "FROM DNOTICE_DBT n2 "
                                         "WHERE n2.T_DATENOTICE <= :DateRequest "
                                           "AND n2.T_TYPENOTICE = 3 " // об исключении сведений о залоге
                                           "AND n2.T_ENSCONTRACTID = n1.T_ENSCONTRACTID "
                                           "AND n2.T_REGNUM = n1.T_REGNUM) "
                                "OR "
                             "NOT EXISTS(SELECT 1 "
                                          "FROM DNOTICE_DBT n3 "
                                         "WHERE n3.T_DATENOTICE <= :DateRequest "
                                           "AND n3.T_TYPENOTICE = 2 "  // Об изменении залог
                                           "AND n3.T_ENSCONTRACTID = n1.T_ENSCONTRACTID "
                                           "AND n3.T_REGNUM = n1.T_REGNUM)) ",
                    "EnsContrID", ensContr.EnsContractID,                       
                    "DateRequest", DateRequest,                     
                    V_GENOBJ);
                    
            IF (RSD_RND.MoveNext())
               data.MortgageNoticeNumber = SQL_NVL(RSD_RND.Value("T_REGNUM", NULL, V_STRING), V_STRING); 
               data.MortgageNoticeDate   = SQL_NVL(RSD_RND.Value("T_REGISTRATIONDATE", NULL, V_DATE), V_DATE);
            END;       
         END;
         // ****************************************************************************************** 
               
         data.DetMortgageContagentsInfo = TArray(); 
         FNCash = NULL;
         FNCash = RsbParty(FNCashPartyID);
         
         // Залогодатель/залогодержатель        
         data.DetMortgageContagentsInfo[data.DetMortgageContagentsInfo.size] = GetDetMortgageContagentsInfo(FNCash, DateRequest);
         
         rp.Mortgages[rp.Mortgages.size] = data;
      END;          
   END;
   
   IF (ParmIsSet(rp.Mortgages))
     rp.CountMortgage = rp.Mortgages.size;   
   ELSE
     rp.CountMortgage = 0;
   END;

   IF (EnsContrExist == FALSE)
      ErrorText = "Договоры обеспечения не найдены!";
      RunError(ErrorText);  
   END;
    
   rp.ReqID         = parm.ReqID;
   rp.SenderID  = CRSDCommand("SELECT T_AUTHORIZATION FROM dlist_fnp_dbt WHERE T_ID = 1", V_INTEGER);
   rp.RealDateTime  =  DtTm(Date(), Time());
   rp.CountClient   = CountClient;
   IF (CountClient == 1) 
     rp.ClientID = PartyID; 

     IF ((GenPropID(parm, "ReqFindClient") != -1) AND (GenPropID(parm.ReqFindClient, "FullName") != -1) 
     AND (ParmIsSet(parm.ReqFindClient)) AND (ParmIsSet(parm.ReqFindClient.FullName)))
       rp.ClientFullName = parm.ReqFindClient.FullName;
     ELSE
       rp.ClientFullName = client.FullName;
     END;            
   END;    
   
   // Информирования пользователей, ответственных за обмен
   InfoUser(parm, EnsContrID_List, rp.ClientFullName);

   RETURN rp;

OnError(ex)
   response.ReqID       = ReqID;    // Из запроса
   response.ClientID    = ClientID; // Идентификатор клиента.
   response.faultType   = 1;        // Тип ошибки
   response.faultCode   = ex.Code;  // Код ошибки
   response.faultString = "information.mac. Строка: " + ex.line + ", Сообщение: " + ex.Message; // Текст ошибки

   RETURN response;
END;