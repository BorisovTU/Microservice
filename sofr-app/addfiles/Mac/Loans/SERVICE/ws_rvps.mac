/*
$Name:             ws_rvps.mac 
$Module:           Кредитование
$Description:      Пакетное формирование РВПС
*/
//
IMPORT SbCrdInter, BankInter,
       "lgo_opstruct.mac", "lgo_const.mac", "lgo_func.mac", total, RsbDataSet;

macro OperationRVPS(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var
      stat = 0, PackName = "", 
      FlagOpt = true, rs = NULL, MacroFun = "",
      Rec = TBfile("rezdata.tmp", "W");
   PRIVATE VAR ErrManager:CErrManager;
   VAR BatchOperID = 0, errid = 0, errtxt = "";
   //Проверяем есть ли в пачке хоть один объект
   VAR cnt1 = 
   CRSDCommand("SELECT COUNT(1) FROM DCNVDOC_DBT WHERE T_EXECID = ? AND  T_CONVTYPEID = ? AND T_PACKID = ? AND T_TASKID = ?",
              "p1", _execID, 
              "p2", _conveyerID, 
              "p3", _packetID, 
              "p4", _taskID,
              V_INTEGER);
   IF (cnt1 == 0)
      RETURN 0;
   END;              

   // ==> Если запись есть, значит пачка уже обработана, выход
   VAR cnt = 
   CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_BATCHOP_DBT WHERE T_EXECID = ? AND  T_CONVTYPEID = ? AND T_PACKID = ? AND T_OPERATIONID = ?",
              "p1", _execID, 
              "p2", _conveyerID, 
              "p3", _packetID, 
              "p4", _operationID,
              V_INTEGER);
   IF (cnt > 0)
      LgoSqlExec("UPDATE DCNVPACK_DBT SET T_STATE = T_STATE + 1 WHERE T_TASKID = ? AND T_EXECID = ? AND T_PACKID = ? AND T_CONVTYPEID = ?",
                 _taskID, _execID, _packetID, _conveyerID);
      RETURN 0;
   END;
   // <==

   IF (NOT OpenLoansCommonFiles())
      LoansError("Ошибка открытия файлов");
      RETURN FALSE;
   END;

   LgoSqlExec("TRUNCATE TABLE DREZDATA_TMP"); 
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");
   LgoSqlExec("TRUNCATE TABLE DREZ_ERR_TMP");
   LgoSqlExec("TRUNCATE TABLE DAPRREZSUM_TMP");

   LgoSqlExec("begin Loansfillpayment.RSO_FillRezSumForGroupRvps(?,?,?,?,?,?,?,?,?); end;",
              _taskID, _execID, _conveyerID, _packetID, 
              _Params.OperType, _Params.OperDate, _Params.Protocol, _Params.CheckVal, _Params.Regulaterez);

   GetRegistryValue("RS-LOANS\\ОПТИМИЗИРОВАТЬ_РВПС", V_BOOL, FlagOpt, stat);
   if (FlagOpt)
       LgoSqlExec("TRUNCATE TABLE DSET_RESCONSTR_TMP");
       GetRegistryValue("RS-LOANS\\РЕЗЕРВЫ_ХП\\ХП_РВПС_ПАКЕТНАЯ", V_STRING, PackName, stat);

       LgoRunStoredFunc(PackName, V_INTEGER, NULL, _Params.OperDate, _Params.OperType, _Params.Protocol, _Params.CheckVal,
                       _Params.CaseTC_StdCrd_Op, _Params.CaseTC_CessCrd_Op, _Params.CaseTC_Guar_Op, _Params.CaseTC_OV_Op, _Params.CaseTC_Karta_Op);
   else
       InstLoadModule("rez_flag.mac");
       InstLoadModule("rez_sum.mac");

       Rec.Clear;
       Rec.Rewind;
       while (Rec.Next)
           Rec.rec.FlagCost = "";
           if (ExecMacro2("ПризнакОтнесенияНаСебестоимость", Rec.rec.ObjID, Rec.rec.ObjN, 0, 0, _Params.OperDate))
               Rec.rec.FlagCost = "X";
           end;

           if ((_Params.OperType == CF_REZ_PREDACQUIRING_DEM)or(_Params.OperType == CF_REZ_DEBTS))
               MacroFun = "РасчетРезерваПредоплаты";
           elif (Rec.rec.ObjID == LO_CASE)
               MacroFun = "РасчетРезерваПортфеля_РВПС";
           else
               MacroFun = "РасчетРезерва_РВПС";
           end;

           if (not (ExecMacro2(MacroFun, Rec.rec.ObjID, Rec.rec.ObjN, _Params.OperDate,
                                          @Rec, false, IIF(_Params.CaseOp == "X", true, false), 
                                          false, IIF(_Params.CheckVal == "X", true, false))))
               continue;
           end;

           Rec.rec.D_d  = 0.0;
           Rec.rec.C_d  = 0.0;
           Rec.rec.D_pr = 0.0;
           Rec.rec.C_pr = 0.0;
           if (Rec.rec.S_res_d_1 < Rec.rec.S_res_d_2)
              Rec.rec.D_d = Rec.rec.S_res_d_2 - Rec.rec.S_res_d_1;
           end;
           if (Rec.rec.S_res_d_1 > Rec.rec.S_res_d_2)
              Rec.rec.C_d = Rec.rec.S_res_d_1 - Rec.rec.S_res_d_2;
           end;
           if (Rec.rec.S_res_pr_1 < Rec.rec.S_res_pr_2)
              Rec.rec.D_pr = Rec.rec.S_res_pr_2 - Rec.rec.S_res_pr_1;
           end;
           if (Rec.rec.S_res_pr_1 > Rec.rec.S_res_pr_2)
              Rec.rec.C_pr = Rec.rec.S_res_pr_1 - Rec.rec.S_res_pr_2;
           end;
           Rec.Update();
       end;
   end;

   if ((_Params.Protocol == 1) or (_Params.Protocol == 4))
        LgoSqlExec("insert INTO DLNSERROR_DBT (T_CNUM,T_OBJID, T_OBJN, T_CREDOPERID,  T_ERRORCODE,  T_ERROR) " +
                   " ( " +
                   "select ?,T_OBJID, T_OBJN, 0 as T_CREDOPERID, 0 as T_ERRORCODE, 'SKIP' as T_ERROR FROM DREZDATA_TMP " +
                  "WHERE " +
                  " ((T_FlagCost != 'X' AND T_FlagCostOld != 'X') OR (T_FlagCost  = 'X' AND T_FlagCostOld = 'X')) " +
                  " AND " +
                  "( " +
                  " T_C_d = 0 AND T_D_d = 0 AND T_C_pr = 0 AND T_D_pr = 0 " +
                  " AND T_CalcPerc = T_CurrentSumPerc AND T_CalcExpPerc =  T_CurrentSumExpPerc AND T_CurrentSumKomiss = T_CalcKomiss " +
                  " AND T_S_OFRES_D_1   = 0 AND T_S_OFRES_D_2   = T_S_OFRES_D_1  AND T_S_OFRES_PR_1 = 0 AND T_S_OFRES_PR_1  = T_S_OFRES_PR_2 " +
                  ") " +
                  " OR " +
                  "( " +
                  "((T_FlagCost  = 'X' AND T_FlagCostOld != 'X') OR (T_FlagCost != 'X' AND T_FlagCostOld  = 'X')) " +
                  " AND " +
                  "( " +
                  " T_S_res_d_1   = 0 AND T_S_res_d_1   = T_S_res_d_2  AND T_S_res_pr_1  = T_S_res_pr_2 AND " +
                  " T_CalcPerc    = T_CurrentSumPerc AND T_CalcExpPerc = T_CurrentSumExpPerc AND T_CurrentSumKomiss = T_CalcKomiss " +
                  ") " +
                  ")"+
                  "AND"+
                  "("+
                   " T_S_OFRES_D_1            = T_S_OFRES_D_2"          +
               " AND T_S_OFRES_PR_1           = T_S_OFRES_PR_2"         +
               " AND T_S_OFRES_CALCPERCRES    = T_S_OFRES_CURPERCRES"   +
               " AND T_S_OFRES_CALCEXPPERCRES = T_S_OFRES_CUREXPPERCRES"+
               " AND T_S_OFRES_CALCCOMRES     = T_S_OFRES_CURCOMRES"    +
                  ")" +
                 " )",
                   _execID);                     
   end;   
   
   if ((_Params.Protocol == 1) or (_Params.Protocol == 3))
       LgoSqlExec("DELETE FROM DREZDATA_TMP " +
                  "WHERE " +
                  " ((T_FlagCost != 'X' AND T_FlagCostOld != 'X') OR (T_FlagCost  = 'X' AND T_FlagCostOld = 'X')) " +
                  " AND " +
                  "( " +
                  " T_C_d = 0 AND T_D_d = 0 AND T_C_pr = 0 AND T_D_pr = 0 " +
                  " AND T_CalcPerc = T_CurrentSumPerc AND T_CalcExpPerc =  T_CurrentSumExpPerc AND T_CurrentSumKomiss = T_CalcKomiss " +
                  " AND T_S_OFRES_D_1   = 0 AND T_S_OFRES_D_2   = T_S_OFRES_D_1  AND T_S_OFRES_PR_1 = 0 AND T_S_OFRES_PR_1  = T_S_OFRES_PR_2 " +
                  ") " +
                  " OR " +
                  "( " +
                  "((T_FlagCost  = 'X' AND T_FlagCostOld != 'X') OR (T_FlagCost != 'X' AND T_FlagCostOld  = 'X')) " +
                  " AND " +
                  "( " +
                  " T_S_res_d_1   = 0 AND T_S_res_d_1   = T_S_res_d_2  AND T_S_res_pr_1  = T_S_res_pr_2 AND " +
                  " T_CalcPerc    = T_CurrentSumPerc AND T_CalcExpPerc = T_CurrentSumExpPerc AND T_CurrentSumKomiss = T_CalcKomiss " +
                  ") " +
                  ")"+
                  "AND"+
                  "("+
                   " T_S_OFRES_D_1            = T_S_OFRES_D_2"          +
               " AND T_S_OFRES_PR_1           = T_S_OFRES_PR_2"         +
               " AND T_S_OFRES_CALCPERCRES    = T_S_OFRES_CURPERCRES"   +
               " AND T_S_OFRES_CALCEXPPERCRES = T_S_OFRES_CUREXPPERCRES"+
               " AND T_S_OFRES_CALCCOMRES     = T_S_OFRES_CURCOMRES"    +
                  ")");
   end;

   if (_Params.BatchObjN == 0)
       _Params.BatchObjK = LO_DEPARTMENT;
       _Params.BatchObjN = _Params.FNCash;
   end;

   BatchOperID = СформироватьПакетнуюОперацию(_Params.OperType, _Params.OperDate, _Params.CarryDate, _execID, _conveyerID, _packetID, _operationID, _Params.BatchObjK, _Params.BatchObjN);

   LgoSqlExec("TRUNCATE TABLE DREZDATA_TMP"); 
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");
   LgoSqlExec("TRUNCATE TABLE DREZ_ERR_TMP");
   LgoSqlExec("TRUNCATE TABLE DAPRREZSUM_TMP");

   CloseLoansCommonFiles();

   LgoSqlExec("update dcnvpack_dbt set t_State = T_State + 1 where t_TaskID = ? and t_ExecID = ? and t_PackID = ? and t_ConvTypeID = ?",
              _taskID, _execID, _packetID, _conveyerID);

   IF (BatchOperID <= 0)
      errid = GetMO_ErrorID();
      
      // SCR 198708
      IF (errid == 18765)
         RETURN 0;
      END;

      IF (errid <= 0)
         errid = 18644;
      END;

      errtxt = GetMO_LoansError();

      IF (Trim(errtxt) == "")
         errtxt = "Ошибка выполнения операции";
      END;

      RunError(errtxt, errid);
   END;

   RETURN 0;
   //сюда попадем, если было выброшено исключение.
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         RunError(err.Err, err.Message);
      ELSE
         RunError(18644, "Ошибка выполнения операции");
      END;
END;
