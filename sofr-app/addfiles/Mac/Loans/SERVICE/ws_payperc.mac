/*
$Name:             ws_payperc.mac
$Module:           Кредитование
$Description:      Конвейер начисление
*/

IMPORT SbCrdInter,
       "lgo_opstruct.mac", "lgo_const.mac", "lgo_func.mac", total;

macro OperationPerc(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
   PRIVATE VAR ErrManager:CErrManager;
   VAR BatchOperID = 0, errid = 0, errtxt = "";

   // ==> Если запись есть, значит пачка уже обработана, выход
   VAR cnt = 
   CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_BATCHOP_DBT WHERE T_EXECID = ? AND  T_CONVTYPEID = ? AND T_PACKID = ? AND T_OPERATIONID = ?",
              "p1", _execID, 
              "p2", _conveyerID, 
              "p3", _packetID, 
              "p4", _operationID,
              V_INTEGER);
   IF (cnt > 0)
      LgoSqlExec("UPDATE DCNVPACK_DBT SET T_STATE = T_STATE + 1 WHERE T_TASKID = ? AND T_EXECID = ? AND T_PACKID = ? AND T_CONVTYPEID = ?",
                 _taskID, _execID, _packetID, _conveyerID);
      RETURN 0;
   END;
   // <==

   IF (NOT OpenLoansCommonFiles())
      LoansError("Ошибка открытия файлов");
      RETURN FALSE;
   END;

   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DLCHSUMRG_TMP"); 
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   IF (_Params.InitStruct.CalcDataForOp)
      LgoSqlExec("TRUNCATE TABLE DPERCPAY_TMP");
      LgoSqlExec("TRUNCATE TABLE DPERCRANG_TMP"); 
      LgoSqlExec("TRUNCATE TABLE DPAYMENT_TMP");

      // Заданы параметры
      IF ((GenPropID(_Params, "Choice") != -1) AND (GenPropID(_Params, "Protocol") != -1))

         VAR RS = RsdCommand(RslDefCon, "BEGIN LoansStruct.RSO_SetParmOperation(?, ?, ?); END;");

         RS.AddParam("CNum",     RSDBP_IN); RS.value("CNum")     = GetCNum();
         RS.AddParam("Choice",   RSDBP_IN); RS.value("Choice")   = _Params.Choice;
         RS.AddParam("Protocol", RSDBP_IN); RS.value("Protocol") = _Params.Protocol;

         RS.execute();
      END;

      LgoSqlExec("begin Loansfillpayment.RSO_FillPaymentForGroupPercOp(?,?,?,?,?,?,?,?,?); END;",
                  _taskID, _execID, _conveyerID, _packetID, 
                  _Params.OperType, 
                  _Params.OperDate, 
                  _Params.CalcDate, 
                  _Params.Graph, 
                  _Params.addgraff);

      CalcGroupPercent(_Params.OperDate, _Params.CalcDate, _Params.Graph);
   END;

   VAR RS_OP = CRSDCommand("SELECT DISTINCT T_TYPEOPERNUMBER FROM DPAYMENT_TMP ORDER BY 1", V_GENOBJ);
   WHILE (RS_OP.MoveNext)

      _Params.OperType = CRSDCommand("SELECT T_TYPEOPERNUMBER FROM DTYPE_OP_DBT WHERE T_TYPEOPERID = ?", "p1", RS_OP.Value(0), V_INTEGER);

      BatchOperID = СформироватьПакетнуюОперацию(_Params.OperType, 
                                                 _Params.OperDate, 
                                                 _Params.CarryDate, 
                                                 _execID, 
                                                 _conveyerID, 
                                                 _packetID, 
                                                 _operationID);

      // Отладочная информация, нужно знать какая ошибка или операция сформировались
      LgoSqlExec("SELECT " + BatchOperID + " AS BatchOperID FROM DUAL");
   END;

   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DLCHSUMRG_TMP"); 
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   IF (_Params.InitStruct.CalcDataForOp)
      LgoSqlExec("TRUNCATE TABLE DPERCPAY_TMP");
      LgoSqlExec("TRUNCATE TABLE DPERCRANG_TMP"); 
   END;

   CloseLoansCommonFiles();

   VAR RS2 = RsdCommand(RslDefCon, "BEGIN LoansStruct.RSO_ClearParmOperation(?); END;");
   RS2.AddParam("CNum", RSDBP_IN); RS2.value("CNum") = GetCNum();
   RS2.execute();

   LgoSqlExec("update dcnvpack_dbt set t_State = T_State + 1 where t_TaskID = ? and t_ExecID = ? and t_PackID = ? and t_ConvTypeID = ?",
              _taskID, _execID, _packetID, _conveyerID);

   IF (BatchOperID <= 0)
      errid = GetMO_ErrorID();
      
      // SCR 198708
      IF (errid == 18765)
         RETURN 0;
      END;

      IF (errid <= 0)
         errid = 18644;
      END;

      errtxt = GetMO_LoansError();

      IF (Trim(errtxt) == "")
         errtxt = "Ошибка выполнения операции";
      END;

      RunError(errtxt, errid);
   END;

   RETURN 0;
   //сюда попадем, если было выброшено исключение.
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         RunError(err.Err, err.Message);
      ELSE
         RunError(18644, "Ошибка выполнения операции");
      END;
END;

// Запустить Count потоков с задержкой Delay
MACRO RunThread(NumberSession : integer,
                NumberPackage : integer,
                Count         : integer,
                Delay         : integer)
   VAR t1 = Time, T2 = 0, C, GetC = GetCNum();

   // Есть ли главное задание ?
   C = LnSelectValue("SELECT COUNT(1) FROM DWS_DBT WHERE T_TASKID = 0", V_INTEGER);
   IF (C == 0)
      RETURN;
   END;

   C = LnSelectValue("SELECT COUNT(1) FROM DWS_DBT WHERE T_CNUM = " + getC, V_INTEGER);
   IF (C == 0)
      LnSQLExec("INSERT INTO DWS_DBT (T_CNUM, T_TASKID) VALUES (" + GetC + "," + NumberPackage + ")");
      RETURN;
   END;

   C = LnSelectValue("SELECT COUNT(DISTINCT T_CNUM) FROM DWS_DBT WHERE T_TASKID > 0", V_INTEGER);
   IF (C >= Count)
      // Удалим задания и на выход
      // LnSQLExec("DELETE FROM DWS_DBT");
      RETURN;
   END;

   Sleep(Delay);

   RETURN 0;
END;