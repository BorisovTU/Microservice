/*
$Name:             status.mac
$Module:           Кредитование
$Description:      Работа с уведомлениями Запрос статуса
*/
import XmlRpcInter, RSD, loans_fnp, ServiceAction;

MACRO Receive(URL, ID)
   VAR RS =
   CRSDCommand("SELECT A1.T_AUTHORIZATION, A2.T_TYPENOTICE, A1.T_CHECK FROM DLIST_FNP_DBT A1 JOIN DNOTICE_DBT A2 ON (A1.T_ID = A2.T_SERVICEID) "+
               "WHERE A2.T_ID = ?",
               "p1", ID,
               V_GENOBJ);
   RS.MoveNext();

VAR hdr: RSCFNPSource;
   hdr.MessageID     = CreateGUID();
   hdr.SenderID      = SQL_NVL(RS.Value("T_AUTHORIZATION"), V_STRING);
   hdr.SenderPointID = {OperDprt};

   VAR timeOut = 0;
   VAR outParams;
   VAR encoding;
   VAR reqId = CreateGUID( );
   VAR outResponse;
   VAR errMsg;

   VAR NotifList = RSCFNPRequestList();
   NotifList.ObjectIdList = TArray;

   VAR CT = RSCFNPObjectIdParam();

   CT.OrigNotificationId = ID;
   CT.ObjectId           = NULL; // Передается уникальный идентификатор уведомления (ObjectID), присвоенный ему при создании в RS-Connect

   IF   (SQL_NVL(RS.Value("T_TYPENOTICE"), V_INTEGER) == 1)
      CT.ObjectType         = "FormUZ1";
   ELIF (SQL_NVL(RS.Value("T_TYPENOTICE"), V_INTEGER) == 2)
      CT.ObjectType         = "FormUI1";
   ELIF (SQL_NVL(RS.Value("T_TYPENOTICE"), V_INTEGER) == 3)
      CT.ObjectType         = "FormUP1";
   END;

   VAR ExCondition = RSCFNPExCondition();
   ExCondition.ExConditionCode = "";
   IF (SQL_NVL(RS.Value("T_CHECK"), V_STRING) == "X")
      ExCondition.ExConditionCode = "1";
   ELSE
      ExCondition.ExConditionCode = "2";
   END;

   ExCondition.ExConditionText = "";
   CT.ExCondition = ExCondition;
   NotifList.ObjectIdList[0] = CT;

   VAR paramArray = TArray();
   paramArray[0]  = hdr;
   paramArray[1]  = NotifList;

   VAR xmlReq = TXRRequest();
   xmlReq.reqId = CreateGUID();
   xmlReq.macroName = "ws_fnp_getstatus";
   xmlReq.macroFunc = "GetNotificationStatus";
   xmlReq.Parms     = paramArray;
   xmlReq.direction = "1";

   // Входные параметры
   VAR inParams = TArray();
   VAR inParam_request = RslSoapPrm();
   inParam_request.Name = "request";
   inParam_request.Value = xmlReq.GenerateMessage(false);
   inParams[0] = inParam_request;

   VAR inParam_timeout = RslSoapPrm();
   inParam_timeout.Name = "timeout";
   inParam_timeout.Value = timeOut;
   inParams[1] = inParam_timeout;

   VAR inParam_priority = RslSoapPrm();
   inParam_priority.Name = "priority";
   inParam_priority.Value = 0;
   inParams[2] = inParam_priority;

   BegAction(2000, "Запрос статуса уведомления...", TRUE);
   VAR stat = CallSimpleExtService( reqId, URL, "XMLRPCCall", inParams, "", "", timeOut, outParams, encoding, outResponse, errMsg );
   EndAction();

   PrintLn("stat:",stat);
   VAR xmlOut = CreateXMLParser( );

   xmlOut.validateOnParse = true;
   xmlOut.async = false;

   VAR res = xmlOut.loadXML(outResponse);

   IF (not res)
      MsgBox("Ошибка разбора ответа SOAP");
   END;

   VAR XMLRPCCallResult = xmlOut.getElementsByTagName( "XMLRPCCallResult" );
   VAR XMLResult = XMLRPCCallResult.item(0).text;
   res = xmlOut.loadXML(XMLResult);
   IF (not res)
      MsgBox("Ошибка разбора ответа XML-RPC");
   END;
   VAR Obj;
   ConvertToRsl(XMLResult, obj);

   VAR faultTag = xmlOut.getElementsByTagName("fault");

   IF (faultTag.length != 0)
      MsgBoxEx("Ошибка времени выполнения на стороне Rs-Connect.|" + obj.faultString );
   ELSE
      // Успешное выполнение
      FOR (VAR item, Obj.ResultList)
         IF (ValType(item.Response) != V_UNDEF)
            // Класс RSCFNPResponseNotification - данные по уведомлению
        VAR O2  = item.Response;
            var O21 = item.Response.ResultNotificationData ; // Результирующие сведения по обработке уведомления, полученные из ФНП (заполняется в случае получения сведений)    1
            var O22 = O2.RejectReason           ; // Причина отказа (если уведомление отклонено)                                                                      0-1
            var O23 = O2.StatusCode             ; // Код статуса обработки запроса (конкретное содержание статусов согласуется на этапе проектирования)               1
            var O24 = O2.StatusName             ; // Наименование текущего статуса                                                                                    1

            VAR Exists = CRSDCommand("SELECT '1' FROM DNOTSTATUS_DBT WHERE T_NOTICEID = ? AND T_STATUSCODE = ?",
            "p1", ID,
            "p2", O23,
            V_STRING);

            IF (Exists == "")
               CRSDCommand("INSERT INTO DNOTSTATUS_DBT (T_NOTICEID, T_STATUSDATE, T_STATUSTIME, T_STATUSCODE, T_STATUSNAME) " +
                           "VALUES (?,?,?,?,?)",
                           "p1", ID,
                           "p2", {curdate},
                           "p3", time(),
                           "p4", O23,
                           "p5", O24,
                           V_GENOBJ);

               CRSDCommand(
               "UPDATE DNOTICE_DBT SET T_STATUSNOTICE = ? WHERE T_ID = ?",
               "p1", int(O23),
               "p2", ID,
               V_GENOBJ);

               MsgBoxEx("Получен статус уведомления " + O23 + ":" + O24);
            ELSE
               MsgBoxEx("Статус уведомления не изменился!");
            END;

            // Класс RSCFNPResultNotificationData - Результирующие сведения по обработке уведомления, полученные из ФНП
            VAR O211 = O21.RegistrationCertificateNotaryData; // RSCFNPRegistrationCertificateNotaryData; //Данные свидетельства, заполняемые нотариусом    1
            VAR O212 = O21.RegistrationCertificateData      ; // RSCFNPRegistrationCertificateData;       //Основные данные свидетельства                   1
     
            IF (ValType(O212.NotificationReferenceNumber) != V_UNDEF) // Регистрационный номер. Заполняется для свидетельства
               
               CRSDCommand(
               "UPDATE DNOTICE_DBT SET T_REGNUM = ? WHERE T_ID = ?",
               "p1", O212.NotificationReferenceNumber,
               "p3", ID,
              V_GENOBJ);
            END;

            IF (ValType(O212.RegistrationTime) != V_UNDEF) // Дата и время регистрации. Заполняется для свидетельства
               VAR dt, tm;
               DtTmSplit (O212.RegistrationTime, dt, tm );

               CRSDCommand(
               "UPDATE DNOTICE_DBT SET T_REGISTRATIONDATE = ?, "+
                                     " T_REGISTRATIONTIME = ?  "+
               " WHERE T_ID = ?",
               "p1", dt,
               "p2", tm,
               "p3", ID,
              V_GENOBJ);
            END;

            // Класс RSCFNPRegistrationCertificateNotaryData - Данные свидетельства, заполняемые нотариусом
            VAR IssuePlace          = O211.IssuePlace          ; // Место выдачи свидетельства или отказа                                              1
            VAR IssueDate           = O211.IssueDate           ; // Дата выдачи свидетельства или отказа                                               1
            VAR NotaryName          = O211.NotaryName          ; // ФИО выдавшего свидетельство или отказ (нотариуса или врио)                         1
            VAR NotaryJobTitle      = O211.NotaryJobTitle      ; // Должность выдавшего свидетельство или отказ, включая округ (нотариуса или врио)    1
            VAR NotaryJobTitleShort = O211.NotaryJobTitleShort ; // Должность выдавшего свидетельство или отказ (нотариуса или врио)                   1
            VAR NotaryID            = O211.NotaryID            ; // ID нотариуса                                                                       1
            VAR SignerID            = O211.SignerID            ; // ID подписавшего документ (нотариуса или врио)                                      1
            VAR RegistryNumber      = O211.RegistryNumber      ; // Реестровый номер свидетельства или отказа                                          1
            VAR Tariff              = O211.Tariff              ; // Взыскано госпошлины (по тарифу), в случае отказа, поле скрывается                  0-1
            VAR Recovered           = O211.Recovered           ; // Взыскано за техническую и правовую работу                                          0-1
            
            IF (ValType(O22) != V_STRING)
               O22 = "";
            END;

            IF (ValType(IssuePlace         ) == V_UNDEF)
               IssuePlace = ""; //Место выдачи свидетельства или отказа
            END;

            IF (ValType(IssueDate          ) == V_UNDEF)
               IssueDate = date(0,0,0);   //Дата выдачи свидетельства или отказа
            END;

            IF (ValType(NotaryName         ) == V_UNDEF)
               NotaryName = ""; //ФИО выдавшего свидетельство или отказ (нотариуса или врио)
            END;

            IF (ValType(NotaryJobTitle     ) == V_UNDEF)
               NotaryJobTitle = ""; //Должность выдавшего свидетельство или отказ, включая округ (нотариуса или врио)
            END;
                        
            IF (ValType(NotaryJobTitleShort) == V_UNDEF)
               NotaryJobTitleShort = ""; //Должность выдавшего свидетельство или отказ (нотариуса или врио)
            END;
                        
            IF (ValType(NotaryID           ) == V_UNDEF)
               NotaryID = ""; //ID нотариуса
            END;
                        
            IF (ValType(SignerID           ) == V_UNDEF)
               SignerID = ""; //ID подписавшего документ (нотариуса или врио)
            END;
                        
            IF (ValType(RegistryNumber     ) == V_UNDEF)
               RegistryNumber = ""; //Реестровый номер свидетельства или отказа
            END;
                        
            IF (ValType(Tariff             ) == V_UNDEF)
               Tariff = 0;  //Взыскано госпошлины (по тарифу), в случае отказа, поле скрывается
            END;
                        
            IF (ValType(Recovered          ) == V_UNDEF)
               Recovered = 0; // Взыскано за техническую и правовую работу
            END;

            IF ((ValType(O211.IssuePlace         ) != V_UNDEF) OR
                (ValType(O211.IssueDate          ) != V_UNDEF) OR
                (ValType(O211.NotaryName         ) != V_UNDEF) OR
                (ValType(O211.NotaryJobTitle     ) != V_UNDEF) OR
                (ValType(O211.NotaryJobTitleShort) != V_UNDEF) OR
                (ValType(O211.NotaryID           ) != V_UNDEF) OR
                (ValType(O211.SignerID           ) != V_UNDEF) OR
                (ValType(O211.RegistryNumber     ) != V_UNDEF) OR
                (ValType(O211.Tariff             ) != V_UNDEF) OR
                (ValType(O211.Recovered          ) != V_UNDEF) OR
                (ValType(O2.RejectReason         ) != V_UNDEF))

                Exists = CRSDCommand("SELECT 1 FROM DFNP_REGISTER_DBT WHERE T_NOTICEID = ?",
               "p1", ID,
              V_INTEGER);

               IF (Exists == 1)
                  CRSDCommand(
                  "UPDATE DFNP_REGISTER_DBT SET "+
                  "T_REGISTRYNUMBER = ?, T_ISSUEDATE = ?, "+
                  "T_ISSUEPLACE = ?, T_REJECTREASON = ?, T_NOTARYNAME = ?, "+
                  "T_NOTARYJOBTITLE = ?, T_NOTARYJOBTITLESHORT = ?, T_NOTARYID = ?, "+
                  "T_SIGNERID = ?, T_TARIFF = ?, T_RECOVERED = ?"+
                  "WHERE T_NOTICEID = ?",
                  "p2",  RegistryNumber,
                  "p3",  IssueDate,
                  "p4",  IssuePlace,
                  "p5",  O22,
                  "p6",  NotaryName,
                  "p7",  NotaryJobTitle,
                  "p8",  NotaryJobTitleShort,
                  "p9",  NotaryID,
                  "p10", SignerID,
                  "p11", Tariff,
                  "p12", Recovered,
                  "p1",  ID,
                 V_GENOBJ);

               ELSE
                
                  CRSDCommand(
                  "INSERT INTO DFNP_REGISTER_DBT ("+
                  "T_NOTICEID, T_REGISTRYNUMBER, T_ISSUEDATE, "+
                  "T_ISSUEPLACE, T_REJECTREASON, T_NOTARYNAME, "+
                  "T_NOTARYJOBTITLE, T_NOTARYJOBTITLESHORT, T_NOTARYID, "+
                  "T_SIGNERID, T_TARIFF, T_RECOVERED)"+
                  "VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)",
                  "p1", ID,
                  "p2", RegistryNumber,
                  "p3", IssueDate,
                  "p4", IssuePlace,
                  "p5", O22,
                  "p6", NotaryName,
                  "p7", NotaryJobTitle,
                  "p8", NotaryJobTitleShort,
                  "p9", NotaryID,
                  "p10", SignerID,
                  "p11", Tariff,
                  "p12", Recovered,
                 V_GENOBJ);
               END;
            END;
         ELSE
            PrintLn(item.ExecError.ReturnCode+":"+item.ExecError.ReturnText);
            MsgBoxEx("Ошиба получения статуса!");
            MsgBoxEx(item.ExecError.ReturnCode+":"+item.ExecError.ReturnText);
         END;
      END;
   END;

END;