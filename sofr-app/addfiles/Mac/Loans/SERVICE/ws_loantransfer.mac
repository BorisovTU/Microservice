/*
$Name:             ws_loantransfer.mac
$Module:           Кредитование
$Description:      Пакетный перевод ссуд
*/
IMPORT SbCrdInter, RsbDataSet,
       "lgo_opstruct.mac", "lgo_const.mac", "lgo_func.mac", total;

macro OperationLoanTransfer(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var
      CalcPercDown = 0, CalcPercUp = 0, SkipStep = true, rs = NULL, stat = false;
   PRIVATE VAR ErrManager:CErrManager;
   VAR BatchOperID = 0, errid = 0, errtxt = "";

   // ==> Если запись есть, значит пачка уже обработана, выход
   VAR cnt = 
   CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_BATCHOP_DBT WHERE T_EXECID = ? AND  T_CONVTYPEID = ? AND T_PACKID = ? AND T_OPERATIONID = ?",
              "p1", _execID, 
              "p2", _conveyerID, 
              "p3", _packetID, 
              "p4", _operationID,
              V_INTEGER);
   IF (cnt > 0)
      LgoSqlExec("UPDATE DCNVPACK_DBT SET T_STATE = T_STATE + 1 WHERE T_TASKID = ? AND T_EXECID = ? AND T_PACKID = ? AND T_CONVTYPEID = ?",
                 _taskID, _execID, _packetID, _conveyerID);
      RETURN 0;
   END;
   // <==

   IF (NOT OpenLoansCommonFiles())
      LoansError("Ошибка открытия файлов");
      RETURN FALSE;
   END;

   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DLCHSUMRG_TMP");
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   IF ((_Params.CalcPercDown) OR (_Params.CalcPercUp))
      IF (_Params.CalcPercDown == 1)
          CalcPercDown = 1;
      END;

      IF (_Params.CalcPercUp == 1)
          CalcPercUp = 1;
      END;

      SkipStep = false;
   END;

   IF (_Params.InitStruct.CalcDataForOp)
      LgoSqlExec("TRUNCATE TABLE DTRANSFER_TMP");
      LgoSqlExec("TRUNCATE TABLE DPERCPAY_TMP");
      LgoSqlExec("TRUNCATE TABLE DPAYMENT_TMP");
      LgoSqlExec("TRUNCATE TABLE DPERCRANG_TMP");
      LgoSqlExec("TRUNCATE TABLE DGROUPRSK_TMP");

      LgoSqlExec("begin RSI_LoansRisk.FillGroup_Pay_For_CaseGr(?,?,?,?,?,?,?); end;",
                   _taskID, _execID, _conveyerID, _packetID,
                   _Params.OperDate, _Params.Fiz, _Params.Jur);

      LgoSqlExec("begin RSI_LoansRisk.CalcRiskGroup(?); end;",
                   _Params.OperDate);

      IF (NOT SkipStep)
         LgoSqlExec("begin LoansFillPayment.RSI_FillPaymentForGroupRiskOp(?,?,?,?,?); end;",
                    CF_RISK, _Params.OperDate, _Params.CalcDate, CalcPercDown, CalcPercUp);
      END;
   END;

   rs = TRsbDataSet("select t.T_BRIEFCASEID as CaseFromID, t.T_GROUPIN as CaseToID from DTRANSFER_TMP t, DCNVDOC_DBT d WHERE " +
                          " d.T_OBJECTTYPE = " + LO_CASE +
                      " AND d.T_OBJECTID   = DECODE (t.T_REFERENCEID, 0, t.T_BRIEFCASEID, t.T_REFERENCEID) " +
                      " AND d.T_TASKID     = " + _taskID +
                      " AND d.T_EXECID     = " + _execID +
                      " AND d.T_CONVTYPEID = " + _conveyerID +
                      " AND d.T_PACKID     = " + _packetID +
                      " GROUP BY t.t_BRIEFCASEID, t.t_GROUPIN");
   stat = rs.moveNext();
   IF (stat)
      BatchOperID = -1;
      WHILE (stat)
         BatchOperID = СформироватьПакетнуюОперацию(CF_RISK, _Params.OperDate, _Params.CarryDate, _execID, _conveyerID, _packetID, _operationID, rs.CaseFromID, rs.CaseToID, SkipStep);
         stat = rs.moveNext();
      END;
   ELSE
      BatchOperID = 0;
   END;

   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DLCHSUMRG_TMP");
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   IF (_Params.InitStruct.CalcDataForOp)
      LgoSqlExec("TRUNCATE TABLE DPERCPAY_TMP");
      LgoSqlExec("TRUNCATE TABLE DPERCRANG_TMP");
   END;
   CloseLoansCommonFiles();

   LgoSqlExec("update dcnvpack_dbt set t_State = T_State + 1 where t_TaskID = ? and t_ExecID = ? and t_PackID = ? and t_ConvTypeID = ?",
              _taskID, _execID, _packetID, _conveyerID);

   IF (BatchOperID < 0)
      errid = GetMO_ErrorID();
      
      // SCR 198708
      IF (errid == 18765)
         RETURN 0;
      END;

      IF (errid <= 0)
         errid = 18644;
      END;

      errtxt = GetMO_LoansError();

      IF (Trim(errtxt) == "")
         errtxt = "Ошибка выполнения операции";
      END;

      RunError(errtxt, errid);
   END;

   RETURN 0;
   //сюда попадем, если было выброшено исключение.
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         RunError(err.Err, err.Message);
      ELSE
         RunError(18644, "Ошибка выполнения операции");
      END;
END;
