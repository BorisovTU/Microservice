/*
$Name:             begin.mac
$Module:           Кредитование
$Description:      Работа с уведомлениями Возникновение
*/
import XmlRpcInter, RSD, loans_fnp, ServiceAction;

MACRO Unload(urlAddress, ID)

////////////////////// TEST ////////////////////////////
VAR hdr: RSCFNPSource;

   VAR RSG = 
   CRSDCommand("SELECT A1.T_NAMESYSTEM, A1.T_AUTHORIZATION, A1.T_CHECK FROM DLIST_FNP_DBT A1 JOIN DNOTICE_DBT A2 ON (A1.T_ID = A2.T_SERVICEID) "+
               "WHERE A2.T_ID = ?",
               "p1", ID,
              V_GENOBJ);
   RSG.MoveNext();

   hdr.MessageID     = CreateGUID();
   hdr.SenderID      = SQL_NVL(RSG.Value("T_AUTHORIZATION"), V_STRING);
   hdr.SenderPointID = {OperDprt};

   VAR NL = RSCFNPNotificationList();
   NL.PledgeNotificationList = TArray;

   VAR UZ1 = RSCFNPPledgeNotificationUZ1();
   
   UZ1.OrigNotificationId = ID;  // Идентификатор уведомления в смежной системе

   // Условия обработки
   VAR CE: RSCFNPExCondition;
   CE.ExConditionCode = "";  // Код условий обработки (список кодов определяется на этапе проектирования реализации). Должен быть предусмотрен код "ручная обработка".
   IF (SQL_NVL(RSG.Value("T_CHECK"), V_STRING) == "X")
      CE.ExConditionCode = "1";
   ELSE
      CE.ExConditionCode = "2";
   END;

   CE.ExConditionText = "";  // Текстовое описание кода условий обработки

   UZ1.ExCondition = CE; // Условия обработки

   UZ1.PersonalProperties = GetPersonalProperty(ID);
   UZ1.Pledgors           = GetPledgor(ID);

   // Список Залогодержателей
   UZ1.Pledgees           = GetPledgees(ID);
   
   // Сведения о договоре залога, иной сделке, на основании которой возникает залог в силу закона
   UZ1.PledgeContract     = GetPledgeContract(ID);
   UZ1.NotificationApplicant = GetNotificationApplicant(ID);

   NL.PledgeNotificationList[0] = UZ1;

   VAR timeOut = 0;
   VAR outParams;
   VAR encoding;
   VAR reqId = CreateGUID( );
   VAR outResponse;
   VAR errMsg;

   VAR paramArray = TArray();
   paramArray[0] = hdr;
   paramArray[1] = NL;

   VAR xmlReq = TXRRequest();
   xmlReq.reqId = CreateGUID();
   xmlReq.macroName = "ws_fnp_savenotification";
   xmlReq.macroFunc = "SaveUZ1"; //"SendPNOccMovable";
   xmlReq.Parms     = paramArray;
   xmlReq.direction = "1";

   // Входные параметры
   VAR inParams = TArray();
   VAR inParam_request = RslSoapPrm();
   inParam_request.Name = "request";
   inParam_request.Value = xmlReq.GenerateMessage(false);
   inParams[0] = inParam_request;

   VAR inParam_timeout = RslSoapPrm();
   inParam_timeout.Name = "timeout";
   inParam_timeout.Value = timeOut;
   inParams[1] = inParam_timeout;

   VAR inParam_priority = RslSoapPrm();
   inParam_priority.Name = "priority";
   inParam_priority.Value = 0;
   inParams[2] = inParam_priority;

   BegAction(2000, "Идет выгрузка...", TRUE);
   VAR stat = CallSimpleExtService( reqId, urlAddress, "XMLRPCCall", inParams, "", "", timeOut, outParams, encoding, outResponse, errMsg );
   EndAction();
  
   PrintLn("stat:",stat);
   VAR xmlOut = CreateXMLParser( );

   xmlOut.validateOnParse = true;
   xmlOut.async = false;

   VAR res = xmlOut.loadXML(outResponse);

   IF (not res)
      MsgBox("Ошибка разбора ответа SOAP");
   END;

   VAR XMLRPCCallResult = xmlOut.getElementsByTagName( "XMLRPCCallResult" );
   VAR XMLResult = XMLRPCCallResult.item(0).text;
   res = xmlOut.loadXML(XMLResult);
   IF (not res)
      MsgBox("Ошибка разбора ответа XML-RPC");
   END;
   VAR Obj;
   ConvertToRsl(XMLResult, obj);

   VAR faultTag = xmlOut.getElementsByTagName("fault");
   IF (faultTag.length != 0)
      MsgBoxEx("Ошибка времени выполнения на стороне Rs-Connect.|" + obj.faultString );
   ELSE
      PrintLn("SenderID:"+Obj.SenderID);
      PrintLn("MessageSendID:"+Obj.MessageSendID);
      PrintLn("SenderPointID:"+Obj.SenderPointID);
      PrintLn("MessageRevID:"+Obj.MessageRevID);
      
      VAR item;
      // Успешное выполнение
      FOR( item, obj.ObjectList )
         IF (ValType(item.Response.ObjectID) != V_UNDEF)
            PrintLn(item.OrigNotificationId);
            PrintLn(item.Response.ObjectID);
            PrintLn(item.Response.ObjectType);
            PrintLn(item.Response.StatusCode);
            PrintLn(item.Response.StatusName);

            CRSDCommand(
            "INSERT INTO DNOTSTATUS_DBT (T_NOTICEID, T_STATUSDATE, T_STATUSTIME, T_STATUSCODE, T_STATUSNAME) "+
            "VALUES (?, ?, ?, ?, ?) ",
            "p1", ID,
            "p2", {curdate},
            "p3", time,
            "p4", item.Response.StatusCode,
            "p5", item.Response.StatusName,
            V_GENOBJ);

            CRSDCommand(
            "UPDATE DNOTICE_DBT SET T_STATUSNOTICE = ? WHERE T_ID = ?",
            "p1", int(item.Response.StatusCode),
            "p2", ID,
            V_GENOBJ);

            MsgBoxEx("Уведомление отправлено в " + SQL_NVL(RSG.Value("T_NAMESYSTEM"), V_STRING));

         ELSE
            PrintLn(item.ExecError.ReturnCode+":"+item.ExecError.ReturnText);
            MsgBoxEx("Уведомление не отправлено!");
            MsgBoxEx(item.ExecError.ReturnCode+":"+item.ExecError.ReturnText);
         END;
      END;
   END;
   
   RETURN TRUE;
END;

//func1("http://rsutmp/RSBankWS/RSBankWS.asmx?WSDL", 21);
//func1("http://192.168.100.107/RSBankWS/RSBankWS.asmx?op=XMLRPCCall", 1);
//func1("http://192.168.100.107/RSBankWS/RSBankWS.asmx?op=XMLRPCCall", 1);
//func1("http://192.168.100.107/RSBankWS/RSBankWS.asmx", 1);
//func1("http://tereshenko/RSBankWS/RSBankWS.asmx?WSDL", 1);