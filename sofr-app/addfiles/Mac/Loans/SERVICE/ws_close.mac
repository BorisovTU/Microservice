/*
$Name:             ws_close.mac
$Module:           Кредитование
$Description:      Пакетное закрытие договоров
*/
IMPORT SbCrdInter,
       "lgo_opstruct.mac", "lgo_const.mac", "lgo_func.mac", total;

MACRO OperationClose(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
   VAR RSDcmd = NULL;
   PRIVATE VAR ErrManager:CErrManager;
   VAR BatchOperID = 0, errid = 0, errtxt = "";

   // ==> Если запись есть, значит пачка уже обработана, выход
   VAR cnt = 
   CRSDCommand("SELECT COUNT(1) FROM DCNVPACK_BATCHOP_DBT WHERE T_EXECID = ? AND  T_CONVTYPEID = ? AND T_PACKID = ? AND T_OPERATIONID = ?",
              "p1", _execID, 
              "p2", _conveyerID, 
              "p3", _packetID, 
              "p4", _operationID,
              V_INTEGER);
   IF (cnt > 0)
      LgoSqlExec("UPDATE DCNVPACK_DBT SET T_STATE = T_STATE + 1 WHERE T_TASKID = ? AND T_EXECID = ? AND T_PACKID = ? AND T_CONVTYPEID = ?",
                 _taskID, _execID, _packetID, _conveyerID);
      RETURN 0;
   END;
   // <==

   IF (NOT OpenLoansCommonFiles())
      LoansError("Ошибка открытия файлов");
      RETURN FALSE;
   END;
   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   IF (_Params.InitStruct.CalcDataForOp)

      RSDcmd = RsdCommand(RslDefCon, "BEGIN LoansFillPayment.RSO_FillListContrForClose(?,?,?,?,?,?); end;");
      RSDcmd.AddParam("TaskID",     RSDBP_IN); RSDcmd.value("TaskID")     = _taskID;
      RSDcmd.AddParam("ExecID",     RSDBP_IN); RSDcmd.value("ExecID")     = _execID;
      RSDcmd.AddParam("ConvTypeID", RSDBP_IN); RSDcmd.value("ConvTypeID") = _conveyerID;
      RSDcmd.AddParam("PacketID",   RSDBP_IN); RSDcmd.value("PacketID")   = _packetID;
      RSDcmd.AddParam("opdate",     RSDBP_IN); RSDcmd.value("opdate")     = _Params.OperDate;
      RSDcmd.AddParam("typeop",     RSDBP_IN); RSDcmd.value("typeop")     = _Params.OperType;
      RSDcmd.Execute();
   END;

   BatchOperID = СформироватьПакетнуюОперацию(_Params.OperType, _Params.OperDate, _Params.CarryDate, _execID, _conveyerID, _packetID, _operationID);

   LgoSqlExec("TRUNCATE TABLE DDOC_ACC_TMP");
   LgoSqlExec("TRUNCATE TABLE DCREDIT_C_TMP"); 
   LgoSqlExec("TRUNCATE TABLE DLNSERROR_TMP");

   CloseLoansCommonFiles();

   LgoSqlExec("update dcnvpack_dbt set t_State = T_State + 1 where t_TaskID = ? and t_ExecID = ? and t_PackID = ? and t_ConvTypeID = ?",
              _taskID, _execID, _packetID, _conveyerID);

   IF (BatchOperID <= 0)
      errid = GetMO_ErrorID();
      
      // SCR 198708
      IF (errid == 18765)
         RETURN 0;
      END;

      IF (errid < 0)
         errid = 18644;
      END;

      errtxt = GetMO_LoansError();

      IF (Trim(errtxt) == "")
         errtxt = "Ошибка выполнения операции";
      END;

      IF (errid != 0)
          RunError(errtxt, errid);
      END;
   END;

   RETURN 0;
   //сюда попадем, если было выброшено исключение.
   OnError(err)
      IF (ValType(err.Err) == V_INTEGER)
         RunError(err.Err, err.Message);
      ELSE
         RunError(18644, "Ошибка выполнения операции");
      END;

END;