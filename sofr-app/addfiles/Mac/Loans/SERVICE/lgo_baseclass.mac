/*
$Name:             lgo_baseclass.mac
$Module:           Кредитование
$Description:      Базовый класс для режимов работы
                   Принцип работы следующий: динамически создается код рсл и запускается
*/
Import RsbDataSet, "lgo_const.mac", globals, rsexts;
private class CrdCnvBase(_TaskNum, _TaskParm, _GroupOpInitStruct)
  private var
      TaskNum    = 0,
      TaskParm   = NULL,
      InitStruct = NULL,
      TaskID     = 0, 
      ExecID     = 0;

  var
      Conveyer     = "", // основное хранилище текста рсл
      ConvFunct    = "", // доп функции
      ImportConv   = "", // импорт для функций отбора
      ImportConvOp = ""; // импорт для функций операций

  macro GetTaskID()
      return TaskID;
  end;

  macro GetExecID()
      return ExecID;
  end;

  macro SetExecID(_ExecID)
      ExecID = _ExecID;
  end;

  // запуск
  macro Run()
      Conveyer = ImportConv + ImportConvOp + ";" + ConvFunct + 
                 "macro ExecGroupOp(_TaskID, _ExecID, _TaskParm, _InitStruct) " +
                 "var ConvRSLObj = NULL, OpRSLObj = NULL; " + Conveyer + "return _ExecID; end;";
      SetExecID(ExecMacroModule(Conveyer, "ExecGroupOp", TaskID, ExecID, TaskParm, InitStruct));
      return true;
  end;

  // добавить параметры отбора
  macro AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize)
      // прикомпилим необходимые файлы
      if (strlen(_PanelFile) > 0)
          ImportConv   = ImportConv + ", \"" + _PanelFile +  "\"" ;
      end;
      if (strlen(_SelectFile) > 0)
          ImportConv   = ImportConv + ", \"" + _SelectFile +  "\"" ;
      end;

      // создадим рсл-объект с параметрами отбора
      Conveyer = Conveyer +
      "ConvRSLObj = GetRslObj(" + TaskNum + ", 0, _TaskParm, _InitStruct, \"" + _PanelClass + "\");";

      if (_packetSize > 0)
          Conveyer = Conveyer +
          "ConvRSLObj.SetPacketSize(" + _packetSize + ");";
      end;
  end;

  macro AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, _OpFile, _OpProc, _Service, _Threads)
      // прикомпилим необходимые файлы
      if (strlen(_PanelFile) > 0)
          ImportConvOp   = ImportConvOp + ", \"" + _PanelFile +  "\"" ;
      end;
      if (strlen(_OpFile) > 0)
          ImportConvOp   = ImportConvOp + ", \"" + _OpFile +  "\"" ;
      end;

      // создадим рсл-объект с параметрами операции
      Conveyer = Conveyer +
      "OpRSLObj = GetRslObj(" + TaskNum + ", " + _operationID + ", _TaskParm, _InitStruct, \"" + _PanelClass + "\");";

      if (_Threads > 0)
          Conveyer = Conveyer +
          "OpRSLObj.SetThreads(" + _Threads + ");";
      end;

      if (strlen(_Service) > 0)
          Conveyer = Conveyer +
          "OpRSLObj.SetService(\"" + _Service + "\");";
      end;
  end;

//constructor
  TaskNum      = _TaskNum;
  TaskParm     = _TaskParm;
  InitStruct   = _GroupOpInitStruct;
  TaskID       = CnvTaskID(TaskNum);
  ExecID       = 0;
  Conveyer     = "";
  ImportConv   = "IMPORT XmlRpcInter, \"lgo_opstruct.mac\", \"lgo_parmstruct.mac\", \"lgo_inparm.mac\" ";
  ImportConvOp = "";
  ConvFunct    = "private macro GetRslObj(_TaskNum, _OperationID, _TaskParm, _InitStruct, _PanelClass) " +
                 "  var retObj = GenObject(_PanelClass);" + 
                 "retObj.SetData(_TaskParm, _InitStruct, _TaskNum, _OperationID);" +
                 "return retObj; end;";
end;
//........................................................................//

//........................................................................//
// Реализация старта операции для режима "В одной сессии"
//........................................................................//
//........................................................................//
class (CrdCnvBase) CrdCnvOneSession(_TaskNum, _TaskParm, _GroupOpInitStruct)
  macro Run()
      Conveyer = "var PacketCnt = 0; " + Conveyer;
      return Run();
  end;

  macro AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize)
      AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize);

      // вызов функции отбора
      Conveyer = Conveyer +
      "PacketCnt = 0; ";
      if (strlen(_SelectProc) > 0)
          Conveyer = Conveyer +
          "PacketCnt = "+ _SelectProc + "(_TaskID, _ExecID," + _convID + ", ConvRSLObj.GetPacketSize(), ConvRSLObj.GetData());";
      end;
  end;

  macro AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, _OpFile, _OpProc, _Service, _Threads)
      AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, _OpFile, _OpProc, _Service, _Threads);

      // старт операции по пачкам
      if (strlen(_OpProc) > 0)
          Conveyer = Conveyer +
          "while (PacketCnt > 0)" +
               _OpProc + "(_TaskID, _ExecID, " + _convID + "," + _operationID + ", PacketCnt, OpRSLObj.GetData());" +
          "    PacketCnt = PacketCnt - 1;" + 
          "end;";
      end;
  end;

//constructor
  private var
      ExecSeq = NULL;

  InitCrdCnvBase(_TaskNum, _TaskParm, _GroupOpInitStruct);

  if (GetTaskID() > 0)
      // небольшая хитрость для получения уникального ИД сессии запуска пакетной операции
      ExecSeq = TRsbDataSet("select dcnvexec_dbt_seq.nextval as ExecID from dual");
      ExecSeq.MoveNext;
      SetExecID(Int(ExecSeq.ExecID));
  else
      return NULL;
  end;
end;
//........................................................................//

//........................................................................//
// Реализация старта операции для режима "С использованием механизма параллельных вычислений"
//........................................................................//
//........................................................................//
class (CrdCnvBase) CrdCnvMPE(_TaskNum, _TaskParm, _GroupOpInitStruct)
  macro Run()
      Conveyer = "var Start_str, End_str, xml, MPE, request, PacketCnt = 0;" + Conveyer;
      return Run();
  end;

  macro AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize)
      AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize);

      // вызов функции отбора
      Conveyer = Conveyer +
      "PacketCnt = 0; ";
      if (strlen(_SelectProc) > 0)
          Conveyer = Conveyer +
          "PacketCnt = "+ _SelectProc + "(_TaskID, _ExecID, " + _convID + ", ConvRSLObj.GetPacketSize(), ConvRSLObj.GetData());";
      end;
  end;

  macro AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, _OpFile, _OpProc, _Service, _Threads)
      AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, "", "", _Service, _Threads);

      // формируем запрос к сервису для старта операции и осылаем его
      if (strlen(_OpProc) > 0)
          Conveyer = Conveyer +
          "ConvertToXML(OpRSLObj.GetData(), NULL, xml);" + 
          "Start_str = Index(xml, \"<param>\");" + 
          "End_str   = Index(xml, \"</params>\");" + 
          "xml = SubStr( xml, Start_str, End_str - Start_str );"
          "MPE = RslMPExecutor(OpRSLObj.GetService(), OpRSLObj.GetThreads());" + 
          "MPE.Run();" + 
          "while (PacketCnt > 0)" +
              "request = " +
              "\"<?xml version='1.0' encoding='windows-1251'?>\" + " +
              "\"<methodCall xmlns=\\\"http://www.softlab.ru/xml-rpc/schema\\\"\" + " +
              "\"            xmlns:r=\\\"http://www.softlab.ru/xml-rpc/schema\\\"\" + " +
              "\"            r:oper=\\\""  + {oper} + "\\\"\" + " + 
              "\"            r:reqId=\\\"\" + CreateGUID() + \"\\\" >\" + " +
              "\"  <methodName>RunMacro." + StrSubst(_OpFile, ".mac", "")+ "." + _OpProc + "</methodName>\" + " +
              "\"  <params>\" + " +
              "\"    <param><value><integer>\" + _TaskID + \"</integer></value></param>\" + " +
              "\"    <param><value><integer>\" + _ExecID + \"</integer></value></param>\" + " +
              "\"    <param><value><integer>" + _convID + "</integer></value></param>\" + " +
              "\"    <param><value><integer>" + _operationID + "</integer></value></param>\" + " +
              "\"    <param><value><integer>\" + PacketCnt + \"</integer></value></param>\" + " + 
              "\"    \" + xml + \"\" + " +
              "\"  </params>\" + " +
              "\"</methodCall>\";" +
              "MPE.Add(request);"
              "PacketCnt = PacketCnt - 1;" + 
          "end;" + 
          "while (not MPE.IsFinish())" +
              "Sleep(1000);" +
          "end; MPE.Destructor();"
      end;
  end;

//constructor
  private var
      ExecSeq = NULL;

  InitCrdCnvBase(_TaskNum, _TaskParm, _GroupOpInitStruct);

  if (GetTaskID() > 0)
      // небольшая хитрость для получения уникального ИД сессии запуска пакетной операции
      ExecSeq = TRsbDataSet("select dcnvexec_dbt_seq.nextval as ExecID from dual");
      ExecSeq.MoveNext;
      SetExecID(Int(ExecSeq.ExecID));
      ImportConv = ImportConv + ", RsbMPExecutor";
  else
      return NULL;
  end;
end;
//........................................................................//

//........................................................................//
// Реализация старта операции для режима "С использованием механизма конвейера"
//........................................................................//
//........................................................................//
class (CrdCnvBase) CrdCnvUseCnv(_TaskNum, _TaskParm, _GroupOpInitStruct)
  macro Run()
      Conveyer = "var CnvExecObj = RsbConveyerExec(_TaskID); " + Conveyer + 
      "CnvExecObj.Run(); _ExecID = CnvExecObj.ExecID;";
      return Run();
  end;

  macro AddParamsForConveyer(_convID, _PanelFile, _PanelClass, _SelectFile, _SelectProc, _packetSize)
      AddParamsForConveyer(_convID, _PanelFile, _PanelClass, "", "", _packetSize);

      Conveyer = Conveyer +
      "CnvExecObj.AddParamsForConveyer(" + _convID + ", ConvRSLObj.GetData(), ConvRSLObj.GetPacketSize());";
  end;

  macro AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, _OpFile, _OpProc, _Service, _Threads)
      AddParamsForOperation(_convID, _operationID, _PanelFile, _PanelClass, "", "", _Service, _Threads);

       Conveyer = Conveyer +
       "CnvExecObj.AddParamsForOperation(" + _convID + ", " + _operationID + ", OpRSLObj.GetData(), OpRSLObj.GetService(), OpRSLObj.GetThreads());";
  end;

//constructor
  InitCrdCnvBase(_TaskNum, _TaskParm, _GroupOpInitStruct);

  if (GetTaskID() > 0)
      SetExecID(0);
  else
      return NULL;
  end;
end;