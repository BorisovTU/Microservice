/*
$Name:             crdRunGroupOp.mac
$Module:           Кредитование
$Description:      Реализация механизма запуска пакетных операций
*/
//........................................................................//
// Реализация механизма запуска пакетных операций                         //
// 06.07.2012 Куперин М.В.                                                //
// Версия 1.0                                                             //
//........................................................................//
//........................................................................//
IMPORT BankInter, RsbDataSet, SbCrdInter,
       "lgo_const.mac", "lgo_inparm.mac", "lgo_baseclass.mac", "lgo_func.mac";       
/*
  Функция запуска пакетной операции. 
  Все настройки берутся из дистоибутивных таблиц конвейера
  Можеь работать в 3-х режимах:
  1) Выполнение операции в одной сессии
  2) Выполнение операции через механизм паралельных вычислений
  3) Выполнение операции через механизм конвейера

  Входные параметры (при добавлении новых описываем их в lgo_inparm.mac):
    _OpNum        - Вид операции (вид конвейера)
    _OpParm       - Параметры операции (параметры отбора и параметры операции)
    _OpInitStruct - Инициализирующая структура (GroupOpInitStruct)

  Возвращаемое значение:
    номер пакетной операции
*/
macro RunGroupOp(_OpNum, _OpParm, _OpInitStruct)
  var
      retVal       = 0,
      ConveyerExec = NULL,
      CnvRS        = NULL,
      CnvOpRS      = NULL,
      runOp        = false;

    // если инициализирующую структуру не передали, то создадим ее с параметрами по умолчанию
    if (ValType(_OpInitStruct) == V_UNDEF)
        _OpInitStruct = GroupOpInitStruct(GROUPOP_WM_INONESESSION, true, true);
    end;

    // если отбираем объекты, то их и расчитываем
    if (_OpInitStruct.SelectDataForOp)
        _OpInitStruct.CalcDataForOp = true;
    end;

    // в зависимости от режима работы выбираем объект для старта операции
    if (_OpInitStruct.WorkMode == GROUPOP_WM_INONESESSION)
        ConveyerExec = CrdCnvOneSession(_OpNum, _OpParm, _OpInitStruct);
    elif (_OpInitStruct.WorkMode == GROUPOP_WM_USEMPE)
        ConveyerExec = CrdCnvMPE(_OpNum, _OpParm, _OpInitStruct);
    elif (_OpInitStruct.WorkMode == GROUPOP_WM_USECNV)
        ConveyerExec = CrdCnvUseCnv(_OpNum, _OpParm, _OpInitStruct);
    end;

    // заполним параметры объекта операции
    if (ConveyerExec)
        // отберем конвейеры по виду запуска
        CnvRS   = TRsbDataSet("select * from dcnvtaskcnv_dbt where t_TaskID = " + ConveyerExec.GetTaskID() + " and (t_notUsed is NULL or t_notUsed <> 'X') order by t_TaskID");
        while (CnvRS.moveNext)
            ConveyerExec.AddParamsForConveyer(CnvRS.ConvTypeID, CnvRS.PanelMacro, CnvRS.PanelClass, CnvRS.SelectMacro, CnvRS.SelectProc, CnvRS.PacketSize);

            // отберем операции по виду конвейера
            CnvOpRS = TRsbDataSet("select opr.* from dcnvopr_dbt cnvopr, dcnvoprtype_dbt opr where opr.t_OperationID = cnvopr.t_OperationID and cnvopr.t_ConvTypeID = " + CnvRS.ConvTypeID + " and (cnvopr.t_notUsed is NULL or cnvopr.t_notUsed <> 'X') order by cnvopr.t_Order");
            while (CnvOpRS.moveNext)
                ConveyerExec.AddParamsForOperation(CnvRS.ConvTypeID, CnvOpRS.OperationID, CnvOpRS.PanelMacro, CnvOpRS.PanelClass, CnvOpRS.T_Macro, CnvOpRS.Proc, CnvOpRS.Service, CnvOpRS.Threads);
                runOp = true;
            end;
        end;

        if (runOp)
            // если есть операции, которые необходимо исполнить, то стартуем пакетную операцию
            ConveyerExec.Run();
        else
            retVal = 0;
        end;
    else
        retVal = 0;
    end;

    //в любом случае сводим все операции. необходимо для очистки лишних записей в таблицах
    retVal = LgoRunStoredFunc("RSO_LoansRsBus.SvodBatchOper", V_INTEGER, NULL, /*ExecID*/ConveyerExec.GetExecID());
/* 260871
   IF (retVal == 0)
      LoansError("Не сформировано ни одной операции!");
   END; 
*/
    // Перекидываем коды ошибок обратно и чистим таблицу
    IF (ValType(ConveyerExec.GetExecID()/*ExecID*/) != V_UNDEF)
       LgoSqlExec("INSERT INTO DLNSERROR_TMP (T_OBJID, T_OBJN, T_CREDOPERID, T_ERROR, T_ERRORCODE) SELECT T_OBJID, T_OBJN, T_CREDOPERID, T_ERROR, T_ERRORCODE FROM DLNSERROR_DBT WHERE T_CNUM = " +/* ExecID*/ConveyerExec.GetExecID());
       LgoSqlExec("DELETE FROM DLNSERROR_DBT WHERE T_CNUM = " + /*ExecID*/ConveyerExec.GetExecID());
    END;

    return retVal;
end;
