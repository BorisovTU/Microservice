/*
$Name:             lgo_parmstruct.mac
$Module:           Кредитование
$Description:      Параметры для пакетный операций с использованием контейнера
*/
IMPORT RsbFormsInter, SbCrdInter,
       "lgo_const.mac", "lgo_opstruct.mac", "globals.mac", "lgo_inparm.mac", total, "DlReport_h.mac";

PRIVATE VAR
    CNV_Panels = TArray();

//........................................................................//
//........................................................................//
// базовый класс для определения параметров отбора и параметров операции
//........................................................................//
CLASS (CRsbFormConv) GroupOpBaseParm()
   var CNV_PanelsID = 0;
   var Params = NULL;
   PRIVATE var prm_isInitObj = false;

   MACRO isInitObj()
       RETURN prm_isInitObj;
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      RETURN NULL;
   END;

   MACRO SetData(_Params, _InitStruct, _TaskNum, _OperationID)
      var obj = NULL;

      IF (ValType(_TaskNum) == V_UNDEF)
         obj = _Params;

         IF ((GenPropID(_Params, "InitStruct") != -1) AND ((ValType(obj.InitStruct) == V_UNDEF) OR (ValType(obj.InitStruct) == V_INTEGER)))
            obj.InitStruct = GroupOpInitStruct(GROUPOP_WM_USECNV, TRUE, TRUE);
         END;
      ELSE
         obj = ConvertData(_TaskNum, _Params, _InitStruct);
         obj.InitStruct   = _InitStruct;
         obj.OperationID = _OperationID;
      END;

      Params = obj;

      if (isInitObj())
          Redraw();
      END;
   END;

   MACRO GetData()
      RETURN Params;
   END;

//constructor
   InitCRsbFormConv();
   SetSize    (71, 14); 
   SetPosition(1,  14);
   CNV_Panels[CNV_Panels.size] = this;
   CNV_PanelsID = CNV_Panels.size;

   Init();
   prm_isInitObj = true;
END;
//........................................................................//
//........................................................................//
//........................................................................//
//........................................................................//
//........................................................................//
// класс для описания параметров отбора для "Общий класс отбора"
class (GroupOpBaseParm) GroupOpSelParmBase()
  var
      control_DOP        = false,
      control_FIID       = false,
      control_CD         = false,
      control_VK         = false,
      control_BCID       = false,
      control_PVK        = false,
      control_FNC        = false,
      control_PacketSize = false,
      control_ZERO       = false;
  var
      VK_objid = LO_CREDIT;

   MACRO AddControl_DOP(x, y, label)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 15;
      END;
      control.name       = "DOP";
      control.dataType   = 9; //FT_DATE;
      control.textLength = 10;
      control.setPosition(x, y);
      control.setSize    (10, 1);
      addControl(control);
      control_DOP = true;
   END;
   MACRO AddControl_FIID(x, y, label)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 8;
      END;
      control.name       = "FIID";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 3;
      control.setPosition(x, y);
      control.setSize    (3, 1);
      control.Editable  = false;
      addControl(control);
      control_FIID = true;
   END;
   MACRO AddControl_CD(x, y, label)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 18;
      END;
      control.name       = "CD";
      control.dataType   = 9; //FT_DATE;
      control.textLength = 10;
      control.setPosition(x, y);
      control.setSize    (10, 1);
      addControl(control);
      control_CD = true;
   END;
   MACRO AddControl_VK(x, y, label, objid)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          y = y + 1;
      END;
      control.name       = "VK";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 1024;
      control.setPosition(x, y);
      control.setSize    (71, 4);
      control.Editable   = false;
      addControl(control);
      control_VK = true;
      VK_objid = objid;
   END;
   
   MACRO AddControl_BCID(x, y, label)
      var control = TRsbCheckBox;
      control.name       = "BCID_CHKB";
      control.dataType   = 12;
      control.setPosition(x, y);
      control.Checked = FALSE;
      addControl(control);
      x = x + 5;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 15;
      END;
      control   = TRsbEditField;
      control.name       = "BCID";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 15;
      control.setPosition(x, y);
      control.setSize    (15, 1);
      control.Editable  = false;
      addControl(control);
      control_BCID = true;
   END;

   MACRO AddControl_PVK(x, y, label)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 20;
      END;
      control.name       = "PVK";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 10;
      control.setPosition(x, y);
      control.setSize    (15, 1);
      control.Editable   = false;
      addControl(control);
      control_PVK = true;
   END;
   MACRO AddControl_FNC(x, y, label)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 20;
      END;
      control.name       = "FNC";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 20;
      control.setPosition(x, y);
      control.setSize    (15, 1);
      control.Editable  = false;
      control.Enabled   = false;
      addControl(control);
      control_FNC = true;
   END;
   
   MACRO AddControl_Zero(x, y, label)
      var control = TRsbCheckBox;
      control.name       = "ZERO_CHKB";
      control.dataType   = 12;
      control.setPosition(x, y);
      control.Checked = FALSE;
      addControl(control);
      x = x + 5;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 15;
      END;
      control_ZERO = TRUE;
   END;

   MACRO AddControl_PacketSize(x, y, label)
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          y = y + 1;
      END;
      AddPacketSize(x, y);
      control_PacketSize = true;
   END;

   MACRO GetControlValue(name)
    var
        retVal = NULL;

      IF (name == "DOP")
          if(control_DOP)
              retVal = getControl("DOP").Value;
          else
              retVal = {curdate};
          END;
      ELIF (name == "CD")
          if(control_CD)
              retVal = getControl("CD").Value;
          else
              if(control_DOP)
                  retVal = getControl("DOP").Value;
              else
                  retVal = {curdate};
              END;
          END;
      ELIF (name == "ZERO_CHKB")
          IF (control_ZERO)
             IF (getControl("ZERO_CHKB").Checked == TRUE)
                retVal = 1;
             ELSE
                retVal = 0;
             END;
          ELSE
             retVal = 1;
          END;
      END;

      RETURN retVal;
   END;

   MACRO GetRslObj() 
      RETURN ConveerOperation(GetControlValue("DOP"), 
                              GetControlValue("CD"), 
                              GetData().CurCode, 
                              GetData().CreditKind, 
                              GetData().UserCreditKind, 
                              GetData().FNCash, 
                              GetData().BriefCaseID,
                              GetControlValue("ZERO_CHKB"));
   END;

   // Сохраненные данные, выводим на экран и запоминаем
   MACRO SetData(_Params)
      RECORD Портфель("lbrfcase.dbt");
      VAR idx = 0;
      VAR TypeCrd = "";

      IF ((control_DOP) AND (GenPropID(_Params, "OperDate") != -1) AND (ValType(_Params.OperDate) != V_UNDEF))
          getControl("DOP").Value  = {curdate};
      END;
      
      IF (control_FIID AND (GenPropID(_Params, "CurCode") != -1))
          if (_Params.CurCode >= 0)
             getControl("FIID").Value = FindShortName(_Params.CurCode);
          else
             getControl("FIID").Value = "ВСЕ";
          END;
      END;
      
      IF (control_CD AND (GenPropID(_Params, "CarryDate") != -1) AND (ValType(_Params.CarryDate) != V_UNDEF ))
          getControl("CD").Value   = {curdate};        
      END;

      IF (control_VK AND (GenPropID(_Params, "CreditKind") != -1))
         getControl("VK").Value = "";
         IF (ValType(_Params.CreditKind) != V_UNDEF)
            WHILE (idx < _Params.CreditKind.size)
               TypeCrd = FindTypeCrd(_Params.CreditKind[idx]);
               IF (TypeCrd)
                   getControl("VK").Value = getControl("VK").Value + TypeCrd + "\n";
               END;
               idx = idx + 1;
            END;
         END;

         IF (getControl("VK").Value == "")
            getControl("VK").Value = "Не выбраны";
         END;
      END;
      
      IF (control_BCID AND (GenPropID(_Params, "BriefCaseID") != -1))
         getControl("BCID").Value = "";
         IF ((ValType(_Params.BriefCaseID) == V_INTEGER) AND 
             (_Params.BriefCaseID > 0))
             getControl("BCID_CHKB").Checked = "X";
             Loans_FindCASE(_Params.BriefCaseID, Портфель);
             getControl("BCID").Value = Портфель.UserNumber;
         END;

         IF (getControl("BCID_CHKB").Checked == "X")
             getControl("BCID").Enabled   = true;
         ELSE
             getControl("BCID").Enabled   = false;
         END;
      END;

      IF (control_PVK AND (GenPropID(_Params, "UserCreditKind") != -1))
          getControl("PVK").Value  = string(_Params.UserCreditKind);
      END;

      IF (control_FNC AND (GenPropID(_Params, "FNCash") != -1))
         CB_GetDepartmentCodeAndName(_Params.FNCash, NULL, getControl("FNC").Value);
      END;

      IF (control_ZERO AND (GenPropID(_Params, "ZeroRest") != -1))
         getControl("ZERO_CHKB").Checked = FALSE;

         IF ((ValType(_Params.ZeroRest) == V_INTEGER) AND (_Params.ZeroRest == 1))
            getControl("ZERO_CHKB").Checked = TRUE;
         END;
      END;

      RETURN SetData(_Params);
   END;

   MACRO eventHandler(EV)
      FILE ВидыКредитов ("set_tcr.tmp",  "loans.def");
      VAR ID = 0, rslobj = NULL, CreditKind = TArray(),
           idx = 0, form = NULL;
      VAR ZeroRest = 1;

      IF (GenPropID(GetData(), "ZeroRest") != -1)
         ZeroRest = GetData().ZeroRest;
      END;

      // F2 F9
      if ((EV.keyCode == 316) OR (EV.keyCode == 323) OR (EV.keyCode == 27))
         idx = CNV_Panels.size;
         while (idx > 0)
           form = CNV_Panels[idx - 1];            
           form.SetData(form.GetRslObj()); 
           idx = idx - 1;
         END;    
      // F3
      ELIF (EV.keyCode == 317)
         if (FocusedControl.Name == "FIID")
            ID = ListFinInstr();
            if (ID >= 0)
               rslobj = ConveerOperation(GetControlValue("DOP"), 
                                         GetControlValue("CD"), 
                                         ID, 
                                         CreditKind, 
                                         GetData().UserCreditKind, 
                                         GetData().FNCash, 
                                         GetData().BriefCaseID,
                                         ZeroRest);
            END;
         ELIF (FocusedControl.Name == "VK")
            УстановитьВидыКредитов (0, GetData().CurCode, VK_objid);
            while (Next(ВидыКредитов))
               if ((ВидыКредитов.SetFlag == "X") AND ((ВидыКредитов.CurCode == GetData().CurCode) OR (GetData().CurCode == -1)))
                  CreditKind(CreditKind.Size) = ВидыКредитов.CreditTypeId_Ref;
               END;
            END;
            rslobj = ConveerOperation(GetControlValue("DOP"), 
                                      GetControlValue("CD"), 
                                      GetData().CurCode, 
                                      CreditKind, 
                                      GetData().UserCreditKind, 
                                      GetData().FNCash, 
                                      GetData().BriefCaseID,
                                      ZeroRest);

         ELIF   (FocusedControl.Name == "BCID")
             ID = RSL_ListCase(TRUE, 1, -2, -2, -2, false);
             if (ID >= 0)
                 rslobj = ConveerOperation(GetControlValue("DOP"), 
                                           GetControlValue("CD"), 
                                           GetData().CurCode, 
                                           GetData().CreditKind, 
                                           GetData().UserCreditKind, 
                                           GetData().FNCash, 
                                           ID,
                                           ZeroRest);
             END;
         ELIF (FocusedControl.Name == "PVK")
            rslobj = ConveerOperation(GetControlValue("DOP"), 
                                      GetControlValue("CD"), 
                                      GetData().CurCode, 
                                      GetData().CreditKind, 
                                      Loans_UserTypeCredit(), 
                                      GetData().FNCash, 
                                      GetData().BriefCaseID,
                                      ZeroRest);

         ELIF (FocusedControl.Name == "FNC")
            ID = ListDepartment();
            if (ID > 0)
               rslobj = ConveerOperation(GetControlValue("DOP"), 
                                         GetControlValue("CD"), 
                                         GetData().CurCode, 
                                         GetData().CreditKind, 
                                         GetData().UserCreditKind, 
                                         ID, 
                                         GetData().BriefCaseID,
                                         ZeroRest);
            END;
         END;
         if (rslobj != NULL)
            SetData(rslobj);
            FocusedControl.Redraw;
         END;
      ELIF (EV.keyCode == 32)
         if   (FocusedControl.Name == "FIID")
             rslobj = ConveerOperation(GetControlValue("DOP"), 
                                       GetControlValue("CD"), 
                                      -1, 
                                       CreditKind, 
                                       GetData().UserCreditKind, 
                                       GetData().FNCash, 
                                       GetData().BriefCaseID,
                                       ZeroRest);

         ELIF   (FocusedControl.Name == "BCID_CHKB")
             rslobj = ConveerOperation(GetControlValue("DOP"), 
                                       GetControlValue("CD"), 
                                       GetData().CurCode, 
                                       GetData().CreditKind, 
                                       GetData().UserCreditKind, 
                                       GetData().FNCash, 
                                       NULL,
                                       ZeroRest);

         ELIF   (FocusedControl.Name == "BCID")
             rslobj = ConveerOperation(GetControlValue("DOP"), 
                                       GetControlValue("CD"), 
                                       GetData().CurCode, 
                                       GetData().CreditKind, 
                                       GetData().UserCreditKind, 
                                       GetData().FNCash, 
                                       NULL,
                                       ZeroRest);

         ELIF   (FocusedControl.Name == "PVK")
            rslobj = ConveerOperation(GetControlValue("DOP"), 
                                      GetControlValue("CD"), 
                                      GetData().CurCode, 
                                      GetData().CreditKind, 
                                      "", 
                                      GetData().FNCash, 
                                      GetData().BriefCaseID,
                                      ZeroRest);
         
         ELIF (FocusedControl.Name == "ZERO_CHKB")
            IF (GetData().ZeroRest == 1)
               GetData().ZeroRest = 0;
            ELSE
               GetData().ZeroRest = 1;
            END;

            rslobj = ConveerOperation(GetControlValue("DOP"), 
                                      GetControlValue("CD"), 
                                      GetData().CurCode, 
                                      GetData().CreditKind, 
                                      "", 
                                      GetData().FNCash, 
                                      GetData().BriefCaseID,
                                      GetControlValue("ZERO_CHKB"));
         END;

         IF (rslobj != NULL)
            SetData(rslobj);
            FocusedControl.Redraw;
         END;
      END;

      RETURN true;
   END;

   //destructor
   MACRO destructor
       CloseLoansCommonFiles();
   END;

//constructor
   CNV_Panels.size = 0;
   InitGroupOpBaseParm();
   OpenLoansCommonFiles()
END;
//........................................................................//
//........................................................................//
// класс для описания параметров отбора для "Операция начисления по кредитам"
class (GroupOpSelParmBase) GroupOpSelParm_PercCredit()
   MACRO Init()
      IF (not isInitObj())
          AddControl_DOP(2, 2, "Дата операции");
          AddControl_FIID(61, 2, "Валюта");
          AddControl_CD(2, 3, "Дата проводки в БУ");
          AddControl_VK(2, 4, "Виды кредита", LO_CREDIT);
          AddControl_BCID(2, 9, "Портфель:");
          AddControl_PVK(2, 10, "Пользовательские ВК");
          AddControl_FNC(2, 11, "Филиал");
          AddControl_Zero(2, 12, "Не отбирать договоры с нулевым остатком ОД");
          AddControl_PacketSize(2, 13, "────────────────────────────────────────────────────────────────");
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      END;

      SetData(ConveerOperation({curdate}, {curdate}, -1, TArray(), "", {OperDprt}, NULL, 1));
   END;

   MACRO eventHandler(EV)
       RETURN eventHandler(EV);
   END;

   // Сохраненные данные, выводим на экран и запоминаем
   MACRO SetData(_Params)
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      VAR obj = ConveerOperation(_Params.OperDate, 
                                 _Params.OperDate, 
                                 _Params.CurCode, 
                                 _Params.CreditKind, 
                                 _Params.UserCreditKind, 
                                 _Params.FNCash, 
                                 _Params.Case,
                                 _Params.ZeroRest);
      RETURN obj;
   END;

//constructor
   InitGroupOpSelParmBase();
END;
//........................................................................//
//........................................................................//
// класс для описания параметров отбора для "Операция начисления по депозитам"
class (GroupOpSelParm_PercCredit) GroupOpSelParm_PercDeposit()
   MACRO Init()
      if (not isInitObj())
          AddControl_DOP(2, 2, "Дата операции");
          AddControl_FIID(61, 2, "Валюта");
          AddControl_CD(2, 3, "Дата проводки в БУ");
          AddControl_VK(2, 4, "Виды депозита", LO_DEPOSIT);
          AddControl_PVK(2, 9, "Пользовательские ВД");
          AddControl_FNC(2, 10, "Филиал");
          AddControl_PacketSize(2, 11, "────────────────────────────────────────────────────────────────");
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      END;

      SetData(ConveerOperation({curdate}, {curdate}, -1, TArray(), "", {OperDprt}, NULL));
   END;

//constructor
   InitGroupOpSelParm_PercCredit();
END;
//........................................................................//
//........................................................................//
// класс для описания параметров отбора для остальных операций
class (GroupOpSelParmBase) GroupOpSelParm_Credit()
   MACRO Init()
      if (not isInitObj())
          AddControl_DOP(2, 2, "Дата операции");
          AddControl_FIID(61, 2, "Валюта");
          AddControl_CD(2, 3, "Дата проводки в БУ");
          AddControl_VK(2, 4, "Виды кредита", LO_CREDIT);
          AddControl_PVK(2, 9, "Пользовательские ВК");
          AddControl_FNC(2, 10, "Филиал");
          AddControl_PacketSize(2, 11, "────────────────────────────────────────────────────────────────");
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      END;

      SetData(ConveerOperation({curdate}, {curdate}, -1, TArray(), "", {OperDprt}, NULL));
   END;

   MACRO eventHandler(EV)
       RETURN eventHandler(EV);
   END;

   // Сохраненные данные, выводим на экран и запоминаем
   MACRO SetData(_Params)
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      VAR obj = ConveerOperation(_Params.OperDate, _Params.OperDate, _Params.CurCode, _Params.CreditKind, _Params.UserCreditKind, _Params.FNCash, _Params.Case);
      RETURN obj;
   END;

//constructor
   InitGroupOpSelParmBase();
END;

//Повышев 13.06.2017
//........................................................................//
//........................................................................//
// класс для описания параметров отбора объектов для выгрузки Кредитной Истории
class (GroupOpBaseParm) GroupOpSelParm_Bki(_ReadOnly)

   var ReadOnly = _ReadOnly;

   var Data:BkiConvParams;

   MACRO Init()
      if (not isInitObj())

          // Вид БКИ
          addLabel(TRsbLabel(2, 1, "БКИ:"));
          var control = TRsbEditField;
          control.name       = "Bki";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 30;
          control.setPosition(6, 1);
          control.setSize    (15, 1);
          control.rawType    = FBT;
          control.enabled = not ReadOnly;
          addControl(control);     
          
          addLabel(TRsbLabel(22, 1, "Вид передачи:"));
          control = TRsbEditField;
          control.name       = "SendKind";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 30;
          control.setPosition(33, 1);
          control.setSize    (15, 1);
          control.rawType    = FBT;
          control.enabled = not ReadOnly;
          addControl(control);     
          
          addLabel(TRsbLabel(50, 1, "Дата отчета:"));
          control = TRsbEditField;
          control.name       = "RepDate";
          control.dataType   = 9; //FT_STRING;
          control.textLength = 10;
          control.setPosition(62, 1);
          control.setSize    (10, 1);
          control.enabled = not ReadOnly;
          addControl(control);     
          
          addLabel(TRsbLabel(4, 2, "Формированик КИ по группе БКИ:"));
          control = TRsbCheckBox;
          control.name       = "ByGroup";
          control.setPosition(2, 2);
          control.enabled = not ReadOnly;
          addControl( control );
          
          control = TRsbEditField;
          control.name       = "BkiGroup";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 30;
          control.setPosition(35, 2);
          control.setSize    (15, 1);
          control.rawType    = FBT;
          control.enabled = not ReadOnly;
          addControl(control);     
          
          addLabel(TRsbLabel(4, 3, "Автоматическое определение начала периода формирования КИ:"));

          control = TRsbCheckBox;
          control.name       = "AutoDetectStartDate";
          control.setPosition(2, 3);
          control.enabled = not ReadOnly;
          addControl( control );

          control = TRsbEditField;
          control.name       = "StartDate";
          control.dataType   = 9; //FT_STRING;
          control.textLength = 10;
          control.setPosition(62, 3);
          control.setSize    (10, 1);
          control.enabled = not ReadOnly;
          addControl(control);     
          
          addLabel(TRsbLabel(2, 4, "Путь:"));
          control = TRsbEditField;
          control.name       = "PathForming";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 50;
          control.setPosition(7, 4);
          control.setSize    (50, 1);
          control.enabled = not ReadOnly;
          addControl(control);              
          
          addLabel(TRsbLabel(2, 5, "Виды кредита"));
          control   = TRsbEditField;
          control.name       = "VK";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 1024;
          control.setPosition(2, 6);
          control.setSize    (71, 4);
          control.Editable   = false;
          addControl(control);
         
          addLabel(TRsbLabel(2, 10, "Филиал"));
          control   = TRsbEditField;
          control.name       = "FNC";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 20;
          control.setPosition(22, 10);
          control.setSize    (15, 1);
          control.Editable  = false;
          control.Enabled   = false;
          addControl(control);
      
          var comboPartyType = TRsbComboBox;

          comboPartyType.addChoice( 1, "Юридические лица" );
          comboPartyType.addChoice( 2, "Физические лица");

          comboPartyType.name = "LegalForm";
          comboPartyType.setPosition(2, 11);
          comboPartyType.setSize(20, 1);
          comboPartyType.dataType = 7; //FT_STRING;
          comboPartyType.enabled = not ReadOnly; //FT_STRING;
          addControl( comboPartyType );
          
          addLabel(TRsbLabel(2, 12, "Операционист:"));
          control = TRsbEditField;
          control.name       = "Oper";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 30;
          control.setPosition(15, 12);
          control.setSize    (30, 1);
          control.rawType    = FBT;
          control.enabled = not ReadOnly;
          addControl(control);     

          AddPacketSize(2, 14);
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      end;

      //Параметры по умолчанию ( если нет сохраненных параметров или после сброса)
      SetData(BkiConvParams(
                              0,           //БКИ
                              0,           //Вид передачи
                              0,           //Группа БКИ
                              2,           //Вид субъектов
                              0,           //Операционист
                              {curdate},   //Дата отчета
                              {curdate},   //Начало периода формирования
                              false    ,   //По группе БКИ?
                              true     ,   //Дата начала периода формирования определяется автоматически?
                              ""       ,   //Путь
                              TArray() ,   //Виды кредита
                              {OperDprt}   //Филиал
                           )
                          );
   END;

   macro EnterResult( rs, cmd, id, key )
      if( (cmd == DLG_KEY) and (key == 13) )
         return CM_SELECT;
      end;
      
      return CM_DEFAULT;
   end;
   
   macro RefreshShowValues
   
      if (Data.BkiID > 0)
         getControl("Bki").Value = CRSDCommand("select t_bkishortname from dlbki_dbt where t_bkiid = :bkiid", "bkiid", Data.BkiID, V_STRING);
      else
         getControl("Bki").Value = "";
      end;
      
      if (Data.SendKind > 0)
         getControl("SendKind").Value = CRSDCommand("   SELECT t_name         " +
                                                    "         FROM dllclsval_dbt t, dllvalues_dbt vals " +
                                                    "        WHERE t.t_classificator = :classificator  " +
                                                    "          and T.T_LIST = VALS.T_LIST              " +
                                                    "          and t.t_element = vals.t_element        " +
                                                    "          and t.t_element = :element              " +
                                                    "     ORDER BY  t.t_sort, t.t_element              ", 
                                                    "classificator", execMacroFile("likepy", "ifThenElse", Data.LegalForm == 2, 1512, 1516),
                                                    // "classificator", 1512,
                                                    "element"      , Data.SendKind ,
                                                    V_STRING
                                                    );
      else                                              
         getControl("SendKind").Value = "";
      end;      
      
      if (Data.BkiGroup > 0)
         getControl("BkiGroup").Value = CRSDCommand("SELECT t_shortname FROM dlbkigrp_dbt WHERE t_groupid = :groupid", 
                                                    "groupid"      , Data.BkiGroup ,
                                                    V_STRING
                                                    );
      else                                              
         getControl("BkiGroup").Value = "";
      end;
      
      if (Data.LegalForm > 0)
         getControl("LegalForm").currentChoice = Data.LegalForm;
      end;
      
      if (Data.RepDate != null)
         getControl("RepDate").Value = Data.RepDate;
      end;
      
      if (Data.StartDate != null)
         getControl("StartDate").Value = Data.StartDate;
      end;
      
      if (Data.AutoDetectStartDate)
         getControl("AutoDetectStartDate" ).checked = true;
         getControl("StartDate").enabled = false;
      else
         getControl("AutoDetectStartDate" ).checked = false;
         getControl("StartDate").enabled = true;
      end;
      
      if (Data.ByGroup)
      
         getControl("ByGroup" ).checked = true;
      
         getControl("BkiGroup").enabled = true;
         getControl("Bki"     ).enabled = false;
         getControl("PathForming").enabled = false;
      else
      
         getControl("ByGroup" ).checked = false;
      
         getControl("BkiGroup").enabled = false;
         getControl("Bki"     ).enabled = true;
         getControl("PathForming").enabled = true;
      end;
      
      if (Data.PathForming != null)
         getControl("PathForming").Value = Data.PathForming;
      end;
      
      if (Data.FNCash != null)
         CB_GetDepartmentCodeAndName(Data.FNCash, NULL, getControl("FNC").Value);
      end;

      if (Data.CreditKind.size > 0)
         getControl("VK").Value = "";
         
         for (var TypeCrd, Data.CreditKind)
            TypeCrd = FindTypeCrd(TypeCrd);
            if (TypeCrd)
                getControl("VK").Value = getControl("VK").Value + TypeCrd + "\n";
            end;
         end;
      else
         getControl("VK").Value = "Не выбраны";
      end;

      if (Data.Oper > 0)
         getControl("Oper").Value = CRSDCommand("select t_name from dperson_dbt where t_oper = :oper", "oper", Data.Oper, V_STRING);
      else
         getControl("Oper").Value = "";
      end;
      


   end;
   
   MACRO eventHandler(EV)
      FILE ВидыКредитов ("set_tcr.tmp",  "loans.def"); /* временный файл для выбора видов кредита */
      var person = TRecHandler ("person");

      Data.PathForming = getControl("PathForming").Value;

      //F2 Запуск конвейера
      if( (EV.KeyCode == 316) AND (ReadOnly == false) ) 

         if (Data.ByGroup)
            prompt("Формирование КИ по группе БКИ не реализовано в текущий версии системы", getControl("ByGroup"));
            return false;
            if (Data.BkiGroup == 0)
               prompt("Не указана группа БКИ", getControl("BkiGroup"));
               return false;
            end;
         else
            if (Data.BkiID == 0)
               prompt("Не указано БКИ", getControl("Bki"));
               return false;
            end;
            if (Data.PathForming == "") //todo сразу проверять корректность пути
               prompt("Не задан путь для сформированных файлов", getControl("PathForming"));
               return false;
            end;
         end;
         
         if (Data.SendKind == 0)
            prompt("Не указан вид передачи", getControl("SendKind"));
            return false;
         end;
         
         if (Data.CreditKind.size == 0)
            prompt("Не указаны виды кредита", getControl("VK"));
            return false;
         end;
         
      //F3
      elif( (EV.KeyCode == 317) AND (ReadOnly == false) )

         //Выборк БКИ
         if(FocusedControl.Name == "Bki")
            var rs = RsdRecordset("select t_bkishortname, t_bkiid, t_contractnumber, t_contractdate, t_sourceid, t_durationdelivery, t_pathforming  from dlbki_dbt", RSDVAL_CLIENT, RSDVAL_STATIC);
            if (RunScroll(rs, null, null, null, R2M(this, "EnterResult")))
               Data.BkiID = rs.value("t_bkiid");
               Data.PathForming = rs.value("t_pathforming");
               Data.BkiGroup = 0;
            end;
            
         //Вид передачи
         elif(FocusedControl.Name == "SendKind")
            var cmd = RSDCommand("   SELECT vals.t_element, t_code, t_name         " +
                                 "         FROM dllclsval_dbt t, dllvalues_dbt vals " +
                                 "        WHERE t.t_classificator = :classificator  " +
                                 "          and T.T_LIST = VALS.T_LIST              " +
                                 "          and t.t_element = vals.t_element        " +
                                 "     ORDER BY  t.t_sort, t.t_element              ");
                                 
            cmd.addParam("classificator", RSDBP_IN, execMacroFile("likepy", "ifThenElse", Data.LegalForm == 2, 1512, 1516));
                                 
            rs = RsdRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
            if (RunScroll(rs, null, null, null, R2M(this, "EnterResult")))
               Data.SendKind = rs.value("t_element");
            end;
            
         //Группа БКИ
         elif(FocusedControl.Name == "BkiGroup")
            rs = RsdRecordset("SELECT t_groupid, t_shortname, t_groupname, t_comment FROM dlbkigrp_dbt", 
                                    RSDVAL_CLIENT, RSDVAL_STATIC);
            if (RunScroll(rs, null, null, null, R2M(this, "EnterResult")))
               Data.BkiGroup = rs.value("t_groupid");
               Data.BkiID = 0;
               Data.PathForming = "";
            end;
            
         //Форма субъектов
         elif(FocusedControl.Name == "LegalForm")
            Data.LegalForm = getControl("LegalForm").CurrentChoice;
         
         //Виды кредита
         elif (FocusedControl.Name == "VK")
            УстановитьВидыКредитов (0, -1, LO_CREDIT);
            Data.CreditKind.size = 0;
            while (Next(ВидыКредитов))
               if (ВидыКредитов.SetFlag == "X")
                  Data.CreditKind[Data.CreditKind.Size] = ВидыКредитов.CreditTypeId_Ref;
               end;
            end;
            
         //Филиал
         elif(FocusedControl.Name == "FNC")
            var ID = ListDepartment();
            if (ID > 0)
               Data.FNCash = ID;
            end;
         elif(FocusedControl.Name == "Oper")
            
            if (ListOper (person, 0, Data.Oper))
               Data.Oper = person.("Oper");
            end;
         end;
         
      elif( (EV.KeyCode == 32) AND (ReadOnly == false) )
         //Флажок "По группе БКИ"
         if(FocusedControl.Name == "ByGroup")
            Data.ByGroup =  not Data.ByGroup;
         elif(FocusedControl.Name == "AutoDetectStartDate")
            Data.AutoDetectStartDate =  not Data.AutoDetectStartDate;
         elif(FocusedControl.Name == "Bki")
            Data.BkiID = 0;
         elif(FocusedControl.Name == "SendKind")
            Data.SendKind = 0;
         elif(FocusedControl.Name == "BkiGroup")
            Data.BkiGroup = 0;
         elif(FocusedControl.Name == "Oper")
            Data.Oper = 0;
         end;
      end;
      
      RefreshShowValues();
      
      this.Redraw();
   END;
   
   // Сохраненные данные, выводим на экран и запоминаем
   MACRO SetData(_Params)

      if (GenClassName(_Params) == "RCW\\TXRMessageImpl") //Восстановление из сохраненных настроек
         Data = BkiConvParams(_Params.BkiID, _Params.SendKind, _Params.BkiGroup, _Params.LegalForm, _Params.Oper, {curdate}, {curdate}, _Params.ByGroup, _Params.AutoDetectStartDate, _Params.PathForming, _Params.CreditKind, _Params.FNCash  );
      else
         Data = _Params;
      end;
        
      RefreshShowValues();
   END;
   
   
   Macro GetData()
      return BkiConvParams(Data.BkiID, Data.SendKind, Data.BkiGroup, Data.LegalForm, Data.Oper, Data.RepDate, Data.StartDate, Data.ByGroup, Data.AutoDetectStartDate, Data.PathForming, Data.CreditKind, Data.FNCash );
   end;

//constructor
   InitGroupOpBaseParm();
END;




//........................................................................//
//........................................................................//
// класс для описания параметров отбора для "Операций по портфелям"
class (GroupOpBaseParm) GroupOpSelParm_Case()
  VAR 
      CaseGroup = false;

   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 2, "Дата операции"));
          VAR control = TRsbEditField;
          control.name       = "DOP";
          control.dataType   = 9; //FT_DATE;
          control.textLength = 10;
          control.setPosition(17, 2);
          control.setSize    (10, 1);
          addControl(control);

          // Валюта
          addLabel(TRsbLabel(61, 2, "Валюта"));
          control = TRsbEditField;
          control.name       = "FIID";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 3;
          control.setPosition(69, 2);
          control.setSize    (3, 1);
          control.Editable  = false;
          addControl(control);

          addLabel(TRsbLabel(2, 3, "Виды кредита"));
          control = TRsbEditField;
          control.name       = "VK";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 1024;
          control.setPosition(2, 4);
          control.setSize    (71, 4);
          control.Editable   = false;
          addControl(control);

          addLabel(TRsbLabel(2, 8, "Пользовательские ВК"));
          control = TRsbEditField;
          control.name       = "PVK";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 10;
          control.setPosition(22, 8);
          control.setSize    (10, 1);
          control.Editable   = false;
          addControl(control);

          addLabel(TRsbLabel(2, 9, "Портфель"));
          control = TRsbEditField;
          control.name       = "CASE";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 36;
          control.setPosition(13, 9);
          control.setSize    (36, 1);
          control.Editable   = false;
          addControl(control);      

          addLabel(TRsbLabel(2, 10, "Филиал"));
          control = TRsbEditField;
          control.name       = "FNC";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 20;
          control.setPosition(22, 10);
          control.setSize    (15, 1);
          control.Editable  = false;
          control.Enabled   = false;
          addControl(control);

          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddPacketSize(2, 12);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      END;

      SetData(ConveerOperation({curdate}, {curdate}, -1, TArray(), "", {OperDprt}, 0));
   END;

   MACRO eventHandler(EV)
      FILE ВидыКредитов ("set_tcr.tmp",  "loans.def"); /* временный файл для выбора видов кредита */
      VAR ID = 0, rslobj = NULL, CreditKind = TArray();

      // F2 F9
      IF ((EV.keyCode == 316) OR (EV.keyCode == 323))
         SetData(ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, GetData().CurCode, GetData().CreditKind, GetData().UserCreditKind, GetData().FNCash, GetData().BriefCaseID));
      // F3
      ELIF (EV.keyCode == 317)
         IF   (FocusedControl.Name == "FIID")
            ID = ListFinInstr();
            IF (ID >= 0)
               rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, ID, CreditKind, GetData().UserCreditKind, GetData().FNCash, GetData().BriefCaseID);
            END;

         ELIF (FocusedControl.Name == "VK")
            УстановитьВидыКредитов (0, GetData().CurCode, LO_CREDIT);
            WHILE (Next(ВидыКредитов))
               IF ((ВидыКредитов.SetFlag == "X") AND ((ВидыКредитов.CurCode == GetData().CurCode) OR (GetData().CurCode == -1)))
                  CreditKind(CreditKind.Size) = ВидыКредитов.CreditTypeId_Ref;
               END;
            END;

            rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, GetData().CurCode, CreditKind, GetData().UserCreditKind, GetData().FNCash, GetData().BriefCaseID);

         ELIF (FocusedControl.Name == "PVK")
            rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, GetData().CurCode, GetData().CreditKind, Loans_UserTypeCredit(), GetData().FNCash, GetData().BriefCaseID);
         ELIF (FocusedControl.Name == "CASE")
            ID = RSL_ListCase(TRUE, 1, -2, -2, -2, CaseGroup);
            IF (ID > 0)
               rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, GetData().CurCode, GetData().CreditKind, GetData().UserCreditKind, GetData().FNCash, ID);
            END;
         END;

         IF (rslobj != NULL)
            SetData(rslobj);
            FocusedControl.Redraw;
         END;
      ELIF (EV.keyCode == 32)
         IF   (FocusedControl.Name == "FIID")
             rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, -1, CreditKind, GetData().UserCreditKind, GetData().FNCash, GetData().BriefCaseID);
         ELIF   (FocusedControl.Name == "CASE")
             rslobj = ConveerOperation(getControl("DOP").Value, getControl("DOP").Value, GetData().CurCode, GetData().CreditKind, GetData().UserCreditKind, GetData().FNCash, 0);
         END;

         IF (rslobj != NULL)
            SetData(rslobj);
            FocusedControl.Redraw;
         END;
      END;

      RETURN TRUE;
   END;

   // Сохраненные данные, выводим на экран и запоминаем
   MACRO SetData(_Params)
      RECORD Портфель("lbrfcase.dbt");
      VAR idx = 0;
      VAR TypeCrd = "";

      IF (_Params.CurCode >= 0)
         getControl("FIID").Value = FindShortName(_Params.CurCode);
      ELSE
         getControl("FIID").Value = "ВСЕ";
      END;

      getControl("PVK").Value  = string(_Params.UserCreditKind);
      getControl("DOP").Value  = _Params.OperDate;

      CB_GetDepartmentCodeAndName(_Params.FNCash, NULL, getControl("FNC").Value);

      getControl("VK").Value = "";

      IF (ValType(_Params.CreditKind) != V_UNDEF)
         WHILE (idx < _Params.CreditKind.size)
            TypeCrd = FindTypeCrd(_Params.CreditKind[idx]);
            if (TypeCrd)
                getControl("VK").Value = getControl("VK").Value + TypeCrd + "\n";
            END;
            idx = idx + 1;
         END;
      END;

      IF (getControl("VK").Value == "")
         getControl("VK").Value = "Не выбраны";
      END;

      getControl("CASE").Value = "";
      IF (_Params.BriefCaseID > 0)
         Loans_FindCASE(_Params.BriefCaseID, Портфель);
         getControl("CASE").Value = Портфель.UserNumber;
      END;

      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      VAR obj = NULL;
  
      obj = ConveerOperation(_Params.OperDate, _Params.OperDate, _Params.CurCode, _Params.CreditKind, _Params.UserCreditKind, _Params.FNCash, _Params.BriefCaseID);

      RETURN obj;
   END;

//constructor
   InitGroupOpBaseParm();
END;

class (GroupOpSelParm_Case) GroupOpSelParm_CaseLTRANSFER()
//constructor
   InitGroupOpSelParm_Case();
   CaseGroup = true;
END;
//........................................................................//


//........................................................................//
// класс для описания параметров отбора для "Простого отбора портфелей"
class (GroupOpBaseParm) GroupOpSelParm_SimpleCase()
   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddPacketSize(2, 12);
      END;

      SetData(SimpleOperation(1));
   END;

//constructor
  InitGroupOpBaseParm();
END;
//........................................................................//
//........................................................................//
//........................................................................//
//........................................................................//
/*****************************************************************************/
/* ОПЕРАЦИИ                                                                  */
/*****************************************************************************/
//общий класс для параметров операции
CLASS (GroupOpBaseParm) GroupOpParm_OpBase()
   VAR
      control_TYPEOP        = false,
      control_DER           = false,
      control_SPOD          = false,
      control_PROTOCOL      = false,
      control_CHOICE        = false;

   VAR
       value_TYPEOP = -1,
       value_DER    = 0,
       value_SPOD   = "",
       value_Protocol = "X",
       value_Choice   = 1;

   PRIVATE VAR systOP = 0;

   MACRO AddControl_TYPEOP(x, y, label, sys)
      var control   = TRsbEditField;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + 14;
      END;
      control.name       = "TYPEOP";
      control.dataType   = 7; //FT_STRING;
      control.textLength = 80;
      control.setPosition(x, y);
      control.setSize    (40, 1);
      control.editable   = false;
      addControl(control);
      control_TYPEOP = true;
      systOP = sys;
   END;

   MACRO AddControl_DER(x, y, label)
      var control = NULL;
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          x = x + strlen(label);
      END;
      control = TRsbRadioButton(1);
      control.name       = "DER_RB1";
      control.setPosition(x, y);
      addControl(control);
      addLabel(TRsbLabel((x-1), y, "( )"));
      control = TRsbRadioButton(1);
      control.name       = "DER_RB2";
      control.setPosition(x, (y+1));
      addLabel(TRsbLabel((x-1), (y+1), "( )"));
      addControl(control);
      x = x + 3;
      control   = TRsbEditField;
      control.name       = "DER";
      control.dataType   = 9;
      control.textLength = 10;
      control.setPosition(x, y);
      control.setSize    (10, 1);
      addControl(control);
      addLabel(TRsbLabel((x+1), (y+1), "по графику"));
      control_DER = true;
   END;

   MACRO AddControl_SPOD(x, y, label)
      var control = TRsbCheckBox;
      control.name       = "SPOD";
      control.dataType   = 12;
      control.setPosition(x, y);
      control.Checked = "";
      addControl(control);
      x = x + 5;
      IF (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
      END;
      control_SPOD = true;
   END;

   MACRO AddControl_SERVINFO(x, y, label)
      if (ValType(label) == V_STRING)
          addLabel(TRsbLabel(x, y, label));
          y = y + 1;
      END;
      AddService(x, y);
      AddThreads(x, y + 1);
   END;

   MACRO AddControl_Choice  (X, Y)
      var control   = TRsbEditField;

      control.name       = "CHOICE";
      control.dataType   = 7;
      control.textLength = 80;
      control.setPosition(x, y);
      control.setSize    (60, 1);
      control.editable   = false;
      addControl(control);
      control_CHOICE     = true;
   END;

   MACRO AddControl_Protocol(X, Y, LABEL)
      var control = TRsbCheckBox;
      control.name       = "PROTOCOL";
      control.dataType   = 12;
      control.setPosition(x, y);
      control.Checked = "";
      addControl(control);
      x = x + 5;
      IF (ValType(label) == V_STRING)
         addLabel(TRsbLabel(x, y, label));
      END;
      control_PROTOCOL   = true;
   END;

   MACRO GetControlValue(name)
      var
        retVal = NULL;

      IF (name == "TYPEOP")       // Операция
          retVal = value_TYPEOP;
      ELIF (name == "DER")        // Погашение согласно графику
          retVal = value_DER;
      ELIF (name == "SPOD")       // СПОД
          retVal = value_SPOD;
      ELIF (name == "DOP")        // Дата окончания расчета
          retVal = CNV_Panels[0].getcontrol("DOP").Value;
      ELIF (name == "CD")         // По графику
          retVal = CNV_Panels[0].getcontrol("CD").Value;
      ELIF (name == "PROTOCOL")   // Протокол
          retVal = value_Protocol;
      ELIF (name == "CHOICE")     // Выбранная операция не используется по договору
          retVal = value_Choice;
      END;

      RETURN retVal;
   END;

   MACRO GetRslObj()
      RETURN NULL;
   END;

   MACRO SetParmFromRslObj(rslobj)
   END;

   MACRO eventHandler(EV)
      var rslobj = NULL;
      record llvalues("llvalues.dbt");
      VAR idx = 0, form = NULL;

      IF ((EV.keyCode == 316) OR (EV.keyCode == 323) OR (EV.keyCode == 27))
         idx = CNV_Panels.size;
         while (idx > 0)
           form = CNV_Panels[idx - 1];            
           form.SetData(form.GetRslObj()); 
           idx = idx - 1;
         END;
      // F3
      ELIF (EV.keyCode == 317)
         IF (FocusedControl.Name == "TYPEOP")
            value_TYPEOP = ListSystTypeOp(systOP);
            IF (value_TYPEOP > 0)
               rslobj = GetRslObj();
            END;
         ELIF (FocusedControl.Name == "CHOICE")
            IF ( LL_ListLLVALUES(1435, llvalues) == TRUE )
               value_Choice = llvalues.ELEMENT;
               rslobj = GetRslObj();
            END;
         END;
      ELIF (EV.keyCode == 32)
          IF (FocusedControl.Name == "TYPEOP")
              value_TYPEOP = -1;
          ELIF (FocusedControl.Name == "DER_RB1")
              IF (FocusedControl.checked)
                  value_DER = 1;
              ELSE
                  value_DER = 2;
              END;
          ELIF (FocusedControl.Name == "DER_RB2")
              IF (FocusedControl.checked)
                  value_DER = 2;
              ELSE
                  value_DER = 1;
              END;
          ELIF (FocusedControl.Name == "SPOD")
              IF (FocusedControl.checked)
                  value_SPOD = "X";
              ELSE
                  value_SPOD = "";
              END;
          ELIF (FocusedControl.Name == "PROTOCOL")
              IF (FocusedControl.checked)
                  value_Protocol = "X";
              ELSE
                  value_Protocol = "";
              END;
          END;
          rslobj = GetRslObj();
      END;

      IF (rslobj != NULL)
         SetData(rslobj);
         FocusedControl.Redraw;
      END;
   END;

   // Базовый класс SetData, вызывается из погашения, начисления, просрочки
   MACRO SetData(_Params)
      RECORD ВидОперации("type_op.dbt");

      SetParmFromRslObj(_Params);

      IF ((control_TYPEOP)and
          (value_TYPEOP > 0))
         Loans_FindTypeOp(value_TYPEOP, 0, ВидОперации);
         getControl("TYPEOP").Value = ВидОперации.TypeOperName;
      ELSE
         getControl("TYPEOP").Value = "Не выбрана";
      END;

      IF (control_DER)
         IF (value_DER == 1)
            getControl("DER_RB1").Checked = "X";
            getControl("DER_RB2").Checked = "";
            getControl("DER").enabled = true;
         ELSE
            getControl("DER_RB1").Checked = "";
            getControl("DER_RB2").Checked = "X";
            getControl("DER").enabled = false;
         END;
      END;

      IF (control_SPOD)
         getControl("SPOD").Checked = value_SPOD;
      END;

      // Если на вкладке есть поле "Протокол"
      IF (control_Protocol)
         getControl("PROTOCOL").Checked = value_Protocol;
      END;

      // Если на вкладке есть поле "Выбор"
      IF (control_Choice)
         getControl("CHOICE").Value = GetLLVALname (1435, value_Choice);
      END;

      RETURN SetData(_Params);
   END;

//constructor
   InitGroupOpBaseParm();
END;

//........................................................................//
//........................................................................//
//класс для описания параметров операции "Начисление по кредитам"
class (GroupOpParm_OpBase) GroupOpParm_PayPercCredit()
   MACRO Init()
      if (not isInitObj())
          AddControl_TYPEOP(2, 2, "Операция", CS_PERC);
          AddControl_DER(2, 3, "Дата окончания начисления");

          var control = TRsbCheckBox;
          control.name       = "PPG";
          control.dataType   = 12;
          control.setPosition(2, 5);
          control.Checked = "";
          addControl(control);
          addLabel(TRsbLabel(5, 5, "Начисление по графику"));

          AddControl_SPOD(2, 6, "Отражение СПОД");

          addLabel(TRsbLabel(2, 7, "─ Обработка договоров с индивидуальными настройками─────────────"));
          addLabel(TRsbLabel(2, 8, "Если выбранная операция не используется по договору:"));

          AddControl_Choice  (2, 09);
          AddControl_Protocol(2, 10, "Выводить в протокол");

          AddControl_SERVINFO(2, 11, "────────────────────────────────────────────────────────────────");

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(30899);
      END;

      value_TYPEOP = CF_PERC;
      getControl("DER").Value = GetControlValue("DOP");
      SetData(GetRslObj());
   END;

   MACRO GetRslObj()
      var DER = 0, AddGraff = "", Graph = 0;

      DER = GetControlValue("DER");

      IF (DER == 1)
         Graph = 0;
         DER = getControl("DER").Value;
      ELIF (DER == 2)
         Graph = 1;
         DER = getControl("DER").Value;
      ELSE
         Graph = 0;
         DER = date(0,0,0);
      END;

      IF (getControl("PPG").checked)
         AddGraff = "X";
      END;
   
      RETURN PercOperation(-1, TArray(), "", 0, {OperDprt}, 0, " ",
                            GetControlValue("DOP"), 
                            GetControlValue("CD"), 
                            GetControlValue("TYPEOP"),
                            GetControlValue("DOP"), 
                            Graph, 
                            AddGraff,
                            GetControlValue("SPOD"),
                            GetControlValue("CHOICE"),
                            GetControlValue("PROTOCOL"));
   END;

   MACRO SetParmFromRslObj(rslobj)

      IF (GenPropID(rslobj, "OperType") != -1)
         value_TYPEOP = rslobj.OperType;
      END;

      IF (GenPropID(rslobj, "Graph") != -1)
         IF (rslobj.Graph == 1)
            value_DER = 2;
         ELSE
            value_DER = 1;
         END;
      END;

      getcontrol("DER").Value = {curdate};      

      IF ((GenPropID(rslobj, "SPOD") != -1) AND (ValType(rslobj.SPOD) != V_UNDEF ))  
         value_SPOD   = rslobj.SPOD;
      END;

      IF (GenPropID(rslobj, "Protocol") != -1)
         value_Protocol = rslobj.Protocol;
      ELSE
         value_Protocol = "X";
      END;

      IF (GenPropID(rslobj, "Choice") != -1)
         value_Choice = rslobj.Choice;
      ELSE
         value_Choice = 1;
      END;      
   END;

   MACRO eventHandler(EV)
      RETURN eventHandler(EV);
   END;

   MACRO SetData(_Params)
      IF (GenPropID(_Params, "addgraff") != -1)
         getControl("PPG").Checked = _Params.addgraff;
      END;

      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
       RETURN PercOperation(_Params.CurCode, 
                            _Params.CreditKind, 
                            _Params.UserCreditKind, 
                            _Params.Case, 
                            _Params.FNCash, 
                            _Params.PcAddAfter, 
                            _Params.SMAString,
                            _Params.OperDate, 
                            _Params.CarryDate, 
                            _Params.OperType, 
                            _Params.CalcDate, 
                            _Params.Graph, 
                            _Params.addgraff, 
                            _Params.SPOD,
                            _Params.Choice,
                            _Params.Protocol);
   END;

//constructor
   InitGroupOpParm_OpBase();
END;
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Начисление" по депозитам
class (GroupOpParm_OpBase) GroupOpParm_PayPercDeposit()
   MACRO Init()
      if (not isInitObj())
          AddControl_TYPEOP(2, 2, "Операция:", CS_DEPOPERC);

          addLabel(TRsbLabel(2, 3, "Дата окончания начисления:"));
          var control = TRsbEditField;
          control.name       = "DER";
          control.dataType   = 9;
          control.textLength = 10;
          control.setPosition(22, 3);
          control.setSize    (10, 1);
          addControl(control);

          AddControl_SPOD(2, 4, "Отражение СПОД");

          control = TRsbCheckBox;
          control.name       = "PCA";
          control.dataType   = 12;
          control.setPosition(2, 5);
          control.Checked = "";
          addControl(control);
          addLabel(TRsbLabel(5, 5, "Начисление % по договорам с истекшим сроком"));

          AddControl_SERVINFO(2, 11, "────────────────────────────────────────────────────────────────");
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(3087);
      END;

      value_TYPEOP = CF_DEPOPERC;
      getControl("DER").Value = GetControlValue("DOP");
      SetData(GetRslObj());
   END;

   MACRO GetRslObj()
      var AddAfter = 0;
      if (getControl("PCA").checked)
         AddAfter = 1;
      END;
   
      RETURN PercOperation(-1, TArray(), "", 0, {OperDprt},
                            AddAfter,
                            " ",
                            GetControlValue("DOP"), 
                            GetControlValue("CD"), 
                            GetControlValue("TYPEOP"),
                            getcontrol("DER").Value, 
                            0, 
                            "",
                            GetControlValue("SPOD"),
                            3,
                            ""
                           );
   END;

   MACRO SetParmFromRslObj(rslobj)
       value_TYPEOP = rslobj.OperType;
       value_DER   = 0;
       value_SPOD  = rslobj.SPOD;
   END;
 
   MACRO eventHandler(EV)
      RETURN eventHandler(EV);
   END;

   MACRO SetData(_Params)
      getcontrol("DER").Value   = _Params.CalcDate;
      getControl("PCA").Checked = _Params.PcAddAfter;
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
       RETURN PercOperation(_Params.CurCode, 
                            _Params.CreditKind, 
                            _Params.UserCreditKind, 
                            _Params.Case, 
                            _Params.FNCash, 
                            _Params.PcAddAfter, 
                            _Params.SMAString,
                            _Params.OperDate, 
                            _Params.CarryDate, 
                            _Params.OperType, 
                            _Params.CalcDate, 
                            _Params.Graph, 
                            _Params.addgraff, 
                            _Params.SPOD,
                            3,
                            "");
   END;

//constructor
   InitGroupOpParm_OpBase();
END;
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Просрочки"
class (GroupOpParm_OpBase) GroupOpParm_Exp()
   var
       value_ADDPR = 0;

   MACRO Init()
      if (not isInitObj())
          AddControl_TYPEOP(2, 2, "Операция:", CS_EXP);
          AddControl_DER(2, 3, "Дата окончания расчета");

          var control = TRsbCheckBox;
          control.name       = "PPG";
          control.dataType   = 12; //FT_CHR;
          control.setPosition(2, 5);
          addControl(control);                      
          addLabel(TRsbLabel(5, 5, "Просрочка в период отсрочки платежа"));

          AddControl_SPOD(2, 6, "Отражение СПОД");

          addLabel(TRsbLabel(2, 7, "Просрочка начисленных %"));
          control = TRsbEditField;
          control.name       = "ADDPR";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 30;
          control.setPosition(26, 7);
          control.setSize    (29, 1);
          control.editable   = false;
          addControl(control);

          addLabel(TRsbLabel(2, 8, "─ Обработка договоров с индивидуальными настройками─────────────"));
          addLabel(TRsbLabel(2, 9, "Если выбранная операция не используется по договору:"));

          AddControl_Choice  (2, 10);
          AddControl_Protocol(2, 11, "Выводить в протокол");
          
          AddControl_SERVINFO(2, 12, "────────────────────────────────────────────────────────────────");
          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(30906);          
      END;

      value_TYPEOP = CF_EXP;
      getControl("DER").Value = GetControlValue("DOP");
      value_ADDPR = 0;
      SetData(GetRslObj());
   END;

   MACRO GetRslObj()
      var DER = 0, cExpiration = 0, Graph = 0;

      DER = GetControlValue("DER");

      if (DER == 1)
         Graph = 0;
         DER = getControl("DER").Value;
      ELIF (DER == 2)
         Graph = 1;
         DER = getControl("DER").Value;
      else
         Graph = 0;
         DER = date(0,0,0);
      END;

      if (getControl("PPG").checked)
         cExpiration = 1;
      END;

      RETURN ExpOperation(GetControlValue("TYPEOP"), 
                          GetControlValue("DOP"),
                          GetControlValue("DOP"),
                          GetControlValue("CD"), 
                          -1, 
                          TArray(), 
                          LO_CREDIT, 
                          {OperDprt}, 
                          "", 
                          value_ADDPR, 
                          " ", 
                          Graph, 
                          cExpiration,
                          GetControlValue("SPOD"),
                          GetControlValue("CHOICE"),
                          GetControlValue("PROTOCOL")
                         );
   END;

   MACRO SetParmFromRslObj(rslobj)
      value_TYPEOP = rslobj.OperType;
      IF (rslobj.ForGraph == 1)
         value_DER = 2;
      ELSE
         value_DER = 1;
      END;
      
      getcontrol("DER").Value = {curdate};      
      
      IF ((GenPropID(rslobj, "SPOD") != -1) AND (ValType(rslobj.SPOD) != V_UNDEF ))  
            value_SPOD   = rslobj.SPOD;
      END;
      value_ADDPR  = rslobj.AddPrc;

      IF (GenPropID(rslobj, "Protocol") != -1)
         value_Protocol = rslobj.Protocol;
      ELSE
         value_Protocol = "X";
      END;

      IF (GenPropID(rslobj, "Choice") != -1)
         value_Choice = rslobj.Choice;
      ELSE
         value_Choice = 1;
      END;      
   END;

   MACRO eventHandler(EV)
      var rslobj = NULL;

      if (EV.keyCode == 317)
         if (FocusedControl.Name == "ADDPR")
            value_ADDPR = List_Alg(1036);
            rslobj = GetRslObj();
         END;
      END;
      if (rslobj != NULL)
          SetData(rslobj);
          FocusedControl.Redraw;
      END;

      RETURN eventHandler(EV);
   END;

   MACRO SetData(_Params)
      getControl("PPG").Checked = _Params.cExpiration;
      getControl("ADDPR").Value = GetNameAlg(1036, _Params.AddPrc); 
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      RETURN ExpOperation(_Params.OperType,
                          _Params.OperDate, 
                          _Params.CalcDate, 
                          _Params.CarryDate, 
                          _Params.CurCode, 
                          _Params.CreditKind,   
                          _Params.ObjectTypeID, 
                          _Params.FNCash, 
                          _Params.UserCreditKind, 
                          _Params.AddPrc, 
                          _Params.SMAString, 
                          _Params.ForGraph, 
                          _Params.cExpiration,
                          _Params.SPOD,
                          _Params.Choice,
                          _Params.Protocol);
   END;

//constructor
   InitGroupOpParm_OpBase();
END;





//........................................................................//
//........................................................................//
//класс для описания параметров операции "Погашение"
class (GroupOpParm_OpBase) GroupOpParm_Duty()
   var
       value_ADDPR = 0;

   MACRO Init()
      IF (not isInitObj())
         AddControl_TYPEOP(2, 2, "Операция:", CS_DUTY);
         AddControl_DER(2, 3, "Дата окончания расчета");

         var control = TRsbCheckBox;
         control.name       = "PPG";
         control.dataType   = 12; //FT_CHR;
         control.setPosition(2, 5);
         addControl(control);                      
         addLabel(TRsbLabel(4, 5, "Погашение согласно графику"));

         AddControl_SPOD(2, 6, "Отражение СПОД");

         addLabel(TRsbLabel(2, 7, "Погашение начисленных %"));
         control = TRsbEditField;
         control.name       = "ADDPR";
         control.dataType   = 7; //FT_STRING;
         control.textLength = 30;
         control.setPosition(26, 7);
         control.setSize    (29, 1);
         control.editable   = false;
         addControl(control);

         addLabel(TRsbLabel(2, 8, "─ Обработка договоров с индивидуальными настройками─────────────"));
         addLabel(TRsbLabel(2, 9, "Если выбранная операция не используется по договору:"));

         AddControl_Choice  (2, 10);
         AddControl_Protocol(2, 11, "Выводить в протокол");
         
         AddControl_SERVINFO(2, 12, "────────────────────────────────────────────────────────────────");
         addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
         
         setHelpPage(30913);
      END;

      value_TYPEOP = CF_DUTN;
      
      getControl("DER").Value = GetControlValue("DOP");

      value_ADDPR = 0;
      SetData(GetRslObj());
   END;

   MACRO GetRslObj()
      var DER = 0, IsGraphPay = "", Graph = 0;

      DER = GetControlValue("DER");

      IF (DER == 1)
         Graph = 0;
         DER = getControl("DER").Value;
      ELIF (DER == 2)
         Graph = 1;
         DER = getControl("DER").Value;
      ELSE
         Graph = 0;
         DER = date(0,0,0);
      END;

      IF (getControl("PPG").checked)
         IsGraphPay = "X";
      END;

      RETURN DutyOperation(GetControlValue("TYPEOP"),
                           GetControlValue("DOP"),
                           GetControlValue("DOP"),
                           GetControlValue("CD"),
                           -1,
                           TArray(),
                           "",
                           {OperDprt},
                           LO_CREDIT,
                           "",
                           "",
                           IsGraphPay,
                           0,
                           DPSRC_DEFAULT,
                           value_ADDPR,
                           Graph,
                           0,
                           GetControlValue("SPOD"),
                           GetControlValue("CHOICE"),
                           GetControlValue("PROTOCOL")
                          );
   END;

   MACRO SetParmFromRslObj(rslobj)
      value_TYPEOP = rslobj.OperType;

      IF (rslobj.forGraph == 1)
          value_DER = 2;
      ELSE
          value_DER = 1;
      END;

      getcontrol("DER").Value = {curdate};
      
      IF ((GenPropID(rslobj, "SPOD") != -1) AND (ValType(rslobj.SPOD) != V_UNDEF ))  
        value_SPOD   = rslobj.SPOD;
      END; 
      value_ADDPR    = rslobj.addprc;
      
      IF (GenPropID(rslobj, "Protocol") != -1)
         value_Protocol = rslobj.Protocol;
      ELSE
         value_Protocol = "X";
      END;

      IF (GenPropID(rslobj, "Choice") != -1)
         value_Choice = rslobj.Choice;
      ELSE
         value_Choice = 1;
      END;      
   END;

   // Обработчик клавиатуры
   MACRO eventHandler(EV)
      VAR rslobj = NULL;

      // F3
      // Поле ""
      IF (EV.keyCode == 317)
         IF (FocusedControl.Name == "ADDPR")
            value_ADDPR = List_Alg(1036);
            rslobj = GetRslObj();
         END;
      END;

      IF (rslobj != NULL)
         SetData(rslobj);
         FocusedControl.Redraw;
      END;

      RETURN eventHandler(EV);
   END;

   MACRO SetData(_Params)
      getControl("PPG").Checked = _Params.IsGraphPay;
      getControl("ADDPR").Value = GetNameAlg(1036, _Params.AddPrc); 

      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      RETURN DutyOperation(_Params.OperType,
                           _Params.OperDate,
                           _Params.CalcDate,
                           _Params.CarryDate,
                           _Params.CurCode,
                           _Params.CreditKind,
                           _Params.UserCreditKind,
                           _Params.FnCash,
                           _Params.ObjectTypeID,
                           _Params.YesnoUsertype,
                           _Params.SMAString,
                           _Params.IsGraphPay,
                           _Params.DutyPriority,
                           _Params.DP_Source,
                           _Params.AddPrc,
                           _Params.ForGraph,
                           _Params.mode,
                           _Params.SPOD,
                           _Params.Choice,
                           _Params.Protocol
                          );
   END;

//constructor
   InitGroupOpParm_OpBase();
END;












//........................................................................//
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Формирование РВПС"
class (GroupOpBaseParm) GroupOpParm_RVPS()
 
   MACRO Init()
      if (not isInitObj())
          // Валюта
          addLabel(TRsbLabel(2, 2, "Операция"));
          var control = TRsbEditField;
          control.name       = "TYPEOP";
          control.dataType   = 7; //FT_STRING;
          control.textLength = 80;
          control.setPosition(16, 2);
          control.setSize    (40, 1);
          control.editable   = false;
          addControl(control);

          addLabel(TRsbLabel(2, 3, "Дата операции"));
          control = TRsbEditField;
          control.name       = "DOP";
          control.dataType   = 9; //FT_DATE;
          control.textLength = 10;
          control.setPosition(16, 3);
          control.setSize    (10, 1);
          addControl(control);

          addLabel(TRsbLabel(2, 4, "Контроль"));
          control = TRsbCheckBox;
          control.name       = "CHCK";
          control.dataType   = 12; //FT_CHR;
          control.setPosition(17, 4);
          addControl(control);                      

          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddService(2, 12);
          AddThreads(2, 13);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
                   
          setHelpPage(30908);
      END;

      SetData(RVPSOperation(CF_REZ, {curdate}, {curdate}, {OperDprt}, 0, 1, LO_CREDIT, TArray(), TArray(), "", "", 0, 0, "", 0, 0, "", 1, 0, 0));
   END;

   MACRO eventHandler(EV)
      VAR ID = 0, rslobj = NULL, CheckVal = 0;

      IF (getControl("CHCK").Checked)
         CheckVal = 1;
      ELSE
         CheckVal = 0;
      END;

      IF ((EV.keyCode == 316) OR (EV.keyCode == 323))
         SetData(RVPSOperation(GetData().OperType, getControl("DOP").Value, getControl("DOP").Value, GetData().FNCash, GetData().Protocol, CheckVal, 
                               GetData().ObjectTypeID, GetData().CreditKind, GetData().CaseKind, GetData().UserCreditKind, 
                               GetData().UserCaseKind, GetData().KKS, GetData().Regulaterez, GetData().SMAString, 
                               GetData().CrdOp, GetData().CaseOp, GetData().CessID));
      END;

      // F3
      IF (EV.keyCode == 317)
         IF (FocusedControl.Name == "TYPEOP")
            ID = ListSystTypeOp(CS_REZ);
            IF (ID > 0)
               rslobj = RVPSOperation(
                           ID, getControl("DOP").Value, getControl("DOP").Value, GetData().FNCash, GetData().Protocol, CheckVal, 
                           GetData().ObjectTypeID, GetData().CreditKind, GetData().CaseKind, GetData().UserCreditKind, 
                           GetData().UserCaseKind, GetData().KKS, GetData().Regulaterez, GetData().SMAString, 
                           GetData().CrdOp, GetData().CaseOp, GetData().CessID);
            END;
         END;

         IF (rslobj != NULL)
            SetData(rslobj);
            FocusedControl.Redraw;
         END;
      END;

      RETURN TRUE;
   END;

   MACRO SetData(_Params)
      RECORD ВидОперации("type_op.dbt");

      IF (_Params.OperType > 0)
         Loans_FindTypeOp(_Params.OperType, 0, ВидОперации);
         getControl("TYPEOP").Value = ВидОперации.TypeOperName;
      ELSE
         getControl("TYPEOP").Value = "Не выбрана";
      END;

      getControl("DOP").Value  = _Params.OperDate;

      IF (_Params.CheckVal == 1)
         getControl("CHCK").Checked = true;
      ELSE
         getControl("CHCK").Checked = false;
      END;

      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
     var obj = RVPSOperation(_Params.OperType, _Params.OperDate, _Params.CarryDate, _Params.FNCash, _Params.Protocol, _Params.CheckVal, _Params.ObjectTypeID, 
                             _Params.CreditKind, _Params.CaseKind, _Params.UserCreditKind, _Params.UserCaseKind, _Params.KKS, _Params.Regulaterez, 
                             _Params.SMAString, _Params.CrdOp, _Params.CaseOp, _Params.CessID);
       RETURN obj;
   END;

//constructor
   InitGroupOpBaseParm();
END;
//........................................................................//
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Перевод ссуд"
class (GroupOpBaseParm) GroupOpParm_LoanTransfer()
   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 2, "Дата операции"));
          VAR control = TRsbEditField;
          control.name       = "DOP";
          control.dataType   = 9; //FT_DATE;
          control.textLength = 10;
          control.setPosition(22, 2);
          control.setSize    (10, 1);
          addControl(control);

          addLabel(TRsbLabel(2, 5, "Физ лица"));
          control = TRsbCheckBox;
          control.name       = "F1";
          control.dataType   = 12; //FT_CHR;
          control.setPosition(13, 5);
          addControl(control);                      

          addLabel(TRsbLabel(2, 6, "Юр лица"));
          control = TRsbCheckBox;
          control.name       = "F2";
          control.dataType   = 12; //FT_CHR;
          control.setPosition(13, 6);
          addControl(control);                      

          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddService(2, 12);
          AddThreads(2, 13);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(30917);
      END;

      SetData(TransferOperation({curdate}, {curdate}, {curdate}, 0, 0, 0, "X", "X"));
   END;

   MACRO eventHandler(EV)
      VAR rslobj = NULL, Fiz = "", Jur = "";

      IF (getControl("F1").checked == TRUE)
         Fiz = "X";
      ELSE
         Fiz = "";
      END;

      IF (getControl("F2").checked == TRUE)
         Jur = "X";
      ELSE
         Jur = "";
      END;

      IF ((EV.keyCode == 316) OR (EV.keyCode == 323))
         rslobj = TransferOperation(getControl("DOP").Value, 
                                    getControl("DOP").Value, 
                                    getControl("DOP").Value, 
                                    0, 0, 0, 
                                    Fiz, Jur);

         SetData(rslobj);

         RETURN TRUE;
      END;

      RETURN TRUE;
   END;

   // Вывести информацию на экран
   MACRO SetData(_Params)
      getControl("DOP").Value  = _Params.OperDate;

      // Дата расчета по графику
      IF (_Params.Fiz == "X")
         getControl("F1").Checked = true;
      ELSE
         getControl("F1").Checked = false;
      END;

      // Погашение согласно графику
      IF (_Params.Jur == "X")
         getControl("F2").Checked = true;
      ELSE
         getControl("F2").Checked = false;
      END;

      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      var obj = TransferOperation(_Params.OperDate, 
                                  _Params.CarryDate, 
                                  _Params.CalcDate, 
                                  _Params.CalcPercDown, 
                                  _Params.CalcPercUp, 
                                  _Params.BriefCaseId, 
                                  _Params.Fiz, 
                                  _Params.Jur);
  
      RETURN obj;
   END;
   
//constructor
   InitGroupOpBaseParm();
END;
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Закрытие"
CLASS (GroupOpBaseParm) GroupOpParm_Close()
   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 2, "Дата операции"));
          VAR control = TRsbEditField;
          control.name       = "DOP";
          control.dataType   = 9; //FT_DATE;
          control.textLength = 10;
          control.setPosition(22, 2);
          control.setSize    (10, 1);
          addControl(control);

          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddService(2, 12);
          AddThreads(2, 13);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(30916);
      END;

      SetData(CloseOperation(CF_CLCRD, {curdate}, -1, TArray(), "", LO_CREDIT, {OperDprt}, "", " "));
   END;

   MACRO eventHandler(EV)
      VAR rslobj = NULL;

      IF ((EV.keyCode == 316) OR (EV.keyCode == 323))
         rslobj = CloseOperation(GetData().OperType,
                                 getControl("DOP").Value,
                                 GetData().CurCode,
                                 GetData().CreditKind,
                                 GetData().UserCreditKind,
                                 GetData().ObjectTypeID,
                                 GetData().FNCash,
                                 GetData().YesNoUserType,
                                 ""
                                );
         SetData(rslobj);

         RETURN TRUE;
      END;

      RETURN TRUE;
   END;

   // Вывести информацию на экран
   MACRO SetData(_Params)
      getControl("DOP").Value  = _Params.OperDate;
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      var obj =
      CloseOperation(_Params.OperType,
                     _Params.OperDate, 
                     _Params.CurCode,
                     _Params.CreditKind,
                     _Params.UserCreditKind,
                     _Params.ObjectTypeID,
                     _Params.FNCash,
                     _Params.YesnoUsertype,
                     _Params.SMAString                     
                    );

      RETURN obj;
   END;

//constructor
   InitGroupOpBaseParm();
END;
//........................................................................//
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Перерасчет графика погашения"
CLASS (GroupOpBaseParm) GroupOpParm_PlanPay()
   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 2, "Дата операции"));
          VAR control = TRsbEditField;
          control.name       = "DOP";
          control.dataType   = 9; //FT_DATE;
          control.textLength = 10;
          control.setPosition(22, 2);
          control.setSize    (10, 1);
          addControl(control);

          addLabel(TRsbLabel(2, 11, "────────────────────────────────────────────────────────────────"));
          AddService(2, 12);
          AddThreads(2, 13);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
          
          setHelpPage(30933);          
      END;

      SetData(RecalcGraphConvParams (CF_RECALCGPAY, {curdate}, -1, TArray(), "", {OperDprt}));
   END;

   MACRO eventHandler(EV)
      VAR rslobj = NULL;

      IF ((EV.keyCode == 316) OR (EV.keyCode == 323))
         rslobj = RecalcGraphConvParams(GetData().OperType,
                                        getControl("DOP").Value,
                                        GetData().CurCode,
                                        GetData().CreditKind,
                                        GetData().UserCreditKind,
                                        GetData().FNCash);
         SetData(rslobj);

         RETURN TRUE;
      END;

      RETURN TRUE;
   END;

   // Вывести информацию на экран
   MACRO SetData(_Params)
      getControl("DOP").Value  = _Params.OperDate;
      RETURN SetData(_Params);
   END;

   MACRO ConvertData(_TaskNum, _Params, _InitStruct)
      var obj =
      RecalcGraphConvParams(_Params.OperType,
                            _Params.OperDate, 
                            _Params.CurCode,
                            _Params.CreditKind,
                            _Params.UserCreditKind,
                            _Params.FNCash);

      RETURN obj;
   END;

//constructor
   InitGroupOpBaseParm();
END;


//........................................................................//
//........................................................................//
//........................................................................//
//класс для описания параметров операции "Выгрузка Кредитной Истории"
CLASS (GroupOpBaseParm) GroupOpParm_Bki()
   MACRO Init()
      if (not isInitObj())
          addLabel(TRsbLabel(2, 1, "────────────────────────────────────────────────────────────────"));
          AddService(2, 2);
          AddThreads(2, 3);

          addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
      end;
   END;

   MACRO eventHandler(EV)
      RETURN TRUE;
   END;

   MACRO SetData(_Params)
      return null;
   END;

   //Вся необходимая информация задается на вкладке отбора
   MACRO GetData()
      return CNV_Panels[0].GetData();
   END;


//constructor
   InitGroupOpBaseParm();
END;