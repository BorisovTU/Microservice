IMPORT RSD;

macro LgoSqlExec(CmdText)
  var
      i = 0, ParmNum = 1,
      ParmVal = NULL, RSDcmd = NULL,
      ParmName = "ParmName";

    RSDcmd = RsdCommand(CmdText);
    if (RSDcmd != NULL)
        while (GetParm(ParmNum, ParmVal))
            ParmName = "Parm" + ParmNum;
            RSDcmd.addParam(ParmName, RSDBP_IN); RSDcmd.value(ParmName) = ParmVal;
            ParmNum = ParmNum + 1;
        end;
 
       RSDcmd.execute;
    end;
    return true;

    onError(er)
      return false;
end;

macro LgoRunStoredFunc(ProcName, RetValType, RetValSize)
  var
      ParmNum = 3, ParmVal = NULL, 
      RSDcmd = NULL, i = 0, ParmArr:TArray = Tarray();

  var
      cmdText = "begin ? := " + ProcName + "(";

    while (GetParm(ParmNum, ParmVal))
        ParmArr[ParmArr.size] = ParmVal;
        if (ParmNum == 3)
           cmdText = cmdText + "?";
        else
           cmdText = cmdText + ", ?";
        end;
        ParmNum = ParmNum + 1;
    end;
    cmdText = cmdText + "); end;";

    RSDcmd = RsdCommand(RslDefCon, cmdText);
    if  (RSDcmd == NULL)
        return NULL;
    end;

    if (RetValSize != NULL)
        RSDcmd.AddParam("retval", RSDBP_RETVAL, RetValType, RetValSize);
    else
       RSDcmd.AddParam("retval", RSDBP_RETVAL, RetValType            );
    end;

    ParmNum = 1;
    while(ParmNum <= ParmArr.size)
       RSDcmd.AddParam(string(ParmNum), RSDBP_IN, ParmArr[ParmNum - 1]);
       ParmNum = ParmNum + 1;
    end;

    RSDcmd.execute;
    RETURN RSDcmd.value(0);

    onError(er)
        return false;
END;

MACRO IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else
        return value_false;
    end;
END;