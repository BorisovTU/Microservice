/*
$Name: ws_lns_CrdCommLimit.mac
$Module: WebLoans
$Description: Сервис для работы с лимитами кредитного комитета
*/

IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac";

CLASS CLnsCrdCommLimitItem()
    VAR LimitID: integer            = 0;
    VAR LimitDate: Date             = Date(0,0,0);
    VAR LimitSum: Money             = $0; 
    VAR LimitCurrency: integer      = 0;
    VAR LimitCurrencyName: string   = "";
    VAR UserID: integer             = 0;
    VAR UserName: string            = "";
END;

MACRO Lns_GetCrdCommLimitList(ObjID, OnjN)
    VAR result = TArray(), item = NULL;
     
    VAR query = "SELECT c.*, p.t_name AS t_username, f.t_ccy AS t_limitcurrencyname FROM dcrdcommlimit_dbt c, dfininstr_dbt f, dperson_dbt p  WHERE t_objid = " + ObjID + " AND t_objn = " + OnjN + " AND t_oper =  c.t_userid AND f.t_fiid = c.T_LIMITCURRENCY order by t_limitid ";
    
    VAR RS = TRsbDataSet(query);

    WHILE (RS.MoveNext)
        item = CLnsCrdCommLimitItem();
        item.LimitID            = RS.LimitID;
        item.LimitDate          = RS.LimitDate;
        item.LimitSum           = RS.LimitSum;
        item.LimitCurrency      = RS.LimitCurrency;
        item.LimitCurrencyName  = RS.LimitCurrencyName;
        item.UserID             = RS.UserID;
        item.UserName           = RS.UserName;
        
        result[result.size]= item;
    END;

    return result;
END;

MACRO Lns_InsertCrdCommLimit(LnsObjID, CrdCommLimit)
    var ResponseBuilder = WebResponseBuilder();
    
    var cmdText = "INSERT INTO dcrdcommlimit_dbt (t_objid, t_objn, t_limitid, t_limitdate, t_limitsum, t_limitcurrency, t_userid) VALUES (?,?,?,?,?,?,?)";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, LnsObjID.ObjID);
    cmd.AddParam("", RSDBP_IN, LnsObjID.ObjN);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitID);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitDate);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitSum);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitCurrency);  
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.UserID);     
    
    cmd.execute();

    return ResponseBuilder.Result;
    
    OnError(err)
        if (err.Code == 42)
            ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, "Дублируется дата!" );
        else
            ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_UpdateCrdCommLimit(CrdCommLimit)
    var ResponseBuilder = WebResponseBuilder();
    var cmdText = "UPDATE dcrdcommlimit_dbt SET t_limitdate = ?, t_limitsum = ?, t_limitcurrency = ?, t_userid = ? WHERE t_limitid = ?";

    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitDate);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitSum);
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitCurrency);  
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.UserID);
    
    cmd.AddParam("", RSDBP_IN, CrdCommLimit.LimitID);    
    
    cmd.execute();
    
    return ResponseBuilder.Result;
    
    OnError(err)
        if (err.Code == 42)
            ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, "Дублируется дата!" );
        else
            ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_DeleteCrdCommLimit(LimitID: integer)
    var ResponseBuilder = WebResponseBuilder();
    
    var cmdText = "DELETE FROM dcrdcommlimit_dbt WHERE t_limitid = ?";
    
    var cmd = RsdCommand(cmdText);

    cmd.AddParam("", RSDBP_IN, LimitID);
    
    cmd.execute();
    
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;