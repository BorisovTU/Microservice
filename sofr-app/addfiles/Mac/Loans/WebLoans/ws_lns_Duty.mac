/*
$Name: ws_lns_Duty.mac
$Module: WebLoans
$Description: Сервис источника данных для получения информации по СО
*/

IMPORT SbCrdInter, RsbDataSet, globals, total, rsbus_structs, DDUTY_CRD_DBT;
IMPORT ws_lns_CommonLib, fmt_lib, ws_loansoperation, ws_lns_CrdContract, ws_Lns_Graph, ws_validation;

CLASS CLnsDutyViewPrm()
    VAR CrdID: integer             = 0;
    VAR CrdN:  integer              = 0;
    VAR DutyForm: integer          = 0;
    VAR DutyStatus: string         = "";
    VAR CrdNumber: string          = "";
    VAR UsNumber: string           = "";
    VAR Duration: integer          = 0;
    VAR AllocDuration: string      = "";
    VAR TypeDuration: string       = "";
    VAR RegDate: DateTime          = Date(0,0,0);
    VAR CloseDate: DateTime        = Date(0,0,0);
    VAR SumCredit: Money           = $0;
    VAR SumDuty: Money             = $0;
    VAR SumDebt: Money             = $0;
    VAR ExpDuty: Money             = $0;
    VAR CurCode: string            = "";
    VAR Account: CLnsMaskedAccount = NULL;
    VAR DateTransferRest: DateTime = Date(0,0,0);
    VAR ReasonTransferrest: string = "";
    VAR Comment: string            = "";
END;

CLASS CLnsDutyPostActionData()
    VAR IsBorrowingRates: bool = false;
    VAR IsAccOpen: bool = false;
    VAR IsGenerateGraphs: bool = false;
    VAR IsPayCredit: bool = false;
    VAR RatesSource: integer = 0;
    VAR PaySum: Money = $0;
END;

CLASS CLnsAdditionalDutyPrm()
    VAR CrdNumber: string = "";
    VAR TypeDuration: string = "";
    VAR Limit: Money = $0;
    VAR CrdSum: Money = $0;
    VAR CrdPayType: integer = 0;
    VAR CurCode: string = "";
    VAR DutyForm: integer = 0;
    VAR McGroupID: integer = 0;
    VAR RatesSource: integer = 0;
END;

// получить срок размещения СО (строка)
MACRO getAllocDuration(id, idgroup)
    var qStr = "SELECT p.t_Name FROM dmcperiod_dbt p " + 
               "WHERE p.t_groupnum = " + idgroup +
               " AND p.t_number = " + id;
    var result = LnSelectValue(qStr, V_STRING, 0);
    return result;
END;

MACRO lns_GetDutyViewPrm(ObjN)
    var result = CLnsDutyViewPrm(),
        Acc = CLnsMaskedAccount(),
        qStr = "",
        rs = NULL;
    
    qStr = "SELECT " + 
           "duty.t_usernumber, " + 
           "duty.t_dutystate, " +
           "duty.t_dutyfirstdate, " +
           "duty.t_dutylastdate, " +
           "duty.t_duration, " +
           "duty.t_dutysum, " + 
           "duty.t_userfield, " +
           "duty.t_datetransferrest, " +
           "credit.t_creditsum, " +
           "credit.t_curcode, "
           "credit.t_UsCreditNumber, " +
           "credit.t_CreditNumber, " +
           "credit.t_crd_kind, " +
           "fininstr.t_Ccy, " +
           "crlinparm.t_duty, " +
           "crlinparm.t_mcgroupid "
           "FROM dduty_crd_dbt duty, dcredit_c_dbt credit, " + 
           "dfininstr_dbt fininstr, dparmdemandline_dbt crlinparm " +          
           "WHERE credit.t_creditnumber = duty.t_creditnumber_ref " + 
           "AND fininstr.t_FIID = credit.t_CurCode "
           "AND crlinparm.t_objecttypeid = credit.t_Crd_Kind "  +
           "AND crlinparm.t_objectnumber = credit.t_CreditNumber " +
           "AND duty.t_dutyid = " + ObjN;
    
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        result.DutyStatus = String(rs.DutyState);
        result.DutyForm = Int(rs.duty);
        if (result.DutyForm == 0) // по траншам
            result.Duration = Int(rs.duration);
            result.TypeDuration = " дней";
            result.RegDate = Date(rs.DutyFirstDate);
            result.CloseDate = Date(rs.DutyLastDate);
            result.SumDuty = Money(rs.DutySum);
        elif (result.DutyForm == 1) // по ссудным счетам
            result.AllocDuration = getAllocDuration(int(rs.duration), int(rs.mcgroupid));
            result.SumDuty = NULL;
        else // Ещё по чему то.
            ;
        end;
        result.UsNumber = String(rs.usernumber);
        result.CrdNumber = String(rs.uscreditnumber);
        result.ExpDuty = ОстатокРегистра(LO_DUTY, ObjN, TDR_EXPREST, {curdate});
        result.SumDebt = result.ExpDuty + ОстатокРегистра(LO_DUTY, ObjN, TDR_MAINREST, {curdate});
        result.SumCredit = Money(rs.CreditSum);
        result.Curcode = String(rs.Ccy);
        result.CrdID = Int(rs.crd_kind);
        result.CrdN = Int(rs.CreditNumber);
        Acc.Account = НомерСчета (ObjN, int(rs.CurCode), CRD_ACC, LO_DUTY);
        Acc.Chapter = "А";
        result.Account = Acc;
        result.Comment = String(rs.userfield);
        result.DateTransferRest = Date(rs.datetransferrest);
        
    
    end;
    return result;
END;

MACRO lns_InitNewDuty(CrdN, DutyForm)
    var result = DDUTY_CRD_DBT();
    var credit = lns_GetCredit_c(CrdN);

    result.CreditNumber_ref = credit.creditnumber;
    result.FirstDutyID_ref = 0;
    result.DutyNumber = 1;
    if (DutyForm != DF_BY_ACCOUNT)
        result.DutyFirstDate = {curdate};
    else
        result.DutyFirstDate = credit.regDate;
    end;
    
    if (credit.PayType == CF_NONREP)
        result.DutySum = credit.CreditSum;
    elif((credit.PayType == CF_CRLIN) or
         (credit.PayType == CF_CRLINNEW) or
         (credit.PayType == CF_CRLINNO))
        result.DutySum = ОстатокРегистра(1, CrdN, TDR_AVAILABLELIMIT, {curdate});
    else
        result.DutySum = $0;
    end;
    
    result.DutyLastDate = credit.ReturnDate;
    
    if (DutyForm != DF_BY_ACCOUNT)
        result.Duration = result.DutyLastDate - result.DutyFirstDate;
    end;
    
    result.Annuit = credit.annuit;
    result.DutyState = CF_OPEN;
    result.DateTransferRest = Date(0,0,0);
    result.UserField = "";
    result.CloseReason = "";
    result.RetailAccPay_ref = 0;
    result.RetailAccDuty_ref = 0;
    result.DutyType = 0;
    result.SystType = 0;
    result.UserNumber = lns_GetGenerationNumberObject(LO_DUTY, result);
    return result;
END;

MACRO lns_GetAdditionalDutyPrm(CrdN)
    var result = CLnsAdditionalDutyPrm();
    var CoRates: integer = 0, ErrCode: integer = 0;
    var qStr = "";
    var rs = NULL;
    qStr = "SELECT " + 
           "credit.t_creditsum, " +
           "credit.t_curcode, "
           "credit.t_UsCreditNumber, " +
           "credit.t_CreditSum, " +
           "credit.t_paytype, " +
           "fininstr.t_Ccy, " +
           "crlinparm.t_duty, " +
           "crlinparm.t_mcgroupid "
           "FROM dcredit_c_dbt credit, " + 
           "dfininstr_dbt fininstr, dparmdemandline_dbt crlinparm " +          
           "WHERE credit.t_creditnumber = " + CrdN + 
           "AND fininstr.t_FIID = credit.t_CurCode "
           "AND crlinparm.t_objecttypeid = credit.t_Crd_Kind "  +
           "AND crlinparm.t_objectnumber = credit.t_CreditNumber ";
    rs = TRsbDataSet(qStr);
    if (rs.moveNext())
        result.CrdNumber = String(rs.UsCreditNumber);
        result.CurCode   = String(rs.ccy);
        result.DutyForm  = Int(rs.duty);
        result.CrdSum    = Money(rs.CreditSum);
        result.CrdPayType = Int(rs.PayType);
        result.McGroupID = int(rs.mcgroupid);
        result.TypeDuration = "дней";
        result.Limit = ОстатокРегистра(1, CrdN, TDR_AVAILABLELIMIT, {curdate});
        if (GetRegistryValue ("RS-LOANS\\УМОЛЧАНИЕ_ОБЪЕКТ_СО", V_INTEGER, CoRates, ErrCode) != V_INTEGER)
            RunError(ErrCode);
        end;
        if (CoRates == LO_TYPECREDIT) // заимствовать из ВК
            result.RatesSource = 0;
        elif (CoRates == LO_CREDIT) // из КД
            result.RatesSource = 1;
        else
            ;
        end;
    end;
    return result;
END;

MACRO lns_InsNewDuty(OpDate, Duty, PostAction)
    var result = WebResponseBuilder();
    var LnsObjID = CLnsObjID();
    var qStr = "";
    var rs = NULL;
    var stat = 0;
    record dutyBuff ("duty_crd.dbt");
    CopyClassObjToRecord(Duty, dutyBuff);
    stat = MakeOperation(Duty.CreditNumber_ref, LO_CREDIT, CF_DUTYOPEN, 0, OpDate, dutyBuff);
    if(stat == 0)
        result.AddErrorEx(0, MessageSeverity.RuntimeError, GetMO_LoansError());
    else
        qStr = "SELECT * FROM dcrd_op_dbt WHERE t_credoperid = " + stat;
        rs = TRsbDataSet(qStr);
        rs.moveNext();
        LnsObjID.ObjID = LO_DUTY;
        LnsObjID.ObjN = rs.ObjectId_Ref;

        result.SetUserObject(LnsObjID);

        if (PostAction != NULL) // постоперации
            var credit = lns_GetCredit_c(Duty.CreditNumber_ref);

            if (PostAction.IsAccOpen == true) // открыть счета
                stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, OP_ACC, 0, date(rs.CredOperDate), rs.ObjectId_Ref, 1);
                if (stat == 0)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка открытия счетов: " + GetMO_LoansError());
                end;
            end;

            if (PostAction.IsGenerateGraphs == true) // построить графики
                var errors = MakeGraphForObject(rs.ObjectTypeId_Ref, rs.ObjectId_Ref, date(rs.CredOperDate));
                for (var err, errors)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка построения графиков: " + err);
                end;
            end;
            
            if (PostAction.IsBorrowingRates == true) // заимствовать тарифы
                if (PostAction.RatesSource == 0)
                    stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, CF_RATE, credit.CurCode, date(rs.CredOperDate), 0, credit.CREDITTYPEID_REF, LO_TYPECREDIT);
                elif (PostAction.RatesSource == 1)
                    stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, CF_RATE, credit.CurCode, date(rs.CredOperDate), 0, Duty.CreditNumber_ref, LO_CREDIT);
                end;
                if (stat == 0)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка заимствования тарифов: " + GetMO_LoansError());
                end;
            end;
            
            if (PostAction.IsPayCredit == true) // выдать кредит
                var typeOp = RunStoredFunc("loansoperation.GetAndCheckDefaultOperation", V_INTEGER, NULL,rs.ObjectTypeId_Ref, rs.ObjectId_Ref, credit.CREDITTYPEID_REF, CS_PAY, CF_PAYN, 1, "");
                stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, typeOp, credit.CurCode, date(rs.CredOperDate), 0, 0, "", PostAction.PaySum);
                if (stat == 0)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка выдачи: " + GetMO_LoansError());
                end;
            end;
        end
    end;
    return result.Result;
END;


		
                          
