/*
$Name: ws_lns_BKI.mac
$Module: WebLoans
$Description: Сервис источника данных для получения информации по БКИ
*/

IMPORT RSD, RsbDataSet, SbCrdInter, ws_LoansOperation, likepy;

CLASS LBKIContItem()
    VAR CreditNumber_ref: integer  = 0;           // Ссылка на договор
    VAR Number          : integer  = 0;           // Порядковый номер
    VAR BKIID_ref       : integer  = 0;           // Ссылка на БКИ
    VAR BKIName         : string   = "";          // Название БКИ
    VAR AccessCode      : string   = "";          // Код доступа
    VAR ChangeDate      : DateTime = Date(0,0,0); // Дата изменения
    VAR ObjectTypeID    : integer  = 0;           // Идентификатор объекта Loans
    VAR ObjectNumber    : integer  = 0;           // Номер объекта Loans
END;

CLASS LBKIItem()
    VAR BKIID       : integer = 0;  // идентификатор БКИ
    VAR BKIName     : string  = ""; // Полное наименование БКИ
    VAR BKIShortName: string  = ""; // Краткое название БКИ
END;

MACRO lns_GetObjectBKIList(ObjID, ObjN)
    var item = NULL;
    var result = TArray();
    var qStr = "SELECT bc.*, b.t_bkishortname " + 
               "FROM DLBKICONT_DBT bc " + 
               "JOIN DLBKI_DBT b ON b.t_bkiid = bc.t_bkiid_ref " + 
               "WHERE bc.t_objecttypeid=? AND bc.t_objectnumber=?";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_objecttypeid", RSDBP_IN, ObjID);
    cmd.AddParam("p_objectnumber", RSDBP_IN, ObjN);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    while (rs.MoveNext)
        item = LBKIContItem();
        item.CreditNumber_ref = rs.CreditNumber_ref;
        item.Number           = rs.Number;
        item.BKIID_ref        = rs.BKIID_ref;
        item.BKIName          = rs.bkishortname;
        item.AccessCode       = rs.AccessCode;
        item.ChangeDate       = rs.t_Date;
        item.ObjectTypeID     = rs.ObjectTypeID;
        item.ObjectNumber     = rs.ObjectNumber;
        result[result.size] = item;
    end;
    return result;
END;

PRIVATE MACRO GetNextBKINumber(ObjID, ObjN)
    var result = 1;
    var qStr = "SELECT MAX(T_NUMBER) as t_maxval FROM DLBKICONT_DBT " +
               "WHERE T_OBJECTTYPEID=? AND T_OBJECTNUMBER=?";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_objecttypeid", RSDBP_IN, ObjID);
    cmd.AddParam("p_objectnumber", RSDBP_IN, ObjN);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        var maxval = rs.maxval;
        if (maxval)
            result = maxval + 1;
        end;
    end;
    return result;
END;


PRIVATE MACRO CheckUpdateBKI(BKIItem)
    if (BKIItem)
        if (BKIItem.Number <= 0)
            RunError("", "Введите порядковый номер БКИ!");
        end;
        if (BKIItem.BKIID_ref <= 0)
            RunError("", "Выберите БКИ!");
        end;
    end;
END;

PRIVATE MACRO CheckDeleteBKI(BKIItem)
    var qStr = "SELECT tr.* FROM dltr_file_dbt tr " +
               "JOIN dldevbki_dbt dev ON dev.t_tfid_ref = tr.t_tfid " + 
               "JOIN dlbki_dbt bk ON bk.t_bkiid = tr.t_bkiid_ref " +
               "WHERE dev.t_bkiid_ref = ? " +
               "AND   dev.t_objectNumber = ? " +
               "AND   dev.t_objecttypeid = ? ";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_bkiid_ref",    RSDBP_IN, BKIItem.BkiID_ref);
    cmd.AddParam("p_objectnumber", RSDBP_IN, BKIItem.ObjectNumber);
    cmd.AddParam("p_objecttypeid", RSDBP_IN, BKIItem.ObjectTypeID);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        RunError("", "Есть передача КИ. Удаление запрещено!");
    end;

END;

MACRO lns_InitObjectBKI(ObjID, ObjN)
    var result = LBKIContItem();
    if (ObjID == LO_CREDIT)
        result.CreditNumber_ref = ObjN;
    end;
    result.Number       = GetNextBKINumber(ObjID, ObjN);
    result.ChangeDate   = {curdate}; 
    result.ObjectTypeID = ObjID;
    result.ObjectNumber = ObjN;
    return result;
END;

MACRO lns_InsertObjectBKI(item)
    var result = TWsMessage();
    record BKIContBuff ("lbkicont.dbt");
    var BKICont = TBFile ("lbkicont.dbt", "rw", 0, "lbkicont.dbt", "loans.def");

    CheckUpdateBKI(item);

    BKIContBuff.CreditNumber_ref = item.CreditNumber_ref;
    BKIContBuff.Number           = item.Number;
    BKIContBuff.BKIID_ref        = item.BKIID_ref;
    BKIContBuff.AccessCode       = item.AccessCode;
    BKIContBuff.Date             = item.ChangeDate;
    BKIContBuff.ObjectTypeID     = item.ObjectTypeID;
    BKIContBuff.ObjectNumber     = item.ObjectNumber;
    copy(BKICont, BKIContBuff);
    BKICont.insert();
    return result;
    OnError(err)
        result.MsgID = err.Code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO lns_UpdateObjectBKI(item)
    var result = TWsMessage();
    record BKIContBuff ("lbkicont.dbt");
    var BKICont = TBFile ("lbkicont.dbt", "rw", 0, "lbkicont.dbt", "loans.def");

    CheckUpdateBKI(item);

    BKIContBuff.CreditNumber_ref = item.CreditNumber_ref;
    BKIContBuff.Number           = item.Number;
    BKIContBuff.BKIID_ref        = item.BKIID_ref;
    BKIContBuff.AccessCode       = item.AccessCode;
    BKIContBuff.Date             = {curdate};
    BKIContBuff.ObjectTypeID     = item.ObjectTypeID;
    BKIContBuff.ObjectNumber     = item.ObjectNumber;

    BKICont.rec.ObjectTypeID = item.ObjectTypeID;
    BKICont.rec.ObjectNumber = item.ObjectNumber;
    BKICont.rec.BKIID_ref    = item.BKIID_ref;
    if (BKICont.GetEQ())
        copy(BKICont, BKIContBuff);
        BKICont.update();
    else
        result.MsgText = "Запись не найдена!";
        result.MsgID = 1;
    end;
    return result;
    OnError(err)
        result.MsgID = err.Code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO lns_DeleteObjectBKI(item)
    var result = TWsMessage();
    var BKICont = TBFile ("lbkicont.dbt", "rw", 0, "lbkicont.dbt", "loans.def");

    CheckDeleteBKI(item);

    BKICont.rec.ObjectTypeID = item.ObjectTypeID;
    BKICont.rec.ObjectNumber = item.ObjectNumber;
    BKICont.rec.BKIID_ref    = item.BKIID_ref;
    BKICont.rec.Number       = item.Number;
    if (BKICont.GetEQ())
        BKICont.delete();
    else
        result.MsgText = "Запись не найдена!";
        result.MsgID = 1;
    end;

    return result;
    OnError(err)
        result.MsgID = err.Code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO lns_GetBKIList(ObjID, ObjN)
    var item = null;
    var result = TArray();
    var qStr = "SELECT b.t_BKIID, b.t_BKISHORTNAME, b.t_BKINAME FROM DLBKI_DBT b ";
    var cmd = RsdCommand();
    cmd.CmdText = qStr;
    if (ObjID and ObjN)
        qStr = qStr + "WHERE b.t_BKIID not in (SELECT bc.t_bkiid_ref FROM DLBKICONT_DBT bc WHERE bc.T_OBJECTTYPEID=? AND bc.T_OBJECTNUMBER=?)";
        cmd.CmdText = qStr;
        cmd.AddParam("p_objecttypeid", RSDBP_IN, ObjID);
        cmd.AddParam("p_objectnumber", RSDBP_IN, ObjN);
    end;
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    while (rs.MoveNext)
        item = LBKIItem();
        item.BKIID        = rs.BKIID;
        item.BKIName      = rs.BKIName;
        item.BKIShortName = rs.BKIShortName;
        result[result.size] = item;
    end;
    return result;
END;

MACRO lns_GetBKIByID(BKIID)
    var result = LBKIItem();
    var qStr = "SELECT t_BKIID, t_BKISHORTNAME, t_BKINAME FROM DLBKI_DBT WHERE t_BKIID=?";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_bkiid", RSDBP_IN, BKIID);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        result.BKIID        = rs.BKIID;
        result.BKIName      = rs.BKIName;
        result.BKIShortName = rs.BKIShortName;    
    end;
    return result;
END;