/*
$Name: ws_lns_Tariff.mac
$Module: WebLoans
$Description: Сервис получения данных по тарифам
*/
IMPORT SbCrdInter, RsbDataSet;
IMPORT "total.mac", globals;

CLASS CLnsTariffUsRate()
    var name = string();
    var value = string();
    var rateid = 0;
    var hasHistory = false;
END;

CLASS CLnsTariffHistoryItem()
    var date: DateTime;
    var value = string();
END;

MACRO lns_usratelist(ObjID: integer, ObjN: integer)
    var result = TArray();
    var item = null;
    var qStr = "";
    var rs = NULL;
    var RSDcmd = NULL;
    if (ObjID == LO_CLAIM) // для заявки c типовой тарификацией, тарифы берутся из ВК.
        qStr = "SELECT t_credittypeid_ref, T_TPLANTCRID, T_CONDID, T_TUNETYPE FROM dclaim_dbt where t_claimid = " + ObjN;
        rs = TRsbDataSet(qStr);
        if (rs.MoveNext)
            if (rs.TuneType != CF_IND) // если тарификация не индивидуальная, то идем в вид кредита
                ObjID = LO_TYPECREDIT;
                ObjN = rs.credittypeid_ref;
                if (rs.CondID)
                    ObjID = LO_CONDTARIFFPLAN;
                    ObjN = rs.CondID;
                elif (rs.TPlanTcrID)
                    ObjID = LO_TARIFFPLAN;
                    ObjN = rs.TPlanTcrID;
                end;
            end;
        else
            RunError("Не найдена заявка с идентификатором " + ObjN);
        end;
    end;
    
    qStr = "select lc.t_TypeRateName, lc.t_TypeRateID, rate.t_RateID_Ref, " +
           "(select count(*) from dlchistry_dbt hist where  rate.t_RateID_ref = hist.t_RateID_ref(+)) cnt " +
           "from dlcusrate_dbt rate, dlctypuse_dbt lc " + 
           "where rate.t_ObjectID_ref = " + ObjID +
           " and rate.t_CredObjID_ref = " + ObjN + 
           " and lc.t_TypeRateID = rate.t_TypeRateID_ref";
    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
        item = CLnsTariffUsRate();
        item.name  = string(rs.TypeRateName);
        item.value = GetTrffRateStr(ObjID, ObjN, rs.TypeRateID, {curdate});
        item.rateid = int(rs.RateID_Ref);
        if (int(rs.cnt)>0)
            item.hasHistory = true;
        end;
        result[result.size] = item;
    end;

    return result;
END;

MACRO lns_getratehistory(objID, objN, RateID: integer)
    var result = TArray();
    var item = null;
    var qStr = "";
    var rs = NULL;
    var RSDcmd = NULL;

    if (ObjID == LO_CLAIM) // для заявки, тарифы берутся из ВК.
        qStr = "SELECT t_credittypeid_ref, T_TPLANTCRID, T_CONDID, T_TUNETYPE FROM dclaim_dbt where t_claimid = " + ObjN;
        rs = TRsbDataSet(qStr);
        if (rs.MoveNext)
            if (rs.TuneType != CF_IND) // если тарификация не индивидуальная, то идем в вид кредита
                ObjID = LO_TYPECREDIT;
                ObjN = rs.credittypeid_ref;
                if (rs.CondID)
                    ObjID = LO_CONDTARIFFPLAN;
                    ObjN = rs.CondID;
                elif (rs.TPlanTcrID)
                    ObjID = LO_TARIFFPLAN;
                    ObjN = rs.TPlanTcrID;
                end;
            end;
        else
            RunError("Не найдена заявка с идентификатором " + ObjN);
        end;
    end;

    RsdCommand("delete from DLCHISTRY_TMP;").execute();
    RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := RSI_loansTariff.FillFileLRTMP (?); END;");
    
    RSDcmd.addParam("retval", RSDBP_RETVAL, V_INTEGER );
    RSDcmd.addParam("RateID", RSDBP_IN); RSDcmd.Value ("RateID") = RateID;
    RSDcmd.execute();

    rs = TRsbDataSet("select lch.*, rate.t_TypeRateID_ref from DLCHISTRY_TMP lch, dlcusrate_dbt rate where lch.t_RateID_Ref=rate.t_RateID_Ref");
    while (rs.moveNext())
        item = CLnsTariffHistoryItem();
        item.date = rs.ratedate;
        item.value = GetTrffRateStr(ObjID, ObjN, rs.TypeRateID_ref, rs.ratedate);
        result[result.size] = item;
    end;

    return result;
END;
