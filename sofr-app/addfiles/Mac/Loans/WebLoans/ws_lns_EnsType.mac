/*
$Name: ws_lns_EnsType.mac
$Module: WebLoans
$Description: Сервис источника данных для листалки "Виды обеспечений"
*/
IMPORT RsbDataSet, total;

CLASS CLnsEnsType()
    VAR EnsTypeID: integer = 0;   // Идентификатор вида обеспечения
    VAR EnsTypeName: string = ""; // Название вида обеспечения
    VAR FullName: string = "";    // Полное название вида обеспечения
END;

MACRO lns_EnsTypeList(TypeEnsID)
    VAR result = TArray(), obj = NULL;
    VAR rs = NULL;
    VAR query = "SELECT et.* FROM DENSTYPE_DBT et";
    
    if (TypeEnsID != null)
        query = query + ", DENS_KIND_DBT ek " +
                "WHERE et.t_enstypeid = ek.t_enstypeid_ref " +
                "AND ek.t_typeensid_ref = " + TypeEnsID;
    end;
    
    rs = TRsbDataSet(query);
    WHILE (rs.moveNext())
        obj = CLnsEnsType();

        obj.EnsTypeID   = rs.EnsTypeID;
        obj.EnsTypeName = rs.EnsTypeName;
        obj.FullName    = rs.FullName;
        
        result[result.size] = obj;
    END;
  
    RETURN result;
END;

MACRO lns_EnsTypeByID(EnsTypeID)
   VAR result = CLnsEnsType();
   VAR rs = NULL;

   VAR query = "select * from DENSTYPE_DBT WHERE T_ENSTYPEID = " + EnsTypeID;

    rs = TRsbDataSet(query);
    if (rs.moveNext())
        result.EnsTypeID   = rs.EnsTypeID;
        result.EnsTypeName = rs.EnsTypeName;
        result.FullName    = rs.FullName;
    end;
  
    RETURN result;

END;



