/*
$Name: LnsRemoteCall.mac
$Module: WebLoans
$Description: Вызов удаленных процедур. (RsConnect)
*/

IMPORT XmlRpcInter, serviceaction, RsbDataSet;


MACRO LnsGetGuid()
 var GUID = CreateGUID();
 return SubStr(GUID, 2, 36);
END;


// Функция удаленного вызова макроса. Отправляет RsXMLRPC, завернутое в SOAP
// Подходит для вызова RsConnect
// Возвращает RSL объект - результат вызова удаленной функции
// url - адрес сервиса
// timeout - таймаут вызова
// macrofile - имя макроса
// func - имя функции
// далее - сколько угодно параметров, которые нужно передать в удаленную функцию
// URL = "http://localhost:8080/RsConnect/ws/rsconnect?wsdl"
// macrofile = "GU_statement"
// func = "Statement"

MACRO LnsRemoteMacroCall(url, timeout, macrofile, func)
    
    var parm:variant, i:integer = 4, parms= TArray();
    while( GetParm(i, parm) )
        parms[parms.size] = parm;
        i = i + 1;
    end;

    var XRReq = TXRRequest();
    XRReq.macroName = macrofile;
    XRReq.macroFunc = func;
    XRReq.Parms = Parms;
    XRReq.Direction = 1;
    XRReq.Parms = Parms;
    
    var login = "";
    var pass = "";
   
    var method = "XMLRPCCall";
    var outParams;
    var RespObj;
    var reqParms = TArray;

    var inParam_request = RslSoapPrm;
  	inParam_request.Name = "request";  
  	inParam_request.Value = XRReq.GenerateMessage(false);
  	reqParms[0] = inParam_request;
	
    var inParam_timeout = RslSoapPrm;
  	inParam_timeout.Name = "timeout";  
  	inParam_timeout.Value = timeOut;
  	reqParms[1] = inParam_timeout;
	
    var inParam_priority = RslSoapPrm;
  	inParam_priority.Name = "priority";  
  	inParam_priority.Value = 0;
  	reqParms[2] = inParam_priority;

    var stat = CallSimpleExtService("1", url, method, ReqParms, login, pass, timeout, outParams);
    if (stat == 1)
        RunError("Неверный адрес URL");
    elif (stat == 2)
        RunError("Сервис не имеет метода " + method);
    elif (stat == 3)
        RunError("Перечень переданных параметров не соответствует перечню параметров метода");
    elif (stat == 4)
        RunError("Ошибка авторизации: Неверные логин и пароль");
    elif (stat == 5)
        RunError("Ошибка авторизации: Не заданы логин и пароль"); 
    end;
    if (outParams.Size > 0)
        stat = ConvertToRSL(outParams[0].Value, RespObj);
        if (stat != 0)
            RunError("Ошибка разбора ответа");
        end;
        if (GenPropID(RespObj, "faultString") != -1)
             RunError(RespObj.faultString);
        end;
    end;
    return RespObj;
END;

