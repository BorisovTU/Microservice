/*
$Name: ws_lns_ClaimList.mac
$Module: WebLoans
$Description: Источник данных для списка заявок
$Created: 02.10.2018
*/
IMPORT SbCrdInter, BankInter, RsbDataSet, mfo_lib, cb_sql, ws_datafilter;

private const DPRT_TYPE_FILIAL = 1;
private const DPRT_TYPE_VSP    = 2;

CLASS ClaimStageTreeData
    VAR TypeDebtor   : integer;
    VAR ClaimStageID : integer;
    VAR Name         : string;
    VAR Childs;
    
    TypeDebtor   = -1;
    ClaimStageID = -1;
    Name = "";
END;

MACRO lns_GetClaimStageTree()
    var qStr = "",
        rs = null,
        cnt = 0,
        result = TArray(),
        stages = TArray(),
        item;
        
    qStr = "SELECT T_ID, T_NAME FROM DLCLAIMSTAGE_DBT";
                  
    rs = TRsbDataSet(qStr);
    
    while (rs.MoveNext())
        item = ClaimStageTreeData();
        item.ClaimStageID = rs.id;
        item.Name = rs.name;
        stages[stages.size] = item;
    end;

    item = ClaimStageTreeData();
    item.ClaimStageID = 0;
    item.Name = "Все заявки";
    stages[stages.size] = item;
    
    item = ClaimStageTreeData();
    item.TypeDebtor = 1;
    item.Name = "Юридические лица";
    item.childs = TArray();
    cnt = 0;
    while (cnt < stages.size)
        item.childs[cnt] = ClaimStageTreeData();
        item.childs[cnt].ClaimStageID = stages[cnt].ClaimStageID;
        item.childs[cnt].Name = stages[cnt].Name;
        item.childs[cnt].TypeDebtor = 1;
        cnt = cnt + 1;
    end;
    result.value(result.size) = item;
    
    item = ClaimStageTreeData();
    item.TypeDebtor = 2;
    item.Name = "Физические лица";
    item.childs = TArray();
    cnt = 0;
    while (cnt < stages.size)
        item.childs[cnt] = ClaimStageTreeData();
        item.childs[cnt].ClaimStageID = stages[cnt].ClaimStageID;
        item.childs[cnt].Name = stages[cnt].Name;
        item.childs[cnt].TypeDebtor = 2;
        cnt = cnt + 1;
    end;
    result.value(result.size) = item;
    
    item = ClaimStageTreeData();
    item.TypeDebtor = 0;
    item.Name = "Все клиенты";
    item.childs = TArray();
    cnt = 0;
    while (cnt < stages.size)
        item.childs[cnt] = ClaimStageTreeData();
        item.childs[cnt].ClaimStageID = stages[cnt].ClaimStageID;
        item.childs[cnt].Name = stages[cnt].Name;
        item.childs[cnt].TypeDebtor = 0;
        cnt = cnt + 1;
    end;
    result.value(result.size) = item;
    
    return result;
end;

CLASS CLnsClaimTreeModeItem
    VAR ClaimStageId:   integer = 0;
    VAR ClaimStageName: string = "";
END;

MACRO lns_GetClaimTreeModeList()
    var qStr = "",
        rs = null,
        result = TArray(),
        item : CLnsClaimTreeModeItem = NULL;
    
    qStr = "SELECT CLSTG.T_ID, CLSTG.T_NAME" +
           " FROM DLCLAIMSTAGE_DBT CLSTG" +
           " WHERE CLSTG.T_ISNOTUSE <> 'X'";
                  
    rs = TRsbDataSet(qStr);
    
    while (rs.MoveNext())
        item = CLnsClaimTreeModeItem();
        item.ClaimStageID = rs.id;
        item.ClaimStageName = rs.name;
        result[result.size] = item;
    end;
    
    return result;
END;

PRIVATE MACRO GetFininstCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = "";       
    
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = " fi.T_FIID ";
        var i:integer = 0;
        while (i < value.Size)
            if ((ValType(value[i].Fiid) == V_INTEGER) and (condition == FilterCondition_Equal))
                if (i > 0)
                    strSQLCond = strSQLCond + ", " + value[i].Fiid;                
                else    
                    strSQLCond = strSQLCond + " IN (" + value[i].Fiid;
                end;
                            
            end;                  
            i = i + 1;
        end;
        strSQLCond = strSQLCond + ")";
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

PRIVATE MACRO GetClaimStatusCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = "";
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = " st.T_ID ";
        var i:integer = 0;
        while (i < value.Size)
            if ((ValType(value[i].ID) == V_INTEGER) and (condition == FilterCondition_Equal))
                if (i > 0)
                    strSQLCond = strSQLCond + ", " + value[i].ID;                
                else    
                    strSQLCond = strSQLCond + " IN (" + value[i].ID;
                end;
                            
            end;                  
            i = i + 1;
        end;
        strSQLCond = strSQLCond + ")";
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

PRIVATE MACRO GetPartyCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = "";       
    
    if ((ValType(value) != V_UNDEF) 
    and (value.Size > 0)
    and (ValType(value[0].partyid) == V_INTEGER)
    and (condition == FilterCondition_Equal)
    )
        strSQLCond = " pr.T_PARTYID = " + value[0].partyid;
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

PRIVATE MACRO GetDepartmentCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = "";           
   
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        var i:integer = 0;
        while (i < value.Size)
            if ((ValType(value[i].Code) == V_INTEGER) and (condition == FilterCondition_Equal))
                if (i > 0)
                    strSQLCond = strSQLCond + ", " + value[i].Code;                
                else    
                  if ((ValType(value[i].NodeType) == V_INTEGER) and (ValType(value[i].NodeType) == DPRT_TYPE_FILIAL))
                          strSQLCond = " cl.T_FNCASH IN (" + value[i].Code;        
                  elif ((ValType(value[i].NodeType) == V_INTEGER) and (ValType(value[i].NodeType) == DPRT_TYPE_VSP)) 
                          strSQLCond = " cl.T_TERRITORIALSTRUCT IN (" + value[i].Code;
                  end;		
                end;
                            
            end;                  
            i = i + 1;
        end;
        strSQLCond = strSQLCond + ")";
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

PRIVATE MACRO GetClaimRouteCondition(condition:Integer, value ,type:Integer):String
    var strSQLCond:String = "";           
   
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = " cl.T_PROCESSNAME ";
        var i:integer = 0;
        while (i < value.Size)
            if ((ValType(value[i].ProcessID) == V_STRING) and (condition == FilterCondition_Equal))
                if (i > 0)
                    strSQLCond = strSQLCond + "OR cl.T_PROCESSNAME LIKE '" + StrSubst(StrSubst(value[i].ProcessID, "?", "_" ), "*", "%" ) + "' ";               
                else    
                    strSQLCond = strSQLCond + " LIKE '" + StrSubst(StrSubst(value[i].ProcessID, "?", "_" ), "*", "%" ) + "' ";
                end;
                            
            end;                  
            i = i + 1;
        end;
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

private const CRD_PRODUCT_CONTRACT = 0;
private const CRD_PRODUCT_DRAFT    = 1;
private const CRD_PRODUCT_GS       = 2;

PRIVATE MACRO GetProductCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = "";
   
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        var i:integer = 0;
        strSQLCond = strSQLCond + "(";
        while (i < value.Size)
            if (ValType(value[i]) == V_INTEGER)
            
                if (i > 0)
                    strSQLCond = strSQLCond + " OR ";
                end;        
                    
                if (value[i] == CRD_PRODUCT_CONTRACT)
                    strSQLCond = strSQLCond + " CL.T_CREDITNUMBER_REF IN (SELECT CR.T_CREDITNUMBER" + 
                                                                        " FROM DCREDIT_C_DBT CR" +
                                                                        " WHERE CR.T_CRD_KIND = 1 AND CR.T_CREDITSTATE LIKE 'О')";
                elif (value[i] == CRD_PRODUCT_DRAFT)
                    strSQLCond = strSQLCond + " CL.T_CREDITNUMBER_REF IN (SELECT CR.T_CREDITNUMBER" + 
                                                                        " FROM DCREDIT_C_DBT CR" +
                                                                        " WHERE CR.T_CRD_KIND = 1 AND CR.T_CREDITSTATE LIKE 'Ч')";                
                elif (value[i] == CRD_PRODUCT_GS)
                    strSQLCond = strSQLCond + " CL.T_CREDITNUMBER_REF IN (SELECT CR.T_CREDITNUMBER" + 
                                                                        " FROM DCREDIT_C_DBT CR" +
                                                                        " WHERE CR.T_CRD_KIND = 25)";                
                end;           
            end;                  
            i = i + 1;
        end;
        strSQLCond = strSQLCond + ")";
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

PRIVATE MACRO GetWhere(FilterItems)
    var fp = FilterParser( FilterItems );
    fp.AddFieldCondition    ( 0, "cl", "T_CREDITTYPEID_REF");
    fp.AddFieldCondition    ( 1, "cl", "T_BCREDITSUM");
    fp.AddUserFieldCondition( 2, @GetFininstCondition);
    fp.AddUserFieldCondition( 3, @GetClaimStatusCondition);
    //fp.AddFieldCondition    ( 3, "cl", "T_CLAIMSTATE");
    fp.AddFieldCondition    ( 4, "cl", "T_CLAIMNUMBER");
    fp.AddFieldCondition    ( 5, "cl", "T_GIVINGDATE");
    fp.AddUserFieldCondition( 6, @GetPartyCondition);
    fp.AddFieldCondition    ( 7, NULL, "DECODE(cl.T_TYPEDEBTOR, 2, 0, 1)");
    fp.AddUserFieldCondition( 8, @GetProductCondition);
    fp.AddUserFieldCondition( 9, @GetDepartmentCondition);
    fp.AddUserFieldCondition( 10, @GetDepartmentCondition);
    fp.AddUserFieldCondition( 11, @GetClaimRouteCondition);
    fp.AddFieldCondition    ( 12, "cl", "T_NEXTACTION");
    return fp.GetFullSQLCondition();
END;

PRIVATE MACRO GetListQuery(TypeDebtor, ClaimStageID, FilterConditionItems)
    var restriction_str = "";
    CaptureOutput;
    [
        SELECT cl.T_CLAIMID, 
               cl.T_TYPEDEBTOR as TypeDebtorID, 
               cl.t_stageid as ClaimStageID,
               cl.T_CLAIMNUMBER, 
               cl.T_GIVINGDATE, 
               st.T_NAME as ClaimState, 
               pr.t_name as Debtor, 
               pr.t_partyid as DebtorID,
               DECODE(cl.T_TYPEDEBTOR, 2, 'ФЛ', 'ЮЛ') as TypeDebtor,
               DECODE(cl.T_CANOPENPRODUCT, 'X', cl.T_BCREDITSUM, cl.T_LCREDITSUM) as CreditSum, 
               fi.T_CCY as CurCode,
               cl.T_NEXTACTION as Action, 
               TO_DATE('01.01.0001', 'DD.MM.YYYY') as NextActionDate
        FROM DCLAIM_DBT cl
        JOIN DPARTY_DBT pr ON cl.T_LOANERID_REF = pr.T_PARTYID
        JOIN DFININSTR_DBT fi ON cl.T_BCREDITCURCODE = fi.T_FIID
        JOIN DLCLAIMSTATUS_DBT st ON cl.T_CLAIMSTATE = st.T_ID
        WHERE cl.t_moduletype = #
    ](string(LNS_MT_WEBCREDIT));
    VAR queryStr = StopCaptureOutput;
    if (TypeDebtor > 0)
        queryStr = queryStr + " AND cl.T_TYPEDEBTOR = " + TypeDebtor;
    end;

    if (ClaimStageID > 0)
        queryStr = queryStr + " AND cl.t_stageid = " + ClaimStageID;
    end;

    if (GetObjectRestriction(restriction_str, 55, {oper}, "cl"))
        queryStr = queryStr + " AND " + restriction_str;
    end;
    if (FilterConditionItems != null)
        var filter = GetWhere(FilterConditionItems);
        if (filter != "")
            queryStr = queryStr + " AND " + filter;
        end;
    end;    
    
    queryStr = queryStr + " ORDER BY T_CLAIMID ";
    return queryStr;
END;

CLASS CListClaimItem
   VAR ClaimID        : integer  = 0;
   VAR TypeDebtorID   : integer  = 0;
   VAR ClaimStageID   : integer  = 0;
   VAR ClaimNumber    : string   = "";
   VAR GivingDate     : Date     = Date(0,0,0);
   VAR ClaimState     : string   = "";
   VAR Debtor         : string   = "";
   VAR DebtorID       : integer  = 0;
   VAR TypeDebtor     : string   = "";
   VAR CreditSum      : Money    = $0;
   VAR CurCode        : string   = "";
   VAR Action         : string   = "";
   VAR NextActionDate : Date     = Date(0,0,0);
END;

MACRO lns_GetClaimList(pagesize : integer,
                       pagenum  : integer,
                       TypeDebtor: integer,
                       ClaimStageID: integer,
                       FilterConditionItems)
    var result = TArray(); 
    var obj = null; 
    var queryStr = GetListQuery(TypeDebtor, ClaimStageID, FilterConditionItems);

    IF (pagesize > 0)
        queryStr = SQL_WrapSelect(queryStr, pagenum, pagesize);
    END;

    VAR rs = TRsbDataSet(queryStr);

    WHILE (rs.moveNext)
        obj = CListClaimItem();

        obj.ClaimID        = rs.ClaimID;
        obj.TypeDebtorID   = rs.TypeDebtorID;
        obj.ClaimStageID   = rs.ClaimStageID;
        obj.ClaimNumber    = rs.ClaimNumber;
        obj.GivingDate     = rs.GivingDate;
        obj.ClaimState     = rs.ClaimState;
        obj.Debtor         = rs.Debtor;
        obj.DebtorID       = rs.DebtorID;
        obj.TypeDebtor     = rs.TypeDebtor;
        obj.CreditSum      = rs.CreditSum;
        obj.CurCode        = rs.CurCode;
        obj.Action         = rs.Action;
        obj.NextActionDate = rs.NextActionDate;

        result.value(result.Size) = obj;
    END;

    RETURN result;
END;

MACRO lns_GetClaimListCount(TypeDebtor, ClaimStageID, FilterConditionItems)
    var qstr = GetListQuery(TypeDebtor, ClaimStageID, FilterConditionItems);
    var result = SQL_GetNRecs(qstr);
    RETURN result;
END;