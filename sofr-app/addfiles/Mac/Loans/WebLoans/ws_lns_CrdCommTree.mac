/*
$Name: ws_lns_CrdCommTree.mac
$Module: WebLoans
$Description: Сервис источника данных дерева видов кредитных комитетов
*/
IMPORT RSD, PTInter, total, likepy;
IMPORT fmt_lib, DTYPECRDCOMM_DBT;

CLASS CLnsCrdCommTreeItem()
    VAR ItemName     : string  = "";   // Имя элемента
    VAR TypeDebtor   : integer = 0;    // Тип заемщика: физик/юрик/все
    VAR TypeCrdCommID: integer = 0;    // Вид кредитного комитета
    VAR Children               = null; // Дочерние элементы
END;

MACRO lns_GetCrdCommTree()
    var result = Tarray();
    var resultItem = null;
    var TypeCrdCommList = TArray();
    var CrdCommTreeItem = null;
    var CrdCommChildItem = null;
    var TypeCrdComm = null;
        
    CrdCommTreeItem = CLnsCrdCommTreeItem();
    CrdCommTreeItem.ItemName = "Комитеты по заявкам юр. лиц";
    CrdCommTreeItem.TypeDebtor = PTLEGF_INST; // юрики;
    CrdCommTreeItem.Children = TArray();
    result[result.size] = CrdCommTreeItem;

    CrdCommTreeItem = CLnsCrdCommTreeItem();
    CrdCommTreeItem.ItemName = "Комитеты по заявкам физ. лиц";
    CrdCommTreeItem.TypeDebtor = PTLEGF_PERSN; // физики;
    CrdCommTreeItem.Children = TArray();
    result[result.size] = CrdCommTreeItem;

    CrdCommTreeItem = CLnsCrdCommTreeItem();
    CrdCommTreeItem.ItemName = "Комитеты по заявкам юр. и физ. лиц";
    CrdCommTreeItem.TypeDebtor = PTLEGF_ALL; // юрики и физики;
    CrdCommTreeItem.Children = TArray();
    result[result.size] = CrdCommTreeItem;
    
    CrdCommTreeItem = CLnsCrdCommTreeItem();
    CrdCommTreeItem.ItemName = "Все комитеты";
    CrdCommTreeItem.TypeDebtor = 3; // ваще все; В C константа есть, но в интер не вынесена
    CrdCommTreeItem.Children = TArray();
    result[result.size] = CrdCommTreeItem;

    var qStr = "SELECT * FROM DTYPECRDCOMM_DBT ORDER BY t_TypeDebtor";
    var rs = RsdRecordset(qStr);
    while (rs.MoveNext)
        TypeCrdCommList.value(TypeCrdCommList.Size) = GetObjectFromRecordset("DTYPECRDCOMM_DBT", rs);
    end;

    for (resultItem, result)
        for (TypeCrdComm, TypeCrdCommList)
            if ((resultItem.TypeDebtor == 3) or (TypeCrdComm.TypeDebtor == resultItem.TypeDebtor))
                CrdCommChildItem = CLnsCrdCommTreeItem();
                CrdCommChildItem.ItemName = TypeCrdComm.typecrdcommname;
                CrdCommChildItem.TypeDebtor = TypeCrdComm.TypeDebtor; 
                CrdCommChildItem.TypeCrdCommID = TypeCrdComm.ID;
                resultItem.children[resultItem.children.size] = CrdCommChildItem;
            end;
        end;
    end;
    return result;
END;
