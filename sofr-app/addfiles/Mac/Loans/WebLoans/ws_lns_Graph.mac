/*
$Name: ws_lns_Graph.mac
$Module: WebLoans
$Description: Сервис получения информации по графикам
*/
IMPORT RSD, mfo_lib, SbCrdInter, BankInter, RsbDataSet, globals, likepy, total;


/*
Новая функциональность графиков (CIS)
*/
PRIVATE CONST CAPTION_ONLYNAME = 0;   // в строке заголовка - только название графика
PRIVATE CONST CAPTION_WITHOBJECT = 1; // в строке заголовка название графика + название объекта
PRIVATE CONST CAPTION_FULL = 2;       // в строке заголовка название графика + название объекта + пользовательский номер объекта

CLASS CLnsGraphItem()
    VAR PayDate: DateTime = Date(0,0,0); // Дата платежа
    VAR ExpDate: DateTime = Date(0,0,0); // Дата просрочки
    VAR SumPay : Money    = $0;          // Сумма платежа
END;

CLASS CLnsGraph()
    VAR ID           : integer = 0;     // Идентификатор графика
    VAR GraphKindID  : integer = 0;     // Идентификатор вида графика
    VAR GraphKindName: string  = "";    // Название вида графика
    VAR ObjID        : integer = 0;     // Тип объекта (КД, СО, ДГ, БГ)
    VAR ObjN         : Integer = 0;     // Номер объекта
    VAR ObjName      : string  = 0;     // Название объекта
    VAR ObjUsNumber  : string  = "";    // Пользовательский номер объекта
    VAR IsGrace      : bool    = false; // Отсрочка платежа
    VAR GraphItems   : TArray  = null;  // массив записей графика одного вида для объекта
END;


CLASS CLnsGraphTreeItem() // для отображения дерева графиков по объекту
    VAR GraphID       : integer = 0;        // Идентифкатор графика для объекта
    VAR ParentID      : integer = 0;        // Идентификатор родительского элемента
    VAR CredOperID    : integer = 0;        // Идентификатор операции, по которой формировался график
    VAR GraphKindID   : integer = 0;        // Идентификатор вида графика
    VAR GraphKindName : string  = "";       // Название вида графика
    VAR ObjectTypeName: string  = "";       // Название вида объекта
    VAR IsAnnuity     : bool    = false;    // Аннуитет
    VAR IsInComplete  : bool    = false;    // Свод
    VAR IsMatching    : bool    = false;    // Квитовка
    VAR IsDefault     : bool    = false;    // По умолчанию
    VAR IsGroup       : bool    = false;    // Является ли группой
    VAR Children                = TArray(); // дочерние элементы
END;


/*
Блок вспомогательных объектов: классов и функций
Для чего нужен каждый объект - есть комменты
*/

// Доп. класc для хранения идентификатора графика и последней операции по нему
CLASS LatestGraphOper()
    var GraphID = 0; // График
    var GraphKind = 0; // вид графика
    var OperId = 0;    // последняя операция
END;

// нет лямбд, поэтому для функции map пришлось делать вспомогательную функцию.
// которая из объекта класса LatestGraphOper формирует SQL выражение
// для выборки последней операции для конкретного вида графика
PRIVATE MACRO GetLatestOperExpr(LatestOperItem)
    if (LatestOperItem != null)
        CaptureOutput;
        [
            (gtp.t_id = #
            AND gkp.t_credopid_ref = #
            )
        ](string(LatestOperItem.GraphID), string(LatestOperItem.OperId));
        return StopCaptureOutput;
    end;
    return "";
END;

PRIVATE MACRO GetGraphID(LatestOperItem)
    return LatestOperItem.GraphID;
END;
/*
        Конец блока вспомогательных объектов 
*/



// возвращает последнюю операцию формирования графика
// GraphID - идентификатор t_id из таблицы dgraphtreeparm_dbt 
PRIVATE MACRO GetLatestOperForGraph(GraphID)
    var result = 0;
    CaptureOutput;
    [
        SELECT distinct RSI_LOANSGRAPHKIND.GETLASTOPIDGRAPH(go.t_objectid, go.t_objectnumber, gk.t_num) as t_lastoper
        FROM DGRAPHTREEPARM_DBT gtp
        JOIN DGRAPHOBJECT_DBT go ON gtp.t_graphobject_id_ref = go.t_id
        JOIN DGRAPHKIND_DBT gk ON gtp.t_graphkind_id_ref = gk.t_id
        WHERE (gtp.t_id = #
           OR gtp.t_parentid = # 
           )
          AND gk.T_ISGROUP != 'X'
    ] (string(GraphID), string(GraphID));
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    if(rs.moveNext)
        result = rs.lastOper;
    end;
    return result;
    OnError(err)
        return result; // 0
END;

// возвращает список последних операций для объекта в разрезе видов графиков.
// список объектов LatestGraphOper 
// Т.е. последюю для комиссий, последнюю для ОД, последнюю для % и т.д.
// группы - не выбираем.
PRIVATE MACRO GetLatestGraphOper(ObjID, ObjN)
    var result = TArray();
    CaptureOutput;
    [
        SELECT distinct RSI_LOANSGRAPHKIND.GETLASTOPIDGRAPH(go.t_objectid, go.t_objectnumber, gk.t_num) as t_lastoper,
               gk.t_id as t_graphkindid,
               gtp.t_id 
        FROM DGRAPHTREEPARM_DBT gtp
        JOIN DGRAPHOBJECT_DBT go ON gtp.t_graphobject_id_ref = go.t_id
        JOIN DGRAPHKIND_DBT gk ON gtp.t_graphkind_id_ref = gk.t_id
        WHERE gk.T_ISGROUP != 'X'
          AND go.T_OBJECTID = # 
          AND go.T_OBJECTNUMBER = #  
    ](string(objID), string(ObjN));
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    while(rs.moveNext)
        var item = LatestGraphOper();
        item.GraphID = rs.ID;
        item.GraphKind = rs.GraphKindID;
        item.OperId = rs.lastOper;
        result[result.size] = item;
    end;
    return result;
    OnError(err)
        return result; // возвращаем пустой список
END;

// функция проверяет, является ли вид графика c GraphID группой.
// если нет, то возвращает его.
// если является, то возвращает Tarray видов графиков, входящих в группу
PRIVATE MACRO GetGraphTypeIDList(GraphID: integer)
    var result = TArray();
    CaptureOutput;
    [
        SELECT gtp.t_id,
               gtp.t_parentid,
               gk.t_isgroup
        FROM dgraphtreeparm_dbt gtp
        JOIN dgraphkind_dbt gk ON gtp.t_graphkind_id_ref = gk.t_id
        WHERE gtp.t_id=# 
           OR gtp.t_parentid=#
    ](string(GraphID), string(GraphID));
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    while (rs.moveNext)
        if (rs.isgroup != "X")
            result[result.size] = rs.id;
        end;
    end;
    return result;
END;

// возвращает, нужно ли включать в сводный график для КЛ
// графики по СО, открытых в рамках этой КЛ
PRIVATE MACRO IsDutyInCompleteGraph()
    VAR result = true;
    VAR regVal = true;
    VAR stat = 0;
    GetRegistryValue("RS-LOANS\\ГРАФИКИ\\СВОД_ГРАФИКОВ_ПО_ОБЪЕКТАМ", V_BOOL, regVal, stat);
    if (stat == 0)
        result = regVal;
    end;
    return result;
END;

PRIVATE MACRO GetUserObjectNumber(ObjID, ObjN)
    var result = "";
    CaptureOutput;
    if ((ObjID == LO_CREDIT) or (ObjID == LO_GUARANTEE))
        [ 
            select cr.t_uscreditnumber as t_number 
            from dcredit_c_dbt cr
            where cr.t_creditnumber = #
              and cr.t_crd_kind = #
        ] (string(ObjN), string(ObjID));
    elif ((ObjID == LO_DUTY) or (ObjID == LO_BANKGUARANTEE))
        [
            SELECT dt.t_usernumber as t_number 
            FROM dduty_crd_dbt dt
            WHERE dt.t_dutyid = #
        ](string(ObjN));
    end;
    var qStr = StopCaptureOutput;
    if (qStr != "")
        result = LnSelectValue(qStr, V_STRING);
    end;
    return result;
END;

// возвращает список идентификаторов, CО или БГ для КД и ДГ соответственно
PRIVATE MACRO GetDutyIDList(ObjID, ObjN)
    var result = TArray();
    if ((ObjID == LO_CREDIT) or (ObjID == LO_GUARANTEE))
        CaptureOutput;
        [
            SELECT dut.t_dutyid
            FROM dcredit_c_dbt crd
            JOIN dduty_crd_dbt dut ON dut.t_creditnumber_ref = crd.t_creditnumber
            WHERE crd.t_creditnumber = #
            AND crd.t_crd_kind = #
        ](string(ObjN), string(ObjID));
        var qStr = StopCaptureOutput;
        var rs = TRsbDataSet(qStr);
        while (rs.movenext)
            result[result.size] = rs.dutyid;
        end;
    end;
    return result;
END;

// построение дерева графиков по рекордсету
// верхний уровень дерева - виды графиков.
// второй уровень - периоды платежей по данному виду графика.
// используется и для сводного и для обычного графика
// поэтому набор полей в запросах должен быть одинаковый и для сводного и для обычного 
PRIVATE MACRO BuildGraphTree(rs, CaptionType)
    if (CaptionType == null) CaptionType = CAPTION_ONLYNAME; end;
    var result = TArray();
    var CurrentGraphID = 0; // для построения дерева
    var Graph = null; 
    var GraphItem = null;
    while (rs.moveNext)
        if (rs.GraphID != CurrentGraphID)
            Graph = CLnsGraph();
            Graph.ID            = rs.GraphID;
            Graph.GraphKindID   = rs.GraphKindID;
            Graph.GraphKindName = rs.GraphKindName;
            Graph.ObjID         = rs.ObjectID;
            Graph.ObjN          = rs.ObjectNumber;
            Graph.ObjName       = rs.ShortName;
            Graph.IsGrace       = (rs.IsGrace == "X");
            // далее - формирование заголовка графика
            if (CaptionType == CAPTION_WITHOBJECT)
                Graph.GraphKindName = Graph.GraphKindName + " " + Graph.ObjName;
            elif (CaptionType == CAPTION_FULL)
                Graph.ObjUsNumber   = GetUserObjectNumber(Graph.ObjID, Graph.ObjN); // получаем номер объекта только когда это действительно нужно.
                Graph.GraphKindName = Graph.GraphKindName + " " + Graph.ObjName + " " + Graph.ObjUsNumber;
            end;
            Graph.GraphItems    = TArray();
            result[result.size] = Graph;
            CurrentGraphID      = rs.GraphID;
        end;

        GraphItem = CLnsGraphItem();
        GraphItem.PayDate = rs.PlannedPayDate;
        GraphItem.ExpDate = rs.PlannedExpDate;
        GraphItem.SumPay  = rs.PlannedPaySum;
        // смотрим, если для графика не установлен параметр "отсрочка платежа"
        // но при этом даты различаются, значит думаем, что это ВЖЖЖЖЖ неспроста:
        // возможно,  периодичность этого графика зависит от другого графика, по которому ВЫСТАВЛЕН
        // признак "отсрочка платежа", и дату просрочки нужно отображать.
        if ((Graph.IsGrace == false) and 
            (GraphItem.PayDate != GraphItem.ExpDate))
            Graph.IsGrace = true;
        end;
        Graph.GraphItems[Graph.GraphItems.Size] = GraphItem;
    end;
    return result;
END;

// получение графика
MACRO lns_GetGraphList(GraphID)
    var result = TArray();
    var GraphTypeIDList: TArray = GetGraphTypeIDList(GraphID);
    var LatestOper = GetLatestOperForGraph(GraphID);
    if (GraphTypeIDList.Size > 0)
        CaptureOutput;
        [
            SELECT gtp.t_id as t_GraphID,
                   gk.t_id as t_GraphKindID,
                   gk.t_CapColumn as t_GraphKindName,
                   gkp.t_isGrace,
                   pp.t_PlannedPayDate,
                   pp.t_PlannedExpDate,
                   pp.t_PlannedPaySum,
                   pp.t_objectid_ref as t_objectnumber,
                   pp.t_objectTypeID_ref as t_objectid,
                   ' ' as t_shortname
            FROM dplanpay_dbt pp
            JOIN dgraphtreeparm_dbt gtp ON gtp.t_id = pp.t_graphparm_id
            JOIN dgraphkind_dbt gk ON gk.t_id = gtp.t_graphkind_id_ref
            JOIN dgraphkindparm_dbt gkp ON gtp.t_id = gkp.t_graphtreeparm_id_ref
                                       AND pp.t_credoperid_ref = gkp.t_credopid_ref
            WHERE pp.t_graphparm_id in (
                #
            )
            AND pp.t_credoperid_ref = #
            ORDER BY t_GraphID, t_PlannedPayDate
        ](string(join(GraphTypeIDList, ",")), string(LatestOper));
        var qStr = StopCaptureOutput;
        var rs = TRsbDataSet(qStr);
        result = BuildGraphTree(rs);
    end;
    return result;
END;

// Сводный график для объекта (одного объекта. без подчиненных)
PRIVATE MACRO GetCompleteGraphListForObject(ObjID, ObjN, CaptionType)
    var result = TArray();
    var LatestOperList = GetLatestGraphOper(ObjID, ObjN);
    if (LatestOperList.Size > 0)
        var GraphOpQStr = " AND (" + join(map(LatestOperList, @GetLatestOperExpr), " OR ") + ")";
        CaptureOutput;
        [
            SELECT gtp.t_id as t_GraphID,
                   gk.t_id as t_GraphKindID,
                   gk.t_CapColumn as t_GraphKindName,
                   lo.t_shortname,
                   gkp.t_isGrace,
                   go.t_objectnumber,
                   go.t_objectid,
                   pp.t_PlannedPayDate,
                   pp.t_PlannedExpDate,
                   pp.t_PlannedPaySum
            FROM dplanpay_dbt pp
            JOIN dgraphtreeparm_dbt gtp ON gtp.t_id = pp.t_graphparm_id
            JOIN dgraphkind_dbt gk      ON gk.t_id  = gtp.t_graphkind_id_ref
            JOIN dgraphobject_dbt go    ON go.t_id  = gtp.t_graphobject_id_ref
            JOIN dlobject_dbt lo        ON lo.t_objectid = pp.t_objecttypeid_ref
            JOIN dgraphkindparm_dbt gkp ON gtp.t_id = gkp.t_graphtreeparm_id_ref
                                       AND pp.t_credoperid_ref = gkp.t_credopid_ref
            JOIN dgraphkindparm_dbt gkpzero ON gtp.t_id = gkpzero.t_graphtreeparm_id_ref
                                           AND gkpzero.t_credopid_ref = 0
            LEFT JOIN dgraphtreeparm_dbt parentgtp ON gtp.t_parentid = parentgtp.t_id
            LEFT JOIN dgraphkindparm_dbt parentgkp ON parentgtp.t_id = parentgkp.t_graphtreeparm_id_ref
                                                  AND parentgkp.t_credopid_ref = 0
            WHERE (gkpzero.t_svod = 'X' OR parentgkp.t_svod = 'X')
            AND go.t_objectid=#
            AND go.t_objectnumber=#
            #
            ORDER BY t_GraphID, t_PlannedPayDate
        ] (string(ObjID), string(ObjN), GraphOpQStr);
        var qStr = StopCaptureOutput;
        var rs = TRsbDataSet(qStr);
        result = BuildGraphTree(rs, CaptionType);
    end;
    return result;
END;

// Сводный график для объекта и всех его подчиненных объектов
// в зависимости от настройки реестра, там могут быть сводные по СО, например.
// 
MACRO lns_GetCompleteGraphList(ObjID, ObjN)
    var HasDuty = IsDutyInCompleteGraph();
    var DutyIDList = Tarray();
    var CaptionType = CAPTION_ONLYNAME; // по умолчанию в заголовке показываем только название графика
    
    if (((ObjID == LO_CREDIT) or (ObjID == LO_GUARANTEE)) and HasDuty ) // этот блок только чтобы определить, что показывать в заголовках.
        DutyIDList = GetDutyIDList(ObjID, ObjN);
        if (DutyIDList != null)
            if (DutyIDList.Size == 1)
                CaptionType = CAPTION_WITHOBJECT;
            elif (DutyIDList.Size > 1)
                CaptionType = CAPTION_FULL;
            end;
        end;
    end;
    
    // получили список по самому объекту
    var result = GetCompleteGraphListForObject(ObjID, ObjN, 
                                              IfThenElse(CaptionType == CAPTION_FULL, // для объекта нам не нужен номер
                                                         CAPTION_WITHOBJECT,          // делаем только название объекта
                                                         CaptionType)); 
    if (((ObjID == LO_CREDIT) or (ObjID == LO_GUARANTEE)) and HasDuty ) 
        // если наш объект - КД или ДГ и выставлена настройка реестра, 
        // то получаем сводные для подчиненных объектов
        // пока только для СО
        
        var SubObjectID = 0;
        if (ObjID == LO_CREDIT)
            SubObjectID = LO_DUTY;
        elif (ObjID == LO_GUARANTEE)
            SubObjectID = LO_BANKGUARANTEE;
        end;

        for(var DutyID, DutyIDList)
            result = joinArrays(result, GetCompleteGraphListForObject(SubObjectID, DutyID, CaptionType));
        end;
    end;
    return result;
END;

// список графиков для объекта
// все параметры выбираем для шаблонных параметров CredOpID = 0
// меняем тоже только для шаблонных.
MACRO lns_GetGraphTree(ObjID, ObjN)
    var result = Tarray();
    var GraphTreeItem = null;
    var LatestOperList = GetLatestGraphOper(ObjID, ObjN);
    if (LatestOperList.Size > 0)
        CaptureOutput;
        [
            SELECT gtp.t_id,
                   gtp.t_parentid,
                   gtp.t_graphkind_id_ref,
                   gk.t_name,
                   gk.t_isgroup,
                   gkp.t_svod,
                   gkp.t_kvit,
                   gkp.t_annuitet,
                   gkp.t_default,
                   gkp.t_credopid_ref
            FROM DGRAPHTREEPARM_DBT gtp
            JOIN DGRAPHOBJECT_DBT go ON go.t_id = gtp.t_graphobject_id_ref            
            JOIN DGRAPHKIND_DBT gk ON gk.t_id = gtp.t_graphkind_id_ref
            JOIN DGRAPHKINDPARM_DBT gkp ON gtp.t_id = gkp.t_graphtreeparm_id_ref
            WHERE go.t_objectid = #
            AND go.t_objectnumber = #
            AND gkp.t_credopid_ref = 0
            ORDER BY gtp.t_parentid, gtp.t_id
        ](string(ObjID), string(ObjN));
        var qStr = StopCaptureOutput;
        var rs = TRsbDataSet(qStr);

        while (rs.moveNext)
            GraphTreeItem = CLnsGraphTreeItem();
            GraphTreeItem.GraphID        = rs.ID;
            GraphTreeItem.CredOperID     = rs.CredOpID_Ref;
            GraphTreeItem.GraphKindID    = rs.GraphKind_id_ref;
            GraphTreeItem.GraphKindName  = rs.Name;
            GraphTreeItem.ParentID       = rs.ParentID;
            //GraphTreeItem.ObjectTypeName = 
            GraphTreeItem.IsAnnuity      = rs.Annuitet == "X";
            GraphTreeItem.IsInComplete   = rs.Svod     == "X";
            GraphTreeItem.IsMatching     = rs.Kvit     == "X";
            GraphTreeItem.IsDefault      = rs.Default  == "X";
            GraphTreeItem.IsGroup        = rs.IsGroup  == "X";
            GraphTreeItem.Children       = TArray();
            
            if (rs.parentid == 0) // если родителя нет, то добавляем на верхний уровень
                result[result.size] = GraphTreeItem;
            else
                for (var item, result) // ищем родителя на верхнем уровне
                    if (item.GraphID == rs.parentid) // нашли родителя
                        item.Children[item.Children.size] = GraphTreeItem; // добавляем в список детей
                    end;
                end;
            end;
        end;
    end;
    return result;
END;

// Изменение настроек графика.
// Параметр типа CLnsGraphTreeItem без Children
MACRO lns_UpdateGraphParm(GraphParm)
    CaptureOutput;
    [
        UPDATE DGRAPHKINDPARM_DBT gkp
           SET gkp.t_svod = ?,
               gkp.t_kvit = ?,
               gkp.t_annuitet = ?,
               gkp.t_default = ?
         WHERE gkp.t_graphtreeparm_id_ref = ?
           AND gkp.t_credopid_ref = ?
    ];
    var qStr = StopCaptureOutput;
    var cmd = RsdCommand(qStr);
    cmd.NullConversion = True;
    cmd.addParam("p1", RSDBP_IN, IfThenElse(GraphParm.IsInComplete, "X",""));
    cmd.addParam("p2", RSDBP_IN, IfThenElse(GraphParm.IsMatching, "X",""));
    cmd.addParam("p3", RSDBP_IN, IfThenElse(GraphParm.IsAnnuity, "X",""));
    cmd.addParam("p4", RSDBP_IN, IfThenElse(GraphParm.IsDefault, "X",""));
    cmd.addParam("p5", RSDBP_IN, GraphParm.GraphID);
    cmd.addParam("p6", RSDBP_IN, 0); // сохраняем только в шаблонные настройки 
    cmd.Execute();
    return 0;
    
    OnError(err)
        return err.Code;
END;

MACRO lns_GetDefaultGraphID(ObjID, ObjN)
    var result = -2;
    CaptureOutput;
    [
        SELECT gtp.t_id
        FROM DGRAPHTREEPARM_DBT gtp
        JOIN DGRAPHOBJECT_DBT go ON go.t_id = gtp.t_graphobject_id_ref            
        JOIN DGRAPHKINDPARM_DBT gkp ON gtp.t_id = gkp.t_graphtreeparm_id_ref
        WHERE go.t_objectid = #
        AND go.t_objectnumber = #
        AND gkp.t_credopid_ref = 0
        AND gkp.t_default = 'X'
    ](string(ObjID), string(ObjN));
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        result = rs.ID;
    end;
    return result;
END;

// содержится ли элемент item в массиве arr
PRIVATE MACRO Contains(arr, item)
    for (var it, arr)
        if (it == item)
            return true;
        end;
    end;
    return false;
END;

// получить список графиков по объекту с учетом зависимостей.
PRIVATE MACRO GetGraphKindIDList(ObjID, ObjN)
    var result = Tarray();
    CaptureOutput;
    [
        SELECT gtpgroup.t_graphkind_id_ref as t_groupid, 
               gtp.t_graphkind_id_ref      as t_id, 
               CASE gkp.t_flagdatesubgraph 
                   WHEN 'X' THEN gkp.t_datesubgraphid
                   ELSE 0
               END as t_datesubgraph,
               
               CASE gkp.t_flagsumsubgraph
                   WHEN 'X' THEN gkp.t_sumsubgraphid
                   ELSE 0
               END as t_sumsubgraph
        FROM DGRAPHTREEPARM_DBT gtp
        JOIN DGRAPHOBJECT_DBT go    ON go.t_id = gtp.t_graphobject_id_ref
        JOIN DGRAPHKINDPARM_DBT gkp ON gkp.t_graphtreeparm_id_ref = gtp.t_id AND gkp.t_credopid_ref = 0
        JOIN DGRAPHKIND_DBT gk      ON gk.t_id = gtp.t_graphkind_id_ref
        LEFT JOIN DGRAPHTREEPARM_DBT gtpgroup ON gtpgroup.t_id = gtp.t_parentid
        WHERE go.t_objectid = ?
        AND go.t_objectnumber = ?
        AND gk.t_isgroup != 'X'
        ORDER BY t_datesubgraph, t_sumsubgraph, t_id
    ];
    var qStr = StopCaptureOutput();
    var cmd = RsdCommand(qStr);
    cmd.addParam("p1", RSDBP_IN, ObjID);
    cmd.addParam("p2", RSDBP_IN, ObjN);
    cmd.Execute;
    var rs = TRsbDataSet(cmd);
    while (rs.moveNext)
        var GraphID = IfThenElse(rs.GroupID != NULL, rs.GroupID, rs.ID);
        if (not Contains(result, GraphID))
            result[result.Size] = GraphID;
        end;
    end;
    return result;
END;

MACRO MakeGraphForObject(ObjID, ObjN, OpDate)
    if (OpDate == null) OpDate = {curdate}; end;
    var ErrorList = TArray();
    var GraphKindIDList = GetGraphKindIDList(ObjID, ObjN);
    var stat = 0;
    for (var GraphKindID, GraphKindIDList)
        //если хотим брать данные из временных таблиц, то добавляем ещё флаг 1 после GraphKindID
        stat = MakeOperation(ObjN, ObjID, CF_RECALCGPAY, 0, OpDate, null, GraphKindID); 
        if (stat == 0)
            ErrorList[ErrorList.Size] = GetMO_LoansError();
        end;
    end;
    return ErrorList;
END;