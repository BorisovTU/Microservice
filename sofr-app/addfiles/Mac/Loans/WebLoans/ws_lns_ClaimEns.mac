/*
$Name: ws_lns_ClaimEns.mac
$Module: WebLoans
$Description: Сервис для работы с обеспечениями по заявке
*/
IMPORT fmt_lib, ws_validation, RsbDataSet, RSD;
IMPORT ws_lns_CreditEns, ws_lns_CrdContract;
IMPORT DLCLAIMENSCONTR_DBT, DLCLAIMENSOBJ_DBT, DENSCONTR_DBT, DENSOBJ_DBT, DECN_EOB_DBT;

CLASS CLnsClaimEnsContract()
    VAR EnsContrID: integer = 0;    // Идентификатор обеспечения
    VAR TypeEnsID: integer = 0;     // Идентификатор типа обеспечения
    VAR TypeEnsName: string = "";   // Тип обеспечения
    VAR PartyID: integer = 0;       // Идентификатор поручителя/гаранта
    VAR PartyName: string = "";     // Поручитель/гарант
    VAR PartyType: integer = 0;     // Тип: физик/юрик
    VAR Sum: Money = $0;            // Сумма обеспечения
    VAR Currency: string = "";      // Валюта
    VAR Comment: string = "";       // Комментарий
    VAR SecurDecision: string = ""; // Заключение СБ
    VAR Children = null;            // Дочерние элементы
END;

CLASS CLnsClaimEnsObject()
    VAR EnsObjID: integer = 0;       // Идентификатор обеспечения
    VAR TypeEnsID: integer = 0;      // Идентификатор типа обеспечения
    VAR TypeEnsName: string = "";    // Тип обеспечения
    VAR EnsTypeID: integer = 0;      // Идентификатор Вида обеспечения
    VAR EnsTypeName: string = "";    // Вид обеспечения
    VAR PartyID: integer = 0;        // Идентификатор залогодателя
    VAR PartyName: string = "";      // Залогодатель
    VAR PartyType: integer = 0;      // Тип: физик/юрик
    VAR MarketSum: Money = $0;       // Рыночная стоимость
    VAR Discount: double = 0.0;      // Дисконт
    VAR MortgageSum: Money = $0;     // Залоговая стоимость
    VAR Currency: string = "";       // Валюта
    VAR Description: string = "";    // Описание
    VAR SecurDecision: string = "";  // Заключение СБ
    VAR Children = null;             // Дочерние элементы
END;


MACRO Lns_GetClaimEnsContractList(ClaimID)
    var qStr = "",
        rs = null,
        result = TArray();
    var p1, p2;
    var TypeEnsID = 0;

    qStr = "SELECT te.T_TYPEENSID, te.T_TYPEENSNAME, contr.T_ID, contr.T_SUM, contr.T_COMMENT, " +
           "       contr.T_SECURDECISION, prt.T_NAME, prt.T_PARTYID, prt.T_LEGALFORM, fi.T_CCY " +
           "FROM dtype_ens_dbt te, dlclaimenscontr_dbt contr, dparty_dbt prt, dfininstr_dbt fi " +
           "WHERE contr.T_TYPEENSID_REF = te.T_TYPEENSID AND " +
           "      contr.T_PARTYID_REF = prt.T_PARTYID AND " +
           "      contr.T_CURCODE = fi.T_FIID AND " +
           "      contr.T_CLAIMID_REF = " + ClaimID +
           " ORDER BY te.T_TYPEENSID";
    
    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
        if (TypeEnsID  != rs.TypeEnsID)
            p1 = CLnsClaimEnsContract();
            p1.TypeEnsID = rs.TypeEnsID;
            p1.TypeEnsName = rs.TypeEnsName;
            p1.PartyName = rs.TypeEnsName; // так надо для нормального простроения иерархического скроллинга
            p1.Sum = null;
	        p1.Children = TArray();
            result[result.size] = p1;
            TypeEnsID  = rs.TypeEnsID;
        end;
        p2 = CLnsClaimEnsContract();
        p2.EnsContrID = rs.ID;
        p2.TypeEnsID = rs.TypeEnsID;
        p2.TypeEnsName = rs.TypeEnsName;
        p2.PartyID = rs.PartyID;
        p2.PartyName = rs.Name;
        p2.PartyType = rs.LegalForm;
        p2.Sum = rs.Sum;
        p2.Currency = rs.ccy;
        p2.Comment = rs.Comment;
        p2.SecurDecision = rs.SecurDecision;

        p1.Children[p1.Children.size] = p2;
    end;
    return result;
END;

MACRO Lns_GetClaimEnsContr(ClaimEnsContrID)
    var ResponseBuilder = WebResponseBuilder();
    var result = null;

    var qStr = "select * from dlclaimenscontr_dbt where t_id = " + ClaimEnsContrID;
    var rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        result = GetObjectFromRecordset("DLCLAIMENSCONTR_DBT", rs);
    end;
    ResponseBuilder.SetUserObject(result);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

PRIVATE MACRO CheckClaimEnsContr(ClaimEnsContr)
    if (ClaimEnsContr.typeensid_ref == 0)
        RunError("Выберите тип обеспечения!");
    end;  
    if (ClaimEnsContr.partyid_ref == 0)
        RunError("Выберите поручителя/гарана!");
    end;
    if (ClaimEnsContr.sum == $0)
        RunError("Введите сумму обеспечения!");
    end;
    
    var rs = RsdRecordset("select * from dclaim_dbt where t_claimid = " + String(ClaimEnsContr.claimid_ref));
    rs.MoveNext;
    var claim = GetObjectFromRecordset("DCLAIM_DBT", rs);
    if (claim.loanerid_ref == ClaimEnsContr.partyid_ref)
        RunError("Контрагент по поручительству/гарантии не может совпадать с заемщиком!");
    end;
END;

MACRO Lns_InsertClaimEnsContr(ClaimEnsContr)
    var ResponseBuilder = WebResponseBuilder();
    record ClaimEnsContrBuff ("lclaimenscontr.dbt");
    var lclaimenscontr = TBFile ("lclaimenscontr.dbt", "rw", 0, "lclaimenscontr.dbt", "loans.def");
    CheckClaimEnsContr(ClaimEnsContr);
    CopyClassObjToRecord(ClaimEnsContr, ClaimEnsContrBuff);
    copy(lclaimenscontr, ClaimEnsContrBuff);
    lclaimenscontr.insert();
    
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO Lns_UpdateClaimEnsContr(ClaimEnsContr)
    var ResponseBuilder = WebResponseBuilder();
    record ClaimEnsContrBuff ("lclaimenscontr.dbt");
    var lclaimenscontr = TBFile ("lclaimenscontr.dbt", "rw", 0, "lclaimenscontr.dbt", "loans.def");
    CheckClaimEnsContr(ClaimEnsContr);
    CopyClassObjToRecord(ClaimEnsContr, ClaimEnsContrBuff);
    lclaimenscontr.rec.id = ClaimEnsContrBuff.id;
    var stat = lclaimenscontr.GetEQ();
    if (stat)
        copy(lclaimenscontr, ClaimEnsContrBuff);
        lclaimenscontr.update();
    else
        RunError("Обеспечение не найдено!");
    end;
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO Lns_DeleteClaimEnsContr(ClaimEnsContrID)
    var ResponseBuilder = WebResponseBuilder();
    var lclaimenscontr = TBFile ("lclaimenscontr.dbt", "rw", 0, "lclaimenscontr.dbt", "loans.def");

    lclaimenscontr.rec.id = ClaimEnsContrID;
    var stat = lclaimenscontr.GetEQ();
    if (stat)
        lclaimenscontr.delete();
    else
        RunError("Обеспечение не найдено!");
    end;

    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO Lns_GetClaimEnsObjectList(ClaimID)
    var qStr = "",
        rs = null,
        result = TArray();
    var p1, p2;
    var TypeEnsID = 0;

    qStr = "SELECT te.T_TYPEENSID, te.T_TYPEENSNAME, et.t_enstypeid, et.t_enstypename, " +
           "       prt.T_NAME, prt.T_PARTYID, prt.T_LEGALFORM, fi.T_CCY, " +
           "       obj.t_id, obj.T_MARKETSUM, obj.T_DISCOUNT, obj.T_MORTGAGESUM, " +
           "       obj.T_DESCRIPTION, obj.T_SECURDECISION " +
           "FROM dtype_ens_dbt te, " +
           "       denstype_dbt et, " + 
           "       dlclaimensobj_dbt obj, " +
           "       dparty_dbt prt, " +
           "       dfininstr_dbt fi " +
           "WHERE  te.T_TYPEENSID = obj.T_TYPEENSID_REF AND " +
           "       et.T_ENSTYPEID = obj.T_ENSTYPEID_REF AND " +
           "       obj.T_PARTYID_REF = prt.T_PARTYID AND " +
           "       obj.T_CURCODE = fi.T_FIID AND " +
           "       obj.T_CLAIMID_REF = " + ClaimID +
           " ORDER BY te.T_TYPEENSID";
    
    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
        if (TypeEnsID  != rs.TypeEnsID)
            p1 = CLnsClaimEnsObject();
            p1.TypeEnsID = rs.TypeEnsID;
            p1.TypeEnsName = rs.TypeEnsName;
            p1.EnsTypeName = rs.TypeEnsName; // так надо для нормального простроения иерархического скроллинга
            p1.MarketSum = null;
            p1.Discount = null;
            p1.MortgageSum = null;
	        p1.Children = TArray();
            result[result.size] = p1;
            TypeEnsID  = rs.TypeEnsID;
        end;
        p2 = CLnsClaimEnsObject();
        p2.EnsObjID = rs.ID;
        p2.TypeEnsID = rs.TypeEnsID;
        p2.TypeEnsName = rs.TypeEnsName;
        p2.EnsTypeID = rs.EnsTypeID;
        p2.EnsTypeName = rs.EnsTypeName;
        p2.PartyID = rs.PartyID;
        p2.PartyName = rs.Name;
        p2.PartyType = rs.LegalForm;
        p2.MarketSum = rs.MarketSum;
        p2.Discount = rs.Discount;
        p2.MortgageSum = rs.MortgageSum;
        p2.Currency = rs.Ccy;
        p2.Description = rs.Description;
        p2.SecurDecision = rs.SecurDecision;
        p2.Children = null;
        
        p1.Children[p1.Children.size] = p2;
    end;
    return result;
END;

MACRO Lns_GetClaimEnsObj(ClaimEnsObjID)
    var ResponseBuilder = WebResponseBuilder();
    var result = null;
    var qStr = "select * from dlclaimensobj_dbt where t_id = " + ClaimEnsObjID;
    var rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        result = GetObjectFromRecordset("DLCLAIMENSOBJ_DBT", rs);
    end;
    ResponseBuilder.SetUserObject(result);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

PRIVATE MACRO CheckClaimEnsObj(ClaimEnsObj)
    if(ClaimEnsObj.typeensid_ref == 0)   // Тип обеспечения
        RunError("Выберите тип обеспечения!");
    end;
    if(ClaimEnsObj.enstypeid_ref == 0)   // Вид обеспечения
        RunError("Выберите вид обеспечения!");
    end;
    if(ClaimEnsObj.partyid_ref == 0)     // Залогодатель
        RunError("Выберите залогодателя!");
    end;
    if(ClaimEnsObj.marketsum == $0)      // Рыночная стоимость
        RunError("Введите рыночную стоимость!");
    end;
    if(ClaimEnsObj.countunit == 0.0)     // Кол-во
        RunError("Введите корректное кол-во единиц!");
    end;
END;

MACRO Lns_InsertClaimEnsObj(ClaimEnsObj)
    var ResponseBuilder = WebResponseBuilder();
    record ClaimEnsObjBuff ("lclaimensobj.dbt");
    var lclaimensobj = TBFile ("lclaimensobj.dbt", "rw", 0, "lclaimensobj.dbt", "loans.def");

    ClaimEnsObj.typecountunit = 796; // УБРАТЬ ПОСЛЕ РЕАЛИЗАЦИИ ЛИСТАЛКИ ед. измерения - штуки(796) 

    CheckClaimEnsObj(ClaimEnsObj);
    CopyClassObjToRecord(ClaimEnsObj, ClaimEnsObjBuff);
    copy(lclaimensobj, ClaimEnsObjBuff);
    lclaimensobj.insert();
    
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO Lns_UpdateClaimEnsObj(ClaimEnsObj)
    var ResponseBuilder = WebResponseBuilder();
    record ClaimEnsObjBuff ("lclaimensobj.dbt");
    var lclaimensobj = TBFile ("lclaimensobj.dbt", "rw", 0, "lclaimensobj.dbt", "loans.def");

    ClaimEnsObj.typecountunit = 796; // УБРАТЬ ПОСЛЕ РЕАЛИЗАЦИИ ЛИСТАЛКИ ед. измерения - штуки(796)
    
    CheckClaimEnsObj(ClaimEnsObj);
    CopyClassObjToRecord(ClaimEnsObj, ClaimEnsObjBuff);
    lclaimensobj.rec.id = ClaimEnsObjBuff.id;
    var stat = lclaimensobj.GetEQ();
    if (stat)
        copy(lclaimensobj, ClaimEnsObjBuff);
        lclaimensobj.update();
    else
        RunError("Обеспечение не найдено!");
    end;
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO Lns_DeleteClaimEnsObj(ClaimEnsObjID)
    var ResponseBuilder = WebResponseBuilder();
    var lclaimensobj = TBFile ("lclaimensobj.dbt", "rw", 0, "lclaimensobj.dbt", "loans.def");

    lclaimensobj.rec.id = ClaimEnsObjID;
    var stat = lclaimensobj.GetEQ();
    if (stat)
        lclaimensobj.delete();
    else
        RunError("Обеспечение не найдено!");
    end;

    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

// далее - функции для открытия ДО и ОО из обеспечений по заявке
MACRO Lns_GetLClaimEnsContrList(ClaimID)
    var result = TArray();
    var qStr = "SELECT * FROM DLCLAIMENSCONTR_DBT WHERE T_CLAIMID_REF=" + ClaimID;
    var rs = RsdRecordset(qStr);
    while (rs.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DLCLAIMENSCONTR_DBT", rs);
    end;
    return result;
END;

MACRO Lns_GetLClaimEnsObjList(ClaimID)
    var result = TArray();
    var qStr = "SELECT * FROM DLCLAIMENSOBJ_DBT WHERE T_CLAIMID_REF=" + ClaimID +
               " ORDER BY t_partyid_ref, t_typeensid_ref, t_curcode";
    var rs = RsdRecordset(qStr);
    while (rs.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DLCLAIMENSOBJ_DBT", rs);
    end;
    return result;
END;

MACRO Lns_GetLClaimEnsObjSumm(ClaimID, PartyId, TypeEnsID, Curcode)
    var result = 0;
    var qStr = "SELECT SUM(CL.T_MORTGAGESUM) SUM_DO, CL.t_partyid_ref PartyID, CL.t_typeensid_ref TypeEnsID, CL.t_curcode Curcode" +
               " FROM DLCLAIMENSOBJ_DBT CL" + 
               " WHERE T_CLAIMID_REF = " + ClaimID + 
               " GROUP BY CL.t_partyid_ref, CL.t_typeensid_ref, CL.t_curcode" +
               " ORDER BY t_partyid_ref, t_typeensid_ref, t_curcode";
    
    var rs = TRsbDataSet(qStr);
    
    while (rs.MoveNext)        
        if ((rs.PartyID == PartyId) and (rs.TypeEnsID == TypeEnsID) and (rs.Curcode == Curcode))
            result = rs.SUM_DO;
        end;
    end;
       
    return result;
END;

// глобализмы - для передачи в транзакцию.
private var ClaimEnsToCrdGlobal_CrdContrID: integer;
private var ClaimEnsToCrdGlobal_ClaimID: integer;
private var ClaimEnsToCrdGlobal_OpDate: Date;
private var ClaimEnsToCrdGlobal_Err_Message: String;
private var ClaimEnsToCrdGlobal_Err_Err: String;

MACRO MakeEnsContrFromClaimTrn
    var CrdEnsContr = DENSCONTR_DBT();
    var CrdContrID = ClaimEnsToCrdGlobal_CrdContrID;
    var ClaimID = ClaimEnsToCrdGlobal_ClaimID;
    var OpDate = ClaimEnsToCrdGlobal_OpDate;

    var CrdContr = lns_GetCredit_c(CrdContrID);
    var result = null;
    
    if (CrdContr.creditnumber != 0) // КД нашли
        // открываем договоры типа "поручительство" и "гарантия"
        var ClaimEnsContrList = Lns_GetLClaimEnsContrList(ClaimID);
        var ClaimEnsContr = null;
        for (ClaimEnsContr, ClaimEnsContrList)
            CrdEnsContr.EnsSysType = ClaimEnsContr.typeensid_ref;
            CrdEnsContr.EnsContractFirstDate = CrdContr.regdate;
            CrdEnsContr.EnsContractLastDate = CrdContr.returndate;
            CrdEnsContr.EnsContractSum = ClaimEnsContr.sum;
            CrdEnsContr.EnsContractCur = ClaimEnsContr.curcode;
            CrdEnsContr.EnsContractStatus = 1;
            CrdEnsContr.ClaimID_Ref = ClaimID;
            CrdEnsContr.QualityID_Ref = 1; // ККС - 2
            CrdEnsContr.EnsCategory = 1; // обеспечение - основное.
            CrdEnsContr.FNCash = CrdContr.fncash;
            if (GetEnsSysTypeByTypeEns(ClaimEnsContr.typeensid_ref) == 8) // гарантия
                CrdEnsContr.Guarantor = ClaimEnsContr.partyid_ref;
                CrdEnsContr.EnsContractPersonID_Ref = CrdContr.clientid_ref; // заемщик и контрагент по ДО "гарантия" должны совпадать
                if (ВидСубъекта(ClaimEnsContr.partyid_ref, PTK_BANK) == true)
                    CrdEnsContr.IsGuarantee = "X";
                end;
            else
                CrdEnsContr.EnsContractPersonID_Ref = ClaimEnsContr.partyid_ref;
            end;
            CrdEnsContr.EnsContractNumber = lns_GetGenerationNumberObject(LO_ENSCONTR, CrdEnsContr, CrdContrID); //сгенерить
            result = Lns_InsertEnsContr(CrdContrID, Opdate, CrdEnsContr);
            if (result.ErrorMessages.Size > 0)
                RunError("Ошибка открытия договора обеспечения", result.ErrorMessages[0].Message);
            end;
        end;
        // открываем остальные ДО (в основном "залог")
        var ClaimEnsObj = null;
        var ClaimEnsObjList = Lns_GetLClaimEnsObjList(ClaimID);
        var OldPartyID = 0, OldTypeEns = 0, OldCurCode = 0;
        var EnsContrID = 0;
        for (ClaimEnsObj, ClaimEnsObjList)
            if ((OldPartyID != ClaimEnsObj.partyid_ref) or
                (OldTypeEns != ClaimEnsObj.typeensid_ref) or
                (OldCurCode != ClaimEnsObj.curcode))
                // открываем новый ДО
                CrdEnsContr = DENSCONTR_DBT();
                CrdEnsContr.EnsSysType = ClaimEnsObj.typeensid_ref;
                CrdEnsContr.EnsContractFirstDate = CrdContr.regdate;
                CrdEnsContr.EnsContractLastDate = CrdContr.returndate;
//                CrdEnsContr.EnsContractSum = ClaimEnsObj.mortgagesum;
                CrdEnsContr.EnsContractSum = Lns_GetLClaimEnsObjSumm(ClaimID, ClaimEnsObj.partyid_ref, ClaimEnsObj.typeensid_ref, ClaimEnsObj.curcode);
                CrdEnsContr.EnsContractCur = ClaimEnsObj.curcode;
                CrdEnsContr.EnsContractStatus = 1;
                CrdEnsContr.ClaimID_Ref = ClaimID;
                CrdEnsContr.QualityID_Ref = 1; // ККС - 2
                CrdEnsContr.EnsCategory = 1; // обеспечение - основное.
                CrdEnsContr.FNCash = CrdContr.fncash;
                CrdEnsContr.EnsContractPersonID_Ref = ClaimEnsObj.partyid_ref;
                CrdEnsContr.EnsContractNumber = lns_GetGenerationNumberObject(LO_ENSCONTR, CrdEnsContr, CrdContrID);
                result = Lns_InsertEnsContr(CrdContrID, OpDate, CrdEnsContr);
                if (result.ErrorMessages.Size > 0)
                    RunError("Ошибка открытия договора обеспечения", result.ErrorMessages[0].Message); // ошибка. Бросаем исключение
                else
                    EnsContrID = result.UserObject; // всё хорошо. Сохраняем ID только что созданного ДО.
                end;
                
            end;
            
            // добавляем новый ОО
            var EnsObj = DENSOBJ_DBT();
            EnsObj.EnsObjectID        = 0;                         // ID объекта обеспечения
            EnsObj.EnsTypeID_Ref      = ClaimEnsObj.enstypeid_ref; // Ссылка на ID вида обеспечения
            EnsObj.EnsObjectName      = ClaimEnsObj.description;   // Наименование объекта обеспечения
            EnsObj.EnsObjectSum       = ClaimEnsObj.marketsum;     // Стоимость объекта обеспечения/ В ходе исследования #155122 выяснили, что поле, вероятно-устаревшее
            EnsObj.EnsObjectCur       = ClaimEnsObj.curcode;       // ID валюты
            EnsObj.QualityID_Ref      = 1;                         // Категория качества
            EnsObj.CountUnit          = ClaimEnsObj.countunit;     // Количество единиц
            EnsObj.TypeCountUnit      = ClaimEnsObj.typecountunit; // Тип единицы

            var ECN_EOB = DECN_EOB_DBT();
            ECN_EOB.EnsContractID_Ref = EnsContrID;              // Ссылка на ДО
            ECN_EOB.EnsObjectID_Ref   = 0;                       // Ссылка на ОО
            ECN_EOB.RatingSum         = ClaimEnsObj.mortgagesum; // Оценочная стоимость
            ECN_EOB.RatingCurCode     = ClaimEnsObj.curcode;     // Валюта оценочной стоимости
            ECN_EOB.MarketSum         = ClaimEnsObj.marketsum;   // Рыночная стоимость
            ECN_EOB.MarketCurCode     = ClaimEnsObj.curcode;     // Валюта рыночной стоимости

            result = Lns_InsertEnsObj(EnsContrID, EnsObj, ECN_EOB);
            if (result.ErrorMessages.Size > 0)
                RunError("Ошибка добавления объекта обеспечения", result.ErrorMessages[0].Message); // ошибка. Бросаем исключение
            end;

            OldPartyID = ClaimEnsObj.partyid_ref;
            OldTypeEns = ClaimEnsObj.typeensid_ref; 
            OldCurCode = ClaimEnsObj.curcode;
        end;
    else
        RunError("", "Не найден кредитный договор!");
    end;
    return true;
    OnError(err)
        ClaimEnsToCrdGlobal_Err_Message = err.message;
        ClaimEnsToCrdGlobal_Err_Err = err.err;
        return false;
END;

MACRO Lns_MakeEnsContrFromClaim(CrdContrID, ClaimID, Opdate)
    ClaimEnsToCrdGlobal_ClaimID = ClaimID;
    ClaimEnsToCrdGlobal_CrdContrID = CrdContrID;
    ClaimEnsToCrdGlobal_OpDate= Opdate;
    ClaimEnsToCrdGlobal_Err_Message = "";
    ClaimEnsToCrdGlobal_Err_Err = "";
    
    if(not RtTrn("MakeEnsContrFromClaimTrn"))
        RunError(ClaimEnsToCrdGlobal_Err_Message, ClaimEnsToCrdGlobal_Err_Err);
    end;
END;