/*
$Name: ws_lns_OtherDebtor.mac
$Module: WebLoans
$Description: Сервис получения информации по созаемщикам
*/

import CTInter, "total.mac", PTInter, "pr_party_lib.mac", "ws_LoansOperation.mac", "ws_partyclasses.mac";

/*Класс панели "Созаемщики"*/
CLASS CLnsOtherDebtorItem()
    var debtorNumber: integer = 0;
    var partyObj : TWsPartyEx = TWsPartyEx();
    var partyID: integer = 0;
    var name: string = "";
    var conclusion = "";
END;

/*Получить ID клиента по номеру и типу объекта*/
MACRO getClientID(objID, objN)
    var clientID;
    
    if (objID == LO_CREDIT)
        clientID = LnSelectValue("SELECT T_CLIENTID_REF FROM DCREDIT_C_DBT WHERE T_CRD_KIND = " + objID + " AND T_CREDITNUMBER = " + objN, V_INTEGER);
    else
        clientID = LnSelectValue("SELECT T_LOANERID_REF FROM DCLAIM_DBT WHERE T_CLAIMID = " + objN, V_INTEGER);
    end;
    return clientID;
END;

/*Функция получения списка созаемщиков по договору/заявке*/
MACRO Lns_GetOtherDebtorList(objID, objN)
    var rs = null;
    var result = TArray();
    var item = null, partyID, showClient;
    
    partyID = getClientID(objID, objN);
    
    rs = TRsbDataSet("SELECT T1.T_OTHERDEBTORNUM, T2.T_NAME, T2.T_PARTYID, T3.T_CODE "              +
                     "            FROM DLOTHDEB_DBT T1, DPARTY_DBT T2, DOBJCODE_DBT T3 " +
                     "            WHERE     T1.T_PARTYID_REF = T2.T_PARTYID "            +
                     "                  AND T2.T_PARTYID = T3.T_OBJECTID "               +
                     "                  AND T3.T_OBJECTTYPE = " + OBJTYPE_PARTY          +
                     "                  AND T3.T_CODEKIND = " + PTCK_CONTR               +
                     "                  AND T1.T_OBJECTTYPEID_REF = " + objID            + 
                     "                  AND T1.T_ISDELETED != CHR(88) "                  +
                     "                  AND T1.T_OBJECTNUMBER_REF = " + objN);
    
    showClient = LnSelectValue("SELECT COUNT(*) FROM DOTHDEBSHOW_DBT WHERE T_SHOWDEBTOR != CHR(0) AND T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN, V_INTEGER);
                     
    while(rs.moveNext())
        if ((NOT showClient) AND (rs.PARTYID == partyID)) //Не выбираем клиента являющегося заемщиком
            continue;
        end;
        item = CLnsOtherDebtorItem();
        item.debtorNumber = rs.OTHERDEBTORNUM;
        item.partyID = rs.PARTYID;
        item.name = rs.NAME;
        item.partyObj.partyid   = rs.PARTYID;
        item.partyObj.partyname = rs.NAME;
        item.partyObj.code      = rs.CODE;       
        item.conclusion = "";
        result[result.size] = item;
    end;
    
    return result;
END;

/*Устанавливаем признак отображения заемщика в списке созаемщиков по объекту*/
MACRO Lns_SetShowDebtor(objID, objN)
    var qStr = "",
        stat = true,
        result = TWsMessage();
     result.MsgText = "";
    result.MsgID = 0; 
    
    var existObject = LnSelectValue("SELECT COUNT(*) FROM DOTHDEBSHOW_DBT WHERE T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN, V_INTEGER);

    if(existObject)
        qStr = "UPDATE DOTHDEBSHOW_DBT SET T_SHOWDEBTOR = CHR(88) WHERE T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN;
    else
        qStr = "INSERT INTO DOTHDEBSHOW_DBT (T_OBJECTTYPEID_REF, T_OBJECTNUMBER_REF, T_SHOWDEBTOR) " +
               "VALUES ( " +
               objID + ", " +
               objN + ", CHR(88))";
    end;
    
    stat = LnSqlExec(qStr, false);
    
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка";
    end;
    
    return result;
END;

/*Функция возвращает следующий свободный номер созаемщика в рамках договора\заявки*/
MACRO Lns_GetOtherDebtorNumber(objID, objN)
    var maxval: integer;
    
    maxval = LnSelectValue("SELECT MAX (T_OTHERDEBTORNUM) FROM DLOTHDEB_DBT WHERE T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN, V_INTEGER);
    
    return maxval + 1;
END;

/*Добавляет нового созаемщика*/
MACRO Lns_InsertOtherDebtor(objID, objN, otherDebtorItem)
    var qStr = "",
        stat = true,
        result = TWsMessage(),
        partyID,
        wasDeleted;
        
    partyID = getClientID(objID, objN);
    //Клиент был ранее удален из созаемщиков
    wasDeleted = LnSelectValue("SELECT COUNT (*) FROM DLOTHDEB_DBT WHERE T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN + " AND T_PARTYID_REF = " + otherDebtorItem.partyID + " AND T_ISDELETED = CHR (88)", V_INTEGER);
 
    result.MsgText = "";
    result.MsgID = 0;
       
    if ((otherDebtorItem.partyID == 0) OR (otherDebtorItem.partyID == -1))
        result.MsgID = 1;
        result.MsgText = "Необходимо выбрать созаемщика.";
    elif (otherDebtorItem.partyID == partyID)
        result.MsgID = 2;
        result.MsgText = "Субъект является заемщиком по рассматриваемому кредиту. Добавить его в список созаемщиков?";
    elif (wasDeleted) //Если клиент ранее был удален из созаемщиков по договору
        qStr = "UPDATE DLOTHDEB_DBT SET T_ISDELETED = CHR(0) WHERE T_OBJECTTYPEID_REF = " + objID +
               " AND T_OBJECTNUMBER_REF = " + objN + " AND T_PARTYID_REF = " + otherDebtorItem.partyID;
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления созаемщика";
        end;
    else
        qStr = "INSERT INTO DLOTHDEB_DBT (T_OBJECTTYPEID_REF, T_OBJECTNUMBER_REF, T_PARTYID_REF, T_OTHERDEBTORNUM, T_ISDELETED, T_DATE) " +
        "VALUES ( " + 
        objID + ", " +
        objN + ", " +
        otherDebtorItem.partyID + ", " +
        Lns_GetOtherDebtorNumber(ObjID, ObjN) + ", CHR(0), " +
        To_SQLData(date(0, 0, 0)) + ")";
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления созаемщика";
        end;
    end;
    return result;
END;

/*Изменяет существующего созаемщика*/
MACRO Lns_UpdateOtherDebtor(objID, objN, otherDebtorItem)
    var result = TWsMessage(),
        partyID,
        qStr = "",
        stat = true;
    
    result.MsgText = "";
    result.MsgID = 0;
    
    partyID = getClientID(objID, objN);
    
    //Клиент был ранее удален из созаемщиков
    var wasDeleted = LnSelectValue("SELECT COUNT (*) FROM DLOTHDEB_DBT WHERE T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN + " AND T_PARTYID_REF = " + otherDebtorItem.partyID + " AND T_ISDELETED = CHR (88)", V_INTEGER);
    
    if ((otherDebtorItem.partyID == 0) OR (otherDebtorItem.partyID == -1))
        result.MsgID = 1;
        result.MsgText = "Необходимо выбрать созаемщика.";
    else
        qStr = "UPDATE DLOTHDEB_DBT SET T_PARTYID_REF = " + otherDebtorItem.partyID +
               " WHERE T_OBJECTTYPEID_REF = " + objID +
               " AND T_OBJECTNUMBER_REF = " + objN +
               " AND T_OTHERDEBTORNUM = " + otherDebtorItem.debtorNumber;
        stat = LnSqlExec(qStr, false);
        
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка обновления созаемщика";
        end;
    end;
    
    return result;
END; 
   
/*Удаляет существующего созаемщика*/
MACRO Lns_DeleteOtherDebtor(objID, objN, otherDebtorItem)
    var result = TWsMessage(), partyID, stat = true;
    result.MsgText = "";
    result.MsgID = 0;
    
    partyID = getClientID(objID, objN);
    
    if(otherDebtorItem.partyID == partyID)
        stat = LnSqlExec("UPDATE DOTHDEBSHOW_DBT SET T_SHOWDEBTOR = CHR(0) WHERE " +
                         " T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN, false);
    else
        stat = LnSqlExec("UPDATE DLOTHDEB_DBT SET T_ISDELETED = CHR(88) WHERE " +
                         " T_OBJECTTYPEID_REF = " + objID + " AND T_OBJECTNUMBER_REF = " + objN + 
                         " AND T_PARTYID_REF = " + otherDebtorItem.partyID, false);
    end;
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления созаемщика";
    end;
    return result;
END;