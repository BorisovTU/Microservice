/*
$Name: LnsClaimRouteCore.mac
$Module: WebLoans
$Description: Функции работы с маршрутом BPMN
*/

import xr_wf_baseexecute, wf_common, XmlRpcInter, total, serviceaction, SbCrdInter, RSD, RsbDataSet, Globals;

PRIVATE CONST CONTEXT_ROLES = "roles";

PRIVATE CONST ROLE_SALER       = "saler";
PRIVATE CONST ROLE_SECURITY    = "security";
PRIVATE CONST ROLE_UNDERWRITER = "underwriter";
PRIVATE CONST ROLE_INSPECTOR   = "inspector";

MACRO LoansWfContext
    var jvm = getJvm;
    var builder = jvm.CreateJavaObject("ru.softlab.rsretail.workflow.util.ContextBuilder");
    var qStr = "SELECT t_postid_ref as t_postid FROM dlop_post_dbt WHERE t_oper = " + {oper};
    var rs = TRsbDataSet(qStr);
    while (rs.moveNext)
        if (rs.postid == TOP_INSP)
            builder.setMultipleString(CONTEXT_ROLES, ROLE_INSPECTOR);
        elif (rs.postid == TOP_SALER)
            builder.setMultipleString(CONTEXT_ROLES, ROLE_SALER);
        elif (rs.postid == TOP_SECURITY)
            builder.setMultipleString(CONTEXT_ROLES, ROLE_SECURITY);
        elif (rs.postid == TOP_UNDERWRITER)
            builder.setMultipleString(CONTEXT_ROLES, ROLE_UNDERWRITER);
        end;
    end; 
    return builder.getContext;
END;

// Загрузка параметров маршрута из заявки \\
MACRO LoadClaimRouteParm(ClaimID)
    var parm = null,
        xml = "",
        qStr = "",
        RSDCmd = null,
        retVal = NULL;
        
    qStr = "SELECT * FROM dclaim_dbt WHERE t_claimid = " + ClaimID;
    var rs = TRsbDataSet(qStr);
    if (rs.moveNext)
        xml = rs.processparm;
    end; 
    if ((xml != null) and (xml != ""))
        var stat = ConvertToRSL(Xml, parm);
        retVal = TProcessData();
        retval.PersistentID = parm.PersistentID;
        retval.IsFinished   = parm.IsFinished;
        retval.IsAborted    = parm.IsAborted;
        retval.UiSessionID  = parm.UiSessionID;
        retval.RetVal       = parm.RetVal;
        retval.CurrentForm  = parm.CurrentForm;
    end;
    return retVal;
END;

//  Получение имени класса по идентификатору маршрута \\
MACRO GetClassName(ProcessID: string)
    var ClassName = "",
        qStr = "";
    
    qStr = "select t_className from dwf_procdef_dbt where t_id = " + SQLString(ProcessID);
    ClassName = LnSelectValue(qStr, V_STRING, 0);
    if ((ClassName == "") or (ClassName == NULL))
        RunError("Неверный идентификатор маршрута: " + ProcessID);
    end;
    return ClassName;
END;

// Получение идентификатора текущего шага для маршрута заявки \\
MACRO GetCurrentStepID(PersistentID: string)
    var qStr = "",
        StepID = "";
    
    qStr = "select t_currentactivityid from dwf_process_dbt where t_id = " + SQLString(PersistentID);
    StepID = LnSelectValue(qStr, V_STRING, 0);

    if ((StepID == "") or (StepID == NULL))
        RunError("Неверный идентификатор процесса: " + PersistentID);
    end;

    return StepID;
END;

// получение параметров шага маршрута
MACRO GetStep(StepID: string, RouteName:string)
    var ClassName = GetClassName(RouteName);
    var w = createProcess(ClassName);
    var info = wf.getWorkflowProcessInfo(w);
    var Step = info.activityInfo(StepID);
    return Step;
END;

// проверяет на возможность выполнения шага текущим пользователем
MACRO CanExecuteStep(Step)
    var i = 0;
    var context = LoansWfContext;
    var roles = context.apply(CONTEXT_ROLES);
    var perf = Step.performers;
    while (i < perf.length())
        if (roles.contains(perf.apply(i)))
            return true;
        end;
        i = i + 1;
    end;
    return false;
END;

// получение имени шага маршрута
MACRO GetStepName(Step)
    var StepName = Step.name;
    return StepName;
END;

// получение названия маршрута
MACRO GetRouteName(ClaimID: integer)
    return LnSelectValue("select t_processname from dclaim_dbt where t_claimid = " + ClaimID, V_STRING, 0);
END;


CLASS StepInfo
    VAR StepID:     string = "";
    VAR StepName:   String = "";
    VAR CanExecute: Bool   = False;
END;
// Получение параметров текущего шага для заявки \\
/* 
Если вдруг эта функция вылетает с ошибкой,
первым делом нужно проверить файлик obj\LocalWorkflowRepository.properties
и выставить там правильный локальный путь к файлам xpdl 
 Обычно это 
   directory.xpdl = ../XML
*/
MACRO GetCurrentStepInfo(ClaimID: integer)
    var CurrentStepID = "",
        CurrentStep = StepInfo(),
        RouteName = GetRouteName(ClaimID),
        RouteParm = LoadClaimRouteParm(ClaimID);

    if (RouteParm != null)
        CurrentStepID = GetCurrentStepID(RouteParm.PersistentID);
        var Step = GetStep(CurrentStepID, RouteName);
        CurrentStep.StepID     = CurrentStepID;
        CurrentStep.StepName   = GetStepName(Step);
        CurrentStep.CanExecute = CanExecuteStep(Step);
    end;
    return CurrentStep;    
END;

// сохранение истории выполнения шага
MACRO lns_SaveStep(claimid, stepid, stepname, claimstage, claimstatus, uiparm, stepparm)
    var qStr = "insert into dlclaimhistory_dbt " +
               "(t_claimid, t_stepid, t_stepname, t_stepdate, t_user, t_claimstatus, t_claimstage, t_uiparm, t_stepparm) " + 
               "values (?, ?, ?, ?, ?, ?, ?, ?, ?)";
    var rs =  RSDCommand(qStr);
    rs.AddParam("p_claimid",     RSDBP_IN, claimid);
    rs.AddParam("p_stepid",      RSDBP_IN, stepid);
    rs.AddParam("p_stepname",    RSDBP_IN, stepname);
    rs.AddParam("p_stepdate",    RSDBP_IN, {curdate});
    rs.AddParam("p_user",        RSDBP_IN, {oper});
    rs.AddParam("p_claimstatus", RSDBP_IN, claimstatus);
    rs.AddParam("p_claimstage",  RSDBP_IN, claimstage);
    rs.AddParam("p_uiparm",      RSDBP_IN, uiparm);
    rs.AddParam("p_stepparm",    RSDBP_IN, stepparm);

    rs.execute;
    return true;
    OnError
        return false;
END;

// Сохранение параметров маршрута для заявки \\
MACRO SaveClaimRouteParm(ClaimID, parm)
    var XMLParm  = "";
    var qStr = "";
    var RSDCmd = null;
    var StepName = "";

    if (parm.isFinished or parm.isAborted)
        StepName = ""; // Оставим эту ветку условия. Вдруг клиенту захочется при отработанном маршруте написать "Всё закончилось хорошо"
    else
        var StepID = GetCurrentStepID(parm.PersistentID);
        var RouteName = GetRouteName(ClaimID);
        StepName = GetStepName(GetStep(StepID, RouteName));
    end;
    ConvertToXML(parm, NULL, @XMLParm);
    qStr = "update dclaim_dbt set t_processparm = ?, t_nextaction = ? WHERE t_claimid = ?";
    
    RSDcmd = RsdCommand(RslDefCon, qStr);
    RSDcmd.addParam("", RsdBp_In, XMLParm);
    RSDcmd.addParam("", RsdBp_In, StepName);
    RSDcmd.addParam("", RsdBp_In, ClaimID);
    RSDcmd.execute;
    
    return true;
    
    onError(err)
        LoansError(err.Message);
        return false;
END;



// Запуск маршрута для заявки \\
MACRO StartClaimRoute(claimid)
    var RouteName = "", parm=NULL;
    RouteName = LnSelectValue("SELECT t_processname FROM DCLAIM_DBT WHERE t_claimid = " + claimid, V_STRING, 0);
    if (RouteName == "")
        RunError("Не задан идентификатор маршрута.");
    end;
    parm = startProcess(RouteName, LoansWfContext, claimid);
    SaveClaimRouteParm(claimid, parm);
    return parm;
END;

// Выполнение следующего шага (нескольких шагов) маршрута для заявки \\ 
MACRO ResumeClaimRoute(claimid, parm)
    if (parm == null)
        parm = LoadClaimRouteParm(ClaimID); // пытаемся загрузить из заявки
        if (parm == null) // если всё таки не загрузили
            RunError("Невозможно загрузить параметры маршрута заявки!");
        else
            if (parm.isFinished)
                RunError("Маршрут завершен.");
            end;
            if (parm.isAborted)
                RunError("Маршрут прерван. Дальнейшее выполнение невозможно.");
            end;
            if ((parm.UiSessionId != NULL)and(parm.UiSessionId != "")) // если была открыта UI сессия, но не завершена
                return parm;                                           // то просто возвращаем параметры без resumeProcess
            end;
            parm = resumeProcess(parm, LoansWfContext);
        end;
    else
        //получаем параметры шага, которые нужны нам ДО выполнения шага
        var StepID = GetCurrentStepID(parm.PersistentID);
        var RouteName = GetRouteName(claimid);
        var StepName = GetStepName(GetStep(StepID, RouteName));
        var uiparm = "";
        var stepparm = "";
        var field = null;

        if (parm != NULL)
            if (parm.UiSessionId != NULL) // есть форма
                for (field, parm.CurrentForm.Fields)
                    if (field.Name == "XAMLUI")
                        uiparm = field.Value;    
                    end;
                    if (field.Name == "result")
                        stepparm = field.Value;
                    end;
                end;
            end;
        end;
        // Всё, что нужно получили. Выполняем следующий шаг маршрута.
        parm = resumeProcess(parm, LoansWfContext);
        
        // Выполнили. Получаем параметры заявки и сохраняем историю.
        var qStr = "SELECT t_claimstate, t_stageid FROM dclaim_dbt WHERE t_claimid=" + claimid;
        var rs = TRsbDataSet(qStr);
        var ClaimStage = 0;
        var ClaimStatus = 0;
        if (rs.moveNext)
            ClaimStage = rs.stageid;
            ClaimStatus = rs.claimstate;
        else
            RunError("Ошибка получения параметров заявки");
        end;

        if (lns_SaveStep(claimid, StepID, StepName, claimstage, claimstatus, uiparm, StepParm) == false)
            RunError("Ошибка сохранения истории маршрута");
        end;
    end;

    if (SaveClaimRouteParm(claimid, parm) == false)
        RunError("Ошибка сохранения текущего статуса маршрута");
    end;
    return parm;    
END;


