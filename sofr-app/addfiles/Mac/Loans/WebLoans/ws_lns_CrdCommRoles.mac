/*
$Name: ws_lns_CrdCommRoles.mac
$Module: WebLoans
$Description: Сервис для работы со справочником ролей участников КК
*/

import LnsClaimRouteCore;
IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", "DCRDCOMM_ROLES_DBT.mac";
/*
MACRO Lns_StartRoute(claimid)
    var parm = StartClaimRoute(claimid);
    return parm;
END;

MACRO Lns_RouteNextStep(claimid, parm)
    parm = ResumeClaimRoute(claimid, parm);
    return parm;
END;
*/
MACRO Lns_GetCrdCommRolesList(FilterConditionItems, HideUnused)
    VAR result = TArray();
    VAR query = "select * from dcrdcomm_roles_dbt ";
    if (HideUnused)
        query = query + "where t_isnotuse != 'X' or t_isnotuse is NULL"
    end;
        query = query + " order by t_roleid";
    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DCRDCOMM_ROLES_DBT", rs);
    END;

    return result;
END;


MACRO Lns_InsertCrdCommRole(Role)
    var qStr = "",
        stat = true,
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (Role.RoleName == "")
        result.MsgID = 1;
        result.MsgText = "Введите наименование роли."
    else
        qStr = "insert into dcrdcomm_roles_dbt (t_roleid, t_rolename, t_isvoter, t_isnotuse, t_isupdate) " +
        "values ( " +
        Role.RoleID + ", " +
        SQLString(Role.RoleName) + ", " +
        SQLString(Role.isVoter)  + ", " +
        SQLString(Role.isNotUse) + ", " +
        SQLString(Role.isUpdate) + ")";
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления роли.";
        end;
    end;
    return result;
END;

MACRO Lns_UpdateCrdCommRole(Role)
    var qStr = "",
        stat = true,
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (Role.RoleName == "")
        result.MsgID = 1;
        result.MsgText = "Введите наименование роли."
    else
        qStr = "update dcrdcomm_roles_dbt set " +
        "t_rolename=" + SQLString(Role.RoleName) + ", " +
        "t_isvoter =" + SQLString(Role.IsVoter) + ", " +
        "t_isnotuse=" + SQLString(Role.isNotUse) + ", " +
        "t_isupdate=" + SQLString(Role.isUpdate) + " " +
        "where t_roleid=" + Role.RoleID;
        stat = LnSqlExec(qStr, false);
        
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка обновления роли.";
        end;
    end;
    return result;
END;

MACRO Lns_DeleteCrdCommRole(RoleID)
    var stat = LnSqlExec("delete from dcrdcomm_roles_dbt where t_roleid = " + RoleID, false),
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления роли.";
    end;
    return result;
END;


MACRO Lns_GetRoleID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_roleid) FROM dcrdcomm_roles_dbt", V_INTEGER, 0);
    if (maxval < 1000)
        return 1000;
    else
        return maxval + 1;
    end;
END;

MACRO Lns_GetRoleByID(RoleID: integer)
    VAR query = "select * from dcrdcomm_roles_dbt where t_roleid = " + RoleID,
        RS = RsdRecordset(query),
        result = null;
        
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DCRDCOMM_ROLES_DBT", rs);
    end;
    return result;
END;
/*
MACRO Lns_GetClaimRouteByProcessID(ProcessID: string)
    VAR query = "select * from dlclaimroute_dbt where t_processid = " + SQLString(ProcessID),
        RS = RsdRecordset(query),
        result = null;
        
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DLCLAIMROUTE_DBT", rs);
    end;

    return result;
END;
*/


