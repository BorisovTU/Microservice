/*
$Name: ws_lns_ProductType.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Типы продуктов"
*/

IMPORT SbCrdInter, mfo_lib, fmt_lib, likepy, RSD, RsbDataSet;
IMPORT Total, DLPRODUCTTYPE_DBT, ws_validation, "ws_lns_CommonLib.mac";

CLASS ProductTypeEditPrm()
    VAR ProductType   = null; // Структура тип продукта DLPRODUCTTYPE_DBT
    VAR TypeCrdIdList = null; // Список идентификаторов ВК, для которых задан данный тип продукта
END;

PRIVATE MACRO GetFilterSQL(FilterConditionItems)
    return "1 = 1"; // тут добавить обработку фильтров
END;

PRIVATE MACRO GetProductTypeListSQL(FilterConditionItems, TypeDebtor, HideUnused)
    CaptureOutput;
    [
        SELECT pt.*, 
               ct.t_credittypeid 
        FROM DLPRODUCTTYPE_DBT pt, 
             DTYPE_CRD_DBT ct 
        WHERE ct.t_producttype(+) = pt.T_ID
    ];
    var qStr = StopCaptureOutput;
    if (TypeDebtor != null)
        qStr = qStr + " AND pt.t_typedebtor = " + TypeDebtor;
    end;
    if (HideUnused)
        qStr = qStr + " AND pt.t_isnotuse != 'X'"
    end;
    if (FilterConditionItems != null)
        qStr = qStr + " AND " + GetFilterSQL(FilterConditionItems);
    end; 
    return qStr + " ORDER BY pt.t_id";
END;

//========================================================================
// Список типов продуктов для листалки и справочника
//========================================================================
MACRO Lns_GetProductTypeList(FilterConditionItems, TypeDebtor, HideUnused)
    VAR ProductID = 0;
    VAR result = TArray();
    VAR item = null;
    VAR qStr = GetProductTypeListSQL(FilterConditionItems, TypeDebtor, HideUnused);
    VAR rs = TRsbDataSet(qStr);
    // одним запросом получаем и список типов продукта и список ВК, привязанных к ним.
    // а дальше разгребаем по тому же алгоритму, что и дерево продуктов.
    while (rs.MoveNext)
        if (rs.ID != ProductID)
            item = ProductTypeEditPrm();
            item.ProductType = DLPRODUCTTYPE_DBT();
            item.TypeCrdIdList = TArray();
            item.ProductType.ID                = rs.ID;
            item.ProductType.TypeName          = rs.TypeName;
            item.ProductType.TypeDebtor        = rs.TypeDebtor;
            item.ProductType.AutoSelectCrdType = rs.AutoSelectCrdType;
            item.ProductType.ProcType          = rs.ProcType;
            item.ProductType.ProcName          = rs.ProcName;
            item.ProductType.IsNotUse          = rs.IsNotUse;
            item.ProductType.IsUpdate          = rs.IsUpdate;
            result[result.size] = item;

            ProductID = rs.ID;
        end;
        if (rs.CreditTypeID != null)
            item.TypeCrdIdList[item.TypeCrdIdList.Size] = rs.CreditTypeID;
        end;
    end;
    return result;
END;

//========================================================================
// Получение типа продукта по идентификатору
//========================================================================
MACRO Lns_GetProductTypeByID(ProductTypeID)
    VAR ProductID = 0;
    VAR item = null;
    CaptureOutput;
    [
        SELECT pt.*, 
               ct.t_credittypeid 
        FROM DLPRODUCTTYPE_DBT pt, 
             DTYPE_CRD_DBT ct 
        WHERE ct.t_producttype(+) = pt.T_ID
             AND pt.T_ID = #
    ](String(ProductTypeID));
    var qStr = StopCaptureOutput;
    VAR rs = TRsbDataSet(qStr);
    while (rs.MoveNext)
        if (rs.ID != ProductID)
            item = ProductTypeEditPrm();
            item.ProductType = DLPRODUCTTYPE_DBT();
            item.TypeCrdIdList = TArray();
            item.ProductType.ID                = rs.ID;
            item.ProductType.TypeName          = rs.TypeName;
            item.ProductType.TypeDebtor        = rs.TypeDebtor;
            item.ProductType.AutoSelectCrdType = rs.AutoSelectCrdType;
            item.ProductType.ProcType          = rs.ProcType;
            item.ProductType.ProcName          = rs.ProcName;
            item.ProductType.IsNotUse          = rs.IsNotUse;
            item.ProductType.IsUpdate          = rs.IsUpdate;
            ProductID = rs.ID;
        end;
        if (rs.CreditTypeID != null)
            item.TypeCrdIdList[item.TypeCrdIdList.Size] = rs.CreditTypeID;
        end;
    end;
    return item;
END;

//========================================================================
// инициализация новой записи справочника типов продукта
//========================================================================
MACRO Lns_InitNewProductType()
    var result = WebResponseBuilder();
    var ProductTypeID = LnSelectValue("SELECT MAX(t_id) FROM dlproducttype_dbt", V_INTEGER, 0);
    if (ProductTypeID < 1000)
        ProductTypeID = 1000;
    else
        ProductTypeID = ProductTypeID + 1;
    end;
    var ProdTypePrm = ProductTypeEditPrm();
    ProdTypePrm.ProductType = DLPRODUCTTYPE_DBT();
    ProdTypePrm.ProductType.ID = ProductTypeID;

    result.SetUserObject(ProdTypePrm);
    return result.Result;
    OnError(err)
        result.AddErrorEx(err.Code, MessageSeverity.RuntimeError, err.Message);
        return result.Result;
END;

PRIVATE CONST TYPE_PROC  = 0;
PRIVATE CONST TYPE_MACRO = 1;

// проверка при вставке записи. Если что-то не то, делаем RunError
// Ошибка обработается в функции вставки записи.
// Чтобы ошибка отобразилась красиво, делаем не RunError("текст ошибки"),
// а RunError("", "Текст ошибки")
MACRO CheckInsertProductType(ProductTypePrm)
    ; // добавить проверку если нужно
    if (ProductTypePrm.ProductType.TypeName == "")
        RunError("", "Введите название типа.");    
    end;    
    /*if (ProductTypePrm.TypeCrdIDList.size == 0)    
        RunError("", "Введите вид кредита.");            
    end;*/
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_MACRO) and (ProductTypePrm.ProductType.ProcName == ""))
        RunError("", "Введите имя макроса подбора вида кредита.");                
    end;   
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_PROC) and (ProductTypePrm.ProductType.ProcName == ""))
        RunError("", "Введите имя ХП подбора вида кредита.");                
    end;           
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_PROC) and not ws_lns_ExistStoredProc(ProductTypePrm.ProductType.ProcName)) 
        RunError("", "Хранимая процедура не найдена в БД!");        
    end;    
END;

// проверка при обновлении записи. Если что-то не то, делаем RunError
// Ошибка обработается в функции обновления записи.
MACRO CheckUpdateProductType(ProductTypePrm)
    ; // добавить проверку если нужно
    if (ProductTypePrm.ProductType.TypeName == "")
        RunError("", "Введите название типа.");    
    end;    
    /*if (ProductTypePrm.TypeCrdIDList.size == 0)    
        RunError("", "Введите вид кредита.");            
    end;*/    
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_MACRO) and (ProductTypePrm.ProductType.ProcName == ""))
        RunError("", "Введите имя макроса подбора вида кредита.");                
    end;   
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_PROC) and (ProductTypePrm.ProductType.ProcName == ""))
        RunError("", "Введите имя ХП подбора вида кредита.");                
    end;       
    if ((ProductTypePrm.ProductType.AutoSelectCrdType == "X") and (ProductTypePrm.ProductType.ProcType == TYPE_PROC) and not ws_lns_ExistStoredProc(ProductTypePrm.ProductType.ProcName)) 
        RunError("", "Хранимая процедура не найдена в БД!");
    end;        
END;

//========================================================================
// отвязывает тип продукта от всех ВК
//========================================================================
MACRO UnSetProductType(ProductTypeID)
    var qStr = "UPDATE DTYPE_CRD_DBT SET t_producttype=0 WHERE t_producttype=" + ProductTypeID;
    var RSDcmd = RsdCommand(RslDefCon, qStr);
        RSDcmd.execute;
END;

//========================================================================
// привязывает тип продукта для выбранных ВК
//========================================================================
MACRO SetProductType(ProductTypeID, TypeCrdIDList)
    if ((TypeCrdIDList != null) and (TypeCrdIDList.Size > 0))
        CaptureOutput;
        [
            UPDATE DTYPE_CRD_DBT SET t_producttype = #
            WHERE (t_producttype=0 or t_producttype is NULL) 
            AND t_credittypeid in 
            (
                #
            )
        ](string(ProductTypeID), join(TypeCrdIDList, ", "));
        var qStr =  StopCaptureOutput;
        var RSDcmd = RsdCommand(RslDefCon, qStr);
        RSDcmd.execute;
    else
        UnSetProductType(ProductTypeID);
    end;
END;

//========================================================================
// Вставка записи
//========================================================================
// Глобальные переменные для передачи параметров в транзакцию.
PRIVATE VAR GlobalInsert_ProductTypePrm = null;
PRIVATE VAR GlobalInsert_ErrCode = 0;
PRIVATE VAR GlobalInsert_ErrMessage = "";

MACRO InsertProductTypeTrn
    var ProductTypePrm = GlobalInsert_ProductTypePrm;
    record ProductTypeBuff ("lproducttype.dbt");
    var lproducttype = TBFile ("lproducttype.dbt", "rw", 0, "lproducttype.dbt", "loans.def");
    CheckInsertProductType(ProductTypePrm);
    CopyClassObjToRecord(ProductTypePrm.ProductType, ProductTypeBuff);
    copy(lproducttype, ProductTypeBuff);
    lproducttype.insert(); // добавили тип продукта
    SetProductType(ProductTypePrm.ProductType.ID, ProductTypePrm.TypeCrdIdList); // назначили тип продукта выбранным ВК
    return true;
    OnError(err)
        GlobalInsert_ErrCode = err.Code; 
        GlobalInsert_ErrMessage = IfThenElse(err.Err != null, string(err.Err), err.Message);
        return false;  
END;

MACRO Lns_InsertProductType(ProductTypePrm)
    var result = WebResponseBuilder();
    GlobalInsert_ProductTypePrm = ProductTypePrm;
    GlobalInsert_ErrCode = 0;
    GlobalInsert_ErrMessage = "";
    
    if(not RtTrn("InsertProductTypeTrn"))
        result.AddErrorEx(GlobalInsert_ErrCode, MessageSeverity.RuntimeError, GlobalInsert_ErrMessage);
    end;
    
    return result.Result;    
END;

//========================================================================
// Изменение записи
//========================================================================
// Глобальные переменные для передачи параметров в транзакцию.
PRIVATE VAR GlobalUpdate_ProductTypePrm = null;
PRIVATE VAR GlobalUpdate_ErrCode = 0;
PRIVATE VAR GlobalUpdate_ErrMessage = "";

MACRO UpdateProductTypeTrn
    var ProductTypePrm = GlobalUpdate_ProductTypePrm;
    
    record ProductTypeBuff ("lproducttype.dbt");
    var lproducttype = TBFile ("lproducttype.dbt", "rw", 0, "lproducttype.dbt", "loans.def");
    CheckUpdateProductType(ProductTypePrm);
    CopyClassObjToRecord(ProductTypePrm.ProductType, ProductTypeBuff);
    lproducttype.rec.id = ProductTypeBuff.id;
    var stat = lproducttype.GetEQ();
    if (stat)
        copy(lproducttype, ProductTypeBuff);
        lproducttype.update();
        SetProductType(ProductTypePrm.ProductType.ID, ProductTypePrm.TypeCrdIdList);
    else
        GlobalUpdate_ErrCode = 1;
        GlobalUpdate_ErrMessage = "Тип продукта не найден";
        return false;
    end;

    return true;
    OnError(err)
        GlobalUpdate_ErrCode = err.Code;
        GlobalUpdate_ErrMessage = IfThenElse(err.Err != null, string(err.Err), err.Message);
        return false; 
END;

MACRO Lns_UpdateProductType(ProductTypePrm)
    var result = WebResponseBuilder();
    GlobalUpdate_ProductTypePrm = ProductTypePrm;
    GlobalUpdate_ErrCode = 0;
    GlobalUpdate_ErrMessage = "";
    
    if(not RtTrn("UpdateProductTypeTrn"))
        result.AddErrorEx(GlobalUpdate_ErrCode, MessageSeverity.RuntimeError, GlobalUpdate_ErrMessage);
    end;
    
    return result.Result;
END;

//========================================================================
// Удаление записи
//========================================================================
// Глобальные переменные для передачи параметров в транзакцию.
PRIVATE VAR GlobalDelete_ProductTypeID = null;
PRIVATE VAR GlobalDelete_ErrCode = 0;
PRIVATE VAR GlobalDelete_ErrMessage = "";

MACRO DeleteProductTypeTrn
    var ProductTypeID = GlobalDelete_ProductTypeID;
    
    var lproducttype = TBFile ("lproducttype.dbt", "rw", 0, "lproducttype.dbt", "loans.def");
    lproducttype.rec.id = ProductTypeID;
    var stat = lproducttype.GetEQ();
    if (stat)
        lproducttype.delete();
        UnSetProductType(ProductTypeID);
    else
        GlobalDelete_ErrCode = 1;
        GlobalDelete_ErrMessage = "Тип продукта не найден";
        return false;
    end;

    return true;
    OnError(err)
        GlobalDelete_ErrCode = err.Code; 
        GlobalDelete_ErrMessage = IfThenElse(err.Err != null, string(err.Err), err.Message);
        return false; 
END;

MACRO Lns_DeleteProductType(ProductTypeID)
    var result = WebResponseBuilder();
    GlobalDelete_ProductTypeID = ProductTypeID;
    GlobalDelete_ErrCode = 0;
    GlobalDelete_ErrMessage = "";
    
    if(not RtTrn("DeleteProductTypeTrn"))
        result.AddErrorEx(GlobalDelete_ErrCode, MessageSeverity.RuntimeError, GlobalDelete_ErrMessage);
    end;

    return result.Result;
END;