/*
$Name: ws_lns_ProcCalcSumEns.mac
$Module: WebLoans
$Description: Сервис для работы со справочником <Процедуры расчета суммы обеспечения>
*/

IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", DPROCCALCSUMENS_DBT;
IMPORT mfo_lib; // для CaptureOutput

import "ws_datafilter.mac";
import "ws_validation.mac";
import "cb_sql.mac";
import "oralib.mac";

CLASS CLnsProcCalcSunEnsItem()
    VAR ProcCalcSumEns  : DPROCCALCSUMENS_DBT = null;    // ID номер процедуры расчета суммы обеспечения в справочнике
    VAR SysTypeList     = null;
    VAR EnsTypeList     = null;
    VAR SysTypeName     : string  = "";   // Название системного типа обеспечения  
END;

CLASS CLnsProcCalcSunEnsItemList()
    VAR ProcCalcSumEnsID              : integer = 0;    // Номер кредитного комитета
    VAR ProcCalcSumEnsName            : string  = "";   // Название вида кредитного комитета
    VAR StoredProcedure               : string  = "";   // Хранимая процедура}
END;

/* ===================================
   Транзакции (Insert, Update, Delete)
   =================================== */
PRIVATE VAR Obj_Global;
PRIVATE VAR ID_Global;
PRIVATE VAR Error_Global;
PRIVATE VAR ErrMsg;

MACRO InsertProcCalcSumEnsTrn
    var Obj = Obj_Global;
    record ProcCalcSumEnsBuff ("proccalcsumens.dbt");
    var proccalcsumens_dbt = TBFile ("proccalcsumens.dbt", "rw", 0, "proccalcsumens.dbt", "loans.def");
    if (not ws_lns_ExistStoredProc(Obj.ProcCalcSumEns.StoredProcedure)) 
        ErrMsg = "Хранимая процедура не найдена в БД!";
        RunError("", ErrMsg);
    end;    
    
    CopyClassObjToRecord(Obj.ProcCalcSumEns, ProcCalcSumEnsBuff);
    copy(proccalcsumens_dbt, ProcCalcSumEnsBuff);
    proccalcsumens_dbt.insert();
    
    // 0  - Системный тип
    // 1  - Тип обеспечения
    
    var ins : RsbSQLInsert;
    if (Obj.SysTypeList != null)
        ins = RsbSQLInsert("INSERT INTO dproccalcsumensdepobj_dbt (t_parentobjn, t_childobjid, t_childobjn) VALUES (" + Obj.ProcCalcSumEns.ID + ", 0, ?)", 1, Obj.SysTypeList.Size );
        ins.AddParam( V_INTEGER, Obj.SysTypeList );
        ins.Insert();
    end;
    
    if (Obj.EnsTypeList != null)
        ins = RsbSQLInsert("INSERT INTO dproccalcsumensdepobj_dbt (t_parentobjn, t_childobjid, t_childobjn) VALUES (" + Obj.ProcCalcSumEns.ID + ", 1, ?)", 1, Obj.EnsTypeList.Size );
        ins.AddParam( V_INTEGER, Obj.EnsTypeList );
        ins.Insert();
    end;
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;
END;

MACRO UpdateProcCalcSumEnsTrn
    var Obj = Obj_Global;
    record ProcCalcSumEnsBuff ("proccalcsumens.dbt");
    var proccalcsumens_dbt = TBFile ("proccalcsumens.dbt", "rw", 0, "proccalcsumens.dbt", "loans.def");
    CopyClassObjToRecord(Obj.ProcCalcSumEns, ProcCalcSumEnsBuff);
    proccalcsumens_dbt.rec.id = ProcCalcSumEnsBuff.ID;
    if (proccalcsumens_dbt.GetEQ())
        if (not ws_lns_ExistStoredProc(Obj.ProcCalcSumEns.StoredProcedure)) 
            ErrMsg = "Хранимая процедура не найдена в БД!";
            RunError("", ErrMsg);
        end;    
        copy(proccalcsumens_dbt, ProcCalcSumEnsBuff);
        proccalcsumens_dbt.update();
    else
        RunError("", "Процедура расчета суммы обеспечения не найдена!");
    end;
    
    var strcmd;  
    strcmd = "delete from dproccalcsumensdepobj_dbt where t_parentobjn = " + Obj.ProcCalcSumEns.ID;
    execSQL(strcmd);    

    var ins : RsbSQLInsert;
    if (Obj.SysTypeList != null)
        ins = RsbSQLInsert("INSERT INTO dproccalcsumensdepobj_dbt (t_parentobjn, t_childobjid, t_childobjn) VALUES (" + Obj.ProcCalcSumEns.ID + ", 0, ?)", 1, Obj.SysTypeList.Size );
        ins.AddParam( V_INTEGER, Obj.SysTypeList );
        ins.Insert();
    end;
    
    if (Obj.EnsTypeList != null)
        ins = RsbSQLInsert("INSERT INTO dproccalcsumensdepobj_dbt (t_parentobjn, t_childobjid, t_childobjn) VALUES (" + Obj.ProcCalcSumEns.ID + ", 1, ?)", 1, Obj.EnsTypeList.Size );
        ins.AddParam( V_INTEGER, Obj.EnsTypeList );
        ins.Insert();
    end;
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;
END;

MACRO DeleteProcCalcSumEnsTrn
    var ID = ID_Global;
    var proccalcsumens_dbt = TBFile ("proccalcsumens.dbt", "rw", 0, "proccalcsumens.dbt", "loans.def");
    proccalcsumens_dbt.rec.id = ID;
    if (proccalcsumens_dbt.GetEQ())
        proccalcsumens_dbt.delete();
    else
        RunError("", "Процедура расчета суммы обеспечения не найдена!");
    end;

    var strcmd = "delete from dproccalcsumensdepobj_dbt where t_parentobjn = " + ID;
    execSQL(strcmd);
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;   
END;
/* ========================================*/

PRIVATE MACRO GetEnsTypeCondition(
  condition:Integer
 ,value
 ,type:Integer
):String
    var strSQLCond:String = "";       
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = " T_ID IN (SELECT PP.T_PARENTOBJN" +
                              " FROM dproccalcsumensdepobj_dbt PP" + 
                              " WHERE PP.T_CHILDOBJID = 0 AND PP.T_CHILDOBJN " ;
        
        var i:integer = 0;
        while (i < value.Size)
            if ((ValType(value[i].NumberAlg) == V_INTEGER) and (condition == FilterCondition_Equal))
                if (i > 0)
                    strSQLCond = strSQLCond + ", " + value[i].NumberAlg;                
                else    
                    strSQLCond = strSQLCond + " IN (" + value[i].NumberAlg;
                end;
                            
            end;                  
            i = i + 1;
        end;
        strSQLCond = strSQLCond + "))";
    else
        strSQLCond = " 1 = 1 ";
    end;

    return strSQLCond;
END;

MACRO GetWhere(FilterItems)
    var fp = FilterParser( FilterItems );
    fp.AddFieldCondition( 0, null,  "t_proccalcsumensname");
    fp.AddUserFieldCondition( 1, @GetEnsTypeCondition);
    fp.AddFieldCondition( 2, null,  "t_isnotuse");
    fp.AddFieldCondition( 3, null,  "t_isupdate");
    return fp.GetFullSQLCondition();
END;

MACRO GetList(ParentObjN, ChildObjID)
    VAR query = "select t_childobjn from dproccalcsumensdepobj_dbt where t_parentobjn = " + ParentObjN + " and t_childobjid = " + ChildObjID,
        RS = RsdRecordset(query),
        result = TArray;   
    
    while (RS.moveNext)
        result[result.Size] = RS.value(0);
    end;
    
    return result;
END;


MACRO Lns_GetProcCalcSumEnsList(FilterConditionItems)
    VAR result = TArray(),
        filter = "",
        qStr = "", str = "",
        item = null, RSNA = null,
        sz = 0;
    qStr = "SELECT t_id, t_proccalcsumensname, t_storedprocedure, t_isnotuse, t_isupdate FROM dproccalcsumens_dbt WHERE 1=1";
    
    filter = GetWhere(FilterConditionItems);
    
    if (filter != "")
        qStr = qStr + " AND " + filter;
    end;
    
    qStr = qStr + " order by t_id";
    
    VAR RS = TRsbDataSet(qStr, RSDVAL_CLIENT, RSDVAL_STATIC);

    WHILE (RS.MoveNext)
        item = CLnsProcCalcSunEnsItem();
        item.ProcCalcSumEns.id                 = RS.id;
        item.ProcCalcSumEns.proccalcsumensname = RS.proccalcsumensname;
        item.ProcCalcSumEns.storedprocedure    = RS.storedprocedure;
        item.ProcCalcSumEns.isnotuse           = RS.isnotuse;
        item.ProcCalcSumEns.isupdate           = RS.isupdate;
        
        item.SysTypeList                       = GetList(RS.id, 0);
        item.EnsTypeList                       = GetList(RS.id, 1);
        
        sz = item.SysTypeList.Size;
        
        if   (sz >  1) item.SysTypeName = "Несколько";
        elif (sz == 1) 
            str = "SELECT t_sznamealg FROM dnamealg_dbt WHERE t_itypealg = 994 AND t_inumberalg = " + item.SysTypeList[0];
            RSNA = RsdRecordset(str);
            if (RSNA.moveNext())
                item.SysTypeName = RSNA.value(0);
            end;
        end;
        
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_InsertProcCalcSumEns(obj)
    var ResponseBuilder = WebResponseBuilder();
    Obj_Global = obj;
    if(not RtTrn("InsertProcCalcSumEnsTrn"))
        RunError(Error_Global.Message, Error_Global.Err);
    end;
    return ResponseBuilder.Result;
    OnError(err)
        if (ErrMsg != "")
            ResponseBuilder.AddErrorEx(0, MessageSeverity.RuntimeError, ErrMsg);
        else
        ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_UpdateProcCalcSumEns(obj)
    var ResponseBuilder = WebResponseBuilder();
    Obj_Global = obj;
    if(not RtTrn("UpdateProcCalcSumEnsTrn"))
        RunError(Error_Global.Message, Error_Global.Err);
    end;
    return ResponseBuilder.Result;
    OnError(err)
        if (ErrMsg != "")
            ResponseBuilder.AddErrorEx(0, MessageSeverity.RuntimeError, ErrMsg);
        else
        ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_DeleteProcCalcSumEns(id)       
    var ResponseBuilder = WebResponseBuilder();
    ID_Global = id;
    if(not RtTrn("DeleteProcCalcSumEnsTrn"))
        RunError(Error_Global.Message, Error_Global.Err);    
    end;
    return ResponseBuilder.Result;
    OnError(err)
        ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return ResponseBuilder.Result;
END;

MACRO Lns_GetProcCalcSumEnsID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dproccalcsumens_dbt", V_INTEGER, 0);

    if (maxval < 100)
        maxval = 100;
    else
        maxval = maxval + 1;
    end;
    
    return maxval;
END;

/* ===========================================
   Листалка процедур расчета суммы обеспечения
   =========================================== */
MACRO Lns_GetProcCalcSumEnsItemList(typeensid_ref)
    VAR result = TArray(),
        qStr = "",
        item = null;
   
    qStr = " SELECT t.t_id, t.t_proccalcsumensname, t.t_storedprocedure " +
           " FROM dproccalcsumens_dbt t " +
           /*" INNER JOIN dproccalcsumensdepobj_dbt d ON d.T_PARENTOBJN = t.T_ID AND d.T_CHILDOBJID = 1 AND d.T_CHILDOBJN = " + typeensid_ref +*/
           " WHERE t.t_isnotuse = chr(0) " +
           " ORDER BY t.t_id ";
    
    VAR RS = TRsbDataSet(qStr, RSDVAL_CLIENT, RSDVAL_STATIC);

    WHILE (RS.MoveNext)
        item = CLnsProcCalcSunEnsItemList();
        item.ProcCalcSumEnsID      = RS.id;
        item.ProcCalcSumEnsName    = RS.proccalcsumensname;
        item.StoredProcedure       = RS.storedprocedure;
        
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_GetProcCalcSumEnsItemListByID(ProcCalcSumEnsID: integer)
    VAR query = "select t_id, t_proccalcsunensname, t_storedprocedure from dproccalcsunens_dbt where t_id = " + ProcCalcSumEnsID,
        rs = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC),
        result = CLnsProcCalcSunEnsItemList();
        
    if (rs.MoveNext)
        result.ProcCalcSunEnsID   = rs.id;
        result.ProcCalcSunEnsName = rs.proccalcsunensname;
        result.StoredProcedure    = rs.storedprocedure;
    end;

    return result;
END;

MACRO Lns_CalcSum(claimid_ref: integer, StoredProcedure: string)   
    VAR arr = makeArray( SQLParam( "p_claimid_ref", claimid_ref) );

    return execStoredFunc(StoredProcedure, V_MONEY, arr);
    
    OnError(err)
        return $0;
END;

MACRO Lns_ProcCalcSumEnsListCount(typeensid_ref)   
    VAR qStr= "", result = 0; 
    
    qStr = " SELECT COUNT(*) cnt " +
           " FROM dproccalcsumens_dbt t " +
           /*" INNER JOIN dproccalcsumensdepobj_dbt d ON d.T_PARENTOBJN = t.T_ID AND d.T_CHILDOBJID = 1 AND d.T_CHILDOBJN = " + typeensid_ref +*/
           " WHERE t.t_isnotuse = chr(0) " +
           " ORDER BY t.t_id ";
   
    VAR rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        result = int(rs.cnt);
    END;

    return result;
END;