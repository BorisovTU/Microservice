/*
$Name: ws_lns_ClaimStage.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Стадии рассмотрения завки"
*/
IMPORT RSD, BankInter;
IMPORT cb_sql, likepy;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", "DLCLAIMSTAGE_DBT.mac";

private var StageID; // необходима для удаления внутри транзакции 

CLASS ClaimStagePRM()
	// Структура таблицы стадий
	var ClaimStage: DLCLAIMSTAGE_DBT;
	// Признак того, что стадия (ID >999) была присвоена хотя бы для одной кредитной заявки в результате выполнения любого из шагов маршрута её рассмотрения
	var isUsed: bool;
	// Признак того, что к стадии привязан хотя бы один статус И в панели <Статусы заявок для стадии> для любого статуса установлен признак <Для новой заявки>
	var isDefault: bool;
END;

MACRO getIsDefault(ClaimStageID)
    var result: bool = false;
    var defCount = 0;
    var qStr, rs = null; 
    
    qStr = "SELECT COUNT(*)" +
    " FROM DLCLAIMSTAGESTATUS_DBT CLS" +
    " WHERE CLS.T_ISDEFAULT = 'X' AND CLS.T_CLAIMSTAGEID = " + ClaimStageID;
           
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        defCount = rs.value(0);
    end;                
   
    if (defCount != 0)
        result = true;
    end;
   
    return result;
END;

MACRO Lns_ExistClaimStageNum(num: integer)
    var stat = LnSelectValue("SELECT count(0) FROM dlclaimstage_dbt WHERE t_num = " + num, V_INTEGER, 0);
    if (stat > 0)
        return true;
    else
        return false;
    end;
END;

PRIVATE MACRO GetStageListQuery(FilterConditionItems)
    VAR query = "select * from dlclaimstage_dbt WHERE 1=1 ";
    VAR filterstr = "";
    if (FilterConditionItems != null)
        var fp = FilterParser( FilterConditionItems );
        fp.AddFieldCondition( 0, null, "t_name");
        fp.AddFieldCondition( 1, null, "t_isnotuse");
        fp.AddFieldCondition( 2, null, "t_isupdate");
        filterstr = fp.GetFullSQLCondition();
    end;
    if (filterstr != "")
        query = query + " AND " + filterstr;
    end;
    query = query + " order by t_num ";
    return query;
END;

MACRO Lns_GetDirectoryDataList(FilterConditionItems)
    VAR result = TArray();
    VAR query = GetStageListQuery(FilterConditionItems); 
    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        var item = ClaimStagePRM();
        item.ClaimStage  = GetObjectFromRecordset("DLCLAIMSTAGE_DBT", rs);
        item.isUsed      = false;
        item.isDefault   = getIsDefault(item.ClaimStage.ID);
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_GetDirectoryDataListCount(FilterConditionItems)
    VAR query = GetStageListQuery(FilterConditionItems);
    return SQL_GetNRecs(query);
END;

PRIVATE MACRO CheckUpdateClaimStage(ClaimStage)
    VAR ClaimStageNum = LnSelectValue("SELECT t_num FROM dlclaimstage_dbt WHERE t_id = " + ClaimStage.ClaimStage.ID, V_INTEGER, 0);
    if (ClaimStage.ClaimStage.Num < 1)
        RunError("Ошибка сохранения страдии", "Некорректный номер стадии");
    elif (ClaimStageNum != ClaimStage.ClaimStage.Num)
        if (Lns_ExistClaimStageNum(ClaimStage.ClaimStage.Num))
            RunError("Ошибка сохранения страдии", "В справочнике уже есть стадия с порядковым номером " + ClaimStage.ClaimStage.Num);
        end;
    elif (ClaimStage.ClaimStage.Name == "")
        RunError("Ошибка сохранения страдии", "Введите название стадии.");
    end;
END;

PRIVATE MACRO CheckDeleteClaimStage(ClaimStageID)
    var qStr = "SELECT 1 FROM dclaim_dbt WHERE t_stageid=? and rownum = 1";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_stageid", RSDBP_IN, ClaimStageID);
    cmd.execute;
    var rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        RunError("Ошибка удаления стадии", "Нельзя удалить стадию! Она была назначена заявке клиента при рассмотрении");
    end;

    qStr = "SELECT 1 FROM dlclaimhistory_dbt WHERE t_claimstage=? and rownum = 1";
    cmd = RsdCommand(qStr);
    cmd.AddParam("p_claimstage", RSDBP_IN, ClaimStageID);
    cmd.execute;
    rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        RunError("Ошибка удаления стадии", "Нельзя удалить стадию! Она была назначена заявке клиента при рассмотрении");
    end;
END;

MACRO Lns_InsertClaimStage(ClaimStage)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    CheckUpdateClaimStage(ClaimStage);
    var qStr = "insert into dlclaimstage_dbt (t_id, t_Num, t_name, t_isnotuse, t_isupdate) values (?,?,?,?,?)";
    var cmd = RsdCommand(qStr);
    cmd.NullConversion = True;
    cmd.AddParam("p_id",       RSDBP_IN, ClaimStage.ClaimStage.ID);
    cmd.AddParam("p_num",      RSDBP_IN, ClaimStage.ClaimStage.Num);
    cmd.AddParam("p_name",     RSDBP_IN, ClaimStage.ClaimStage.Name);
    cmd.AddParam("p_isnotuse", RSDBP_IN, ifThenElse(ClaimStage.ClaimStage.isNotUse, "X", ""));
    cmd.AddParam("p_isupdate", RSDBP_IN, ifThenElse(ClaimStage.ClaimStage.isUpdate, "X", ""));
    cmd.execute();
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO Lns_UpdateClaimStage(ClaimStage)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    CheckUpdateClaimStage(ClaimStage);
    var qStr = "update dlclaimstage_dbt set t_num=?, t_name=?, t_isnotuse=?, t_isupdate=? where t_id=?";
    var cmd = RsdCommand(qStr);
    cmd.NullConversion = True;
    cmd.AddParam("p_num",      RSDBP_IN, ClaimStage.ClaimStage.Num);
    cmd.AddParam("p_name",     RSDBP_IN, ClaimStage.ClaimStage.Name);
    cmd.AddParam("p_isnotuse", RSDBP_IN, ifThenElse(ClaimStage.ClaimStage.isNotUse, "X", ""));
    cmd.AddParam("p_isupdate", RSDBP_IN, ifThenElse(ClaimStage.ClaimStage.isUpdate, "X", ""));
    cmd.AddParam("p_id",       RSDBP_IN, ClaimStage.ClaimStage.ID);
    cmd.execute();
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

// транзакция удаления стадии. Параметр ID стадии передается через
// глобальную переменную StageID
private MACRO deleteClaimStageTrn
    var stat = true;
    stat = LnSqlExec("delete from dlclaimstage_dbt where t_id = " + StageID, false); // удаляем стадию
    stat = LnSqlExec("delete from dlclaimstagestatus_dbt where t_claimstageid = " + StageID, false); // удаляем привязанные к стадии статусы
END;

MACRO Lns_DeleteClaimStage(ClaimStageID)
    var result = TWsMessage();
    StageID = ClaimStageID; // StageID - глобальная переменная (в начале мароса)
    result.MsgText = "";
    result.MsgID = 0;
    CheckDeleteClaimStage(ClaimStageID);
    if(not ProcessTrn(NULL, "deleteClaimStageTrn"))
        RunError("Ошибка удаления стадии заявки", "Ошибка удаления стадии заявки");
    end;
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO Lns_InitNewClaimStage()
    var maxid: integer,
        maxnum: integer,
        NewStage = ClaimStagePRM();
        
    NewStage.ClaimStage = DLCLAIMSTAGE_DBT();
    maxid = LnSelectValue("SELECT max(t_id) FROM dlclaimstage_dbt", V_INTEGER, 0);
    if (maxid < 1000)
        maxid = 1000;
    else
        maxid = maxid + 1;
    end;
    maxnum = LnSelectValue("SELECT max(t_num) FROM dlclaimstage_dbt", V_INTEGER, 0);
    if (maxnum == 0)
        maxnum = 1;
    else
        maxnum = maxnum + 1;
    end;
    NewStage.ClaimStage.id = maxid;
    NewStage.ClaimStage.num = maxnum;
    NewStage.isUsed      = false;
    NewStage.isDefault   = false;
    
    return NewStage;
END;


CLASS CLnsClaimStageItem()
    VAR ID: integer = 0;
    VAR Name: string = "";
END;

MACRO Lns_GetClaimStageList()
    var result = TArray();
    var qStr = "SELECT * FROM dlclaimstage_dbt";
    var rs = TRsbDataSet(qStr);
    while (rs.MoveNext)
        var item = CLnsClaimStageItem();
        item.ID = rs.ID;
        item.Name = rs.Name;
        result[result.size] = item;
    end;
    return result;
END;

MACRO Lns_GetClaimStageByID(ClaimStageID)
    var result = CLnsClaimStageItem();
    var qStr = "SELECT * FROM dlclaimstage_dbt WHERE T_ID=" + ClaimStageID;
    var rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        result.ID = rs.ID;
        result.Name = rs.Name;
    end;
    return result;
END;