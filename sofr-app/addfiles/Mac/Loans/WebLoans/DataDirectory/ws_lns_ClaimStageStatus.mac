/*
$Name: ws_lns_ClaimStageStatus.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Статусы заявок для стадии"
*/
IMPORT RsbDataSet, "total.mac", "ws_LoansOperation.mac";

CLASS CLnsClaimStageStatusItem()
  VAR ClaimStatusID     = 0,
      ClaimStatusName   = "",
      IsDefault         = "",      // Новая заявка по умолчанию открывается в этой стадии-статусе.
      IsEdit            = "",      // Заявка в это стадии может редактироваться
      BeforePackageName = "";
END;

MACRO Lns_UnsetIsDefault()
    var qStr = "update dlclaimstagestatus_dbt set t_isdefault=chr(0)";
    var stat = LnSqlExec(qStr, false);
    return stat;
END;

MACRO GetStageStatusIsDefault(ClaimStageID, ClaimStatusID)
    var qStr = "",
        isdefault = "";
    qStr = "select t_isdefault from dlclaimstagestatus_dbt where " +
           " t_claimstageid = " + ClaimStageID + 
           " AND t_claimstatusid = " + ClaimStatusID;
    isdefault = LnSelectValue(qStr, V_STRING, 0);
    return isdefault;
END;
// Проверка, установлен ли для стадии или статуса признак "Не используется"
MACRO CheckIsUsed(ClaimStageID, ClaimStageStatus)
    var result = TWsMessage(),
        qStr = "",
        rs = null;

    result.MsgText = "";
    result.MsgID = 0;
    qStr = "SELECT status.t_isnotuse as t_statusnotuse, " +
           "       status.t_name as t_statusname, " +
           "       stage.t_isnotuse as t_stagenotuse, " + 
           "       stage.t_name as t_stagename " +
           "FROM dlclaimstatus_dbt status, dlclaimstage_dbt stage, dlclaimstagestatus_dbt stst " + 
           "WHERE status.t_id = stst.t_claimstatusid AND stage.t_id = stst.t_claimstageid " +
           " AND stst.t_claimstageid=" + ClaimStageID + 
           " AND stst.t_claimstatusid= " + ClaimStageStatus.ClaimStatusID;
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        if (rs.statusnotuse == "X")
            result.MsgID = 1;
            result.MsgText = "Нельзя использовать статус \"" + rs.statusname + "\" для новой заявки! \n" +
                             "Для статуса установлен признак \"Не используется\".";
        elif (rs.stagenotuse == "X")
            result.MsgID = 1;
            result.MsgText = "Нельзя использовать стадию \"" + rs.stagename + "\" для новой заявки! \n" +
                             "Для стадии установлен признак \"Не используется\".";
        end;
    end;
    return result; 
END;

MACRO CheckDefault(ClaimStageID, ClaimStatusID, ClaimStageStatus)
    var result = TWsMessage();
        
    result.MsgText = "";
    result.MsgID = 0;
    
    if ((GetStageStatusIsDefault(ClaimStageID, ClaimStatusID) == "X") and 
        (ClaimStageStatus.IsDefault == ""))

        result.MsgID = 1;
        result.MsgText = "Нельзя снять признак \"Для новой заявки\"! Установите признак на другом статусе, который будет присваиваться новой заявке.";

    end;
    return result;
END;

MACRO Lns_CheckClaimStageStatus(ClaimStageID, ClaimStatusID, ClaimStageStatus)
    var result = TWsMessage(),
        isdefault = "";

    result.MsgText = "";
    result.MsgID = 0;
    if (ClaimStageStatus.IsDefault == "X")                    // если установлен признак "по умолчанию", 
        result = CheckIsUsed(ClaimStageID, ClaimStageStatus); // то проверяем стадию и статус на признак "Не используется"
    else
        result = CheckDefault(ClaimStageID, ClaimStatusID, ClaimStageStatus); // нельзя так просто взять и снять признак "Для новой заявки"
    end;
    return result;
END;

MACRO Lns_GetDirectoryDataList(StageID, FilterConditionItems)
  VAR result = TArray();
  VAR query = "select t.t_ClaimStatusID, t.t_isedit, t.t_isdefault, tt.t_Name, t.t_BeforePackageName " + 
              "from dlclaimstagestatus_dbt t, dlclaimstatus_dbt tt " + 
              "where tt.t_ID = t.t_ClaimStatusID and t.t_ClaimStageID = " + String(StageID) + " ";
  VAR RS = TRsbDataSet(query);

    WHILE (RS.MoveNext)
        var item = CLnsClaimStageStatusItem();
        
        item.ClaimStatusID     = RS.ClaimStatusID;
        item.ClaimStatusName   = RS.Name;
        item.BeforePackageName = RS.BeforePackageName;
        item.IsDefault         = RS.IsDefault;
        item.IsEdit            = RS.IsEdit;

        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_InsertClaimStageStatus(ClaimStageID, ClaimStageStatus)
    var qStr = "",
        stat = true,
        result = TWsMessage();
    
    result.MsgText = "";
    result.MsgID = 0;
    
    result = CheckIsUsed(ClaimStageID, ClaimStageStatus);
    if (result.MsgID == 0)
        if ((ClaimStageStatus.BeforePackageName == "") or
            (ws_lns_ExistStoredProc(ClaimStageStatus.BeforePackageName)) )
            
            if (ClaimStageStatus.IsDefault == "X") // Если проставлен признак
                Lns_UnsetIsDefault();                   // То сначала снимаем его со всех
            end;
            
            qStr = "insert into dlclaimstagestatus_dbt (t_claimstageid, t_claimstatusid, t_isdefault, t_isedit, t_beforepackagename) " +
            "values ( " +
            ClaimStageID + ", " +
            ClaimStageStatus.ClaimStatusID + ", " +
            SQLString(ClaimStageStatus.IsDefault) + ", " +
            SQLString(ClaimStageStatus.IsEdit) + ", " +
            SQLString(ClaimStageStatus.BeforePackageName) + ")";
            stat = LnSqlExec(qStr, false);
            if (stat == false)
                result.MsgID = 1;
                result.MsgText = "Ошибка добавления статуса заявки";
            end;
            
        else
            result.MsgID = 1;
            result.MsgText = "Хранимая процедура не найдена в БД!";
        end;
    end;
    return result;
END;

MACRO Lns_UpdateClaimStageStatus(ClaimStageID, ClaimStatusID, ClaimStageStatus)
    var qStr = "",
        stat = true,
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    result = Lns_CheckClaimStageStatus(ClaimStageID, ClaimStatusID, ClaimStageStatus);
    if (result.MsgID == 0)

        if ((ClaimStageStatus.BeforePackageName == "") or 
            (ws_lns_ExistStoredProc(ClaimStageStatus.BeforePackageName)) )
            
            if (ClaimStageStatus.IsDefault == "X") // Если проставлен признак
                Lns_UnsetIsDefault();                   // То сначала снимаем его со всех
            end;

            qStr = "update dlclaimstagestatus_dbt set " +
            "t_claimstatusid=" + ClaimStageStatus.ClaimStatusID + ", "
            "t_beforepackagename=" + SQLString(ClaimStageStatus.BeforePackageName) + ", " +
            "t_isdefault=" + SQLString(ClaimStageStatus.IsDefault) + ", " +
            "t_isedit=" + SQLString(ClaimStageStatus.IsEdit) +   
            " where t_claimstageid=" + ClaimStageID + 
            " and t_claimstatusid=" + ClaimStatusID;
            stat = LnSqlExec(qStr, false);
            if (stat == false)
                result.MsgID = 1;
                result.MsgText = "Ошибка обновления статуса заявки";
            end;
        else
            result.MsgID = 1;
            result.MsgText = "Хранимая процедура не найдена в БД!";
        end;
    end;
    return result;
END;

MACRO Lns_DeleteClaimStageStatus(ClaimStageID, ClaimStatusID)
    var stat = true,
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (GetStageStatusIsDefault(ClaimStageID, ClaimStatusID) != "X")
        stat = LnSqlExec("delete from dlclaimstagestatus_dbt where t_claimstageid = " + ClaimStageID + 
                        " and t_claimstatusid = " +  ClaimStatusID, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка удаления статуса заявки";
        end;
    else
        result.MsgID = 1;
        result.MsgText = "Нельзя удалить статус с установленным признаком \"Для новой заявки\"!";
    end;
    return result;
END;