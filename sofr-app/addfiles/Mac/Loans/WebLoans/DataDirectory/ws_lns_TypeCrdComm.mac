/*
$Name: ws_lns_TypeCrdComm.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Типы кредитных комитетов"
*/
IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", DTYPECRDCOMM_DBT, ws_dprt, "ws_LoansOperation.mac", "ws_person.mac";
IMPORT mfo_lib; // для CaptureOutput

import "ws_datafilter.mac";
import "ws_validation.mac";
import "cb_sql.mac";
import "oralib.mac";

CLASS CLnsTypeCrdCommItem()
    VAR TypeCrdCommID              : integer = 0;    // Номер кредитного комитета
    VAR TypeCrdCommName            : string  = "";   // Название вида кредитного комитета
    VAR TypeCrdCommTypeDebtor      : integer = 0;    // Тип заемщиков
    VAR TypeCrdCommTypeDebtorName  : string  = "";   // Тип заемщиков
    VAR TypeCrdCommFNCash          : integer = 0;    // Филиал банка
    VAR TypeCrdCommFNCashNAme      : string  = "";   // Филиал банка
    VAR TypeCrdCommBranch          : integer = 0;    // Подразделение
    VAR TypeCrdCommBranchName      : string  = "";   // Подразделение
    VAR TypeCrdCommIsNotUsage      : string  = "";   // Признак того, что вид кредитного комитета не используется
    VAR TypeCrdCommIsUpdate        : string  = "";   // Признак обновления при переходе на следующую сборку
END;

CLASS CLnsTypeCrdCommEditPrm()
    VAR TypeCrdComm     = null; // Вид КК
    VAR UserGroupList   = null; // Список номеров пользователей\ групп пользователей
    VAR CrdTypesList    = null; // Список видов кредита
    VAR RouteList       = null; // Список маршрутов рассмотрения заявок
    
    VAR FNCashObject: TWsDepartment = null; // костыль для инициализации ядерной листалки
    VAR BranchObject: TWsDepartment = null; // костыль для инициализации ядерной листалки
END;

CLASS CLnsTypeCrdCommItemList()
    VAR TypeCrdCommID              : integer = 0;    // Номер кредитного комитета
    VAR TypeCrdCommName            : string  = "";   // Название вида кредитного комитета
END;

/* =================================================================
   Для ядерной листалки пользователей "UserListWidget" (DPERSON_DBT)
   ================================================================= */

MACRO UserGroupObjectToUserGroup_List(UserGroupObjectList)
    var result = TArray();
    var i = 0;
    
    while(i < UserGroupObjectList.Size)
        result.value(result.Size) = UserGroupObjectList[i].Number;
        
        i = i + 1;
    end;
    
    return result;
END;

MACRO UserGroupToUserGroupObject_List(UserGroupList)
    var result = TArray();
    var Person = null;
    var i = 0;
    
    while(i < UserGroupList.Size)
        Person = TWsPerson();
        Person.Number = UserGroupList[i];
        result.value(result.Size) = Person;
        
        i = i +1;
    end;
    
    return result;
END;

/* ================================================================= */

/* ===================================
   Транзакции (Insert, Update, Delete)
   =================================== */
PRIVATE VAR TypeCrdCommEditPrm_Global;
PRIVATE VAR TypeCrdCommID_Global;
PRIVATE VAR Error_Global;
PRIVATE VAR ErrMsg;

PRIVATE MACRO CheckTypeCrdComm(EditPrm)
    // проверка при сохранении
    // если ошибка - делаем RunError("", "Текст ошибки");
    
    if ((EditPrm.TypeCrdComm.isprocedure == "X") AND not(ws_lns_ExistStoredProc(EditPrm.TypeCrdComm.procedurename)))
        ErrMsg = "Хранимая процедура не найдена в БД!";
        RunError("", ErrMsg);
    end;
END;

MACRO InsertTypeCrdCommTrn
    var EditPrm = TypeCrdCommEditPrm_Global;
    
    CheckTypeCrdComm(EditPrm);
    
    record TypeCrdCommBuff ("typecrdcomm.dbt");
    var typecrdcomm_dbt = TBFile ("typecrdcomm.dbt", "rw", 0, "typecrdcomm.dbt", "loans.def");
    CopyClassObjToRecord(EditPrm.TypeCrdComm, TypeCrdCommBuff);
    copy(typecrdcomm_dbt, TypeCrdCommBuff);
    typecrdcomm_dbt.insert();
    
    // 7  - вид кредита
    // 50 - вид КК
    // 92 - пользователь (?)
    // 51 - маршрут рассмотрения заявок
    
    var ins : RsbSQLInsert;
    if (EditPrm.CrdTypesList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 7, ?)", 1, EditPrm.CrdTypesList.Size );
        ins.AddParam( V_INTEGER, EditPrm.CrdTypesList );
        ins.Insert();
    end;
    
    if (EditPrm.RouteList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 51, ?)", 1, EditPrm.RouteList.Size );
        ins.AddParam( V_INTEGER, EditPrm.RouteList );
        ins.Insert();
    end;
    
    if (EditPrm.UserGroupList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 92, ?)", 1, EditPrm.UserGroupList.Size );
        ins.AddParam( V_INTEGER, UserGroupObjectToUserGroup_List(EditPrm.UserGroupList) );
        ins.Insert();
    end;
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;
END;

MACRO UpdateTypeCrdCommTrn
    var EditPrm = TypeCrdCommEditPrm_Global;
    
    CheckTypeCrdComm(EditPrm);
    
    record TypeCrdCommBuff ("typecrdcomm.dbt");
    var typecrdcomm_dbt = TBFile ("typecrdcomm.dbt", "rw", 0, "typecrdcomm.dbt", "loans.def");
    CopyClassObjToRecord(EditPrm.TypeCrdComm, TypeCrdCommBuff);
    typecrdcomm_dbt.rec.id = TypeCrdCommBuff.id;
    if (typecrdcomm_dbt.GetEQ())
        copy(typecrdcomm_dbt, TypeCrdCommBuff);
        typecrdcomm_dbt.update();
    else
        RunError("", "Вид кредитного комитета не найден!");
    end;
    
    var strcmd;  
    strcmd = "delete from dcrdcommdepobj_dbt where t_parentobjn = " + EditPrm.TypeCrdComm.ID;
    execSQL(strcmd);    

    var ins : RsbSQLInsert;
    if (EditPrm.CrdTypesList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 7, ?)", 1, EditPrm.CrdTypesList.Size );
        ins.AddParam( V_INTEGER, EditPrm.CrdTypesList );
        ins.Insert();
    end;
    
    if (EditPrm.RouteList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 51, ?)", 1, EditPrm.RouteList.Size );
        ins.AddParam( V_INTEGER, EditPrm.RouteList );
        ins.Insert();
    end;
    
    if (EditPrm.UserGroupList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (50, " + EditPrm.TypeCrdComm.ID + ", 92, ?)", 1, EditPrm.UserGroupList.Size );
        ins.AddParam( V_INTEGER, UserGroupObjectToUserGroup_List(EditPrm.UserGroupList) );
        ins.Insert();
    end;
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;
END;

MACRO DeleteTypeCrdCommTrn
    var TypeCrdCommID = TypeCrdCommID_Global;
    var typecrdcomm_dbt = TBFile ("typecrdcomm.dbt", "rw", 0, "typecrdcomm.dbt", "loans.def");
    typecrdcomm_dbt.rec.id = TypeCrdCommID;
    if (typecrdcomm_dbt.GetEQ())
        typecrdcomm_dbt.delete();
    else
        RunError("", "Вид кредитного комитета не найден!");
    end;

    var strcmd = "delete from dcrdcommdepobj_dbt where t_parentobjn = " + TypeCrdCommID;
    execSQL(strcmd);
    
    return true;
    OnError(err)
        Error_Global = err;
        return false;   
END;
/* ========================================*/


MACRO GetWhere(FilterItems)
    var fp = FilterParser( FilterItems );
    fp.AddFieldCondition( 0, null, "t_id");
    fp.AddFieldCondition( 1, null, "t_typecrdcommname");
    fp.AddFieldCondition( 2, null, "t_typedebtor");
    return fp.GetFullSQLCondition();
END;

MACRO Lns_GetTypeCrdCommDataList(pageSize, pageIndex, FilterConditionItems)
    VAR result = TArray(),
        filter = "",
        qStr = "",
        item = null;
    
    CaptureOutput;    
    [
    SELECT t.t_id,
           t.t_typecrdcommname,
           t.t_fncash,
           NVL((SELECT p1.t_name FROM dparty_dbt p1, ddp_dep_dbt d1 WHERE d1.t_partyid = p1.t_partyid AND d1.t_code = t.t_fncash), CHR(0)) as t_fncashname,
           t.t_branch,
           NVL((SELECT p2.t_name FROM dparty_dbt p2, ddp_dep_dbt d2 WHERE d2.t_partyid = p2.t_partyid AND d2.t_code = t.t_branch), CHR(0)) as t_branchname,
           t.t_typedebtor,
           case t.t_typedebtor
               when 1 then 'Юридические лица'
               when 2 then 'Физические лица'
           end as t_typedebtorname,
           t.t_isnotusage,
           t.t_isupdate
    FROM dtypecrdcomm_dbt t       
    WHERE 1=1
    ];
    
    qStr = StopCaptureOutput;
    
    filter = GetWhere(FilterConditionItems);
    
    if (filter != "")
        qStr = qStr + " and " + filter;
    end;
    
    qStr = qStr + " order by t.t_id";
        
    qStr = SQL_WrapSelect(qStr, pageIndex, pageSize);
    
    VAR RS = TRsbDataSet(qStr, RSDVAL_CLIENT, RSDVAL_STATIC);

    WHILE (RS.MoveNext)
        item = CLnsTypeCrdCommItem();
        item.TypeCrdCommID             = RS.id;
        item.TypeCrdCommName           = RS.typecrdcommname;
        item.TypeCrdCommTypeDebtor     = RS.typedebtor;
        item.TypeCrdCommTypeDebtorName = RS.typedebtorname;
        item.TypeCrdCommFNCash         = RS.fncash;
        item.TypeCrdCommFNCashName     = RS.fncashname;
        item.TypeCrdCommBranch         = RS.branch;
        item.TypeCrdCommBranchName     = RS.branchname;
        item.TypeCrdCommIsNotUsage     = RS.isnotusage;
        item.TypeCrdCommIsUpdate       = RS.isupdate;
        
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_GetTypeCrdCommDataListCount(FilterConditionItems)
    VAR fp = null,
        filter = "",
        qStr = "";

    qStr = "select * from dtypecrdcomm_dbt where 1=1";

    filter = GetWhere(FilterConditionItems);
    
    if (filter != "")
        qStr = qStr + " and " + filter;
    end;
    
    return SQL_GetNRecs(qStr);
END;

MACRO Lns_InsertTypeCrdComm(TypeCrdCommEditPrm)
    var ResponseBuilder = WebResponseBuilder();
    TypeCrdCommEditPrm_Global = TypeCrdCommEditPrm;
    if(not RtTrn("InsertTypeCrdCommTrn"))
        RunError(Error_Global.Message, Error_Global.Err);
    end;
    return ResponseBuilder.Result;
    OnError(err)
        if (ErrMsg != "")
            ResponseBuilder.AddErrorEx(0, MessageSeverity.RuntimeError, ErrMsg);
        else
            ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_UpdateTypeCrdComm(TypeCrdCommEditPrm)
    var ResponseBuilder = WebResponseBuilder();
    TypeCrdCommEditPrm_Global = TypeCrdCommEditPrm;
    if(not RtTrn("UpdateTypeCrdCommTrn"))
        RunError(Error_Global.Message, Error_Global.Err);
    end;
    return ResponseBuilder.Result;
    OnError(err)
        if (ErrMsg != "")
            ResponseBuilder.AddErrorEx(0, MessageSeverity.RuntimeError, ErrMsg);
        else
            ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        end;
        return ResponseBuilder.Result;
END;

MACRO Lns_DeleteTypeCrdComm(TypeCrdCommID)       
    var ResponseBuilder = WebResponseBuilder();
    TypeCrdCommID_Global = TypeCrdCommID;
    if(not RtTrn("DeleteTypeCrdCommTrn"))
        RunError(Error_Global.Message, Error_Global.Err);    
    end;

    return ResponseBuilder.Result;
    OnError(err)
        ResponseBuilder.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return ResponseBuilder.Result;
END;

MACRO Lns_GetTypeCrdCommID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dtypecrdcomm_dbt", V_INTEGER, 0);

    if (maxval < 1000)
        maxval = 1000;
    else
        maxval = maxval + 1;
    end;
    
    return maxval;
END;

MACRO GetList(ParentObjID, ParentObjN, ChildObjID)
    VAR query = "select t_childobjn from dcrdcommdepobj_dbt where t_parentobjid = " + ParentObjID + " and t_parentobjn = " + ParentObjN + " and t_childobjid = " + ChildObjID,
        RS = RsdRecordset(query),
        result = TArray;   
    
    while (RS.moveNext)
        result[result.Size] = RS.value(0);
    end;
    
    return result;
END;

MACRO Lns_GetTypeCrdCommByID(TypeCrdCommID: integer)
    VAR query = "select * from dtypecrdcomm_dbt where t_id = " + TypeCrdCommID,
        RS = RsdRecordset(query),
        result = CLnsTypeCrdCommEditPrm();
        
    if (RS.MoveNext)
        result.TypeCrdComm = GetObjectFromRecordset("DTYPECRDCOMM_DBT", rs);
        result.CrdTypesList = GetList(50, TypeCrdCommID, 7);
        result.UserGroupList = UserGroupToUserGroupObject_List(GetList(50, TypeCrdCommID, 92));
        result.RouteList = GetList(50, TypeCrdCommID, 51);
        
        result.FNCashObject = GetTWsDepartmentByCode(result.TypeCrdComm.FNCash);
        result.BranchObject = GetTWsDepartmentByCode(result.TypeCrdComm.Branch);
    end;

    return result;
END;

/* ==================================
   Листалка видов кредитных комитетов
   ================================== */
MACRO Lns_GetTypeCrdCommList()
    VAR result = TArray(),
        qStr = "",
        item = null;
    
    CaptureOutput;    
    [
    SELECT t.t_id,
           t.t_typecrdcommname      
    FROM dtypecrdcomm_dbt t       
    WHERE t_isnotusage = chr(0)
    ORDER BY t_id
    ];
    
    qStr = StopCaptureOutput;
    
    VAR RS = TRsbDataSet(qStr, RSDVAL_CLIENT, RSDVAL_STATIC);

    WHILE (RS.MoveNext)
        item = CLnsTypeCrdCommItemList();
        item.TypeCrdCommID      = RS.id;
        item.TypeCrdCommName    = RS.typecrdcommname;
        
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_GetTypeCrdCommItemListByID(TypeCrdCommID: integer)
    VAR query = "select t_id, t_typecrdcommname from dtypecrdcomm_dbt where t_id = " + TypeCrdCommID,
        rs = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC),
        result = CLnsTypeCrdCommItemList();
        
    if (rs.MoveNext)
        result.TypeCrdCommID   = rs.id;
        result.TypeCrdCommName = rs.typecrdcommname;
    end;

    return result;
END;
/* ================================== */