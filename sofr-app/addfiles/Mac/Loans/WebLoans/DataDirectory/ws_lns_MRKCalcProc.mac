/*
$Name: ws_lns_MRKCalcProc.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Процедуры расчета МРК"
*/
IMPORT RSD, globals, RsbDataSet;
IMPORT ws_datafilter;
IMPORT fmt_lib, total, ws_LoansOperation, DLMRKCALCPROC_DBT;
IMPORT ws_validation;
IMPORT ws_lns_CommonLib;


// сервисы для справочника
MACRO Lns_GetMRKCalcProcList(FilterConditionItems)
    VAR result = TArray(),
        fp = null,
        filterstr = "";
   
    if (FilterConditionItems != null)
        fp = FilterParser( FilterConditionItems );
        fp.AddFieldCondition( 0, null, "t_procname");
        fp.AddFieldCondition( 1, null, "t_typedebtor");
        fp.AddFieldCondition( 2, null, "t_isnotuse");
        fp.AddFieldCondition( 3, null, "t_isupdate");
        filterstr = fp.GetFullSQLCondition();
    end;
    
    VAR query = "select * from dlmrkcalcproc_dbt where 1=1 ";
    
    if (filterstr != "")
        query = query + " and " + filterstr;
    end;
        query = query + " order by t_id";
    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DLMRKCALCPROC_DBT", rs);
    END;

    return result;
END;

MACRO CheckMRKCalcProc(MRKCalcProc)
	var stat;
	stat = ws_lns_ExistStoredProc(MRKCalcProc.StoredFunc); 
	if (stat == false)
		RunError("Хранимая процедура не найдена в БД!");
	end;	
END;


MACRO Lns_InsertMRKCalcProc(MRKCalcProc)
    var result = TWsMessage();
    record MRKCalcProcBuff ("lmrkcalcproc.dbt");
    var lmrkcalcproc = TBFile ("lmrkcalcproc.dbt", "rw", 0, "lmrkcalcproc.dbt", "loans.def");
    result.MsgText = "";
    result.MsgID = 0;
    CheckMRKCalcProc(MRKCalcProc);
    CopyClassObjToRecord(MRKCalcProc, MRKCalcProcBuff);
    copy(lmrkcalcproc, MRKCalcProcBuff);
    lmrkcalcproc.insert();
    
    return result;
    
    OnError(err)
        var errStr;
		errStr = err.Message;
        result.MsgText = StrSubst(errStr, "Error: Ошибка выполнения", "");
        result.MsgID = err.Code;
        return result;
END;

MACRO Lns_UpdateMRKCalcProc(MRKCalcProc)
    var result = TWsMessage();
    record MRKCalcProcBuff ("lmrkcalcproc.dbt");
    var lmrkcalcproc = TBFile ("lmrkcalcproc.dbt", "rw", 0, "lmrkcalcproc.dbt", "loans.def");
    result.MsgText = "";
    result.MsgID = 0;
    CheckMRKCalcProc(MRKCalcProc);
    CopyClassObjToRecord(MRKCalcProc, MRKCalcProcBuff);
    lmrkcalcproc.rec.id = MRKCalcProcBuff.id;
    var stat = lmrkcalcproc.GetEQ();
    if (stat)
        copy(lmrkcalcproc, MRKCalcProcBuff);
        lmrkcalcproc.update();
    else
        result.MsgText = "Процедура не найдена!";
        result.MsgID = 1;
    end;
    return result;
    
    OnError(err)
        var errStr;
		errStr = err.Message;
        result.MsgText = StrSubst(errStr, "Error: Ошибка выполнения", "");
        result.MsgID = err.Code;
        return result;
END;

MACRO Lns_DeleteMRKCalcProc(MRKCalcProcID)
    var result = TWsMessage();
    var lmrkcalcproc = TBFile ("lmrkcalcproc.dbt", "rw", 0, "lmrkcalcproc.dbt", "loans.def");
    result.MsgText = "";
    result.MsgID = 0;
    lmrkcalcproc.rec.id = MRKCalcProcID;
    var stat = lmrkcalcproc.GetEQ();
    if (stat)
        lmrkcalcproc.delete();
    else
        result.MsgText = "Процедура не найдена!";
        result.MsgID = 1;
    end;

    return result;
    
    OnError(err)
        result.MsgText = err.Message;
        result.MsgID = err.Code;
        return result;
END;

MACRO Lns_GetMRKCalcProcID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dlmrkcalcproc_dbt", V_INTEGER, 0);
    if (maxval < 100)
        return 100;
    else
        return maxval + 1;
    end;
END;

MACRO Lns_InitNewMRKCalcProc()
    var result = DLMRKCALCPROC_DBT();
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dlmrkcalcproc_dbt", V_INTEGER, 0);
    if (maxval < 100)
        result.id = 100;
    else
        result.id = maxval + 1;
    end;
    result.TypeDebtor = 2; // физлицо по умолчанию(пусь будет так).
    return result;
END;

// сервисы для листалки
MACRO Lns_MRKCalcProcLister(TypeDebtor)
    VAR result = TArray();
    VAR qStr = "select * from dlmrkcalcproc_dbt where t_isnotuse != 'X' " + 
               "AND t_typedebtor = " + TypeDebtor;
    VAR RS = RsdRecordset(qStr);

    while (RS.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DLMRKCALCPROC_DBT", rs);
    end;

    return result;
END;

MACRO Lns_GetMRKCalcProcByID(MRKCalcProcID)
    VAR result = null;
    VAR query = "select * from dlmrkcalcproc_dbt where t_id = " + MRKCalcProcID;
    VAR RS = RsdRecordset(query);
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DLMRKCALCPROC_DBT", rs);
    else
        RunError("Процедура не найдена!");
    end;
    return result;
END;

// Расчет МРК
MACRO Lns_CalcMRK(ObjID, ObjN, MRKCalcProcID)
    var result = WebResponseBuilder();
    var rs = null;
    var qStr = "";
    var MRKCalcProc = Lns_GetMRKCalcProcByID(MRKCalcProcID);
    var MRK = RunStoredFunc(MRKCalcProc.StoredFunc, V_MONEY, 0, ObjID, ObjN, {curdate});   
    if (MRK == 0) // что-то пошло не так. Достаем список ошибок.
        qStr = "SELECT * FROM DLNSERROR_TMP WHERE T_OBJID = " + ObjID +
               " AND T_OBJN = " + ObjN;
        rs = TRSBDataSet(qStr);
        while (rs.MoveNext)
            result.AddErrorEx(rs.ErrorCode, MessageSeverity.RuntimeError, rs.Error);
        end;
    else
        result.SetUserObject(MRK);
    end;
    return result.Result;
    
    OnError(err)
        result.AddErrorEx(err.Code, MessageSeverity.RuntimeError, err.Message);
        return result.Result;
END;