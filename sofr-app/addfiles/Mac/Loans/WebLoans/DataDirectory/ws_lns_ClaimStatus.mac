/*
$Name: ws_lns_ClaimStatus.mac
$Module: WebLoans
$Description: Макрос для работы со справочником "Статусы заявок"
*/
IMPORT RSD, BankInter;
IMPORT ws_datafilter, cb_sql, likepy;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", "DLCLAIMSTATUS_DBT", "likepy.mac";

CLASS ClaimStatusPRM()
	// Структура таблицы статусов
	var ClaimStatus: DLCLAIMSTATUS_DBT;
	// Признак того, что статус (ID >999) был присвоен хотя бы для одной кредитной заявки в результате выполнения любого из шагов маршрута её рассмотрения
	var isUsed: bool;
	// Признак того, что статус привязан хотя бы к одной стадии рассмотрения заявки И в панели <Статусы заявок для стадии> для данного статуса установлен признак  <Для новой заявки>
	var isDefault: bool;
END;

MACRO getIsDefault(ClaimStatusID)
    var result: bool = false;
    var defCount = 0;
    var qStr, rs = null; 
    
    qStr = "SELECT COUNT(*)" +
           " FROM DLCLAIMSTAGESTATUS_DBT CLS" +
           " WHERE CLS.T_ISDEFAULT = 'X' AND CLS.T_CLAIMSTATUSID = " + ClaimStatusID;
           
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        defCount = rs.value(0);
    end;                
   
    if (defCount != 0)
        result = true;
    end;
   
    return result;
END;

PRIVATE MACRO GetStatusListQuery(FilterConditionItems)
    var qStr = "select * from dlclaimstatus_dbt where 1=1 ";
    var filterstr = "";
    if (FilterConditionItems != null)
        var fp = FilterParser( FilterConditionItems );
        fp.AddFieldCondition( 0, null, "t_name");
        fp.AddFieldCondition( 1, null, "t_isnotuse");
        fp.AddFieldCondition( 2, null, "t_isupdate");
        filterstr = fp.GetFullSQLCondition();
    end;
    if (filterstr != "")
        qStr = qStr + " and " + filterstr;
    end;
    qStr = qStr + " order by t_id";
    return qStr;
END;

MACRO Lns_GetDirectoryDataList(FilterConditionItems)
    VAR result = TArray();
    VAR query = GetStatusListQuery(FilterConditionItems);
    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        var item = ClaimStatusPRM();
        item.ClaimStatus = GetObjectFromRecordset("DLCLAIMSTATUS_DBT", rs);
        item.isUsed      = false;
        item.isDefault   = getIsDefault(item.ClaimStatus.ID);
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO Lns_GetDirectoryDataListCount(FilterConditionItems)
    VAR query = GetStatusListQuery(FilterConditionItems);
    return SQL_GetNRecs(query);
END;

MACRO Lns_GetClaimStatusList(StageID, StageIDForFilter)
    VAR result = TArray(),
        fp = null;

    VAR query = "";
    if (StageIDForFilter > 0)
        query = query + " SELECT CS.*" + 
                        " FROM DLCLAIMSTATUS_DBT CS" + 
                        " WHERE 1=1 AND T_ISNOTUSE != 'X' and CS.T_ID in (SELECT CLSTGSTAT.T_CLAIMSTATUSID" +
                                                                        " FROM DLCLAIMSTAGESTATUS_DBT CLSTGSTAT" +
                                                                        " WHERE CLSTGSTAT.T_CLAIMSTAGEID = " + StageIDForFilter + ")" +
                        " ORDER BY T_ID";    
    elif (StageIDForFilter == 0)
        query = query + " SELECT CS.*" + 
                        " FROM DLCLAIMSTATUS_DBT CS " +
                        " WHERE 1=1 AND T_ISNOTUSE != 'X' and CS.T_ID in (SELECT CLSTGSTAT.T_CLAIMSTATUSID" +
                                                                        " FROM DLCLAIMSTAGESTATUS_DBT CLSTGSTAT" + 
                                                                        " INNER JOIN DLCLAIMSTAGE_DBT CLSTG" +
                                                                        " ON CLSTGSTAT.T_CLAIMSTAGEID = CLSTG.T_ID" +
                                                                        " WHERE CLSTG.T_ISNOTUSE != 'X'" + 
                                                                        " )" +  
                        " ORDER BY T_ID";
    else
        query = query + "select cs.* from dlclaimstatus_dbt cs where 1=1 " +
                "and t_isnotuse != 'X' " + 
                "and cs.t_id not in (select t_claimstatusid from dlclaimstagestatus_dbt where t_claimstageid = " + StageID +
                ") order by t_id";
    end;

    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        var item = GetObjectFromRecordset("DLCLAIMSTATUS_DBT", rs);        
        result.value(result.Size) = item;   
    END;

    return result;
END;

PRIVATE MACRO CheckUpdateClaimStatus(ClaimStatus)
    if (ClaimStatus.ClaimStatus.Name == "")
        RunError("Ошибка сохранения статуса", "Введите название статуса");
    end;
END;

PRIVATE MACRO CheckDeleteClaimStatus(ClaimStatusID)
    var qStr = "SELECT 1 FROM dclaim_dbt WHERE t_claimstate=? and rownum = 1";
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_claimstate", RSDBP_IN, ClaimStatusID);
    cmd.execute;
    var rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        RunError("Ошибка удаления статуса", "Нельзя удалить статус! Он был назначен заявке клиента при рассмотрении");
    end;

    qStr = "SELECT 1 FROM dlclaimhistory_dbt WHERE t_claimstatus=? and rownum = 1";
    cmd = RsdCommand(qStr);
    cmd.AddParam("p_claimstatus", RSDBP_IN, ClaimStatusID);
    cmd.execute;
    rs = TRsbDataSet(cmd);
    if (rs.MoveNext)
        RunError("Ошибка удаления статуса", "Нельзя удалить статус! Он был назначен заявке клиента при рассмотрении");
    end;
END;

MACRO Lns_InsertClaimStatus(ClaimStatus)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    CheckUpdateClaimStatus(ClaimStatus);
    var qStr = "insert into dlclaimstatus_dbt (t_id, t_name, t_isnotuse, t_isupdate) values (?,?,?,?)";
    var cmd = RsdCommand(qStr);
    cmd.NullConversion = True;
    cmd.AddParam("p_id",       RSDBP_IN, ClaimStatus.ClaimStatus.ID);
    cmd.AddParam("p_name",     RSDBP_IN, ClaimStatus.ClaimStatus.Name);
    cmd.AddParam("p_isnotuse", RSDBP_IN, ifThenElse(ClaimStatus.ClaimStatus.isNotUse, "X", ""));
    cmd.AddParam("p_isupdate", RSDBP_IN, ifThenElse(ClaimStatus.ClaimStatus.isUpdate, "X", ""));
    cmd.execute();
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result;
END;

MACRO Lns_UpdateClaimStatus(ClaimStatus)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    CheckUpdateClaimStatus(ClaimStatus);
    var qStr = "update dlclaimstatus_dbt set t_name=?, t_isnotuse=?, t_isupdate=? where t_id=?";
    var cmd = RsdCommand(qStr);
    cmd.NullConversion = True;
    cmd.AddParam("p_name",     RSDBP_IN, ClaimStatus.ClaimStatus.Name);
    cmd.AddParam("p_isnotuse", RSDBP_IN, ifThenElse(ClaimStatus.ClaimStatus.isNotUse, "X", ""));
    cmd.AddParam("p_isupdate", RSDBP_IN, ifThenElse(ClaimStatus.ClaimStatus.isUpdate, "X", ""));
    cmd.AddParam("p_id",       RSDBP_IN, ClaimStatus.ClaimStatus.ID);
    cmd.execute();
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result
END;

MACRO Lns_DeleteClaimStatus(ClaimStatusID)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    CheckDeleteClaimStatus(ClaimStatusID);
    var stat = LnSqlExec("delete from dlclaimstatus_dbt where t_id = " + ClaimStatusID, false);   
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления статуса заявки";
    end;
    return result;
    OnError(err)
        result.MsgID = err.code;
        result.MsgText = IfThenElse(err.err, err.err, err.Message);
        return result
END;

MACRO Lns_GetClaimStatusID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dlclaimstatus_dbt", V_INTEGER, 0);
    if (maxval < 1000)
        return 1000;
    else
        return maxval + 1;
    end;
END;

MACRO Lns_GetClaimStatusByID(ClaimStatusID: integer)
    VAR query = "select * from dlclaimstatus_dbt where t_id = " + ClaimStatusID,
        RS = RsdRecordset(query),
        result = DLCLAIMSTATUS_DBT();
        
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DLCLAIMSTATUS_DBT", rs);          
    end;

    return result;
END;

MACRO Lns_GetClaimStatusByIDList(ClaimStatusIDList)
    VAR query = "SELECT * FROM dlclaimstatus_dbt WHERE t_id in (" + join(ClaimStatusIDList, ",") + ")",
        RS = RsdRecordset(query),
        result = TArray();

    IF ((ClaimStatusIDList != null) AND (ClaimStatusIDList.Size != 0))
        WHILE (RS.MoveNext)
            var item = GetObjectFromRecordset("DLCLAIMSTATUS_DBT", rs);            
            result[result.size] = item;
        END;
    END;
    return result;
END;
