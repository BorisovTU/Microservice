/*
$Name: ws_lns_ResponsOperHistory.mac
$Module: WebLoans
$Description: Сервис для работы с историей изменения ответственного пользователя
*/

import LnsClaimRouteCore, ws_validation;
IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", "DLCLAIMROUTE_DBT.mac", "ws_person.mac";

CLASS ResponsOperData()
    VAR responsOperDate : date      = date(0,0,0); // Дата изменения ответственного пользователя
    VAR responsOperObj  : TWsPerson = TWsPerson(); //Объект ответственного пользователя (для листалки операционистов) 
END;

MACRO Lns_GetResponsOperHistoryByID(ObjID: integer, ObjTypeId: integer)
    var query, rsRec;
    var result = TArray();
        
    query = " select O.T_SETDATE, O.T_OPER, P.T_NAME" +
            " from doper_crd_dbt o" +
            " inner join dperson_dbt p" +
            " on P.T_OPER = O.T_OPER" +
            " where O.T_OBJECTID = " + ObjID + " and O.T_OBJECTTYPEID_REF = " + ObjTypeId;

    rsRec = RsdRecordset(query);
 
    while (rsRec.MoveNext)        
        var curRec = ResponsOperData();
        
        curRec.responsOperDate = rsRec.fld(0).value;
        curRec.responsOperObj.Number = rsRec.fld(1).value;
        curRec.responsOperObj.Name_  = rsRec.fld(2).value;
       
        result.value(result.Size) = curRec;
    end;
    
    return result;
END;

//Для получения объекта операциониста в поле Сотрудник на вкладке "Заявка"
MACRO Lns_GetResponsOperObj (ObjID: integer, ObjTypeId: integer, RespOperDate: date)
    var query, rsRec;
    var result : TWsPerson = TWsPerson();

    query = "select O.T_OPER, P.T_NAME" + 
            " from doper_crd_dbt o " +
            " inner join dperson_dbt p" +
            " on P.T_OPER = O.T_OPER" +
            " where O.T_OBJECTID = " + ObjID + " and O.T_OBJECTTYPEID_REF = " + ObjTypeId + 
                    " and O.T_SETDATE = (select NVL(MAX(o.T_SETDATE), TO_DATE('01.01.0001','DD.MM.YYYY'))" + 
                    " from doper_crd_dbt o" + 
                    " where O.T_OBJECTID = " + ObjID + " and O.T_OBJECTTYPEID_REF = " + ObjTypeId + " and ((O.T_SETDATE < TO_DATE(" + SQLString(RespOperDate) + ",'DD.MM.YYYY')) OR (O.T_SETDATE = TO_DATE(" + SQLString(RespOperDate) + ",'DD.MM.YYYY'))))";
                    
    rsRec = RsdRecordset(query);
 
    while (rsRec.MoveNext)        
        result.Number = rsRec.fld(0).value;    
        result.Name_  = rsRec.fld(1).value;    
    end;
    
    return result;
END;    

MACRO Lns_GetLatestRespOperDate (ObjID: integer, ObjTypeId: integer)
    var query, rsRec;
    var result : date = date(0,0,0);

    query = "select NVL(MAX(o.T_SETDATE), TO_DATE('01.01.0001','DD.MM.YYYY')) from doper_crd_dbt o where O.T_OBJECTID = " + ObjID + " and O.T_OBJECTTYPEID_REF = " + ObjTypeId;
    rsRec = RsdRecordset(query);
 
    while (rsRec.MoveNext)        
        result = rsRec.fld(0).value;    
    end;
    
    return result;
END;

MACRO Lns_IsEditModeDependOnLnsObj(ObjID: integer, ObjTypeId: integer)
    var result : bool = true;
    var query, rsRec;
    var creditState : string = "";
    var claimRouteParm = TProcessData();

    if (ObjTypeId == LO_CREDIT)
        query = "SELECT CR.T_CREDITSTATE" +
                " FROM DCREDIT_C_DBT CR" +
                " WHERE CR.T_CREDITNUMBER = " + ObjID;
                
        rsRec = RsdRecordset(query);

        while (rsRec.MoveNext)        
            creditState = rsRec.fld(0).value;    
        end;   

        if ((Index(creditState, "О") == 0) and (Index(creditState, "Ч") == 0))
            result = false;
        end;    
    elif (ObjTypeId == LO_CLAIM)
        claimRouteParm = LoadClaimRouteParm(ObjID);
        if ((claimRouteParm != null) AND (claimRouteParm.IsFinished OR claimRouteParm.IsAborted))
            result = false;
        end;
    end;   
    
    return result;    
END;

MACRO Lns_GetLnsObjDprtNode (ObjID: integer, ObjTypeId: integer)
    var result : integer = 0;
    var query1, rsRec1, query2, rsRec2;
    
    if (ObjTypeId == LO_CREDIT)
        query1 = "SELECT NVL(CR.T_TERRITORIALSTRUCT, 0)" +
                 " FROM DCREDIT_C_DBT CR" +
                 " WHERE CR.T_CREDITNUMBER = " + ObjID;
                 
        rsRec1 = RsdRecordset(query1);

        while (rsRec1.MoveNext)        
            result = rsRec1.fld(0).value;    
        end;                 
    elif (ObjTypeId == LO_CLAIM)
        query2 = "SELECT NVL(CL.T_TERRITORIALSTRUCT, 0)" +
                 " FROM DCLAIM_DBT CL" +
                 " WHERE CL.T_CLAIMID = " + ObjID;
                 
        rsRec2 = RsdRecordset(query2);

        while (rsRec2.MoveNext)        
            result = rsRec2.fld(0).value;    
        end;                         
    end;
    return result;
END;

MACRO Lns_UpdateResponsOper(ObjID: integer, ObjTypeId: integer, NewRespOperDate: date, NewRespOperID: integer)
    var cmdStr : string = "",
        NewRespOperDateStr : string,
        stat = true,
        result = TWsMessage(),
        lastDate : date;

    NewRespOperDateStr = SQLString(NewRespOperDate);
    
    if (Index(" ", NewRespOperDateStr) == 0)
        NewRespOperDateStr = StrSubst(NewRespOperDateStr, " ", "\0");
    end;    
        
//    lastDate = Lns_GetLatestRespOperDate (ObjID, ObjTypeId);
    
    var query1, rsRec1;
    var result1 = TArray();
        
    query1 = "SELECT O.T_SETDATE" + 
             " FROM doper_crd_dbt O" +
             " WHERE O.T_OBJECTID = " + ObjID + " AND O.T_OBJECTTYPEID_REF = " + ObjTypeId +
             " ORDER BY O.T_SETDATE";

    rsRec1 = RsdRecordset(query1);
 
    while (rsRec1.MoveNext) 
        var curItem : date = rsRec1.fld(0).value;    
        result1.value(result1.Size) = curItem;
    end;
    
    if (result1.Size  <= 2)
        lastDate = result1[0];   
    elif (result1.Size > 2)
        lastDate = result1[result1.Size - 2];
    end;
    
    result.MsgText = "";
    result.MsgID = 0;
    if (NewRespOperDate == date(0,0,0))
        result.MsgID = 1;
        result.MsgText = "Введите дату назначения ответственного сотрудника.";
    elif ((lastDate != NULL) and ((NewRespOperDate <= lastDate)/* OR (NewRespOperDate == lastDate)*/))    
        result.MsgID = 1;
        result.MsgText = "Дата должна быть больше предыдущей даты назначения ответственного сотрудника.";
    elif (NewRespOperID == 0)
        result.MsgID = 1;
        result.MsgText = "Введите номер ответственного сотрудника.";
    else
        cmdStr = " update doper_crd_dbt o set " +
               " O.T_SETDATE = TO_DATE(" + NewRespOperDateStr + ",'DD.MM.YYYY')," +
               " O.T_OPER    = " + NewRespOperID +
               " where O.T_OBJECTID = " + ObjID + 
                 " and O.T_OBJECTTYPEID_REF = " + ObjTypeId + 
                 " and O.T_SETDATE = (select MAX(o1.T_SETDATE) " +
                                    " from doper_crd_dbt o1 " +
                                    " where O1.T_OBJECTID = " + ObjID + " and O1.T_OBJECTTYPEID_REF = " + ObjTypeId + ")";

        stat = LnSqlExec(cmdStr, false);

        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка обновления ответственного сотрудника";
        end;
    end;
    return result;
END;

MACRO Lns_InsertResponsOper(ObjID: integer, ObjTypeId: integer, RespOperDate: date, RespOperID: integer)
    var qStr = "",
        stat = true,
        RespOperDateStr : string,
        lastDate,
        result = TWsMessage();

    result.MsgText = "";
    result.MsgID = 0;
    
    RespOperDateStr = SQLString(RespOperDate);
    
    if (Index(" ", RespOperDateStr) == 0)
        RespOperDateStr = StrSubst(RespOperDateStr, " ", "\0");
    end;       
    
    lastDate = Lns_GetLatestRespOperDate (ObjID, ObjTypeId);
        
    if (RespOperDate == date(0,0,0)) 
        result.MsgID = 1;
        result.MsgText = "Введите дату назначения ответственного сотрудника.";
    elif ((lastDate != NULL) and ((RespOperDate <= lastDate)))    
        result.MsgID = 1;
        result.MsgText = "Дата должна быть больше предыдущей даты назначения ответственного сотрудника.";
    elif (RespOperID == 0) 
        result.MsgID = 1;
        result.MsgText = "Введите номер ответственного сотрудника.";
    else
        qStr = "INSERT INTO DOPER_CRD_DBT (T_OBJECTID, T_OBJECTTYPEID_REF, T_OPER, T_SETDATE) " +
               " VALUES ( " +
               ObjID + ", " +
               ObjTypeId + ", " + 
               RespOperID + ", " +
               "TO_DATE(" + RespOperDateStr + ",'DD.MM.YYYY'))";
               
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления ответственного сотрудника.";
        end;    
    end;    
        
    return result;
END;

MACRO Lns_DeleteResponsOper(ObjID: integer, ObjTypeId: integer, RespOperDate: date, RespOperID: integer)
    var stat, result, qStr : string, RespOperDateStr : string;
    RespOperDateStr = SQLString(RespOperDate);
    
    if (Index(" ", RespOperDateStr) == 0)
        RespOperDateStr = StrSubst(RespOperDateStr, " ", "\0");
    end;     
    
    qStr = "DELETE FROM DOPER_CRD_DBT" +
           " WHERE T_OBJECTID = " + ObjID + " AND T_OBJECTTYPEID_REF = " + ObjTypeId + " AND T_SETDATE = TO_DATE(" + RespOperDateStr + ",'DD.MM.YYYY') AND T_OPER = " + RespOperID;

    stat = LnSqlExec(qStr, false);
    result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления участника.";
    end;
    return result;
END;

