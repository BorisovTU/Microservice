/*
$Name: ws_lns_CrdContract.mac
$Module: WebLoans
$Description: Сервис источника данных для получения информации по КД
*/
IMPORT SbCrdInter, RsbDataSet, globals, Календарь, PTInter;
IMPORT "total.mac", "ws_lns_TariffPlan.mac";
IMPORT "ws_loansoperation.mac", "ws_lns_CommonLib.mac", "xirr.mac", "ws_lns_Graph.mac";
IMPORT "fmt_lib.mac", "DCREDIT_C_DBT.mac", "DCLAIM_DBT.mac", "ws_lns_PurposeForCB.mac", "ws_lns_Specific.mac", "ws_lns_UserCreditType.mac";

CLASS CLnsCrdContractEditPrm()
    var credit = null;
    var PurposeForCBIDList = TArray();
END;

CLASS CLnsCreditViewPrm()
    var ContractStatus = "";
    var IsEmployer = false;
    var PayType = 0;
    var LoanerName = "";
    var LoanerStatus = "";
    var ClientStatus = "";
    var GANumber = "";
    var UsNumber = "";
    var Duration = 0;
    var TypeDuration = "";
    var RegDate = NULL;
    var ReturnDate = NULL;
    var TypeCredit = "";
    var PayTypeName = "";
    var CurCode = "";
    var SumCredit = NULL;
    var AvailableLimit = NULL;
    var EnsLimit = NULL;
    var PayLimit = NULL;
    var DebtLimit = NULL;
    var SumDebt = NULL;
    var TotalCost = "";
    var UserTypeCredit = "";
    var Specific = "";
    var MortageRegDate = NULL;
    var ReqCrdType = "";
    var PurposeForCB = "";
    var Comment = "";
    var TariffPlan = "";
    var TariffPlanCond = "";
    var IsAnnuity = true;
    var WithTariffPlan = false;
    var RegViewMode = 1;
    var LnsObjID = CLnsObjID();
    var TotalCostDetailVisibility = true;
    var IsNewDutyEnabled = true;
    var WithOtherDebtors = true;
    var ClaimID = 0;
    var ClaimNumStr = "";
    var IsClaimVisible = false;
END;

MACRO getClaimNumberStr(ClaimID)
    var result = "";
    var qStr = "", rs = NULL;
    
    qStr = "SELECT CL.T_CLAIMNUMBER AS CLAIMNUMSTR" + 
            " FROM DCLAIM_DBT CL" +
            " WHERE CL.T_CLAIMID = " + ClaimID;
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        result = rs.ClaimNumStr;
    end;

    return result;
END;

MACRO getIsClaimVisible(CreditNumber)
    var result:bool = false;
    var qStr = "", rs = NULL;
    
    qStr = "SELECT COUNT(*) AS CNT" + 
           " FROM DCLAIM_DBT CL" + 
           " WHERE CL.T_CREDITNUMBER_REF = " + CreditNumber;
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        if (rs.Cnt > 0)
            result = true;
        end;
    end;

    return result;
END;

MACRO lns_GetViewPrm(ObjN, ClientID)
    var result = CLnsCreditViewPrm();
    var qStr = "";
    var rs = NULL;
    var precision: integer = 0, ErrCode;
    
    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
        RunError(ErrCode);
    end;
    if (precision > 28)
        precision = 28;
    end;
    SetDefPrec (precision);
    
    qStr =  qStr + "select ";
    qStr =  qStr + "credit.t_creditstate as t_ContractStatus, ";
    qStr =  qStr + "decode(NVL(persn.t_IsEmployer, chr(0)), 'X', 'X', decode(credit.t_LegalForm, credit.t_TypeDebtor, chr(0), 'X')) AS t_IsEmployer, ";
    qStr =  qStr + "credit.t_PayType as t_PayType, ";
    qStr =  qStr + "credit.t_TypeDebtor as t_TypeDebtor, ";
    qStr =  qStr + "credit.t_ClientID_Ref as t_ClientID, ";
    qStr =  qStr + "case credit.t_TypeDebtor ";
    qStr =  qStr + "when 1 then 'Индивидуальный предприниматель' ";
    qStr =  qStr + "when 2 then 'Физическое лицо' ";
    qStr =  qStr + "end as t_ClientStatus, ";
    qStr =  qStr + "credit.t_UsCreditNumber as t_UsNumber, ";
    qStr =  qStr + "genagr.t_UsCreditNumber as t_GANumber, ";
    qStr =  qStr + "credit.t_Duration as t_Duration, ";
    qStr =  qStr + "namealg.t_szNameAlg as t_TypeDuration, ";
    qStr =  qStr + "credit.t_RegDate as t_RegDate, ";
    qStr =  qStr + "credit.t_ReturnDate as t_ReturnDate, ";
    qStr =  qStr + "typecrd.t_CreditTypeName as t_TypeCredit, ";
    qStr =  qStr + "namealg2.t_szNameAlg as t_PayTypeName, ";
    qStr =  qStr + "fininstr.t_Ccy as t_CurCode, ";
    qStr =  qStr + "credit.t_CreditSum as t_SumCredit, ";
    qStr =  qStr + "credit.t_UserType as t_UserTypeCredit, ";
    qStr =  qStr + "credit.t_Specific as t_Specific, ";
    qStr =  qStr + "credit.t_MortageRegDate as t_MortageRegDate, ";
    qStr =  qStr + "credit.t_UserField1 as t_Comment, ";
    qStr =  qStr + "credit.t_Annuit as t_Annuit, ";
    qStr =  qStr + "HXIRR.T_calcRATE as t_TotalCost, ";
    qStr =  qStr + "reqcrdtype.t_Name as t_ReqCrdName, ";
    qStr =  qStr + "credit.t_paytype as payType, ";
    qStr =  qStr + "(SELECT count(*) from dcrd_op_dbt op where op.t_creditnumber_ref = credit.t_CreditNumber AND op.t_IsDeleted = 0 AND op.t_systemoperationid = "+CS_PAY+") as paycnt, ";
    qStr =  qStr + "(SELECT count(*) from dduty_crd_dbt duty where duty.t_creditnumber_ref=credit.t_creditnumber) as dutycount, ";
    qStr =  qStr + "crlinparm.t_islimitation as t_isEnsLim, ";
    qStr =  qStr + "credit.t_tplantcrid as TPid, ";
    qStr =  qStr + "credit.t_paytype as payType, ";
    qStr =  qStr + "(SELECT count(*) from dcrd_op_dbt op where op.t_creditnumber_ref = credit.t_CreditNumber AND op.t_IsDeleted = 0 AND op.t_systemoperationid = "+CS_PAY+") as paycnt, ";
    qStr =  qStr + "(SELECT count(*) from dduty_crd_dbt duty where duty.t_creditnumber_ref=credit.t_creditnumber) as dutycount, ";
    qStr =  qStr + "typecrd.t_otherdebtor otherdebtor, ";
    qStr =  qStr + "(SELECT MAX(LINK_CLAIM.CLAIMID_1) FROM (SELECT CL.T_CLAIMID AS CLAIMID_1"; 
                                            qStr =  qStr + " FROM DCLAIM_DBT CL"; 
                                            qStr =  qStr + " WHERE CL.T_CREDITNUMBER_REF = " + ObjN;
                                            qStr =  qStr + " UNION ALL";
                                            qStr =  qStr + " SELECT -100 AS CLAIMID_1";
                                            qStr =  qStr + " FROM DUAL";
                                            qStr =  qStr + " ) LINK_CLAIM) AS CLAIMID ";    
    qStr =  qStr + "from dcredit_c_dbt credit, ";
    qStr =  qStr + "dcredit_c_dbt genagr, ";
    qStr =  qStr + "dtype_crd_dbt typecrd, ";
    qStr =  qStr + "dnamealg_dbt namealg, ";
    qStr =  qStr + "dfininstr_dbt fininstr, ";
    qStr =  qStr + "dnamealg_dbt namealg2, ";
    qStr =  qStr + "dpersn_dbt persn, ";
    qStr =  qStr + "dlreqcrdtype_dbt reqcrdtype, ";
    qStr =  qStr + "dparmdemandline_dbt crlinparm, ";
    qStr =  qStr + "(SELECT max(T_CALCDATE) as t_Calcdate, t_objectid, t_objectnumber, t_calcrate from dlhistry_xirr_dbt where T_CALCDATE <= "; 
    qStr =  qStr + SQLDate({curdate}) + " group by t_objectid, t_objectnumber, t_calcrate)  hxirr ";
    qStr =  qStr + "where  credit.t_CreditNumber = " + ObjN + " and credit.t_Crd_Kind = 1";
    qStr =  qStr + " and typecrd.t_CreditTypeID = credit.t_CreditTypeID_Ref ";
    qStr =  qStr + "and (namealg.t_iNumberAlg = credit.t_TypeDuration and namealg.t_iTypeAlg = 987) ";
    qStr =  qStr + "and fininstr.t_FIID = credit.t_CurCode ";
    qStr =  qStr + "and (namealg2.t_iNumberAlg = credit.t_PayType and namealg2.t_iTypeAlg = 1010) ";
    qStr =  qStr + "and persn.t_PersonID(+) = credit.t_ClientID_Ref ";
    qStr =  qStr + "and HXIRR.T_OBJECTNUMBER(+) = CREDIT.T_CREDITNUMBER ";
    qStr =  qStr + "and HXIRR.T_OBJECTID(+) = credit.t_Crd_Kind ";
    qStr =  qStr + "and reqcrdtype.T_REQCRDTYPEID(+) = credit.T_REQCRDTYPEID ";
    qStr =  qStr + "and genagr.T_CREDITNUMBER(+) = credit.T_GENERALAGREEMENT_REF ";
    qStr =  qStr + "and crlinparm.t_objecttypeid(+) = credit.t_Crd_Kind and crlinparm.t_objectnumber(+) = credit.t_CreditNumber";
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        result.ContractStatus = String(rs.ContractStatus);
        result.IsEmployer = false;
        if (String(rs.IsEmployer) == "X")
            result.IsEmployer = true;
        end;
        result.PayType = Int(rs.PayType);
        result.ClientStatus = String(rs.ClientStatus);
        result.UsNumber = String(rs.UsNumber);
        result.Duration = Int(rs.Duration);
        result.TypeDuration = String(rs.TypeDuration);
        result.RegDate = Date(rs.RegDate);
        result.ReturnDate = Date(rs.ReturnDate);
        result.TypeCredit = String(rs.TypeCredit);
        result.PayTypeName = String(rs.PayTypeName);
        result.CurCode = String(rs.CurCode);
        result.SumCredit = Money(rs.SumCredit);
        
        if (result.PayType == CF_CRLIN)
            result.AvailableLimit = ОстатокРегистра(1, ObjN, TDR_AVAILABLELIMIT, {curdate});
            result.PayLimit = ОстатокРегистра(1, ObjN, TCLTR_LIMPAY, {curdate});
            result.DebtLimit = ОстатокРегистра(1, ObjN, TCLTR_LIMDUTY, {curdate});
        elif (result.PayType == CF_CRLINNO)
            result.AvailableLimit = ОстатокРегистра(1, ObjN, TDR_AVAILABLELIMIT, {curdate});
            result.PayLimit = ОстатокРегистра(1, ObjN, TCLTR_LIMPAY, {curdate});
        elif (result.PayType == CF_CRLINNEW)
            result.AvailableLimit = ОстатокРегистра(1, ObjN, TDR_AVAILABLELIMIT, {curdate});
            result.DebtLimit = ОстатокРегистра(1, ObjN, TCLTR_LIMDUTY, {curdate});
            result.SumCredit = NULL; //сумма скрыта
        else // тип выдачи - не КЛ
            result.AvailableLimit = NULL;
            result.PayLimit = NULL;
            result.DebtLimit = NULL;
            if (lns_CrdHasDuty(ObjN) == false) // Не открыто ни одного СО.
                result.RegViewMode = 0;
            end;
        end;
     
        if (rs.isEnsLim == "X") // признак "ограничение лимита обеспечением"
            result.EnsLimit = RunStoredFunc("loanskernel.calcnlo", V_MONEY, NULL, ObjN, {curdate});
        else
            result.EnsLimit = NULL;
        end;
        
        result.SumDebt = Money(GetAllRegRest(LO_CREDIT, ObjN, TDR_MAINREST, date(31,12,9999)) + 
                               GetAllRegRest(LO_CREDIT, ObjN, TDR_EXPREST,  date(31,12,9999)) + 
                               GetAllRegRest(LO_CREDIT, ObjN, TDR_LIM,      date(31,12,9999)));
        
        if (int(rs.TypeDebtor) == 2) // ПСК выводим только для физиков.
            if (rs.TotalCost == null)
                result.TotalCost = "Не рассчитана ";
            else
                result.TotalCost = string(double(rs.TotalCost)) + "% годовых ";
            end;
        else
            result.TotalCost = "";
        end;

        result.UserTypeCredit = GetUserCreditTypeNameByStr(String(rs.UserTypeCredit));
        result.Specific = GetSpecificNameByStr(String(rs.Specific));

        if (rs.ReqCrdName == null)
            result.ReqCrdType = "";
        else
            result.ReqCrdType = String(rs.ReqCrdName);
        end;
        if (rs.GANumber == null)
            result.GANumber = "";
        else
            result.GANumber = rs.GANumber;
        end;
        result.MortageRegDate = Date(rs.MortageRegDate);
        result.Comment = String(rs.Comment);
        if (rs.t_Annuit == "X")
            result.IsAnnuity = true;
        else
            result.IsAnnuity = false;
        end;

        if ((ClientID != NULL) and (ClientID != 0))
            if (ClientID != Int(rs.ClientID))
                result.LoanerName = LnSelectValue("SELECT t_name from dparty_dbt where t_partyid = " + Int(rs.ClientID), V_STRING, 0);
                result.LoanerStatus = String(rs.ClientStatus);
                result.ClientStatus = "";
            else
                result.LoanerName = "";
                result.LoanerStatus = ""; 
            end;
        end;
        
        if (rs.TPid > 0)
            result.WithTariffPlan = true;
        else
            result.WithTariffPlan = false;
        end;
        
        if ( 
                ( (int(rs.dutycount) > 0) and //если есть СО и тип "Разовый" или "До востребования"
                    ( (rs.payType == CF_NONREP) or (rs.payType == CF_POSTERESTANTE) )
                )
            or
                ( (rs.payType == CF_NONREP) and (int(rs.paycnt)>0) ) //или разовый и была выдача
            or
                ( (rs.payType == CF_POSTERESTANTE) and (int(rs.paycnt)==0) )//или до востребования и выдачи не было
           )
                result.IsNewDutyEnabled = false; //запретить вводить новые СО
        end;
        
        result.LnsObjID.ObjID = LO_CREDIT;
        result.LnsObjID.ObjN = ObjN;

        if (int(rs.TypeDebtor) == 1) result.TotalCostDetailVisibility = false; end;  //Если договор ЮЛ
        if ((result.ContractStatus == "З") or (result.ContractStatus == "Д"))        //либо закрыт и без рассчитанной истории ПСК
            if (rs.TotalCost == null) result.TotalCostDetailVisibility = false; end; //метку "Расчет ПСК" не выводим
        end;
        
        if (rs.otherdebtor == "X")
            result.WithOtherDebtors = true;
        else
            result.WithOtherDebtors = false;
        end;
        result.ClaimID        = Int(rs.CLAIMID);
        result.ClaimNumStr    = getClaimNumberStr(Int(rs.CLAIMID));
        result.IsClaimVisible = getIsClaimVisible(ObjN);        
        
        result.PurposeForCB = GetPurposeStrByObjID(LO_CREDIT, ObjN);
    end;

    return result;
END;

MACRO lns_GetEditPrm(ObjN)
    var result = CLnsCrdContractEditPrm();
    var qStr = "";
    var rs = NULL;

    qStr =  qStr + "select * ";
    qStr =  qStr + "from dcredit_c_dbt t where ";
    qStr =  qStr + "t.t_CreditNumber = " + ObjN + " and t.t_Crd_Kind = 1"; 

    rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        result.credit = GetObjectFromRecordset("DCREDIT_C_DBT", rs);
        result.PurposeForCBIDList = GetPurposeForCBIDList(LO_CREDIT, ObjN);
    end;

    return result;
END;

MACRO lns_InitNewContract(OpDate, ClientID)
    var credit = DCREDIT_C_DBT();
    var result = CLnsCrdContractEditPrm();
    var qStr;
    var rs = NULL;

    qStr = "SELECT t_LegalForm FROM DPARTY_DBT WHERE T_PARTYID = " + ClientID;
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        credit.legalform = rs.LegalForm;
        if (credit.legalform == 1)
            credit.typedebtor = 1;
        else
            credit.typedebtor = 2;
        end;
    end;
    credit.clientid_ref      = ClientID;
    credit.creditstate       = CF_OPEN;
    credit.duration          = 1;
    credit.typeduration      = 1; // по умолчанию в днях
    credit.regdate           = OpDate;
    credit.returndate        = OpDate + credit.duration; // добавить проверки на выходной день
    credit.crd_kind          = LO_CREDIT;
    credit.fncash            = {OperDprt}; // будем считать, что опер находится в этом же филиале
    credit.territorialstruct = {OperDprtNode};
    credit.acceptance        = 1;
    credit.beforeduty        = 0;
    credit.flagob            = 1;
    credit.privilegeflag     = "";
    credit.mortageregdate    = date(0,0,0);
    credit.uscreditnumber    = "";
    credit.groupnum          = 0;
    credit.usertype          = "";
    credit.paytype           = 1;
    credit.repdate          = date(0,0,0);

    // вызвать макрос из dlobject_dbt.t_macro
    
    result.credit = credit;
    result.PurposeForCBIDList = TArray();

    return result;
END;

MACRO lns_InitContractByCreditType(CreditType, ContractEditPrm)
    var result = ContractEditPrm;
    var qStr = "", rs = NULL;
    qStr =  "select * from dtype_crd_dbt where t_credittypeid = " + CreditType;
    
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        result.credit.credittypeid_ref = CreditType;
        result.credit.paytype          = rs.TypeOverdraft;
        result.credit.specific         = rs.Specific;
        result.credit.annuit           = rs.Annuit;
        if (
            (result.credit.duration == 0) and
            (result.credit.typeduration == 0) 
           )
            result.credit.duration = rs.Duration;
        end;
        result.credit.curcode = rs.CurCode;
        if (result.credit.creditnumber == 0)
            result.credit.duration = rs.Duration;
            result.credit.typeduration = rs.TypeDuration;
            if (result.credit.duration > 0)
                if (result.credit.typeduration == 0) // анализировать DayOffWeek + проверки на выходной день
                    result.credit.returndate = GetDateAfterMonthsWBranch(result.credit.regdate, result.credit.duration, MONTH_CALENDAR);
                else
                    result.credit.returndate = result.credit.regdate + result.credit.duration;
                end;
            end;
        else
            result.credit.returndate = result.credit.regdate;
        end;
        if (result.credit.uscreditnumber == "")
            result.credit.UsCreditNumber = lns_GetGenerationNumberObject(result.credit.crd_kind, result.credit);
        end;
        
        result.credit.tplantcrid = Lns_GetPlanID(CreditType, rs.CurCode, result.credit.clientid_ref, result.credit.crd_kind, {curdate});
        result.PurposeForCBIDList = GetPurposeForCBIDList(LO_TYPECREDIT, CreditType);
        // в довершениии вызывать mac_crd.mac
    end;

    return result;
END;

MACRO lns_InitContractByClaimID(ClaimID)
    var result = CLnsCrdContractEditPrm();
    var credit = DCREDIT_C_DBT();
    var claim = TBFile ("claim.dbt", "r", 0, "claim.dbt", "loans.def");
    claim.rec.claimid = ClaimID;
    var stat = claim.GetEQ();
    if (stat)
        result = lns_InitNewContract({curdate}, claim.rec.loanerid_ref);
        result = lns_InitContractByCreditType(claim.rec.credittypeid_ref, result);
    
        // тут много копирования
        result.credit.credittypeid_ref  = claim.rec.credittypeid_ref;
        result.credit.fncash            = claim.rec.fncash;
        result.credit.clientid_ref      = claim.rec.loanerid_ref;
        result.credit.creditsum         = claim.rec.bcreditsum;
        result.credit.curcode           = claim.rec.bcreditcurcode;
        result.credit.duration          = claim.rec.bcreditduration;
        result.credit.typeduration      = claim.rec.bctypeduration;
        if (result.credit.duration > 0)
            if (result.credit.typeduration == 0) // анализировать DayOffWeek + проверки на выходной день
                result.credit.returndate = GetDateAfterMonthsWBranch(result.credit.regdate, result.credit.duration, MONTH_CALENDAR);
            else
                result.credit.returndate = result.credit.regdate + result.credit.duration;
            end;
        end;
        result.credit.territorialstruct = claim.rec.territorialstruct;
        result.credit.specific          = claim.rec.specific;
        result.credit.typedebtor        = claim.rec.typedebtor;
        result.credit.tplantcrid        = claim.rec.tplantcrid;
        result.credit.condid            = claim.rec.condid;
        result.credit.reqcrdtypeid      = claim.rec.reqcrdtypeid;

        result.PurposeForCBIDList = GetPurposeForCBIDList(LO_CLAIM, claim.rec.claimid);
    else
        RunError("Заявка не найдена!");
    end;
    return result;
END;

CLASS PostActionData()
    var IsBorrowingRates;
    var IsAccOpen;
    var IsAccReserve;
    var IsGenerateGraphs;
    var IsChangePayLimit;
    var IsChangeDebtLimit;
    var IsCalcPSK;
    var SumPayLimit;
    var SumDebtLimit;
END;

MACRO lns_InsNewContract(OpDate, ContractEditPrm, PostAction)
    var Contract = ContractEditPrm.credit;
    var result = WebResponseBuilder(),
        LnsObjID = CLnsObjID();
    var qStr = "", rs = NULL;
    var AvailableLimit = $0;
    var stat = 0;
    record creditBuff ("credit_c.dbt");

    if (PT_CheckClientService (Contract.clientid_ref, PTSK_LOANS, {curdate}, {OperDprt}) != 0)
        if (not PT_BindClientWithBranch(Contract.clientid_ref,
                                        PTSK_LOANS,
                                        {OperDprt},
                                        {oper},
                                        {curdate},
                                        false))
		    RunError("Ошибка установки вида обслуживания");	
	    end;
    end;

    // проверки пока не выполняем
    creditBuff.CreditNumber      = 0;
    creditBuff.DURATION          = Contract.returndate - Contract.regdate;
    creditBuff.TYPEDURATION      = 1;
    creditBuff.TERRITORIALSTRUCT = Contract.territorialstruct;   
    creditBuff.PAYTYPE           = Contract.paytype;
    creditBuff.CRD_KIND          = Contract.crd_kind;
    creditBuff.FNCASH            = Contract.fncash;
    creditBuff.CREDITSTATE       = Contract.creditstate;
    creditBuff.LEGALFORM         = Contract.legalform;
    creditBuff.TypeDebtor        = Contract.typedebtor;
    creditBuff.FlagOB            = Contract.flagob;
    creditBuff.GroupNum          = Contract.groupnum;
    creditBuff.UserType          = Contract.usertype;
    creditBuff.Specific          = Contract.specific;
    creditBuff.USCREDITNUMBER    = Contract.uscreditnumber;
    creditBuff.REGDATE           = Contract.regdate;
    creditBuff.RETURNDATE        = Contract.returndate;
    creditBuff.RepDate           = Contract.repdate;
    creditBuff.CREDITSUM         = Contract.creditsum;
    creditBuff.CREDITTYPEID_REF  = Contract.credittypeid_ref;
    creditBuff.ClientID_Ref      = Contract.clientid_ref;
    creditBuff.PrivilegeFlag     = Contract.privilegeflag;
    creditBuff.BeforeDuty        = Contract.beforeduty;
    creditBuff.FineCond          = 0;
    creditBuff.ANNUIT            = Contract.annuit;
    creditBuff.indClassification = "X";
    creditBuff.UserField1        = Contract.userfield1;
    creditBuff.UserField2        = "";
    creditBuff.CURCODE           = Contract.curcode;
    creditBuff.ReqCrdTypeID      = Contract.ReqCrdTypeID;
    creditBuff.MortageRegDate    = Contract.MortageRegDate;
    creditBuff.TPlanTcrID        = Contract.TPlanTcrID;
    creditBuff.CondID            = Contract.CondID;
    // Выполняем операцию "открытие договора"
    if (creditBuff.CREDITSTATE == CF_OPEN)
        stat = MakeOperation(0, 0, CF_OPCRD, 0, OpDate, creditBuff);
    elif (creditBuff.CREDITSTATE == CF_OPENDRAFT)                               // доп условие сделано для того, что так то по-хорошему
        stat = MakeOperation(0, 0, CF_OPCRD, 0, OpDate, creditBuff);    // черновик должен окрываться операцией CF_DRAFTOPEN,
                                                                                // а ни разу не операцией CF_OPCRD
    else
       ; // операции открытия всего остального 
    end;
    
    if(stat == 0)
        result.AddErrorEx(0, MessageSeverity.RuntimeError, GetMO_LoansError());
    else
        qStr = "SELECT * FROM dcrd_op_dbt WHERE t_credoperid = " + stat;
        rs = TRsbDataSet(qStr);
        rs.moveNext();
        
        LnsObjID.ObjID = LO_CREDIT;
        LnsObjID.ObjN = rs.ObjectId_Ref;

        if (ContractEditPrm.PurposeForCBIDList != null)
            UpdatePurposeForCBList(LnsObjID.ObjID, LnsObjID.ObjN, ContractEditPrm.PurposeForCBIDList);
        end;

        result.SetUserObject(LnsObjID);

        if (PostAction.IsBorrowingRates == true)
            var ObjIDFrom = LO_TYPECREDIT;
            var ObjNFrom  = creditBuff.CREDITTYPEID_REF;
            
            if (creditBuff.CondID)
                ObjIDFrom = LO_CONDTARIFFPLAN;
                ObjNFrom = creditBuff.CondID;
            elif (creditBuff.TPlanTcrID)
                ObjIDFrom = LO_TARIFFPLAN;
                ObjNFrom = creditBuff.TPlanTcrID;
            end;

            stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, CF_RATE, creditBuff.CurCode, date(rs.CredOperDate), 0, ObjNFrom, ObjIDFrom);
            if (stat == 0)
                result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка заимствования тарифов: " + GetMO_LoansError());
            end;
        end;
        if (PostAction.IsAccOpen == true)
            stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, OP_ACC, 0, date(rs.CredOperDate), rs.ObjectId_Ref, 1);
            if (stat == 0)
                result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка открытия счетов: " + GetMO_LoansError());
            end;
        end;
        if (PostAction.IsGenerateGraphs == true)
            var errors = MakeGraphForObject(rs.ObjectTypeId_Ref, rs.ObjectId_Ref, OpDate);
            for (var err, errors)
                result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка построения графиков: " + err);
            end;
        end;
        if (PostAction.IsAccReserve == true)
            stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, OP_RESERVEACC, 0, date(rs.CredOperDate), rs.ObjectId_Ref, 1);
            if (stat == 0)
                result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка резервирования счетов: " + GetMO_LoansError());
            end;
        end;
        if (PostAction.IsCalcPSK == true)
            SavePSKData(CalcPSK(rs.ObjectTypeId_Ref, rs.ObjectId_Ref, date(rs.CredOperDate)).rec);
        end;
        if ((Contract.PayType == CF_CRLIN) OR (Contract.PayType == CF_CRLINNO))
            if (PostAction.IsChangePayLimit == true)
                AvailableLimit = GetAvailableLimit(rs.ObjectId_Ref, date(rs.CredOperDate), CS_CHANGE_LIM, CF_CHLIM_PAY, Contract.creditsum);
                stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref,  CF_CHLIM_PAY, 0, date(rs.CredOperDate), 0, 0, 0, PostAction.SumPayLimit, AvailableLimit);
                if (stat == 0)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка установки лимита выдачи: " + GetMO_LoansError());
                end;
            end;
        end;
        if ((Contract.PayType == CF_CRLIN) OR (Contract.PayType == CF_CRLINNEW))
            if (PostAction.IsChangeDebtLimit == true)
                AvailableLimit = GetAvailableLimit(rs.ObjectId_Ref, date(rs.CredOperDate), CS_CHANGE_LIM, CF_CHLIM_DUTY, Contract.creditsum);
                stat = MakeOperation(rs.ObjectId_Ref, rs.ObjectTypeId_Ref, CF_CHLIM_DUTY, 0, date(rs.CredOperDate), 0, 0, 0, PostAction.SumDebtLimit, AvailableLimit);
                if (stat == 0)
                    result.AddErrorEx(0, MessageSeverity.RuntimeError, "Ошибка установки лимита задолженности: " + GetMO_LoansError());
                end;
            end;
        end;
        
    end;
    return result.Result;
END;

MACRO lns_UpdContract(ContractEditPrm)
    var Contract = ContractEditPrm.credit;
    var result = TWsMessage();
    var qStr = "", RSDcmd = NULL;
    var stat = 0;
    record creditBuff ("credit_c.dbt");
    CopyClassObjToRecord(Contract, creditBuff);

    //Проверки аннуитета
    RSDcmd = RsdCommand(RslDefCon, "BEGIN ? := LoansUserFunction.GetLastCredOperID_ForPlanPay (?, ?, ?); END;");

    RSDcmd.addParam( "retval", RSDBP_RETVAL, V_INTEGER );

    RSDcmd.addParam("CredOperID",     RSDBP_IN); RSDcmd.Value("CredOperID")     = 1;
    RSDcmd.addParam("OperTypeNumber", RSDBP_IN); RSDcmd.Value("OperTypeNumber") = Contract.creditnumber;
    RSDcmd.addParam("StageID",        RSDBP_IN); RSDcmd.Value("StageID")        = 0;

    RSDcmd.execute();

    if (RSDcmd.value(0) > 0)
        var rsbset = TRsbDataSet("select ASCII(t_annuit) annuit from dlgpayop_dbt where t_credoperid_ref = " + RSDcmd.value(0));
        if (rsbset.moveNext)
           if ((int(rsbset.annuit) == 88) and (Contract.annuit == "X") or
                (int(rsbset.annuit) == 0) and (Contract.annuit == ""))
                stat = 0;
            else 
                if (Contract.annuit == "")
                    result.MsgText = "По договору построен аннуитетный график погашения, но в панели договора признак аннуитетного платежа не установлен.";
                else
                    result.MsgText = "По договору построен не аннуитетный график погашения, но в панели договора установлен признак аннуитетного платежа.";
                end;
                stat = -1;
            end;
       END;
    end;

    if (stat == 0)
    qStr =  qStr + "update dcredit_c_dbt t set ";

    if (StrLen(Contract.usertype) != 0) 
        qStr =  qStr + " t.t_usertype = '" + Contract.usertype + "', ";
    else
        qStr =  qStr + " t.t_usertype = chr(1), ";
    end;

    if (StrLen(String(Contract.ReqCrdTypeID)) != 0) 
        qStr =  qStr + " t.t_ReqCrdTypeID = " + Contract.ReqCrdTypeID + ", ";
    end;
    
    if (StrLen(Contract.specific) != 0) 
        qStr =  qStr + " t.t_specific = '" + Contract.specific + "', ";
    else
        qStr =  qStr + " t.t_specific = chr(1), ";
    end;
    if ((Contract.MortageRegDate != NULL) and (Date(Contract.MortageRegDate) != Date(0,0,0))) 
        qStr =  qStr + " t.t_mortageregdate = " + SQLDate(Date(Contract.MortageRegDate)) + ", ";
    else
        qStr =  qStr + " t.t_mortageregdate = " + SQLDate(Date(0,0,0)) + ", ";
    end;
    
    if (StrLen(Contract.userfield1) != 0) 
            qStr =  qStr + " t.t_userfield1 = '" + Contract.userfield1 + "', ";
    else
            qStr =  qStr + " t.t_userfield1 = chr(1), ";
    end;
    
        if (Contract.annuit == "X") 
            qStr =  qStr + " t.t_annuit = 'X'";
        else
            qStr =  qStr + " t.t_annuit = chr(0) ";
        end;

    qStr =  qStr + " where t.t_CreditNumber = " + Contract.CreditNumber + " and t.t_Crd_Kind = " + Contract.Crd_Kind; 

    RSDcmd = RsdCommand(qStr);
    RSDcmd.execute;

    result.MsgText = "";
    UpdatePurposeForCBList(LO_CREDIT, Contract.CreditNumber, ContractEditPrm.PurposeForCBIDList);
    ОбновитьИнформациюПоПродукту(creditBuff,{curdate});
    end;
    
    result.MsgID = stat;
    
    return result;
END;

MACRO lns_UpdDraft(ContractEditPrm)
    var Contract = ContractEditPrm.credit;
    var result = TWsMessage();
    record creditBuff ("credit_c.dbt");
    var credit_c = TBFile ("credit_c.dbt", "rw", 0, "credit_c.dbt", "loans.def");
    CopyClassObjToRecord(Contract, creditBuff);
    credit_c.AddFilter("T_CreditNumber = " + creditBuff.CreditNumber);
    credit_c.rec.CreditNumber = creditBuff.CreditNumber;
    var stat = credit_c.GetEQ();
    if (stat)
        copy(credit_c, creditBuff);
        credit_c.update();
        UpdatePurposeForCBList(LO_CREDIT, Contract.CreditNumber, ContractEditPrm.PurposeForCBIDList);
        result.MsgText = "";
        result.MsgID = 0;
    else
        result.MsgText = "Черновик не найден!";
        result.MsgID = stat;
    end;
    credit_c.DropFilter();
    ОбновитьИнформациюПоПродукту(creditBuff,{curdate});
    return result;
END;

MACRO lns_GetCredit_c(ObjN)
    var qStr = "";
    var rs = NULL;
    var result = DCREDIT_C_DBT();

    qStr =  qStr + "select * ";
    qStr =  qStr + "from dcredit_c_dbt t where ";
    qStr =  qStr + "t.t_CreditNumber = " + ObjN + " and t.t_Crd_Kind = 1"; 

    rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        result = GetObjectFromRecordset("DCREDIT_C_DBT", rs);
    end;

    return result;
END;


