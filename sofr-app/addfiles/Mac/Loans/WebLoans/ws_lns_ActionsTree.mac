/*
$Name: ws_lns_ActionsTree.mac
$Module: WebLoans
$Description: Сервис источника данных для дерева операций
*/
IMPORT SbCrdInter, RsbDataSet;
IMPORT "total.mac", "fmt_lib.mac";
IMPORT LnsClaimRouteCore;

CLASS CLnsOperID
    var SysOperID:  integer = 0;
    var TypeOperID: integer = 0;
END;

CLASS CLnsAction
    var Header = "";
    var LnsOperID = CLnsOperID();
    var IsShowDocOperations = true;
END;

CLASS CLnsActionsTreeItem
    var Name         = "";
    var LnsAction    = NULL;
    var ItemTemplate = NULL;
    var Childs       = NULL;
END;

MACRO DefinIsShowDocOperations(_TypeOperID)
  var
      retVal = false,
	  rs = TRsbDataSet("select count(1) as CNT from dstep_op_dbt where t_TypeOperID_ref = " + String(_TypeOperID) + " and t_IsOFF <> 'X' and t_Reverseanentry = 0");
	  
    rs.moveNext();
    if (rs.CNT > 0)
        retVal = true;
    end;

    return retVal;
END;

PRIVATE MACRO GetCreditState(ObjID, ObjN)
    var qStr = "", 
        rs = NULL;

    if ((ObjID == LO_CREDIT) or (ObjID == LO_KARTA))
        qStr = "SELECT t_creditstate FROM dcredit_c_dbt WHERE " +
               "t_creditnumber = " + ObjN +
               " AND t_crd_kind = " + ObjID;
        rs = TRsbDataSet(qStr);
        if (rs.MoveNext())
            return rs.CreditState;
        else
            return "";
        end;
    elif (ObjID == LO_DUTY)
        qStr = "SELECT t_dutystate FROM dduty_crd_dbt WHERE " +
               "t_dutyid = " + ObjN;
        rs = TRsbDataSet(qStr);
        if (rs.MoveNext())
            return rs.DutyState;
        else
            return "";
        end;

    end;
END;

MACRO lns_GetOperTree(ObjID, ObjN, GroupActions) // дерево операций
    var result = TArray();
    var actions_tree = TArray();
    var item = NULL;
    var child = NULL;
    var rs = NULL;
    var qStr = "";
    var sysop = 0;
    var idx = 0;
    var CreditState = "";
    var paytype = 1;
    var transhtype = 0;
    if ((ObjID == LO_CREDIT) or (ObjID == LO_KARTA))
        qStr = "select * from dcredit_c_dbt where t_creditnumber = " + ObjN;
    elif (ObjID == LO_DUTY)
        qStr = "select t.* from dcredit_c_dbt t, dduty_crd_dbt tt where tt.t_creditnumber_ref = t.t_creditnumber and tt.t_dutyid = " + ObjN;
    end;

    rs = RsdRecordset(qStr);
    if (rs.MoveNext)
        var obj = GetObjectFromRecordset("DCREDIT_C_DBT", rs);
        paytype = obj.paytype;
    end;

    CreditState = GetCreditState(ObjID, ObjN);

    LnSqlExec("delete from dtype_oplist_tmp");

    RunStoredFunc("RSI_LOANS_ADDITIONAL_ALGO.gettypeoper_web", 
                  V_INTEGER, 
                  NULL, 
                  ObjID, 
                  ObjN,
                  CreditState,
                  GroupActions,
                  paytype,
                  transhtype);
    
    qStr = "SELECT oplist.t_igdefoprid, oplist.t_typeopernumber, systop.t_systemoperationname, typeop.t_typeopername " + 
	       "FROM dtype_oplist_tmp oplist, dtype_op_dbt typeop, dsyst_op_dbt systop " +
           "WHERE oplist.t_igdefoprid = systop.t_systemoperationid " + 
           "AND oplist.t_typeopernumber = typeop.t_typeoperid " + 
           "ORDER BY oplist.t_igdefoprid, oplist.t_typeopernumber";
 
    rs = TRsbDataSet(qStr);
    while (rs.moveNext())
        child = CLnsActionsTreeItem();
        child.Name = rs.typeopername;
        child.LnsAction = CLnsAction(); 
        child.LnsAction.Header = rs.typeopername;
        child.LnsAction.LnsOperID.SysOperID = rs.igdefoprid;
        child.LnsAction.LnsOperID.TypeOperID = rs.typeopernumber;
        child.LnsAction.IsShowDocOperations = DefinIsShowDocOperations(child.LnsAction.LnsOperID.TypeOperID);

        if (sysop != rs.t_igdefoprid)
            item = CLnsActionsTreeItem();
            item.Name = rs.systemoperationname;
            item.Childs = TArray();
            actions_tree[actions_tree.size]= item;

            sysop = rs.t_igdefoprid;
        end;
        idx = actions_tree.size - 1;
        actions_tree[idx].Childs[actions_tree[idx].Childs.size] = child;
    end;

    idx = 0;
    while (idx < actions_tree.size)
        if (actions_tree[idx].Childs.size > 0)
            result[result.size] = actions_tree[idx];
        end;
        idx = idx + 1;
    end;

    LnSqlExec("delete from dtype_oplist_tmp");

    return result;
END;

MACRO lns_GetClaimRouteTree(ObjID)
    var result = TArray();
    var Tree = NULL;
    var Action = NULL;
    var ClaimRouteParm = LoadClaimRouteParm(ObjID);
    var StepName = ""; 
    Action = CLnsAction();
    Action.LnsOperID = CLnsOperID();
    Action.LnsOperID.SysOperID = -1; // не выполняем операцию, а ходим по маршруту
    Action.IsShowDocOperations = false;
    
    Tree = CLnsActionsTreeItem();
    Tree.LnsAction    = Action;
    Tree.ItemTemplate = NULL;
    Tree.Childs       = NULL;
    
    if (ClaimRouteParm != null)
        var CurrentStep = GetCurrentStepInfo(ObjID);
        if  ((ClaimRouteParm.isFinished == true) or 
             (ClaimRouteParm.isAborted == true) or 
             (CurrentStep.CanExecute == false)) // если маршрут завершился
            return result;                                                               // или отменен
        end;                                                                             // сразу возвращаем пустой список.
        StepName = CurrentStep.StepName;
        Action.LnsOperID.TypeOperID = -2;
        Action.Header = StepName;
        Tree.Name     = StepName;
    else
        Action.LnsOperID.TypeOperID = -1; // запуск -2 - продолжение.
        Action.Header = "Отправить заявку на рассмотрение";
        Tree.Name     = "Отправить заявку на рассмотрение"; 
    end;
        
    result[result.size] = Tree;
    return result;
END;

MACRO lns_GetActionsTree(ObjID, ObjN, GroupActions)
    var result = null;
    if ((ObjID == LO_CLAIM) and (GroupActions == 1)) // для заявки в первой панели показываем запуск маршрута
        var ClaimType = LnSelectValue("SELECT t_moduletype FROM dclaim_dbt WHERE t_claimid = " + ObjN, V_INTEGER, 0);
        if (ClaimType == LNS_MT_WEBCREDIT)
            result = lns_GetClaimRouteTree(ObjN);
        end;
    else                                            // для всего остального - операции.
        result = lns_GetOperTree(ObjID, ObjN, GroupActions);
    end;
    return result;
END;
