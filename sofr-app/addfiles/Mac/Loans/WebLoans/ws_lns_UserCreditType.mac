/*
$Name: ws_lns_UserCreditType.mac
$Module: WebLoans
$Description: Сервис источника данных для листалки "Пользовательский тип кредита"
*/
IMPORT RsbDataSet, total, likepy;

PRIVATE CLASS CLnsUserCreditType
  var Code:string    = "",
      Name:string    = "",
      Contens:string = "",
END;

MACRO lns_UserCreditTypeList()
   VAR result = TArray(), obj = NULL;
   VAR rs = NULL;

   VAR query = "select * from dtypeac_dbt where t_inumtype = 101"; //заменить на константу TA_USBCRD

    rs = TRsbDataSet(query);

    WHILE (rs.moveNext())
        obj = CLnsUserCreditType();
        
        obj.Name        = rs.Name_type;         // имя
        obj.Code        = rs.Type_account;      // буквенное обозначение
        obj.Contens     = rs.Contens;           // Описание
        
        result[result.size] = obj;
    END;
  
    RETURN result;
END;

MACRO lns_UserCreditTypeListByStr(UserCreditType)
    VAR result = TArray(), obj = NULL;
    VAR rs = NULL;
    VAR query = "select * from dtypeac_dbt where t_inumtype = 101 "; 
    if (strlen(UserCreditType) > 0)
        var i = 0;
        var s = "";
        while (i < strlen(UserCreditType))
            s = s + "'" + substr(UserCreditType, i + 1, 1) + "'";
            if (i != strlen(UserCreditType) - 1)
                s = s + ", ";
            end;
            i = i + 1;
        end;	 
        query = query + " and t_type_account in (" + s + ")";
        rs = TRsbDataSet(query);
        while (rs.moveNext())
            obj = CLnsUserCreditType();
            
            obj.Name        = rs.Name_type;         // имя
            obj.Code        = rs.Type_account;      // буквенное обозначение
            obj.Contens     = rs.Contens;           // Описание
            
            result[result.size] = obj;
        end;
    end;
    RETURN result;
END;

MACRO GetUserCreditTypeNameByStr(UserCreditType)
    var result = "";
    var UsCrdTypeArr = StrSplit2(UserCreditType, 1);
    if (UsCrdTypeArr.size == 0)
        result = "";
    elif (UsCrdTypeArr.size == 1)
        result = LnSelectValue("select t_name_type from dtypeac_dbt where t_inumtype = 101 and t_type_account='" + UsCrdTypeArr[0] + "'", V_STRING);
    else
        result = join(UsCrdTypeArr, ", ");
    end;
    return result;
END;