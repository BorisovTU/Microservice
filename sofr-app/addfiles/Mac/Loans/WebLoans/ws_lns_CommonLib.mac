/*
$Name: ws_lns_CommonLib.mac
$Module: WebLoans
$Description: Библиотека общих макрофункций
*/

IMPORT SbCrdInter, BankInter, globals, RsbDataSet, serviceaction, "fmt_lib.mac", "total.mac";
IMPORT DCREDIT_C_DBT, DCRDCOMMMEET_DBT;

CLASS CLnsObjID
    var ObjID: integer = 0;
    var ObjN:  Integer = 0;
END;

CLASS LnsSimpleObjectData
VAR ObjID:      integer = 0,
    ObjN :      integer = 0,
    Name :      string = "",
    ShortName:  string = "",
    State:      string = "",
    StateName:  string = "",
    Number:     string = "";
END;

MACRO lns_GetSimpleObjectData(ObjID, ObjN)
    var SObject = LnsSimpleObjectData(),
        QStr = "",
        rs = null;
    if (ObjID == LO_CREDIT)
        QStr = "select cr.t_creditstate as t_state, cr.t_uscreditnumber as t_number, ob.t_objectname, ob.t_shortname " +
               "from dcredit_c_dbt cr, dlobject_dbt ob " +
               "where cr.t_crd_kind = ob.t_objectid " + 
               "and cr.t_creditnumber = " + ObjN + 
               " and cr.t_crd_kind = " + ObjID;
    elif (ObjID == LO_DUTY)
        QStr = "select dt.t_dutystate as t_state, (cr.T_USCREDITNUMBER || ' / ' || dt.t_usernumber) as t_number, ob.t_objectname, ob.t_shortname " +
               "from dduty_crd_dbt dt, dcredit_c_dbt cr, dlobject_dbt ob " +
               "where ob.t_objectid = " + ObjID +  
               " and dt.t_creditnumber_ref = cr.t_creditnumber " +
               "and dt.t_dutyid = " + ObjN;
    elif (ObjID == LO_CLAIM)
        QStr = "select 'О' as t_state, cl.t_claimnumber as t_number, ob.t_objectname, ob.t_shortname " +
               "from dclaim_dbt cl, dlobject_dbt ob " +
               "where ob.t_objectid = " + ObjID +  
               "and cl.t_claimid = " + ObjN;
    end;
    rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        SObject.ObjID = ObjID;
        SObject.ObjN = ObjN;
        SObject.Name      = SQL_NVL(RS.Value("t_ObjectName",     NULL, V_STRING),  V_STRING);
        SObject.ShortName = SQL_NVL(RS.Value("t_ShortName",      NULL, V_STRING),  V_STRING);
        SObject.Number    = SQL_NVL(RS.Value("t_number", NULL, V_STRING),  V_STRING);
        SObject.State     = SQL_NVL(RS.Value("t_state",    NULL, V_STRING),  V_STRING);
        if (SObject.State == "О")
            SObject.StateName = "";
        elif (SObject.State == "Ч")
            SObject.StateName = "Черновик";
        elif (SObject.State == "З")
            SObject.StateName = "Закрытый";
        elif (SObject.State == "Д")
            SObject.StateName = "Закрытый черновик";
        end;
    end;
    return SObject;
END;

MACRO lns_GetGenerationNumberObject(Parm1, Parm2, Parm3)
  var
      ObjID = 0, ObjN = 0,
      Object = NULL, recObject = NULL, 
      rs = NULL, qStr = "", retVal = "";

    record credit("credit_c.dbt");
    record duty("duty_crd.dbt");
    record claim("claim.dbt");
    record enscontr("enscontr.dbt");

    if (ValType(Parm3) == V_UNDEF)
        Parm3 = 0;
    end;
    record crdcommmeet("crdcommmeet.dbt");

    if (ValType(Parm2) == V_INTEGER)
        ObjID = Parm1;
        ObjN = Parm2;

        if (ObjID == LO_CREDIT)
            recObject = TRecHandler("credit_c");

            qStr = "select * from dcredit_c_dbt t where " +
                   "t.t_CreditNumber = " + String(ObjN) + " and t.t_Crd_Kind = " + String(ObjID); 

            rs = RsdRecordset(qStr);
            if (rs.MoveNext())
                Object = GetObjectFromRecordset("DCREDIT_C_DBT", rs);
            end;
        elif (ObjID == LO_DUTY)
            recObject = TRecHandler("duty_crd");

            qStr =  "select * from dduty_crd_dbt t where t.t_DutyID=" + String(ObjN); 

            rs = RsdRecordset(qStr);
            if (rs.MoveNext())
                Object = GetObjectFromRecordset("DDUTY_CRD_DBT", rs);
            end;
        elif (ObjID == LO_CLAIM)
            recObject = TRecHandler("claim");

            qStr = "SELECT  t_claimid,t_claimnumber,t_loanerid_ref,t_creditnumber_ref, " +
            "t_claimstate,t_givingdate,t_approvaldate,t_rejectiondate,t_lcreditsum, " +
            "t_lcreditcurcode,t_lselfsum,t_lselfcurcode,t_lcreditduration, " +
            "t_lctypeduration,t_lpayduration,t_lptypeduration,t_lpercrate, " +
            "t_lcreditpurpose,t_bcreditsum,t_bcreditcurcode,t_bpaysum, " +
            "t_bpaycurcode,t_bcreditduration,t_bctypeduration,t_bpayduration, " +
            "t_bptypeduration,t_bpercrate,t_credittypeid_ref,t_rejectmotive, " +
            "t_groupnum,t_sumobject,t_sumobjectcurcode,t_credcomid_ref, " +
            "t_credcomdecision,t_note,t_fncash,t_mrk,t_tunetype,t_kind_operation, " +
            "t_status,t_blockoper,t_comment,t_claimkind,t_daysbeforecredit,t_accountkarta, " +
            "t_beneficiary,t_typedebtor,t_tplantcrid,t_condid,t_reqcrdtypeid, " +
            "t_specific,t_territorialstruct,t_processname,t_stageid,t_canopenproduct,t_moduletype, t_state " + 
            "FROM dclaim_dbt WHERE t_claimid = " + ObjN;

            rs = RsdRecordset(qStr);
            if (rs.MoveNext())
                Object = GetObjectFromRecordset("DCLAIM_DBT", rs);
            end;
        elif (ObjID == LO_ENSCONTR)
            recObject = TRecHandler("enscontr");
            qStr = "SELECT * FROM denscontr_dbt t WHERE t.t_enscontractid="+ String(ObjN);
            rs = RsdRecordset(qStr);
            if (rs.MoveNext())
                Object = GetObjectFromRecordset("DENSCONTR_DBT", rs);
            end;
        elif (ObjID == LO_CRDCOMMMEET)
            recObject = TRecHandler("crdcommmeet");
            qStr = "select * from dcrdcommmeet_dbt t where t.t_meetid=" +  String(ObjN) ; 
            rs = RsdRecordset(qStr);
            if (rs.MoveNext())
                Object = GetObjectFromRecordset("DCRDCOMMMEET_DBT", rs);
            end;
        end;
    else
        ObjID = Parm1;
        Object = Parm2;

        if (ObjID == LO_CREDIT)
            recObject = TRecHandler("credit_c");
            ObjN = Object.CreditNumber;
        elif (ObjID == LO_DUTY)
            recObject = TRecHandler("duty_crd");
            ObjN = Object.DutyID;
        elif (ObjID == LO_CLAIM)
            recObject = TRecHandler("claim");
            ObjN = Object.ClaimID;
        elif (ObjID == LO_ENSCONTR)
            recObject = TRecHandler("enscontr");
            ObjN = Object.EnsContractID;
        elif (ObjID == LO_CRDCOMMMEET)
            recObject = TRecHandler("crdcommmeet");
            ObjN = Object.MeetID;
        end;
    end;

    CopyClassObjToRecord(Object, recObject);
    if (ObjID == LO_CREDIT)
        copy(credit, recObject);
        retVal = СгенерироватьНомерОбъекта(ObjID, credit);
    elif (ObjID == LO_DUTY)
        copy(duty, recObject);
        retVal = СгенерироватьНомерОбъекта(ObjID, duty);
    elif (ObjID == LO_CLAIM)
        copy(claim, recObject);
        retVal = СгенерироватьНомерОбъекта(ObjID, claim);
    elif (ObjID == LO_ENSCONTR)
        copy(enscontr, recObject);
        retVal = СгенерироватьНомерОбъекта(ObjID, enscontr, Parm3); // для ДО нужно передать ID КД.
    elif (ObjID == LO_CRDCOMMMEET)
        copy(crdcommmeet, recObject);
        retVal = СгенерироватьНомерОбъекта(ObjID, crdcommmeet);
    end;

    return retVal;
END;

MACRO lns_GetFilterConditionItem(_id, _items)
  var 
      retVal = NULL, idx = 0;

    if (ValType(_items) != V_UNDEF)
        while (idx < _items.Size)
            if (_id == _items[idx].Id)
                retVal = _items[idx];
            end;

            if (retVal != NULL) break; end;

            idx = idx + 1;
        end;
    end;

    return retVal;
END;

// открывались ли по договору СО.
MACRO lns_CrdHasDuty(ObjN) 
    var DutyCount = 0;
    DutyCount = LnSelectValue("SELECT count(0) FROM DDUTY_CRD_DBT WHERE t_creditnumber_ref = " + ObjN, V_INTEGER, 0);
    if (DutyCount > 0)
        return true;
    else
        return false;
    end;
END;

// Определение модуля заявки. 1 - кредитование, 2 - веб кредитование. LNS_MT_CREDIT и LNS_MT_WEBCREDIT соответственно
MACRO GetClaimType(ClaimID)
    return LnSelectValue("SELECT t_moduletype FROM dclaim_dbt WHERE t_claimid = " + ClaimID, V_INTEGER, 0);
END;

// проверка на существование хранимки.
MACRO ws_lns_ExistStoredProc(procname: string) 
    var qStr = "select count(1) from user_procedures " + 
               "where object_type = 'PACKAGE' "
               "and upper(object_name||'.'||procedure_name)=upper('" + procname + "')",
        stat = LnSelectValue(qStr , V_INTEGER, 0);
        if (stat > 0)
            return true;
        else
            return false;
	end;
END;

// Получение LnsValue из RSXMLRpc строки
MACRO GetLnsObjectFromXML(xmlstring)
    var RslLnsObj = null;
    xmlstring = "<methodResponse xmlns=\"http://www.softlab.ru/xml-rpc/schema\" " + 
                "xmlns:n0=\"http://www.softlab.ru/xml-rpc/schema\" " +
                "n0:reqId=\"{D6237739-84A6-481B-A23D-0F3E6B5C4B83}\" n0:logicalId=\"\"><params><param>" +
                xmlstring + "</param></params></methodResponse>";
    ConvertToRsl(xmlstring, RslLnsObj);
    return RslLnsObj;
END;

// Сжимает строку, возвращает base64 строку
MACRO LnsCompressString(UncompressedString: string)
    var UncompressedData = RslBinaryData(UncompressedString);
    var CompressedData = RslBinaryData();
    CompressBinaryData(UncompressedData, CompressedData);
    return CompressedData.asBase64();
END;

// Распаковывает строку из сжатого base64
MACRO LnsUnCompressString(CompressedString: string)
    var CompressedData = RslBinaryData(CompressedString, true);
    var UncompressedData = RslBinaryData();
    UnCompressBinaryData(CompressedData, UncompressedData);
    return UncompressedData.asASCIIZ();
END;