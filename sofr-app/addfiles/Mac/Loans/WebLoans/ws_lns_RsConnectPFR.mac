/*
$Name: ws_lns_RsConnectPFR.mac
$Module: WebLoans
$Description: Получение выписки из ПФР через RsConnect
$Created: 28.07.2018
*/
IMPORT wf_commonUtils, RsbDataSet, attached_obj_scroll, mfo_lib, LnsRemoteCall;

/*
После реализации СМЭВ3 в коннект, вынести глобализмы в справочник Взаимодействие с госорганиами.
Проверить в коннекте форматы данных. Актуализировать здесь.
*/


PRIVATE CONST RemoteURL = "http://localhost:8080/RsConnect/ws/rsconnect?wsdl";
PRIVATE CONST RemoteStatementMacro = "GU_statement";
PRIVATE CONST RemoteStatementFunc = "Statement";
PRIVATE CONST RemoteStatusFunc = "GetStatus";
PRIVATE CONST RemoteStatementDocFunc = "GetDocument";

/*
Объект RSC_GetDocumentReturn - возвращается функцией GetDocument из RsConnect
Архив с файлами передается в том же виде что и приходит от ПФР - архив закодированный в base64. В архиве все файлы, которые передает госорган. По мимо pdf, еще и XML.
Если данные не вернулись, то причина указана в свойстве ResultText
*/
CLASS RSC_GetDocumentReturn
   VAR ReqID       : string = ""; // Уникальный идентификатор запроса, на который сформирован ответ 
   VAR SenderID    : string = ""; // Идентификатор (код) Абонента, кому адресован ответ
   VAR StatementDoc: string = ""; // Ответ от органа власти размещен внутри архива, закодированного в Base64
   VAR ResultText  : string = ""; // Результат обработки запроса. Текст
END;

CLASS FioClass
    VAR LastName: String = ""; // Фамилия
    VAR FirstName: String = ""; // Имя
    VAR Patronymic: String = ""; // Отчество
END;

CLASS PassportClass
    VAR Series: String = "";
    VAR Number: String = "";
    VAR IssueDate: Date = Date(0,0,0);
END;

CLASS PFRRequestData
    VAR ReqID    : string = "";
    VAR SenderID : string = "";
    VAR ReqType  : integer = 0;
    VAR DocID    : string = "";
    VAR ClientID : string = "";
    VAR FIO       = FioClass();
    VAR SNILS    : string = "";
    VAR BirthDate: Date = Date(0,0,0);
    VAR Passport  = PassportClass();
    VAR Phone    : string = "";
    VAR TimeZona : integer = 0;
END;

CLASS PFRStatusReqClass()
    VAR ReqID: string = "";
    VAR SenderID: string = "";
    VAR ObjectID: string = "";
    VAR InitReqID: string = "";
END;

// добавляет прикрепленный документ к заявке.
MACRO AttachClaimObject(ClaimID, ObjectType, FileName, ObjectBase64Str, Comment)
    var result = WebCore_InsertImgData(967, ClaimID, ObjectType, ObjectBase64Str, "", FileName, Comment, null, 0);
    return result;
END;

// отправляет в RsConnect запрос на выписку из ПФР
// Возвращает ID запроса в RsConnect, при помощи которого можно потом получить из коннекта документы выписки
MACRO AskPFRClientInfo(ClaimID)
    CaptureOutput;
    [
        SELECT prsn.T_Name1, 
               prsn.T_Name2, 
               prsn.T_Name3, 
               prsn.t_born, 
               idc.t_paperseries, 
               idc.t_papernumber, 
               idc.t_paperissueddate,
               objc.t_code as t_snils, 
               con.t_value as t_phone 
        FROM dclaim_dbt cl
        JOIN dpersn_dbt prsn ON prsn.t_personid = cl.t_loanerid_ref
        LEFT JOIN dpersnidc_dbt idc ON idc.t_personid = cl.t_loanerid_ref
                                    AND idc.t_paperkind = 0
        LEFT JOIN dobjcode_dbt objc ON objc.t_objectid = cl.t_loanerid_ref
                                    AND objc.t_objecttype = 3 
                                    AND objc.t_codekind = 63
        LEFT JOIN dcontact_dbt con  ON con.t_partyid = cl.t_loanerid_ref 
                                    AND con.t_addresstype = 2 
                                    AND con.t_contacttype = 1 
        WHERE cl.t_claimid = ?
    ];
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    if (rs.MoveNext) 
        var ReqObj = PFRRequestData();

        ReqObj.ReqID = LnsGetGuid();
        ReqObj.SenderID= "RSBankV6";
        ReqObj.ReqType = 1;
        ReqObj.DocID = String(ClaimID);
        ReqObj.ClientID = "1";
        ReqObj.FIO.LastName = rs.Name1;
        ReqObj.FIO.FirstName = rs.Name2;
        ReqObj.FIO.Patronymic = rs.Name3;
        ReqObj.SNILS = rs.snils;
        ReqObj.BirthDate = Date(rs.Born);
        ReqObj.Passport.Series = rs.PaperSeries; 
        ReqObj.Passport.Number = rs.PaperNumber;
        ReqObj.Passport.IssueDate = Date(rs.PaperIssuedDate); 
        ReqObj.Phone = rs.Phone;
        ReqObj.TimeZona = 0;

        var response = LnsRemoteMacroCall(RemoteURL, 10000, RemoteStatementMacro, RemoteStatementFunc, ReqObj);
    else
        RunError("Клиент не найден");
    end;
    return ReqObj.ReqID;
END;

// Спрашивает у Коннекта, а не пришла ли выписка. Если выписка пришла, добавляет её к прикрепеленным документам по заявке.
MACRO SavePFRClientInfo(ClaimID, ReqID)
    var GetStatusReqParm = PFRStatusReqClass();
    GetStatusReqParm.ReqID = LnsGetGuid();
    GetStatusReqParm.SenderID = "RSBankV6";
    GetStatusReqParm.ObjectID = NULL;
    GetStatusReqParm.InitReqID = ReqID;
    var Resp = LnsRemoteMacroCall(RemoteURL, 10000, RemoteStatementMacro, RemoteStatusFunc, GetStatusReqParm);
    if ((Resp.QueryResultStatus != 16) AND (Resp.QueryResultStatus != 12))
        RunError("Невозможно получить выписку: " + Resp.QueryResultText)
    else
        var response = LnsRemoteMacroCall(RemoteURL, 10000, RemoteStatementMacro, RemoteStatementDocFunc, GetStatusReqParm); // туь отдаем в качестве параметра GetStatusReqParm
        if (response == null)
            RunError("Ошибка получения документа");
        elif (response.StatementDoc == NULL)
            RunError(response.ResultText);
        else
            var AttachResult = AttachClaimObject(ClaimID, 107, "PFR_DOC.zip", response.StatementDoc, "Выписка из ПФР");
            if (AttachResult.ErrorMessages.Size > 0)
                RunError(AttachResult.ErrorMessages[0]);
            end;
        end;
    end;
END;