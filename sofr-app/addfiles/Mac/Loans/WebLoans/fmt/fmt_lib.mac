/*                                                                                                                                                                                                      
$Name: fmt_lib.mac                                                                                                                                                                                
$Module: Кредитование                                                                                                                                                                                   
$Description: Вспомогательные функции                                                                                                                                                           
*/    
IMPORT RSD, RCW;

PRIVATE CONST V_SPECVAL = 26;

private macro SQL_ConvTypeStr(val)
    if (ValType(val) == V_SPECVAL)
        return "";
    end;

    if (ValType(val) == V_UNDEF ) return "";
    elif (ValType(val) == V_STRING )
        if ((StrLen(val) == 1) and (CodeFor(val) == 1)) 
            val = String("");
        end;
    end;

    return val;
END;

macro CopyRecordsetToClassObj(rs, cls)
    var iprop = 0;
    var ifld = 0;
    var nfld = rs.fldCount;
    var val;
    var savedNullConv;

    if(IsEqClass("RsdRecordset", rs))
        savedNullConv = rs.Command.NullConversion;
        rs.Command.NullConversion = true;
    end;

    while(ifld < nfld)
        iprop = GenPropID(cls, substr(rs.fld(ifld).name, 3));
        if(iprop != -1)
            val = rs.value(ifld);
            if(ValType(cls(iprop)) == V_STRING)
                cls(iprop) = SQL_ConvTypeStr(val);
            elif(val != null)
                cls(iprop) = rs.value(ifld);
            end;
        end;
        ifld = ifld + 1;
    end;

    if(IsEqClass("RsdRecordset", rs))
        rs.Command.NullConversion = savedNullConv;
    end;
end;

macro GetObjectFromRecordset(_typeObj:string, _recordSet)
    var obj = NULL;
    var idx = 0;
    var numFld = 0;

    if (
	 (ValType(_recordSet) != V_GENOBJ) or
        (not IsEqClass("RsdRecordset", _recordSet))
       )
	 return NULL;
    end;

    InstLoadModule(_typeObj+".mac");
    obj = GenObject(_typeObj); 
    CopyRecordsetToClassObj(_recordSet, obj);

    return obj;
OnError()
    return NULL;
end;

macro CopyClassObjToRecord(cls, rec)
    var iprop = "";
    var ifld = 0;
    var arr = GetObjProps(cls);
    var nfld = arr.size;//fldNumber(rec);
    var r = NULL;

    if (IsEqClass("TRecHandler", rec))
        r = rec.rec;
    else
        r = rec;
    end;

    while(ifld < nfld)
        iprop = FldIndex(rec, arr[ifld]);
        if (iprop != -1)
            r(iprop) = cls(GenPropID(cls, arr[ifld]));
        end;
        ifld = ifld + 1;
    end;
end; 
