/*
$Name: ws_lns_Log.mac
$Module: WebLoans
$Description: Сервис журнала неданих действий
*/
IMPORT SbCrdInter, RsbDataSet, globals;
IMPORT "ws_actionlog.mac";

PRIVATE MACRO FillLogParm(ObjID, ObjN, OperationID, ClientID)
  var str = "", rs = NULL,
      retVal = ActionLogItem();

    retVal.Oper = {oper};
    retVal.ObjectType = 100;

    rs = TRsbDataSet("select * from dlobject_dbt where t_ObjectID = " + String(ObjID));
    if (rs.MoveNext())
        retVal.TypeName = rs.ObjectName;
    else
        retVal.TypeName = "Объект RS-Loans";
    end;

    if (ObjID == LO_CREDIT)
        rs = TRsbDataSet("select t_UsCreditNumber, tt.t_PartyID, tt.t_ShortName from dcredit_c_dbt t, dparty_dbt tt where tt.t_PartyID = t.t_ClientID_Ref and t.t_crd_kind = " + String(ObjID) + " and t.t_CreditNumber = " + String(ObjN));
        if (rs.MoveNext())
            retVal.ObjectName = "№" + rs.UsCreditNumber + ", клиент: " + rs.ShortName;
            if (ClientID == 0)
                ClientID = rs.PartyID;
            end;
        end;
    elif (ObjID == LO_DUTY)
        rs = TRsbDataSet("select tt.t_UserNumber, ttt.t_PartyID, ttt.t_ShortName from dcredit_c_dbt t, dduty_crd_dbt tt, dparty_dbt ttt where ttt.t_PartyID = t.t_ClientID_Ref and t.t_CreditNumber = tt.t_CreditNumber_Ref and tt.t_DutyID = " + String(ObjN));
        if (rs.MoveNext())
            retVal.ObjectName = "№" + rs.UserNumber + ", клиент: " + rs.ShortName;
            if (ClientID == 0)
                ClientID = rs.PartyID;
            end;
        end;
    elif (ObjID == LO_CLAIM)
        rs = TRsbDataSet("select t.t_claimnumber, tt.t_PartyID, tt.t_ShortName from dclaim_dbt t, dparty_dbt tt where tt.t_PartyID = t.t_LoanerID_Ref AND t.t_ClaimID = " + String(ObjN));
        if (rs.MoveNext())
            retVal.ObjectName = "№" + rs.ClaimNumber + ", клиент: " + rs.ShortName;
            if (ClientID == 0)
                ClientID = rs.PartyID;
            end;
        end;
    else
        retVal.ObjectName = "Объект номер " + ObjN;
    end;

    retVal.ObjectID = ObjID +"|"+ ObjN +"|" + ClientID + "|"+ OperationID;
    retVal.Action = 0;
    retVal.Time = DtTm(date(), time());
    retVal.IsFix = false;

    return retVal;
END;

MACRO lns_LogNewObject(ObjID, ObjN, ClientID)
  VAR
      parm = FillLogParm(ObjID, ObjN, 0, ClientID);

    parm.Action = 0;
    WriteActionLog(parm);

    return 0;
END;

MACRO lns_LogUpdateObject(ObjID, ObjN, ClientID)
  VAR
      parm = FillLogParm(ObjID, ObjN, 0, ClientID);

    parm.Action = 1;
    WriteActionLog(parm);

    return 0;
END;
