/*
$Name: ws_lns_CreditEns.mac
$Module: WebLoans
$Description: Сервис для работы с обеспечениями по кредитному договору
*/

IMPORT fmt_lib, ws_validation, RsbDataSet, RSD, Total, likepy; //, ws_lns_CrdContract;
IMPORT DLCLAIMENSCONTR_DBT, DLCLAIMENSOBJ_DBT, DENSCONTR_DBT, DENSOBJ_DBT, DECN_EOB_DBT;

// типа "Класс исключения" для обеспечения 
CLASS CEnsError
    var ErrCode = 0;
    var ErrMessage = "";
END;


// глобализмы - для передачи в транзакцию.
private var InsEnsContrGlobal_EnsContr;
private var InsEnsContrGlobal_CreditNumber: integer;
private var InsEnsContrGlobal_OpDate: Date;
private var InsEnsContrGlobal_Err;
private var InsEnsContrGlobal_EnsContractID: integer;

MACRO Lns_InsEnsContrTrn
    record ensRec ("enscontr.dbt");
    var EnsError = null; // для хранения сообщения об ошибке.
    var crdID = InsEnsContrGlobal_CreditNumber;
    var OpDate = InsEnsContrGlobal_OpDate;
    var opID = 0;
    var ensID = 0;
    CopyClassObjToRecord(InsEnsContrGlobal_EnsContr, ensRec);
    // Операция  открытия договора обеспечения
    opID = MakeOperation(crdID, LO_ENSCONTR, CF_INSERT_ENS, int(ensRec.ENSCONTRACTCUR), OpDate, ensRec, 1, 0);
    if (opID == 0)
        RunError("Ошибка в операции открытия договора обеспечения: " + GetMO_LoansError(), 18589);
    end;
    // Определим ID созданного договора 
    ensRec.ENSCONTRACTID = LnSelectValue("SELECT t_objectid_ref FROM dcrd_op_dbt WHERE t_credoperid = " + opID, V_INTEGER);
    ensID = ensRec.ENSCONTRACTID;
    InsEnsContrGlobal_EnsContractID = ensID;

    // Операция открытия счетов с помощью категорий учета по договору
    opID = MakeOperation(ensID, LO_ENSCONTR, OP_ACC, 0, OpDate, crdID, 1);
    if (opID == 0)
        RunError("Ошибка в операции открытия счетов по договору обеспечения с помощью категорий учета: " + GetMO_LoansError(), 18579);
    end;
        
    // Операция принятия обеспечения на внебаланс
    opID = MakeOperation(crdID, 0, CF_ENS, int(ensRec.ENSCONTRACTCUR), OpDate, ensID, "", "", ensRec.ENSCONTRACTSUM);
    if (opID == 0)
        RunError("Ошибка в операции принятия обеспечения на внебаланс: " + GetMO_LoansError(), 18580);
    end;

    return true;

    OnError(err)
        EnsError = CEnsError();
        EnsError.ErrMessage = err.Message;
        EnsError.ErrCode = err.Code;
        InsEnsContrGlobal_Err = EnsError;
        return false;
END;


MACRO Lns_InsertEnsContr(CreditNumber, OpDate, EnsContr)
    var ResponseBuilder = WebResponseBuilder();
    InsEnsContrGlobal_CreditNumber = CreditNumber;
    InsEnsContrGlobal_OpDate = OpDate;
    InsEnsContrGlobal_EnsContr = EnsContr;
    InsEnsContrGlobal_Err = null;
    InsEnsContrGlobal_EnsContractID = 0;
    
    if(not RtTrn("Lns_InsEnsContrTrn"))
         RunError(InsEnsContrGlobal_Err.ErrMessage, InsEnsContrGlobal_Err.ErrMessage);
    end;
    
    ResponseBuilder.SetUserObject(InsEnsContrGlobal_EnsContractID);
    
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, IfThenElse(err.err != null, err.err, err.Message) );
        return ResponseBuilder.Result;
END;


// добавление объекта обеспечения.
// EnsContrID - идентификатор ДО. 
// EnsObj и ECN_EOB - объекты классов DLCLAIMENSOBJ_DBT и DECN_EOB_DBT соответственно
MACRO Lns_InsertEnsObj(EnsContrID, EnsOBJ, ECN_EOB)
    record ensObjRec ("ensobj.dbt");
    record Ecn_EobRec ("ecn_eob.dbt");
    var ResponseBuilder = WebResponseBuilder();
    CopyClassObjToRecord(EnsOBJ, ensObjRec);
    CopyClassObjToRecord(ECN_EOB, Ecn_EobRec);
    var EnsObjID = EnsObjTrn(EnsContrID, ensObjRec, Ecn_EobRec);
    if (EnsObjID == 0) // ошибка
        RunError(GetMO_LoansError());
    else
        ResponseBuilder.SetUserObject(EnsObjID);        
    end;
    
    return ResponseBuilder.Result;

    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO GetEnsSysTypeByTypeEns(TypeEnsID)
    return LnSelectValue("SELECT T_SYSTYPEENSID_REF FROM DTYPE_ENS_DBT WHERE T_TYPEENSID=" + TypeEnsID, V_INTEGER, 0);
END;

