/*
$Name: ws_lns_GraphKind.mac
$Module: WebLoans
$Description: Источник данных для листалки "виды графиков"
*/
IMPORT mfo_lib, SbCrdInter, RsbDataSet, globals;

PRIVATE CONST GRAPHCOMPLETEKIND = -1; // вид графика - сводный.
PRIVATE CONST GRAPHLISTKIND = -2; // вид графика - сводный.

CLASS CLnsGraphKindItem()
    VAR ID           : integer = 0;  // Идентификатор вида графика для объекта
    VAR GraphKindID  : integer = 0;  // Идентификатор вида графика из справочника видов графиков
    VAR GraphKindName: string  = ""; // Название вида графика
    VAR Children = TArray();         // Дочерние элементы
END;

PRIVATE MACRO GetCompleteGraphItem() // добавляем в листалку сводный график
    var result = CLnsGraphKindItem();
    result.ID            = GRAPHCOMPLETEKIND;
    result.GraphKindID   = 0;
    result.GraphKindName = "Сводный график"; 
    result.Children = TArray();
    return result;
END;

PRIVATE MACRO GetGraphListItem() // добавляем в листалку список графиков
    var result = CLnsGraphKindItem();
    result.ID            = GRAPHLISTKIND;
    result.GraphKindID   = 0;
    result.GraphKindName = "Список графиков"; 
    result.Children = TArray();
    return result;
END;

MACRO Lns_GetGraphKindList(ObjID, ObjN)
    var result = TArray();
    var graphitem = null;
    
    result[result.size] = GetCompleteGraphItem(); // добавляем в листалку сводный график

    result[result.size] = GetGraphListItem(); // добавляем в листалку список графиков

    CaptureOutput;
    [
        SELECT gtp.t_id,
               gtp.t_parentid,
               gk.t_id as t_graphkindid,
               gk.t_Name
        FROM dgraphtreeparm_dbt gtp
        JOIN dgraphkind_dbt gk ON gk.t_id = gtp.t_graphkind_id_ref
        JOIN dgraphobject_dbt go ON go.t_id = gtp.t_graphobject_id_ref
        WHERE go.t_objectid=# AND go.t_objectnumber=#
        ORDER BY gtp.t_parentid, gtp.t_id
    ](string(ObjID), string(ObjN));
    var QStr = StopCaptureOutput;
    var rs = TRsbDataSet(QStr);
    while (rs.MoveNext)
        graphitem = CLnsGraphKindItem();
        graphitem.ID            = rs.ID;
        graphitem.GraphKindID   = rs.GraphKindID;
        graphitem.GraphKindName = rs.Name;
        graphitem.Children = TArray();
        if (rs.parentid == 0) // если родителя нет, то добавляем на верхний уровень
            result[result.size] = graphitem;
        else
            for (var item, result) // ищем родителя на верхнем уровне
                if (item.id == rs.parentid) // нашли родителя
                    item.Children[item.Children.size] = graphitem; // добавляем в список детей
                end;
            end;
        end;
    end;
    return result;
END;

MACRO Lns_GetGraphKindByID(GraphKindID)
    var result = CLnsGraphKindItem();
    if (GraphKindID == GRAPHCOMPLETEKIND) // если попросили сводный график
        result = GetCompleteGraphItem();
    elif (GraphKindID == GRAPHLISTKIND) // если попросили список графиков
        result = GetGraphListItem();
    else 
        CaptureOutput;
        [
            SELECT gtp.t_id,
                gk.t_id as t_graphkindid,
                gk.t_Name
            FROM dgraphtreeparm_dbt gtp
            JOIN dgraphkind_dbt gk ON gk.t_id = gtp.t_graphkind_id_ref
            WHERE gtp.t_id=#
        ](string(GraphKindID));
        var qStr = StopCaptureOutput;
        var rs = TRsbDataSet(QStr);
        if (rs.MoveNext)
            result.ID            = rs.ID;
            result.GraphKindID   = rs.GraphKindID;
            result.GraphKindName = rs.Name;
        end;
    end;
    return result;
END;
