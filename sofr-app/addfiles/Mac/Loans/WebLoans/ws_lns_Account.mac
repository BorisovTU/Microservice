/*
$Name: ws_lns_Account.mac
$Module: WebLoans
$Description: Макрос для работы со счетами по объекту
*/
IMPORT XmlRpcInter, SbCrdInter, RsbDataSet, BankInter;
IMPORT "fmt_lib.mac", "DIGSETACC_DBT.mac", "DIGCRDACC_DBT.mac";
IMPORT "total.mac", "ws_datafilter.mac", "ws_dprt.mac", "ws_lns_CrdContract.mac";
IMPORT "ws_loansoperation.mac";


PRIVATE CONST PRIV_DEL_LACCOUNT = 82; // Удалять зарезервированные счета
/*************************************************************************************************/
/*                               1. Открытые счета по объекту                                    */
/*************************************************************************************************/
/*                              1.1 Скроллинг открытых счетов                                    */
CLASS CLnsAccountItem()
  VAR AccountID     : integer = 0;
  VAR AccountNumber : CLnsMaskedAccount = CLnsMaskedAccount();
  VAR AccountCateg  : integer = 0;
  VAR AccountCategS : string  = "";
  VAR CurCode       : integer = 0;
  VAR Currency      : string  = "";
  VAR RegDate       : date    = date(0,0,0);
  VAR Chapter       : string  = "";
  VAR OpenDate      : date    = date(0,0,0);
  VAR Rest          : money   = $0;
  VAR State         : string  = "";
  VAR StateS        : string  = "";
  VAR ActType       : string  = "";
  VAR ActTypeS      : string  = "";
  VAR curcodeODB    : integer = 0;
  VAR currencyODB   : string  = "";
  VAR ODBAccount    : string  = "";
END;

PRIVATE MACRO GetAccountListSqlFilter(FilterConditionItems)
  VAR
      fp = FilterParser(FilterConditionItems),
      sql = "", item = NULL;

    fp.AddFieldCondition( 0, "al", "AccountNumber");
    fp.AddFieldCondition( 2, "al", "Rest");
    fp.AddFieldCondition( 3, "al", "CurCode");
    fp.AddFieldCondition( 4, "al", "StateN");
    fp.AddFieldCondition( 5, "al", "ActTypeN");
    fp.AddFieldCondition( 6, "al", "ODBAccount");
    fp.AddFieldCondition( 7, "al", "CurCodeODB");
    fp.AddFieldCondition( 8, "al", "OpenDate");

    sql = fp.GetFullSQLCondition();

    item = lns_GetFilterConditionItem(1, FilterConditionItems);
    if (item != NULL)
        sql = sql + " and " + GetFilterStringSQLCondition(item, "al.AccountCategS", true, false);
    end;

    return sql;
END;

MACRO lns_AccountList(ObjID: integer, ObjN: integer, FilterConditionItems)
  VAR result = TArray();
  VAR query = "SELECT al.* from ( " +
              " SELECT t.T_ACCID             AS AccountID, " +
              " t.T_ACCOUNTNUMBER_REF AS AccountNumber,  " +
              " t.T_CREDITACCOUNTTYPE AS AccountCateg, " +
              " t.T_URGENCY AS urgency, " + 
              " SB_ACC.T_NAMEACCOUNT  AS AccountCategS, " +
              " t.t_curcode           AS CurCode, " +
              " (SELECT T_CCY FROM DFININSTR_DBT WHERE T_FIID = t.t_curcode) AS Currency, " +
              " t.t_regdate           AS RegDate,  " +
              " t.t_chapter           AS Chapter,  " +
              " sb_acc.t_OpenDate     AS OpenDate, " +
              " LoansKernel.GetAccRest(t.T_ACCOUNTNUMBER_REF, " +
                                     " t.t_curcode, " +
                                     " ?, " +
                                     " sb_acc.T_OPERDPRT) AS Rest, " +
              " sb_acc.t_accountstate     AS State, " +
              
              " CASE sb_acc.t_accountstate  " +
                " WHEN 'О' THEN 0 " +
                " WHEN 'З' THEN 1 " +
                " WHEN 'Р' THEN 2 " +
                " ELSE 3 " +
              " END                       AS StateN, " +
              
              " CASE sb_acc.t_accountstate  " +
                " WHEN 'О' THEN 'Открыт' " +
                " WHEN 'З' THEN 'Закрыт' " +
                " WHEN 'Р' THEN 'Зарезервирован' " +
                " ELSE 'Не открыт' " +
              " END                       AS StateS, " +
              " sb_acc.t_accounttype      AS ActType, " +
              
              " CASE sb_acc.t_accounttype " +
                 " WHEN 'А' THEN 0 " +
                 " WHEN 'П' THEN 1 " +
                 " ELSE 2 " +
              " END                       AS ActTypeN, " +
              
              " CASE sb_acc.t_accounttype " +
                 " WHEN 'А' THEN 'Активный' " +
                 " WHEN 'П' THEN 'Пассивный' " +
                 " ELSE 'Активно-пассивный' " +
              " END                       AS ActTypeS, " +
              " sb_acc.t_curcodeodb       AS CurCodeODB, " +
              " (SELECT T_CCY FROM DFININSTR_DBT WHERE T_FIID = sb_acc.t_curcodeodb) AS currencyODB,     " +
              " sb_acc.t_odbaccount       AS ODBAccount " +
       " FROM dsb_acc_dbt sb_acc, dacc_crd_dbt t WHERE  t.t_objectid = ? AND t.t_objectnumber = ? " +
       " AND sb_acc.t_CurCode=t.t_CurCode AND sb_acc.t_AccountNumber=t.t_AccountNumber_Ref " +
       ") al ";
    if (FilterConditionItems != null)
        var sqlFilterCondition = GetAccountListSqlFilter(filterConditionItems);
        if(sqlFilterCondition != "")
            query = query + "WHERE" + sqlFilterCondition;
        end;
    end;
    query = query + " ORDER BY al.CurCode, al.AccountCateg, al.urgency ";
    VAR RS = CRSDCommand(query,
                        "p1", {curdate},
                        "p2", ObjID,
                        "p3", ObjN,
                         V_GENOBJ);

    WHILE (RS.MoveNext)
        var item = CLnsAccountItem();

        item.AccountID     = SQL_NVL(RS.Value("AccountID",     NULL, V_INTEGER), V_INTEGER);
        item.AccountNumber.Account = SQL_NVL(RS.Value("AccountNumber", NULL, V_STRING),  V_STRING);
        item.AccountNumber.Chapter = SQL_NVL(RS.Value("Chapter", NULL, V_STRING),  V_STRING);
        item.AccountCateg  = SQL_NVL(RS.Value("AccountCateg",  NULL, V_INTEGER), V_INTEGER);
        item.AccountCategS = SQL_NVL(RS.Value("AccountCategS", NULL, V_STRING),  V_STRING);
        item.CurCode       = SQL_NVL(RS.Value("CurCode",       NULL, V_INTEGER), V_INTEGER);
        item.Currency      = SQL_NVL(RS.Value("Currency",      NULL, V_STRING),  V_STRING);
        item.RegDate       = SQL_NVL(RS.Value("RegDate",       NULL, V_DATE),    V_DATE);
        item.Chapter       = SQL_NVL(RS.Value("Chapter",       NULL, V_STRING),  V_STRING);
        item.OpenDate      = SQL_NVL(RS.Value("OpenDate",      NULL, V_DATE),    V_DATE);
        item.Rest          = SQL_NVL(RS.Value("Rest",          NULL, V_MONEY),   V_MONEY);
        item.State         = SQL_NVL(RS.Value("State",         NULL, V_STRING),  V_STRING);
        item.StateS        = SQL_NVL(RS.Value("StateS",        NULL, V_STRING),  V_STRING);
        item.ActType       = SQL_NVL(RS.Value("ActType",       NULL, V_STRING),  V_STRING);
        item.ActTypeS      = SQL_NVL(RS.Value("ActTypeS",      NULL, V_STRING),  V_STRING);
        item.curcodeODB    = SQL_NVL(RS.Value("curcodeODB",    NULL, V_INTEGER), V_INTEGER);
        item.currencyODB   = SQL_NVL(RS.Value("currencyODB",   NULL, V_STRING),  V_STRING);
        item.ODBAccount    = SQL_NVL(RS.Value("ODBAccount",    NULL, V_STRING),  V_STRING);

        result.value(result.Size) = item;
    END;

    return result;
END;

/*************************************************************************************************/
/*                            2. Зарезервированные счета по объекту                              */
/*************************************************************************************************/
/*                          2.1 Скроллинг зарезервированных счетов                               */
CLASS CLnsResAccountItem()
  VAR AccountID     : integer = 0;
  VAR AccountNumber : CLnsMaskedAccount  = CLnsMaskedAccount();
  VAR AccountCateg  : integer = 0;
  VAR AccountCategS : string  = "";
  VAR CurCode       : integer = 0;
  VAR Currency      : string  = "";
  VAR Chapter       : string  = "";
  VAR OpenDate      : date    = date(0,0,0);
  VAR State         : string  = "";
  VAR StateS        : string  = "";
  VAR Urgency       : string  = "";
END;

PRIVATE MACRO GetResAccountListSqlFilter(FilterConditionItems)
  VAR
      fp = FilterParser( FilterConditionItems );
  
    fp.AddFieldCondition( 0, "al", "AccountCategS");
    fp.AddFieldCondition( 1, "al", "AccountNumber");
    fp.AddFieldCondition( 2, "al", "CurCode");
    fp.AddFieldCondition( 3, "al", "ChapterN");
    fp.AddFieldCondition( 4, "al", "StateN");
    fp.AddFieldCondition( 5, "al", "Urgency");
    fp.AddFieldCondition( 6, "al", "OpenDate");

    return fp.GetFullSQLCondition();
END;

MACRO lns_ResAccountList(ObjID: integer, ObjN: integer, FilterConditionItems)
  VAR result = TArray();
  VAR query = "SELECT al.* from ( " +
              " SELECT t.T_ACCID AS AccountID, " +
              " t.t_reserveaccount AS AccountNumber, " +
              " t.T_CREDITACCOUNTTYPE AS AccountCateg, " +
              " crd_type.T_NameTypeAccount   AS AccountCategS, " +
              " t.t_curcode           AS CurCode, " +
              " (SELECT T_CCY FROM DFININSTR_DBT WHERE T_FIID = t.t_curcode) AS Currency, " +
              " t.t_chapter           AS Chapter, " +
              
              " CASE t.t_chapter  " +
                " WHEN 'А' THEN 1 " +
                " WHEN 'В' THEN 3 " +
                " ELSE 0 " +
              " END                       AS ChapterN, " +
              
              " T.T_DATEOPOPEN   AS OpenDate, " +
              " sb_acc.t_accountstate     AS State, " +
              
              " CASE sb_acc.t_accountstate " +
              "   WHEN 'О' THEN 0 " +
              "   WHEN 'З' THEN 1 " +
              "   WHEN 'Р' THEN 2 " +
              " ELSE 3 " +
              " END AS StateN, " +
              
              " CASE sb_acc.t_accountstate " +
              "   WHEN 'О' THEN 'Открыт' " +
              "   WHEN 'З' THEN 'Закрыт' " +
              "   WHEN 'Р' THEN 'Зарезервирован' " +
              " ELSE 'Не открыт' " +
              " END AS StateS, " +
              " nvl(period.t_Name, ' ') as Urgency" +
              " FROM dsb_acc_dbt sb_acc, dacc_crd_dbt t, dcrd_op_dbt crd_op, dtaccred_dbt crd_type, dmcperiod_dbt period WHERE  (t.t_objectid = ? AND t.t_objectnumber = ?) " +
              " AND (t.t_ReserveCredOperID_Ref <> 0 AND t.t_ReserveCredOperID_Ref = crd_op.t_CredOperID " +
              " AND CRD_TYPE.T_TYPEACCCRED = T.T_CREDITACCOUNTTYPE " +
              " AND period.t_id(+) = t.T_URGENCY" + 
              " AND crd_op.t_FnCash = 1 AND sb_acc.t_CurCode(+) = t.t_CurCode AND sb_acc.t_AccountNumber(+) = t.t_ReserveAccount) " +
              ") al ";
    if (FilterConditionItems != null)
        var sqlFilterCondition = GetResAccountListSqlFilter(filterConditionItems);
        if(sqlFilterCondition != "")
            query = query + "WHERE" + sqlFilterCondition;
        end;
    end;
    query = query + " ORDER BY al.CurCode, al.AccountCateg";
    VAR RS = CRSDCommand(query,
                        "p1", ObjID,
                        "p2", ObjN,
                         V_GENOBJ);

    WHILE (RS.MoveNext)
        var item = CLnsResAccountItem();
      
        item.AccountID     = SQL_NVL(RS.Value("AccountID",     NULL, V_INTEGER), V_INTEGER);
        item.AccountNumber.Account = SQL_NVL(RS.Value("AccountNumber", NULL, V_STRING),  V_STRING);
        item.AccountNumber.Chapter = SQL_NVL(RS.Value("Chapter", NULL, V_STRING), V_STRING);
        item.AccountCateg  = SQL_NVL(RS.Value("AccountCateg",  NULL, V_INTEGER), V_INTEGER);
        item.AccountCategS = SQL_NVL(RS.Value("AccountCategS", NULL, V_STRING),  V_STRING);
        item.CurCode       = SQL_NVL(RS.Value("CurCode",       NULL, V_INTEGER), V_INTEGER);
        item.Currency      = SQL_NVL(RS.Value("Currency",      NULL, V_STRING),  V_STRING);
        item.Chapter       = SQL_NVL(RS.Value("Chapter",       NULL, V_STRING),  V_STRING);
        item.OpenDate      = SQL_NVL(RS.Value("OpenDate",      NULL, V_DATE),    V_DATE);
        item.State         = SQL_NVL(RS.Value("State",         NULL, V_STRING),  V_STRING);
        item.StateS        = SQL_NVL(RS.Value("StateS",        NULL, V_STRING),  V_STRING);
        item.Urgency       = SQL_NVL(RS.Value("Urgency",       NULL, V_STRING),  V_STRING);
      
        result.value(result.Size) = item;
    END;

    return result;
END;


MACRO lns_DeleteResAccount(ObjID, ObjN, AccIDList)
    var acc_crd = TBFile ("acc_crd.dbt", "rw", 0, "acc_crd.dbt", "loans.def");
    var result = TWsMessage();

    result.MsgID = 0;
    result.MsgText = "";

    if (CheckActionRestriction(PRIV_DEL_LACCOUNT))
        if ((AccIDList != NULL) and (AccIDList.Size > 0))
            for (var AccID, AccIDList)
                acc_crd.rec.accid = AccID;
                var stat = acc_crd.GetEQ();
                if (stat)
                    acc_crd.delete();
                else
                    result.MsgID = stat;
                    result.MsgText = "Ошибка удаления одного или нескольких счетов";
                end;
            end;
        end;
    else
        RunError("У вас нет прав на удаление зарезервированных счетов!");
    end;
    
    return result;

    OnError(err)
        result.MsgID = err.Code;
        result.MsgText = err.Message;
        return result;    
END;

/*************************************************************************************************/
/*                             3. Счета других подсистем по объекту                              */
/*************************************************************************************************/
/*                          3.1 Скроллинг счетов других подсистем                                */
CLASS CLnsSPIAccountItem()
  VAR SetTAccID        : integer = 0;
  VAR Name             : string  = "";
  VAR Account          : CLnsMaskedAccount  = CLnsMaskedAccount();
  VAR UsePriority      : integer = 0;
  VAR Rest             : money   = $0;
  VAR RecName          : string  = "";
END;

MACRO lns_SPIAccountList(ObjID: integer, ObjN: integer, FilterConditionItems)
  VAR result = TArray();
  VAR query = "SELECT t.t_settaccid, t.t_chapter, igtypacc.t_Name, t.t_account, igcrdacc.t_usepriority, t.t_recname " +
              " FROM digsetacc_dbt t, digcrdacc_dbt igcrdacc, digtypacc_dbt igtypacc " +
              " WHERE  (t.t_SETTACCID = igcrdacc.t_SETACCID " +
              " AND igcrdacc.t_TypeAccID = igtypacc.t_TypeAccID) " + 
              " AND (igcrdacc.t_CreditNumber = ? ) ORDER BY t.t_settaccid";
    VAR RS = CRSDCommand(query,
                        "p1", ObjN,
                        V_GENOBJ);

    WHILE (RS.MoveNext)
        var item = CLnsSPIAccountItem();
      
        item.SetTAccID        = SQL_NVL(RS.Value("t_SetTAccID",         NULL, V_INTEGER), V_INTEGER);
        item.Name             = SQL_NVL(RS.Value("t_Name",              NULL, V_STRING),  V_STRING);
        item.Account.Account  = SQL_NVL(RS.Value("t_Account",           NULL, V_STRING),  V_STRING);
        item.Account.Chapter  = SQL_NVL(RS.Value("t_Chapter",           NULL, V_INTEGER), V_INTEGER);
        item.UsePriority      = SQL_NVL(RS.Value("t_UsePriority",       NULL, V_INTEGER), V_INTEGER);
        item.Rest             = GetSPIAccountRest(ObjID, ObjN, item.SetTAccID);
        item.RecName          = SQL_NVL(RS.Value("t_RecName",           NULL, V_STRING),  V_STRING);
      
        result.value(result.Size) = item;
    END;

    return result;
END;

/*                              3.2 Получение параметров СПИ                                     */
CLASS CLnsSPIAccount()
  VAR SetAcc: DIGSETACC_DBT = null;
  VAR CrdAcc: DIGCRDACC_DBT = null;
  VAR Department: TWsDepartment = null;
  VAR Branch: TWsDepartment = null;
END;

MACRO lns_GetSPIAccount(ObjID: integer, ObjN: integer, AccID: integer)
  VAR qStr: string = "", rs = NULL;
  VAR SPIAcc: CLnsSPIAccount = CLnsSPIAccount();

    qStr = "SELECT * FROM DIGCRDACC_DBT WHERE T_CREDITNUMBER = " + String(ObjN) +
           " AND T_SETACCID = " + String(AccID);
    rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        SPIAcc.CrdAcc = GetObjectFromRecordset("DIGCRDACC_DBT", rs);
    end;
	
    qStr = "SELECT * FROM DIGSETACC_DBT WHERE T_SETTACCID = " + String(AccID);
    rs = RsdRecordset(qStr);
    if (rs.MoveNext())
        SPIAcc.SetAcc = GetObjectFromRecordset("DIGSETACC_DBT", rs);
    end;
	
    SPIAcc.Department = GetTWsDepartmentByCode(SPIAcc.SetAcc.operdprt);
    SPIAcc.Branch = GetTWsDepartmentByCode(SPIAcc.SetAcc.operdprtnode);

    return SPIAcc;
END;

/*                              3.3 Инициализация нового СПИ                                     */
MACRO lns_InitNewSPIAccount(ObjID: integer, ObjN: integer)
  VAR SPIAcc: CLnsSPIAccount = CLnsSPIAccount();
  VAR Credit = lns_GetCredit_c(ObjN);

    SPIAcc.SetAcc = DIGSETACC_DBT();
    SPIAcc.CrdAcc = DIGCRDACC_DBT();
  
    SPIAcc.SetAcc.settaccid    = 0;
    SPIAcc.SetAcc.fiid         = Credit.curcode;
    SPIAcc.SetAcc.bankid       = 0;
    SPIAcc.SetAcc.bankcodekind = 3;
    SPIAcc.SetAcc.codekind     = 16;
    SPIAcc.SetAcc.operdprt     = 0; 
    SPIAcc.SetAcc.chapter      = 1;
    SPIAcc.SetAcc.partyid      = Credit.clientid_ref;
    SPIAcc.SetAcc.bankcorrid       = 0;
    SPIAcc.SetAcc.bankcorrcodekind = 3;

    SPIAcc.CrdAcc.typeaccid    = 0;
    SPIAcc.CrdAcc.isinstancy   = "";
    SPIAcc.CrdAcc.creditnumber = ObjN;

    //SPIAcc.Department = GetTWsDepartmentByCode(SPIAcc.SetAcc.operdprt);

    return SPIAcc;
END;

/*                                  3.4 Вставка нового СПИ                                       */
MACRO lns_InsertSPIAccount(ObjID: integer, ObjN: integer, SPIAcc)
  RECORD SetAcc ("igsetacc.dbt");
  RECORD CrdAcc ("igcrdacc.dbt");
  var qStr: string = "", rs = NULL;
  var countStr: string = "", countRs = NULL, countRec: integer = 0;
  var stat = 0;
  var result = TWsMessage();

    result.MsgText = "";
    result.MsgID = 0;
    CopyClassObjToRecord(SPIAcc.CrdAcc, CrdAcc);
    CopyClassObjToRecord(SPIAcc.SetAcc, SetAcc);

    if (CrdAcc.UsePriority < 0)
        qStr = "SELECT NVL(MAX(t_UsePriority), 0) + 1 as t_CNT" +
              " FROM digcrdacc_dbt igcrdacc " +
              " WHERE  (igcrdacc.t_CreditNumber = ?) " +
              " AND (igcrdacc.t_TypeAccID = ? )";
        rs = CRSDCommand(qStr,
                         "p1", CrdAcc.CreditNumber,
                         "p2", CrdAcc.TypeAccID,
                         V_GENOBJ);
        RS.MoveNext();
        CrdAcc.UsePriority = SQL_NVL(RS.Value("t_CNT", NULL, V_INTEGER), V_INTEGER);
    end;

    stat = InsertSPIAccount(ObjID, ObjN, CrdAcc, SetAcc);

    if (stat)
        result.MsgID = GetMO_ErrorID();
        result.MsgText = GetMO_LoansError();
    end;

    return result;
END;

/*                              3.5 Обновление параметров СПИ                                    */
MACRO lns_UpdateSPIAccount(ObjID: integer, ObjN: integer, SPIAcc)
  RECORD SetAcc ("igsetacc.dbt");
  RECORD CrdAcc ("igcrdacc.dbt");
   var stat = 0;
   var result = TWsMessage();

    result.MsgText = "";
    result.MsgID = 0;
    CopyClassObjToRecord(SPIAcc.CrdAcc, CrdAcc);
    CopyClassObjToRecord(SPIAcc.SetAcc, SetAcc);
	
    stat = UpdateSPIAccount(ObjID, ObjN, CrdAcc, SetAcc);

    if (stat)
        result.MsgID = GetMO_ErrorID();
        result.MsgText = GetMO_LoansError();
    end;

    return result;
END;

/*                                     3.6 Удаление СПИ                                          */
MACRO lns_DeleteSPIAccount(ObjID: integer, ObjN: integer, AccID: integer)
  var stat = 0;
  var result = TWsMessage();
  
    result.MsgText = "";
    result.MsgID = 0;
	
    stat = DeleteSPIAccount(ObjID, ObjN, AccID);
	
    if (stat)
        result.MsgID = GetMO_ErrorID();
        result.MsgText = GetMO_LoansError();
    end;

    return result;
END;

/*                                     3.7 Получение приоритета СПИ                                          */
MACRO lns_GetSPIAccountPriority(ObjN: integer, TypeAccID: integer)
  var qStr: string = "", rs = NULL;
  var result: integer = 0;
    if (TypeAccID != 0)
            qStr = "SELECT NVL(MAX(t_UsePriority), 0) + 1 as t_CNT" +
                    " FROM digcrdacc_dbt igcrdacc " +
                    " WHERE  (igcrdacc.t_CreditNumber = ?) " +
                    " AND (igcrdacc.t_TypeAccID = ? )";
            rs = CRSDCommand(qStr,
                       "p1", ObjN,
                       "p2", TypeAccID,
                        V_GENOBJ);
            RS.MoveNext();
            result = SQL_NVL(RS.Value("t_CNT", NULL, V_INTEGER), V_INTEGER);
    end;
    return result;
END;


// является ли клиент - заемщиком по договору
PRIVATE MACRO IsDebtor(ObjID, ObjN, PartyID): bool
    var qStr = "SELECT * FROM DLOTHDEB_DBT " + 
               "WHERE t_objecttypeid_ref=? " + 
               "AND t_objectnumber_ref=? " + 
               "AND t_partyID_ref=? " +
               "AND t_isdeleted != 'X'";
    var cmd = CRSDCommand(qStr,
                          "p1", ObjID,
                          "p2", ObjN,
                          "p3", PartyID,
                          V_GENOBJ);
    if (cmd.MoveNext())
        return true;
    else
        return false;
    end;
END;

// Является ли клиент поручителем по одному из связанных ДО
PRIVATE MACRO IsGuarantorByEns(ObjID, ObjN, PartyID): bool
    var qStr = "SELECT * FROM DENSCONTR_DBT " + 
               "WHERE t_enscontractpersonid_ref=? " + 
               "AND t_enscontractid IN " + 
               "(SELECT t_enscontractid_ref FROM DENS_CRD_DBT " +
               "WHERE t_creditnumber_ref = ?)";
    var cmd = CRSDCommand(qStr,
                          "p1", PartyID,
                          "p2", ObjN,
                          V_GENOBJ);
    if (cmd.MoveNext())
        return true;
    else
        return false;
    end;
END;

// Является ли клиент поручителем
PRIVATE MACRO IsGuarantor(ObjID, ObjN, PartyID): bool
    var qStr = "SELECT ep.* FROM DENSPARTY_DBT ep "+ 
               "JOIN DENS_CRD_DBT ec ON ec.t_enscontractid_ref = ep.t_enscontractid " +
               "WHERE ec.t_creditnumber_ref=? " +
               "AND ep.t_partyid=?";
    var cmd = CRSDCommand(qStr,
                          "p1", ObjN,
                          "p2", PartyID,
                          V_GENOBJ);
    if (cmd.MoveNext())
        return true;
    else
        return false;
    end;           
END;

// проверка, является ли владелец счета - заемщиком или поручителем.
// Три функции проверки - это переписанный на RSL и разложенный по функциям метод
// bool CAccBnkScroling::Debtor() из файла cntbnkacc.cpp
MACRO lns_CheckDebtor(ObjID, ObjN, PartyID)
     return IsDebtor(ObjID, ObjN, PartyID) and 
            IsGuarantor(ObjID, ObjN, PartyID) and 
            IsGuarantorByEns(ObjID, ObjN, PartyID);
END;

