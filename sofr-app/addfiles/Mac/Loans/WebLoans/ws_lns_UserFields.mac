/*
$Name: ws_lns_UserFields.mac
$Module: WebLoans
$Description: Сервис источника данных для пользовательских полей
*/
IMPORT SbCrdInter, RsbDataSet;
IMPORT "total.mac", "ws_common.mac", globals;

CLASS CLnsUserFields()
    var name = string();
    var value = string();
    var id = 0;
    var parent = 0;
    var parentName = string();
    var children = TArray();
    var type = 0;
    var usFieldIDRef = 0;
END;

CLASS CLnsUFDir()
    var id = 0;
    var name = string();
END;

MACRO lns_getuflist(ObjID, ObjN)
    var
       result = TArray(),
       qStr = "", lastParent = "",
       rs = NULL, 
       item = NULL,
       UFType = 0;

    qStr = qStr + "SELECT lguf.t_usfieldgroupname AS groupname, luf.t_usfieldname AS FieldName ";
    qStr = qStr + ", lufv.t_usfieldvalue AS FieldValue, lufot.t_usfobjtypeid ufid, lufot.t_parentid AS parentid ";
    qStr = qStr + ", luf.t_usfieldtype AS UFType, luf.t_isdironly dironly, lufot.t_usfieldid_ref ";
    qStr = qStr + "FROM dlufield_dbt luf, dlufvalue_dbt lufv, dlufotype_dbt lufot, dlgroupuf_dbt lguf ";
    qStr = qStr + "WHERE lufot.t_usfieldid_ref = luf.t_usfieldid (+) ";
    qStr = qStr + "AND lufv.t_usfobjtypeid_ref (+) = lufot.t_usfobjtypeid ";
    qStr = qStr + "AND lufot.t_objectid_ref = " + ObjID + " AND lufot.t_objid_ref = " + ObjN;
    qStr = qStr + "AND lufot.t_usfieldgroupid_ref = lguf.t_usfieldgroupid (+)";
    qStr = qStr + "order by t_usfieldgroupid_ref, t_usfobjtypeid";


    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
       item = CLnsUserFields();
       if (VALTYPE(rs.FieldName) == V_UNDEF)
          item.name  = string(rs.GroupName);
          lastParent = rs.GroupName;
       else
          item.name = string(rs.FieldName);
          UFType = int(rs.UFType);
       end;

       if (VALTYPE(rs.FieldValue) == V_UNDEF)
          item.value = "";
       else
          item.value = string(rs.FieldValue);
       end;
       
       item.id = rs.ufid;
       item.parent = rs.parentid;
       
       if ((UFType >=0) and (UFType <=1)) //целые значения
          item.type = 0;
       elif ((UFType >= 2) and (UFType <=6)) //вещественные значения
          item.type = 2;
       elif ((UFType >= 7) and (UFType <= 12)) 
          item.type = UFType;
       elif (UFType == 25)
          item.type = 25;
       end;
       if (rs.dironly == "X") 
          item.type = 100;
       end;
       
       item.usFieldIDRef = rs.usFieldID_Ref;

       if (rs.parentid == 0)
          item.parentName = "";
          result[result.size] = item;
       else 
          item.parentName = lastParent;
          result[result.size-1].Children[result[result.size-1].Children.size] = item;
       end;
    end;

    return result;
END;

MACRO lns_getufdirlist(UFID)
    var
       result = TArray(),
       qStr = "",
       rs = NULL, 
       item = NULL;

    qStr = qStr + "SELECT T_ID, T_USFIELDVALUE ";
    qStr = qStr + "FROM dlufvdir_dbt ";
    qStr = qStr + "WHERE t_usfieldid_ref = " + UFID;
    qStr = qStr + "order by t_id";

    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
       item = CLnsUserFields();
       item.id = rs.id;
       item.value = rs.usfieldvalue;
       result[result.size] = item;
    end;

    return result;
END;

//Вынести в интер!
PRIVATE CONST
    USF_NOEDIT     = 3;

MACRO lns_saveufvalue(UFID, value)
    var qStr = "",
        rs = NULL,
        ResponseBuilder = WebResponseBuilder();
    
    qStr = "SELECT * FROM DLUSFFLAG_DBT fl, DLUFOTYPE_DBT tp WHERE fl.T_FLAG = 'X' AND fl.T_USFLAGID_REF = " + USF_NOEDIT + " ";
    qStr = qStr + " AND tp.T_USFOBJTYPEID = " + UFID  + " AND tp.T_OBJECTID_REF = fl.T_OBJECTID_REF AND tp.T_USFIELDID_REF = fl.T_USFIELDID_REF ";
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        ResponseBuilder.AddErrorEx( 0, MessageSeverity.RuntimeError, "Невозможно сохранить значение поля!" );
        return ResponseBuilder.Result;
    end;
    
    qStr = "SELECT * FROM DLUFVALUE_DBT WHERE T_USFOBJTYPEID_REF = " + UFID;
    
    rs = TRsbDataSet(qStr);
    
    if (rs.MoveNext())
        qStr = "UPDATE DLUFVALUE_DBT ";
        qStr = qStr + "SET T_USFIELDVALUE = '" + value + "' ";
        qStr = qStr + "WHERE T_USFOBJTYPEID_REF = " + UFID;
    else
        qStr = "INSERT INTO DLUFVALUE_DBT(T_USFOBJTYPEID_REF, T_USFIELDVALUE, T_USFIELDVALDATE, T_ISOPERATING) ";
        qStr = qStr + "VALUES("+UFID+", '"+value+"', TO_DATE('"+{curdate}+"', 'DD.MM.YYYY'), 'X')";
    end;

    LnSQLExec(qStr);

    ResponseBuilder.SetUserObject(0);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;