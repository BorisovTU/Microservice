/*
$Name: ws_lns_OperChangeRate.mac
$Module: WebLoans
$Description: Сервис для виджета операции "Изменение тарифов"
*/
IMPORT SbCrdInter, RsbDataSet, RSD, mfo_lib, Globals, ora_func, ws_LoansOperation;

CLASS CEditRateItem()
    VAR IsSelected    : bool     = false;       // выбран ли для редактирования в веб интерфейсе
    VAR IsEditable    : bool     = false;       // Можно ли редактировать?
    VAR TypeRateID    : integer  = 0;           // Идентификатор тарифа
    VAR TypeRateName  : string   = "";          // Название тарифа
    VAR RateDate      : DateTime = Date(0,0,0); // Дата тарифа
    VAR Calendar      : integer  = 0;           // Периода расчета (1 - дн, 2 - мес, 3 - кв, 4 - год)
    VAR ValueBase     : integer  = 0;           // База расчета (кол-во периодов)
    VAR DayInMon      : integer  = 0;           // Дней в месяце (если 0, берется кол-во календарных дней)
    VAR IsFormulaValue: string   = "";          // Является ли значение формулой
    VAR Value         : string   = "";          // Значение тарифа
    VAR UnitName      : string   = "";          // Единица измерения
END;

// 
PRIVATE MACRO GetEditRateItemFromRecordset(rs): CEditRateItem
    var result = CEditRateItem();
    result.TypeRateID     = rs.TypeRateID;
    result.TypeRateName   = rs.TypeRateName;
    result.RateDate       = {curdate};
    result.Calendar       = rs.Calendar;
    result.ValueBase      = rs.ValueBase;
    result.DayInMon       = rs.DayInMon;
    result.IsFormulaValue = rs.IsFormulaValue;
    // Дальше - всё самое интересное.
    // Определение значения, единицы измерения и возможности редактирования тарифа
    // В вебе пока что можно редактировать только простые тарифы.
    if (rs.IsConditional == "X")    // сложный тариф, нельзя редактировать
        result.value = "Сложный тариф";
        result.IsEditable = false;
    elif (rs.IsFloatingRate == "X") // плавающая ставка, нельзя редактировать
        result.value = "Плавающая ставка";
        result.IsEditable = false;
    elif (rs.IsFormulaValue == "X") // формула, ваще никак нельзя редактировать
        result.value = "Формула";
        result.IsEditable = false;
    else                            // остальное вроде можно редактировать
        if (rs.TypeTariff == TYPE_ABS) // абсолютное значение. Очевидно, деньги
            result.IsEditable = true;
            result.Value = rs.value;   // тут сумма
            result.UnitName = LnSelectValue("SELECT t_ccy FROM dfininstr_dbt WHERE t_fiid = " + rs.UnitValue, V_STRING, 0); // получаем валюту
        elif (rs.TypeTariff == TYPE_PERCSUM) // процент от суммы. Пока непонятно, что с ним делать: редактировать или нет
            result.IsEditable = true;
            result.Value = rs.Value;
            result.UnitName = "% от " + 
                              LnSelectValue("SELECT t_spvariablename FROM dspvariable_dbt WHERE t_spvariableid = " + rs.SpVariableID, V_STRING, 0);
        elif (rs.TypeTariff == TYPE_PERCPER) // процент за период
            result.IsEditable = true;
            result.Value = rs.Value;
            if (rs.FlagCalendNumDays == "X")
                if ((rs.ValueBase == 1) and (rs.Calendar == ALG_YEAR))       // база - год
                    result.UnitName = "% годовых";
                elif ((rs.ValueBase == 1) and (rs.Calendar ==  ALG_QUARTER)) // база - квартал
                    result.UnitName = "% в квартал";
                elif ((rs.ValueBase == 1) and (rs.Calendar ==  ALG_MON))     // база - месяц
                    result.UnitName = "% в месяц"; 
                elif ((rs.ValueBase == 1) and (rs.Calendar ==  ALG_DAY))     // база - день
                    result.UnitName = "% в день";
                else
                    var CalendarString = RunStoredFunc("LoansUserFunction.LinguisticAnalysis", V_STRING, NULL, rs.ValueBase, rs.Calendar);
                    result.UnitName = "% База : " + rs.ValueBase + " " + CalendarString;
                end;
            else
                var PeriodString = RunStoredFunc("LoansUserFunction.LinguisticAnalysis", V_STRING, NULL, rs.ValueBase, rs.Calendar);
                var MonString = RunStoredFunc("LoansUserFunction.LinguisticAnalysis", V_STRING, NULL, rs.DayInMon);
                result.UnitName = "% База : " + rs.ValueBase + " " + PeriodString + " В месяце: " + rs.DayInMon + " " + MonString;
            end;
        end;
    end;
    return result;
END;

// Функция получает объекты тарифов для изменения.
// Если значения тарифов не заданы, то берутся дефолтные значения с видов тарифов
MACRO lns_GetEditRateItemList(ObjID, ObjN)
    var result = TArray();
    CaptureOutput();
    [
        SELECT trftype.T_TYPERATEID,
               trftype.T_TYPERATENAME,
               NVL(trfval.T_CALENDAR,  trftype.T_CALENDAR) as T_CALENDAR,
               NVL(trfval.T_VALUEBASE, trftype.T_VALUEBASE) as T_VALUEBASE,
               NVL(trfval.T_DAYINMON,  trftype.T_DAYINMON) as T_DAYINMON,
               NVL(trfval.T_ISFORMULAVALUE, chr(0)) as T_ISFORMULAVALUE,
               NVL(trfval.T_VALUE, '0.00') as T_VALUE,
               NVL(trfval.T_ISCONDITIONAL, chr(0)) as T_ISCONDITIONAL,
               NVL(trfval.T_ISFLOATINGRATE, chr(0)) as T_ISFLOATINGRATE,
               NVL(trfval.T_TYPETARIFF, trftype.T_TYPE) as T_TYPETARIFF,
               NVL(trfval.T_UNITVALUE, DECODE(trftype.T_TYPE, 3, 0, -1)) as T_UNITVALUE,
               NVL(trfval.T_SPVARIABLEID, trftype.T_SPVARIABLEID) as T_SPVARIABLEID,
               NVL(trfval.T_FLAGCALENDNUMDAYS, trftype.T_FLAGCALENDNUMDAYS) as T_FLAGCALENDNUMDAYS
        FROM dlcusrate_dbt trfobj
        JOIN dlctypuse_dbt trftype ON trftype.T_TYPERATEID = TRFOBJ.T_TYPERATEID_REF
        LEFT JOIN DLCHISTRY_DBT trfhist ON trfhist.T_RATEID_REF = TRFOBJ.T_RATEID_REF
                                        AND trfhist.T_RATESTATE = 2
                                        AND trfhist.T_RATEDATE <= ?
        LEFT JOIN DTRFFVALUE_DBT trfval ON trfval.T_PARENTID = trfhist.T_ID
        WHERE trfobj.T_OBJECTID_REF = ?
          AND trfobj.T_CREDOBJID_REF = ?
        ORDER BY trftype.T_TYPERATEID
    ];
    var qStr = StopCaptureOutput;
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_ratedate",     RSDBP_IN, {curdate});
    cmd.AddParam("p_objecttypeid", RSDBP_IN, ObjID);
    cmd.AddParam("p_objectnumber", RSDBP_IN, ObjN);
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    while (rs.movenext)
        result[result.Size] = GetEditRateItemFromRecordset(rs);
    end;
    return result;
END;



// Операция изменения тарифов
// RateList - TArray из CEditRateItem
MACRO lns_ChangeRate(ObjID, ObjN, OpDate, RateList)
    var result = TWsMessage();
        result.MsgID = 0;
        result.MsgText = "Не выбраны тарифы для изменения!";
    var TariffArray = TArray();
    if ((RateList != null) and (RateList.Size > 0))
        // Заполняем массив значений для операции изменения тарифов
        for (var item, RateList)
            if (item.IsSelected)
                TariffArray[TariffArray.Size] = item.TypeRateID;
                TariffArray[TariffArray.Size] = item.RateDate;
                TariffArray[TariffArray.Size] = item.Calendar;
                TariffArray[TariffArray.Size] = item.ValueBase;
                TariffArray[TariffArray.Size] = item.DayInMon;
                TariffArray[TariffArray.Size] = item.IsFormulaValue;
                TariffArray[TariffArray.Size] = item.Value;
            end;
        end;
        // выполняем операцию
        var opid = MakeOperation(ObjN, ObjID, CF_RATE, 0, OpDate, TariffArray);
        if(opid == 0)
            result.MsgID = 0;
            result.MsgText = GetMO_LoansError();
        else
            result.MsgID = opid;
            result.MsgText = "";
        end;
    end;
    return result;
END;

/* Test test test 
var res = lns_GetEditRateItemList(1, 126);
    [+------+--------------------------------------------------------+-----------+----+-----+------+---+------------------+------------------+];
for (var it, res)
    [| #### | ###################################################### |###########|####|#####|######|###|##################|##################|]
    (it.TypeRateID,it.TypeRateName,Date(it.RateDate),it.Calendar,it.ValueBase,it.DayInMon,it.IsFormulaValue,it.Value, it.UnitName);
end;
    [+------+--------------------------------------------------------+-----------+----+-----+------+---+------------------+------------------+];

res[3].value = "8.00000";
//res[0].RateDate = Date(12, 11, 2016);

var newArr = Tarray();
newarr[newarr.size] = res[3];
var rv = lns_ChangeRate(1, 126, {curdate}, newarr);
if (rv.MsgId != 0)
    println(rv.MsgText);
else
    println("\n Всё ОК \n");
    res = lns_GetEditRateItemList(1, 126);
    [+------+--------------------------------------------------------+-----------+----+-----+------+---+------------------+------------------+];
    for (it, res)
    [| #### | ###################################################### |###########|####|#####|######|###|##################|##################|]
    (it.TypeRateID,it.TypeRateName,Date(it.RateDate),it.Calendar,it.ValueBase,it.DayInMon,it.IsFormulaValue,it.Value, it.UnitName);
    end;
    [+------+--------------------------------------------------------+-----------+----+-----+------+---+------------------+------------------+];
end;
End test test test */