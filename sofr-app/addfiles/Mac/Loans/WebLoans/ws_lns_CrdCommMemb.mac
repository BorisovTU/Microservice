/*
$Name: ws_lns_CrdCommMemb.mac
$Module: WebLoans
$Description: Сервис для работы со списком участников заседаний кредитного комитета
*/

import ws_validation;
IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_person.mac", "ws_LoansOperation.mac";

CLASS CrdCommMemb()
    VAR ID              : integer   = 0;           // Автоинкрементный ID
    VAR objID           : integer   = 0;           // Идентификатор объекта (Заседание КК или Вид КК, dlobject_dbt t_objectid)
    VAR objN            : integer   = 0;           // Номер заседания КК (DCRDCOMMMEET_DBT t_meetid) или номер вида КК (DTYPECRDCOMM_DBT t_id)    
    VAR operObj         : TWsPerson = TWsPerson(); // Объект пользователя (для листалки операционистов) 
    VAR operPostData    : string    = "";          // Должности пользователя
    VAR roleID          : integer   = 0;           // Номер роли (dcrdcomm_roles_dbt t_roleID) 
    VAR roleName        : string    = "";          // Название роли
    VAR isVoter         : string    = "";          // Признак участия в голосовании
END;

MACRO Lns_GetOperPostData (operId: integer)
    var query, rsRec;
    VAR resultStr = "";
    
    query = "SELECT POST.T_POSTNAME" +
            " FROM DLOP_POST_DBT POST_USR" +
            " INNER JOIN DLPOSTS_DBT POST" +
            " ON POST_USR.T_POSTID_REF = POST.T_POSTID" +
            " WHERE POST_USR.T_OPER = " + operId;
            
    rsRec = RsdRecordset(query);

    while (rsRec.MoveNext)                       
        if (resultStr != "")
            resultStr = resultStr + ", " + rsRec.fld(0).value;
        else
            resultStr = rsRec.fld(0).value;
        end;
    end;
    
    return resultStr;
END;

MACRO Lns_GetCrdCommMembList(ObjID: integer, ObjTypeId: integer)
    var query, rs = null;
    VAR result = TArray();
    query = "SELECT CM.T_ID, CM.T_OBJID, CM.T_OBJN, CM.T_ROLEID, CR.T_ROLENAME, CR.T_ISVOTER, CM.T_OPER, P.T_NAME" +
            " FROM DCRDCOMMMEMB_DBT CM" + 
            " INNER JOIN DCRDCOMM_ROLES_DBT CR" +
            " ON CM.T_ROLEID = CR.T_ROLEID" +
            " INNER JOIN DPERSON_DBT P" +
            " ON CM.T_OPER = P.T_OPER" +
            " WHERE CM.T_OBJID = " + ObjTypeId +  " AND CM.T_OBJN = " + ObjID;
            
    rs = TRsbDataSet(query);

    while (rs.MoveNext())        
        var curRec = CrdCommMemb();
        
        curRec.ID                    = rs.id;
        curRec.objID                 = rs.objID;
        curRec.objN                  = rs.objN;
        curRec.roleID                = rs.roleID;
        curRec.roleName              = rs.roleName;
        if (rs.isVoter == "X")
            curRec.isVoter  =  "X";
        else 
            curRec.isVoter  =  "";
        end;   

        curRec.operObj.Number        = rs.oper;
        curRec.operObj.Name_         = rs.name;
        curRec.operPostData          = Lns_GetOperPostData(rs.oper);
        
        result.value(result.Size) = curRec;
    end;
    
    return result;
END;

MACRO Lns_InsertCrdCommMemb(ObjID: integer, ObjN: integer, OperID: integer, RoleID: integer)
    var qStr = "",
        stat = true,
        result = TWsMessage();
        
    result.MsgText = "";
    result.MsgID = 0;
    if (OperID == 0)
        result.MsgID = 1;
        result.MsgText = "Введите номер участника."
    elif (RoleID == 0)
        result.MsgID = 1;
        result.MsgText = "Введите роль участника."    
    else       
        qStr = "INSERT INTO DCRDCOMMMEMB_DBT (T_OBJID, T_OBJN, T_OPER, T_ROLEID) " + 
               " VALUES ( " +
               ObjID  + ", " +
               ObjN   + ", " +
               OperID + ", " + 
               RoleID +  ")";
               
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления участника.";
        end;
    end;
    return result;
END;

MACRO Lns_UpdateCrdCommMemb(ID: integer, NewOperID: integer, NewRoleID: integer)
    var cmdStr : string = "",
        stat = true,
        result = TWsMessage();
      
    result.MsgText = "";
    result.MsgID = 0;
    if (NewOperID == 0)
        result.MsgID = 1;
        result.MsgText = "Введите номер участника."
    elif (NewRoleID == 0)
        result.MsgID = 1;
        result.MsgText = "Введите роль участника."
    else
                                   
        cmdStr = "UPDATE DCRDCOMMMEMB_DBT CM SET" +
                 " CM.T_OPER = " + NewOperID + ","
                 " CM.T_ROLEID = " + NewRoleID +
                 " WHERE CM.T_ID = " +  ID;                           
        stat = LnSqlExec(cmdStr, false);

        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка обновления участника.";
        end;
    end;
    return result;
END;

MACRO Lns_DeleteCrdCommMemb(ID)
    var stat, result;

    stat = LnSqlExec("DELETE FROM DCRDCOMMMEMB_DBT WHERE T_ID = " + ID, false);
    result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления участника.";
    end;
    return result;
END;


