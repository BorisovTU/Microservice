/*
$Name: ws_lns_GraphGroup.mac
$Module: WebLoans
$Description: Источник данных для листалки "Группы графиков"
*/
IMPORT mfo_lib, SbCrdInter, RsbDataSet, globals;

CLASS CLnsGraphGroupItem()
    VAR ID       : integer = 0;  // Идентификатор группы
    VAR Num      : integer = 0;  // Номер группы
    VAR GroupName: string  = ""; // Имя группы
END;

MACRO Lns_GetGraphGroupList(CreditType)
    var result = TArray();
    var item = NULL;
    CaptureOutput;
    if (CreditType) // задан вид кредита. Ищем группу, в которой есть и ОД и %.
        [
        SELECT gk.t_id,
               gk.t_num,
               gk.t_name,
               count(0) as t_cnt
        FROM DGRAPHKIND_DBT gk
        JOIN DGRAPHTREEPARM_DBT gtpgroup ON gtpgroup.T_GRAPHKIND_ID_REF = gk.T_ID
        JOIN DGRAPHOBJECT_DBT   go       ON go.T_ID = gtpgroup.T_GRAPHOBJECT_ID_REF
        JOIN DGRAPHTREEPARM_DBT gtp      ON gtp.T_PARENTID = gtpgroup.T_ID
        JOIN DGRAPHKINDPARM_DBT gkp      ON gkp.T_GRAPHTREEPARM_ID_REF = gtp.T_ID
        WHERE gk.T_ISGROUP = 'X'
          AND go.T_OBJECTID = 7
          AND go.T_OBJECTNUMBER = #
          AND gkp.T_CREDOPID_REF = 0
          AND gkp.T_SUBTYPEGRAPH in (0, 1)
          AND gtpgroup.T_PARENTID = 0
        GROUP BY gk.t_id,
                 gk.t_num,
                 gk.t_name
        HAVING count(0) > 1;
        ] (string(CreditType));
    else
        [
        SELECT gk.t_id,
               gk.t_num,
               gk.t_name
        FROM DGRAPHKIND_DBT gk
        WHERE gk.t_module = 'Ц'
          AND gk.t_isgroup = 'X'
          AND gk.t_isban != 'X'
        ];
    end;
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    while (rs.moveNext)
        item = CLnsGraphGroupItem();
        item.ID        = rs.ID;
        item.Num       = rs.NUM;
        item.GroupName = rs.NAME;
        result[result.size] = item;
    end;
    return result;
END;

MACRO Lns_GetGraphGroupByID(GraphGroupID)
    var result = CLnsGraphGroupItem();
    CaptureOutput;
    [
        SELECT gk.t_id,
               gk.t_num,
               gk.t_name
        FROM DGRAPHKIND_DBT gk
        WHERE gk.t_id = #
    ] (string(GraphGroupID));
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    if (rs.moveNext)
        result.ID        = rs.ID;
        result.Num       = rs.NUM;
        result.GroupName = rs.NAME;
    end;
    return result;
END;