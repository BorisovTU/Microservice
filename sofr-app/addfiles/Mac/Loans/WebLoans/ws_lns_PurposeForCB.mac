/*
$Name: ws_lns_PurposeForCB.mac
$Module: WebLoans
$Description: Сервис источника данных для листалки "Цель кредитования для ЦБ"
*/
IMPORT RsbDataSet, total, likepy;

CLASS CLnsPurposeForCB()
    VAR PurpID:     integer = 0;     // Идентификатор цели кредитования для ЦБ
    VAR PurpName:   string  = "";    // Название цели кредитования для ЦБ
    VAR PurpCode:   string  = "";    // Код цели кредитования для ЦБ
END;

MACRO Lns_GetPurposeForCBList()
    VAR result = TArray(), obj = NULL;
    VAR rs = NULL;
    VAR query = "select p.* from dpurple_dbt p ";
    
    rs = TRsbDataSet(query);

    WHILE (rs.moveNext())
        obj = CLnsPurposeForCB();

        obj.PurpID   = rs.PurpID;
        obj.PurpName = rs.PurpName;
        obj.PurpCode = rs.PurpCode;
        
        result[result.size] = obj;
    END;

    RETURN result;
END;

MACRO Lns_GetPurposeStrByIDList(PurposeIDList)
    var qStr = "",
        result = "",
        rs = null,
        RecCount = 0,
        PurpName = "",
        PurpCodes = TArray();
    if (PurposeIDList != null)    
        qStr = "SELECT * FROM dpurple_dbt WHERE t_purpid in (" + join(PurposeIDList, ",") + ")";
        rs = TRsbDataSet(qStr);
        while (rs.moveNext())
            RecCount = RecCount + 1;
            PurpName = rs.PurpName;
            PurpCodes[PurpCodes.Size] = rs.PurpCode;
        end;
        if (RecCount == 1)
            result = PurpName;
        else
            result = join(PurpCodes, ", ");
        end;
    end;
    return result;
END;

// Функция возвращает название цели кредитования (если она одна)
// и список номеров, разделенных запятой (если их несколько)
MACRO GetPurposeStrByObjID(ObjID, ObjN)
    var qStr = "",
        result = "",
        rs = null,
        RecCount = 0,
        PurpName = "",
        PurpCodes = TArray();   
    qStr = "SELECT p.* FROM dpurple_dbt p, dobjpurple_dbt op WHERE p.t_purpid=op.t_purpid " +
           "AND op.t_objecttypeid = " + ObjID + 
           " AND op.t_objectnumber = " + ObjN;
    rs = TRsbDataSet(qStr);
    while (rs.moveNext())
        RecCount = RecCount + 1;
        PurpName = rs.PurpName;
        PurpCodes[PurpCodes.Size] = rs.PurpCode;
    end;
    if (RecCount == 1)
        result = PurpName;
    else
        result = join(PurpCodes, ", ");
    end;
    return result;
END;

MACRO GetPurposeForCBIDList(ObjID, ObjN)
    var result = TArray(),
        rs = TRsbDataSet("SELECT T_PURPID FROM DOBJPURPLE_DBT WHERE " + 
                         " T_OBJECTTYPEID = " + ObjID +
                         " AND T_OBJECTNUMBER = " + ObjN);
    while (rs.MoveNext())
        result[result.size] = rs.PurpID;
    end;
    return result;
END;

MACRO UpdatePurposeForCBList(ObjID, ObjN, PurposeForCBIDList)
    var rs = null,
        i = 0,
        QStr = "",
        stat = true;
        
    qStr = "DELETE FROM DOBJPURPLE_DBT WHERE " +
           " T_OBJECTTYPEID=" + ObjID +
           " AND T_OBJECTNUMBER=" + ObjN;    
    stat = LnSqlExec(qStr, false);
    while ((i < PurposeForCBIDList.size) and (stat == true))
        qStr = "INSERT INTO DOBJPURPLE_DBT (T_OBJECTTYPEID, T_OBJECTNUMBER, T_PURPID) VALUES (?, ?, ?)";
        rs = RSDCommand(qStr);
        rs.AddParam("p_objecttypeid", RSDBP_IN, ObjID);
        rs.AddParam("p_objectnumber", RSDBP_IN, ObjN);
        rs.AddParam("p_purpid",       RSDBP_IN, PurposeForCBIDList[i]);
        rs.Execute;
        i = i + 1;
    end;
END;