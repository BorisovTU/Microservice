/*
$Name: ws_lns_CrdCommMeet.mac
$Module: WebLoans
$Description: Сервис источника данных списка заседаний КК
*/
IMPORT RSD, PTInter, globals, Календарь, total, likepy, ws_validation, ws_datafilter, SbCrdInter, fmt_lib, cb_sql, ws_dprt, "ws_person.mac";
IMPORT DTYPECRDCOMM_DBT, DCRDCOMMMEET_DBT;
IMPORT ws_lns_CommonLib;
IMPORT "oralib.mac";

CLASS CLnsCrdCommMeetListItem()
    VAR MeetID       : integer = 0;          // Идентификатор заседания
    VAR MeetNumber   : string  = "";         // Номер заседания
    VAR TypeCrdCommID: integer = 0;          // Вид комитета
    VAR TypeCrdComm  : string  = "";         // Вид комитета
    VAR TypeDebtor   : string  = "";         // Тип заемщиков
    VAR BeginDate    : string  = "";         // Дата и время начала
    VAR EndDate      : string  = "";         // Дата и время окончания
    VAR Quorum       : integer = 0;          // Кворум
    VAR ClaimCount   : integer = 0;          // Кол-во заявок
    VAR MeetStatusID : integer = 0;          // Идентификатор статуса заседания
    VAR MeetStatus   : string  = "";         // Статус заседания
    VAR Note         : string  = "";         // Примечание
END;

CLASS CLnsCrdCommMeetEditPrm()
    VAR CrdCommMeet      = null;       // Параметры заседания КК
    VAR UserGroupIDList  = null;       // Список ответственных пользователей для отбора заявок
    VAR CrdTypeIDList    = null;       // Список видов кредита для отбора заявок
    VAR ClaimRouteIDList = null;       // Список маршрутов для отбора заявок
    VAR Fncash: TWsDepartment = null;  // костыль для инициализации
    VAR Branch: TWsDepartment = null;  // ядерной листалки
END;

/* =================================================================
   Для ядерной листалки пользователей "UserListWidget" (DPERSON_DBT)
   ================================================================= */

MACRO UserGroupObjectToUserGroup_List(UserGroupObjectList)
    var result = TArray();
    var i = 0;
    
    while(i < UserGroupObjectList.Size)
        result.value(result.Size) = UserGroupObjectList[i].Number;
        
        i = i + 1;
    end;
    
    return result;
END;

MACRO UserGroupToUserGroupObject_List(UserGroupList)
    var result = TArray();
    var Person = null;
    var i = 0;
    
    while(i < UserGroupList.Size)
        Person = TWsPerson();
        Person.Number = UserGroupList[i];
        result.value(result.Size) = Person;
        
        i = i +1;
    end;
    
    return result;
END;

/* ================================================================= */

MACRO GetWhere(FilterItems)
    var fp = FilterParser( FilterItems );
    fp.AddFieldCondition( 0, null, ""); // Не реализовано
    fp.AddFieldCondition( 1, null, "m.t_meetstatus");
    fp.AddFieldCondition( 2, null, "m.t_meetnumber");
    fp.AddFieldCondition( 3, null, "m.t_begindate");
    fp.AddFieldCondition( 4, null, "m.t_enddate");
    fp.AddFieldCondition( 5, null, "m.t_typedebtor");
    return fp.GetFullSQLCondition();
END;

MACRO lns_GetCrdCommMeetList(TypeCrdComm, TypeDebtor, PageSize, PageIndex, OrderItems, FilterItems)
    var result = TArray();
    var item = null;
    var FilterStr = "";

    var qStr = "SELECT m.t_meetid, m.t_meetnumber, m.t_typecrdcommid, " +
               "m.t_begindate, m.t_enddate, m.t_endtime, m.t_begintime, " +
               "m.t_meetquorum, m.t_claimcount, m.t_meetstatus, m.t_note, " +
               "case m.t_TypeDebtor " +
               "when 0 then 'ФЛ, ЮЛ' " +
               "when 1 then 'ЮЛ' " +
               "when 2 then 'ФЛ' " +
               "end as t_TypeDebtorStr, " +
               "tc.t_typecrdcommname, " +
               "ms.t_name as t_statusname " +
               "FROM dcrdcommmeet_dbt m, dtypecrdcomm_dbt tc, dllvalues_dbt ms " +
               "WHERE m.t_typecrdcommid = tc.t_id AND ms.t_list=1450 " + 
               "AND ms.t_element = m.t_meetstatus ";
    if (TypeCrdComm > 0)
        qStr = qStr + " AND m.t_typecrdcommid=" + TypeCrdComm;
    else
        if (TypeDebtor < 3)
            qStr = qStr + " AND m.t_TypeDebtor=" + TypeDebtor;
        end;
    end;
    if (FilterItems != null)
        FilterStr = GetWhere(FilterItems);
        if (FilterStr != "")
            qStr = qStr + " and " + FilterStr;
        end;
    end;

    if( (OrderItems == null) or (OrderItems.Size() > 0) )
       ;// qStr = qStr + " ORDER BY " + PT_GetSortStr(OrderItems, "");
    end;
    qStr = SQL_WrapSelect(qStr, PageIndex, PageSize);

    var rs = TRsbDataSet(qStr);
    while (rs.MoveNext)
        item = CLnsCrdCommMeetListItem();
        item.MeetID        = rs.MeetID;
        item.MeetNumber    = rs.Meetnumber;
        item.TypeCrdCommID = rs.TypeCrdCommID;
        item.TypeCrdComm   = rs.TypeCrdCommName;
        item.TypeDebtor    = rs.TypeDebtorStr;
        item.BeginDate     = string(date(rs.begindate)) + " " + string(time(rs.begintime)); 
        item.EndDate       = string(date(rs.enddate)) + " " + string(time(rs.endtime)); 
        item.Quorum        = rs.MeetQuorum;
        item.ClaimCount    = rs.ClaimCount;
        item.MeetStatusID  = rs.MeetStatus;
        item.MeetStatus    = rs.StatusName;
        item.Note          = rs.Note;
        result[result.size] = item;
    end;
    return result;
END;

PRIVATE MACRO CheckCrdCommEditPrm(CrdCommMeetEditPrm)
    // проверка при сохранении
    // если ошибка - делаем RunError("", "Текст ошибки");
    if (CrdCommMeetEditPrm.CrdCommMeet.BeginTime == Date(0,0,0)) 
        CrdCommMeetEditPrm.CrdCommMeet.BeginTime = Time(0,0,0);
    end;
    
    if (CrdCommMeetEditPrm.CrdCommMeet.EndTime == Date(0,0,0)) 
        CrdCommMeetEditPrm.CrdCommMeet.EndTime = Time(0,0,0);
    end;
END;


MACRO lns_InsertCrdCommMeet(CrdCommMeetEditPrm)
    RslDefCon.BeginTrans();
    var result = WebResponseBuilder();
    record CrdCommMeetBuff ("crdcommmeet.dbt");
    var CrdCommMeetFile = TBFile ("crdcommmeet.dbt", "rw", 0, "crdcommmeet.dbt", "loans.def");
    CheckCrdCommEditPrm(CrdCommMeetEditPrm);
    CopyClassObjToRecord(CrdCommMeetEditPrm.CrdCommMeet, CrdCommMeetBuff);
    copy(CrdCommMeetFile, CrdCommMeetBuff);
    CrdCommMeetFile.insert();
    
    // вставка списка идентификаторов.
    
    // 7  - вид кредита
    // 52 - заседание КК
    // 92 - пользователь (?)
    
    var ins : RsbSQLInsert;
    if (CrdCommMeetEditPrm.CrdTypeIDList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (52, " + CrdCommMeetEditPrm.CrdCommMeet.MeetID + ", 7, ?)", 1, CrdCommMeetEditPrm.CrdTypeIDList.Size );
        ins.AddParam( V_INTEGER, CrdCommMeetEditPrm.CrdTypeIDList );
        ins.Insert();
    end;
    
    if (CrdCommMeetEditPrm.UserGroupIDList != null)
        ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (52, " + CrdCommMeetEditPrm.CrdCommMeet.MeetID + ", 92, ?)", 1, CrdCommMeetEditPrm.UserGroupIDList.Size );
        ins.AddParam( V_INTEGER, UserGroupObjectToUserGroup_List(CrdCommMeetEditPrm.UserGroupIDList) );
        ins.Insert();
    end;
    
    RslDefCon.CommitTrans();
    return result.Result;
    OnError(err)
        RslDefCon.RollbackTrans();
        result.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return result.Result;
END;

MACRO lns_UpdateCrdCommMeet(CrdCommMeetEditPrm)
    RslDefCon.BeginTrans();
    var result = WebResponseBuilder();
    record CrdCommMeetBuff ("crdcommmeet.dbt");
    var CrdCommMeetFile = TBFile ("crdcommmeet.dbt", "rw", 0, "crdcommmeet.dbt", "loans.def");
    CheckCrdCommEditPrm(CrdCommMeetEditPrm);
    CopyClassObjToRecord(CrdCommMeetEditPrm.CrdCommMeet, CrdCommMeetBuff);
    CrdCommMeetFile.rec.meetid = CrdCommMeetBuff.meetid;
    if (CrdCommMeetFile.GetEQ())
        copy(CrdCommMeetFile, CrdCommMeetBuff);
        CrdCommMeetFile.update();
        
        // апдейт списков идентификаторов
        var strcmd;  
        strcmd = "delete from dcrdcommdepobj_dbt where t_parentobjn = " + CrdCommMeetEditPrm.CrdCommMeet.MeetID;
        execSQL(strcmd); 
        
        // 7  - вид кредита
        // 52 - заседание КК
        // 92 - пользователь (?)
        
        var ins : RsbSQLInsert;
        if (CrdCommMeetEditPrm.CrdTypeIDList != null)
            ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (52, " + CrdCommMeetEditPrm.CrdCommMeet.MeetID + ", 7, ?)", 1, CrdCommMeetEditPrm.CrdTypeIDList.Size );
            ins.AddParam( V_INTEGER, CrdCommMeetEditPrm.CrdTypeIDList );
            ins.Insert();
        end;
        
        if (CrdCommMeetEditPrm.UserGroupIDList != null)
            ins = RsbSQLInsert("INSERT INTO dcrdcommdepobj_dbt (t_parentobjid, t_parentobjn, t_childobjid, t_childobjn) VALUES (52, " + CrdCommMeetEditPrm.CrdCommMeet.MeetID + ", 92, ?)", 1, CrdCommMeetEditPrm.UserGroupIDList.Size );
            ins.AddParam( V_INTEGER, UserGroupObjectToUserGroup_List(CrdCommMeetEditPrm.UserGroupIDList) );
            ins.Insert();
        end;
    else
        RunError("", "Заседание кредитного комитета не найдено!");
    end;
    RslDefCon.CommitTrans();
    return result.Result;
    OnError(err)
        RslDefCon.RollbackTrans();
        result.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return result.Result;
END;

MACRO lns_DeleteCrdCommMeet(CrdCommMeetID)
    RslDefCon.BeginTrans();
    var result = WebResponseBuilder();
    var CrdCommMeetFile = TBFile ("crdcommmeet.dbt", "rw", 0, "crdcommmeet.dbt", "loans.def");
    CrdCommMeetFile.rec.meetid = CrdCommMeetID;
    if (CrdCommMeetFile.GetEQ())
        CrdCommMeetFile.delete();
        
        // удаление списков идентификаторов
        var strcmd = "delete from dcrdcommdepobj_dbt where t_parentobjn = " + CrdCommMeetID;
        execSQL(strcmd);
    else
        RunError("", "Заседание кредитного комитета не найдено!");
    end;
    RslDefCon.CommitTrans();
    return result.Result;
    OnError(err)
        RslDefCon.RollbackTrans();
        result.AddErrorEx(err.code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return result.Result;
END;

MACRO GetList(ParentObjID, ParentObjN, ChildObjID)
    VAR query = "select t_childobjn from dcrdcommdepobj_dbt where t_parentobjid = " + ParentObjID + " and t_parentobjn = " + ParentObjN + " and t_childobjid = " + ChildObjID,
        RS = RsdRecordset(query),
        result = TArray;   
    
    while (RS.moveNext)
        result[result.Size] = RS.value(0);
    end;
    
    return result;
END;

MACRO lns_GetCrdCommMeetEditPrm(CrdCommMeetID)
    var result = CLnsCrdCommMeetEditPrm();
    var rs = RsdRecordset("SELECT * FROM dcrdcommmeet_dbt WHERE t_MeetID = " +  CrdCommMeetID);
    if (rs.MoveNext)
        result.CrdCommMeet      = GetObjectFromRecordset("DCRDCOMMMEET_DBT", rs);
        result.CrdTypeIDList = GetList(52, CrdCommMeetID, 7);
        result.UserGroupIDList = UserGroupToUserGroupObject_List(GetList(52, CrdCommMeetID, 92));
        result.ClaimRouteIDList = null;
        result.Fncash = GetTWsDepartmentByCode(result.CrdCommMeet.Fncash);
        result.Branch = GetTWsDepartmentByCode(result.CrdCommMeet.Branch);
    else
        RunError("Заседание кредитного комитета не найдено!");
    end; 
    return result;
END;

MACRO lns_InitNewCrdCommMeet(TypeCrdCommID)
    var result = CLnsCrdCommMeetEditPrm();
    var rs = RsdRecordset("SELECT * FROM dtypecrdcomm_dbt WHERE T_ID = " +  TypeCrdCommID);
    var TypeCrdComm = NULL;
    if (rs.MoveNext)
        TypeCrdComm = GetObjectFromRecordset("DTYPECRDCOMM_DBT", rs);
        result.CrdCommMeet      = DCRDCOMMMEET_DBT();
        result.CrdCommMeet.MeetStatus       = 1; // запланировано
        result.CrdCommMeet.BeginDate        = {curdate};
        result.CrdCommMeet.EndDate          = {curdate};
        result.CrdCommMeet.BorrowMemberSrc  = 1;
        result.CrdCommMeet.TypeCrdCommID    = TypeCrdComm.ID;
        result.CrdCommMeet.TypeDebtor       = TypeCrdComm.typedebtor;
        // result.CrdCommMeet.Limit            = // добавить после истории лимита по виду КК
        // result.CrdCommMeet.LimitCurCode     = 
        result.CrdCommMeet.Fncash           = TypeCrdComm.fncash;
        result.CrdCommMeet.Branch           = TypeCrdComm.branch;
        result.CrdCommMeet.DecisionCond     = TypeCrdComm.decision;
        result.CrdCommMeet.MeetQuorum       = TypeCrdComm.session;
        result.CrdCommMeet.VoteQuorum       = TypeCrdComm.voting;
        result.CrdCommMeet.Note             = "";
        result.CrdCommMeet.TerritorialCond  = TypeCrdComm.selection;
        result.CrdCommMeet.IsCrdSum         = TypeCrdComm.iscrdsum;
        result.CrdCommMeet.CrdSumFrom       = TypeCrdComm.crdsumfrom;
        result.CrdCommMeet.CrdSumTo         = TypeCrdComm.crdsumto;
        result.CrdCommMeet.CrdSumCurCode    = TypeCrdComm.crdsumcurrency;
        result.CrdCommMeet.IsDate           = TypeCrdComm.isperiod;
        result.CrdCommMeet.DateFrom         = {curdate};
        if (TypeCrdComm.periodtype == 1) // рабочих дней
            result.CrdCommMeet.DateTo = DateAfterWorkDays({curdate}, -1 * int(TypeCrdComm.periodnumber));
        elif (TypeCrdComm.periodtype == 2) // календарных дней
            result.CrdCommMeet.DateTo = DateAfterCalenDays({curdate}, -1 * int(TypeCrdComm.periodnumber));
        else
            result.CrdCommMeet.DateTo = {curdate};
        end;
        result.CrdCommMeet.IsProcedure      = TypeCrdComm.isprocedure;
        result.CrdCommMeet.ProcedureName    = TypeCrdComm.procedurename;
        result.CrdCommMeet.IsUserGroup      = TypeCrdComm.isusergroup;
        result.CrdCommMeet.IsCrdTypes       = TypeCrdComm.iscrdtypes;

        result.CrdCommMeet.MeetNumber = lns_GetGenerationNumberObject(LO_CRDCOMMMEET, result.CrdCommMeet);
        //result.CrdCommMeet.IsClaimRoutes    = TypeCrdComm.;
        result.UserGroupIDList  = null; // докинуть из Вида
        result.CrdTypeIDList    = null; // докинуть из Вида
        result.ClaimRouteIDList = null; // докинуть из Вида
        result.Fncash = GetTWsDepartmentByCode(result.CrdCommMeet.Fncash);
        result.Branch = GetTWsDepartmentByCode(result.CrdCommMeet.Branch);
    end;
    return result;
END;