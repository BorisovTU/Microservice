/*
$Name: ws_lns_ContractList.mac
$Module: WebLoans
$Description: Сервис получения списка договоров
*/
IMPORT BankInter, SbCrdInter, RsbDataSet, mfo_lib,  globals, total, ws_lns_CommonLib, likepy, cb_sql, ws_datafilter;

CLASS CLnsContractListItem()
    VAR ObjID         : integer  = 0;
    VAR ObjN          : integer  = 0;
    VAR ContractStatus: string   = "";
    VAR IsEmployer    : bool     = false;
    VAR ClientID      : integer  = 0;
    VAR ClientName    : string   = "";
    VAR ClientType    : string   = "";
    VAR LegalForm     : integer  = 0;
    VAR UsNumber      : string   = "";
    VAR AccNumber     : string   = "";
    VAR RegDate       : DateTime = Date(0,0,0);
    VAR ReturnDate    : DateTime = Date(0,0,0);
    VAR TypeCredit    : string   = "";
    VAR CurCode       : string   = "";
    VAR SumCredit     : Money    = $0;
    VAR AvailableLimit: Money    = $0;
    VAR SumDebt       : Money    = $0;
    VAR CloseDate     : DateTime = Date(0,0,0);
    VAR MaxOpenDate   : DateTime = Date(0,0,0);
END;

PRIVATE MACRO GetPartyCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = " 1 = 1 ";       
    
    if ((ValType(value) != V_UNDEF)              and 
        (value.Size > 0)                         and 
        (ValType(value[0].partyid) == V_INTEGER) and 
        (condition == FilterCondition_Equal))

        strSQLCond = " filtered.T_ClientID = " + value[0].partyid;
    end;

    return strSQLCond;
END;


PRIVATE MACRO GetFIIDFromFIObject(FIObject)
    return FIObject.Fiid;
END;

PRIVATE MACRO GetFininstCondition(condition:Integer, value, type:Integer):String
    var strSQLCond:String = " 1 = 1 ";       
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = " filtered.t_CurCode IN (" + join(map(value, @GetFIIDFromFIObject), ",") + ") ";
    end;
    return strSQLCond;
END;

PRIVATE MACRO GetOneUsCrdTypeCondition(UsCrdType)
    return " instr(filtered.t_usertype, '" + UsCrdType + "') > 0 ";
END;

PRIVATE MACRO GetUserTypeCrdCondition(condition:Integer, value, type:Integer):String
    var strSQLCond: String = " 1 = 1 ";       
    if ((ValType(value) != V_UNDEF) and (value.Size > 0))
        strSQLCond = "(" + join(map(value, @GetOneUsCrdTypeCondition), " OR ") + ")";
    end;
    return strSQLCond;
END;

PRIVATE MACRO GetExpiredCondition(condition:Integer, value, type:Integer):String
    var strSQLCond: String = " 1 = 1 ";       
    if ((ValType(value) != V_UNDEF) and (value.Size == 1))
        if (value[0] == 0) // выводить просроченные
            strSQLCond = " filtered.t_SumExp > 0 "
        elif (value[0] == 1) // исключить просроченные
            strSQLCond = " filtered.t_SumExp = 0 "
        end;
    end;
    return strSQLCond;
END;

PRIVATE MACRO GetFilterConditionString(FilterItems)
    var fp = FilterParser(FilterItems);
    fp.AddFieldCondition    ( 0, NULL, " CASE filtered.t_ContractStatus WHEN 'О' THEN 0 WHEN 'З' THEN 1 WHEN 'Ч' THEN 2 WHEN 'Д' THEN 3 END "); // статус
    fp.AddFieldCondition    ( 1, "filtered", "T_USNUMBER");                       // Номер договора
    fp.AddFieldCondition    ( 2, "filtered", "T_REGDATE");                        // Дата начала
    fp.AddFieldCondition    ( 3, "filtered", "T_RETURNDATE");                     // Дата окончания
    fp.AddUserFieldCondition( 4, @GetPartyCondition);                             // Замещик
    fp.AddFieldCondition    ( 5, NULL, "DECODE(filtered.T_TYPEDEBTOR, 2, 0, 1)"); // Тип заемщика
    fp.AddFieldCondition    ( 6, "filtered", "T_CREDITTYPEID_REF");               // Вид кредита
    fp.AddFieldCondition    ( 7, "filtered", "T_SUMCREDIT");                      // Сумма договора
    fp.AddFieldCondition    ( 8, "filtered", "T_AVAILABLELIMIT");                 // Лимит
    fp.AddFieldCondition    ( 9, "filtered", "T_ACCNUMBER");                      // Ccудный счет
    fp.AddFieldCondition    ( 10, "filtered", "T_SUMDEBT");                       // Остаток задолженности
    fp.AddUserFieldCondition( 11, @GetFininstCondition);                          // Валюта
    fp.AddFieldCondition    ( 12, "filtered", "T_PAYTYPE");                       // Тип выдачи
    fp.AddUserFieldCondition( 13, @GetExpiredCondition);                          // Просроченные договоры
    fp.AddUserFieldCondition( 14, @GetUserTypeCrdCondition);                      // Пользовательский вид кредита
    fp.AddFieldCondition    ( 15, "filtered", "T_CLOSEDATE");                     // Дата закрытия
    //fp.AddFieldCondition    ( 16, "filtered", "T_NEXTACTION");                        // Предельная дата
    var filterString = fp.GetFullSQLCondition();
    return filterString;
END;

// генерит строку сортировки для одного элемента 
PRIVATE MACRO GetOrderItemStr(orderitem)
    var orderStr = "";
    if (orderitem != null)
        orderStr = " filtered.t_" + orderitem.Column + " " + IfThenElse(orderitem.Direction == 0, "asc", "desc");
    end;
    return orderStr;
END;

// Преобразовывает массив OrderItems в выражение ORDER BY 
PRIVATE MACRO GetOrderStr( OrderItems )
    var OrderStr: string = "";
    if ((OrderItems != null) and (OrderItems.Size))
        OrderStr = " ORDER BY " + join(map(OrderItems, @GetOrderItemStr), ", ")
    end;
    return OrderStr;
END;

/*
Функция генерирует строку запроса для получения записей скроллинга.
Сначала генерится запрос получения всех данных.
Затем на него накладываются ограничения по типу клиента, типу выдачи и статусу договора
Затем накладывается ограничение СУД.
Потом запрос оборачивается в ещё один select. Это сделано для того, чтоб можно было фильтровать по тем
полям, которые получены в результате выполнения хранимок.

Ограничения: Названия полей внутри блока select должны совпадать с названиями
полей транспортного класса, иначе не будет работать сортировка, так как внутри orderitems приходят как раз
названия полей транспортного класса, а при генерации блока ORDER BY к ним просто прибавляется t_ (смотри выше функцию GetOrderItemStr)
*/
PRIVATE MACRO GetQueryString(ClientType, CreditState, PayType, FilterItems, OrderItems)
    CaptureOutput();
    [
        SELECT credit.t_creditstate as t_ContractStatus,
               credit.t_creditnumber as t_ObjN,
               credit.t_crd_kind as t_ObjID,
               credit.t_LegalForm as t_LegalForm,
               credit.t_PayType as t_PayType,
               credit.t_TypeDebtor as t_TypeDebtor,
               credit.t_ClientID_Ref as t_ClientID,
               credit.t_usertype as t_usertype,
               case credit.t_LegalForm
                   when 1 then 'ЮЛ'
                   when 2 then 'ФЛ'
               end as t_ClientType,
               credit.t_UsCreditNumber as t_UsNumber,
               credit.t_RegDate as t_RegDate,
               credit.t_ReturnDate as t_ReturnDate,
               credit.t_CloseDate as t_CloseDate,
               credit.T_CREDITTYPEID_REF as T_CREDITTYPEID_REF,
               typecrd.t_CreditTypeName as t_TypeCredit,
               namealg2.t_szNameAlg as t_PayTypeName,
               fininstr.t_Ccy as t_Currency,
               credit.t_CreditSum as t_SumCredit,
               credit.t_CurCode as t_CurCode,
               party.t_Name as t_ClientName,
               LoansKernel.getaccount(credit.t_crd_kind, 
                                      credit.t_creditnumber, 
                                      1, 
                                      credit.t_curcode) as t_accnumber, 

               LoansKernel.getregrest(68, 
                                      credit.t_crd_kind, 
                                      credit.t_creditnumber,
                                      rsbsessiondata.curdate, 
                                      credit.t_curcode) as t_AvailableLimit, 

               LoansKernel.getregrestwithso(1, 
                                            credit.t_crd_kind, 
                                            credit.t_creditnumber, 
                                            to_date ('9999-12-31', 'yyyy-mm-dd'), 
                                            credit.t_curcode) + 
               LoansKernel.getregrestwithso(2, 
                                            credit.t_crd_kind, 
                                            credit.t_creditnumber, 
                                            to_date ('9999-12-31', 'yyyy-mm-dd'), 
                                            credit.t_curcode) +
               LoansKernel.getregrestwithso(16, 
                                            credit.t_crd_kind, 
                                            credit.t_creditnumber, 
                                            to_date ('9999-12-31', 'yyyy-mm-dd'), 
                                            credit.t_curcode) as t_SumDebt,

               LoansKernel.getregrestwithso(2, 
                                            credit.t_crd_kind, 
                                            credit.t_creditnumber, 
                                            rsbsessiondata.curdate, 
                                            credit.t_curcode) +
               LoansKernel.getregrestwithso(3, 
                                            credit.t_crd_kind, 
                                            credit.t_creditnumber, 
                                            rsbsessiondata.curdate, 
                                            credit.t_curcode) as t_SumExp
        FROM dcredit_c_dbt credit
        JOIN dtype_crd_dbt typecrd  ON credit.t_CreditTypeID_Ref = typecrd.t_CreditTypeID 
        JOIN dfininstr_dbt fininstr ON credit.t_CurCode = fininstr.t_FIID
        JOIN dnamealg_dbt namealg2  ON credit.t_PayType = namealg2.t_iNumberAlg AND namealg2.t_iTypeAlg = 1010
        LEFT JOIN dparty_dbt party  ON credit.t_ClientID_Ref = party.t_PartyID 
        WHERE credit.t_crd_kind = 1
    ];
    var qStr = StopCaptureOutput();
    if ((ClientType != null) and (ClientType > 0))
        qStr = qStr + " and credit.t_LegalForm = " + ClientType + " ";
    end;
    
    if ((CreditState != null) and (CreditState != ""))
        qStr = qStr + " and credit.t_creditstate = '" + CreditState + "' ";
    end;
    
    if ((PayType != null) and (PayType > 0))
        if (PayType == 1) // Разовый
            qStr = qStr + " and credit.t_PayType = 1 ";
        elif (PayType == 2) // Кредитная линия (любая)
            qStr = qStr + " and credit.t_PayType in (3, 4, 5) ";
        end;
    end;
    
    var restriction_str = "";
    if (GetObjectRestriction(restriction_str, 36, {oper}, "credit")) // 36 - ограничение "КД для изменения"
        qStr = qStr + " AND " + restriction_str;
    end;

    // наложили ограничения на внутренний запрос. Далее оборачиваем его для того, чтобы мочь фильтровать по полям, которые генеряться хранимками

    qStr = "SELECT filtered.* FROM (" + qStr + ") filtered WHERE 1 = 1 ";


    if (FilterItems != null)
        qStr = qStr + " AND " + GetFilterConditionString(FilterItems);
    end;

    if (OrderItems != null)
        qStr = qStr + GetOrderStr( OrderItems );
    end;
    return qStr;
END;

MACRO lns_GetContractList(ClientType, CreditState, PayType, PageSize, PageIndex, OrderItems, FilterItems)
    var result = TArray();
    var qStr = GetQueryString(ClientType, CreditState, PayType, FilterItems, OrderItems);
    IF (pagesize > 0)
        qStr = SQL_WrapSelect(qStr, PageIndex, PageSize);
    END;
    var rs = TRsbDataSet(qStr);
    
    while (rs.MoveNext)
        var item = CLnsContractListItem();
        
        item.ObjID          = rs.ObjID;
        item.ObjN           = rs.ObjN;
        item.ContractStatus = rs.ContractStatus;
        item.IsEmployer     = false;
        item.ClientID       = rs.ClientID;
        item.ClientName     = rs.ClientName;
        item.ClientType     = rs.ClientType;
        item.UsNumber       = rs.UsNumber;
        item.LegalForm      = rs.LegalForm;
        item.RegDate        = rs.RegDate;
        item.ReturnDate     = rs.ReturnDate;
        item.TypeCredit     = rs.TypeCredit;
        item.CurCode        = rs.Currency;
        item.SumCredit      = rs.SumCredit;
        item.CloseDate      = rs.CloseDate;
        item.MaxOpenDate    = Date(0,0,0);
        item.AccNumber      = rs.AccNumber;
        item.AvailableLimit = rs.AvailableLimit;
        item.SumDebt        = rs.SumDebt; 
        result.value(result.Size) = item;
    END;

    return result;
END;

MACRO lns_GetContractListCount(ClientType, CreditState, PayType, FilterItems)
    var qStr = GetQueryString(ClientType, CreditState, PayType, FilterItems);
    var result = SQL_GetNRecs(qStr);
    RETURN result;
END;

