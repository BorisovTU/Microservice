/*
$Name: ws_lns_Registers.mac
$Module: WebLoans
$Description: Сервис получения информации по регистрам
*/
IMPORT SbCrdInter, RsbDataSet;
IMPORT "total.mac";
import "ws_datafilter.mac";

CLASS CLnsRegRestItem()
    var regID = 0;
    var name = string();
    var rest = money(0);
    var consRest = money(0);
    var recNum = 0;
END;

CLASS CLnsRegHistoryItem()
    var restDate = DATE(0, 0, 0);
    var regDebt = $0;
    var regCred = $0;
    var regRest = $0;
END;

private macro GetSqlFilterCondition(FilterConditionItems)
  var fp = FilterParser( FilterConditionItems );
  fp.AddFieldCondition( 0, "al", "t_Name");
  fp.AddFieldCondition( 1, "al", "t_Rest");
  fp.AddFieldCondition( 2, "al", "t_ConsRest");
  return fp.GetFullSQLCondition();
end;
MACRO lns_regrestlist(ObjID: integer, ObjN: integer, opdate: date, FilterConditionItems)
    var result = TArray();
    var item = null;
    var qStr = "";
    var rs = NULL;

    qStr =  qStr + "select al.* from ( ";
    qStr =  qStr + "select spr_reg.t_Name as t_Name, ";
    qStr =  qStr + " LoansKernel.GetRegRest(reg.t_ID, TO_DATE('31.12.9999', 'dd.mm.yyyy')) as t_Rest, ";
    qStr =  qStr + " spr_reg.t_RegID as t_RegID, reg.t_ID as t_RegIDRef, ";
    qStr =  qStr + " (select count(1) from ddutyrest_dbt dr where dr.t_id_ref = reg.t_id) as t_RecNum, ";
    qStr =  qStr + " nvl(sv.t_restSum, 0) as t_ConsRest ";
    qStr =  qStr + " from dlcusreg_dbt reg, dspr_reg_dbt spr_reg, dsvodregview_dbt sv ";
    qStr =  qStr + " where reg.t_RegID = spr_reg.t_RegID ";
    qStr =  qStr + " and reg.t_ObjectTypeID = " + ObjID + " and reg.t_ObjectNumber = " + ObjN;
    qStr =  qStr + " and sv.t_CreditNumber (+)= " + ObjN + " and sv.t_regid (+)= reg.t_RegID";
    qStr =  qStr + ") al ";
    if (FilterConditionItems != null)
      var sqlFilterCondition = GetSqlFilterCondition(filterConditionItems);
      if(sqlFilterCondition != "")
         qStr = qStr + "WHERE" + sqlFilterCondition;
      end;
    end;
    qStr = qStr + " order by al.t_RegID ";

    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
        item = CLnsRegRestItem();
        item.regID = rs.RegIDRef;
        item.name = string(rs.Name);
        item.rest = money(rs.Rest);
        item.consRest = money(rs.ConsRest);
        item.recNum = int(rs.RecNum); //непонятный баг; TRsbDataSet воспринимает результат count как вещественное 
        result[result.size] = item;
    end;

    return result;
END;

MACRO lns_reghistory(RegIDRef: integer )
    var result = TArray();
    var item = null;
    var qStr = "";
    var rs = NULL;

    qStr =  qStr + "select dr.t_RestDate, dr.t_RestSum, dr.t_Prihod, dr.t_Rashod ";
    qStr =  qStr + " from ddutyrest_dbt dr ";
    qStr =  qStr + " where dr.t_ID_Ref = " + RegIDRef;
    
    rs = TRsbDataSet(qStr);
    while (rs.MoveNext())
        item = CLnsRegHistoryItem();
        item.restDate = date(rs.RestDate);
        item.regDebt = money(rs.Prihod);
        item.regCred = money(rs.Rashod);
        item.regRest = money(rs.RestSum);
        result[result.size] = item;
    end;

    return result;
END;

MACRO lns_regsvhistory(RegIDRef: integer)
    var result = TArray();
    var item = null;
    var qStr = "";
    var rs = NULL;
    var RegID, ObjID, ObjN: integer;
    qStr = "SELECT t.t_objecttypeid, t.t_objectnumber, t_regid FROM dlcusreg_dbt t WHERE t.t_id=" + RegIDRef;
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        RegID = int(rs.RegID);
        ObjID = int(rs.ObjectTypeID);
        ObjN  = int(rs.ObjectNumber);
        if (ObjID == LO_CREDIT)
            qStr = "SELECT t.t_RestDate, t.t_RestSum, t.t_Prihod, t.t_Rashod FROM dsvdrestview_dbt t WHERE t.t_RegID = " + RegID +
                   " AND t_creditnumber = " + ObjN;
            rs = TRsbDataSet(qStr);
            while (rs.MoveNext())
                item = CLnsRegHistoryItem();
                item.restDate = date(rs.RestDate);
                item.regDebt = money(rs.Prihod);
                item.regCred = money(rs.Rashod);
                item.regRest = money(rs.RestSum);
                result[result.size] = item;
            end;
        end;
    else
        ;// Регистр не найден.
    end;
    return result;
END;

MACRO lns_getregname(RegIDRef: integer)
    var result = "";
    var qStr = "";
    var rs = NULL;

    qStr =  qStr + "select spr_reg.t_Name as t_Name ";
    qStr =  qStr + " from dlcusreg_dbt reg, dspr_reg_dbt spr_reg ";
    qStr =  qStr + " where reg.t_RegID = spr_reg.t_RegID ";
    qStr =  qStr + " and reg.t_id = " + RegIDRef;
    
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        result = rs.Name;
    end;

    return result;
END;

MACRO lns_getregcurcode(RegIDRef: integer)
    var result = "";
    var qStr = "";
    var rs = NULL;

    qStr =  qStr + "select fininstr.t_Ccy as t_ccy ";
    qStr =  qStr + " from dlcusreg_dbt reg, dcredit_c_dbt credit, dfininstr_dbt fininstr ";
    qStr =  qStr + " where fininstr.t_FIID = credit.t_CurCode ";
    qStr =  qStr + " and credit.t_Crd_Kind = reg.t_ObjectTypeID ";
    qStr =  qStr + " and credit.t_CreditNumber = reg.t_ObjectNumber ";
    qStr =  qStr + " and reg.t_id = " + RegIDRef;
    
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        result = rs.ccy;
    end;

    return result;
END;