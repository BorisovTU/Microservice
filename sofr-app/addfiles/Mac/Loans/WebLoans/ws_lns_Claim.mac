/*
$Name: ws_lns_Claim.mac
$Module: WebLoans
$Description: Сервис источника данных для получения информации по заявке
*/

IMPORT rsd, SbCrdInter, BankInter, RsbDataSet, globals, total, ws_lns_CrdContract, DCLAIM_DBT, ws_lns_CommonLib, ws_LoansOperation;
IMPORT ws_lns_Specific, ws_lns_PurposeForCB, ws_lns_ClaimEns, ws_lns_GraphGroup;
IMPORT mfo_lib, Календарь, cb_sql, likepy; 
IMPORT "ws_lns_ResponsOperHistory.mac", "ws_person.mac", "acsgroup_widgetclass.mac";

CLASS CLnsClaimViewPrm()
    VAR LabelStateText    : string  = "";         // Метка состояния заявки
    VAR UsNumber          : string  = "";         // Номер заявки
    VAR Filial            : string  = "";         // Филиал
    VAR ClaimStatus       : string  = "";         // Статус
    VAR RegDate           : Date    = Date(0,0,0);// Дата заявки
    VAR TerritorialStruct : string  = "";         // Подразделение
    VAR ClientStatus      : string  = "";         // Статус клиента
    VAR ClientSumCredit   : Money   = $0;         // Клиент: Сумма кредита
    VAR ClientSumCreditFI : string  = "";         // Клиент: Валюта
    VAR ClientDuration    : integer = 0;          // Клиент: Срок кредита
    VAR ClientTypeDuration: string  = "";         // Клиент: Тип срока кредита
    VAR ClientPeriod      : integer = 0;          // Клиент: Период погашения
    VAR ClientTypePeriod  : string  = "";         // Клиент: Тип периода погашения
    VAR ClientRate        : double  = 0.0;        // Клиент: Процентная ставка
    VAR ClientPurpose     : string  = "";         // Клиент: Цель кредита
    VAR ClientTypeCredit  : string  = "";         // Клиент: Тип кредита
    VAR ClientDescr       : string  = "";         // Клиент: Описание кредита
    VAR ClientCost        : Money   = $0;         // Клиент: Стоимость объекта
    VAR ClientCostFI      : string  = "";         // Клиент: Валюта
    VAR ClientOwnFunds    : Money   = $0;         // Клиент: Собственные средства
    VAR ClientOwnFundsFI  : string  = "";         // Клиент: Валюта
    VAR BankMRK           : Money   = $0;         // Банк: Максимальный кредит
    VAR BankSumCredit     : Money   = $0;         // Банк: Сумма кредита
    VAR BankSumCreditFI   : string  = "";         // Банк: Валюта
    VAR BankSumPayment    : Money   = $0;         // Банк: Сумма платежа
    VAR BankDuration      : integer = 0;          // Банк: Срок кредита
    VAR BankTypeDuration  : string  = "";         // Банк: Тип срока
    VAR BankPeriod        : integer = 0;          // Банк: период погашения
    VAR BankTypePeriod    : string  = "";         // Банк: тип периода
    VAR BankTCR           : string  = "";         // Банк: Вид кредита
    VAR BankRate          : string  = "";         // Банк: Ставки тарифов
    VAR BankTariffPlan    : string  = "";         // Банк: Тарифный план
    VAR BankTariffPlanCond: string  = "";         // Банк: Условие тарифной сетки
    VAR BankPurposeForCB  : string  = "";         // Банк: Цели кредита для ЦБ
    VAR BankClaimRout     : string  = "";         // Банк: Маршрут рассмотрения
    VAR BankOpeningPeriod : integer = 0;          // Банк: Срок открытия договора
    VAR BankOpeningTypePeriod: string = "";       // Банк: Тип срока: рабочих дней/календарных дней
    VAR BankOpeningDate             = NULL;       // Банк: Дата открытия договора
    VAR ComitetNumber     : string  = "";         // Номер кредитного комитета
    VAR ComitetDate       : Date    = Date(0,0,0);// Дата комитета
    VAR ComitetAnswer     : string  = "";         // Решение комитета
    VAR Comment           : string  = "";         // Примечание
    VAR ProductNumber     : string  = "";         // Номер продукта
    VAR ProductDate       : Date    = Date(0,0,0);// Дата оформления продукта
    VAR ProductStatus     : string  = "";         // Статус продукта
    VAR CanOpenProduct    : string  = "";         // Возможность открытия Продукта из заявки
    VAR WithTariffPlan    : bool = false;         // Исползование тарифных планов
    VAR ResponsOperName   : string  = "";         // ф.и.о. сотрудника, ответственного за заявку    
    VAR GroupOperName     : string  = "Не задана";// Название группы пользователей по заявке
    VAR HasDecision       : string  = "";         // Признак наличия решения кредитного отдела
    VAR StateSupportSum   : Money   = $0;         // Сумма господдержки
    VAR StateSupportSumFI : string  = "";         // Валюта господдержки
    VAR StateSupportType  : string  = "";         // Тип господдержки
    VAR ProductType       : string  = "";         // Тип продукта
    VAR ClientCategory    : string  = "";         // Категория клиента
    VAR CanEditClaim      : bool    = false;      // Возможность редактирования заявки
    VAR HasRouteStep      : bool    = false;      // Выполнен хотя бы один шаг маршрута по заявке
    VAR HasOtherDebtor    : bool    = false;      // По вк установлен признак "Созаемщики"
    VAR HasStateSupport   : bool    = false;      // По вк установлен  признак "Учет средств господдержки"
    VAR HasSelfSum        : bool    = false;      // По ВК установлен признак "первоначальный взнос"
END;

CLASS CLnsClaimEditPrm()
    VAR Claim = null;                       // параметры заявки
    VAR PurposeForCBIDList =  TArray();     // список идентификаторов целей кредитования для цб.
    VAR ResponsOperObj : TWsPerson    = TWsPerson();   // Объект ответственного пользователя (для листалки операционистов в поле Сотрудник)
    VAR ResponsOperID  : integer      = 0;      
    VAR GroupOperObj   : TWebAcsGroup = TWebAcsGroup();// Объект группы пользователей по заявке (для листалки в поле Группа сотрудников)
    VAR GroupOperID    : integer      = 0;          
END;

// получение категории клиента
PRIVATE MACRO GetClientCategory(ClientID)
    var result = 0;
    CaptureOutput;
    [
        SELECT T_ATTRID 
        FROM DOBJATCOR_DBT
        WHERE t_objecttype = ?
          AND t_groupid = ?
          AND t_object = ?
          AND t_general = ?
    ];
    var qStr = StopCaptureOutput;
    var cmd = RsdCommand(qStr);
    cmd.AddParam("p_objecttype", RSDBP_IN, 3);
    cmd.AddParam("p_groupid",    RSDBP_IN, 81);
    cmd.AddParam("p_object",     RSDBP_IN, ClientID);
    cmd.AddParam("p_general",    RSDBP_IN, "X");
    cmd.execute();
    var rs = TRsbDataSet(cmd);
    if (rs.moveNext)
        result = rs.ATTRID;
    end;
    return result;
    OnError(err)
        return result;
END;

MACRO lns_GetGroupOperNameByID (GroupOperID : integer)
    var query = "SELECT GR.T_NAME FROM DACSGROUP_DBT GR WHERE GR.T_GROUPID = " + GroupOperID;
    return LnSelectValue(query, V_STRING, 0);
END;

// получение параметров для заявки в ВЕБ кредитовании.
MACRO lns_GetNewClaimViewPrm(ObjN)
    var result = CLnsClaimViewPrm(),
        rs = NULL,
        qStr = "",
        credit = null;
    CaptureOutput;    
    [
    SELECT cl.*,
           clstat.t_Name as t_ClaimStatus,
           clstst.t_IsEdit as t_IsEdit,
           ficls.t_ccy as t_ClientSumCreditFI,
           ficlc.t_ccy as t_ClientCostFI,
           ficlo.t_ccy as t_ClientOwnFundsFI,
           fibas.t_ccy as t_BankSumCreditFI,
           fissup.t_ccy as t_StateSupportFI,
           cltdur.t_szNameAlg as t_ClientTypeDuration,
           cltper.t_szNameAlg as t_ClientTypePeriod,
           batdur.t_szNameAlg as t_BankTypeDuration,
           batper.t_szNameAlg as t_BankTypePeriod,
           opper.t_szNameAlg  as t_BankOpeningTypePeriod,
           case cl.t_TypeDebtor
               when 1 then 'Индивидуальный предприниматель'
               when 2 then 'Физическое лицо'
           end as t_ClientStatus,
           typecrd.t_CreditTypeName,
           typecrd.t_OtherDebtor,
           typecrd.t_isstatesupport,
           typecrd.t_isclntownfunds,
           prtype.t_TypeName as t_ProductType,
           reqtype.t_Name as t_RecCrdTypeName,
           ratealg.t_szNameAlg as TuneTypeName,
           trffplan.t_trffplanname planname, 
           cond.t_condname as t_condname,
           tplan.t_flagtpwconditions t_withtariffplan, 
           tplan.t_tplantcrid t_trffplanid,
           credcom.t_credcomnumber,
           credcom.t_credcomdate,
           route.t_processname as t_RouteName,
           credcomdec.t_name as t_credcomdecisions,
           credit.t_uscreditnumber as t_creditnumber,
           credit.t_regdate as t_creditdate,
           case credit.t_creditstate
               when 'О' then 'Договор открыт'
               when 'З' then 'Договор закрыт'
               when 'Ч' then 'Черновик открыт'
               when 'Д' then 'Черновик закрыт'
           end as t_creditstate,
           attr.t_name as t_ClientCategory,
           RSO_LOANS_NBKI.MaxCreditOpenDate (CL.T_APPROVALDATE,
                                             CL.T_DAYSBEFORECREDIT,
                                             CL.T_CREDITTYPEID_REF) as t_BankOpeningDate

    FROM dclaim_dbt cl

    JOIN dlclaimstatus_dbt clstat ON cl.t_claimstate = clstat.t_id
    JOIN dlclaimstagestatus_dbt clstst ON clstst.t_claimstageid = cl.t_stageid AND
                                          clstst.t_claimstatusid = cl.t_claimstate
    JOIN dfininstr_dbt ficls    ON cl.t_lcreditcurcode      = ficls.t_fiid
    JOIN dfininstr_dbt ficlc    ON cl.t_sumobjectcurcode    = ficlc.t_fiid
    JOIN dfininstr_dbt ficlo    ON cl.t_lselfcurcode        = ficlo.t_fiid
    JOIN dfininstr_dbt fibas    ON cl.t_bcreditcurcode      = fibas.t_fiid
    JOIN dfininstr_dbt fissup   ON cl.t_statesupportcurcode = fissup.t_fiid
    JOIN dnamealg_dbt  cltdur   ON cl.t_lctypeduration      = cltdur.t_iNumberAlg  AND cltdur.t_iTypeAlg = 987
    JOIN dnamealg_dbt  cltper   ON cl.t_lptypeduration      = cltper.t_iNumberAlg  AND cltper.t_iTypeAlg = 987
    JOIN dnamealg_dbt  batdur   ON cl.t_bctypeduration      = batdur.t_iNumberAlg  AND batdur.t_iTypeAlg = 987
    JOIN dnamealg_dbt  batper   ON cl.t_bptypeduration      = batper.t_iNumberAlg  AND batper.t_iTypeAlg = 987
    JOIN dnamealg_dbt  ratealg  ON cl.t_TuneType            = ratealg.t_iNumberAlg AND ratealg.t_iTypeAlg = 1008
    JOIN dtype_crd_dbt typecrd  ON cl.t_credittypeid_ref    = typecrd.t_CreditTypeID
    JOIN dnamealg_dbt  opper    ON typecrd.t_typeperiod     = opper.t_iNumberAlg   AND opper.t_iTypeAlg = 1031
    JOIN dlclaimroute_dbt route ON cl.t_processname         = route.t_processid

    LEFT JOIN dcredit_c_dbt     credit     ON cl.t_creditnumber_ref = credit.t_creditnumber
    LEFT JOIN dlreqcrdtype_dbt  reqtype    ON cl.t_reqcrdtypeid     = reqtype.t_reqcrdtypeid
    LEFT JOIN dtplan_tcr_dbt    tplan      ON cl.t_tplantcrid       = tplan.t_tplantcrid
    LEFT JOIN dltrffplan_dbt    trffplan   ON tplan.t_trffplanid    = trffplan.t_trffplanid
    LEFT JOIN dcond_tplan_dbt   cond       ON cl.t_condid           = cond.t_condid
    LEFT JOIN dcred_com_dbt     credcom    ON cl.t_credcomid_ref    = credcom.t_credcomid
    LEFT JOIN dlproducttype_dbt prtype     ON typecrd.t_ProductType = prtype.t_id
    LEFT JOIN dllvalues_dbt     credcomdec ON cl.t_credcomdecision  = credcomdec.t_element AND
                                              credcomdec.t_list = 1401
    LEFT JOIN dobjattr_dbt      attr       ON cl.t_client_category  = attr.t_attrid AND 
                                              attr.t_objecttype = 3 AND 
                                              attr.t_groupid    = 81
    WHERE cl.t_claimid = #
    ](string(ObjN));
    qStr = StopCaptureOutput;
    rs = TRsbDataSet(qStr);
    if (rs.moveNext)
        //result.LabelStateText    = 
        result.UsNumber            = rs.ClaimNumber;
        result.Filial              = rs.FnCash;
        result.ClaimStatus         = rs.ClaimStatus;
        result.RegDate             = rs.GivingDate;
        result.TerritorialStruct   = rs.TerritorialStruct;
        result.ClientSumCredit     = rs.lCreditSum;
        result.ClientSumCreditFI   = rs.ClientSumCreditFI;
        result.ClientDuration      = rs.lCreditDuration;
        result.ClientTypeDuration  = rs.ClientTypeDuration;
        result.ClientPeriod        = rs.lPayDuration;
        result.ClientTypePeriod    = rs.ClientTypePeriod;
        result.ClientRate          = rs.lPercRate;
        result.ClientTypeCredit    = rs.RecCrdTypeName;
        result.ClientDescr         = rs.lCreditPurpose;
        result.ClientCost          = rs.SumObject;
        result.ClientCostFI        = rs.ClientCostFI;
        result.ClientOwnFunds      = rs.lSelfSum;
        result.ClientOwnFundsFI    = rs.ClientOwnFundsFI;
        result.BankMRK             = rs.MRK;
        result.BankSumCredit       = rs.bCreditSum;
        result.BankSumCreditFI     = rs.BankSumCreditFI;
        result.BankSumPayment      = rs.bPaySum;
        result.BankDuration        = rs.bCreditDuration;
        result.BankTypeDuration    = rs.BankTypeDuration;
        result.BankPeriod          = rs.bPayDuration;
        result.BankTypePeriod      = rs.BankTypePeriod;
        result.BankTCR             = rs.CreditTypeName; 
        result.BankRate            = rs.TuneTypeName;
        result.BankClaimRout       = rs.RouteName;
        result.BankOpeningPeriod   = rs.DaysBeforeCredit;
        result.BankOpeningTypePeriod = rs.BankOpeningTypePeriod;
        result.BankOpeningDate     = rs.BankOpeningDate;
        result.ComitetNumber       = rs.CredComNumber;
        result.ComitetDate         = rs.CredComDate;
        result.ComitetAnswer       = rs.CredComDecisions; 
        result.Comment             = rs.Note;
        result.CanOpenProduct      = rs.CanOpenProduct;
        result.ProductNumber       = rs.CreditNumber;
        result.ProductDate         = rs.CreditDate;
        result.ProductStatus       = rs.CreditState;

        result.StateSupportSum     = rs.StateSupportSum;
        result.StateSupportSumFI   = rs.StateSupportFI;
        result.StateSupportType    = rs.StateSupportType;
        result.ProductType         = rs.ProductType;
        result.ClientCategory      = rs.ClientCategory;

        result.CanEditClaim        = rs.IsEdit == "X";
        result.HasOtherDebtor      = rs.OtherDebtor == "X";
        result.HasRouteStep        = rs.ProcessParm != "";
        result.WithTariffPlan      = rs.WithTariffPlan == "X";
        result.HasStateSupport     = rs.IsStateSupport == "X";
        result.HasSelfSum          = rs.IsClntOwnFunds == "X";
        result.ClientStatus        = IfThenElse(rs.typedebtor != rs.legalform, rs.ClientStatus, "");
        
        var groupOperNameStr       = lns_GetGroupOperNameByID (rs.GroupNum);
        result.GroupOperName       = IfThenElse(groupOperNameStr == "", "Не задана", groupOperNameStr);
        
        result.ClientPurpose       = GetSpecificNameByStr(String(rs.Specific));

        result.HasDecision = rs.CanOpenProduct;

        result.BankPurposeForCB    = GetPurposeStrByObjID(LO_CLAIM, ObjN);
        
        var tp = GetTariffPlan(LO_CLAIM, ObjN);
        
        result.BankTariffPlan      = tp.Plan.Name;
        result.BankTariffPlanCond  = tp.Cond.Name;
       
        //определение ф.и.о. сотрудника, ответственного за заявку
        VAR ResponsOperObj : TWsPerson = TWsPerson();
        ResponsOperObj = Lns_GetResponsOperObj(ObjN, LO_CLAIM, {curdate});
        if (ResponsOperObj != NULL)
            result.ResponsOperName = ResponsOperObj.Name_;
        end;
    end;
    return result;
END;

// получение параметров "старой" заявки
MACRO lns_GetOldClaimViewPrm(ObjN)
    var result = CLnsClaimViewPrm(),
        rs = NULL,
        qStr = "",
        credit = null;
    CaptureOutput;    
    [
    SELECT cl.*,
           clstat.t_name as t_ClaimStatus,
           ficls.t_ccy as t_ClientSumCreditFI,
           ficlc.t_ccy as t_ClientCostFI,
           ficlo.t_ccy as t_ClientOwnFundsFI,
           fibas.t_ccy as t_BankSumCreditFI,
           cltdur.t_szNameAlg as t_ClientTypeDuration,
           cltper.t_szNameAlg as t_ClientTypePeriod,
           batdur.t_szNameAlg as t_BankTypeDuration,
           batper.t_szNameAlg as t_BankTypePeriod,
           case cl.t_TypeDebtor
               when 1 then 'Индивидуальный предприниматель'
               when 2 then 'Физическое лицо'
           end as t_ClientStatus,
           typecrd.t_CreditTypeName,
           reqtype.t_Name as t_RecCrdTypeName,
           ratealg.t_szNameAlg as TuneTypeName,
           trffplan.t_trffplanname planname, 
           cond.t_condname as t_condname,
           tplan.t_flagtpwconditions t_withtariffplan, 
           tplan.t_tplantcrid t_trffplanid,
           credit.t_uscreditnumber as t_creditnumber,
           credit.t_regdate as t_creditdate,
           case credit.t_creditstate
               when 'О' then 'Договор открыт'
               when 'З' then 'Договор закрыт'
               when 'Ч' then 'Черновик открыт'
               when 'Д' then 'Черновик закрыт'
           end as t_creditstate
    FROM dclaim_dbt cl,
         dcredit_c_dbt credit,
         dfininstr_dbt ficls,
         dfininstr_dbt ficlc, 
         dfininstr_dbt ficlo,
         dfininstr_dbt fibas, 
         dnamealg_dbt  cltdur,
         dnamealg_dbt  cltper,
         dnamealg_dbt  batdur, 
         dnamealg_dbt  batper,
         dnamealg_dbt  ratealg,
         dtype_crd_dbt typecrd,
         dllvalues_dbt clstat,
         dlreqcrdtype_dbt reqtype,
         dtplan_tcr_dbt tplan, 
         dltrffplan_dbt trffplan, 
         dcond_tplan_dbt cond
         
    WHERE cl.t_credittypeid_ref = typecrd.t_CreditTypeID
      AND cl.t_reqcrdtypeid = reqtype.t_reqcrdtypeid
      AND cl.t_lcreditcurcode = ficls.t_fiid
      AND cl.t_sumobjectcurcode = ficlc.t_fiid
      AND cl.t_lselfcurcode = ficlo.t_fiid
      AND cl.t_bcreditcurcode = fibas.t_fiid
      AND (cltdur.t_iNumberAlg = cl.t_lctypeduration AND cltdur.t_iTypeAlg = 987)
      AND (cltper.t_iNumberAlg = cl.t_lptypeduration AND cltper.t_iTypeAlg = 987)
      AND (batdur.t_iNumberAlg = cl.t_bctypeduration AND batdur.t_iTypeAlg = 987)
      AND (batper.t_iNumberAlg = cl.t_bptypeduration AND batper.t_iTypeAlg = 987)
      AND (ratealg.t_iNumberAlg = cl.t_TuneType AND ratealg.t_iTypeAlg = 1008)
      AND (clstat.t_element = cl.t_claimstate AND clstat.t_list=1400)
      AND credit.t_creditnumber(+) = cl.t_creditnumber_ref
      AND tplan.t_tplantcrid(+) = cl.t_tplantcrid
      AND trffplan.t_trffplanid(+) = tplan.t_trffplanid
      AND cond.t_condid(+) = cl.t_condid 
    ];
    qStr = StopCaptureOutput;
    qStr = qStr + " AND cl.t_claimid = " + ObjN;
    rs = TRsbDataSet(qStr);
    if (rs.moveNext)
        result.LabelStateText      = NULL;
        result.UsNumber            = rs.ClaimNumber;
        result.Filial              = rs.FnCash;
        result.ClaimStatus         = rs.ClaimStatus;
        result.RegDate             = rs.GivingDate;
        result.TerritorialStruct   = NULL;
        result.ClientSumCredit     = rs.lCreditSum;
        result.ClientSumCreditFI   = rs.ClientSumCreditFI;
        result.ClientDuration      = rs.lCreditDuration;
        result.ClientTypeDuration  = rs.ClientTypeDuration;
        result.ClientPeriod        = rs.lPayDuration;
        result.ClientTypePeriod    = rs.ClientTypePeriod;
        result.ClientRate          = rs.lPercRate;
        result.ClientTypeCredit    = rs.RecCrdTypeName;
        result.ClientDescr         = rs.lCreditPurpose;
        result.ClientCost          = rs.SumObject;
        result.ClientCostFI        = rs.ClientCostFI;
        result.ClientOwnFunds      = rs.lSelfSum;
        result.ClientOwnFundsFI    = rs.ClientOwnFundsFI;
        result.BankMRK             = rs.MRK;
        result.BankSumCredit       = rs.bCreditSum;
        result.BankSumCreditFI     = rs.BankSumCreditFI;
        result.BankSumPayment      = rs.bPaySum;
        result.BankDuration        = rs.bCreditDuration;
        result.BankTypeDuration    = rs.BankTypeDuration;
        result.BankPeriod          = rs.bPayDuration;
        result.BankTypePeriod      = rs.BankTypePeriod;
        result.BankTCR             = rs.CreditTypeName; 
        result.BankRate            = rs.TuneTypeName;
        result.BankOpeningPeriod   = rs.DaysBeforeCredit;
        result.Comment             = rs.Note;
        result.CanEditClaim        = false;
        result.ProductNumber       = rs.CreditNumber;
        result.ProductDate         = rs.CreditDate;
        result.ProductStatus       = rs.CreditState;
        result.WithTariffPlan      = rs.WithTariffPlan == "X";
        result.ClientStatus        = IfThenElse(rs.typedebtor != rs.legalform, rs.ClientStatus, "");
        
        var groupOperNameStr       = lns_GetGroupOperNameByID (rs.GroupNum);
        result.GroupOperName       = IfThenElse(groupOperNameStr == "", "Не задана", groupOperNameStr);
        
        result.ClientPurpose       = GetSpecificNameByStr(String(rs.Specific));

        if (Date(rs.ApprovalDate) == Date(0,0,0))
            result.BankOpeningDate = NULL;
        else
            result.BankOpeningDate = DateAfterWorkDays(Date(rs.ApprovalDate), Int(rs.DaysBeforeCredit));
        end;
        result.HasDecision = "X";

        result.BankPurposeForCB    = GetPurposeStrByObjID(LO_CLAIM, ObjN);
        
        var tp = GetTariffPlan(LO_CLAIM, ObjN);
        
        result.BankTariffPlan      = tp.Plan.Name;
        result.BankTariffPlanCond  = tp.Cond.Name;
         
        //определение ф.и.о. сотрудника, ответственного за заявку
        VAR ResponsOperObj : TWsPerson = TWsPerson();
        ResponsOperObj = Lns_GetResponsOperObj(ObjN, LO_CLAIM, {curdate});
        if (ResponsOperObj != NULL)
            result.ResponsOperName = ResponsOperObj.Name_;
        end;
    end;
    return result;
END;

MACRO lns_GetClaimViewPrm(ObjN)
    if (GetClaimType(ObjN) == LNS_MT_WEBCREDIT)
        return lns_GetNewClaimViewPrm(ObjN)
    else
        return lns_GetOldClaimViewPrm(ObjN);
    end;
END;


MACRO getSelectClaimStr()
var QStr = "";
    CaptureOutput;
    [
    SELECT  t_claimid, t_claimnumber, t_loanerid_ref, t_creditnumber_ref, 
            t_claimstate, t_givingdate, t_approvaldate, t_rejectiondate, 
            t_lcreditsum, t_lcreditcurcode, t_lselfsum, t_lselfcurcode, 
            t_lcreditduration, t_lctypeduration, t_lpayduration, t_lptypeduration, 
            t_lpercrate, t_lcreditpurpose, t_bcreditsum, t_bcreditcurcode, t_bpaysum, 
            t_bpaycurcode, t_bcreditduration, t_bctypeduration, t_bpayduration, 
            t_bptypeduration, t_bpercrate, t_credittypeid_ref, t_rejectmotive, 
            t_groupnum, t_sumobject, t_sumobjectcurcode, t_credcomid_ref, 
            t_credcomdecision, t_note, t_fncash, t_mrk, t_tunetype, 
            t_kind_operation, t_status, t_blockoper, t_comment, t_claimkind, 
            t_daysbeforecredit, t_accountkarta, t_beneficiary, t_typedebtor, 
            t_tplantcrid, t_condid, t_reqcrdtypeid, t_specific, t_territorialstruct, 
            t_processname, t_stageid, t_canopenproduct, t_moduletype, t_state, 
            t_legalform, t_statesupportsum, t_statesupportcurcode, t_statesupporttype, 
            t_lgraphtype, t_lpayday, t_lpaypercdur, t_lpayperctypedur, t_lpaypercday, 
            t_lissuedate, t_bgraphtype, t_bpayday, t_bpaypercdur, t_bpayperctypedur, t_bpaypercday, t_bissuedate,
            t_isManyBeneficiary, t_isAbsolute, t_product_type, t_client_category, t_rejreason, t_nextaction
    FROM dclaim_dbt
    ];
    QStr = StopCaptureOutput;
    return QStr;
END;

MACRO lns_GetClaimEditPrm(ObjN)
    var qStr = "",
        rs = null,
        result = CLnsClaimEditPrm();
    qStr = getSelectClaimStr() + " where t_claimid = " + ObjN;
    rs = RsdRecordset(qStr);
    if (rs.MoveNext)
        result.claim = GetObjectFromRecordset("DCLAIM_DBT", rs);
        result.PurposeForCBIDList = GetPurposeForCBIDList(LO_CLAIM, ObjN);
        VAR ResponsOperObj : TWsPerson = TWsPerson();
            ResponsOperObj = Lns_GetResponsOperObj(ObjN, LO_CLAIM, {curdate});        
        result.ResponsOperObj     = ResponsOperObj;
        result.ResponsOperID      = ResponsOperObj.Number;
    end;
    return result;
END;

MACRO lns_InitNewClaim(ClientID)
    var Claim = DCLAIM_DBT(),
        rs = null,
        qStr = "",
        result = CLnsClaimEditPrm();
    
    Claim.client_category   = GetClientCategory(ClientID); // Категория заемщика
    Claim.loanerid_ref      = ClientID;             // Идентификатор субъекта
    Claim.givingdate        = {curdate};            // Дата подачи заявки

    Claim.lcreditduration   = 12;                   // Заемщик :Срок кредита
    Claim.lctypeduration    = 0;                    // Заемщик :тип  срока
    Claim.lpayduration      = 1;                    // Заемщик :Период погашения
    Claim.lptypeduration    = 0;                    // Заемщик :Тип периода

    Claim.bcreditduration   = 12;                   // Банк       :Срок кредита
    Claim.bctypeduration    = 0;                    // Банк       :Тип срока
    Claim.bpayduration      = 1;                    // Банк       :Период платежа
    Claim.bptypeduration    = 0;                    // Банк       :Тип периода
    Claim.lpayday           = 10;                   // Заемщик: Число месяца для погашения ОД
    Claim.lpaypercdur       = Claim.lpayduration;   // Заемщик: Период погашения процентов
    Claim.lpayperctypedur   = Claim.lptypeduration; // Заемщик: Тип периода процентов
    Claim.lpaypercday       = Claim.lpayday;        // Заемщик: Число месяца для погашения процентов
    Claim.lissuedate        = {curdate};            // Заемщик: Дата выдачи
    Claim.bpayday           = 10;                   // Банк: Число месяца для погашения ОД
    Claim.bpaypercdur       = Claim.bpayduration;   // Банк: Период погашения процентов
    Claim.bpayperctypedur   = Claim.bptypeduration; // Банк: Тип периода процентов
    Claim.bpaypercday       = Claim.bpayday;        // Банк: Число месяца для погашения процентов
    Claim.bissuedate        = {curdate};            // Банк: Дата выдачи

    Claim.processname       = "";
    Claim.fncash            = {OperDprt};           // Филиал
    Claim.territorialstruct = {OperDprtNode};       // Подразделение
    Claim.moduletype        = LNS_MT_WEBCREDIT;     // заявка для модуля веб кредитование.
    Claim.state             = "О";                  // статус
    Claim.tunetype          = CF_GROUP;             // тип тарифов
    Claim.groupnum          = 0;
    
    rs = TRsbDataSet("SELECT * FROM dlclaimstagestatus_dbt WHERE t_isdefault = 'X'");
    if (rs.MoveNext())
        Claim.claimstate = rs.ClaimStatusID;
        Claim.stageid    = rs.ClaimStageID;
    else
        RunError("Не установлена стадия и статус для новой заявки!");
    end;

    qStr = "SELECT t_LegalForm FROM DPARTY_DBT WHERE T_PARTYID = " + ClientID;
    rs = TRsbDataSet(qStr);
    if (rs.MoveNext())
        if (rs.LegalForm == 1)
            Claim.typedebtor = 1;
            Claim.legalform = 1;
        else
            Claim.typedebtor = 2;
            Claim.legalform = 2;
        end;
    end;
    Claim.claimnumber = lns_GetGenerationNumberObject(LO_CLAIM, Claim);
    result.Claim = Claim;
    result.PurposeForCBIDList = TArray();
    result.ResponsOperID      = {oper};
    result.ResponsOperObj.Number = {oper};
    
    return result;
END;

MACRO lns_InitNewClaimByCreditType(CreditType, ClaimEditPrm)
    var result = ClaimEditPrm;
    var qStr = "", rs = NULL;
    qStr = "SELECT *" +
           " FROM DTYPE_CRD_DBT TCRD" + 
           " LEFT OUTER JOIN DLCLAIMROUTE_DBT RT" + 
           " ON TCRD.T_ROUTEID_DEFAULT = RT.T_ID" + 
           " WHERE TCRD.T_CREDITTYPEID = " + CreditType;
    rs = TRsbDataSet(qStr);
    if  (rs.moveNext())
        result.Claim.daysbeforecredit = rs.StandardDayNumber;
        result.Claim.specific = rs.specific;
        result.Claim.lcreditduration = rs.Duration;
        result.Claim.lctypeduration = rs.TypeDuration;
        if ((rs.IsAutoFindRoute == "X") and (rs.RouteID_Default != 0))
            result.Claim.processname = rs.ProcessID;
        else
            result.Claim.processname = "";        
        end;
    end;

    if ((ClaimEditPrm.PurposeForCBIDList == Null) or
        (ClaimEditPrm.PurposeForCBIDList.Size == 0))
        result.PurposeForCBIDList = GetPurposeForCBIDList(LO_TYPECREDIT, CreditType)
    end;

    var GroupList = Lns_GetGraphGroupList(CreditType);
    if ((GroupList != NULL) and (GroupList.Size > 0))
        result.claim.lgraphtype = GroupList[0].ID;
    end;

    result.claim.lpercrate = Double(ПолучитьСтавкуТарифа (LO_TYPECREDIT, ClaimEditPrm.claim.credittypeid_ref, TRU_CRD , {curdate}));

    return result;
END;

MACRO lns_InsertClaim(OpDate, ClaimEditPrm)
    var ResponseBuilder = WebResponseBuilder(),
        OperID = 0,
        ObjN = 0,
        LnsObjID = CLnsObjID();
    record claimBuff ("claim.dbt");

    CopyClassObjToRecord(ClaimEditPrm.Claim, claimBuff);
    OperID = MakeOperation(0, 0, CF_OPENCLAIM, 0, OpDate, claimBuff);
    if (OperID == 0)
        RunError(GetMO_LoansError())
    end;
    
    ObjN = LnSelectValue("SELECT t_objectid_ref FROM dcrd_op_dbt WHERE t_credoperid = " + OperID, V_INTEGER, 0);
    
    if (ClaimEditPrm.PurposeForCBIDList != Null)
        UpdatePurposeForCBList(LO_CLAIM, ObjN, ClaimEditPrm.PurposeForCBIDList);
    end;
    
    if (ClaimEditPrm.ResponsOperObj != NULL)
        Lns_InsertResponsOper(ObjN, LO_CLAIM, {curdate}, ClaimEditPrm.ResponsOperID);
    end;   
    
    LnsObjID.ObjID = LO_CLAIM;
    LnsObjID.ObjN = ObjN;
    
    ResponseBuilder.SetUserObject(LnsObjID);
    
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO lns_UpdateClaim(ClaimEditPrm)
    var ResponseBuilder = WebResponseBuilder(),
        LnsObjID = CLnsObjID();
    record claimBuff ("claim.dbt");

    CopyClassObjToRecord(ClaimEditPrm.Claim, claimBuff);
    if (UpdateCrdClaim(claimBuff) == 0)
        RunError(GetMO_LoansError());
    end;
    if (ClaimEditPrm.PurposeForCBIDList != Null)
        UpdatePurposeForCBList(LO_CLAIM, ClaimEditPrm.Claim.ClaimID, ClaimEditPrm.PurposeForCBIDList);
    end;
    
    LnsObjID.ObjID = LO_CLAIM;
    LnsObjID.ObjN = ClaimEditPrm.Claim.ClaimID;
    
    ResponseBuilder.SetUserObject(LnsObjID);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

// Открытие КД из заявки.
// глобализмы для передачи параметров в транзакцию.
PRIVATE VAR GlobalOpDate;
PRIVATE VAR GlobalClaimID;
PRIVATE VAR GlobalContractEditPrm;
PRIVATE VAR GlobalLnsObjID;
PRIVATE VAR GlobalErr;

MACRO OpenCrdFromClaimTrn
    record creditBuff ("credit_c.dbt");
    var stat = 0;
    var rs = NULL;
    var LnsObjID = NULL;
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    // получаем значение через глобализмы
    var ContractEditPrm = GlobalContractEditPrm;
    var ClaimID = GlobalClaimID;
    var OpDate = GlobalOpDate;
    var PostAction = PostActionData();
    
    //устанавливаем параметры постопераций
    PostAction.IsBorrowingRates = true;
    PostAction.IsGenerateGraphs = true;
    if (ContractEditPrm.credit.CREDITSTATE == CF_OPENDRAFT)
        PostAction.IsAccReserve = true;
        PostAction.IsAccOpen = false;
    elif (ContractEditPrm.credit.CREDITSTATE == CF_OPEN)
        PostAction.IsAccReserve = false;
        PostAction.IsAccOpen = true;
    end;
    PostAction.IsChangePayLimit = false;
    PostAction.IsChangeDebtLimit = false;
    PostAction.IsCalcPSK = false;
    PostAction.SumPayLimit = 0;
    PostAction.SumDebtLimit = 0;
    // открываем КД
    var response = lns_InsNewContract(OpDate, ContractEditPrm, PostAction);
    if (response.UserObject != NULL)
        LnsObjID = response.UserObject;
        Lns_MakeEnsContrFromClaim(LnsObjID.ObjN, ClaimID, OpDate);

        LnSqlExec("UPDATE dclaim_dbt set t_creditnumber_ref = " + LnsObjID.ObjN + 
                  ", t_state= 'З' WHERE t_claimid = " + ClaimID);
        GlobalLnsObjID = LnsObjID;
    else
        RunError("Ошибка открытия кредитного договора", response.ErrorMessages[0].Message);
    end;

    return true;

    OnError(err)
        var str1 : string = "";
        var str2 : string = "";        

        result.MsgID = err.Code;
        if (err.err == NULL)
            str1 = StrSubst (err.Message, "Error:", "" );
            str2 = StrSubst (str1, "Ошибка выполнения", "" );
            result.MsgText = str2;
        else
            str1 = StrSubst (String(err.err), "Error:", "" );
            str2 = StrSubst (str1, "Ошибка выполнения", "" );

            result.MsgText = str2;
        end;
        GlobalErr = result;
        return false;
END;

MACRO lns_OpenCrdFromClaim(OpDate, ClaimID, ContractEditPrm)
    var result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;

    // заполняем глобализмы для передачи в функцию, вызываемую внутри транзакции
    GlobalOpDate = OpDate;
    GlobalClaimID = ClaimID;
    GlobalContractEditPrm = ContractEditPrm;
    GlobalLnsObjID = NULL; // глобализм для возвращаемого из транзакции значения
    GlobalErr = TWsMessage();
    if(not RtTrn("OpenCrdFromClaimTrn"))
        result = GlobalErr;
    end;
    GlobalOpDate = null;
    GlobalClaimID = null;
    GlobalContractEditPrm = null;
    return result;
END;

// утверждение заявки
MACRO ClaimApprove(ClaimID)
    var claim = TBFile ("claim.dbt", "rw", 0, "claim.dbt", "loans.def");
    claim.rec.claimid = ClaimID;
    var stat = claim.GetEQ();
    if (stat)
        claim.rec.approvaldate = {curdate};
        claim.update();
    end;
END;

// отклонение заявки
MACRO ClaimReject(ClaimID, RejReason, RejComment)
    var claim = TBFile ("claim.dbt", "rw", 0, "claim.dbt", "loans.def");
    claim.rec.claimid = ClaimID;
    var stat = claim.GetEQ();
    if (RejReason == NULL) RejReason = 0; end;
    if (RejComment == NULL) RejComment = ""; end;
    if (stat)
        claim.rec.rejectiondate = {curdate};
        claim.rec.rejreason = RejReason;
        claim.rec.rejectmotive = RejComment;
        claim.update();
    end;
END;

