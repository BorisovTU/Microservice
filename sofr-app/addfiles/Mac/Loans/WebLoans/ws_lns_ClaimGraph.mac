/*
$Name: ws_lns_ClaimGraph.mac
$Module: WebLoans
$Description: Сервис для работы с графиками в заявках
*/

IMPORT SbCrdInter, RsbDataSet, ws_file, ws_validation, likepy, planpay_attr_glob, planpay, fmt_lib, total, xirr, DCLAIM_DBT; 

CLASS CLnsClaimGraphGridItem()
    VAR PayID   : integer  = 0;  // Идентификатор
    VAR PayDate : DateTime = Date(0,0,0); // Дата платежа
    VAR PayDelay: integer  = 0;  // Просрочка
    VAR SumOD   : Money    = $0; // Сумма основного долга
    VAR SumPerc : Money    = $0; // Сумма процентов
    VAR Sum     : Money    = $0; // Общая сумма погашения
END;

CLASS CLnsClaimGraphInfo()
    VAR PaySumFrom: Money  = $0;   // Сумма платежа ОТ
    VAR PaySumTo  : Money  = $0;   // Сумма платежа ДО
    VAR TotalSum  : Money  = $0;   // Общая сумма выплат
    VAR OverPaySum: Money  = $0;   // Сумма переплаты
    VAR PSK       : string = "";   // ПСК (строка потому что это значение только для отображения и так удобнее управлять точностью)
    VAR Currency  : String = "";   // Валюта
    VAR GraphList : TArray = NULL; // График погашения
END;

CLASS CLnsClaimGraphParm()
    VAR ObjID             : integer = 0;          // Идентификатор объекта Loans
    VAR ObjN              : integer = 0;          // Номер объекта Loans
    VAR CreditType        : integer = 0;          // Вид кредита
    VAR GraphType         : integer = 0;          // Группа графиков
    VAR CreditSum         : Money   = $0;         // Сумма кредита
    VAR CurCode           : integer = 0;          // Валюта кредита
    VAR CreditDuration    : integer = 0;          // Срок кредита
    VAR TypeDuration      : integer = 0;          // Тип срока
    VAR PayDate           : DateTime= Date(0,0,0);// Дата выдачи
    VAR Perc              : Double  = 0.0;        // Процентная ставка
    VAR DutyPeriodOD      : integer = 0;          // Период погашения ОД
    VAR DutyTypePeriodOD  : integer = 0;          // Тип периода погашения ОД
    VAR DutyDayOD         : integer = 0;          // День месяца для погашения ОД
    VAR DutyPeriodPerc    : integer = 0;          // Период погашения процентов
    VAR DutyTypePeriodPerc: integer = 0;          // Тип периода погашения процентов
    VAR DutyDayPerc       : integer = 0;          // День месяца для погашения процентов
END;

CLASS CLnsClaimGraphViewParm() // параметры расчета графика для просмотра
    VAR ObjID             : integer = 0;          // Идентификатор объекта Loans
    VAR ObjN              : integer = 0;          // Номер объекта Loans
    VAR GraphType         : string  = "";         // Группа графиков
    VAR CreditSum         : Money   = $0;         // Сумма кредита
    VAR CurCode           : string  = "";         // Валюта кредита
    VAR CreditDuration    : integer = 0;          // Срок кредита
    VAR TypeDuration      : string  = "";         // Тип срока
    VAR PayDate           : DateTime= Date(0,0,0);// Дата выдачи
    VAR Perc              : Double  = 0.0;        // Процентная ставка
    VAR DutyPeriodOD      : integer = 0;          // Период погашения ОД
    VAR DutyTypePeriodOD  : string  = "";         // Тип периода погашения ОД
    VAR DutyDayOD         : integer = 0;          // День месяца для погашения ОД
    VAR DutyPeriodPerc    : integer = 0;          // Период погашения процентов
    VAR DutyTypePeriodPerc: string  = "";         // Тип периода погашения процентов
    VAR DutyDayPerc       : integer = 0;          // День месяца для погашения процентов
END;


PRIVATE MACRO ClearTempGraphTables()
    ClearTable("DPLAN_PAY_TMP");
    ClearTable("DGRAPHTREEPARM_TMP");
    ClearTable("DGRAPHOBJECT_TMP");        
    ClearTable("DGRAPHKINDPARM_TMP");
    ClearTable("DKINDGRAPHS_TMP");
    ClearTable("DGRAPH_GRACEPERIOD_TMP");
    ClearTable("DPLANLINK_TMP");
END;


// функция получает парараметры, по которым считается график заявки
// параметры ДЛЯ ПРОСМОТРА!
// IsAccepted - нужен чтоб не лезть ещё раз в DCLAIM_DBT
MACRO Lns_GetClaimGraphViewPrm(ClaimID, IsAccepted)
    var result = CLnsClaimGraphViewParm();
    CaptureOutput;
    if (IsAccepted) // если заявка одобрена, достаем параметры из тех, что предлагает банк
        [
            SELECT cl.t_claimid,
                cl.t_bcreditsum      as t_creditsum,
                cl.t_bcreditduration as t_creditduration,
                cl.t_bissuedate      as t_issuedate,
                cl.t_bpercrate       as t_percrate,
                cl.t_bpayduration    as t_payduration,
                cl.t_bpayday         as t_payday,
                cl.t_bpaypercdur     as t_paypercdur,
                cl.t_bpaypercday     as t_paypercday,
                gk.t_name            as t_GraphTypeName,
                fi.t_ccy             as t_ClaimCurCode,
                cltdur.t_szNameAlg   as t_CreditTypeDur,
                dper.t_szNameAlg     as t_DutyTypePeriod,
                dpper.t_szNameAlg    as t_DutyPercTypePeriod
            FROM DCLAIM_DBT cl
            LEFT JOIN DGRAPHKIND_DBT gk   ON gk.t_id             = cl.t_bgraphtype
            LEFT JOIN DNAMEALG_DBT cltdur ON cltdur.t_iNumberAlg = cl.t_bctypeduration
            LEFT JOIN DNAMEALG_DBT dper   ON dper.t_iNumberAlg   = cl.t_bptypeduration
            LEFT JOIN DNAMEALG_DBT dpper  ON dpper.t_iNumberAlg  = cl.t_bpayperctypedur
            LEFT JOIN DFININSTR_DBT fi    ON fi.t_fiid           = cl.t_bcreditcurcode
            WHERE cltdur.t_iTypeAlg = 987
            AND dper.t_iTypeAlg   = 987
            AND dpper.t_iTypeAlg  = 987
            AND cl.t_claimid = #
        ](string(ClaimID));
    else // если заявка не одобрена, достаем параметры из тех, что просит клиент
        [
            SELECT cl.t_claimid,
                cl.t_lcreditsum      as t_creditsum,
                cl.t_lcreditduration as t_creditduration,
                cl.t_lissuedate      as t_issuedate,
                cl.t_lpercrate       as t_percrate,
                cl.t_lpayduration    as t_payduration,
                cl.t_lpayday         as t_payday,
                cl.t_lpaypercdur     as t_paypercdur,
                cl.t_lpaypercday     as t_paypercday,
                gk.t_name            as t_GraphTypeName,
                fi.t_ccy             as t_ClaimCurCode,
                cltdur.t_szNameAlg   as t_CreditTypeDur,
                dper.t_szNameAlg     as t_DutyTypePeriod,
                dpper.t_szNameAlg    as t_DutyPercTypePeriod
            FROM DCLAIM_DBT cl
            LEFT JOIN DGRAPHKIND_DBT gk   ON gk.t_id             = cl.t_lgraphtype
            LEFT JOIN DNAMEALG_DBT cltdur ON cltdur.t_iNumberAlg = cl.t_lctypeduration
            LEFT JOIN DNAMEALG_DBT dper   ON dper.t_iNumberAlg   = cl.t_lptypeduration
            LEFT JOIN DNAMEALG_DBT dpper  ON dpper.t_iNumberAlg  = cl.t_lpayperctypedur
            LEFT JOIN DFININSTR_DBT fi    ON fi.t_fiid           = cl.t_lcreditcurcode
            WHERE cltdur.t_iTypeAlg = 987
            AND dper.t_iTypeAlg   = 987
            AND dpper.t_iTypeAlg  = 987
            AND cl.t_claimid = #
        ](string(ClaimID));
    end;
    var qStr = StopCaptureOutput;
    var rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        result.ObjID              = LO_CLAIM;              // Идентификатор объекта Loans
        result.ObjN               = rs.ClaimID;            // Номер объекта Loans
        result.GraphType          = rs.GraphTypeName;      // Группа графиков
        result.CreditSum          = rs.CreditSum;          // Сумма кредита
        result.CurCode            = rs.ClaimCurCode;       // Валюта кредита
        result.CreditDuration     = rs.CreditDuration;     // Срок кредита
        result.TypeDuration       = rs.CreditTypeDur;      // Тип срока
        result.PayDate            = rs.IssueDate;          // Дата выдачи
        result.Perc               = rs.PercRate;           // Процентная ставка
        result.DutyPeriodOD       = rs.PayDuration;        // Период погашения ОД
        result.DutyTypePeriodOD   = rs.DutyTypePeriod;     // Тип периода погашения ОД
        result.DutyDayOD          = rs.PayDay;             // День месяца для погашения ОД
        result.DutyPeriodPerc     = rs.PayPercDur;         // Период погашения процентов
        result.DutyTypePeriodPerc = rs.DutyPercTypePeriod; // Тип периода погашения процентов
        result.DutyDayPerc        = rs.PayPercDay;         // День месяца для погашения процентов
    end;
    return result;
END;



// получить ID графика по идентификатору типа
PRIVATE MACRO GetGraphTreeParmID(ObjID: integer, ObjN: integer, GraphKindID: integer)
    CaptureOutput;
    [
        SELECT gtp.T_ID 
        FROM dgraphtreeparm_dbt gtp, dgraphobject_dbt go
        WHERE go.T_OBJECTID = #
          AND go.T_OBJECTNUMBER = #
          AND go.T_ID = gtp.T_GRAPHOBJECT_ID_REF
          AND gtp.T_GRAPHKIND_ID_REF = #
    ](string(ObjID), string(ObjN), string(GraphKindID));
    var qStr = StopCaptureOutput;
    return LnSelectValue(qStr, V_INTEGER, 0);
END;

// Получение параметров расчета графика для заявки
// Используется, когда заявка уже есть.
// Если на заявке не проставлен признак Возможности открытия договора (dclaim_dbt.t_canopenproduct),
// то параметры берем из тех, что попросил клиент. 
// Если есть возможность открытия договора, то берем из тех, что предлагает банк
PRIVATE MACRO GetGraphCalcParmByClaimID(ClaimID)
    var parm_calc = Tddition_parm_calc_graph(); // класс из planpay_attr_glob.mac
    var ClaimFile = Tbfile ("claim.dbt");
    ClaimFile.rec.CLAIMID = ClaimID;
    if (ClaimFile.GetEQ())
        var rec = ClaimFile.rec;
        parm_calc.objID          = LO_CLAIM;
        parm_calc.objNum         = rec.ClaimID;
        Copy (parm_calc.RecClaim, ClaimFile);
        if (rec.CanOpenProduct != "X")
            parm_calc.GraphID        = GetGraphTreeParmID(LO_TYPECREDIT, 
                                                        rec.CreditTypeID_Ref, 
                                                        rec.lgraphtype);
            parm_calc.TOTALSUMMA     = rec.LCreditSum;
            parm_calc.DateBegin      = rec.LIssueDate;
            parm_calc.DURATION       = rec.LCreditDuration;
            parm_calc.TYPEDURATION   = rec.LCTypeDuration;
            parm_calc.CurCode        = rec.LCreditCurCode;
            parm_calc.PERIODAMT_OD   = rec.LPayDuration;
            parm_calc.PERIOD_OD      = rec.LPTypeDuration;
            parm_calc.DAY_OD         = rec.LPayDay;
            parm_calc.RATE           = rec.LPercRate;
            parm_calc.PERIODAMT_Perc = rec.LPayPercDur;
            parm_calc.PERIOD_Perc    = rec.LPayPercTypeDur;
            parm_calc.DAY_Perc       = rec.LPayPercDay;
        else
            parm_calc.GraphID        = GetGraphTreeParmID(LO_TYPECREDIT, 
                                                        rec.CreditTypeID_Ref, 
                                                        rec.bgraphtype);
            parm_calc.TOTALSUMMA     = rec.BCreditSum;
            parm_calc.DateBegin      = rec.BIssueDate;
            parm_calc.DURATION       = rec.BCreditDuration;
            parm_calc.TYPEDURATION   = rec.BCTypeDuration;
            parm_calc.CurCode        = rec.BCreditCurCode;
            parm_calc.PERIODAMT_OD   = rec.BPayDuration;
            parm_calc.PERIOD_OD      = rec.BPTypeDuration;
            parm_calc.DAY_OD         = rec.BPayDay;
            parm_calc.RATE           = rec.BPercRate;
            parm_calc.PERIODAMT_Perc = rec.BPayPercDur;
            parm_calc.PERIOD_Perc    = rec.BPayPercTypeDur;
            parm_calc.DAY_Perc       = rec.BPayPercDay;
        end;
    else
        RunError("Заявка не найдена");
    end;
    return parm_calc;
END;

// Получение объекта класса параметров для рассчета графика для заявки
// Используется, когда заявки еще нет, а график посчитать уже хочется
// ClaimEditPrm параметр типа DCLAIM_DBT
PRIVATE MACRO GetGraphCalcParm(ClaimEditPrm)
    var parm_calc = Tddition_parm_calc_graph(); // класс из planpay_attr_glob.mac
    if (ClaimEditPrm != NULL)
        parm_calc.objID          = LO_CLAIM;                     // Тип объекта
        if (ClaimEditPrm.ClaimID == 0)
            ClaimEditPrm.ClaimID = -1; // костыль для новой заявки. 
        end;
        if (ClaimEditPrm.canopenproduct != "X")
            parm_calc.objNum         = ClaimEditPrm.ClaimID;             // Номер объекта 
            parm_calc.TOTALSUMMA     = ClaimEditPrm.LCreditSum;          // Сумма для рассчета
            parm_calc.DateBegin      = ClaimEditPrm.LIssueDate;          // Дата начала объекта(дата первой выдачи)
            parm_calc.DURATION       = ClaimEditPrm.LCreditDuration;     // Срок кредита
            parm_calc.TYPEDURATION   = ClaimEditPrm.LCTypeDuration;      // Тип срока кредита
            parm_calc.CurCode        = ClaimEditPrm.LCreditCurCode;      // Валюта кредита
            parm_calc.PERIODAMT_OD   = ClaimEditPrm.LPayDuration;        // Период погашения ОД
            parm_calc.PERIOD_OD      = ClaimEditPrm.LPTypeDuration;      // Тип периода погашения ОД
            parm_calc.DAY_OD         = ClaimEditPrm.LPayDay;             // День погашения ОД
            parm_calc.RATE           = ClaimEditPrm.LPercRate;           // Процентная ставка
            parm_calc.PERIODAMT_Perc = ClaimEditPrm.LPayPercDur;         // Период погашения процентов
            parm_calc.PERIOD_Perc    = ClaimEditPrm.LPayPercTypeDur;     // Тип периода погашения процентов
            parm_calc.DAY_Perc       = ClaimEditPrm.LPayPercDay;         // День погашения процентов 
            parm_calc.GraphID        = GetGraphTreeParmID(LO_TYPECREDIT, // График берем из вида кредита
                                                          ClaimEditPrm.CreditTypeID_Ref, 
                                                          ClaimEditPrm.lgraphtype);
        else
            parm_calc.objNum         = ClaimEditPrm.ClaimID;         
            parm_calc.TOTALSUMMA     = ClaimEditPrm.BCreditSum;      
            parm_calc.DateBegin      = ClaimEditPrm.BIssueDate;      
            parm_calc.DURATION       = ClaimEditPrm.BCreditDuration; 
            parm_calc.TYPEDURATION   = ClaimEditPrm.BCTypeDuration;  
            parm_calc.CurCode        = ClaimEditPrm.BCreditCurCode;  
            parm_calc.PERIODAMT_OD   = ClaimEditPrm.BPayDuration;    
            parm_calc.PERIOD_OD      = ClaimEditPrm.BPTypeDuration;  
            parm_calc.DAY_OD         = ClaimEditPrm.BPayDay;         
            parm_calc.RATE           = ClaimEditPrm.BPercRate;       
            parm_calc.PERIODAMT_Perc = ClaimEditPrm.BPayPercDur;     
            parm_calc.PERIOD_Perc    = ClaimEditPrm.BPayPercTypeDur; 
            parm_calc.DAY_Perc       = ClaimEditPrm.BPayPercDay;     
            parm_calc.GraphID        = GetGraphTreeParmID(LO_TYPECREDIT, 
                                                          ClaimEditPrm.CreditTypeID_Ref, 
                                                          ClaimEditPrm.bgraphtype);
        end; 
        CopyClassObjToRecord(ClaimEditPrm, parm_calc.RecClaim);
    else
        RunError("Необходимо задать параметры заявки!");
    end;
    RETURN parm_calc;
END;

PRIVATE MACRO CheckGraphCalcParm(parm_calc)
    if (parm_calc != null)
        ;
    else
        RunError("", "Отсутствуют параметры для расчета графика!");
    end;
END;

// забирает из DPLAN_PAY_TMP список графиков и строит из него сводный график ОД + %
PRIVATE MACRO GetClaimGraphList(ObjID: integer, ObjN: integer)
    var item: CLnsClaimGraphGridItem = NULL;
    var RawArr = TArray();
    var ResultArr = TArray();
    CaptureOutput();
    [
        SELECT pp.*, gkp.t_subtypegraph 
        FROM DPLAN_PAY_TMP pp 
        JOIN DGRAPHKIND_DBT     gk  ON pp.t_type = gk.t_id
        JOIN DGRAPHTREEPARM_TMP gtp ON gtp.t_graphkind_id_ref = gk.t_num
        JOIN DGRAPHOBJECT_TMP   go  ON gtp.t_graphobject_id_ref = go.t_id
        JOIN DGRAPHKINDPARM_DBT gkp ON gkp.t_graphtreeparm_id_ref = gtp.t_id

        WHERE gk.t_isgroup = chr(0)
          AND gkp.t_flagtypegraph = chr(88)
          AND gkp.t_typegraph = #
          AND gkp.t_subtypegraph in (#, #)
          AND go.t_objectid = #
          AND go.t_objectNumber = #
             
        ORDER BY T_PLANNEDPAYDATE, T_TYPE
    ](GRA_DUTY, VGS_Duty, VGS_Perc, string(ObjID), string(ObjN));
    var qStr = StopCaptureOutput();
    var rs = TRsbDataSet(qStr);
    while (rs.MoveNext)
        item = CLnsClaimGraphGridItem();
        item.PayID    = rs.SubTypeGraph;
        item.PayDate  = rs.PlannedPayDate;
        item.PayDelay = Date(rs.PlannedExpDate) - Date(rs.PlannedPayDate);
        item.SumOD    = rs.PlannedPaySum;
        item.SumPerc  = rs.PlannedPercentSum;
        item.Sum      = rs.PlannedPercentSum + rs.PlannedPaySum;
        RawArr[RawArr.Size] = item;
    end;
    
    var i: integer = 0;
    var CurrentPayDate: Date;
    var NewGraphItem: CLnsClaimGraphGridItem;
    while (i < RawArr.Size)
        if (RawArr[i].PayDate != CurrentPayDate)
            NewGraphItem = CLnsClaimGraphGridItem();
            NewGraphItem.PayDate  = RawArr[i].PayDate;
            NewGraphItem.PayDelay = RawArr[i].PayDelay;
            if (RawArr[i].PayID == VGS_Duty) // это ОД
                NewGraphItem.SumOD = RawArr[i].SumOD;
            elif (RawArr[i].PayID == VGS_Perc) // это %
                NewGraphItem.SumPerc = RawArr[i].SumOD;
            end;
            CurrentPayDate = RawArr[i].PayDate;
            ResultArr[ResultArr.Size] = NewGraphItem;
        else
            if (RawArr[i].PayID == VGS_Duty) // это ОД
                NewGraphItem.PayDate  = RawArr[i].PayDate;
                NewGraphItem.PayDelay = RawArr[i].PayDelay;
                NewGraphItem.SumOD = RawArr[i].SumOD;
            elif (RawArr[i].PayID == VGS_Perc) // это %
                NewGraphItem.SumPerc = RawArr[i].SumOD;
            end;
        end;
        NewGraphItem.Sum = NewGraphItem.SumOD + NewGraphItem.SumPerc;
        i = i + 1;
    end;
    return ResultArr;
END;

PRIVATE MACRO CheckGraphError()
    var qStr = "SELECT T_ERROR FROM DLNSERROR_TMP";
    var rs = TRsbDataSet(qStr);
    if (rs.MoveNext)
        RunError(rs.error);
    end;
END;


PRIVATE MACRO GetFullGraphInfo(parm_calc)
    var GraphInfo = CLnsClaimGraphInfo();
    // Вычищаем временные таблицы
    ClearTempGraphTables();
    // Считаем график. Заполнили временные таблицы
    CalcPlan(@parm_calc);
    // Проверяем, всё ли хорошо
    CheckGraphError();
    // Получаем график из временных таблиц
    GraphInfo.GraphList = GetClaimGraphList(LO_CLAIM, parm_calc.objNum);
    // Считаем ПСК по временным таблицам
    var psk_record = CalcPSK_claim(@parm_calc);
    GraphInfo.PSK = GetPSKStringWithPrec(psk_record.rec.CalcRate);

    var graphItem = NULL;
    var MinSum = $0; // максимальная выплата
    var MaxSum = $0; // минимальная выплата
    var Sum = $0;    // общая сумма выплат
    if ((GraphInfo.GraphList != null) AND (GraphInfo.GraphList.Size > 0))
        MinSum = GraphInfo.GraphList[0].Sum;
    end;
    for (graphItem, GraphInfo.GraphList)
        Sum = Sum + graphItem.Sum;
        if (graphItem.Sum < MinSum)
            MinSum = graphItem.Sum;
        end;
        if (graphItem.Sum > MaxSum)
            MaxSum = graphItem.Sum;
        end;
    end;
    
    GraphInfo.PaySumFrom = MinSum; // Сумма платежа ОТ
    GraphInfo.PaySumTo   = MaxSum; // Сумма платежа ДО
    GraphInfo.TotalSum   = Sum;    // Общая сумма выплат
    GraphInfo.OverPaySum = Sum - parm_calc.TOTALSUMMA; // Сумма переплаты
    GraphInfo.Currency   = LnSelectValue("SELECT t_ccy FROM dfininstr_dbt WHERE t_fiid = " + parm_calc.CurCode, V_STRING, 0); // Валюта 

    return GraphInfo;
END;

// получение графика для существующей заявки
// заявка уже есть в базе
MACRO Lns_GetClaimGraphInfoByID(ClaimID)
    var parm_calc = GetGraphCalcParmByClaimID(ClaimID);
    return GetFullGraphInfo(parm_calc);
END;

// получение графика для новой заявки (ещё нет в базе)
// ClaimEditPrm объект типа DCLAIM_DBT
MACRO Lns_GetClaimGraphInfo(ClaimEditPrm)
    var parm_calc = GetGraphCalcParm(ClaimEditPrm);
    return GetFullGraphInfo(parm_calc);
END;

MACRO Lns_GetClaimGraphCommonReport(GraphParm)
    var Response = WebResponseBuilder(),
        ReportFileName = "",
        ReportFile = null;
    
    // сформировать отчет в файл с именем в ReportFileName
    /* 

    ReportFile = GetTextFile(ReportFileName);
    Response.SetUserObject(ReportFile);*/
    return Response.Result;

    OnError(err)
        Response.AddErrorEx(err.Code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return Response.Result;
END;

MACRO Lns_GetClaimPSKReport(GraphParm)
    var Response = WebResponseBuilder(),
        ReportFileName = "",
        ReportFile = null;
    
    // сформировать отчет в файл с именем в ReportFileName
    /* 

    ReportFile = GetTextFile(ReportFileName);
    Response.SetUserObject(ReportFile);*/
    return Response.Result;

    OnError(err)
        Response.AddErrorEx(err.Code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return Response.Result;
END;

MACRO Lns_GetClaimGraphReport(GraphParm)
    var Response = WebResponseBuilder(),
        ReportFileName = "",
        ReportFile = null;
    
    // сформировать отчет в файл с именем в ReportFileName
    /* 
    
    ReportFile = GetTextFile(ReportFileName);
    Response.SetUserObject(ReportFile);*/
    return Response.Result;

    OnError(err)
        Response.AddErrorEx(err.Code, MessageSeverity.RuntimeError, IfThenElse(err.err, err.err, err.Message));
        return Response.Result;
END;


