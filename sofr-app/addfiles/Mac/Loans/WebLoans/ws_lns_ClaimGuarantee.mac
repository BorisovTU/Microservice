/*
$Name: ws_lns_ClaimGuarantee.mac
$Module: WebLoans
$Description: Сервис источника данных для получения информации по заявке (Гарантия)
*/

IMPORT rsd, SbCrdInter, BankInter, RsbDataSet, globals, total, ws_lns_CrdContract, DCLAIM_DBT, ws_lns_CommonLib, ws_LoansOperation;
IMPORT ws_lns_Specific, ws_lns_PurposeForCB, ws_lns_ClaimEns;
IMPORT mfo_lib, Календарь, cb_sql, likepy; 
IMPORT "ws_lns_ResponsOperHistory.mac", "ws_person.mac", "acsgroup_widgetclass.mac";

CLASS CLnsClaimGuaranteeViewPrm()
    VAR LabelStateText         : string  = "";         // Метка состояния заявки
    VAR UsNumber               : string  = "";         // Номер заявки
    VAR RegDate                : Date    = Date(0,0,0);// Дата заявки
    VAR principal              : string  = "";         // Принципал
    VAR beneficiar             : string  = "";         // Бенефициар
    VAR isManyBeneficiary      : bool    = false;      // Признак "Наличие разных бенефициаров"
    VAR ClientSumGuarantee     : Money   = $0;         // Клиент: Сумма гарантии
    VAR ClientSumGuaranteeFI   : string  = "";         // Клиент: Валюта
    VAR ClientDuration         : integer = 0;          // Клиент: Срок гарантии
    VAR ClientTypeDuration     : string  = "";         // Клиент: Тип срока гарантии
    VAR ClientRate             : double  = 0.0;        // Клиент: Процентная ставка
    VAR BankSumGuarantee       : Money   = $0;         // Банк: Сумма гарантии
    VAR BankSumGuaranteeFI     : string  = "";         // Банк: Валюта
    VAR BankDuration           : integer = 0;          // Банк: Срок кредита
    VAR BankTypeDuration       : string  = "";         // Банк: Тип срока
    VAR BankTCR                : string  = "";         // Банк: Вид кредита
    VAR BankRate               : string  = "";         // Банк: Ставки тарифов
    VAR WithTariffPlan         : bool    = false;      // Исползование тарифных планов
    VAR BankTariffPlan         : string  = "";         // Банк: Тарифный план
    VAR BankTariffPlanCond     : string  = "";         // Банк: Условие тарифной сетки
    VAR BankClaimGuaranteeRout : string  = "";         // Банк: Маршрут рассмотрения
    VAR BankOpeningPeriod      : integer = 0;          // Банк: Срок открытия договора
    VAR BankOpeningDate                  = NULL;       // Банк: Дата открытия договора
    VAR isAbsolute             : bool    = false;      // Признак "Безусловное обязательство выдачи"
    VAR Comment                : string  = "";         // Примечание
END;

CLASS CLnsClaimGuaranteeEditPrm()
    VAR ClaimGuarantee = null;                         // параметры заявки
END;

// получение параметров для заявки в ВЕБ кредитовании.
MACRO lns_GetClaimGuaranteeViewPrm(ObjN)
    var result = CLnsClaimGuaranteeViewPrm(),
        rs = NULL,
        qStr = "",
        credit = null;
    CaptureOutput;
    [
    SELECT cl.t_claimnumber as t_usnumber,
           cl.t_givingdate as t_regdate,
           party1.t_shortname as t_principal,
           party2.t_shortname as t_beneficiar,
           cl.t_lcreditsum as t_clientsumguarantee,
           fi1.t_ccy as t_clientsumguaranteefi,
           cl.t_lcreditduration as t_clientduration,
           cltdur.t_szNameAlg as t_clienttypeduration,
           cl.t_lpercrate as t_clientrate,
           cl.t_bCreditSum as t_banksumguarantee,
           fi2.t_ccy as t_banksumguaranteefi,
           cl.t_bcreditduration as t_bankduration,
           batdur.t_szNameAlg as t_banktypeduration,
           typecrd.t_credittypename as t_banktcr,
           ratealg.t_sznamealg as t_bankrate,
           route.t_processname as t_routename,
           cl.t_daysbeforecredit as t_bankopeningperiod,
           cl.t_note as t_comment,
           cl.t_approvaldate,
           cl.t_ismanybeneficiary,
           cl.t_isabsolute
    FROM dclaim_dbt cl,
         dparty_dbt party1,
         dparty_dbt party2,
         dfininstr_dbt fi1,
         dnamealg_dbt cltdur,
         dfininstr_dbt fi2,
         dnamealg_dbt batdur,
         dtype_crd_dbt typecrd,
         dnamealg_dbt ratealg,
         dlclaimroute_dbt route
    WHERE cl.t_loanerid_ref = party1.t_partyid
      AND cl.t_beneficiary = party2.t_partyid
      AND fi1.t_fiid = cl.t_lcreditcurcode
      AND (cltdur.t_iNumberAlg = cl.t_lctypeduration AND cltdur.t_iTypeAlg = 987)
      AND fi2.t_fiid = cl.t_bcreditcurcode
      AND (batdur.t_iNumberAlg = cl.t_bctypeduration AND batdur.t_iTypeAlg = 987)
      AND cl.t_credittypeid_ref = typecrd.t_CreditTypeID
      AND (ratealg.t_iNumberAlg = cl.t_TuneType AND ratealg.t_iTypeAlg = 1008)
      AND cl.t_processname = route.t_processid
    ];
    qStr = StopCaptureOutput;
    qStr = qStr + " AND cl.t_claimid = " + ObjN;
    rs = TRsbDataSet(qStr);
    if (rs.moveNext)
        //result.LabelStateText       = 
        result.UsNumber               = rs.UsNumber;
        result.RegDate                = rs.RegDate;
        result.principal              = rs.principal;
        result.beneficiar             = rs.beneficiar;
        result.isManyBeneficiary      = SQL_ConvTypeBool(rs.isManyBeneficiary);
        result.ClientSumGuarantee     = rs.ClientSumGuarantee;
        result.ClientSumGuaranteeFI   = rs.ClientSumGuaranteeFI;
        result.ClientDuration         = rs.ClientDuration;
        result.ClientTypeDuration     = rs.ClientTypeDuration;
        result.ClientRate             = rs.ClientRate;
        result.BankSumGuarantee       = rs.BankSumGuarantee;
        result.BankSumGuaranteeFI     = rs.BankSumGuaranteeFI;
        result.BankDuration           = rs.BankDuration;
        result.BankTypeDuration       = rs.BankTypeDuration;
        result.isAbsolute             = SQL_ConvTypeBool(rs.isAbsolute);
        result.BankTCR                = rs.BankTCR; 
        result.BankRate               = rs.BankRate;
        
        var tp = GetTariffPlan(LO_CLAIM, ObjN);
        
        result.BankTariffPlan         = tp.Plan.Name;
        result.BankTariffPlanCond     = tp.Cond.Name;
        
        result.BankClaimGuaranteeRout = rs.RouteName;
        result.BankOpeningPeriod      = rs.BankOpeningPeriod;
        
        if (Date(rs.ApprovalDate) == Date(0,0,0))
            result.BankOpeningDate    = NULL;
        else
            result.BankOpeningDate    = DateAfterWorkDays(Date(rs.ApprovalDate), Int(rs.BankOpeningPeriod));
        end;

        result.Comment                = rs.Comment;
    end;
    
    return result;
END;

MACRO lns_GetClaimGuaranteeEditPrm(ObjN)
    var qStr = "",
        rs = null,
        result = CLnsClaimGuaranteeEditPrm();
    qStr = "SELECT * FROM dclaim_dbt WHERE t_claimid = " + ObjN;
    rs = RsdRecordset(qStr);
    if (rs.MoveNext)
        result.ClaimGuarantee = GetObjectFromRecordset("DCLAIM_DBT", rs);
    end;
    return result;
END;