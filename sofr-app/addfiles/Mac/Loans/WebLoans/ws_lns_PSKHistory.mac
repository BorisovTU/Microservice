/*
$Name: ws_lns_PSKHistory.mac
$Module: WebLoans
$Description: Сервис источника данных для скроллинга "История ПСК"
*/
IMPORT RsbDataSet, mfo_lib, "total.mac", "xirr.mac", "xirr_report.mac", "ws_common.mac", "ws_file.mac";

CLASS CLnsPSKHistoryItem()
    VAR ID: integer          = 0;
    VAR CalcDate: Date       = Date(0,0,0);
    VAR RealRate: double     = 0.0;
    VAR RealRateS: string    = "";
    VAR SumCredit: Money     = $0;
    VAR CurCode: string      = "";
    VAR Duration: integer    = 0;
    VAR DurationType: integer= 0;
    VAR DurationTypeS: string= "";
    VAR CalcRate: double     = 0.0;
    VAR CalcRateS: string    = "";
    VAR CalcRateSum: Money  = $0;
END;

MACRO lns_PSKHistoryList(ObjID, ObjN)
    VAR result = TArray(), item = NULL;
    VAR rs = NULL;
    VAR precision:integer = 0, ErrCode;
    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
        RunError(ErrCode);
    end;
    if (precision > 28)
        precision = 28;
    end;
    SetDefPrec (precision);
    VAR query = "SELECT xirr.*, " +
                "fininstr.t_Ccy as t_CurCode, " +
                "namealg.t_szNameAlg as t_DurationTypeS " + 
                "FROM dlhistry_xirr_dbt xirr, "  + 
                "dcredit_c_dbt credit, " + 
                "dnamealg_dbt namealg, " + 
                "dfininstr_dbt fininstr " + 
                "WHERE credit.t_creditnumber = xirr.T_OBJECTNUMBER and " + 
                "credit.t_crd_kind = xirr.T_OBJECTID and " + 
                "(namealg.t_iNumberAlg = credit.t_TypeDuration and namealg.t_iTypeAlg = 987) and " + 
                "credit.t_curcode = fininstr.t_fiid and " + 
                "xirr.T_OBJECTID = " + ObjID + " and " + 
                "xirr.T_OBJECTNUMBER = " + ObjN;
    
    rs = RsdRecordset(query);
    
    WHILE (rs.moveNext())
        item = CLnsPSKHistoryItem();
        item.ID            = SQL_NVL(RS.Value("T_ID"          , NULL, V_INTEGER), V_INTEGER);
        item.CalcDate      = SQL_NVL(RS.Value("T_CALCDATE"    , NULL, V_DATE)   , V_DATE);
        item.SumCredit     = SQL_NVL(RS.Value("T_SUMCREDIT"   , NULL, V_MONEY)  , V_MONEY);
        item.CurCode       = SQL_NVL(RS.Value("T_CURCODE"     , NULL, V_STRING) , V_STRING);
        item.Duration      = SQL_NVL(RS.Value("T_DURATION"    , NULL, V_INTEGER), V_INTEGER);
        item.DurationType  = SQL_NVL(RS.Value("T_DURATIONTYPE", NULL, V_INTEGER) , V_INTEGER);
        item.DurationTypeS = SQL_NVL(RS.Value("T_DURATIONTYPES", NULL, V_STRING) , V_STRING);
        item.CalcRate      = SQL_NVL(RS.Value("T_CALCRATE"    , NULL, V_DOUBLE) , V_DOUBLE);
        item.CalcRateSum   = SQL_NVL(RS.Value("T_CALCSUMRATE" , NULL, V_MONEY) , V_MONEY);
        item.CalcRateS     = string(SQL_NVL(RS.Value("T_CALCRATE"    , NULL, V_DOUBLE) , V_DOUBLE));
        item.RealRateS     = SQL_NVL(RS.Value("T_REALRATE", NULL, V_STRING) , V_STRING);
        item.RealRate      = ПроцСтавка (ObjID, ObjN, TRU_CRD, item.CalcDate); // не хранится в таблице в виде числа. (приходится рассчитывать)

        result[result.size]= item;
    END;
    RETURN result;
END;

MACRO lns_GetCalcPSKItem(ObjID, ObjN, OpDate)
    var lhistry_xirr = TRecHandler("lhistry_xirr.dbt"),
        PSKItem = CLnsPSKHistoryItem(),
        BegDate = date(0,0,0),
        EndDate = date(0,0,0),
        СurCode = "",
        Duration = 0,
        DurationTypeStr = "",
        CreditSum = $0,
        query = "",
        rs = null,
        ResponseBuilder = WebResponseBuilder();
    
    VAR precision:integer = 0, ErrCode;
    if (GetRegistryValue ("RS-LOANS\\ПСК\\ТОЧНОСТЬ_РЕЗУЛЬТАТОВ", V_INTEGER, precision, ErrCode) != V_INTEGER)
        RunError(ErrCode);
    end;
    if (precision > 28)
        precision = 28;
    end;
    SetDefPrec (precision);

    // чтоб не дёргать 2 раза СУБД, выбираем недостающие поля для CLnsPSKHistoryItem и даты начала-окончания одним запросом.
    query = "SELECT credit.t_regdate, credit.t_returndate, credit.t_fullduration, credit.t_creditsum, fininstr.t_Ccy as t_CurCode, namealg.t_szNameAlg as t_DurationTypeS " +
            "FROM dcredit_c_dbt credit, dnamealg_dbt namealg, dfininstr_dbt fininstr " + 
            "WHERE  (namealg.t_iNumberAlg = credit.t_TypeDuration and namealg.t_iTypeAlg = 987) " + 
            "and credit.t_curcode = fininstr.t_fiid " + 
            "and credit.T_CRD_KIND = " + ObjID + " and credit.T_CREDITNUMBER = " + ObjN;
    rs = RsdRecordset(query);
    if (rs.moveNext())
        CreditSum       = SQL_NVL(RS.Value("T_CREDITSUM"    , NULL, V_MONEY)  , V_MONEY);
        Duration        = SQL_NVL(RS.Value("T_FULLDURATION" , NULL, V_INTEGER), V_INTEGER);
        СurCode         = SQL_NVL(RS.Value("T_CURCODE"      , NULL, V_STRING) , V_STRING);
        DurationTypeStr = SQL_NVL(RS.Value("T_DURATIONTYPES", NULL, V_STRING) , V_STRING);
        BegDate         = SQL_NVL(RS.Value("T_REGDATE"      , NULL, V_DATE)   , V_DATE);
        EndDate         = SQL_NVL(RS.Value("T_RETURNDATE"   , NULL, V_DATE)   , V_DATE);
    else
        RunError("Не найдены параметры договора.");
    end;
    
    if ((OpDate > EndDate) or (OpDate < BegDate))
        //RunError ("Дата отчета не попадает в период действия договора.");
        // вместо того чтобы выводить ошибку, нужно сформировать ответ. Но в ПСК поставить 0.
        // ответ будет неполным. т.к. рассчет ПСК не производился.
        PSKItem.ID = 0;
        PSKItem.CalcDate = OpDate;
        PSKItem.RealRate = ПроцСтавка (ObjID, ObjN, TRU_CRD, OpDate);
        PSKItem.SumCredit = CreditSum;
        PSKItem.Duration = Duration;
        PSKItem.CalcRateS = string(double(0));
        PSKItem.CurCode = СurCode;
        PSKItem.DurationTypeS = DurationTypeStr;
    else
        lhistry_xirr = CalcPSK(ObjID, ObjN, OpDate);
        PSKItem.ID = 0;
        PSKItem.CalcDate = lhistry_xirr.rec.CalcDate;
        PSKItem.RealRate = ПроцСтавка (ObjID, ObjN, TRU_CRD, OpDate);
        PSKItem.RealRateS = lhistry_xirr.rec.RealRate;
        PSKItem.SumCredit = lhistry_xirr.rec.SumCredit;
        PSKItem.Duration = lhistry_xirr.rec.Duration;
        PSKItem.DurationType = lhistry_xirr.rec.DurationType;
        PSKItem.CalcRate = lhistry_xirr.rec.CalcRate;
        PSKItem.CalcRateS = string(double(lhistry_xirr.rec.CalcRate));
        PSKItem.CurCode = СurCode;
        PSKItem.DurationTypeS = DurationTypeStr;
    end;
    ResponseBuilder.SetUserObject(PSKItem);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO lns_SavePSKItem(ObjID, ObjN, PSKHistoryItem)
    var lhistry_xirr = TRecHandler("lhistry_xirr.dbt"),
        ResponseBuilder = WebResponseBuilder(),
        BegDate = date(0,0,0),
        EndDate = date(0,0,0);
    
    var credit = TBFile ("credit_c.dbt", "r", 0);
    credit.Clear;
    credit.rec.CreditNumber = ObjN;
    if (credit.GetEQ)
        BegDate = credit.rec.RegDate;
        EndDate = credit.rec.ReturnDate;
    else
        RunError("Не найдены параметры договора.");
    end;
    
    if ((PSKHistoryItem.CalcDate > EndDate) or (PSKHistoryItem.CalcDate < BegDate))
        RunError ("Дата отчета не попадает в период действия договора.");
    end;
    
    if (IsExistsDate(ObjID, ObjN, PSKHistoryItem.CalcDate))
        RunError ("На эту дату расчет ПСК уже производился.");
    end;
    
    lhistry_xirr.rec.ObjectID = ObjID;
    lhistry_xirr.rec.ObjectNumber = ObjN;
    lhistry_xirr.rec.CalcDate = PSKHistoryItem.CalcDate;
    lhistry_xirr.rec.RealRate = PSKHistoryItem.CalcRateS;
    lhistry_xirr.rec.SumCredit = PSKHistoryItem.SumCredit;
    lhistry_xirr.rec.Duration = PSKHistoryItem.Duration;
    lhistry_xirr.rec.DurationType = PSKHistoryItem.DurationType;
    lhistry_xirr.rec.CalcRate = PSKHistoryItem.CalcRate;
    SavePSKData(lhistry_xirr.rec);
    
    ResponseBuilder.SetUserObject(0);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

MACRO lns_DeletePSKItem (PskID: integer)
    var ResponseBuilder = WebResponseBuilder();
    
    LnSQLExec("DELETE FROM DLHISTRY_XIRR_DBT WHERE T_ID = " + PskID);
    
    ResponseBuilder.SetUserObject(0);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;

PRIVATE MACRO ReadFromFile(FileName: string)
    var result = "";
    FILE TextFile() txt;
    var executionStatus: Integer = 0;
    var errorMessage: String = "";
    if (NOT open(TextFile, FileName))
        executionStatus = status(errorMessage);
        runError("Ошибка при создании файла|" + FileName + "|(" + executionStatus + ") " + errorMessage);
    end;
    while(Next(TextFile)) 
        result = result + TextFile.str + "\r\n";
    end;
    close(textFile);
    return result;
END;

MACRO lns_PrintPSKItem(ObjID, ObjN, PSKHistoryItem)
    var lhistry_xirr = TRecHandler("lhistry_xirr.dbt"),
        credit = TBFile ("credit_c.dbt", "r", 0),    
        ResponseBuilder = WebResponseBuilder(),
        report = null,
        container = null,   
        curcode: integer = 0,
        namefile = "";

    credit.Clear;
    credit.rec.CreditNumber = ObjN;
    if (credit.GetEQ)
        curcode = credit.rec.curcode;
    else
        RunError("Не найдены параметры договора.");
    end;

    lhistry_xirr.rec.ObjectID = ObjID;
    lhistry_xirr.rec.ObjectNumber = ObjN;
    lhistry_xirr.rec.CalcDate = PSKHistoryItem.CalcDate;
    lhistry_xirr.rec.RealRate = PSKHistoryItem.CalcRateS;
    lhistry_xirr.rec.SumCredit = PSKHistoryItem.SumCredit;
    lhistry_xirr.rec.Duration = PSKHistoryItem.Duration;
    lhistry_xirr.rec.DurationType = PSKHistoryItem.DurationType;
    lhistry_xirr.rec.CalcRate = PSKHistoryItem.CalcRate;
    lhistry_xirr.rec.CalcSumRate = PSKHistoryItem.CalcRateSum;

    SetOutput (GetTxtFileName("psk_rep"), false);

    LoadPSKData(PSKHistoryItem.ID);
    MakeXirrReport(lhistry_xirr.rec, curcode);

    namefile = SetOutput(null, true);

    var ReportString = ReadFromFile(namefile);
    container = CreateFileContainer(namefile, false, OEMToUTF8(ReportString));
    ResponseBuilder.SetUserObject(container);
    return ResponseBuilder.Result;
    
    OnError(err)
        ResponseBuilder.AddErrorEx( err.Code, MessageSeverity.RuntimeError, err.Message );
        return ResponseBuilder.Result;
END;


