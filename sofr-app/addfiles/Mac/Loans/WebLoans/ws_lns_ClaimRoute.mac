/*
$Name: ws_lns_ClaimRoute.mac
$Module: WebLoans
$Description: Сервис для работы с маршрутами BPMN
*/

import LnsClaimRouteCore, ws_validation, likepy;
IMPORT RSD;
IMPORT "fmt_lib.mac", "total.mac", "ws_LoansOperation.mac", "DLCLAIMROUTE_DBT.mac";

MACRO Lns_StartRoute(claimid)
    var result = WebResponseBuilder();
    var parm = StartClaimRoute(claimid);
    result.SetUserObject(parm);
    return result.Result;
    OnError(err)
        result.AddErrorEx(0, MessageSeverity.RuntimeError, err.Message);
        return result.Result;
END;

MACRO Lns_RouteNextStep(claimid, parm)
    var result = WebResponseBuilder();
    parm = ResumeClaimRoute(claimid, parm);
    result.SetUserObject(parm);
    return result.Result;
    OnError(err)
        result.AddErrorEx(0, MessageSeverity.RuntimeError, err.Message);
        return result.Result;
END;

MACRO Lns_GetClaimRouteList(FilterConditionItems, TypeDebtor, HideUnused)
    VAR result = TArray();
    VAR query = "select * from dlclaimroute_dbt where 1 = 1 ";
    if (HideUnused)
        query = query + " AND (t_isnotuse != 'X' or t_isnotuse is NULL)"
    end;
    if ((TypeDebtor != NULL) and (TypeDebtor != 0))
        query = query + " AND t_typedebtor in (0, " + TypeDebtor + ") ";
    end;
        query = query + " order by t_id";
    VAR RS = RsdRecordset(query);

    WHILE (RS.MoveNext)
        result.value(result.Size) = GetObjectFromRecordset("DLCLAIMROUTE_DBT", rs);
    END;

    return result;
END;

MACRO Lns_GetCountOfLinkCrdTypes (ClaimRouteID)
    var qStr = "",
    rs = null,
    result:integer = 0;

    qStr = "SELECT COUNT(*) VK_CNT" +
           " FROM DTYPE_CRD_DBT t" +
           " WHERE T.T_ROUTEID_DEFAULT = " + ClaimRouteID + "AND T.T_ISDEFROUTE = 'X'";
           
    rs = TRsbDataSet(qStr);
    
    if (rs.MoveNext())
        result = rs.vk_cnt;
    end;
    
    return result;
END;

MACRO Lns_GetCountOfLinkClaims (ClaimRouteID)
    var qStr = "",
    rs = null,
    result:integer = 0;
          
    qStr = "SELECT COUNT (*) CLM_NUM" + 
           " FROM DCLAIM_DBT CLM" +
           " INNER JOIN DLCLAIMROUTE_DBT CLMRT" +
           " ON CLM.T_PROCESSNAME = CLMRT.T_PROCESSID" +
           " WHERE CLMRT.T_ID = " + ClaimRouteID;
           
    rs = TRsbDataSet(qStr);
    
    if (rs.MoveNext())
        result = rs.clm_num;
    end;
    
    return result;
END;

MACRO Lns_InsertClaimRoute(ClaimRoute)
    var qStr = "",
        stat = true,
        result = TWsMessage();
    result.MsgText = "";
    result.MsgID = 0;
    if (ClaimRoute.ProcessName == "")
        result.MsgID = 1;
        result.MsgText = "Введите название маршрута."
    elif (ClaimRoute.ProcessID == "")
        result.MsgID = 1;
        result.MsgText = "Введите идентификатор процесса."
    else
        qStr = "insert into dlclaimroute_dbt (t_id, t_processid, t_processname, t_typedebtor, t_isnotuse, t_isupdate) " +
        "values ( " +
        ClaimRoute.ID + ", " +
        SQLString(ClaimRoute.ProcessID) + ", " +
        SQLString(ClaimRoute.ProcessName) + ", " +
        ClaimRoute.TypeDebtor + ", " +
        SQLString(ClaimRoute.isNotUse) + ", " +
        SQLString(ClaimRoute.isUpdate) + ")";
        stat = LnSqlExec(qStr, false);
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка добавления маршрута заявки";
        end;
    end;
    return result;
END;

MACRO Lns_UpdateClaimRoute(ClaimRoute)
    var qStr = "", qStr1 = "",
        stat = true,
        result = TWsMessage(),
        countVK = 0,
        rs1 = null;
    result.MsgText = "";
    result.MsgID = 0;
    if (ClaimRoute.ProcessID == "")
        result.MsgID = 1;
        result.MsgText = "Введите идентификатор процесса.";
    else
        countVK = Lns_GetCountOfLinkCrdTypes (ClaimRoute.ID);
        if ((countVK > 0) and (ClaimRoute.isNotUse == "X"))
            result.MsgID = 2;          
            result.MsgText = "Нельзя установить признак \"Не используется\" для маршрута! Он по умолчанию используется по виду кредита.";

            qStr1 = "SELECT T.T_CREDITTYPEID id, T.T_CREDITTYPENAME name" + 
                    " FROM DTYPE_CRD_DBT t " + 
                    " WHERE T.T_ROUTEID_DEFAULT = " + ClaimRoute.ID;            
                             
            rs1 = TRsbDataSet(qStr1);
            
            while (rs1.MoveNext())
                result.MsgText = result.MsgText + "\n";
                result.MsgText = result.MsgText + rs1.id + " \"" + rs1.name + "\"";
            end;           
    else
        qStr = "update dlclaimroute_dbt set " +
        "t_processid=" + SQLString(ClaimRoute.ProcessID) + ", " +
        "t_processname=" + SQLString(ClaimRoute.ProcessName) + ", " +
        "t_typedebtor=" + ClaimRoute.TypeDebtor + ", " +
        "t_isnotuse=" + SQLString(ClaimRoute.isNotUse) + ", " +
        "t_isupdate=" + SQLString(ClaimRoute.isUpdate) + " " +
        "where t_id=" + ClaimRoute.ID;
        stat = LnSqlExec(qStr, false);
        
        if (stat == false)
            result.MsgID = 1;
            result.MsgText = "Ошибка обновления маршрута заявки";
        end;
    end;
    end;
    return result;
END;

MACRO Lns_DeleteClaimRoute(ClaimRouteID)
    var stat, countVK = 0, countClm = 0, result = TWsMessage(), rs1 = null, qStr1 = "";

    countVK = Lns_GetCountOfLinkCrdTypes (ClaimRouteID);
    countClm = Lns_GetCountOfLinkClaims (ClaimRouteID);
    if (countVK > 0)
        result.MsgID = 2;          
        result.MsgText = "Нельзя удалить маршрут! Он";
        if ((countClm > 0) and (ClaimRouteID >= 1000))
            result.MsgText = result.MsgText + " привязан к заявке клиента и";
        end;
        result.MsgText = result.MsgText + " по умолчанию используется по виду кредита.";

        qStr1 = "SELECT T.T_CREDITTYPEID id, T.T_CREDITTYPENAME name" + 
                " FROM DTYPE_CRD_DBT t " + 
                " WHERE T.T_ROUTEID_DEFAULT = " + ClaimRouteID;            
                         
        rs1 = TRsbDataSet(qStr1);
        
        while (rs1.MoveNext())
            result.MsgText = result.MsgText + "\n";
            result.MsgText = result.MsgText + rs1.id + " \"" + rs1.name + "\"";
        end;           
    elif ((countClm > 0) and (ClaimRouteID >= 1000))
        result.MsgID = 3;
        result.MsgText = "Нельзя удалить маршрут! Он привязан к заявке клиента.";    
    else
        stat = LnSqlExec("delete from dlclaimroute_dbt where t_id = " + ClaimRouteID, false);
    result.MsgText = "";
    result.MsgID = 0;
    if (stat == false)
        result.MsgID = 1;
        result.MsgText = "Ошибка удаления маршрута заявки";
    end;
    end;

    return result;
END;

MACRO Lns_GetClaimRouteID()
    var maxval: integer;
    maxval = LnSelectValue("SELECT max(t_id) FROM dlclaimroute_dbt", V_INTEGER, 0);
    if (maxval < 1000)
        return 1000;
    else
        return maxval + 1;
    end;
END;

MACRO Lns_GetClaimRouteByID(ClaimRouteID: integer)
    VAR query = "select * from dlclaimroute_dbt where t_id = " + ClaimRouteID,
        RS = RsdRecordset(query),
        result = null;
        
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DLCLAIMROUTE_DBT", rs);
    end;
    return result;
END;

MACRO Lns_GetClaimRouteByProcessID(ProcessID: string)
    VAR query = "select * from dlclaimroute_dbt where t_processid = " + SQLString(ProcessID),
        RS = RsdRecordset(query),
        result = null;
        
    if (RS.MoveNext)
        result = GetObjectFromRecordset("DLCLAIMROUTE_DBT", rs);
    end;

    return result;
END;

MACRO Lns_GetClaimRouteByIDList(ClaimRouteIDList)
    VAR query = "select * from dlclaimroute_dbt where t_id in (" + join(ClaimRouteIDList, ",") + ")" ,
        RS = RsdRecordset(query),
        result = TArray(),
        item = null;
        
    while (RS.MoveNext)
        item = GetObjectFromRecordset("DLCLAIMROUTE_DBT", rs);
        result[result.Size] = item;
    end;
    return result;
END;


