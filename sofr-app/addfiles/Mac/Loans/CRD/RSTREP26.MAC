/*
$Name:             rstrep26.mac
$Module:           Кредитование
$Description:      Отчет о состоянии кредитного портфеля по форме 2.6
*/

/*------------------------------------------------------------------------------
                     Библиотека интерпретируемых модулей

 Filename    : rstrep26.mac
 Description : Отчет о состоянии кредитного портфеля по форме 2.6

 Programmer  : TT
 22.05.03    : Создан
------------------------------------------------------------------------------*/

Import ActiveX, VBAConst, total;

VAR FinInst = TBFile("Fininstr.dbt", "R", 6, "Fininstr.dbt", "loans.def");
FILE Credit       ("credit_c.dbt", "loans.def");
FILE ВидыКредитов ("set_tcr.tmp",  "loans.def"); /* временный файл для выбора видов кредита */
FILE Филиалы      ("set_lfd.tmp",  "loans.def"); /* временный файл филиалов */

private FILE DutyCrd  ("duty_crd.dbt", "loans.def")  key 1;
//private FILE riskgr   ("riskgr.dbt",   "loans.def")  key 0;
RECORD riskgr_val("riskgr_val.dbt");
RECORD riskgr("riskgr.dbt");

private var {OperDprtNode};

CONST
  ALL_FLAGCUR = 0; /* все виды кредитов для данного режима валютности */

Var
   КоличествоДнейОД:integer, КоличествоДнейПроц:integer, CreditID, Инсайдер, Count, SumTmp, EnsureSum, RepDate,
   Row = 14, Итог1 = $0, Итог2 = $0, Итог3 = $0, Итог4 = $0, DepNum = "", CurRateUSD = 0.0,
   SumTmpThousRUR = $0, SumTmpThousUSD = $0, Итог1USD = $0, RezSum = $0, RezSumFact = $0, AxErr:object = null;

macro CheckDepart(FNCash)
    Филиалы.FNCash = FNCash;
    if (GetEQ(Филиалы))
      if (Филиалы.SetFlag == "X")
         return true;
      end;
    end;
    return false;
end;

macro КоличествоПролонгаций(CreditNumber, Firstdate, LastDate)
   var ProlNum = 0, rs = null, query: string;
  
   query = "SELECT COUNT(*) FROM DCRD_OP_DBT WHERE " +
           " T_CreditNumber_Ref  = " + CreditNumber +
           " AND T_StageID_Ref   = " + CF_COMPLIT +
           " AND T_IsDeleted     = 0" +
           " AND T_SystemOperationID = " + CS_PROL +
           " AND T_CredOperDate      >= " + SQLDate(FirstDate) +
           " AND T_CredOperDate      <= " + SQLDate(LastDate);

   rs = LnGetRecordSet(query);
        
   if ((rs != null) and (rs.MoveNext))
       if (ValType(rs.Value(0), null, V_INTEGER) != V_UNDEF)
          ProlNum = rs.Value(0, null, V_INTEGER);
       end;
   end;
   return ProlNum;
end;


macro LastDutyLastDate(CreditNumber)
    var DutyLastDate:date = date(0,0,0);

    if (КоличествоПролонгаций(CreditNumber, date(0,0,0), RepDate) == 0)
       return Credit.ReturnDate;
    end;

    ClearRecord(DutyCrd);
    DutyCrd.CreditNumber_Ref = CreditNumber;
    DutyCrd.DutyState        = CF_OPEN;
    if (not GetEQ(DutyCrd))
       return Credit.ReturnDate;
    end;

    DutyLastDate = DutyCrd.DutyLastDate;
    while (next(DutyCrd) and (DutyCrd.CreditNumber_Ref == CreditNumber)
                         and (DutyCrd.DutyType         == 0)
                         and (DutyCrd.DutyState        == CF_OPEN))
        if (DutyLastDate < DutyCrd.DutyLastDate)
            DutyLastDate = DutyCrd.DutyLastDate;
        end;
    end;

    return DutyLastDate;
end;


macro РассчитатьРезерв(ObjType, CreditNumber, Type, Дата)
   /*
   ClearRecord(riskgr);
   riskGr.NumberRiskGroup = ГруппаРиска(ObjType, CreditNumber, Type, Дата);
   if (GetEQ(riskgr))
       return money(round(ЭлементРасчетнойБазы(ObjType, CreditNumber, Дата, TDR_MAINREST) * riskgr.DefaultPercentRate / 100.0));
   end;
   */
   IF (Loans_FindRiskGroup(ГруппаРиска(ObjType, CreditNumber, Type, Дата),riskgr,riskgr_val,Дата))
     return money(round(ЭлементРасчетнойБазы(ObjType, CreditNumber, Дата, TDR_MAINREST) * riskgr_val.DefaultPercentRate / 100.0));
   END;

   return $0;
end;


macro OneContractRep ()
   var ЮрЛицо, CurRateCRD = 0.0;
   var ДатаПросрочкиОД = date(0,0,0), 
       ДатаПросрочкиПроц= date(0,0,0);
   КоличествоДнейОД = КоличествоДнейПроц = 0;
   КоличествоДнейПросрочки(Credit.CreditNumber, Credit.CurCode, RepDate, КоличествоДнейОД, КоличествоДнейПроц, @ДатаПросрочкиОД, @ДатаПросрочкиПроц);

   CurRateCRD = GetRate(RepDate, Credit.CurCode);
   if ((ValType(CurRateCRD) == V_UNDEF) or (CurRateCRD == 0.0))
      CurRateCRD = 1.0;
   end;

   if (IsInsider (Credit.ClientID_Ref))
      Инсайдер = "X";
   else
      Инсайдер = "";
   end;
   
   CreditID = Credit.CreditNumber;
   
   ActiveSheet.Cells (Row,  4).Value = FindClient (Credit.ClientID_Ref);
   if (DepClnt.LegalForm != 2)
      ЮрЛицо = true;
   else
      ЮрЛицо = false;
   end;

   /* Формирование отчёта */
   ActiveSheet.Cells (Row,  1).Value = Row - 13;
   if (ЮрЛицо)
      /* Название организационно-правовой формы собственности */
      ActiveSheet.Cells (Row,  2).Value = НазваниеОПФС(Credit.ClientID_Ref, RepDate);
      /* Отрасль экономики */
      ActiveSheet.Cells (Row,  3).Value = НазваниеОЭ(Credit.ClientID_Ref, RepDate);
   end;
   /* Подразделение, выдавшее кредит */
   ActiveSheet.Cells (Row,  5).Value = GetUsFieldValue(1 /*Кредитный договор*/, Credit.CreditNumber, 80 /*Подразделение*/, Credit.RegDate);
   ActiveSheet.Cells (Row,  6).Value = Credit.UsCreditNumber + " от " + DateToStringShort(Credit.RegDate);
   SumTmpThousRUR = money(round(СуммаВыданногоКредита (Credit.CreditNumber, Credit.CurCode, Date (0,0,0), RepDate) * CurRateCRD));
   SumTmpThousUSD = money(round(SumTmpThousRUR / CurRateUSD));
   Итог1 = Итог1 + SumTmpThousRUR;
   Итог1USD = Итог1USD  + SumTmpThousUSD;
   ActiveSheet.Cells (Row, 8).Value = Double (SumTmpThousRUR);
   ActiveSheet.Cells (Row, 9).Value = Double (SumTmpThousUSD);
   ActiveSheet.Cells (Row, 7).Value = DateToStringShort (Credit.PaymentDate);
   ActiveSheet.Cells (Row, 10).Value = FindTypeCrd (Credit.CreditTypeID_Ref);
   ActiveSheet.Cells (Row, 11).Value = DateToStringShort (Credit.ReturnDate);
   ActiveSheet.Cells (Row, 12).Value = DateToStringShort (LastDutyLastDate(Credit.CreditNumber));
   ActiveSheet.Cells (Row, 13).Value = КоличествоПролонгаций(Credit.CreditNumber, Credit.RegDate, RepDate);

   ActiveSheet.Cells (Row, 14).Value = DateToStringShort(ДатаПросрочкиОД);
   ActiveSheet.Cells (Row, 15).Value = Double (Loans_FindRateVal(GetObjectID_FromCrdKind(Credit.Crd_kind), Credit.CreditNumber, TRU_CRD, RepDate));
   SumTmpThousRUR = money(round(ОстатокКД (Credit.CreditNumber, TDR_MAINREST, RepDate)*CurRateCRD
                               + ОстатокКД (Credit.CreditNumber, TDR_EXPREST, RepDate)*CurRateCRD));
   Итог2 = Итог2 + SumTmpThousRUR;
   ActiveSheet.Cells (Row, 16).Value = Double (SumTmpThousRUR);
   SumTmp = money(round(ОстатокКД (Credit.CreditNumber, TDR_EXPREST, RepDate)* CurRateCRD));
   Итог3 = Итог3 + SumTmp;
   ActiveSheet.Cells (Row, 17).Value = Double (SumTmp);
   SumTmp = money(round(ОстатокКД (Credit.CreditNumber, TDR_EXPPERC, RepDate)* CurRateCRD));
   Итог4 = Итог4 + SumTmp;
   ActiveSheet.Cells (Row, 18).Value = Double (SumTmp);
   ActiveSheet.Cells (Row, 19).Value = LnSelectValue("SELECT a.T_AccountNumber_Ref from dacc_crd_dbt a,dduty_crd_dbt b, dsb_acc_dbt c " +
                                        	      "where a.T_CurCode = " + Credit.CurCode +
	                                              " AND a.T_CreditAccountType = 1 " +
        	                                      " AND((a.T_ObjectID = 1 OR a.T_ObjectID = 8 OR a.T_ObjectID = 21) AND " +
                	                              " a.T_ObjectNumber = " +Credit.CreditNumber+ ") OR " +
                        	                      " (a.T_ObjectID = 6 AND " +
                                	              "  a.T_ObjectNumber = b.T_DutyID AND " +
                                        	      "  b.T_CreditNumber_Ref = " +Credit.CreditNumber+ ")", V_STRING);
   SumTmp = СуммаОбеспечения (Credit.CreditNumber, 0, 0, RepDate);
   EnsureSum = SumTmp;
   ActiveSheet.Cells (Row, 20).Value = Double (SumTmp);
   ActiveSheet.Cells (Row, 21).Value = ГруппаРиска (GetObjectID_FromCrdKind(Credit.Crd_kind), Credit.CreditNumber, 1, RepDate);
   SumTmp = РассчитатьРезерв(GetObjectID_FromCrdKind(Credit.Crd_kind), Credit.CreditNumber, 1, RepDate);
   ActiveSheet.Cells (Row, 22).Value = SumTmp;
   RezSum = RezSum +  SumTmp;
   SumTmp = ОстатокНаСчете(LO_CREDIT, Credit.CreditNumber, 0, REZ_ACC, RepDate);
   ActiveSheet.Cells (Row, 23).Value = doubleL(SumTmp);
   RezSumFact = RezSumFact + SumTmp;
//   ActiveSheet.Cells (Row, 24).Value = GetUsFieldValue(1 /*Кредитный договор*/, Credit.CreditNumber, 81 /*Перспективы*/, Credit.RegDate);
   Row = Row + 1;
end;

/************************************************************
 **                       Точка входа                      **
 ************************************************************/
RepDate = {curdate};
if (NOT GetDate (RepDate, "Введите дату отчёта:"))
  return;
end;
FinInst.AddFilter("t_CCY = 'USD'");
FinInst.rewind;
FinInst.next;

CurRateUSD = GetRate(RepDate, FinInst.rec.FiID);
if ((ValType(CurRateUSD) == V_UNDEF) or (CurRateUSD == 0.0))
   if (not GetTrue(false, "Курс Доллара США не установлен на дату отчета! Продолжить?"))
      return;
   else
      CurRateUSD = 1.0;
   end;
end;

if (УстановитьФилиалы() == false)
  return;
end;
if (NOT NewExcelWorkbook (false, "RstRep26.xls"))
  Exit (0, "Не открыт файл отчёта RstRep26.xls");
end;
УстановитьВидыКредитов (ALL_FLAGCUR);
rewind(ВидыКредитов);
DepNum = LnSelectValue("SELECT T_Name from Ddp_dep_dbt where T_Code = " + {OperDprtNode}, V_STRING);
ActiveSheet.Cells(1, 1).Value = "ОСБ №" + DepNum;
ActiveSheet.Range ("A1").Font.Bold = true;
ActiveSheet.Cells(7, 1).Value = "Отчет о состоянии кредитного портфеля ОСБ №" + DepNum;
ActiveSheet.Range ("A7").Font.Bold = true;
ActiveSheet.Cells(9, 3).Value = "ССУДНАЯ ЗАДОЛЖЕННОСТЬ по состоянию на " + String(RepDate:m);
ActiveSheet.Range ("C9").Font.Bold = true;


InitProgress (NRecords (Credit), "Подготовка отчета...", "Файл договоров (credit_c.dbt)");
Count = 0;
while (Next (Credit))
   if ((Credit.CreditState == "Ч") OR (Credit.CreditState == "Д"))
      continue;
   end;
   UseProgress (Count = Count + 1);

   ВидыКредитов.CreditTypeID_Ref = Credit.CreditTypeID_Ref;
   if (GetEQ (ВидыКредитов) AND (ВидыКредитов.SetFlag == "X") AND CheckDepart(Credit.FNCash))
     OneContractRep ();
   end;
end;
RemProgress ();

// Форматирование 
ActiveSheet.Range ("A14:X"+String (Row-1)).Select;
ExcelApplication.Selection.Borders (xlEdgeLeft).LineStyle = xlContinuous;
ExcelApplication.Selection.Borders (xlEdgeRight).LineStyle = xlContinuous;
ExcelApplication.Selection.Borders (xlInsideVertical).LineStyle = xlContinuous;
ExcelApplication.Selection.Borders (xlEdgeBottom).LineStyle = xlContinuous;

ActiveSheet.Cells (Row,  1).Value = "Итого:";
ActiveSheet.Cells (Row,  8).Value = Double (Итог1);
ActiveSheet.Cells (Row,  9).Value = Double (Итог1USD);
ActiveSheet.Cells (Row, 16).Value = Double (Итог2);
ActiveSheet.Cells (Row, 17).Value = Double (Итог3);
ActiveSheet.Cells (Row, 18).Value = Double (Итог4);
ActiveSheet.Cells (Row, 22).Value = Double (RezSum);
ActiveSheet.Cells (Row, 23).Value = Double (RezSumFact);
ActiveSheet.Range ("H"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("I"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("P"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("Q"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("R"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("V"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("W"+String (Row)).Font.Bold = true;
ActiveSheet.Range ("A"+String (Row)).Font.Bold = true;

ActiveSheet.Range ("A"+String (Row)+":X"+String (Row)).Select;
ExcelApplication.Selection.Borders (xlEdgeBottom).LineStyle = xlContinuous;
ExcelApplication.Selection.Borders (xlEdgeLeft).LineStyle = xlContinuous;
ExcelApplication.Selection.Borders (xlEdgeRight).LineStyle = xlContinuous;
ActiveSheet.Range ("A1").Select;
ExcelApplication.Visible = true;

ActiveSheet.Cells (Row + 4, 1).Value = "Управляющий ОСБ №" + depNum;
ActiveSheet.Range ("A"+String (Row + 4)).Font.Bold = true;

ActiveSheet.Cells (Row + 6, 1).Value = "Главный бухгалтер:";
ActiveSheet.Range ("A"+String (Row + 6)).Font.Bold = true;

ActiveSheet.Cells (Row + 8, 1).Value = "Исполнитель:";
ActiveSheet.Range ("A"+String (Row + 8)).Font.Bold = true;

ActiveSheet.Cells (Row + 9, 1).Value = "тел:";
ActiveSheet.Range ("A"+String (Row + 9)).Font.Bold = true;


MsgBox("Отчет сформирован");
return 0;

onError(axErr)
    if(axErr.Code != 0)         
        ExcelError(axErr, false);
    end;
    Exit(1);



                        